
#Область ПрограммныйИнтерфейс

// Заполнить аналитики учета номенклатуры.
// 
// Параметры:
//  Объект - ДокументОбъект.ОтчетДавальцуМеждуОрганизациями
//
Процедура ЗаполнитьАналитикиУчетаНоменклатуры(Объект) Экспорт
	
	//++ Локализация
	МестаУчета = РегистрыСведений.АналитикаУчетаНоменклатуры.МестаУчета(Неопределено, Неопределено, Неопределено, Неопределено);
	
	ИменаПолей = РегистрыСведений.АналитикаУчетаНоменклатуры.ИменаПолейКоллекцииПоУмолчанию();
	ИменаПолей.Работа       = "Подразделение";
	ИменаПолей.Произвольный = "Подразделение";
	
	РегистрыСведений.АналитикаУчетаНоменклатуры.ЗаполнитьВКоллекции(Объект.РНПТМатериалов, МестаУчета, ИменаПолей);
	//-- Локализация
	
КонецПроцедуры

// Заполняет реквизиты, хранящие информацию о видах запасов в табличной части 'Товары' документа.
//
// Параметры:
//	Объект - ДокументОбъект.ОтчетДавальцуМеждуОрганизациями
//	Отказ - Булево - признак того, что не удалось заполнить данные.
//	ТаблицыДокумента - см. Документы.ПриобретениеТоваровУслуг.КоллекцияТабличныхЧастейТоваров.
//
Процедура ЗаполнитьВидыЗапасовПриОбмене(Объект, Отказ, ТаблицыДокумента) Экспорт
	
	//++ Локализация
	ЗаполнитьВидыЗапасов(Объект, Отказ);
	//-- Локализация
	
КонецПроцедуры

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив из Строка - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	МеханизмыДокумента.Добавить("ПрослеживаемостьИмпортныхТоваровВПроизводстве");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив из Строка - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	//++ Локализация
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	#Область РНПТМатериалов
	
	ИмяТЧ           = "РНПТМатериалов";
	ПредставлениеТЧ = НСтр("ru = 'РНПТ материалов';
							|en = 'RNPT of materials'");
	
	МассивНепроверяемыхРеквизитов.Добавить("РНПТМатериалов.КоличествоПоРНПТ");
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИмяТЧ           = ИмяТЧ;
	ПараметрыПроверки.ПредставлениеТЧ = ПредставлениеТЧ;
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(Объект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(Объект, Документы.ОтчетДавальцуМеждуОрганизациями);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(Объект, ПараметрыУказанияСерий.РНПТМатериалов, Отказ, МассивНепроверяемыхРеквизитов);
	
	ПараметрыПроверки = ЗапасыСервер.ПараметрыПроверкиЗаполненияНомеровГТД();
	ПараметрыПроверки.ИмяТЧ           = ИмяТЧ;
	ПараметрыПроверки.ПредставлениеТЧ = ПредставлениеТЧ;
	ПараметрыПроверки.ПроверятьРаботы = Истина;
	
	ЗапасыСервер.ПроверитьЗаполнениеНомеровГТД(Объект, Отказ, ПараметрыПроверки);
	
	ПараметрыПроверки = Новый Структура;
	УчетПрослеживаемыхТоваровЛокализация.ДополнитьПараметрыПроверкиЗаполненияКоличестваПоРНПТ(ПараметрыПроверки);
	ПараметрыПроверки.ИмяТЧ           = ИмяТЧ;
	ПараметрыПроверки.ПредставлениеТЧ = ПредставлениеТЧ;
	
	УчетПрослеживаемыхТоваровЛокализация.ПроверитьЗаполнениеКоличестваПоРНПТ(Объект, Отказ, ПараметрыПроверки);
	УчетПрослеживаемыхТоваровЛокализация.ПроверитьДанныеПрослеживаемостиНомеровГТД(Объект, Объект.РНПТМатериалов, Объект.Дата, ИмяТЧ);
	
	#КонецОбласти
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ПроверитьСоответствиеРНПТМатериаловТоварам(Объект, Отказ, ПроверяемыеРеквизиты);
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	//++ Локализация
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ЗаполнитьВидыЗапасов(Объект, Отказ);
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Если Не Объект.ВидыЗапасовУказаныВручную Тогда
			Объект.ВидыЗапасов.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(Объект, "ВидыЗапасов");
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
	//++ Локализация
	Объект.ВидыЗапасов.Очистить();
	Объект.ВидыЗапасовУказаныВручную = Ложь;
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//++ Локализация
	
	// Счет-фактура
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "СчетФактура";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура';
										|en = 'Tax invoice'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Ложь);
	КомандаПечати.ДополнительныеПараметры.Вставить("ДополнитьПолученнымиСчетамиФактуры", Истина);
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(
		КомандаПечати,
		"ХозяйственнаяОперация",
		Перечисления.ХозяйственныеОперации.ОтчетДавальцуМеждуОрганизациями);
	
	Если НЕ ПраваПользователяПовтИсп.ЭтоПартнер() Тогда
		
		// Счет-фактура (в валюте)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "СчетФактураВВалюте";
		КомандаПечати.Представление = НСтр("ru = 'Счет-фактура (в валюте)';
											|en = 'Tax invoice (in currency)'");
		КомандаПечати.ФункциональныеОпции = "ИспользоватьНесколькоВалют";
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Истина);
		КомандаПечати.ДополнительныеПараметры.Вставить("ДополнитьПолученнымиСчетамиФактуры", Истина);
		УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(
			КомандаПечати,
			"ХозяйственнаяОперация",
			Перечисления.ХозяйственныеОперации.ОтчетДавальцуМеждуОрганизациями);
		
		// Универсальный передаточный документ (УПД)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "УПД";
		КомандаПечати.Представление = НСтр("ru = 'Универсальный передаточный документ (УПД)';
											|en = 'Universal transfer document (UTD)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ДополнитьПолученнымиСчетамиФактуры", Истина);
		КомандаПечати.ОсновнаяПечатнаяФорма = Истина;
		КомандаПечати.НаименованиеПечатнойФормы = НСтр("ru = 'Универсальный передаточный документ';
														|en = 'Universal transfer document'");
		УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(
			КомандаПечати,
			"ХозяйственнаяОперация",
			Перечисления.ХозяйственныеОперации.ОтчетДавальцуМеждуОрганизациями);
		
	КонецЕсли;
	
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект, представление - имя области в которой был выведен объект (выходной параметр).
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
КонецПроцедуры

//++ Локализация

// Формирует текст запроса для получения данных основания при печати Счет-фактуры.
// 
// Возвращаемое значение:
//  Строка - Текст запроса данных основания для печатной формы счет фактура
//
Функция ТекстЗапросаДанныхОснованияДляПечатнойФормыСчетФактура() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                       КАК Ссылка,
	|	ДанныеДокументов.ХозяйственнаяОперация        КАК ХозяйственнаяОперация,
	|	ДанныеДокументов.Валюта                       КАК Валюта,
	|	ДанныеДокументов.Организация                  КАК Организация,
	|	ДанныеДокументов.НалогообложениеНДС           КАК НалогообложениеНДС,
	|	ДанныеДокументов.Подразделение                КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)      КАК Склад,
	|	ЛОЖЬ                                          КАК РасчетыЧерезОтдельногоКонтрагента,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Грузоотправитель,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Грузополучатель,
	|	НЕОПРЕДЕЛЕНО                                  КАК Номенклатура,
	|	""""                                          КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Комиссионер,
	|	ДанныеДокументов.Основание                    КАК Основание,
	|	ДанныеДокументов.ОснованиеДата                КАК ОснованиеДата,
	|	ДанныеДокументов.ОснованиеНомер               КАК ОснованиеНомер,
	|	ДанныеДокументов.БанковскийСчетПереработчика  КАК БанковскийСчетОрганизации,
	|	ДанныеДокументов.БанковскийСчетДавальца       КАК БанковскийСчетКонтрагента,
	|	НЕОПРЕДЕЛЕНО                                  КАК БанковскийСчетГрузоотправителя,
	|	НЕОПРЕДЕЛЕНО                                  КАК БанковскийСчетГрузополучателя,
	|	ДанныеДокументов.ДоверенностьНомер            КАК ДоверенностьНомер,
	|	ДанныеДокументов.ДоверенностьДата             КАК ДоверенностьДата,
	|	ДанныеДокументов.ДоверенностьВыдана           КАК ДоверенностьВыдана,
	|	ДанныеДокументов.ДоверенностьЛицо             КАК ДоверенностьЛицо,
	|	ДанныеДокументов.Ответственный                КАК Менеджер,
	|	НЕОПРЕДЕЛЕНО                                  КАК Кладовщик,
	|	НЕОПРЕДЕЛЕНО                                  КАК ДолжностьКладовщика,
	|	""""                                          КАК АдресДоставки,
	|	""""                                          КАК АдресДоставкиЗначенияПолей,
	|	""""                                          КАК АдресДоставкиЗначение,
	|	""""                                          КАК ДанныеТранспортнаяНакладная,
	|	""""                                          КАК СопроводительныеДокументы,
	|	ДанныеДокументов.Дата                         КАК Период,
	|	""""                                          КАК КодСпециальныхОбстоятельств
	|
	|//ОператорПОМЕСТИТЬ
	|ИЗ
	|	Документ.ОтчетДавальцуМеждуОрганизациями КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&ДокументОснование_ОтчетДавальцуМеждуОрганизациями)
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает данные для формирования печатной формы УПД
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ОтчетДавальцуМеждуОрганизациями - ссылки на документы, по которым необходимо получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемая при печати УПД, содержащая следующие свойства:
//		* РезультатПоШапке - РезультатЗапроса - общие данные накладной.
//		* РезультатПоТабличнойЧасти - РезультатЗапроса - данные табличной части накладной.
//
Функция ПолучитьДанныеДляПечатнойФормыУПД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка        КАК Ссылка,
	|	ДанныеДокументов.Валюта        КАК Валюта,
	|	ДанныеДокументов.Организация   КАК Организация,
	|	ДанныеДокументов.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ДанныеДокументов.Дата          КАК Период,
	|	МАКСИМУМ(ТаблицаТоваров.Номенклатура.ПрослеживаемыйТовар) КАК ЕстьПрослеживаемыеТовары
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ОтчетДавальцуМеждуОрганизациями КАК ДанныеДокументов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетДавальцуМеждуОрганизациями.Товары КАК ТаблицаТоваров
	|	ПО ТаблицаТоваров.Ссылка = ДанныеДокументов.Ссылка
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокументов.Ссылка,
	|	ДанныеДокументов.Валюта,
	|	ДанныеДокументов.Организация,
	|	ДанныеДокументов.Подразделение,
	|	ДанныеДокументов.Дата
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	УчетНДСРФ.ДополнитьЗапросПолученияДанныхДляПечатиУПД(Запрос);
	
	Запрос =
		СхемыЗапросов.ДобавитьПолеВыборкиВЗапрос(
			Запрос,
			"ДанныеДокументов.ЕстьПрослеживаемыеТовары",
			"ЕстьПрослеживаемыеТовары");
	
	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	Обработки.ПечатьОбщихФорм.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                КАК Ссылка,
	|	&ПредставлениеСчетФактура                             КАК ПредставлениеДокумента,
	|	2                                                     КАК СтатусУПД,
	|	ДанныеДокумента.Номер                                 КАК Номер,
	|	ДанныеДокумента.Дата                                  КАК Дата,
	|	НЕОПРЕДЕЛЕНО                                          КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО                                          КАК ДатаИсправления,
	|	ЛОЖЬ                                                  КАК Исправление,
	|	НЕОПРЕДЕЛЕНО                                          КАК НомерСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                          КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                          КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                          КАК ДатаИсправленияСчетаФактуры,
	|	ЛОЖЬ                                                  КАК КорректировочныйСчетФактура,
	|	""""                                                  КАК СтрокаПоДокументу,
	|	НЕОПРЕДЕЛЕНО                                          КАК ВалютаСчетаФактуры,
	|	ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)         КАК Партнер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Давалец.ОбособленноеПодразделение
	|			ТОГДА ДанныеДокумента.Давалец.ГоловнаяОрганизация
	|		ИНАЧЕ ДанныеДокумента.Давалец
	|	КОНЕЦ                                                 КАК Контрагент,
	|	ДанныеДокумента.НалогообложениеНДС                    КАК НалогообложениеНДС,
	|	ДанныеПоставщика.ГоловнаяОрганизация                  КАК Организация,
	|	ДанныеДокумента.Организация.Префикс                   КАК Префикс,
	|	0                                                     КАК ИндексПодразделения,
	|	ТаблицаОтветственныеЛица.РуководительНаименование     КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность        КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ДанныеДокумента.Давалец                               КАК Грузополучатель,
	|	ДанныеДокумента.Организация                           КАК Грузоотправитель,
	|	ДанныеПоставщика.КПППоставщика                        КАК КПППоставщика,
	|	ДанныеДокумента.Давалец.КПП                           КАК КПППокупателя,
	|	ДанныеДокумента.Давалец.ИНН                           КАК ИННПокупателя,
	|	""""                                                  КАК АдресДоставки,
	|	ДанныеДокумента.Валюта                                КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное             КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код                            КАК ВалютаКод,
	|	ИСТИНА                                                КАК ТолькоУслуги,
	|	ДанныеДокументов.ЕстьПрослеживаемыеТовары             КАК ЕстьПрослеживаемыеТовары,
	|	ДанныеДокумента.Основание                             КАК Основание,
	|	ДанныеДокумента.ОснованиеДата                         КАК ОснованиеДата,
	|	ДанныеДокумента.ОснованиеНомер                        КАК ОснованиеНомер,
	|	ДанныеДокумента.ДоверенностьНомер                     КАК ДоверенностьНомер,
	|	ДанныеДокумента.ДоверенностьДата                      КАК ДоверенностьДата,
	|	ДанныеДокумента.ДоверенностьВыдана                    КАК ДоверенностьВыдана,
	|	ДанныеДокумента.ДоверенностьЛицо                      КАК ДоверенностьЛицо,
	|	ДанныеДокумента.Ответственный                         КАК Менеджер,
	|	НЕОПРЕДЕЛЕНО                                          КАК Кладовщик,
	|	НЕОПРЕДЕЛЕНО                                          КАК ДолжностьКладовщика,
	|	ИСТИНА                                                КАК ВыводитьКодНоменклатуры,
	|	""""                                                  КАК КодСпециальныхОбстоятельств,
	|	ДанныеДокументов.ТребуетсяНаличиеСФ                   КАК ТребуетсяНаличиеСФ,
	|
	|	ДанныеДокумента.БанковскийСчетДавальца                КАК БанковскийСчетОрганизации,
	|	ДанныеДокумента.БанковскийСчетПереработчика           КАК БанковскийСчетКонтрагента,
	|	ДанныеДокумента.БанковскийСчетДавальца                КАК БанковскийСчетГрузополучателя,
	|	ДанныеДокумента.БанковскийСчетПереработчика           КАК БанковскийСчетГрузоотправителя,
	|	ЛОЖЬ                                                  КАК ЭтоПередачаНаКомиссию
	|
	|ИЗ
	|	Документ.ОтчетДавальцуМеждуОрганизациями КАК ДанныеДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|	ПО ДанныеДокумента.Ссылка = ДанныеПоставщика.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|	ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|ГДЕ
	|	&ВыводитьОсновнойУПД
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                      КАК Ссылка,
	|	ТаблицаДокумента.Номенклатура                                КАК Номенклатура,
	|	ТаблицаДокумента.Номенклатура.НаименованиеПолное             КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ                                                        КАК НоменклатураКод,
	|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения               КАК ЕдиницаИзмерения,
	|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияНаименование,
	|	ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код           КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения                   КАК ЕдиницаИзмеренияТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Представление     КАК ЕдиницаИзмеренияТНВЭДНаименование,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Код               КАК ЕдиницаИзмеренияТНВЭДКод,
	|	ТаблицаДокумента.Характеристика                              КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное           КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.Серия.Наименование                          КАК СерияНаименование,	
	|	ТаблицаДокумента.СтавкаНДС                                   КАК СтавкаНДС,
	|	ТаблицаДокумента.НомерГТД                                    КАК НомерГТДСсылка,
	|	НЕОПРЕДЕЛЕНО                                                 КАК ТипНомераГТД,
	|	ТаблицаДокумента.НомерГТД                                    КАК НомерГТД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения                КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код            КАК СтранаПроисхожденияКод,
	|	ТаблицаДокумента.Количество                                  КАК Количество,
	|	ТаблицаДокумента.КоличествоПоРНПТ                            КАК КоличествоПоРНПТ,
	|	ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество   КАК Цена,
	|	ТаблицаДокумента.СуммаБезНДС                                 КАК СуммаБезНДС,
	|	ТаблицаДокумента.СуммаНДС                                    КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС     КАК СуммаСНДС,
	|	ТаблицаДокумента.НомерСтроки                                 КАК НомерСтроки,
	|	ТаблицаДокумента.Серия                                       КАК Серия,
	|	ЛОЖЬ                                                         КАК ЭтоВозвратнаяТара,
	|	ТаблицаДокумента.Номенклатура.КодТНВЭД                       КАК КодТНВЭД
	|
	|ИЗ
	|	ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка
	|";
	
	Запрос.УстановитьПараметр("ПредставлениеСчетФактура", НСтр("ru = 'счет-фактура';
																|en = 'tax invoice'"));
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	Запрос.УстановитьПараметр("ВыводитьОсновнойУПД", 
		Не (ПараметрыПечати.Свойство("НеВыводитьОсновнойУПД")
			И ПараметрыПечати.НеВыводитьОсновнойУПД));
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[1];
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке",          РезультатПоШапке);
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти", РезультатПоТабличнойЧасти);
	
	Возврат ДанныеДляПечати;
	
КонецФункции

// Формирует временную таблицу, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями:
//		Ссылка,
//		Валюта.
//
//	ПараметрыЗаполнения - Структура - структура, возвращаемая функцией ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров.
//
Процедура ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл",   ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	
	Если ПараметрыЗаполнения.ПересчитыватьВВалютуРегл И ПараметрыЗаполнения.АктуализироватьРасчеты Тогда
		
		Если Не ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
			
			Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
			|ИЗ
			|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
			|
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
			|		ПО РасчетыСКлиентами.Регистратор = ДанныеДокументов.Ссылка
			|
			|ГДЕ
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Организация.ВалютаРегламентированногоУчета
			|	И РасчетыСКлиентами.Активность
			|";
			
			ТаблицаАналитик = Запрос.Выполнить().Выгрузить();
			МассивАналитикУчетаПоПартнерам = ТаблицаАналитик.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
			
			Если МассивАналитикУчетаПоПартнерам.Количество() > 0 Тогда
				
				ОкончаниеПериодаРасчета =
					КонецМесяца(ВзаиморасчетыСервер.ПолучитьМаксимальнуюДатуВКоллекцииДокументов(МенеджерВременныхТаблиц)) + 1;
				
				АналитикиРасчета = РаспределениеВзаиморасчетовВызовСервера.АналитикиРасчета();
				АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикУчетаПоПартнерам;
				
				Попытка
					
					РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(
						ОкончаниеПериодаРасчета, АналитикиРасчета);
					
				Исключение
					
					ТекстСообщения = НСтр("ru = 'Печатная форма сформирована по неактуальным данным.
												|Необходимо актуализировать взаиморасчеты вручную и переформировать печатную форму.';
												|en = 'Print form is generated according to irrelevant data. 
												|Update AR/AP accounting manually, and then create the print form again.'");
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
					
				КонецПопытки;
				
			КонецЕсли;
			
		Иначе
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ДанныеДокументов.Ссылка КАК Ссылка
			|ИЗ
			|	ТаблицаДанныхДокументов КАК ДанныеДокументов
			|ГДЕ 
			|	ДанныеДокументов.Валюта <> ДанныеДокументов.Организация.ВалютаРегламентированногоУчета
			|	ИЛИ ДанныеДокументов.Валюта <> &ВалютаУправленческогоУчета";
			
			МассивДокументов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			
			Если ПланыОбмена.ГлавныйУзел() = Неопределено Тогда
				НеоперативнаяОчередьЗаданий.ВыполнитьЗаданияНеоперативнойОчередиПоСпискуДокументов(МассивДокументов,
					Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженийПоФинансовымРегистрам);
			Иначе
				РегистрыСведений.СуммыДокументовВВалютахУчета.РассчитатьСуммыДокументовВВалютахУчета(МассивДокументов);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                     КАК Ссылка,
	|	ТаблицаДокумента.НомерСтроки                КАК НомерСтроки,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ВЫРАЗИТЬ("""" КАК Строка(200))              КАК Содержание,
	|	ТаблицаДокумента.Количество                 КАК Количество,
	|	ТаблицаДокумента.Количество                 КАК КоличествоУпаковок,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД,
	|	0                                           КАК КоличествоПоРНПТ,
	|	ТаблицаДокумента.Цена                       КАК Цена,
	|	ТаблицаДокумента.СтавкаНДС                  КАК СтавкаНДС,
	|	ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.СуммаБезНДСРегл,
	|		ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) КАК СуммаБезНДС,
	|	ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.БазаНДСРегл,
	|		ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) КАК БазаНДСРегл,
	|	ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.СуммаНДСРегл, ТаблицаДокумента.СуммаНДС) КАК СуммаНДС,
	|	ЛОЖЬ                                        КАК ЭтоТовар,
	|	ЛОЖЬ                                        КАК ВернутьМногооборотнуюТару
	|ПОМЕСТИТЬ ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров
	|ИЗ
	|	Документ.ОтчетДавальцуМеждуОрганизациями.Товары КАК ТаблицаДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО ТаблицаДокумента.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|	И ТаблицаДокумента.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки
	|	И СуммыДокументовВВалютахУчета.Активность
	|	И &ПересчитыватьВВалютуРегл
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|	ПО ИСТИНА
	|
	|ГДЕ
	|	НЕ ТаблицаДокумента.Номенклатура.ПрослеживаемыйТовар
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                     КАК Ссылка,
	|	ТаблицаДокумента.НомерСтроки                КАК НомерСтроки,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ВЫРАЗИТЬ("""" КАК Строка(200))              КАК Содержание,
	|	ТаблицаДокумента.Количество                 КАК Количество,
	|	ТаблицаДокумента.Количество                 КАК КоличествоУпаковок,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
	|	ТаблицаДокумента.НомерГТД                   КАК НомерГТД,
	|	ТаблицаДокумента.КоличествоПоРНПТ           КАК КоличествоПоРНПТ,
	|	ТаблицаДокумента.Цена                       КАК Цена,
	|	ТаблицаДокумента.СтавкаНДС                  КАК СтавкаНДС,
	|	ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.СуммаБезНДСРегл,
	|		ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) КАК СуммаБезНДС,
	|	ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.БазаНДСРегл,
	|		ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС) КАК БазаНДСРегл,
	|	ЕСТЬNULL(
	|		СуммыДокументовВВалютахУчета.СуммаНДСРегл, ТаблицаДокумента.СуммаНДС) КАК СуммаНДС,
	|	ЛОЖЬ                                        КАК ЭтоТовар,
	|	ЛОЖЬ                                        КАК ВернутьМногооборотнуюТару
	|ИЗ
	|	Документ.ОтчетДавальцуМеждуОрганизациями.ВидыЗапасов КАК ТаблицаДокумента
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|	ПО ТаблицаДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|	ПО ТаблицаДокумента.Ссылка = СуммыДокументовВВалютахУчета.Регистратор
	|	И ТаблицаДокумента.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки
	|	И СуммыДокументовВВалютахУчета.Активность
	|	И &ПересчитыватьВВалютуРегл
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|	ПО ИСТИНА
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

//-- Локализация
#КонецОбласти

#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	
	//++ Локализация
	
	// Общие фрагменты проводок по включению в стоимость НДС
	ТекстыОтражения.Добавить(РеглУчетВыборкиСерверПовтИсп.ТекстВключениеНДСпоПриобретеннымЦенностям());
	
	ТекстыОтражения.Добавить(ТекстВыпускУслугПереработки()); // Выпуск услуг переработки (Дт 20 :: Кт 20)
	ТекстыОтражения.Добавить(ТекстСебестоимостьПродаж()); // Себестоимость продаж (Дт 90.02 :: Кт 20)
	ТекстыОтражения.Добавить(ТекстСписаниеТоваровНаРасходы()); // Списание на расходы (Дт 2Х :: Кт 20)
	ТекстыОтражения.Добавить(ТекстСписаниеТоваровНаСтатьиАктивовПассивов()); // Списание на расходы (Дт 9Х :: Кт 20)
	ТекстыОтражения.Добавить(ТекстРеализацияУслугПереработки()); // Реализация услуг переработки (Дт 62 :: Кт 90.01, 91.01)
	ТекстыОтражения.Добавить(ТекстНачислениеНДСЗаУслуги()); // Начисление НДС (Дт <90.3, 91.2> :: Кт <68.2>)
	ТекстыОтражения.Добавить(ТекстСписаниеНаПартииПроизводства()); // Списание на партии производства (Дт 20 :: Кт 20)
	ТекстыОтражения.Добавить(ТекстПриобретениеРабот()); // Приобретение работ (Дт 20 :: Кт 60)
	ТекстыОтражения.Добавить(ТекстЗакупкаНДС()); // НДС при приобретении (Дт 19 :: Кт 60)
	
	// Выпуск продукции с использованием плановой стоимости (счет 40)
	ТекстыОтражения.Добавить(ПроизводствоБезЗаказаЛокализация.ТекстВыпускСобственнойПродукцииПлановаяСтоимость()); // Выпуск продукции на склад (плановая стоимость) (Дт 20, 43 :: Кт 40)
	ТекстыОтражения.Добавить(ПроизводствоБезЗаказаЛокализация.ТекстВыпускПродукцииСоСписаниемНаРасходыПлановаяСтоимость()); // Выпуск продукции со списанием на расходы (плановая стоимость) (Дт 25 :: Кт 40)
	ТекстыОтражения.Добавить(ПроизводствоБезЗаказаЛокализация.ТекстВыпускПродукцииСоСписаниемНаАктивыПассивыПлановаяСтоимость()); // Выпуск продукции со списанием на активы/пассивы (плановая стоимость) (Дт 94 :: Кт 40)
	ТекстыОтражения.Добавить(ПроизводствоБезЗаказаЛокализация.ТекстВыпускСобственнойПродукцииНаСкладОтклонение()); // Выпуск продукции на склад (отклонение) (Дт 20, 43 :: Кт 40)
	ТекстыОтражения.Добавить(РегистраторРасчетаСебестоимостиЛокализация.ТекстВыпускСобственнойПродукцииОтклонениеНаСебестоимостьПродаж()); // Выпуск продукции (отклонение на себестоимость продаж) (Дт 90.02 :: Кт 40)
	ТекстыОтражения.Добавить(РегистраторРасчетаСебестоимостиЛокализация.ТекстВыпускПродукцииСоСписаниемНаРасходыОтклонение()); // Выпуск продукции со списанием на расходы (отклонение) (Дт 25 :: Кт 40)
	ТекстыОтражения.Добавить(РегистраторРасчетаСебестоимостиЛокализация.ТекстВыпускПродукцииСоСписаниемНаАктивыПассивыОтклонение()); // Выпуск продукции со списанием на активы/пассивы (отклонение) (Дт 94 :: Кт 40)
	ТекстыОтражения.Добавить(РегистраторРасчетаСебестоимостиЛокализация.ТекстВыпускСобственнойПродукцииФактическаяСтоимостьМатериальныеЗатраты()); // Выпуск собственной продукции (фактическая стоимость) (Дт 40 :: Кт 20)
	ТекстыОтражения.Добавить(РегистраторРасчетаСебестоимостиЛокализация.ТекстВыпускСобственнойПродукцииФактическаяСтоимостьПостатейныеРасходы()); // Выпуск собственной продукции (фактическая стоимость) (Дт 40 :: Кт 20)
	ТекстыОтражения.Добавить(РегистраторРасчетаСебестоимостиЛокализация.ТекстВыпускСобственнойПродукцииФактическаяСтоимостьТрудозатраты()); // Выпуск собственной продукции (фактическая стоимость) (Дт 40 :: Кт 20)
	
	//-- Локализация
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстыЗапроса = Новый Массив;
	
	//++ Локализация
	
	#Область ВТПолученияНалогообложенияПрибыли
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВыручкаИСебестоимость.Ссылка КАК Ссылка,
	|	ВыручкаИСебестоимость.Период КАК Период,
	|	ВыручкаИСебестоимость.Организация КАК Организация,
	|	ВыручкаИСебестоимость.Подразделение КАК Подразделение,
	|	ВыручкаИСебестоимость.НДВыручки КАК НаправлениеДеятельности,
	|	ВыручкаИСебестоимость.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ВТПолученияНалогообложенияПрибыли
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимость
	|	ПО ДокументыКОтражению.Ссылка = ВыручкаИСебестоимость.Ссылка
	|		И ВыручкаИСебестоимость.НастройкаХозОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОтчетДавальцуМеждуОрганизациями)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО 
	|		УчетнаяПолитикаОрганизаций.Ссылка = ВыручкаИСебестоимость.Ссылка
	|		И УчетнаяПолитикаОрганизаций.Организация = ВыручкаИСебестоимость.Организация
	|		И УчетнаяПолитикаОрганизаций.Период = ВыручкаИСебестоимость.Период
	|		И УчетнаяПолитикаОрганизаций.РаздельныйУчетПоНалогуНаПрибыль
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Строки.Ссылка КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	ВыручкаИСебестоимость.Подразделение КАК Подразделение,
	|	ВыручкаИСебестоимость.НДВыручки КАК НаправлениеДеятельности,
	|	Строки.Номенклатура КАК Номенклатура
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = ДокументыКОтражению.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетДавальцуМеждуОрганизациями)
	|		И Строки.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		И Строки.СтатьяДоходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|		И Строки.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимость
	|	ПО Строки.Ссылка = ВыручкаИСебестоимость.Ссылка
	|		И Строки.Организация = ВыручкаИСебестоимость.Организация
	|		И Строки.ИдентификаторФинЗаписи = ВыручкаИСебестоимость.ИдентификаторФинЗаписи
	|		И Строки.НастройкаХозяйственнойОперации = ВыручкаИСебестоимость.НастройкаХозОперации
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО 
	|		УчетнаяПолитикаОрганизаций.Ссылка = Строки.Ссылка
	|		И УчетнаяПолитикаОрганизаций.Организация = Строки.Организация
	|		И УчетнаяПолитикаОрганизаций.Период = Строки.Период
	|		И УчетнаяПолитикаОрганизаций.РаздельныйУчетПоНалогуНаПрибыль";

	ТекстыЗапроса.Добавить(ТекстЗапроса);

	#КонецОбласти
	
	#Область РаздельныйУчет_НастройкиНалогообложенияПрибыли
	
	ПараметрыПолученияНалогообложенияПрибыли = РаздельныйУчетПоНалогуНаПрибыль.НовыйПараметрыПолученияТаблицыНастроекНалогообложенияПрибыли();
	ПараметрыПолученияНалогообложенияПрибыли.ИмяТаблицы = "ВТПолученияНалогообложенияПрибыли";
	ПараметрыПолученияНалогообложенияПрибыли.ИмяПоля_Период = "Период";
	ПараметрыПолученияНалогообложенияПрибыли.ИмяПоля_Организация = "Организация";
	ПараметрыПолученияНалогообложенияПрибыли.ИмяПоля_Подразделение = "Подразделение";
	ПараметрыПолученияНалогообложенияПрибыли.ИмяПоля_НаправлениеДеятельности = "НаправлениеДеятельности";
	ПараметрыПолученияНалогообложенияПрибыли.ИмяПоля_ОбъектУчета = "Номенклатура";
	ТекстыЗапроса.Добавить(РаздельныйУчетПоНалогуНаПрибыль.ТекстЗапроса_НастройкиНалогообложенияПрибыли(ПараметрыПолученияНалогообложенияПрибыли));
	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ ВТПолученияНалогообложенияПрибыли");

	#КонецОбласти
	
	ТекстыЗапроса.Добавить(РегистраторРасчетаСебестоимостиЛокализация.ТекстЗапросаВыручкаИСебестоимостьПродаж());
	
	ТекстыЗапроса.Добавить("");
	
	//-- Локализация
	
	Возврат СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

#КонецОбласти

//++ Локализация
#Область Фискализация

// Возвращает таблицу товаров для заполнения позиций строк в параметрах чека
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для получения товарных позиций
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблицу с товарными позициями с количественными и суммовыми показателями
Функция ПозицииНоменклатурыПоДокументу(ДокументСсылка) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка      КАК Ссылка,
	|	ДанныеДокументов.Валюта      КАК Валюта,
	|	ДанныеДокументов.Организация КАК Организация
	|
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ОтчетДавальцуМеждуОрганизациями КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка = &Ссылка
	|;";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ПересчитыватьВВалютуРегл = Истина;
	
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров.НомерСтроки,
	|	ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров.Номенклатура,
	|	ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров.Характеристика,
	|	ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров.Упаковка,
	|	ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров.КоличествоУпаковок КАК Количество,
	|	(ВЫРАЗИТЬ((ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров.СуммаБезНДС + ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров.СуммаНДС) /
	|		ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров.КоличествоУпаковок КАК ЧИСЛО(31,2))) КАК Цена,
	|	ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров.СуммаБезНДС + ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров.СуммаНДС КАК Сумма,
	|	ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|	ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров.СуммаНДС КАК СуммаНДС
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров КАК ОтчетДавальцуМеждуОрганизациямиТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Количество             КАК Количество,
	|	ТаблицаНоменклатуры.Сумма                  КАК Сумма,
	|	ТаблицаНоменклатуры.СтавкаНДС              КАК СтавкаНДС,
	|	ТаблицаНоменклатуры.СуммаНДС               КАК СуммаНДС,
	|	ТаблицаНоменклатуры.Цена                   КАК Цена,
	|	
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаНоменклатуры.Номенклатура) = ТИП(Строка) ТОГДА
	|			ТаблицаНоменклатуры.Номенклатура
	|		ИНАЧЕ
	|			ТаблицаНоменклатуры.Номенклатура.НаименованиеПолное
	|	КОНЕЦ КАК НоменклатураНаименование,
	|	ЕСТЬNULL(ТаблицаНоменклатуры.Характеристика.НаименованиеПолное, """") КАК ХарактеристикаНаименование,
	|	ТаблицаНоменклатуры.Упаковка               КАК Упаковка,
	|	ТаблицаНоменклатуры.Упаковка               КАК УпаковкаНаименование
	|
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНоменклатуры.НомерСтроки
	|";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти
//-- Локализация

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений из Строка - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРНПТМатериаловВПроизводстве(Запрос, ТекстыЗапроса, Регистры);
	//-- Локализация
	
КонецПроцедуры

//++ Локализация

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПериодыОтражения.Период        КАК Период,
	|	ПериодыОтражения.Организация   КАК Организация,
	|	ПериодыОтражения.ДатаОтражения КАК ДатаОтражения
	|ИЗ
	|	(ВЫБРАТЬ
	|		&Период                      КАК Период,
	|		&Организация                 КАК Организация,
	|		НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&Период                                                        КАК Период,
	|		&Организация                                                   КАК Организация,
	|		НАЧАЛОПЕРИОДА(НДССостояниеРеализации0.ДатаПодтверждения, ДЕНЬ) КАК ДатаОтражения
	|	ИЗ
	|		РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|	ГДЕ
	|		НДССостояниеРеализации0.ДокументРеализации = &Ссылка
	|		И НДССостояниеРеализации0.Состояние В (
	|			ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.НеПодтвержденаРеализация0),
	|			ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&Период                      КАК Период,
	|		&Давалец                     КАК Организация,
	|		НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|	) КАК ПериодыОтражения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|" + ДоходыИРасходыСервер.ДополнитьТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Ложь);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРНПТМатериаловВПроизводстве(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РНПТМатериаловВПроизводстве";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	ТаблицаТовары.ПартияПроизводства         КАК ПартияПроизводства,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	&Организация                             КАК Организация,
	|	ТаблицаТовары.НомерГТД                   КАК НомерГТД,
	|	ТаблицаТовары.Количество                 КАК Количество,
	|	ТаблицаТовары.КоличествоПоРНПТ           КАК КоличествоПоРНПТ,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносРНПТМатериаловНаПродукцию) КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.ОтчетДавальцуМеждуОрганизациями.РНПТМатериалов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)   КАК ВидДвижения,
	|	ТаблицаТовары.ПартияПроизводства         КАК ПартияПроизводства,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	&ОрганизацияПолучатель                   КАК Организация,
	|	ТаблицаТовары.НомерГТД                   КАК НомерГТД,
	|	ТаблицаТовары.Количество                 КАК Количество,
	|	ТаблицаТовары.КоличествоПоРНПТ           КАК КоличествоПоРНПТ,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства) КАК ХозяйственнаяОперация
	|ИЗ
	|	Документ.ОтчетДавальцуМеждуОрганизациями.ВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В(&Ссылка)
	|	И ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ПереноситьРНПТМатериала
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- Локализация

#КонецОбласти

//++ Локализация

#Область ЗаполнениеНомеровГТД

Функция ВременныеТаблицыДанныхДокумента(Объект)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Товары.ПартияПроизводства         КАК ПартияПроизводства,
	|	Товары.СтатьяКалькуляции          КАК СтатьяКалькуляции,
	|	Товары.СписатьНаРасходы           КАК СписатьНаРасходы,
	|	Товары.СтатьяРасходов             КАК СтатьяРасходов,
	|	Товары.АналитикаРасходов          КАК АналитикаРасходов,
	|	Товары.АналитикаАктивовПассивов   КАК АналитикаАктивовПассивов,
	|	Товары.НастройкаСчетовУчета       КАК НастройкаСчетовУчета,
	|	Товары.СтавкаНДС                  КАК СтавкаНДС,
	|	Товары.КодСтроки                  КАК КодСтроки,
	|	Товары.ИдентификаторСтроки        КАК ИдентификаторСтроки,
	|	Товары.Количество                 КАК Количество,
	|	Товары.ДоляСтоимости              КАК ДоляСтоимости,
	|	Товары.Цена                       КАК Цена,
	|	Товары.Сумма                      КАК Сумма,
	|	Товары.СуммаНДС                   КАК СуммаНДС,
	|	Товары.СуммаСНДС                  КАК СуммаСНДС,
	|	Товары.СуммаВзаиморасчетов        КАК СуммаВзаиморасчетов,
	|	Товары.СуммаНДСВзаиморасчетов     КАК СуммаНДСВзаиморасчетов
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	&Товары КАК Товары
	|ГДЕ
	|	&ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Товары.ПартияПроизводства         КАК ПартияПроизводства,
	|	Товары.СтатьяКалькуляции          КАК СтатьяКалькуляции,
	|	Товары.СписатьНаРасходы           КАК СписатьНаРасходы,
	|	Товары.СтатьяРасходов             КАК СтатьяРасходов,
	|	Товары.АналитикаРасходов          КАК АналитикаРасходов,
	|	Товары.АналитикаАктивовПассивов   КАК АналитикаАктивовПассивов,
	|	Товары.НастройкаСчетовУчета       КАК НастройкаСчетовУчета,
	|	Товары.СтавкаНДС                  КАК СтавкаНДС,
	|	Товары.ИдентификаторСтроки        КАК ИдентификаторСтроки,
	|	Товары.КодСтроки                  КАК КодСтроки,
	|	Товары.Количество                 КАК Количество,
	|	Товары.ДоляСтоимости              КАК ДоляСтоимости,
	|	Товары.Цена                       КАК Цена,
	|	Товары.Сумма                      КАК Сумма,
	|	Товары.СуммаНДС                   КАК СуммаНДС,
	|	Товары.СуммаСНДС                  КАК СуммаСНДС,
	|	Товары.СуммаВзаиморасчетов        КАК СуммаВзаиморасчетов,
	|	Товары.СуммаНДСВзаиморасчетов     КАК СуммаНДСВзаиморасчетов
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	ВтТовары КАК Товары
	|ГДЕ
	|	Товары.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ПартияПроизводства         КАК ПартияПроизводства,
	|	ВидыЗапасов.СтатьяКалькуляции          КАК СтатьяКалькуляции,
	|	ВидыЗапасов.СписатьНаРасходы           КАК СписатьНаРасходы,
	|	ВидыЗапасов.СтатьяРасходов             КАК СтатьяРасходов,
	|	ВидыЗапасов.АналитикаРасходов          КАК АналитикаРасходов,
	|	ВидыЗапасов.АналитикаАктивовПассивов   КАК АналитикаАктивовПассивов,
	|	ВидыЗапасов.НастройкаСчетовУчета       КАК НастройкаСчетовУчета,
	|	ВидыЗапасов.СтавкаНДС                  КАК СтавкаНДС,
	|	ВидыЗапасов.ИдентификаторСтроки        КАК ИдентификаторСтроки,
	|	ВидыЗапасов.КодСтроки                  КАК КодСтроки,
	|	ВидыЗапасов.Количество                 КАК Количество,
	|	ВидыЗапасов.КоличествоПоРНПТ           КАК КоличествоПоРНПТ,
	|	ВидыЗапасов.ДоляСтоимости              КАК ДоляСтоимости,
	|	ВидыЗапасов.Цена                       КАК Цена,
	|	ВидыЗапасов.Сумма                      КАК Сумма,
	|	ВидыЗапасов.СуммаНДС                   КАК СуммаНДС,
	|	ВидыЗапасов.СуммаСНДС                  КАК СуммаСНДС,
	|	ВидыЗапасов.СуммаВзаиморасчетов        КАК СуммаВзаиморасчетов,
	|	ВидыЗапасов.СуммаНДСВзаиморасчетов     КАК СуммаНДСВзаиморасчетов
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ИЗ
	|	&ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	&ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РНПТМатериалов.ПартияПроизводства КАК ПартияПроизводства,
	|	РНПТМатериалов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	РНПТМатериалов.Номенклатура       КАК Номенклатура,
	|	РНПТМатериалов.Характеристика     КАК Характеристика,
	|	РНПТМатериалов.Серия              КАК Серия,
	|	РНПТМатериалов.Назначение         КАК Назначение,
	|	РНПТМатериалов.НомерГТД           КАК НомерГТД,
	|	РНПТМатериалов.Количество         КАК Количество,
	|	РНПТМатериалов.КоличествоПоРНПТ   КАК КоличествоПоРНПТ
	|ПОМЕСТИТЬ ВтРНПТМатериалов
	|ИЗ
	|	&РНПТМатериалов КАК РНПТМатериалов
	|ГДЕ
	|	&ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|";
	
	Запрос.УстановитьПараметр("Товары",         Объект.Товары.Выгрузить());
	Запрос.УстановитьПараметр("ВидыЗапасов",    Объект.ВидыЗапасов.Выгрузить());
	Запрос.УстановитьПараметр("РНПТМатериалов", Объект.РНПТМатериалов.Выгрузить());
	
	УчетПрослеживаемыхТоваровЛокализация.УстановитьПараметрыИспользованияУчетаПрослеживаемыхТоваров(Запрос);
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

Функция ПроверитьИзменениеДокумента(Объект, МенеджерВТ)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА
	|ИЗ
	|	Документ.ОтчетДавальцуМеждуОрганизациями КАК РеквизитыДокумента
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|	И НЕ РеквизитыДокумента.Дата = &Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Товары.ПартияПроизводства         КАК ПартияПроизводства,
	|	Товары.СтатьяКалькуляции          КАК СтатьяКалькуляции,
	|	Товары.СписатьНаРасходы           КАК СписатьНаРасходы,
	|	Товары.СтатьяРасходов             КАК СтатьяРасходов,
	|	Товары.АналитикаРасходов          КАК АналитикаРасходов,
	|	Товары.АналитикаАктивовПассивов   КАК АналитикаАктивовПассивов,
	|	Товары.НастройкаСчетовУчета       КАК НастройкаСчетовУчета,
	|	Товары.СтавкаНДС                  КАК СтавкаНДС,
	|	Товары.ИдентификаторСтроки        КАК ИдентификаторСтроки,
	|	Товары.КодСтроки                  КАК КодСтроки,
	|	Товары.Цена                       КАК Цена
	|ИЗ
	|	(ВЫБРАТЬ
	|		Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Товары.ПартияПроизводства         КАК ПартияПроизводства,
	|		Товары.СтатьяКалькуляции          КАК СтатьяКалькуляции,
	|		Товары.СписатьНаРасходы           КАК СписатьНаРасходы,
	|		Товары.СтатьяРасходов             КАК СтатьяРасходов,
	|		Товары.АналитикаРасходов          КАК АналитикаРасходов,
	|		Товары.АналитикаАктивовПассивов   КАК АналитикаАктивовПассивов,
	|		Товары.НастройкаСчетовУчета       КАК НастройкаСчетовУчета,
	|		Товары.СтавкаНДС                  КАК СтавкаНДС,
	|		Товары.ИдентификаторСтроки        КАК ИдентификаторСтроки,
	|		Товары.КодСтроки                  КАК КодСтроки,
	|		Товары.Цена                       КАК Цена,
	|		Товары.Количество                 КАК Количество,
	|		Товары.ДоляСтоимости              КАК ДоляСтоимости,
	|		Товары.Сумма                      КАК Сумма,
	|		Товары.СуммаНДС                   КАК СуммаНДС,
	|		Товары.СуммаСНДС                  КАК СуммаСНДС,
	|		Товары.СуммаВзаиморасчетов        КАК СуммаВзаиморасчетов,
	|		Товары.СуммаНДСВзаиморасчетов     КАК СуммаНДСВзаиморасчетов
	|	ИЗ
	|		ТаблицаТоваров КАК Товары
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ВидыЗапасов.ПартияПроизводства         КАК ПартияПроизводства,
	|		ВидыЗапасов.СтатьяКалькуляции          КАК СтатьяКалькуляции,
	|		ВидыЗапасов.СписатьНаРасходы           КАК СписатьНаРасходы,
	|		ВидыЗапасов.СтатьяРасходов             КАК СтатьяРасходов,
	|		ВидыЗапасов.АналитикаРасходов          КАК АналитикаРасходов,
	|		ВидыЗапасов.АналитикаАктивовПассивов   КАК АналитикаАктивовПассивов,
	|		ВидыЗапасов.НастройкаСчетовУчета       КАК НастройкаСчетовУчета,
	|		ВидыЗапасов.СтавкаНДС                  КАК СтавкаНДС,
	|		ВидыЗапасов.ИдентификаторСтроки        КАК ИдентификаторСтроки,
	|		ВидыЗапасов.КодСтроки                  КАК КодСтроки,
	|		ВидыЗапасов.Цена                       КАК Цена,
	|		-ВидыЗапасов.Количество                КАК Количество,
	|		-ВидыЗапасов.ДоляСтоимости             КАК ДоляСтоимости,
	|		-ВидыЗапасов.Сумма                     КАК Сумма,
	|		-ВидыЗапасов.СуммаНДС                  КАК СуммаНДС,
	|		-ВидыЗапасов.СуммаСНДС                 КАК СуммаСНДС,
	|		-ВидыЗапасов.СуммаВзаиморасчетов       КАК СуммаВзаиморасчетов,
	|		-ВидыЗапасов.СуммаНДСВзаиморасчетов    КАК СуммаНДСВзаиморасчетов
	|	ИЗ
	|		ТаблицаВидыЗапасов КАК ВидыЗапасов
	|	) КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ПартияПроизводства,
	|	Товары.СтатьяКалькуляции,
	|	Товары.СписатьНаРасходы,
	|	Товары.СтатьяРасходов,
	|	Товары.АналитикаРасходов,
	|	Товары.АналитикаАктивовПассивов,
	|	Товары.НастройкаСчетовУчета,
	|	Товары.СтавкаНДС,
	|	Товары.ИдентификаторСтроки,
	|	Товары.КодСтроки,
	|	Товары.Цена
	|
	|ИМЕЮЩИЕ
	|	НЕ СУММА(Товары.Количество) = 0
	|	ИЛИ НЕ СУММА(Товары.ДоляСтоимости) = 0
	|	ИЛИ НЕ СУММА(Товары.Сумма) = 0
	|	ИЛИ НЕ СУММА(Товары.СуммаНДС) = 0
	|	ИЛИ НЕ СУММА(Товары.СуммаСНДС) = 0
	|	ИЛИ НЕ СУММА(Товары.СуммаВзаиморасчетов) = 0
	|	ИЛИ НЕ СУММА(Товары.СуммаНДСВзаиморасчетов) = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИСТИНА
	|ИЗ
	|	(ВЫБРАТЬ
	|		РНПТМатериалов.ПартияПроизводства КАК ПартияПроизводства,
	|		РНПТМатериалов.Номенклатура       КАК Номенклатура,
	|		РНПТМатериалов.Характеристика     КАК Характеристика,
	|		РНПТМатериалов.Серия              КАК Серия,
	|		РНПТМатериалов.Назначение         КАК Назначение,
	|		РНПТМатериалов.НомерГТД           КАК НомерГТД,
	|		РНПТМатериалов.Количество         КАК Количество,
	|		РНПТМатериалов.КоличествоПоРНПТ   КАК КоличествоПоРНПТ
	|	ИЗ
	|		ВтРНПТМатериалов КАК РНПТМатериалов
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РНПТМатериалов.ПартияПроизводства КАК ПартияПроизводства,
	|		РНПТМатериалов.Номенклатура       КАК Номенклатура,
	|		РНПТМатериалов.Характеристика     КАК Характеристика,
	|		РНПТМатериалов.Серия              КАК Серия,
	|		РНПТМатериалов.Назначение         КАК Назначение,
	|		РНПТМатериалов.НомерГТД           КАК НомерГТД,
	|		-РНПТМатериалов.Количество        КАК Количество,
	|		-РНПТМатериалов.КоличествоПоРНПТ  КАК КоличествоПоРНПТ
	|	ИЗ
	|		Документ.ОтчетДавальцуМеждуОрганизациями.РНПТМатериалов КАК РНПТМатериалов
	|	ГДЕ
	|		РНПТМатериалов.Ссылка = &Ссылка
	|	) КАК РНПТМатериалов
	|
	|СГРУППИРОВАТЬ ПО
	|	РНПТМатериалов.ПартияПроизводства,
	|	РНПТМатериалов.Номенклатура,
	|	РНПТМатериалов.Характеристика,
	|	РНПТМатериалов.Серия,
	|	РНПТМатериалов.Назначение,
	|	РНПТМатериалов.НомерГТД
	|
	|ИМЕЮЩИЕ
	|	НЕ СУММА(РНПТМатериалов.Количество) = 0
	|	ИЛИ НЕ СУММА(РНПТМатериалов.КоличествоПоРНПТ) = 0
	|";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Дата",   Объект.Дата);
	
	ДокументИзменен = Ложь;
	Для Каждого ТекущийРезультат Из Запрос.ВыполнитьПакет() Цикл
		Если Не ТекущийРезультат.Пустой() Тогда
			ДокументИзменен = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДокументИзменен;
	
КонецФункции

Процедура РаспределитьНомераГТД(Объект, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ПартияПроизводства         КАК ПартияПроизводства,
	|	ВидыЗапасов.Количество                 КАК Количество,
	|	ВидыЗапасов.КоличествоПоРНПТ           КАК КоличествоПоРНПТ
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ИЗ
	|	&ВидыЗапасов КАК ВидыЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.ПартияПроизводства      КАК ПартияПроизводства,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	СУММА(ТаблицаВидыЗапасов.Количество)       КАК Количество,
	|	СУММА(ТаблицаВидыЗапасов.КоличествоПоРНПТ) КАК КоличествоПоРНПТ
	|ИЗ
	|	ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаВидыЗапасов.ПартияПроизводства,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры
	|";
	Запрос.УстановитьПараметр("ВидыЗапасов", Объект.ВидыЗапасов.Выгрузить());
	
	ОтборПоПартии = Новый Структура("ПартияПроизводства");
	ИменаКолонок  = "АналитикаУчетаНоменклатуры, НомерГТД, Количество, КоличествоПоРНПТ";
	
	ПартииПроизводства = Запрос.Выполнить().Выгрузить();
	Для Каждого ТекущаяПартия Из ПартииПроизводства Цикл
		
		ЗаполнитьЗначенияСвойств(ОтборПоПартии, ТекущаяПартия);
		
		ТаблицаРНПТМатериалов   = Объект.РНПТМатериалов.Выгрузить(ОтборПоПартии, ИменаКолонок);
		ТаблицаПроселживаемости = ЗакупкиСервер.ПолучитьПрослеживаемыеНомераГТД(ТаблицаРНПТМатериалов);
		
		ПараметрыСоздания = ЗакупкиСервер.ПараметрыПоискаИлиСозданияСоставныхПрослеживаемыхКомплектов();
		ПараметрыСоздания.ИзвлечьНоменклатуруИзОбъектаВКолонке   = ТаблицаПроселживаемости.Колонки.АналитикаУчетаНоменклатуры.Имя;
		ПараметрыСоздания.ИзвлечьХарактеристикуИзОбъектаВКолонке = ТаблицаПроселживаемости.Колонки.АналитикаУчетаНоменклатуры.Имя;
		ПараметрыСоздания.ИзвлечьСериюИзОбъектаВКолонке          = ТаблицаПроселживаемости.Колонки.АналитикаУчетаНоменклатуры.Имя;
		ПараметрыСоздания.НоменклатураШапки                      = ТекущаяПартия.Номенклатура;
		ПараметрыСоздания.ХарактеристикаШапки                    = ТекущаяПартия.Характеристика;
		
		СоставныеПрослеживаемыеНомераГТД =
			ЗакупкиСервер.НайтиИлиСоздатьСоставныеПрослеживаемыеНомераГТД(
				ТаблицаПроселживаемости,
				ТекущаяПартия.Количество,
				Объект.Дата,
				Отказ,
				ПараметрыСоздания);
		
		Если СоставныеПрослеживаемыеНомераГТД.ЕстьСоставныеПрослеживаемыеНомераГТД Тогда
			
			КоличествоПартииПроизводства       = ТекущаяПартия.Количество;
			КоличествоПоРНПТПартииПроизводства = ТекущаяПартия.КоличествоПоРНПТ;
			
			НомерГТДТекущий = СоставныеПрослеживаемыеНомераГТД.ОсновнойСоставнойНомерГТД;
			
			Для Каждого ТекущиеДанные Из Объект.ВидыЗапасов.НайтиСтроки(ОтборПоПартии) Цикл
				
				ТекущиеДанные.НомерГТД = НомерГТДТекущий;
				
				КоличествоТекущее = Мин(ТекущиеДанные.Количество, КоличествоПартииПроизводства);
				
				// Если есть РНПТ погрешности округления и мы начинаем работать с крайней единицей видов запасов изделия,
				// то текущую строку надо разбить на две, если ее количество превышает остаточную единицу,
				// для всех оставшихся строк в качестве НомерГТД будет установлен ОстаточныйСоставнойНомерГТД.
				Если Не СоставныеПрослеживаемыеНомераГТД.ОстаточныйСоставнойНомерГТД = Неопределено
					И Не НомерГТДТекущий = СоставныеПрослеживаемыеНомераГТД.ОстаточныйСоставнойНомерГТД
					И КоличествоПартииПроизводства - КоличествоТекущее <= 1 Тогда
					
					КоличествоТекущее = КоличествоПартииПроизводства - 1;
					КоличествоНовое   = ТекущиеДанные.Количество - КоличествоТекущее;
					
					ТекущиеДанные.Количество = КоличествоТекущее;
					
					НомерГТДТекущий = СоставныеПрослеживаемыеНомераГТД.ОстаточныйСоставнойНомерГТД;
					
					НоваяСтрока = Объект.ВидыЗапасов.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
					НоваяСтрока.НомерГТД         = НомерГТДТекущий;
					НоваяСтрока.Количество       = КоличествоНовое;
					НоваяСтрока.КоличествоПоРНПТ = КоличествоПоРНПТПартииПроизводства * КоличествоНовое / КоличествоПартииПроизводства;
					
					КоличествоПартииПроизводства       = КоличествоПартииПроизводства - КоличествоНовое;
					КоличествоПоРНПТПартииПроизводства = КоличествоПоРНПТПартииПроизводства - НоваяСтрока.КоличествоПоРНПТ;
					
				КонецЕсли;
				
				ТекущиеДанные.КоличествоПоРНПТ = КоличествоПоРНПТПартииПроизводства * КоличествоТекущее / КоличествоПартииПроизводства;
				
				КоличествоПартииПроизводства       = КоличествоПартииПроизводства - КоличествоТекущее;
				КоличествоПоРНПТПартииПроизводства = КоличествоПоРНПТПартииПроизводства - ТекущиеДанные.КоличествоПоРНПТ;
				
				Если КоличествоПартииПроизводства = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Для Каждого ТекущиеДанный Из Объект.ВидыЗапасов.НайтиСтроки(Новый Структура("Количество", 0)) Цикл
				Объект.ВидыЗапасов.Удалить(ТекущиеДанный);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Проверки

Процедура ПроверитьСоответствиеРНПТМатериаловТоварам(Объект, Отказ, ПроверяемыеРеквизиты)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Товары",         Объект.Товары.Выгрузить());
	Запрос.УстановитьПараметр("РНПТМатериалов", Объект.РНПТМатериалов.Выгрузить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки        КАК НомерСтроки,
	|	Товары.ПартияПроизводства КАК ПартияПроизводства,
	|	Товары.Номенклатура       КАК Номенклатура
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РНПТМатериалов.НомерСтроки        КАК НомерСтроки,
	|	РНПТМатериалов.ПартияПроизводства КАК ПартияПроизводства,
	|	РНПТМатериалов.Номенклатура       КАК Номенклатура
	|ПОМЕСТИТЬ ВтРНПТМатериалов
	|ИЗ
	|	&РНПТМатериалов КАК РНПТМатериалов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки                               КАК НомерСтроки,
	|	МАКСИМУМ(Товары.НесколькоПрослеживаемыхРабот)    КАК НесколькоПрослеживаемыхРабот,
	|	МАКСИМУМ(Товары.ОтсутствуетПартияПроизводства)   КАК ОтсутствуетПартияПроизводства,
	|	МАКСИМУМ(Товары.ОтсутствуетПрослеживаемаяРабота) КАК ОтсутствуетПрослеживаемаяРабота,
	|	МАКСИМУМ(Товары.ОтсутствуетРНПТМатериалов)       КАК ОтсутствуетРНПТМатериалов
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(Товары.НомерСтроки) КАК НомерСтроки,
	|		ИСТИНА                       КАК НесколькоПрослеживаемыхРабот,
	|		ЛОЖЬ                         КАК ОтсутствуетПартияПроизводства,
	|		ЛОЖЬ                         КАК ОтсутствуетПрослеживаемаяРабота,
	|		ЛОЖЬ                         КАК ОтсутствуетРНПТМатериалов
	|	ИЗ
	|		ВтТовары КАК Товары
	|	ГДЕ
	|		Товары.Номенклатура.ПрослеживаемыйТовар
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Товары.ПартияПроизводства
	|	
	|	ИМЕЮЩИЕ
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Товары.Номенклатура) > 1
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РНПТМатериалов.НомерСтроки КАК НомерСтроки,
	|		ЛОЖЬ                       КАК НесколькоПрослеживаемыхРабот,
	|		ИСТИНА                     КАК ОтсутствуетПартияПроизводства,
	|		ЛОЖЬ                       КАК ОтсутствуетПрослеживаемаяРабота,
	|		ЛОЖЬ                       КАК ОтсутствуетРНПТМатериалов
	|	ИЗ
	|		ВтРНПТМатериалов КАК РНПТМатериалов
	|	ГДЕ
	|		НЕ ИСТИНА В(
	|			ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				ВтТовары КАК Товары
	|			ГДЕ
	|				Товары.ПартияПроизводства = РНПТМатериалов.ПартияПроизводства
	|			)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РНПТМатериалов.НомерСтроки КАК НомерСтроки,
	|		ЛОЖЬ                       КАК НесколькоПрослеживаемыхРабот,
	|		ЛОЖЬ                       КАК ОтсутствуетПартияПроизводства,
	|		ИСТИНА                     КАК ОтсутствуетПрослеживаемаяРабота,
	|		ЛОЖЬ                       КАК ОтсутствуетРНПТМатериалов
	|	ИЗ
	|		ВтРНПТМатериалов КАК РНПТМатериалов
	|	ГДЕ
	|		НЕ ИСТИНА В(
	|			ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				ВтТовары КАК Товары
	|			ГДЕ
	|				Товары.ПартияПроизводства = РНПТМатериалов.ПартияПроизводства
	|				И Товары.Номенклатура.ПрослеживаемыйТовар
	|				И Товары.Номенклатура.ПереноситьРНПТМатериала
	|			)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Товары.НомерСтроки   КАК НомерСтроки,
	|		ЛОЖЬ                 КАК НесколькоПрослеживаемыхРабот,
	|		ЛОЖЬ                 КАК ОтсутствуетПартияПроизводства,
	|		ЛОЖЬ                 КАК ОтсутствуетПрослеживаемаяРабота,
	|		ИСТИНА               КАК ОтсутствуетРНПТМатериалов
	|	ИЗ
	|		ВтТовары КАК Товары
	|	ГДЕ
	|		Товары.Номенклатура.ПереноситьРНПТМатериала
	|		И НЕ ИСТИНА В(
	|			ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				ВтРНПТМатериалов КАК РНПТМатериалов
	|			ГДЕ
	|				Товары.ПартияПроизводства = РНПТМатериалов.ПартияПроизводства
	|			)
	|	) КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.НомерСтроки
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.НесколькоПрослеживаемыхРабот Тогда
			
			ТекстОшибки = НСтр("ru = 'В списке ""Услуги по переработке"" для партии производства указано несколько прослеживаемых работ.
									|Необходимо оставить только одну прослеживаемую работу.';
									|en = 'In the ""Processing services"" list, multiple traceable works are specified for the production lot.
									|Only one traceable work must be retained.'");
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				Объект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки, "ПартияПроизводства"),,
				Отказ);
			
		КонецЕсли;
		
		Если Выборка.ОтсутствуетПартияПроизводства Тогда
			
			ТекстОшибки = НСтр("ru = 'В строке %1 списка ""РНПТ материалов"" указана партия производства по которой отсутствует услуга по переработке.
									|Необходимо удалить эту строку, либо добавить услугу переработки по этой партии производства.';
									|en = 'In the %1 line of the ""RNPT materials"" list, a production lot is indicated for which there is no processing service.
									|It is necessary to either delete this line or add a processing service for this production lot.'");
			
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(ТекстОшибки, Выборка.НомерСтроки),
				Объект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("РНПТМатериалов", Выборка.НомерСтроки, "ПартияПроизводства"),,
				Отказ);
			
		КонецЕсли;
		
		Если Не Выборка.ОтсутствуетПартияПроизводства
			И Выборка.ОтсутствуетПрослеживаемаяРабота Тогда
			
			ТекстОшибки = НСтр("ru = 'В строке %1 списка ""РНПТ материалов"" указана партия производства по которой отсутствует прослеживаемая услуга по переработке.
									|Необходимо указать прослеживаемую работу в качестве услуги переработки.';
									|en = 'In the %1 line of the ""RNPT materials"" list, a production lot is indicated for which there is no traceable processing service.
									|It is necessary to specify a traceable work as the processing service.'");
			
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(ТекстОшибки, Выборка.НомерСтроки),
				Объект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("РНПТМатериалов", Выборка.НомерСтроки, "ПартияПроизводства"),,
				Отказ);
			
		КонецЕсли;
		
		Если Выборка.ОтсутствуетРНПТМатериалов Тогда
			
			ТекстОшибки = НСтр("ru = 'В строке %1 списка ""Услуги по переработке"" указана прослеживаемая услуга по переработке для партии производства которой не указаны РНПТ материалов.
									|Необходимо указать РНПТ материалов для партии производства, либо указать не прослеживаемую услугу переработки.';
									|en = 'In the %1 line of the ""Processing services"" list, a traceable processing service is indicated for a production lot without specified RNPT materials.
									|It is necessary to either specify RNPT materials for the production lot or indicate a non-traceable processing service.'");
			
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(ТекстОшибки, Выборка.НомерСтроки),
				Объект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки, "Номенклатура"),,
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ФрагментыПроводокРеглУчета

Функция ТекстВыпускУслугПереработки()
	
	Возврат "
	|ВЫБРАТЬ // Выпуск услуг переработки (Дт 20 :: Кт 20)
	|	Стоимости.Ссылка КАК Ссылка,
	|	Стоимости.Период КАК Период,
	|	Стоимости.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Стоимости.СуммаБалансовая КАК Сумма,
	|	Стоимости.СуммаБалансоваяУУ КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
	|	Стоимости.КорГруппаПродукции КАК АналитикаУчетаДт,
	|	Стоимости.КорПодразделениеАналитики КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Стоимости.КорПодразделениеАналитики КАК ПодразделениеДт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)  КАК СубконтоДт1,
	|	ВЫБОР
	|		КОГДА Стоимости.КорТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Работы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты)
	|	КОНЕЦ КАК СубконтоДт2,
	|	Стоимости.КорГруппаПродукции КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Стоимости.СуммаНУ КАК СуммаНУДт,
	|	Стоимости.СуммаПР КАК СуммаПРДт,
	|	Стоимости.СуммаВР КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаКт,
	|	Стоимости.ГруппаПродукции КАК АналитикаУчетаКт,
	|	Стоимости.ПодразделениеАналитики КАК МестоУчетаКт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Стоимости.ПодразделениеАналитики КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СубконтоКт1,
	|	ВЫБОР
	|		КОГДА Стоимости.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Работы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты)
	|	КОНЕЦ КАК СубконтоКт2,
	|	Стоимости.ГруппаПродукции КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Стоимости.СуммаНУ КАК СуммаНУКт,
	|	Стоимости.СуммаПР КАК СуммаПРКт,
	|	Стоимости.СуммаВР КАК СуммаВРКт,
	|	""Выпуск услуг переработки"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтчетДавальцуМеждуОрганизациями КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Стоимости.Ссылка = Операция.Ссылка
	|		И Стоимости.Организация = Операция.Организация
	|		И Стоимости.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Стоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|		И Стоимости.КорРазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК КорСтоимости
	|	ПО
	|		Стоимости.Ссылка = КорСтоимости.Ссылка
	|		И Стоимости.Организация = КорСтоимости.Организация
	|		И Стоимости.ИдентификаторФинЗаписи = КорСтоимости.ИдентификаторФинЗаписи
	|		И Стоимости.НастройкаХозяйственнойОперации = КорСтоимости.НастройкаХозяйственнойОперации
	|		И КорСтоимости.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И КорСтоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	
	|ГДЕ
	|	(НЕ Стоимости.НаправлениеДеятельности = Стоимости.КорНаправлениеДеятельности
	|		ИЛИ НЕ Стоимости.ГруппаПродукции = Стоимости.КорГруппаПродукции
	|		ИЛИ НЕ Стоимости.ПодразделениеАналитики = Стоимости.КорПодразделениеАналитики)
	|";
	
КонецФункции

Функция ТекстСебестоимостьПродаж()
	
	Возврат "
	|ВЫБРАТЬ // Себестоимость продаж (Дт 90.02 :: Кт 20)
	|
	|	Строки.Ссылка КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Стоимости.СуммаБалансовая КАК Сумма,
	|	Стоимости.СуммаБалансоваяУУ КАК СуммаУУ,
	|
	|	ВыручкаИСебестоимость.ВидСчетаСебестоимости КАК ВидСчетаДт,
	|	ВыручкаИСебестоимость.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ВыручкаИСебестоимость.Подразделение КАК ПодразделениеДт,
	|	ВыручкаИСебестоимость.НДВыручки КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ВыручкаИСебестоимость.Номенклатура КАК СубконтоДт1,
	|	ЕСТЬNULL(ТаблицаНалогообложенияПрибыли.ВариантНалогообложенияПрибыли, НЕОПРЕДЕЛЕНО) КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	Стоимости.СуммаНУ КАК СуммаНУДт,
	|	Стоимости.СуммаПР КАК СуммаПРДт,
	|	Стоимости.СуммаВР КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаКт,
	|	Строки.ГруппаПродукции КАК АналитикаУчетаКт,
	|	Строки.ПодразделениеАналитики КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	Строки.ГруппаПродукции КАК СубконтоКт2,
	|	Строки.ТипЗатрат КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Стоимости.СуммаНУ КАК СуммаНУКт,
	|	Стоимости.СуммаПР КАК СуммаПРКт,
	|	Стоимости.СуммаВР КАК СуммаВРКт,
	|	""Себестоимость продаж"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтчетДавальцуМеждуОрганизациями КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|		И Строки.Организация = Операция.Организация
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Строки.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		И Строки.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ВыпускПродукции)
	|		И Строки.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.ВидДвижения = Стоимости.ВидДвижения
	|		И Строки.Организация = Стоимости.Организация
	|		И Строки.ИдентификаторФинЗаписи = Стоимости.ИдентификаторФинЗаписи
	|		И Строки.НастройкаХозяйственнойОперации = Стоимости.НастройкаХозяйственнойОперации
	|		И Стоимости.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ВыпускПродукции)
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимость
	|	ПО Строки.Ссылка = ВыручкаИСебестоимость.Ссылка
	|		И Строки.Организация = ВыручкаИСебестоимость.Организация
	|		И Строки.ИдентификаторФинЗаписи = ВыручкаИСебестоимость.ИдентификаторФинЗаписи
	|		И Строки.НастройкаХозяйственнойОперации = ВыручкаИСебестоимость.НастройкаХозОперации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РаздельныйУчет_НастройкиНалогообложенияПрибыли КАК ТаблицаНалогообложенияПрибыли
	|	ПО 
	|		ТаблицаНалогообложенияПрибыли.Период = Строки.Период
	|		И ТаблицаНалогообложенияПрибыли.Организация = Строки.Организация
	|		И ТаблицаНалогообложенияПрибыли.Подразделение = ВыручкаИСебестоимость.Подразделение
	|		И ТаблицаНалогообложенияПрибыли.ОбъектУчета = Строки.Номенклатура
	|		И ТаблицаНалогообложенияПрибыли.НаправлениеДеятельности = ВыручкаИСебестоимость.НДВыручки
	|
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетДавальцуМеждуОрганизациями)
	|";
	
КонецФункции

Функция ТекстСписаниеТоваровНаРасходы()
	
	ТекстЗапроса = ПроизводствоБезЗаказаЛокализация.ТекстСписаниеТоваровНаРасходы(Истина, Истина, Истина);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоУмолчанию", "Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)");
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстСписаниеТоваровНаСтатьиАктивовПассивов()
	
	ТекстЗапроса = ПроизводствоБезЗаказаЛокализация.ТекстСписаниеТоваровНаСтатьиАктивовПассивов(Истина, Истина);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоУмолчанию", "Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)");

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстРеализацияУслугПереработки()
	
	Возврат "
	|ВЫБРАТЬ // Реализация услуг переработки (Дт 62 :: Кт 90.01, 91.01)
	|
	|	Расчеты.Ссылка КАК Ссылка,
	|	Расчеты.Период КАК Период,
	|	Расчеты.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ВыручкаИСебестоимость.СуммаРегл КАК Сумма,
	|	ВыручкаИСебестоимость.СуммаУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчетаДт,
	|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	Расчеты.Валюта КАК ВалютаДт,
	|	Расчеты.Подразделение КАК ПодразделениеДт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	Расчеты.Контрагент КАК СубконтоДт1,
	|	Расчеты.Договор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	ВыручкаИСебестоимость.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаНалогообложенияПрибыли.Организация ЕСТЬ NULL
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаРаздельныйНУ)
	|		ИНАЧЕ ВыручкаИСебестоимость.ВидСчетаВыручки
	|	КОНЕЦ КАК ВидСчетаКт,
	|	ВыручкаИСебестоимость.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	ВыручкаИСебестоимость.Подразделение КАК ПодразделениеКт,
	|	ВыручкаИСебестоимость.НДВыручки КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	ВыручкаИСебестоимость.Номенклатура КАК СубконтоКт1,
	|	Суммы.СтавкаНДС.ПеречислениеСтавкаНДС КАК СубконтоКт2,
	|	ЕСТЬNULL(ТаблицаНалогообложенияПрибыли.ВариантНалогообложенияПрибыли, ВыручкаИСебестоимость.Номенклатура) КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	ВыручкаИСебестоимость.Количество КАК КоличествоКт,
	|	ВыручкаИСебестоимость.СуммаБезНДСРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Реализация услуг переработки"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтчетДавальцуМеждуОрганизациями КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РасчетыСКлиентамиНоваяАрхитектура КАК Расчеты
	|	ПО 
	|		ДокументыКОтражению.Ссылка = Расчеты.Ссылка
	|		И Расчеты.Организация = Операция.Организация
	|		И Расчеты.НастройкаХозОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПустаяСсылка)
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		ВТВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимость
	|	ПО 
	|		Расчеты.Ссылка = ВыручкаИСебестоимость.Ссылка
	|		И Расчеты.ОбъектРасчетов = ВыручкаИСебестоимость.ИсточникГФУРасчетов
	|		И ВыручкаИСебестоимость.НастройкаХозОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОтчетДавальцуМеждуОрганизациями)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО 
	|		Суммы.Регистратор = ВыручкаИСебестоимость.Ссылка
	|		И Суммы.ИдентификаторСтроки = ВыручкаИСебестоимость.ИдентификаторФинЗаписи
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РаздельныйУчет_НастройкиНалогообложенияПрибыли КАК ТаблицаНалогообложенияПрибыли
	|	ПО 
	|		ТаблицаНалогообложенияПрибыли.Период = Расчеты.Период
	|		И ТаблицаНалогообложенияПрибыли.Организация = Расчеты.Организация
	|		И ТаблицаНалогообложенияПрибыли.Подразделение = ВыручкаИСебестоимость.Подразделение
	|		И ТаблицаНалогообложенияПрибыли.ОбъектУчета = ВыручкаИСебестоимость.Номенклатура
	|		И ТаблицаНалогообложенияПрибыли.НаправлениеДеятельности = ВыручкаИСебестоимость.НДВыручки
	|
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетДавальцуМеждуОрганизациями)
	|";
	
КонецФункции

Функция ТекстНачислениеНДСЗаУслуги()
	
	ТекстНачислениеНДСЗаУслуги = "
	|ВЫБРАТЬ // Начисление НДС (Дт <90.3, 91.2> :: Кт <68.2>)
	|
	|	ВыручкаИСебестоимость.Ссылка КАК Ссылка,
	|	ВыручкаИСебестоимость.Период КАК Период,
	|	ВыручкаИСебестоимость.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Суммы.СуммаНДСРегл КАК Сумма,
	|	Суммы.СуммаНДСУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСПриПродаже) КАК ВидСчетаДт,
	|	ВыручкаИСебестоимость.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК МестоУчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ВыручкаИСебестоимость.Подразделение КАК ПодразделениеДт,
	|	ВыручкаИСебестоимость.НДВыручки КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	ВыручкаИСебестоимость.Номенклатура КАК СубконтоДт1,
	|	Суммы.СтавкаНДС.ПеречислениеСтавкаНДС КАК СубконтоДт2,
	|	ЕСТЬNULL(ТаблицаНалогообложенияПрибыли.ВариантНалогообложенияПрибыли, НЕОПРЕДЕЛЕНО) КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	&ВалютаРеглУчета КАК ВалютаКт,
	|	ВыручкаИСебестоимость.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.НДС) КАК СчетКт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог) КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	Суммы.СуммаНДСРегл КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Начисление НДС"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтчетДавальцуМеждуОрганизациями КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимость
	|	ПО ДокументыКОтражению.Ссылка = ВыручкаИСебестоимость.Ссылка
	|		И ВыручкаИСебестоимость.НастройкаХозОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОтчетДавальцуМеждуОрганизациями)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО Суммы.Регистратор = ВыручкаИСебестоимость.Ссылка
	|		И Суммы.ИдентификаторСтроки = ВыручкаИСебестоимость.ИдентификаторФинЗаписи
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РаздельныйУчет_НастройкиНалогообложенияПрибыли КАК ТаблицаНалогообложенияПрибыли
	|	ПО 
	|		ТаблицаНалогообложенияПрибыли.Период = ВыручкаИСебестоимость.Период
	|		И ТаблицаНалогообложенияПрибыли.Организация = ВыручкаИСебестоимость.Организация
	|		И ТаблицаНалогообложенияПрибыли.Подразделение = ВыручкаИСебестоимость.Подразделение
	|		И ТаблицаНалогообложенияПрибыли.ОбъектУчета = ВыручкаИСебестоимость.Номенклатура
	|		И ТаблицаНалогообложенияПрибыли.НаправлениеДеятельности = ВыручкаИСебестоимость.НДВыручки
	|
	|ГДЕ
	|	Суммы.СуммаНДСРегл <> 0
	|	И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетДавальцуМеждуОрганизациями)";
	
	Возврат ТекстНачислениеНДСЗаУслуги;
	
КонецФункции

Функция ТекстСписаниеНаПартииПроизводства()
	
	Возврат "
	|ВЫБРАТЬ //// Списание на партии производства (Дт 20 :: Кт 20)
	|
	|	Стоимости.Ссылка КАК Ссылка,
	|	Стоимости.Период КАК Период,
	|	Стоимости.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Стоимости.СуммаБалансовая КАК Сумма,
	|	Стоимости.СуммаБалансоваяУУ КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
	|	Стоимости.ГруппаПродукции КАК АналитикаУчетаДт,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Стоимости.Склад) = ТИП(Справочник.ДоговорыКонтрагентов)
	|		ТОГДА Стоимости.Склад.Партнер
	|		ИНАЧЕ Стоимости.ПодразделениеАналитики
	|	КОНЕЦ КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Стоимости.ПодразделениеАналитики КАК ПодразделениеДт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Стоимости.ГруппаПродукции КАК СубконтоДт1,
	|	Стоимости.Номенклатура КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА Стоимости.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Работы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты)
	|	КОНЕЦ КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаКт,
	|	КорСтоимости.ГруппаПродукции КАК АналитикаУчетаКт,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(КорСтоимости.Склад) = ТИП(Справочник.ДоговорыКонтрагентов)
	|		ТОГДА КорСтоимости.Склад.Партнер
	|		ИНАЧЕ КорСтоимости.ПодразделениеАналитики
	|	КОНЕЦ КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	КорСтоимости.ПодразделениеАналитики КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	КорСтоимости.ГруппаПродукции КАК СубконтоКт1,
	|	КорСтоимости.Номенклатура КАК СубконтоКт2,
	|	ВЫБОР
	|		КОГДА КорСтоимости.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Работы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты)
	|	КОНЕЦ		КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Списание на партии производства"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтчетДавальцуМеждуОрганизациями КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Стоимости.Ссылка = Операция.Ссылка
	|		И Стоимости.Организация = Операция.Давалец
	|		И Стоимости.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Стоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|		И Стоимости.НастройкаХозяйственнойОперации <> ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВключениеНДСВСтоимость)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК КорСтоимости
	|	ПО
	|		Стоимости.Ссылка = КорСтоимости.Ссылка
	|		И Стоимости.Организация = КорСтоимости.Организация
	|		И Стоимости.ИдентификаторФинЗаписи = КорСтоимости.ИдентификаторФинЗаписи
	|		И Стоимости.НастройкаХозяйственнойОперации = КорСтоимости.НастройкаХозяйственнойОперации
	|		И КорСтоимости.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И КорСтоимости.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	
	|ГДЕ
	|	(НЕ Стоимости.НаправлениеДеятельности = Стоимости.КорНаправлениеДеятельности
	|		ИЛИ НЕ Стоимости.ГруппаПродукции = Стоимости.КорГруппаПродукции
	|		ИЛИ НЕ Стоимости.ПодразделениеАналитики = Стоимости.КорПодразделениеАналитики)
	|";
	
КонецФункции

Функция ТекстПриобретениеРабот()
	
	Возврат "
	|ВЫБРАТЬ //// Приобретение работ (Дт 20 :: Кт 60)
	|
	|	Строки.Ссылка КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.СуммаБалансовая КАК Сумма,
	|	Строки.СуммаБалансоваяУУ КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
	|	Строки.ГруппаПродукции КАК АналитикаУчетаДт,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(Строки.Склад) = ТИП(Справочник.ДоговорыКонтрагентов)
	|		ТОГДА Строки.Склад.Партнер
	|		ИНАЧЕ Строки.ПодразделениеАналитики
	|	КОНЕЦ КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.ГруппаПродукции КАК СубконтоДт1,
	|	Строки.Номенклатура КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА Строки.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Работы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты)
	|	КОНЕЦ КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаКт,
	|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Расчеты.Валюта КАК ВалютаКт,
	|	Расчеты.Подразделение КАК ПодразделениеКт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	Расчеты.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО		КАК СубконтоКт3,
	|
	|	ВЫБОР
	|		КОГДА Суммы.Валюта = Суммы.ВалютаВзаиморасчетов
	|		ТОГДА Суммы.СуммаБезНДС
	|		ИНАЧЕ Суммы.СуммаВзаиморасчетов - Суммы.СуммаНДСВзаиморасчетов
	|	КОНЕЦ КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Приобретение работ"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтчетДавальцуМеждуОрганизациями КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|		И Строки.Организация = Операция.Давалец
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		И Строки.НастройкаХозяйственнойОперации <> ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВключениеНДСВСтоимость)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Суммы.Регистратор = Строки.Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСПоставщиками КАК Расчеты
	|	ПО
	|		Строки.Ссылка = Расчеты.Ссылка
	|";
	
КонецФункции

Функция ТекстЗакупкаНДС() // НДС при приобретении (Дт 19 :: Кт 60)
	
	Возврат "
	|ВЫБРАТЬ //// НДС при приобретении по рег. НДСПредъявленный (Дт 19 :: Кт 60) @НДСПредъявленный
	|
	|	НДСПредъявленный.Регистратор КАК Ссылка,
	|	НДСПредъявленный.Период КАК Период,
	|	НДСПредъявленный.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	НДСПредъявленный.НДС КАК Сумма,
	|	НДСПредъявленный.НДСУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаДт,
	|	НДСПредъявленный.ВидДеятельностиНДС КАК АналитикаУчетаДт,
	|	НДСПредъявленный.ВидЦенности КАК МестоУчетаДт,
	|
	|	&ВалютаРеглУчета КАК ВалютаДт,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеДт,
	|	ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, НДСПредъявленный.НаправлениеДеятельности) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	НДСПредъявленный.Поставщик	КАК СубконтоДт1,
	|	НДСПредъявленный.СчетФактура КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	НДСПредъявленный.НДС КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчетаКт,
	|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Расчеты.Валюта КАК ВалютаКт,
	|	Расчеты.Подразделение КАК ПодразделениеКт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	Расчеты.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	ВЫБОР
	|		КОГДА Суммы.Валюта = Суммы.ВалютаВзаиморасчетов
	|		ТОГДА Суммы.СуммаНДС
	|		ИНАЧЕ Суммы.СуммаНДСВзаиморасчетов
	|	КОНЕЦ КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""НДС при приобретении"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтчетДавальцуМеждуОрганизациями КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|	ПО
	|		НДСПредъявленный.Регистратор = ДокументыКОтражению.Ссылка
	|		И НДСПредъявленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И НДСПредъявленный.Активность И НЕ НДСПредъявленный.Сторно
	|		И НЕ НДСПредъявленный.РегламентнаяОперация
	|		И НЕ НДСПредъявленный.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		НДСПредъявленный.Регистратор = Суммы.Регистратор
	|		И НДСПредъявленный.ИдентификаторСтроки = Суммы.ИдентификаторСтроки
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВТРасчетыСПоставщиками КАК Расчеты
	|	ПО 
	|		ДокументыКОтражению.Ссылка = Расчеты.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК ВводСобственныхСредств
	|	ПО ДокументыКОтражению.Ссылка = ВводСобственныхСредств.Ссылка
	|		И НДСПредъявленный.СчетФактура = ВводСобственныхСредств.СчетФактура
	|		И НДСПредъявленный.НаправлениеДеятельности = ВводСобственныхСредств.ИсходноеНаправлениеДеятельности
	|
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтчетДавальцуМеждуОрганизациями)
	|";
	
КонецФункции

#КонецОбласти

#Область ВидыЗапасов

// Заполнить виды запасов.
// 
// Параметры:
//  Объект - ДокументОбъект.ОтчетДавальцуМеждуОрганизациями - Объект
//  Отказ - Булево - Отказ
//
Процедура ЗаполнитьВидыЗапасов(Объект, Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерВТ = ВременныеТаблицыДанныхДокумента(Объект);
	Если УчетПрослеживаемыхТоваровЛокализация.ИспользоватьУчетПрослеживаемыхИмпортныхТоваров(Объект.Дата)
		И ПроверитьИзменениеДокумента(Объект, МенеджерВТ) Тогда
		
		ИменаРеквизитовДокумента =
			"Организация, ТипЗапасов, ВладелецТовара, Контрагент, Соглашение, Договор, Валюта,
			|НалогообложениеНДС, НалогообложениеОрганизации, ВидЗапасов, ГруппаФинансовогоУчета,
			|ГруппаПродукции, ХозяйственнаяОперация, ВидЦены, СохраняемыйВидЗапасов";
		
		РеквизитыДокумента = Новый Структура(ИменаРеквизитовДокумента);
		РеквизитыДокумента.ТипЗапасов  = Перечисления.ТипыЗапасов.Работа;
		РеквизитыДокумента.Организация = Объект.Организация;
		
		НалогообложениеОрганизации =
			УчетНДСУП.ПараметрыУчетаПоОрганизации(Объект.Организация, Объект.Дата).ОсновноеНалогообложениеНДСПродажи;
		РеквизитыДокумента.НалогообложениеОрганизации = НалогообложениеОрганизации;
		
		ВидЗапасов =
			Справочники.ВидыЗапасов.ВидЗапасовДокумента(
				Объект.Организация,
				Объект.ХозяйственнаяОперация,
				РеквизитыДокумента);
		
		РеквизитыДокумента.Организация = Объект.Давалец;
		
		НалогообложениеОрганизации =
			УчетНДСУП.ПараметрыУчетаПоОрганизации(Объект.Давалец, Объект.Дата).ОсновноеНалогообложениеНДСПродажи;
		РеквизитыДокумента.НалогообложениеОрганизации = НалогообложениеОрганизации;
		
		КорВидЗапасов =
			Справочники.ВидыЗапасов.ВидЗапасовДокумента(
				Объект.Давалец,
				Объект.ХозяйственнаяОперация,
				РеквизитыДокумента);
		
		Объект.ВидыЗапасов.Очистить();
		Для Каждого ТекущиеДанные Из РасчетСебестоимостиПрикладныеАлгоритмы.ВыгрузитьВременнуюТаблицу(МенеджерВТ, "ТаблицаТоваров") Цикл
			
			НоваяСтрока = Объект.ВидыЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные,, "ИдентификаторСтроки");
			НоваяСтрока.ВидЗапасов    = ВидЗапасов;
			НоваяСтрока.КорВидЗапасов = КорВидЗапасов;
			НоваяСтрока.КоличествоПоРНПТ = НоваяСтрока.Количество;
			
		КонецЦикла;
		
		РаспределитьНомераГТД(Объект, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

//-- Локализация

#КонецОбласти
