////////////////////////////////////////////////////////////////////////////////
// Подсистема "Сервис доставки".
// ОбщийМодуль.СервисДоставкиПереопределяемый.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ЗаполнениеПараметровОтбора

// Заполняет параметры отбора для формы выбора адреса организации.
//
// Параметры:
//  ПараметрыОтбора - Структура - параметры отбора для формы выбора.
//  Организация     - ОпределяемыйТип.ОрганизацияСервисДоставки - ссылка на организацию.
//
Процедура ЗаполнитьПараметрыОтбораАдресаОрганизации(ПараметрыОтбора, Организация) Экспорт
	
КонецПроцедуры

// Заполняет параметры отбора для формы выбора адреса контрагента.
//
// Параметры:
//  ПараметрыОтбора - Структура - параметры отбора для формы выбора.
//  Контрагент      - ОпределяемыйТип.КонтрагентСервисДоставки - ссылка на контрагента.
//
Процедура ЗаполнитьПараметрыОтбораАдресаКонтрагента(ПараметрыОтбора, Контрагент) Экспорт
	
	// ++ СервисДоставки.ERP
	ПараметрыОтбора.Вставить("Контрагент", Контрагент);
	// -- СервисДоставки.ERP
	
КонецПроцедуры

// Заполняет параметры отбора для формы выбора контактного лица организации.
//
// Параметры:
//  ПараметрыОтбора - Структура - параметры отбора для формы выбора.
//  Организация     - ОпределяемыйТип.ОрганизацияСервисДоставки - ссылка на организацию.
//
Процедура ЗаполнитьПараметрыОтбораКонтактногоЛицаОрганизации(ПараметрыОтбора, Организация) Экспорт
	
КонецПроцедуры

// Заполняет параметры отбора для формы выбора контактного лица контрагента.
//
// Параметры:
//  ПараметрыОтбора - Структура - параметры отбора для формы выбора.
//  Контрагент      - ОпределяемыйТип.КонтрагентСервисДоставки - ссылка на контрагента.
//
Процедура ЗаполнитьПараметрыОтбораКонтактногоЛицаКонтрагента(ПараметрыОтбора, Контрагент) Экспорт
	
	// ++ СервисДоставки.ERP
	Партнер = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, Новый Структура("Партнер")).Партнер;
	СписокПартнеров = ПартнерыИКонтрагенты.ПолучитьНижестоящихПартнеров(Партнер);
	
	ПараметрыОтбора.Вставить("Владелец", СписокПартнеров);
	// -- СервисДоставки.ERP
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеПараметровДаннымиИзУчетнойСистемы

// Заполнить валюту организации.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации - организация.
//  Валюта - СправочникСсылка.Валюты - валюта организации.
Процедура ЗаполнитьВалютуОрганизации(Организация, Валюта) Экспорт
	
	ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	
КонецПроцедуры

// Заполняет параметры заказа на доставку данными из объекта-основания заказа.
//
// Параметры:
//  Параметры - см. СервисДоставки.НовыйПараметрыЗаказаНаДоставку
//
Процедура ЗаполнитьПараметрыЗаказаНаДоставку(Параметры) Экспорт
	
	ДокументыОснования = Параметры.ДокументыОснования;
	
	Если ДокументыОснования.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонЗапросаШапка = ШаблонЗапросаДанныеШапкиДокумента();
	ШаблонЗапросаВидыЗапасов = ШаблонЗапросаВидыЗапасовДокумента();
	ПустойШаблонЗапросаВидыЗапасов = ШаблонЗапросаВидыЗапасовПустойРезультат();
	ШаблонЗапросаТовары = ШаблонЗапросаТоварыДокумента();
	
	Основания = ДокументыОснования.ВыгрузитьЗначения();
	ОснованияПоТипам = РазложитьМассивСсылокПоТипам(Основания);
	ПервыйЗапросПоШапке = Истина;
	ПервыйЗапросПоТоварам = Истина;
	ТекстЗапроса = "";
	ТекстЗапросаПоШапке = "";
	ТекстЗапросаПоТоварам = "";
	ТекстЗапросаВидовЗапасов = "";
	
	Для Каждого КлючИЗначение Из ОснованияПоТипам Цикл
		
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(КлючИЗначение.Ключ);
		ИмяТаблицыОбъекта = МетаданныеОбъекта.ПолноеИмя();
		ИмяОбъекта = СтрРазделить(ИмяТаблицыОбъекта, ".")[1];
		
		ТекстЗапросаПоТипу = ШаблонЗапросаШапка;
		ИзменитьТекстЗапросаПоРеквизитамШапкиОбъекта(ТекстЗапросаПоТипу, ИмяОбъекта, МетаданныеОбъекта);
		ДополнитьЗапросПоОснованиям(ТекстЗапросаПоШапке, ТекстЗапросаПоТипу, ПервыйЗапросПоШапке, ИмяТаблицыОбъекта);
		
		ИмяТаблицыЗапасов = "ВидыЗапасов";
		Если МетаданныеОбъекта.ТабличныеЧасти.Найти(ИмяТаблицыЗапасов) = Неопределено Тогда
			ТекстЗапросаВидовЗапасов = ПустойШаблонЗапросаВидыЗапасов;
		Иначе
			ТекстЗапросаВидовЗапасов = "";
			ДополнитьЗапросПоОснованиям(
				ТекстЗапросаВидовЗапасов, ШаблонЗапросаВидыЗапасов, Истина, ИмяТаблицыОбъекта + "." + ИмяТаблицыЗапасов);
		КонецЕсли;
		
		СписокТабличныхЧастейТовары = Новый Массив();
		СписокТабличныхЧастейТовары.Добавить("Товары");
		СписокТабличныхЧастейТовары.Добавить("ЗаменяющиеТовары");
		
		Для Каждого ИмяТаблицыТЧ Из СписокТабличныхЧастейТовары Цикл
		
			ТекстЗапросаПоТипу = ШаблонЗапросаТовары;
			
			Если МетаданныеОбъекта.ТабличныеЧасти.Найти(ИмяТаблицыТЧ) <> Неопределено Тогда
				
				МетаданныеОбъектаТЧ = МетаданныеОбъекта.ТабличныеЧасти[ИмяТаблицыТЧ]; // Структура
				ИмяТаблицыТЧОбъекта = ИмяТаблицыОбъекта + "." + МетаданныеОбъектаТЧ.Имя;
				
				ИзменитьТекстЗапросаПоРеквизитамТабличнойЧастиОбъекта(ТекстЗапросаПоТипу, МетаданныеОбъектаТЧ, Параметры);
				ДополнитьЗапросПоОснованиям(ТекстЗапросаПоТоварам, ТекстЗапросаПоТипу, ПервыйЗапросПоТоварам, ИмяТаблицыТЧОбъекта);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапросаПоШапке + ОбщегоНазначения.РазделительПакетаЗапросов() 
	+ ТекстЗапросаВыборкаИзДанныхОснований();
	
	Если ТекстЗапросаПоТоварам <> "" Тогда 
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов()
		+ ТекстЗапросаВидовЗапасов
		+ ОбщегоНазначения.РазделительПакетаЗапросов()
		+ ТекстЗапросаПоТоварам
		+ ОбщегоНазначения.РазделительПакетаЗапросов()
		+ ТекстЗапросаВыборкаИзДанныхОснованийПоТоварам();
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаВесУпаковки", Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("УпаковкиЕдиницыИзмерения", "СправочникНоменклатура", Ложь));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаОбъемУпаковки", Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки("УпаковкиЕдиницыИзмерения", "СправочникНоменклатура", Ложь));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаДлинаУпаковки", Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаДлинаУпаковки("УпаковкиЕдиницыИзмерения", "СправочникНоменклатура", Ложь));
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Основания", Основания);
	Запрос.УстановитьПараметр("Дата", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("РозничныйПокупатель", Справочники.Партнеры.РозничныйПокупатель);
	
	ДанныеПоУмолчанию = СервисДоставки.ПараметрыПоУмолчанию(Параметры.ТипГрузоперевозки);
	
	Запрос.УстановитьПараметр("СпособОпределенияКонтактногоЛица", ДанныеПоУмолчанию.СпособОпределенияКонтактногоЛица);
	Запрос.УстановитьПараметр("ОтветственныйСклада", ДанныеПоУмолчанию.КонтактноеЛицо);
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.УстановитьПараметр("НаименованиеДляПечатиВидовНоменклатуры", Константы.НаименованиеДляПечатиВидовНоменклатуры.Получить());
	Результаты = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	РезультатПоШапке = Результаты[1];
	Если РезультатПоШапке.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаПоДокументу = РезультатПоШапке.Выгрузить();
	ДанныеПоДокументу = ТаблицаПоДокументу[0];
	
	СервисДоставкиКлиентСервер.ЗаполнитьСтруктуруПоЛинейнымДанным(Параметры, ДанныеПоДокументу, ТаблицаПоДокументу.Колонки);
	
	Параметры.Вставить("ОрганизацияБизнесСетиСсылка", ДанныеПоДокументу.ОрганизацияБизнесСетиСсылка);
	
	// Заполним грузоперевозчика
	Если ЗначениеЗаполнено(Параметры.Грузоперевозчик.Ссылка) Тогда
		ГрузоперевозчикКонтрагент = ПартнерыИКонтрагенты.ПолучитьКонтрагентаПартнераПоУмолчанию(Параметры.Грузоперевозчик.Ссылка);
		Если ЗначениеЗаполнено(ГрузоперевозчикКонтрагент) Тогда
			ГрузоперевозчикПараметры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ГрузоперевозчикКонтрагент, "ИНН,КПП,НаименованиеПолное,Наименование");
			ГрузоперевозчикПараметры.Наименование = 
				?(ЗначениеЗаполнено(ГрузоперевозчикПараметры.НаименованиеПолное),
				ГрузоперевозчикПараметры.НаименованиеПолное,
				ГрузоперевозчикПараметры.Наименование);
					
			ЗаполнитьЗначенияСвойств(Параметры.Грузоперевозчик, ГрузоперевозчикПараметры);
		КонецЕсли;
		
	КонецЕсли;
	
	// Заполним значения адресов
	Попытка
		СервисДоставки.ЗаполнитьАдресПоПараметрам(Параметры.Отправитель.Адрес);
	Исключение
		ТекстСообщения = НСтр("ru = 'Адрес отправителя не прошел проверку. Заполнение пропущено.';
								|en = 'The sender address did not pass the check. The filling is skipped.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецПопытки;
	Попытка
		СервисДоставки.ЗаполнитьАдресПоПараметрам(Параметры.Получатель.Адрес);
	Исключение
		ТекстСообщения = НСтр("ru = 'Адрес получателя не прошел проверку. Заполнение пропущено.';
								|en = 'The recipient address did not pass the check. The filling is skipped.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецПопытки;
	Попытка
		СервисДоставки.ЗаполнитьАдресПоПараметрам(Параметры.Отправитель.Контрагент.ЮридическийАдрес);
	Исключение
		ТекстСообщения = НСтр("ru = 'Юридический адрес контрагента отправителя не прошел проверку. Заполнение пропущено.';
								|en = 'The legal address of the sender counterparty did not pass the check. The filling is skipped.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецПопытки;
	Попытка
		СервисДоставки.ЗаполнитьАдресПоПараметрам(Параметры.Получатель.Контрагент.ЮридическийАдрес);
	Исключение
		ТекстСообщения = НСтр("ru = 'Юридический адрес контрагента получателя не прошел проверку. Заполнение пропущено.';
								|en = 'The legal address of the recipient counterparty did not pass the check. The filling is skipped.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецПопытки;
	
	//Заполним телефоны контактных лиц
	СервисДоставки.ЗаполнитьПараметрыКонтактногоЛица(Параметры.Отправитель.КонтактноеЛицо);
	
	СервисДоставки.ЗаполнитьПараметрыКонтактногоЛица(Параметры.Получатель.КонтактноеЛицо);
	
	РезультатИтогиПоТаблицеТовары = Результаты[6];
	РезультатНаименованийДляПечати = Результаты[7];
	
	// Заполним параметры грузовых мест
	Если Не РезультатИтогиПоТаблицеТовары.Пустой() Тогда
		ВыборкаПоТоварам = РезультатИтогиПоТаблицеТовары.Выбрать();
		Если ВыборкаПоТоварам.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(Параметры.Груз, ВыборкаПоТоварам);
			Параметры.ПолнаяСтоимость = ВыборкаПоТоварам.ПолнаяСтоимость;
		КонецЕсли;
	КонецЕсли;
	
	Если Не РезультатНаименованийДляПечати.Пустой() 
		И Не СервисДоставкиКлиентСервер.ЭтоДеловыеЛинии(Параметры.ТипГрузоперевозки) Тогда
		
		ВыборкаПоТоварам = РезультатНаименованийДляПечати.Выбрать();
		Пока ВыборкаПоТоварам.Следующий() Цикл
			Если ЗначениеЗаполнено(СокрЛП(ВыборкаПоТоварам.ТоварНаименование)) Тогда
				Параметры.Груз.Содержимое = Параметры.Груз.Содержимое + ?(Параметры.Груз.Содержимое <> "", ", ", "") + ВыборкаПоТоварам.ТоварНаименование;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	СоответствиеСтавокНДС = СоответствиеСтавокНДС();
	
	Если Параметры.Груз.ВидРасшифровки = "ПоВидам" И СервисДоставкиКлиентСервер.ЭтоСДЭК(Параметры.ТипГрузоперевозки)Тогда
		
		РезультатПоВидамНоменклатуры = Результаты[8];
		
		Если Не РезультатПоВидамНоменклатуры.Пустой() Тогда
			
			ВыборкаПоВидам = РезультатПоВидамНоменклатуры.Выбрать();
			Пока ВыборкаПоВидам.Следующий() Цикл
				НоваяСтрокаПозиции = Параметры.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаПозиции, ВыборкаПоВидам);
				НоваяСтрокаПозиции.Артикул = СокрЛП(НоваяСтрокаПозиции.Артикул);
				НоваяСтрокаПозиции.ИдентификаторДокументаОснования = XMLСтрока(ВыборкаПоВидам.ДокументОснование);
				НоваяСтрокаПозиции.СтавкаНДС = СоответствиеСтавокНДС.Получить(ВыборкаПоВидам.СтавкаНДС);
			КонецЦикла;
			
		КонецЕсли;
		
	Иначе
		
		РезультатПоТоварнымПозициям = Запрос.МенеджерВременныхТаблиц.Таблицы["Товары"].ПолучитьДанные();
		
		Если Не РезультатПоТоварнымПозициям.Пустой() Тогда
			
			ВыборкаПоПозиции = РезультатПоТоварнымПозициям.Выбрать();
			
			Пока ВыборкаПоПозиции.Следующий() Цикл
				
				НоваяСтрокаПозиции = Параметры.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаПозиции, ВыборкаПоПозиции);
				
				НоваяСтрокаПозиции.ИдентификаторДокументаОснования = XMLСтрока(ВыборкаПоПозиции.ДокументОснование);
				
				НоваяСтрокаПозиции.СтавкаНДС = СоответствиеСтавокНДС.Получить(ВыборкаПоПозиции.СтавкаНДС);
				
				Если Параметры.ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьер() Тогда
					
					Если ТипЗнч(ДанныеПоДокументу.ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
						НоваяСтрокаПозиции.ИННВладельцаГруза = ВыборкаПоПозиции.ИННВладельцаГруза;
					Иначе
						НоваяСтрокаПозиции.ИННВладельцаГруза = ДанныеПоДокументу.ОтправительКонтрагентИНН;
					КонецЕсли; 
					
					Если ВыборкаПоПозиции.ЭтоУслуга Тогда
						НоваяСтрокаПозиции.ТипНоменклатуры = 1; // услуга
					Иначе
						НоваяСтрокаПозиции.ТипНоменклатуры = 0; // товар
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет параметры контрагента данными по ссылке, указанной в Параметры.
//
// Параметры:
//  Параметры - см. СервисДоставки.НовыйПараметрыКонтрагента
//
Процедура ЗаполнитьПараметрыКонтрагента(Параметры) Экспорт
	
	ИмяСправочника = Неопределено;
	Если ТипЗнч(Параметры.Ссылка) = Тип("СправочникСсылка.Организации") Тогда
		ИмяСправочника = "Организации";
		РеквизитНаименования = "ОсновнаяТаблица.НаименованиеСокращенное";
		Параметры.ЭтоОрганизация = Истина;
	ИначеЕсли ТипЗнч(Параметры.Ссылка) = Тип("СправочникСсылка.Контрагенты") Тогда
		ИмяСправочника = "Контрагенты";
		РеквизитНаименования = "ОсновнаяТаблица.НаименованиеПолное";
		Параметры.ЭтоОрганизация = Ложь;
	Иначе
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ОсновнаяТаблица.Ссылка КАК Ссылка,
	               |	ОсновнаяТаблица.ИНН КАК ИНН,
	               |	ОсновнаяТаблица.КПП КАК КПП,
	               |	&РеквизитНаименования КАК Наименование,
	               |	ВЫБОР
	               |		КОГДА ОсновнаяТаблица.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	               |			ТОГДА 1
	               |		КОГДА ОсновнаяТаблица.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	               |			ТОГДА 2
	               |		КОГДА ОсновнаяТаблица.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель)
	               |			ТОГДА 3
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК ЮрФизЛицо
	               |ИЗ
	               |	&ТаблицаДляЗапроса КАК ОсновнаяТаблица
	               |ГДЕ
	               |	ОсновнаяТаблица.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Параметры.Ссылка);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаДляЗапроса", "Справочник." + ИмяСправочника);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РеквизитНаименования", РеквизитНаименования);
	
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ЗаполнитьЗначенияСвойств(Параметры, РезультатЗапроса.Выгрузить()[0]);
		
		Если Параметры.ЮрФизЛицо <> 2 Тогда
			
			Параметры.ЮридическийАдрес.Владелец = Параметры.Ссылка;
			СервисДоставки.ЗаполнитьАдресПоПараметрам(Параметры.ЮридическийАдрес);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет параметры контактного лица данными по ссылке, указанной в Параметры.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма настроек, в которой необходимо заполнить контактное лицо.
//  Параметры - см. СервисДоставки.НовыйПараметрыКонтактногоЛица
//
Процедура ЗаполнитьПараметрыКонтактногоЛицаПоУмолчанию(Форма, Параметры) Экспорт
	
	Если Не Параметры.Контрагент.ЭтоОрганизация Тогда
		Возврат;
	КонецЕсли;
	
	//Заполним контактных лиц
	Если Параметры.КонтактноеЛицо.СпособОпределенияКонтактногоЛица = 3 
		ИЛИ Параметры.КонтактноеЛицо.СпособОпределенияКонтактногоЛица = 0 Тогда // Менеджер из документа основания
		
		Если Форма.ДокументыОснования.Количество() = 1 Тогда
			
			Менеджер = Неопределено;
			Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Менеджер", Форма.ДокументыОснования[0].Значение.Метаданные()) Тогда
				Менеджер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.ДокументыОснования[0].Значение, "Менеджер");
			ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("Ответсвенный", Форма.ДокументыОснования[0].Значение.Метаданные()) Тогда
				Менеджер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.ДокументыОснования[0].Значение, "Ответственный");
			КонецЕсли;
			
			Если Менеджер <> Неопределено Тогда
				КонтактноеЛицоОрганиазции = Менеджер.ФизическоеЛицо;
				Если ЗначениеЗаполнено(КонтактноеЛицоОрганиазции) Тогда
					ПараметрыКонтрагента = Параметры.Контрагент;
					Если ПараметрыКонтрагента.ЭтоОрганизация Тогда
						Параметры.КонтактноеЛицо.Ссылка = КонтактноеЛицоОрганиазции;
						СервисДоставки.ЗаполнитьПараметрыКонтактногоЛица(Параметры.КонтактноеЛицо);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли Параметры.КонтактноеЛицо.СпособОпределенияКонтактногоЛица = 4 Тогда // Ответственный за склад
		
		ИмяРекзизита = Форма.ТекущийЭлемент.Имя;
		КонтактноеЛицоОрганиазции = Неопределено;
		
		Если ЗначениеЗаполнено(Параметры.Адрес.Владелец) Тогда
			Если ТипЗнч(Параметры.Адрес.Владелец) = Тип("СправочникСсылка.Склады") Тогда
				КонтактноеЛицоОрганиазции = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Адрес.Владелец, "ТекущийОтветственный");
			КонецЕсли;
		ИначеЕсли Форма.ДокументыОснования.Количество() = 1 Тогда
			
			ДокументОснование = Форма.ДокументыОснования[0].Значение;
			
			Если ИмяРекзизита = "ОтправительКонтрагент" Тогда
				
				Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Склад", ДокументОснование.Метаданные()) Тогда
					КонтактноеЛицоОрганиазции = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование.Склад, "ТекущийОтветственный");
				ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("СкладОтправитель", ДокументОснование.Метаданные()) Тогда
					КонтактноеЛицоОрганиазции = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование.СкладОтправитель, "ТекущийОтветственный");
				КонецЕсли;
			
			ИначеЕсли ИмяРекзизита = "ПолучательКонтрагент" Тогда
				
				Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Склад", ДокументОснование.Метаданные()) Тогда
					КонтактноеЛицоОрганиазции = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование.Склад, "ТекущийОтветственный");
				ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("СкладПолучатель", ДокументОснование.Метаданные()) Тогда
					КонтактноеЛицоОрганиазции = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование.СкладПолучатель, "ТекущийОтветственный");
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КонтактноеЛицоОрганиазции) Тогда
			ПараметрыКонтрагента = Параметры.Контрагент;
			Если ПараметрыКонтрагента.ЭтоОрганизация Тогда
				Параметры.КонтактноеЛицо.Ссылка = КонтактноеЛицоОрганиазции;
				СервисДоставки.ЗаполнитьПараметрыКонтактногоЛица(Параметры.КонтактноеЛицо);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Параметры.КонтактноеЛицо.СпособОпределенияКонтактногоЛица = 5 Тогда // Текущий пользователь

		КонтактноеЛицоОрганиазции  = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыСеанса.ТекущийПользователь, "ФизическоеЛицо");
	
		Если ЗначениеЗаполнено(КонтактноеЛицоОрганиазции) Тогда

			ПараметрыКонтрагента = Параметры.Контрагент;
			Если ПараметрыКонтрагента.ЭтоОрганизация Тогда
				Параметры.КонтактноеЛицо.Ссылка = КонтактноеЛицоОрганиазции;
				СервисДоставки.ЗаполнитьПараметрыКонтактногоЛица(Параметры.КонтактноеЛицо);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Заменяет текст наименования вида телефона для заголовка перед номером телефона.
//
// Параметры:
//  ЗначениеПредставления - Строка - представление названия телефона.
//  ВидКонтактнойИнформации - ПеречислениеСсылка.ТипыКонтактнойИнформации - тип контактной информации.
//
Процедура СкорректироватьНаименованиеВидаТелефона(ЗначениеПредставления, Знач ВидКонтактнойИнформации) Экспорт
	
КонецПроцедуры

// Заполняет параметры валюты.
//
// Параметры:
//  Параметры - см. СервисДоставки.НовыйПараметрыВалюты
//
Процедура ЗаполнитьПараметрыВалюты(Параметры) Экспорт

	Если (Не Параметры.Свойство("Код"))
		ИЛИ (Не ЗначениеЗаполнено(Параметры.Код)) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("Ссылка", Справочники.Валюты.НайтиПоКоду(Параметры.Код));
	
	Если ЗначениеЗаполнено(Параметры.Ссылка) Тогда
		Параметры.Вставить("Наименование", 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ссылка, "Наименование"));
	Иначе
		Параметры.Вставить("Наименование", "RUB");
	КонецЕсли;
	
КонецПроцедуры

// Заполняет таблицу данными документов.
//
// Параметры:
//  ОснованияСписок - см. СервисДоставки.ДокументыОснованияСписок
//
Процедура ЗаполнитьДокументыОснованияСписок(ОснованияСписок) Экспорт

	Если ОснованияСписок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонЗапроса =
	"ВЫБРАТЬ
	|Т.Ссылка КАК Ссылка,
	|0 КАК СуммаДокумента,
	|NULL КАК СкладОтправитель,
	|NULL КАК СкладПолучатель,
	|NULL КАК ПартнерОтправитель,
	|NULL КАК ПартнерПолучатель,
	|NULL КАК Валюта,
	|NULL КАК Отправитель,
	|NULL КАК Получатель,
	|"""" КАК ОтправительАдрес,
	|"""" КАК ПолучательАдрес,
	|"""" КАК ПолучательАдресЗначение,
	|"""" КАК ПолучательАдресЗначенияПолей,
	|ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОтгрузки,
	|ДАТАВРЕМЯ(1, 1, 1) КАК ДатаДоставки,
	|0 КАК СпособОтгрузки,
	|0 КАК СпособДоставки
	|ПОМЕСТИТЬ ДанныеОснований
	|ИЗ #ТаблицаДокумента КАК Т
	|
	|ГДЕ Т.Ссылка В (&Основания)";

	Основания = ОснованияСписок.ВыгрузитьКолонку("Ссылка");
	ОснованияПоТипам = РазложитьМассивСсылокПоТипам(Основания);
	ПервыйЗапрос = Истина;
	ТекстЗапроса = "";
	
	Для Каждого КлючИЗначение Из ОснованияПоТипам Цикл
		
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(КлючИЗначение.Ключ);
		ИмяТаблицыОбъекта = МетаданныеОбъекта.ПолноеИмя();
		ИмяОбъекта = СтрРазделить(ИмяТаблицыОбъекта, ".")[1];
		
		ТекстЗапросаПоТипу = ШаблонЗапроса;
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаДокумента", МетаданныеОбъекта) Тогда
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "0 КАК СуммаДокумента", "Т.СуммаДокумента КАК СуммаДокумента");
		КонецЕсли;
		
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Валюта", МетаданныеОбъекта) Тогда
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК Валюта", "Т.Валюта КАК Валюта");
		КонецЕсли;
		
		СНашегоСклада = ЭтоДокументОтгрузкиСНашегоСклада(ИмяОбъекта);
		НаНашСклад    = ЭтоДокументДоставкиНаНашСклад(ИмяОбъекта);
		
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Организация", МетаданныеОбъекта) Тогда
			Если СНашегоСклада Тогда
				ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК Отправитель", "Т.Организация КАК Отправитель");
				
				Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Склад", МетаданныеОбъекта) Тогда
					ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК СкладОтправитель", "Т.Склад КАК СкладОтправитель");
				ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("СкладОтправитель", МетаданныеОбъекта) Тогда
					ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК СкладОтправитель", "Т.СкладОтправитель КАК СкладОтправитель");
				КонецЕсли;
				
			ИначеЕсли НаНашСклад Тогда
				ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК Получатель", "Т.Организация КАК Получатель");
				ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК СкладПолучатель", "Т.Склад КАК СкладПолучатель");
			КонецЕсли;
		КонецЕсли;
		
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Контрагент", МетаданныеОбъекта) Тогда
			Если СНашегоСклада Тогда
				ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК Получатель", "Т.Контрагент КАК Получатель");
				ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК ПартнерПолучатель", "Т.Партнер КАК ПартнерПолучатель");
			ИначеЕсли НаНашСклад Тогда
				ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК Отправитель", "Т.Контрагент КАК Отправитель");
				ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК ПартнерОтправитель", "Т.Партнер КАК ПартнерОтправитель");
			КонецЕсли;
		КонецЕсли;
		
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ОрганизацияПолучатель", МетаданныеОбъекта) Тогда
			
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК Получатель",
				"ВЫБОР
				|	КОГДА Т.ОрганизацияПолучатель <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
				|		ТОГДА Т.ОрганизацияПолучатель
				|	ИНАЧЕ Т.Организация
				|КОНЕЦ КАК Получатель");
			
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК СкладПолучатель", "Т.СкладПолучатель КАК СкладПолучатель");
			
		КонецЕсли;
		
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаОтгрузки", МетаданныеОбъекта) Тогда
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОтгрузки", "Т.ДатаОтгрузки КАК ДатаОтгрузки");
		КонецЕсли;
		
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаПоступления", МетаданныеОбъекта) Тогда
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ДАТАВРЕМЯ(1, 1, 1) КАК ДатаДоставки", "Т.ДатаПоступления КАК ДатаДоставки");
		КонецЕсли;
		
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СпособДоставки", МетаданныеОбъекта) Тогда
			
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "0 КАК СпособОтгрузки",
				"	ВЫБОР
				|		КОГДА Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчика)
				|			ТОГДА 2
				|		КОГДА Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаПоАдресу)
				|			ТОГДА 1
				|		ИНАЧЕ 0
				|	КОНЕЦ КАК СпособОтгрузки");
			
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "0 КАК СпособДоставки",
				"	ВЫБОР
				|		КОГДА Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаДоНашегоСклада)
				|			ТОГДА 2
				|		КОГДА Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаДоПунктаПередачи)
				|			ТОГДА 1
				|		ИНАЧЕ 0
				|	КОНЕЦ КАК СпособДоставки");
			
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, """"" КАК ОтправительАдрес",
				"	ВЫБОР
				|		КОГДА Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.НашимиСиламиСАдресаОтправителя)
				|			ИЛИ Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ОтОтправителяОпределяетСлужбаДоставки)
				|			ТОГДА Т.АдресДоставки
				|		ИНАЧЕ """"
				|	КОНЕЦ КАК ОтправительАдрес");
			
			Если СНашегоСклада Тогда
				
				ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, """"" КАК ПолучательАдрес,",
					"Т.АдресДоставки КАК ПолучательАдрес,");
				
				ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, """"" КАК ПолучательАдресЗначенияПолей",
					"Т.АдресДоставкиЗначенияПолей КАК ПолучательАдресЗначенияПолей");
				
				ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, """"" КАК ПолучательАдресЗначение",
					"Т.АдресДоставкиЗначение КАК ПолучательАдресЗначение");
				
			КонецЕсли;
			
		КонецЕсли;
		
		ДополнитьЗапросПоОснованиям(ТекстЗапроса, ТекстЗапросаПоТипу, ПервыйЗапрос, ИмяТаблицыОбъекта);
		
	КонецЦикла;
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов()
	+ "ВЫБРАТЬ
	  |	Т.Ссылка КАК Ссылка,
	  |	Т.СуммаДокумента КАК СуммаДокумента,
	  |	Т.Валюта КАК Валюта,
	  |	Т.Отправитель КАК Отправитель,
	  |	Т.Получатель КАК Получатель,
	  |	ВЫБОР
	  |		КОГДА НЕ Т.СкладОтправитель ЕСТЬ NULL
	  |			ТОГДА СкладыКонтактнаяИнформацияОтправитель.Представление
	  |		КОГДА Т.ОтправительАдрес = """"
	  |			ТОГДА ПартнерыКонтактнаяИнформацияОтправитель.Представление
	  |		ИНАЧЕ Т.ОтправительАдрес
	  |	КОНЕЦ КАК ОтправительАдрес,
	  |	ВЫБОР
	  |		КОГДА НЕ Т.СкладПолучатель ЕСТЬ NULL
	  |			ТОГДА СкладыКонтактнаяИнформацияПолучатель.Представление
	  |		КОГДА Т.ПолучательАдрес = """"
	  |			ТОГДА ПартнерыКонтактнаяИнформацияПолучатель.Представление
	  |		ИНАЧЕ Т.ПолучательАдрес
	  |	КОНЕЦ КАК ПолучательАдрес,
	  |	Т.ПолучательАдресЗначение КАК ПолучательАдресЗначение,
	  |	Т.ПолучательАдресЗначенияПолей КАК ПолучательАдресЗначенияПолей,
	  |	Т.ДатаОтгрузки КАК ДатаОтгрузки,
	  |	Т.ДатаДоставки КАК ДатаДоставки,
	  |	Т.СпособОтгрузки КАК СпособОтгрузки,
	  |	Т.СпособДоставки КАК СпособДоставки
	  |ИЗ
	  |	ДанныеОснований КАК Т
	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады.КонтактнаяИнформация КАК СкладыКонтактнаяИнформацияОтправитель
	  |		ПО Т.СкладОтправитель = СкладыКонтактнаяИнформацияОтправитель.Ссылка
	  |			И (СкладыКонтактнаяИнформацияОтправитель.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресСклада))
	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады.КонтактнаяИнформация КАК СкладыКонтактнаяИнформацияПолучатель
	  |		ПО Т.СкладПолучатель = СкладыКонтактнаяИнформацияПолучатель.Ссылка
	  |			И (СкладыКонтактнаяИнформацияПолучатель.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.АдресСклада))
	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформацияОтправитель
	  |		ПО Т.ПартнерОтправитель = ПартнерыКонтактнаяИнформацияОтправитель.Ссылка
	  |			И (ПартнерыКонтактнаяИнформацияОтправитель.Вид = &АдресДоставки)
	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформацияПолучатель
	  |		ПО Т.ПартнерПолучатель = ПартнерыКонтактнаяИнформацияПолучатель.Ссылка
	  |			И (ПартнерыКонтактнаяИнформацияПолучатель.Вид = &АдресДоставки)
	  |
	  |УПОРЯДОЧИТЬ ПО
	  |	Ссылка";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Основания", Основания);
	ВидКонтактнойИнформации = Неопределено;
	ПолучитьЗначениеВидаКонтактнойИнформацииДляАдресаДоставкиСкладаКонтрагента(ВидКонтактнойИнформации);
	Запрос.УстановитьПараметр("АдресДоставки", ВидКонтактнойИнформации);
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	ОснованияСписок = РезультатЗапроса[1].Выгрузить();
	
	Для Каждого Основание Из ОснованияСписок Цикл
		ПараметрыАдреса = СервисДоставки.НовыйПараметрыАдреса("АдресДоставки");
		ПараметрыАдреса.Представление = Основание.ПолучательАдрес;
		ПараметрыАдреса.Значение = Основание.ПолучательАдресЗначение;
		ПараметрыАдреса.ЗначенияПолей = Основание.ПолучательАдресЗначенияПолей;
		СервисДоставкиСлужебный.ЗаполнитьАдресПоПараметрам(ПараметрыАдреса);
		Основание.ПолучательАдрес = ПараметрыАдреса.Представление;
	КонецЦикла;
	
КонецПроцедуры

// Заполняет соответствие ставок НДС.
//
// Параметры:
//  СтавкиНДС - см. СервисДоставкиПовтИсп.СоответствиеСтавокНДСИзСервиса
//
Процедура ЗаполнитьСоответствиеСтавокНДСИзСервиса(СтавкиНДС) Экспорт

	СтавкиНДС.Вставить("0", Строка(Перечисления.СтавкиНДС.НДС0));
	СтавкиНДС.Вставить("5/105", Строка(Перечисления.СтавкиНДС.НДС5_105));
	СтавкиНДС.Вставить("5", Строка(Перечисления.СтавкиНДС.НДС5));
	СтавкиНДС.Вставить("7/107", Строка(Перечисления.СтавкиНДС.НДС7_107));
	СтавкиНДС.Вставить("7", Строка(Перечисления.СтавкиНДС.НДС7));
	СтавкиНДС.Вставить("10/110", Строка(Перечисления.СтавкиНДС.НДС10_110));
	СтавкиНДС.Вставить("10", Строка(Перечисления.СтавкиНДС.НДС10));
	СтавкиНДС.Вставить("18/118", Строка(Перечисления.СтавкиНДС.НДС18_118));
	СтавкиНДС.Вставить("18", Строка(Перечисления.СтавкиНДС.НДС18));
	СтавкиНДС.Вставить("20/120", Строка(Перечисления.СтавкиНДС.НДС20_120));
	СтавкиНДС.Вставить("20", Строка(Перечисления.СтавкиНДС.НДС20));
	СтавкиНДС.Вставить("22/122", Строка(Перечисления.СтавкиНДС.НДС22_122));
	СтавкиНДС.Вставить("22", Строка(Перечисления.СтавкиНДС.НДС22));
	СтавкиНДС.Вставить("БезНДС", Строка(Перечисления.СтавкиНДС.БезНДС));
	
КонецПроцедуры 

// Заполняет контактное лицо получателя в структуре параметров заказа.
// 
// Параметры:
//  ПараметрыЗаказа - См. СервисДоставки.НовыйПараметрыЗапросаСоздатьИзменитьЗаказНаДоставку - Структура Заказа на доставку
//  ЗаполнитьКонтактноеЛицо - Булево - Заполнить контактное лицо
//  ЗаполнитьТелефон - Булево - Заполнить телефон
Процедура ЗаполнитьРеквизитыКонтактногоЛицаПолучателяПоУмолчанию(ПараметрыЗаказа, ЗаполнитьКонтактноеЛицо = Истина,
	ЗаполнитьТелефон = Истина) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыЗаказа.Получатель.Контрагент.Ссылка) И (ЗаполнитьКонтактноеЛицо
		Или ЗаполнитьТелефон) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Владелец", ПараметрыЗаказа.Получатель.Контрагент.Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТаблицаКонтактныхЛиц.Ссылка КАК Ссылка,
		|	ТаблицаКонтактныхЛиц.Ссылка.Наименование КАК Наименование,
		|	ТаблицаКонтактныхЛиц.Значение КАК Значение,
		|	ТаблицаКонтактныхЛиц.Представление КАК Представление
		|ИЗ
		|	Справочник.КонтактныеЛицаПартнеров.КонтактнаяИнформация КАК ТаблицаКонтактныхЛиц
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК ТаблицаКонтрагентов
		|		ПО ТаблицаКонтактныхЛиц.Ссылка.Владелец = ТаблицаКонтрагентов.Партнер
		|			И (ТаблицаКонтрагентов.Ссылка = &Владелец)
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(&Владелец) = ТИП(Справочник.Контрагенты)
		|	И НЕ ТаблицаКонтактныхЛиц.Ссылка.ПометкаУдаления
		|	И ТаблицаКонтактныхЛиц.Представление <> """"
		|	И ТаблицаКонтактныхЛиц.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТаблицаКонтактныхЛиц.Ссылка,
		|	ТаблицаКонтактныхЛиц.Ссылка.Наименование,
		|	ТаблицаКонтактныхЛиц.Значение,
		|	ТаблицаКонтактныхЛиц.Представление
		|ИЗ
		|	Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ТаблицаКонтактныхЛиц
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(&Владелец) = ТИП(Справочник.Организации)
		|	И НЕ ТаблицаКонтактныхЛиц.Ссылка.ПометкаУдаления
		|	И ТаблицаКонтактныхЛиц.Представление <> """"
		|	И ТаблицаКонтактныхЛиц.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			Если ЗаполнитьКонтактноеЛицо Тогда
				ЗаполнитьЗначенияСвойств(ПараметрыЗаказа.Получатель.КонтактноеЛицо, Выборка, "Ссылка, Наименование");
			КонецЕсли;
			
			Если ЗаполнитьТелефон Тогда
				ЗаполнитьЗначенияСвойств(ПараметрыЗаказа.Получатель.КонтактноеЛицо.Телефон, Выборка, "Значение, Представление");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВидыКонтактнойИнформации

// Получает вид контактной информации для адреса по переданному владельцу.
//
// Параметры:
//  Владелец  - СправочникСсылка, ДокументСсылка - владелец адреса.
//  ВидКонтактнойИнформации - СправочникСсылка.ВидыКонтактнойИнформации - вид контактной информации.
//  ТипАдреса - Строка - тип адреса, доступны: АдресДоставки, Юридический, Фактический.
//
Процедура ПолучитьЗначениеВидаКонтактнойИнформацииДляАдресаПоВладельцу(Владелец, ВидКонтактнойИнформации, ТипАдреса = "АдресДоставки") Экспорт
	
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Склады") Тогда
		ПолучитьЗначениеВидаКонтактнойИнформацииДляАдресаДоставкиСкладаОрганизации(ВидКонтактнойИнформации);
	ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.Организации") Тогда
		ПолучитьЗначениеВидаКонтактнойИнформацииДляЮридическогоАдресаОрганизации(ВидКонтактнойИнформации);
	ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.Партнеры") 
		И ТипАдреса = "АдресДоставки" Тогда
		ПолучитьЗначениеВидаКонтактнойИнформацииДляАдресаДоставкиСкладаКонтрагента(ВидКонтактнойИнформации);
	ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.Контрагенты") 
		И ТипАдреса = "Юридический" Тогда
		ПолучитьЗначениеВидаКонтактнойИнформацииДляЮридическогоАдресаКонтрагента(ВидКонтактнойИнформации);
	ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.Контрагенты") 
		И ТипАдреса = "АдресДоставки" Тогда
		ПолучитьЗначениеВидаКонтактнойИнформацииДляФактическогоАдресаКонтрагента(ВидКонтактнойИнформации);
	КонецЕсли;
	
КонецПроцедуры

// Получает вид контактной информации для адреса доставки организации.
//
// Параметры:
//  ВидКонтактнойИнформации - СправочникСсылка.ВидыКонтактнойИнформации,Неопределено - вид контактной информации.
//
Процедура ПолучитьЗначениеВидаКонтактнойИнформацииДляАдресаДоставкиСкладаОрганизации(ВидКонтактнойИнформации) Экспорт
	
	ВидКонтактнойИнформации = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.АдресСклада");
	
КонецПроцедуры

// Получает вид контактной информации для юридического адреса организации.
//
// Параметры:
//  ВидКонтактнойИнформации - СправочникСсылка.ВидыКонтактнойИнформации,Неопределено - вид контактной информации.
//
Процедура ПолучитьЗначениеВидаКонтактнойИнформацииДляЮридическогоАдресаОрганизации(ВидКонтактнойИнформации) Экспорт
	
	ВидКонтактнойИнформации = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации");
	
КонецПроцедуры

// Получает вид контактной информации для адреса доставки контрагента.
//
// Параметры:
//  ВидКонтактнойИнформации - СправочникСсылка.ВидыКонтактнойИнформации,Неопределено - вид контактной информации.
//
Процедура ПолучитьЗначениеВидаКонтактнойИнформацииДляАдресаДоставкиСкладаКонтрагента(ВидКонтактнойИнформации) Экспорт
	
	ВидКонтактнойИнформации = ВидКонтактнойИнформацииАдресДоставкиПартнера();
	
	Если Не ЗначениеЗаполнено(ВидКонтактнойИнформации) Тогда
		ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.АдресПартнера;
	КонецЕсли;
	
КонецПроцедуры

// Получает вид контактной информации для юридического адреса доставки.
//
// Параметры:
//  ВидКонтактнойИнформации - СправочникСсылка.ВидыКонтактнойИнформации,Неопределено - вид контактной информации.
//
Процедура ПолучитьЗначениеВидаКонтактнойИнформацииДляЮридическогоАдресаКонтрагента(ВидКонтактнойИнформации) Экспорт
	
	ВидКонтактнойИнформации = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ЮрАдресКонтрагента");
	
КонецПроцедуры

// Получает вид контактной информации для телефона контактного лица контрагента.
//
// Параметры:
//  ВидКонтактнойИнформации - СправочникСсылка.ВидыКонтактнойИнформации,Неопределено - вид контактной информации.
//
Процедура ПолучитьЗначениеВидаКонтактнойИнформацииДляТелефонаКонтактногоЛицаКонтрагента(ВидКонтактнойИнформации) Экспорт
	
	ВидКонтактнойИнформации = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица");
	
КонецПроцедуры

// Получает вид контактной информации для телефона контактного лица организации.
//
// Параметры:
//  ВидКонтактнойИнформации - СправочникСсылка.ВидыКонтактнойИнформации,Неопределено - вид контактной информации.
//
Процедура ПолучитьЗначениеВидаКонтактнойИнформацииДляТелефонаКонтактногоЛицаОрганизации(ВидКонтактнойИнформации) Экспорт
	
	ВидКонтактнойИнформации = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица");
	
КонецПроцедуры

// Получает вид контактной информации для юридического адреса доставки.
//
// Параметры:
//  ВидКонтактнойИнформации - СправочникСсылка.ВидыКонтактнойИнформации,Неопределено - вид контактной информации.
//
Процедура ПолучитьЗначениеВидаКонтактнойИнформацииДляФактическогоАдресаКонтрагента(ВидКонтактнойИнформации) Экспорт
	
	ВидКонтактнойИнформации = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ФактАдресКонтрагента");
	
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаНаложенныхПлатежей

// Ограничение длительности обработки одной пачки платежей (в секундах)
// Параметры:
//	Лимит - Число - количество секунд(возвращаемое значение)
Процедура ЛимитВремениОбработкиПачкиПлатежей(Лимит) Экспорт
	
	Лимит = 300;
	
КонецПроцедуры

// Разрешение на использование наложенных платежей.
// Параметры:
//  ИспользоватьНаложенныеПлатежи - Булево - Параметр, который нужно заполнить.
// 
Процедура ИспользоватьНаложенныеПлатежи(ИспользоватьНаложенныеПлатежи) Экспорт
	
	ИспользоватьНаложенныеПлатежи = Истина;

КонецПроцедуры

// Разрешение на загрузку наложенных платежей регламентным заданием.
//
// Параметры:
//  Признак - Булево - Исходящий параметр. Истина - разрешена загрузка с помощью регламентного задания
// 
Процедура ИспользоватьРегламентЗагрузкиНаложенныхПлатежей(Признак) Экспорт
	
	Признак = Истина;
	
КонецПроцедуры

// Обработать данные наложенного платежа: создать платеж, подтвердить обработку.
// 
// Параметры:
//  АтрибутыПлатежа - см. СервисДоставки.НовыйДанныеНаложенногоПлатежа
//  Обработано - Булево - признак корректной обработки (возвращаемое значение)
//  ТекстОшибки - Строка - текст ошибки (возвращаемое значение), будет помещен в журнал регистрации
// 
Процедура ОбработатьДанныеНаложенногоПлатежа(Знач АтрибутыПлатежа, Обработано, ТекстОшибки = "") Экспорт
	
	// ++ СервисДоставки.ERP
	ДокументОснование = АтрибутыПлатежа.ДокументОснование; 
	СуммаПлатежа = АтрибутыПлатежа.СуммаПлатежа; 
	ДатаПлатежа = АтрибутыПлатежа.ДатаРегистрацииПлатежа;
	ИдентификаторДокументаДоставки = АтрибутыПлатежа.ИдентификаторДокумента;
	
	Если НЕ ЗначениеЗаполнено(ДокументОснование) Тогда
		ТекстОшибки = НСтр( 
			"ru = 'Не указан документ-основание по наложенному платежу.
			|Идентификатор документа доставки: %1';
			|en = 'Base document for the cash on delivery is not specified.
			|Delivery document ID: %1'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстОшибки, ИдентификаторДокументаДоставки);
		Возврат;
	КонецЕсли; 
	
	УстановитьПривилегированныйРежим(Истина);
	
	КодАвторизацииДоставки = Лев(КодАвторизацииПоЛитералу(НРег(ИдентификаторДокументаДоставки)), 6);
	ДокументыОплатыОбработанРанее = Ложь;
	ДокументыОплатыПоОснованию = ПолучитьДокументыОплатыПоОснованию(ДокументОснование);
	
	Для Каждого Элем Из ДокументыОплатыПоОснованию Цикл
		Если КодАвторизацииДоставки = Элем.КодАвторизации тогда
			ДокументыОплатыОбработанРанее = Истина;
			Если Элем.Сумма <> СуммаПлатежа Тогда
				ИмяСобытия = "СервисДоставки.Наложенныйплатеж.Создание";
				ТекстСобытия = 
					НСтр("ru = 'По документу ""%1"" 
					|сумма зарегистрированного платежа ""%2 (%3) 
					|отличается от загружаемой суммы (%4)';
					|en = 'In the ""%1"" document, 
					|the amount of registered payment ""%2 (%3)"" 
					|is different from the amount to import (%4)'");
				ТекстСобытия = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстСобытия, ДокументОснование, Элем.ДокументОплаты, Элем.Сумма, СуммаПлатежа);
				ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Предупреждение,,, ТекстСобытия);
			КонецЕсли;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ДокументыОплатыОбработанРанее Тогда
		Обработано = Истина;
		Возврат;
	КонецЕсли;
	
	ДокументОплаты = Документы.ОперацияПоПлатежнойКарте.СоздатьДокумент();
	Попытка
		ДокументОплаты.Заполнить(ДокументОснование);
		ДокументОплаты.Дата = ?(ЗначениеЗаполнено(ДатаПлатежа), ДатаПлатежа, ТекущаяДатаСеанса());
		
		ДокументОплаты.СуммаДокумента = СуммаПлатежа;
		
		// перезаполнение документа оплаты по атрибутам платежа
		ДокументОплаты.Партнер = ДокументОснование.Партнер;
		ДокументОплаты.Контрагент = ДокументОснование.Контрагент;
		ДокументОплаты.ДоговорЭквайринга = ПолучитьДоговорЭквайринга(ДокументОплаты.Организация, АтрибутыПлатежа.Перевозчик);
		Если ДокументОплаты.РасшифровкаПлатежа.Количество() > 0 
			И Не ЗначениеЗаполнено(ДокументОплаты.РасшифровкаПлатежа[0].СтатьяДвиженияДенежныхСредств) Тогда 
			ДокументОплаты.РасшифровкаПлатежа[0].СтатьяДвиженияДенежныхСредств = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
				ДокументОплаты.ДоговорЭквайринга, "СтатьяДвиженияДенежныхСредствПоступлениеОплаты");
		КонецЕсли;
		ДокументОплаты.КодАвторизации = КодАвторизацииДоставки;
		
		Если Не ЗначениеЗаполнено(ДокументОплаты.Ответственный) Тогда
			ДокументОплаты.Ответственный = ДокументОплаты.Автор;
		КонецЕсли;
		
		ДокументОплаты.Записать(РежимЗаписиДокумента.Запись);
		Если ДокументОплаты.ПроверитьЗаполнение() Тогда
			ДокументОплаты.Записать(РежимЗаписиДокумента.Проведение);
			Обработано = Истина;
		Иначе
			ТекстОшибки = НСтр(
				"ru = 'Не удалось зарегистрировать наложенный платеж по ""%1"" 
				|из-за ошибки заполнения эквайринговой операции.';
				|en = 'Cannot register the cash on delivery of ""%1"" 
				|due to the acquiring transaction filling error.'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ДокументОснование);
			Возврат;
		КонецЕсли;
	Исключение
		ИмяСобытия = "СервисДоставки.Наложенныйплатеж.Запись";
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	// -- СервисДоставки.ERP

КонецПроцедуры

// Проверяет, есть ли у переданной организации договор эквайринга с оператором доставки.
// Параметры:
//	Организация - СправочникСсылка.Организации
//	Перевозчик - ОпределяемыйТип.КонтрагентСервисДоставки, Неопределено - незаполненное значение параметра 
//						означает наличие хотя бы одного перевозчика с договором эквайринга. 
//	Результат - Булево - возможна доставка наложенным платежом.
Процедура ВозможнаДоставкаНаложеннымПлатежом(Знач Организация, Знач Перевозчик, Результат) Экспорт
	
	// ++ СервисДоставки.ERP
	Результат = ЗначениеЗаполнено(ПолучитьДоговорЭквайринга(Организация, Перевозчик));
	// -- СервисДоставки.ERP
	
КонецПроцедуры

#КонецОбласти

#Область ЧастичнаяДоставка

// Разрешение на использование частичной доставки.
// Позволяет отобразить/скрыть интерфейс, предназначенный для работы с частичной доставкой.
//  
// Параметры:
//  Признак - Булево - Исходящий параметр. Истина - включена возможность работы с частичной доставкой
// 
Процедура ИспользоватьЧастичнуюДоставку(Признак) Экспорт
	
	Признак = Истина;

КонецПроцедуры

// Разрешение на загрузку данных частичной доставки регламентным заданием.
//
// Параметры:
//  Признак - Булево - Исходящий параметр. Истина - разрешена загрузка с помощью регламентного задания
// 
Процедура ИспользоватьРегламентСинхронизацииПоЧастичнойДоставке(Признак) Экспорт
	
	Признак = Истина;
	
КонецПроцедуры

// Скорректировать документы по частичной доставке. В данной процедуре в Результат необходимо добавить
// ИдентификаторыЗаказовНаДоставку - Массив Из УникальныйИдентификатор - Заказы на доставку подлежащие обработке
// 
// Параметры:
//  ПараметрыОбработки - см. СервисДоставки.СкорректироватьДокументыПоЧастичнойДоставке
//  Результат - Структура:
//  * Ошибки - Массив - Массив ошибок
//  КоличествоОбработанныхДанных - Число - Количество обработанных данных
Процедура СкорректироватьДокументыПоЧастичнойДоставке(Знач ПараметрыОбработки, Результат, КоличествоОбработанныхДанных) Экспорт
	
	Организация = ПараметрыОбработки.Организация;
	ТипГрузоперевозки = ПараметрыОбработки.ТипГрузоперевозки;
	ИдентификаторыЗаказовНаДоставку = ПараметрыОбработки.ИдентификаторыЗаказовНаДоставку;
	ДокументыКИсправлению = ПараметрыОбработки.ДокументыКИсправлению;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОрганизацияБизнесСети", Организация);
	Запрос.УстановитьПараметр("ТипГрузоперевозки", ТипГрузоперевозки);
	
	СостоянияДляОбработки = Новый Массив;
	СостоянияДляОбработки.Добавить(Перечисления.СостоянияОбработкиПроцедурСинхронизацииСервисДоставки.Обработать);
	СостоянияДляОбработки.Добавить(Перечисления.СостоянияОбработкиПроцедурСинхронизацииСервисДоставки.Скорректировать);
	Запрос.УстановитьПараметр("СостоянияДляОбработки", СостоянияДляОбработки);
	
	Запрос.УстановитьПараметр("ВсеЗаказыНаДоставку", ИдентификаторыЗаказовНаДоставку.Количество() = 0);
	Запрос.УстановитьПараметр("ИдентификаторыДокументов", ИдентификаторыЗаказовНаДоставку);
	
	Запрос.УстановитьПараметр("ВсеДокументыКИсправлению", ДокументыКИсправлению.Количество() = 0);
	Запрос.УстановитьПараметр("ДокументыКИсправлению", ДокументыКИсправлению);
	
	Запрос.Текст = ТекстЗапросаОбработкиДанныхПоЧастичнойДоставке() + "
		|ВЫБРАТЬ
		|	врДанные.ИдентификаторДокумента КАК ИдентификаторДокумента,
		|	врДанные.ДокументОснование КАК ДокументОснование,
		|	врДанные.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	врДанные.КодСтрокиЗаказаКлиента КАК КодСтрокиЗаказаКлиента,
		|
		|	врДанные.КоличествоДоставлено КАК КоличествоДоставлено,
		|	врДанные.КоличествоВозврат КАК КоличествоВозврат,
		|
		|	врДанные.ЕстьЗаказКлиента КАК ЕстьЗаказКлиента,
		|	врДанные.ЗаказКлиентаСкорректирован КАК ЗаказКлиентаСкорректирован,
		|	врДанные.ЗаказКлиента КАК ЗаказКлиента,
		|	врДанные.ЗаказКлиентаПредставление КАК ЗаказКлиентаПредставление,
		|
		|	врДанные.ЕстьРеализация КАК ЕстьРеализация,
		|	врДанные.РеализацияСкорректирована КАК РеализацияСкорректирована,
		|	врДанные.Реализация КАК Реализация,
		|	врДанные.РеализацияПредставление КАК РеализацияПредставление,
		|
		|	врДанные.ЕстьКорректировкаРеализации КАК ЕстьКорректировкаРеализации,
		|	врДанные.КорректировкаРеализацииСкорректирована КАК КорректировкаРеализацииСкорректирована,
		|	врДанные.КорректировкаРеализации КАК КорректировкаРеализации,
		|	врДанные.КорректировкаРеализацииПредставление КАК КорректировкаРеализацииПредставление,
		|
		|	врДанные.РеализацияСторнирована КАК РеализацияСторнирована
		|
		|ИЗ
		|	врДанные КАК врДанные
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИдентификаторДокумента,
		|	ЗаказКлиентаСкорректирован Убыв,
		|	РеализацияСкорректирована Убыв
		|
		|ИТОГИ
		|	МАКСИМУМ(ЕстьЗаказКлиента),
		|	МИНИМУМ(ЗаказКлиентаСкорректирован),
		|	МАКСИМУМ(ЕстьРеализация),
		|	МИНИМУМ(РеализацияСкорректирована),
		|	МАКСИМУМ(ЕстьКорректировкаРеализации),
		|	МИНИМУМ(КорректировкаРеализацииСкорректирована),
		|	МИНИМУМ(РеализацияСторнирована)
		|ПО
		|	врДанные.ИдентификаторДокумента,
		|	врДанные.ЗаказКлиента,
		|	врДанные.Реализация,
		|	врДанные.КорректировкаРеализации
		|";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;

	ВыборкаЗаказНаДоставку = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЗаказНаДоставку.Следующий() Цикл

		ВыборкаЗаказКлиента = ВыборкаЗаказНаДоставку.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		КоличествоОбработанныхДанных = КоличествоОбработанныхДанных + ВыборкаЗаказКлиента.Количество();
		Пока ВыборкаЗаказКлиента.Следующий() Цикл

			Если ВыборкаЗаказКлиента.ЕстьРеализация Тогда

				СкорректированныеПозиции = Новый Соответствие;
				РеализацияСкоррректированаУспешно = Истина;

				ВыборкаРеализация = ВыборкаЗаказКлиента.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаРеализация.Следующий() Цикл

					Если Не ВыборкаРеализация.ЕстьРеализация Тогда
						Продолжить;
					КонецЕсли;

					Если ВыборкаРеализация.РеализацияСкорректирована И Не ВыборкаРеализация.РеализацияСторнирована Тогда
						//Все "идентификаторы строки" заказа, попавшие в эту реализацию считаются скорректированными
						//Строки реализаций, соответствующие этим "идентификаторы строки" 
						//должны быть исключены (удалены) из всех остальных реализаций
						Если ВыборкаРеализация.ЕстьЗаказКлиента Тогда
							Выборка = ВыборкаРеализация.Выбрать();

							Пока Выборка.Следующий() Цикл
								Если ЗначениеЗаполнено(Выборка.ИдентификаторСтроки) Тогда
									СкорректированныеПозиции.Вставить(Выборка.ИдентификаторСтроки, Истина);
								КонецЕсли;
							КонецЦикла;

						КонецЕсли;

						Продолжить;
					КонецЕсли;

					Если ВыборкаРеализация.РеализацияСторнирована Тогда

						РеализацияСкоррректированаУспешно = Истина;

					ИначеЕсли ВыборкаРеализация.ЕстьКорректировкаРеализации Тогда
					
						//Реализацию корректируем документом "Корректировка реализации" 
						ВыборкаКР = ВыборкаРеализация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаКР.Следующий() Цикл
							ОбъектКорректировки = ВыборкаКР.КорректировкаРеализации.ПолучитьОбъект();

							Попытка
								ОбъектКорректировки.Заблокировать();
							Исключение
								ТекстСообщения = СтрШаблон(НСтр(
									"ru = '%1 находится в процессе редактирования и не может быть изменен.';
									|en = '%1 is being edited and cannot be changed.'"),
									ОбъектКорректировки);
								ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
								Продолжить;
							КонецПопытки;
						
							Если ОбъектКорректировки.Расхождения.Количество() > 0 Тогда
								ВариантОтражения = ОбъектКорректировки.Расхождения[0].ВариантОтражения;
								СтатьяРасходов = ОбъектКорректировки.Расхождения[0].СтатьяРасходов;
							Иначе
								ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУчестьПриИнвентаризации;
								СтатьяРасходов = Неопределено;
							КонецЕсли;
						
							//1. Загрузим табличную часть "Товары" (включая "идентификаторы строк") данными реализации.
							//2. Скорректируем количество, опираясь на "идентификаторы строк" или "коды строк".
							//3. Сформируем новые "идентификаторы строк".
							Документы.КорректировкаРеализации.ЗаполнитьТоварыПоИсходнымДанным(
								ВыборкаРеализация.Реализация, ОбъектКорректировки.Товары,
								ОбъектКорректировки.Расхождения);

							Выборка = ВыборкаКР.Выбрать();
							Пока Выборка.Следующий() Цикл
								Если Не ЗначениеЗаполнено(Выборка.ИдентификаторСтроки) Тогда
									Продолжить;
								КонецЕсли;

								Если Выборка.ЕстьЗаказКлиента Тогда
									СтруктурПоиска = Новый Структура("ЗаказКлиента, КодСтроки", Выборка.ЗаказКлиента,
										Выборка.КодСтрокиЗаказаКлиента);
								Иначе
									СтруктурПоиска = Новый Структура("ИдентификаторСтроки", Выборка.ИдентификаторСтроки);
								КонецЕсли;

								НайденныеСтроки = ОбъектКорректировки.Товары.НайтиСтроки(СтруктурПоиска);
								Если НайденныеСтроки.Количество() = 1 Тогда
									
									ИзменитьКоличествоУпаковокВСтрокеКорректировкиРеализации(ОбъектКорректировки,
										НайденныеСтроки[0], Выборка.КоличествоДоставлено);
										
								КонецЕсли;
							КонецЦикла;

							Для Каждого СтрокаТовары Из ОбъектКорректировки.Товары Цикл
								СтрокаТовары.ИдентификаторСтроки = "";
							КонецЦикла;

							ОбъектКорректировки.ЗаполнитьРасхождения();
							
							Для Каждого СтрокаРасхождения Из ОбъектКорректировки.Расхождения Цикл
								СтрокаРасхождения.ВариантОтражения = ВариантОтражения;
								СтрокаРасхождения.СтатьяРасходов = СтатьяРасходов;
							КонецЦикла;
							
							Попытка
								ОбъектКорректировки.Записать(?(ОбъектКорректировки.Проведен,
									РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
							Исключение
								Результат.Ошибки.Добавить(ОбработкаОшибок.ПодробноеПредставлениеОшибки(
									ИнформацияОбОшибке()));
								РеализацияСкоррректированаУспешно = Ложь;
							КонецПопытки;
						КонецЦикла;

					Иначе
						//Исправляем табличную часть "Товары" реализации
						
						ОбъектКорректировки = ВыборкаРеализация.Реализация.ПолучитьОбъект();

						Попытка
							ОбъектКорректировки.Заблокировать();
						Исключение
							ТекстСообщения = СтрШаблон(НСтр(
								"ru = '%1 находится в процессе редактирования и не может быть изменен.';
								|en = '%1 is being edited and cannot be changed.'"),
								ОбъектКорректировки);
							ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
							Продолжить;
						КонецПопытки;

						Выборка = ВыборкаРеализация.Выбрать();
						Пока Выборка.Следующий() Цикл
							Если Не ЗначениеЗаполнено(Выборка.ИдентификаторСтроки) Тогда
								Продолжить;
							КонецЕсли;

							Действие = "Скорректировать";

							Если Выборка.ЕстьЗаказКлиента Тогда
								Если СкорректированныеПозиции.Получить(Выборка.ИдентификаторСтроки) <> Неопределено Тогда
									Действие = "Удалить";
								КонецЕсли;

								СтруктурПоиска = Новый Структура("ЗаказКлиента, КодСтроки", Выборка.ЗаказКлиента,
									Выборка.КодСтрокиЗаказаКлиента);

							Иначе
								СтруктурПоиска = Новый Структура("ИдентификаторСтроки", Выборка.ИдентификаторСтроки);
							КонецЕсли;

							НайденныеСтроки = ОбъектКорректировки.Товары.НайтиСтроки(СтруктурПоиска);

							Если НайденныеСтроки.Количество() = 1 Тогда
								Если Выборка.КоличествоДоставлено = 0 Тогда
									ОбъектКорректировки.Товары.Удалить(НайденныеСтроки[0]);
								ИначеЕсли Действие = "Удалить" Тогда
									ОбъектКорректировки.Товары.Удалить(НайденныеСтроки[0]);
								Иначе
									
									ИзменитьКоличествоУпаковокВСтрокеРеализации(ОбъектКорректировки,
										НайденныеСтроки[0], Выборка.КоличествоДоставлено);
										
									Если Выборка.ЕстьЗаказКлиента Тогда
										СкорректированныеПозиции.Вставить(Выборка.ИдентификаторСтроки, Истина);
									КонецЕсли;

								КонецЕсли;
							КонецЕсли;
						КонецЦикла;

						Попытка
							ОбъектКорректировки.Записать(?(ОбъектКорректировки.Проведен, РежимЗаписиДокумента.Проведение,
								РежимЗаписиДокумента.Запись));
						Исключение
							Результат.Ошибки.Добавить(ОбработкаОшибок.ПодробноеПредставлениеОшибки(
								ИнформацияОбОшибке()));
							РеализацияСкоррректированаУспешно = Ложь;
						КонецПопытки;
					КонецЕсли;

				КонецЦикла;

				Если РеализацияСкоррректированаУспешно И ВыборкаЗаказКлиента.ЕстьЗаказКлиента Тогда
					//Скорректируем заказ, используя стандартный механизм "Отменить непоставленные строки"

					ОбъектКорректировки = ВыборкаЗаказКлиента.ЗаказКлиента.ПолучитьОбъект();

					Попытка
						ОбъектКорректировки.Заблокировать();
					Исключение
						ТекстСообщения = СтрШаблон(НСтр(
							"ru = '%1 находится в процессе редактирования и не может быть изменен.';
							|en = '%1 is being edited and cannot be changed.'"),
							ОбъектКорректировки);
						ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
						Продолжить;
					КонецПопытки;

					СтруктураКорректировки = ЗаказыСервер.СтруктураКорректировкиСтрокЗаказа();
					СтруктураКорректировки.ДокументОбъект               = ОбъектКорректировки;
					СтруктураКорректировки.ПричинаОтмены                = Справочники.ПричиныОтменыЗаказовКлиентов.ПустаяСсылка();
					СтруктураКорректировки.ПроверятьОстатки             = Истина;
					СтруктураКорректировки.ОтменитьНеотработанныеСтроки = Истина;

					ПродажиСервер.СкорректироватьСтрокиЗаказа(СтруктураКорректировки);
					Попытка
						ОбъектКорректировки.Записать(?(ОбъектКорректировки.Проведен, РежимЗаписиДокумента.Проведение,
							РежимЗаписиДокумента.Запись));
					Исключение
						Результат.Ошибки.Добавить(ОбработкаОшибок.ПодробноеПредставлениеОшибки(
							ИнформацияОбОшибке()));
					КонецПопытки;
				КонецЕсли;

			ИначеЕсли ВыборкаЗаказКлиента.ЗаказКлиента <> Неопределено Тогда
				ОбъектКорректировки = ВыборкаЗаказКлиента.ЗаказКлиента.ПолучитьОбъект();

				Попытка
					ОбъектКорректировки.Заблокировать();
				Исключение
					ТекстСообщения = СтрШаблон(НСтр(
						"ru = '%1 находится в процессе редактирования и не может быть изменен.';
						|en = '%1 is being edited and cannot be changed.'"), ОбъектКорректировки);
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
					Продолжить;
				КонецПопытки;
				
				КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
				
				СтруктураДействий = Новый Структура;
				СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(
					ОбъектКорректировки);
				СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
				СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
				СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
				СтруктураДействий.Вставить("ПересчитатьСумму");
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки",
					Новый Структура("Очищать", Ложь));
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки",
					Новый Структура("Очищать", Истина));
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомСкидкиБонуснымиБаллами");
				
				ОбрабатываемыеСтроки = Новый Массив;
				
				Выборка = ВыборкаЗаказКлиента.Выбрать();
				Пока Выборка.Следующий() Цикл
					Если Не ЗначениеЗаполнено(Выборка.ИдентификаторСтроки) Тогда
						Продолжить;
					КонецЕсли;

					СтруктураПоискаСтроки = Новый Структура("ИдентификаторСтроки, Отменено",
						Выборка.ИдентификаторСтроки, Ложь);
					НайденныеСтроки = ОбъектКорректировки.Товары.НайтиСтроки(СтруктураПоискаСтроки);
					Если НайденныеСтроки.Количество() = 1 Тогда
						Если Выборка.КоличествоДоставлено = 0 Тогда
							НайденныеСтроки[0].Отменено = Истина;
						Иначе

							КоличествоКОтмене = НайденныеСтроки[0].КоличествоУпаковок - Выборка.КоличествоДоставлено;
							НайденныеСтроки[0].КоличествоУпаковок = Выборка.КоличествоДоставлено;
							
							ОбрабатываемыеСтроки.Добавить(НайденныеСтроки[0]);

							НоваяСтрока = ОбъектКорректировки.Товары.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденныеСтроки[0], ,
								"КодСтроки, ИдентификаторСтроки");
							НоваяСтрока.КоличествоУпаковок = КоличествоКОтмене;
							НоваяСтрока.ИдентификаторСтроки = Новый УникальныйИдентификатор;
							НоваяСтрока.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.КОбеспечению;
							НоваяСтрока.Отменено = Истина;
							ОбрабатываемыеСтроки.Добавить(НоваяСтрока);

						КонецЕсли;
					КонецЕсли;

				КонецЦикла;
				
				ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
					ОбрабатываемыеСтроки, СтруктураДействий, КэшированныеЗначения);

				Попытка
					ОбъектКорректировки.Записать(?(ОбъектКорректировки.Проведен, РежимЗаписиДокумента.Проведение,
						РежимЗаписиДокумента.Запись));
				Исключение
					Результат.Ошибки.Добавить(ОбработкаОшибок.ПодробноеПредставлениеОшибки(
						ИнформацияОбОшибке()));
				КонецПопытки;

			КонецЕсли;
		КонецЦикла;

	КонецЦикла;
	
	СервисДоставки.ОбработатьДанныеПоЧастичнойДоставке(Организация, ТипГрузоперевозки, ИдентификаторыЗаказовНаДоставку);
	
	Результат.Вставить("ИдентификаторыДокументов", ИдентификаторыЗаказовНаДоставку);
	
КонецПроцедуры

// Обработать данные по частичной доставке.
// 
// Параметры:
//  ПараметрыОбработки - Структура:
//  * ОрганизацияБизнесСети - ОпределяемыйТип.ОрганизацияСервисДоставки - организация, чьи заказы на доставку подлежат проверке
//  * ТипГрузоперевозки - ПеречислениеСсылка.ТипыГрузоперевозки - тип грузоперевозки
//  * ИдентификаторыЗаказовНаДоставку - Массив из УникальныйИдентификатор - идентификаторы заказов на доставку, подлежащие проверке
//  Результат - Структура:
//  * Ошибки - Массив - Массив ошибок
//  КоличествоОбработанныхДанных - Число - Количество обработанных данных
Процедура ОбработатьДанныеПоЧастичнойДоставке(Знач ПараметрыОбработки, Результат, КоличествоОбработанныхДанных) Экспорт
	
	ОрганизацияБизнесСети = ПараметрыОбработки.ОрганизацияБизнесСети;
	ТипГрузоперевозки = ПараметрыОбработки.ТипГрузоперевозки;
	ИдентификаторыЗаказовНаДоставку = ?(ПараметрыОбработки.ИдентификаторыЗаказовНаДоставку = Неопределено, Новый Массив,
		ПараметрыОбработки.ИдентификаторыЗаказовНаДоставку);
	
	Запрос = Новый Запрос;

	Запрос.УстановитьПараметр("ОрганизацияБизнесСети", ОрганизацияБизнесСети);
	Запрос.УстановитьПараметр("ТипГрузоперевозки", ТипГрузоперевозки);
	
	СостоянияДляОбработки = Новый Массив;
	СостоянияДляОбработки.Добавить(Перечисления.СостоянияОбработкиПроцедурСинхронизацииСервисДоставки.Обработать);
	СостоянияДляОбработки.Добавить(Перечисления.СостоянияОбработкиПроцедурСинхронизацииСервисДоставки.Скорректировать);
	Запрос.УстановитьПараметр("СостоянияДляОбработки", СостоянияДляОбработки);
	
	Запрос.УстановитьПараметр("ВсеЗаказыНаДоставку", ИдентификаторыЗаказовНаДоставку.Количество() = 0);
	Запрос.УстановитьПараметр("ИдентификаторыДокументов", ИдентификаторыЗаказовНаДоставку);
	
	Запрос.Текст = ТекстЗапросаОбработкиДанныхПоЧастичнойДоставке() + "
	|ВЫБРАТЬ
	|	врДанные.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	врДанные.ДокументОснование КАК ДокументОснование,
	|	врДанные.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	врДанные.КоличествоДоставлено КАК КоличествоДоставлено,
	|	врДанные.ЕстьЗаказКлиента КАК ЕстьЗаказКлиента,
	|	врДанные.ЗаказКлиентаСкорректирован КАК ЗаказКлиентаСкорректирован,
	|	врДанные.ЗаказКлиента КАК ЗаказКлиента,
	|	врДанные.ЗаказКлиентаПредставление КАК ЗаказКлиентаПредставление,
	|	врДанные.ЕстьРеализация КАК ЕстьРеализация,
	|	врДанные.РеализацияСкорректирована КАК РеализацияСкорректирована,
	|	врДанные.Реализация КАК Реализация,
	|	врДанные.РеализацияПредставление КАК РеализацияПредставление,
	|	врДанные.ЕстьКорректировкаРеализации КАК ЕстьКорректировкаРеализации,
	|	врДанные.КорректировкаРеализацииСкорректирована КАК КорректировкаРеализацииСкорректирована,
	|	врДанные.КорректировкаРеализации КАК КорректировкаРеализации,
	|	врДанные.КорректировкаРеализацииПредставление КАК КорректировкаРеализацииПредставление,
	|	врДанные.РеализацияСторнирована КАК РеализацияСторнирована
	|
	|ИЗ
	|	врДанные КАК врДанные
	|
	|ИТОГИ
	|	МАКСИМУМ(КоличествоДоставлено),
	|	МАКСИМУМ(ЕстьЗаказКлиента),
	|	МИНИМУМ(ЗаказКлиентаСкорректирован),
	|	МАКСИМУМ(ЕстьРеализация),
	|	МИНИМУМ(РеализацияСкорректирована),
	|	МАКСИМУМ(ЕстьКорректировкаРеализации),
	|	МИНИМУМ(КорректировкаРеализацииСкорректирована),
	|	МИНИМУМ(РеализацияСторнирована)
	|ПО
	|	ИдентификаторДокумента,
	|	ДокументОснование,
	|	ЗаказКлиента,
	|	Реализация,
	|	КорректировкаРеализации
	|";

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;

	ВыборкаЗаказНаДоставку = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЗаказНаДоставку.Следующий() Цикл

		ДеревоСвязанныхДокументов = НовыйДеревоСвязанныхДокументовЗаказаНаДоставку();

		СоставСводнойРекомендацииПоИсправлениюКратко = Новый Массив;

		ВыборкаДокументОснование = ВыборкаЗаказНаДоставку.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		КоличествоДокументовОснований = ВыборкаДокументОснование.Количество();
		КоличествоОбработанныхДанных = КоличествоОбработанныхДанных + КоличествоДокументовОснований;

		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДанныеЗаказовСервисДоставки");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Организация", ОрганизацияБизнесСети);
			ЭлементБлокировки.УстановитьЗначение("ТипГрузоперевозки", ТипГрузоперевозки);
			ЭлементБлокировки.УстановитьЗначение("ИдентификаторДокумента",
				ВыборкаЗаказНаДоставку.ИдентификаторДокумента);

			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЖурналЗаказовСервисДоставки");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Организация", ОрганизацияБизнесСети);
			ЭлементБлокировки.УстановитьЗначение("ТипГрузоперевозки", ТипГрузоперевозки);
			ЭлементБлокировки.УстановитьЗначение("ИдентификаторДокумента",
				ВыборкаЗаказНаДоставку.ИдентификаторДокумента);

			Блокировка.Заблокировать();

			НаборЗаписейДанныеЗаказов = РегистрыСведений.ДанныеЗаказовСервисДоставки.СоздатьНаборЗаписей();
			НаборЗаписейДанныеЗаказов.Отбор.Организация.Установить(ОрганизацияБизнесСети);
			НаборЗаписейДанныеЗаказов.Отбор.ТипГрузоперевозки.Установить(ТипГрузоперевозки);
			НаборЗаписейДанныеЗаказов.Отбор.ИдентификаторДокумента.Установить(
				ВыборкаЗаказНаДоставку.ИдентификаторДокумента);

			НаборЗаписейДанныеЗаказов.Прочитать();

			ЗаписиПоДокументамОснованиям = Новый Соответствие;

			Для Каждого ЗаписьДанныеЗаказов Из НаборЗаписейДанныеЗаказов Цикл
				ЗаписиПоДокументамОснованиям.Вставить(ЗаписьДанныеЗаказов.ДокументОснование, ЗаписьДанныеЗаказов);
			КонецЦикла;

			Пока ВыборкаДокументОснование.Следующий() Цикл

				ВыборкаЗаказКлиента = ВыборкаДокументОснование.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаЗаказКлиента.Следующий() Цикл

					Если ЗначениеЗаполнено(ВыборкаЗаказКлиента.ЗаказКлиента) Тогда

						СтрокаЗаказаКлиента = ДеревоСвязанныхДокументов.Строки.Найти(ВыборкаЗаказКлиента.ЗаказКлиента,
							"Документ", Ложь);
						Если СтрокаЗаказаКлиента = Неопределено Тогда
							СтрокаЗаказаКлиента = ДеревоСвязанныхДокументов.Строки.Добавить();
							СтрокаЗаказаКлиента.Документ = ВыборкаЗаказКлиента.ЗаказКлиента;
						КонецЕсли;

						Если Не ВыборкаЗаказКлиента.ЗаказКлиентаСкорректирован Тогда
							СтрокаЗаказаКлиента.Информация = НСтр("ru = 'отмена строк';
																	|en = 'cancel lines'");
							Если СоставСводнойРекомендацииПоИсправлениюКратко.Найти(СтрокаЗаказаКлиента.Информация)
								= Неопределено Тогда
								СоставСводнойРекомендацииПоИсправлениюКратко.Добавить(СтрокаЗаказаКлиента.Информация);
							КонецЕсли;
						КонецЕсли;

					Иначе
						СтрокаЗаказаКлиента = ДеревоСвязанныхДокументов;
					КонецЕсли;

					Если ВыборкаЗаказКлиента.ЕстьРеализация Тогда

						ВыборкаРеализация = ВыборкаЗаказКлиента.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаРеализация.Следующий() Цикл

							СтрокаРеализации = СтрокаЗаказаКлиента.Строки.Добавить();
							СтрокаРеализации.Документ = ВыборкаРеализация.Реализация;

							Если ВыборкаРеализация.ЕстьКорректировкаРеализации Тогда

								Если Не ВыборкаРеализация.КорректировкаРеализацииСкорректирована Тогда
									Если Не ВыборкаРеализация.РеализацияСкорректирована Тогда
										Если ВыборкаРеализация.КоличествоДоставлено = 0 Тогда
											СтрокаРеализации.Информация = НСтр("ru = 'удаление строк';
																				|en = 'delete lines'");
										Иначе
											СтрокаРеализации.Информация = НСтр("ru = 'корректировка количества';
																				|en = 'quantity adjustment'");
										КонецЕсли;
										Если СоставСводнойРекомендацииПоИсправлениюКратко.Найти(
											СтрокаРеализации.Информация) = Неопределено Тогда
											СоставСводнойРекомендацииПоИсправлениюКратко.Добавить(
												СтрокаРеализации.Информация);
										КонецЕсли;
									КонецЕсли;
								КонецЕсли;

								ВыборкаКР = ВыборкаРеализация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
								Пока ВыборкаКР.Следующий() Цикл
									СтрокаКР = СтрокаРеализации.Строки.Добавить();
									СтрокаКР.Документ = ВыборкаКР.КорректировкаРеализации;
									Если ВыборкаКР.ЕстьКорректировкаРеализации
										И Не ВыборкаКР.КорректировкаРеализацииСкорректирована Тогда
										Если ВыборкаКР.КоличествоДоставлено = 0 Тогда
											СтрокаКР.Информация = НСтр("ru = 'удаление строк';
																		|en = 'delete lines'");
										Иначе
											СтрокаКР.Информация = НСтр("ru = 'корректировка количества';
																		|en = 'quantity adjustment'");
										КонецЕсли;
										Если СоставСводнойРекомендацииПоИсправлениюКратко.Найти(СтрокаКР.Информация)
											= Неопределено Тогда
											СоставСводнойРекомендацииПоИсправлениюКратко.Добавить(СтрокаКР.Информация);
										КонецЕсли;
									КонецЕсли;
								КонецЦикла;
							Иначе
								Если ВыборкаРеализация.ЕстьРеализация
									И Не ВыборкаРеализация.РеализацияСкорректирована
									И Не ВыборкаРеализация.РеализацияСторнирована Тогда

									Если ВыборкаРеализация.КоличествоДоставлено = 0 Тогда
										СтрокаРеализации.Информация = НСтр("ru = 'удаление строк';
																			|en = 'delete lines'");
									Иначе
										СтрокаРеализации.Информация = НСтр("ru = 'корректировка количества';
																			|en = 'quantity adjustment'");
									КонецЕсли;
									Если СоставСводнойРекомендацииПоИсправлениюКратко.Найти(
										СтрокаРеализации.Информация) = Неопределено Тогда
										СоставСводнойРекомендацииПоИсправлениюКратко.Добавить(
											СтрокаРеализации.Информация);
									КонецЕсли;

								КонецЕсли;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;

				ЗаписьДанныеЗаказов = ЗаписиПоДокументамОснованиям.Получить(ВыборкаДокументОснование.ДокументОснование);

				Если ЗаписьДанныеЗаказов = Неопределено Тогда
					ЗаписьДанныеЗаказов = НаборЗаписейДанныеЗаказов.Добавить();
					ЗаписьДанныеЗаказов.Организация = ОрганизацияБизнесСети;
					ЗаписьДанныеЗаказов.ТипГрузоперевозки = ТипГрузоперевозки;
					ЗаписьДанныеЗаказов.ИдентификаторДокумента = ВыборкаЗаказНаДоставку.ИдентификаторДокумента;

					ЗаписьДанныеЗаказов.ДокументОснование = ВыборкаДокументОснование.ДокументОснование;

				КонецЕсли;

			КонецЦикла;

			Если СоставСводнойРекомендацииПоИсправлениюКратко.Количество() > 0 Тогда
				СостояниеОбработкиЧастичнойДоставки = Перечисления.СостоянияОбработкиПроцедурСинхронизацииСервисДоставки.Скорректировать;
				РекомендацииПоИсправлениюЧДКратко = НСтр("ru = 'Обработать (';
														|en = 'Process ('") + СтрСоединить(
					СоставСводнойРекомендацииПоИсправлениюКратко, ",") + ")";
				ДокументыКИсправлению = Новый ХранилищеЗначения(ДеревоСвязанныхДокументов);
			Иначе
				СостояниеОбработкиЧастичнойДоставки = Перечисления.СостоянияОбработкиПроцедурСинхронизацииСервисДоставки.Выгрузить;
				РекомендацииПоИсправлениюЧДКратко = "";
				ДокументыКИсправлению = Новый ХранилищеЗначения(НовыйДеревоСвязанныхДокументовЗаказаНаДоставку());
			КонецЕсли;
			
			//Если у одного заказа на доставку несколько документов-оснований, нуждающихся в корректировке,
			//то в каждую запись регистра внесем информацию по всем документам-основаниям
			Для Каждого ЗаписьДанныеЗаказов Из НаборЗаписейДанныеЗаказов Цикл
				ЗаписьДанныеЗаказов.РекомендацииПоИсправлениюЧДКратко = РекомендацииПоИсправлениюЧДКратко;
				ЗаписьДанныеЗаказов.СостояниеОбработкиЧастичнойДоставки = СостояниеОбработкиЧастичнойДоставки;
				ЗаписьДанныеЗаказов.ДокументыКИсправлению = ДокументыКИсправлению;
			КонецЦикла;

			НаборЗаписейДанныеЗаказов.Записать(Истина);
		
			//Зарегистрируем сводное состояние по заказу на доставку в журнале заказов
			ЗаписьЖурналЗаказов = РегистрыСведений.ЖурналЗаказовСервисДоставки.СоздатьМенеджерЗаписи();
			ЗаписьЖурналЗаказов.Организация = ОрганизацияБизнесСети;
			ЗаписьЖурналЗаказов.ТипГрузоперевозки = ТипГрузоперевозки;
			ЗаписьЖурналЗаказов.ИдентификаторДокумента = ВыборкаЗаказНаДоставку.ИдентификаторДокумента;

			ЗаписьЖурналЗаказов.Прочитать();

			ЗаписьЖурналЗаказов.Организация = ОрганизацияБизнесСети;
			ЗаписьЖурналЗаказов.ТипГрузоперевозки = ТипГрузоперевозки;
			ЗаписьЖурналЗаказов.ИдентификаторДокумента = ВыборкаЗаказНаДоставку.ИдентификаторДокумента;

			ЗаписьЖурналЗаказов.ТребуетсяКорректировкаПоЧастичнойДоставке = СостояниеОбработкиЧастичнойДоставки
				= Перечисления.СостоянияОбработкиПроцедурСинхронизацииСервисДоставки.Скорректировать;

			ЗаписьЖурналЗаказов.Записать(Истина);

			ЗафиксироватьТранзакцию();

		Исключение

			ОтменитьТранзакцию();
			Результат.Ошибки.Добавить(ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

		КонецПопытки;

	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СобытияДокументов

// Выполняет операции со связанным с документом заказом на доставку.
//
// Параметры:
// 	ДокументОбъект - ДокументОбъект - объект документа-основания заказа на доставку.
//
Процедура ПередЗаписьюОснованияЗаказаНаДоставку(ДокументОбъект) Экспорт
	
	// ++ СервисДоставки.ERP
	
	ДопустимыеСостояния = Новый массив;	//Состояния заказа на доставку, при которых допустимо изменять товарный состав в сервисе интеграции
	ДопустимыеСостояния.Добавить(0);	//Черновик
	ДопустимыеСостояния.Добавить(1);	//В обработке
	ДопустимыеСостояния.Добавить(2);	//Забор груза от адреса
	ДопустимыеСостояния.Добавить(3);	//Ожидает доставки в пункт приема груза
	
	ИдентификаторыЗаказовНаДоставку = СервисДоставки.НайтиЗаказыНаДоставкуПоОснованию(ДокументОбъект.Ссылка, Истина, ДопустимыеСостояния);
	Если ЗначениеЗаполнено(ИдентификаторыЗаказовНаДоставку) Тогда
		
		Если ТипЗнч(ДокументОбъект.Ссылка) = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
			ИмяТабличнойЧасти = "ВозвращаемыеТовары";
		Иначе
			ИмяТабличнойЧасти = "Товары";
		КонецЕсли;
		
		ТаблицаТоваров = НовыйОписаниеТаблицыТоваровДокументаОснования();
		
		Для Каждого СтрокаТовары Из ДокументОбъект[ИмяТабличнойЧасти] Цикл
			НоваяСтрока = ТаблицаТоваров.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовары);
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
		Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваров);
		
		ДополнитьТекстЗапросаТоварыДоИзменения(Запрос.Текст);
		ДополнитьТекстЗапросаТоварыПослеИзменения(Запрос.Текст);
		
		Запрос.Текст = Запрос.Текст + "
		|" +
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьИзмененияТоварногоСостава
		|ИЗ
		|	ТоварыДоИзменения
		|		ПОЛНОЕ СОЕДИНЕНИЕ ТоварыПослеИзменения
		|		ПО ТоварыДоИзменения.Номенклатура = ТоварыПослеИзменения.Номенклатура
		|		И ТоварыДоИзменения.Характеристика = ТоварыПослеИзменения.Характеристика
		|		И ТоварыДоИзменения.Количество = ТоварыПослеИзменения.Количество
		|ГДЕ
		|	(ТоварыДоИзменения.Номенклатура ЕСТЬ NULL
		|	ИЛИ ТоварыПослеИзменения.Номенклатура ЕСТЬ NULL)";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяДокумента",
			СтрШаблон("%1.%2", ДокументОбъект.Метаданные().ПолноеИмя(), ИмяТабличнойЧасти));
		
		УстановитьПривилегированныйРежим(Истина);
		РезультатЗапроса = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Не РезультатЗапроса.Пустой() Тогда
			СервисДоставки.УстановитьПризнакИзмененияЗаказов("ИдентификаторДокумента", ИдентификаторыЗаказовНаДоставку, Истина);
		КонецЕсли;
		
	КонецЕсли;
	// -- СервисДоставки.ERP
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Заполняет список выбора способов определения реквизита заказа на доставку для общих настроек.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма настроек, в которой необходимо заполнить список выбора.
//
Процедура ЗаполнитьСписокВыбораСпособаОпределенияКонтактногоЛица(Форма) Экспорт
	
	СписокВыбора = Форма.Элементы.СпособОпределенияКонтактногоЛица.СписокВыбора;
	
	// ++ СервисДоставки.ERP
	СписокВыбора.Вставить(0, 3, НСтр("ru = 'Менеджер из документа отгрузки';
									|en = 'Manager from shipping document'"));
	СписокВыбора.Вставить(1, 4, НСтр("ru = 'Ответственный склада';
									|en = 'Warehouse supervisor'"));
	СписокВыбора.Вставить(2, 5, НСтр("ru = 'Текущий пользователь';
									|en = 'Current user'"));
	// -- СервисДоставки.ERP
	
КонецПроцедуры

// Получение доступности функционала доставки в прикладном решении.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма панели настроек, в которой необходимо установить доступность.
//
Процедура УстановитьДоступностьНастройкиСервисаДоставки(Форма) Экспорт
	
	// ++ СервисДоставки.ERP
	ИспользоватьУправлениеДоставкой = Константы.ИспользоватьУправлениеДоставкой.Получить();
	ЭтоПолнаяВерсия = Не СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации();
	
	Элементы = Форма.Элементы;
	
	Если ЭтоПолнаяВерсия
		И Не ИспользоватьУправлениеДоставкой Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СервисыДоставки", "Доступность", Ложь);
		
		ТекстЗаголовка = НСтр("ru = 'Невозможно включение интеграции с сервисами, потому что отключена работа с доставкой. 
									|Включить работу с доставкой можно в разделе ""Склад и доставка""';
									|en = 'Cannot enable service integration because delivery operations are disabled. 
									|You can enable delivery operations in the ""Warehouse and delivery"" section'");
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
			"КомментарийИспользоватьСервис1СДоставка", "Заголовок", ТекстЗаголовка);
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаКомментарийИспользоватьСервис1СДоставка", "Видимость", ЭтоПолнаяВерсия И Не ИспользоватьУправлениеДоставкой);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ГруппаНастройкиБизнесСетьСервисДоставки", "Видимость", ЭтоПолнаяВерсия);
	// -- СервисДоставки.ERP
	
КонецПроцедуры

// Добавление команды поиска торговых предложений в формах.
//
// Параметры:
//  КомандыСозданияНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании.
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	// ++ СервисДоставки.ERP
	Если ПравоДоступа("Использование", Метаданные.Обработки.СервисДоставки) Тогда
		
		ТипыДокументовОснованийКурьерика = СервисДоставкиКлиентСервер.ТипыДокументовОснованийКурьерика();
		
		ТипыИсточников = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ТипыИсточников", Новый Массив);
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Обработчик = "СервисДоставкиКлиентПереопределяемый.ЗаказНаДоставкуСоздатьНаОсновании";
		КомандаСоздатьНаОсновании.Идентификатор = "СоздатьНовыйЗаказНаДоставку";
		КомандаСоздатьНаОсновании.Представление = НСтр("ru = 'Заказ на доставку через Деловые линии';
														|en = 'Delivery order via Business lines'");
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.МножественныйВыбор = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьСервис1СДоставка";
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Обработчик = "СервисДоставкиКлиентПереопределяемый.ЗаказНаДоставкуСоздатьНаОсновании";
		КомандаСоздатьНаОсновании.Идентификатор = "СоздатьНовыйЗаказНаКурьерскуюДоставку";
		КомандаСоздатьНаОсновании.Представление = НСтр("ru = 'Заказ на доставку через Яндекс Доставка';
														|en = 'Delivery order via Yandex Delivery'");
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.МножественныйВыбор = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьСервис1СКурьер";
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Обработчик = "СервисДоставкиКлиентПереопределяемый.ЗаказНаДоставкуСоздатьНаОсновании";
		КомандаСоздатьНаОсновании.Идентификатор = "СоздатьНовыйЗаказНаДоставкуСДЭК";
		КомандаСоздатьНаОсновании.Представление = НСтр("ru = 'Заказ на доставку через СДЭК';
														|en = 'Delivery order via CDEK'");
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.МножественныйВыбор = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьСервис1ССДЭК";

		Для Каждого ТипИсточника Из ТипыИсточников Цикл
			Если ТипыДокументовОснованийКурьерика.Найти(ТипИсточника) <> Неопределено Тогда
				КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
				КомандаСоздатьНаОсновании.Обработчик = "СервисДоставкиКлиентПереопределяемый.ЗаказНаДоставкуСоздатьНаОсновании";
				КомандаСоздатьНаОсновании.Идентификатор = "СоздатьНовыйЗаказНаДоставкуКурьерика";
				КомандаСоздатьНаОсновании.Представление = НСтр("ru = 'Заказ на доставку через 1С-Курьерика';
																|en = 'Delivery order via 1C:Courierica'");
				КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
				КомандаСоздатьНаОсновании.МножественныйВыбор = Истина;
				КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьСервис1СКурьерика";
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	// -- СервисДоставки.ERP
	
КонецПроцедуры

// Обработка создания библиотечной формы.
//
// Параметры:
//  Форма - Форма - создаваемая управляемая форма;
//  Отказ - Булево - флаг отказа от создания формы;
//  СтандартнаяОбработка - Булево - флаг стандартной обработки формы.
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Заполняет шаблон формы оплаты для параметров заказа на доставку.
// Например, когда Форма оплаты определена перечислением "ФормыОплаты":
//    Если ФормаОплаты = Перечисления.ФормыОплаты.Наличная Тогда
//      СвойстваФормыОплаты.Вставить("ФормаОплаты", 2);
//      СвойстваФормыОплаты.Вставить("ФормаОплатыПредставление", НСтр("ru='Наличная'"));
//    Иначе
//      СвойстваФормыОплаты.Вставить("ФормаОплаты", 1);
//      СвойстваФормыОплаты.Вставить("ФормаОплатыПредставление", НСтр("ru='Безналичная'"));
//    КонецЕсли;.
//
// Параметры:
//  ФормаОплаты - ОпределяемыйТип.ФормаОплатыСервисДоставки - форма оплаты заказа на доставку;
//  СвойстваФормыОплаты - Структура - содержит:
//    ФормаОплаты - Число - 1 - безналичная, 2 - наличная, по умолчанию 2;
//    ФормаОплатыПредставление - Строка - "Безналичная", "Наличная", по умолчанию "Наличная".
//
Процедура ОпределитьПараметрыЗаказаФормаОплаты(ФормаОплаты, СвойстваФормыОплаты) Экспорт
	
КонецПроцедуры

// Определяет можно ли использовать сервис доставки в текущей конфигурации.
//
// Параметры:
//  Результат - Булево - Признак возможности использовать сервис доставки.
//
Процедура ПроверитьДоступностьСервисаДоставки(Результат) Экспорт
	
	// ++ СервисДоставки.ERP
	Результат = Результат И (Не ПолучитьФункциональнуюОпцию("БазоваяВерсия"));
	// -- СервисДоставки.ERP
	
КонецПроцедуры

// Адаптирует текст запроса к базе данных для получения контрагента или организации.
//
// Параметры:
//  ТекстЗапроса - Строка - текст запроса, который требуется адаптировать для текущей конфигурации (основная таблица уже есть в запросе);
//  ТипДанных - Тип - тип данных к таблице которых будет выполнен запрос.
//
Процедура ОбработатьЗапросПолученияУчастникаГрузоперевозкиПоИННиКПП(ТекстЗапроса, ТипДанных) Экспорт
	
	Если ТипДанных = Тип("СправочникСсылка.Организации") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Таблица.Наименование", "Таблица.НаименованиеСокращенное");
	ИначеЕсли ТипДанных = Тип("СправочникСсылка.Контрагенты") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Таблица.Наименование", "Таблица.НаименованиеПолное");
	КонецЕсли;
	
КонецПроцедуры

// Заполнить данные по взаиморасчетам.
// 
// Параметры:
//  СтруктураВзаиморасчетов - см. СервисДоставки.НовыйСтруктураВзаиморасчетовПоДокументу
//  ДокументСсылка - Неопределено, ДокументСсылка - Документ на основании которого необходимо заполнить данные
Процедура ЗаполнитьДанныеПоВзаиморасчетам(СтруктураВзаиморасчетов, ДокументСсылка = Неопределено) Экспорт
	
	ТипыДокументовБезВзаиморасчетов = Новый Массив;
	ТипыДокументовБезВзаиморасчетов.Добавить(Тип("ДокументСсылка.ЗаказНаПеремещение"));
	ТипыДокументовБезВзаиморасчетов.Добавить(Тип("ДокументСсылка.ПеремещениеТоваров"));
	
	Если Не ДокументСсылка = Неопределено
		И ТипыДокументовБезВзаиморасчетов.Найти(ТипЗнч(ДокументСсылка)) = Неопределено Тогда
	
		СтруктураПараметров = Документы[ДокументСсылка.Метаданные().Имя].ПараметрыВзаиморасчеты(ДокументСсылка);
		Если ТипЗнч(СтруктураПараметров) = Тип("Массив")
			И СтруктураПараметров.Количество() > 0 Тогда
			СтруктураПараметров = СтруктураПараметров.Получить(0);
		КонецЕсли;
		
		Организация = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(ДокументСсылка, СтруктураПараметров.Организация);
		ВалютаОрганизации = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
		
		ПутьКДаннымСуммаВзаиморасчетов = ?(СтруктураПараметров.СуммаВзаиморасчетов = "", СтруктураПараметров.СуммаДокумента, СтруктураПараметров.СуммаВзаиморасчетов);
		СуммаВзаиморасчетов = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(ДокументСсылка, ПутьКДаннымСуммаВзаиморасчетов,, 0);
		ВалютаДокумента = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(ДокументСсылка, СтруктураПараметров.ВалютаДокумента,, ВалютаОрганизации);
		ТипРасчетов = СтруктураПараметров.ТипРасчетов;
		ПорядокРасчетов = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(ДокументСсылка, СтруктураПараметров.ПорядокРасчетов);
		
		ЭтоДоговорСНакладными = СтруктураПараметров.ЭтоСправочник
			И СтруктураПараметров.ДокументСсылка = СтруктураПараметров.Договор
			И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамНакладным;
		ЭтоЗаказСНакладными = СтруктураПараметров.ЭтоЗаказ
			И ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным;
		
		ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
		ОбъектРасчетов = ОбъектыРасчетовСервер.ПолучитьОбъектРасчетовПоСсылке(ДокументСсылка, Организация, ТипРасчетов);
		СтруктураОплаты = ВзаиморасчетыСервер.СостояниеВзаиморасчетовОбъектаРасчетов(ОбъектРасчетов,
			СуммаВзаиморасчетов,
			ТипРасчетов,
			Организация,
			ЭтоЗаказСНакладными,
			ЭтоДоговорСНакладными);
			
		Если ВалютаОрганизации = ВалютаДокумента Тогда
			
			СуммаДокумента = СуммаВзаиморасчетов;
			СуммаОплат = СтруктураОплаты.СуммаОплат
			
		Иначе
			
			СуммаДокумента = РаботаСКурсамиВалют.ПересчитатьВВалюту(СуммаВзаиморасчетов,
				ВалютаДокумента,
				ВалютаОрганизации,
				ТекущаяДатаСеанса());
				
			СуммаОплат = РаботаСКурсамиВалют.ПересчитатьВВалюту(СтруктураОплаты.СуммаОплат,
				ВалютаДокумента,
				ВалютаОрганизации,
				ТекущаяДатаСеанса());
			
		КонецЕсли;
		
		СтруктураВзаиморасчетов.СуммаДокумента = СуммаДокумента;
		СтруктураВзаиморасчетов.СуммаОплат = СуммаОплат;
		СтруктураВзаиморасчетов.ДоступноВедениеВзаиморасчетов = Истина;
		
	Иначе
		
		СтруктураВзаиморасчетов.ДоступноВедениеВзаиморасчетов = Ложь;
		
	КонецЕсли;
	
Конецпроцедуры

// Получить данные для списка платежных документов.
// 
// Параметры:
//  Организация - ОпределяемыйТип.ОрганизацияСервисДоставки - организация-перевозчик
// 
// Возвращаемое значение:
//  Структура - Получить данные для списка платежных документов:
//   * Организация - ОпределяемыйТип.ОрганизацияСервисДоставки
//   * ДоговорыЭквайринга - Массив из СправочникСсылка.ДоговорыЭквайринга
Функция ПолучитьДанныеДляСпискаПлатежныхДокументов(Организация) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Организация", Организация);
	Результат.Вставить("ДоговорыЭквайринга", Новый Массив);
	
	// Доступные договоры эквайринга по наложенным платежам через СБП
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиЗагрузкиНаложенныхПлатежейСервисДоставки.ДоговорЭквайринга КАК ДоговорЭквайринга
	|ИЗ
	|	РегистрСведений.НастройкиЗагрузкиНаложенныхПлатежейСервисДоставки КАК НастройкиЗагрузкиНаложенныхПлатежейСервисДоставки
	|ГДЕ
	|	НастройкиЗагрузкиНаложенныхПлатежейСервисДоставки.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.ДоговорыЭквайринга.Добавить(Выборка.ДоговорЭквайринга);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Дополнить основания.
// 
// Параметры:
//  ДокументыОснования - Массив Из ОпределяемыйТип.ОснованиеЗаказаСервисДоставки
//  ЗначениеОтбора - Неопределено, ДокументСсылка -
Процедура ДополнитьОснованияЗаказаИЗначениеОтбора(ДокументыОснования, ЗначениеОтбора) Экспорт
	Если Не ЗначениеЗаполнено(ДокументыОснования) Тогда
		Возврат;
	КонецЕсли;
	Источник = ДокументыОснования[0];
	Если ТипЗнч(Источник) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ЗначениеОтбора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "ЗаказКлиента");
		ДокументыОснования.Добавить(ЗначениеОтбора);
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументСсылка.ЗаказКлиента")  Тогда 
		ЗначениеОтбора = Источник;
	КонецЕсли;
КонецПроцедуры

// Проверить способ доставки основания.
// 
// Параметры:
//  ДокументыОснования - Массив Из ОпределяемыйТип.ОснованиеЗаказаСервисДоставки
//  РезультатПроверки - см. СервисДоставкиСлужебный.ДанныеПроверкиОснованияЗаказаНаДоставку
Процедура ПроверитьСпособДоставкиОснования(ДокументыОснования, РезультатПроверки) Экспорт
	
	РеквизитыОснований = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ДокументыОснования,
		"ХозяйственнаяОперация, РеализацияПоЗаказам, СпособДоставки");

	СпособДоставкиПрименим =Истина;
	Пояснения = Новый Массив; // Массив Из Строка;
	Для Каждого КлючЗначение Из РеквизитыОснований Цикл
		ЗначенияРеквизитов = КлючЗначение.Значение;
			// используем условие видимости элементов Доставки см. ДоставкаТоваров
		НакладнаяПоЗаказу = ЗначенияРеквизитов.РеализацияПоЗаказам
			Или КомиссионнаяТорговляСервер.РеализацияЧерезКомиссионера(ЗначенияРеквизитов.ХозяйственнаяОперация);
		СпособДоставкиПрименимДляДокумента = Не НакладнаяПоЗаказу Или ЗначенияРеквизитов.СпособДоставки
			= Перечисления.СпособыДоставки.СиламиПеревозчика;
		СпособДоставкиПрименим = СпособДоставкиПрименим И СпособДоставкиПрименимДляДокумента;

		Если Не СпособДоставкиПрименимДляДокумента Тогда
			Пояснения.Добавить(КлючЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;

	РезультатПроверки.ТекстПояснения = СтрСоединить(Пояснения, "; ");
	РезультатПроверки.СпособДоставкиПрименим = СпособДоставкиПрименим;
	
КонецПроцедуры

#Область ОбработкаРезультатовЗапросовДокументов

// Обрабатывает результат создания и изменения заказа на доставку в сервисе 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//  ДокументыОснования - СписокЗначений из ДокументСсылка, Неопределено - список документов оснований заказа на доставку
//                                        (значение - ОпределяемыйТип.ОснованиеЗаказаСервисДоставки).
//
Процедура ОбработатьРезультатЗапросаСозданияИзмененияЗаказаНаДоставку(Результат, ДокументыОснования = Неопределено) Экспорт
	
КонецПроцедуры

// Обрабатывает результат оформления заказа на доставку в сервисе 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//  ДокументыОснования - ОпределяемыйТип.ОснованиеЗаказаСервисДоставки, Неопределено - документ, основание заказа на доставку.
//
Процедура ОбработатьРезультатЗапросаОформленияЗаказаНаДоставку(Результат, ДокументыОснования = Неопределено) Экспорт
	
КонецПроцедуры

// Обрабатывает результат отмены заказа на доставку в сервисе 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//  ДокументыОснования - ОпределяемыйТип.ОснованиеЗаказаСервисДоставки, Неопределено - документ, основание заказа на доставку.
//
Процедура ОбработатьРезультатЗапросаОтменыЗаказаНаДоставку(Результат, ДокументыОснования = Неопределено) Экспорт
	
КонецПроцедуры

// Обрабатывает результат обновления заказа на доставку в сервисе 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//  ДокументыОснования - ОпределяемыйТип.ОснованиеЗаказаСервисДоставки, Неопределено - документ, основание заказа на доставку.
//
Процедура ОбработатьРезультатЗапросаОбновленияЗаказаНаДоставку(Результат, ДокументыОснования = Неопределено) Экспорт
	
КонецПроцедуры

// Обрабатывает результат запроса состояния заказа на доставку в сервисе 1С:Доставка.
//
// Параметры:
//  ИнформацияОЗаказе - Структура - содержит информацию о состоянии заказа в сервисе;
//
Процедура ОбработатьРезультатСостоянияЗаказаНаДоставку(ИнформацияОЗаказе) Экспорт
	
КонецПроцедуры

// Обрабатывает результат получения заказа на доставку из сервиса 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//  ДокументыОснования - ОпределяемыйТип.ОснованиеЗаказаСервисДоставки, Неопределено - документ, основание заказа на доставку.
//
Процедура ОбработатьРезультатЗапросаПолученияЗаказаНаДоставку(Результат, ДокументыОснования = Неопределено) Экспорт
	
КонецПроцедуры

// Обрабатывает результат отмены заказа на доставку в сервисе 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//
Процедура ОбработатьРезультатЗапросаОтменыМультизаказаНаДоставку(Результат) Экспорт
	
КонецПроцедуры

// Обрабатывает результат обновления заказа на доставку в сервисе 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//
Процедура ОбработатьРезультатЗапросаОбновленияМультизаказаНаДоставку(Результат) Экспорт
	
КонецПроцедуры

// Обрабатывает результат получения заказа на доставку из сервиса 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//
Процедура ОбработатьРезультатЗапросаПолученияМультизаказаНаДоставку(Результат) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область УстаревшиеПроцедурыИФункции
// Устарела. Следует использовать СервисДоставкиПереопределяемый.ОбработатьРезультатЗапросаСозданияИзмененияЗаказаНаДоставку.
// Обрабатывает результат создания заказа на доставку в сервисе 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//  ДокументОснование - ОпределяемыйТип.ОснованиеЗаказаСервисДоставки, Неопределено - документ, основание заказа на доставку.
//
Процедура ОбработатьРезультатСозданияЗаказаНаДоставку(Результат, ДокументОснование = Неопределено) Экспорт
	
КонецПроцедуры

// Устарела. Следует использовать СервисДоставкиПереопределяемый.ОбработатьРезультатЗапросаОформленияЗаказаНаДоставку.
// Обрабатывает результат оформления заказа на доставку в сервисе 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//  ДокументОснование - ОпределяемыйТип.ОснованиеЗаказаСервисДоставки, Неопределено - документ, основание заказа на доставку.
//
Процедура ОбработатьРезультатОформленияЗаказаНаДоставку(Результат, ДокументОснование = Неопределено) Экспорт
	
КонецПроцедуры

// Устарела. Следует использовать СервисДоставкиПереопределяемый.ОбработатьРезультатЗапросаОтменыЗаказаНаДоставку.
// Обрабатывает результат отмены заказа на доставку в сервисе 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//  ДокументОснование - ОпределяемыйТип.ОснованиеЗаказаСервисДоставки, Неопределено - документ, основание заказа на доставку.
//
Процедура ОбработатьРезультатОтменыЗаказаНаДоставку(Результат, ДокументОснование = Неопределено) Экспорт
	
КонецПроцедуры

// Устарела. Следует использовать СервисДоставкиПереопределяемый.ОбработатьРезультатЗапросаОбновленияЗаказаНаДоставку.
// Обрабатывает результат обновления заказа на доставку в сервисе 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//  ДокументОснование - ОпределяемыйТип.ОснованиеЗаказаСервисДоставки, Неопределено - документ, основание заказа на доставку.
//
Процедура ОбработатьРезультатОбновленияЗаказаНаДоставку(Результат, ДокументОснование = Неопределено) Экспорт
	
КонецПроцедуры

// Устарела. Следует использовать СервисДоставкиПереопределяемый.ОбработатьРезультатЗапросаПолученияЗаказаНаДоставку.
// Обрабатывает результат получения заказа на доставку из сервиса 1С:Доставка.
//
// Параметры:
//  Результат - Структура - содержит результат выполнения запроса в сервис;
//  ДокументОснование - ОпределяемыйТип.ОснованиеЗаказаСервисДоставки, Неопределено - документ, основание заказа на доставку.
//
Процедура ОбработатьРезультатПолученияЗаказаНаДоставку(Результат, ДокументОснование = Неопределено) Экспорт
	
КонецПроцедуры

// Устарела. Следует использовать СервисДоставки.ЗаполнитьПараметрыКонтактногоЛица.
// Заполняет параметры контактного лица данными по ссылке, указанной в Параметры.
//
// Параметры:
//  Параметры - см. СервисДоставки.НовыйПараметрыКонтактногоЛица
//
Процедура ЗаполнитьДанныеКонтактногоЛица(Параметры) Экспорт
	
КонецПроцедуры

// Устарела. Не использовать.
// Вместо него использовать СервисДоставки.ЗаполнитьПараметрыКонтактногоЛица.
// Заполняет параметры контактного лица данными по ссылке, указанной в Параметры.
//
// Параметры:
//  Параметры - см. СервисДоставки.НовыйПараметрыКонтактногоЛица
//
Процедура ЗаполнитьПараметрыКонтактногоЛица(Параметры) Экспорт
	
КонецПроцедуры

// Устарела. Не использовать.
// Заполняет параметры телефона контактной информацией по владельцу, указанному в Параметры.
//
// Параметры:
//  Параметры - см. СервисДоставки.НовыйПараметрыКонтактногоЛица
//
Процедура ЗаполнитьТелефонКонтактногоЛица(Параметры) Экспорт
	
КонецПроцедуры

// Устарела. Не использовать.
// Вместо него использовать СкорректироватьНаименованиеВидаТелефона()
// Формирует текст наименования вида телефона для заголовка перед номером телефона.
//
// Параметры:
//  Параметры - СтрокаТаблицыЗначений из см. УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов. 
//  ВидТелефона - Строка - название вида телефона, которое необходимо сформировать или оставить без изменения.
//
Процедура СформироватьНаименованиеВидаТелефона(Параметры, ВидТелефона) Экспорт
	
	СкорректироватьНаименованиеВидаТелефона(ВидТелефона, Параметры.Вид);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗагрузкаНаложенныхПлатежей

Функция ПолучитьДоговорЭквайринга(Знач Организация, Знач Перевозчик)
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Организация", Организация);
	Запрос.Параметры.Вставить("Перевозчик", Перевозчик);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрНастроек.ДоговорЭквайринга КАК ДоговорЭквайринга
	|ИЗ
	|	РегистрСведений.НастройкиЗагрузкиНаложенныхПлатежейСервисДоставки КАК РегистрНастроек
	|ГДЕ
	|	РегистрНастроек.Организация = &Организация
	|	И &ОтборПоПеревозчику
	|	И РегистрНастроек.ДоговорЭквайринга.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
	|	И НЕ РегистрНастроек.ДоговорЭквайринга.ПометкаУдаления
	|	И РегистрНастроек.ДоговорЭквайринга.Организация = РегистрНастроек.Организация
	|	И РегистрНастроек.ДоговорЭквайринга.Контрагент = РегистрНастроек.Перевозчик";
	
	Если ЗначениеЗаполнено(Перевозчик) Тогда
		ОтборПоПеревозчику = "РегистрНастроек.Перевозчик = &Перевозчик";
	Иначе
		ОтборПоПеревозчику = "ИСТИНА";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоПеревозчику", ОтборПоПеревозчику);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ДоговорЭквайринга;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

// Возвращаемое значение:
//	ТаблицаЗначений:
//	*ДокументОплаты - ДокументСсылка.ОперацияПоПлатежнойКарте - документ оплаты
//	*Сумма - Число - сумма документа оплаты
//	*КодАвторизации - Строка - контрольная сумма
//	*Дата - Дата - дата документа оплаты
//	*Номер - Строка - номер документа оплаты
Функция ПолучитьДокументыОплатыПоОснованию(Знач ДокументОснованиеОплаты)
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ОснованиеПлатежа", ДокументОснованиеОплаты);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТабРасшифровкаПлатежа.Ссылка КАК ДокументОплаты,
	|	ТабРасшифровкаПлатежа.Сумма КАК Сумма,
	|	ДокОперация.КодАвторизации КАК КодАвторизации,
	|	ДокОперация.Дата КАК Дата,
	|	ДокОперация.Номер КАК Номер
	|ИЗ
	|	Документ.ОперацияПоПлатежнойКарте.РасшифровкаПлатежа КАК ТабРасшифровкаПлатежа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОперацияПоПлатежнойКарте КАК ДокОперация
	|		ПО ТабРасшифровкаПлатежа.Ссылка = ДокОперация.Ссылка
	|ГДЕ
	|	ТабРасшифровкаПлатежа.ОснованиеПлатежа = &ОснованиеПлатежа
	|	И ДокОперация.Проведен
	|	И НЕ ДокОперация.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Номер";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

//Параметры:
//	ИсходнаяСтрока - Строка - исходная строка по которой будет вычисляться контрольная сумма
// Возвращаемое значение:
//	Строка
Функция КодАвторизацииПоЛитералу(Знач ИсходнаяСтрока)
	
	Если ТипЗнч(ИсходнаяСтрока) <> Тип("Строка") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Хеширование = Новый ХешированиеДанных(ХешФункция.CRC32);
	Хеширование.Добавить(ИсходнаяСтрока);
	ТекСумма = Хеширование.ХешСумма;
	СуммаКак16 = ПреобразоватьДесятичноеЧислоВШестнадцатеричнуюСистемуСчисления(ТекСумма);
	Пока СтрДлина(СуммаКак16) % 2 <> 0 Цикл
		СуммаКак16 = СуммаКак16+"0";
	КонецЦикла;
	
	ДвоичныеДанные = ПолучитьДвоичныеДанныеИзHexСтроки(СуммаКак16);
	
	Сумма64 = Base64Строка(ДвоичныеДанные);
	
	Возврат Сумма64;
	 
КонецФункции

Функция ПреобразоватьДесятичноеЧислоВШестнадцатеричнуюСистемуСчисления(Знач ДесятичноеЧисло)
	
	Результат = "";
	
	Пока ДесятичноеЧисло > 0 Цикл
		ОстатокОтДеления = ДесятичноеЧисло % 16;
		ДесятичноеЧисло  = (ДесятичноеЧисло - ОстатокОтДеления) / 16;
		Результат        = Сред("0123456789abcdef", ОстатокОтДеления + 1, 1) + Результат;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ЧастичнаяДоставка

// Текст запроса обработки данных по частичной доставке.
// 
// Возвращаемое значение:
//  Строка - Текст запроса обработки данных по частичной доставке
Функция ТекстЗапросаОбработкиДанныхПоЧастичнойДоставке()

	// Заказ клиента считается скорректированным, если:
	// а) Все реализации скорректированы и "неотмененное" количество в заказе клиента совпадает с количеством 
	//     в реализации
	// б) Реализации отсутствуют, а "неотмененное" количество в заказе клиента совпадает с количеством фактически
	//     доставленного товара покупателю
	
	// Реализация считается скорректированной, если:
	// а) Количество товара в последнем документе "Корректировка реализации" совпадает с количеством фактически
	//     доставленного товара покупателю.
	// б) Проведенные документы "Корректировка реализации" отсутствуют. Количество товара в реализации совпадает
	//     с количеством фактически доставленного товара покупателю.
	
	// Корректировка реализации считается скорректированной, если:
	// а) Документ "Корректировка реализации" является последним по времени, среди множества, относящихся к одной
	//     реализации. Количество товара совпадает с количеством фактически доставленного товара покупателю.

	// 1. Получаем все строки заказов на доставку, в которых произошел возврат
	// 2. Дополняем данные п.1 ссылками на заказы клиентов, на основании которых сделаны заказы на доставку.
	//     Через "ВНУТРЕННЕЕ СОЕДИНЕНИЕ" исключаем заказы на доставку, созданные не на основании заказов клиентов.
	// 3. Дополняем данные п.1 ссылками на РТУ, на основании которых сделаны заказы на доставку.
	//     Дополняем данные п.2 ссылками на РТУ, созданными на основании заказов клиентов.
	//     Через "ВНУТРЕННЕЕ СОЕДИНЕНИЕ" исключаем заказы на доставку, не связанные с РТУ и заказами клиентов
	// 4. Получаем таблицу товаров реализации без детализации по строкам документа. Это позволит сделать сравнение
	//     с данными документа "Корректировка реализации"
	// 5. Получаем таблицу сторнированных реализаций
	// 6. Получаем последние корректировки реализаций:
	// 6.1. Получаем максимальную дату проведенной несторинрованной корректировки по реализации
	// 6.2. Получаем все корректирови по указанной дате
	// 6.3. Оставляем только последнюю корректировку
	// 7. Дополняем данные п.3 ссылками на корректировки реализации
	// 8. Получаем таблицу реализаций, которые были успешно исправлены в ручном режиме
	// 9. Получаем таблицу заказов клиента, которые были успешно исправлены в ручном режиме
	//10. Формируем итоговую таблицу
	
	ТекстЗапроса =
	"//1
	|ВЫБРАТЬ
	|	ДанныеЗаказовСервисДоставки.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	ДанныеЗаказовСервисДоставки.ДокументОснование КАК ДокументОснование,
	|	ДанныеЧД.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ДанныеЧД.Количество КАК КоличествоЗаявлено,
	|	ДанныеЧД.КоличествоВозврат КАК КоличествоВозврат,
	|	ДанныеЧД.Количество - ДанныеЧД.КоличествоВозврат КАК КоличествоДоставлено
	|ПОМЕСТИТЬ врДанныеЧД
	|ИЗ
	|	РегистрСведений.ДанныеЗаказовСервисДоставки КАК ДанныеЗаказовСервисДоставки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеЧастичнойДоставкиСервисДоставки КАК ДанныеЧД
	|		ПО ДанныеЗаказовСервисДоставки.ИдентификаторДокумента = ДанныеЧД.ИдентификаторДокумента
	|		И ДанныеЗаказовСервисДоставки.ДокументОснование = ДанныеЧД.ДокументОснование
	|ГДЕ
	|	ДанныеЗаказовСервисДоставки.Организация = &ОрганизацияБизнесСети
	|	И ДанныеЗаказовСервисДоставки.ТипГрузоперевозки = &ТипГрузоперевозки
	|	И ДанныеЗаказовСервисДоставки.СостояниеОбработкиЧастичнойДоставки В (&СостоянияДляОбработки)
	|	И ВЫБОР
	|		КОГДА &ВсеЗаказыНаДоставку
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ДанныеЗаказовСервисДоставки.ИдентификаторДокумента В (&ИдентификаторыДокументов)
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|//2
	|ВЫБРАТЬ
	|	врДанныеЧД.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	врДанныеЧД.ДокументОснование КАК ДокументОснование,
	|	врДанныеЧД.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	тчТовары.КодСтроки КАК КодСтрокиЗаказаКлиента,
	|	врДанныеЧД.КоличествоДоставлено КАК КоличествоДоставлено,
	|	врДанныеЧД.КоличествоВозврат КАК КоличествоВозврат,
	|	ЕСТЬNULL(тчТовары.КоличествоУпаковок, 0) КАК ЗаказКлиентаКоличество,
	|	ИСТИНА КАК ЕстьЗаказКлиента,
	|	ВЫБОР
	|		КОГДА тчТовары.КоличествоУпаковок = врДанныеЧД.КоличествоДоставлено
	|			ТОГДА ИСТИНА
	|		КОГДА тчТовары.Отменено
	|		И врДанныеЧД.КоличествоДоставлено = 0
	|			ТОГДА ИСТИНА
	|		КОГДА тчТовары.Ссылка ЕСТЬ NULL
	|		И врДанныеЧД.КоличествоЗаявлено = врДанныеЧД.КоличествоВозврат
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЗаказКлиентаСкорректирован,
	|	врДанныеЧД.ДокументОснование КАК ЗаказКлиента,
	|	ПРЕДСТАВЛЕНИЕ(врДанныеЧД.ДокументОснование) КАК ЗаказКлиентаПредставление
	|ПОМЕСТИТЬ врДанныеЧД_Уровень1
	|ИЗ
	|	врДанныеЧД КАК врДанныеЧД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК тчТовары
	|		ПО врДанныеЧД.ДокументОснование = тчТовары.Ссылка
	|		И врДанныеЧД.ИдентификаторСтроки = тчТовары.ИдентификаторСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДокументОснованиеСсылка
	|		ПО врДанныеЧД.ДокументОснование = ДокументОснованиеСсылка.Ссылка
	|		И (ДокументОснованиеСсылка.Проведен)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|//3
	|ВЫБРАТЬ
	|	врДанныеЧД.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	врДанныеЧД.ДокументОснование КАК ДокументОснование,
	|	врДанныеЧД.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	тчТовары.КодСтроки КАК КодСтрокиЗаказаКлиента,
	|	врДанныеЧД.КоличествоДоставлено КАК КоличествоДоставлено,
	|	врДанныеЧД.КоличествоВозврат КАК КоличествоВозврат,
	|	ЕСТЬNULL(тчТоварыЗаказКлиента.КоличествоУпаковок, 0) КАК ЗаказКлиентаКоличество,
	|	ЕСТЬNULL(тчТовары.КоличествоУпаковок, 0) КАК РеализацияКоличество,
	|	ВЫБОР
	|		КОГДА ДокументЗаказКлиента.Ссылка IS NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьЗаказКлиента,
	|	ВЫБОР
	|		КОГДА ДокументЗаказКлиента.Ссылка IS NULL
	|			ТОГДА ИСТИНА
	|		КОГДА тчТоварыЗаказКлиента.Ссылка ЕСТЬ NULL И врДанныеЧД.КоличествоЗаявлено = врДанныеЧД.КоличествоВозврат
	|			ТОГДА ИСТИНА
	|		КОГДА ЕСТЬNULL(тчТоварыЗаказКлиента.КоличествоУпаковок,0) = врДанныеЧД.КоличествоДоставлено
	|			ТОГДА ИСТИНА
	|		КОГДА тчТоварыЗаказКлиента.Отменено
	|		И врДанныеЧД.КоличествоДоставлено = 0
	|			ТОГДА ИСТИНА
	|		КОГДА тчТоварыЗаказКлиента.Ссылка ЕСТЬ NULL
	|		И врДанныеЧД.КоличествоЗаявлено = врДанныеЧД.КоличествоВозврат
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЗаказКлиентаСкорректирован,
	|	тчТовары.ЗаказКлиента КАК ЗаказКлиента,
	|	ПРЕДСТАВЛЕНИЕ(тчТовары.ЗаказКлиента) КАК ЗаказКлиентаПредставление,
	|	ИСТИНА КАК ЕстьРеализация,
	|	Ложь КАК РеализацияСкорректирована,
	|	врДанныеЧД.ДокументОснование КАК Реализация,
	|	ПРЕДСТАВЛЕНИЕ(врДанныеЧД.ДокументОснование) КАК РеализацияПредставление,
	|	тчТовары.Номенклатура,
	|	тчТовары.НоменклатураПартнера,
	|	тчТовары.Характеристика,
	|	тчТовары.Назначение,
	|	тчТовары.НоменклатураНабора,
	|	тчТовары.ХарактеристикаНабора,
	|	тчТовары.Серия,
	|	"""" КАК Содержание,
	|	тчТовары.Упаковка,
	|	тчТовары.ЗаказКлиента,
	|	тчТовары.Склад,
	|	тчТовары.Подразделение,
	|	тчТовары.СтавкаНДС,
	|	тчТовары.КодТНВЭД
	|ПОМЕСТИТЬ врДанныеЧД_Уровень2
	|ИЗ
	|	врДанныеЧД КАК врДанныеЧД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК тчТовары
	|		ПО врДанныеЧД.ДокументОснование = тчТовары.Ссылка
	|		И врДанныеЧД.ИдентификаторСтроки = тчТовары.ИдентификаторСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК ДокументОснованиеСсылка
	|		ПО врДанныеЧД.ДокументОснование = ДокументОснованиеСсылка.Ссылка
	|		И (ДокументОснованиеСсылка.Проведен)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДокументЗаказКлиента
	|		ПО ДокументЗаказКлиента.Ссылка = тчТовары.ЗаказКлиента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК тчТоварыЗаказКлиента
	|		ПО тчТоварыЗаказКлиента.Ссылка = тчТовары.ЗаказКлиента
	|			И тчТоварыЗаказКлиента.КодСтроки = тчТовары.КодСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	врДанныеЧД_Уровень1.ИдентификаторДокумента,
	|	врДанныеЧД_Уровень1.ДокументОснование,
	|	врДанныеЧД_Уровень1.ИдентификаторСтроки,
	|	врДанныеЧД_Уровень1.КодСтрокиЗаказаКлиента,
	|	врДанныеЧД_Уровень1.КоличествоДоставлено,
	|	врДанныеЧД_Уровень1.КоличествоВозврат,
	|	врДанныеЧД_Уровень1.ЗаказКлиентаКоличество КАК ЗаказКлиентаКоличество,
	|	ЕСТЬNULL(тчТовары.КоличествоУпаковок, 0) КАК РеализацияКоличество,
	|	врДанныеЧД_Уровень1.ЕстьЗаказКлиента,
	|	врДанныеЧД_Уровень1.ЗаказКлиентаСкорректирован,
	|	врДанныеЧД_Уровень1.ЗаказКлиента,
	|	врДанныеЧД_Уровень1.ЗаказКлиентаПредставление,
	|	ВЫБОР
	|		КОГДА тчТовары.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	Ложь,
	|	тчТовары.Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(тчТовары.Ссылка),
	|	тчТовары.Номенклатура,
	|	тчТовары.НоменклатураПартнера,
	|	тчТовары.Характеристика,
	|	тчТовары.Назначение,
	|	тчТовары.НоменклатураНабора,
	|	тчТовары.ХарактеристикаНабора,
	|	тчТовары.Серия,
	|	"""" КАК Содержание,
	|	тчТовары.Упаковка,
	|	тчТовары.ЗаказКлиента,
	|	тчТовары.Склад,
	|	тчТовары.Подразделение,
	|	тчТовары.СтавкаНДС,
	|	тчТовары.КодТНВЭД
	|ИЗ
	|	врДанныеЧД_Уровень1 КАК врДанныеЧД_Уровень1
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.Товары КАК тчТовары
	|		ПО врДанныеЧД_Уровень1.ДокументОснование = тчТовары.ЗаказКлиента
	|		И врДанныеЧД_Уровень1.КодСтрокиЗаказаКлиента = тчТовары.КодСтроки
	|		И тчТовары.Ссылка.Проведен
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|//4
	|ВЫБРАТЬ
	|	врДанныеЧД_Уровень2.Реализация,
	|	врДанныеЧД_Уровень2.Номенклатура,
	|	врДанныеЧД_Уровень2.НоменклатураПартнера,
	|	врДанныеЧД_Уровень2.Характеристика,
	|	врДанныеЧД_Уровень2.Назначение,
	|	врДанныеЧД_Уровень2.НоменклатураНабора,
	|	врДанныеЧД_Уровень2.ХарактеристикаНабора,
	|	врДанныеЧД_Уровень2.Серия,
	|	врДанныеЧД_Уровень2.Упаковка,
	|	врДанныеЧД_Уровень2.ЗаказКлиента,
	|	врДанныеЧД_Уровень2.Склад,
	|	врДанныеЧД_Уровень2.Подразделение,
	|	врДанныеЧД_Уровень2.СтавкаНДС,
	|	врДанныеЧД_Уровень2.КодТНВЭД,
	|	СУММА(врДанныеЧД_Уровень2.РеализацияКоличество) КАК ТоварРеализацияКоличество,
	|	СУММА(врДанныеЧД_Уровень2.КоличествоДоставлено) КАК ТоварКоличествоДоставлено
	|ПОМЕСТИТЬ врТоварыРеализации
	|ИЗ
	|	врДанныеЧД_Уровень2 КАК врДанныеЧД_Уровень2
	|СГРУППИРОВАТЬ ПО
	|	врДанныеЧД_Уровень2.Реализация,
	|	врДанныеЧД_Уровень2.Номенклатура,
	|	врДанныеЧД_Уровень2.НоменклатураПартнера,
	|	врДанныеЧД_Уровень2.Характеристика,
	|	врДанныеЧД_Уровень2.Назначение,
	|	врДанныеЧД_Уровень2.НоменклатураНабора,
	|	врДанныеЧД_Уровень2.ХарактеристикаНабора,
	|	врДанныеЧД_Уровень2.Серия,
	|	врДанныеЧД_Уровень2.Упаковка,
	|	врДанныеЧД_Уровень2.ЗаказКлиента,
	|	врДанныеЧД_Уровень2.Склад,
	|	врДанныеЧД_Уровень2.Подразделение,
	|	врДанныеЧД_Уровень2.СтавкаНДС,
	|	врДанныеЧД_Уровень2.КодТНВЭД
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|//5
	|ВЫБРАТЬ
	|	врДанныеЧД_Уровень2.Реализация КАК Реализация,
	|	ИСТИНА КАК РеализацияСторнирована
	|ПОМЕСТИТЬ врПроверкаРеализацияСторнирована
	|ИЗ
	|	врДанныеЧД_Уровень2 КАК врДанныеЧД_Уровень2
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументовСторноИсправление
	|		ПО врДанныеЧД_Уровень2.Реализация = РеестрДокументовСторноИсправление.СторнируемыйДокумент
	|		И РеестрДокументовСторноИсправление.Проведен
	|		И НЕ РеестрДокументовСторноИсправление.ДополнительнаяЗапись
	|
	|СГРУППИРОВАТЬ ПО
	|	врДанныеЧД_Уровень2.Реализация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|//6.1
	|ВЫБРАТЬ
	|	врДанныеЧД_Уровень2.Реализация КАК Реализация,
	|	МАКСИМУМ(КорректировкаРеализации.Дата) КАК ДатаКорректировкиРеализации
	|ПОМЕСТИТЬ врДатыПоследнихКорректировокРеализация
	|ИЗ
	|	Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ врДанныеЧД_Уровень2 КАК врДанныеЧД_Уровень2
	|		ПО врДанныеЧД_Уровень2.Реализация = КорректировкаРеализации.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументовСторноИсправление
	|		ПО КорректировкаРеализации.Ссылка = РеестрДокументовСторноИсправление.СторнируемыйДокумент
	|		И РеестрДокументовСторноИсправление.Проведен
	|		И НЕ РеестрДокументовСторноИсправление.ДополнительнаяЗапись
	|ГДЕ
	|	КорректировкаРеализации.Проведен
	|	И
	|		КорректировкаРеализации.ВидКорректировки <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияПерепоставленногоТовара)
	|	И
	|		КорректировкаРеализации.ВидКорректировки <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара)
	|	И РеестрДокументовСторноИсправление.Ссылка ЕСТЬ NULL
	|СГРУППИРОВАТЬ ПО
	|	врДанныеЧД_Уровень2.Реализация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|//6.2
	|ВЫБРАТЬ
	|	врДатыПоследнихКорректировокРеализация.Реализация КАК Реализация,
	|	КорректировкаРеализации.Ссылка КАК КорректировкаРеализации,
	|	КорректировкаРеализации.МоментВремени КАК МоментВремени
	|ПОМЕСТИТЬ врКорректировкиРеализации
	|ИЗ
	|	врДатыПоследнихКорректировокРеализация КАК врДатыПоследнихКорректировокРеализация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации КАК КорректировкаРеализации
	|		ПО врДатыПоследнихКорректировокРеализация.ДатаКорректировкиРеализации = КорректировкаРеализации.Дата
	|		И врДатыПоследнихКорректировокРеализация.Реализация = КорректировкаРеализации.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|//6.3
	|ВЫБРАТЬ
	|	врКорректировкиРеализации.Реализация КАК Реализация,
	|	врКорректировкиРеализации.КорректировкаРеализации КАК КорректировкаРеализации,
	|	Количество(врКорректировкиРеализации1.Реализация) КАК КоличествоДляПоискаПоследнего
	|ПОМЕСТИТЬ врПоследниеКорректировкиРеализации
	|ИЗ
	|	врКорректировкиРеализации КАК врКорректировкиРеализации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ врКорректировкиРеализации КАК врКорректировкиРеализации1
	|		ПО врКорректировкиРеализации.Реализация = врКорректировкиРеализации1.Реализация
	|		И врКорректировкиРеализации.МоментВремени <= врКорректировкиРеализации1.МоментВремени
	|СГРУППИРОВАТЬ ПО
	|	врКорректировкиРеализации.Реализация,
	|	врКорректировкиРеализации.КорректировкаРеализации
	|ИМЕЮЩИЕ
	|	Количество(врКорректировкиРеализации1.Реализация) = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|//7
	|ВЫБРАТЬ
	|	врДанныеЧД_Уровень2.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	врДанныеЧД_Уровень2.ДокументОснование КАК ДокументОснование,
	|	врДанныеЧД_Уровень2.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	врДанныеЧД_Уровень2.КодСтрокиЗаказаКлиента КАК КодСтрокиЗаказаКлиента,
	|	врДанныеЧД_Уровень2.КоличествоДоставлено КАК КоличествоДоставлено,
	|	врДанныеЧД_Уровень2.КоличествоВозврат КАК КоличествоВозврат,
	|	врДанныеЧД_Уровень2.ЗаказКлиентаКоличество КАК ЗаказКлиентаКоличество,
	|	врДанныеЧД_Уровень2.РеализацияКоличество КАК РеализацияКоличество,
	|	врДанныеЧД_Уровень2.ЕстьЗаказКлиента КАК ЕстьЗаказКлиента,
	|	врДанныеЧД_Уровень2.ЗаказКлиентаСкорректирован КАК ЗаказКлиентаСкорректирован,
	|	врДанныеЧД_Уровень2.ЗаказКлиента КАК ЗаказКлиента,
	|	врДанныеЧД_Уровень2.ЗаказКлиентаПредставление КАК ЗаказКлиентаПредставление,
	|	врДанныеЧД_Уровень2.ЕстьРеализация КАК ЕстьРеализация,
	|	врДанныеЧД_Уровень2.Реализация КАК Реализация,
	|	врДанныеЧД_Уровень2.РеализацияПредставление КАК РеализацияПредставление,
	|	ВЫБОР
	|		КОГДА врПоследниеКорректировкиРеализации.КорректировкаРеализации ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьКорректировкаРеализации,
	|	ВЫБОР
	|		КОГДА ТоварыКорректировки.Ссылка ЕСТЬ NULL И ЕстьNULL(ТоварыРеализации.ТоварКоличествоДоставлено,0) = 0
	|			ТОГДА ИСТИНА
	|		КОГДА ТоварыКорректировки.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		КОГДА ТоварыКорректировки.КоличествоУпаковок = ТоварыРеализации.ТоварКоличествоДоставлено
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КорректировкаРеализацииСкорректирована,
	|	врПоследниеКорректировкиРеализации.КорректировкаРеализации КАК КорректировкаРеализации,
	|	ПРЕДСТАВЛЕНИЕ(врПоследниеКорректировкиРеализации.КорректировкаРеализации) КАК КорректировкаРеализацииПредставление
	|ПОМЕСТИТЬ врПолнаяЦепочка
	|ИЗ
	|	врДанныеЧД_Уровень2 КАК врДанныеЧД_Уровень2
	|		ЛЕВОЕ СОЕДИНЕНИЕ врТоварыРеализации КАК ТоварыРеализации
	|		ПО врДанныеЧД_Уровень2.Реализация = ТоварыРеализации.Реализация
	|		И врДанныеЧД_Уровень2.Номенклатура = ТоварыРеализации.Номенклатура
	|		И врДанныеЧД_Уровень2.НоменклатураПартнера = ТоварыРеализации.НоменклатураПартнера
	|		И врДанныеЧД_Уровень2.Характеристика = ТоварыРеализации.Характеристика
	|		И врДанныеЧД_Уровень2.Назначение = ТоварыРеализации.Назначение
	|		И врДанныеЧД_Уровень2.НоменклатураНабора = ТоварыРеализации.НоменклатураНабора
	|		И врДанныеЧД_Уровень2.ХарактеристикаНабора = ТоварыРеализации.ХарактеристикаНабора
	|		И врДанныеЧД_Уровень2.Серия = ТоварыРеализации.Серия
	|		И врДанныеЧД_Уровень2.Упаковка = ТоварыРеализации.Упаковка
	|		И врДанныеЧД_Уровень2.ЗаказКлиента = ТоварыРеализации.ЗаказКлиента
	|		И врДанныеЧД_Уровень2.Склад = ТоварыРеализации.Склад
	|		И врДанныеЧД_Уровень2.Подразделение = ТоварыРеализации.Подразделение
	|		И врДанныеЧД_Уровень2.СтавкаНДС = ТоварыРеализации.СтавкаНДС
	|		И врДанныеЧД_Уровень2.КодТНВЭД = ТоварыРеализации.КодТНВЭД
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ врПоследниеКорректировкиРеализации КАК врПоследниеКорректировкиРеализации
	|		ПО врДанныеЧД_Уровень2.Реализация = врПоследниеКорректировкиРеализации.Реализация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.КорректировкаРеализации.Товары КАК ТоварыКорректировки
	|		ПО ТоварыКорректировки.Ссылка = врПоследниеКорректировкиРеализации.КорректировкаРеализации
	|		И врДанныеЧД_Уровень2.Номенклатура = ТоварыКорректировки.Номенклатура
	|		И врДанныеЧД_Уровень2.НоменклатураПартнера = ТоварыКорректировки.НоменклатураПартнера
	|		И врДанныеЧД_Уровень2.Характеристика = ТоварыКорректировки.Характеристика
	|		И врДанныеЧД_Уровень2.Назначение = ТоварыКорректировки.Назначение
	|		И врДанныеЧД_Уровень2.НоменклатураНабора = ТоварыКорректировки.НоменклатураНабора
	|		И врДанныеЧД_Уровень2.ХарактеристикаНабора = ТоварыКорректировки.ХарактеристикаНабора
	|		И врДанныеЧД_Уровень2.Серия = ТоварыКорректировки.Серия
	|		И врДанныеЧД_Уровень2.Упаковка = ТоварыКорректировки.Упаковка
	|		И врДанныеЧД_Уровень2.ЗаказКлиента = ТоварыКорректировки.ЗаказКлиента
	|		И врДанныеЧД_Уровень2.Склад = ТоварыКорректировки.Склад
	|		И врДанныеЧД_Уровень2.Подразделение = ТоварыКорректировки.Подразделение
	|		И врДанныеЧД_Уровень2.СтавкаНДС = ТоварыКорректировки.СтавкаНДС
	|		И врДанныеЧД_Уровень2.КодТНВЭД = ТоварыКорректировки.КодТНВЭД
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	врДанныеЧД.ИдентификаторДокумента,
	|	врДанныеЧД.ДокументОснование,
	|	врДанныеЧД.ИдентификаторСтроки,
	|	0,
	|	0,
	|	врДанныеЧД.КоличествоДоставлено,
	|	0 КАК ЗаказКлиентаКоличество,
	|	0 КАК РеализацияКоличество,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО,
	|	"""",
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО,
	|	""""
	|ИЗ
	|	врДанныеЧД КАК врДанныеЧД
	|ГДЕ
	|	НЕ (врДанныеЧД.ИдентификаторДокумента, врДанныеЧД.ДокументОснование, врДанныеЧД.ИдентификаторСтроки) В
	|		(ВЫБРАТЬ
	|			врДанныеЧД_Уровень2.ИдентификаторДокумента,
	|			врДанныеЧД_Уровень2.ДокументОснование,
	|			врДанныеЧД_Уровень2.ИдентификаторСтроки
	|		ИЗ
	|			врДанныеЧД_Уровень2 КАК врДанныеЧД_Уровень2)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|//8
	|ВЫБРАТЬ
	|	врПолнаяЦепочка.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	врПолнаяЦепочка.ДокументОснование КАК ДокументОснование,
	|	врПолнаяЦепочка.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	МАКСИМУМ(врПолнаяЦепочка.КоличествоДоставлено) КАК КоличествоДоставлено,
	|	МАКСИМУМ(врПолнаяЦепочка.ЗаказКлиентаКоличество) КАК ЗаказКлиентаКоличество,
	|	СУММА(врПолнаяЦепочка.РеализацияКоличество) КАК РеализацияКоличество,
	|	МАКСИМУМ(врПолнаяЦепочка.ЕстьРеализация) КАК ЕстьРеализация,
	|	ВЫБОР
	|		КОГДА МИНИМУМ(врПолнаяЦепочка.КорректировкаРеализацииСкорректирована И врПолнаяЦепочка.ЕстьКорректировкаРеализации)
	|			ТОГДА ИСТИНА
	|		КОГДА СУММА(врПолнаяЦепочка.РеализацияКоличество) = МАКСИМУМ(врПолнаяЦепочка.КоличествоДоставлено)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РеализацияСкорректирована,
	|	МИНИМУМ(врПолнаяЦепочка.ЗаказКлиентаСкорректирован) КАК ЗаказКлиентаСкорректирован
	|ПОМЕСТИТЬ врПроверкаРеализацияСкорректирована
	|ИЗ
	|	врПолнаяЦепочка КАК врПолнаяЦепочка
	|СГРУППИРОВАТЬ ПО
	|	врПолнаяЦепочка.ИдентификаторДокумента,
	|	врПолнаяЦепочка.ДокументОснование,
	|	врПолнаяЦепочка.ИдентификаторСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|//9
	|ВЫБРАТЬ
	|	врПроверкаРеализации.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	врПроверкаРеализации.ДокументОснование КАК ДокументОснование,
	|	врПроверкаРеализации.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаСкорректирован
	|			ТОГДА ИСТИНА
	|		КОГДА РеализацияСкорректирована И РеализацияКоличество = ЗаказКлиентаКоличество
	|			ТОГДА ИСТИНА
	|		КОГДА Не ЕстьРеализация И ЗаказКлиентаКоличество = КоличествоДоставлено
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЗаказКлиентаСкорректирован
	|ПОМЕСТИТЬ врПроверкаЗаказКлиентаСкорректирован
	|ИЗ
	|	врПроверкаРеализацияСкорректирована КАК врПроверкаРеализации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|//10
	|ВЫБРАТЬ
	|	врПолнаяЦепочка.*,
	|	ЕстьNULL(врПроверкаРеализацияСкорректирована.РеализацияСкорректирована, Ложь) КАК РеализацияСкорректирована,
	|	ЕстьNULL(врПроверкаЗаказКлиентаСкорректирован.ЗаказКлиентаСкорректирован, Ложь) КАК ЗаказКлиентаСкорректирован,
	|	ЕстьNULL(врПроверкаРеализацияСторнирована.РеализацияСторнирована, Ложь) КАК РеализацияСторнирована
	|ПОМЕСТИТЬ врДанные
	|ИЗ
	|	врПолнаяЦепочка КАК врПолнаяЦепочка
	|		ЛЕВОЕ СОЕДИНЕНИЕ врПроверкаРеализацияСкорректирована КАК врПроверкаРеализацияСкорректирована
	|		ПО врПолнаяЦепочка.ИдентификаторДокумента = врПроверкаРеализацияСкорректирована.ИдентификаторДокумента
	|		И врПолнаяЦепочка.ДокументОснование = врПроверкаРеализацияСкорректирована.ДокументОснование
	|		И врПолнаяЦепочка.ИдентификаторСтроки = врПроверкаРеализацияСкорректирована.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ врПроверкаЗаказКлиентаСкорректирован КАК врПроверкаЗаказКлиентаСкорректирован
	|		ПО врПолнаяЦепочка.ИдентификаторДокумента = врПроверкаЗаказКлиентаСкорректирован.ИдентификаторДокумента
	|		И врПолнаяЦепочка.ДокументОснование = врПроверкаЗаказКлиентаСкорректирован.ДокументОснование
	|		И врПолнаяЦепочка.ИдентификаторСтроки = врПроверкаЗаказКлиентаСкорректирован.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ врПроверкаРеализацияСторнирована КАК врПроверкаРеализацияСторнирована
	|		ПО врПроверкаРеализацияСторнирована.Реализация = врПолнаяЦепочка.Реализация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врДанныеЧД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врДанныеЧД_Уровень1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ врДанныеЧД_Уровень2";
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает дерево, описывающее связь заказа на доставку с заказом клиента, РТУ и корректировкой реализации
// 
// Возвращаемое значение:
//  ДеревоЗначений - дерево связанных документов заказа на доставку:
// * Документ - ДокументСсылка - документ, связанный с заказом на доставку
// * Информация - Строка - информация по документу в зависимости от области применения дерева.
Функция НовыйДеревоСвязанныхДокументовЗаказаНаДоставку()
	
	ДеревоСвязанныхДокументов = Новый ДеревоЗначений;
	ДеревоСвязанныхДокументов.Колонки.Добавить("Документ");
	ДеревоСвязанныхДокументов.Колонки.Добавить("Информация", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки("150")));
	
	Возврат ДеревоСвязанныхДокументов;
	
КонецФункции

Процедура ИзменитьКоличествоУпаковокВСтрокеРеализации(ДокументРеализация, СтрокаТЧ, КоличествоУпаковок)
	
	СтрокаТЧ.КоличествоУпаковок = КоличествоУпаковок;
	
	СтруктураДействий = Новый Структура;
	ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок(СтруктураДействий, ДокументРеализация);
		
	КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();

	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТЧ, СтруктураДействий, КэшированныеЗначения);	
	
КонецПроцедуры

Процедура ИзменитьКоличествоУпаковокВСтрокеКорректировкиРеализации(ДокументКР, СтрокаТЧ, КоличествоУпаковок)
	
	ТекущаяСтрока = Новый Структура;
	ТекущаяСтрока.Вставить("Номенклатура",                СтрокаТЧ.Номенклатура);
	ТекущаяСтрока.Вставить("Упаковка",                    СтрокаТЧ.Упаковка);
	ТекущаяСтрока.Вставить("Количество",                  ?(СтрокаТЧ.Количество <> 0, СтрокаТЧ.Количество, 1));
	ТекущаяСтрока.Вставить("КоличествоУпаковок",          КоличествоУпаковок);
	ТекущаяСтрока.Вставить("Цена",                        СтрокаТЧ.Цена);
	ТекущаяСтрока.Вставить("Сумма",                       СтрокаТЧ.Сумма);
	ТекущаяСтрока.Вставить("СтавкаНДС",                   СтрокаТЧ.СтавкаНДС);
	ТекущаяСтрока.Вставить("СуммаНДС",                    СтрокаТЧ.СуммаНДС);
	ТекущаяСтрока.Вставить("СуммаСНДС",                   СтрокаТЧ.СуммаСНДС);
	
	СтруктураДействий = Новый Структура;
	ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок(СтруктураДействий, ДокументКР);
	
	КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	ПакетнаяОбработкаТабличнойЧастиВызовСервера.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
		
	ЗаполнитьЗначенияСвойств(СтрокаТЧ,ТекущаяСтрока);
	
	Если СтрокаТЧ.КоличествоУпаковок = 0 Тогда
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, ТекущаяСтрока, , "Количество,КоличествоУпаковок");
	Иначе
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, ТекущаяСтрока);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок(СтруктураДействий, Объект)
	
	СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);
	
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.РеализацияТоваровУслуг") Тогда
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСумму");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомСкидкиБонуснымиБаллами");
		
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект.КорректировкаРеализации") Тогда
		
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСумму");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Получает вид контактной информации для адреса доставки партнера.
//
// Возвращаемое значение:
//  СправочникСсылка.ВидыКонтактнойИнформации, Неопределено - вид контактной информации.
//
Функция ВидКонтактнойИнформацииАдресДоставкиПартнера()
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыКонтактнойИнформации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыКонтактнойИнформации КАК ВидыКонтактнойИнформации
	|ГДЕ
	|	ВидыКонтактнойИнформации.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес)
	|	И ВидыКонтактнойИнформации.Родитель = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.СправочникПартнеры)
	|	И ВидыКонтактнойИнформации.Наименование = &АдресДоставкиНаименование";
	
	Запрос.Параметры.Вставить("АдресДоставкиНаименование", "Адрес доставки");
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Результат.Выгрузить()[0].Ссылка;
	
КонецФункции

Функция РазложитьМассивСсылокПоТипам(МассивСсылок)
	
	СоответствиеТипов = Новый Соответствие;
	
	Для Счетчик = 0 По МассивСсылок.Количество() -1 Цикл
		
		ТипСсылки = ТипЗнч(МассивСсылок[Счетчик]);
		Массив = СоответствиеТипов[ТипСсылки];
		Если Массив = Неопределено Тогда
			Массив = Новый Массив;
			СоответствиеТипов.Вставить(ТипСсылки, Массив);
		КонецЕсли;
		Массив.Добавить(МассивСсылок[Счетчик]);
		
	КонецЦикла;
	
	Возврат СоответствиеТипов;
	
КонецФункции

Процедура ДополнитьЗапросПоОснованиям(ТекстЗапроса, ТекстЗапросаДляДополнения, ПервыйЗапрос, ИмяТаблицы)
	
	Если Не ЗначениеЗаполнено(ТекстЗапросаДляДополнения) Тогда
		Возврат;
	КонецЕсли;
	ТекстЗапросаДляДополнения = СтрЗаменить(ТекстЗапросаДляДополнения, "#ТаблицаДокумента", ИмяТаблицы);
	Если ПервыйЗапрос Тогда
		ПервыйЗапрос = Ложь;
		ТекстЗапроса = ТекстЗапросаДляДополнения;
	Иначе
		ТекстЗапросаДляДополнения = СтрЗаменить(ТекстЗапросаДляДополнения, "ПОМЕСТИТЬ ДанныеОснованийТовары", "");
		ТекстЗапросаДляДополнения = СтрЗаменить(ТекстЗапросаДляДополнения, "ПОМЕСТИТЬ ДанныеОснований", "");
		ТекстЗапросаДляДополнения = СтрЗаменить(ТекстЗапросаДляДополнения, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ");
		ТекстЗапроса = ТекстЗапроса +
		"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|"
		+ ТекстЗапросаДляДополнения;
	КонецЕсли;
	
КонецПроцедуры

Функция СоответствиеСтавокНДС()
	
	Соответствие  = Новый Соответствие();
	
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС0,       "0");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС5,       "5");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС7,       "7");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС10,      "10");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС18,      "18");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС5_105,   "5/105");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС7_107,   "7/107");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС10_110,  "10/110");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС18_118,  "18/118");
	Соответствие.Вставить(Перечисления.СтавкиНДС.БезНДС,     "БезНДС");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС20,      "20");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС20_120,  "20/120");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС22,      "22");
	Соответствие.Вставить(Перечисления.СтавкиНДС.НДС22_122,  "22/122");
	
	Возврат Соответствие;
	
КонецФункции

Функция ЭтоДокументОтгрузкиСНашегоСклада(ИмяДокумента)
	
	Возврат СтрНайти("ЗаказКлиента,РеализацияТоваровУслуг,ВозвратТоваровПоставщику,ЗаявкаНаВозвратТоваровОтКлиента,ЗаказНаПеремещение,ПеремещениеТоваров", ИмяДокумента) <> 0;
	
КонецФункции

Функция ЭтоДокументДоставкиНаНашСклад(ИмяДокумента)
	
	Возврат СтрНайти("ЗаказПоставщику,ПриобретениеТоваровУслуг,ЗаказНаПеремещение,ПеремещениеТоваров", ИмяДокумента) <> 0;
	
КонецФункции

Процедура ДополнитьТекстЗапросаТоварыДоИзменения(ТекстЗапроса)
	
	// ++ СервисДоставки.ERP
	ТекстЗапроса = ТекстЗапроса + "
	|" +
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Количество КАК Количество
	|ПОМЕСТИТЬ ТоварыДокументаДоЗаписи
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Ссылка = &Ссылка
	|	И НЕ ТаблицаТоваров.Отменено
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Количество
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Количество
	|ИЗ
	|	Документ.ЗаказПоставщику.Товары КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Ссылка = &Ссылка
	|	И НЕ ТаблицаТоваров.Отменено
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Количество
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Товары КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Количество
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента.ВозвращаемыеТовары КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Ссылка = &Ссылка
	|	И НЕ ТаблицаТоваров.Отменено
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Количество
	|ИЗ
	|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Количество
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Количество
	|ИЗ
	|	Документ.ЗаказНаПеремещение.Товары КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыДокументаДоЗаписи.Номенклатура КАК Номенклатура,
	|	ТоварыДокументаДоЗаписи.Характеристика КАК Характеристика,
	|	СУММА(ТоварыДокументаДоЗаписи.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыДоИзменения
	|ИЗ
	|	ТоварыДокументаДоЗаписи КАК ТоварыДокументаДоЗаписи
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыДокументаДоЗаписи.Номенклатура,
	|	ТоварыДокументаДоЗаписи.Характеристика
	|" + ОбщегоНазначения.РазделительПакетаЗапросов();
	// -- СервисДоставки.ERP
	
КонецПроцедуры

Процедура ДополнитьТекстЗапросаТоварыПослеИзменения(ТекстЗапроса)
	
	// ++ СервисДоставки.ERP
	ТекстЗапроса = ТекстЗапроса + "
	|" + 
	"ВЫБРАТЬ
	|	ВТ.Номенклатура КАК Номенклатура,
	|	ВТ.Характеристика КАК Характеристика,
	|	ВТ.Количество КАК Количество
	|ПОМЕСТИТЬ ТоварыДокументаПослеЗаписи
	|ИЗ
	|	&ТаблицаТоваров КАК ВТ
	|ГДЕ
	|	НЕ ВТ.Отменено
	|;
	|
	|ВЫБРАТЬ
	|	ТоварыДокументаПослеЗаписи.Номенклатура КАК Номенклатура,
	|	ТоварыДокументаПослеЗаписи.Характеристика КАК Характеристика,
	|	СУММА(ТоварыДокументаПослеЗаписи.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыПослеИзменения
	|ИЗ
	|	ТоварыДокументаПослеЗаписи КАК ТоварыДокументаПослеЗаписи
	|СГРУППИРОВАТЬ ПО
	|	ТоварыДокументаПослеЗаписи.Номенклатура,
	|	ТоварыДокументаПослеЗаписи.Характеристика
	|" + ОбщегоНазначения.РазделительПакетаЗапросов();
	// -- СервисДоставки.ERP
	
КонецПроцедуры

Функция НовыйОписаниеТаблицыТоваровДокументаОснования()
	
	Результат = Новый ТаблицаЗначений();
	Результат.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Результат.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Результат.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	Результат.Колонки.Добавить("Отменено", Новый ОписаниеТипов("Булево"));
	
	Возврат Результат;
	
КонецФункции

#Область ТекстыЗапросов

Функция ШаблонЗапросаДанныеШапкиДокумента()
	Возврат "ВЫБРАТЬ
	|Т.Ссылка КАК ДокументОснование,
	|0 КАК ФормаОплаты,
	|NULL КАК ГрузоперевозчикСсылка,
	|"""" КАК ГрузоперевозчикИдентификатор,
	|"""" КАК ГрузоперевозчикНаименование,
	|"""" КАК ГрузоперевозчикТелефон,
	|"""" КАК ГрузоперевозчикИНН,
	|"""" КАК ГрузоперевозчикКПП,
	|"""" КАК ТарифНаименование,
	|"""" КАК ТарифИдентификатор,
	|"""" КАК ТарифНеГабарит,
	|ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОтгрузки,
	|ДАТАВРЕМЯ(1, 1, 1) КАК ДатаДоставки,
	|ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиС,
	|ДАТАВРЕМЯ(1, 1, 1) КАК ВремяОтгрузкиПо,
	|ДАТАВРЕМЯ(1, 1, 1) КАК ВремяДоставкиС,
	|ДАТАВРЕМЯ(1, 1, 1) КАК ВремяДоставкиПо,
	|0 КАК СпособОтгрузки,
	|0 КАК СпособДоставки,
	|NULL КАК Менеджер,
	|NULL КАК СкладОтправитель,
	|NULL КАК СкладПолучатель,
	|NULL КАК ОтправительКонтрагентСсылка,
	|"""" КАК ОтправительКонтрагентИНН,
	|"""" КАК ОтправительКонтрагентКПП,
	|"""" КАК ОтправительКонтрагентНаименование,
	|0 КАК ОтправительКонтрагентЮрФизЛицо,
	|NULL КАК ОтправительКонтрагентЮридическийАдресВладелец,
	|"""" КАК ОтправительКонтрагентЮридическийАдресПредставление,
	|"""" КАК ОтправительКонтрагентЮридическийАдресЗначение,
	|ЛОЖЬ КАК ОтправительКонтрагентЭтоОрганизация,
	|NULL КАК ОтправительАдресВладелец,
	|"""" КАК ОтправительАдресВладелецНаименование,
	|"""" КАК ОтправительАдресПредставление,
	|"""" КАК ОтправительАдресЗначенияПолей,
	|"""" КАК ОтправительАдресЗначение,
	|NULL КАК ОтправительКонтактноеЛицоСсылка,
	|"""" КАК ОтправительКонтактноеЛицоНаименование,
	|"""" КАК ОтправительКонтактноеЛицоТелефонПредставление,
	|"""" КАК ОтправительКонтактноеЛицоТелефонЗначение,
	|NULL КАК ПолучательКонтрагентСсылка,
	|"""" КАК ПолучательКонтрагентИНН,
	|"""" КАК ПолучательКонтрагентКПП,
	|"""" КАК ПолучательКонтрагентНаименование,
	|0 КАК ПолучательКонтрагентЮрФизЛицо,
	|NULL КАК ПолучательКонтрагентЮридическийАдресВладелец,
	|"""" КАК ПолучательКонтрагентЮридическийАдресПредставление,
	|"""" КАК ПолучательКонтрагентЮридическийАдресЗначение,
	|ЛОЖЬ КАК ПолучательКонтрагентЭтоОрганизация,
	|NULL КАК ПолучательАдресВладелец,
	|"""" КАК ПолучательАдресВладелецНаименование,
	|"""" КАК ПолучательАдресПредставление,
	|"""" КАК ПолучательАдресЗначенияПолей,
	|"""" КАК ПолучательАдресЗначение,
	|NULL КАК ПолучательКонтактноеЛицоСсылка,
	|"""" КАК ПолучательКонтактноеЛицоНаименование,
	|"""" КАК ПолучательКонтактноеЛицоТелефонПредставление,
	|"""" КАК ПолучательКонтактноеЛицоТелефонЗначение,
	|NULL КАК ПлательщикКонтрагентСсылка,
	|"""" КАК ПлательщикКонтрагентИНН,
	|"""" КАК ПлательщикКонтрагентКПП,
	|"""" КАК ПлательщикКонтрагентНаименование,
	|0 КАК ПлательщикКонтрагентЮрФизЛицо,
	|ИСТИНА КАК ПлательщикКонтрагентЭтоОрганизация,
	|NULL КАК ПлательщикКонтрагентЮридическийАдресВладелец,
	|"""" КАК ПлательщикКонтрагентЮридическийАдресПредставление,
	|"""" КАК ПлательщикКонтрагентЮридическийАдресЗначение,
	|NULL КАК ПлательщикКонтактноеЛицоСсылка,
	|"""" КАК ПлательщикКонтактноеЛицоНаименование,
	|"""" КАК ПлательщикКонтактноеЛицоТелефонПредставление,
	|"""" КАК ПлательщикКонтактноеЛицоТелефонЗначение,
	|1 КАК ПлательщикРоль,
	|"""" КАК ПунктПриемаГрузаИдентификатор,
	|NULL КАК ПунктПриемаГрузаСсылка,
	|"""" КАК ПунктВыдачиГрузаИдентификатор,
	|NULL КАК ПунктВыдачиГрузаСсылка,
	|0 КАК СуммаДокумента,
	|NULL КАК ВалютаСсылка,
	|"""" КАК ВалютаКод,
	|"""" КАК ВалютаНаименование,
	|ВЫБОР
	|	КОГДА ЕСТЬNULL(КурсыВалюты.Кратность, 0) > 0
	|			И ЕСТЬNULL(КурсыВалюты.Курс, 0) > 0
	|		ТОГДА КурсыВалюты.Курс / КурсыВалюты.Кратность
	|	ИНАЧЕ 1
	|КОНЕЦ КАК ВалютаКоэффициент,
	|ОрганизацииБизнесСеть.Организация КАК ОрганизацияБизнесСетиСсылка,
	|ОрганизацииБизнесСеть.Идентификатор КАК ОрганизацияБизнесСетиИдентификатор,
	|Т.ДополнительнаяИнформацияПоДоставке КАК ДополнительнаяИнформация
	|ПОМЕСТИТЬ ДанныеОснований
	|ИЗ #ТаблицаДокумента КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|		ПО Т.Организация = ОрганизацииБизнесСеть.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалюты
	|		ПО ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) = КурсыВалюты.Валюта
	|ГДЕ
	|	Т.Ссылка В (&Основания)
	|";
КонецФункции

Функция ШаблонЗапросаВидыЗапасовПустойРезультат()
	Возврат "ВЫБРАТЬ
	|	NULL КАК Ссылка,
	|	0 КАК НомерСтроки,
	|	NULL КАК УдалитьВидЗапасов,
	|	NULL КАК ТипДокумента,
	|	NULL КАК ИННВладельцаГруза
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ГДЕ
	|	ЛОЖЬ";
КонецФункции

Функция ШаблонЗапросаВидыЗапасовДокумента()
	Возврат "ВЫБРАТЬ
	|	ТабВидыЗапасов.Ссылка КАК Ссылка,
	|	ТабВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ТабВидыЗапасов.ВидЗапасов КАК УдалитьВидЗапасов,
	|	ТИПЗНАЧЕНИЯ(ТабВидыЗапасов.Ссылка) КАК ТипДокумента,
	|	ВЫБОР
	|		КОГДА ТабВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|			ТОГДА ТабВидыЗапасов.ВидЗапасов.Контрагент.ИНН
	|		ИНАЧЕ ТабВидыЗапасов.Ссылка.Организация.ИНН
	|	КОНЕЦ КАК ИННВладельцаГруза
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ИЗ
	|	#ТаблицаДокумента КАК ТабВидыЗапасов
	|ГДЕ
	|	ТабВидыЗапасов.Ссылка В(&Основания)";
КонецФункции

Функция ШаблонЗапросаТоварыДокумента()
	Возврат "ВЫБРАТЬ
	|Т.Ссылка КАК Ссылка,
	|&ПодстановкаАртикул КАК Артикул,
	|&ПодстановкаЭтоУслуга КАК ЭтоУслуга,
	|ДанныеВидовЗапасов.ИННВладельцаГруза КАК ИННВладельцаГруза,
	|"""" КАК ИдентификаторСтроки,
	|NULL КАК Номенклатура,
	|NULL КАК Упаковка,
	|0 КАК Цена,
	|0 КАК Количество,
	|0 КАК СуммаВсего,
	|0 КАК СуммаСНДС,
	|0 КАК СуммаНДС,
	|&ПодстановкаСтавкаНДС КАК СтавкаНДС,
	|&ПодстановкаПредставлениеСтавкаНДС КАК СтавкаНДСПредставление,
	|0 КАК Вес,
	|0 КАК Объем,
	|0 КАК Длина,
	|0 КАК Ширина,
	|0 КАК Высота
	|ПОМЕСТИТЬ ДанныеОснованийТовары
	|ИЗ #ТаблицаДокумента КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|	ПО Т.Номенклатура = СправочникНоменклатура.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаВидыЗапасов КАК ДанныеВидовЗапасов
	|	ПО Т.Ссылка = ДанныеВидовЗапасов.Ссылка
	|		И Т.НомерСтроки = ДанныеВидовЗапасов.НомерСтроки
	|ГДЕ
	|	Т.Ссылка В (&Основания)
	|	И &ПодстановкаТипНоменклатуры
	|	И &ПодстановкаОтменено
	|";
КонецФункции

Процедура ИзменитьТекстЗапросаПоРеквизитамШапкиОбъекта(ТекстЗапросаПоТипу, Знач ИмяОбъекта, Знач МетаданныеОбъекта)

	СНашегоСклада = ЭтоДокументОтгрузкиСНашегоСклада(ИмяОбъекта);
	НаНашСклад    = ЭтоДокументДоставкиНаНашСклад(ИмяОбъекта);
	
	ИзменитьТекстЗапросаДляРеквизитаОрганизация(ТекстЗапросаПоТипу, МетаданныеОбъекта, СНашегоСклада, НаНашСклад);
	ИзменитьТекстЗапросаДляРеквизитаОрганизацияПолучатель(ТекстЗапросаПоТипу, МетаданныеОбъекта);
	ИзменитьТекстЗапросаДляРеквизитаКонтрагент(ТекстЗапросаПоТипу, МетаданныеОбъекта, СНашегоСклада, НаНашСклад);
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Менеджер", МетаданныеОбъекта) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК Менеджер", "Т.Менеджер КАК Менеджер");
	ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("Ответственный", МетаданныеОбъекта) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК Менеджер", "Т.Ответственный КАК Менеджер");
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("КонтактноеЛицо", МетаданныеОбъекта) Тогда
		Если СНашегоСклада Тогда
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК ПолучательКонтактноеЛицоСсылка",
				"Т.КонтактноеЛицо КАК ПолучательКонтактноеЛицоСсылка");
		ИначеЕсли НаНашСклад Тогда
			ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК ОтправительКонтактноеЛицоСсылка",
				"Т.КонтактноеЛицо КАК ОтправительКонтактноеЛицоСсылка");
		КонецЕсли;
	ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("ЗаказПоставщику", МетаданныеОбъекта) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК ОтправительКонтактноеЛицоСсылка",
			"Т.ЗаказПоставщику.КонтактноеЛицо КАК ОтправительКонтактноеЛицоСсылка");
	КонецЕсли;
	
	ИзменитьТекстЗапросаДляРеквизитаСклад(ТекстЗапросаПоТипу, МетаданныеОбъекта, СНашегоСклада, НаНашСклад);
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ПеревозчикПартнер", МетаданныеОбъекта) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК ГрузоперевозчикСсылка", "Т.ПеревозчикПартнер КАК ГрузоперевозчикСсылка");
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаОтгрузки", МетаданныеОбъекта) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОтгрузки", "Т.ДатаОтгрузки КАК ДатаОтгрузки");
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаПоступления", МетаданныеОбъекта) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ДАТАВРЕМЯ(1, 1, 1) КАК ДатаДоставки", "Т.ДатаПоступления КАК ДатаДоставки");
	КонецЕсли;
	
	ИзменитьТекстЗапросаДляРеквизитаСпособДоставки(ТекстЗапросаПоТипу, МетаданныеОбъекта, СНашегоСклада);
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Валюта", МетаданныеОбъекта) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК ВалютаСсылка", "Т.Валюта КАК ВалютаСсылка");
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) = КурсыВалюты.Валюта", "Т.Валюта = КурсыВалюты.Валюта");
	КонецЕсли;
	
КонецПроцедуры

Процедура ИзменитьТекстЗапросаДляРеквизитаОрганизация(ТекстЗапроса, Знач МетаданныеОбъекта, Знач СНашегоСклада, Знач НаНашСклад)
	
	Если Не ОбщегоНазначения.ЕстьРеквизитОбъекта("Организация", МетаданныеОбъекта) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ПлательщикКонтрагентСсылка", "Т.Организация КАК ПлательщикКонтрагентСсылка");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПлательщикКонтрагентИНН", "Т.Организация.ИНН КАК ПлательщикКонтрагентИНН");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПлательщикКонтрагентКПП", "Т.Организация.КПП КАК ПлательщикКонтрагентКПП");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПлательщикКонтрагентНаименование", "Т.Организация.НаименованиеСокращенное КАК ПлательщикКонтрагентНаименование");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ПлательщикКонтрагентЮридическийАдресВладелец", "Т.Организация КАК ПлательщикКонтрагентЮридическийАдресВладелец");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "0 КАК ПлательщикЮрФизЛицо",
									"ВЫБОР
									|	КОГДА Т.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
									|		ТОГДА 1
									|	КОГДА Т.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
									|		ТОГДА 2
									|	КОГДА Т.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель)
									|		ТОГДА 3
									|	ИНАЧЕ 0
									|КОНЕЦ КАК ПлательщикЮрФизЛицо");
	
	Если СНашегоСклада Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ОтправительКонтрагентСсылка", "Т.Организация КАК ОтправительКонтрагентСсылка");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ОтправительКонтрагентИНН", "Т.Организация.ИНН КАК ОтправительКонтрагентИНН");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ОтправительКонтрагентКПП", "Т.Организация.КПП КАК ОтправительКонтрагентКПП");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ОтправительКонтрагентНаименование", "Т.Организация.НаименованиеСокращенное КАК ОтправительКонтрагентНаименование");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "0 КАК ОтправительКонтрагентЮрФизЛицо",
										"ВЫБОР
										|	КОГДА Т.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
										|		ТОГДА 1
										|	КОГДА Т.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
										|		ТОГДА 2
										|	КОГДА Т.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель)
										|		ТОГДА 3
										|	ИНАЧЕ 0
										|КОНЕЦ КАК ОтправительКонтрагентЮрФизЛицо");
		
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Склад", МетаданныеОбъекта) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ОтправительАдресВладелец", "Т.Склад КАК ОтправительАдресВладелец");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ОтправительАдресВладелецНаименование", "Т.Склад.Наименование КАК ОтправительАдресВладелецНаименование");
		ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("СкладОтправитель", МетаданныеОбъекта) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ОтправительАдресВладелец", "Т.СкладОтправитель КАК ОтправительАдресВладелец");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ОтправительАдресВладелецНаименование", "Т.СкладОтправитель.Наименование КАК ОтправительАдресВладелецНаименование");
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ОтправительКонтрагентЮридическийАдресВладелец", "Т.Организация КАК ОтправительКонтрагентЮридическийАдресВладелец");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ЛОЖЬ КАК ОтправительКонтрагентЭтоОрганизация", "ИСТИНА КАК ОтправительКонтрагентЭтоОрганизация");
		
	ИначеЕсли НаНашСклад Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ПолучательКонтрагентСсылка", "Т.Организация КАК ПолучательКонтрагентСсылка");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательКонтрагентИНН", "Т.Организация.ИНН КАК ПолучательКонтрагентИНН");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательКонтрагентКПП", "Т.Организация.КПП КАК ПолучательКонтрагентКПП");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательКонтрагентНаименование", "Т.Организация.НаименованиеСокращенное КАК ПолучательКонтрагентНаименование");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "0 КАК ПолучательКонтрагентЮрФизЛицо",
			"ВЫБОР
			|	КОГДА Т.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
			|		ТОГДА 1
			|	КОГДА Т.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
			|		ТОГДА 2
			|	КОГДА Т.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель)
			|		ТОГДА 3
			|	ИНАЧЕ 0
			|КОНЕЦ КАК ПолучательКонтрагентЮрФизЛицо");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ПолучательАдресВладелец", "Т.Склад КАК ПолучательАдресВладелец");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательАдресВладелецНаименование", "Т.Склад.Наименование КАК ПолучательАдресВладелецНаименование");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ПолучательКонтрагентЮридическийАдресВладелец", "Т.Организация КАК ПолучательКонтрагентЮридическийАдресВладелец");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ЛОЖЬ КАК ПолучательКонтрагентЭтоОрганизация", "ИСТИНА КАК ПолучательКонтрагентЭтоОрганизация");
	КонецЕсли;

КонецПроцедуры

Процедура ИзменитьТекстЗапросаДляРеквизитаОрганизацияПолучатель(ТекстЗапроса, Знач МетаданныеОбъекта)
	
	Если Не ОбщегоНазначения.ЕстьРеквизитОбъекта("ОрганизацияПолучатель", МетаданныеОбъекта) Тогда
		Возврат;
	КонецЕсли;
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ПолучательКонтрагентСсылка",
		"ВЫБОР
		|	КОГДА Т.ОрганизацияПолучатель <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|		ТОГДА Т.ОрганизацияПолучатель
		|	ИНАЧЕ Т.Организация
		|КОНЕЦ КАК ПолучательКонтрагентСсылка");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательКонтрагентИНН",
		"ВЫБОР
		|	КОГДА Т.ОрганизацияПолучатель <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|		ТОГДА Т.ОрганизацияПолучатель.ИНН
		|	ИНАЧЕ Т.Организация.ИНН
		|КОНЕЦ КАК ПолучательКонтрагентИНН");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательКонтрагентКПП",
		"ВЫБОР
		|	КОГДА Т.ОрганизацияПолучатель <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|		ТОГДА Т.ОрганизацияПолучатель.КПП
		|	ИНАЧЕ Т.Организация.КПП
		|КОНЕЦ КАК ПолучательКонтрагентКПП");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательКонтрагентНаименование",
		"ВЫБОР
		|	КОГДА Т.ОрганизацияПолучатель <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|		ТОГДА Т.ОрганизацияПолучатель.НаименованиеСокращенное
		|	ИНАЧЕ Т.Организация.НаименованиеСокращенное
		|КОНЕЦ КАК ПолучательКонтрагентНаименование");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "0 КАК ПолучательКонтрагентЮрФизЛицо",
		"ВЫБОР
		|	КОГДА Т.ОрганизацияПолучатель <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|		ТОГДА 
		|			ВЫБОР
		|				КОГДА Т.ОрганизацияПолучатель.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
		|					ТОГДА 1
		|				КОГДА Т.ОрганизацияПолучатель.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
		|					ТОГДА 2
		|				КОГДА Т.ОрганизацияПолучатель.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель)
		|					ТОГДА 3
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	ИНАЧЕ ВЫБОР
		|				КОГДА Т.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
		|					ТОГДА 1
		|				КОГДА Т.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
		|					ТОГДА 2
		|				КОГДА Т.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель)
		|					ТОГДА 3
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|КОНЕЦ КАК ПолучательКонтрагентЮрФизЛицо");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ПолучательАдресВладелец", 
		"Т.СкладПолучатель КАК ПолучательАдресВладелец");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательАдресВладелецНаименование", 
		"Т.СкладПолучатель.Наименование КАК ПолучательАдресВладелецНаименование");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ПолучательКонтрагентЮридическийАдресВладелец", 
		"Т.СкладПолучатель КАК ПолучательКонтрагентЮридическийАдресВладелец");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ЛОЖЬ КАК ПолучательКонтрагентЭтоОрганизация", 
		"ИСТИНА КАК ПолучательКонтрагентЭтоОрганизация");

КонецПроцедуры

Процедура ИзменитьТекстЗапросаДляРеквизитаКонтрагент(ТекстЗапроса, Знач МетаданныеОбъекта, Знач СНашегоСклада, Знач НаНашСклад)
	Если Не ОбщегоНазначения.ЕстьРеквизитОбъекта("Контрагент", МетаданныеОбъекта) Тогда
		Возврат;
	КонецЕсли;
	
	Если СНашегоСклада Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ПолучательКонтрагентСсылка", 
			"Т.Контрагент КАК ПолучательКонтрагентСсылка");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательКонтрагентИНН", 
			"Т.Контрагент.ИНН КАК ПолучательКонтрагентИНН");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательКонтрагентКПП", 
			"Т.Контрагент.КПП КАК ПолучательКонтрагентКПП");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательКонтрагентНаименование",
			"ВЫБОР 
			|	КОГДА Т.Контрагент.НаименованиеПолное <> """" 
			|		ТОГДА Т.Контрагент.НаименованиеПолное 
			|	ИНАЧЕ Т.Контрагент.Наименование 
			|КОНЕЦ КАК ПолучательКонтрагентНаименование");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "0 КАК ПолучательКонтрагентЮрФизЛицо",
			"ВЫБОР
			|	КОГДА Т.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
			|		ТОГДА 1
			|	КОГДА Т.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
			|		ТОГДА 2
			|	КОГДА Т.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель)
			|		ТОГДА 3
			|	ИНАЧЕ 0
			|КОНЕЦ КАК ПолучательКонтрагентЮрФизЛицо");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ПолучательАдресВладелец", 
			"Т.Партнер КАК ПолучательАдресВладелец");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ПолучательАдресВладелецНаименование", 
			"Т.Партнер.Наименование КАК ПолучательАдресВладелецНаименование");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ПолучательКонтрагентЮридическийАдресВладелец", 
			"Т.Контрагент КАК ПолучательКонтрагентЮридическийАдресВладелец");
		
	ИначеЕсли НаНашСклад Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ОтправительКонтрагентСсылка", 
			"Т.Контрагент КАК ОтправительКонтрагентСсылка");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ОтправительКонтрагентИНН", 
			"Т.Контрагент.ИНН КАК ОтправительКонтрагентИНН");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ОтправительКонтрагентКПП", 
			"Т.Контрагент.КПП КАК ОтправительКонтрагентКПП");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ОтправительКонтрагентНаименование",
			"ВЫБОР 
			|	КОГДА Т.Контрагент.НаименованиеПолное <> """" 
			|		ТОГДА Т.Контрагент.НаименованиеПолное 
			|	ИНАЧЕ Т.Контрагент.Наименование 
			|КОНЕЦ КАК ОтправительКонтрагентНаименование");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "0 КАК ОтправительКонтрагентЮрФизЛицо",
			"ВЫБОР
			|	КОГДА Т.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
			|		ТОГДА 1
			|	КОГДА Т.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
			|		ТОГДА 2
			|	КОГДА Т.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель)
			|		ТОГДА 3
			|	ИНАЧЕ 0
			|КОНЕЦ КАК ОтправительКонтрагентЮрФизЛицо");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ОтправительАдресВладелец", 
			"Т.Партнер КАК ОтправительАдресВладелец");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ОтправительАдресВладелецНаименование", 
			"Т.Партнер.Наименование КАК ОтправительАдресВладелецНаименование");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК ОтправительКонтрагентЮридическийАдресВладелец", 
			"Т.Контрагент КАК ОтправительКонтрагентЮридическийАдресВладелец");
	КонецЕсли;
	
КонецПроцедуры

Процедура ИзменитьТекстЗапросаДляРеквизитаСклад(ТекстЗапроса, Знач МетаданныеОбъекта, Знач СНашегоСклада, Знач НаНашСклад)
	
	Если СНашегоСклада Тогда
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Склад", МетаданныеОбъекта) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК СкладОтправитель",
				"Т.Склад КАК СкладОтправитель");
		ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("СкладОтправитель", МетаданныеОбъекта) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК СкладОтправитель",
				"Т.СкладОтправитель КАК СкладОтправитель");
		КонецЕсли;
	КонецЕсли;
	
	Если НаНашСклад Тогда
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Склад", МетаданныеОбъекта) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК СкладПолучатель",
				"Т.Склад КАК СкладПолучатель");
		ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("СкладОтправитель", МетаданныеОбъекта) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "NULL КАК СкладПолучатель",
				"Т.СкладПолучатель КАК СкладПолучатель");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ИзменитьТекстЗапросаДляРеквизитаСпособДоставки(ТекстЗапроса, Знач МетаданныеОбъекта, Знач СНашегоСклада)
	
	Если Не ОбщегоНазначения.ЕстьРеквизитОбъекта("СпособДоставки", МетаданныеОбъекта) Тогда
		Возврат;
	КонецЕсли;
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "0 КАК СпособОтгрузки",
		"	ВЫБОР
		|		КОГДА Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчика)
		|			ТОГДА 2
		|		КОГДА Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаПоАдресу)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СпособОтгрузки");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "0 КАК СпособДоставки",
		"	ВЫБОР
		|		КОГДА Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаДоНашегоСклада)
		|			ТОГДА 2
		|		КОГДА Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаДоПунктаПередачи)
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СпособДоставки");
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ВремяДоставкиС", МетаданныеОбъекта) Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "0 КАК ВремяОтгрузкиС",
			"	ВЫБОР
			|		КОГДА Т.СпособДоставки = ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаПоАдресу)
			|			ИЛИ ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.НашимиСиламиСАдресаОтправителя)
			|			ИЛИ ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ОтОтправителяОпределяетСлужбаДоставки)
			|			ТОГДА ШапкаДокумента.ВремяДоставкиС
			|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
			|	КОНЕЦ КАК ВремяОтгрузкиС");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "0 КАК ВремяОтгрузкиДо",
			"	ВЫБОР
			|		КОГДА Т.СпособДоставки = ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаПоАдресу)
			|			ИЛИ ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.НашимиСиламиСАдресаОтправителя)
			|			ИЛИ ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ОтОтправителяОпределяетСлужбаДоставки)
			|			ТОГДА ШапкаДокумента.ВремяДоставкиДо
			|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
			|	КОНЕЦ КАК ВремяОтгрузкиДо");
		
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ВремяДоставкиПо", МетаданныеОбъекта) Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "0 КАК ВремяДоставкиС",
			"	ВЫБОР
			|		КОГДА Т.СпособДоставки = ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ДоКлиента)
			|			ИЛИ ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.КПолучателюОпределяетСлужбаДоставки)
			|			ИЛИ ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаДоПунктаПередачи)
			|			ТОГДА ШапкаДокумента.ВремяДоставкиС
			|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
			|	КОНЕЦ КАК ВремяДоставкиС");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "0 КАК ВремяДоставкиДо",
			"	ВЫБОР
			|		КОГДА Т.СпособДоставки = ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ДоКлиента)
			|			ИЛИ ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.КПолучателюОпределяетСлужбаДоставки)
			|			ИЛИ ШапкаДокумента.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.СиламиПеревозчикаДоПунктаПередачи)
			|			ТОГДА ШапкаДокумента.ВремяДоставкиДо
			|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
			|	КОНЕЦ КАК ВремяДоставкиДо");
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ОтправительАдресПредставление",
		"	ВЫБОР
		|		КОГДА Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.НашимиСиламиСАдресаОтправителя)
		|			ИЛИ Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ОтОтправителяОпределяетСлужбаДоставки)
		|			ТОГДА Т.АдресДоставки
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ОтправительАдресПредставление");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ОтправительАдресЗначенияПолей",
		"	ВЫБОР
		|		КОГДА Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.НашимиСиламиСАдресаОтправителя)
		|			ИЛИ Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ОтОтправителяОпределяетСлужбаДоставки)
		|			ТОГДА Т.АдресДоставкиЗначенияПолей
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ОтправительАдресЗначенияПолей");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ОтправительАдресЗначение",
		"	ВЫБОР
		|		КОГДА Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.НашимиСиламиСАдресаОтправителя)
		|			ИЛИ Т.СпособДоставки = ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ОтОтправителяОпределяетСлужбаДоставки)
		|			ТОГДА Т.АдресДоставкиЗначение
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ОтправительАдресЗначение");
	
	Если СНашегоСклада Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательАдресПредставление",
			"Т.АдресДоставки КАК ПолучательАдресПредставление");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательАдресЗначенияПолей",
			"Т.АдресДоставкиЗначенияПолей КАК ПолучательАдресЗначенияПолей");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, """"" КАК ПолучательАдресЗначение",
			"Т.АдресДоставкиЗначение КАК ПолучательАдресЗначение");
		
	КонецЕсли;

КонецПроцедуры

Процедура ИзменитьТекстЗапросаПоРеквизитамТабличнойЧастиОбъекта(ТекстЗапросаПоТипу, Знач МетаданныеОбъектаТЧ, Параметры)
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ИдентификаторСтроки", МетаданныеОбъектаТЧ) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			""""" КАК ИдентификаторСтроки", "Т.ИдентификаторСтроки КАК ИдентификаторСтроки");
	КонецЕсли;				
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Номенклатура", МетаданныеОбъектаТЧ) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "NULL КАК Номенклатура", "Т.Номенклатура КАК Номенклатура");
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			"&ПодстановкаАртикул",
			"ВЫБОР
			|	КОГДА ДЛИНАСТРОКИ(Т.Номенклатура.Артикул) > 0
			|		ТОГДА Т.Номенклатура.Артикул
			|	ИНАЧЕ Т.Номенклатура.Код
			|КОНЕЦ");
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Упаковка", МетаданныеОбъектаТЧ) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			"NULL КАК Упаковка", "Т.Упаковка КАК Упаковка");
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("КоличествоУпаковок", МетаданныеОбъектаТЧ) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			"0 КАК Количество", "Т.КоличествоУпаковок КАК Количество");
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Цена", МетаданныеОбъектаТЧ) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			"0 КАК Цена", "Т.Цена КАК Цена");
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Сумма", МетаданныеОбъектаТЧ) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			"0 КАК СуммаВсего", "Т.Сумма КАК Сумма");
	Иначе
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			"0 КАК СуммаВсего", "0 КАК Сумма");
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаНДС", МетаданныеОбъектаТЧ) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			"0 КАК СуммаНДС", "Т.СуммаНДС КАК СуммаНДС");
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СуммаСНДС", МетаданныеОбъектаТЧ) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			"0 КАК СуммаСНДС", "Т.СуммаСНДС КАК СуммаСНДС");
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СтавкаНДС", МетаданныеОбъектаТЧ) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			"&ПодстановкаСтавкаНДС", "Т.СтавкаНДС.ПеречислениеСтавкаНДС");
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			"&ПодстановкаПредставлениеСтавкаНДС", "Т.СтавкаНДС.Наименование");
	КонецЕсли;
	
	Если Параметры.ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьер() Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			"&ПодстановкаЭтоУслуга", 
			"Т.Номенклатура.ВидНоменклатуры.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))");
	Иначе
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			"&ПодстановкаТипНоменклатуры",
			"НЕ СправочникНоменклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))");
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Отменено", МетаданныеОбъектаТЧ) Тогда
		ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу,
			"&ПодстановкаОтменено",
			"НЕ Т.Отменено");
	КонецЕсли;
	
	ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "&ПодстановкаАртикул", """""");
	ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "&ПодстановкаЭтоУслуга", "ЛОЖЬ");
	ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "&ПодстановкаСтавкаНДС", "NULL");
	ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "&ПодстановкаПредставлениеСтавкаНДС", """""");
	ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "&ПодстановкаТипНоменклатуры", "ИСТИНА");
	ТекстЗапросаПоТипу = СтрЗаменить(ТекстЗапросаПоТипу, "&ПодстановкаОтменено", "ИСТИНА");
КонецПроцедуры

Функция ТекстЗапросаВыборкаИзДанныхОснований()
	Возврат "ВЫБРАТЬ
	  |	ШапкаДокумента.ДокументОснование КАК ДокументОснование,
	  |	ШапкаДокумента.ОрганизацияБизнесСетиСсылка КАК ОрганизацияБизнесСетиСсылка,
	  |	ШапкаДокумента.ФормаОплаты КАК ФормаОплаты,
	  |	ШапкаДокумента.ГрузоперевозчикСсылка КАК ГрузоперевозчикСсылка,
	  |	ШапкаДокумента.ГрузоперевозчикИдентификатор КАК ГрузоперевозчикИдентификатор,
	  |	ШапкаДокумента.ГрузоперевозчикНаименование КАК ГрузоперевозчикНаименование,
	  |	ШапкаДокумента.ГрузоперевозчикТелефон КАК ГрузоперевозчикТелефон,
	  |	ШапкаДокумента.ГрузоперевозчикИНН КАК ГрузоперевозчикИНН,
	  |	ШапкаДокумента.ГрузоперевозчикКПП КАК ГрузоперевозчикКПП,
	  |	ШапкаДокумента.ТарифНаименование КАК ТарифНаименование,
	  |	ШапкаДокумента.ТарифИдентификатор КАК ТарифИдентификатор,
	  |	ШапкаДокумента.ТарифНеГабарит КАК ТарифНеГабарит,
	  |	ШапкаДокумента.ДатаОтгрузки КАК ДатаОтгрузки,
	  |	ШапкаДокумента.ДатаДоставки КАК ДатаДоставки,
	  |	ШапкаДокумента.ВремяОтгрузкиС КАК ВремяОтгрузкиС,
	  |	ШапкаДокумента.ВремяОтгрузкиПо КАК ВремяОтгрузкиПо,
	  |	ШапкаДокумента.ВремяДоставкиС КАК ВремяДоставкиС,
	  |	ШапкаДокумента.ВремяДоставкиПо КАК ВремяДоставкиПо,
	  |	ШапкаДокумента.СпособОтгрузки КАК СпособОтгрузки,
	  |	ШапкаДокумента.СпособДоставки КАК СпособДоставки,
	  |	ШапкаДокумента.ОтправительКонтрагентСсылка КАК ОтправительКонтрагентСсылка,
	  |	ШапкаДокумента.ОтправительКонтрагентИНН КАК ОтправительКонтрагентИНН,
	  |	ШапкаДокумента.ОтправительКонтрагентКПП КАК ОтправительКонтрагентКПП,
	  |	ШапкаДокумента.ОтправительКонтрагентНаименование КАК ОтправительКонтрагентНаименование,
	  |	ШапкаДокумента.ОтправительКонтрагентЮрФизЛицо КАК ОтправительКонтрагентЮрФизЛицо,
	  |	ШапкаДокумента.ОтправительКонтрагентЭтоОрганизация КАК ОтправительКонтрагентЭтоОрганизация,
	  |	ШапкаДокумента.ОтправительКонтрагентЮридическийАдресВладелец КАК ОтправительКонтрагентЮридическийАдресВладелец,
	  |	ШапкаДокумента.ОтправительАдресВладелец КАК ОтправительАдресВладелец,
	  |	ШапкаДокумента.ОтправительАдресВладелецНаименование КАК ОтправительАдресВладелецНаименование,
	  |	ШапкаДокумента.ОтправительАдресПредставление КАК ОтправительАдресПредставление,
	  |	ШапкаДокумента.ОтправительАдресЗначенияПолей КАК ОтправительАдресЗначенияПолей,
	  |	ВЫБОР КОГДА ШапкаДокумента.ОтправительАдресПредставление = """" ТОГДА """" ИНАЧЕ ШапкаДокумента.ОтправительАдресЗначение КОНЕЦ КАК ОтправительАдресЗначение,
	  |	ВЫБОР
	  |		КОГДА ШапкаДокумента.ОтправительКонтрагентЭтоОрганизация
	  |			ТОГДА ВЫБОР
	  |					КОГДА &СпособОпределенияКонтактногоЛица = 3
	  |						ТОГДА ПользователиМенеджер.ФизическоеЛицо
	  |					КОГДА &СпособОпределенияКонтактногоЛица = 4
	  |						ТОГДА СкладыОтправитель.ТекущийОтветственный
	  |					КОГДА &СпособОпределенияКонтактногоЛица = 5
	  |						ТОГДА ПользователиТекущий.ФизическоеЛицо
	  |					КОГДА &СпособОпределенияКонтактногоЛица = 1
	  |						ТОГДА NULL
	  |					КОГДА &СпособОпределенияКонтактногоЛица = 2
	  |						ТОГДА &ОтветственныйСклада
	  |					ИНАЧЕ ПользователиМенеджер.ФизическоеЛицо
	  |				КОНЕЦ
	  |		ИНАЧЕ ВЫБОР
	  |				КОГДА ШапкаДокумента.ОтправительКонтрагентЮрФизЛицо = 2
	  |					ТОГДА ЕСТЬNULL(КонтактныеЛицаОтправитель.Ссылка, ШапкаДокумента.ОтправительКонтрагентСсылка)
	  |				ИНАЧЕ ШапкаДокумента.ОтправительКонтактноеЛицоСсылка
	  |			КОНЕЦ
	  |	КОНЕЦ КАК ОтправительКонтактноеЛицоСсылка,
	  |	ВЫБОР
	  |		КОГДА ШапкаДокумента.ОтправительКонтрагентЭтоОрганизация
	  |			ТОГДА ШапкаДокумента.ОтправительКонтактноеЛицоНаименование
	  |		ИНАЧЕ ВЫБОР
	  |				КОГДА ШапкаДокумента.ОтправительКонтрагентЮрФизЛицо = 2
	  |					ТОГДА ЕСТЬNULL(КонтактныеЛицаОтправитель.Наименование, ШапкаДокумента.ОтправительКонтрагентНаименование)
	  |				ИНАЧЕ КонтактныеЛицаОтправитель.Наименование
	  |			КОНЕЦ
	  |	КОНЕЦ КАК ОтправительКонтактноеЛицоНаименование,
	  |	ШапкаДокумента.ОтправительКонтактноеЛицоТелефонПредставление КАК ОтправительКонтактноеЛицоТелефонПредставление,
	  |	ШапкаДокумента.ОтправительКонтактноеЛицоТелефонЗначение КАК ОтправительКонтактноеЛицоТелефонЗначение,
	  |	ШапкаДокумента.ПолучательКонтрагентСсылка КАК ПолучательКонтрагентСсылка,
	  |	ШапкаДокумента.ПолучательКонтрагентИНН КАК ПолучательКонтрагентИНН,
	  |	ШапкаДокумента.ПолучательКонтрагентКПП КАК ПолучательКонтрагентКПП,
	  |	ШапкаДокумента.ПолучательКонтрагентНаименование КАК ПолучательКонтрагентНаименование,
	  |	ШапкаДокумента.ПолучательКонтрагентЮрФизЛицо КАК ПолучательКонтрагентЮрФизЛицо,
	  |	ШапкаДокумента.ПолучательКонтрагентЭтоОрганизация КАК ПолучательКонтрагентЭтоОрганизация,
	  |	ШапкаДокумента.ПолучательКонтрагентЮридическийАдресВладелец КАК ПолучательКонтрагентЮридическийАдресВладелец,
	  |	ВЫБОР
	  |		КОГДА ШапкаДокумента.ПолучательАдресВладелец = &РозничныйПокупатель
	  |			ТОГДА ШапкаДокумента.ПолучательКонтрагентСсылка
	  |		ИНАЧЕ ШапкаДокумента.ПолучательАдресВладелец
	  |	КОНЕЦ КАК ПолучательАдресВладелец,
	  |	ВЫБОР
	  |		КОГДА ШапкаДокумента.ПолучательАдресВладелец = &РозничныйПокупатель
	  |			ТОГДА ШапкаДокумента.ПолучательКонтрагентНаименование
	  |		ИНАЧЕ ШапкаДокумента.ПолучательАдресВладелецНаименование
	  |	КОНЕЦ КАК ПолучательАдресВладелецНаименование,
	  |	ШапкаДокумента.ПолучательАдресПредставление КАК ПолучательАдресПредставление,
	  |	ШапкаДокумента.ПолучательАдресЗначенияПолей КАК ПолучательАдресЗначенияПолей,
	  |	ВЫБОР КОГДА ШапкаДокумента.ПолучательАдресПредставление = """" ТОГДА """" ИНАЧЕ ШапкаДокумента.ПолучательАдресЗначение КОНЕЦ КАК ПолучательАдресЗначение,
	  |	ВЫБОР
	  |		КОГДА ШапкаДокумента.ПолучательКонтрагентЭтоОрганизация
	  |			ТОГДА ВЫБОР
	  |					КОГДА &СпособОпределенияКонтактногоЛица = 3
	  |						ТОГДА ПользователиМенеджер.ФизическоеЛицо
	  |					КОГДА &СпособОпределенияКонтактногоЛица = 4
	  |						ТОГДА СкладыПолучатель.ТекущийОтветственный
	  |					КОГДА &СпособОпределенияКонтактногоЛица = 5
	  |						ТОГДА ПользователиТекущий.ФизическоеЛицо
	  |					КОГДА &СпособОпределенияКонтактногоЛица = 1
	  |						ТОГДА NULL
	  |					КОГДА &СпособОпределенияКонтактногоЛица = 2
	  |						ТОГДА &ОтветственныйСклада
	  |					ИНАЧЕ ПользователиМенеджер.ФизическоеЛицо
	  |				КОНЕЦ
	  |		ИНАЧЕ ВЫБОР
	  |				КОГДА ШапкаДокумента.ПолучательКонтрагентЮрФизЛицо = 2
	  |					ТОГДА ЕСТЬNULL(КонтактныеЛицаПолучатель.Ссылка, ШапкаДокумента.ПолучательКонтрагентСсылка)
	  |				ИНАЧЕ ШапкаДокумента.ПолучательКонтактноеЛицоСсылка
	  |			КОНЕЦ
	  |	КОНЕЦ КАК ПолучательКонтактноеЛицоСсылка,
	  |	ВЫБОР
	  |		КОГДА ШапкаДокумента.ПолучательКонтрагентЭтоОрганизация
	  |			ТОГДА ШапкаДокумента.ПолучательКонтактноеЛицоНаименование
	  |		ИНАЧЕ ВЫБОР
	  |				КОГДА ШапкаДокумента.ПолучательКонтрагентЮрФизЛицо = 2
	  |					ТОГДА ЕСТЬNULL(КонтактныеЛицаПолучатель.Наименование, ШапкаДокумента.ПолучательКонтрагентНаименование)
	  |				ИНАЧЕ ЕСТЬNULL(КонтактныеЛицаПолучатель.Наименование, """")
	  |			КОНЕЦ
	  |	КОНЕЦ КАК ПолучательКонтактноеЛицоНаименование,
	  |	ШапкаДокумента.ПолучательКонтактноеЛицоТелефонПредставление КАК ПолучательКонтактноеЛицоТелефонПредставление,
	  |	ШапкаДокумента.ПолучательКонтактноеЛицоТелефонЗначение КАК ПолучательКонтактноеЛицоТелефонЗначение,
	  |	ШапкаДокумента.ПлательщикКонтрагентСсылка КАК ПлательщикКонтрагентСсылка,
	  |	ШапкаДокумента.ПлательщикКонтрагентИНН КАК ПлательщикКонтрагентИНН,
	  |	ШапкаДокумента.ПлательщикКонтрагентКПП КАК ПлательщикКонтрагентКПП,
	  |	ШапкаДокумента.ПлательщикКонтрагентНаименование КАК ПлательщикКонтрагентНаименование,
	  |	ШапкаДокумента.ПлательщикКонтрагентЮрФизЛицо КАК ПлательщикКонтрагентЮрФизЛицо,
	  |	ШапкаДокумента.ПлательщикКонтрагентЭтоОрганизация КАК ПлательщикКонтрагентЭтоОрганизация,
	  |	ШапкаДокумента.ПлательщикКонтрагентЮридическийАдресВладелец КАК ПлательщикКонтрагентЮридическийАдресВладелец,
	  |	ШапкаДокумента.ПлательщикКонтактноеЛицоСсылка КАК ПлательщикКонтактноеЛицоСсылка,
	  |	ШапкаДокумента.ПлательщикКонтактноеЛицоНаименование КАК ПлательщикКонтактноеЛицоНаименование,
	  |	ШапкаДокумента.ПлательщикКонтактноеЛицоТелефонПредставление КАК ПлательщикКонтактноеЛицоТелефонПредставление,
	  |	ШапкаДокумента.ПлательщикКонтактноеЛицоТелефонЗначение КАК ПлательщикКонтактноеЛицоТелефонЗначение,
	  |	ВЫБОР
	  |		КОГДА ШапкаДокумента.ОтправительКонтрагентЭтоОрганизация
	  |			ТОГДА 1
	  |		КОГДА ШапкаДокумента.ПолучательКонтрагентЭтоОрганизация
	  |			ТОГДА 2
	  |		ИНАЧЕ 1
	  |	КОНЕЦ КАК ПлательщикРоль,
	  |	ШапкаДокумента.ВалютаКоэффициент КАК ВалютаКоэффициент,
	  |	ПОДСТРОКА(ШапкаДокумента.ДополнительнаяИнформация, 0, 500) КАК ДополнительнаяИнформация
	  |ИЗ
	  |	ДанныеОснований КАК ШапкаДокумента
	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаОтправитель
	  |		ПО ШапкаДокумента.ОтправительКонтактноеЛицоСсылка = КонтактныеЛицаОтправитель.Ссылка
	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПолучатель
	  |		ПО ШапкаДокумента.ПолучательКонтактноеЛицоСсылка = КонтактныеЛицаПолучатель.Ссылка
	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СкладыОтправитель
	  |		ПО ШапкаДокумента.СкладОтправитель = СкладыОтправитель.Ссылка
	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СкладыПолучатель
	  |		ПО ШапкаДокумента.СкладПолучатель = СкладыПолучатель.Ссылка
	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК ПользователиМенеджер
	  |		ПО ШапкаДокумента.Менеджер = ПользователиМенеджер.Ссылка
	  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК ПользователиТекущий
	  |		ПО (&ТекущийПользователь = ПользователиТекущий.Ссылка)";
КонецФункции

Функция ТекстЗапросаВыборкаИзДанныхОснованийПоТоварам()
	Возврат 
		"ВЫБРАТЬ
		|ТаблицаТовары.Ссылка КАК ДокументОснование,
		|	ТаблицаТовары.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	ТаблицаТовары.Упаковка КАК Упаковка,
		|	ТаблицаТовары.Количество КАК Количество,
		|	ТаблицаТовары.Артикул КАК Артикул,
		|	ТаблицаТовары.ЭтоУслуга КАК ЭтоУслуга,
		|	ТаблицаТовары.ИННВладельцаГруза КАК ИННВладельцаГруза,
		|	ТаблицаТовары.Сумма * ШапкаДокумента.ВалютаКоэффициент КАК Сумма,
		|	ТаблицаТовары.Цена * ШапкаДокумента.ВалютаКоэффициент КАК Цена,
		|	ТаблицаТовары.СуммаСНДС * ШапкаДокумента.ВалютаКоэффициент КАК СуммаСНДС,
		|	ТаблицаТовары.СуммаНДС * ШапкаДокумента.ВалютаКоэффициент КАК СуммаНДС,
		|	ТаблицаТовары.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаТовары.СтавкаНДСПредставление КАК СтавкаНДСПредставление
		|ПОМЕСТИТЬ ВременнаяТаблицаТовары
		|ИЗ
		|	ДанныеОснований КАК ШапкаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеОснованийТовары КАК ТаблицаТовары
		|		ПО (ТаблицаТовары.Ссылка = ШапкаДокумента.ДокументОснование)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		| Товары.ДокументОснование КАК ДокументОснование,
		|	Товары.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.СтавкаНДС КАК СтавкаНДС,
		|	Товары.СтавкаНДСПредставление КАК СтавкаНДСПредставление,
		|	Товары.Артикул КАК Артикул,
		|	Товары.ЭтоУслуга КАК ЭтоУслуга,
		|	Товары.ИННВладельцаГруза КАК ИННВладельцаГруза,
		|	СправочникНоменклатура.Наименование КАК Наименование,
		|	УпаковкиЕдиницыИзмерения.Наименование КАК ЕдиницаИзмерения,
		|	Товары.Количество КАК Количество,
		|	Товары.Цена КАК Цена,
		|	Товары.СуммаСНДС КАК Сумма,
		|	Товары.СуммаНДС КАК СуммаНДС,
		|	Товары.Упаковка КАК Упаковка,
		|	&ТекстЗапросаВесУпаковки КАК ВесЕдиницыТовара,
		|	&ТекстЗапросаОбъемУпаковки КАК ОбъемЕдиницыТовара,
		|	&ТекстЗапросаВесУпаковки * Товары.Количество КАК Вес,
		|	&ТекстЗапросаОбъемУпаковки * Товары.Количество КАК Объем,
		|	1000000 / ВЫБОР
		|		КОГДА Товары.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|				И УпаковкиЕдиницыИзмерения.ОбъемЕдиницаИзмерения.Знаменатель <> 0
		|			ТОГДА УпаковкиЕдиницыИзмерения.ОбъемЕдиницаИзмерения.Знаменатель
		|		КОГДА СправочникНоменклатура.ОбъемЕдиницаИзмерения <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|				И СправочникНоменклатура.ОбъемЕдиницаИзмерения.Знаменатель <> 0
		|			ТОГДА СправочникНоменклатура.ОбъемЕдиницаИзмерения.Знаменатель
		|		ИНАЧЕ
		|			1
		|	КОНЕЦ КАК ОбъемКоэффициентСм3,
		|	ВЫБОР
		|		КОГДА УпаковкиЕдиницыИзмерения.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка)
		|				И ЕСТЬNULL(УпаковкиЕдиницыИзмерения.ВысотаЕдиницаИзмерения.Знаменатель, 0) <> 0
		|			ТОГДА (ВЫРАЗИТЬ(ЕСТЬNULL(УпаковкиЕдиницыИзмерения.Высота, 0) * (ЕСТЬNULL(УпаковкиЕдиницыИзмерения.ВысотаЕдиницаИзмерения.Числитель, 0) / ЕСТЬNULL(УпаковкиЕдиницыИзмерения.ВысотаЕдиницаИзмерения.Знаменатель, 1)) КАК ЧИСЛО(27, 11))) * 100
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Высота,
		|	ВЫБОР
		|		КОГДА УпаковкиЕдиницыИзмерения.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка)
		|				И ЕСТЬNULL(УпаковкиЕдиницыИзмерения.ШиринаЕдиницаИзмерения.Знаменатель, 0) <> 0
		|			ТОГДА (ВЫРАЗИТЬ(ЕСТЬNULL(УпаковкиЕдиницыИзмерения.Ширина, 0) * (ЕСТЬNULL(УпаковкиЕдиницыИзмерения.ШиринаЕдиницаИзмерения.Числитель, 0) / ЕСТЬNULL(УпаковкиЕдиницыИзмерения.ШиринаЕдиницаИзмерения.Знаменатель, 1)) КАК ЧИСЛО(27, 11))) * 100
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Ширина,
		|	ВЫБОР
		|		КОГДА &ТекстЗапросаДлинаУпаковки <> 0
		|			ТОГДА &ТекстЗапросаДлинаУпаковки
		|		КОГДА УпаковкиЕдиницыИзмерения.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка)
		|				И ЕСТЬNULL(УпаковкиЕдиницыИзмерения.ШиринаЕдиницаИзмерения.Знаменатель, 0) <> 0
		|			ТОГДА ВЫРАЗИТЬ(ЕСТЬNULL(УпаковкиЕдиницыИзмерения.Глубина, 0) * (ЕСТЬNULL(УпаковкиЕдиницыИзмерения.ГлубинаЕдиницаИзмерения.Числитель, 0) / ЕСТЬNULL(УпаковкиЕдиницыИзмерения.ГлубинаЕдиницаИзмерения.Знаменатель, 1)) КАК ЧИСЛО(27, 11))
		|		ИНАЧЕ 0
		|	КОНЕЦ * 100 КАК Длина,
		|	ВЫБОР
		|		КОГДА СправочникВидыНоменклатуры.ИспользоватьИндивидуальноеНаименованиеПриПечати
		|			ТОГДА ЕСТЬNULL(СправочникВидыНоменклатуры.НаименованиеДляПечати, """")
		|		КОГДА &НаименованиеДляПечатиВидовНоменклатуры <> """"
		|			ТОГДА &НаименованиеДляПечатиВидовНоменклатуры
		|		ИНАЧЕ СправочникВидыНоменклатуры.Наименование
		|	КОНЕЦ КАК НаименованиеДляПечати
		|ПОМЕСТИТЬ Товары
		|ИЗ
		|	ВременнаяТаблицаТовары КАК Товары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК СправочникВидыНоменклатуры
		|		ПО (СправочникНоменклатура.ВидНоменклатуры = СправочникВидыНоменклатуры.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
		|		ПО (ВЫБОР
		|				КОГДА Товары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|					ТОГДА СправочникНоменклатура.ЕдиницаИзмерения = УпаковкиЕдиницыИзмерения.Ссылка
		|				ИНАЧЕ Товары.Упаковка = УпаковкиЕдиницыИзмерения.Ссылка
		|			КОНЕЦ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(СУММА(Товары.Вес), 0) КАК Вес,
		|	ЕСТЬNULL(СУММА(Товары.Объем), 0) КАК Объем,
		|	ЕСТЬNULL(МАКСИМУМ(Товары.Высота), 0) КАК МаксимальнаяВысота,
		|	ЕСТЬNULL(МАКСИМУМ(Товары.Длина), 0) КАК МаксимальнаяДлина,
		|	ЕСТЬNULL(МАКСИМУМ(Товары.Ширина), 0) КАК МаксимальнаяШирина,
		|	ЕСТЬNULL(МАКСИМУМ(Товары.Вес), 0) КАК МаксимальныйВес,
		|	ЕСТЬNULL(СУММА(Товары.Сумма), 0) КАК ПолнаяСтоимость,
		|	ЕСТЬNULL(СУММА(ВЫБОР
		|			КОГДА Товары.ЭтоУслуга
		|				ТОГДА 0
		|			ИНАЧЕ Товары.Сумма
		|		КОНЕЦ), 0) КАК Стоимость
		|ИЗ
		|	Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.НаименованиеДляПечати КАК ТоварНаименование
		|ИЗ
		|	Товары КАК Товары
		|ГДЕ
		|	НЕ Товары.ЭтоУслуга
		|
		|СГРУППИРОВАТЬ ПО
		|	Товары.НаименованиеДляПечати
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.ДокументОснование КАК ДокументОснование,
		|	СправочникВидыНоменклатуры.Ссылка КАК ВидНоменклатуры,
		|	УникальныйИдентификатор(СправочникВидыНоменклатуры.Ссылка) КАК ИдентификаторСтроки,
		|	Товары.СтавкаНДС КАК СтавкаНДС,
		|	МАКСИМУМ(Товары.СтавкаНДСПредставление) КАК СтавкаНДСПредставление,
		|	МАКСИМУМ(Товары.Артикул) КАК Артикул,
		|	МАКСИМУМ(Товары.ЭтоУслуга) КАК ЭтоУслуга,
		|	МАКСИМУМ(Товары.ИННВладельцаГруза) КАК ИННВладельцаГруза,
		|	СправочникВидыНоменклатуры.Наименование КАК Наименование,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА СправочникВидыНоменклатуры.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.Пустаяссылка)
		|				ТОГДА Товары.ЕдиницаИзмерения
		|			ИНАЧЕ ЕСТЬNULL(УпаковкиЕдиницыИзмерения.Наименование, """")
		|		КОНЕЦ) КАК ЕдиницаИзмерения,
		|	СУММА(Товары.Количество) КАК Количество,
		|	СУММА(Товары.Сумма) КАК Сумма,
		|	СУММА(Товары.СуммаНДС) КАК СуммаНДС,
		|	ВЫБОР
		|		КОГДА СУММА(Товары.Количество) > 0
		|			ТОГДА ВЫРАЗИТЬ(СУММА(Товары.Вес) / СУММА(Товары.Количество) КАК ЧИСЛО(15,3))
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ВесЕдиницыТовара,
		|	СУММА(Товары.Вес) КАК Вес,
		|	СУММА(Товары.Объем) КАК Объем,
		|	МАКСИМУМ(Товары.ОбъемКоэффициентСм3) КАК ОбъемКоэффициентСм3,
		|	МАКСИМУМ(Товары.Высота) КАК Высота,
		|	МАКСИМУМ(Товары.Ширина) КАК Ширина,
		|	МАКСИМУМ(Товары.Длина) КАК Длина
		|ИЗ
		|	Товары КАК Товары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК СправочникВидыНоменклатуры
		|		ПО (СправочникНоменклатура.ВидНоменклатуры = СправочникВидыНоменклатуры.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
		|		ПО (СправочникВидыНоменклатуры.ЕдиницаИзмерения = УпаковкиЕдиницыИзмерения.Ссылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	Товары.ДокументОснование,
		|	СправочникВидыНоменклатуры.Ссылка,
		|	СправочникВидыНоменклатуры.Наименование,
		|	Товары.СтавкаНДС";
КонецФункции

#КонецОбласти

#КонецОбласти