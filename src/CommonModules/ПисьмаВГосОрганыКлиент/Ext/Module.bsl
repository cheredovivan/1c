
#Область ПрограммныйИнтерфейс

Процедура ОтправитьПисьмо(Форма, Письмо) Экспорт
	
	Если НЕ ПисьмаВГосОрганыКлиентСервер.ЭтоФормаПисьма(Форма) Тогда
		Форма.КоличествоСвязанныхПисем = ПисьмаВГосОрганыВызовСервера.КоличествоСвязанныхПисем(Письмо);
	КонецЕсли;
	
	Всего = 0;
	ВАрхиве = 0;
	
	ПисьмаВГосОрганыВызовСервера.ПроверитьВложения(Письмо, Всего, ВАрхиве);
	
	РеквизитыПисьма = ПисьмаВГосОрганыВызовСервера.РеквизитыПерепискиПриОтправке(Письмо);
	
	Если РеквизитыПисьма.Тип = ПредопределенноеЗначение("Перечисление.ТипыПерепискиСКонтролирующимиОрганами.ПерепискаСФНС") Тогда
		
		ПараметрыФормы = Новый Структура("ЗапретПисемФНС, ЗапретВложенийФНС, ЗапретПисемФНСДатаНачала");
		
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, Форма);
		
		Если ПараметрыФормы.ЗапретПисемФНС = Неопределено Тогда
			
			СобытияЗапретВложенийПисемФНС = ПисьмаВГосОрганыКлиентСервер.СобытияЗапретВложенийПисемФНС();
			
			ЗаполнитьЗначенияСвойств(ПараметрыФормы, СобытияЗапретВложенийПисемФНС);
			
		КонецЕсли;
		
		ПараметрыФормы.Вставить("Организация", РеквизитыПисьма.Отправитель);
		
		Если ПараметрыФормы.ЗапретПисемФНС
			Или (ПараметрыФормы.ЗапретВложенийФНС
				И Всего > 0) Тогда
				
			ОткрытьФорму("Обработка.ДокументооборотСКонтролирующимиОрганами.Форма.ОшибкаОтправкиПисьмаЗапретПисемВложенийФНС",
				ПараметрыФормы);
				
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВАрхиве > 0 Тогда
		
		ВсеВАрхиве = ?(ВАрхиве = Всего, 1, 0);
		
		Описание = Новый ОписаниеОповещения("ОтправитьПисьмо_ПослеПроверкиАрхивных", ЭтотОбъект, Истина);
		
		Форма.КонтекстЭДОКлиент.ПоказатьУведомлениеАрхивныхФайлов(Описание, 18 + ВсеВАрхиве, 2, Ложь);
		
		Возврат;
		
	КонецЕсли;
	
	ОтправитьПисьмо_ПослеПроверкиАрхивных(Форма, Письмо, КодВозвратаДиалога.Да, Ложь);
	
КонецПроцедуры

Процедура ОтправитьПисьмо_ПослеПроверкиАрхивных(Форма, Письмо, Результат, УдалитьПредварительноАрхивные) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
		
	Если УдалитьПредварительноАрхивные Тогда 
		ПисьмаВГосОрганыВызовСервера.УдалитьАрхивныеВложения(Письмо);
	КонецЕсли;
	
	ОтправитьПисьмо_ПроверкаТребованийКФайлам(Форма, Письмо);
	
КонецПроцедуры

Процедура ОтправитьПисьмо_ПроверкаТребованийКФайлам(Форма, Письмо, ИгнорироватьОшибку = Ложь)
		
	АнализПисем = ПисьмаВГосОрганыВызовСервера.ПланируемыйМассивПисем(Письмо);
	
	Если АнализПисем.Допустимо
		И АнализПисем.КоличествоПисем = 1 Тогда
		
		Если ПисьмаВГосОрганыВызовСервера.ОтветНаТребованиеОтправляютКакПисьмо(Письмо) И НЕ ИгнорироватьОшибку Тогда 
			ПредложитьСоздатьОтветНаТребование(Форма, Письмо);
		Иначе
			НепосредственноОтправитьПисьмо(Форма, Письмо);
		КонецЕсли;
		
	Иначе
		
		РазделениеПисем(Форма, Письмо, АнализПисем, ИгнорироватьОшибку);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПредложитьСоздатьОтветНаТребование(Форма, Письмо)
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("Письмо", Письмо);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОтправитьПисьмо_ПослеОтказаОтСозданияПакета", 
		ЭтотОбъект,
		ДополнительныеПараметры);
		
	Ошибка = ПисьмаВГосОрганыКлиентСервер.ОшибкаНаОтветНаТребованиеФНСПисьмом();
		
	ПоказатьОшибкуПроверкиПисьмаФНС(
		Письмо,
		Ошибка,
		ОписаниеОповещения);
	
КонецПроцедуры
	
Процедура ОтправитьПисьмо_ПослеОтказаОтСозданияПакета(Результат, ВходящийКонтекст) Экспорт
	
	ИгнорироватьОшибку = Истина;
	ОтправитьПисьмо_ПроверкаТребованийКФайлам(ВходящийКонтекст.Форма, ВходящийКонтекст.Письмо, ИгнорироватьОшибку);
	
КонецПроцедуры

Процедура ПоказатьОшибкуПроверкиПисьмаФНС(Письмо, Ошибка, ОписаниеЗавершения = Неопределено) Экспорт
	
	Ошибка.Вставить("Письмо", Письмо);
	
	ДополнительныеПараметры = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Ошибка);
	ДополнительныеПараметры.Вставить("ОписаниеЗавершения", ОписаниеЗавершения);
	 
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПоказатьОшибкуПроверкиПисьмаФНС_ПослеСозданияПакета", 
		ЭтотОбъект, 
		ДополнительныеПараметры);
		
	ОткрытьФорму(
		"Обработка.ДокументооборотСКонтролирующимиОрганами.Форма.ОшибкаОтправкиПисьмаВФНС", 
		Ошибка,
		,
		,
		,
		,
		ОписаниеОповещения);
		
КонецПроцедуры
	
Процедура ПоказатьОшибкуПроверкиПисьмаФНС_ПослеСозданияПакета(Опись, ВходящийКонтекст) Экспорт
	
	Если НЕ ВходящийКонтекст.БлокироватьОтправку И НЕ ЗначениеЗаполнено(Опись) Тогда
		ВыполнитьОбработкуОповещения(ВходящийКонтекст.ОписаниеЗавершения);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Опись) Тогда
		ПоказатьЗначение(, Опись);
	КонецЕсли;
		
КонецПроцедуры

Процедура НепосредственноОтправитьПисьмо(Форма, Письмо) Экспорт
	
	КонтекстЭДОКлиент = Форма.КонтекстЭДОКлиент;
	
	Свойства = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЗначенияРеквизитовОбъекта(Письмо, "Организация, Тип");
	Свойства.Вставить("Ссылка", Письмо);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОтправкиЗавершение", ЭтотОбъект, Свойства);
	
	Если Свойства.Тип = ПредопределенноеЗначение("Перечисление.ТипыПерепискиСКонтролирующимиОрганами.ПерепискаСФНС") Тогда
		КонтекстЭДОКлиент.ОтправкаНеформализованногоДокументаВФНС(Письмо, Свойства.Организация, ОписаниеОповещения);
	ИначеЕсли Свойства.Тип = ПредопределенноеЗначение("Перечисление.ТипыПерепискиСКонтролирующимиОрганами.ПерепискаСПФР") Тогда
		КонтекстЭДОКлиент.ОтправкаНеформализованногоДокументаВПФР(Письмо, Свойства.Организация, ОписаниеОповещения);
	ИначеЕсли Свойства.Тип = ПредопределенноеЗначение("Перечисление.ТипыПерепискиСКонтролирующимиОрганами.ПерепискаСФСГС") Тогда
		КонтекстЭДОКлиент.ОтправкаНеформализованногоДокументаВФСГС(Письмо, Свойства.Организация, ОписаниеОповещения);
	Иначе
		КонтекстЭДОКлиент.ОбъектЗаполненКорректно(Письмо);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеОтправкиЗавершение(Результат, ВходящийКонтекст) Экспорт
	
	ОповеститьОЗавершенииОтправки(ВходящийКонтекст.Ссылка);
	
КонецПроцедуры

Процедура ОповеститьОЗавершенииОтправки(Письмо) Экспорт
	
	Свойства = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЗначенияРеквизитовОбъекта(Письмо, "Организация, Тип");
	Свойства.Вставить("Ссылка", Письмо);
	
	Оповестить("Завершение отправки в контролирующий орган", Свойства, Письмо);
	
КонецПроцедуры

Процедура РазделениеПисем(Форма, Письмо, АнализПисем = Неопределено, ИгнорироватьОшибку = Ложь) Экспорт
	
	Если АнализПисем = Неопределено Тогда
		АнализПисем = ПисьмаВГосОрганыВызовСервера.ПланируемыйМассивПисем(Письмо);
	КонецЕсли;
	
	СоответствуетТребованиям = АнализПисем.Допустимо И АнализПисем.КоличествоПисем = 1;
	Если СоответствуетТребованиям Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ АнализПисем.Допустимо Тогда
		
		Размер = АнализПисем.МаксимальныйРазмер;
		
		Ошибка = ПисьмаВГосОрганыКлиентСервер.ОшибкаНаРазмерПисьмаФНС(Размер);
			
		ПоказатьОшибкуПроверкиПисьмаФНС(
			Письмо,
			Ошибка);
			
		Возврат;
		
	КонецЕсли;
	
	Если ПисьмаВГосОрганыВызовСервера.ОтветНаТребованиеОтправляютКакПисьмо(Письмо) И НЕ ИгнорироватьОшибку Тогда 
		ПредложитьСоздатьОтветНаТребование(Форма, Письмо);
	Иначе
		РазбитьНаНесколькоПисем(Форма, Письмо, АнализПисем);
	КонецЕсли;
		
КонецПроцедуры

Процедура РазбитьНаНесколькоПисем(Форма, Письмо, АнализПисем)
	
	СписокКоманд = Новый СписокЗначений;
	СписокКоманд.Добавить("Разделить", НСтр("ru = 'Разбить на несколько писем';
											|en = 'Разбить на несколько писем'"));
	СписокКоманд.Добавить("Отмена", НСтр("ru = 'Отмена';
										|en = 'Отмена'"));
	
	Если АнализПисем.ПревышениеКоличества > 0 Тогда
		ТекстВопроса = СтрШаблон(НСтр("ru = 'Количество вложений превышено на %1. Разрешено: %2 Мб.';
										|en = 'Количество вложений превышено на %1. Разрешено: %2 Мб.'"), 
						АнализПисем.ПревышениеКоличества, АнализПисем.МаксКоличество);
	Иначе
		ТекстВопроса = СтрШаблон(НСтр("ru = 'Размер вложений превышен на %1 Мб. Разрешено: %2 Мб.';
										|en = 'Размер вложений превышен на %1 Мб. Разрешено: %2 Мб.'"), 
							АнализПисем.ПревышениеРазмера, АнализПисем.МаксКонтейнера);
	КонецЕсли;
	
	ТекстВопроса = ТекстВопроса + Символы.ПС
				+ НСтр("ru = 'Перед отправкой письмо требуется разбить на несколько.';
						|en = 'Перед отправкой письмо требуется разбить на несколько.'");

	ПараметрыЦикла = Новый Структура();
	ПараметрыЦикла.Вставить("АнализПисем", АнализПисем);
	ПараметрыЦикла.Вставить("ТекущийСчетчик", 1);
	ПараметрыЦикла.Вставить("ВсегоПисем", АнализПисем.КоличествоПисем);
	ПараметрыЦикла.Вставить("Ошибка", "");
	ПараметрыЦикла.Вставить("Письмо", Письмо);
	
	Форма.ПараметрыЦиклаРазделения = ПараметрыЦикла;
	
	НоваяОперация = Новый Структура;
	НоваяОперация.Вставить("ИдентификаторОперации", "Вопрос");
	НоваяОперация.Вставить("Наименование", ТекстВопроса);
	НоваяОперация.Вставить("ТекущееСостояние", "");
	НоваяОперация.Вставить("ТипИндикатора", 0);
	НоваяОперация.Вставить("ПрерыватьОперацию", Ложь);
	НоваяОперация.Вставить("ВсегоИндикатор", 0);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СписокКоманд", СписокКоманд);
	ПараметрыФормы.Вставить("ЗаголовокФормы", "Превышен размер письма!");
	ПараметрыФормы.Вставить("ИмяПиктограммы", "Внимание48");
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("Письмо", Письмо);
	
	ОповещениеСледующее = Новый ОписаниеОповещения("РазделениеПисемПослеВопроса", ЭтотОбъект, ДополнительныеПараметры);
	ФормаОперации = ФормаОжидания(ПараметрыФормы, ОповещениеСледующее);
	ФормаОперации.ДобавитьОперацию(НоваяОперация);

КонецПроцедуры

Процедура РазделениеПисемПослеВопроса(ОтветПользователя, ВходящийКонтекст) Экспорт
	
	Форма  = ВходящийКонтекст.Форма;
	Письмо = ВходящийКонтекст.Письмо;
	
	Если ОтветПользователя <> "Разделить" Тогда
		Возврат;
	КонецЕсли;
	
	Если ПисьмаВГосОрганыКлиентСервер.ЭтоФормаПисьма(Форма) И НЕ Форма.ПроверитьПередОтправкой() Тогда
		Возврат;
	КонецЕсли;
	
	Всего = 0;
	ВАрхиве = 0;
	ПисьмаВГосОрганыВызовСервера.ПроверитьВложения(ВходящийКонтекст.Письмо, Всего, ВАрхиве);
	
	Если ВАрхиве > 0 Тогда 
		Форма.КонтекстЭДОКлиент.ПоказатьУведомлениеАрхивныхФайлов(, 9, 3, Истина);
		Возврат;
	КонецЕсли;
	
	ОповещениеПрерывания = Новый ОписаниеОповещения("РазделениеПисемПрерывание", ЭтотОбъект, Форма);
	
	НоваяОперация = Новый Структура;
	НоваяОперация.Вставить("ИдентификаторОперации", "ФормированиеПисем");
	НоваяОперация.Вставить("Наименование", "");
	НоваяОперация.Вставить("ТекущееСостояние", НСтр("ru = 'Формирование писем';
													|en = 'Формирование писем'"));
	НоваяОперация.Вставить("ТипИндикатора", 0);
	НоваяОперация.Вставить("ПрерыватьОперацию", Истина);
	НоваяОперация.Вставить("ОповещениеЗавершения", ОповещениеПрерывания);
	НоваяОперация.Вставить("ВсегоИндикатор", 0);
	
	ФормаОперации = ФормаОжидания();
	ФормаОперации.ДобавитьОперацию(НоваяОперация);
	
	Форма.ПодключитьОбработчикОжидания("Подключаемый_РазделениеПисемОжидание", 0.1, Истина);
	
КонецПроцедуры

Процедура Подключаемый_РазделениеПисемОжидание(Форма) Экспорт
	
	ПараметрыЦиклаРазделения = Форма.ПараметрыЦиклаРазделения;
	Письмо = ПараметрыЦиклаРазделения.Письмо;
	
	Если ЗначениеЗаполнено(ПараметрыЦиклаРазделения.Ошибка) Тогда
		
		ТекстОписания = ПараметрыЦиклаРазделения.Ошибка;
		ТекстОписания = НСтр("ru = 'Возникла ошибка при разбиении писем.';
							|en = 'Возникла ошибка при разбиении писем.'") + Символы.ПС
						+ СтрШаблон(НСтр("ru = 'Было создано писем: %1.';
										|en = 'Было создано писем: %1.'"), ПараметрыЦиклаРазделения.ТекущийСчетчик - 2) + Символы.ПС
						+ ТекстОписания;
		ФормаОперации = ФормаОжидания();
		ФормаОперации.СтатусОперации("ФормированиеПисем", "", Истина);
		ПоказатьПредупреждение(, ТекстОписания);
		
	ИначеЕсли ПараметрыЦиклаРазделения.ВсегоПисем >= ПараметрыЦиклаРазделения.ТекущийСчетчик Тогда
		
		ОповещениеПрерывания = Новый ОписаниеОповещения("РазделениеПисемПослеСоздания", ЭтотОбъект, Форма);
		
		ТекущийСчетчик = ПараметрыЦиклаРазделения.ТекущийСчетчик;
		ПараметрыЦиклаРазделения.ТекущийСчетчик = ПараметрыЦиклаРазделения.ТекущийСчетчик + 1;
		
		ДлительнаяОперация = ПисьмаВГосОрганыВызовСервера.РазделениеПисемСозданиеНаСервере(
			Письмо, 
			ПараметрыЦиклаРазделения.АнализПисем, 
			ТекущийСчетчик,
			Форма.КоличествоСвязанныхПисем);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ПараметрыОжидания.ФормаВладелец = Форма;

		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеПрерывания, ПараметрыОжидания);
				
		ФормаОперации = ФормаОжидания();
		ФормаОперации.СтатусОперации("ФормированиеПисем", 
					СтрШаблон(НСтр("ru = 'Формирование письма %1 из %2';
									|en = 'Формирование письма %1 из %2'") + " ...", ТекущийСчетчик, ПараметрыЦиклаРазделения.ВсегоПисем));
		
	Иначе
		
		ФормаОперации = ФормаОжидания();
		ФормаОперации.СтатусОперации("ФормированиеПисем", "", Истина);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТекущееПисьмо", Письмо);
		
		ОповещениеСледующее = Новый ОписаниеОповещения("РазделениеПисемПослеОправки", ЭтотОбъект, Письмо);
		ОткрытьФорму("Справочник.ПерепискаСКонтролирующимиОрганами.Форма.ФормаГрупповойОтправки", ПараметрыФормы, , Письмо, , , ОповещениеСледующее);
		
		Если ПисьмаВГосОрганыКлиентСервер.ЭтоФормаПисьма(Форма) Тогда
			Форма.ПослеОткрытияФормыГрупповойОтправки();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура РазделениеПисемПослеСоздания(РезультатВызова, Форма) Экспорт
	
	Ошибка = НСтр("ru = 'Ошибка при создании письма';
					|en = 'Ошибка при создании письма'");
	Успешно = Ложь;
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатВызова, "Статус", "") = "Выполнено" Тогда
		РезультатВыполнения = ПолучитьИзВременногоХранилища(РезультатВызова.АдресРезультата);
		Успешно = РезультатВыполнения.Выполнено;
		Ошибка = РезультатВыполнения.Ошибка;
	КонецЕсли;
	
	Если НЕ Успешно Тогда
		Форма.ПараметрыЦиклаРазделения.Ошибка = Ошибка;
	КонецЕсли;
	
	Форма.Подключаемый_РазделениеПисемОжидание();
	
КонецПроцедуры

Процедура РазделениеПисемПрерывание(ВыборПользователя, Форма) Экспорт
	
	Форма.ПараметрыЦиклаРазделения.Ошибка = НСтр("ru = 'Операция была прервана.';
												|en = 'Операция была прервана.'");
	Форма.ПараметрыЦиклаРазделения.ТекущийСчетчик = Форма.ПараметрыЦиклаРазделения.ВсегоПисем + 1;
	
КонецПроцедуры

Процедура РазделениеПисемПослеОправки(РезультатОтправки, Письмо) Экспорт
	
	ОповеститьОЗавершенииОтправки(Письмо);
	
КонецПроцедуры

Функция ФормаОжидания(ПараметрыФормы = Неопределено, ОповещениеСледующее = Неопределено)
	
	КлючПоиска = "ДлительнаяОперация";
	
	ИмяФормыОжидания = "Справочник.ПерепискаСКонтролирующимиОрганами.Форма.ДлительнаяОперация"; 
	
	Результат = Неопределено;
	ВсеОкна = ПолучитьОкна();
	Для Каждого СтрокаМассива Из ВсеОкна Цикл
		Если НЕ СтрокаМассива.Основное И СтрокаМассива.Содержимое[0].КлючУникальности = КлючПоиска Тогда
			Результат = СтрокаМассива.Содержимое[0];
			Прервать;
		КонецЕсли;	
	КонецЦикла;
	
	Если Результат = Неопределено Тогда
		Результат = ОткрытьФорму(ИмяФормыОжидания, ПараметрыФормы, ЭтотОбъект, "ДлительнаяОперация", , , ОповещениеСледующее);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПриказНаРазмераПисьмаФНС5Мб()
	Возврат "ЕД-7-26/1011@";
КонецФункции

Функция ПриказНаРазмераПисьмаФНС1Мб()
	Возврат "ЕД-7-26/509@";
КонецФункции

Функция ОшибкаРазмераПисьмаФНС(Ошибки) Экспорт
	
	Приказ5Мб = ПриказНаРазмераПисьмаФНС5Мб();
	Приказ1Мб = ПриказНаРазмераПисьмаФНС1Мб();
	
	Для каждого Ошибка Из Ошибки Цикл
		Если СтрНайти(Ошибка.ОписаниеОшибки, Приказ5Мб) > 0
			ИЛИ СтрНайти(Ошибка.ОписаниеОшибки, Приказ1Мб) > 0 Тогда
			Возврат Ошибка.ОписаниеОшибки;
		КонецЕсли;
	КонецЦикла; 
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


	
#КонецОбласти