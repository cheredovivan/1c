#Область ПрограммныйИнтерфейс

// Возвращает адрес сервера.
// Параметры:
// 	Авторизация - Булево - признак работы с ключом авторизации
// Возвращаемое значение:
// 	Строка - адрес сервера.
//
Функция АдресСервера(Авторизация = Ложь) Экспорт
	
	РежимРаботыСТестовымКонтуром = ИнтеграцияСАТУРНКлиентСервер.РежимРаботыСТестовымКонтуромСАТУРН();
	
	Если РежимРаботыСТестовымКонтуром Тогда
		Возврат "demo-hs.fgis-saturn.ru";
	ИначеЕсли Авторизация Тогда
		Возврат "fgis-saturn.ru";
	Иначе
		Возврат "api.fgis-saturn.ru";
	КонецЕсли;
	
КонецФункции

// URL путь на сервере для отправки запроса.
// 
// Параметры:
// 	Авторизация - Булево - признак работы с ключом авторизации
// Возвращаемое значение:
//  Строка - URL путь.
Функция АдресРесурса(Авторизация = Ложь) Экспорт
	
	Если Авторизация Тогда
		Возврат "backend/role/newaccesstoken";
	Иначе
		Возврат "probeInnerArm/innerArm/seapi/";
	КонецЕсли;
	
КонецФункции

// Возвращает параметры для отправки HTTP запросов.
// 
// Параметры:
//  ПараметрыОптимизации - см. ИнтеграцияСАТУРНСлужебный.ПараметрыОптимизации
//  Авторизация - Булево - признак работы с ключом авторизации
// Возвращаемое значение:
//  Структура - Описание:
//   * ИспользоватьЗащищенноеСоединение - Булево - Признак использования SSL.
//   * Таймаут - Число - Таймаут соединения.
//   * Порт - Число - Порт соединения.
//   * Сервер - Строка - адрес сервера.
//   * АдресРесурса - Строка - адрес ресурса.
//   * ПредставлениеСервиса - Строка - представления сервиса.
//   * КлючАвторизации - Строка - токен авторизации
Функция ПараметрыОтправкиHTTPЗапросов(ПараметрыОптимизации = Неопределено, Авторизация = Ложь) Экспорт
	
	ПараметрыОтправкиHTTPЗапросов = Новый Структура;
	ПараметрыОтправкиHTTPЗапросов.Вставить("ПредставлениеСервиса",                НСтр("ru = 'ФГИС Сатурн';
																						|en = 'ФГИС Сатурн'"));
	ПараметрыОтправкиHTTPЗапросов.Вставить("Сервер",                              АдресСервера(Авторизация));
	ПараметрыОтправкиHTTPЗапросов.Вставить("АдресРесурса",                        АдресРесурса(Авторизация));
	ПараметрыОтправкиHTTPЗапросов.Вставить("Порт",                                443);
	ПараметрыОтправкиHTTPЗапросов.Вставить("Таймаут",                             30);
	ПараметрыОтправкиHTTPЗапросов.Вставить("ИспользоватьЗащищенноеСоединение",    Истина);
	ПараметрыОтправкиHTTPЗапросов.Вставить("КлючАвторизации",                     "");
	
	Если ПараметрыОптимизации = Неопределено Тогда
		ПараметрыОптимизации = ИнтеграцияСАТУРНСлужебный.ПараметрыОптимизации();
	КонецЕсли;
	ПараметрыОтправкиHTTPЗапросов.Таймаут = ПараметрыОптимизации.ТаймаутHTTPЗапросов;
	
	Возврат ПараметрыОтправкиHTTPЗапросов;
	
КонецФункции

#Область КонвертацияТипов

// Имя класса сущности.
// 
// Параметры:
//  Операция - ПеречислениеСсылка.ВидыОперацийСАТУРН - Операция
// 
// Возвращаемое значение:
//  Строка - Имя класса сущности
Функция ИмяКлассаСущности(Операция) Экспорт
	
	Если Операция = Перечисления.ВидыОперацийСАТУРН.АктИнвентаризацииСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.АктИнвентаризацииИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.АктИнвентаризацииПринятиеКУчету
		Или Операция = Перечисления.ВидыОперацийСАТУРН.АктИнвентаризацииЧтениеДокумента Тогда
		
		Возврат "Inventorization";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЧтениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПеревозка
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяРозничнаяТорговля
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяСписаниеВПроизводство
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяУтилизация
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеПриемки
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеЧастичнойПриемки
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеЧтение
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяВозврат
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяВозвратИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяГрузУтерян
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяГрузУтерянИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеВозврата
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеЧастичногоВозврата
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеВозвратаИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеУтериГрузаПриВозврате
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеУтериГрузаПриВозвратеИзменениеДокумента
		
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЗагрузкаДокументов
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЗагрузкаДокументовЧтение
		
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеАннулирование
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортЧерезПунктДосмотраАннулирование
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПеревозкаАннулирование
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяСписаниеВПроизводствоАннулирование
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяУтилизацияАннулирование
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяУтилизацияПодтверждение
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяУтилизацияАннулированиеПодтверждения
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяРозничнаяТорговляАннулирование
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяГрузУтерянАннулирование
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеЧастичнойПриемкиАннулирование
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеЧастичногоВозвратаАннулирование
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеВозвратаАннулирование
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеУтериГрузаПриВозвратеАннулирование
		
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЗагрузкаСтатусов
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЗагрузкаСтатусовЧтение
		
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортЧерезПунктДосмотра
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортПодтверждениеПриемаНаДосмотр
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортПодтверждениеПриемаНаДосмотрИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортПодтверждение
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортПодтверждениеИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортОтказ
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортОтказИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеУтериГрузаПриВозвратеЧастично Тогда
		
		Возврат "Invoice";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ПланПримененияСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПланПримененияИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПланПримененияПринятиеКУчету
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПланПримененияАннулирование Тогда
		
		Возврат "PatUsagePlanRec";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.АктПримененияСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.АктПримененияИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.АктПримененияПринятиеКУчету
		Или Операция = Перечисления.ВидыОперацийСАТУРН.АктПримененияАннулирование Тогда
		
		Возврат "PatUsage";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ПроизводственнаяОперацияСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПроизводственнаяОперацияИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПроизводственнаяОперацияЧтениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПроизводственнаяОперацияПринятиеКУчету
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПроизводственнаяОперацияАннулирование Тогда
		
		Возврат "PatProductionAct";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ЗапросОстатковПартий
		Или Операция = Перечисления.ВидыОперацийСАТУРН.АктИнвентаризацииЧтениеАктуальногоОстатка Тогда
		
		Возврат "WarehouseStates";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.МестоХраненияЗапросКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.МестоХраненияСозданиеКлассификатора Тогда
		
		Возврат "Warehouse";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияЗапросКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияСозданиеКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияИзменениеКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияСменаСтатусаИзЧерновикаВОтменено
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияСменаСтатусаИзЧерновикаВАктуально Тогда

		Возврат "Contractor";

	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ПАТСозданиеКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПАТИзменениеКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПАТСменаСтатусаИзЧерновикаВОтменено
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПАТСменаСтатусаИзЧерновикВАктуально
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПАТЗапросКлассификатора Тогда
		
		Возврат "PatProduct";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортируемаяПартияЗапросКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПодтверждениеПриемки
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииОтменаПриемки
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспорт
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортАннулирование
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭССозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСПринятиеКУчету Тогда
		
		Возврат "PAImportRecord";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ПартияЗапросКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ЗапросОстатковПартийЗапросРодительскихПартий Тогда
		
		Возврат "PATBatch";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.МестоПримененияЗапросКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.МестоПримененияСозданиеКлассификатора Тогда
		
		Возврат "Acreage";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ЗагрузкаКлассификаторов
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеПринятиеКУчету Тогда
		
		Возврат "PredInfo";
		
	Иначе
		
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Внутренняя ошибка. Не обработана операция получения имени класса сущности %1';
										|en = 'Внутренняя ошибка. Не обработана операция получения имени класса сущности %1'"), Операция);
		
	КонецЕсли;
	
КонецФункции

// Имя операции.
// 
// Параметры:
//  Операция - ПеречислениеСсылка.ВидыОперацийСАТУРН - Операция
// 
// Возвращаемое значение:
//  Строка - Имя операции
Функция ИмяОперации(Операция) Экспорт
	
	Если Операция = Перечисления.ВидыОперацийСАТУРН.АктИнвентаризацииСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПланПримененияСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.АктПримененияСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПроизводственнаяОперацияСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПАТСозданиеКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияСозданиеКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.МестоХраненияСозданиеКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.МестоПримененияСозданиеКлассификатора Тогда
		
		Возврат "static/createNew()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортСозданиеДокумента Тогда
		
		Возврат "static/createNewExpRec()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.АктИнвентаризацииИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПланПримененияИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.АктПримененияИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПроизводственнаяОперацияИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПАТИзменениеКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияИзменениеКлассификатора Тогда
	
		Возврат "draft/updateCard()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.АктИнвентаризацииПринятиеКУчету
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПланПримененияПринятиеКУчету
		Или Операция = Перечисления.ВидыОперацийСАТУРН.АктПримененияПринятиеКУчету
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПроизводственнаяОперацияПринятиеКУчету Тогда
		
		Возврат "draft/setActual()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ЗапросОстатковПартий
		Или Операция = Перечисления.ВидыОперацийСАТУРН.АктИнвентаризацииЧтениеАктуальногоОстатка Тогда
		
		Возврат "static/getTotals()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.АктИнвентаризацииЧтениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПроизводственнаяОперацияЧтениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЧтениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеЧтение
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЗагрузкаДокументовЧтение
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЗагрузкаСтатусовЧтение Тогда
		
		Возврат "any/getFullCard()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПеревозка Тогда
		
		Возврат "draft/doSend()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяРозничнаяТорговля Тогда
		
		Возврат "draft/doSendToRetale()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяУтилизация Тогда
		
		Возврат "draft/doSendToDestroy()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяСписаниеВПроизводство Тогда
		
		Возврат "draft/doSendToIndustrial()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспорт Тогда
		
		Возврат "static/sendExport()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортАннулирование Тогда
		
		Возврат "static/cancelExport()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеПриемки Тогда
		
		Возврат "onWay/notifyDelivered()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяВозвратИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяГрузУтерянИзменениеДокумента Тогда
		
		Возврат "onWay/updateAcceptedCounts()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяВозврат Тогда
		
		Возврат "onWay/sendBack()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяГрузУтерян Тогда
		
		Возврат "onWay/notifyLost()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяГрузУтерянАннулирование Тогда
		
		Возврат "lostOnWay/undoNotifyLost()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеУтериГрузаПриВозвратеАннулирование Тогда
		
		Возврат "lostOnReturning/undoNotifyLost()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеВозврата Тогда
		
		Возврат "onReturning/notifyDelivered()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеВозвратаАннулирование Тогда
		
		Возврат "returned/undoNotifyDelivered()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеВозвратаИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеУтериГрузаПриВозвратеИзменениеДокумента  Тогда
		
		Возврат "onReturning/updateReturnedCounts()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеУтериГрузаПриВозврате Тогда
		
		Возврат "onReturning/notifyLost()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеЧастичногоВозврата Тогда
		
		Возврат "onPartialReturning/notifyDelivered()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеЧастичногоВозвратаАннулирование Тогда
		
		Возврат "partialReturned/undoNotifyDelivered()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеУтериГрузаПриВозвратеЧастично Тогда
		
		Возврат "onPartialReturning/notifyLost()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеЧастичнойПриемки Тогда
		
		Возврат "onWay/partiallyAcceptAndSendBack()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеЧастичнойПриемкиАннулирование Тогда
		
		Возврат "onPartialReturning/undoSendPartialBack()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортЧерезПунктДосмотра Тогда
		
		Возврат "draft/doSendToExportInspection()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортПодтверждениеПриемаНаДосмотр Тогда
		
		Возврат "onWayToExportInspection/notifyDeliveredToInspection()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортПодтверждениеПриемаНаДосмотрИзменениеДокумента Тогда
		
		Возврат "onWayToExportInspection/updateAcceptedCounts()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортПодтверждение Тогда
		
		Возврат "onExportInspection/notifyExport()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортПодтверждениеИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортОтказИзменениеДокумента Тогда
		
		Возврат "onExportInspection/updateAcceptedCounts()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортОтказ Тогда
		
		Возврат "onExportInspection/refuseToExport()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ПАТСменаСтатусаИзЧерновикаВОтменено
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияСменаСтатусаИзЧерновикаВОтменено Тогда
		
		Возврат "draft/setCanceled()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ПАТСменаСтатусаИзЧерновикВАктуально
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияСменаСтатусаИзЧерновикаВАктуально Тогда
		
		Возврат "draft/setActual()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзменениеДокумента Тогда
		
		Возврат "imported/setWarehouse()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПодтверждениеПриемки Тогда
		
		Возврат "imported/doDelivered()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииОтменаПриемки Тогда
		
		Возврат "imported/setCanceled()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭССозданиеДокумента Тогда
		
		Возврат "static/createNewImpRecEAEU()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСИзменениеДокумента Тогда
		
		Возврат "static/updateImpRecEAEU()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСПринятиеКУчету Тогда
		
		Возврат "static/doImportedEAEU()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеСозданиеДокумента Тогда
		
		Возврат "static/createNewPredInfo()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеИзменениеДокумента Тогда
		
		Возврат "static/updatePredInfo()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеПринятиеКУчету Тогда
		
		Возврат "draft/doSend()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияЗапросКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортируемаяПартияЗапросКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПартияЗапросКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПАТЗапросКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.МестоПримененияЗапросКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.МестоХраненияЗапросКлассификатора
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ЗапросОстатковПартийЗапросРодительскихПартий
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЗагрузкаДокументов
		Или Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЗагрузкаСтатусов Тогда
		
		Возврат "static/getList()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.АктПримененияАннулирование
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПроизводственнаяОперацияАннулирование
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ПланПримененияАннулирование Тогда
		
		Возврат "actual/undoSetActual()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПодтверждениеАннулирование Тогда
		
		Возврат "delivered/undoNotifyDelivered()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяСписаниеВПроизводствоАннулирование Тогда
		
		Возврат "onIndustrSale/undoDoSendToIndustrial()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяЭкспортЧерезПунктДосмотраАннулирование Тогда
		
		Возврат "onWayToExportInspection/undoSendToExport()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяПеревозкаАннулирование Тогда
		
		Возврат "onWay/undoDoSend()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяРозничнаяТорговляАннулирование Тогда
		
		Возврат "onRetailSale/undoDoSendToRetale()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяУтилизацияАннулирование Тогда
		
		Возврат "onDestroy/undoDoSendToDestroy()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяУтилизацияПодтверждение Тогда
		
		Возврат "onDestroy/doDestroyed()";
	
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.НакладнаяУтилизацияАннулированиеПодтверждения Тогда
		
		Возврат "destroyed/undoDoDestroyed()";
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ЗагрузкаКлассификаторов Тогда
		
		Возврат "static/getNsi()";
		
	Иначе
		
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Внутренняя ошибка. Не обработано имя операции %1';
										|en = 'Внутренняя ошибка. Не обработано имя операции %1'"), Операция);
		
	КонецЕсли;
	
КонецФункции

// Способ применения ПАТ.
// 
// Параметры:
//  СпособПрименения - ПеречислениеСсылка.СпособыПримененияПАТСАТУРН - 
// 
// Возвращаемое значение:
//  Строка, ПеречислениеСсылка.СпособыПримененияПАТСАТУРН - Способ применения ПАТ
Функция СпособПримененияПАТ(СпособПрименения) Экспорт
	
	Если СпособПрименения = Перечисления.СпособыПримененияПАТСАТУРН.АвиационноеПрименениеПестицидовИАгрохимикатов Тогда
		Возврат 2;
	ИначеЕсли СпособПрименения = Перечисления.СпособыПримененияПАТСАТУРН.НаземноеПрименениеПестицидовИАгрохимикатов Тогда
		Возврат 1;
	ИначеЕсли СпособПрименения = Перечисления.СпособыПримененияПАТСАТУРН.ОбработкаПолейПестицидами Тогда
		Возврат 4;
	ИначеЕсли СпособПрименения = Перечисления.СпособыПримененияПАТСАТУРН.ОбработкаСтроенийПестицидами Тогда
		Возврат 3;
	ИначеЕсли СпособПрименения = Перечисления.СпособыПримененияПАТСАТУРН.ОбработкаТранспортныхСредствПестицидами Тогда
		Возврат 4;
	ИначеЕсли СпособПрименения = Перечисления.СпособыПримененияПАТСАТУРН.ПрименениеПестицидовИАгрохимикатов Тогда
		Возврат 6;
	
	ИначеЕсли СпособПрименения = 2 Тогда
		Возврат Перечисления.СпособыПримененияПАТСАТУРН.АвиационноеПрименениеПестицидовИАгрохимикатов;
	ИначеЕсли СпособПрименения = 1 Тогда
		Возврат Перечисления.СпособыПримененияПАТСАТУРН.НаземноеПрименениеПестицидовИАгрохимикатов;
	ИначеЕсли СпособПрименения = 4 Тогда
		Возврат Перечисления.СпособыПримененияПАТСАТУРН.ОбработкаПолейПестицидами;
	ИначеЕсли СпособПрименения = 3 Тогда
		Возврат Перечисления.СпособыПримененияПАТСАТУРН.ОбработкаСтроенийПестицидами;
	ИначеЕсли СпособПрименения = 4 Тогда
		Возврат Перечисления.СпособыПримененияПАТСАТУРН.ОбработкаТранспортныхСредствПестицидами;
	ИначеЕсли СпособПрименения = 6 Тогда
		Возврат Перечисления.СпособыПримененияПАТСАТУРН.ПрименениеПестицидовИАгрохимикатов;
	Иначе
		
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Внутренняя ошибка. Неизвестный способ применения ПАТ %1';
										|en = 'Внутренняя ошибка. Неизвестный способ применения ПАТ %1'"), СпособПрименения);
		
	КонецЕсли;
	
КонецФункции

// Тип места применения.
// 
// Параметры:
//  Значение - ПеречислениеСсылка.ТипыМестПримененияСАТУРН - 
// 
// Возвращаемое значение:
//  Строка, ПеречислениеСсылка.ТипыМестПримененияСАТУРН - Тип места применения
Функция ТипМестаПрименения(Значение) Экспорт
	
	Если Значение = Перечисления.ТипыМестПримененияСАТУРН.ЗарегистрованноеМестоПрименения Тогда
		Возврат "[зарегистрированные места применения]";
	ИначеЕсли Значение = Перечисления.ТипыМестПримененияСАТУРН.Суда Тогда
		Возврат "Суда";
	ИначеЕсли Значение = Перечисления.ТипыМестПримененияСАТУРН.АвтомобилиАвтомобильныеПрицепы Тогда
		Возврат "Автомобили/автомобильные прицепы";
	ИначеЕсли Значение = Перечисления.ТипыМестПримененияСАТУРН.Вагоны Тогда
		Возврат "Вагоны";
	ИначеЕсли Значение = Перечисления.ТипыМестПримененияСАТУРН.ВоздушныйТранспорт Тогда
		Возврат "Воздушный транспорт";
	ИначеЕсли Значение = Перечисления.ТипыМестПримененияСАТУРН.СкладскиеПомещения Тогда
		Возврат "(иные) Складские помещения (склады, хранилища, силосы)";
	ИначеЕсли Значение = Перечисления.ТипыМестПримененияСАТУРН.ПроизводственныеПомещения Тогда
		Возврат "(иные) Производственные помещения";
	ИначеЕсли Значение = Перечисления.ТипыМестПримененияСАТУРН.ЗемлиСельскохозяйственногоНазначения Тогда
		Возврат "Земли несельскохозяйственного назначения";
	ИначеЕсли Значение = Перечисления.ТипыМестПримененияСАТУРН.Иное Тогда
		Возврат "Иное";
	ИначеЕсли Значение = Перечисления.ТипыМестПримененияСАТУРН.Здание Тогда
		Возврат "Здание";
	ИначеЕсли Значение = Перечисления.ТипыМестПримененияСАТУРН.ГрузовойОтсекТранспортногоСудна Тогда
		Возврат "Грузовой отсек транспортного судна";
	ИначеЕсли Значение = Перечисления.ТипыМестПримененияСАТУРН.ПрочиеЗемельныеУчастки Тогда
		Возврат "Прочие земельные участки";
	
	ИначеЕсли Значение = "[зарегистрированные места применения]" Тогда
		Возврат Перечисления.ТипыМестПримененияСАТУРН.ЗарегистрованноеМестоПрименения;
	ИначеЕсли Значение = "Суда" Тогда
		Возврат Перечисления.ТипыМестПримененияСАТУРН.Суда;
	ИначеЕсли Значение = "Автомобили/автомобильные прицепы" Тогда
		Возврат Перечисления.ТипыМестПримененияСАТУРН.АвтомобилиАвтомобильныеПрицепы;
	ИначеЕсли Значение = "Вагоны" Тогда
		Возврат Перечисления.ТипыМестПримененияСАТУРН.Вагоны;
	ИначеЕсли Значение = "Воздушный транспорт" Тогда
		Возврат Перечисления.ТипыМестПримененияСАТУРН.ВоздушныйТранспорт;
	ИначеЕсли Значение = "(иные) Складские помещения (склады, хранилища, силосы)" Тогда
		Возврат Перечисления.ТипыМестПримененияСАТУРН.СкладскиеПомещения;
	ИначеЕсли Значение = "(иные) Производственные помещения" Тогда
		Возврат Перечисления.ТипыМестПримененияСАТУРН.ПроизводственныеПомещения;
	ИначеЕсли Значение = "Земли несельскохозяйственного назначения" Тогда
		Возврат Перечисления.ТипыМестПримененияСАТУРН.ЗемлиСельскохозяйственногоНазначения;
	ИначеЕсли Значение = "Иное" Тогда
		Возврат Перечисления.ТипыМестПримененияСАТУРН.Иное;
	ИначеЕсли Значение = "Здание" Тогда
		Возврат Перечисления.ТипыМестПримененияСАТУРН.Здание;
	ИначеЕсли Значение = "Грузовой отсек транспортного судна" Тогда
		Возврат Перечисления.ТипыМестПримененияСАТУРН.ГрузовойОтсекТранспортногоСудна;
	ИначеЕсли Значение = "Прочие земельные участки" Тогда
		Возврат Перечисления.ТипыМестПримененияСАТУРН.ПрочиеЗемельныеУчастки;
	
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

// Возвращает статус версионного объекта по коду
//
// Параметры:
//  Значение - Число - код статуса
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбъектовСАТУРН - статус
//
Функция Статус(Значение) Экспорт
	
	Статус = Неопределено;
	
	Если Значение = "actual" Тогда
		Статус = Перечисления.СтатусыОбъектовСАТУРН.Актуально;
	ИначеЕсли Значение = "archived" Тогда
		Статус = Перечисления.СтатусыОбъектовСАТУРН.Архив;
	ИначеЕсли Значение = "canceled" Тогда
		Статус = Перечисления.СтатусыОбъектовСАТУРН.Отменен;
	ИначеЕсли Значение = "draft" Тогда
		Статус = Перечисления.СтатусыОбъектовСАТУРН.Черновик;
	ИначеЕсли Значение = "approval" Тогда
		Статус = Перечисления.СтатусыОбъектовСАТУРН.НаСогласовании;
	
	ИначеЕсли Значение = Перечисления.СтатусыОбъектовСАТУРН.Актуально Тогда
		Статус = "actual";
	ИначеЕсли Значение = Перечисления.СтатусыОбъектовСАТУРН.Архив Тогда
		Статус = "archived";
	ИначеЕсли Значение = Перечисления.СтатусыОбъектовСАТУРН.Отменен Тогда
		Статус = "canceled";
	ИначеЕсли Значение = Перечисления.СтатусыОбъектовСАТУРН.Черновик Тогда
		Статус = "draft";
	ИначеЕсли Значение = Перечисления.СтатусыОбъектовСАТУРН.НаСогласовании Тогда
		Статус = "approval";
		
	Иначе
		Статус = Перечисления.СтатусыОбъектовСАТУРН.ПустаяСсылка();
		ОбщегоНазначения.СообщитьПользователю(
			СтрШаблон(
				НСтр("ru = 'Неизвестный статус с кодом: %1';
					|en = 'Неизвестный статус с кодом: %1'"),
				Значение));
	КонецЕсли;
	
	Возврат Статус;
	
КонецФункции

// Статус импорта продукции.
// 
// Параметры:
//  Значение - ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН, Строка - Статус импорта продукции
Функция СтатусИмпортаПродукции(Значение) Экспорт
	
	Статус = Неопределено;
	
	Если Значение = "received" Тогда
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено;
	ИначеЕсли Значение = "imported" Тогда
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ввезено;
	ИначеЕсли Значение = "canceled" Тогда
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Отменен;
	ИначеЕсли Значение = "archived" Тогда
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Архив;
	ИначеЕсли Значение = "draft" Тогда
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Черновик;
	
	ИначеЕсли Значение = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено Тогда
		Статус = "received";
	ИначеЕсли Значение = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ввезено Тогда
		Статус = "imported";
	ИначеЕсли Значение = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Отменен Тогда
		Статус = "canceled";
	ИначеЕсли Значение = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Архив Тогда
		Статус = "archived";
	ИначеЕсли Значение = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Черновик Тогда
		Статус = "draft";
		
	Иначе
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПустаяСсылка();
		ОбщегоНазначения.СообщитьПользователю(
			СтрШаблон(
				НСтр("ru = 'Неизвестный статус с кодом: %1';
					|en = 'Неизвестный статус с кодом: %1'"),
				Значение));
	КонецЕсли;
	
	Возврат Статус;
	
КонецФункции

// Статус накладной.
// 
// Параметры:
//  Значение - ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН, ПеречислениеСсылка.СтатусыОбработкиНакладнойСАТУРН - Статус накладной
Функция СтатусНакладной(Значение) Экспорт
	
	Статус = Неопределено;
	
	Если Значение = "onWay" Тогда
		Статус = Перечисления.СтатусыОбработкиНакладнойСАТУРН.ВПути;
	ИначеЕсли Значение = "onReturning" Тогда
		Статус = Перечисления.СтатусыОбработкиНакладнойСАТУРН.КВозврату;
	ИначеЕсли Значение = "returned" Тогда
		Статус = Перечисления.СтатусыОбработкиНакладнойСАТУРН.Возвращен;
	ИначеЕсли Значение = "lost" Тогда
		Статус = Перечисления.СтатусыОбработкиНакладнойСАТУРН.Утрачен;
	ИначеЕсли Значение = "delivered" Тогда
		Статус = Перечисления.СтатусыОбработкиНакладнойСАТУРН.Получен;
	ИначеЕсли Значение = "onRetailSale" Тогда
		Статус = Перечисления.СтатусыОбработкиНакладнойСАТУРН.ОтправленВРозничнуюТорговлю;
	ИначеЕсли Значение = "onIndustrSale" Тогда
		Статус = Перечисления.СтатусыОбработкиНакладнойСАТУРН.СписанВПромышленность;
	ИначеЕсли Значение = "onExportSent" Тогда
		Статус = Перечисления.СтатусыОбработкиНакладнойСАТУРН.НаправленНаЭкспорт;
	ИначеЕсли Значение = "onDestroy" Тогда
		Статус = Перечисления.СтатусыОбработкиНакладнойСАТУРН.ОтправленНаУтилизацию;
	ИначеЕсли Значение = "canceled" Тогда
		Статус = Перечисления.СтатусыОбработкиНакладнойСАТУРН.Отменен;
	ИначеЕсли Значение = "draft" Тогда
		Статус = Перечисления.СтатусыОбработкиНакладнойСАТУРН.Черновик;
	ИначеЕсли Значение = "archived" Тогда
		Статус = Перечисления.СтатусыОбработкиНакладнойСАТУРН.Архив;
	ИначеЕсли Значение = "onPartialReturning" Тогда
		Статус = Перечисления.СтатусыОбработкиНакладнойСАТУРН.КВозвратуЧастично;
	Иначе
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПустаяСсылка();
		ОбщегоНазначения.СообщитьПользователю(
			СтрШаблон(
				НСтр("ru = 'Неизвестный статус с кодом: %1';
					|en = 'Неизвестный статус с кодом: %1'"),
				Значение));
	КонецЕсли;
	
	Возврат Статус;
	
КонецФункции

// Статус импортируемой продукции.
// 
// Параметры:
//  Значение - ПеречислениеСсылка.СтатусыОбъектовСАТУРН, Строка -
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбъектовСАТУРН, Строка - Статус импортируемой продукции
Функция СтатусИмпортируемойПродукции(Значение) Экспорт
	
	Статус = Неопределено;
	
	Если Значение = "received"
		Или Значение = "delivered"
		Или Значение = "eac" Тогда
		Статус = Перечисления.СтатусыОбъектовСАТУРН.Актуально;
	ИначеЕсли Значение = "imported"
		Или Значение = "drafteac" Тогда
		Статус = Перечисления.СтатусыОбъектовСАТУРН.Черновик;
	ИначеЕсли Значение = "canceled" Тогда
		Статус = Перечисления.СтатусыОбъектовСАТУРН.Отменен;
	ИначеЕсли Значение = "archived" Тогда
		Статус = Перечисления.СтатусыОбъектовСАТУРН.Архив;
	
	ИначеЕсли Значение = Перечисления.СтатусыОбъектовСАТУРН.Актуально Тогда
		Статус = "delivered";
	ИначеЕсли Значение = Перечисления.СтатусыОбъектовСАТУРН.Черновик Тогда
		Статус = "imported";
	ИначеЕсли Значение = Перечисления.СтатусыОбъектовСАТУРН.Отменен Тогда
		Статус = "canceled";
	ИначеЕсли Значение = Перечисления.СтатусыОбъектовСАТУРН.Архив Тогда
		Статус = "archived";
		
	Иначе
		Статус = Перечисления.СтатусыОбъектовСАТУРН.ПустаяСсылка();
		ОбщегоНазначения.СообщитьПользователю(
			СтрШаблон(
				НСтр("ru = 'Неизвестный статус с кодом: %1';
					|en = 'Неизвестный статус с кодом: %1'"),
				Значение));
	КонецЕсли;
	
	Возврат Статус;
	
КонецФункции

// Вид продукции.
// 
// Параметры:
//  Значение - Строка, ПеречислениеСсылка.ВидыПродукцииСАТУРН -
// 
// Возвращаемое значение:
//  Неопределено, Строка, ПеречислениеСсылка.ВидыПродукцииСАТУРН - Вид продукции
Функция ВидПродукции(Значение) Экспорт
	
	ВозвращаемоеЗначение = Неопределено;
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Если Значение = "P" Тогда
		ВозвращаемоеЗначение = Перечисления.ВидыПродукцииСАТУРН.Пестицид;
	ИначеЕсли Значение = "A" Тогда
		ВозвращаемоеЗначение = Перечисления.ВидыПродукцииСАТУРН.Агрохимикат;
	ИначеЕсли Значение = "T" Тогда
		ВозвращаемоеЗначение = Перечисления.ВидыПродукцииСАТУРН.Тукосмесь;
	ИначеЕсли Значение = "K" Тогда
		ВозвращаемоеЗначение = Перечисления.ВидыПродукцииСАТУРН.Комбипак;
	
	ИначеЕсли Значение = Перечисления.ВидыПродукцииСАТУРН.Пестицид Тогда
		ВозвращаемоеЗначение = "P";
	ИначеЕсли Значение = Перечисления.ВидыПродукцииСАТУРН.Агрохимикат Тогда
		ВозвращаемоеЗначение = "A";
	ИначеЕсли Значение = Перечисления.ВидыПродукцииСАТУРН.Тукосмесь Тогда
		ВозвращаемоеЗначение = "T";
	ИначеЕсли Значение = Перечисления.ВидыПродукцииСАТУРН.Комбипак Тогда
		ВозвращаемоеЗначение = "K";
	
	Иначе
		ОбщегоНазначения.СообщитьПользователю(
			СтрШаблон(
				НСтр("ru = 'Неизвестный тип ПАТ: %1';
					|en = 'Неизвестный тип ПАТ: %1'"),
				Значение));
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Тип измеряемой величины.
// 
// Параметры:
//  Значение - Строка, ПеречислениеСсылка.ТипыИзмеряемыхВеличинСАТУРН -
// 
// Возвращаемое значение:
//  Неопределено, Строка, ПеречислениеСсылка.ТипыИзмеряемыхВеличинСАТУРН - Тип измеряемой величины
Функция ТипИзмеряемойВеличины(Значение) Экспорт
	
	ВозвращаемоеЗначение = Неопределено;
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Если Значение = Перечисления.ТипыИзмеряемыхВеличинСАТУРН.Вес Тогда
		ВозвращаемоеЗначение = "кг";
	ИначеЕсли Значение = Перечисления.ТипыИзмеряемыхВеличинСАТУРН.Объем Тогда
		ВозвращаемоеЗначение = "л";
		
	ИначеЕсли ВРег(Значение) = "KG" Или ВРег(Значение) = "КГ" Тогда
		ВозвращаемоеЗначение = Перечисления.ТипыИзмеряемыхВеличинСАТУРН.Вес;
	ИначеЕсли Значение = "L" Или ВРег(Значение) = "Л" Тогда
		ВозвращаемоеЗначение = Перечисления.ТипыИзмеряемыхВеличинСАТУРН.Объем;
	
	Иначе
		ОбщегоНазначения.СообщитьПользователю(
			СтрШаблон(
				НСтр("ru = 'Неизвестный тип измеряемой величины: %1';
					|en = 'Неизвестный тип измеряемой величины: %1'"),
				Значение));
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Вид используемой территории - в собственности или в аренде.
// 
// Параметры:
//  Значение - Строка, ПеречислениеСсылка.ВидыИспользуемыхТерриторийСАТУРН -
// 
// Возвращаемое значение:
//  Неопределено, Строка, ПеречислениеСсылка.ВидыИспользуемыхТерриторийСАТУРН - Вид места хранения.
Функция ВидИспользуемойТерритории(Значение) Экспорт
	
	ВозвращаемоеЗначение = Неопределено;
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Если Значение = Перечисления.ВидыИспользуемыхТерриторийСАТУРН.ВПользовании Тогда
		ВозвращаемоеЗначение = "rent";
	ИначеЕсли Значение = Перечисления.ВидыИспользуемыхТерриторийСАТУРН.ВСобственности Тогда
		ВозвращаемоеЗначение = "own";
	
	ИначеЕсли ВРег(Значение) = "RENT" Тогда
		ВозвращаемоеЗначение = Перечисления.ВидыИспользуемыхТерриторийСАТУРН.ВПользовании;
	ИначеЕсли ВРег(Значение) = "OWN" Тогда
		ВозвращаемоеЗначение = Перечисления.ВидыИспользуемыхТерриторийСАТУРН.ВСобственности;
	
	Иначе
		ОбщегоНазначения.СообщитьПользователю(
			СтрШаблон(
				НСтр("ru = 'Неизвестный вид места хранения: %1';
					|en = 'Неизвестный вид места хранения: %1'"),
				Значение));
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Дата из строки ISO.
// 
// Параметры:
//  Значение - Строка
// 
// Возвращаемое значение:
//  Дата - Дата из строки ISO
Функция ДатаИзСтрокиISO(ЗНАЧ Значение) Экспорт
	
	Значение = СокрЛП(Значение);
	
	ДлинаСтроки = СтрДлина(Значение);
	ЕстьТ       = (СтрНайти(Значение, "T") > 0);
	ДатаЧислом  = ОбщегоНазначения.ОписаниеТипаЧисло(15, 0, ДопустимыйЗнак.Неотрицательный).ПривестиЗначение(Значение);
	Результат   = '00010101';
	
	Если ЗначениеЗаполнено(Значение)
		И ДлинаСтроки < 25 Тогда
		Если ДлинаСтроки = 19 И ЕстьТ Тогда
			Результат = ПрочитатьДатуJSON(Значение, ФорматДатыJSON.ISO);
		ИначеЕсли ДлинаСтроки = 16 И ЕстьТ > 0 И Прав(Значение, 6) = "T00:00" Тогда
			Результат = ПрочитатьДатуJSON(Значение + ":00", ФорматДатыJSON.ISO);
		ИначеЕсли ДлинаСтроки = 17 И ЕстьТ > 0 И Прав(Значение, 1) = "Z" Тогда
			Результат = ПрочитатьДатуJSON(Лев(Значение, СтрДлина(Значение) - 1) + ":00Z", ФорматДатыJSON.ISO);
		ИначеЕсли ДатаЧислом > 1000000000000 Тогда // длина числа 14 и более
			Результат = ОбщегоНазначенияИСКлиентСервер.ДатаИзСтрокиUNIX(ДатаЧислом);
		ИначеЕсли ДатаЧислом > 1000000000 Тогда // длина числа 10 и более
			Результат = ОбщегоНазначенияИСКлиентСервер.ДатаИзСтрокиUNIX(ДатаЧислом, 1);
		Иначе
			Результат = ОбщегоНазначенияИС.ДатаИзСтрокиISO(СтрЗаменить(Значение, "+", ":00+"));
			Если Не ЗначениеЗаполнено(Результат) Тогда
				//@skip-check empty-except-statement
				Попытка
					Результат = ПрочитатьДатуJSON(Значение, ФорматДатыJSON.ISO);
				Исключение
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли СтрДлина(Значение) = 25 Тогда
		Результат = ОбщегоНазначенияИС.ДатаИзСтрокиISO(Значение);
	ИначеЕсли ЕстьТ Тогда
		Результат = ПрочитатьДатуJSON(Значение, ФорматДатыJSON.ISO);
	Иначе
		Результат = '00010101';
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Тип хозяйствующего субъекта.
// 
// Параметры:
//  Значение - ПеречислениеСсылка.ТипыОрганизацийСАТУРН
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыОрганизацийСАТУРН, Неопределено - Тип хозяйствующего субъекта
Функция ТипХозяйствующегоСубъекта(Значение) Экспорт
	
	Если Не ЗначениеЗаполнено(Значение)
		Или Значение = "0" Тогда
		Возврат Перечисления.ТипыОрганизацийСАТУРН.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ИнтеграцияСАТУРН.ТипОрганизации(Число(Значение));
	
КонецФункции

// Данные организации.
// 
// Параметры:
//  ЭлементДанных - Структура
// 
// Возвращаемое значение:
//  см. Справочники.КлассификаторОрганизацийСАТУРН.ДанныеОбъекта
Функция ДанныеОрганизации(ЭлементДанных) Экспорт
	
	Возврат Справочники.КлассификаторОрганизацийСАТУРН.ДанныеОбъекта(ЭлементДанных);
	
КонецФункции

// Данные места хранения.
// 
// Параметры:
//  ЭлементДанных - Структура
// 
// Возвращаемое значение:
//  см. Справочники.МестаХраненияСАТУРН.ДанныеОбъекта
Функция ДанныеМестаХранения(ЭлементДанных) Экспорт
	
	Возврат Справочники.МестаХраненияСАТУРН.ДанныеОбъекта(ЭлементДанных);
	
КонецФункции

// Данные места применения.
// 
// Параметры:
//  ЭлементДанных - Структура
// 
// Возвращаемое значение:
//  см. Справочники.МестаПримененияСАТУРН.ДанныеОбъекта
Функция ДанныеМестаПрименения(ЭлементДанных) Экспорт
	
	Возврат Справочники.МестаПримененияСАТУРН.ДанныеОбъекта(ЭлементДанных);
	
КонецФункции

// Данные ПАТ.
// 
// Параметры:
//  ЭлементДанных - Структура
// 
// Возвращаемое значение:
//  см. Справочники.КлассификаторПАТСАТУРН.ДанныеОбъекта
Функция ДанныеПАТ(ЭлементДанных) Экспорт
	
	Возврат Справочники.КлассификаторПАТСАТУРН.ДанныеОбъекта(ЭлементДанных);
	
КонецФункции

// Данные партии.
// 
// Параметры:
//  ЭлементДанных - Структура
// 
// Возвращаемое значение:
//  см. Справочники.ПартииСАТУРН.ДанныеОбъекта
Функция ДанныеПартии(ЭлементДанных) Экспорт

	Возврат Справочники.ПартииСАТУРН.ДанныеОбъекта(ЭлементДанных);

КонецФункции

// Данные импортируемой партии.
// 
// Параметры:
//  ЭлементДанных - Структура
// 
// Возвращаемое значение:
//  см. Справочники.ИмпортируемаяПартияСАТУРН.ДанныеОбъекта
Функция ДанныеИмпортируемойПартии(ЭлементДанных) Экспорт
	
	Возврат Справочники.ИмпортируемаяПартияСАТУРН.ДанныеОбъекта(ЭлементДанных);
	
КонецФункции

// Данные классификатора страны.
// 
// Параметры:
//  ЭлементДанных - Структура
// 
// Возвращаемое значение:
//  см. Справочники.КлассификаторСтранСАТУРН.ДанныеОбъекта
Функция ДанныеКлассификатораСтраны(ЭлементДанных) Экспорт
	
	Возврат Справочники.КлассификаторСтранСАТУРН.ДанныеОбъекта(ЭлементДанных);
	
КонецФункции

// Данные классификатора транспортного средства.
// 
// Параметры:
//  ЭлементДанных - Структура
// 
// Возвращаемое значение:
//  см. Справочники.КлассификаторТранспортныхСредствСАТУРН.ДанныеОбъекта
Функция ДанныеКлассификатораТранспортногоСредства(ЭлементДанных) Экспорт
	
	Возврат Справочники.КлассификаторТранспортныхСредствСАТУРН.ДанныеОбъекта(ЭлементДанных);
	
КонецФункции

// Данные классификатора территориального управления.
// 
// Параметры:
//  ЭлементДанных - Структура
// 
// Возвращаемое значение:
//  см. Справочники.КлассификаторТерриториальныхУправленийСАТУРН.ДанныеОбъекта
Функция ДанныеКлассификатораТерриториальногоУправления(ЭлементДанных) Экспорт
	
	Возврат Справочники.КлассификаторТерриториальныхУправленийСАТУРН.ДанныеОбъекта(ЭлементДанных);
	
КонецФункции

// Данные классификатора таможенных пунктов.
// 
// Параметры:
//  ЭлементДанных - Структура
// 
// Возвращаемое значение:
//  см. Справочники.КлассификаторТаможенныхПунктовСАТУРН.ДанныеОбъекта
Функция ДанныеКлассификатораТаможенныхПунктов(ЭлементДанных) Экспорт
	
	Возврат Справочники.КлассификаторТаможенныхПунктовСАТУРН.ДанныеОбъекта(ЭлементДанных);
	
КонецФункции

// Список контрагентов.
// 
// Параметры:
//  ПараметрыПоиска - Структура - Параметры поиска
//  НомерСтраницы - Число - Номер страницы
//  КоличествоЭлементовНаСтранице - Число - Количество элементов на странице
// 
// Возвращаемое значение:
//  см. ОбработатьРезультатЗапросаСпискаОбъектов
Функция СписокКонтрагентов(ПараметрыПоиска, НомерСтраницы, КоличествоЭлементовНаСтранице) Экспорт
	
	ПараметрыЗапроса     = ИнтеграцияСАТУРН.ПараметрыПолученияКлючаСессии();
	ПараметрыОптимизации = ИнтеграцияСАТУРНСлужебный.ПараметрыОптимизации();
	
	ДанныеКлючаСессии = ИнтеграцияСАТУРН.ПолучитьКлючСессии(ПараметрыЗапроса, ПараметрыОптимизации);
	
	Если Не ЗначениеЗаполнено(ДанныеКлючаСессии.КлючАвторизации) Тогда
		ВозвращаемоеЗначение = ВозвращаемоеЗначениеЗапросаСпискаОбъектов();
		ВозвращаемоеЗначение.ТекстОшибки = ДанныеКлючаСессии.ТекстОшибки;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Если НомерСтраницы > 1 Тогда
		ПозицияПервогоЭлемента = (НомерСтраницы - 1) * КоличествоЭлементовНаСтранице;
	Иначе
		ПозицияПервогоЭлемента = 0;
	КонецЕсли;
	
	РасширенныйСоставДанных = Ложь;
	Если ПараметрыПоиска.Свойство("РасширенныйСоставДанных")
		И ПараметрыПоиска.РасширенныйСоставДанных Тогда
		РасширенныйСоставДанных = Истина;
	КонецЕсли;
	
	Операция         = Справочники.КлассификаторОрганизацийСАТУРН.ОперацияЗагрузкиКлассификатора();
	ТелоЗапроса      = ИнтеграцияСАТУРНСлужебный.КонвертСообщенияJSON(Операция);
	АргументыЗапроса = АргументыОперации(РасширенныйСоставДанных, Истина);
	АргументыЗапроса.pos  = ПозицияПервогоЭлемента;
	АргументыЗапроса.size = КоличествоЭлементовНаСтранице;
	ТелоЗапроса.Вставить("opargs", АргументыЗапроса);
	
	Если ПараметрыПоиска.Свойство("Идентификатор") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "id", Формат(ПараметрыПоиска.Идентификатор, "ЧГ=;"));
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("GUID") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "sys_guid", ПараметрыПоиска.GUID);
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("НаименованиеПолное") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "name", "%" + ПараметрыПоиска.НаименованиеПолное + "%", "like");
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("ИНН") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "inn", ПараметрыПоиска.ИНН);
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("КПП") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "kpp", ПараметрыПоиска.КПП);
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("ОГРН") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "ogrn", ПараметрыПоиска.ОГРН);
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("ФИО") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "personFio", ПараметрыПоиска.ФИО);
	КонецЕсли;
	
	ВидАдреса = Неопределено;
	ПараметрыПоиска.Свойство("ВидАдреса", ВидАдреса);
	
	Если ВидАдреса = Неопределено Тогда
		ВидАдреса = "legalAddress";
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ДанныеАдреса") Тогда
		
		Если ПараметрыПоиска.ДанныеАдреса.ТипАдреса = "ВСвободнойФорме" Тогда
			
			Если ЗначениеЗаполнено(ПараметрыПоиска.АдресПредставление) Тогда
				ДобавитьУсловиеОтбора(АргументыЗапроса, ВидАдреса, "%" + ПараметрыПоиска.АдресПредставление + "%", "like");
			КонецЕсли;
			
		Иначе
			
			АдресРазобран = Ложь;
			
			Если ПараметрыПоиска.ДанныеАдреса.Свойство("Регион")
				И ЗначениеЗаполнено(ПараметрыПоиска.ДанныеАдреса.Регион) Тогда
				ДобавитьУсловиеОтбора(АргументыЗапроса, ВидАдреса, "%" + ПараметрыПоиска.Регион + "%", "like");
				АдресРазобран = Истина;
			КонецЕсли;
			
			Если ПараметрыПоиска.ДанныеАдреса.Свойство("Город")
				И ЗначениеЗаполнено(ПараметрыПоиска.ДанныеАдреса.Город) Тогда
				ДобавитьУсловиеОтбора(АргументыЗапроса, ВидАдреса, "%" + ПараметрыПоиска.ДанныеАдреса.Город + "%", "like");
				АдресРазобран = Истина;
			КонецЕсли;
			
			Если ПараметрыПоиска.ДанныеАдреса.Свойство("Район")
				И ЗначениеЗаполнено(ПараметрыПоиска.ДанныеАдреса.Район) Тогда
				ДобавитьУсловиеОтбора(АргументыЗапроса, ВидАдреса, "%" + ПараметрыПоиска.ДанныеАдреса.Район + "%", "like");
				АдресРазобран = Истина;
			КонецЕсли;
			
			Если ПараметрыПоиска.ДанныеАдреса.Свойство("Улица")
				И ЗначениеЗаполнено(ПараметрыПоиска.ДанныеАдреса.Улица) Тогда
				ДобавитьУсловиеОтбора(АргументыЗапроса, ВидАдреса, "%" + ПараметрыПоиска.ДанныеАдреса.Улица + "%", "like");
				АдресРазобран = Истина;
			КонецЕсли;
			
			Если Не АдресРазобран Тогда
				ДобавитьУсловиеОтбора(АргументыЗапроса, ВидАдреса, "%" + ПараметрыПоиска.ДанныеАдреса.Представление + "%", "like");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыОтправкиЗапроса                 = ПараметрыОтправкиHTTPЗапросов();
	ПараметрыОтправкиЗапроса.КлючАвторизации = ДанныеКлючаСессии.КлючАвторизации;
	
	РезультатЗапроса = ОтправитьДанныеВСервис(
		ТелоЗапроса,
		ПараметрыОтправкиЗапроса);
	
	Результат = ОбработатьРезультатЗапросаСпискаОбъектов(
		Метаданные.Справочники.КлассификаторОрганизацийСАТУРН, РезультатЗапроса);
	
	Возврат Результат;
	
КонецФункции

// Список мест хранения.
// 
// Параметры:
//  ПараметрыПоиска - Структура - Параметры поиска
//  НомерСтраницы - Число - Номер страницы
//  КоличествоЭлементовНаСтранице - Число - Количество элементов на странице
// 
// Возвращаемое значение:
//  см. ОбработатьРезультатЗапросаСпискаОбъектов
Функция СписокМестХранения(ПараметрыПоиска, НомерСтраницы, КоличествоЭлементовНаСтранице) Экспорт
	
	ПараметрыЗапроса     = ИнтеграцияСАТУРН.ПараметрыПолученияКлючаСессии();
	ПараметрыОптимизации = ИнтеграцияСАТУРНСлужебный.ПараметрыОптимизации();
	
	ДанныеКлючаСессии = ИнтеграцияСАТУРН.ПолучитьКлючСессии(ПараметрыЗапроса, ПараметрыОптимизации);
	
	Если Не ЗначениеЗаполнено(ДанныеКлючаСессии.КлючАвторизации) Тогда
		ВозвращаемоеЗначение = ВозвращаемоеЗначениеЗапросаСпискаОбъектов();
		ВозвращаемоеЗначение.ТекстОшибки = ДанныеКлючаСессии.ТекстОшибки;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Если НомерСтраницы > 1 Тогда
		ПозицияПервогоЭлемента = (НомерСтраницы - 1) * КоличествоЭлементовНаСтранице;
	Иначе
		ПозицияПервогоЭлемента = 0;
	КонецЕсли;
	
	РасширенныйСоставДанных = Ложь;
	Если ПараметрыПоиска.Свойство("РасширенныйСоставДанных")
		И ПараметрыПоиска.РасширенныйСоставДанных Тогда
		РасширенныйСоставДанных = Истина;
	КонецЕсли;
	
	Операция         = Справочники.МестаХраненияСАТУРН.ОперацияЗагрузкиКлассификатора();
	ТелоЗапроса      = ИнтеграцияСАТУРНСлужебный.КонвертСообщенияJSON(Операция);
	АргументыЗапроса = АргументыОперации(РасширенныйСоставДанных, Истина);
	АргументыЗапроса.pos  = ПозицияПервогоЭлемента;
	АргументыЗапроса.size = КоличествоЭлементовНаСтранице;
	ТелоЗапроса.Вставить("opargs", АргументыЗапроса);
	
	Если ПараметрыПоиска.Свойство("Идентификатор") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "id", Формат(ПараметрыПоиска.Идентификатор, "ЧГ=;"));
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("GUID") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "sys_guid", ПараметрыПоиска.GUID);
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("Наименование") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "name", "%" + ПараметрыПоиска.Наименование + "%", "like");
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("ИдентификаторОрганизации") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "ownerId", ПараметрыПоиска.ИдентификаторОрганизации);
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("Организация") Тогда
		//@skip-check bsl-legacy-check-string-literal
		ИдентификаторОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыПоиска.Организация, "Идентификатор");
		ДобавитьУсловиеОтбора(АргументыЗапроса, "ownerId", ИдентификаторОрганизации);
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("ЭтоПроизводственнаяПлощадка") Тогда
		ЗначениеПлощадки = ОбщегоНазначенияИСКлиентСервер.ОбъектВТекстJSON(ПараметрыПоиска.ЭтоПроизводственнаяПлощадка);
		ДобавитьУсловиеОтбора(АргументыЗапроса, "isProducingStoreArea", ЗначениеПлощадки);
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("ЭтоСкладВременногоХранения") Тогда
		ЗначениеВременногоСклада = ОбщегоНазначенияИСКлиентСервер.ОбъектВТекстJSON(ПараметрыПоиска.ЭтоСкладВременногоХранения);
		ДобавитьУсловиеОтбора(АргументыЗапроса, "isTempStoringArea", ЗначениеВременногоСклада);
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("БезПодтипов") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "isProducingStoreArea", ОбщегоНазначенияИСКлиентСервер.ОбъектВТекстJSON(Ложь));
		ДобавитьУсловиеОтбора(АргументыЗапроса, "isTempStoringArea",    ОбщегоНазначенияИСКлиентСервер.ОбъектВТекстJSON(Ложь));
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("Статус") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "lcState", Статус(ПараметрыПоиска.Статус));
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ДанныеАдреса") Тогда
		
		Если ПараметрыПоиска.ДанныеАдреса.ТипАдреса = "ВСвободнойФорме" Тогда
			Если ЗначениеЗаполнено(ПараметрыПоиска.АдресПредставление) Тогда
				ДобавитьУсловиеОтбора(АргументыЗапроса, "addr", "%" + ПараметрыПоиска.АдресПредставление + "%", "like");
			КонецЕсли;
		Иначе
			Если ПараметрыПоиска.ДанныеАдреса.Свойство("Регион")
				И ЗначениеЗаполнено(ПараметрыПоиска.ДанныеАдреса.Регион) Тогда
				ДобавитьУсловиеОтбора(АргументыЗапроса, "addr", "%" + ПараметрыПоиска.ДанныеАдреса.Регион + "%", "like");
			КонецЕсли;
			
			Если ПараметрыПоиска.ДанныеАдреса.Свойство("Город")
				И ЗначениеЗаполнено(ПараметрыПоиска.ДанныеАдреса.Город) Тогда
				ДобавитьУсловиеОтбора(АргументыЗапроса, "addr", "%" + ПараметрыПоиска.ДанныеАдреса.Город + "%", "like");
			КонецЕсли;
			
			Если ПараметрыПоиска.ДанныеАдреса.Свойство("Район")
				И ЗначениеЗаполнено(ПараметрыПоиска.ДанныеАдреса.Район) Тогда
				ДобавитьУсловиеОтбора(АргументыЗапроса, "addr", "%" + ПараметрыПоиска.ДанныеАдреса.Район + "%", "like");
			КонецЕсли;
			
			Если ПараметрыПоиска.ДанныеАдреса.Свойство("Улица")
				И ЗначениеЗаполнено(ПараметрыПоиска.ДанныеАдреса.Улица) Тогда
				ДобавитьУсловиеОтбора(АргументыЗапроса, "addr", "%" + ПараметрыПоиска.ДанныеАдреса.Улица + "%", "like");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыОтправкиЗапроса                 = ПараметрыОтправкиHTTPЗапросов();
	ПараметрыОтправкиЗапроса.КлючАвторизации = ДанныеКлючаСессии.КлючАвторизации;

	РезультатЗапроса = ОтправитьДанныеВСервис(
		ТелоЗапроса,
		ПараметрыОтправкиЗапроса);
	
	Результат = ОбработатьРезультатЗапросаСпискаОбъектов(
		Метаданные.Справочники.МестаХраненияСАТУРН, РезультатЗапроса, РасширенныйСоставДанных);
	
	Возврат Результат;
	
КонецФункции

// Список мест применения.
// 
// Параметры:
//  ПараметрыПоиска - Структура - Параметры поиска
//  НомерСтраницы - Число - Номер страницы
//  КоличествоЭлементовНаСтранице - Число - Количество элементов на странице
// 
// Возвращаемое значение:
//  см. ОбработатьРезультатЗапросаСпискаОбъектов
Функция СписокМестПрименения(ПараметрыПоиска, НомерСтраницы, КоличествоЭлементовНаСтранице) Экспорт
	
	ПараметрыЗапроса     = ИнтеграцияСАТУРН.ПараметрыПолученияКлючаСессии();
	ПараметрыОптимизации = ИнтеграцияСАТУРНСлужебный.ПараметрыОптимизации();
	
	ДанныеКлючаСессии = ИнтеграцияСАТУРН.ПолучитьКлючСессии(ПараметрыЗапроса, ПараметрыОптимизации);
	
	Если Не ЗначениеЗаполнено(ДанныеКлючаСессии.КлючАвторизации) Тогда
		ВозвращаемоеЗначение = ВозвращаемоеЗначениеЗапросаСпискаОбъектов();
		ВозвращаемоеЗначение.ТекстОшибки = ДанныеКлючаСессии.ТекстОшибки;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Если НомерСтраницы > 1 Тогда
		ПозицияПервогоЭлемента = (НомерСтраницы - 1) * КоличествоЭлементовНаСтранице;
	Иначе
		ПозицияПервогоЭлемента = 0;
	КонецЕсли;
	
	РасширенныйСоставДанных = Ложь;
	Если ПараметрыПоиска.Свойство("РасширенныйСоставДанных")
		И ПараметрыПоиска.РасширенныйСоставДанных Тогда
		РасширенныйСоставДанных = Истина;
	КонецЕсли;
	
	Операция         = Справочники.МестаПримененияСАТУРН.ОперацияЗагрузкиКлассификатора();
	ТелоЗапроса      = ИнтеграцияСАТУРНСлужебный.КонвертСообщенияJSON(Операция);
	АргументыЗапроса = АргументыОперации(РасширенныйСоставДанных, Истина);
	АргументыЗапроса.pos  = ПозицияПервогоЭлемента;
	АргументыЗапроса.size = КоличествоЭлементовНаСтранице;
	ТелоЗапроса.Вставить("opargs", АргументыЗапроса);
	
	Если ПараметрыПоиска.Свойство("Наименование") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "name", "%" + ПараметрыПоиска.Наименование + "%", "like");
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("ИдентификаторОрганизации") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "managingContractorId", ПараметрыПоиска.ИдентификаторОрганизации);
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("Организация") Тогда
		//@skip-check bsl-legacy-check-string-literal
		ИдентификаторОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыПоиска.Организация, "Идентификатор");
		ДобавитьУсловиеОтбора(АргументыЗапроса, "managingContractorId", ИдентификаторОрганизации);
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("Статус") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "lcState", Статус(ПараметрыПоиска.Статус));
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("Идентификатор") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "id", Формат(ПараметрыПоиска.Идентификатор, "ЧГ=;"));
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("КадастровыйНомер") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "cadRegNum", ПараметрыПоиска.КадастровыйНомер);
	КонецЕсли;
	
	ПараметрыОтправкиЗапроса                 = ПараметрыОтправкиHTTPЗапросов();
	ПараметрыОтправкиЗапроса.КлючАвторизации = ДанныеКлючаСессии.КлючАвторизации;

	РезультатЗапроса = ОтправитьДанныеВСервис(
		ТелоЗапроса,
		ПараметрыОтправкиЗапроса);
	
	Результат = ОбработатьРезультатЗапросаСпискаОбъектов(
		Метаданные.Справочники.МестаПримененияСАТУРН, РезультатЗапроса, РасширенныйСоставДанных);
	
	Возврат Результат;
	
КонецФункции

// Список инвентаризаций.
// 
// Параметры:
//  ФлагПолнаяКарточка - Число - Флаг полная карточка
// 
// Возвращаемое значение:
//  см. ОбработатьРезультатЗапросаСпискаОбъектов
Функция СписокИнвентаризаций(ФлагПолнаяКарточка = 1) Экспорт
	
	ПараметрыЗапроса = ИнтеграцияСАТУРН.ПараметрыПолученияКлючаСессии();
	ДанныеКлючаСессии = ИнтеграцияСАТУРН.ПолучитьКлючСессии(ПараметрыЗапроса);
	
	Если Не ЗначениеЗаполнено(ДанныеКлючаСессии.КлючАвторизации) Тогда
		ВозвращаемоеЗначение = ВозвращаемоеЗначениеЗапросаСпискаОбъектов();
		ВозвращаемоеЗначение.ТекстОшибки = ДанныеКлючаСессии.ТекстОшибки;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ТелоЗапроса = Новый Структура;
	ТелоЗапроса.Вставить("com",     "execOperation");
	ТелоЗапроса.Вставить("otype",   "Inventorization");
	ТелоЗапроса.Вставить("op",      "static/getList()");
	
	ТелоЗапроса.Вставить("opargs", Новый Структура);
	ТелоЗапроса.opargs.Вставить("pos",          1);
	ТелоЗапроса.opargs.Вставить("size",         100);
	ТелоЗапроса.opargs.Вставить("getFullCards", ФлагПолнаяКарточка);
	
	ПараметрыОтправкиЗапроса                 = ПараметрыОтправкиHTTPЗапросов();
	ПараметрыОтправкиЗапроса.КлючАвторизации = ДанныеКлючаСессии.КлючАвторизации;
	
	РезультатЗапроса = ОтправитьДанныеВСервис(
		ТелоЗапроса,
		ПараметрыОтправкиЗапроса);
	
	Результат = ОбработатьРезультатЗапросаСпискаОбъектов(
		Метаданные.Документы.АктИнвентаризацииСАТУРН, РезультатЗапроса);
	
	Возврат Результат;
	
КонецФункции

// Список ПАТ.
// 
// Параметры:
//  ПараметрыПоиска - Структура - Параметры поиска
//  НомерСтраницы - Число - Номер страницы
//  КоличествоЭлементовНаСтранице - Число - Количество элементов на странице
// 
// Возвращаемое значение:
//  см. ОбработатьРезультатЗапросаСпискаОбъектов
Функция СписокПАТ(ПараметрыПоиска, НомерСтраницы, КоличествоЭлементовНаСтранице) Экспорт
	
	ПараметрыЗапроса     = ИнтеграцияСАТУРН.ПараметрыПолученияКлючаСессии();
	ПараметрыОптимизации = ИнтеграцияСАТУРНСлужебный.ПараметрыОптимизации();
	
	ДанныеКлючаСессии = ИнтеграцияСАТУРН.ПолучитьКлючСессии(ПараметрыЗапроса, ПараметрыОптимизации);
	
	Если Не ЗначениеЗаполнено(ДанныеКлючаСессии.КлючАвторизации) Тогда
		ВозвращаемоеЗначение = ВозвращаемоеЗначениеЗапросаСпискаОбъектов();
		ВозвращаемоеЗначение.ТекстОшибки = ДанныеКлючаСессии.ТекстОшибки;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Если НомерСтраницы > 1 Тогда
		ПозицияПервогоЭлемента = (НомерСтраницы - 1) * КоличествоЭлементовНаСтранице;
	Иначе
		ПозицияПервогоЭлемента = 0;
	КонецЕсли;
	
	РасширенныйСоставДанных = Ложь;
	Если ПараметрыПоиска.Свойство("РасширенныйСоставДанных")
		И ПараметрыПоиска.РасширенныйСоставДанных Тогда
		РасширенныйСоставДанных = Истина;
	КонецЕсли;
	
	Операция         = Справочники.КлассификаторПАТСАТУРН.ОперацияЗагрузкиКлассификатора();
	ТелоЗапроса      = ИнтеграцияСАТУРНСлужебный.КонвертСообщенияJSON(Операция);
	АргументыЗапроса = АргументыОперации(РасширенныйСоставДанных, Истина);
	АргументыЗапроса.pos  = ПозицияПервогоЭлемента;
	АргументыЗапроса.size = КоличествоЭлементовНаСтранице;
	ТелоЗапроса.Вставить("opargs", АргументыЗапроса);
	
	ДобавитьУсловиеОтбора(АргументыЗапроса, Неопределено, Неопределено);
	
	Если ПараметрыПоиска.Свойство("Наименование") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "name", "%" + ПараметрыПоиска.Наименование + "%", "like");
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("Идентификатор") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "id", Формат(ПараметрыПоиска.Идентификатор, "ЧГ=;"));
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("ВидПродукции") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "type", ВидПродукции(ПараметрыПоиска.ВидПродукции));
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("Статус") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "lcState", Статус(ПараметрыПоиска.Статус));
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("Группа") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "group", "%" + ПараметрыПоиска.Группа + "%", "like");
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("Регистрант") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "registrant", "%" + ПараметрыПоиска.Регистрант + "%", "like");
	КонецЕсли;
	
	ПараметрыОтправкиЗапроса                 = ПараметрыОтправкиHTTPЗапросов();
	ПараметрыОтправкиЗапроса.КлючАвторизации = ДанныеКлючаСессии.КлючАвторизации;
	
	РезультатЗапроса = ОтправитьДанныеВСервис(
		ТелоЗапроса,
		ПараметрыОтправкиЗапроса);

	Результат = ОбработатьРезультатЗапросаСпискаОбъектов(
		Метаданные.Справочники.КлассификаторПАТСАТУРН, РезультатЗапроса, РасширенныйСоставДанных);

	Возврат Результат;

КонецФункции

// Список партий.
//
// Параметры:
//  ПараметрыПоиска - Структура - Параметры поиска
//  НомерСтраницы - Число - Номер страницы
//  КоличествоЭлементовНаСтранице - Число - Количество элементов на странице
//
// Возвращаемое значение:
//  см. ОбработатьРезультатЗапросаСпискаОбъектов
Функция СписокПартий(ПараметрыПоиска, НомерСтраницы, КоличествоЭлементовНаСтранице) Экспорт

	ПараметрыЗапроса     = ИнтеграцияСАТУРН.ПараметрыПолученияКлючаСессии();
	ПараметрыОптимизации = ИнтеграцияСАТУРНСлужебный.ПараметрыОптимизации();
	
	ДанныеКлючаСессии = ИнтеграцияСАТУРН.ПолучитьКлючСессии(ПараметрыЗапроса, ПараметрыОптимизации);
	
	Если Не ЗначениеЗаполнено(ДанныеКлючаСессии.КлючАвторизации) Тогда
		ВозвращаемоеЗначение = ВозвращаемоеЗначениеЗапросаСпискаОбъектов();
		ВозвращаемоеЗначение.ТекстОшибки = ДанныеКлючаСессии.ТекстОшибки;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;

	Если НомерСтраницы > 1 Тогда
		ПозицияПервогоЭлемента = (НомерСтраницы - 1) * КоличествоЭлементовНаСтранице;
	Иначе
		ПозицияПервогоЭлемента = 0;
	КонецЕсли;

	РасширенныйСоставДанных = Ложь;
	Если ПараметрыПоиска.Свойство("РасширенныйСоставДанных")
		И ПараметрыПоиска.РасширенныйСоставДанных Тогда
		РасширенныйСоставДанных = Истина;
	КонецЕсли;
	
	Операция         = Справочники.ПартииСАТУРН.ОперацияЗагрузкиКлассификатора();
	ТелоЗапроса      = ИнтеграцияСАТУРНСлужебный.КонвертСообщенияJSON(Операция);
	АргументыЗапроса = АргументыОперации(РасширенныйСоставДанных, Истина);
	АргументыЗапроса.pos  = ПозицияПервогоЭлемента;
	АргументыЗапроса.size = КоличествоЭлементовНаСтранице;
	ТелоЗапроса.Вставить("opargs", АргументыЗапроса);
	
	Если ПараметрыПоиска.Свойство("Наименование") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "name", "%" + ПараметрыПоиска.Наименование + "%", "like");
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("Идентификатор") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "id", Формат(ПараметрыПоиска.Идентификатор, "ЧГ=;"));
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("Статус") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "lcState", Статус(ПараметрыПоиска.Статус));
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("ПАТ") Тогда
		//@skip-check bsl-legacy-check-string-literal
		ИдентификаторПАТ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыПоиска.ПАТ, "Идентификатор");
		ДобавитьУсловиеОтбора(АргументыЗапроса, "patProductId", ИдентификаторПАТ);
	КонецЕсли;
	Если ПараметрыПоиска.Свойство("ИдентификаторПАТ") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "patProductId", ПараметрыПоиска.ИдентификаторПАТ);
	КонецЕсли;

	ПараметрыОтправкиЗапроса                 = ПараметрыОтправкиHTTPЗапросов();
	ПараметрыОтправкиЗапроса.КлючАвторизации = ДанныеКлючаСессии.КлючАвторизации;
	
	РезультатЗапроса = ОтправитьДанныеВСервис(
		ТелоЗапроса,
		ПараметрыОтправкиЗапроса);
	
	Результат = ОбработатьРезультатЗапросаСпискаОбъектов(
		Метаданные.Справочники.ПартииСАТУРН, РезультатЗапроса, РасширенныйСоставДанных);
	
	Возврат Результат;
	
КонецФункции

// Список импортируемых партий.
// 
// Параметры:
//  ПараметрыПоиска - Структура
//  НомерСтраницы - Число - номер страницы
//  КоличествоЭлементовНаСтранице - Число - количество элементов на странице
// 
// Возвращаемое значение:
//  см. ОбработатьРезультатЗапросаСпискаОбъектов
Функция СписокИмпортируемыхПартий(ПараметрыПоиска, НомерСтраницы, КоличествоЭлементовНаСтранице) Экспорт

	ПараметрыЗапроса     = ИнтеграцияСАТУРН.ПараметрыПолученияКлючаСессии();
	ПараметрыОптимизации = ИнтеграцияСАТУРНСлужебный.ПараметрыОптимизации();
	
	ДанныеКлючаСессии = ИнтеграцияСАТУРН.ПолучитьКлючСессии(ПараметрыЗапроса, ПараметрыОптимизации);
	
	Если Не ЗначениеЗаполнено(ДанныеКлючаСессии.КлючАвторизации) Тогда
		ВозвращаемоеЗначение = ВозвращаемоеЗначениеЗапросаСпискаОбъектов();
		ВозвращаемоеЗначение.ТекстОшибки = ДанныеКлючаСессии.ТекстОшибки;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;

	Если НомерСтраницы > 1 Тогда
		ПозицияПервогоЭлемента = (НомерСтраницы - 1) * КоличествоЭлементовНаСтранице;
	Иначе
		ПозицияПервогоЭлемента = 0;
	КонецЕсли;

	РасширенныйСоставДанных = Ложь;
	Если ПараметрыПоиска.Свойство("РасширенныйСоставДанных")
		И ПараметрыПоиска.РасширенныйСоставДанных Тогда
		РасширенныйСоставДанных = Истина;
	КонецЕсли;
	
	Операция         = Справочники.ИмпортируемаяПартияСАТУРН.ОперацияЗагрузкиКлассификатора();
	ТелоЗапроса      = ИнтеграцияСАТУРНСлужебный.КонвертСообщенияJSON(Операция);
	АргументыЗапроса = АргументыОперации(РасширенныйСоставДанных, Истина);
	АргументыЗапроса.pos  = ПозицияПервогоЭлемента;
	АргументыЗапроса.size = КоличествоЭлементовНаСтранице;
	ТелоЗапроса.Вставить("opargs", АргументыЗапроса);
	
	Если ПараметрыПоиска.Свойство("Идентификатор") Тогда
		ДобавитьУсловиеОтбора(АргументыЗапроса, "id", Формат(ПараметрыПоиска.Идентификатор, "ЧГ=;"));
	КонецЕсли;
	
	ПараметрыОтправкиЗапроса                 = ПараметрыОтправкиHTTPЗапросов();
	ПараметрыОтправкиЗапроса.КлючАвторизации = ДанныеКлючаСессии.КлючАвторизации;
	
	РезультатЗапроса = ОтправитьДанныеВСервис(
		ТелоЗапроса,
		ПараметрыОтправкиЗапроса);
	
	Результат = ОбработатьРезультатЗапросаСпискаОбъектов(
		Метаданные.Справочники.ИмпортируемаяПартияСАТУРН, РезультатЗапроса, РасширенныйСоставДанных);
	
	Возврат Результат;
	
КонецФункции

// Список классификаторов стран.
// 
// Параметры:
//  МетаданныеКлассификатора - ОбъектМетаданных - тип загружаемого справочника
// 
// Возвращаемое значение:
//  см. ОбработатьРезультатЗапросаСпискаОбъектов
Функция СписокКлассификаторов(МетаданныеКлассификатора) Экспорт
	
	ПараметрыЗапроса     = ИнтеграцияСАТУРН.ПараметрыПолученияКлючаСессии();
	ПараметрыОптимизации = ИнтеграцияСАТУРНСлужебный.ПараметрыОптимизации();
	
	ДанныеКлючаСессии = ИнтеграцияСАТУРН.ПолучитьКлючСессии(ПараметрыЗапроса, ПараметрыОптимизации);
	
	Если Не ЗначениеЗаполнено(ДанныеКлючаСессии.КлючАвторизации) Тогда
		ВозвращаемоеЗначение = ВозвращаемоеЗначениеЗапросаСпискаОбъектов();
		ВозвращаемоеЗначение.ТекстОшибки = ДанныеКлючаСессии.ТекстОшибки;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Операция         = Справочники.КлассификаторСтранСАТУРН.ОперацияЗагрузкиКлассификатора();
	ТелоЗапроса      = ИнтеграцияСАТУРНСлужебный.КонвертСообщенияJSON(Операция);
	АргументыЗапроса = АргументыОперации();
	ТелоЗапроса.Вставить("opargs", АргументыЗапроса);
	
	Если МетаданныеКлассификатора = Метаданные.Справочники.КлассификаторСтранСАТУРН Тогда
		ИмяСловаря = "country";
	ИначеЕсли МетаданныеКлассификатора = Метаданные.Справочники.КлассификаторТаможенныхПунктовСАТУРН Тогда
		ИмяСловаря = "border";
	ИначеЕсли МетаданныеКлассификатора = Метаданные.Справочники.КлассификаторТерриториальныхУправленийСАТУРН Тогда
		ИмяСловаря = "tu";
	ИначеЕсли МетаданныеКлассификатора = Метаданные.Справочники.КлассификаторТранспортныхСредствСАТУРН Тогда
		ИмяСловаря = "transport";
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестный тип классификатора ФГИС ""Сатурн"".';
								|en = 'Неизвестный тип классификатора ФГИС ""Сатурн"".'");
	КонецЕсли;
	
	ДобавитьАргумент(АргументыЗапроса, "dictionary", ИмяСловаря);
	
	ПараметрыОтправкиЗапроса                 = ПараметрыОтправкиHTTPЗапросов();
	ПараметрыОтправкиЗапроса.КлючАвторизации = ДанныеКлючаСессии.КлючАвторизации;
	
	РезультатЗапроса = ОтправитьДанныеВСервис(
		ТелоЗапроса,
		ПараметрыОтправкиЗапроса);
	
	Результат = ОбработатьРезультатЗапросаСпискаОбъектов(
		МетаданныеКлассификатора, РезультатЗапроса,, Операция);
	
	Возврат Результат;
	
КонецФункции

// Добавить условие отбора.
// 
// Параметры:
//  АргументыОперации - Структура
//  Поле - Строка - Поле
//  Значение - Строка, Число -
//  Условие - Строка - Условие
Процедура ДобавитьУсловиеОтбора(АргументыОперации, Поле, Значение, Условие = "=") Экспорт
	
	Если АргументыОперации.Свойство("filters") Тогда
		Отборы = АргументыОперации.filters;
	Иначе
		Отборы = Новый Массив;
		АргументыОперации.Вставить("filters", Отборы);
	КонецЕсли;
	
	Если Поле = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура;
	
	Отбор.Вставить("column",    Поле);
	Отбор.Вставить("condition", Условие);
	Если ТипЗнч(Значение) = Тип("Массив") Тогда
		Отбор.Вставить("value", Значение);
		Если Значение.Количество() > 1 Тогда
			Отбор.condition = "in";
		КонецЕсли;
	Иначе
		Отбор.Вставить("value", Новый Массив());
		Отбор.value.Добавить(Значение);
	КонецЕсли;
	
	Отборы.Добавить(Отбор);
	
КонецПроцедуры

// Добавить аргумент.
// 
// Параметры:
//  АргументыОперации - Структура - 
//  Имя - Строка - 
//  Значение - Строка, Число, Неопределено, Булево, Дата, Структура - 
Процедура ДобавитьАргумент(АргументыОперации, Имя, Значение) Экспорт
	
	АргументыОперации.Вставить(Имя, Значение);
	
КонецПроцедуры

// Добавить аргумент theCard.
// 
// Параметры:
//  АргументыОперации - Структура - 
//  Имя - Строка - 
//  Значение - Строка, Число, Неопределено, Булево, Дата, Структура - 
Процедура ДобавитьАргументtheCard(АргументыОперации, Имя, Значение) Экспорт
	
	Если Не АргументыОперации.Свойство("theCard") Тогда
		АргументыОперации.Вставить("theCard", Новый Структура);
	КонецЕсли;
	
	АргументыОперации.theCard.Вставить(Имя, Значение);
	
КонецПроцедуры

// Добавить аргумент шапки.
// 
// Параметры:
//  АргументыОперации - Структура
//  Имя - Строка - 
//  Значение - Строка, Число, Неопределено, Булево, Дата, Структура -
Процедура ДобавитьАргументШапки(АргументыОперации, Имя, Значение) Экспорт
	
	Если АргументыОперации.Свойство("theCard") Тогда
		Если Не АргументыОперации.theCard.Свойство("head") Тогда
			АргументыОперации.theCard.Вставить("head", Новый Структура);
		КонецЕсли;
	Иначе
		ДобавитьАргументtheCard(АргументыОперации, "head", Новый Структура);
	КонецЕсли;
	
	АргументыОперации.theCard.head.Вставить(Имя, Значение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область РаботаСВнешнимиСервисами

Функция URLЗапроса(HTTPЗапрос, ПараметрыОтправкиHTTPЗапросов, HTTPМетод) Экспорт

	Если ПараметрыОтправкиHTTPЗапросов.ИспользоватьЗащищенноеСоединение Тогда
		Протокол = "https";
	Иначе
		Протокол = "http";
	КонецЕсли;
	
	URLЗапроса = СтрШаблон(
		"%1 %2://%3:%4/%5",
		HTTPМетод,
		Протокол,
		ПараметрыОтправкиHTTPЗапросов.Сервер,
		Формат(ПараметрыОтправкиHTTPЗапросов.Порт, "ЧГ=0;"),
		HTTPЗапрос.АдресРесурса);
	
	Возврат URLЗапроса;

КонецФункции

Функция ПолучитьДанныеИзСервиса(ПараметрыОтправкиHTTPЗапросов, ЗаголовкиHTTP = Неопределено) Экспорт
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Content-Type",   "application/json; charset=utf-8");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("Cache-Control",  "no-cache");
	Если ЗаголовкиHTTP <> Неопределено Тогда
		Для Каждого КлючИЗначение Из ЗаголовкиHTTP Цикл
			ЗаголовокHTTP.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
	HTTPЗапрос  = Новый HTTPЗапрос(ПараметрыОтправкиHTTPЗапросов.АдресРесурса, ЗаголовокHTTP);
	HTTPОтвет   = Неопределено;
	ТекстОшибки = "";
	
	Если ПараметрыОтправкиHTTPЗапросов.ИспользоватьЗащищенноеСоединение Тогда
		ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTPS");
		ЗащищенноеСоединение = ОбщегоНазначенияИСКлиентСерверПовтИсп.ЗащищенноеСоединение();
	Иначе
		ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTP");
		ЗащищенноеСоединение = Неопределено;
	КонецЕсли;
	
	Попытка
		
		Соединение = Новый HTTPСоединение(
			ПараметрыОтправкиHTTPЗапросов.Сервер,
			ПараметрыОтправкиHTTPЗапросов.Порт,,,
			ИнтернетПрокси,
			ПараметрыОтправкиHTTPЗапросов.Таймаут,
			ЗащищенноеСоединение);
		
		HTTPОтвет = Соединение.Получить(HTTPЗапрос);
		
	Исключение
		
		ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			ПараметрыОтправкиHTTPЗапросов.ПредставлениеСервиса,
			УровеньЖурналаРегистрации.Ошибка,,,
			СтрШаблон(
				НСтр("ru = 'Ошибка при выполнении запроса GET %1 в %2 %3:%4';
					|en = 'Ошибка при выполнении запроса GET %1 в %2 %3:%4'"),
				ПараметрыОтправкиHTTPЗапросов.АдресРесурса,
				ПараметрыОтправкиHTTPЗапросов.ПредставлениеСервиса,
				ПараметрыОтправкиHTTPЗапросов.Сервер,
				ПараметрыОтправкиHTTPЗапросов.Порт) + Символы.ПС +
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	HTTPМетод = "GET";
	
	ПараметрыЛогированияЗапросов   = ЛогированиеЗапросовСАТУРН.ПараметрыЛогированияЗапросов();
	ДанныеЗаписи                   = ЛогированиеЗапросовИС.НоваяСтруктураДанныхЗаписи();
	ДанныеЗаписи.HTTPМетод         = HTTPМетод;
	ДанныеЗаписи.HTTPЗапросОтвет   = HTTPЗапрос;
	ДанныеЗаписи.АдресРесурса      = URLЗапроса(HTTPЗапрос, ПараметрыОтправкиHTTPЗапросов, ДанныеЗаписи.HTTPМетод);
	ЛогированиеЗапросовИС.Вывести(ДанныеЗаписи, ПараметрыЛогированияЗапросов);
	
	ДанныеЗаписи                 = ЛогированиеЗапросовИС.НоваяСтруктураДанныхЗаписи();
	ДанныеЗаписи.HTTPЗапросОтвет = HTTPОтвет;
	ДанныеЗаписи.ТекстОшибки     = ТекстОшибки;
	ЛогированиеЗапросовИС.Вывести(ДанныеЗаписи, ПараметрыЛогированияЗапросов);
	
	Если ПараметрыОтправкиHTTPЗапросов.Свойство("ДатаПоследнегоЗапроса") Тогда
		ПараметрыОтправкиHTTPЗапросов.ДатаПоследнегоЗапроса = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ПараметрыОтправкиHTTPЗапросов", ПараметрыОтправкиHTTPЗапросов);
	ВозвращаемоеЗначение.Вставить("HTTPМетод",                     HTTPМетод);
	ВозвращаемоеЗначение.Вставить("HTTPЗапрос",                    HTTPЗапрос);
	ВозвращаемоеЗначение.Вставить("HTTPОтвет",                     HTTPОтвет);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                   ТекстОшибки);
	ВозвращаемоеЗначение.Вставить("КлючАвторизации",               Неопределено);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ОтправитьДанныеВСервис(ТелоЗапроса, ПараметрыОтправкиHTTPЗапросов, ЗаголовокHTTP = Неопределено) Экспорт
	
	HTTPМетод = "POST";
	
	ЭтоДвоичныеДанные = ТипЗнч(ТелоЗапроса) = Тип("ДвоичныеДанные");
	ЭтоФайл           = ТипЗнч(ТелоЗапроса) = Тип("Файл");
	
	Если ТелоЗапроса <> Неопределено И ТипЗнч(ТелоЗапроса) = Тип("Строка") Тогда
		ТелоЗапросаJSON = ТелоЗапроса;
	ИначеЕсли ТелоЗапроса <> Неопределено И Не ЭтоДвоичныеДанные И Не ЭтоФайл Тогда
		НастройкиСериализации                        = Новый НастройкиСериализацииJSON();
		НастройкиСериализации.ВариантЗаписиДаты      = ВариантЗаписиДатыJSON.ЛокальнаяДатаСоСмещением;
		НастройкиСериализации.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
		ТелоЗапросаJSON = ОбщегоНазначенияИСКлиентСервер.ОбъектВТекстJSON(ТелоЗапроса, Истина, НастройкиСериализации);
	КонецЕсли;
	
	Если ЗаголовокHTTP = Неопределено Тогда
		ЗаголовокHTTP = Новый Соответствие();
		ЗаголовокHTTP.Вставить("Content-Type",   "application/json; charset=utf-8");
		ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
		ЗаголовокHTTP.Вставить("Date",           ИнтеграцияИС.ДатаСЧасовымПоясом(ТекущаяДатаСеанса()));
	КонецЕсли;
	
	ЗаголовокHTTP.Вставить(
		"Authorization",
		СтрШаблон("Bearer %1", ПараметрыОтправкиHTTPЗапросов.КлючАвторизации));
	
	HTTPЗапрос  = Новый HTTPЗапрос(ПараметрыОтправкиHTTPЗапросов.АдресРесурса, ЗаголовокHTTP);
	Если ТелоЗапроса <> Неопределено Тогда
		Если ЭтоДвоичныеДанные Тогда
			//@skip-check bsl-legacy-check-expression-type
			HTTPЗапрос.УстановитьТелоИзДвоичныхДанных(ТелоЗапроса);
		ИначеЕсли ЭтоФайл Тогда
			HTTPЗапрос.УстановитьИмяФайлаТела(ТелоЗапроса.ПолноеИмя);
		Иначе
			HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапросаJSON, КодировкаТекста.UTF8, ИспользованиеByteOrderMark.НеИспользовать);
		КонецЕсли;
	КонецЕсли;
	HTTPОтвет   = Неопределено;
	ТекстОшибки = "";
	
	Если ПараметрыОтправкиHTTPЗапросов.ИспользоватьЗащищенноеСоединение Тогда
		ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTPS");
		ЗащищенноеСоединение = ОбщегоНазначенияИСКлиентСерверПовтИсп.ЗащищенноеСоединение();
	Иначе
		ИнтернетПрокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси("HTTP");
		ЗащищенноеСоединение = Неопределено;
	КонецЕсли;
	
	Попытка
		
		Соединение = Новый HTTPСоединение(
			ПараметрыОтправкиHTTPЗапросов.Сервер,
			ПараметрыОтправкиHTTPЗапросов.Порт,,,
			ИнтернетПрокси,
			ПараметрыОтправкиHTTPЗапросов.Таймаут,
			ЗащищенноеСоединение);
		
		HTTPОтвет = Соединение.ВызватьHTTPМетод(HTTPМетод, HTTPЗапрос);
		
	Исключение
		
		ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'САТУРН';
				|en = 'САТУРН'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			СтрШаблон(
				НСтр("ru = 'Ошибка при выполнении запроса POST %1 в ФГИС ""Сатурн"" %2:%3';
					|en = 'Ошибка при выполнении запроса POST %1 в ФГИС ""Сатурн"" %2:%3'"),
				ПараметрыОтправкиHTTPЗапросов.АдресРесурса,
				ПараметрыОтправкиHTTPЗапросов.Сервер,
				ПараметрыОтправкиHTTPЗапросов.Порт) + Символы.ПС +
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	ПараметрыЛогированияЗапросов   = ЛогированиеЗапросовСАТУРН.ПараметрыЛогированияЗапросов();
	ДанныеЗаписи                   = ЛогированиеЗапросовИС.НоваяСтруктураДанныхЗаписи();
	ДанныеЗаписи.HTTPМетод         = HTTPМетод;
	ДанныеЗаписи.HTTPЗапросОтвет   = HTTPЗапрос;
	ДанныеЗаписи.АдресРесурса      = URLЗапроса(HTTPЗапрос, ПараметрыОтправкиHTTPЗапросов, ДанныеЗаписи.HTTPМетод);
	ЛогированиеЗапросовИС.Вывести(ДанныеЗаписи, ПараметрыЛогированияЗапросов);
	
	ДанныеЗаписи                 = ЛогированиеЗапросовИС.НоваяСтруктураДанныхЗаписи();
	ДанныеЗаписи.HTTPЗапросОтвет = HTTPОтвет;
	ДанныеЗаписи.ТекстОшибки     = ТекстОшибки;
	ЛогированиеЗапросовИС.Вывести(ДанныеЗаписи, ПараметрыЛогированияЗапросов);
	
	Если ПараметрыОтправкиHTTPЗапросов.Свойство("ДатаПоследнегоЗапроса") Тогда
		ПараметрыОтправкиHTTPЗапросов.ДатаПоследнегоЗапроса = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ПараметрыОтправкиHTTPЗапросов", ПараметрыОтправкиHTTPЗапросов);
	ВозвращаемоеЗначение.Вставить("HTTPМетод",                     HTTPМетод);
	ВозвращаемоеЗначение.Вставить("HTTPЗапрос",                    HTTPЗапрос);
	ВозвращаемоеЗначение.Вставить("HTTPОтвет",                     HTTPОтвет);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                   ТекстОшибки);
	ВозвращаемоеЗначение.Вставить("КлючАвторизации",               ПараметрыОтправкиHTTPЗапросов.КлючАвторизации);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

Функция ФорматироватьЛогЗапросов(ИсходныйТекст, ДополнительныеПараметры = Неопределено) Экспорт
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	ТекстовыйДокумент.УстановитьТекст(ИсходныйТекст);
	
	КоличествоСтрок = ТекстовыйДокумент.КоличествоСтрок();
	Если КоличествоСтрок = 0 Тогда
		Возврат ИсходныйТекст;
	КонецЕсли;
	
	Для СмещениеСКонца = 0 По КоличествоСтрок - 1 Цикл
		
		НомерСтрокиСКонца = КоличествоСтрок - СмещениеСКонца;
		ТекущаяСтрока     = ТекстовыйДокумент.ПолучитьСтроку(НомерСтрокиСКонца);
		ДанныеСтроки      = Новый Массив();
		Если СтрНачинаетсяС(ТекущаяСтрока, "{")
			Или СтрНачинаетсяС(ТекущаяСтрока, "[") Тогда
			Попытка
				ДанныеСтроки.Добавить(ОбщегоНазначенияИС.ФорматироватьJSON(ТекущаяСтрока));
			Исключение
				Продолжить;
			КонецПопытки;
		Иначе
			Продолжить;
		КонецЕсли;
		
		ТекстовыйДокумент.УдалитьСтроку(НомерСтрокиСКонца);
		ТекстовыйДокумент.ВставитьСтроку(НомерСтрокиСКонца, СтрСоединить(ДанныеСтроки, Символы.ПС));
		
	КонецЦикла;
	
	Возврат СокрЛП(ТекстовыйДокумент.ПолучитьТекст());
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СлужебныеПоля(ВключитьДатыИзменения = Истина) Экспорт
	
	СлужебныеПоля = Новый Соответствие();
	СлужебныеПоля.Вставить("_FORMAT",     Истина);
	СлужебныеПоля.Вставить("_FORMAT_VER", Истина);
	СлужебныеПоля.Вставить("_guid",       Истина);
	
	СлужебныеПоля.Вставить("sys_timeTo",        Истина);
	СлужебныеПоля.Вставить("sys_authorId",      Истина);
	СлужебныеПоля.Вставить("sys_note",          Истина);
	СлужебныеПоля.Вставить("sys_ver",           Истина);
	
	Если Не ВключитьДатыИзменения Тогда
		СлужебныеПоля.Вставить("sys_timeFrom",  Истина); // Есть при getFullCards = 0
		СлужебныеПоля.Вставить("sys_changedAt", Истина); // Есть при getFullCards = 0
		СлужебныеПоля.Вставить("sysChangeDat",  Истина); // Есть при getFullCards = 0
		СлужебныеПоля.Вставить("sys_guid",      Истина); // Есть при getFullCards = 0
	КонецЕсли;
	
	СлужебныеПоля.Вставить("sys_ownerClass",    Истина);
	СлужебныеПоля.Вставить("sys_ownerId",       Истина);
	СлужебныеПоля.Вставить("sys_ownerListName", Истина);
	СлужебныеПоля.Вставить("sys_order",         Истина);
	СлужебныеПоля.Вставить("sys_recSource",     Истина);
	
	СлужебныеПоля.Вставить("sys_rwx_ownerClass",      Истина);
	СлужебныеПоля.Вставить("sys_rwx_ownerGroupClass", Истина);
	СлужебныеПоля.Вставить("sys_rwx_ownerGroupIds",   Истина);
	СлужебныеПоля.Вставить("sys_rwx_ownerIds",        Истина);
	
	СлужебныеПоля.Вставить("_raw_owner_class", Истина);
	СлужебныеПоля.Вставить("_raw_owner_id",    Истина);
	
	Возврат СлужебныеПоля;
	
КонецФункции

Функция АргументыОперации(ПолнаяКарточка = Неопределено, Пагинация = Ложь) Экспорт
	
	АргументыОперации = Новый Структура;
	Если ПолнаяКарточка <> Неопределено Тогда
		Если ПолнаяКарточка Тогда
			АргументыОперации.Вставить("getFullCards", 1);
		Иначе
			АргументыОперации.Вставить("getFullCards", 0);
		КонецЕсли;
	КонецЕсли;
	
	Если Пагинация Тогда
		АргументыОперации.Вставить("pos",  0);
		АргументыОперации.Вставить("size", 100);
	КонецЕсли;
	
	Возврат АргументыОперации;
	
КонецФункции

// Возвращаемое значение запроса списка объектов.
// 
// Возвращаемое значение:
//  Структура - Обработать результат запроса списка объектов:
// * Список - Неопределено, Массив из Структура - 
// * ТекстОшибки - Строка - 
// * ПараметрыОбмена - Неопределено -
//                   - см. ИнтеграцияСАТУРН.ПараметрыОбмена
Функция ВозвращаемоеЗначениеЗапросаСпискаОбъектов()
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("Список",          Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",     "");
	ВозвращаемоеЗначение.Вставить("ПараметрыОбмена", Неопределено);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Обработать результат запроса списка объектов.
// 
// Параметры:
//  МетаданныеОбъекта - ОбъектМетаданных
//  РезультатЗапроса - см. ОтправитьДанныеВСервис
//  ВернутьПараметрыОбмена - Булево - Вернуть параметры обмена
//  Операция - ПеречислениеСсылка.ВидыОперацийСАТУРН
// 
// Возвращаемое значение:
//  см. ВозвращаемоеЗначениеЗапросаСпискаОбъектов
Функция ОбработатьРезультатЗапросаСпискаОбъектов(МетаданныеОбъекта, РезультатЗапроса, ВернутьПараметрыОбмена = Ложь, Операция = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = ВозвращаемоеЗначениеЗапросаСпискаОбъектов();
	
	РезультатОтправкиЗапроса = ИнтеграцияСАТУРНСлужебный.ОбработатьРезультатОтправкиHTTPЗапроса(РезультатЗапроса, Операция);
	
	Если РезультатОтправкиЗапроса.СтатусОбработки <> Перечисления.СтатусыОбработкиСообщенийСАТУРН.ЗаявкаВыполнена Тогда
		ВозвращаемоеЗначение.ТекстОшибки = РезультатОтправкиЗапроса.ТекстОшибки;
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Если ВернутьПараметрыОбмена Тогда
		ПараметрыОбмена = ИнтеграцияСАТУРН.ПараметрыОбмена();
	Иначе
		ПараметрыОбмена = Неопределено;
	КонецЕсли;
	
	ВозвращаемоеЗначение.Список = ИнтеграцияСАТУРНСлужебный.ОбработатьРезультатЗапросаСпискаОбъектов(
		МетаданныеОбъекта, РезультатОтправкиЗапроса.Объект, ПараметрыОбмена);
	ВозвращаемоеЗначение.ПараметрыОбмена = ПараметрыОбмена;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция РезультатОтправкиЗапросаКорректен(ТекстОшибки, РезультатОтправкиЗапроса, ВозвращаемоеЗначение) Экспорт
	
	Если РезультатОтправкиЗапроса.КодСостояния <> 200 Тогда
		ВозвращаемоеЗначение.Вставить("КодОшибки", РезультатОтправкиЗапроса.КодСостояния);
		ВозвращаемоеЗначение.ТекстОшибки = СтрШаблон(
			НСтр("ru = '%1.
			           |Текст ошибки: Код состояния HTTP-запроса: %2';
			           |en = '%1.
			           |Текст ошибки: Код состояния HTTP-запроса: %2'"), ТекстОшибки, РезультатОтправкиЗапроса.КодСостояния);
		Возврат Ложь;
	КонецЕсли;
	
	Если Не РезультатОтправкиЗапроса.Объект.Свойство("resCode") Тогда
		ВозвращаемоеЗначение.Вставить("КодОшибки", Неопределено);
		ВозвращаемоеЗначение.ТекстОшибки = СтрШаблон(
				НСтр("ru = '%1.
			               |Текст ошибки: Формат данных отличается от ожидаемого';
			               |en = '%1.
			               |Текст ошибки: Формат данных отличается от ожидаемого'"), ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	Если Не РезультатОтправкиЗапроса.Объект.Свойство("resCode")
		Или РезультатОтправкиЗапроса.Объект.resCode < 0 Тогда
		ВозвращаемоеЗначение.Вставить("КодОшибки", РезультатОтправкиЗапроса.Объект.resCode);
		Если РезультатОтправкиЗапроса.Объект.Свойство("resMsg") Тогда
			ВозвращаемоеЗначение.ТекстОшибки = СтрШаблон(
			НСтр("ru = '%1.
			           |Текст ошибки: %2 (Код ошибки: %3)
			           |%4';
			           |en = '%1.
			           |Текст ошибки: %2 (Код ошибки: %3)
			           |%4'"),
			ТекстОшибки,
			РезультатОтправкиЗапроса.Объект.resDescription,
			РезультатОтправкиЗапроса.Объект.resCode,
			РезультатОтправкиЗапроса.Объект.resMsg);
		Иначе
			ВозвращаемоеЗначение.ТекстОшибки = СтрШаблон(
			НСтр("ru = '%1.
			           |Текст ошибки: %2 (Код ошибки: %3)';
			           |en = '%1.
			           |Текст ошибки: %2 (Код ошибки: %3)'"),
			ТекстОшибки,
			РезультатОтправкиЗапроса.Объект.resDescription,
			РезультатОтправкиЗапроса.Объект.resCode);
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти