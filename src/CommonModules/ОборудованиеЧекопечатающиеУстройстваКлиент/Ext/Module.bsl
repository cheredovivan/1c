
#Область ПрограммныйИнтерфейс

// Производить печать чека на фискальном устройстве.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьФискализациюЧекаНаФискальномУстройстве(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт;
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("CheckFiscalization", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры);
	// Будет сформировано сообщения в службу технической поддержки при ошибке.  
	ПараметрыВыполнениеКоманды.СообщенияВСлужбуТехническойПоддержки = Истина;
	Если ПараметрыОперации.Свойство("АвтономнаяККТ") И ПараметрыОперации.АвтономнаяККТ Тогда
		Если ОбщегоНазначенияБПОКлиент.ИспользуетсяАвтономнаяККТ() Тогда
			МодульОборудованиеЧекопечатающиеАвтономныеУстройстваКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеЧекопечатающиеАвтономныеУстройстваКлиент");
			МодульОборудованиеЧекопечатающиеАвтономныеУстройстваКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, 
				ИдентификаторКлиента, ПараметрыОперации, ПараметрыВыполнениеКоманды, ДополнительныеПараметры);
		КонецЕсли;
	ИначеЕсли ПараметрыОперации.ОблачнаяККТ Тогда
		Если ОбщегоНазначенияБПОКлиент.ИспользуютсяОблачныеККТ() Тогда
			МодульОборудованиеОблачныеККТКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеЧекопечатающиеОблачныеККТКлиент");
			МодульОборудованиеОблачныеККТКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, 
				ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ПараметрыВыполнениеКоманды);
		КонецЕсли;
	Иначе
		МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
			ПараметрыОперации, ПараметрыВыполнениеКоманды);
	КонецЕсли;
		
КонецПроцедуры

// Производить печать чека на фискальном устройстве пакетом.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьФискализациюЧекаНаФискальномУстройствеПакетом(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("CheckFiscalizationPacket", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры);
		
	МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
		ПараметрыОперации, ПараметрыВыполнениеКоманды);
	
КонецПроцедуры

// Производить инкассацию на фискальном устройстве.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьИнкассациюНаФискальномУстройстве(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("Encash", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Истина, Истина);
		
	Если ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ЭтоККТБезПодключения(ПараметрыОперации) Тогда
		Если ОбщегоНазначенияБПОКлиент.ИспользуетсяАвтономнаяККТ() Тогда
			МодульОборудованиеЧекопечатающиеАвтономныеУстройстваКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеЧекопечатающиеАвтономныеУстройстваКлиент");
			МодульОборудованиеЧекопечатающиеАвтономныеУстройстваКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, 
				ИдентификаторКлиента, ПараметрыОперации, ПараметрыВыполнениеКоманды);
		КонецЕсли;
	Иначе
		МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
			ПараметрыОперации, ПараметрыВыполнениеКоманды); 
	КонецЕсли;
		
КонецПроцедуры

// Производить печать текста на фискальном устройстве.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьПечатьТекста(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	КонтекстОперации = Новый Структура();
	КонтекстОперации.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	КонтекстОперации.Вставить("ИдентификаторКлиента"   , ИдентификаторКлиента);   
	КонтекстОперации.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	КонтекстОперации.Вставить("ПараметрыОперации"      , ПараметрыОперации);
	КонтекстОперации.Вставить("ДополнительныеПараметры", ДополнительныеПараметры); 
	КонтекстОперации.Вставить("ПодготовитьДанные"      , Ложь); 
	КонтекстОперации.Вставить("ПодготовитьДанныеКлиент", Истина); 
	КонтекстОперации.Вставить("ОбработатьДанные"       , Ложь); 
	КонтекстОперации.Вставить("Команда"  , "PrintText"); 
	КонтекстОперации.Вставить("Результат", Истина);
	
	ВыполнитьВыборФискальногоУстройства(КонтекстОперации);
	
КонецПроцедуры

// Начать открытие смены на фискальном устройстве.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьОткрытиеСменыНаФискальномУстройстве(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	Контекст = Новый Структура();
	Контекст.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	Контекст.Вставить("ИдентификаторКлиента"   , ИдентификаторКлиента);
	Контекст.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	Контекст.Вставить("ПараметрыОперации"      , ПараметрыОперации);
	Контекст.Вставить("КассаККМ"               , ПараметрыОперации.КассаККМ);
	Оповещение = Новый ОписаниеОповещения("НачатьОткрытиеСменыНаФискальномУстройстве_Завершение", ЭтотОбъект, Контекст);
	
	ПараметрыОперации.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("OpenShift", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Истина, Истина);   
	// Будет сформировано сообщения в службу технической поддержки при ошибке.  
	ПараметрыВыполнениеКоманды.СообщенияВСлужбуТехническойПоддержки = Истина;
	ПараметрыВыполнениеКоманды.ОбработатьДанныеКлиент = Истина;
	
	Если ПараметрыОперации.АвтономнаяККТ Тогда
		Если ОбщегоНазначенияБПОКлиент.ИспользуетсяАвтономнаяККТ() Тогда
			МодульОборудованиеЧекопечатающиеАвтономныеУстройстваКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеЧекопечатающиеАвтономныеУстройстваКлиент");
			МодульОборудованиеЧекопечатающиеАвтономныеУстройстваКлиент.НачатьВыполнениеКоманды(Оповещение, 
				ИдентификаторКлиента, ПараметрыОперации, ПараметрыВыполнениеКоманды);
			КонецЕсли;
	ИначеЕсли ПараметрыОперации.ОблачнаяККТ Тогда
		Если ОбщегоНазначенияБПОКлиент.ИспользуютсяОблачныеККТ() Тогда
			МодульОборудованиеОблачныеККТКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеЧекопечатающиеОблачныеККТКлиент");
			МодульОборудованиеОблачныеККТКлиент.НачатьВыполнениеКоманды(Оповещение, 
				ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ПараметрыВыполнениеКоманды);
		КонецЕсли;
	Иначе
		МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(Оповещение, ИдентификаторКлиента, ИдентификаторУстройства, 
			ПараметрыОперации, ПараметрыВыполнениеКоманды);
	КонецЕсли;
	
КонецПроцедуры

// Завершение операции открытие смены на фискальном устройстве.
//
// Параметры:
//   Результат - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании.
//   Контекст - Структура
//
Процедура НачатьОткрытиеСменыНаФискальномУстройстве_Завершение(Результат, Контекст) Экспорт
	
	Если Результат.Свойство("ПараметрыККТ") Тогда    
		ПараметрыККТ = Результат.ПараметрыККТ;
		Если ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ЭтоККТБезПодключения(Контекст.ПараметрыОперации) Тогда
			ПараметрыККТ.Вставить("ТипОборудования", ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ККТ"));
			ПараметрыККТ.Вставить("ПризнакАвтономногоРежима", Истина);
		КонецЕсли;
		МенеджерОборудованияКлиентПереопределяемый.ПриОткрытииСменыНаККТ(Контекст.ИдентификаторУстройства, Результат.ПараметрыККТ);
	КонецЕсли;
	
	ПоказатьПредупреждениеДрайверСнятыйСПоддержки(Результат);
	
	ВыполнитьОбработкуОповещения(Контекст.ОповещениеПриЗавершении, Результат); 
	
КонецПроцедуры

// Начать закрытие смены на фискальном устройстве.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьЗакрытиеСменыНаФискальномУстройстве(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОперации.Вставить("ДополнительныеПараметры", ДополнительныеПараметры); 
		
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("CloseShift", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Истина, Истина);
	 // Будет сформировано сообщения в службу технической поддержки при ошибке.  
	ПараметрыВыполнениеКоманды.СообщенияВСлужбуТехническойПоддержки = Истина;
	ПараметрыВыполнениеКоманды.ОбработатьДанныеКлиент = Истина;
	
	Если ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ЭтоККТБезПодключения(ПараметрыОперации) Тогда
		Если ОбщегоНазначенияБПОКлиент.ИспользуетсяАвтономнаяККТ() Тогда
			МодульОборудованиеЧекопечатающиеАвтономныеУстройстваКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеЧекопечатающиеАвтономныеУстройстваКлиент");
			МодульОборудованиеЧекопечатающиеАвтономныеУстройстваКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, 
			ИдентификаторКлиента, ПараметрыОперации, ПараметрыВыполнениеКоманды);
		КонецЕсли;
	ИначеЕсли ПараметрыОперации.ОблачнаяККТ Тогда
		Если ОбщегоНазначенияБПОКлиент.ИспользуютсяОблачныеККТ() Тогда
			МодульОборудованиеОблачныеККТКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеЧекопечатающиеОблачныеККТКлиент");
			МодульОборудованиеОблачныеККТКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, 
			ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ПараметрыВыполнениеКоманды);
		КонецЕсли;
	Иначе
		КонтекстОперации = Новый Структура();     
		КонтекстОперации.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
		Оповещение = Новый ОписаниеОповещения("НачатьЗакрытиеСменыНаФискальномУстройстве_Завершение", ЭтотОбъект, КонтекстОперации);
		
		МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(Оповещение, ИдентификаторКлиента, ИдентификаторУстройства, 
			ПараметрыОперации, ПараметрыВыполнениеКоманды);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьЗакрытиеСменыНаФискальномУстройстве_Завершение(Результат, КонтекстОперации) Экспорт
	
	ПоказатьПредупреждениеДрайверСнятыйСПоддержки(Результат);
	
	ВыполнитьОбработкуОповещения(КонтекстОперации.ОповещениеПриЗавершении, Результат);      
	
КонецПроцедуры

// Начать открытие денежного ящика фискальном устройстве.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьОткрытиеДенежногоЯщика(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства = Неопределено, ПараметрыОперации = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт;
	
	КонтекстОперации = Новый Структура();
	КонтекстОперации.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	КонтекстОперации.Вставить("ИдентификаторКлиента"   , ИдентификаторКлиента);   
	КонтекстОперации.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	КонтекстОперации.Вставить("ПараметрыОперации"      , ПараметрыОперации);
	КонтекстОперации.Вставить("ДополнительныеПараметры", ДополнительныеПараметры); 
	КонтекстОперации.Вставить("ПодготовитьДанные"      , Ложь); 
	КонтекстОперации.Вставить("ОбработатьДанные"       , Ложь); 
	КонтекстОперации.Вставить("Команда"  , "OpenCashDrawer"); 
	КонтекстОперации.Вставить("Результат", Истина);
	
	ВыполнитьВыборФискальногоУстройства(КонтекстОперации);
	
КонецПроцедуры

// Получение ширины строки в символах для фискального устройства.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьПолучениеШириныСтрокиПечатающегоУстройства(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства = Неопределено, ПараметрыОперации = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	КонтекстОперации = Новый Структура();
	КонтекстОперации.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	КонтекстОперации.Вставить("ИдентификаторКлиента"   , ИдентификаторКлиента);   
	КонтекстОперации.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	КонтекстОперации.Вставить("ПараметрыОперации"      , ПараметрыОперации);
	КонтекстОперации.Вставить("ДополнительныеПараметры", ДополнительныеПараметры); 
	КонтекстОперации.Вставить("ПодготовитьДанные"      , Ложь); 
	КонтекстОперации.Вставить("ОбработатьДанные"       , Ложь); 
	КонтекстОперации.Вставить("Команда"  , "GetLineLength"); 
	КонтекстОперации.Вставить("Результат", Истина);
	
	ВыполнитьВыборФискальногоУстройства(КонтекстОперации);
	
КонецПроцедуры

// Производить аннулирование чека на фискальном устройстве.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьАннулированиеЧекаНаФискальномУстройстве(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("AnnulCheck", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Ложь, Ложь);
		
	МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
		ПараметрыОперации, ПараметрыВыполнениеКоманды);

КонецПроцедуры

// Начать формирование отчета о текущем состоянии расчетов на фискальном устройстве.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьФормированиеОтчетаОТекущемСостоянииРасчетов(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	КонтекстОперации = Новый Структура();
	КонтекстОперации.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	КонтекстОперации.Вставить("ИдентификаторКлиента"   , ИдентификаторКлиента);   
	КонтекстОперации.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	КонтекстОперации.Вставить("ПараметрыОперации"      , ПараметрыОперации);
	КонтекстОперации.Вставить("ДополнительныеПараметры", ДополнительныеПараметры); 
	КонтекстОперации.Вставить("ПодготовитьДанные"      , Ложь); 
	КонтекстОперации.Вставить("ПодготовитьДанныеКлиент", Истина); 
	КонтекстОперации.Вставить("ОбработатьДанные"       , Ложь); 
	КонтекстОперации.Вставить("Команда"  , "ReportCurrentStatusOfSettlements"); 
	КонтекстОперации.Вставить("Результат", Истина);
	
	ВыполнитьВыборФискальногоУстройства(КонтекстОперации);
	
КонецПроцедуры

// Начать формирование отчета отчет без гашения на фискальном устройстве.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьФормированиеОтчетаБезГашения(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	КонтекстОперации = Новый Структура();
	КонтекстОперации.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	КонтекстОперации.Вставить("ИдентификаторКлиента"   , ИдентификаторКлиента);   
	КонтекстОперации.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	КонтекстОперации.Вставить("ПараметрыОперации"      , ПараметрыОперации);
	КонтекстОперации.Вставить("ДополнительныеПараметры", ДополнительныеПараметры); 
	КонтекстОперации.Вставить("ПодготовитьДанные"      , Ложь); 
	КонтекстОперации.Вставить("ПодготовитьДанныеКлиент", Истина); 
	КонтекстОперации.Вставить("ОбработатьДанные"       , Ложь); 
	КонтекстОперации.Вставить("Команда"  , "PrintXReport"); 
	КонтекстОперации.Вставить("Результат", Истина);
	
	ВыполнитьВыборФискальногоУстройства(КонтекстОперации);
	
КонецПроцедуры

// Производить получение параметров фискального устройства.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьПолучениеПараметровФискальногоУстройства(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ЭтоККТБезПодключения(ПараметрыОперации) Тогда
		
		ПараметрыККТ = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыРегистрацииККТ();
		ПараметрыККТ.Вставить("ТипОборудования", ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ККТ"));
		ПараметрыККТ.Вставить("ПризнакАвтономногоРежима", Истина);

		РезультатОперации = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании(Истина);
		РезультатОперации.Вставить("ПараметрыККТ" , ПараметрыККТ);
		
		ВыполнитьОбработкуОповещения(ОповещениеПриЗавершении, РезультатОперации);
		
	Иначе
		ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("GetDataKKT", 
			"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Ложь, Истина);
			
		МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
			ПараметрыОперации, ПараметрыВыполнениеКоманды);
	КонецЕсли;
	
КонецПроцедуры

// Получение текущего состояния фискального устройства.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьПолучениеТекущегоСостоянияФискальногоУстройства(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("GetCurrentStatus", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Ложь, Истина);
	
	ПараметрыВыполнениеКоманды.ПодготовитьДанныеКлиент = Истина; 
	ПараметрыВыполнениеКоманды.ОбработатьДанныеКлиент = Истина; 
		
	МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
		ПараметрыОперации, ПараметрыВыполнениеКоманды);
	
КонецПроцедуры

// Начать операцию ФН для фискального устройства.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьОперациюФНДляФискальногоУстройства(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("OperationFN", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Истина, Истина);
		
	МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
		ПараметрыОперации, ПараметрыВыполнениеКоманды);
	
КонецПроцедуры

// Начать формирование чека коррекции на фискальном устройстве.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьФормированиеЧекаКоррекцииНаФискальномУстройстве(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("PrintReceiptCorrection", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Истина, Истина);
	
	Если ПараметрыОперации.ОблачнаяККТ Тогда
		Если ОбщегоНазначенияБПОКлиент.ИспользуютсяОблачныеККТ() Тогда
			МодульОборудованиеОблачныеККТКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеЧекопечатающиеОблачныеККТКлиент");
			МодульОборудованиеОблачныеККТКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, 
			ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ПараметрыВыполнениеКоманды);
		КонецЕсли;
	Иначе
		МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
		ПараметрыОперации, ПараметрыВыполнениеКоманды);
	КонецЕсли;
	
КонецПроцедуры

// Осуществляет печать копии чека.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьПечатьКопииЧека(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	КонтекстОперации = Новый Структура();
	КонтекстОперации.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	КонтекстОперации.Вставить("ИдентификаторКлиента"   , ИдентификаторКлиента);   
	КонтекстОперации.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	КонтекстОперации.Вставить("ПараметрыОперации"      , ПараметрыОперации);
	КонтекстОперации.Вставить("ДополнительныеПараметры", ДополнительныеПараметры); 
	КонтекстОперации.Вставить("ПодготовитьДанные"      , Истина); 
	КонтекстОперации.Вставить("ОбработатьДанные"       , Истина); 
	КонтекстОперации.Вставить("Команда"  , "PrintCheckCopy"); 
	КонтекстОперации.Вставить("Результат", Истина);
		
	ВыполнитьВыборФискальногоУстройства(КонтекстОперации);
	
КонецПроцедуры

// Осуществляет печать QR кода на фискальном устройстве.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьПечатьQRКодаНаФискальномУстройстве(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	КонтекстОперации = Новый Структура();
	КонтекстОперации.Вставить("ОповещениеПриЗавершении", ОповещениеПриЗавершении);
	КонтекстОперации.Вставить("ИдентификаторКлиента"   , ИдентификаторКлиента);   
	КонтекстОперации.Вставить("ИдентификаторУстройства", ИдентификаторУстройства);
	КонтекстОперации.Вставить("ПараметрыОперации"      , ПараметрыОперации);
	КонтекстОперации.Вставить("ДополнительныеПараметры", ДополнительныеПараметры); 
	КонтекстОперации.Вставить("ПодготовитьДанные"      , Ложь); 
	КонтекстОперации.Вставить("ПодготовитьДанныеКлиент", Истина); 
	КонтекстОперации.Вставить("ОбработатьДанные"       , Ложь); 
	КонтекстОперации.Вставить("Команда"  , "PrintQRCode"); 
	КонтекстОперации.Вставить("Результат", Истина);
	
	ВыполнитьВыборФискальногоУстройства(КонтекстОперации);
	
КонецПроцедуры

// Осуществляет открытие сессии регистрации КМ.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьОткрытиеСессииРегистрацииКМ(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("OpenSessionRegistrationKM", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Ложь, Ложь);
		
	МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
		ПараметрыОперации, ПараметрыВыполнениеКоманды);
		
КонецПроцедуры

// Осуществляет закрытии сессии регистрации КМ.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьЗакрытииСессииРегистрацииКМ(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("CloseSessionRegistrationKM", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Ложь, Ложь);
		
	МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
		ПараметрыОперации, ПараметрыВыполнениеКоманды);
		
КонецПроцедуры

// Начать подтверждение КМ.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьПодтверждениеКМ(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("ConfirmKM", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Ложь, Ложь);
		
	МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
		ПараметрыОперации, ПараметрыВыполнениеКоманды);
		
КонецПроцедуры

// Осуществляет запрос КМ.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыПроверкиКМ.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьПроверкуКМ(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = МенеджерОборудованияКлиентСервер.ДополнительныеПараметрыОперации(Истина);
	Иначе
		Если ДополнительныеПараметры.Свойство("ОставитьПодключенным") Тогда    
			ДополнительныеПараметры.ОставитьПодключенным = Истина;     
		КонецЕсли;
	КонецЕсли;
		
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("CheckKM", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Ложь, Истина);
	ПараметрыВыполнениеКоманды.Вставить("ОбработатьДанныеПриОшибке", Истина);
	ПараметрыВыполнениеКоманды.ПодготовитьДанные = Ложь;
	ПараметрыВыполнениеКоманды.ПодготовитьДанныеКлиент = Истина;
	ПараметрыОперации.Вставить("ИдентификаторКлиента", ИдентификаторКлиента);
	
	МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
		ПараметрыОперации, ПараметрыВыполнениеКоманды);
	
КонецПроцедуры

// Осуществляет повторный запрос КМ, который выполняет проверку без ожидания ответа от удаленного сервера и без подтверждения пользователя.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыПроверкиКМ.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьПовторнуюПроверкуКМ(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОперации.ОжидатьПолученияОтветаПроверкиКМ = Ложь;
	ПараметрыОперации.Вставить("ИдентификаторКлиента", ИдентификаторКлиента);
	Для Каждого ЗапросКМ Из ПараметрыОперации.ЗапросыКМ Цикл
		ЗапросКМ.ВыполнятьВалидацию = Ложь;
		ЗапросКМ.ЗапросРазрешенияПродажиКМ = Ложь;
		ЗапросКМ.ИгнорироватьРезультатПроверкиКМ  = Истина;
	КонецЦикла;
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("CheckKM", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Истина, Истина);
	ПараметрыВыполнениеКоманды.Вставить("ОбработатьДанныеПриОшибке", Истина);
	
	МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
		ПараметрыОперации, ПараметрыВыполнениеКоманды);
	
КонецПроцедуры

// Функция возвращает поддерживает ли фискальное устройство.
// 
// Параметры:
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - Идентификатор устройства.
// 
// Возвращаемое значение:
//  Булево - Фискальное устройство поддерживает проверку кодов маркировки
Функция ФискальноеУстройствоПоддерживаетПроверкуКодовМаркировки(ИдентификаторУстройства) Экспорт
	
	Возврат ОборудованиеЧекопечатающиеУстройстваВызовСервера.ФискальноеУстройствоПоддерживаетПроверкуКодовМаркировки(ИдентификаторУстройства);
	
КонецФункции

// Функция возвращает для фискального устройства версию ФФД.
// 
// Параметры:
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - Идентификатор устройства.
// 
// Возвращаемое значение:
//  Неопределено - Фискальное устройство поддерживает версию ФФД
Функция ФискальноеУстройствоПоддерживаетВерсиюФФД(ИдентификаторУстройства) Экспорт
	
	Возврат ОборудованиеЧекопечатающиеУстройстваВызовСервера.ФискальноеУстройствоПоддерживаетВерсиюФФД(ИдентификаторУстройства);
	
КонецФункции

// Функция возвращает поддерживает ли фискальное устройство ТС ПИоТ.
// 
// Параметры:
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - Идентификатор устройства.
// 
// Возвращаемое значение:
//  Булево - Фискальное устройство поддерживает ТС ПИоТ
Функция ФискальноеУстройствоПоддерживаетТСПИоТ(ИдентификаторУстройства) Экспорт
	
	Возврат ОборудованиеЧекопечатающиеУстройстваВызовСервера.ФискальноеУстройствоПоддерживаетТСПИоТ(ИдентификаторУстройства);
	
КонецФункции

#Область ПакетнаяОбработка

#Область ФискализацияЧекаСОплатойКартой

// Возвращает структуру для заполнения данными оплаты эквайринговым терминалом и фискализации
//
// Возвращаемое значение:
//   Структура:
//     * ПараметрыФискализацииЧека - См. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека
//     * ПараметрыВыполненияЭквайринговойОперации - См. ОборудованиеПлатежныеСистемыКлиентСервер.ПараметрыВыполненияЭквайринговойОперации
//     * ОповещениеВопросПользователю - ОписаниеОповещения - Описание оповещения пользователя, для самостоятельной выдачи 
//                            сообщения, это оповещение будет вызвано в случае отрицательного ответа от эквайрингового 
//                            терминала при оплате, в процедуру будут переданы результат выполнения пакета операций, 
//                            а также структура содержащая контекст пакета, если требуется продолжить выполнение пакета,
//                            тогда нужно будет вызвать в модуле ОборудованиеЧекопечатающиеУстройстваКлиент процедуры 
//                            "НачатьФискализациюЧекаСОплатойКартой_ПродолжитьВыполнение" или
//                            "НачатьФискализациюЧекаСОплатойКартой_ПовторитьОплату".
//                            передав туда этот контекст.
//                            Процедура оповещения содержит следующие параметры:
//         Результат - Структура:
//           * РезультатОперации - См. РезультатФискализацииЧекаСОплатойКартой
//           * КонтекстОперации - Структура - контекст пакета операции который нужно передать в процедуру
//                            "ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьФискализациюЧекаСОплатойКартой_ПродолжитьВыполнение"
//                            для продолжения выполнения пакета
//         ДополнительныеПараметры - Любой - значение переданное при создании оповещения
//     * СлипЧекВТелеФискального - Булево, Неопределено - Выводить слип-чек в теле фискального, Истина - слип-чек будет выведен
//                            в теле фискального, Ложь - короткий слип-чек не будет выводиться, Неопределено - короткий слип-чек 
//                            будет выводиться если поддерживает драйвер оборудования.
//
// Пример:
//   Определение процедуры оповещения пользователя
//   Процедура ВопросПользователюЗавершение(Результат, ДополнительныеПараметры)
//      РезультатОперации = Результат.РезультатОперации;
//      КонтекстОперации  = Результат.КонтекстОперации;
//      ...
//      // продолжить выполнение пакета операций
//      ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьФискализациюЧекаСОплатойКартой_ПродолжитьВыполнение(Истина, КонтекстОперации);
//   КонецПроцедуры
// 
Функция ПараметрыФискализацияЧекаСОплатойКартой() Экспорт
	
	Результат = ПараметрыЭквайринговаяОперацияСФискализацией();
	Возврат Результат;
	
КонецФункции

// Выполняет оплату на эквайринговом терминале, а затем фискализацию чека с печатью слип-чека в теле фискального,
// если операция при фискализации закончилась неуспешно, тогда происходит аварийная отмена последней операции на 
// эквайринговом терминале.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - Описание процедуры которая будет вызвана при завершении операции
//                             со следующими параметрами:
//     * Результат - См. РезультатФискализацииЧекаСОплатойКартой
//   ИдентификаторКлиента - ФормаКлиентскогоПриложения - идентификатор формы.
//   ИдентификаторККТ - СправочникСсылка.ПодключаемоеОборудование - ККТ на котором будет печататься фискальный чек
//                      содержащий в себе слип-чек.
//   ИдентификаторЭТ - СправочникСсылка.ПодключаемоеОборудование - Эквайринговый терминал для обработки операции.
//   ПараметрыОперации - См. ПараметрыФискализацияЧекаСОплатойКартой
//   ДополнительныеПараметры - Структура - Дополнительные параметры которые можно передать в операции 
//                             оплаты и фискализации.
Процедура НачатьФискализациюЧекаСОплатойКартой(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторККТ, 
	ИдентификаторЭТ, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	// Подготовить контекст пакета операций
	КонтекстОперации = КонтекстЭквайринговойОперацииСФискализацией();
	КонтекстОперации.ТипОперации = "ФискализацияЧекаСОплатой";

	КонтекстОперации.ОповещениеЗавершения = ОповещениеПриЗавершении;
	КонтекстОперации.ИдентификаторКлиента = ИдентификаторКлиента;
	КонтекстОперации.ИдентификаторККТ     = ИдентификаторККТ;
	КонтекстОперации.ИдентификаторЭТ      = ИдентификаторЭТ;
	ЗаполнитьЗначенияСвойств(КонтекстОперации, ПараметрыОперации);
	
	ПодготовитьКонтекстПакетнойОперации(КонтекстОперации, ДополнительныеПараметры);

	// Выбрать ЭТ если не указан конкретный
	ОповещениеЗавершенияПодключения = Новый ОписаниеОповещения("НачатьЭкваринговуюОперациюСФискализацией_ЭквайринговаяОперация",
		ЭтотОбъект,
		КонтекстОперации);
	ПакетнаяОбработка_ВыборУстройства(ОповещениеЗавершенияПодключения, КонтекстОперации);

КонецПроцедуры

// Запускает продолжение выполнения пакета, после обработки вопроса пользователя, или же успешной операции на 
// эквайринговом терминале
// 
// Параметры:
//   Результат - Булево - Если Истина, тогда продолжается выполнение операции, иначе вызывается оповещение
//               пользователя о завершении работы пакета
//   КонтекстОперации - см. КонтекстЭквайринговойОперацииСФискализацией
Процедура НачатьФискализациюЧекаСОплатойКартой_ПродолжитьВыполнение(Результат, КонтекстОперации) Экспорт
	
	ОписаниеОшибки = КонтекстОперации.РезультатВыполненияЭквайринговойОперации.ОписаниеОшибки;

	Если Результат Тогда
		НачатьЭкваринговуюОперациюСФискализацией_Фискализация(КонтекстОперации);
	Иначе
		КонтекстОперации.Результат = Ложь;
		КонтекстОперации.ОписаниеОшибки = ОписаниеОшибки;
		ВывестиСлипЧекКакТекстовыйДокумент("Отказ", КонтекстОперации);
	КонецЕсли;
	
КонецПроцедуры

// Запускает повторно эквайринговой операции.
// 
// Параметры:
//   КонтекстОперации - см. КонтекстЭквайринговойОперацииСФискализацией
Процедура НачатьФискализациюЧекаСОплатойКартой_ПовторитьОплату(КонтекстОперации) Экспорт
	
	ОписаниеОшибки = КонтекстОперации.РезультатВыполненияЭквайринговойОперации.ОписаниеОшибки;

	РезультатВыполнения = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании();
	РезультатВыполнения.Результат              = Истина;
	РезультатВыполнения.ОписаниеОшибки         = ОписаниеОшибки;
	РезультатВыполнения.ПодключенноеУстройство = КонтекстОперации.ПодключенныйЭТ;
	НачатьЭкваринговуюОперациюСФискализацией_ЭквайринговаяОперация(РезультатВыполнения, КонтекстОперации);
	
КонецПроцедуры

#КонецОбласти

#Область ПокупкаСЗачислением

// Возвращает структуру для заполнения данными выплаты эквайринговым терминалом и фискализации
//
// Возвращаемое значение:
//   Структура:
//     * ПараметрыФискализацииЧека - См. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека
//     * ПараметрыВыполненияЭквайринговойОперации - См. ОборудованиеПлатежныеСистемыКлиентСервер.ПараметрыВыполненияЭквайринговойОперации
//     * ОповещениеВопросПользователю - ОписаниеОповещения - Описание оповещения пользователя, для самостоятельной выдачи 
//                            сообщения, это оповещение будет вызвано в случае отрицательного ответа от эквайрингового 
//                            терминала при оплате, в процедуру будут переданы результат выполнения пакета операций, 
//                            а также структура содержащая контекст пакета, если требуется продолжить выполнение пакета,
//                            тогда нужно будет вызвать в модуле ОборудованиеЧекопечатающиеУстройстваКлиент процедуры 
//                            "НачатьФискализациюЧекаСОплатойКартой_ПродолжитьВыполнение" или
//                            "НачатьФискализациюЧекаСОплатойКартой_ПовторитьОплату".
//                            передав туда этот контекст.
//                            Процедура оповещения содержит следующие параметры:
//         Результат - Структура:
//           * РезультатОперации - См. РезультатФискализацииЧекаСОплатойКартой
//           * КонтекстОперации - Структура - контекст пакета операции который нужно передать в процедуру
//                            "ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьФискализациюЧекаСОплатойКартой_ПродолжитьВыполнение"
//                            для продолжения выполнения пакета
//         ДополнительныеПараметры - Любой - значение переданное при создании оповещения
//     * СлипЧекВТелеФискального - Булево, Неопределено - Выводить слип-чек в теле фискального, Истина - слип-чек будет выведен
//                            в теле фискального, Ложь - короткий слип-чек не будет выводиться, Неопределено - короткий слип-чек 
//                            будет выводиться если поддерживает драйвер оборудования.
//
// Пример:
//   Определение процедуры оповещения пользователя
//   Процедура ВопросПользователюЗавершение(Результат, ДополнительныеПараметры)
//      РезультатОперации = Результат.РезультатОперации;
//      КонтекстОперации  = Результат.КонтекстОперации;
//      ...
//      // продолжить выполнение пакета операций
//      ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьФискализациюЧекаСОплатойКартой_ПродолжитьВыполнение(Истина, КонтекстОперации);
//   КонецПроцедуры
// 
Функция ПараметрыПокупкаСЗачислением() Экспорт
	
	Результат = ПараметрыЭквайринговаяОперацияСФискализацией();
	Возврат Результат;
	
КонецФункции

// Выполняет оплату на эквайринговом терминале, а затем фискализацию чека с печатью слип-чека в теле фискального,
// если операция при фискализации закончилась неуспешно, тогда происходит аварийная отмена последней операции на 
// эквайринговом терминале.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - Описание процедуры которая будет вызвана при завершении операции
//                             со следующими параметрами:
//     * Результат - См. РезультатФискализацииЧекаСОплатойКартой
//   ИдентификаторКлиента - ФормаКлиентскогоПриложения - идентификатор формы.
//   ИдентификаторККТ - СправочникСсылка.ПодключаемоеОборудование - ККТ на котором будет печататься фискальный чек
//                      содержащий в себе слип-чек.
//   ИдентификаторЭТ - СправочникСсылка.ПодключаемоеОборудование - Эквайринговый терминал для обработки операции.
//   ПараметрыОперации - См. ПараметрыФискализацияЧекаСОплатойКартой
//   ДополнительныеПараметры - Структура - Дополнительные параметры которые можно передать в операции 
//                             оплаты и фискализации.
Процедура НачатьПокупкаСЗачислением(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторККТ, 
	ИдентификаторЭТ, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	// Подготовить контекст пакета операций
	КонтекстОперации = КонтекстЭквайринговойОперацииСФискализацией();
	КонтекстОперации.ТипОперации = "ПокупкаСЗачислением";
	
	КонтекстОперации.ОповещениеЗавершения = ОповещениеПриЗавершении;
	КонтекстОперации.ИдентификаторКлиента = ИдентификаторКлиента;
	КонтекстОперации.ИдентификаторККТ     = ИдентификаторККТ;
	КонтекстОперации.ИдентификаторЭТ      = ИдентификаторЭТ;
	ЗаполнитьЗначенияСвойств(КонтекстОперации, ПараметрыОперации);
	
	ПодготовитьКонтекстПакетнойОперации(КонтекстОперации, ДополнительныеПараметры);

	// Выбрать ЭТ если не указан конкретный
	ОповещениеЗавершенияПодключения = Новый ОписаниеОповещения("НачатьЭкваринговуюОперациюСФискализацией_ЭквайринговаяОперация",
		ЭтотОбъект,
		КонтекстОперации);
	ПакетнаяОбработка_ВыборУстройства(ОповещениеЗавершенияПодключения, КонтекстОперации);

КонецПроцедуры

// Запускает продолжение выполнения пакета, после обработки вопроса пользователя, или же успешной операции на 
// эквайринговом терминале
// 
// Параметры:
//   Результат - Булево - Если Истина, тогда продолжается выполнение операции, иначе вызывается оповещение
//               пользователя о завершении работы пакета
//   КонтекстОперации - см. КонтекстЭквайринговойОперацииСФискализацией
Процедура НачатьПокупкаСЗачислением_ПродолжитьВыполнение(Результат, КонтекстОперации) Экспорт
	
	ОписаниеОшибки = КонтекстОперации.РезультатВыполненияЭквайринговойОперации.ОписаниеОшибки;

	Если Результат Тогда
		НачатьЭкваринговуюОперациюСФискализацией_Фискализация(КонтекстОперации);
	Иначе
		КонтекстОперации.Результат = Ложь;
		КонтекстОперации.ОписаниеОшибки = ОписаниеОшибки;
		ВывестиСлипЧекКакТекстовыйДокумент("Отказ", КонтекстОперации);
	КонецЕсли;
	
КонецПроцедуры

// Запускает повторно эквайринговой операции.
// 
// Параметры:
//   КонтекстОперации - см. КонтекстЭквайринговойОперацииСФискализацией
Процедура НачатьПокупкаСЗачислением_ПовторитьОплату(КонтекстОперации) Экспорт
	
	ОписаниеОшибки = КонтекстОперации.РезультатВыполненияЭквайринговойОперации.ОписаниеОшибки;

	РезультатВыполнения = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании();
	РезультатВыполнения.Результат              = Истина;
	РезультатВыполнения.ОписаниеОшибки         = ОписаниеОшибки;
	РезультатВыполнения.ПодключенноеУстройство = КонтекстОперации.ПодключенныйЭТ;
	НачатьЭкваринговуюОперациюСФискализацией_ЭквайринговаяОперация(РезультатВыполнения, КонтекстОперации);
	
КонецПроцедуры

#КонецОбласти

#Область ПродажаСВыдачейНаличных

// Возвращает структуру для заполнения данными операции продажи с выдачей наличных
//
// Возвращаемое значение:
//   Структура:
//     * ПараметрыФискализацииЧека - См. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека
//     * ПараметрыФискализацииЧекаВыдачи - См. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека
//     * ПараметрыФискализацииЧекаВозврата - См. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека
//     * ПараметрыВыполненияЭквайринговойОперации - См. ОборудованиеПлатежныеСистемыКлиентСервер.ПараметрыВыполненияЭквайринговойОперации
//     * ПризнакАгента - ПеречислениеСсылка.ПризнакиАгента - признак агента для автоматического формирования чека выдачи,
//                            см стандарт, по умолчанию Банковский платежный агент
//     * ДанныеАгента - См. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыДанныеАгента
//     * ДанныеПоставщика - См. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыДанныеПоставщика
//     * СлипЧекВТелеФискального - Булево, Неопределено - Выводить слип-чек в теле фискального, Истина - слип-чек будет выведен
//                            в теле фискального, Ложь - короткий слип-чек не будет выводиться, Неопределено - короткий слип-чек 
//                            будет выводиться если поддерживает драйвер оборудования.
//     * ВыводитьУведомленияПользователя - Булево - если установлена Истина (по умолчанию), тогда при ошибке операции
//                                         в пакете, будет выведено сообщение пользователю.
//     * СуммаВыдачи - Число - сумма выдачи наличных денежных средств
Функция ПараметрыПродажаСВыдачейНаличных() Экспорт
	
	ПараметрыОперацииФискализацииЧекаПродажи  = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека();
	ПараметрыВыполненияЭквайринговойОперации  = Неопределено;
	ПараметрыДанныеАгента                     = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыДанныеАгента();
	ПараметрыДанныеПоставщика                 = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыДанныеПоставщика();
	ПризнакАгента                             = ПредопределенноеЗначение("Перечисление.ПризнакиАгента.БанковскийПлатежныйАгент");
	
	Если ОбщегоНазначенияБПОКлиент.ИспользуетсяПлатежныеСистемы() Тогда
		МодульОборудованиеПлатежныеСистемыКлиентСервер = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиентСервер");
		ПараметрыВыполненияЭквайринговойОперации = МодульОборудованиеПлатежныеСистемыКлиентСервер.ПараметрыВыполненияЭквайринговойОперации();
	КонецЕсли;
	
	Результат = Новый Структура();
	Результат.Вставить("ПараметрыФискализацииЧека",                ПараметрыОперацииФискализацииЧекаПродажи);
	Результат.Вставить("ПараметрыФискализацииЧекаВыдачи",          Неопределено);
	Результат.Вставить("ПараметрыФискализацииЧекаВозврата",        Неопределено);
	Результат.Вставить("ПараметрыВыполненияЭквайринговойОперации", Неопределено);
	Результат.Вставить("ПризнакАгента",                            ПризнакАгента);
	Результат.Вставить("ДанныеАгента",                             ПараметрыДанныеАгента);
	Результат.Вставить("ДанныеПоставщика",                         ПараметрыДанныеПоставщика);
	Результат.Вставить("СлипЧекВТелеФискального",                  Неопределено);
	Результат.Вставить("ВыводитьУведомленияПользователя",          Ложь);
	Результат.Вставить("СуммаВыдачи",                              0);
	Результат.Вставить("ДокументОснованиеВыдачи",                  Неопределено);
	Результат.Вставить("ДокументОснованиеВозврата",                Неопределено);
	Результат.Вставить("РеквизитыКартыQR",                         "");
	
	Возврат Результат;
	
КонецФункции

// Заполняет параметры фискализации чека выдачи, по данным эквайринговой операции, параметров чека продажи,
// и данных переданных в параметрах операции, тогда они будут получены из чека продажи в соответствии 
// с методическими указаниями для ФФД 1.05 и 1.1 ФНС России 
//
// Параметры:
//   ПараметрыОперации - см. ПараметрыПродажаСВыдачейНаличных
//
Процедура ЗаполнитьПараметрыЧекаВыдачи(ПараметрыОперации) Экспорт
	ПризнакАгента                            = ПараметрыОперации.ПризнакАгента;
	ДанныеАгента                             = ПараметрыОперации.ДанныеАгента;
	ДанныеПоставщика                         = ПараметрыОперации.ДанныеПоставщика;
	ПараметрыФискализацииЧека                = ПараметрыОперации.ПараметрыФискализацииЧека;
	ПараметрыВыполненияЭквайринговойОперации = ПараметрыОперации.ПараметрыВыполненияЭквайринговойОперации;
	СуммаНаличных                            = ПараметрыВыполненияЭквайринговойОперации.СуммаНаличных;

	// создать чек выдачи наличных
	ПараметрыЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека();
	
	СписокСвойствДляКопирования = "ДокументОснование, Организация, ТорговыйОбъект, Электронно, Отправляет1СSMS, 
		|Отправляет1СEmail, СистемаНалогообложения, АдресРасчетов, МестоРасчетов, НомерАвтомата, ОтправительEmail, 
		|ПокупательEmail, ПокупательНомер, Получатель, ПолучательИНН, СубъектПерсональныхДанных, ЕстьПерсональныеДанные,
		|ТипПерсональныхДанных, СерийныйНомер, ОрганизацияНазвание, ОрганизацияИНН, ОрганизацияКПП, АдресМагазина, 
		|НаименованиеМагазина, НомерКассы, НомерСмены, ДатаВремя, ТекстШапки, ТекстПодвала, КопийЧека, КассаККМ";
	ЗаполнитьЗначенияСвойств(ПараметрыЧека, ПараметрыФискализацииЧека, СписокСвойствДляКопирования);

	Если ЗначениеЗаполнено(ПараметрыОперации.ДокументОснованиеВыдачи) Тогда
		ПараметрыЧека.ДокументОснование = ПараметрыОперации.ДокументОснованиеВыдачи;
	КонецЕсли;
	ПараметрыЧека.ТипРасчета        = ПредопределенноеЗначение("Перечисление.ТипыРасчетаДенежнымиСредствами.РасходДенежныхСредств");
	ПараметрыЧека.НомерЧека         = Число(ПараметрыФискализацииЧека.НомерЧека) + 1;
	
	ЗаполнитьЗначенияСвойств(ПараметрыЧека.СведенияОПокупателе, ПараметрыФискализацииЧека.СведенияОПокупателе);
	
	// Позиции чека для фискализации.
	ПараметрыЧека.ПозицииЧека.Очистить();
	СтрокаПозицииЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыФискальнойСтрокиЧека(); 
	СтрокаПозицииЧека.НомерСтрокиТовара      = 1;
	СтрокаПозицииЧека.Наименование           = НСтр("ru = 'Выдача наличных денежных средств';
													|en = 'Cash issue'");
	СтрокаПозицииЧека.Количество             = 1;
	СтрокаПозицииЧека.Цена                   = СуммаНаличных;
	СтрокаПозицииЧека.ЦенаСоСкидками         = СуммаНаличных;
	СтрокаПозицииЧека.Сумма                  = СуммаНаличных;
	СтрокаПозицииЧека.СтавкаНДС              = Неопределено;
	СтрокаПозицииЧека.СуммаНДС               = 0;
	СтрокаПозицииЧека.ЕдиницаИзмерения       = НСтр("ru = 'Штука';
													|en = 'Piece'");
	СтрокаПозицииЧека.КодЕдиницыИзмерения    = "796";
	СтрокаПозицииЧека.ДанныеАгента           = ПараметрыЧека.ДанныеАгента;
	СтрокаПозицииЧека.ДанныеПоставщика       = ПараметрыЧека.ДанныеПоставщика;
	СтрокаПозицииЧека.ПризнакСпособаРасчета  = ПредопределенноеЗначение("Перечисление.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой");
	СтрокаПозицииЧека.ПризнакПредметаРасчета = ПредопределенноеЗначение("Перечисление.ПризнакиПредметаРасчета.Услуга");
	СтрокаПозицииЧека.ПризнакАгентаПоПредметуРасчета = ПризнакАгента;
	СтрокаПозицииЧека.ДанныеАгента           = ДанныеАгента;
	СтрокаПозицииЧека.ДанныеПоставщика       = ДанныеПоставщика;
	
	
	ПараметрыЧека.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	
	ПараметрыЧека.ТаблицаОплат.Очистить();
	СтрокаОплаты           = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыСтрокиОплаты(); 
	СтрокаОплаты.Сумма     = СуммаНаличных; 
	СтрокаОплаты.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Наличные");
	ПараметрыЧека.ТаблицаОплат.Добавить(СтрокаОплаты);

	ПараметрыЧека.ДетализацияОплаты.Очистить();
	
	ПараметрыОперации.ПараметрыФискализацииЧекаВыдачи = ПараметрыЧека;
	
КонецПроцедуры

// Заполняет параметры фискализации чека возврата формируемого в случае невыполнения фискализации чека выдачи, 
// по данным параметров чека продажи
// 
// Параметры:
//   ПараметрыОперации - см. ПараметрыПродажаСВыдачейНаличных
//
Процедура ЗаполнитьПараметрыЧекаВозврата(ПараметрыОперации) Экспорт

	ПараметрыФискализацииЧека                = ПараметрыОперации.ПараметрыФискализацииЧека;
	
	// создать чек возврата
	ПараметрыЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииЧекаКоррекции();
	
	ЗаполнитьЗначенияСвойств(ПараметрыЧека, ПараметрыФискализацииЧека);
	ПараметрыЧека.НомерЧека  = Число(ПараметрыФискализацииЧека.НомерЧека) + 1;
	ПараметрыЧека.ТипРасчета = ПредопределенноеЗначение("Перечисление.ТипыРасчетаДенежнымиСредствами.ВозвратДенежныхСредств");
	Если ЗначениеЗаполнено(ПараметрыОперации.ДокументОснованиеВозврата) Тогда
		ПараметрыЧека.ДокументОснование = ПараметрыОперации.ДокументОснованиеВозврата;
	ИначеЕсли ЗначениеЗаполнено(ПараметрыОперации.ДокументОснованиеВыдачи) Тогда
		ПараметрыЧека.ДокументОснование = ПараметрыОперации.ДокументОснованиеВыдачи;
	КонецЕсли;
	
	ПараметрыОперации.ПараметрыФискализацииЧекаВозврата = ПараметрыЧека;

КонецПроцедуры

// Выполняет операцию оплата с выдачей наличных на эквайринговом терминале, затем фискализацию чека продажи 
// фискализацию чека выдачи. Если фискализация чека продажи не проходит, тогда выполняется аварийная отмена
// последней эквайринговой операции. Если фискализация чека выдачи не проходит тогда выполняется отмена операции 
// продажи с помощью чека коррекции, а затем выполняется аварийная отмена последней эквайринговой операции.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - Описание процедуры которая будет вызвана при завершении операции
//                             со следующими параметрами:
//     * Результат - См. ОборудованиеЧекопечатающиеУстройстваКлиент.РезультатПродажаСВыдачейНаличных
//   ИдентификаторКлиента - ФормаКлиентскогоПриложения - идентификатор формы.
//   ИдентификаторККТ - СправочникСсылка.ПодключаемоеОборудование - ККТ на котором будет печататься фискальный чек
//                      содержащий в себе слип-чек.
//   ИдентификаторЭТ - СправочникСсылка.ПодключаемоеОборудование - Эквайринговый терминал для обработки операции.
//   ПараметрыОперации - См. ПараметрыПродажаСВыдачейНаличных
//   ДополнительныеПараметры - Структура - Дополнительные параметры которые можно передать в операции 
//                             оплаты и фискализации.
Процедура НачатьПродажаСВыдачейНаличных(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторККТ, 
	ИдентификаторЭТ, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	КонтекстОперации                      = КонтекстПродажаСВыдачейНаличных();
	КонтекстОперации.ОповещениеЗавершения = ОповещениеПриЗавершении;
	КонтекстОперации.ИдентификаторКлиента = ИдентификаторКлиента;
	КонтекстОперации.ИдентификаторККТ     = ИдентификаторККТ;
	КонтекстОперации.ИдентификаторЭТ      = ИдентификаторЭТ;
	ЗаполнитьЗначенияСвойств(КонтекстОперации, ПараметрыОперации);
	
	ПодготовитьКонтекстПакетнойОперации(КонтекстОперации, ДополнительныеПараметры);
	
	Если ПараметрыОперации.ПараметрыВыполненияЭквайринговойОперации = Неопределено Тогда
		ПараметрыОперации.ПараметрыВыполненияЭквайринговойОперации = КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации;
	КонецЕсли;
	Если КонтекстОперации.ПараметрыФискализацииЧекаВыдачи = Неопределено Тогда
		ЗаполнитьПараметрыЧекаВыдачи(ПараметрыОперации);
		КонтекстОперации.ПараметрыФискализацииЧекаВыдачи = ПараметрыОперации.ПараметрыФискализацииЧекаВыдачи;
	КонецЕсли;
	Если КонтекстОперации.ПараметрыФискализацииЧекаВозврата = Неопределено Тогда
		ЗаполнитьПараметрыЧекаВозврата(ПараметрыОперации);
		КонтекстОперации.ПараметрыФискализацииЧекаВозврата = ПараметрыОперации.ПараметрыФискализацииЧекаВозврата;
	КонецЕсли;
	
	// Выбрать ЭТ если не указан конкретный
	ОповещениеЗавершенияПодключения = Новый ОписаниеОповещения("НачатьПродажаСВыдачейНаличных_ЭквайринговаяОперация",
		ЭтотОбъект,
		КонтекстОперации);
	ПакетнаяОбработка_ВыборУстройства(ОповещениеЗавершенияПодключения, КонтекстОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

// Возвращает структуру для заполнения данных открытия формы настройки регистрации ККТ
//
// Возвращаемое значение:
//  Структура:
//    * ФискальноеУстройство - СправочникСсылка.ПодключаемоеОборудование - ссылка на ККТ
//    * Организация - ОпределяемыйТип.ОрганизацияБПО
//    * ТипОперации - Число
//    * ОбработчикЗавершения - ОписаниеОповещения
Функция ПараметрыФормаНастройкиРегистрацииККТ() Экспорт
	
	Параметры = Новый Структура();
	Параметры.Вставить("ФискальноеУстройство");
	Параметры.Вставить("Организация");
	Параметры.Вставить("ТипОперации", 0);
	Возврат Параметры;
	
КонецФункции

// Выполняет открытие формы настроек для регистрации ККТ
//
// Параметры:
//  ОповещениеПриЗавершении - ОписаниеОповещения
//  ПараметрыОткрытия - см. ПараметрыФормаНастройкиРегистрацииККТ
Процедура ОткрытьФормуНастройкиРегистрацииККТ(ОповещениеПриЗавершении, ПараметрыОткрытия) Экспорт
	
	ОборудованиеЧекопечатающиеУстройстваКлиентЛокализация.ОткрытьФормуНастройкиРегистрацииККТ(ОповещениеПриЗавершении, ПараметрыОткрытия);
	
КонецПроцедуры

// Возвращает планируемые статусы маркированного товара
//
// Возвращаемое значение:
//  Соответствие
Функция ПланируемыеСтатусыМаркированногоТовара() Экспорт
	
	Результат = Новый Соответствие();
	
	// ++ Локализация
	Результат.Вставить("ШтучныйТоварРеализован"       , ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.ШтучныйТоварРеализован"));
	Результат.Вставить("МерныйТоварВСтадииРеализации" , ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.МерныйТоварВСтадииРеализации"));
	Результат.Вставить("ШтучныйТоварВозвращен"        , ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.ШтучныйТоварВозвращен"));
	Результат.Вставить("ЧастьТовараВозвращена"        , ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.ЧастьТовараВозвращена"));      
	Результат.Вставить("ШтучныйТоварВСтадииРеализации", ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.ШтучныйТоварВСтадииРеализации"));      
	Результат.Вставить("МерныйТоварРеализован"        , ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.МерныйТоварРеализован"));      
	Результат.Вставить("СтатусТовараНеИзменился"      , ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.СтатусТовараНеИзменился"));
	// -- Локализация
	
	Возврат Результат;
	
КонецФункции

// Возвращает значение мера количества предмета расчета литр
//
// Возвращаемое значение:
//  ПеречислениеСсылка.МераКоличестваПредметаРасчетаККТ
Функция МераКоличестваПредметаРасчетаЛитр() Экспорт
	
	Возврат ПредопределенноеЗначение("Перечисление.МераКоличестваПредметаРасчетаККТ.Литр");
	
КонецФункции

// Возвращает значение статус обработки запроса по умолчанию
//
// Возвращаемое значение:
//  ПеречислениеСсылка
Функция СтатусОбработкиЗапросаПоУмолчанию() Экспорт
	
	// ++ Локализация
	Возврат ПредопределенноеЗначение("Перечисление.СтатусОбработкиЗапросаКМ.ПустаяСсылка");
	// -- Локализация
	
КонецФункции

// Возвращает значение статус результата ожидается
//
// Возвращаемое значение:
//  ПеречислениеСсылка
Функция СтатусРезультатаОжидается() Экспорт
	
	// ++ Локализация
	Возврат ПредопределенноеЗначение("Перечисление.СтатусРезультатаЗапросаКМ.Ожидается");
	// -- Локализация
	
КонецФункции

// Возвращает значение статус товара по умолчанию
//
// Возвращаемое значение:
//  ПеречислениеСсылка
Функция СтатусТовараПоУмолчанию() Экспорт
	
	// ++ Локализация
	Возврат ПредопределенноеЗначение("Перечисление.ОтветОИСМОСтатусеТовара.ПустаяСсылка");
	// -- Локализация
	
КонецФункции

// Возвращает значение статус результата по умолчанию
//
// Возвращаемое значение:
//  ПеречислениеСсылка
Функция СтатусРезультатаПоУмолчанию() Экспорт
	
	// ++ Локализация
	Возврат ПредопределенноеЗначение("Перечисление.СтатусРезультатаЗапросаКМ.ПустаяСсылка");
	// -- Локализация
	
КонецФункции

// Осуществляет запрос КМ без ожидания ответа.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьЗапросКМБезОжиданияОтвета(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("RequestKMKMNoWait", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Ложь, Истина);
	ПараметрыВыполнениеКоманды.ПодготовитьДанныеКлиент = Истина;
		
	МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
		ПараметрыОперации, ПараметрыВыполнениеКоманды);
	
КонецПроцедуры

#Область КодыОФД

///////////////////////////////////////////////////////////////////////////////
// Коды единицы измерения

// Возвращает код меры количества предмета расчета ККТ.
// 
// Параметры:
//  МераКоличестваПредметаРасчетаККТ - ПеречислениеСсылка.МераКоличестваПредметаРасчетаККТ - Мера количества предмета расчета ККТ
// 
// Возвращаемое значение:
//  Произвольный, Число - Код меры количества предмета расчета ККТ     
Функция КодМерыКоличестваПредметаРасчетаККТ(МераКоличестваПредметаРасчетаККТ) Экспорт
	
	КлассификаторыБиблиотеки = МенеджерОборудованияКлиент.КлассификаторыБиблиотеки();
	КодыМеры = КлассификаторыБиблиотеки.КодыМерыКоличестваПредметаРасчетаККТ;
	Возврат КодыМеры.Получить(МераКоличестваПредметаРасчетаККТ);
	
КонецФункции

// Возвращает меру количества предмета расчета по коду.
// 
// Параметры:
//  КодМерыКоличестваПредметаРасчетаККТ - ПеречислениеСсылка.МераКоличестваПредметаРасчетаККТ - Код меры количества предмета расчета ККТ
// 
// Возвращаемое значение:                                                                                    
//  Произвольный, ПеречислениеСсылка.МераКоличестваПредметаРасчетаККТ - Мера количества предмета расчета ККТ по коду        
//
Функция МераКоличестваПредметаРасчетаККТПоКоду(КодМерыКоличестваПредметаРасчетаККТ) Экспорт
	
	КлассификаторыБиблиотеки = МенеджерОборудованияКлиент.КлассификаторыБиблиотеки();
	МераКоличества = КлассификаторыБиблиотеки.МераКоличестваПредметаРасчетаККТПоКоду;
	Возврат МераКоличества.Получить(КодМерыКоличестваПредметаРасчетаККТ);
	
КонецФункции

// Возвращает меру количества предмета расчета по коду единицы измерения.
// 
// Параметры:
//  КодЕдиницыИзмерения - Число - Код единицы измерения.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.МераКоличестваПредметаРасчетаККТ, Произвольный - Мера количества предмета расчета по коду единицы измерения
Функция МераКоличестваПредметаРасчетаПоКодуЕдиницыИзмерения(КодЕдиницыИзмерения) Экспорт
	
	КлассификаторыБиблиотеки = МенеджерОборудованияКлиент.КлассификаторыБиблиотеки();
	МераКоличества = КлассификаторыБиблиотеки.МераКоличестваПредметаРасчетаПоКодуЕдиницыИзмерения;
	Результат = МераКоличества.Получить(Строка(КодЕдиницыИзмерения));
	Если Результат = Неопределено Тогда
		Результат = МераКоличества.Получить(Неопределено);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает код единицы измерения по ККТ коду.
// 
// Параметры:
//  КодМерыКоличестваПредметаРасчетаККТ - Число - Код меры количества предмета расчета ККТ.
// 
// Возвращаемое значение:
//  Строка
//
Функция КодЕдиницыИзмеренияПоККТКоду(КодМерыКоличестваПредметаРасчетаККТ) Экспорт
	
	КлассификаторыБиблиотеки = МенеджерОборудованияКлиент.КлассификаторыБиблиотеки();
	МераКоличества = КлассификаторыБиблиотеки.КодыЕдиницИзмеренияПоККТКоду;
	КодЕдиницыИзмерения = МераКоличества.Получить(КодМерыКоличестваПредметаРасчетаККТ);
	Возврат КодЕдиницыИзмерения;
	
КонецФункции

#КонецОбласти

#Область УстаревшиеПроцедурыИФункции

// Устарела: следует использовать НачатьПроверкуКМ.
// Осуществляет запрос КМ.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьЗапросКМ(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("RequestKM", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Ложь, Ложь);
	ПараметрыВыполнениеКоманды.ПодготовитьДанныеКлиент = Истина;
	ПараметрыВыполнениеКоманды.ОбработатьДанныеКлиент = Истина;
		
	МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
		ПараметрыОперации, ПараметрыВыполнениеКоманды);
	
КонецПроцедуры

// Устарела: следует использовать НачатьПроверкуКМ.
// Начать получения результатов запроса КМ.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьПолученияРезультатовЗапросаКМ(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, ПараметрыОперации, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды("GetProcessingKMResult", 
		"ОборудованиеЧекопечатающиеУстройства", ДополнительныеПараметры, Ложь, Истина);
		
	МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства, 
		ПараметрыОперации, ПараметрыВыполнениеКоманды);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Подготовить данные операции, перед выполнением команды.
// 
// Параметры:
//  ПараметрыПодключения - Структура - см. МенеджерОборудованияКлиентСервер.ПараметрыПодключения()
//  Команда - Строка - команда для выполнения
//  ПараметрыОперации - Структура - 
// 
// Возвращаемое значение:
//  Структура - Подготовить данные операции:
//  * Кассир - ОпределяемыйТип.СубъектПерсональныхДанныхБПО
//  * КассирИНН - Число
//  * ДокументОснование - ОпределяемыйТип.ОснованиеФискальнойОперацииБПО
//  * ИдентификаторФискальнойЗаписи - УникальныйИдентификатор
//  * Организация - ОпределяемыйТип.ОрганизацияБПО.
//  * ТорговыйОбъект - ОпределяемыйТип.ТорговыйОбъектБПО
//  * КорректируемыйДокумент - ОпределяемыйТип.ОснованиеФискальнойОперацииБПО
//  * ЕстьПерсональныеДанные - Булево -
//  * СубъектПерсональныхДанных - ОпределяемыйТип.СубъектПерсональныхДанныхБПО
//  * ТипПерсональныхДанных - ПеречислениеСсылка.ТипыПерсональныхДанныхККТ.
//  * ТипРасчета - ПеречислениеСсылка.ТипыРасчетаДенежнымиСредствами
//  * ТипДокумента - ПеречислениеСсылка.ТипыФискальныхДокументовККТ
//  * РевизияИнтерфейса - Число -
//  * ШиринаСтроки - Число - 
//  * РегистрационныйНомерККТ - Строка -
//  * ЗаводскойНомерФН - Строка - 
//  * ДополнительныйРеквизит - Строка - 
//  * НомерСменыККТ - Число -
//  * НомерЧекаККТ - Число -
//  * НомерЧекаЗаСмену - Число -
//  * ДатаВремяЧека - Дата
//  * СуммаЧека - Число -
//  * ОплатаНаличные - Число -
//  * ОплатаЭлектронно - Число -
//  * ОплатаПредоплата - Число -
//  * ОплатаПостоплата - Число -
//  * ОплатаВстречноеПредоставление - Число -
//  * ФискальныйПризнак - Строка -
//  * АдресСайтаПроверки - Строка -
//  * ОшибкаПечати - Булево -
//  * Электронно - Булево -
//  * Отправляет1СSMS - Булево -
//  * Отправляет1СEmail - Булево -
//  * АвтономныйРежим - Булево -
//  * ДанныеЧекаXML - Строка
//  * ДанныеЧекаXMLПерсональныеДанные - Строка 
//  * РезультатОперацииXML - Булево
//  * ТекстСообщения - Строка
//  * ТекущееСостояние - Структура -:
//    ** НомерСменыККТ - Число -
//    ** НомерЧекаККТ - Число -
//    ** СтатусСмены - Число -
//    ** ДатаВремя - Дата
//    ** ПараметрыXML - Строка -
//    ** РезультатXML - Строка -
//    ** КассоваяСмена - ДокументСсылка.КассоваяСмена
//    ** ТестовыеЧеки - Булево
//    ** Результат - Булево -
//    ** ТекстОшибки - Строка
//  * ЧекКоррекцииСторно - Булево
//  * СформироватьЧекКоррекции - Булево -
//  * НеприменениеККТ - Булево -
//  * ВерсияФФДККТ11 - Булево -
//  * Результат - Булево -
//  * ОперацияЗаписана - Булево -
//  * ТекстОшибки - Строка
//  * СтатусСмены - Число -
//  * ТестовыеЧеки - Булево
//  * ЕдиныйЧек - Булево -
//  * ИдентификаторОплатыПлатежнойСистемы - УникальныйИдентификатор
//  * ТипПлатежнойСистемы - ПеречислениеСсылка.ТипыПлатежнойСистемыККТ  
//
Функция ПодготовитьДанныеОперации(ПараметрыПодключения, Команда, ПараметрыОперации, ДанныеОперации) Экспорт
	
	ШиринаСтроки = ?(ПараметрыПодключения.Свойство("ШиринаСтроки") И ПараметрыПодключения.ШиринаСтроки > 0, ПараметрыПодключения.ШиринаСтроки, 32);
	
	Если ДанныеОперации = Неопределено Тогда
		ДанныеОперации = Новый Структура();
		ДанныеОперации.Вставить("Результат", Истина);
		ДанныеОперации.Вставить("ТекстОшибки");
		ДанныеОперации.Вставить("ТестовыеЧеки");
		ДанныеОперации.Вставить("ТекстЧека");
	КонецЕсли;
	
	// Для всех операций проверяем корректность ИНН кассира.
	Если ПараметрыОперации <> Неопределено И ПараметрыОперации.Свойство("КассирИНН") И НЕ ПустаяСтрока(ПараметрыОперации.КассирИНН) Тогда
		ОписаниеОшибки = "";
		Если НЕ ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ИННСоответствуетТребованиям(ПараметрыОперации.КассирИНН, Ложь, ОписаниеОшибки) Тогда
			ТекстОшибки = НСтр("ru = 'ИНН кассира некорректен (%Ошибка%)';
								|en = 'Cashier TIN is incorrect (%Ошибка%)'");
			ОписаниеОшибки = СтрЗаменить(ТекстОшибки, "%Ошибка%", ОписаниеОшибки);
			ДанныеОперации.Результат = Ложь;
			ДанныеОперации.ТекстОшибки = ОписаниеОшибки;
			Возврат ДанныеОперации;
		КонецЕсли;
	КонецЕсли;
	
	// Для всех операций проверяем корректность ИНН организации.
	Если  ПараметрыОперации <> Неопределено И ПараметрыОперации.Свойство("ОрганизацияИНН") И НЕ ПустаяСтрока(ПараметрыОперации.ОрганизацияИНН) Тогда
		Если СтрДлина(ПараметрыОперации.ОрганизацияИНН) <> 10 И СтрДлина(ПараметрыОперации.ОрганизацияИНН) <> 12 Тогда
			ОписаниеОшибки = НСтр("ru = 'Неверная длина ИНН организации.';
									|en = 'Incorrect length of the company TIN.'");
			ДанныеОперации.Результат = Ложь;
			ДанныеОперации.ТекстОшибки = ОписаниеОшибки;
			Возврат ДанныеОперации;
		КонецЕсли;
	КонецЕсли;
	
	// Для всех операций проверяем корректность длины поля Кассир (ФФД 1021) (макс 64)
	Если  ПараметрыОперации <> Неопределено И ПараметрыОперации.Свойство("Кассир") И НЕ ПустаяСтрока(ПараметрыОперации.Кассир) Тогда
		Если СтрДлина(ПараметрыОперации.Кассир) > 64 Тогда
			ПараметрыОперации.Кассир = Лев(СокрЛП(ПараметрыОперации.Кассир),64);
		КонецЕсли;
	КонецЕсли;
	
	Если Команда = "PrintText" Тогда
		
		ДанныеОперации.ТестовыеЧеки = ПолучитьXMLПакетДляТекста(ПараметрыОперации.СтрокиТекста, ПараметрыПодключения.РевизияИнтерфейса); 
		ДанныеОперации.ТекстЧека = ПараметрыОперации.СтрокиТекста; 
		Возврат ДанныеОперации;
		
	ИначеЕсли Команда = "PrintQRCode" Тогда
		
		ТекстЧека = Символы.ПС;
		ТестовыйЧек = Новый Массив();
		Если Не ПустаяСтрока(ПараметрыОперации.QRКод.ЗначениеКода) Тогда
			
			СтрокаЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыТекстовойСтрокиЧека();
			ТестовыйЧек.Добавить(СтрокаЧека);
			
			СтрокаЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыШтрихкодВСтрокеЧека("QR", ПараметрыОперации.QRКод.ЗначениеКода);
			ТестовыйЧек.Добавить(СтрокаЧека);
			
			СтрокаЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыТекстовойСтрокиЧека();
			ТестовыйЧек.Добавить(СтрокаЧека);
				
			ТекстЧека = ТекстЧека + Символы.ПС + "|ШтрихКод|QR|" + ПараметрыОперации.QRКод.ЗначениеКода + Символы.ПС; 
			ТекстЧека = ТекстЧека + Символы.НПП + Символы.ПС; 
			
			Если Не ПустаяСтрока(ПараметрыОперации.ТипПлатежнойСистемы) Тогда
				СтрокаЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыТекстовойСтрокиЧека(ПараметрыОперации.ТипПлатежнойСистемы);
				СтрокаЧека.Выравнивание = "Центр";
				ТестовыйЧек.Добавить(СтрокаЧека);
				ТекстЧека = ТекстЧека + ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ВыровнятьПоле(ПараметрыОперации.ТипПлатежнойСистемы, ПараметрыПодключения.ШиринаСтроки, "Центр") + Символы.ПС; 
			КонецЕсли;
			
			СтрокаЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыТекстовойСтрокиЧека(ПараметрыОперации.QRКод.ТекстПользователя);
			СтрокаЧека.Выравнивание = "Центр";
			ТестовыйЧек.Добавить(СтрокаЧека);
			
			Если ЗначениеЗаполнено(ПараметрыОперации.СуммаОперации) Тогда
				ФорматЧисла = "ЧРД=.;ЧЦ=12;ЧДЦ=2;ЧН=0.00;ЧГ=0";
				ТекстСуммаОперации = СтрШаблон(НСтр("ru = 'Сумма операции: %1';
													|en = 'Transaction amount: %1'"), Формат(ПараметрыОперации.СуммаОперации, ФорматЧисла));
				СтрокаЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыТекстовойСтрокиЧека(ТекстСуммаОперации);
				СтрокаЧека.Выравнивание = "Центр";
				ТестовыйЧек.Добавить(СтрокаЧека);
				ТекстЧека = ТекстЧека + ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ВыровнятьПоле(ТекстСуммаОперации, ПараметрыПодключения.ШиринаСтроки, "Центр") + Символы.ПС; 
				ТекстЧека = ТекстЧека + Символы.НПП + Символы.ПС; 
			КонецЕсли;
			
			СтрокаЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыТекстовойСтрокиЧека();
			ТестовыйЧек.Добавить(СтрокаЧека);
		КонецЕсли;
		
		НефискальныеДокументы = Новый Массив();
		НефискальныеДокументы.Добавить(ТестовыйЧек);
		
		ТестовыеЧеки =  Новый Массив();
		ЗаполнитьXMLПакетыДляТекстовогоДокумента(
			ТестовыеЧеки, НефискальныеДокументы, 
			ПараметрыПодключения, 
			ПараметрыПодключения.ШиринаСтроки);
		ДанныеОперации.Вставить("ТестовыеЧеки", ТестовыеЧеки);
		ДанныеОперации.ТекстЧека = ТекстЧека; 
		
		Возврат ДанныеОперации;
		
	ИначеЕсли Команда = "OpenShift"  Тогда
		
		
	ИначеЕсли Команда = "CloseShift" Тогда
		
		
	ИначеЕсли Команда = "ReportCurrentStatusOfSettlements" Или Команда = "GetCurrentStatus"  Тогда
		
		Если ПараметрыОперации = Неопределено Тогда
			ПараметрыОперации = Новый Структура();
		КонецЕсли;
		
		ДанныеОперации = ПараметрыФискальнойОперации();
		ДанныеОперации.ПараметрыXML = ПолучитьXMLПакетДляОперации(ПараметрыОперации, ПараметрыПодключения.РевизияИнтерфейса);  
		
		Возврат ДанныеОперации;                                                                                                 
		
	ИначеЕсли Команда = "PrintXReport"  Тогда
		
		Если ПараметрыПодключения.ТипыОборудования.Свойство("ПринтерЧеков") И ПараметрыПодключения.ТипыОборудования.ПринтерЧеков 
				И ПараметрыОперации.Свойство("АвтономнаяККТ") И НЕ ПараметрыОперации.АвтономнаяККТ Тогда
			ОписаниеОшибки = НСтр("ru = 'Фискализация на принтерах чеков запрещена.';
									|en = 'Fiscalization on receipt printers is not allowed.'");
			ДанныеОперации.Результат = Ложь;
			ДанныеОперации.ТекстОшибки = ОписаниеОшибки;
			Возврат ДанныеОперации;
		КонецЕсли;
		
		Если ПараметрыОперации = Неопределено Тогда
			ПараметрыОперации = Новый Структура();
		КонецЕсли;
		
		ДанныеОперации = ПараметрыФискальнойОперации();
		ДанныеОперации.ПараметрыXML = ПолучитьXMLПакетДляОперации(ПараметрыОперации, ПараметрыПодключения.РевизияИнтерфейса);
		
		Возврат ДанныеОперации;
		
	ИначеЕсли Команда = "Encash" Тогда
		
	ИначеЕсли Команда = "OperationFN" Тогда
		
	ИначеЕсли Команда = "RequestKM" Или Команда = "RequestKMKMNoWait" Тогда
		
		ДанныеОперации = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыЗапросКМ();
		
		СтатусПоследнейСмены = ПолучитьСтатусПоследнейСмены(ПараметрыПодключения.ИдентификаторУстройства);
		Если Не СтатусПоследнейСмены.Активна = Истина Тогда
			ОписаниеОшибки = НСтр("ru = 'Кассовая смена не открыта или истекла.';
									|en = 'POS shift is not opened or expired.'");
			ДанныеОперации.Результат = Ложь;
			ДанныеОперации.ТекстОшибки = ОписаниеОшибки;
			Возврат ДанныеОперации;
		КонецЕсли;
		
		// ++ Локализация
		Если Команда = "RequestKMKMNoWait" Тогда
			ПараметрыОперации.ОжидатьПолучениеОтветаОИСМ = Ложь;
		КонецЕсли;                              
		
		Если ОбщегоНазначенияБПОКлиент.ИспользуетсяМаркировка() Тогда
			Если ПустаяСтрока(ПараметрыОперации.ИдентификаторЗапроса) Тогда
				ПараметрыОперации.ИдентификаторЗапроса = Новый УникальныйИдентификатор;
			КонецЕсли;
			
			Если Не ПустаяСтрока(ПараметрыОперации.КонтрольнаяМарка) Тогда
				ДанныеОперации.Результат = Истина;
				МодульМенеджерОборудованияМаркировкаКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("МенеджерОборудованияМаркировкаКлиент");
				ДанныеОперации.Вставить("ПараметрыXML", 
				МодульМенеджерОборудованияМаркировкаКлиент.СформироватьXMLДляЗапросаКМ(ПараметрыОперации, ПараметрыПодключения.РевизияИнтерфейса));
				ЗаполнитьЗначенияСвойств(ДанныеОперации, ПараметрыОперации);
			Иначе
				ТекстОшибки = НСтр("ru = 'Контрольная марка не может быть пустой.';
									|en = 'Control mark cannot be blank.'");
				ДанныеОперации.Результат = Ложь;
				ДанныеОперации.ТекстОшибки = ТекстОшибки;
			КонецЕсли;
		КонецЕсли;
		
		Возврат ДанныеОперации;
		// -- Локализация
		
	ИначеЕсли Команда = "CheckKM" Тогда
		
		ДанныеОперации = ОбщегоНазначенияБПОКлиент.СкопироватьРекурсивно(ПараметрыОперации);
		ДанныеОперации.Вставить("Результат", Истина);
		ДанныеОперации.Вставить("ТекстОшибки", "");
		// ++ Локализация
		СтатусПоследнейСмены = ПолучитьСтатусПоследнейСмены(ПараметрыПодключения.ИдентификаторУстройства);
		Если Не СтатусПоследнейСмены.Активна = Истина Тогда
			ОписаниеОшибки = НСтр("ru = 'Кассовая смена не открыта или истекла.';
									|en = 'POS shift is not opened or expired.'");
			ДанныеОперации.Результат = Ложь;
			ДанныеОперации.ТекстОшибки = ОписаниеОшибки;
			Возврат ДанныеОперации;
		КонецЕсли;
		
		Если ОбщегоНазначенияБПОКлиент.ИспользуетсяМаркировка() Тогда
			МодульМенеджерОборудованияМаркировкаКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("МенеджерОборудованияМаркировкаКлиент");
			
			Для Каждого Операция Из ДанныеОперации.ЗапросыКМ Цикл
				Если ПустаяСтрока(Операция.ИдентификаторЗапроса) Тогда
					Операция.ИдентификаторЗапроса = Строка(Новый УникальныйИдентификатор);
				КонецЕсли;
				Операция.ОтправлятьНаСерверОИСМ = Операция.ЗапросПроверкиКМСредствамиККТ;
				Операция.Вставить("ПараметрыXML", МодульМенеджерОборудованияМаркировкаКлиент.СформироватьXMLДляЗапросаКМ(Операция, ПараметрыПодключения.РевизияИнтерфейса));
			КонецЦикла;
		КонецЕсли;
		
		// -- Локализация
		Возврат ДанныеОперации;
		
	ИначеЕсли Команда = "CheckFiscalizationPacket" Тогда
		
		//СтатусПоследнейСмены = ПолучитьСтатусПоследнейСмены(ПараметрыПодключения.ИдентификаторУстройства);
		//Если Не СтатусПоследнейСмены.Активна = Истина Тогда
		//	ОписаниеОшибки = НСтр("ru='Кассовая смена не открыта или истекла.'");
		//	ДанныеОперации.Результат = Ложь;
		//	ДанныеОперации.ТекстОшибки = ОписаниеОшибки;
		//	Возврат ДанныеОперации;
		//КонецЕсли;
		//
		//ДанныеОперации = ПараметрыФискализацииЧека();       
		//
		//Если ПараметрыОперации <> Неопределено И ПараметрыОперации.Свойство("ДанныеЧекаXML") Тогда
		//	ДанныеОперации.ДанныеЧекаXML = ПараметрыОперации.ДанныеЧекаXML;
		//КонецЕсли;
		//
		//Возврат ДанныеОперации;
		
	КонецЕсли
	
КонецФункции

// Обработать данные операции после выполнения команды.
// 
// Параметры:
//  ПараметрыПодключения - Структура - см. МенеджерОборудованияКлиентСервер.ПараметрыПодключения()
//  Команда - Строка - команда для выполнения
//  РезультатВыполнения - Структура - 
//  ДанныеОперации - Структура - 
Процедура ОбработатьДанныеОперации(ПараметрыПодключения, Команда, РезультатВыполнения, ДанныеОперации) Экспорт
	
	РевизияИнтерфейса = ПараметрыПодключения.РевизияИнтерфейса;
	
	Если Команда = "CheckFiscalization" Или Команда = "CheckFiscalizationPacket" Тогда
		
	ИначеЕсли Команда = "PrintReceiptCorrection" Тогда
		
	ИначеЕсли Команда = "CloseShift" Тогда
		
		Если ОбщегоНазначенияБПОКлиент.ИспользуетсяКассоваяСмена() 
			И РезультатВыполнения.Свойство("СтатусПоследнейСмены") Тогда
			МодульКассовыеСменыКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("КассовыеСменыКлиент");
			МодульКассовыеСменыКлиент.УстановитьСтатусПоследнейСмены(
				ПараметрыПодключения.ИдентификаторУстройства, 
				РезультатВыполнения.СтатусПоследнейСмены);
		КонецЕсли;
	
	ИначеЕсли Команда = "OpenShift" Тогда
		
		Если ОбщегоНазначенияБПОКлиент.ИспользуетсяКассоваяСмена() 
			И РезультатВыполнения.Свойство("СтатусПоследнейСмены") Тогда
			МодульКассовыеСменыКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("КассовыеСменыКлиент");
			МодульКассовыеСменыКлиент.УстановитьСтатусПоследнейСмены(
				ПараметрыПодключения.ИдентификаторУстройства, 
				РезультатВыполнения.СтатусПоследнейСмены);
		КонецЕсли;
		
	ИначеЕсли Команда = "Encash" Тогда
		
		
	ИначеЕсли Команда = "GetDataKKT" Тогда
		
	ИначеЕсли Команда = "GetCurrentStatus" Тогда
		
		Если РевизияИнтерфейса >= 3000 Тогда // Версия стандарта 3.0 (ОФД 1.1)
			ФискальныеДанныеСтруктура = ПолучитьПараметрыСменыИзXMLПакета(ДанныеОперации.РезультатXML, РевизияИнтерфейса);
			НомерСменыККТ  = ФискальныеДанныеСтруктура.НомерСменыККТ;
			НомерЧекаККТ   = ФискальныеДанныеСтруктура.НомерЧекаККТ;
			СтатусСмены = ФискальныеДанныеСтруктура.СтатусСмены;     
		Иначе
			ФискальныеДанныеСтруктура = ПолучитьПараметрыСостоянияИзXMLПакета(ДанныеОперации.РезультатXML);
			НомерСменыККТ  = ДанныеОперации.НомерСменыККТ;
			НомерЧекаККТ   = ДанныеОперации.НомерЧекаККТ;
			СтатусСмены = ДанныеОперации.СтатусСмены;
		КонецЕсли;
		РезультатВыполнения.Вставить("НомерСменыККТ" , НомерСменыККТ);
		РезультатВыполнения.Вставить("НомерЧекаККТ"  , НомерЧекаККТ);
		РезультатВыполнения.Вставить("СтатусСмены", СтатусСмены);
		РезультатВыполнения.Вставить("ФискальныеДанные", ФискальныеДанныеСтруктура);
		
	ИначеЕсли Команда = "RequestKM" Тогда
		
		// ++ Локализация
		Если ОбщегоНазначенияБПОКлиент.ИспользуетсяМаркировка() Тогда
			МодульМенеджерОборудованияМаркировкаКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("МенеджерОборудованияМаркировкаКлиент");
			ДанныеСтруктура = МодульМенеджерОборудованияМаркировкаКлиент.РезультатыЗапросаКМИзXMLПакета(РезультатВыполнения.РезультатXML);
			РезультатВыполнения.Вставить("КодМаркировкиПроверен", ДанныеСтруктура.КодМаркировкиПроверен);  
			РезультатВыполнения.Вставить("РезультатПроверки", ДанныеСтруктура.РезультатПроверки);  
		КонецЕсли;
		// -- Локализация
		
	ИначеЕсли Команда = "GetProcessingKMResult"  Или Команда = "RequestKMKMNoWait"  Тогда
		
	ИначеЕсли Команда = "CheckKM" Тогда
		
	ИначеЕсли Команда = "OperationFN" Тогда
		
	КонецЕсли
	
КонецПроцедуры

#Область ФормированиеДанныхККТ

// Получить XMLПакет для операции.
// 
// Параметры:
//  ОбщиеПараметры - Структура, Булево - Общие параметры
//  РевизияИнтерфейса - Число - Ревизия интерфейса
// 
// Возвращаемое значение:
//  Строка - Получить XMLПакет для операции
Функция ПолучитьXMLПакетДляОперации(ОбщиеПараметры, РевизияИнтерфейса = 0) Экспорт
	
#Если ВебКлиент Тогда
	Возврат ОборудованиеЧекопечатающиеУстройстваВызовСервера.
		ПолучитьXMLПакетДляОперации(ОбщиеПараметры, РевизияИнтерфейса);
#Иначе
	Возврат ОборудованиеЧекопечатающиеУстройстваКлиентСервер.
		ПолучитьXMLПакетДляОперации(ОбщиеПараметры, РевизияИнтерфейса);
#КонецЕсли

КонецФункции  

// Получить XMLПакет для текста.
// 
// Параметры:
//  СтрокаТекста - Строка, Структура - Строка текста
//  РевизияИнтерфейса - Число - Ревизия интерфейса
// 
// Возвращаемое значение:
//  Массив из ЗаписьXML - Получить XMLПакет для текста.
Функция ПолучитьXMLПакетДляТекста(СтрокаТекста, РевизияИнтерфейса = 0) Экспорт
	
#Если ВебКлиент Тогда
	Возврат ОборудованиеЧекопечатающиеУстройстваВызовСервера.
		ПолучитьXMLПакетДляТекста(СтрокаТекста, РевизияИнтерфейса);
#Иначе
	Возврат ОборудованиеЧекопечатающиеУстройстваКлиентСервер.
		ПолучитьXMLПакетДляТекста(СтрокаТекста, РевизияИнтерфейса);
#КонецЕсли

КонецФункции  

Процедура ЗаполнитьXMLПакетыДляТекстовогоДокумента(ТестовыеЧеки, НефискальныеДокументы, ПараметрыПодключения, ШиринаСтроки = 32) Экспорт
	
#Если ВебКлиент Тогда
	ОборудованиеЧекопечатающиеУстройстваВызовСервера.
		ЗаполнитьXMLПакетыДляТекстовогоДокумента(ТестовыеЧеки, НефискальныеДокументы, ПараметрыПодключения, ШиринаСтроки);
#Иначе
	ОборудованиеЧекопечатающиеУстройстваКлиентСервер.
		ЗаполнитьXMLПакетыДляТекстовогоДокумента(ТестовыеЧеки, НефискальныеДокументы, ПараметрыПодключения, ШиринаСтроки);
#КонецЕсли

КонецПроцедуры

Функция ПолучитьПараметрыСостоянияИзXMLПакета(Данные, НомерСмены = Неопределено) Экспорт
	
#Если ВебКлиент Тогда
	Возврат ОборудованиеЧекопечатающиеУстройстваВызовСервера.
		ПолучитьПараметрыСостоянияИзXMLПакета(Данные, НомерСмены);
#Иначе
	Возврат ОборудованиеЧекопечатающиеУстройстваКлиентСервер.
		ПолучитьПараметрыСостоянияИзXMLПакета(Данные, НомерСмены);
#КонецЕсли

КонецФункции

Функция ПолучитьПараметрыСменыИзXMLПакета(ДанныеXML, РевизияИнтерфейса = 0, НомерСменыККТ = 0, НомерЧекаККТ = 0) Экспорт
	
#Если ВебКлиент Тогда
	Возврат ОборудованиеЧекопечатающиеУстройстваВызовСервера.
		ПолучитьПараметрыСменыИзXMLПакета(ДанныеXML, РевизияИнтерфейса, НомерСменыККТ, НомерЧекаККТ);
#Иначе
	Возврат ОборудованиеЧекопечатающиеУстройстваКлиентСервер.
		ПолучитьПараметрыСменыИзXMLПакета(ДанныеXML, РевизияИнтерфейса, НомерСменыККТ, НомерЧекаККТ);
#КонецЕсли

КонецФункции

#КонецОбласти


#Область ПакетнаяОбработка

// Обрабатывает результат выбора эквайрингового терминала
// 
// Параметры:
//   РезультатВыполнения - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   КонтекстОперации - См. КонтекстЭквайринговойОперацииСФискализацией
Процедура ПакетнаяОбработка_ВыборУстройстваЗавершение(РезультатВыполнения, КонтекстОперации) Экспорт
	
	Результат = РезультатВыполнения.Результат;
	
	Если Не Результат Тогда
		// Эквайринговый терминал не выбран, завершить пакет операций
		ЗавершитьПакетОпераций(Ложь, РезультатВыполнения.ОписаниеОшибки, КонтекстОперации);
		Возврат;
	КонецЕсли;
	
	ИдентификаторЭТ                  = РезультатВыполнения.ИдентификаторУстройства;
	ИдентификаторКлиента             = КонтекстОперации.ИдентификаторКлиента;
	КонтекстОперации.ИдентификаторЭТ = ИдентификаторЭТ;
	
	// Подключить ЭТ, если он не подключен (требуется для проверки возможности печати коротких слип-чеков)
	Оповещение = Новый ОписаниеОповещения("ПакетнаяОбработка_ПодключениеУстройстваЗавершение",
		ЭтотОбъект,
		КонтекстОперации);
	ПодключенноеУстройство = МенеджерОборудованияКлиент.ПолучитьПодключенноеУстройство(ИдентификаторЭТ);
	Если ПодключенноеУстройство = Неопределено Тогда
		ТипыОборудования = Новый Массив();
		ТипыОборудования.Добавить("ЭквайринговыйТерминал");
		МенеджерОборудованияКлиент.НачатьПодключениеОборудования(Оповещение,
			ИдентификаторКлиента,
			ТипыОборудования,
			ИдентификаторЭТ);
	Иначе
		РезультатВыполнения.ПодключенноеУстройство = ПодключенноеУстройство;
		ВыполнитьОбработкуОповещения(Оповещение, РезультатВыполнения);
	КонецЕсли;

КонецПроцедуры

// Обрабатывает результат подключения эквайрингового терминала
// 
// Параметры:
//   РезультатВыполнения - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   КонтекстОперации - См. КонтекстЭквайринговойОперацииСФискализацией
Процедура ПакетнаяОбработка_ПодключениеУстройстваЗавершение(РезультатВыполнения, КонтекстОперации) Экспорт

	Результат              = РезультатВыполнения.Результат;
	ОписаниеОшибки         = РезультатВыполнения.ОписаниеОшибки;
	ПодключенноеУстройство = РезультатВыполнения.ПодключенноеУстройство;
	Оповещение             = КонтекстОперации.ОповещениеЗавершенияПодключения;
	
	Если Не Результат Тогда
		// Не удалось подключить эквайринговый терминал
		ЗавершитьПакетОпераций(Ложь, ОписаниеОшибки, КонтекстОперации);
		Возврат;
	КонецЕсли;
	КонтекстОперации.ПодключенныйЭТ = ПодключенноеУстройство;
	
	ОпределитьВозможностьПечатиСлипЧекаВТелеФискального(КонтекстОперации);
	
	// Подключить ЭТ, если он не подключен (требуется для проверки возможности печати коротких слип-чеков)
	ВыполнитьОбработкуОповещения(Оповещение, РезультатВыполнения);

КонецПроцедуры

// Обрабатывает результат печати слип-чека как текстового документа
// 
// Параметры:
//   РезультатВыполнения - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   КонтекстОперации - См. КонтекстЭквайринговойОперацииСФискализацией
Процедура ПакетнаяОбработка_ПечатьТекстаЗавершение(РезультатВыполнения, КонтекстОперации) Экспорт
	
	Результат      = КонтекстОперации.Результат;
	ОписаниеОшибки = КонтекстОперации.ОписаниеОшибки;
	
	ЗавершитьПакетОпераций(Результат, ОписаниеОшибки, КонтекстОперации);
	
КонецПроцедуры

#Область ЭквайринговаяОперацияСФискализацией

// Возвращает заполненную структуру результата выполнения пакета операций
//
// Параметры:
//   РезультатОперации - Булево - Результат выполнения пакета операций
//   ОписаниеОшибки - Строка - Описание ошибки выполнения пакета операции
//   КонтекстОперации - см. КонтекстЭквайринговойОперацииСФискализацией
//
// Возвращаемое значение:
//  Структура:
//    * Результат - Булево - Результат выполнения пакета операций.
//    * ОписаниеОшибки - Строка - Описание ошибки выполнения пакета операции.
//    * РезультатВыполненияЭквайринговойОперации - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//    * РезультатФискализацииЧека - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//    * РезультатАварийнойОтменыОперации - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//    * ПараметрыЭквайринговойОперации - см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыПакетнойОперацииВОчередиЧеков
Функция РезультатЭквайринговойОперацииСФискализацией(РезультатОперации, ОписаниеОшибки, КонтекстОперации) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Результат",                                РезультатОперации);
	Результат.Вставить("ОписаниеОшибки",                           ОписаниеОшибки);
	Если КонтекстОперации.ТипОперации = "ПокупкаСЗачислением" Тогда
		Результат.Вставить("ТипОперации",                              ПредопределенноеЗначение("Перечисление.ТипыПакетнойОперацииВОчередиЧеков.ПокупкаСЗачислением"));
	ИначеЕсли КонтекстОперации.ТипОперации = "ФискализацияЧекаСОплатой" Тогда
		Результат.Вставить("ТипОперации",                              ПредопределенноеЗначение("Перечисление.ТипыПакетнойОперацииВОчередиЧеков.ФискализацияЧекаСОплатойКартой"));
	Иначе
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неизвестный тип операции %1';
										|en = 'Unknown transaction type: %1'"), КонтекстОперации.ТипОперации);
	КонецЕсли;
	Результат.Вставить("РезультатФискализацииЧека",                КонтекстОперации.РезультатФискализацииЧека);
	Результат.Вставить("РезультатАварийнойОтменыОперации",         КонтекстОперации.РезультатАварийнойОтменыОперации);
	Результат.Вставить("РезультатВыполненияЭквайринговойОперации", КонтекстОперации.РезультатВыполненияЭквайринговойОперации);
	Результат.Вставить("ПараметрыЭквайринговойОперации",           Новый Структура());
	
	Возврат Результат;
	
КонецФункции

// Начинает выполнение операции на эквайринговом терминале
// 
// Параметры:
//   РезультатВыполнения - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   КонтекстОперации - См. КонтекстЭквайринговойОперацииСФискализацией
Процедура НачатьЭкваринговуюОперациюСФискализацией_ЭквайринговаяОперация(РезультатВыполнения, КонтекстОперации) Экспорт
	
	// Проверить возможность выполнения пакета операций
	ОписаниеОшибки = "";
	Если Не ЭкваринговаяОперацияСФискализациейДопустима(КонтекстОперации, ОписаниеОшибки) Тогда
		ЗавершитьПакетОпераций(Ложь, ОписаниеОшибки, КонтекстОперации);
		Возврат;
	КонецЕсли;
	
	// начать выполнение операций
	Оповещение = Новый ОписаниеОповещения("НачатьЭкваринговуюОперациюСФискализацией_ЭквайринговаяОперацияЗавершение",
		ЭтотОбъект,
		КонтекстОперации);
	МодульОборудованиеПлатежныеСистемыКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиент");
	МодульОборудованиеПлатежныеСистемыКлиент.НачатьВыполнениеОперацииНаЭквайринговомТерминале(Оповещение, 
		КонтекстОперации.ИдентификаторКлиента, 
		КонтекстОперации.ИдентификаторЭТ, 
		КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации,
		КонтекстОперации.ДополнительныеПараметры,
		КонтекстОперации.ИдентификаторККТ);
КонецПроцедуры

// Обрабатывает результат выполнения эквайринговой операции, в случае успеха вставляет слип-чек, 
// в тело фискального чека.
// 
// Параметры:
//   РезультатВыполнения - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   КонтекстОперации - См. КонтекстЭквайринговойОперацииСФискализацией
Процедура НачатьЭкваринговуюОперациюСФискализацией_ЭквайринговаяОперацияЗавершение(
	РезультатВыполнения, КонтекстОперации) Экспорт
	
	РезультатВыполнения.Вставить("СуммаОперации", КонтекстОперации.СуммаОперации);
	КонтекстОперации.РезультатВыполненияЭквайринговойОперации = РезультатВыполнения;
	
	Если РезультатВыполнения.Результат Тогда
		ВставитьСлипЧекВТелоФискальногоЧека(КонтекстОперации);
		НачатьЭкваринговуюОперациюСФискализацией_Фискализация(КонтекстОперации);
	Иначе
		ОбработатьОтрицательныйРезультатЭквайринговойОперацииСФискализацией(КонтекстОперации);
	КонецЕсли;
	
КонецПроцедуры

// Запускает фискализацию кассового чека.
// 
// Параметры:
//   КонтекстОперации - См. КонтекстЭквайринговойОперацииСФискализацией
Процедура НачатьЭкваринговуюОперациюСФискализацией_Фискализация(КонтекстОперации) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("НачатьЭкваринговуюОперациюСФискализацией_ФискализацияЗавершение", 
		ЭтотОбъект, 
		КонтекстОперации);
	НачатьФискализациюЧекаНаФискальномУстройстве(Оповещение, 
		КонтекстОперации.ИдентификаторКлиента, 
		КонтекстОперации.ИдентификаторККТ, 
		КонтекстОперации.ПараметрыФискализацииЧека, 
		КонтекстОперации.ДополнительныеПараметры);
	
КонецПроцедуры

// Обрабатывает результаты фискализации чека, в случае ошибки фискализации вызывает аварийную отмену последней операции 
// эквайрингового терминала
// 
// Параметры:
//   РезультатВыполнения - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   КонтекстОперации - См. КонтекстЭквайринговойОперацииСФискализацией
Процедура НачатьЭкваринговуюОперациюСФискализацией_ФискализацияЗавершение(РезультатВыполнения, КонтекстОперации) Экспорт
	
	КонтекстОперации.РезультатФискализацииЧека = РезультатВыполнения;
	Результат      = РезультатВыполнения.Результат;
	ОписаниеОшибки = РезультатВыполнения.ОписаниеОшибки;
	
	Если Результат Тогда
		Если КонтекстОперации.ПараметрыФискализацииЧека.Электронно Тогда
			КонтекстОперации.Результат = Истина;
			ВывестиСлипЧекКакТекстовыйДокумент("Электронный", КонтекстОперации);
		Иначе
			ЗавершитьПакетОпераций(Истина, ОписаниеОшибки, КонтекстОперации);
		КонецЕсли;
	Иначе
		Если КонтекстОперации.РезультатВыполненияЭквайринговойОперации.Результат Тогда
			МодульОборудованиеПлатежныеСистемыКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиент");
			МодульОборудованиеПлатежныеСистемыКлиентСервер = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиентСервер");
			Оповещение = Новый ОписаниеОповещения("НачатьЭкваринговуюОперациюСФискализацией_АварийнаяОтменаЗавершение", 
				ЭтотОбъект, 
				КонтекстОперации);
			ПараметрыОтменыЭквайринговойОперации = МодульОборудованиеПлатежныеСистемыКлиентСервер.ПараметрыВыполненияЭквайринговойОперации();
			ЗаполнитьЗначенияСвойств(
				ПараметрыОтменыЭквайринговойОперации, 
				КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации, 
				, 
				"ТипТранзакции, ИдентификаторЗапроса");
			МодульОборудованиеПлатежныеСистемыКлиент.НачатьВыполнениеАварийнойОтменыОперации(Оповещение, 
				КонтекстОперации.ИдентификаторКлиента, 
				КонтекстОперации.ИдентификаторЭТ, 
				ПараметрыОтменыЭквайринговойОперации,
				КонтекстОперации.ДополнительныеПараметры);
		Иначе
			КонтекстОперации.Результат      = Ложь;
			КонтекстОперации.ОписаниеОшибки = ОписаниеОшибки;
			ВывестиСлипЧекКакТекстовыйДокумент("Отказ", КонтекстОперации);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает результаты аварийной отмены последней операции эквайрингового терминала
// 
// Параметры:
//   РезультатВыполнения - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   КонтекстОперации - См. КонтекстЭквайринговойОперацииСФискализацией
Процедура НачатьЭкваринговуюОперациюСФискализацией_АварийнаяОтменаЗавершение(РезультатВыполнения, КонтекстОперации) Экспорт
	
	КонтекстОперации.РезультатАварийнойОтменыОперации = РезультатВыполнения;
	КонтекстОперации.Результат                        = Ложь;
	Если РезультатВыполнения.Результат Тогда
		КонтекстОперации.ОписаниеОшибки = КонтекстОперации.РезультатФискализацииЧека.ОписаниеОшибки;
	Иначе
		КонтекстОперации.ОписаниеОшибки = КонтекстОперации.РезультатАварийнойОтменыОперации.ОписаниеОшибки;
	КонецЕсли;
	ВывестиСлипЧекКакТекстовыйДокумент("Отказ", КонтекстОперации);
	
КонецПроцедуры

#КонецОбласти

#Область ПродажаСВыдачейНаличных

// Начинает выполнение эквайринговой операции "Оплата с выдачей наличных"
// 
// Параметры:
//   РезультатВыполнения - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   КонтекстОперации - См. КонтекстПродажаСВыдачейНаличных
Процедура НачатьПродажаСВыдачейНаличных_ЭквайринговаяОперация(РезультатВыполнения, КонтекстОперации) Экспорт
	
	// Проверить возможность выполнения пакета операций
	ОписаниеОшибки = "";
	Если Не ПродажаСВыдачейНаличныхДопустима(КонтекстОперации, ОписаниеОшибки) Тогда
		ЗавершитьПакетОпераций(Ложь, ОписаниеОшибки, КонтекстОперации);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"НачатьПродажаСВыдачейНаличных_ЭквайринговаяОперацияЗавершение",
		ЭтотОбъект,
		КонтекстОперации);
	МодульОборудованиеПлатежныеСистемыКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиент");
	МодульОборудованиеПлатежныеСистемыКлиент.НачатьВыполнениеОперацииНаЭквайринговомТерминале(
		Оповещение, 
		КонтекстОперации.ИдентификаторКлиента, 
		КонтекстОперации.ИдентификаторЭТ, 
		КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации,
		КонтекстОперации.ДополнительныеПараметры,
		КонтекстОперации.ИдентификаторККТ);
		
КонецПроцедуры

// Начинает выполнение фискализации продажи
// 
// Параметры:
//   РезультатВыполнения - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   КонтекстОперации - См. КонтекстЭквайринговойОперацииСФискализацией
Процедура НачатьПродажаСВыдачейНаличных_ЭквайринговаяОперацияЗавершение(РезультатВыполнения, КонтекстОперации) Экспорт
	
	РезультатВыполнения.Вставить("СуммаОперации", КонтекстОперации.СуммаОперации);
	РезультатВыполнения.Вставить("СуммаНаличных", КонтекстОперации.СуммаВыдачи);
	КонтекстОперации.РезультатВыполненияЭквайринговойОперации = РезультатВыполнения;

	Результат      = РезультатВыполнения.Результат;
	ОписаниеОшибки = РезультатВыполнения.ОписаниеОшибки;

	Если РезультатВыполнения.Результат Тогда
		// Эквайринговая операция выполнена успешно, требуется произвести фискализацию продажи
		Если КонтекстОперации.СлипЧекВТелеФискального Тогда
			ВставитьСлипЧекВТелоФискальногоЧека(КонтекстОперации);
		КонецЕсли;
		
		Оповещение = Новый ОписаниеОповещения(
			"НачатьПродажаСВыдачейНаличных_ФискализацияПродажиЗавершение",
			ЭтотОбъект,
			КонтекстОперации);
			
		НачатьФискализациюЧекаНаФискальномУстройстве(
			Оповещение, 
			КонтекстОперации.ИдентификаторКлиента, 
			КонтекстОперации.ИдентификаторККТ, 
			КонтекстОперации.ПараметрыФискализацииЧека,
			КонтекстОперации.ДополнительныеПараметры);
	Иначе
		// Эквайринговая операция не выполнена, завершение пакета операций
		Если КонтекстОперации.ВыводитьУведомленияПользователя Тогда
			ТекстСообщения = НСтр("ru = 'Оплата с выдачей наличных не удалась.%1%2';
									|en = 'Payment with cash issue failed.%1%2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Символы.ПС, ОписаниеОшибки);
			ОбщегоНазначенияБПОКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;

		КонтекстОперации.Результат = Ложь;
		КонтекстОперации.ОписаниеОшибки = ОписаниеОшибки;
		ВывестиСлипЧекКакТекстовыйДокумент("Отказ", КонтекстОперации);

	КонецЕсли;
			
КонецПроцедуры

// Обрабатывает результат выполнения фискализации продажи
// 
// Параметры:
//   РезультатВыполнения - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   КонтекстОперации - См. КонтекстПродажаСВыдачейНаличных
Процедура НачатьПродажаСВыдачейНаличных_ФискализацияПродажиЗавершение(РезультатВыполнения, КонтекстОперации) Экспорт

	КонтекстОперации.РезультатФискализацииЧека = РезультатВыполнения;

	Результат      = РезультатВыполнения.Результат;
	ОписаниеОшибки = РезультатВыполнения.ОписаниеОшибки;
	
	Если Результат Тогда
		// Фискализация продажи выполнена успешно, требуется произвести фискализацию выдачи
		
		Оповещение = Новый ОписаниеОповещения("НачатьПродажаСВыдачейНаличных_ФискализацияВыдачиЗавершение",
			ЭтотОбъект,
			КонтекстОперации);
		НачатьФискализациюЧекаНаФискальномУстройстве(
			Оповещение, 
			КонтекстОперации.ИдентификаторКлиента, 
			КонтекстОперации.ИдентификаторККТ, 
			КонтекстОперации.ПараметрыФискализацииЧекаВыдачи,
			КонтекстОперации.ДополнительныеПараметры);
	Иначе
		// продажа не выполнена, отмена эквайринговой операции
		Если КонтекстОперации.ВыводитьУведомленияПользователя Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось произвести фискализацию чека продажи.%1%2';
									|en = 'Cannot fiscalize the sales receipt.%1%2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Символы.ПС, ОписаниеОшибки);
			ОбщегоНазначенияБПОКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		// фискализация не удалась, требуется произвести отмену операции на ЭТ
		МодульОборудованиеПлатежныеСистемыКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиент");
		МодульОборудованиеПлатежныеСистемыКлиентСервер = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиентСервер");
		Оповещение = Новый ОписаниеОповещения(
			"НачатьПродажаСВыдачейНаличных_АварийнаяОтменаЗавершение", 
			ЭтотОбъект, 
			КонтекстОперации);
		ПараметрыОтменыЭквайринговойОперации = МодульОборудованиеПлатежныеСистемыКлиентСервер.ПараметрыВыполненияЭквайринговойОперации();
		ЗаполнитьЗначенияСвойств(
			ПараметрыОтменыЭквайринговойОперации, 
			КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации, 
			, 
			"ТипТранзакции, ИдентификаторЗапроса");
		МодульОборудованиеПлатежныеСистемыКлиент.НачатьВыполнениеАварийнойОтменыОперации(
			Оповещение, 
			КонтекстОперации.ИдентификаторКлиента, 
			КонтекстОперации.ИдентификаторЭТ, 
			ПараметрыОтменыЭквайринговойОперации,
			КонтекстОперации.ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает результат выполнения фискализации выдачи, в случае не удачи выполняет фискализацию чека возврата 
// и аварийную отмену последней эквайринговой операции. Если операция успешна, тогда операция завершается.
// 
// Параметры:
//   РезультатВыполнения - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   КонтекстОперации - См. КонтекстПродажаСВыдачейНаличных
Процедура НачатьПродажаСВыдачейНаличных_ФискализацияВыдачиЗавершение(РезультатВыполнения, КонтекстОперации) Экспорт

	КонтекстОперации.РезультатФискализацииЧекаВыдачи = РезультатВыполнения;
	Результат = РезультатВыполнения.Результат;
	
	Если Результат Тогда
		Если КонтекстОперации.ПараметрыФискализацииЧекаВыдачи.Электронно Тогда
			КонтекстОперации.Результат = Истина;
			ВывестиСлипЧекКакТекстовыйДокумент("Электронный", КонтекстОперации);
		Иначе
			ЗавершитьПакетОпераций(Истина, "", КонтекстОперации);
		КонецЕсли;
	Иначе
		// Фискализация не удалась, требуется произвести отмену фискализации продажи и отмену операции на ЭТ
		Оповещение = Новый ОписаниеОповещения(
			"НачатьПродажаСВыдачейНаличных_ФискализацияВозвратаПродажиЗавершение", 
			ЭтотОбъект, 
			КонтекстОперации);
		
		НачатьФискализациюЧекаНаФискальномУстройстве(
			Оповещение, 
			КонтекстОперации.ИдентификаторКлиента, 
			КонтекстОперации.ИдентификаторККТ, 
			КонтекстОперации.ПараметрыФискализацииЧекаВозврата,
			КонтекстОперации.ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает результат выполнения фискализации возврата продажи, и выполняет аварийную отмену последней 
// эквайринговой операции. 
// 
// Параметры:
//   РезультатВыполнения - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   КонтекстОперации - См. ОборудованиеЧекопечатающиеУстройстваКлиент.КонтекстПродажаСВыдачейНаличных
Процедура НачатьПродажаСВыдачейНаличных_ФискализацияВозвратаПродажиЗавершение(РезультатВыполнения, КонтекстОперации) Экспорт

	КонтекстОперации.РезультатФискализацииЧекаВозврата = РезультатВыполнения;
	Результат = РезультатВыполнения.Результат;
	
	// Независимо от результата выполнить аварийную отмену эквайринговой операции
	МодульОборудованиеПлатежныеСистемыКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиент");
	Оповещение = Новый ОписаниеОповещения(
		"НачатьПродажаСВыдачейНаличных_АварийнаяОтменаЗавершение", 
		ЭтотОбъект, 
		КонтекстОперации);
	МодульОборудованиеПлатежныеСистемыКлиент.НачатьВыполнениеАварийнойОтменыОперации(
		Оповещение, 
		КонтекстОперации.ИдентификаторКлиента, 
		КонтекстОперации.ИдентификаторЭТ, 
		КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации,
		КонтекстОперации.ДополнительныеПараметры);
		
КонецПроцедуры

// Обрабатывает результат выполнения аварийной отмены последней эквайринговой операции
// 
// Параметры:
//   РезультатВыполнения - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   КонтекстОперации - См. КонтекстПродажаСВыдачейНаличных
Процедура НачатьПродажаСВыдачейНаличных_АварийнаяОтменаЗавершение(РезультатВыполнения, КонтекстОперации) Экспорт

	КонтекстОперации.РезультатАварийнойОтменыОперации = РезультатВыполнения;
	
	Если РезультатВыполнения.Результат Тогда
		// аварийная отмена выполнена успешно
		Если ЗначениеЗаполнено(КонтекстОперации.РезультатФискализацииЧекаВыдачи) Тогда
			// отмена в момент фискализации чека выдачи
			ОписаниеОшибки = КонтекстОперации.РезультатФискализацииЧекаВыдачи.ОписаниеОшибки;
			ТекстСообщения = НСтр("ru = 'Фискализация чека выдачи не выполнена.%1%2';
									|en = 'Issue receipt is not fiscalized.%1%2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Символы.ПС, ОписаниеОшибки);
			Если Не КонтекстОперации.РезультатФискализацииЧекаВозврата.Результат Тогда
				// чек возврата неудачен
				ОписаниеОшибки = КонтекстОперации.РезультатФискализацииЧекаВозврата.ОписаниеОшибки;
				ТекстСообщенияКоррекции = НСтр("ru = '%1%2Фискализация чека возврата не выполнена.%2%3';
												|en = '%1%2Refund receipt is not fiscalized.%2%3'");
				ТекстСообщенияКоррекции = СтрШаблон(ТекстСообщенияКоррекции, ТекстСообщения, Символы.ПС, ОписаниеОшибки);
			КонецЕсли;
		Иначе 
			// отмена в момент фискализации чека продажи
			ОписаниеОшибки = КонтекстОперации.РезультатФискализацииЧека.ОписаниеОшибки;
			ТекстСообщения = НСтр("ru = 'Фискализация чека продажи не выполнена.%1%2';
									|en = 'Sales receipt is not fiscalized.%1%2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Символы.ПС, ОписаниеОшибки);
		КонецЕсли;
	Иначе
		// аварийная отмена оплаты с выдачей наличных не удалась
		ОписаниеОшибки = КонтекстОперации.РезультатАварийнойОтменыОперации.ОписаниеОшибки;
		ТекстСообщения = НСтр("ru = 'Аварийная отмена оплаты с выдачей наличных не выполнена.%1%2';
								|en = 'Abnormal cancellation of payment with cash issue is not completed.%1%2'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Символы.ПС, ОписаниеОшибки);
	КонецЕсли;
	Если КонтекстОперации.ВыводитьУведомленияПользователя Тогда
		ОбщегоНазначенияБПОКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

	КонтекстОперации.Результат = Ложь;
	КонтекстОперации.ОписаниеОшибки = ОписаниеОшибки;
	ВывестиСлипЧекКакТекстовыйДокумент("Отказ", КонтекстОперации);
	
КонецПроцедуры

// Возвращает заполненную структуру результата выполнения пакета операций
//
// Параметры:
//   РезультатОперации - Булево - Результат выполнения пакета операций
//   ОписаниеОшибки - Строка - Описание ошибки выполнения пакета операции
//   КонтекстОперации - см. КонтекстЭквайринговойОперацииСФискализацией
//
// Возвращаемое значение:
//  Структура:
//    * Результат - Булево - Результат выполнения пакета операций.
//    * ОписаниеОшибки - ИнформацияОбОшибке - Описание ошибки выполнения пакета операции.
//    * РезультатВыполненияЭквайринговойОперации - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//    * РезультатФискализацииЧека - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//    * РезультатФискализацииЧекаВыдачи - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//    * РезультатФискализацииЧекаВозврата - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//    * РезультатАварийнойОтменыОперации - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//    * ПараметрыЭквайринговойОперации - см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыПакетнойОперацииВОчередиЧеков
//    * ПараметрыПродажаСВыдачейНаличных - см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыПакетнойОперацииВОчередиЧеков
Функция РезультатПродажаСВыдачейНаличных(РезультатОперации, ОписаниеОшибки, КонтекстОперации) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Результат",                                РезультатОперации);
	Результат.Вставить("ОписаниеОшибки",                           ОписаниеОшибки);
	Результат.Вставить("ТипОперации",                              ПредопределенноеЗначение("Перечисление.ТипыПакетнойОперацииВОчередиЧеков.ПродажаСВыдачейНаличных"));
	Результат.Вставить("РезультатФискализацииЧека",                КонтекстОперации.РезультатФискализацииЧека);
	Результат.Вставить("РезультатФискализацииЧекаВыдачи",          КонтекстОперации.РезультатФискализацииЧекаВыдачи);
	Результат.Вставить("РезультатФискализацииЧекаВозврата",        КонтекстОперации.РезультатФискализацииЧекаВозврата);
	Результат.Вставить("РезультатАварийнойОтменыОперации",         КонтекстОперации.РезультатАварийнойОтменыОперации);
	Результат.Вставить("РезультатВыполненияЭквайринговойОперации", КонтекстОперации.РезультатВыполненияЭквайринговойОперации);
	Результат.Вставить("ПараметрыЭквайринговойОперации",           Новый Структура());
	Результат.Вставить("ПараметрыПродажаСВыдачейНаличных",         Новый Структура());
	Возврат Результат;
	
КонецФункции

// Возвращает структуру контекст выполнения пакета операций
//
// Возвращаемое значение:
//  Структура:
//   * ТипОперации - Строка - тип операции 
//   * Результат - Булево - результат выполнения пакета операций
//   * ОписаниеОшибки - Строка - Описание ошибки пакета операций
//   * ОповещениеЗавершения - ОписаниеОповещения - Оповещение вызываемое при завершении выполнения пакета
//   * ИдентификаторКлиента - ФормаКлиентскогоПриложения - идентификатор формы.
//   * ИдентификаторККТ - СправочникСсылка.ПодключаемоеОборудование - ККТ на котором будет печататься фискальный чек
//                        содержащий в себе слип-чек
//   * ИдентификаторЭТ - СправочникСсылка.ПодключаемоеОборудование - Эквайринговый терминал для обработки операции
//   * ПодключенныйЭТ - Структура - параметры подключенного терминала
//   * ПараметрыФискализацииЧека - см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека
//   * ПараметрыФискализацииЧекаВыдачи -см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека
//   * ПараметрыФискализацииЧекаВозврата - см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека
//   * ПараметрыВыполненияЭквайринговойОперации - см. ОборудованиеПлатежныеСистемыКлиентСервер.ПараметрыВыполненияЭквайринговойОперации
//   * РезультатВыполненияЭквайринговойОперации - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   * РезультатФискализацииЧека - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   * РезультатФискализацииЧекаВыдачи - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   * РезультатФискализацииЧекаВозврата - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   * РезультатАварийнойОтменыОперации - см. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   * ВыводитьУведомленияПользователя - Булево - если установлена Истина (по умолчанию), тогда при ошибке операции
//                                       в пакете, будет выведено сообщение пользователю.
//   * ОставитьПодключенным - Булево - Оставить оборудование подключенным после выполнения пакета
//   * ДополнительныеПараметры - Структура - Дополнительные параметры которые можно передать в операции 
//                               оплаты и фискализации
//   * РеквизитыКартыQR - Строка
//
Функция КонтекстПродажаСВыдачейНаличных() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ТипОперации",                              "ПродажаСВыдачейНаличных");
	Результат.Вставить("Результат",                                Ложь);
	Результат.Вставить("ОписаниеОшибки",                           "");
	Результат.Вставить("ОповещениеЗавершения",                     Неопределено);
	Результат.Вставить("ИдентификаторКлиента",                     Неопределено);
	Результат.Вставить("ИдентификаторККТ",                         Неопределено);
	Результат.Вставить("ИдентификаторЭТ",                          Неопределено);
	Результат.Вставить("ПодключенныйЭТ",                           Неопределено);
	Результат.Вставить("ПараметрыФискализацииЧека",                Неопределено);
	Результат.Вставить("ПараметрыФискализацииЧекаВозврата",        Неопределено);
	Результат.Вставить("ПараметрыФискализацииЧекаВыдачи",          Неопределено);
	Результат.Вставить("ПараметрыВыполненияЭквайринговойОперации", Неопределено);
	Результат.Вставить("РезультатВыполненияЭквайринговойОперации", Неопределено);
	Результат.Вставить("РезультатФискализацииЧека",                Неопределено);
	Результат.Вставить("РезультатФискализацииЧекаВозврата",        Неопределено);
	Результат.Вставить("РезультатФискализацииЧекаВыдачи",          Неопределено);
	Результат.Вставить("РезультатАварийнойОтменыОперации",         Неопределено);
	Результат.Вставить("СлипЧекВТелеФискального",                  Неопределено);
	Результат.Вставить("ВыводитьУведомленияПользователя",          Неопределено);
	Результат.Вставить("ОставитьПодключенным",                     Ложь);
	Результат.Вставить("ДополнительныеПараметры",                  Новый Структура());
	Результат.Вставить("ПризнакАгента",                            Неопределено);
	Результат.Вставить("ДанныеАгента",                             Неопределено);
	Результат.Вставить("ДанныеПоставщика",                         Неопределено);
	Результат.Вставить("СуммаВыдачи",                              0);
	Результат.Вставить("СуммаОперации",                            0);
	Результат.Вставить("РеквизитыКартыQR",                         "");
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтатусПоследнейСмены(ИдентификаторУстройства) 
	
	Если ОбщегоНазначенияБПОКлиент.ИспользуетсяКассоваяСмена() Тогда
		МодульКассовыеСменыКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("КассовыеСменыКлиент");
		Возврат МодульКассовыеСменыКлиент.СтатусПоследнейСмены(ИдентификаторУстройства);
	Иначе
		РезультатОперации = Новый Структура();
		РезультатОперации.Вставить("Активна", Истина);
		РезультатОперации.Вставить("Открыта", Истина);
		РезультатОперации.Вставить("ТекущийНомерЧека", 1); 
		РезультатОперации.Вставить("КассоваяСмена", Неопределено);
		Возврат РезультатОперации; 
	КонецЕсли;
	
КонецФункции


// Параметры фискальной операции.
// 
// Возвращаемое значение:
//   см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыФискальнойОперации.
Функция ПараметрыФискальнойОперации() Экспорт
	
	Возврат ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыФискальнойОперации();
	
КонецФункции


Процедура ВыполнитьВыборФискальногоУстройства(КонтекстОперации)
	
	Если КонтекстОперации.ИдентификаторУстройства = Неопределено Или ПустаяСтрока(КонтекстОперации.ИдентификаторУстройства) Тогда
		
		ПоддерживаемыеТипы = МенеджерОборудованияКлиентСервер.ПараметрыТипыОборудования();
		ПоддерживаемыеТипы.ПринтерЧеков = Истина;
		ПоддерживаемыеТипы.ККТ = Истина;
		
		Оповещение = Новый ОписаниеОповещения("ВыполнитьОперациюНаФискальномУстройстве", ЭтотОбъект, КонтекстОперации);
		МенеджерОборудованияКлиент.ВыбратьУстройство(Оповещение, ПоддерживаемыеТипы,
			НСтр("ru = 'Выберите фискальное устройство';
				|en = 'Select a fiscal device'"), 
			НСтр("ru = 'Фискальное устройство не подключено.';
				|en = 'Fiscal device is not connected.'"), 
			НСтр("ru = 'Фискальное устройство не выбрано.';
				|en = 'Fiscal device is not selected.'"));
			
	Иначе
		ВыполнитьОперациюНаФискальномУстройстве(КонтекстОперации, КонтекстОперации);
	КонецЕсли
	
КонецПроцедуры

Процедура ВыполнитьОперациюНаФискальномУстройстве(РезультатВыбора, КонтекстОперации) Экспорт
	
	Если РезультатВыбора.Результат Тогда 
		ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды(КонтекстОперации.Команда, 
			"ОборудованиеЧекопечатающиеУстройства", КонтекстОперации.ДополнительныеПараметры, 
			КонтекстОперации.ПодготовитьДанные, КонтекстОперации.ОбработатьДанные);
			
		Если КонтекстОперации.Свойство("ПодготовитьДанныеКлиент") Тогда
			ПараметрыВыполнениеКоманды.ПодготовитьДанныеКлиент = КонтекстОперации.ПодготовитьДанныеКлиент;
		КонецЕсли;
			
		МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(КонтекстОперации.ОповещениеПриЗавершении, КонтекстОперации.ИдентификаторКлиента, 
			РезультатВыбора.ИдентификаторУстройства, КонтекстОперации.ПараметрыОперации, ПараметрыВыполнениеКоманды);
	Иначе
		ВыполнитьОбработкуОповещения(КонтекстОперации.ОповещениеПриЗавершении, РезультатВыбора);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьПредупреждениеДрайверСнятыйСПоддержки(Результат)    
	
	Если Результат.Свойство("ДрайверСнятСПоддержки") И Результат.ДрайверСнятСПоддержки       
		 И Результат.Свойство("СообщениеПриИспользовании") И Результат.СообщениеПриИспользовании Тогда      
		ТекстЗаголовок = НСтр("ru = 'Используемый драйвер устарел и снят с поддержки.';
								|en = 'The driver is outdated and is no longer supported.'");
		ТекстПредупреждения =        
			Строка(Результат.ИдентификаторУстройства) + Символы.ПС +
			НСтр("ru = 'Использование данного драйвера может приводить к некорректному формированию фискальных данных на ККТ.';
				|en = 'This driver may lead to incorrect generation of fiscal data on the cash register.'") + Символы.ПС +
			НСтр("ru = 'Необходимо использовать актуальную версию драйвера ККТ для данного производителя.';
				|en = 'Use the current version of the cash register driver for this manufacturer.'") + Символы.ПС +
			НСтр("ru = 'Перенастройте оборудование в разделе: «Поддержка оборудования – Подключаемое оборудование»';
				|en = 'Reconfigure the peripherals in Peripherals support – Peripherals'");
		ПоказатьПредупреждение(Неопределено, ТекстПредупреждения, 0, ТекстЗаголовок);
	КонецЕсли;

КонецПроцедуры

#Область ПакетнаяОбработка

// Запустить выбор эквайрингового терминала
// 
// Параметры:
//   ОповещениеЗавершенияПодключения - ОписаниеОповещения 
//   КонтекстОперации - См. КонтекстЭквайринговойОперацииСФискализацией
Процедура ПакетнаяОбработка_ВыборУстройства(ОповещениеЗавершенияПодключения, КонтекстОперации)
	
	ИдентификаторЭТ = КонтекстОперации.ИдентификаторЭТ;
	КонтекстОперации.Вставить("ОповещениеЗавершенияПодключения", ОповещениеЗавершенияПодключения);
	// Выбрать ЭТ если не указан конкретный
	Оповещение = Новый ОписаниеОповещения("ПакетнаяОбработка_ВыборУстройстваЗавершение",
		ЭтотОбъект,
		КонтекстОперации);
	Если ИдентификаторЭТ = Неопределено Или  ПустаяСтрока(ИдентификаторЭТ) Тогда
		ПоддерживаемыеТипы = МенеджерОборудованияКлиентСервер.ПараметрыТипыОборудования();
		ПоддерживаемыеТипы.ЭквайринговыйТерминал = Истина;
		МенеджерОборудованияКлиент.ВыбратьУстройство(Оповещение,
			ПоддерживаемыеТипы,
			НСтр("ru = 'Выберите эквайринговый терминал';
				|en = 'Select a POS terminal'"), 
			НСтр("ru = 'Эквайринговый терминал не подключен.';
				|en = 'POS terminal is not connected.'"),
			НСтр("ru = 'Эквайринговый терминал не выбран.';
				|en = 'POS terminal is not selected.'"));
	Иначе
		РезультатВыполнения = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании();
		РезультатВыполнения.Результат               = Истина;
		РезультатВыполнения.ИдентификаторУстройства = ИдентификаторЭТ;
		ВыполнитьОбработкуОповещения(Оповещение, РезультатВыполнения);
	КонецЕсли;

КонецПроцедуры

#Область ЭквайринговаяОперацияСФискализацией

// Возвращает структуру для заполнения данными операции с эквайринговым терминалом и фискализации
//
// Возвращаемое значение:
//   Структура:
//     * ПараметрыФискализацииЧека - См. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека
//     * ПараметрыВыполненияЭквайринговойОперации - См. ОборудованиеПлатежныеСистемыКлиентСервер.ПараметрыВыполненияЭквайринговойОперации
//     * ОповещениеВопросПользователю - ОписаниеОповещения - Описание оповещения пользователя, для самостоятельной выдачи 
//                            сообщения, это оповещение будет вызвано в случае отрицательного ответа от эквайрингового 
//                            терминала при оплате, в процедуру будут переданы результат выполнения пакета операций, 
//                            а также структура содержащая контекст пакета, если требуется продолжить выполнение пакета,
//                            тогда нужно будет вызвать в модуле ОборудованиеЧекопечатающиеУстройстваКлиент процедуры 
//                            "НачатьФискализациюЧекаСОплатойКартой_ПродолжитьВыполнение" или
//                            "НачатьФискализациюЧекаСОплатойКартой_ПовторитьОплату".
//                            передав туда этот контекст.
//                            Процедура оповещения содержит следующие параметры:
//         Результат - Структура:
//           * РезультатОперации - См. РезультатФискализацииЧекаСОплатойКартой
//           * КонтекстОперации - Структура - контекст пакета операции который нужно передать в процедуру
//                            "ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьФискализациюЧекаСОплатойКартой_ПродолжитьВыполнение"
//                            для продолжения выполнения пакета
//         ДополнительныеПараметры - Любой - значение переданное при создании оповещения
//     * СлипЧекВТелеФискального - Булево, Неопределено - Выводить слип-чек в теле фискального, Истина - слип-чек будет выведен
//                            в теле фискального, Ложь - короткий слип-чек не будет выводиться, Неопределено - короткий слип-чек 
//                            будет выводиться если поддерживает драйвер оборудования.
//
// Пример:
//   Определение процедуры оповещения пользователя
//   Процедура ВопросПользователюЗавершение(Результат, ДополнительныеПараметры)
//      РезультатОперации = Результат.РезультатОперации;
//      КонтекстОперации  = Результат.КонтекстОперации;
//      ...
//      // продолжить выполнение пакета операций
//      ОборудованиеЧекопечатающиеУстройстваКлиент.НачатьФискализациюЧекаСОплатойКартой_ПродолжитьВыполнение(Истина, КонтекстОперации);
//   КонецПроцедуры
// 
Функция ПараметрыЭквайринговаяОперацияСФискализацией()
	
	Если ОбщегоНазначенияБПОКлиент.ИспользуетсяПлатежныеСистемы() Тогда
		МодульОборудованиеПлатежныеСистемыКлиентСервер = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиентСервер");
		ПараметрыВыполненияЭквайринговойОперации = МодульОборудованиеПлатежныеСистемыКлиентСервер.ПараметрыВыполненияЭквайринговойОперации();
	КонецЕсли;
	ПараметрыОперацииФискализацииЧека = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека();
	
	Результат = Новый Структура();
	Результат.Вставить("ПараметрыФискализацииЧека",                ПараметрыОперацииФискализацииЧека);
	Результат.Вставить("ПараметрыВыполненияЭквайринговойОперации", Неопределено);
	Результат.Вставить("ОповещениеВопросПользователю",             Неопределено);
	Результат.Вставить("СлипЧекВТелеФискального",                  Неопределено);
	Результат.Вставить("РеквизитыКартыQR",                         "");
	Возврат Результат;
	
КонецФункции

// Возвращает структуру, контекст выполнения пакета операций
//
// Возвращаемое значение:
//  Структура:
//   * ТипОперации - Строка - тип операции 
//   * Результат - Булево - результат выполнения пакета операций
//   * ОписаниеОшибки - Строка - Описание ошибки пакета операций
//   * ОповещениеЗавершения - ОписаниеОповещения - Оповещение вызываемое при завершении выполнения пакета
//   * ИдентификаторКлиента - ФормаКлиентскогоПриложения - идентификатор формы.
//   * ИдентификаторККТ - СправочникСсылка.ПодключаемоеОборудование - ККТ на котором будет печататься фискальный чек
//                        содержащий в себе слип-чек
//   * ИдентификаторЭТ - СправочникСсылка.ПодключаемоеОборудование - Эквайринговый терминал для обработки операции
//   * ПодключенныйЭТ - Структура - параметры подключенного терминала
//   * ПараметрыФискализацииЧека - См. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека
//   * ПараметрыВыполненияЭквайринговойОперации - См. ОборудованиеПлатежныеСистемыКлиентСервер.ПараметрыВыполненияЭквайринговойОперации
//   * РезультатВыполненияЭквайринговойОперации - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   * РезультатФискализацииЧека - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   * РезультатАварийнойОтменыОперации - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//   * ОповещениеВопросПользователю - ОписаниеОповещения - Описание оповещения пользователя, для самостоятельной выдачи 
//                            сообщения, это оповещение будет вызвано в случае отрицательного ответа от эквайрингового 
//                            терминала при оплате, в процедуру будут переданы результат выполнения пакета операций, 
//                            а также структура содержащая контекст пакета.
//   * ОставитьПодключенным - Булево - Оставить оборудование подключенным после выполнения пакета
//   * ДополнительныеПараметры - Структура - Дополнительные параметры которые можно передать в операции 
//                               оплаты и фискализации
//   * РеквизитыКартыQR - Строка
Функция КонтекстЭквайринговойОперацииСФискализацией() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ТипОперации",                              "");
	Результат.Вставить("Результат",                                Ложь);
	Результат.Вставить("ОписаниеОшибки",                           "");
	Результат.Вставить("ОповещениеЗавершения",                     Неопределено);
	Результат.Вставить("ИдентификаторКлиента",                     Неопределено);
	Результат.Вставить("ИдентификаторККТ",                         Неопределено);
	Результат.Вставить("ИдентификаторЭТ",                          Неопределено);
	Результат.Вставить("ПодключенныйЭТ",                           Неопределено);
	Результат.Вставить("ИдентификаторЗаписи",                      Неопределено);
	Результат.Вставить("ПараметрыФискализацииЧека",                Неопределено);
	Результат.Вставить("ПараметрыВыполненияЭквайринговойОперации", Неопределено);
	Результат.Вставить("РезультатВыполненияЭквайринговойОперации", Неопределено);
	Результат.Вставить("РезультатФискализацииЧека",                Неопределено);
	Результат.Вставить("РезультатАварийнойОтменыОперации",         Неопределено);
	Результат.Вставить("СлипЧекВТелеФискального",                  Неопределено);
	Результат.Вставить("ОповещениеВопросПользователю",             Неопределено);
	Результат.Вставить("ОставитьПодключенным",                     Ложь);
	Результат.Вставить("ДополнительныеПараметры",                  Новый Структура());
	Результат.Вставить("СуммаОперации",                            0);
	Результат.Вставить("РеквизитыКартыQR",                         "");
	Возврат Результат;
	
КонецФункции

// Задает вопрос пользователю, или же вызывает оповещение переданное в параметрах операции 
// переданной в процедуру "НачатьФискализациюЧекаСОплатойКартой"
//
// Параметры:
//   КонтекстОперации - см. КонтекстЭквайринговойОперацииСФискализацией
Процедура ОбработатьОтрицательныйРезультатЭквайринговойОперацииСФискализацией(КонтекстОперации)
	
	РезультатВыполнения          = КонтекстОперации.РезультатВыполненияЭквайринговойОперации;
	ОповещениеВопросПользователю = КонтекстОперации.ОповещениеВопросПользователю;
	ОписаниеОшибки               = РезультатВыполнения.ОписаниеОшибки;
	
	Если ОповещениеВопросПользователю <> Неопределено Тогда
		// Пользователь создал свое оповещение
		Результат = Новый Структура("РезультатОперации, КонтекстОперации");
		Результат.КонтекстОперации  = КонтекстОперации;
		Результат.РезультатОперации = РезультатЭквайринговойОперацииСФискализацией(Ложь, ОписаниеОшибки, КонтекстОперации);
		ВыполнитьОбработкуОповещения(ОповещениеВопросПользователю, Результат);
	Иначе
		КонтекстОперации.Результат      = Ложь;
		КонтекстОперации.ОписаниеОшибки = ОписаниеОшибки;
		ВывестиСлипЧекКакТекстовыйДокумент("Отказ", КонтекстОперации);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет проверку допустимости выполнения операции
//
// Параметры:
//   КонтекстОперации - См. КонтекстЭквайринговойОперацииСФискализацией.
//   ОписаниеОшибки - Строка - Описание ошибки параметра операции
//
// Возвращаемое значение:
//   Булево - Возвращает истина если переданные параметры операции корректны
Функция ЭкваринговаяОперацияСФискализациейДопустима(КонтекстОперации, ОписаниеОшибки)
	
	ОписаниеОшибки = "";
	// Используется подсистема "Платежные системы"
	Если Не ОбщегоНазначенияБПОКлиент.ИспользуетсяПлатежныеСистемы() Тогда
		ТекстОшибки = НСтр("ru = 'Подсистема ""Платежные системы"" не подключена.';
							|en = '""Payment systems"" subsystem is not connected.'");
		ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
	КонецЕсли;
	
	ПараметрыВыполненияЭквайринговойОперации = КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации;
	Если ПараметрыВыполненияЭквайринговойОперации.ТипТранзакции <> "AuthorizeSales"
		И ПараметрыВыполненияЭквайринговойОперации.ТипТранзакции <> "PurchaseWithEnrollment" Тогда
		ТекстОшибки = НСтр("ru = 'Операция %1 не поддерживается данным режимом.';
							|en = '%1 transaction is not supported by this mode.'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ПараметрыВыполненияЭквайринговойОперации.ТипТранзакции);
		ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
	КонецЕсли;

	Если ПараметрыВыполненияЭквайринговойОперации.СуммаОперации <= 0 Тогда
		ОписаниеОшибки = ОписаниеОшибки + НСтр("ru = 'Сумма операции должна быть больше 0.';
												|en = 'Transaction amount must be greater than 0.'") + Символы.ПС;
	КонецЕсли;
	
	Если ПустаяСтрока(ОписаниеОшибки) Тогда
		Возврат Истина;
	Иначе
		ОписаниеОшибки = Лев(ОписаниеОшибки, СтрДлина(ОписаниеОшибки) - 1);
		Возврат Ложь;
	КонецЕсли;
		
КонецФункции

#КонецОбласти

#Область ПродажаСВыдачейНаличных

// Выполняет проверку корректности заполнения банковского платежного агента
//
// Параметры:
//   КонтекстОперации - см. КонтекстПродажаСВыдачейНаличных.
//   ОписаниеОшибки - Строка - Описание ошибки параметра операции
//
Процедура ПроверитьКорректностьБанковскогоАгента(КонтекстОперации, ОписаниеОшибки)
	
	ПараметрыЧекаВыдачи = КонтекстОперации.ПараметрыФискализацииЧекаВыдачи;
	Если ПараметрыЧекаВыдачи.ПозицииЧека.Количество()=0 Тогда
		ТекстОшибки    = НСтр("ru = 'Не задана строка операции выдачи в позиции чека.';
								|en = 'Issue transaction line is not specified in the receipt item.'");
		ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
	Иначе
		ДанныеАгентаПозиция = ПараметрыЧекаВыдачи.ПозицииЧека[0].ДанныеАгента; // см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыДанныеАгента
		ДанныеАгентаШапка   = ПараметрыЧекаВыдачи.ДанныеАгента; // см. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыДанныеАгента
		
		Если Не ЗначениеЗаполнено(ДанныеАгентаПозиция.ОператорПеревода.Телефон)
			И Не ЗначениеЗаполнено(ДанныеАгентаШапка.ОператорПеревода.Телефон) Тогда
			ТекстОшибки    = НСтр("ru = 'Не установлен телефон оператора перевода банковского платежного агента (эквайера).';
									|en = 'Phone number of the bank paying agent operator (acquirer) is not set.'") ;
			ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеАгентаПозиция.ОператорПеревода.Наименование)
			И Не ЗначениеЗаполнено(ДанныеАгентаШапка.ОператорПеревода.Наименование) Тогда
			ТекстОшибки    = НСтр("ru = 'Не установлено наименование оператора перевода банковского платежного агента (эквайера).';
									|en = 'Name of the bank paying agent operator (acquirer) is not set.'") ;
			ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеАгентаПозиция.ОператорПеревода.Адрес)
			И Не ЗначениеЗаполнено(ДанныеАгентаШапка.ОператорПеревода.Адрес) Тогда
			ТекстОшибки    = НСтр("ru = 'Не установлен адрес оператора перевода банковского платежного агента (эквайера).';
									|en = 'Address of the bank paying agent operator (acquirer) is not set.'") ;
			ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеАгентаПозиция.ОператорПеревода.ИНН)
			И Не ЗначениеЗаполнено(ДанныеАгентаШапка.ОператорПеревода.ИНН) Тогда
			ТекстОшибки    = НСтр("ru = 'Не установлен ИНН оператора перевода банковского платежного агента (эквайера).';
									|en = 'TIN of the bank paying agent operator (acquirer) is not set.'") ;
			ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеАгентаПозиция.ПлатежныйАгент.Телефон)
			И Не ЗначениеЗаполнено(ДанныеАгентаШапка.ПлатежныйАгент.Телефон) Тогда
			ТекстОшибки    = НСтр("ru = 'Не установлен телефон банковского платежного агента (организации).';
									|en = 'Bank paying agent (company) phone number is not set .'") ;
			ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеАгентаПозиция.ПлатежныйАгент.Операция)
			И Не ЗначениеЗаполнено(ДанныеАгентаШапка.ПлатежныйАгент.Операция) Тогда
			ТекстОшибки    = НСтр("ru = 'Не установлена операция банковского платежного агента.';
									|en = 'Bank paying agent transaction is not set.'") ;
			ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет проверку допустимости выполнения операции
//
// Параметры:
//   КонтекстОперации - см. КонтекстПродажаСВыдачейНаличных.
//   ОписаниеОшибки - Строка - Описание ошибки параметра операции
//
// Возвращаемое значение:
//   Булево - Возвращает истина если переданные параметры операции корректны
Функция ПродажаСВыдачейНаличныхДопустима(КонтекстОперации, ОписаниеОшибки)
	
	КратностьСуммыВыдачиНаличных    = 100;
	
	ПараметрыВыполненияЭквайринговойОперации = КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации;
	ПараметрыФискализацииЧекаВыдачи          = КонтекстОперации.ПараметрыФискализацииЧекаВыдачи;
	ТипТранзакции                            = ПараметрыВыполненияЭквайринговойОперации.ТипТранзакции;
	СуммаОперации                            = ПараметрыВыполненияЭквайринговойОперации.СуммаОперации;
	СуммаНаличных                            = ПараметрыВыполненияЭквайринговойОперации.СуммаНаличных;
	ПодключенноеУстройство                   = КонтекстОперации.ПодключенныйЭТ;

	Отказ = Ложь;
	МенеджерОборудованияВызовСервераПереопределяемый.ПроверитьДостаточностьНаличныхСредств(
		КонтекстОперации.ИдентификаторККТ, 
		СуммаНаличных,
		Отказ);
	Если Отказ Тогда
		ТекстОшибки = НСтр("ru = 'В ККТ не достаточно денежных средств.';
							|en = 'There is not enough cash in the cash register.'");
		ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
	КонецЕсли;
	
	// проверка кратности суммы выдачи наличных
	Если СуммаНаличных % КратностьСуммыВыдачиНаличных <> 0 Тогда
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Сумма выдачи наличных должна быть кратной %1.';
									|en = 'Cash issue amount must be a multiple of %1.'"), КратностьСуммыВыдачиНаличных);
		ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
	КонецЕсли;
	
	Если ПараметрыФискализацииЧекаВыдачи = Неопределено Тогда
		ТекстОшибки = НСтр("ru = 'Не заданы параметры фискализации чека выдачи наличных.';
							|en = 'Cash issue receipt fiscalization parameters are not set.'");
		ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
	КонецЕсли;
	
	// Используется подсистема "Платежные системы"
	Если Не ОбщегоНазначенияБПОКлиент.ИспользуетсяПлатежныеСистемы() Тогда
		ТекстОшибки = НСтр("ru = 'Подсистема ""Платежные системы"" не подключена.';
							|en = '""Payment systems"" subsystem is not connected.'");
		ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
	КонецЕсли;
	
	Если ТипТранзакции <> "PayByPaymentCardWithCashWithdrawal" Тогда
		ТекстОшибки = НСтр("ru = 'Операция %1 не поддерживается данным режимом.';
							|en = '%1 transaction is not supported by this mode.'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ТипТранзакции);
		ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
	КонецЕсли;

	Если СуммаОперации <= 0 Тогда
		ТекстОшибки = НСтр("ru = 'Сумма операции должна быть больше 0.';
							|en = 'Transaction amount must be greater than 0.'");
		ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки + Символы.ПС;
	КонецЕсли;
	
	ПроверитьКорректностьБанковскогоАгента(КонтекстОперации, ОписаниеОшибки);
	
	Если ПустаяСтрока(ОписаниеОшибки) Тогда
		Возврат Истина;
	Иначе
		ОписаниеОшибки = Лев(ОписаниеОшибки, СтрДлина(ОписаниеОшибки) - 1);
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

// Заполняет параметры выполнения эквайринговой операции на основании параметров фискальной операции
//
// Параметры:
//   КонтекстОперации - См. КонтекстПродажаСВыдачейНаличных
Процедура ПодготовитьПараметрыЭквайринговойОперации(КонтекстОперации)
	
	ПараметрыВыполненияЭквайринговойОперации = КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации;
	ПараметрыФискализацииЧека                = КонтекстОперации.ПараметрыФискализацииЧека;
	
	Если ПараметрыВыполненияЭквайринговойОперации = Неопределено Тогда
		
		Если ОбщегоНазначенияБПОКлиент.ИспользуетсяПлатежныеСистемы() Тогда
			МодульОборудованиеПлатежныеСистемыКлиентСервер = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиентСервер");
			ПараметрыВыполненияЭквайринговойОперации = МодульОборудованиеПлатежныеСистемыКлиентСервер.ПараметрыВыполненияЭквайринговойОперации();
		КонецЕсли;
		КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации = ПараметрыВыполненияЭквайринговойОперации;
		
		СуммаОперации = 0;
		ТаблицаОплат  = ПараметрыФискализацииЧека.ТаблицаОплат;
		Для Каждого СтрокаОплаты Из ТаблицаОплат Цикл
			Если СтрокаОплаты.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Электронно") Тогда
				СуммаОперации = СуммаОперации + СтрокаОплаты.Сумма;
			КонецЕсли;
		КонецЦикла;
		
		Если КонтекстОперации.ТипОперации = "ПродажаСВыдачейНаличных" Тогда
			СуммаВыдачи = КонтекстОперации.СуммаВыдачи;
			ПараметрыВыполненияЭквайринговойОперации.СуммаНаличных = СуммаВыдачи;
			ПараметрыВыполненияЭквайринговойОперации.СуммаОперации = СуммаОперации;
		ИначеЕсли КонтекстОперации.ТипОперации = "ПокупкаСЗачислением" Тогда
			ПараметрыВыполненияЭквайринговойОперации.СуммаОперации = СуммаОперации;
		ИначеЕсли КонтекстОперации.ТипОперации = "ФискализацияЧекаСОплатой" Тогда
			ПараметрыВыполненияЭквайринговойОперации.СуммаОперации = СуммаОперации;
		Иначе
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Неизвестный тип операции %1';
											|en = 'Unknown transaction type: %1'"), КонтекстОперации.ТипОперации);
		КонецЕсли;
		
		ПараметрыВыполненияЭквайринговойОперации.ДокументОснование = ПараметрыФискализацииЧека.ДокументОснование;
		ПараметрыВыполненияЭквайринговойОперации.РеквизитыКартыQR  = КонтекстОперации.РеквизитыКартыQR;
		ПараметрыВыполненияЭквайринговойОперации.ИдентификаторЗапроса = Новый УникальныйИдентификатор();
		
	КонецЕсли;
	
	Если КонтекстОперации.ТипОперации = "ПродажаСВыдачейНаличных" Тогда
		ПараметрыВыполненияЭквайринговойОперации.ТипТранзакции = "PayByPaymentCardWithCashWithdrawal";
	ИначеЕсли КонтекстОперации.ТипОперации = "ПокупкаСЗачислением" Тогда
		ПараметрыВыполненияЭквайринговойОперации.ТипТранзакции = "PurchaseWithEnrollment";
	ИначеЕсли КонтекстОперации.ТипОперации = "ФискализацияЧекаСОплатой" Тогда
		ПараметрыВыполненияЭквайринговойОперации.ТипТранзакции = "AuthorizeSales";
	Иначе
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Неизвестный тип операции %1';
										|en = 'Unknown transaction type: %1'"), КонтекстОперации.ТипОперации);
	КонецЕсли;
	
	
КонецПроцедуры

// Подготавливает контекст пакетной операции для выполнения
// 
// Параметры:
//   КонтекстОперации - См. КонтекстПродажаСВыдачейНаличных
//   ДополнительныеПараметры - Структура - дополнительные параметры переданные при запуске пакета
Процедура ПодготовитьКонтекстПакетнойОперации(КонтекстОперации, ДополнительныеПараметры)
	
	Если Не ЗначениеЗаполнено(ДополнительныеПараметры) Тогда
		ДополнительныеПараметры = Новый Структура();
	КонецЕсли;
	КонтекстОперации.ДополнительныеПараметры = ДополнительныеПараметры;
	
	// запомнить состояние ОставитьПодключенным переданные пользователем
	Если ДополнительныеПараметры.Свойство("ОставитьПодключенным") Тогда
		КонтекстОперации.ОставитьПодключенным = ДополнительныеПараметры.ОставитьПодключенным;
	КонецЕсли;
	
	ПодготовитьПараметрыЭквайринговойОперации(КонтекстОперации);
	
	ПараметрыВыполненияЭквайринговойОперации = КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации;
	КонтекстОперации.СуммаОперации           = ПараметрыВыполненияЭквайринговойОперации.СуммаОперации;
	
	// оставить подключенным оборудование, на все время выполнения пакета
	ДополнительныеПараметры.Вставить("ОставитьПодключенным", Истина);
КонецПроцедуры

// Определяет существует ли возможность печати короткого слип-чека в теле фискального
// 
// Параметры:
//   КонтекстОперации - См. КонтекстПродажаСВыдачейНаличных
Процедура ОпределитьВозможностьПечатиСлипЧекаВТелеФискального(КонтекстОперации)
	
	ПодключенноеУстройство = КонтекстОперации.ПодключенныйЭТ;
	
	// если не установлено в параметрах принудительная печать слип-чека
	Если КонтекстОперации.СлипЧекВТелеФискального = Неопределено Тогда
		// тогда печатать слип чек в зависимости от устройства
		Если ПодключенноеУстройство.КороткиеСлипЧеки И Не ПодключенноеУстройство.ПечатьКвитанцийНаТерминале Тогда
			КонтекстОперации.СлипЧекВТелеФискального = Истина;
		Иначе
			КонтекстОперации.СлипЧекВТелеФискального = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если КонтекстОперации.СлипЧекВТелеФискального Тогда
		КонтекстОперации.ДополнительныеПараметры.Вставить("ИспользоватьПечатающееУстройство", Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Отключает оборудование используемое в пакете (ККТ и ЭТ)
//
// Параметры:
//   КонтекстОперации - см. КонтекстЭквайринговойОперацииСФискализацией
Процедура ОтключитьОборудованиеПакета(КонтекстОперации)
	
	// вернуть исходное значение ОставитьПодключенным
	ДополнительныеПараметры = КонтекстОперации.ДополнительныеПараметры;
	Если ДополнительныеПараметры.Свойство("ОставитьПодключенным") Тогда
		ДополнительныеПараметры.ОставитьПодключенным = КонтекстОперации.ОставитьПодключенным;
	КонецЕсли;
	ОтключатьОборудование = Не КонтекстОперации.ОставитьПодключенным;

	// Требуется отключить оборудование, т.к. во время операции отключение оборудования было отменено
	ПодключенноеУстройствоККТ =  МенеджерОборудованияКлиент.ПолучитьПодключенноеУстройство(КонтекстОперации.ИдентификаторККТ);
	Если ПодключенноеУстройствоККТ <> Неопределено Тогда
		Если ОтключатьОборудование Или ПодключенноеУстройствоККТ.СетевоеОборудование Тогда
			МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоИдентификатору(
				Неопределено, 
				КонтекстОперации.ИдентификаторКлиента, 
				КонтекстОперации.ИдентификаторККТ);
		КонецЕсли;
	КонецЕсли;

	ПодключенноеУстройствоЭТ =  МенеджерОборудованияКлиент.ПолучитьПодключенноеУстройство(КонтекстОперации.ИдентификаторЭТ);
	Если ПодключенноеУстройствоЭТ <> Неопределено Тогда
		Если ОтключатьОборудование Или ПодключенноеУстройствоЭТ.СетевоеОборудование Тогда
			МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПоИдентификатору(
				Неопределено, 
				КонтекстОперации.ИдентификаторКлиента, 
				КонтекстОперации.ИдентификаторЭТ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Формирует результат пакета операций и вызывает оповещение пользователя при завершении если оно было задано.
//
// Параметры:
//   Результат - Булево - флаг того, что пакет операций завершился успешно
//   ОписаниеОшибки - Строка - Описание ошибки, которое будет передано в оповещении
//   КонтекстОперации - см. КонтекстЭквайринговойОперацииСФискализацией
Процедура ЗавершитьПакетОпераций(Результат, ОписаниеОшибки, КонтекстОперации)

	ТипОперации          = КонтекстОперации.ТипОперации;
	ОповещениеЗавершения = КонтекстОперации.ОповещениеЗавершения;
	
	Если КонтекстОперации.РезультатФискализацииЧека <> Неопределено 
		И КонтекстОперации.РезультатФискализацииЧека.Результат Тогда
		ИдентификаторЗапроса          = КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации.ИдентификаторЗапроса;
		ИдентификаторФискальнойЗаписи = КонтекстОперации.ПараметрыФискализацииЧека.ИдентификаторФискальнойЗаписи;
		ЛогированиеОперацийБПОСлужебныйВызовСервера.УстановитьИдентификаторФискальнойЗаписи(ИдентификаторЗапроса, ИдентификаторФискальнойЗаписи);
	КонецЕсли;
	
	ОтключитьОборудованиеПакета(КонтекстОперации);
	Если ОповещениеЗавершения <> Неопределено Тогда
		Если ТипОперации = "ФискализацияЧекаСОплатой" Тогда
			РезультатОперации = РезультатЭквайринговойОперацииСФискализацией(Результат, ОписаниеОшибки, КонтекстОперации);
		ИначеЕсли ТипОперации = "ПокупкаСЗачислением" Тогда
			РезультатОперации = РезультатЭквайринговойОперацииСФискализацией(Результат, ОписаниеОшибки, КонтекстОперации);
		ИначеЕсли ТипОперации = "ПродажаСВыдачейНаличных" Тогда
			РезультатОперации = РезультатПродажаСВыдачейНаличных(Результат, ОписаниеОшибки, КонтекстОперации);
		Иначе
			ВызватьИсключение НСтр("ru = 'Неизвестный тип операции.';
									|en = 'Unknown transaction type.'");
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ОповещениеЗавершения, РезультатОперации);
	КонецЕсли;
КонецПроцедуры

#Область ФормированиеСлипЧекаВТелеФискального

// Возвращает структуру для формирования шаблона слип-чека
//
// Возвращаемое значение:
//   Структура:
//     * ПараметрыФискализацииЧека - См. ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыОперацииФискализацииЧека
//     * ПараметрыВыполненияЭквайринговойОперации - См. ОборудованиеПлатежныеСистемыКлиентСервер.ПараметрыВыполненияЭквайринговойОперации
//     * РезультатВыполненияЭквайринговойОперации - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//     * РезультатАварийнойОтменыОперации - См. МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании
//     * ДополнительныеПараметры - Структура - Дополнительные параметры переданные ранее в процедуру 
//                                 "НачатьФискализациюЧекаСОплатойКартой"
Функция ПараметрыФормированияШаблонаСлипЧека()
	
	Результат = Новый Структура();
	Результат.Вставить("ПараметрыФискализацииЧека",                Неопределено);
	Результат.Вставить("ПараметрыВыполненияЭквайринговойОперации", Неопределено);
	Результат.Вставить("РезультатВыполненияЭквайринговойОперации", Неопределено);
	Результат.Вставить("РезультатФискализацииЧека",                Неопределено);
	Результат.Вставить("РезультатАварийнойОтменыОперации",         Неопределено);
	Результат.Вставить("ДополнительныеПараметры",                  Неопределено);
	Возврат Результат;
	
КонецФункции

// Формирует шаблон слип-чека и печатает его как текстовый документ на ККТ
//
// Параметры:
//   Тип - Строка - Тип слип-чека
//   КонтекстОперации - см. КонтекстЭквайринговойОперацииСФискализацией
Процедура ВывестиСлипЧекКакТекстовыйДокумент(Тип, КонтекстОперации)
	
	Оповещение = Новый ОписаниеОповещения("ПакетнаяОбработка_ПечатьТекстаЗавершение", 
		ЭтотОбъект, 
		КонтекстОперации);
	Если Не КонтекстОперации.СлипЧекВТелеФискального Тогда
		ВыполнитьОбработкуОповещения(Оповещение, Неопределено);
		Возврат;
	КонецЕсли;

	ПараметрыСлипЧека = КонтекстОперации.РезультатВыполненияЭквайринговойОперации;
	Если КонтекстОперации.ТипОперации = "ФискализацияЧекаСОплатой" Тогда
		ПараметрыФискализацииЧека = КонтекстОперации.ПараметрыФискализацииЧека;
		РезультатФискализацииЧека = КонтекстОперации.РезультатФискализацииЧека;
	ИначеЕсли КонтекстОперации.ТипОперации = "ПродажаСВыдачейНаличных" Тогда
		ПараметрыФискализацииЧека = КонтекстОперации.ПараметрыФискализацииЧекаВыдачи;
		РезультатФискализацииЧека = КонтекстОперации.РезультатФискализацииЧекаВыдачи;
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестный тип операции.';
								|en = 'Unknown transaction type.'");
	КонецЕсли;

	// дать возможность пользователю поменять текст слип чека
	СтандартнаяОбработка = Истина;
	ДанныеДляШаблона     = ПараметрыФормированияШаблонаСлипЧека();
	ДанныеДляШаблона.ПараметрыФискализацииЧека                = ПараметрыФискализацииЧека;
	ДанныеДляШаблона.ПараметрыВыполненияЭквайринговойОперации = КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации;
	ДанныеДляШаблона.РезультатВыполненияЭквайринговойОперации = КонтекстОперации.РезультатВыполненияЭквайринговойОперации;
	ДанныеДляШаблона.РезультатФискализацииЧека                = РезультатФискализацииЧека;
	ДанныеДляШаблона.РезультатАварийнойОтменыОперации         = КонтекстОперации.РезультатАварийнойОтменыОперации;
	ДанныеДляШаблона.ДополнительныеПараметры                  = КонтекстОперации.ДополнительныеПараметры;
	
	Если ОбщегоНазначенияБПОКлиент.ИспользуетсяПлатежныеСистемы() Тогда
		МодульОборудованиеПлатежныеСистемыКлиентСервер = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиентСервер");
		Текст = МодульОборудованиеПлатежныеСистемыКлиентСервер.ТекстСлипЧекаПоУмолчанию(
			ДанныеДляШаблона.РезультатВыполненияЭквайринговойОперации, Ложь);
	Иначе
		Текст = "";
	КонецЕсли;
	
	МенеджерОборудованияКлиентПереопределяемый.СформироватьШаблонСлипЧека(Тип, ДанныеДляШаблона, Текст, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;

	// запомнить текст последнего слип-чека в глобальной переменной
	Если Не ПустаяСтрока(Текст) И ОбщегоНазначенияБПОКлиент.ИспользуетсяПлатежныеСистемы() Тогда
		МодульОборудованиеПлатежныеСистемыКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиент");
		МодульОборудованиеПлатежныеСистемыКлиент.УстановитьПоследнийСлипЧек(Текст);
	КонецЕсли;

	ПараметрыОперации = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПараметрыПечатиТекста(Текст);
	НачатьПечатьТекста(Оповещение, 
		КонтекстОперации.ИдентификаторКлиента,
		КонтекстОперации.ИдентификаторККТ, 
		ПараметрыОперации,
		КонтекстОперации.ДополнительныеПараметры);
		
КонецПроцедуры

// Получает текст слип-чека из предопределенной процедуры и вставляет его в тело фискального чека,
// так же записывает текст последнего слип-чека в глобальную переменную
// 
// Параметры:
//   КонтекстОперации - см. КонтекстЭквайринговойОперацииСФискализацией
Процедура ВставитьСлипЧекВТелоФискальногоЧека(КонтекстОперации)

	Если Не КонтекстОперации.СлипЧекВТелеФискального Тогда
		Возврат;
	КонецЕсли;

	// сформировать тест слип чека по умолчанию
	Тип   = "Обычный";
	Если КонтекстОперации.ТипОперации = "ФискализацияЧекаСОплатой" Тогда
		ПараметрыФискализацииЧека = КонтекстОперации.ПараметрыФискализацииЧека;
	ИначеЕсли КонтекстОперации.ТипОперации = "ПокупкаСЗачислением" Тогда
		ПараметрыФискализацииЧека = КонтекстОперации.ПараметрыФискализацииЧека;
	ИначеЕсли КонтекстОперации.ТипОперации = "ПродажаСВыдачейНаличных" Тогда
		ПараметрыФискализацииЧека = КонтекстОперации.ПараметрыФискализацииЧека;
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестный тип операции.';
								|en = 'Unknown transaction type.'");
	КонецЕсли;
	
	// дать возможность пользователю поменять текст слип чека
	СтандартнаяОбработка = Истина;
	ДанныеДляШаблона     = ПараметрыФормированияШаблонаСлипЧека();
	ДанныеДляШаблона.ПараметрыФискализацииЧека                = ПараметрыФискализацииЧека;
	ДанныеДляШаблона.ПараметрыВыполненияЭквайринговойОперации = КонтекстОперации.ПараметрыВыполненияЭквайринговойОперации;
	ДанныеДляШаблона.РезультатВыполненияЭквайринговойОперации = КонтекстОперации.РезультатВыполненияЭквайринговойОперации;
	ДанныеДляШаблона.ДополнительныеПараметры                  = КонтекстОперации.ДополнительныеПараметры;
	
	Если ОбщегоНазначенияБПОКлиент.ИспользуетсяПлатежныеСистемы() Тогда
		МодульОборудованиеПлатежныеСистемыКлиентСервер = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиентСервер");
		Текст = МодульОборудованиеПлатежныеСистемыКлиентСервер.ТекстСлипЧекаПоУмолчанию(
			ДанныеДляШаблона.РезультатВыполненияЭквайринговойОперации);
	Иначе
		Текст = "";
	КонецЕсли;
	
	МенеджерОборудованияКлиентПереопределяемый.СформироватьШаблонСлипЧека(Тип, ДанныеДляШаблона, Текст, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	// добавить текст слип чека в тело фискального чека
	Если ПустаяСтрока(ПараметрыФискализацииЧека.ТекстКороткогоСлипЧека) Тогда
		ПараметрыФискализацииЧека.ТекстКороткогоСлипЧека = Текст;
	КонецЕсли;
	
	// запомнить текст последнего слип-чека в глобальной переменной
	Если Не ПустаяСтрока(Текст) И ОбщегоНазначенияБПОКлиент.ИспользуетсяПлатежныеСистемы() Тогда
		МодульОборудованиеПлатежныеСистемыКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеПлатежныеСистемыКлиент");
		МодульОборудованиеПлатежныеСистемыКлиент.УстановитьПоследнийСлипЧек(Текст);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти