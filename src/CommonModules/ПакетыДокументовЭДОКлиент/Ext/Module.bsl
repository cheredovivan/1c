
// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//  * ИдентификаторПакета - УникальныйИдентификатор
//  Элемент - ДекорацияФормы,КнопкаФормы 
//  НавигационнаяСсылка - Строка
//  СтандартнаяОбработка - Булево
//  РезультатОбработкиНаСервере - Структура:
//  * СоставМенюВыбора - СписокЗначений из ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
Процедура ЭлементУправленияПакета_ОбработкаНавигационнойСсылки(Форма, Элемент, НавигационнаяСсылка, СтандартнаяОбработка, РезультатОбработкиНаСервере) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылка = "Show" Тогда
		
		СоставМеню = РезультатОбработкиНаСервере.СоставМенюВыбора;
		Если Не ЗначениеЗаполнено(СоставМеню) Тогда
			Возврат;
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура("ИдентификаторПакета", Форма.ИдентификаторПакета);
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПослеВыбораДокументаПакетаИзМенюЕще",
			ПакетыДокументовЭДОКлиент, ДополнительныеПараметры);
		
		Форма.ПоказатьВыборИзМеню(ОписаниеОповещенияОЗавершении, СоставМеню, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//  * Объект - ДанныеФормыСтруктура:
//  ** Ссылка - ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  ** Организация - ОпределяемыйТип.Организация
//  ** Контрагент - ОпределяемыйТип.УчастникЭДО
//  ** ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
//  ** ЭтоОблачныйЭДО - Булево
//  * ИдентификаторПакета - УникальныйИдентификатор
//  * ОтображаемыеДокументыПакета - СписокЗначений из ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  * ОтключитьТранслитерацию - Булево
//  Элемент - ДекорацияФормы,КнопкаФормы
//  СтандартнаяОбработка - Булево
Процедура ЭлементУправленияПакета_Нажатие(Форма, Элемент, СтандартнаяОбработка) Экспорт

	Если Элемент = Форма.Элементы["ЭлементУправленияПакета_ЭлементУправленияПакета_ДобавитьДокумент"] Тогда
		
		СпособыДобавленияВПакет = СпособыДобавленияДокументаВПакет();
		
		СписокПунктовМеню = Новый СписокЗначений; // СписокЗначений из Строка
		СписокПунктовМеню.Добавить(СпособыДобавленияВПакет.ДобавитьВПакетФайлСКомпьютера,
			НСтр("ru = 'Файлы с компьютера';
				|en = 'Local files'"));
		СписокПунктовМеню.Добавить(СпособыДобавленияВПакет.ДобавитьВПакетПрисоединенныйФайл,
			НСтр("ru = 'Файл из хранилища';
				|en = 'File from storage'"));
		СписокПунктовМеню.Добавить(СпособыДобавленияВПакет.ДобавитьВПакетДокументИнформационнойБазы,
			НСтр("ru = 'Документ информационной базы';
				|en = 'Infobase document'"));
		
		ДополнительныеПараметры = НовыеПараметрыДобавленияВПакет();
		ДополнительныеПараметры.ИдентификаторПакета = Форма.ИдентификаторПакета;
		ДополнительныеПараметры.ЭлектронныйДокумент = Форма.Объект.Ссылка;
		ДополнительныеПараметры.Отправитель = Форма.Объект.Организация;
		ДополнительныеПараметры.Получатель = Форма.Объект.Контрагент;
		ДополнительныеПараметры.Договор = Форма.Объект.ДоговорКонтрагента;
		ДополнительныеПараметры.ОтключитьТранслитерацию = Форма.ОтключитьТранслитерацию;
		ДополнительныеПараметры.ЭтоОблачныйЭДО = Форма.Объект.ЭтоОблачныйЭДО;
		
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ПослеВыбораСпособаДобавленияДокументаВПакет",
			ЭтотОбъект, ДополнительныеПараметры);
			
		Форма.ПоказатьВыборИзМеню(ОписаниеОповещенияОЗавершении, СписокПунктовМеню, Элемент);
		
	ИначеЕсли СтрНайти(Элемент.Имя, "ЭлементУправленияПакета_УдалитьДокумент_") > 0 Тогда
		ИндексДокумента = Число(СтрЗаменить(Элемент.Имя, "ЭлементУправленияПакета_УдалитьДокумент_", "")); 
		ДокументКУдалениюИзПакета = Форма.ОтображаемыеДокументыПакета[ИндексДокумента - 1].Значение; // ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
		
		УдалитьДокументИзПакета(Форма.ИдентификаторПакета, ДокументКУдалениюИзПакета);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ИдентификаторПакета - УникальныйИдентификатор
//  ДокументКУдалениюИзПакета - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
Процедура УдалитьДокументИзПакета(ИдентификаторПакета, ДокументКУдалениюИзПакета) Экспорт

	КонтекстДиагностики = ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики();
	ДлительнаяОперация = ПакетыДокументовЭДОВызовСервера.УдалитьДокументИзПакета(ИдентификаторПакета,
		ДокументКУдалениюИзПакета, КонтекстДиагностики);
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ИдентификаторПакета", ИдентификаторПакета);
	ПараметрыОповещения.Вставить("УдаленныйДокумент", ДокументКУдалениюИзПакета);
	ПараметрыОповещения.Вставить("КонтекстДиагностики", КонтекстДиагностики);
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		УдалитьДокументИзПакетаПослеОжидания(ДлительнаяОперация, ПараметрыОповещения);
		Возврат;
	КонецЕсли;
	
	ОповещениеПослеОжидания = Новый ОписаниеОповещения("УдалитьДокументИзПакетаПослеОжидания", 
		ПакетыДокументовЭДОКлиент, ПараметрыОповещения);
		
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеПослеОжидания, ПараметрыОжидания);
	
КонецПроцедуры

// Параметры:
//  ВыбранныйСпособДобавления - Неопределено - если пользователь отказался от выбора.
//                            - ЭлементСпискаЗначений
//                            - Строка
//  ДополнительныеПараметры - см. НовыеПараметрыДобавленияВПакет
Процедура ПослеВыбораСпособаДобавленияДокументаВПакет(ВыбранныйСпособДобавления, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйСпособДобавления = Неопределено Тогда		
		Возврат;
	ИначеЕсли ТипЗнч(ВыбранныйСпособДобавления) = Тип("ЭлементСпискаЗначений") Тогда
		СпособДобавления = ВыбранныйСпособДобавления.Значение;
	Иначе
		СпособДобавления = ВыбранныйСпособДобавления;
	КонецЕсли;
	
	ОграниченияИОбъемПакета =
		ПакетыДокументовЭДОВызовСервера.ОграниченияИОбъемПакета(ДополнительныеПараметры);
	ДополнительныеПараметры.ОграниченияОбъемаПакетаДокументов = ОграниченияИОбъемПакета.ОграниченияОбъемаПакетаДокументов;
	ДополнительныеПараметры.ОбъемПакетаДокументов = ОграниченияИОбъемПакета.ОбъемПакетаДокументов;
	
	РезультатПроверкиПревышения = ПревышеноМаксимальноеКоличествоДокументовВПакете(ОграниченияИОбъемПакета.ОграниченияОбъемаПакетаДокументов,
		ОграниченияИОбъемПакета.ОбъемПакетаДокументов, ДополнительныеПараметры);
	Если РезультатПроверкиПревышения.Превышено Тогда
		
		ПараметрыОповещенияПослеВопроса = Новый Структура;
		ПараметрыОповещенияПослеВопроса.Вставить("ВыбранныйСпособДобавления", СпособДобавления);
		ПараметрыОповещенияПослеВопроса.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
		Оповещение = Новый ОписаниеОповещения("ПоказатьВопросОСозданииПакета_Завершение",
			ЭтотОбъект, ПараметрыОповещенияПослеВопроса);
		
		//@skip-check invocation-parameter-type-intersect
		ПоказатьВопросОСозданииПакета(РезультатПроверкиПревышения.ПревышаемыеОграничения, Оповещение);
		Возврат;
		
	КонецЕсли;
	
	ДобавитьДокументВПакетПоСпособуДобавления(СпособДобавления, ДополнительныеПараметры);
	
КонецПроцедуры

// Параметры:
//  ВыбранныйЭлемент - Неопределено - если пользователь отказался от выбора.
//                   - ЭлементСпискаЗначений
//  ДополнительныеПараметры - Структура:
//  * ИдентификаторПакета - УникальныйИдентификатор
//
Процедура ПослеВыбораДокументаПакетаИзМенюЕще(ВыбранныйЭлемент, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
		Оповестить("ВыборТекущегоДокументаПакета", ВыбранныйЭлемент.Значение, ДополнительныеПараметры.ИдентификаторПакета);
	КонецЕсли;
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
// * ИдентификаторПакета - Неопределено,УникальныйИдентификатор
// * ЭлектронныйДокумент - Неопределено,ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
// * Отправитель - Неопределено,ОпределяемыйТип.Организация
// * Получатель - Неопределено,ОпределяемыйТип.УчастникЭДО
// * Договор - Неопределено,ОпределяемыйТип.ДоговорСКонтрагентомЭДО
// * ОбъектыУчета - Массив из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
// * ЭтоОблачныйЭДО - Булево
// * ОтключитьТранслитерацию - Булево
// * ОграниченияОбъемаПакетаДокументов - Неопределено
// 									   - см. ПакетыДокументовЭДО.НовыеОграниченияОбъемаПакетаДокументовУчастников
// * ОбъемПакетаДокументов - Неопределено
//						   - см. ПакетыДокументовЭДОКлиентСервер.НовыйОбъемПакетаДокументов
// * СозданиеНовогоПакета - Булево
// * АдресХранилища - Строка - данные файла, добавляемого в пакет, помещенные во временное хранилище.
Функция НовыеПараметрыДобавленияВПакет() Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("ИдентификаторПакета", Неопределено); 
	Параметры.Вставить("ЭлектронныйДокумент", Неопределено);
	Параметры.Вставить("Отправитель", Неопределено);
	Параметры.Вставить("Получатель", Неопределено);
	Параметры.Вставить("Договор", Неопределено);
	Параметры.Вставить("ОбъектыУчета", Новый Массив);
	Параметры.Вставить("ЭтоОблачныйЭДО", Ложь);
	Параметры.Вставить("ОтключитьТранслитерацию", Ложь);
	Параметры.Вставить("ОграниченияОбъемаПакетаДокументов", Неопределено);
	Параметры.Вставить("ОбъемПакетаДокументов", Неопределено);
	Параметры.Вставить("СозданиеНовогоПакета", Ложь);
	Параметры.Вставить("АдресХранилища", "");
	Возврат Параметры;
КонецФункции

// Возвращаемое значение:
//  Строка
Функция ИмяСобытияДобавленияДокументаВПакет() Экспорт
	Возврат "ДобавлениеДокументаВПакет";
КонецФункции

// Возвращаемое значение:
//  Строка
Функция ИмяСобытияУдалениеДокументаИзПакета() Экспорт
	Возврат "УдалениеДокументаИзПакета";
КонецФункции

// Возвращаемое значение:
//  Строка
Функция ИмяСобытияИзмененияПакетаДокументовЭДОПоОбъектуУчета() Экспорт
	Возврат "ИзменениеПакетаДокументовЭДО";
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ДокументыПакета - Массив Из ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  * ИдентификаторПакета - Неопределено,УникальныйИдентификатор
//  * ОбъектыУчета - Массив Из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
Функция НовыеПараметрыОповещенияПриИзмененииПакетаДокументовЭДОПоОбъектуУчета() Экспорт
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ДокументыПакета", Новый Массив);
	ПараметрыОповещения.Вставить("ИдентификаторПакета", Неопределено);
	ПараметрыОповещения.Вставить("ОбъектыУчета", Новый Массив);
	Возврат ПараметрыОповещения;	
КонецФункции

// Параметры:
//  ДокументыПакета - Массив Из ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  ОповещениеОЗавершении - ОписаниеОповещения - описание процедуры, которая будет вызвана после создания пакета:
//  * РезультатСоздания - Структура:
//                        ** ИдентификаторПакета - Неопределено - если пакет не удалось создать.
//                                               - УникальныйИдентификатор
//                        ** КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  * ПараметрыОповещения - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  КонтекстДиагностики - Неопределено
//                      - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
Процедура СоздатьПакетДокументов(ДокументыПакета, ОповещениеОЗавершении, КонтекстДиагностики = Неопределено) Экспорт
	
	ДлительнаяОперация = ПакетыДокументовЭДОВызовСервера.СоздатьПакетДокументов(ДокументыПакета, КонтекстДиагностики);
	
	ПараметрыОповещенияПослеСоздания = Новый Структура;
	ПараметрыОповещенияПослеСоздания.Вставить("КонтекстДиагностики", КонтекстДиагностики);
	ПараметрыОповещенияПослеСоздания.Вставить("ОповещениеОЗавершении", ОповещениеОЗавершении);
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		СоздатьПакетДокументовПослеОжидания(ДлительнаяОперация, ПараметрыОповещенияПослеСоздания);
		Возврат;
	КонецЕсли;
	
	ОповещениеПослеОжидания = Новый ОписаниеОповещения("СоздатьПакетДокументовПослеОжидания", ПакетыДокументовЭДОКлиент,
		ПараметрыОповещенияПослеСоздания);
		
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеПослеОжидания, ПараметрыОжидания);
	
КонецПроцедуры

// Параметры:
//  ИдентификаторПакета - УникальныйИдентификатор
//  ЭлектронныеДокументы - Массив Из ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  ОповещениеОЗавершении - ОписаниеОповещения - описание процедуры, которая будет вызвана после создания пакета:
//  * РезультатДобавления - Структура:
//                          ** Успех - Булево
//                          ** КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  * ПараметрыОповещения - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  КонтекстДиагностики - Неопределено
//                      - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
Процедура ДобавитьДокументыВПакет(ИдентификаторПакета, ЭлектронныеДокументы, ОповещениеОЗавершении,
	КонтекстДиагностики = Неопределено) Экспорт
		
	ДлительнаяОперация = ПакетыДокументовЭДОВызовСервера.ДобавитьДокументыВПакет(ИдентификаторПакета,
		ЭлектронныеДокументы, КонтекстДиагностики);
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("КонтекстДиагностики", КонтекстДиагностики);
	ПараметрыОповещения.Вставить("ОповещениеОЗавершении", ОповещениеОЗавершении);
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ДобавитьДокументВПакетПослеОжидания(ДлительнаяОперация, ПараметрыОповещения);
		Возврат;
	КонецЕсли;
	
	ОповещениеПослеОжидания = Новый ОписаниеОповещения("ДобавитьДокументВПакетПослеОжидания", ПакетыДокументовЭДОКлиент,
		ПараметрыОповещения);
		
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеПослеОжидания, ПараметрыОжидания);
	
КонецПроцедуры

// Параметры:
// ПараметрыДобавленияВПакет - См. НовыеПараметрыДобавленияВПакет
// ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
// СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура СформироватьЭДПоФайламСДискаДобавитьВПакет(ПараметрыДобавленияВПакет, ОписанияФайлов,
	СообщенияДляПользователя) Экспорт

	ОграниченияИОбъемПакета = ПакетыДокументовЭДОВызовСервера.ОграниченияИОбъемПакета(ПараметрыДобавленияВПакет);
	ПараметрыДобавленияВПакет.ОграниченияОбъемаПакетаДокументов = 
		ОграниченияИОбъемПакета.ОграниченияОбъемаПакетаДокументов;
	ПараметрыДобавленияВПакет.ОбъемПакетаДокументов = ОграниченияИОбъемПакета.ОбъемПакетаДокументов;
	
	РезультатПроверкиПревышения = ПревышеноМаксимальноеКоличествоДокументовВПакете(
		ОграниченияИОбъемПакета.ОграниченияОбъемаПакетаДокументов,
		ОграниченияИОбъемПакета.ОбъемПакетаДокументов,
		ПараметрыДобавленияВПакет);
		
	Если РезультатПроверкиПревышения.Превышено Тогда
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ПараметрыДобавленияВПакет", ПараметрыДобавленияВПакет);
		ПараметрыОповещения.Вставить("ОписанияФайлов", ОписанияФайлов);
		ПараметрыОповещения.Вставить("СообщенияДляПользователя", СообщенияДляПользователя);
		ОповещениеОЗавершении = Новый ОписаниеОповещения(
			"СформироватьЭДПоФайламСДискаДобавитьВПакетПослеОтветаНаВопросОПревышенииОграничения", ЭтотОбъект,
			ПараметрыОповещения);

		//@skip-check invocation-parameter-type-intersect
		ПоказатьВопросОСозданииПакета(РезультатПроверкиПревышения.ПревышаемыеОграничения, ОповещениеОЗавершении);
		Возврат;
	КонецЕсли;
	
	ПомещаемыеФайлы = Новый Массив; // Массив Из ДвоичныеДанные
	Для Каждого ОписаниеФайла Из ОписанияФайлов Цикл
		ПомещаемыеФайлы.Добавить(ОписаниеФайла.ДвоичныеДанные);
	КонецЦикла;
	ПомещениеФайлаВПакетЗапрещеноПоПревышениюРазмера = Ложь;
	
	ПередНачаломДобавленияФайловВПакет(ПомещаемыеФайлы, ПомещениеФайлаВПакетЗапрещеноПоПревышениюРазмера,
		ПараметрыДобавленияВПакет);
	Если ПомещениеФайлаВПакетЗапрещеноПоПревышениюРазмера Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьИДобавитьПроизвольныеДокументыВПакетПолучитьРеквизиты(ПараметрыДобавленияВПакет, ОписанияФайлов,
		СообщенияДляПользователя);
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
// * ДобавитьВПакетФайлСКомпьютера - Строка
// * ДобавитьВПакетПрисоединенныйФайл - Строка 
// * ДобавитьВПакетДокументИнформационнойБазы - Строка
Функция СпособыДобавленияДокументаВПакет() Экспорт
	Способы = Новый Структура;
	Способы.Вставить("ДобавитьВПакетФайлСКомпьютера", "ДобавитьВПакетФайлСКомпьютера");
	Способы.Вставить("ДобавитьВПакетПрисоединенныйФайл", "ДобавитьВПакетПрисоединенныйФайл");
	Способы.Вставить("ДобавитьВПакетДокументИнформационнойБазы", "ДобавитьВПакетДокументИнформационнойБазы");
	Возврат Способы;
КонецФункции

#Область ОграниченияПакетаДокументов

// Параметры:
//  ОграниченияОбъемаПакетаДокументов - См. СервисНастроекЭДО.НовыеОграниченияОбъемаПакетаДокументов
//
// Возвращаемое значение:
//  Строка
Функция ТекстОПревышенииМаксимальногоРазмераПакетаПриСохраненииПакета(ОграниченияОбъемаПакетаДокументов) Экспорт
	
	Возврат СтрШаблон(НСтр("ru = 'Невозможно сохранить пакет документов из-за ограничений оператора %1 (размер пакета %2 Кб).
		|Выберите файлы меньшего размера или отправьте их отдельными документами.';
		|en = 'Cannot save the document package due to the %1 service operator restrictions (package size is %2 Kb).
		|Select files of a smaller size or send them as separate documents.'"),
		ОграниченияОбъемаПакетаДокументов.ОператорЭДО,
		ОграниченияОбъемаПакетаДокументов.МаксимальныйРазмерПакетаЭД);
	
КонецФункции

// Параметры:
//  ОграниченияОбъемаПакетаДокументов - См. СервисНастроекЭДО.НовыеОграниченияОбъемаПакетаДокументов
//
// Возвращаемое значение:
//  Строка
Функция ТекстОПревышенииКоличестваДокументовПриСохраненииПакета(ОграниченияОбъемаПакетаДокументов) Экспорт
	
	Возврат СтрШаблон(НСтр("ru = 'Невозможно сохранить пакет документов из-за ограничений оператора %1 (не более %2 документов в пакете).
		|Уменьшите количество документов в пакете.';
		|en = 'Cannot save the document package due to the limitations of the %1 operator (no more than %2 documents per package).
		|Reduce the number of documents in the package.'"),
		ОграниченияОбъемаПакетаДокументов.ОператорЭДО,
		ОграниченияОбъемаПакетаДокументов.МаксимальноеКоличествоЭДВПакете);
	
КонецФункции

// Параметры:
//  ОграниченияОбъемаПакетаДокументов - см. ПакетыДокументовЭДО.НовыеОграниченияОбъемаПакетаДокументовУчастников
//  ОбъемПакетаДокументов - см. ПакетыДокументовЭДОКлиентСервер.НовыйОбъемПакетаДокументов
//  ДополнительныеПараметры - см. НовыеПараметрыДобавленияВПакет
//
// Возвращаемое значение:
//  см. ПакетыДокументовЭДОКлиентСервер.НовыйРезультатПроверкиПревышения
Функция ПревышеноМаксимальноеКоличествоДокументовВПакете(ОграниченияОбъемаПакетаДокументов, ОбъемПакетаДокументов,
	ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.СозданиеНовогоПакета Тогда
		Возврат ПакетыДокументовЭДОКлиентСервер.НовыйРезультатПроверкиПревышения();
	КонецЕсли;
	
	Возврат ПакетыДокументовЭДОКлиентСервер.ПроверкаПревышенияМаксимальногоКоличестваДокументовВПакете(
		ОграниченияОбъемаПакетаДокументов, ОбъемПакетаДокументов, 1);
	
КонецФункции

// Параметры:
//  ОграниченияОбъемаПакетаДокументов - см. СервисНастроекЭДО.НовыеОграниченияОбъемаПакетаДокументов
//
// Возвращаемое значение:
//  Строка
Функция ТекстОПревышенииМаксимальногоРазмераПакетаСохранениеДокумента(ОграниченияОбъемаПакетаДокументов) Экспорт
	
	Возврат СтрШаблон(НСтр("ru = 'Невозможно сохранить документ из-за ограничений оператора %1 (размер пакета %2 Кб).
		|Выберите файл меньшего размера.';
		|en = 'Cannot save the document due to the %1 service operator restrictions (package size is %2 Kb).
		|Select a file of a smaller size.'"), ОграниченияОбъемаПакетаДокументов.ОператорЭДО,
		ОграниченияОбъемаПакетаДокументов.МаксимальныйРазмерПакетаЭД);
	
КонецФункции

// Параметры:
//  ОграниченияОбъемаПакетаДокументов - см. СервисНастроекЭДО.НовыеОграниченияОбъемаПакетаДокументов
Процедура ПредупредитьОПревышенииМаксимальногоРазмерПакета(ОграниченияОбъемаПакетаДокументов) Экспорт
	
	ТекстПредупреждения =
		СтрШаблон(НСтр("ru = 'Невозможно добавить документ из-за ограничений оператора %1 (размер пакета %2 Кб).
		|Выберите документ меньшего размера.';
		|en = 'Cannot add the document due to the %1 service operator restrictions (package size is %2 Kb).
		|Select a file of a smaller size.'"), ОграниченияОбъемаПакетаДокументов.ОператорЭДО,
		ОграниченияОбъемаПакетаДокументов.МаксимальныйРазмерПакетаЭД);
	
	ПоказатьПредупреждение(, ТекстПредупреждения);

КонецПроцедуры

// Параметры:
//  ОграниченияОбъемаПакетаДокументов - см. СервисНастроекЭДО.НовыеОграниченияОбъемаПакетаДокументов
Процедура ПредупредитьОПревышенииКоличестваДокументовПакета(ОграниченияОбъемаПакетаДокументов) Экспорт
	
	ТекстПредупреждения =
		СтрШаблон(НСтр("ru = 'Невозможно добавить документ из-за ограничений оператора %1 (не более %2 документов в пакете).
		|Уменьшите количество документов в пакете.';
		|en = 'Cannot add the document due to the limitations of the %1 operator (no more than %2 documents per package).
		|Reduce the number of documents in the package.'"), ОграниченияОбъемаПакетаДокументов.ОператорЭДО,
		ОграниченияОбъемаПакетаДокументов.МаксимальноеКоличествоЭДВПакете);
	
	ПоказатьПредупреждение(, ТекстПредупреждения);

КонецПроцедуры

#КонецОбласти // ОграниченияПакетаДокументов

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  ПараметрыДобавленияВПакет - См. НовыеПараметрыДобавленияВПакет
//  ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура СформироватьИДобавитьПроизвольныеДокументыВПакетПолучитьРеквизиты(ПараметрыДобавленияВПакет, ОписанияФайлов,
	СообщенияДляПользователя)

	Если Не ЗначениеЗаполнено(ОписанияФайлов) Тогда
		Возврат;
	КонецЕсли;

	Если ОписанияФайлов.Количество() > 1 Тогда
		СформироватьИДобавитьПроизвольныеДокументыВПакет(ОписанияФайлов, ПараметрыДобавленияВПакет,
			СообщенияДляПользователя);
		Возврат;
	КонецЕсли;
	
	ОписаниеФайла = ОписанияФайлов[0];
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ДополнительныеПараметры", ПараметрыДобавленияВПакет);
	ПараметрыОповещения.Вставить("ПараметрыФайла", ОписаниеФайла);
	ПараметрыОповещения.Вставить("СообщенияДляПользователя", СообщенияДляПользователя);
	
	РеквизитыДокумента = ИнтерфейсДокументовЭДОВызовСервера.РеквизитыДокументаПоФайлу(ОписаниеФайла);
	Если ЗначениеЗаполнено(РеквизитыДокумента) Тогда
		СформироватьИДобавитьПроизвольныйДокументВПакетПолучитьРеквизитыЗавершение(РеквизитыДокумента,
			ПараметрыОповещения);
		Возврат;
	КонецЕсли;
	
	ЗаполнятьЗначениямиПоУмолчанию = Ложь;
	ПриОпределенииСпособаВводаРеквизитовПриДобавленииВПакетФайлаСКомпьютера(ЗаполнятьЗначениямиПоУмолчанию);
	Если ЗаполнятьЗначениямиПоУмолчанию Тогда
		РеквизитыДокумента = РеквизитыНовогоПроизвольногоДокументаПоУмолчанию();
		СформироватьИДобавитьПроизвольныйДокументВПакетПолучитьРеквизитыЗавершение(РеквизитыДокумента,
			ПараметрыОповещения);
		Возврат;
	КонецЕсли;
	
	ОбработчикПослеЗаполненияРеквизитов = Новый ОписаниеОповещения(
		"СформироватьИДобавитьПроизвольныйДокументВПакетПолучитьРеквизитыЗавершение",
		ПакетыДокументовЭДОКлиент, ПараметрыОповещения);
	ИнтерфейсДокументовЭДОКлиент.ОткрытьВводРеквизитовПроизвольногоДокумента(ОбработчикПослеЗаполненияРеквизитов);

КонецПроцедуры

// Параметры:
//  Результат - Неопределено,Структура:
//  * ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//  * Номер - Строка
//  * Дата - Дата
//  * Сумма - Число
//  Параметры - Структура:
//  * ДополнительныеПараметры - См. НовыеПараметрыДобавленияВПакет
//  * ПараметрыФайла - См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  * СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура СформироватьИДобавитьПроизвольныйДокументВПакетПолучитьРеквизитыЗавершение(Результат, Параметры) Экспорт

	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;

	ПараметрыФормирования = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыСозданияДокументаПоФайлу();
	ПараметрыФормирования.Организация = Параметры.ДополнительныеПараметры.Отправитель;
	ПараметрыФормирования.Контрагент = Параметры.ДополнительныеПараметры.Получатель;
	ПараметрыФормирования.Договор = Параметры.ДополнительныеПараметры.Договор;
	ПараметрыФормирования.НомерДокумента = Результат.Номер;
	ПараметрыФормирования.ДатаДокумента = Результат.Дата;
	ПараметрыФормирования.СуммаДокумента = Результат.Сумма;
	ПараметрыФормирования.ВидДокумента = Результат.ВидДокумента;
	ОбъектыУчета =
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры.ДополнительныеПараметры, "ОбъектыУчета", Новый Массив); // Массив из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
	ПараметрыФормирования.ОбъектыУчета = ОбъектыУчета;
	
	ПараметрыФормированияПоФайлам = Новый Соответствие;
	ПараметрыФормированияПоФайлам.Вставить(Параметры.ПараметрыФайла.ИмяФайла, ПараметрыФормирования);

	СформироватьИДобавитьПроизвольныйДокументВПакет(Параметры.ДополнительныеПараметры, ПараметрыФормированияПоФайлам,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Параметры.ПараметрыФайла), Параметры.СообщенияДляПользователя);

КонецПроцедуры

// Параметры:
//  Результат - Неопределено,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  ДополнительныеПараметры - См. НовыеПараметрыДобавленияВПакет
Процедура ДобавитьВПакетДокументИнформационнойБазыЗавершениеВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Оповестить("ВыборТекущегоДокументаПакета", Результат, ДополнительныеПараметры.ИдентификаторПакета);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  Результат - Неопределено,Структура:
//  * ПараметрыВыполненияДействийПоЭДО - см. ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО
//  Контекст - Структура:
//  * ПараметрыДобавления - Структура:
//  ** ИдентификаторПакета - УникальныйИдентификатор
//  ** ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  ** Отправитель - ОпределяемыйТип.Организация
//  ** Получатель - ОпределяемыйТип.УчастникЭДО
//  ** Договор - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
//  * ПараметрыФормированияПоФайлам - Соответствие Из КлючИЗначение:
//  ** Ключ - Строка - Имя файла.
//  ** Значение - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыСозданияДокументаПоФайлу
//  * ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  * СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура ДобавитьВПакетПослеОбработкиОшибокФормирования(Результат, Контекст) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Контекст.ОписанияФайлов.Количество() > 1 Тогда
		ДобавитьВПакетПослеОбработкиОшибокФормированияДляНесколькихФайлов(Результат, Контекст);
		Возврат;
	КонецЕсли;
	
	Для Каждого ПараметрыФормированияПоФайлу Из Контекст.ПараметрыФормированияПоФайлам Цикл
		ПараметрыФормирования = ПараметрыФормированияПоФайлу.Значение;
	КонецЦикла;

	Если ЗначениеЗаполнено(Результат.ПараметрыВыполненияДействийПоЭДО)
		И ЗначениеЗаполнено(Результат.ПараметрыВыполненияДействийПоЭДО.ПодписантыОбъектов) Тогда
		Подписанты = Результат.ПараметрыВыполненияДействийПоЭДО.ПодписантыОбъектов[
			Контекст.ПараметрыДобавления.ЭлектронныйДокумент];
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыФормирования.Подписанты, Подписанты);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.ПараметрыВыполненияДействийПоЭДО)
		И ЗначениеЗаполнено(Результат.ПараметрыВыполненияДействийПоЭДО.МаршрутыПодписанияОбъектов) Тогда
		МаршрутПодписания = Результат.ПараметрыВыполненияДействийПоЭДО.МаршрутыПодписанияОбъектов[
			Контекст.ПараметрыДобавления.ЭлектронныйДокумент];
		Если ЗначениеЗаполнено(МаршрутПодписания) Тогда
			ПараметрыФормирования.МаршрутПодписания = МаршрутПодписания;
		КонецЕсли;
	КонецЕсли;
	
	СформироватьИДобавитьПроизвольныйДокументВПакет(Контекст.ПараметрыДобавления,
		Контекст.ПараметрыФормированияПоФайлам, Контекст.ОписанияФайлов, Контекст.СообщенияДляПользователя);
	
КонецПроцедуры

// Параметры:
//  Результат - См. ДобавитьВПакетПослеОбработкиОшибокФормирования.Результат
//  Контекст - См. ДобавитьВПакетПослеОбработкиОшибокФормирования.Контекст
//
Процедура ДобавитьВПакетПослеОбработкиОшибокФормированияДляНесколькихФайлов(Результат, Контекст)

	Для Каждого ОписаниеФайла Из Контекст.ОписанияФайлов Цикл
		
		ПараметрыФормированияФайла = Контекст.ПараметрыФормированияПоФайлам[ОписаниеФайла.ИмяФайла];
		
		Если ЗначениеЗаполнено(Результат.ПараметрыВыполненияДействийПоЭДО)
			И ЗначениеЗаполнено(Результат.ПараметрыВыполненияДействийПоЭДО.ПодписантыОбъектов) Тогда
			Подписанты = Результат.ПараметрыВыполненияДействийПоЭДО.ПодписантыОбъектов[ОписаниеФайла.ИмяФайла];
			Если ЗначениеЗаполнено(Подписанты) Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыФормированияФайла.Подписанты, Подписанты);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Результат.ПараметрыВыполненияДействийПоЭДО)
			И ЗначениеЗаполнено(Результат.ПараметрыВыполненияДействийПоЭДО.МаршрутыПодписанияОбъектов) Тогда
			МаршрутПодписания = Результат.ПараметрыВыполненияДействийПоЭДО.МаршрутыПодписанияОбъектов[
				ОписаниеФайла.ИмяФайла];
			ПараметрыФормированияФайла.МаршрутПодписания = МаршрутПодписания;
		КонецЕсли;
		
	КонецЦикла;
	
	СформироватьИДобавитьПроизвольныйДокументВПакет(Контекст.ПараметрыДобавления,
		Контекст.ПараметрыФормированияПоФайлам, Контекст.ОписанияФайлов, Контекст.СообщенияДляПользователя);
	
КонецПроцедуры

// Параметры:
//  ПараметрыДобавления - См. НовыеПараметрыДобавленияВПакет
//  ПараметрыФормированияПоФайлам - Соответствие Из КлючИЗначение:
//                                  * Ключ - Строка - Имя файла.
//                                  * Значение - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыСозданияДокументаПоФайлу
//  ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура СформироватьИДобавитьПроизвольныйДокументВПакет(ПараметрыДобавления, ПараметрыФормированияПоФайлам,
	ОписанияФайлов, СообщенияДляПользователя)
	
	Контекст = Новый Структура;
	Контекст.Вставить("ПараметрыДобавления", ПараметрыДобавления);
	Контекст.Вставить("ПараметрыФормированияПоФайлам", ПараметрыФормированияПоФайлам);
	Контекст.Вставить("ОписанияФайлов", ОписанияФайлов);
	Контекст.Вставить("СообщенияДляПользователя", СообщенияДляПользователя);

	ОповещениеПослеФормированияДокумента = Новый ОписаниеОповещения(
		"ДобавитьПроизвольныеДокументыВПакетПослеФормированияДокументовПоФайлам", ЭтотОбъект, Контекст);
	
	СформироватьЭлектронныеДокументыПоФайлам(Контекст, ОповещениеПослеФормированияДокумента);
	
КонецПроцедуры

// Параметры:
//  Контекст - Структура:
//  * ПараметрыДобавления - См. НовыеПараметрыДобавленияВПакет
//  * ПараметрыФормированияПоФайлам - Соответствие Из КлючИЗначение:
//                                    * Ключ - Строка - Имя файла.
//                                    * Значение - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыСозданияДокументаПоФайлу
//  * ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  * СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//  ОповещениеОЗавершении - ОписаниеОповещения - описание процедуры, которая будет вызвана после формирования документа:
//  * РезультатФормирования - Структура:
//   ** Итог - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыйИтогВыполненияДействийПоЭДО
//   ** ОшибкиФормирования - Массив из Структура
//   ** КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  * Контекст - См. СформироватьЭлектронныеДокументыПоФайлам.Контекст
Процедура СформироватьЭлектронныеДокументыПоФайлам(Контекст, ОповещениеОЗавершении)
	
	ПараметрыФормированияПоФайлам = Контекст.ПараметрыФормированияПоФайлам;
	ПараметрыДобавления = Контекст.ПараметрыДобавления;
	
	ПараметрыВыполнения = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(ПараметрыВыполнения.НаборДействий,
		ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Сформировать"));
	
	Для Каждого ОписаниеФайла Из Контекст.ОписанияФайлов Цикл
		ПараметрыФормирования = ПараметрыФормированияПоФайлам[ОписаниеФайла.ИмяФайла];
		
		ОписаниеДокумента = ИнтерфейсДокументовЭДОКлиентСервер.НовоеОписаниеДокументаДляФормированияПоФайлам();
		ОписаниеДокумента.Организация = ПараметрыФормирования.Организация;
		ОписаниеДокумента.Контрагент = ПараметрыФормирования.Контрагент;
		ОписаниеДокумента.Договор = ПараметрыФормирования.Договор;
		ОписаниеДокумента.ВидДокумента = ПараметрыФормирования.ВидДокумента;
		ОписаниеДокумента.Дата = ПараметрыФормирования.ДатаДокумента;
		ОписаниеДокумента.Номер = ПараметрыФормирования.НомерДокумента;
		ОписаниеДокумента.Сумма = ПараметрыФормирования.СуммаДокумента;
		ОписаниеДокумента.СопроводительнаяЗаписка = ПараметрыФормирования.СопроводительнаяЗаписка;
		Если ПараметрыДобавления.ЭтоОблачныйЭДО Тогда
			ОписаниеДокумента.Идентификатор = ЭлектронныеДокументыЭДОКлиентСервер.НовыйИдентификаторДокумента();
		КонецЕсли;
		ОписаниеДокумента.Подписанты = ПараметрыФормирования.Подписанты;
		ОписаниеДокумента.МаршрутПодписания = ПараметрыФормирования.МаршрутПодписания;
		
		НовыеФайлыТитула = ИнтерфейсДокументовЭДОКлиентСервер.НовыеФайлыТитулаДокументаДляФормирования();
		НовыеФайлыТитула.Основной = ОписаниеФайла;
		ОписаниеДокумента.ФайлыТитулов.Добавить(НовыеФайлыТитула);
	
		ПараметрыВыполнения.ОбъектыДействий.ОписанияДокументовПоФайлам.Добавить(ОписаниеДокумента);
	КонецЦикла;
	
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(ОповещениеОЗавершении, ПараметрыВыполнения);
	
КонецПроцедуры

// Параметры:
//  РезультатДобавления - Структура:
//                        * Успех - Булево
//                        * КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  ПараметрыОповещения - Структура:
//  * ДокументыПакета - Массив Из ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  * ИдентификаторПакета - УникальныйИдентификатор
//  * ОбъектыУчета - Массив Из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  * КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  * СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура ДобавитьПроизвольныеДокументыВПакетПослеДобавленияДокументовВПакет(РезультатДобавления,
	ПараметрыОповещения) Экспорт
	
	ДобавленУспешно = РезультатДобавления.Успех;
	
	ПослеИзмененияПакетаДокументов(ПараметрыОповещения, ДобавленУспешно, РезультатДобавления.КонтекстДиагностики);
	
КонецПроцедуры

// Параметры:
//  РезультатСоздания - Структура:
//                      * ИдентификаторПакета - Неопределено - если пакет не удалось создать.
//                                            - УникальныйИдентификатор
//                      * КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  ПараметрыОповещения - Структура:
//  * ДокументыПакета - Массив Из ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  * ИдентификаторПакета - УникальныйИдентификатор
//  * ОбъектыУчета - Массив Из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  * КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  * СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура ДобавитьПроизвольныеДокументыВПакетПослеСозданияПакетаДокументов(РезультатСоздания,
	ПараметрыОповещения) Экспорт
	
	ПакетСозданУспешно = ЗначениеЗаполнено(РезультатСоздания.ИдентификаторПакета);
	
	ПослеИзмененияПакетаДокументов(ПараметрыОповещения, ПакетСозданУспешно, РезультатСоздания.КонтекстДиагностики);
	
КонецПроцедуры

// Параметры:
//  ПараметрыИзменения - Структура:
//  * ДокументыПакета - Массив Из ДокументСсылка.ЭлектронныйДокументВходящийЭДО,ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  * ИдентификаторПакета - УникальныйИдентификатор
//  * ОбъектыУчета - Массив Из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  * СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//  ИзменениеВыполненоУспешно - Булево
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//
Процедура ПослеИзмененияПакетаДокументов(ПараметрыИзменения, ИзменениеВыполненоУспешно,
	КонтекстДиагностики)
	
	Если ИзменениеВыполненоУспешно Тогда
		Оповестить(ИмяСобытияДобавленияДокументаВПакет(), ПараметрыИзменения.ДокументыПакета,
			ПараметрыИзменения.ИдентификаторПакета);
		
		ПараметрыОповещения = НовыеПараметрыОповещенияПриИзмененииПакетаДокументовЭДОПоОбъектуУчета();
		ЗаполнитьЗначенияСвойств(ПараметрыОповещения, ПараметрыИзменения,
			"ДокументыПакета, ИдентификаторПакета, ОбъектыУчета");
		Оповестить(ИмяСобытияИзмененияПакетаДокументовЭДОПоОбъектуУчета(), ПараметрыОповещения);
		
		РаботаСФайламиБЭДКлиент.ПоказатьСообщенияРезультатаПроверкиИменФайловПользователю(
			ПараметрыИзменения.СообщенияДляПользователя);
		
	Иначе
		ИнтерфейсДокументовЭДОКлиент.ПоказатьПредставлениеОшибокКонтекстаДиагностики(КонтекстДиагностики);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ОграниченияОбъемаПакетаДокументов - см. СервисНастроекЭДО.НовыеОграниченияОбъемаПакетаДокументов
//  ОповещениеОЗавершении - ОписаниеОповещения - описание процедуры, которая будет вызвана после ответа на вопрос:
//  * Результат - КодВозвратаДиалога
//  * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
Процедура ПоказатьВопросОСозданииПакета(ОграниченияОбъемаПакетаДокументов, ОповещениеОЗавершении)
	
	ТекстВопроса = ТекстВопросаОСозданииПакета(ОграниченияОбъемаПакетаДокументов);
	
	ПоказатьВопрос(ОповещениеОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

// Параметры:
//  ОграниченияОбъемаПакетаДокументов - см. СервисНастроекЭДО.НовыеОграниченияОбъемаПакетаДокументов
//
// Возвращаемое значение:
//  Строка
Функция ТекстВопросаОСозданииПакета(ОграниченияОбъемаПакетаДокументов) Экспорт
	
	Возврат 
		СтрШаблон(НСтр("ru = 'Невозможно добавить документ из-за ограничений оператора %1 (не более %2 документов в пакете).
		|Сформировать еще один пакет документов?';
		|en = 'Cannot add the document due to the %1 service operator restrictions (no more than %2 documents in the package).
		|Do you want to generate another document package?'"), ОграниченияОбъемаПакетаДокументов.ОператорЭДО,
		ОграниченияОбъемаПакетаДокументов.МаксимальноеКоличествоЭДВПакете);
	
КонецФункции

// Параметры:
//  Результат - КодВозвратаДиалога
//  ПараметрыОповещенияПослеВопроса - Структура:
//										* ВыбранныйСпособДобавления - ЭлементСпискаЗначений
//																	- Строка
//										* ДополнительныеПараметры - см. НовыеПараметрыДобавленияВПакет
Процедура ПоказатьВопросОСозданииПакета_Завершение(Результат, ПараметрыОповещенияПослеВопроса) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = ПараметрыОповещенияПослеВопроса.ДополнительныеПараметры;
	ДополнительныеПараметры.СозданиеНовогоПакета = Истина;
	ДополнительныеПараметры.ОбъемПакетаДокументов = ПакетыДокументовЭДОКлиентСервер.НовыйОбъемПакетаДокументов();
	ДобавитьДокументВПакетПоСпособуДобавления(ПараметрыОповещенияПослеВопроса.ВыбранныйСпособДобавления, ДополнительныеПараметры);
	
КонецПроцедуры

// Параметры:
//  Результат - КодВозвратаДиалога
//  ПараметрыОповещения - Структура:
//  * ПараметрыДобавленияВПакет - См. НовыеПараметрыДобавленияВПакет
//  * ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  * СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура СформироватьЭДПоФайламСДискаДобавитьВПакетПослеОтветаНаВопросОПревышенииОграничения(Результат,
	ПараметрыОповещения) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения.ПараметрыДобавленияВПакет.СозданиеНовогоПакета = Истина;
	ПараметрыОповещения.ПараметрыДобавленияВПакет.ОбъемПакетаДокументов = 
		ПакетыДокументовЭДОКлиентСервер.НовыйОбъемПакетаДокументов();
	
	СформироватьИДобавитьПроизвольныеДокументыВПакетПолучитьРеквизиты(ПараметрыОповещения.ПараметрыДобавленияВПакет,
		ПараметрыОповещения.ОписанияФайлов, ПараметрыОповещения.СообщенияДляПользователя);
	
КонецПроцедуры

// Параметры:
//  СпособДобавления - Строка
//  ДополнительныеПараметры - см. НовыеПараметрыДобавленияВПакет
Процедура ДобавитьДокументВПакетПоСпособуДобавления(СпособДобавления, ДополнительныеПараметры)
	
	СпособыДобавленияВПакет = СпособыДобавленияДокументаВПакет();
	
	Если СпособДобавления = СпособыДобавленияВПакет.ДобавитьВПакетФайлСКомпьютера Тогда
		
		НачатьДобавлениеВПакетФайловСКомпьютера(ДополнительныеПараметры);
		
	ИначеЕсли СпособДобавления = СпособыДобавленияВПакет.ДобавитьВПакетПрисоединенныйФайл Тогда
		
		Параметры = ИнтеграцияБСПБЭДКлиент.НовыеПараметрыВыбораФайловИзОбщегоХранилища();
		Параметры.ЗакрыватьПриВыборе = Истина;
		Параметры.МножественныйВыбор = Ложь;
		
		Оповещение = Новый ОписаниеОповещения("ДобавитьДокументВПакетИзПрисоединенногоФайлаЗавершениеВыбора",
			ЭтотОбъект, ДополнительныеПараметры);
	
		ИнтеграцияБСПБЭДКлиент.ОткрытьФормуВыбораФайловИзОбщегоХранилища(Параметры, Оповещение);
		
	ИначеЕсли СпособДобавления = СпособыДобавленияВПакет.ДобавитьВПакетДокументИнформационнойБазы Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьВПакетДокументИнформационнойБазыЗавершениеВыбора",
			ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ИдентификаторПакета", ДополнительныеПараметры.ИдентификаторПакета);
		ПараметрыФормы.Вставить("ЭлектронныйДокумент", ДополнительныеПараметры.ЭлектронныйДокумент);
		ПараметрыФормы.Вставить("Организация", ДополнительныеПараметры.Отправитель);
		ПараметрыФормы.Вставить("Контрагент", ДополнительныеПараметры.Получатель);
		ПараметрыФормы.Вставить("Договор", ДополнительныеПараметры.Договор);
		ПараметрыФормы.Вставить("ОтключитьТранслитерацию", ДополнительныеПараметры.ОтключитьТранслитерацию);
		ПараметрыФормы.Вставить("ОграниченияОбъемаПакетаДокументов", ДополнительныеПараметры.ОграниченияОбъемаПакетаДокументов);
		ПараметрыФормы.Вставить("ОбъемПакетаДокументов", ДополнительныеПараметры.ОбъемПакетаДокументов);
		ПараметрыФормы.Вставить("СозданиеНовогоПакета", ДополнительныеПараметры.СозданиеНовогоПакета);
		
		ОткрытьФорму("РегистрСведений.ПакетыДокументовЭДО.Форма.ДобавлениеДокументовВПакет", ПараметрыФормы,,,,,
			ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  РезультатДлительнойОперации - См. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ПараметрыОповещения - Структура:
//  * КонтекстДиагностики -См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  * ОповещениеОЗавершении - См. СоздатьПакетДокументов.ОповещениеОЗавершении
Процедура СоздатьПакетДокументовПослеОжидания(РезультатДлительнойОперации, ПараметрыОповещения) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторПакета", Неопределено);
	Результат.Вставить("КонтекстДиагностики", ПараметрыОповещения.КонтекстДиагностики);
	
	РезультатСозданияПакета = ОбщегоНазначенияБЭДКлиент.РезультатВыполненияФоновогоЗадания(
		НСтр("ru = 'Создание пакета документов ЭДО';
			|en = 'Create an EDI document batch'"),
		РезультатДлительнойОперации, Результат.КонтекстДиагностики); // См. ПакетыДокументовЭДО.НовыйРезультатДействийСПакетом
		
	Если РезультатСозданияПакета.Успех Тогда
		Результат.ИдентификаторПакета = РезультатСозданияПакета.ИдентификаторПакета;
	КонецЕсли;
		
	ОбработкаНеисправностейБЭДКлиент.ДополнитьКонтекстДиагностики(Результат.КонтекстДиагностики,
		РезультатСозданияПакета.КонтекстДиагностики);
		
	ВыполнитьОбработкуОповещения(ПараметрыОповещения.ОповещениеОЗавершении, Результат);
	
КонецПроцедуры

// Параметры:
//  РезультатДлительнойОперации - См. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ПараметрыОповещения - Структура:
//  * КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  * ОповещениеОЗавершении - См. ДобавитьДокументыВПакет.ОповещениеОЗавершении
Процедура ДобавитьДокументВПакетПослеОжидания(РезультатДлительнойОперации, ПараметрыОповещения) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("КонтекстДиагностики", ПараметрыОповещения.КонтекстДиагностики);
	
	РезультатДобавления = ОбщегоНазначенияБЭДКлиент.РезультатВыполненияФоновогоЗадания(
		НСтр("ru = 'Добавление документа ЭДО в пакет';
			|en = 'Add an EDI document to the batch'"),
		РезультатДлительнойОперации, Результат.КонтекстДиагностики); // См. ПакетыДокументовЭДО.НовыйРезультатДействийСПакетом
		
	Результат.Успех = РезультатДобавления.Успех;
	
	ОбработкаНеисправностейБЭДКлиент.ДополнитьКонтекстДиагностики(Результат.КонтекстДиагностики,
		РезультатДобавления.КонтекстДиагностики);
		
	ВыполнитьОбработкуОповещения(ПараметрыОповещения.ОповещениеОЗавершении, Результат);
	
КонецПроцедуры

// Параметры:
//  РезультатДлительнойОперации - См. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ПараметрыОповещения - Структура:
//  * ИдентификаторПакета - УникальныйИдентификатор
//  * УдаленныйДокумент - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  * КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
Процедура УдалитьДокументИзПакетаПослеОжидания(РезультатДлительнойОперации, ПараметрыОповещения) Экспорт
	
	КонтекстДиагностики = ПараметрыОповещения.КонтекстДиагностики;
	
	РезультатУдаления = ОбщегоНазначенияБЭДКлиент.РезультатВыполненияФоновогоЗадания(
		НСтр("ru = 'Удаление документа ЭДО из пакета';
			|en = 'Delete an EDI document from the batch'"),
		РезультатДлительнойОперации, КонтекстДиагностики); // См. ПакетыДокументовЭДО.НовыйРезультатДействийСПакетом
		
	ОбработкаНеисправностейБЭДКлиент.ДополнитьКонтекстДиагностики(КонтекстДиагностики,
		РезультатУдаления.КонтекстДиагностики);
		
	Если РезультатУдаления.Успех = Истина Тогда
		Оповестить(ИмяСобытияУдалениеДокументаИзПакета(), ПараметрыОповещения.УдаленныйДокумент,
			ПараметрыОповещения.ИдентификаторПакета);
	Иначе
		ИнтерфейсДокументовЭДОКлиент.ПоказатьПредставлениеОшибокКонтекстаДиагностики(
			КонтекстДиагностики);
	КонецЕсли;
	
КонецПроцедуры

// Oпределяет необходимость вывода окна с заполнением реквизитов произвольного документа при добавлении в пакет
// файла с компьютера.
// 
// Параметры:
//  ЗаполнятьЗначениямиПоУмолчанию - Булево
Процедура ПриОпределенииСпособаВводаРеквизитовПриДобавленииВПакетФайлаСКомпьютера(ЗаполнятьЗначениямиПоУмолчанию)
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
//  * ВидДокумента - СправочникСсылка.ВидыДокументовЭДО 
//  * Номер - Строка
//  * Дата - Дата
//  * Сумма - Число
//
Функция РеквизитыНовогоПроизвольногоДокументаПоУмолчанию()
	
	РеквизитыДокумента = Новый Структура;
	РеквизитыДокумента.Вставить("ВидДокумента", ЭлектронныеДокументыЭДОВызовСервера.ВидДокументаПрочее());
	РеквизитыДокумента.Вставить("Номер", "");
	РеквизитыДокумента.Вставить("Дата", Дата(1, 1, 1));
	РеквизитыДокумента.Вставить("Сумма", 0);
	Возврат РеквизитыДокумента;
	
КонецФункции

#Область ДобавлениеВПакетИзПрисоединенногоФайла

//@skip-check invocation-parameter-type-intersect
//
// Параметры:
//  ВыбранноеЗначение - ОпределяемыйТип.ПрисоединенныйФайл
//  ПараметрыДобавленияВПакет - См. НовыеПараметрыДобавленияВПакет
//
Процедура ДобавитьДокументВПакетИзПрисоединенногоФайлаЗавершениеВыбора(ВыбранноеЗначение,
	ПараметрыДобавленияВПакет) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ДанныеФайла = РаботаСФайламиКлиент.ДанныеФайла(ВыбранноеЗначение, Новый УникальныйИдентификатор, Истина);
	
	РезультатПроверкиПревышения = ПакетыДокументовЭДОКлиентСервер.ПроверкаПревышенияМаксимальногоРазмераПакета(
		ПараметрыДобавленияВПакет.ОграниченияОбъемаПакетаДокументов,
		ПараметрыДобавленияВПакет.ОбъемПакетаДокументов,
		ДанныеФайла.Размер);
	
	Если РезультатПроверкиПревышения.Превышено Тогда
		
		ПредупредитьОПревышенииМаксимальногоРазмерПакета(РезультатПроверкиПревышения.ПревышаемыеОграничения);
		Возврат;
		
	КонецЕсли;
	
	ПодготовитьФайлДляДобавленияВПакет(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла, ДанныеФайла.ИмяФайла,
		ПараметрыДобавленияВПакет);
	
КонецПроцедуры

// Параметры:
//  АдресФайлаВХранилище - Строка
//  ИмяФайла - Строка
//  ПараметрыДобавленияВПакет - См. НовыеПараметрыДобавленияВПакет
//  
Процедура ПодготовитьФайлДляДобавленияВПакет(АдресФайлаВХранилище, ИмяФайла, ПараметрыДобавленияВПакет)
	
	ТранслитерацияОтключена = Не ИнтерфейсДокументовЭДОВызовСервера.ВключенаТранслитерацияИменФайлов();
	
	РезультатыПроверкиИмени = 
		РаботаСФайламиБЭДКлиент.ОписаниеФайлаПослеПроверкиКорректностиИмениПоИмениИАдресуВоВременномХранилище(
			ИмяФайла, АдресФайлаВХранилище, ТранслитерацияОтключена);
	
	Если Не ЗначениеЗаполнено(РезультатыПроверкиИмени.ОписаниеФайла) Тогда
		Для Каждого СообщениеДляПользователя Из РезультатыПроверкиИмени.СообщенияДляПользователя Цикл
			ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеДляПользователя);
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	ОписанияФайлов = Новый Массив; // Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
	//@skip-check invocation-parameter-type-intersect
	Если ЗначениеЗаполнено(РезультатыПроверкиИмени.ОписаниеФайла) Тогда
		ОписанияФайлов.Добавить(РезультатыПроверкиИмени.ОписаниеФайла);
	КонецЕсли;
	
	СообщенияДляПользователя = Новый Массив; // Массив Из ФорматированнаяСтрока
	Если ЗначениеЗаполнено(РезультатыПроверкиИмени.СообщенияДляПользователя) Тогда
		СообщенияДляПользователя = РезультатыПроверкиИмени.СообщенияДляПользователя;
	КонецЕсли;

	СформироватьИДобавитьПроизвольныеДокументыВПакетПолучитьРеквизиты(ПараметрыДобавленияВПакет, ОписанияФайлов,
		СообщенияДляПользователя);
	
КонецПроцедуры

#КонецОбласти // ДобавлениеВПакетИзПрисоединенногоФайла

#Область ДобавлениеВПакетФайловСКомпьютера

// Параметры:
//  ПараметрыДобавленияВПакет - См. НовыеПараметрыДобавленияВПакет
Процедура НачатьДобавлениеВПакетФайловСКомпьютера(ПараметрыДобавленияВПакет)

	ОбработчикПослеВыбораФайлов = Новый ОписаниеОповещения("ДобавитьДокументВПакетИзФайлаНаДискеЗавершениеВыбораФайла",
		ПакетыДокументовЭДОКлиент, ПараметрыДобавленияВПакет);
		
	ОбработчикПередНачаломДобавленияФайлаВПакет = Новый ОписаниеОповещения("ПередНачаломДобавленияФайловВПакет",
		ПакетыДокументовЭДОКлиент, ПараметрыДобавленияВПакет);
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.Диалог.Заголовок = НСтр("ru = 'Выберите файлы для добавления в пакет';
												|en = 'Select the files to add to the package'");
	ПараметрыЗагрузки.Диалог.МножественныйВыбор = Истина;
	ПараметрыЗагрузки.ДействиеПередНачаломПомещенияФайлов = ОбработчикПередНачаломДобавленияФайлаВПакет;
	ФайловаяСистемаКлиент.ЗагрузитьФайлы(ОбработчикПослеВыбораФайлов, ПараметрыЗагрузки);
	
КонецПроцедуры

// Параметры:
//  ПомещенныеФайлы - Неопределено
//                  - Массив из Структура:
//                    * Хранение  - Строка - адрес временного хранилища с двоичными данными файла.
//                    * Имя       - Строка
//                    * ПолноеИмя - Строка
//                    * ИмяФайла  - Строка - имя файла с расширением.
//  ПараметрыДобавленияВПакет - См. НовыеПараметрыДобавленияВПакет
Процедура ДобавитьДокументВПакетИзФайлаНаДискеЗавершениеВыбораФайла(ПомещенныеФайлы, ПараметрыДобавленияВПакет) Экспорт
	
	Если Не ЗначениеЗаполнено(ПомещенныеФайлы) Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьФайлыДляДобавленияВПакет(ПомещенныеФайлы, ПараметрыДобавленияВПакет);
	
КонецПроцедуры

//@skip-check invocation-parameter-type-intersect
//
// Параметры:
//  ПомещаемыеФайлы - Массив Из СсылкаНаФайл
//                  - Массив Из ДвоичныеДанные
//  ОтказОтПомещенияВсехФайлов - Булево
//  ПараметрыДобавленияВПакет - См. НовыеПараметрыДобавленияВПакет
Процедура ПередНачаломДобавленияФайловВПакет(ПомещаемыеФайлы, ОтказОтПомещенияВсехФайлов,
	ПараметрыДобавленияВПакет) Экспорт
	
	РазмерПомещаемыхФайлов = 0;
	КоличествоПомещаемыхФайлов = ПомещаемыеФайлы.Количество();
	
	Для Каждого ПомещаемыйФайл Из ПомещаемыеФайлы Цикл
		РазмерПомещаемыхФайлов = РазмерПомещаемыхФайлов + ПомещаемыйФайл.Размер();
	КонецЦикла;
	
	РезультатПроверкиПревышенияРазмера = ПакетыДокументовЭДОКлиентСервер.ПроверкаПревышенияМаксимальногоРазмераПакета(
		ПараметрыДобавленияВПакет.ОграниченияОбъемаПакетаДокументов, ПараметрыДобавленияВПакет.ОбъемПакетаДокументов,
		РазмерПомещаемыхФайлов);
			
	РезультатПроверкиПревышенияКоличества = ПакетыДокументовЭДОКлиентСервер.ПроверкаПревышенияМаксимальногоКоличестваДокументовВПакете(
		ПараметрыДобавленияВПакет.ОграниченияОбъемаПакетаДокументов, ПараметрыДобавленияВПакет.ОбъемПакетаДокументов,
		КоличествоПомещаемыхФайлов);
			
	Если РезультатПроверкиПревышенияРазмера.Превышено Тогда
		
		ПредупредитьОПревышенииМаксимальногоРазмерПакета(РезультатПроверкиПревышенияРазмера.ПревышаемыеОграничения);
		ОтказОтПомещенияВсехФайлов = Истина;
		
	КонецЕсли;

	Если РезультатПроверкиПревышенияКоличества.Превышено Тогда
		
		ПредупредитьОПревышенииКоличестваДокументовПакета(РезультатПроверкиПревышенияКоличества.ПревышаемыеОграничения);
		ОтказОтПомещенияВсехФайлов = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ПомещенныеФайлы - См. ДобавитьДокументВПакетИзФайлаНаДискеЗавершениеВыбораФайла.ПомещенныеФайлы
//  ПараметрыДобавленияВПакет - См. НовыеПараметрыДобавленияВПакет
// 
Процедура ПодготовитьФайлыДляДобавленияВПакет(ПомещенныеФайлы, ПараметрыДобавленияВПакет)
	
	ТранслитерацияОтключена = Не ИнтерфейсДокументовЭДОВызовСервера.ВключенаТранслитерацияИменФайлов();

	РезультатыПроверкиИмен = РаботаСФайламиБЭДКлиент.ОписанияФайловПослеПроверкиКорректностиИмен(ПомещенныеФайлы,
		ТранслитерацияОтключена);
	
	СформироватьИДобавитьПроизвольныеДокументыВПакет(РезультатыПроверкиИмен.ОписанияФайлов, ПараметрыДобавленияВПакет,
		РезультатыПроверкиИмен.СообщенияДляПользователя);
	
КонецПроцедуры

// Параметры:
//  ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  ПараметрыДобавленияВПакет - См. НовыеПараметрыДобавленияВПакет
//  СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура СформироватьИДобавитьПроизвольныеДокументыВПакет(ОписанияФайлов, ПараметрыДобавленияВПакет,
	СообщенияДляПользователя)
	
	Если Не ЗначениеЗаполнено(ОписанияФайлов) Тогда
		РаботаСФайламиБЭДКлиент.ПоказатьСообщенияРезультатаПроверкиИменФайловПользователю(СообщенияДляПользователя);
		Возврат;
	КонецЕсли;
	
	ВидДокументаПрочее = ЭлектронныеДокументыЭДОВызовСервера.ВидДокументаПрочее();
	
	ПараметрыФормирования = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыСозданияДокументаПоФайлу();
	ПараметрыФормирования.Организация = ПараметрыДобавленияВПакет.Отправитель;
	ПараметрыФормирования.Контрагент = ПараметрыДобавленияВПакет.Получатель;
	ПараметрыФормирования.Договор = ПараметрыДобавленияВПакет.Договор;
	ПараметрыФормирования.ВидДокумента = ВидДокументаПрочее;
	ПараметрыФормирования.ОбъектыУчета = ПараметрыДобавленияВПакет.ОбъектыУчета;
	
	ПараметрыФормированияПоФайлам = Новый Соответствие;
	
	Для Каждого ОписаниеФайла Из ОписанияФайлов Цикл
		ПараметрыФормированияПоФайлам.Вставить(ОписаниеФайла.ИмяФайла,
			ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ПараметрыФормирования));
	КонецЦикла;

	Контекст = Новый Структура;
	Контекст.Вставить("ПараметрыДобавления", ПараметрыДобавленияВПакет);
	Контекст.Вставить("ПараметрыФормированияПоФайлам", ПараметрыФормированияПоФайлам);
	Контекст.Вставить("ОписанияФайлов", ОписанияФайлов);
	Контекст.Вставить("СообщенияДляПользователя", СообщенияДляПользователя);

	ОповещениеПослеФормированияДокумента = Новый ОписаниеОповещения(
		"ДобавитьПроизвольныеДокументыВПакетПослеФормированияДокументовПоФайлам", ЭтотОбъект, Контекст);
	
	СформироватьЭлектронныеДокументыПоФайлам(Контекст, ОповещениеПослеФормированияДокумента);
	
КонецПроцедуры

// Параметры:
//  РезультатФормирования - Структура:
//  * Итог - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыйИтогВыполненияДействийПоЭДО
//  * ОшибкиФормирования - Массив из См. ЭлектронныеДокументыЭДО.НовоеОписаниеОшибкиФормирования
//  * КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  Контекст - Структура:
//  * ПараметрыДобавления - См. НовыеПараметрыДобавленияВПакет
//  * ПараметрыФормированияПоФайлам - Соответствие Из КлючИЗначение:
//                                    * Ключ - Строка - Имя файла.
//                                    * Значение - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыСозданияДокументаПоФайлу
//  * ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  * СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура ДобавитьПроизвольныеДокументыВПакетПослеФормированияДокументовПоФайлам(РезультатФормирования,
	Контекст) Экспорт
	
	ДокументыПакета = Новый Массив; // Массив Из ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
	
	Если РезультатФормирования.ОшибкиФормирования.Количество() Тогда
		Если Не Контекст.ПараметрыДобавления.СозданиеНовогоПакета Тогда
			ДокументыПакета.Добавить(Контекст.ПараметрыДобавления.ЭлектронныйДокумент);
		КонецЕсли;
		Оповещение = Новый ОписаниеОповещения("ДобавитьВПакетПослеОбработкиОшибокФормирования",
			ПакетыДокументовЭДОКлиент, Контекст);
		ДополнительныеПараметры = Новый Структура("ОбъектыУчета", ДокументыПакета);
		ИнтерфейсДокументовЭДОКлиент.НачатьОбработкуОшибокФормированияДокумента(Оповещение,
			РезультатФормирования.ОшибкиФормирования, ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ОбработанныеДокументы = РезультатФормирования.Итог.ОбработанныеДокументы;
	
	Если Не ОбработанныеДокументы.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого РезультатОбработкиДокумента Из ОбработанныеДокументы Цикл
		ЭлектронныйДокумент = РезультатОбработкиДокумента.Ключ; // ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
		ДокументыПакета.Добавить(ЭлектронныйДокумент);
	КонецЦикла;
	
	Если Контекст.ПараметрыДобавления.СозданиеНовогоПакета Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", ЭлектронныйДокумент);
		ИнтерфейсДокументовЭДОКлиент.ОткрытьФормуЭлектронногоДокумента(ПараметрыФормы);
		
		РаботаСФайламиБЭДКлиент.ПоказатьСообщенияРезультатаПроверкиИменФайловПользователю(
			Контекст.СообщенияДляПользователя);
		
		Возврат;
		
	КонецЕсли;
	
	ИдентификаторПакета = Контекст.ПараметрыДобавления.ИдентификаторПакета;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ДокументыПакета", ДокументыПакета);
	ПараметрыОповещения.Вставить("ИдентификаторПакета", ИдентификаторПакета);
	ПараметрыОповещения.Вставить("ОбъектыУчета", Контекст.ПараметрыДобавления.ОбъектыУчета);
	ПараметрыОповещения.Вставить("КонтекстДиагностики", РезультатФормирования.КонтекстДиагностики);
	ПараметрыОповещения.Вставить("СообщенияДляПользователя", Контекст.СообщенияДляПользователя);
	
	Если ЗначениеЗаполнено(ИдентификаторПакета) Тогда
		ОбработчикПослеДобавленияДокументовВПакет = Новый ОписаниеОповещения(
			"ДобавитьПроизвольныеДокументыВПакетПослеДобавленияДокументовВПакет", ПакетыДокументовЭДОКлиент,
			ПараметрыОповещения);
		ДобавитьДокументыВПакет(ИдентификаторПакета, ДокументыПакета, ОбработчикПослеДобавленияДокументовВПакет,
			РезультатФормирования.КонтекстДиагностики);
	Иначе
		ДокументыПакета.Добавить(Контекст.ПараметрыДобавления.ЭлектронныйДокумент);
		ОбработчикПослеСозданияПакетаДокументов = Новый ОписаниеОповещения(
			"ДобавитьПроизвольныеДокументыВПакетПослеСозданияПакетаДокументов", ПакетыДокументовЭДОКлиент,
			ПараметрыОповещения);
		СоздатьПакетДокументов(ДокументыПакета, ОбработчикПослеСозданияПакетаДокументов,
			РезультатФормирования.КонтекстДиагностики);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ДобавлениеВПакетФайловСКомпьютера

#КонецОбласти
