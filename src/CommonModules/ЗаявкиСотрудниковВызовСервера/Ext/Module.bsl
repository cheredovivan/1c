
#Область СлужебныйПрограммныйИнтерфейс

Функция СодержаниеТиповогоОтвета(Ссылка) Экспорт
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ТекстОтвета");
	
КонецФункции

Функция ТребуетсяПодписьПриВыполненииЗаявки(Заявка) Экспорт
	
	Возврат ЗаявкиСотрудников.ТребуетсяПодписьПриВыполненииЗаявки(Заявка);

КонецФункции

Процедура УдалитьФайлСправки(ФайлСправки) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ЭлектронныйДокумент", ФайлСправки);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументЭДОБЗК.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
	|ГДЕ
	|	ДокументЭДОБЗК.ЭлектронныйДокумент = &ЭлектронныйДокумент
	|	И НЕ ДокументЭДОБЗК.ПометкаУдаления";
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДокументыЭДОБЗК = Запрос.Выполнить().Выгрузить();
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.ДокументЭДОБЗК");
		ЭлементБлокировки.ИсточникДанных = ДокументыЭДОБЗК;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки = Блокировка.Добавить(ОбщегоНазначения.ИмяТаблицыПоСсылке(ФайлСправки));
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ФайлСправки);
		Блокировка.Заблокировать();
		
		ФайлСправкиОбъект = ФайлСправки.ПолучитьОбъект();
		ФайлСправкиОбъект.ДополнительныеСвойства.Вставить("УдалениеПечатныхФорм");
		ФайлСправкиОбъект.УстановитьПометкуУдаления(Истина);
		
		Для каждого СтрокаТЗ Из ДокументыЭДОБЗК Цикл
			ДокументОбъект = СтрокаТЗ.Ссылка.ПолучитьОбъект();
			ДокументОбъект.ПометкаУдаления = Истина;
			ДокументОбъект.ДополнительныеСвойства.Вставить("РазрешенаПометкаУдаления", Истина);
			ДокументОбъект.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru = 'Ошибка удаления файла справки.';
								|en = 'An error occurred while deleting the certificate file.'");
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Комментарий = СтрШаблон("%1%2%3", ТекстСообщения, Символы.ПС, ПодробноеПредставлениеОшибки);
		ЗаписьЖурналаРегистрации(ИнтеграцияКабинетСотрудника.ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

#КонецОбласти