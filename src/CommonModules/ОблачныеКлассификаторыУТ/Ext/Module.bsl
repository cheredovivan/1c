
#Область ПрограммныйИнтерфейс

#Область КлассификаторТНВЭД

// См. ОблачныеКлассификаторыПереопределяемый.ОпределитьЗагруженныеЭлементыТНВЭД.
Процедура ОпределитьЗагруженныеЭлементыТНВЭД(Элементы) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КлассификаторТНВЭД.Код КАК Код
	|ИЗ
	|	Справочник.КлассификаторТНВЭД КАК КлассификаторТНВЭД";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Элементы.Добавить(Выборка.Код);
	КонецЦикла;
	
КонецПроцедуры

// См. ОблачныеКлассификаторыПереопределяемый.СоздатьОбновитьЭлементыТНВЭД.
Процедура СоздатьОбновитьЭлементыТНВЭД(ДанныеСервиса, Отказ) Экспорт
	
	Справочники.КлассификаторТНВЭД.СоздатьОбновитьЭлементыТНВЭДИзОблачногоКлассификатора(ДанныеСервиса, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область КлассификаторОКПД2

// См. ОблачныеКлассификаторыПереопределяемый.ОпределитьЗагруженныеЭлементыОКПД2.
Процедура ОпределитьЗагруженныеЭлементыОКПД2(ЭлементыКлассификатора) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КлассификаторОКПД2.Код КАК Код
	|ИЗ
	|	Справочник.КлассификаторОКПД2 КАК КлассификаторОКПД2";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЭлементыКлассификатора.Добавить(Выборка.Код);
	КонецЦикла;
	
КонецПроцедуры	

// См. ОблачныеКлассификаторыПереопределяемый.СоздатьОбновитьЭлементыОКПД2.
Процедура СоздатьОбновитьЭлементыОКПД2(ДанныеСервиса, Отказ) Экспорт
	
	Справочники.КлассификаторОКПД2.СоздатьОбновитьЭлементыОКПД2ИзОблачногоКлассификатора(ДанныеСервиса, Отказ);
	
КонецПроцедуры	

#КонецОбласти

#Область КлассификаторОКЕИ

// См. ОблачныеКлассификаторыПереопределяемый.ОпределитьЗагруженныеЭлементыОКЕИ.
Функция ОпределитьЗагруженныеЭлементыОКЕИ(ДополнительныеПараметры = Неопределено) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕдиницыИзмерения.Код КАК Код,
		|	ЕдиницыИзмерения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницыИзмерения
		|ГДЕ
		|	ЕдиницыИзмерения.Код <> """"";
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		Отбор = Неопределено;
		ДополнительныеПараметры.Свойство("Отбор", Отбор);
		
		Если ЗначениеЗаполнено(Отбор) Тогда
			Запрос.Текст = Запрос.Текст + 
				"	И ЕдиницыИзмерения.ТипИзмеряемойВеличины В (&Отбор)";
			Запрос.УстановитьПараметр("Отбор", Отбор);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока ВыборкаДанных.Следующий() Цикл
		Результат.Вставить(СокрЛП(ВыборкаДанных.Код), ВыборкаДанных.Ссылка);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// См. ОблачныеКлассификаторыПереопределяемый.ПроверитьЭлементОКЕИ.
Функция ПроверитьЭлементОКЕИ(Строка, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		Отбор = Неопределено;
		ДополнительныеПараметры.Свойство("Отбор", Отбор);
		
		Если ЗначениеЗаполнено(Отбор) Тогда
			ТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.ПустаяСсылка();
			Если ЗначениеЗаполнено(Строка.ТипИзмеряемойВеличины) Тогда 
				ТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин[СтрЗаменить(Строка.ТипИзмеряемойВеличины, " ", "")];
			КонецЕсли;
			
			Если ТипЗнч(Отбор) = Тип("Массив") Тогда
				Возврат Отбор.Найти(ТипИзмеряемойВеличины) <> Неопределено;
			Иначе
				Возврат Отбор = ТипИзмеряемойВеличины;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// См. ОблачныеКлассификаторыПереопределяемый.СоздатьОбновитьЭлементОКЕИ.
Функция СоздатьОбновитьЭлементОКЕИ(ДанныеСервиса, НоваяСсылка = Ложь) Экспорт
	
	Если ЗначениеЗаполнено(ДанныеСервиса.Ссылка) Тогда
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(Метаданные.Справочники.УпаковкиЕдиницыИзмерения.ПолноеИмя());
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ДанныеСервиса.Ссылка);
		Блокировка.Заблокировать();
		
		СправочникОбъект = ДанныеСервиса.Ссылка.ПолучитьОбъект();
		Если Не СправочникОбъект.Владелец = Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения Тогда
			Возврат Неопределено;
		КонецЕсли;
		
	Иначе
		НоваяСсылка = Истина;
		
		СправочникОбъект = Справочники.УпаковкиЕдиницыИзмерения.СоздатьЭлемент();
		СправочникОбъект.Владелец = Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеСервиса.УсловноеОбозначениеНациональное) Тогда
		Наименование = ДанныеСервиса.УсловноеОбозначениеНациональное;
	ИначеЕсли ЗначениеЗаполнено(ДанныеСервиса.УсловноеОбозначениеМеждународное) Тогда
		Наименование = ДанныеСервиса.УсловноеОбозначениеМеждународное;
	ИначеЕсли ЗначениеЗаполнено(ДанныеСервиса.КодовоеБуквенноеОбозначениеНациональное) Тогда
		Наименование = ДанныеСервиса.КодовоеБуквенноеОбозначениеНациональное;
	ИначеЕсли ЗначениеЗаполнено(ДанныеСервиса.КодовоеБуквенноеОбозначениеМеждународное) Тогда
		Наименование = ДанныеСервиса.КодовоеБуквенноеОбозначениеМеждународное;
	Иначе
		Наименование = ДанныеСервиса.Наименование;
	КонецЕсли;
	
	СправочникОбъект.Код                     = СокрЛП(ДанныеСервиса.Код);
	СправочникОбъект.Наименование            = СтрЗаменить(Наименование,Символы.ПС, "/");
	СправочникОбъект.МеждународноеСокращение = СтрЗаменить(ДанныеСервиса.КодовоеБуквенноеОбозначениеМеждународное, Символы.ПС, "/");
	СправочникОбъект.НаименованиеПолное      = СтрЗаменить(ДанныеСервиса.Наименование,Символы.ПС, "/");
	СправочникОбъект.Числитель               = ДанныеСервиса.Числитель;
	СправочникОбъект.Знаменатель             = ДанныеСервиса.Знаменатель;
	
	Если ЗначениеЗаполнено(ДанныеСервиса.ТипИзмеряемойВеличины) Тогда
		СправочникОбъект.ТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин[СтрЗаменить(ДанныеСервиса.ТипИзмеряемойВеличины, " ", "")];
	КонецЕсли;
	
	СправочникОбъект.Записать();
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

#КонецОбласти

#КонецОбласти
