#Область ПрограммныйИнтерфейс

// Открытие формы создаваемого документа.
// 
// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовРаспознаваниеДокументов
//  ПараметрыЗаполнения - Структура:
//  *Контрагент - СправочникСсылка.Контрагенты
//  *Организация - СправочникСсылка.Организации
//  *ИмяРеквизитаНомерДокумента - Строка
//  *ИмяРеквизитаДатаДокумента  - Строка 
//  Владелец - Форма
//
Процедура ОткрытиеФормыСоздаваемогоДокумента(ТипДокумента, ПараметрыЗаполнения, Владелец) Экспорт
	
	РаспознаваниеДокументовУПКлиент.ОткрытиеФормыСоздаваемогоДокумента(ТипДокумента, ПараметрыЗаполнения, Владелец);

КонецПроцедуры

// Открытие формы редактирования строки.
// 
// Параметры:
//  ЭтотОбъект - Форма 
//  ДанныеСтроки - СтрокаТабличнойЧасти
//
//@skip-check module-empty-method
Процедура ОткрытиеФормыРедактированияСтроки(ЭтотОбъект, ДанныеСтроки) Экспорт

КонецПроцедуры

// Процедура определяет имя формы создания элемента и параметры ее открытия.
//
// Параметры:
//  ИмяЭлемента - Строка - имя элемента, для которого определяется форма создания.
//  ИмяФормы - Строка - переменная, в которую будет помещено имя формы создания элемента.
//  ПараметрыОткрытия - Структура - параметры открытия формы создания элемента.
//
Процедура ИмяФормыСозданияЭлемента(ИмяЭлемента, ИмяФормы, ПараметрыОткрытия) Экспорт
	
	Направление = ПараметрыОткрытия.Направление;
	ТипДокумента = ПараметрыОткрытия.ТипДокумента;
	НаборДанных = ПараметрыОткрытия.НаборДанных;
	ДополнительныеДанные = ПараметрыОткрытия.ДополнительныеДанные;
	
	ПараметрыОбъекта = Неопределено;
	ДополнительныеДанные.Свойство("ПараметрыОбъекта", ПараметрыОбъекта);
	
	Если ИмяЭлемента = "Партнер" Тогда
		
		ПараметрыОткрытия.Вставить("Основание", Новый Структура());

		Если (Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий")
		И Не ЗначениеЗаполнено(НаборДанных.Получить("Покупатель").Значение)) Или 
		(Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Входящий")
		И Не ЗначениеЗаполнено(НаборДанных.Получить("Продавец").Значение))Тогда
			Если ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.УПД")
				Или ТипДокумента = ПредопределенноеЗначение(
					"Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетФактура") Тогда
					ИННКонтрагента = ДополнительныеДанные.ИННКонтрагента;
					КППКонтрагента = ДополнительныеДанные.КППКонтрагента;
				ИначеЕсли ТипДокумента = ПредопределенноеЗначение(
						"Перечисление.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг") Тогда
				Если Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
					ИННКонтрагента = НаборДанных.Получить("ИННПокупателя").Значение;
				Иначе
					ИННКонтрагента = НаборДанных.Получить("ИННПродавца").Значение;
				КонецЕсли;
			Иначе
				Если Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
					ИННКонтрагента = НаборДанных.Получить("ИННПокупателя").Значение;
					КППКонтрагента = НаборДанных.Получить("КПППокупателя").Значение;
				Иначе
					ИННКонтрагента = НаборДанных.Получить("ИННПродавца").Значение;
					КППКонтрагента = НаборДанных.Получить("КПППродавца").Значение;
				КонецЕсли;
			КонецЕсли;
			ПараметрыОткрытия.Основание.Вставить("ИНН",ИННКонтрагента);
			ПараметрыОткрытия.Основание.Вставить("КПП",КППКонтрагента);  
			ПараметрыОткрытия.Основание.Вставить("ЮридическийАдрес");
		КонецЕсли;
		
		Если Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
			ТекстЗаполнения = НаборДанных.Получить("Покупатель").РаспознанныйТекст;
			Вид = "Клиент";
		Иначе
			ТекстЗаполнения = НаборДанных.Получить("Продавец").РаспознанныйТекст;
			Вид = "Поставщик";
		КонецЕсли;

		ПараметрыОткрытия.Вставить("ПараметрыВыбора", Новый Структура());
		ПараметрыОткрытия.ПараметрыВыбора.Вставить(Вид, Истина);
		ПараметрыОткрытия.Основание.Вставить("СокращенноеНаименование", ТекстЗаполнения);
		ПараметрыОткрытия.Основание.Вставить("Наименование", ТекстЗаполнения);
		ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
		ИмяФормы = "Справочник.Партнеры.Форма.ПомощникНового";
		
	ИначеЕсли ИмяЭлемента = "Покупатель" Или ИмяЭлемента = "Продавец" Тогда
		
		Если ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.УПД") 
			Или ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетФактура") Тогда 
				ИННКонтрагента = ДополнительныеДанные.ИННКонтрагента;
				КППКонтрагента = ДополнительныеДанные.КППКонтрагента;
		ИначеЕсли ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг") Тогда 
			Если Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
				ИННКонтрагента = НаборДанных.Получить("ИННПокупателя").Значение;
			Иначе
				ИННКонтрагента = НаборДанных.Получить("ИННПродавца").Значение;
			КонецЕсли;
		Иначе
			Если Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
				ИННКонтрагента = НаборДанных.Получить("ИННПокупателя").Значение;
				КППКонтрагента = НаборДанных.Получить("КПППокупателя").Значение;
			Иначе
				ИННКонтрагента = НаборДанных.Получить("ИННПродавца").Значение;
				КППКонтрагента = НаборДанных.Получить("КПППродавца").Значение;
			КонецЕсли;
		КонецЕсли;

		Если Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
			ТекстЗаполнения = НаборДанных.Получить("Покупатель").РаспознанныйТекст;
			Вид = "Клиент";
		Иначе
			ТекстЗаполнения = НаборДанных.Получить("Продавец").РаспознанныйТекст;
			Вид = "Поставщик";
		КонецЕсли;
		
		ЗначенияЗаполнения = Новый Структура;
		Партнер = Неопределено;
		
		Если ЗначениеЗаполнено(ПараметрыОбъекта) Тогда
			
			ВидКонтрагента = Неопределено;
			ПараметрыОбъекта.Свойство("ЮридическоеФизическоеЛицо", ВидКонтрагента);
			
			Если ВидКонтрагента = "ФизическоеЛицо" Тогда
				ЗначенияЗаполнения.Вставить("ЮрФизЛицо", ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо"));
				ЗначенияЗаполнения.Вставить("ЮридическоеФизическоеЛицо", 
					ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо"));
			Иначе
				ЗначенияЗаполнения.Вставить("ЮрФизЛицо", ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо"));
				ЗначенияЗаполнения.Вставить("ЮридическоеФизическоеЛицо", 
					ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо"));
			КонецЕсли;
				
			Если ВидКонтрагента = "ОбособленноеПодразделение" Тогда
				ЗначенияЗаполнения.Вставить("ОбособленноеПодразделение", Истина);
			КонецЕсли;
			
			ПараметрыОбъекта.Свойство("Партнер", Партнер);
			
			Если Не ЗначениеЗаполнено(КППКонтрагента) Тогда
				ПараметрыОбъекта.Свойство("КПП", КППКонтрагента);
			КонецЕсли;
			
		КонецЕсли;

		Если ДополнительныеДанные.ИспользоватьПартнеровИКонтрагентов Тогда
			
			Если Не ЗначениеЗаполнено(Партнер) Тогда
				Партнер = НаборДанных.Получить("Партнер").Значение;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Партнер) Тогда
				ЗначенияЗаполнения.Вставить("Партнер", Партнер);
			    ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			Иначе
				ЗначенияЗаполнения.Вставить("НаименованиеПолное", ТекстЗаполнения);
				ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			КонецЕсли;
			
			ЗначенияЗаполнения.Вставить("ИНН", ИННКонтрагента);
			ЗначенияЗаполнения.Вставить("КПП", КППКонтрагента);
			ПараметрыОткрытия.Вставить("ТекстЗаполнения", ТекстЗаполнения);
			ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
			ИмяФормы = "Справочник.Контрагенты.Форма.ФормаЭлемента";
		Иначе
			ПараметрыОткрытия.Вставить("Основание", Новый Структура());
			ПараметрыОткрытия.Вставить("ПараметрыВыбора", Новый Структура());
			ПараметрыОткрытия.ПараметрыВыбора.Вставить(Вид, Истина);
			ПараметрыОткрытия.Основание.Вставить("ИНН", ИННКонтрагента);
			ПараметрыОткрытия.Основание.Вставить("КПП", КППКонтрагента);
			ПараметрыОткрытия.Основание.Вставить("СокращенноеНаименование", ТекстЗаполнения);
			ПараметрыОткрытия.Основание.Вставить("Наименование", ТекстЗаполнения);
			ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
			ИмяФормы = "Справочник.Партнеры.Форма.ПомощникНового";
		КонецЕсли;
		
	ИначеЕсли ИмяЭлемента = "Грузоотправитель" Или ИмяЭлемента = "Грузополучатель" Тогда
		
		Если ИмяЭлемента = "Грузоотправитель" Тогда
			ТекстЗаполнения = НаборДанных.Получить("Грузоотправитель").РаспознанныйТекст;
		Иначе
			ТекстЗаполнения = НаборДанных.Получить("Грузополучатель").РаспознанныйТекст;
		КонецЕсли;  
		
		Если ДополнительныеДанные.ИспользоватьПартнеровИКонтрагентов Тогда
			ПараметрыОткрытия.Вставить("ТекстЗаполнения", ТекстЗаполнения);
			ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
			ИмяФормы = "Справочник.Контрагенты.Форма.ФормаЭлемента";
		Иначе
			ПараметрыОткрытия.Вставить("Основание", Новый Структура());
			ПараметрыОткрытия.Основание.Вставить("СокращенноеНаименование", ТекстЗаполнения);
			ПараметрыОткрытия.Основание.Вставить("Наименование", ТекстЗаполнения);
			ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
			ИмяФормы = "Справочник.Партнеры.Форма.ПомощникНового";
		КонецЕсли;
			
	ИначеЕсли ИмяЭлемента = "Договор" Тогда
		
		Если Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
			Организация = НаборДанных.Получить("ПродавецОрганизация").Значение;
			Контрагент = НаборДанных.Получить("Покупатель").Значение;
			ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту");
		Иначе
			Организация = НаборДанных.Получить("ПокупательОрганизация").Значение;
			Контрагент = НаборДанных.Получить("Продавец").Значение;
			ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика");
		КонецЕсли;
		
		ЗначенияЗаполнения = Новый Структура("Организация", Организация);
		ЗначенияЗаполнения.Вставить("Контрагент", Контрагент);
		ЗначенияЗаполнения.Вставить("ХозяйственнаяОперация", ХозяйственнаяОперация);
		ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
		ПараметрыОткрытия.Вставить("ТекстЗаполнения", НаборДанных.Получить(ИмяЭлемента).РаспознанныйТекст);
		ИмяФормы = "Справочник.ДоговорыКонтрагентов.Форма.ФормаЭлемента";
		
	ИначеЕсли ИмяЭлемента = "Номенклатура" Тогда
		
		Если ЗначениеЗаполнено(ПараметрыОбъекта) Тогда
			Артикул = Неопределено;
			ПараметрыОбъекта.Свойство("Артикул", Артикул);
			ЗначенияЗаполнения = Новый Структура("Артикул", Артикул);
			ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		КонецЕсли;
		
		ПараметрыОткрытия.Вставить("ДополнительныеПараметры", Новый Структура());
		ПараметрыОткрытия.Вставить("ТекстЗаполнения",НаборДанных.Получить("Номенклатура").РаспознанныйТекст);
		ПараметрыОткрытия.ДополнительныеПараметры.Вставить("Артикул", НаборДанных.Получить("Артикул").Значение);
		ПараметрыОткрытия.ДополнительныеПараметры.Вставить("ЕдиницаИзмерения",
			НаборДанных.Получить("ЕдиницаИзмерения").Значение);
		ПараметрыОткрытия.ДополнительныеПараметры.Вставить("СтавкаНДС", НаборДанных.Получить("СтавкаНДС").Значение);
		
		ДанныеНоменклатуры = Неопределено;
		ДополнительныеДанные.Свойство("РеквизитыИСвойстваСоздаваемогоОбъекта", ДанныеНоменклатуры);
		Если Не ДанныеНоменклатуры = Неопределено Тогда
			Для Каждого Элемент Из ДанныеНоменклатуры Цикл
				ПараметрыОткрытия.ДополнительныеПараметры.Вставить(Элемент.Ключ, Элемент.Значение);
			КонецЦикла;
			// Переопределим текст заполнения для наименования
			ТекстЗаполнения = Неопределено;
			ДанныеНоменклатуры.Свойство("Наименование", ТекстЗаполнения);
			Если ЗначениеЗаполнено(ТекстЗаполнения) Тогда
				ПараметрыОткрытия.ТекстЗаполнения = ТекстЗаполнения; 
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
		ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
		ИмяФормы = "Справочник.Номенклатура.Форма.ФормаЭлемента"; 
		
	ИначеЕсли ИмяЭлемента = "БанковскийСчетКонтрагента" Или ИмяЭлемента = "БанковскийСчетОрганизации" Тогда
		
		ИсходящееНаправление = (Направление = ПредопределенноеЗначение(
			"Перечисление.НаправленияРаспознанногоДокумента.Исходящий"));
		
		ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
		
		Если ИмяЭлемента = "БанковскийСчетКонтрагента" Тогда 
			ИмяФормы = "Справочник.БанковскиеСчетаКонтрагентов.Форма.ФормаЭлемента";
			ИмяВладельца = ?(ИсходящееНаправление, "Покупатель", "Продавец");
		Иначе
			ИмяФормы = "Справочник.БанковскиеСчетаОрганизаций.Форма.ФормаЭлемента";
			ИмяВладельца = ?(ИсходящееНаправление, "ПродавецОрганизация", "ПокупательОрганизация");
		КонецЕсли;
		
		Владелец = НаборДанных.Получить(ИмяВладельца).Значение;
		ЗначенияЗаполнения = Новый Структура("Владелец", Владелец);
		ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
	ИначеЕсли ИмяЭлемента = "СтранаПроисхождения" Тогда 
		ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
		ЗначенияЗаполнения = Новый Структура("НаименованиеПолное",
			НаборДанных.Получить("СтранаПроисхождения").РаспознанныйТекст);
		ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ИмяФормы = "Справочник.СтраныМира.Форма.ФормаЭлемента";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
