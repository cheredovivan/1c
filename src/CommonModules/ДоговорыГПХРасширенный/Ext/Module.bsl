#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.ВидыДоговоровАвторскогоЗаказа.СоздатьПредопределенныеВидыДоговоров";
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.ОбщиеДанные = Ложь;
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.29.18";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("e4be4199-cb89-11ee-811a-4cedfb43b11a");
	Обработчик.Процедура       = "Документы.ДоговорРаботыУслуги.ОбновитьДанныеОВремениДляСреднегоФСС";
	Обработчик.Комментарий     = НСтр("ru = 'Обновление данных о времени в договорах ГПХ.';
										|en = 'Update data about time in civil law contracts.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.29.18";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("e4be419b-cb89-11ee-811a-4cedfb43b11a");
	Обработчик.Процедура       = "Документы.ДоговорАвторскогоЗаказа.ОбновитьДанныеОВремениДляСреднегоФСС";
	Обработчик.Комментарий     = НСтр("ru = 'Обновление данных о времени в авторских договорах.';
										|en = 'Update data about time in copyright agreements.'");
	
КонецПроцедуры

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковПереходаСДругойПрограммы.
Процедура ПриДобавленииОбработчиковПереходаСДругойПрограммы(Обработчики) Экспорт
	
	ИмяПроцедуры = "Справочники.ВидыДоговоровАвторскогоЗаказа.СоздатьПредопределенныеВидыДоговоров";
	ОбщиеДанные  = Ложь;
	ОбновлениеБЗК.ДобавитьОбработчикПерехода(Обработчики, ИмяПроцедуры, ОбщиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Определяет объекты, в которых есть процедура ДобавитьКомандыПечати().
// Подробнее см. УправлениеПечатьюПереопределяемый.
//
// Параметры:
//  СписокОбъектов - Массив - список менеджеров объектов.
//
Процедура ПриОпределенииОбъектовСКомандамиПечати(СписокОбъектов) Экспорт
	
	СписокОбъектов.Добавить(Документы.АктПриемкиВыполненныхРаботОказанныхУслуг);
	СписокОбъектов.Добавить(Документы.ДоговорАвторскогоЗаказа);
	СписокОбъектов.Добавить(Документы.ДоговорРаботыУслуги);
	СписокОбъектов.Добавить(Документы.НачислениеПоДоговорам);
	
КонецПроцедуры

#КонецОбласти

#Область Свойства

// См. УправлениеСвойствамиПереопределяемый.ПриПолученииПредопределенныхНаборовСвойств.
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbfd5-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.АктПриемкиВыполненныхРаботОказанныхУслуг);
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbfc6-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.ДоговорАвторскогоЗаказа);
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbfe6-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.ДоговорРаботыУслуги);
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbf19-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.НачислениеПоДоговорам);
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d42dbf42-9802-11e9-80cd-4cedfb43b11a", Метаданные.Справочники.ВидыДоговоровАвторскогоЗаказа);
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.Справочники.АктПриемкиВыполненныхРаботОказанныхУслугПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Справочники.ДоговорАвторскогоЗаказаПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Справочники.ДоговорРаботыУслугиПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Документы.АктПриемкиВыполненныхРаботОказанныхУслуг, Истина);
	Списки.Вставить(Метаданные.Документы.ДоговорАвторскогоЗаказа, Истина);
	Списки.Вставить(Метаданные.Документы.ДоговорРаботыУслуги, Истина);
	Списки.Вставить(Метаданные.Документы.НачислениеПоДоговорам, Истина);
	Списки.Вставить(Метаданные.Справочники.НачислениеПоДоговорамПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.ЖурналыДокументов.ДоговорыГражданскоПравовогоХарактера, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.ПерерасчетДоговоров, Истина);
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииВидовОграниченийПравОбъектовМетаданных.
Процедура ПриЗаполненииВидовОграниченийПравОбъектовМетаданных(Описание) Экспорт
	
	Описание = Описание + "
	|Справочник.АктПриемкиВыполненныхРаботОказанныхУслугПрисоединенныеФайлы.Чтение.ГруппыФизическихЛиц
	|Справочник.АктПриемкиВыполненныхРаботОказанныхУслугПрисоединенныеФайлы.Чтение.Организации
	|Справочник.АктПриемкиВыполненныхРаботОказанныхУслугПрисоединенныеФайлы.Изменение.ГруппыФизическихЛиц
	|Справочник.АктПриемкиВыполненныхРаботОказанныхУслугПрисоединенныеФайлы.Изменение.Организации
	|Справочник.ДоговорАвторскогоЗаказаПрисоединенныеФайлы.Чтение.ГруппыФизическихЛиц
	|Справочник.ДоговорАвторскогоЗаказаПрисоединенныеФайлы.Чтение.Организации
	|Справочник.ДоговорАвторскогоЗаказаПрисоединенныеФайлы.Изменение.ГруппыФизическихЛиц
	|Справочник.ДоговорАвторскогоЗаказаПрисоединенныеФайлы.Изменение.Организации
	|Справочник.ДоговорРаботыУслугиПрисоединенныеФайлы.Чтение.ГруппыФизическихЛиц
	|Справочник.ДоговорРаботыУслугиПрисоединенныеФайлы.Чтение.Организации
	|Справочник.ДоговорРаботыУслугиПрисоединенныеФайлы.Изменение.ГруппыФизическихЛиц
	|Справочник.ДоговорРаботыУслугиПрисоединенныеФайлы.Изменение.Организации
	|Документ.АктПриемкиВыполненныхРаботОказанныхУслуг.Чтение.ГруппыФизическихЛиц
	|Документ.АктПриемкиВыполненныхРаботОказанныхУслуг.Чтение.Организации
	|Документ.АктПриемкиВыполненныхРаботОказанныхУслуг.Изменение.ГруппыФизическихЛиц
	|Документ.АктПриемкиВыполненныхРаботОказанныхУслуг.Изменение.Организации
	|Документ.ДоговорАвторскогоЗаказа.Чтение.ГруппыФизическихЛиц
	|Документ.ДоговорАвторскогоЗаказа.Чтение.Организации
	|Документ.ДоговорАвторскогоЗаказа.Изменение.ГруппыФизическихЛиц
	|Документ.ДоговорАвторскогоЗаказа.Изменение.Организации
	|Документ.ДоговорРаботыУслуги.Чтение.ГруппыФизическихЛиц
	|Документ.ДоговорРаботыУслуги.Чтение.Организации
	|Документ.ДоговорРаботыУслуги.Изменение.ГруппыФизическихЛиц
	|Документ.ДоговорРаботыУслуги.Изменение.Организации
	|Документ.НачислениеПоДоговорам.Чтение.ГруппыФизическихЛиц
	|Документ.НачислениеПоДоговорам.Чтение.Организации
	|Документ.НачислениеПоДоговорам.Изменение.ГруппыФизическихЛиц
	|Документ.НачислениеПоДоговорам.Изменение.Организации
	|Справочник.НачислениеПоДоговорамПрисоединенныеФайлы.Чтение.ГруппыФизическихЛиц
	|Справочник.НачислениеПоДоговорамПрисоединенныеФайлы.Чтение.Организации
	|Справочник.НачислениеПоДоговорамПрисоединенныеФайлы.Изменение.ГруппыФизическихЛиц
	|Справочник.НачислениеПоДоговорамПрисоединенныеФайлы.Изменение.Организации
	|ЖурналДокументов.ДоговорыГражданскоПравовогоХарактера.Чтение.ГруппыФизическихЛиц
	|ЖурналДокументов.ДоговорыГражданскоПравовогоХарактера.Чтение.Организации
	|РегистрСведений.ПерерасчетДоговоров.Чтение.ГруппыФизическихЛиц
	|РегистрСведений.ПерерасчетДоговоров.Чтение.Организации
	|РегистрСведений.ПерерасчетДоговоров.Изменение.ГруппыФизическихЛиц
	|РегистрСведений.ПерерасчетДоговоров.Изменение.Организации";
	
КонецПроцедуры

#КонецОбласти

#Область ДатыЗапретаИзменения

// См. ДатыЗапретаИзмененияПереопределяемый.ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения.
Процедура ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения(ИсточникиДанных) Экспорт
	
	ДатыЗапретаИзменения.ДобавитьСтроку(
		ИсточникиДанных,
		"Документ.АктПриемкиВыполненныхРаботОказанныхУслуг",
		"МесяцНачисления",
		"Зарплата",
		"Организация");
	
	ДатыЗапретаИзменения.ДобавитьСтроку(
		ИсточникиДанных,
		"Документ.ДоговорРаботыУслуги",
		"ДатаЗапрета",
		"Зарплата",
		"Организация");
	
	ДатыЗапретаИзменения.ДобавитьСтроку(
		ИсточникиДанных,
		"Документ.ДоговорАвторскогоЗаказа",
		"ДатаЗапрета",
		"Зарплата",
		"Организация");
	
	ДатыЗапретаИзменения.ДобавитьСтроку(
		ИсточникиДанных,
		"Документ.НачислениеПоДоговорам",
		"МесяцНачисления",
		"Зарплата",
		"Организация");
	
КонецПроцедуры

#КонецОбласти

#Область ПерерасчетДоговоров

Функция ТипыИзмененийДоговоров()
	
	ТипыИзменений = Новый Массив;
	ТипыИзменений.Добавить(Перечисления.ТипИзмененийПерерасчетов.ДоговорыГПХ);
	ТипыИзменений.Добавить(Перечисления.ТипИзмененийПерерасчетов.ПереносДоговораГПХ);
	Возврат ТипыИзменений;
	
КонецФункции

Процедура ДополнитьЗапросСотрудниковПересчетаПериода(Запрос) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорамРасширенная") Тогда
		Возврат;
	КонецЕсли;
	
	Текст =
		"ВЫБРАТЬ
		|	ПерерасчетДоговоров.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрСведений.ПерерасчетДоговоров КАК ПерерасчетДоговоров
		|ГДЕ
		|	ПерерасчетДоговоров.Организация = &Организация
		|	И ПерерасчетДоговоров.ПериодДействия = &Месяц";
	
	Запрос.Текст = Запрос.Текст + ОбщегоНазначения.ТекстОбъединитьВсе() + Текст;
	
КонецПроцедуры
	
Процедура ВосстановитьПерерасчеты(МенеджерВременныхТаблиц, Регистратор, Организация) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорамРасширенная") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АВТОНОМЕРЗАПИСИ() КАК Идентификатор,
		|	&Организация КАК Организация,
		|	ДоговорыПерерасчет.Сотрудник КАК Сотрудник,
		|	ДоговорыПерерасчет.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДоговорыПерерасчет.ПериодДействия КАК ПериодДействия,
		|	ДоговорыПерерасчет.ДатаНачала КАК Период,
		|	ДоговорыПерерасчет.ДокументОснование КАК ИзмененныеДанные,
		|	НЕОПРЕДЕЛЕНО КАК РегистраторПерерасчета,
		|	НЕОПРЕДЕЛЕНО КАК ТипИзменения,
		|	ЛОЖЬ КАК НачалоУчета
		|ПОМЕСТИТЬ ВТПерерасчетыДоговоровПредварительно
		|ИЗ
		|	Документ.НачислениеЗарплаты.ДоговорыПерерасчет КАК ДоговорыПерерасчет
		|ГДЕ
		|	ДоговорыПерерасчет.Ссылка = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПерерасчетыДоговоровПредварительно.Идентификатор КАК Идентификатор,
		|	ПерерасчетыДоговоровПредварительно.ИзмененныеДанные КАК ИсправленныйДокумент
		|ИЗ
		|	ВТПерерасчетыДоговоровПредварительно КАК ПерерасчетыДоговоровПредварительно";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ДокументыИсправления = Новый ТаблицаЗначений();
	ДокументыИсправления.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Число"));
	ДокументыИсправления.Колонки.Добавить("Документ", ОписаниеТиповДокументыДоговоры());
	
	Выборка = РезультатЗапроса.Выбрать();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пока Выборка.Следующий() Цикл
		
		СведенияОбИсправлении = ИсправлениеДокументовЗарплатаКадры.СведенияОбИсправленииДокумента(Выборка.ИсправленныйДокумент);
		
		НоваяСтрока = ДокументыИсправления.Добавить();
		НоваяСтрока.Идентификатор = Выборка.Идентификатор;
		НоваяСтрока.Документ = СведенияОбИсправлении.ДокументИсправление;
		
	КонецЦикла;
	
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, ДокументыИсправления, "ВТРегистраторыПерарасчета");
	УстановитьПривилегированныйРежим(Ложь);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПерерасчетыДоговоровПредварительно.Организация КАК Организация,
		|	ПерерасчетыДоговоровПредварительно.Сотрудник КАК Сотрудник,
		|	ПерерасчетыДоговоровПредварительно.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ПерерасчетыДоговоровПредварительно.ПериодДействия КАК ПериодДействия,
		|	ПерерасчетыДоговоровПредварительно.Период КАК Период,
		|	ПерерасчетыДоговоровПредварительно.ИзмененныеДанные КАК ИзмененныеДанные,
		|	РегистраторыПерарасчета.Документ КАК РегистраторПерерасчета,
		|	ПерерасчетыДоговоровПредварительно.ТипИзменения КАК ТипИзменения,
		|	ПерерасчетыДоговоровПредварительно.НачалоУчета КАК НачалоУчета
		|ПОМЕСТИТЬ ВТПредварительныеПерерасчетыДоговоров
		|ИЗ
		|	ВТПерерасчетыДоговоровПредварительно КАК ПерерасчетыДоговоровПредварительно
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРегистраторыПерарасчета КАК РегистраторыПерарасчета
		|		ПО (РегистраторыПерарасчета.Идентификатор = ПерерасчетыДоговоровПредварительно.Идентификатор)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРегистраторыПерарасчета";
	Запрос.Выполнить();
	
	ЗарегистрироватьПерерасчетыДоговоров(Запрос.МенеджерВременныхТаблиц, Регистратор);
	
КонецПроцедуры

Процедура СоздатьВТПредварительныеПерерасчетыДоговоров(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТипыИзменений", ТипыИзмененийДоговоров());
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПредварительныеПерерасчеты.Период КАК Период,
		|	ПредварительныеПерерасчеты.ПериодДействия КАК ПериодДействия,
		|	ПредварительныеПерерасчеты.Организация КАК Организация,
		|	ПредварительныеПерерасчеты.Сотрудник КАК Сотрудник,
		|	ПредварительныеПерерасчеты.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ПредварительныеПерерасчеты.РегистраторПерерасчета КАК РегистраторПерерасчета,
		|	ПредварительныеПерерасчеты.ТипИзменения КАК ТипИзменения,
		|	ПредварительныеПерерасчеты.ИзмененныеДанные КАК ИзмененныеДанные,
		|	ПредварительныеПерерасчеты.ДокументОснование КАК ДокументОснование
		|ПОМЕСТИТЬ ВТПредварительныеПерерасчетыДоговоров
		|ИЗ
		|	ВТПредварительныеПерерасчеты КАК ПредварительныеПерерасчеты
		|ГДЕ
		|	ПредварительныеПерерасчеты.ТипИзменения В(&ТипыИзменений)";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТНеобходимыеПерерасчетыПереносДоговора(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СотрудникиПериоды.Организация КАК Организация
		|ИЗ
		|	ВТПредварительныеПерерасчетыДоговоров КАК СотрудникиПериоды
		|ГДЕ
		|	СотрудникиПериоды.ТипИзменения = ЗНАЧЕНИЕ(Перечисление.ТипИзмененийПерерасчетов.ПереносДоговораГПХ)";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;

	ПоследниеПериодыНачислений = Новый ТаблицаЗначений;
	ПоследниеПериодыНачислений.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ПоследниеПериодыНачислений.Колонки.Добавить("ПоследнийПериодДействия", Новый ОписаниеТипов("Дата"));
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Запрос.УстановитьПараметр("Организация", Выборка.Организация);
		
		Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИсходныеДанныеПерерасчетов.Организация КАК Организация,
			|	ИсходныеДанныеПерерасчетов.ПериодДействия КАК ПоследнийПериодДействия
			|ИЗ
			|	РегистрСведений.ИсходныеДанныеПерерасчетов КАК ИсходныеДанныеПерерасчетов
			|ГДЕ
			|	ИсходныеДанныеПерерасчетов.Организация = &Организация
			|	И (ИсходныеДанныеПерерасчетов.Регистратор ССЫЛКА Документ.НачислениеЗарплаты
			|			ИЛИ ИсходныеДанныеПерерасчетов.Регистратор ССЫЛКА Документ.НачислениеПоДоговорам)
			|
			|УПОРЯДОЧИТЬ ПО
			|	ПоследнийПериодДействия УБЫВ";
			
		ВыборкаПоОрганизации = Запрос.Выполнить().Выбрать();
		Если ВыборкаПоОрганизации.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(ПоследниеПериодыНачислений.Добавить(), ВыборкаПоОрганизации);
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ПоследниеПериодыНачислений", ПоследниеПериодыНачислений);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПоследниеПериодыНачислений.Организация КАК Организация,
		|	ПоследниеПериодыНачислений.ПоследнийПериодДействия КАК ПоследнийПериодДействия
		|ПОМЕСТИТЬ ВТПоследниеПериодыНачислений
		|ИЗ
		|	&ПоследниеПериодыНачислений КАК ПоследниеПериодыНачислений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СотрудникиПериоды.Организация КАК Организация,
		|	СотрудникиПериоды.Сотрудник КАК Сотрудник,
		|	СотрудникиПериоды.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СотрудникиПериоды.ПериодДействия КАК ПериодДействия,
		|	СотрудникиПериоды.РегистраторПерерасчета КАК РегистраторПерерасчета,
		|	СотрудникиПериоды.ИзмененныеДанные КАК ИсправленныйДоговор
		|ПОМЕСТИТЬ ВТСотрудникиСПоследнимиПериодами
		|ИЗ
		|	ВТПредварительныеПерерасчетыДоговоров КАК СотрудникиПериоды
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоследниеПериодыНачислений КАК ПоследниеПериодыНачислений
		|		ПО СотрудникиПериоды.Организация = ПоследниеПериодыНачислений.Организация
		|ГДЕ
		|	СотрудникиПериоды.ТипИзменения = ЗНАЧЕНИЕ(Перечисление.ТипИзмененийПерерасчетов.ПереносДоговораГПХ)
		|	И СотрудникиПериоды.ПериодДействия <= ПоследниеПериодыНачислений.ПоследнийПериодДействия
		|	И РАЗНОСТЬДАТ(СотрудникиПериоды.ПериодДействия, ПоследниеПериодыНачислений.ПоследнийПериодДействия, МЕСЯЦ) < 12
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиПериоды.Организация КАК Организация,
		|	СотрудникиПериоды.Сотрудник КАК Сотрудник,
		|	СотрудникиПериоды.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ИсходныеДанныеПерерасчетов.ПериодДействия КАК ПериодДействия,
		|	СотрудникиПериоды.РегистраторПерерасчета КАК РегистраторПерерасчета,
		|	МАКСИМУМ(ИсходныеДанныеПерерасчетов.Регистратор) КАК ДокументНачисления,
		|	СотрудникиПериоды.ИсправленныйДоговор КАК ИсправленныйДоговор
		|ПОМЕСТИТЬ ВТНеобходимыеПерерасчетыПереносДоговора
		|ИЗ
		|	ВТСотрудникиСПоследнимиПериодами КАК СотрудникиПериоды
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсходныеДанныеПерерасчетов КАК ИсходныеДанныеПерерасчетов
		|		ПО СотрудникиПериоды.Организация = ИсходныеДанныеПерерасчетов.Организация
		|			И СотрудникиПериоды.ПериодДействия = ИсходныеДанныеПерерасчетов.ПериодДействия
		|			И (ИсходныеДанныеПерерасчетов.Регистратор ССЫЛКА Документ.НачислениеЗарплаты
		|				ИЛИ ИсходныеДанныеПерерасчетов.Регистратор ССЫЛКА Документ.НачислениеПоДоговорам)
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиПериоды.Организация,
		|	СотрудникиПериоды.Сотрудник,
		|	СотрудникиПериоды.ФизическоеЛицо,
		|	СотрудникиПериоды.РегистраторПерерасчета,
		|	ИсходныеДанныеПерерасчетов.ПериодДействия,
		|	СотрудникиПериоды.ИсправленныйДоговор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПоследниеПериодыНачислений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСотрудникиСПоследнимиПериодами";
	Запрос.Выполнить();
		
КонецПроцедуры

Процедура СоздатьВТНеобходимыеПерерасчетыДоговоров(МенеджерВременныхТаблиц, ИсключаемыйРегистратор = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	УдалитьВТ = Новый Массив;
	УдалитьВТ.Добавить("ВТПредварительныеПерерасчетыДоговоров");
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СотрудникиПериоды.Организация КАК Организация,
		|	СотрудникиПериоды.Сотрудник КАК Сотрудник,
		|	СотрудникиПериоды.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ИсходныеДанныеПерерасчетов.ПериодДействия КАК ПериодДействия,
		|	СотрудникиПериоды.РегистраторПерерасчета КАК РегистраторПерерасчета,
		|	ИсходныеДанныеПерерасчетов.Регистратор КАК ДокументНачисления,
		|	СотрудникиПериоды.ИзмененныеДанные КАК ИсправленныйДоговор
		|ПОМЕСТИТЬ ВТНеобходимыеПерерасчеты
		|ИЗ
		|	ВТПредварительныеПерерасчетыДоговоров КАК СотрудникиПериоды
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсходныеДанныеПерерасчетов КАК ИсходныеДанныеПерерасчетов
		|		ПО СотрудникиПериоды.Организация = ИсходныеДанныеПерерасчетов.Организация
		|			И СотрудникиПериоды.Сотрудник = ИсходныеДанныеПерерасчетов.Сотрудник
		|			И СотрудникиПериоды.ПериодДействия <= ИсходныеДанныеПерерасчетов.ПериодДействия
		|			И СотрудникиПериоды.Период <= ИсходныеДанныеПерерасчетов.ДатаОкончания
		|			И (НЕ ИсходныеДанныеПерерасчетов.Регистратор ССЫЛКА Документ.ПереносДанных)
		|			И (&ИсключаемыйРегистратор)
		|			И (ИсходныеДанныеПерерасчетов.ДокументОснование = СотрудникиПериоды.ИзмененныеДанные)
		|ГДЕ
		|	СотрудникиПериоды.ТипИзменения <> ЗНАЧЕНИЕ(Перечисление.ТипИзмененийПерерасчетов.ПереносДоговораГПХ)";
	
	Если ЗначениеЗаполнено(ИсключаемыйРегистратор) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИсключаемыйРегистратор", "ИсходныеДанныеПерерасчетов.Регистратор <> &ИсключаемыйРегистратор");
		Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИсключаемыйРегистратор", "ИСТИНА");
	КонецЕсли;
	
	Если ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТНеобходимыеПерерасчетыПереносДоговора") Тогда
		Запрос.Текст = Запрос.Текст + "
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	НеобходимыеПерерасчетыПереносДоговора.Организация,
		|	НеобходимыеПерерасчетыПереносДоговора.Сотрудник,
		|	НеобходимыеПерерасчетыПереносДоговора.ФизическоеЛицо,
		|	НеобходимыеПерерасчетыПереносДоговора.ПериодДействия,
		|	НеобходимыеПерерасчетыПереносДоговора.РегистраторПерерасчета,
		|	НеобходимыеПерерасчетыПереносДоговора.ДокументНачисления,
		|	НеобходимыеПерерасчетыПереносДоговора.ИсправленныйДоговор
		|ИЗ
		|	ВТНеобходимыеПерерасчетыПереносДоговора КАК НеобходимыеПерерасчетыПереносДоговора";
		УдалитьВТ.Добавить("ВТНеобходимыеПерерасчетыПереносДоговора");
	КонецЕсли;
	
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьЗапросыУничтоженияВременныхТаблиц(Запрос.Текст, УдалитьВТ);
	Запрос.Выполнить();
		
КонецПроцедуры

Процедура ЗарегистрироватьПерерасчетыДоговоров(МенеджерВременныхТаблиц, ИсключаемыйРегистратор = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорамРасширенная") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	СоздатьВТНеобходимыеПерерасчетыПереносДоговора(МенеджерВременныхТаблиц);
	СоздатьВТНеобходимыеПерерасчетыДоговоров(МенеджерВременныхТаблиц, ИсключаемыйРегистратор);

	Запрос.Текст =
		"ВЫБРАТЬ
		|	НеобходимыеПерерасчеты.Организация КАК Организация,
		|	НеобходимыеПерерасчеты.Сотрудник КАК Сотрудник,
		|	НеобходимыеПерерасчеты.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НеобходимыеПерерасчеты.ПериодДействия КАК ПериодДействия,
		|	НеобходимыеПерерасчеты.РегистраторПерерасчета КАК Основание,
		|	НеобходимыеПерерасчеты.ДокументНачисления КАК ДокументНачисления,
		|	НеобходимыеПерерасчеты.ИсправленныйДоговор КАК Договор,
		|	ВЫБОР
		|		КОГДА ПерерасчетДоговоров.Организация ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК СуществующаяЗапись
		|ИЗ
		|	ВТНеобходимыеПерерасчеты КАК НеобходимыеПерерасчеты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПерерасчетДоговоров КАК ПерерасчетДоговоров
		|		ПО НеобходимыеПерерасчеты.Организация = ПерерасчетДоговоров.Организация
		|			И НеобходимыеПерерасчеты.Сотрудник = ПерерасчетДоговоров.Сотрудник
		|			И НеобходимыеПерерасчеты.ПериодДействия = ПерерасчетДоговоров.ПериодДействия
		|			И НеобходимыеПерерасчеты.ИсправленныйДоговор = ПерерасчетДоговоров.Договор
		|			И НеобходимыеПерерасчеты.РегистраторПерерасчета = ПерерасчетДоговоров.Основание
		|			И НеобходимыеПерерасчеты.ДокументНачисления = ПерерасчетДоговоров.ДокументНачисления
		|ГДЕ
		|	НеобходимыеПерерасчеты.РегистраторПерерасчета <> НеобходимыеПерерасчеты.ДокументНачисления";
	
	УдаляемыеВТ = Новый Массив;
	УдаляемыеВТ.Добавить("ВТНеобходимыеПерерасчеты");
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Не Выборка.СуществующаяЗапись Тогда
				НаборЗаписей = РегистрыСведений.ПерерасчетДоговоров.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
				НаборЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
				НаборЗаписей.Отбор.ПериодДействия.Установить(Выборка.ПериодДействия);
				НаборЗаписей.Отбор.Основание.Установить(Выборка.Основание);
				НаборЗаписей.Отбор.ДокументНачисления.Установить(Выборка.ДокументНачисления);
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
				НаборЗаписей.Записать();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдаляемыеВТ);
	
КонецПроцедуры

Процедура ДополнитьЗапросВТПерерасчетЗарплатыСОбластямиПерерасчетов(ТекстЗапроса, РежимДоначисления, Сотрудники = Неопределено) Экспорт
	
	// Кроме периода перерасчета где требуется сторно начисления по исходному документу,
	// нужно добавить период документа-исправления (или основания перерасчета), что бы рассчиталось новое - исправительное начисление.
	
	ТекстПоДоговорам =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НАЧАЛОПЕРИОДА(ПерерасчетДоговоров.ПериодДействия, МЕСЯЦ) КАК НачалоПериода,
		|	КОНЕЦПЕРИОДА(ПерерасчетДоговоров.ПериодДействия, МЕСЯЦ) КАК ОкончаниеПериода,
		|	ПерерасчетДоговоров.Сотрудник КАК Сотрудник,
		|	ПерерасчетДоговоров.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НЕОПРЕДЕЛЕНО КАК ВидРасчета,
		|	ПерерасчетДоговоров.Договор КАК ДокументОснование,
		|	ЛОЖЬ КАК ПерерасчетНачислений,
		|	ИСТИНА КАК ПерерасчетДоговоров,
		|	ЛОЖЬ КАК ПерерасчетЛьгот
		|ИЗ
		|	РегистрСведений.ПерерасчетДоговоров КАК ПерерасчетДоговоров
		|ГДЕ
		|	ПерерасчетДоговоров.Организация = &Организация
		|	И ПерерасчетДоговоров.ПериодДействия <= &ПериодДействия
		|	И ПерерасчетДоговоров.Сотрудник В(&Сотрудники)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НАЧАЛОПЕРИОДА(ПериодыОснований.МесяцНачисления, МЕСЯЦ),
		|	КОНЕЦПЕРИОДА(ПериодыОснований.МесяцНачисления, МЕСЯЦ),
		|	ПериодыОснований.Сотрудник,
		|	ПериодыОснований.ФизическоеЛицо,
		|	НЕОПРЕДЕЛЕНО,
		|	ПериодыОснований.Договор,
		|	ЛОЖЬ,
		|	ИСТИНА,
		|	ЛОЖЬ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВЫБОР
		|			КОГДА ПерерасчетДоговоров.Основание ССЫЛКА Документ.АктПриемкиВыполненныхРаботОказанныхУслуг
		|				ТОГДА НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(ПерерасчетДоговоров.Основание КАК Документ.АктПриемкиВыполненныхРаботОказанныхУслуг).МесяцНачисления, МЕСЯЦ)
		|			КОГДА ПерерасчетДоговоров.Основание ССЫЛКА Документ.ДоговорАвторскогоЗаказа
		|				ТОГДА НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(ПерерасчетДоговоров.Основание КАК Документ.ДоговорАвторскогоЗаказа).ДатаОкончания, МЕСЯЦ)
		|			КОГДА ПерерасчетДоговоров.Основание ССЫЛКА Документ.ДоговорРаботыУслуги
		|				ТОГДА НАЧАЛОПЕРИОДА(ВЫРАЗИТЬ(ПерерасчетДоговоров.Основание КАК Документ.ДоговорРаботыУслуги).ДатаОкончания, МЕСЯЦ)
		|		КОНЕЦ КАК МесяцНачисления,
		|		ПерерасчетДоговоров.ПериодДействия КАК ПериодПерерасчета,
		|		ПерерасчетДоговоров.Сотрудник КАК Сотрудник,
		|		ПерерасчетДоговоров.ФизическоеЛицо КАК ФизическоеЛицо,
		|		ПерерасчетДоговоров.Договор КАК Договор
		|	ИЗ
		|		РегистрСведений.ПерерасчетДоговоров КАК ПерерасчетДоговоров
		|	ГДЕ
		|		ПерерасчетДоговоров.Организация = &Организация
		|		И ПерерасчетДоговоров.ПериодДействия <= &ПериодДействия
		|		И ПерерасчетДоговоров.Сотрудник В(&Сотрудники)) КАК ПериодыОснований
		|ГДЕ
		|	ПериодыОснований.МесяцНачисления <> ДАТАВРЕМЯ(1, 1, 1)
		|	И ПериодыОснований.МесяцНачисления < ПериодыОснований.ПериодПерерасчета
		|	И ПериодыОснований.МесяцНачисления < &ПериодДействия";
	
	Если Не РежимДоначисления Тогда
		ТекстПоДоговорам = СтрЗаменить(ТекстПоДоговорам, "И ПерерасчетДоговоров.ПериодДействия <= &ПериодДействия", "И ПерерасчетДоговоров.ПериодДействия < &ПериодДействия");
	КонецЕсли;
	
	Если Сотрудники = Неопределено Тогда
		ТекстПоДоговорам = СтрЗаменить(ТекстПоДоговорам, "ПерерасчетДоговоров.Сотрудник В(&Сотрудники)", "ИСТИНА");
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.ТекстОбъединитьВсе() + ТекстПоДоговорам;
	
КонецПроцедуры

Процедура ДобавитьОбъединениеСПерерасчетамиДоговоров(ТекстЗапроса, ДополнительныеУсловия = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорамРасширенная") Тогда
		Возврат;
	КонецЕсли;
		
	Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаПерерасчетов.Организация КАК Организация,
		|	ТаблицаПерерасчетов.Сотрудник КАК Сотрудник,
		|	ТаблицаПерерасчетов.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ТаблицаПерерасчетов.ПериодДействия КАК ПериодДействия,
		|	ТаблицаПерерасчетов.Основание КАК Основание,
		|	ТаблицаПерерасчетов.ДокументНачисления КАК ДокументНачисления,
		|	ИСТИНА КАК ПерерасчетНачислений,
		|	ЛОЖЬ КАК ПерерасчетЛьгот,
		|	ЛОЖЬ КАК ПерерасчетУдержаний
		|ИЗ
		|	РегистрСведений.ПерерасчетДоговоров КАК ТаблицаПерерасчетов
		|ГДЕ
		|	ТаблицаПерерасчетов.Организация = &Организация
		|	И &ДополнительныеУсловия";

	Если ДополнительныеУсловия <> Неопределено Тогда
		Текст = СтрЗаменить(Текст, "&ДополнительныеУсловия", ДополнительныеУсловия);
	Иначе
		Текст = СтрЗаменить(Текст, "И &ДополнительныеУсловия", "");
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.ТекстОбъединитьВсе() + Текст;
	
КонецПроцедуры

Процедура ДополнитьОтборПерерасчетовНачислений(ТаблицаОтборов, МенеджерВременныхТаблиц) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорамРасширенная") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СторнируемыеРегистраторы.Сотрудник КАК Сотрудник,
		|	СторнируемыеРегистраторы.ПериодДействия КАК ПериодДействия,
		|	СторнируемыеРегистраторы.ВидРасчета
		|ИЗ
		|	ВТСторнируемыеРегистраторыНачисленийИЛьгот КАК СторнируемыеРегистраторы
		|ГДЕ
		|	СторнируемыеРегистраторы.ПерерасчетДоговоров
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Сотрудник", Выборка.Сотрудник);
			СтруктураПоиска.Вставить("ПериодДействия", Выборка.ПериодДействия);
			
			НайденныеСтроки = ТаблицаОтборов.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() = 0 Тогда
				НоваяСтрока = ТаблицаОтборов.Добавить();
				НоваяСтрока.Сотрудник = Выборка.Сотрудник;
				НоваяСтрока.ПериодДействия = Выборка.ПериодДействия;
				НоваяСтрока.Начисления = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Выборка.ВидРасчета);
			Иначе
				НайденныеСтроки[0].Начисления.Добавить(Выборка.ВидРасчета);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьДоговорамиЗапросВТПериодыРасчетаСотрудников(ТекстыЗапросов, МетаданныеРегистратора) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНачисленияПоДоговорамРасширенная") Тогда
		Возврат;
	КонецЕсли;
	
	Если МетаданныеРегистратора.ТабличныеЧасти.Найти("НачисленияПоДоговорам") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапросов.Добавить("ОБЪЕДИНИТЬ");
	
	ТекстыЗапросов.Добавить(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НачисленияПоДоговорам.Ссылка.Организация КАК Организация,
		|	НачисленияПоДоговорам.Сотрудник КАК Сотрудник,
		|	НАЧАЛОПЕРИОДА(НачисленияПоДоговорам.ДатаОкончания, МЕСЯЦ) КАК ПериодДействия,
		|	НЕОПРЕДЕЛЕНО КАК Начисление,
		|	НачисленияПоДоговорам.ДокументОснование КАК ДокументОснование,
		|	ЛОЖЬ КАК ПерерасчетНачислений,
		|	ИСТИНА КАК ПерерасчетДоговоров,
		|	ЛОЖЬ КАК ПерерасчетЛьгот,
		|	ЛОЖЬ КАК ПерерасчетУдержаний
		|ИЗ
		|	Документ.НачислениеЗарплаты.НачисленияПоДоговорам КАК НачисленияПоДоговорам
		|ГДЕ
		|	НачисленияПоДоговорам.Ссылка = &Регистратор
		|	И &УдалитьПерерасчетыТекущегоПериода");
	
	Если МетаданныеРегистратора.Имя = "НачислениеЗарплаты" Тогда
		ТекстыЗапросов.Добавить(
			"	И НЕ НачисленияПоДоговорам.Ссылка.РежимДоначисления");
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьЗаписьПерерасчетов(Месяц, ФизическоеЛицо, Организация, ДокументНачисления) Экспорт
	
	НаборЗаписей = РегистрыСведений.ПерерасчетДоговоров.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицо);
	НаборЗаписей.Отбор.ПериодДействия.Установить(Месяц);
	НаборЗаписей.Отбор.ДокументНачисления.Установить(ДокументНачисления);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

Функция ОписаниеТиповДокументыДоговоры() Экспорт
	
	Возврат Новый ОписаниеТипов(
		"ДокументСсылка.АктПриемкиВыполненныхРаботОказанныхУслуг,
		|ДокументСсылка.ДоговорАвторскогоЗаказа,
		|ДокументСсылка.ДоговорРаботыУслуги,
		|ДокументСсылка.ВыплатыПоДоговорамОпеки");
	
КонецФункции

Функция НачисленияПоДоговорам() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.ДоговорАвторскогоЗаказа);
	Результат.Добавить(Перечисления.ВидыОсобыхНачисленийИУдержаний.ДоговорРаботыУслуги);
	Возврат Результат;

КонецФункции

#КонецОбласти

