///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП".
// ОбщийМодуль.СистемаБыстрыхПлатежейВызовСервера.
//
// Серверные процедуры настройки подключения к Системе быстрых платежей:
//  - алгоритмы настройки формы "Интернет-поддержка и сервисы".
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// См. СистемаБыстрыхПлатежей.НастройкаПодключенияДоступна.
//
Функция НастройкаПодключенияДоступна() Экспорт
	
	Возврат СистемаБыстрыхПлатежей.НастройкаПодключенияДоступна();
	
КонецФункции

// См. СистемаБыстрыхПлатежейСлужебный.ПараметрыОплатыПоНастройке.
//
Функция ПараметрыОплатыПоНастройке(Знач НастройкаПодключения) Экспорт
	
	Возврат СистемаБыстрыхПлатежейСлужебный.ПараметрыОплатыПоНастройке(НастройкаПодключения);
	
КонецФункции

// Возвращает признак создания всех описанных шаблонов в зависимости от состава подсистем.
//
// Возвращаемое значение:
//  Структура - результат обработки шаблонов сообщений:
//    * ШаблоныСозданы - Булево - Истина, если все описанные шаблоны созданы;
//    * Шаблоны - Массив из СправочникСсылка.ШаблоныСообщений - созданные шаблоны.
//
Функция ШаблоныСозданы() Экспорт
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("ШаблоныСозданы", Ложь);
	РезультатПроверки.Вставить("Шаблоны", Новый Массив);
	
	СистемаБыстрыхПлатежейСобытия.ПриОпределенииСозданныхШаблоновСообщений(
		РезультатПроверки);
	
	Возврат РезультатПроверки;
	
КонецФункции

// Создает предопределенные шаблоны сообщений после проверки существования соответствующих подсистем.
//
// Параметры:
//  УстановитьИспользование - Булево - см. СистемаБыстрыхПлатежейСлужебный.УстановитьИспользованиеШаблоновСообщений
//
// Возвращаемое значение:
//  Массив Из СправочникСсылка.ШаблоныСообщений - Массив созданных предопределенных шаблонов,
//    элементами массива являются ссылки на справочник ШаблоныСообщений.
//
Функция СоздатьПредопределенныеШаблоныСообщений(Знач УстановитьИспользование) Экспорт
	
	Если УстановитьИспользование Тогда
		СистемаБыстрыхПлатежейСлужебный.УстановитьИспользованиеШаблоновСообщений();
	КонецЕсли;
	
	Результат = Новый Массив;
	СистемаБыстрыхПлатежейСобытия.ПриСозданииПредопределенныхШаблоновСообщений(
		Результат,
		СистемаБыстрыхПлатежейСлужебный.НовыйНастройкаСозданияШаблонов(
			Истина,
			Истина));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// См. СистемаБыстрыхПлатежейСлужебный.ИнформацияДляТехническойПоддержки.
//
Функция ИнформацияДляТехническойПоддержки(
		Знач ДокументОперации,
		Знач НастройкаПодключения) Экспорт
	
	Возврат СистемаБыстрыхПлатежейСлужебный.ИнформацияДляТехническойПоддержки(
		ДокументОперации,
		НастройкаПодключения);
	
КонецФункции

// Проверяет, что введены логин и пароль интернет-поддержки.
//
// Возвращаемое значение:
//  Булево - признак того, что интернет-поддержка пользователя подключена.
//
Функция ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Экспорт 
	
	ЕстьДанныеАутентификации = 
		ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
		
	Возврат ЕстьДанныеАутентификации;
	
КонецФункции

// См. СистемаБыстрыхПлатежейСлужебный.ПриОпределенииДоступностиПодключенияПоДокументуОперации.
//
Функция ПриОпределенииДоступностиПодключенияПоДокументуОперации(
		Знач ДокументОперации) Экспорт
	
	Возврат СистемаБыстрыхПлатежейСлужебный.ПриОпределенииДоступностиПодключенияПоДокументуОперации(
		ДокументОперации);
	
КонецФункции

// См. СистемаБыстрыхПлатежейСлужебный.ПриОпределенииПараметровПодключенияДокументаОперации.
//
Функция ПриОпределенииПараметровПодключенияДокументаОперации(
		Знач ДокументОперации) Экспорт
	
	Возврат СистемаБыстрыхПлатежейСлужебный.ПриОпределенииПараметровПодключенияДокументаОперации(
		ДокументОперации);
	
КонецФункции

// Проверяет доступность настройки кассовой ссылки для указанной настройки подключения
//
// Параметры:
//   НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//    настройка выполнения операций.
//
// Возвращаемое значение:
//  Булево - признак доступности настройки кассовой ссылки.
//
Функция ДоступнаНастройкаКассовойСсылки(Знач НастройкаПодключения) Экспорт
	
	Если НастройкаПодключения = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеНастройки = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ДанныеНастройкиПодключения(
		НастройкаПодключения);
	
	Сценарии = СистемаБыстрыхПлатежейСлужебный.НовыйСценарииРаботыПодсистемы();
	СистемаБыстрыхПлатежейСобытия.ПриОпределенииДоступныхСценариев(
		ДанныеНастройки.ВариантНастройки,
		Сценарии);
	
	Возврат Сценарии.КассовыеСсылки;
	
КонецФункции

// Возвращает идентификатор участника для указанной настройки подключения.
// 
// Параметры:
//  Ссылка - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения, для которой
//    необходимо получить идентификатор участника.
//
// Возвращаемое значение:
//  Строка - идентификатор участника.
Функция ИдентификаторУчастникаПоСсылке(Знач Ссылка) Экспорт
	
	Если ТипЗнч(Ссылка) <> Тип("СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		Ссылка,
		"ИдентификаторУчастника");
	
КонецФункции

// Обновляет идентификатор участника СБП у переданной настройки.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - элемент для обновления;
//  ИдентификаторУчастника - Строка - идентификатор участника Системы быстрых платежей.
//  
Процедура ОбновитьИдентификаторУчастника(
		Знач НастройкаПодключения,
		Знач ИдентификаторУчастника) Экспорт
	
	Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ОбновитьИдентификаторУчастника(
		НастройкаПодключения,
		ИдентификаторУчастника);
	
КонецПроцедуры

#КонецОбласти
