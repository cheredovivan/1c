// @strict-types

#Область ПрограммныйИнтерфейс

#Область УстаревшиеПроцедурыИФункции

// Устарела. Определяет валюту по коду.
// 
// Параметры:
//  КодВалюты - Строка - код валюты по классификатору
//  ЗначениеПоУмолчанию - СправочникСсылка.Валюты
// 
// Возвращаемое значение:
//  - СправочникСсылка.Валюты
//  - Неопределено
//
Функция ВалютаПоКоду(КодВалюты, ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Если НЕ ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Валюты") Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	МодульРаботаСКурсамиВалют = ОбщегоНазначения.ОбщийМодуль("РаботаСКурсамиВалют");
	Валюты = МодульРаботаСКурсамиВалют.ДобавитьВалютыПоКоду(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КодВалюты));
	Если ЗначениеЗаполнено(Валюты) Тогда
		Возврат Валюты[0];
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеПоУмолчанию) Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
	ИмяСправочника = ОбщегоНазначенияБЭД.ИмяПрикладногоСправочника("Валюты");
	Если ЗначениеЗаполнено(ИмяСправочника) И Метаданные.Справочники.Найти(ИмяСправочника) <> Неопределено Тогда
		Возврат Справочники[ИмяСправочника].ПустаяСсылка();
	КонецЕсли;
	
	Возврат ЗначениеПоУмолчанию;
	
КонецФункции

#КонецОбласти

// Регистрирует все пространства имен ТЭДО
// 
// Возвращаемое значение:
//  Структура:
//  * ТоварнаяНакладнаяРБ - Строка
//  * ТоварнаяНакладнаяРБТитулПокупателя - Строка
//  * УведомлениеОбУточненииРБ - Строка
//
Функция ПространстваИмен() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ТоварнаяНакладнаяРБ", "http://topby.by/edi/blrdln/4");
	Результат.Вставить("ТоварнаяНакладнаяРБТитулПокупателя", "http://topby.by/edi/blrdnr");
	Результат.Вставить("УведомлениеОбУточненииРБ", "http://topby.by/edi/blrapn");
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область Подписи

// Заполняет данные проверки иностранной подписи.
// 
// Параметры:
//  ДанныеПодписи - см. ЭлектронныеДокументыЭДОКлиентСервер.НовыеДанныеПодписиСУчетомДоверенности
//
Процедура ЗаполнитьДанныеПодписи(ДанныеПодписи) Экспорт
	
	СвойстваПодписи = ДанныеПодписи.СвойстваПодписи; // см. ЭлектронныеДокументыЭДОСлужебный.НовыеДанныеПодписи
	
	Если СвойстваПодписи = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запись = РегистрыСведений.ЭлектронныеПодписиТЭДО.СоздатьМенеджерЗаписи();
	Запись.ПодписанныйОбъект = ДанныеПодписи.ПодписанныйОбъект;
	Запись.ХешПодписи = СвойстваПодписи.ХешПодписи;
	Запись.Прочитать();
	
	Если НЕ Запись.Выбран() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДанныеПодписи.СвойстваПодписи, Запись, "ПодписьВерна, ДатаПодписи, ДатаПроверкиПодписи");
	
КонецПроцедуры

// Возвращает проверенные иностранные подписи
// 
// Параметры:
//  ДанныеДокумента - СтрокаТаблицыЗначений: см. СинхронизацияЭДО.НовыеДанныеОбъектов
// 
// Возвращаемое значение:
//  - Неопределено,
//  - Структура:
//    * ПодписиОсновныхДанных - Массив из см. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
//    * ПодписиДополнительныхДанных - Массив из см. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
//  
Функция ПроверенныеПодписи(ДанныеДокумента) Экспорт
	
	ДанныеТЭДО = ДанныеДокумента.ДанныеТЭДО; // см. ТрансграничныйЭДО.ДанныеКонтейнера
	Если НЕ ЗначениеЗаполнено(ДанныеТЭДО.КодАльфа3Страны) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПодписиОсновныхДанных = Новый Массив(); // Массив из см. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
	МассивПодписей = ДанныеДокумента.ПодписиОсновныхДанных; // Массив из см. СинхронизацияЭДО.НовоеОписаниеДанныхОбъекта
	
	Для Каждого ПодписьОсновныхДанных Из МассивПодписей Цикл
		
		СвойстваПодписи = КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи();
		НайденаПроверкаДТС = Ложь;
		
		Для Каждого ОписаниеПодписи Из ДанныеТЭДО.Подписи Цикл
			
			Если ВРег(ОписаниеПодписи.Подпись.ИмяФайла) <> ВРег(ПодписьОсновныхДанных.ИмяФайла) Тогда
				Продолжить;
			КонецЕсли;
			
			НайденаПроверкаДТС = Истина;
			ТекстОшибки = ?(ОписаниеПодписи.Ошибка = Неопределено, "", ОписаниеПодписи.Ошибка.ТекстОшибки);
			
			СвойстваПодписи = ПроверитьПодписьТЭДО(
				ОписаниеПодписи.Подпись,
				ОписаниеПодписи.КвитанцияДТС,
				ДанныеТЭДО.КодАльфа3Страны,
				ТекстОшибки);
			Прервать;
			
		КонецЦикла;
		
		Если НЕ НайденаПроверкаДТС Тогда
			
			СвойстваПодписиТЭДО = СвойстваПодписи();
			СвойстваПодписиТЭДО.КодАльфа3Страны = ДанныеТЭДО.КодАльфа3Страны;
			СвойстваПодписи.Подпись = ПодписьОсновныхДанных.ДвоичныеДанные;
			СвойстваПодписи.ИмяФайлаПодписи = ПодписьОсновныхДанных.ИмяФайла;
			СвойстваПодписиТЭДО.ТекстОшибки = ТекстОшибкиОтсутствияПроверкиДТС();
			ДатаПодписиУниверсальная = ЭлектроннаяПодпись.ДатаПодписания(ПодписьОсновныхДанных.ДвоичныеДанные, Ложь);
			СвойстваПодписи.ДатаПодписи = МестноеВремя(ДатаПодписиУниверсальная);
			СвойстваПодписиИзДвоичныхДанных = ЭлектроннаяПодпись.СвойстваПодписи(СвойстваПодписи.Подпись);
			ЗаполнитьЗначенияСвойств(СвойстваПодписи, СвойстваПодписиИзДвоичныхДанных,
				"Сертификат, Отпечаток, КомуВыданСертификат");
			СвойстваПодписи.СвойстваПодписиТЭДО = СвойстваПодписиТЭДО;
			
		КонецЕсли;
		
		ПодписиОсновныхДанных.Добавить(СвойстваПодписи);
		
	КонецЦикла;
	
	Возврат Новый Структура("ПодписиОсновныхДанных, ПодписиДополнительныхДанных", ПодписиОсновныхДанных, Новый Массив());
	
КонецФункции

// Проверяет подписи ТЭДО
// 
// Параметры:
//  Результат - см. ЭлектронныеДокументыЭДО.НовыйРезультатПроверкиПодписей
//
Процедура ПроверитьПодписи(Результат) Экспорт
	
	ПодписиДляПроверки = Результат.ПодписиДляПроверки;
	ИндексПодписи = 0;
	ИндексыПроверенныхПодписей = Новый Массив(); // Массив из Число
	
	Для Каждого ПодписьДляПроверки Из ПодписиДляПроверки Цикл
		
		Запись = РегистрыСведений.ЭлектронныеПодписиТЭДО.СоздатьМенеджерЗаписи();
		Запись.ПодписанныйОбъект = ПодписьДляПроверки.ПрисоединенныйФайл;
		Запись.ХешПодписи = КриптографияБЭД.ХешПодписи(ПодписьДляПроверки.ДвоичныеДанныеПодписи);
		Запись.Прочитать();
		
		Если Запись.Выбран() Тогда
			
			Квитанция = РаботаСФайламиБЭД.НовоеОписаниеФайла();
			
			Если ЗначениеЗаполнено(Запись.ФайлКвитанцииДТС) Тогда
				Квитанция.ДвоичныеДанные = РаботаСФайлами.ДвоичныеДанныеФайла(Запись.ФайлКвитанцииДТС);
				Квитанция.ИмяФайла = РаботаСФайлами.ДанныеФайла(Запись.ФайлКвитанцииДТС).ИмяФайла;
			КонецЕсли;
			
			Подпись = РаботаСФайламиБЭД.НовоеОписаниеФайла();
			Подпись.ДвоичныеДанные = ПодписьДляПроверки.ДвоичныеДанныеПодписи;
			Подпись.ИмяФайла = Запись.ИмяФайлаПодписи;
			
			РезультатПроверки = ПроверитьПодписьТЭДО(Подпись, Квитанция, Запись.КодАльфа3Страны, Запись.ТекстОшибки);
			
			ПроверенныеПодписи = Результат.ПроверенныеПодписи.Получить(Запись.ПодписанныйОбъект);
			Если ПроверенныеПодписи = Неопределено Тогда
				ПроверенныеПодписи = Новый Массив();
			КонецЕсли;
			СвойстваПодписи = ЭлектроннаяПодписьКлиентСервер.НовыеСвойстваПодписи();
			
			ПереченьПолей =
				"Подпись,
				|Сертификат,
				|Отпечаток,
				|ИмяФайлаПодписи,
				|ДатаПодписи,
				|ДатаПроверкиПодписи,
				|ПодписьВерна,
				|КомуВыданСертификат";
			
			ЗаполнитьЗначенияСвойств(СвойстваПодписи, РезультатПроверки, СтрЗаменить(ПереченьПолей, Символы.ПС, ""));
			СвойстваПодписи.ПодписанныйОбъект = Запись.ПодписанныйОбъект;
			ПроверенныеПодписи.Добавить(СвойстваПодписи);
			Результат.ПроверенныеПодписи.Вставить(Запись.ПодписанныйОбъект, ПроверенныеПодписи);
			ИндексыПроверенныхПодписей.Добавить(ИндексПодписи);
			
			ЗаполнитьЗначенияСвойств(Запись, РезультатПроверки.СвойстваПодписиТЭДО,
				"ПодписьПроверена, ПодписьВерна, ТекстОшибки, ДатаПроверкиПодписи, ДатаПодписи, ТекстОшибки");
			УстановитьПривилегированныйРежим(Истина);
			Запись.Записать();
			УстановитьПривилегированныйРежим(Ложь);
			
		КонецЕсли;
		
		Пока ИндексыПроверенныхПодписей.Количество() > 0 Цикл
			
			Индекс = ИндексыПроверенныхПодписей.ВГраница();
			Результат.ПодписиДляПроверки.Удалить(ИндексыПроверенныхПодписей[Индекс]);
			ИндексыПроверенныхПодписей.Удалить(Индекс);
			
		КонецЦикла;
		
		ИндексПодписи = ИндексПодписи + 1;
		
	КонецЦикла;
	
КонецПроцедуры

// Инициирует свойства иностранной подписи.
// 
// Возвращаемое значение:
//  Структура:
// * КодАльфа3Страны - Строка
// * КвитанцияДТС - Неопределено
//                - см. РаботаСФайламиБЭД.НовоеОписаниеФайла
// * ПодписьПроверена - Булево
// * ДатаПроверкиПодписи - Дата
// * ДатаПодписи - Дата
// * ПодписьВерна - Булево
// * ИмяФайлаПодписи - Строка
// * ТекстОшибки - Строка
//  
Функция СвойстваПодписи() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("КодАльфа3Страны", "");
	Результат.Вставить("КвитанцияДТС", Неопределено);
	Результат.Вставить("ПодписьПроверена", Ложь);
	Результат.Вставить("ДатаПроверкиПодписи", '00010101');
	Результат.Вставить("ДатаПодписи", '00010101');
	Результат.Вставить("ПодписьВерна", Ложь);
	Результат.Вставить("ИмяФайлаПодписи", "");
	Результат.Вставить("ТекстОшибки", "");
	Возврат Результат;
	
КонецФункции

// Регистрирует иностранную ответную подпись.
// 
// Параметры:
//  ПодписанныйОбъект - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
//  СвойстваПодписи - см. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
//
Процедура ДобавитьПодпись(ПодписанныйОбъект, СвойстваПодписи) Экспорт
	
	Запись = РегистрыСведений.ЭлектронныеПодписиТЭДО.СоздатьМенеджерЗаписи();
	Запись.ПодписанныйОбъект = ПодписанныйОбъект;
	Запись.ХешПодписи = КриптографияБЭД.ХешПодписи(СвойстваПодписи.Подпись);
	Запись.ИмяФайлаПодписи = СвойстваПодписи.ИмяФайлаПодписи;
	СвойстваПодписиТЭДО = СвойстваПодписи.СвойстваПодписиТЭДО; // см. ТрансграничныйЭДО.СвойстваПодписи
	
	Если СвойстваПодписиТЭДО = Неопределено Тогда
		
		Запись.ТекстОшибки = ТекстОшибкиОтсутствияПроверкиДТС();
		
	Иначе
		
		Запись.КодАльфа3Страны = СвойстваПодписиТЭДО.КодАльфа3Страны;
		Запись.ТекстОшибки = СвойстваПодписиТЭДО.ТекстОшибки;
		КвитанцияДТС = СвойстваПодписиТЭДО.КвитанцияДТС; // см. РаботаСФайламиБЭД.НовоеОписаниеФайла
		
		Если КвитанцияДТС <> Неопределено Тогда
			
			ПараметрыДобавленияФайла = РаботаСФайлами.ПараметрыДобавленияФайла();
			Владелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодписанныйОбъект, "ВладелецФайла");
			ПараметрыДобавленияФайла.ВладелецФайлов = Владелец;
			Файл = Новый Файл(КвитанцияДТС.ИмяФайла);
			ПараметрыДобавленияФайла.ИмяБезРасширения = Файл.ИмяБезРасширения;
			ПараметрыДобавленияФайла.РасширениеБезТочки = СтрЗаменить(Файл.Расширение, ".", "");
			Адрес = ПоместитьВоВременноеХранилище(КвитанцияДТС.ДвоичныеДанные);
			Ссылка = Справочники.КвитанцииДТСПрисоединенныеФайлы.ПолучитьСсылку();
			Запись.ФайлКвитанцииДТС = РаботаСФайлами.ДобавитьФайл(ПараметрыДобавленияФайла, Адрес, , , Ссылка);
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Запись, СвойстваПодписиТЭДО,
			"ПодписьПроверена, ПодписьВерна, ТекстОшибки, ДатаПодписи, ДатаПроверкиПодписи, КодАльфа3Страны");
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Запись.Записать();
	
КонецПроцедуры

// Регистрирует иностранные подписи сообщения
// 
// Параметры:
//  СообщениеОбъект - ДокументОбъект.СообщениеЭДО
//  ПараметрыЗагрузки - см. ЭлектронныеДокументыЭДОСлужебный.ПараметрыЗагрузкиСообщения
//  ДанныеОбъекта - СтрокаТаблицыЗначений: см. СинхронизацияЭДО.НовыеДанныеОбъектов
//
Процедура ДобавитьПодписиПриЗагрузкеСообщения(СообщениеОбъект, ПараметрыЗагрузки, ДанныеОбъекта) Экспорт
	
	ДанныеТЭДО = ДанныеОбъекта.ДанныеТЭДО;
	Если ДанныеТЭДО = Неопределено ИЛИ НЕ ЗначениеЗаполнено(ПараметрыЗагрузки.ПодписиОсновныхДанных) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СвойстваПодписи Из ПараметрыЗагрузки.ПодписиОсновныхДанных Цикл
		ДобавитьПодпись(СообщениеОбъект.ОсновнойФайл, СвойстваПодписи);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Определяет ограничение максимальной длины номера ЭД.
// 
// Возвращаемое значение:
//  Число
//
Функция МаксимальнаяДлинаНомераЭД() Экспорт
	Возврат 11;
КонецФункции

// Проверяет запрет аннулирования ЭД
// 
// Параметры:
//  ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
// 
// Возвращаемое значение:
//  Структура:
//   * АннулированиеЗапрещено - Булево
//   * ТекстОшибки - Строка
//
Функция ПроверитьЗапретАннулирования(ЭлектронныйДокумент) Экспорт
	
	Результат = Новый Структура("АннулированиеЗапрещено, ТекстОшибки", Ложь, "");
	
	Если ТипЗнч(ЭлектронныйДокумент) <> Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО") Тогда
		Возврат Результат;
	КонецЕсли;
	
	Формат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭлектронныйДокумент, "ФорматОсновногоТитула");
	Если ЭтоИностранныйФормат(Формат) Тогда
		Результат.АннулированиеЗапрещено = ЭтоИностранныйФормат(Формат);
		Результат.ТекстОшибки = НСтр("ru = 'Аннулирование иностранного документа не предусмотрено.';
									|en = 'Cannot cancel a foreign document.'");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Обрабатывает карточку иностранного отправителя
// 
// Параметры:
//  ОписаниеКонтейнера - см. ТранспортныеКонтейнерыЭДО.РаспаковатьФайлКонтейнера
//
Процедура ДополнитьОписаниеКонтейнера(ОписаниеКонтейнера) Экспорт
	
	Карточка = ОписаниеКонтейнера.Карточка;
	
	Если НЕ ЗначениеЗаполнено(Карточка)
		ИЛИ ЗначениеЗаполнено(Карточка.ИдентификаторСообщения)
		ИЛИ НЕ ЭтоИностранныйОператор(Карточка.ИдентификаторОтправителя) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСообщения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		Карточка.ДополнительныеПараметры, "LinkedDocument", "");
	Карточка.ИдентификаторСообщения = Строка(ИдентификаторСообщения);
	
	Если НЕ ЗначениеЗаполнено(Карточка.ИдентификаторСообщения)
		И ЗначениеЗаполнено(ОписаниеКонтейнера.Метаданные.Документы) Тогда
		Карточка.ИдентификаторСообщения = ОписаниеКонтейнера.Метаданные.Документы[0].ИдентификаторДокументооборота;
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при формировании таблицы типов элементов регламента
// 
// Параметры:
//  Таблица - ТаблицаЗначений:
//   * КодТранзакции - Строка
//   * ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//   * ТипДокумента - Строка
//
Процедура ПриФормированииТаблицыТиповЭлементовРегламента(Таблица) Экспорт
	
	Для Каждого Обработчик Из Обработчики() Цикл
		Обработчик.ПриФормированииТаблицыТиповЭлементовРегламента(Таблица);
	КонецЦикла;
	
КонецПроцедуры

// Формирует данные служебной квитанции.
//
// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
//  ДанныеЭД - см. ФорматыЭДО.НовыеДанныеДляФормированияСлужебногоСообщения
//
// Возвращаемое значение:
// - Неопределено
// - см. ФорматыЭДО.НовыйРезультатФормированияДокументаПоУчету
//
Функция ДанныеСлужебногоСообщения(ТипДокумента, ДанныеЭД) Экспорт
	
	ИмяФайла = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЭД.Основание, "ИмяБезРасширения", ""); // Строка
	Если НЕ ЭтоИностранныйФормат(ИмяФайла) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого Обработчик Из Обработчики() Цикл
		Результат = Обработчик.ДанныеСлужебногоСообщения(ТипДокумента, ДанныеЭД);
		Если ЗначениеЗаполнено(Результат) Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Проверяет доступность отклонения.
// 
// Параметры:
//  ЭлементыРегламента - ТаблицаЗначений:
//   * Дата - Дата
//   * Направление - ПеречислениеСсылка.НаправленияЭДО
//   * Состояние - ПеречислениеСсылка.СостоянияСообщенийЭДО
//   * Ссылка - ДокументСсылка.СообщениеЭДО
//   * Статус - ПеречислениеСсылка.СтатусыСообщенийЭДО
//   * ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//  ЭтоВходящийЭДО - Булево
//  ЕстьАктивноеАннулирование - Булево
//
// Возвращаемое значение:
//  Булево -
//
Функция ОтклонениеДоступно(ЭлементыРегламента, ЭтоВходящийЭДО, ЕстьАктивноеАннулирование) Экспорт
	
	Если НЕ ЭтоВходящийЭДО ИЛИ ЕстьАктивноеАннулирование Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяФайла = "";
	
	Для Каждого ЭлементРегламента Из ЭлементыРегламента Цикл
		
		Если ЭлементРегламента.ТипЭлементаРегламента <> Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя Тогда
			Продолжить;
		КонецЕсли;
		
		Сообщение = ЭлементРегламента.Ссылка;
		Если НЕ ЗначениеЗаполнено(Сообщение) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		ИмяФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сообщение, "ОсновнойФайл.ПолноеИмяФайла");
		
	КонецЦикла;
	
	Для Каждого Обработчик Из Обработчики() Цикл
		ОтклонениеДоступно = Обработчик.ОтклонениеДоступно(ИмяФайла);
		Если ОтклонениеДоступно Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Проверяет иностранного оператора
// 
// Параметры:
//  Идентификатор - Строка
// 
// Возвращаемое значение:
//  Булево -
//
Функция ЭтоИностранныйОператор(Идентификатор) Экспорт
	
	Для Каждого Обработчик Из Обработчики() Цикл
		
		Для Каждого Оператор Из Обработчик.Операторы() Цикл
			Если СтрНачинаетсяС(ВРег(Идентификатор), Оператор.Префикс) Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Ищет GLN контрагента
// 
// Параметры:
//  Идентификатор - Строка
// 
// Возвращаемое значение:
//  Строка -
//
Функция ПолучитьGLN(Идентификатор) Экспорт
	
	Для Каждого Обработчик Из Обработчики() Цикл
		
		GLN = Обработчик.ПолучитьGLN(Идентификатор);
		
		Если ЗначениеЗаполнено(GLN) Тогда
			Возврат GLN;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

// Возвращает наименование страны по коду ОКСМ
// 
// Параметры:
//  КодСтраны - Строка
// 
// Возвращаемое значение:
//  Строка
//
Функция НаименованиеСтраныПоКоду(КодСтраны) Экспорт
	
	Если НЕ ЗначениеЗаполнено(КодСтраны) Тогда
		Возврат "";
	КонецЕсли;
	
	ДанныеСтраны = УправлениеКонтактнойИнформацией.ДанныеСтраныМира(КодСтраны);
	
	Если ДанныеСтраны = Неопределено Тогда
		ДанныеСтраны = УправлениеКонтактнойИнформацией.ДанныеКлассификатораСтранМираПоКоду(КодСтраны);
	КонецЕсли;
	
	Если ДанныеСтраны = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат ДанныеСтраны.НаименованиеПолное;
	
КонецФункции

// Возвращает код страны участника ТЭДО.
// 
// Параметры:
//  ИдентификаторУчастника - Строка
// 
// Возвращаемое значение:
//  Строка - код страны по ОКСМ
//  
Функция КодАльфа3СтраныУчастника(ИдентификаторУчастника) Экспорт
	
	Для Каждого Обработчик Из Обработчики() Цикл
		КодСтраны = Обработчик.КодАльфа3СтраныУчастника(ИдентификаторУчастника);
		Если ЗначениеЗаполнено(КодСтраны) Тогда
			Возврат КодСтраны;
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

// Возвращает код страны участника ТЭДО.
// 
// Параметры:
//  ИдентификаторУчастника - Строка
// 
// Возвращаемое значение:
//  Строка - числовой код страны по ОКСМ.
//
Функция КодСтраныАбонента(ИдентификаторУчастника) Экспорт
	
	Для Каждого Обработчик Из Обработчики() Цикл
		КодСтраны = Обработчик.КодСтраныАбонента(ИдентификаторУчастника);
		Если ЗначениеЗаполнено(КодСтраны) Тогда
			Возврат КодСтраны;
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

// Возвращает код транзакции транспортной информации.
// 
// Параметры:
//  ОписаниеОбъекта - СтрокаТаблицыЗначений: см. СинхронизацияЭДО.НовыеДанныеОбъектов
// 
// Возвращаемое значение:
//  Строка
//
Функция КодТранзакции(ОписаниеОбъекта) Экспорт
	
	Для Каждого Обработчик Из Обработчики() Цикл
		Результат = Обработчик.КодТранзакции(ОписаниеОбъекта);
		Если ЗначениеЗаполнено(Результат) Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
КонецФункции

// Возвращает тип документа для помещения в транспортный контейнер.
//
// Параметры:
//  ОписаниеОбъекта - СтрокаТаблицыЗначений: см. СинхронизацияЭДО.НовыеДанныеОбъектов
//
// Возвращаемое значение:
//  Строка
//
Функция ТипДокументаСтрокой(ОписаниеОбъекта) Экспорт
	
	Для Каждого Обработчик Из Обработчики() Цикл
		Результат = Обработчик.ТипДокументаСтрокой(ОписаниеОбъекта);
		Если ЗначениеЗаполнено(Результат) Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

// Описывает данные ЭД.
// 
// Параметры:
//  ПараметрыПолученияДанных - см. ФорматыЭДО.НовыеПараметрыПолученияДанныхДокумента
// 
// Возвращаемое значение:
//  - Неопределено
//  - Структура:
//    * ОбъектXDTO - Неопределено, ОбъектXDTO -
//    * ПространствоИмен - Строка - см. ПространстваИмен
//    * Сопоставление - Соответствие из КлючИЗначение - :
//      ** Ключ - Число - Номер строки документа.
//      ** Значение - см. ФорматыЭДО.НовыеДанныеДляСопоставленияТоваров
//    * Номенклатура - Соответствие из КлючИЗначение - Сопоставленная номенклатура:
//      ** Ключ - Число - Номер строки документа.
//      ** Значение - Структура - Сопоставленная номенклатура:
//        *** Номенклатура - Неопределено, ОпределяемыйТип.НоменклатураБЭД -
//        *** Характеристика - Неопределено, ОпределяемыйТип.ХарактеристикаНоменклатурыБЭД -
//        *** Упаковка - Неопределено, ОпределяемыйТип.УпаковкаНоменклатурыБЭД -
//
Функция ДанныеЭД(ПараметрыПолученияДанных) Экспорт
	
	ОсновнойФайл = ПараметрыПолученияДанных.ОсновнойФайл;
	ОписаниеФайла = РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла();
	Файл = Новый Файл(ОсновнойФайл.ИмяФайла);
	ОписаниеФайла.ИмяФайла = Файл.Имя;
	ОписаниеФайла.ДвоичныеДанные = ОсновнойФайл.ДвоичныеДанные;
	
	Если НЕ ЭтоФайлДокумента(ОписаниеФайла.ИмяФайла) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого Обработчик Из Обработчики() Цикл
		
		ДанныеЭД = Обработчик.ДанныеЭД(ОписаниеФайла);
		Если ДанныеЭД <> Неопределено Тогда
			Возврат ДанныеЭД;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Извлекает товары из файла.
// 
// Параметры:
//  ОписаниеФайла - см. РаботаСФайламиБЭД.НовоеОписаниеФайла
// 
// Возвращаемое значение:
//  - Неопределено
//  - см. ФорматыЭДО.НоваяТаблицаТоваров
//
Функция ТоварыИзФайла(ОписаниеФайла) Экспорт
	
	Если НЕ ЭтоФайлДокумента(ОписаниеФайла.ИмяФайла) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого Обработчик Из Обработчики() Цикл
		Если ЗначениеЗаполнено(Обработчик.ОпределитьФорматЭД(ОписаниеФайла.ИмяФайла)) Тогда
			Возврат Обработчик.ТоварыИзФайла(ОписаниеФайла);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Дополняет коллекцию поддерживаемых форматов.
// 
// Параметры:
//  Форматы - Структура
//
Процедура ДополнитьФорматы(Форматы) Экспорт
	
	Форматы.Вставить("ТЭДО", Новый Структура());
	
	Для Каждого Обработчик Из Обработчики() Цикл
		
		ПоддерживаемыеФорматы = Новый Структура();
		
		Для Каждого Формат Из Обработчик.ПоддерживаемыеФорматы() Цикл
			ПоддерживаемыеФорматы.Вставить(Формат.Ключ, Формат.Значение);
		КонецЦикла;
		
		Форматы.ТЭДО.Вставить(Обработчик.КодАльфа3Страны(), ПоддерживаемыеФорматы);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ДанныеДляФормирования см. ФорматыЭДО.НовыеДанныеДляФормированияОтветногоТитула
//
Процедура ДополнитьДанныеДляФормированияОтветногоТитула(ДанныеДляФормирования) Экспорт
	
	ДанныеТЭДО = ДанныеДляФормирования.ДанныеТЭДО;
	Если ДанныеТЭДО = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ДанныеТЭДО.Основание = ДанныеДляФормирования.Основание;
	ДанныеТЭДО.ИдентификаторОтправителя = ДанныеДляФормирования.Участники.ИдентификаторОтправителя;
	ДанныеТЭДО.ИдентификаторПолучателя = ДанныеДляФормирования.Участники.ИдентификаторПолучателя;
	
КонецПроцедуры

// Формирует электронный документ.
//
// Параметры:
//  Формат - Строка
//  Данные - см. ДанныеОбъектаУчета
//
// Возвращаемое значение:
//  см. ФорматыЭДО.НовыйРезультатФормированияДокумента
//
Функция СформироватьДокумент(Формат, Данные) Экспорт
	
	Результат = ФорматыЭДО.НовыйРезультатФормированияДокумента();
	
	Для Каждого Обработчик Из Обработчики() Цикл
		
		Результат = Обработчик.СформироватьДокумент(Формат, Данные);
		Если Результат <> Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

//  Выполняет форматную проверку заполнения данных.
// 
// Параметры:
//  Формат - Строка
//  Данные - Неопределено
//         - см. ТрансграничныйЭДО.ДанныеОбъектаУчета
// 
// Возвращаемое значение:
//  см. ФорматыЭДО_ФНС.НовыйРезультатПроверкиЗаполненияДанных
//
Функция ПроверитьЗаполнениеДанных(Формат, Данные) Экспорт
	
	Результат =  ФорматыЭДО_ФНС.НовыйРезультатПроверкиЗаполненияДанных();
	Если Данные = Неопределено ИЛИ Данные["ДанныеXDTO"] = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	ФорматЭД = Неопределено; // см. ОписаниеФорматаЭД
	
	Для Каждого Обработчик Из Обработчики() Цикл
		
		ФорматЭД = Обработчик.ФорматЭД(Формат);
		
		Если ФорматЭД <> Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ФорматЭД = Неопределено Тогда
		
		ШаблонОшибки = НСтр("ru = 'Формат иностранного документа не идентифицирован: %1';
							|en = 'The format of the foreign document is not identified: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Формат);
		ОбщегоНазначенияБЭД.ДобавитьОшибку(Результат.Ошибки, ТекстОшибки);
		Возврат Результат;
		
	КонецЕсли;
	
	Попытка
		
		ДвоичныеДанные = Обработчик.ДвоичныеДанныеОбъектаXDTO(Данные["ДанныеXDTO"], , , ФорматЭД.ТипДанных);
		ДвоичныеДанные = Обработчик.УдалитьПространствоИмен(ДвоичныеДанные, ФорматЭД.ТипДанных);
		ДвоичныеДанные = ОбщегоНазначенияБЭД.ДобавитьПространствоИмен(
			ДвоичныеДанные, ФорматЭД.ПространствоИмен, Обработчик.Кодировка());
		ТипДанных = РаботаСФайламиБЭД.ПолучитьТипЗначенияCML(ФорматЭД.ТипДанных, ФорматЭД.ПространствоИмен);
		РаботаСФайламиБЭД.ПрочитатьXDTO(ДвоичныеДанные.ОткрытьПотокДляЧтения(), ТипДанных);
		
	Исключение
		
		ШаблонОперации = НСтр("ru = 'Проверка заполнения данных. Код формата: %1';
								|en = 'Data filling check. Format code: %1'");
		Операция = СтрШаблон(ШаблонОперации, ФорматЭД.Код);
		ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияБЭД.ДобавитьОшибку(Результат.Ошибки, ТекстОшибки);
		ТекстОшибкиДляЖурнала = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработкаНеисправностейБЭД.ОбработатьОшибку(Операция,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ТЭДО, ТекстОшибкиДляЖурнала, ТекстОшибки);
			
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Готовит данные для отражения в учете.
// 
// Параметры:
//  ДанныеЭД - см. ДанныеЭД
//  Отправитель - ОпределяемыйТип.УчастникЭДО
// 
Процедура ПодготовитьКОтражениюВУчете(ДанныеЭД, Отправитель) Экспорт
	ЗаполнитьСопоставленнуюНоменклатуру(ДанныеЭД, Отправитель);
КонецПроцедуры

// Инициирует параметры заполнения пакета по объекту учета.
// 
// Возвращаемое значение:
//  Структура:
//  * ПространствоИмен - Строка
//  * ТипДанных - Строка
//
Функция НовыеПараметрыЗаполненияПакетаПоОбъектуУчета() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ПространствоИмен", "");
	Результат.Вставить("ТипДанных", "");
	Возврат Результат;
	
КонецФункции

// см. ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеПакетаПоОбъектуУчета
Процедура ЗаполнитьДанныеПакетаПоОбъектуУчета(Данные, ОбъектУчета, Параметры, Отказ) Экспорт
	
	ОбменСКонтрагентамиПереопределяемый.ЗаполнитьДанныеПакетаПоОбъектуУчета(
		Данные, ОбъектУчета, Параметры, Отказ);
	
КонецПроцедуры

// Описывает данные объекта учета для формирования ЭД
// 
// Возвращаемое значение:
//  - Неопределено,
//  - Структура:
//    * Основание - см. РаботаСФайламиБЭД.НовоеОписаниеФайла
//    * ДанныеXDTO - ОбъектXDTO, Неопределено -
//    * ДанныеРучногоФормированияТитула - Произвольный, Неопределено -
//    * ИдентификаторОтправителя - Строка
//    * ИдентификаторПолучателя - Строка
//
Функция ДанныеОбъектаУчета() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Основание", РаботаСФайламиБЭД.НовоеОписаниеФайла());
	Результат.Вставить("ДанныеXDTO", Неопределено);
	Результат.Вставить("ДанныеРучногоФормированияТитула", Неопределено);
	Результат.Вставить("ИдентификаторОтправителя", "");
	Результат.Вставить("ИдентификаторПолучателя", "");
	Возврат Результат;
	
КонецФункции

// Определяет форму ручного формирования ответного титула.
// 
// Параметры:
//  АдресСведенийОбОшибках - Строка
// 
// Возвращаемое значение:
//  Неопределено, Строка -
//
Функция ФормаРучногоФормированияОтветногоТитула(АдресСведенийОбОшибках) Экспорт
	
	ОшибкиПриФормированииДокументов = ПолучитьИзВременногоХранилища(
		АдресСведенийОбОшибках); // Массив из см. ЭлектронныеДокументыЭДО.НовоеОписаниеОшибкиФормирования
	ЭлектронныйДокумент = ОшибкиПриФормированииДокументов[0].ОшибкаФормированияВПрикладнойЧасти.ЭлектронныйДокумент;
	ИнформацияОтправителя = ЭлектронныеДокументыЭДО.ОсновнойФайлИнформацииОтправителя(ЭлектронныйДокумент);
	ПолноеИмяФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ИнформацияОтправителя, "ПолноеИмяФайла");
	
	Для Каждого Обработчик Из Обработчики() Цикл
		Результат = Обработчик.ФормаРучногоФормированияОтветногоТитула(ПолноеИмяФайла);
		Если Результат <> Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает описание данных объекта учета.
// 
// Параметры:
//  ДанныеУчета - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//              - Массив из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//              - Массив из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//              - Неопределено
//  Параметры - см. ИнтеграцияЭДО.НовыеПараметрыФормированияДанныхОбъектаУчета
// 
// Возвращаемое значение:
//  см. ИнтеграцияЭДО.НовыйРезультатФормированияДанныхОбъектаУчета
//
Функция ОписаниеДанныхОбъектаУчета(ДанныеУчета, Параметры) Экспорт
	
	Для Каждого Обработчик Из Обработчики() Цикл
		Результат = Обработчик.ОписаниеДанныхОбъектаУчета(ДанныеУчета, Параметры);
		Если Результат <> Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИнтеграцияЭДО.НовыйРезультатФормированияДанныхОбъектаУчета();
	
КонецФункции

// Дополняет соответствие титулов ЭД
// 
// Параметры:
//  СоответствиеТитулов - Соответствие из КлючИЗначение:
//  * Ключ - Строка
//  * Значение - Строка
//  
Процедура ДополнитьСоответствиеТитулов(СоответствиеТитулов) Экспорт
	
	Для Каждого Обработчик Из Обработчики() Цикл
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(СоответствиеТитулов, Обработчик.СоответствиеТитулов());
	КонецЦикла;
	
КонецПроцедуры

// см. ФорматыЭДО.ПроверитьДокумент
Функция ПроверитьДокумент(ОписаниеФайла) Экспорт
	Возврат ФорматыЭДО.НовыйРезультатПроверки();
КонецФункции

// Формирует представление ЭД.
// 
// Параметры:
//  ИмяФайла - Строка
//  ДвоичныеДанныеФайла - ДвоичныеДанные
// 
// Возвращаемое значение:
//  Структура:
//  * ПредставлениеДокумента - Неопределено, ТабличныйДокумент -
//  * Успех - Булево
//
Функция ПредставлениеДанных(ИмяФайла, ДвоичныеДанныеФайла) Экспорт
	
	Результат = Новый Структура("ПредставлениеДокумента, Успех", Неопределено, Ложь);
	
	Если НЕ ЭтоФайлДокумента(ИмяФайла) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого Обработчик Из Обработчики() Цикл
		
		ПредставлениеДокумента = Обработчик.ПредставлениеДанных(ИмяФайла, ДвоичныеДанныеФайла);
		
		Если ПредставлениеДокумента <> Неопределено Тогда
			
			Результат.ПредставлениеДокумента = ПредставлениеДокумента;
			Результат.Успех = Истина;
			Возврат Результат;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Получает информацию по данным электронного документа.
// 
// Параметры:
//  Сообщение - ДокументСсылка.СообщениеЭДО
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  - Неопределено,
//  - см. НовыеПараметрыДокумента
//  
Функция РаспознатьСообщение(Сообщение, ДвоичныеДанные) Экспорт
	
	ПолноеИмяФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сообщение, "ОсновнойФайл.ПолноеИмяФайла");
	Если НЕ ЭтоФайлДокумента(ПолноеИмяФайла) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого Обработчик Из Обработчики() Цикл
		Результат = Обработчик.РаспознатьСообщение(ПолноеИмяФайла, ДвоичныеДанные);
		Если Результат <> Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Инициирует параметры документа.
// 
// Возвращаемое значение:
//  Структура:
// * ВариантЗаполнения - Строка
// * ДатаДокумента - Дата
// * ИсходныйФормат - Строка
// * НомерДокумента - Строка
// * ОтветныйФормат - Строка
// * Отправитель - см. НовоеОписаниеУчастника
// * ОтражениеВУчете - Структура - :
// ** ВариантЗаполнения - Строка
// ** Формат - Строка
// * ПараметрыПредставления - см. НовыеПараметрыПредставленияСуммы
// * Получатель - см. НовоеОписаниеУчастника
// * СуммаДокумента - Число
// * ТипДокумента - Строка
// * ТипРегламента - Строка
//
Функция НовыеПараметрыДокумента() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ТипДокумента", "");
	Результат.Вставить("ВариантЗаполнения", "");
	Результат.Вставить("ДатаДокумента", '00010101');
	Результат.Вставить("ИсходныйФормат", "");
	Результат.Вставить("НомерДокумента", "");
	Результат.Вставить("ОтветныйФормат", "");
	Результат.Вставить("Отправитель", НовоеОписаниеУчастника());
	Результат.Вставить("ОтражениеВУчете", Новый Структура("ВариантЗаполнения, Формат", "", ""));
	Результат.Вставить("ПараметрыПредставления", НовыеПараметрыПредставленияСуммы());
	Результат.Вставить("Получатель", НовоеОписаниеУчастника());
	Результат.Вставить("СуммаДокумента", 0);
	Результат.Вставить("ТипРегламента", "");
	Возврат Результат;
	
КонецФункции

// Возвращает содержание документа
//
// Параметры:
//  ОписаниеФайла - см. РаботаСФайламиБЭД.НовоеОписаниеФайла
//
// Возвращаемое значение:
//  - Неопределено
//  - см. ФорматыЭДО.НовоеОписаниеФайлаДокумента
//
Функция ПрочитатьСодержаниеДокумента(ОписаниеФайла) Экспорт
	
	Описание = РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла();
	Файл = Новый Файл(ОписаниеФайла.ИмяФайла);
	Описание.ИмяФайла = Файл.Имя;
	Описание.ДвоичныеДанные = ОписаниеФайла.ДвоичныеДанные;
	
	Если НЕ ЭтоФайлДокумента(Описание.ИмяФайла) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого Обработчик Из Обработчики() Цикл
		Результат = Обработчик.ПрочитатьСодержаниеДокумента(Описание);
		Если Результат <> Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Описывает участника ТЭДО.
// 
// Возвращаемое значение:
//  Структура:
// * НалоговыйНомер - Строка
// * GLN - Строка
// * КодАльфа3Страны - Строка
// 
Функция ОписаниеУчастника() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("НалоговыйНомер", "");
	Результат.Вставить("GLN", "");
	Результат.Вставить("КодАльфа3Страны", "");
	Возврат Результат;
	
КонецФункции

// Описывает формат ЭД
// 
// Возвращаемое значение:
//  Структура:
// * Ключ - Строка
// * Код - Строка
// * Наименование - Строка
// * ПространствоИмен - Строка
// * ТипДанных - Строка
// * Макет - Строка
// 
Функция ОписаниеФорматаЭД() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Ключ", "");
	Результат.Вставить("Код", "");
	Результат.Вставить("Наименование", "");
	Результат.Вставить("ПространствоИмен", "");
	Результат.Вставить("ТипДанных", "");
	Результат.Вставить("Макет", "");
	Возврат Результат;
	
КонецФункции

// Описывает оператора ТЭДО.
// 
// Возвращаемое значение:
//  Структура:
// * КодАльфа3Страны - Строка 
// * Префикс - Строка
// * Наименование - Строка
// 
Функция ОписаниеОператора() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("КодАльфа3Страны", "");
	Результат.Вставить("Префикс", "");
	Результат.Вставить("Наименование", "");
	Возврат Результат;
	
КонецФункции

// Возвращает данные объектов ТЭДО
//
// Параметры:
//  РаспакованныеФайлы - см. РаботаСФайламиБЭД.РаспаковатьАрхив
//  ОписаниеКонтейнера - см. ТранспортныеКонтейнерыЭДО.РаспаковатьФайлКонтейнера
// 
// Возвращаемое значение:
//  - Неопределено - если это не трансграничный обмен
//  - Структура:
//    * КодАльфа3Страны - Строка
//    * Подписи - Массив из см. ОписаниеПодписи
// 
Функция ДанныеКонтейнера(РаспакованныеФайлы, ОписаниеКонтейнера) Экспорт
	
	Карточка = ОписаниеКонтейнера.Карточка;
	Если РаспакованныеФайлы = Неопределено ИЛИ Карточка = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый Структура();
	КодАльфа3Страны = КодАльфа3СтраныУчастника(Карточка.ИдентификаторОтправителя);
	Если НЕ ЗначениеЗаполнено(КодАльфа3Страны) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат.Вставить("КодАльфа3Страны", КодАльфа3Страны);
	КарточкаДТС = Неопределено;
	
	Для Каждого РаспакованныйФайл Из РаспакованныеФайлы Цикл
		
		Если НРег(РаспакованныйФайл.Имя) = "dts.xml" Тогда
			
			ДвоичныеДанныеКарточки = ?(ТипЗнч(РаспакованныйФайл) = Тип("Структура"),
				РаспакованныйФайл.ДвоичныеДанные, Новый ДвоичныеДанные(РаспакованныйФайл.ПолноеИмя));
			КарточкаДТС = ПрочитатьКарточкуДТС(ДвоичныеДанныеКарточки);
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Подписи = Новый Массив(); // Массив из Структура
	Результат.Вставить("Подписи", Подписи);
	
	// Если отсутствует карточка ДТС, то считаем что иностранных подписей - нет
	Если КарточкаДТС = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	КарточкиПодписей = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(
		КарточкаДТС, "TransportDepartmentCard", , , Истина); // СписокXDTO
	
	Для Каждого Карточка Из КарточкиПодписей Цикл
		
		ОписаниеПодписи = ОписаниеПодписи();
		Подпись = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Карточка, "ToSign.DocSign"); // ОбъектXDTO
		ОписаниеПодписи.Подпись.ИмяФайла = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Подпись, "file"); // Строка
		НайтиДвоичныеДанныеФайла(ОписаниеПодписи.Подпись, РаспакованныеФайлы);
		ОписаниеПодписи.ХешПодписи = КриптографияБЭД.ХешПодписи(ОписаниеПодписи.Подпись.ДвоичныеДанные);
		Ошибка = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Карточка, "Description.Error"); // ОбъектXDTO
		
		Если Ошибка <> Неопределено Тогда
			
			ТекстОшибки = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Ошибка, "Text"); // Строка
			КодОшибки = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Ошибка, "Code"); // Строка
			ОписаниеПодписи.Ошибка = Новый Структура("ТекстОшибки, КодОшибки", ТекстОшибки, КодОшибки);
			
		Иначе
			
			КвитанцияДТС = РаботаСФайламиБЭД.НовоеОписаниеФайла();
			Квитанция = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Карточка, "Description.File"); // ОбъектXDTO
			
			Если Квитанция <> Неопределено Тогда
				КвитанцияДТС.ИмяФайла = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Квитанция, "Path"); // Строка
				НайтиДвоичныеДанныеФайла(КвитанцияДТС, РаспакованныеФайлы);
				ОписаниеПодписи.КвитанцияДТС = КвитанцияДТС;
			КонецЕсли;
			
			ОписаниеПодписи.КвитанцияДТС = КвитанцияДТС;
			
		КонецЕсли;
		
		Подписи.Добавить(ОписаниеПодписи);
		
	КонецЦикла;
	
	Результат.Вставить("Подписи", Подписи);
	Возврат Результат;
	
КонецФункции

// Идентифицирует иностранную подпись
// 
// Параметры:
//  ПодписанныйОбъект - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
//  ХешПодписи - Строка
// 
// Возвращаемое значение:
//  Булево
//
Функция ЭтоИностраннаяПодпись(ПодписанныйОбъект, ХешПодписи) Экспорт
	
	Запись = РегистрыСведений.ЭлектронныеПодписиТЭДО.СоздатьМенеджерЗаписи();
	Запись.ПодписанныйОбъект = ПодписанныйОбъект;
	Запись.ХешПодписи = ХешПодписи;
	Запись.Прочитать();
	Возврат Запись.Выбран();
	
КонецФункции

// Проверяет иностранный формат сообщения
// 
// Параметры:
//  КодФормата - Строка
// 
// Возвращаемое значение:
//  Булево -
// 
Функция ЭтоИностранныйФормат(КодФормата) Экспорт
	Возврат ЭтоИностранныйФайл(КодФормата);
КонецФункции

// Проверяет иностранный файл сообщения
// 
// Параметры:
//  ИмяФайла - Строка
// 
// Возвращаемое значение:
//  Булево -
// 
Функция ЭтоИностранныйФайл(Знач ИмяФайла) Экспорт
	
	Файл = Новый Файл(ИмяФайла);
	ИмяФайла = Файл.Имя;
	Если НЕ ЭтоФайлДокумента(ИмяФайла) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого Обработчик Из Обработчики() Цикл
		Если ЗначениеЗаполнено(Обработчик.ОпределитьФорматЭД(ИмяФайла)) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Проверяет иностранное сообщение
// 
// Параметры:
//  Сообщение - ДокументСсылка.СообщениеЭДО
// 
// Возвращаемое значение:
//  Булево -
// 
Функция ЭтоИностранноеСообщение(Сообщение) Экспорт
	
	ПолноеИмяФайла = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сообщение, "ОсновнойФайл.ПолноеИмяФайла");
	Возврат ЭтоИностранныйФайл(ПолноеИмяФайла);
	
КонецФункции

// Проверяет иностранный документ
// 
// Параметры:
//  ДанныеДокумента - СтрокаТаблицыЗначений: см. СинхронизацияЭДО.НовыеДанныеОбъектов
// 
// Возвращаемое значение:
//  Булево -
// 
Функция ЭтоТрансграничныйОбмен(ДанныеДокумента) Экспорт
	Возврат ДанныеДокумента.ДанныеТЭДО <> Неопределено;
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Подписи

Функция ТекстОшибкиОтсутствияПроверкиДТС()
	Возврат НСтр("ru = 'Отсутствует проверка ДТС';
				|en = 'There is no trusted third-party check'");
КонецФункции

// Проверяет подписи ТЭДО.
// 
// Параметры:
//  Подпись - см. РаботаСФайламиБЭД.НовоеОписаниеФайла
//  Квитанция - см. РаботаСФайламиБЭД.НовоеОписаниеФайла
//  КодАльфа3Страны - Строка
//  ТекстОшибкиДТС - Строка
// 
// Возвращаемое значение:
//  см. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
//
Функция ПроверитьПодписьТЭДО(Подпись, Квитанция, КодАльфа3Страны, ТекстОшибкиДТС)
	
	СвойстваПодписи = КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи();
	СвойстваПодписи.Подпись = Подпись.ДвоичныеДанные;
	СвойстваПодписи.ИмяФайлаПодписи = Подпись.ИмяФайла;
	РезультатЧтения = СвойстваПодписиИзКвитанцииДТС(Квитанция, Подпись, КодАльфа3Страны, ТекстОшибкиДТС);
	
	СвойстваПодписиТЭДО = СвойстваПодписи();
	СвойстваПодписиТЭДО.КодАльфа3Страны = КодАльфа3Страны;
	СвойстваПодписиИзКвитанцииДТС =
		РезультатЧтения.СвойстваПодписи; // см. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
	
	Если СвойстваПодписиИзКвитанцииДТС = Неопределено Тогда
		СвойстваПодписиТЭДО.ТекстОшибки = РезультатЧтения.ТекстОшибки;
	Иначе
		СвойстваПодписиТЭДО = СвойстваПодписиИзКвитанцииДТС.СвойстваПодписиТЭДО;
	КонецЕсли;
	
	Попытка
		
		СвойстваПодписиИзДвоичныхДанных = ЭлектроннаяПодпись.СвойстваПодписи(Подпись.ДвоичныеДанные);
		СвойстваПодписи.Сертификат = СвойстваПодписиИзДвоичныхДанных.Сертификат;
		СвойстваПодписи.Отпечаток = СвойстваПодписиИзДвоичныхДанных.Отпечаток;
		СвойстваПодписи.КомуВыданСертификат = СвойстваПодписиИзДвоичныхДанных.КомуВыданСертификат;
		СвойстваПодписи.ДатаПодписи = ЭлектроннаяПодпись.ДатаПодписания(Подпись.ДвоичныеДанные, Истина);
		
		Если СвойстваПодписиИзКвитанцииДТС <> Неопределено Тогда
			
			ЗаполнитьЗначенияСвойств(СвойстваПодписи, СвойстваПодписиИзКвитанцииДТС, "ПодписьВерна, ДатаПроверкиПодписи");
			
			Если НЕ ЗначениеЗаполнено(СвойстваПодписиТЭДО.ДатаПодписи) Тогда
				СвойстваПодписиТЭДО.ДатаПодписи = СвойстваПодписи.ДатаПодписи;
			КонецЕсли;
			
		КонецЕсли;
		
	Исключение
		
		Если СвойстваПодписиИзКвитанцииДТС <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СвойстваПодписи, СвойстваПодписиИзКвитанцииДТС);
		КонецЕсли;
		
	КонецПопытки;
	
	СвойстваПодписи.СвойстваПодписиТЭДО = СвойстваПодписиТЭДО;
	Возврат СвойстваПодписи;
	
КонецФункции

// Читает карточку ДТС и возвращает XDTO-объект.
// 
// Параметры:
//  ДвоичныеДанныеКарточки - ДвоичныеДанные
// 
// Возвращаемое значение:
//  ОбъектXDTO -
//  
Функция ПрочитатьКарточкуДТС(ДвоичныеДанныеКарточки)
	
	ПространствоИмен = ПространствоИменКарточкиДТС();
	ДвоичныеДанныеКарточки = ОбщегоНазначенияБЭД.ДобавитьПространствоИмен(
		ДвоичныеДанныеКарточки, ПространствоИмен, "utf-8");
	ТипДанных = РаботаСФайламиБЭД.ПолучитьТипЗначенияCML("TransportDepartment", ПространствоИмен);
	КарточкаДТС = РаботаСФайламиБЭД.ПрочитатьXDTO(ДвоичныеДанныеКарточки.ОткрытьПотокДляЧтения(), ТипДанных);
	
	Возврат КарточкаДТС;
	
КонецФункции

Функция ПространствоИменКарточкиДТС()
	Возврат "dts_card";
КонецФункции

// Прочитать квитанцию ДТС.
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  - Неопределено
//  - Соответствие
//  - Массив из Соответствие
//  
Функция ПрочитатьКвитанциюДТС(ДвоичныеДанные)
	
	Попытка
		
		КвитанцияСтрокой = ПолучитьСтрокуИзДвоичныхДанных(ДвоичныеДанные, КодировкаТекста.UTF8);
		Квитанция = ОбщегоНазначения.JSONВЗначение(КвитанцияСтрокой, , Истина);
		Возврат Квитанция;
		
	Исключение
		
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

// Свойства подписи из квитанции ДТС.
// 
// Параметры:
//  ОписаниеКвитанции - см. РаботаСФайламиБЭД.НовоеОписаниеФайла
//  ОписаниеПодписи - см. РаботаСФайламиБЭД.НовоеОписаниеФайла
//  КодАльфа3Страны - Строка
//  ТекстОшибки - Строка
// 
// Возвращаемое значение:
//  Структура:
//  * СвойстваПодписи - Неопределено
//                    - см. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
//  * ТекстОшибки - Строка
//
Функция СвойстваПодписиИзКвитанцииДТС(ОписаниеКвитанции, ОписаниеПодписи, КодАльфа3Страны, ТекстОшибки = "")
	
	Результат = Новый Структура("СвойстваПодписи, ТекстОшибки", Неопределено, "");
	ПорядковыйНомер = 1;
	
	Если НЕ ЗначениеЗаполнено(ОписаниеКвитанции) ИЛИ НЕ ЗначениеЗаполнено(ОписаниеКвитанции.ДвоичныеДанные) Тогда
		
		Результат.ТекстОшибки = ТекстОшибки;
		Если НЕ ЗначениеЗаполнено(ТекстОшибки) Тогда
			Результат.ТекстОшибки = НСтр("ru = 'Отсутствуют данные квитанции ДТС';
										|en = 'Trusted third-party receipt data is missing'");
		КонецЕсли;
		
		Возврат Результат;
		
	КонецЕсли;
	
	Квитанция = ПрочитатьКвитанциюДТС(ОписаниеКвитанции.ДвоичныеДанные);
	
	Если Квитанция = Неопределено Тогда
		Результат.ТекстОшибки = НСтр("ru = 'Не удалось прочитать квитанцию ДТС';
									|en = 'Cannot read the trusted third-party receipt'");
		Возврат Результат;
	КонецЕсли;
	
	СвойстваПодписи = КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи();
	Результат.СвойстваПодписи = СвойстваПодписи;
	СвойстваПодписи.Подпись = ОписаниеПодписи.ДвоичныеДанные;
	СвойстваПодписи.ИмяФайлаПодписи = ОписаниеПодписи.ИмяФайла;
	СвойстваПодписи.ПорядковыйНомер = ПорядковыйНомер;
	ПорядковыйНомер = ПорядковыйНомер + 1;
	СвойстваПодписиТЭДО = СвойстваПодписи();
	СвойстваПодписи.СвойстваПодписиТЭДО = СвойстваПодписиТЭДО;
	СвойстваПодписиТЭДО.ИмяФайлаПодписи = СвойстваПодписи.ИмяФайлаПодписи;
	СвойстваПодписиТЭДО.КвитанцияДТС = ОписаниеКвитанции;
	СвойстваПодписиТЭДО.КодАльфа3Страны = КодАльфа3Страны;
	
	ОшибкаПроверки = Квитанция["errorTag"]; // Соответствие
	
	Если ОшибкаПроверки <> Неопределено Тогда
		
		МассивОшибок = ОшибкаПроверки["errors"]; // Массив из Строка
		Если МассивОшибок = Неопределено Тогда
			МассивОшибок = Новый Массив();
		КонецЕсли;
		
		МассивОписаний = ОшибкаПроверки["information"]; // Массив из Строка
		Если МассивОписаний = Неопределено Тогда
			МассивОписаний = Новый Массив();
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивОшибок, МассивОписаний);
		СвойстваПодписиТЭДО.ТекстОшибки = СтрСоединить(МассивОшибок, Символы.ПС);
		Возврат Результат;
		
	КонецЕсли;
		
	СвойстваПодписиТЭДО.ПодписьПроверена = Истина;
	РезультатПроверки = Квитанция["verifyResult"];
	СвойстваПодписи.ПодписьВерна = РезультатПроверки["valB"] = Истина;
	СвойстваПодписиТЭДО.ПодписьВерна = СвойстваПодписи.ПодписьВерна;
	
	Попытка
	
		Проверка = Квитанция["ticketInfo"];
		Если Проверка <> Неопределено Тогда
			ДатаПроверкиСтрокой = Проверка["createTimeInfo"]["dvcTime"]["time"]["val"]; // Строка
			Если ДатаЗаполнена(ДатаПроверкиСтрокой) Тогда
				СвойстваПодписиТЭДО.ДатаПроверкиПодписи = ДатаИзПредставленияДТС(ДатаПроверкиСтрокой);
				Если ЗначениеЗаполнено(СвойстваПодписиТЭДО.ДатаПроверкиПодписи) Тогда
					СвойстваПодписи.ДатаПроверкиПодписи = МестноеВремя(СвойстваПодписиТЭДО.ДатаПроверкиПодписи);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ЦепочкаДоказательств = Квитанция["proofList"]; // Массив из Соответствие
		
		Если ЦепочкаДоказательств <> Неопределено Тогда
			
			ЭлементПроверки = ЦепочкаДоказательств[0];
			ДатаПодписиСтрокой = ЭлементПроверки["target"]["createTime"]["val"]; // Строка
			Если ДатаЗаполнена(ДатаПодписиСтрокой) Тогда
				СвойстваПодписи.ДатаПодписи = МестноеВремя(ДатаИзПредставленияДТС(ДатаПодписиСтрокой));
				СвойстваПодписиТЭДО.ДатаПодписи = СвойстваПодписи.ДатаПодписи;
			КонецЕсли;
			ДанныеСертификата = ЭлементПроверки["certificateInfo"]; // Соответствие
			СертификатСтрокой = ДанныеСертификата["certificateRaw"]; // Строка
			СвойстваПодписи.Сертификат = Base64Значение(СертификатСтрокой);
			СвойстваПодписи.КомуВыданСертификат = Строка(ДанныеСертификата["subject"]["val"]);
			
		Иначе
			
			СвойстваПодписиТЭДО.ТекстОшибки = НСтр("ru = 'Сведения о подписанте отсутствуют в квитанции ДТС';
													|en = 'There is no information on the signer in the trusted third-party receipt'");
			
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Операция = НСтр("ru = 'Чтение квитанции ДТС';
						|en = 'Read trusted third-party receipt'");
		Подсистема = ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().Криптография;
		ОбработкаНеисправностейБЭД.ОбработатьОшибку(Операция, Подсистема, ТекстОшибки);
		СвойстваПодписиТЭДО.ТекстОшибки = НСтр("ru = 'Не удалось извлечь сведения о подписанте из квитанции ДТС';
												|en = 'Cannot extract the information on the signer from the trusted third-party receipt'");
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Проверяет заполненность представления даты ДТС
// 
// Параметры:
//  ДатаСтрокой - Строка
// 
// Возвращаемое значение:
//  Булево
//
Функция ДатаЗаполнена(ДатаСтрокой)
	Возврат ЗначениеЗаполнено(ДатаСтрокой)
		И НРег(ДатаСтрокой) <> "не определено"
		И ЕстьЦифрыВСтроке(ДатаСтрокой);
КонецФункции

// Проверяет наличие цифр в строке.
// 
// Параметры:
//  ПроверяемаяСтрока - Строка
// 
// Возвращаемое значение:
//  Булево
//
Функция ЕстьЦифрыВСтроке(ПроверяемаяСтрока)
	
	СтрокаЦифр = "0123456789";
	
	Для Индекс = 0 По СтрДлина(ПроверяемаяСтрока) - 1 Цикл
		Если СтрНайти(СтрокаЦифр, Сред(ПроверяемаяСтрока, Индекс, 1)) > 0 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Читает дату из строкового представления в квитанции ДТС.
// 
// Параметры:
//  ДатаСтрокой - Строка
// 
// Возвращаемое значение:
//  Дата -
//  
Функция ДатаИзПредставленияДТС(ДатаСтрокой)
	
	ДатаСтрокой = НРег(ДатаСтрокой);
	СтрокаСДатой = СтрЗаменить(ДатаСтрокой, " января ", ".01.");
	СтрокаСДатой = СтрЗаменить(СтрокаСДатой, " февраля ", ".02.");
	СтрокаСДатой = СтрЗаменить(СтрокаСДатой, " марта ", ".03.");
	СтрокаСДатой = СтрЗаменить(СтрокаСДатой, " апреля ", ".04.");
	СтрокаСДатой = СтрЗаменить(СтрокаСДатой, " мая ", ".05.");
	СтрокаСДатой = СтрЗаменить(СтрокаСДатой, " июня ", ".06.");
	СтрокаСДатой = СтрЗаменить(СтрокаСДатой, " июля ", ".07.");
	СтрокаСДатой = СтрЗаменить(СтрокаСДатой, " августа ", ".08.");
	СтрокаСДатой = СтрЗаменить(СтрокаСДатой, " сентября ", ".09.");
	СтрокаСДатой = СтрЗаменить(СтрокаСДатой, " октября ", ".10.");
	СтрокаСДатой = СтрЗаменить(СтрокаСДатой, " ноября ", ".11.");
	СтрокаСДатой = СтрЗаменить(СтрокаСДатой, " декабря ", ".12.");
	СтрокаСДатой = СтрЗаменить(СтрокаСДатой, " г.", "");
	СтрокаСДатой = СтрЗаменить(СтрокаСДатой, " utc", "");
	
	Попытка
		Возврат Дата(СтрокаСДатой);
	Исключение
		Возврат '00010101';
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#Область Прочее

// Проверяет файл документа.
// 
// Параметры:
//  ИмяФайла - Строка
// 
// Возвращаемое значение:
//  Булево
//
Функция ЭтоФайлДокумента(ИмяФайла)
	
	Расширение = ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИмяФайла);
	Если ЗначениеЗаполнено(Расширение) И НЕ РаботаСФайламиБЭДКлиентСервер.ЭтоXML(Расширение) Тогда
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
	
КонецФункции

// Заполняет номенклатуру в данных сопоставления.
// 
// Параметры:
//  ДанныеЭД - см. ДанныеЭД
//  Отправитель - ОпределяемыйТип.УчастникЭДО
// 
Процедура ЗаполнитьСопоставленнуюНоменклатуру(ДанныеЭД, Отправитель)
	
	НаСопоставление = Новый Массив(); // Массив из Структура
	
	Для Каждого КлючИЗначение Из ДанныеЭД.Сопоставление Цикл
		
		Сопоставление = КлючИЗначение.Значение;
		НоменклатураКонтрагента = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента();
		ЗаполнитьЗначенияСвойств(НоменклатураКонтрагента, Сопоставление);
		НоменклатураКонтрагента.Владелец = Отправитель;
		НаСопоставление.Добавить(НоменклатураКонтрагента);
		
	КонецЦикла;
	
	Отбор = Новый Структура("НоменклатураКонтрагента", НаСопоставление);
	СоответствиеНоменклатуры = СопоставлениеНоменклатурыКонтрагентов.НайтиСоответствиеНоменклатуры(Отбор, Истина);
	
	Для Каждого КлючИЗначение Из ДанныеЭД.Сопоставление Цикл
		
		Сопоставление = КлючИЗначение.Значение;
		
		Для Каждого ДанныеСоответствия Из СоответствиеНоменклатуры Цикл
			Если ДанныеСоответствия.НоменклатураКонтрагента.Идентификатор = Сопоставление.Идентификатор Тогда
				ДанныеЭД.Номенклатура.Вставить(КлючИЗначение.Ключ, ДанныеСоответствия.НоменклатураИБ);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Ищет и заполняет двоичные данные файла.
// 
// Параметры:
//  ОписаниеФайла - см. РаботаСФайламиБЭД.НовоеОписаниеФайла
//  РаспакованныеФайлы - см. РаботаСФайламиБЭД.РаспаковатьАрхив
//  
Процедура НайтиДвоичныеДанныеФайла(ОписаниеФайла, РаспакованныеФайлы)
	
	Для Каждого РаспакованныйФайл Из РаспакованныеФайлы Цикл
		
		Если ВРег(РаспакованныйФайл.Имя) = ВРег(ОписаниеФайла.ИмяФайла) Тогда
			
			ОписаниеФайла.ДвоичныеДанные = ?(ТипЗнч(РаспакованныйФайл) = Тип("Структура"),
			РаспакованныйФайл.ДвоичныеДанные, Новый ДвоичныеДанные(РаспакованныйФайл.ПолноеИмя));
			Возврат;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Инициирует описание иностранной подписи.
// 
// Возвращаемое значение:
//  Структура:
// * Подпись - см. РаботаСФайламиБЭД.НовоеОписаниеФайла
// * ХешПодписи - Строка
// * КвитанцияДТС - см. РаботаСФайламиБЭД.НовоеОписаниеФайла
// * Ошибка - Неопределено
//          - Структура:
//            ** ТекстОшибки - Строка
//            ** КодОшибки - Строка
// 
Функция ОписаниеПодписи()
	
	Результат = Новый Структура();
	Результат.Вставить("Подпись", РаботаСФайламиБЭД.НовоеОписаниеФайла());
	Результат.Вставить("ХешПодписи", "");
	Результат.Вставить("КвитанцияДТС", РаботаСФайламиБЭД.НовоеОписаниеФайла());
	Результат.Вставить("Ошибка", Неопределено);
	Возврат Результат;
	
КонецФункции

// Возвращает коллекцию обработчиков трансграничного обмена.
// 
// Возвращаемое значение:
//  Массив из ОбработкаМенеджер.ТрансграничныйОбменРБ
//  
Функция Обработчики()
	
	Результат = Новый Массив(); // см. Обработчики
	Результат.Добавить(Обработки.ТрансграничныйОбменРБ);
	Возврат Результат;
	
КонецФункции

// Инициирует описание участника.
// 
// Возвращаемое значение:
//  Структура:
// * Наименование - Строка
// * ИНН - Строка
// * КПП - Строка
// * НалоговыйНомер - Строка
// * КодАльфа3Страны - Строка
//
Функция НовоеОписаниеУчастника()
	
	Результат = Новый Структура();
	Результат.Вставить("Наименование", "");
	Результат.Вставить("ИНН", "");
	Результат.Вставить("КПП", "");
	Результат.Вставить("НалоговыйНомер", "");
	Результат.Вставить("КодАльфа3Страны", "");
	Возврат Результат;
	
КонецФункции

// Новые параметры представления суммы.
// 
// Возвращаемое значение:
//  Структура:
// * ВсегоСтоимостьСНДС - см. НовоеПредставлениеСуммы
// * ВсегоСуммаНДС - см. НовоеПредставлениеСуммы
Функция НовыеПараметрыПредставленияСуммы()
	
	Результат = Новый Структура();
	Результат.Вставить("ВсегоСтоимостьСНДС", НовоеПредставлениеСуммы());
	Результат.Вставить("ВсегоСуммаНДС", НовоеПредставлениеСуммы());
	Возврат Результат;
	
КонецФункции

// Инициирует представление суммы.
// 
// Возвращаемое значение:
//  Структура:
// * КодВалюты - Строка
// * Правило - Строка
// * СуммаДокумента - Число
// 
Функция НовоеПредставлениеСуммы()
	
	Результат = Новый Структура();
	Результат.Вставить("КодВалюты", "");
	Результат.Вставить("Правило", "");
	Результат.Вставить("СуммаДокумента", 0);
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти