#Область СлужебныеПроцедурыИФункции

// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ДатаРаботы - Дата
Процедура ДобавитьДанныеДляПодбораРаботников(Форма, ДатаРаботы) Экспорт
	
	//++ Локализация
	
	УчетТрудозатратВРазрезеСотрудников = ПроизводствоСервер.ИспользуетсяУчетТрудозатратВРазрезеСотрудников(ДатаРаботы);
	ИспользоватьКадровыйУчет           = ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет");
	
	Если НЕ ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ДанныеДляПодбораСотрудников)
			ИЛИ НЕ ПравоДоступа("Чтение", Метаданные.Справочники.Сотрудники)
			ИЛИ НЕ (ИспользоватьКадровыйУчет
			ИЛИ УчетТрудозатратВРазрезеСотрудников) Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Элементы.ГруппаОрганизация.Видимость = Истина;
	
	Если ИспользоватьКадровыйУчет
		И НЕ УчетТрудозатратВРазрезеСотрудников
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ТекущиеКадровыеДанныеСотрудников)
		И ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ОсновныеСотрудникиФизическихЛиц) Тогда
		ТекстЗапроса = "
			|ВЫБРАТЬ ПЕРВЫЕ 1000000
			|	АВТОНОМЕРЗАПИСИ() КАК Номер,
			|	ДанныеДляПодбора.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ВЫБОР
			|		КОГДА &ИспользоватьШтатноеРасписание
			|			ТОГДА ШтатноеРасписание.Должность
			|		ИНАЧЕ ДанныеДляПодбора.Должность
			|	КОНЕЦ КАК Должность,
			|	ДанныеДляПодбора.МестоВСтруктуреПредприятия КАК Подразделение,
			|	ДанныеДляПодбора.Организация КАК Организация,
			|	ДанныеДляПодбора.Подразделение КАК ПодразделениеОрганизации,
			|	ДанныеДляПодбора.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
			|	ДанныеДляПодбора.Сотрудник КАК Сотрудник
			|ПОМЕСТИТЬ ТекущиеКадровыеДанныеФизическиеЛиц
			|{ВЫБРАТЬ
			|	ФизическоеЛицо,
			|	Должность,
			|	Подразделение.*,
			|	Организация.*,
			|	ПодразделениеОрганизации.*,
			|	Сотрудник}
			|ИЗ
			|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеДляПодбораСотрудников КАК ДанныеДляПодбора
			|		ПО ТекущиеКадровыеДанныеСотрудников.Сотрудник = ДанныеДляПодбора.Сотрудник
			|		И (ДанныеДляПодбора.Начало В
			|			(ВЫБРАТЬ
			|			МАКСИМУМ(Т.Начало)
			|			ИЗ
			|				РегистрСведений.ДанныеДляПодбораСотрудников КАК Т
			|			ГДЕ
			|				ТекущиеКадровыеДанныеСотрудников.Сотрудник = Т.Сотрудник
			|				И НЕ Т.ПоДоговоруГПХ))
			|					
			|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|				ПО &ИспользоватьШтатноеРасписание
			|				И ДанныеДляПодбора.ДолжностьПоШтатномуРасписанию = ШтатноеРасписание.Ссылка
			|ГДЕ
			|		ТекущиеКадровыеДанныеСотрудников.ДатаПриема <> ДАТАВРЕМЯ(1, 1, 1)
			|		И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1)
			|
			|УПОРЯДОЧИТЬ ПО
			|	ДанныеДляПодбора.ВидЗанятости.Порядок УБЫВ,
			|	ДанныеДляПодбора.Начало
			|
			|{ГДЕ
			|	ДанныеДляПодбора.МестоВСтруктуреПредприятия.*}
			|;
			|
			|ВЫБРАТЬ
			|	ФизЛица.Ссылка,
			|	ФизЛица.Код,
			|	ФизЛица.Наименование,
			|	
			|	ТекущиеКадровыеДанныеФизическиеЛиц.Должность КАК Должность,
			|	ТекущиеКадровыеДанныеФизическиеЛиц.Подразделение КАК Подразделение,
			|	ТекущиеКадровыеДанныеФизическиеЛиц.Сотрудник.Код КАК ТабНомер,
			|	ТекущиеКадровыеДанныеФизическиеЛиц.Организация КАК Организация,
			|	ТекущиеКадровыеДанныеФизическиеЛиц.ПодразделениеОрганизации КАК ПодразделениеОрганизации
			|ИЗ
			|	Справочник.ФизическиеЛица КАК ФизЛица
			|	
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТекущиеКадровыеДанныеФизическиеЛиц КАК ТекущиеКадровыеДанныеФизическиеЛиц
			|		ПО ФизЛица.Ссылка = ТекущиеКадровыеДанныеФизическиеЛиц.ФизическоеЛицо
			|		И (ТекущиеКадровыеДанныеФизическиеЛиц.Номер В
			|			(ВЫБРАТЬ
			|			МАКСИМУМ(Т.Номер)
			|			ИЗ
			|				ТекущиеКадровыеДанныеФизическиеЛиц КАК Т
			|			ГДЕ
			|				ФизЛица.Ссылка = Т.ФизическоеЛицо))
			|		
			|ГДЕ
			|	(&ТолькоРаботающие И ТекущиеКадровыеДанныеФизическиеЛиц.ФизическоеЛицо ЕСТЬ НЕ NULL ИЛИ Не &ТолькоРаботающие)";
		
		СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
		
		СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
		СвойстваСписка.ОсновнаяТаблица = "Справочник.ФизическиеЛица";
		СвойстваСписка.ДинамическоеСчитываниеДанных = Ложь;
		
		ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Форма.Элементы.Работники, СвойстваСписка);
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Форма.Работники,
			"ТолькоРаботающие",
			Форма.ТолькоРаботающие);
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Форма.Работники,
			"МаксимальнаяДатаНачалоДня",
			НачалоДня(ЗарплатаКадрыПериодическиеРегистры.МаксимальнаяДата()));
		
	КонецЕсли;
	
	Если УчетТрудозатратВРазрезеСотрудников Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ФизЛица.Ссылка,
		|	ФизЛица.Код,
		|	ФизЛица.ДатаРождения,
		|	ФизЛица.Пол,
		|	ФизЛица.ФИО,
		|	ФизЛица.УточнениеНаименования,
		|	ФизЛица.Фамилия,
		|	ФизЛица.Имя,
		|	ФизЛица.Отчество,
		|	Сотрудники.Код КАК ТабНомер,
		|	ДанныеДляПодбора.Наименование,
		|	ВЫБОР
		|		КОГДА &ИспользоватьШтатноеРасписание
		|			ТОГДА ДанныеДляПодбора.ДолжностьПоШтатномуРасписанию.Должность
		|		ИНАЧЕ ДанныеДляПодбора.Должность
		|	КОНЕЦ КАК Должность,
		|	ДанныеДляПодбора.МестоВСтруктуреПредприятия КАК Подразделение,
		|	ДанныеДляПодбора.Организация КАК Организация,
		|	ДанныеДляПодбора.Подразделение КАК ПодразделениеОрганизации,
		|	ДанныеДляПодбора.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
		|	ДанныеДляПодбора.Сотрудник КАК Сотрудник,
		|	ДанныеДляПодбора.Начало КАК ДатаПриема
		|ИЗ
		|	РегистрСведений.ДанныеДляПодбораСотрудников КАК ДанныеДляПодбора
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ДанныеДляПодбора.Сотрудник = Сотрудники.Ссылка
		|		И ДанныеДляПодбора.Наименование = Сотрудники.Наименование
		|		И (ДанныеДляПодбора.ИдентификаторЗаписи В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ДанныеДляПодбораСотрудниковОтбор.ИдентификаторЗаписи
		|			ИЗ
		|				РегистрСведений.ДанныеДляПодбораСотрудников КАК ДанныеДляПодбораСотрудниковОтбор
		|			ГДЕ
		|				ДанныеДляПодбораСотрудниковОтбор.Сотрудник = ДанныеДляПодбора.Сотрудник
		|				И ДанныеДляПодбораСотрудниковОтбор.Наименование = ДанныеДляПодбора.Наименование
		|				И ДанныеДляПодбораСотрудниковОтбор.Подразделение = ДанныеДляПодбора.Подразделение
		|				И ДанныеДляПодбораСотрудниковОтбор.Должность = ДанныеДляПодбора.Должность
		|				И ДанныеДляПодбораСотрудниковОтбор.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|			УПОРЯДОЧИТЬ ПО
		|				ДанныеДляПодбораСотрудниковОтбор.ПоДоговоруГПХ,
		|				ДанныеДляПодбораСотрудниковОтбор.Начало УБЫВ,
		|				ДанныеДляПодбораСотрудниковОтбор.Организация,
		|				ДанныеДляПодбораСотрудниковОтбор.Филиал,
		|				ДанныеДляПодбораСотрудниковОтбор.Подразделение))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизЛица
		|		ПО ФизЛица.Ссылка = ДанныеДляПодбора.ФизическоеЛицо
		|		
		|	ГДЕ
		|		ДанныеДляПодбора.Начало <> ДАТАВРЕМЯ(1, 1, 1)
		|		И ДанныеДляПодбора.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|		И (&РаботающиеНаДату
		|		И ДанныеДляПодбора.Начало <= &ДатаРаботы
		|		И ДанныеДляПодбора.Окончание >= &ДатаРаботы
		|		ИЛИ НЕ &РаботающиеНаДату)";
		
		СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
		
		СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
		СвойстваСписка.ОсновнаяТаблица = "";
		СвойстваСписка.ДинамическоеСчитываниеДанных = Ложь;
		
		ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Форма.Элементы.Работники, СвойстваСписка);
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Форма.Работники,
			"ДатаРаботы",
			?(ЗначениеЗаполнено(ДатаРаботы), ДатаРаботы, ТекущаяДатаСеанса()));
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Форма.Работники,
			"РаботающиеНаДату",
			Булево(Форма.РаботающиеНаДату));
		
		Форма.Элементы.РаботникиДатаПриема.Видимость = Истина;
		Форма.Элементы.РаботникиСотрудник.Видимость = Истина;
		Форма.Элементы.РаботникиНаименование.Видимость = Ложь;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Форма.Работники,
		"ИспользоватьШтатноеРасписание",
		ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание"));
	
	Форма.Элементы.РаботникиПодразделение.Видимость            = Истина;
	Форма.Элементы.РаботникиОрганизация.Видимость              = Истина;
	Форма.Элементы.РаботникиПодразделениеОрганизации.Видимость = Истина;
	Форма.Элементы.РаботникиДолжность.Видимость                = Истина;
	Форма.Элементы.РаботникиТабНомер.Видимость                 = Истина;
	Форма.Элементы.РаботникиКод.Видимость                      = Ложь;
	//-- Локализация
	
КонецПроцедуры

//++ Локализация

// Должность разряд сотрудников.
// 
// Параметры:
//  МассивСотрудников - Массив из СправочникСсылка.Сотрудники
//  Дата - Дата
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Должность и разряд по сотрудникам:
//   * Сотрудник - СправочникСсылка.Сотрудники
//   * Должность - СправочникСсылка.Должности
//   * РазрядКатегория - СправочникСсылка.РазрядыКатегорииДолжностей
//   * Порядок - Число
Функция ДолжностьРазрядСотрудников(МассивСотрудников, Дата) Экспорт
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ДанныеДляПодбора.Сотрудник КАК Сотрудник,
		|	ДанныеДляПодбора.Должность КАК Должность,
		|	РазрядыКатегорииСотрудников.РазрядКатегория КАК РазрядКатегория,
		|	Разряды.РеквизитДопУпорядочивания КАК Порядок
		|ИЗ
		|	РегистрСведений.ДанныеДляПодбораСотрудников КАК ДанныеДляПодбора
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО ДанныеДляПодбора.Сотрудник = Сотрудники.Ссылка
		|		И ДанныеДляПодбора.Наименование = Сотрудники.Наименование
		|		И (ДанныеДляПодбора.ИдентификаторЗаписи В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ДанныеДляПодбораСотрудниковОтбор.ИдентификаторЗаписи
		|			ИЗ
		|				РегистрСведений.ДанныеДляПодбораСотрудников КАК ДанныеДляПодбораСотрудниковОтбор
		|			ГДЕ
		|				ДанныеДляПодбораСотрудниковОтбор.Сотрудник = ДанныеДляПодбора.Сотрудник
		|				И ДанныеДляПодбораСотрудниковОтбор.Наименование = ДанныеДляПодбора.Наименование
		|				И ДанныеДляПодбораСотрудниковОтбор.Подразделение = ДанныеДляПодбора.Подразделение
		|				И ДанныеДляПодбораСотрудниковОтбор.Должность = ДанныеДляПодбора.Должность
		|				И ДанныеДляПодбораСотрудниковОтбор.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|			УПОРЯДОЧИТЬ ПО
		|				ДанныеДляПодбораСотрудниковОтбор.ПоДоговоруГПХ,
		|				ДанныеДляПодбораСотрудниковОтбор.Начало УБЫВ,
		|				ДанныеДляПодбораСотрудниковОтбор.Организация,
		|				ДанныеДляПодбораСотрудниковОтбор.Филиал,
		|				ДанныеДляПодбораСотрудниковОтбор.Подразделение))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазрядыКатегорииСотрудников.СрезПоследних(&ДатаСмены, Сотрудник В (&МассивСотрудников) И (ДействуетДо = ДАТАВРЕМЯ(1,1,1) ИЛИ ДействуетДо >= &ДатаСмены)) КАК РазрядыКатегорииСотрудников
		|		ПО РазрядыКатегорииСотрудников.Сотрудник = ДанныеДляПодбора.Сотрудник
		|		И НЕ РазрядыКатегорииСотрудников.РазрядКатегория = ЗНАЧЕНИЕ(Справочник.РазрядыКатегорииДолжностей.ПустаяСсылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РазрядыКатегорииДолжностей КАК Разряды
		|		ПО РазрядыКатегорииСотрудников.РазрядКатегория = Разряды.Ссылка
		|		
		|	ГДЕ
		|		Сотрудники.Ссылка В (&МассивСотрудников)
		|		И ДанныеДляПодбора.Начало <> ДАТАВРЕМЯ(1, 1, 1)
		|		И ДанныеДляПодбора.ВидСобытия <> ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|		И ДанныеДляПодбора.Начало <= &ДатаСмены
		|		И ДанныеДляПодбора.Окончание >= &ДатаСмены");
	
	Запрос.УстановитьПараметр("ДатаСмены", Дата);
	Запрос.УстановитьПараметр("МассивСотрудников", МассивСотрудников);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Соответствие должности разряда исполнителя.
// 
// Параметры:
//  Исполнитель - СправочникСсылка.Сотрудники
//  Должность - СправочникСсылка.Должности
//  Разряд - СправочникСсылка.РазрядыКатегорииДолжностей
//  Дата - Дата
// 
// Возвращаемое значение:
//  Булево - Соответствие должности разряда исполнителя
Функция СоответствиеДолжностиРазрядаИсполнителя(Исполнитель, Должность, Разряд, Дата) Экспорт
	
	КадровыеДанные = ДолжностьРазрядСотрудников(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Исполнитель),
		Дата);
	
	Для Каждого Данные Из КадровыеДанные Цикл
		Если ЗначениеЗаполнено(Данные.Должность) И ЗначениеЗаполнено(Должность) Тогда
			
			Если Данные.Должность <> Должность Тогда
				Возврат Ложь;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Данные.РазрядКатегория) И ЗначениеЗаполнено(Разряд) Тогда
				Порядок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Разряд, "РеквизитДопУпорядочивания");
				
				Если Данные.Порядок < Порядок Тогда
					Возврат Ложь;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции
//-- Локализация

#КонецОбласти
