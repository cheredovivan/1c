///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ИнтернетЭквайринг".
// ОбщийМодуль.ИнтернетЭквайрингСобытия.
// 
// Обработчики событий, возникающих при работе с Интернет-эквайрингом:
//  - проверка настроек подключения;
//  - перед записью прикладных настроек;
//  - изменение признака использования настройки;
//  - определение доступности подсистемы и команд на общей форме списка настроек платежных систем.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Определяет список команд на общей форме настроек подключения к платежным системам, которые должны быть доступны
//  для строк, относящихся к подсистеме Интернет-эквайринга.
//  В параметр ДоступныеКоманды могут быть добавлены любые имена команд, даже если их нет на форме.
// 
// Параметры:
//  КешПараметров - Структура - см. описание метода НастройкиПлатежныхСистемСобытия.ПриПодготовкеПараметровФормы
//  ВсеКоманды - Соответствие Из КлючИЗначение - описание кнопок формы:
//    * Ключ - Строка - имя элемента формы, с которым связана команда.
//    * Значение - Строка - имя команды.
//  ДоступныеКоманды - Массив Из Строка - имена команд, доступных для подсистемы Интернет-эквайринга.
//
Процедура ПриОпределенииДоступностиКомандСпискаНастроек(
		КешПараметров,
		ВсеКоманды,
		ДоступныеКоманды) Экспорт
	
	Если КешПараметров.ДоступностьНастроек.Получить(НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг()) Тогда
		ДоступныеКоманды.Добавить("ОткрытьШаблоныНазначения");
	КонецЕсли;
	
КонецПроцедуры

// Определяет видимость подсистемы в общем списке настроек подключения к платежным системам, доступность изменения
// настроек и видимость кассовых ссылок.
// 
// Параметры:
//  ВидимостьПодсистемы - Булево - признак видимости подсистемы в списке. Не рекомендуется изменять данный параметр,
//    поскольку форма может открываться в режиме видимости только одной подсистемы.
//  ДоступностьНастроек - Булево - признак доступности для пользователя возможности изменения настроек подключения.
//
Процедура ПриПодготовкеПараметровФормыСпискаНастроек(
		ВидимостьПодсистемы,
		ДоступностьНастроек) Экспорт
	
	Если ВидимостьПодсистемы Тогда
		ДоступностьНастроек = ИнтернетЭквайринг.НастройкаПодключенияДоступна();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет настройки программы установленные в переопределении.
//
// Параметры:
//  Настройки - см. ИнтернетЭквайрингСлужебный.ПрикладныеНастройкиПодключения
//
Процедура ПриПроверкеНастройкиПодключенияПрограммы(Настройки) Экспорт
	
	Ошибки = Новый Массив;
		
	Если Настройки.ОбъектМетаданных = Неопределено Тогда
		Ошибки.Добавить(
			НСтр("ru = 'Не указан объект метаданных отвечающий за настройку параметров Интернет-эквайринга.';
				|en = 'Metadata object responsible for configuring Online acquiring parameters is not specified.'"));
	КонецЕсли;
	
	Для Каждого ДанныеНазначения Из Настройки.ШаблоныНазначений Цикл
		
		Если Не ЗначениеЗаполнено(ДанныеНазначения.ОбъектМетаданных) Тогда
			Ошибки.Добавить(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не указан объект метаданных в шаблоне назначения %1.';
						|en = 'The metadata object is not specified in the %1 assignment template.'"),
					ДанныеНазначения.Наименование));
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеНазначения.Идентификатор) Тогда
			Ошибки.Добавить(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не указан идентификатор в шаблоне назначения %1.';
						|en = 'The ID is not specified in the %1 assignment template.'"),
					ДанныеНазначения.Наименование));
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеНазначения.Наименование) Тогда
			Ошибки.Добавить(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не указано наименование в шаблоне назначения %1.';
						|en = 'The description is not specified in the %1 assignment template.'"),
					ДанныеНазначения.ОбъектМетаданных));
		КонецЕсли;
		
		Для Каждого ДанныеПараметра Из ДанныеНазначения.Параметры Цикл
			
			Если Не ЗначениеЗаполнено(ДанныеПараметра.Наименование) Тогда
				Ошибки.Добавить(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не указано наименование параметра в шаблоне назначения %1.';
							|en = 'The parameter description is not specified in the %1 assignment template.'"),
						ДанныеНазначения.ОбъектМетаданных));
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ДанныеПараметра.Идентификатор) Тогда
				Ошибки.Добавить(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не указан идентификатор параметра в шаблоне назначения %1.';
							|en = 'The parameter ID is not specified in the %1 assignment template.'"),
						ДанныеНазначения.ОбъектМетаданных));
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если Ошибки.Количество() > 0 Тогда
		ОшибкиПроверки = СтрСоединить(Ошибки, Символы.ПС);
		ВызватьИсключение ОшибкиПроверки;
	КонецЕсли;
	
КонецПроцедуры

// Формирует настройки перед записью данных в прикладной регистр.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения к сервису.
//  ПрикладныеПараметры - Соответствие Из КлючИЗначение - значения прикладных настроек:
//   * Ключ - Строка - идентификатор реквизита (соответствует имени измерения, ресурса или реквизита прикладного
//       регистра, определяемого в методе ИнтернетЭквайрингПереопределяемый.ПриОпределенииНастроекПодключения).
//   * Значение - Строка - значение реквизита.
//
Процедура ПриФормированииПрикладныхПараметровПередЗаписью(
		НастройкаПодключения,
		ПрикладныеПараметры) Экспорт
		
	ПрикладныеНастройки = ИнтернетЭквайрингСлужебный.ПрикладныеНастройкиПодключения();
	ИмяАтрибутаНастройкаПодключения = ИнтернетЭквайрингСлужебный.АтрибутНастройкаПодключения(
		ПрикладныеНастройки.ОбъектМетаданных);
	ПрикладныеПараметры.Вставить(
		ИмяАтрибутаНастройкаПодключения,
		НастройкаПодключения);
		
КонецПроцедуры

// Определяет поведение системы при смене признака использования настройки подключения.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу -
//    настройка подключения к Интернет-эквайрингу;
//  Используется - Булево - признак использования настройки;
//  ОбновлятьНастройки - Булево - признак необходимости обновлять настройки онлайн-заказов
//  РезультатОперации - Структура- результат операции:
//   * Ошибка - Булево - признак ошибки выполнения операции;
//   * КодОшибки - Строка - строковый код возникшей ошибки, который может быть обработан вызывающим методом.
//   * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//   * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Процедура ПриИзмененииИспользованияНастройки(
	НастройкаПодключения,
	Используется,
	ОбновлятьНастройки,
	РезультатОперации) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОнлайнЗаказы") Тогда
		МодульОнлайнЗаказыСобытия = ОбщегоНазначения.ОбщийМодуль("ОнлайнЗаказыСобытия");
		МодульОнлайнЗаказыСобытия.ПриИзмененииИспользованияНастройкиОплаты(
			НастройкаПодключения,
			Используется,
			"ИнтернетЭквайринг",
			ОбновлятьНастройки,
			РезультатОперации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
