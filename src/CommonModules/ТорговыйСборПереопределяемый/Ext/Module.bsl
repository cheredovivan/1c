#Область СлужебныйПрограммныйИнтерфейс

// Инициализирует таблицу расчета торгового сбора.
//
// Параметры:
// 		ТаблицаРеквизиты - Таблица значений, реквизиты документа "Регламентная операция"
// 		Отказ - Отказ - Булево - Флаг отказа проведения.
// 
// Возвращаемое значение:
// 	 СтруктураТаблиц - Структура - Таблицы расчета налога
// 							Ключи:
// 								ТаблицаРасчетаТорговогоСбора - ТаблицаЗначений - Таблица расчета торгового сбора.
Функция ПодготовитьТаблицыРасчета(ТаблицаРеквизиты, Отказ) Экспорт
	
	ТаблицаПроводок  = НовыйТаблицаПроводок();
	ТаблицаСправкиРасчета = НовыйТаблицаСправкиРасчета();

	Если ТаблицаРеквизиты.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизиты[0];
	Ошибки = Новый Массив;
	
	СтруктураТаблиц = РассчитатьТорговыйСбор(Реквизиты, ТаблицаПроводок, ТаблицаСправкиРасчета, Ошибки, Отказ);

	Возврат СтруктураТаблиц;
	
КонецФункции 

Функция НовыйТаблицаПроводок()
	
	ТаблицаПроводок = Новый ТаблицаЗначений;
	
	ТаблицаПроводок.Колонки.Добавить("Период",
		ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	ТаблицаПроводок.Колонки.Добавить("Организация",
		Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаПроводок.Колонки.Добавить("СчетДт",
		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаПроводок.Колонки.Добавить("Подразделение",
		Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
	ТаблицаПроводок.Колонки.Добавить("Субконто1");
	ТаблицаПроводок.Колонки.Добавить("Субконто2");
	ТаблицаПроводок.Колонки.Добавить("Субконто3");
	ТаблицаПроводок.Колонки.Добавить("СчетКт",
		Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ТаблицаПроводок.Колонки.Добавить("ВидыПлатежейВГосБюджет",
		Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПлатежейВГосБюджет"));
	ТаблицаПроводок.Колонки.Добавить("Сумма",
		ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	ТаблицаПроводок.Колонки.Добавить("Содержание",
		ОбщегоНазначения.ОписаниеТипаСтрока(150));
	
	Возврат ТаблицаПроводок;
	
КонецФункции

Функция НовыйТаблицаСправкиРасчета()

	Возврат РегистрыСведений.РасчетТорговогоСбора.СоздатьНаборЗаписей().Выгрузить();

КонецФункции

// Формирует таблицу территорий на которых осуществление торговой деятельности облагается торговым сбором.
//
// Возвращаемое значение:
//  ТаблицаЗначений - данные льгот. Колонки таблицы:
//    * ОКТМО - Строка (11)- Код по ОКТМО муниципального образования.
//    * Территория - СправочникСсылка.ТерриторииОсуществленияТорговойДеятельности - Категория территории согласно
//        закону №62 от 17 декабря 2014 г.Москва.
//
Функция ПрочитатьТаблицуТерриторий() Экспорт
	
	МакетТерриторий = РегистрыСведений.ПараметрыТорговыхТочек.ПолучитьМакет("ТерриторииОсуществленияТорговойДеятельности");
	СтрокаXML = МакетТерриторий.ПолучитьТекст();
	ТаблицаТерриторий = ОбщегоНазначения.ЗначениеИзСтрокиXML(СтрокаXML);
	
	Возврат ТаблицаТерриторий;
	
КонецФункции

Функция УплачиваетсяТорговыйСбор(Организация, Период) Экспорт
	
	ЭтоОбособленноеПодразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ОбособленноеПодразделение");
	
	Возврат Период > '20150701' И НЕ ЭтоОбособленноеПодразделение И ПолучитьФункциональнуюОпцию("УплачиваетсяТорговыйСбор");
	
КонецФункции

// Формирует движения по оперативным регистрам ПрочиеРасходы и ДвиженияДоходыРасходыПрочиеАктивыПассивы на основании
// проводки по начислению торгового сбора.
//
// Параметры:
//		НачислениеТорговогоСбора - Запись регистра бухгалтерии "Хозрасчетный" с проводками по начислению торгового сбора;
//		Движения - движения документа "РегламентнаяОперация".
//
Процедура ОтразитьУменьшениеНалогаНаПрибыльНаСуммуТорговогоСбораВОператпивныхРегистрах(Организация, Период, СуммаУменьшения, Движения) Экспорт
	
	Если СуммаУменьшения = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Распределим сумму сторно между складами пропорционально суммам начисленного налога.
	РасчетПоСбору = РасчетТорговогоСбора(Организация, НачалоКвартала(Период), КонецМесяца(Период));
	СуммыСторно = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(СуммаУменьшения, РасчетПоСбору.ВыгрузитьКолонку("СуммаСбора"), 0); // Налог исчисляется в целых рублях
	РасчетПоСбору.Колонки.Добавить("СуммаСторно", ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля());
	РасчетПоСбору.ЗагрузитьКолонку(СуммыСторно, "СуммаСторно");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(РасчетПоСбору.ТорговаяТочка КАК Справочник.ТорговыеТочки) КАК ТорговаяТочка,
	|	РасчетПоСбору.СуммаСторно КАК СуммаРегл
	|ПОМЕСТИТЬ ВТРасчетПоСбору
	|ИЗ
	|	&РасчетПоСбору КАК РасчетПоСбору
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВТРасчетПоСбору.ТорговаяТочка КАК ТорговаяТочка,
	|	0 КАК Сумма,
	|	ВТРасчетПоСбору.СуммаРегл КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА &ЕстьПостояннаяРазница
	|			ТОГДА ВТРасчетПоСбору.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПостояннаяРазница,
	|	Склады.Подразделение КАК Подразделение,
	|	ТорговыеТочки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	Склады.Ссылка КАК АналитикаРасходов,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ПОМЕСТИТЬ ВТТаблицаРасчета
	|ИЗ
	|	ВТРасчетПоСбору КАК ВТРасчетПоСбору
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТорговыеТочки КАК ТорговыеТочки
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|			ПО ТорговыеТочки.Склад = Склады.Ссылка
	|		ПО ВТРасчетПоСбору.ТорговаяТочка = ТорговыеТочки.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВТТаблицаРасчета.Период КАК Период,
	|	ВТТаблицаРасчета.Организация КАК Организация,
	|	ВТТаблицаРасчета.Подразделение КАК Подразделение,
	|	ВТТаблицаРасчета.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВТТаблицаРасчета.СтатьяРасходов КАК СтатьяРасходов,
	|	ВТТаблицаРасчета.АналитикаРасходов КАК АналитикаРасходов,
	|	ВТТаблицаРасчета.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	СУММА(ВТТаблицаРасчета.Сумма) КАК Сумма,
	|	СУММА(ВТТаблицаРасчета.СуммаРегл) КАК СуммаРегл,
	|	0 КАК СуммаБезНДС,
	|	СУММА(ВТТаблицаРасчета.ПостояннаяРазница) КАК ПостояннаяРазница,
	|	СУММА(ВТТаблицаРасчета.Сумма) КАК СуммаУпр,
	|	НастройкиХозяйственныхОпераций.Ссылка КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВТТаблицаРасчета КАК ВТТаблицаРасчета
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|		ПО ВТТаблицаРасчета.ХозяйственнаяОперация = НастройкиХозяйственныхОпераций.ХозяйственнаяОперация
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТаблицаРасчета.Период,
	|	ВТТаблицаРасчета.Организация,
	|	ВТТаблицаРасчета.Подразделение,
	|	ВТТаблицаРасчета.НаправлениеДеятельности,
	|	ВТТаблицаРасчета.СтатьяРасходов,
	|	ВТТаблицаРасчета.АналитикаРасходов,
	|	ВТТаблицаРасчета.ХозяйственнаяОперация,
	|	НастройкиХозяйственныхОпераций.Ссылка
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВТТаблицаРасчета.СуммаРегл) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВТТаблицаРасчета.Период,
	|	ВТТаблицаРасчета.Организация,
	|	ВТТаблицаРасчета.Подразделение,
	|	ВТТаблицаРасчета.НаправлениеДеятельности,
	|	ВТТаблицаРасчета.СтатьяРасходов,
	|	ВТТаблицаРасчета.АналитикаРасходов,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКосвенныхРасходов),
	|	СУММА(ВТТаблицаРасчета.Сумма),
	|	СУММА(ВТТаблицаРасчета.СуммаРегл),
	|	0,
	|	СУММА(ВТТаблицаРасчета.ПостояннаяРазница),
	|	СУММА(ВТТаблицаРасчета.Сумма),
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеКосвенныхРасходов)
	|ИЗ
	|	ВТТаблицаРасчета КАК ВТТаблицаРасчета
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТаблицаРасчета.Период,
	|	ВТТаблицаРасчета.Организация,
	|	ВТТаблицаРасчета.Подразделение,
	|	ВТТаблицаРасчета.НаправлениеДеятельности,
	|	ВТТаблицаРасчета.СтатьяРасходов,
	|	ВТТаблицаРасчета.АналитикаРасходов,
	|	ВТТаблицаРасчета.ХозяйственнаяОперация
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВТТаблицаРасчета.СуммаРегл) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВТТаблицаРасчета.Период КАК Период,
	|	ВТТаблицаРасчета.Организация КАК Организация,
	|	ВТТаблицаРасчета.Подразделение КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	ВТТаблицаРасчета.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&СтатьяАктивовПассивов КАК Статья,
	|	&АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	ВТТаблицаРасчета.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВТТаблицаРасчета.СтатьяРасходов КАК КорСтатья,
	|	ВТТаблицаРасчета.АналитикаРасходов КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК КорАналитикаАктивовПассивов,
	|	СУММА(ВТТаблицаРасчета.Сумма) КАК Сумма,
	|	СУММА(ВТТаблицаРасчета.СуммаРегл) КАК СуммаРегл,
	|	ВТТаблицаРасчета.Организация.ВалютаРегламентированногоУчета КАК Валюта,
	|	СУММА(ВТТаблицаРасчета.СуммаРегл) КАК СуммаВВалюте
	|ИЗ
	|	ВТТаблицаРасчета КАК ВТТаблицаРасчета
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТаблицаРасчета.Период,
	|	ВТТаблицаРасчета.Организация,
	|	ВТТаблицаРасчета.Подразделение,
	|	ВТТаблицаРасчета.НаправлениеДеятельности,
	|	ВТТаблицаРасчета.СтатьяРасходов,
	|	ВТТаблицаРасчета.АналитикаРасходов,
	|	ВТТаблицаРасчета.ХозяйственнаяОперация
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВТТаблицаРасчета.СуммаРегл) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВТТаблицаРасчета.Период,
	|	ВТТаблицаРасчета.Организация,
	|	ВТТаблицаРасчета.Подразделение,
	|	ВТТаблицаРасчета.Подразделение,
	|	ВТТаблицаРасчета.НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКосвенныхРасходов),
	|	ВТТаблицаРасчета.СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО,
	|	ВТТаблицаРасчета.АналитикаРасходов,
	|	ВТТаблицаРасчета.НаправлениеДеятельности,
	|	&СтатьяАктивовПассивов,
	|	ВТТаблицаРасчета.АналитикаРасходов,
	|	&АналитикаАктивовПассивов,
	|	СУММА(ВТТаблицаРасчета.Сумма),
	|	СУММА(ВТТаблицаРасчета.СуммаРегл),
	|	ВТТаблицаРасчета.Организация.ВалютаРегламентированногоУчета,
	|	СУММА(ВТТаблицаРасчета.СуммаРегл)
	|ИЗ
	|	ВТТаблицаРасчета КАК ВТТаблицаРасчета
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТаблицаРасчета.Период,
	|	ВТТаблицаРасчета.Организация,
	|	ВТТаблицаРасчета.Подразделение,
	|	ВТТаблицаРасчета.НаправлениеДеятельности,
	|	ВТТаблицаРасчета.СтатьяРасходов,
	|	ВТТаблицаРасчета.АналитикаРасходов,
	|	ВТТаблицаРасчета.ХозяйственнаяОперация
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВТТаблицаРасчета.СуммаРегл) <> 0";
	
	Запрос.УстановитьПараметр("РасчетПоСбору",            РасчетПоСбору);
	Запрос.УстановитьПараметр("Период",                   Период);
	Запрос.УстановитьПараметр("Организация",              Организация);
	Запрос.УстановитьПараметр("ПустаяТорговаяТочка",      Справочники.ТорговыеТочки.ПустаяСсылка());
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",    Перечисления.ХозяйственныеОперации.НачислениеТорговогоСбора);
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихАктивовПассивов") Тогда
		Запрос.УстановитьПараметр("СтатьяАктивовПассивов", ПланыВидовХарактеристик.СтатьиАктивовПассивов.Налоги);
	Иначе
		Запрос.УстановитьПараметр("СтатьяАктивовПассивов", ПланыВидовХарактеристик.СтатьиАктивовПассивов.УпрБалансПрочиеПассивы);
	КонецЕсли;
	Запрос.УстановитьПараметр("СтатьяРасходов",           ПланыВидовХарактеристик.СтатьиРасходов.ТорговыйСбор);
	Запрос.УстановитьПараметр("АналитикаАктивовПассивов", Справочники.ВидыНалоговВзносов.ТорговыйСбор);
	Запрос.УстановитьПараметр("ИспользоватьУчетПрочихДоходовРасходов", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов"));
	Запрос.УстановитьПараметр("ЕстьПостояннаяРазница", (УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Период) И УчетнаяПолитика.ПоддержкаПБУ18(Организация, Период)));
	Результат = Запрос.ВыполнитьПакет();
	
	Если Не Результат.Получить(2).Пустой() Тогда
		ТаблицаПрочиеРасходы = Результат[2].Выгрузить();
		ОбщегоНазначенияУТ.ДобавитьИдентификаторыВТаблицуЗначений(ТаблицаПрочиеРасходы);
		Движения.ПрочиеРасходы.Загрузить(ТаблицаПрочиеРасходы);
		Движения.ПрочиеРасходы.Записывать = Истина;
	КонецЕсли;
	
	Если Не Результат.Получить(3).Пустой() Тогда
		Движения.ДвиженияДоходыРасходыПрочиеАктивыПассивы.Загрузить(Результат[3].Выгрузить());
		Движения.ДвиженияДоходыРасходыПрочиеАктивыПассивы.Записывать = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РассчитатьТорговыйСбор(Реквизиты, ТаблицаПроводок, ТаблицаСправкиРасчета, Ошибки, Отказ)
	
	СтруктураТаблиц = Новый Структура;
	СтруктураТаблиц.Вставить("ТаблицаПрочиеРасходы",                            Новый ТаблицаЗначений);
	СтруктураТаблиц.Вставить("ТаблицаПрочиеАктивыПассивы",                      Новый ТаблицаЗначений);
	СтруктураТаблиц.Вставить("ТаблицаДвиженияПоПрочимАктивамПассивам",          Новый ТаблицаЗначений);
	СтруктураТаблиц.Вставить("ТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы", Новый ТаблицаЗначений);
	
	// Федеральный закон № 172-ФЗ от 08.06.2020
	// В 2020 году некоторые организации и ИП освобождаются от уплаты сбора за 2-й квартал
	// В этом случае проводки по начислению сбора не формируем.
	ПериодОсвобожденияОтНалогов = НалоговыйУчет.ПериодОсвобожденияОтНалоговПострадавшимОтКоронавируса();
	Если НалоговыйУчет.ДеятельностьОтнесенаКПострадавшимОтКоронавируса(Реквизиты.Организация)
		 И ПериодОсвобожденияОтНалогов.Начало <= Реквизиты.КонДата 
		 И Реквизиты.КонДата <= ПериодОсвобожденияОтНалогов.Конец Тогда
		Возврат СтруктураТаблиц;
	КонецЕсли;
	
	РасчетПоСбору = РасчетТорговогоСбора(Реквизиты.Организация, Реквизиты.НачДата, Реквизиты.КонДата);
	
	Если РасчетПоСбору.Количество() = 0 Тогда
		Возврат СтруктураТаблиц;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	
	ВалютаРегламентированногоУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Реквизиты.Организация);
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("РасчетПоСбору",            РасчетПоСбору);
	Запрос.УстановитьПараметр("Период",                   Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация",              Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПустаяТорговаяТочка",      Справочники.ТорговыеТочки.ПустаяСсылка());
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",    Перечисления.ХозяйственныеОперации.НачислениеТорговогоСбора);
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихАктивовПассивов") Тогда
		Запрос.УстановитьПараметр("СтатьяАктивовПассивов", ПланыВидовХарактеристик.СтатьиАктивовПассивов.Налоги);
	Иначе
		Запрос.УстановитьПараметр("СтатьяАктивовПассивов", ПланыВидовХарактеристик.СтатьиАктивовПассивов.УпрБалансПрочиеПассивы);
	КонецЕсли;
	Запрос.УстановитьПараметр("СтатьяРасходов",           ПланыВидовХарактеристик.СтатьиРасходов.ТорговыйСбор);
	Запрос.УстановитьПараметр("АналитикаАктивовПассивов", Справочники.ВидыНалоговВзносов.ТорговыйСбор);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("ИспользоватьУчетПрочихДоходовРасходов", 
										ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов"));
	Запрос.УстановитьПараметр("КурсВалютыУправленческогоУчета", 
										РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыУправленческогоУчета(
											Реквизиты.Период, ВалютаРегламентированногоУчета));
	Запрос.УстановитьПараметр("ЕстьПостояннаяРазница", 
										(УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период)
											И УчетнаяПолитика.ПоддержкаПБУ18(Реквизиты.Организация, Реквизиты.Период)));
	Запрос.УстановитьПараметр("УправленческийУчетОрганизаций", РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций(Реквизиты.Период));
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РасчетПоСбору.Период КАК Период,
	|	ВЫРАЗИТЬ(РасчетПоСбору.ТорговаяТочка КАК Справочник.ТорговыеТочки) КАК ТорговаяТочка,
	|	РасчетПоСбору.СуммаСбора  КАК СуммаРегл
	|ПОМЕСТИТЬ ВТРасчетПоСбору
	|ИЗ
	|	&РасчетПоСбору КАК РасчетПоСбору
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРасчетПоСбору.Период                    КАК Период,
	|	&Организация                              КАК Организация,
	|	ВТРасчетПоСбору.ТорговаяТочка             КАК ТорговаяТочка,
	|	Склады.Подразделение                      КАК Подразделение,
	|	ТорговыеТочки.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	&СтатьяРасходов                           КАК СтатьяРасходов,
	|	Склады.Ссылка                             КАК АналитикаРасходов,
	|	&ХозяйственнаяОперация                    КАК ХозяйственнаяОперация,
	|	СУММА(ВЫБОР
	|		КОГДА &КурсВалютыУправленческогоУчета <> 0
	|			ТОГДА ВТРасчетПоСбору.СуммаРегл / &КурсВалютыУправленческогоУчета
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                    КАК Сумма,
	|	СУММА(ВТРасчетПоСбору.СуммаРегл)          КАК СуммаРегл,
	|	СУММА(ВЫБОР
	|		КОГДА &ЕстьПостояннаяРазница
	|			ТОГДА ВТРасчетПоСбору.СуммаРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                    КАК ПостояннаяРазница
	|ПОМЕСТИТЬ ВТТаблицаРасчета
	|ИЗ
	|	ВТРасчетПоСбору КАК ВТРасчетПоСбору
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТорговыеТочки КАК ТорговыеТочки
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|			ПО ТорговыеТочки.Склад = Склады.Ссылка
	|		ПО ВТРасчетПоСбору.ТорговаяТочка = ТорговыеТочки.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВТРасчетПоСбору.Период,
	|	ВТРасчетПоСбору.ТорговаяТочка,
	|	Склады.Подразделение,
	|	ТорговыеТочки.НаправлениеДеятельности,
	|	Склады.Ссылка
	|";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
	ОбщегоНазначенияУТ.ДобавитьИдентификаторыВоВременнуюТаблицу("ВТТаблицаРасчета", МенеджерВременныхТаблиц);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ВТТаблицаРасчета.Период                   КАК Период,
	|	ВТТаблицаРасчета.Организация              КАК Организация,
	|	ВТТаблицаРасчета.Подразделение            КАК Подразделение,
	|	ВТТаблицаРасчета.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВТТаблицаРасчета.СтатьяРасходов           КАК СтатьяРасходов,
	|	ВТТаблицаРасчета.АналитикаРасходов        КАК АналитикаРасходов,
	|	ВТТаблицаРасчета.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|	ВТТаблицаРасчета.Сумма                    КАК Сумма,
	|	ВТТаблицаРасчета.СуммаРегл                КАК СуммаРегл,
	|	0                                         КАК СуммаБезНДС,
	|	ВТТаблицаРасчета.ПостояннаяРазница        КАК ПостояннаяРазница,
	|	ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций ТОГДА ВТТаблицаРасчета.Сумма
	|		ИНАЧЕ 0 КОНЕЦ                         КАК СуммаУпр,
	|	ВТТаблицаРасчета.ИдентификаторСтроки      КАК ИдентификаторФинЗаписи,
	|	НастройкиХозяйственныхОпераций.Ссылка     КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВТТаблицаРасчета КАК ВТТаблицаРасчета
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|		ПО ВТТаблицаРасчета.ХозяйственнаяОперация = НастройкиХозяйственныхОпераций.ХозяйственнаяОперация
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|	И ВТТаблицаРасчета.СуммаРегл <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВТТаблицаРасчета.Период                КАК Период,
	|	ВТТаблицаРасчета.Организация           КАК Организация,
	|	ВТТаблицаРасчета.Подразделение         КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                           КАК НаправлениеДеятельности,
	|	&СтатьяАктивовПассивов                 КАК Статья,
	|	&АналитикаАктивовПассивов              КАК Аналитика,
	|	СУММА(ВТТаблицаРасчета.Сумма)          КАК Сумма
	|ИЗ
	|	ВТТаблицаРасчета КАК ВТТаблицаРасчета
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТаблицаРасчета.Период,
	|	ВТТаблицаРасчета.Организация,
	|	ВТТаблицаРасчета.Подразделение,
	|	ВТТаблицаРасчета.НаправлениеДеятельности
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВТТаблицаРасчета.Сумма) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТаблицаРасчета.Период                   КАК Период,
	|	ВТТаблицаРасчета.Организация              КАК Организация,
	|	ВТТаблицаРасчета.Подразделение            КАК Подразделение,
	|	ВТТаблицаРасчета.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	&СтатьяАктивовПассивов                    КАК Статья,
	|	&АналитикаАктивовПассивов                 КАК Аналитика,
	|	НЕОПРЕДЕЛЕНО                              КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|	ВТТаблицаРасчета.СуммаРегл                КАК СуммаРегл,
	|	ВТТаблицаРасчета.ПостояннаяРазница        КАК ПостояннаяРазница,
	|	ВТТаблицаРасчета.Сумма                    КАК СуммаУпр,
	|	ВТТаблицаРасчета.Сумма                    КАК СуммаСНДС,
	|	ВТТаблицаРасчета.Сумма                    КАК СуммаБезНДС,
	|	ВТТаблицаРасчета.ИдентификаторСтроки      КАК ИдентификаторФинЗаписи,
	|	НастройкиХозяйственныхОпераций.Ссылка     КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВТТаблицаРасчета КАК ВТТаблицаРасчета
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|		ПО ВТТаблицаРасчета.ХозяйственнаяОперация = НастройкиХозяйственныхОпераций.ХозяйственнаяОперация
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|	И ВТТаблицаРасчета.СуммаРегл <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТаблицаРасчета.Период                КАК Период,
	|	ВТТаблицаРасчета.Организация           КАК Организация,
	|	ВТТаблицаРасчета.Подразделение         КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                           КАК НаправлениеДеятельности,
	|	ВТТаблицаРасчета.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&СтатьяАктивовПассивов                 КАК Статья,
	|	&АналитикаАктивовПассивов              КАК АналитикаАктивовПассивов,
	|	ВТТаблицаРасчета.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВТТаблицаРасчета.СтатьяРасходов        КАК КорСтатья,
	|	ВТТаблицаРасчета.АналитикаРасходов     КАК КорАналитикаРасходов,
	|	СУММА(ВТТаблицаРасчета.Сумма)          КАК Сумма,
	|	СУММА(ВТТаблицаРасчета.СуммаРегл)      КАК СуммаРегл,
	|	&ВалютаРегламентированногоУчета        КАК Валюта,
	|	СУММА(ВТТаблицаРасчета.СуммаРегл)      КАК СуммаВВалюте,
	|	СУММА(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА ВТТаблицаРасчета.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                 КАК СуммаУпр
	|ИЗ
	|	ВТТаблицаРасчета КАК ВТТаблицаРасчета
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТаблицаРасчета.Период,
	|	ВТТаблицаРасчета.Организация,
	|	ВТТаблицаРасчета.Подразделение,
	|	ВТТаблицаРасчета.НаправлениеДеятельности,
	|	ВТТаблицаРасчета.СтатьяРасходов,
	|	ВТТаблицаРасчета.АналитикаРасходов,
	|	ВТТаблицаРасчета.ХозяйственнаяОперация
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВТТаблицаРасчета.СуммаРегл) <> 0
	|";
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.ВыполнитьПакет();
	
	СтруктураТаблиц.Вставить("ТаблицаПрочиеРасходы",                            Результат[0].Выгрузить());
	СтруктураТаблиц.Вставить("ТаблицаПрочиеАктивыПассивы",                      Результат[1].Выгрузить());
	СтруктураТаблиц.Вставить("ТаблицаДвиженияПоПрочимАктивамПассивам",          Результат[2].Выгрузить());
	СтруктураТаблиц.Вставить("ТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы", Результат[3].Выгрузить());
	
	Возврат СтруктураТаблиц;
	
КонецФункции

Процедура СформироватьДвиженияРасчета(ТаблицыТорговыйСбор, Движения, Отказ) Экспорт

	Если ТаблицыТорговыйСбор.ТаблицаПрочиеРасходы.Количество() <> 0 Тогда
		Движения.ПрочиеРасходы.Записывать = Истина;
		Движения.ПрочиеРасходы.Загрузить(ТаблицыТорговыйСбор.ТаблицаПрочиеРасходы);
	КонецЕсли;
	
	Если ТаблицыТорговыйСбор.ТаблицаПрочиеАктивыПассивы.Количество() <> 0 Тогда
		Движения.ПрочиеАктивыПассивы.Записывать = Истина;
		Движения.ПрочиеАктивыПассивы.Загрузить(ТаблицыТорговыйСбор.ТаблицаПрочиеАктивыПассивы);
	КонецЕсли;
		
	Если ТаблицыТорговыйСбор.ТаблицаДвиженияПоПрочимАктивамПассивам.Количество() <> 0 Тогда
		Движения.ДвиженияПоПрочимАктивамПассивам.Записывать = Истина;
		Движения.ДвиженияПоПрочимАктивамПассивам.Загрузить(ТаблицыТорговыйСбор.ТаблицаДвиженияПоПрочимАктивамПассивам);
	КонецЕсли;

	Если ТаблицыТорговыйСбор.ТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы.Количество() <> 0 Тогда
		Движения.ДвиженияДоходыРасходыПрочиеАктивыПассивы.Записывать = Истина;
		Движения.ДвиженияДоходыРасходыПрочиеАктивыПассивы.Загрузить(ТаблицыТорговыйСбор.ТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы);
	КонецЕсли;
	
КонецПроцедуры

// Формирует текст запроса для рачета торгового сбора для периода каждой регоперации, чтобы заполнить регистр РасчетТорговогоСбора.
//
// Возвращаемое значение:
//  ТекстЗапроса - Строка.
//
Функция ТекстЗапросаРасчетТорговогоСбора() Экспорт
	
	ТекстЗапроса = "ВЫБРАТЬ // Отберем только те операции по которым нет записей регистра
	|	МАКСИМУМ(РегламентнаяОперация.Ссылка) КАК РегламентнаяОперацияСсылка,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ) КАК ПериодРасчета,
	|	РегламентнаяОперация.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_РегламентныеОперации
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасчетТорговогоСбора КАК РасчетТорговогоСбора
	|		ПО РегламентнаяОперация.Ссылка = РасчетТорговогоСбора.Регистратор
	|ГДЕ
	|	РегламентнаяОперация.Проведен
	|	И РегламентнаяОперация.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетТорговогоСбора)
	|	И РасчетТорговогоСбора.Регистратор ЕСТЬ NULL
	|	И КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ) = КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, МЕСЯЦ)
	|СГРУППИРОВАТЬ ПО
	|	РегламентнаяОперация.Организация,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // Выберем параметры торговых точек на момент каждой регоперации
	|	ПараметрыТорговыхТочек.Организация КАК Организация,
	|	ПараметрыТорговыхТочек.ТорговаяТочка КАК ТорговаяТочка,
	|	ВТ_РегламентныеОперации.ПериодРасчета КАК ПериодРасчета,
	|	ПараметрыТорговыхТочек.Период КАК ПериодЗаписи,
	|	ПараметрыТорговыхТочек.Ставка КАК Ставка,
	|	ПараметрыТорговыхТочек.СуммаЛьготы КАК СуммаЛьготы,
	|	ПараметрыТорговыхТочек.СуммаСбора КАК СуммаСбора,
	|	ПараметрыТорговыхТочек.РасшифровкаРасчета КАК РасшифровкаРасчета
	|ПОМЕСТИТЬ ВТ_ПараметрыТорговыхТочекПоПериодам
	|ИЗ
	|	ВТ_РегламентныеОперации КАК ВТ_РегламентныеОперации
	|		// Параметры торговых точек до конца периода регоперации - из них будем брать последнюю
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыТорговыхТочек КАК ПараметрыТорговыхТочек 
	|		ПО ВТ_РегламентныеОперации.ПериодРасчета >= ПараметрыТорговыхТочек.Период
	|			И ВТ_РегламентныеОперации.Организация = ПараметрыТорговыхТочек.Организация
	|		// запись на начало периода - это всегда изменение ставки, она учитывается всегда в текущем периоде
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыТорговыхТочек КАК ПараметрыТорговыхТочекНачалоПериода 
	|		ПО (ПараметрыТорговыхТочекНачалоПериода.Период = НАЧАЛОПЕРИОДА(ВТ_РегламентныеОперации.ПериодРасчета, КВАРТАЛ))
	|			И (ПараметрыТорговыхТочекНачалоПериода.Организация = ПараметрыТорговыхТочек.Организация)
	|			И (ПараметрыТорговыхТочекНачалоПериода.ТорговаяТочка = ПараметрыТорговыхТочек.ТорговаяТочка)
	|ГДЕ
	|	// не берем те записи, сумма сбора которых меньше чем была на начало периода, такие записи будут учитываться только со следующего квартала
	|	(КОНЕЦПЕРИОДА(ПараметрыТорговыхТочек.Период, КВАРТАЛ) <> ВТ_РегламентныеОперации.ПериодРасчета 
	|			ИЛИ ПараметрыТорговыхТочек.СуммаСбора >= ЕСТЬNULL(ПараметрыТорговыхТочекНачалоПериода.СуммаСбора, ПараметрыТорговыхТочек.СуммаСбора))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ТорговаяТочка,
	|	ПериодЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Для каждой регоперации получим дату записи параметров торговой точки для СрезПервых на начало квартала
	|// В дальнейшем получим сумму сбора как максимум от суммы на начало периода и всех записей за период
	|ВЫБРАТЬ
	|	ВТ_ПараметрыТорговыхТочекПоПериодам.Организация КАК Организация,
	|	ВТ_ПараметрыТорговыхТочекПоПериодам.ТорговаяТочка КАК ТорговаяТочка,
	|	ВТ_ПараметрыТорговыхТочекПоПериодам.ПериодРасчета КАК ПериодРасчета, // дата регоперации
	|	МАКСИМУМ(ВТ_ПараметрыТорговыхТочекПоПериодам.ПериодЗаписи) КАК ПериодЗаписи // дата записи 
	|ПОМЕСТИТЬ ВТ_ПараметрыТорговыхТочекСрезПервых
	|ИЗ
	|	ВТ_ПараметрыТорговыхТочекПоПериодам КАК ВТ_ПараметрыТорговыхТочекПоПериодам
	|ГДЕ
	|	ВТ_ПараметрыТорговыхТочекПоПериодам.ПериодЗаписи <= НАЧАЛОПЕРИОДА(ВТ_ПараметрыТорговыхТочекПоПериодам.ПериодРасчета, КВАРТАЛ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПараметрыТорговыхТочекПоПериодам.Организация,
	|	ВТ_ПараметрыТорговыхТочекПоПериодам.ТорговаяТочка,
	|	ВТ_ПараметрыТорговыхТочекПоПериодам.ПериодРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПараметрыТорговыхТочекСрезПервых.Организация КАК Организация,
	|	ВТ_ПараметрыТорговыхТочекСрезПервых.ТорговаяТочка КАК ТорговаяТочка,
	|	ВТ_ПараметрыТорговыхТочекСрезПервых.ПериодРасчета КАК ПериодРасчета,
	|	МАКСИМУМ(ВТ_ПараметрыТорговыхТочекПоПериодам.СуммаСбора) КАК СуммаСбора // сумма сбора - максимальная сумма среди записи на начало периода и всех записей за период
	|ПОМЕСТИТЬ ВТ_СуммаСбораЗаКвартал
	|ИЗ
	|	ВТ_ПараметрыТорговыхТочекСрезПервых КАК ВТ_ПараметрыТорговыхТочекСрезПервых
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыТорговыхТочекПоПериодам КАК ВТ_ПараметрыТорговыхТочекПоПериодам
	|		ПО ВТ_ПараметрыТорговыхТочекСрезПервых.Организация = ВТ_ПараметрыТорговыхТочекПоПериодам.Организация
	|			И ВТ_ПараметрыТорговыхТочекСрезПервых.ТорговаяТочка = ВТ_ПараметрыТорговыхТочекПоПериодам.ТорговаяТочка
	|			И ВТ_ПараметрыТорговыхТочекСрезПервых.ПериодРасчета = ВТ_ПараметрыТорговыхТочекПоПериодам.ПериодРасчета
	|			И ВТ_ПараметрыТорговыхТочекСрезПервых.ПериодЗаписи <= ВТ_ПараметрыТорговыхТочекПоПериодам.ПериодЗаписи
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПараметрыТорговыхТочекСрезПервых.Организация,
	|	ВТ_ПараметрыТорговыхТочекСрезПервых.ТорговаяТочка,
	|	ВТ_ПараметрыТорговыхТочекСрезПервых.ПериодРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Найдем запись параметров торговой точки из предыдущего шага, чтобы записать в регистр дату установки параметров
	|// Так как записей с такой суммой может быть несколько (например если течении квартала площадь уменьшалась и увеличивалась несколько раз),
	|// то возьмем последнюю из них
	|ВЫБРАТЬ
	|	ВТ_СуммаСбораЗаКвартал.Организация КАК Организация,
	|	ВТ_СуммаСбораЗаКвартал.ТорговаяТочка КАК ТорговаяТочка,
	|	ВТ_СуммаСбораЗаКвартал.ПериодРасчета КАК ПериодРасчета,
	|	МАКСИМУМ(ВТ_ПараметрыТорговыхТочекПоПериодам.ПериодЗаписи) КАК ПериодЗаписи
	|ПОМЕСТИТЬ ВТ_ПараметрыТорговыхТочекСрезПоследних
	|ИЗ
	|	ВТ_СуммаСбораЗаКвартал КАК ВТ_СуммаСбораЗаКвартал
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыТорговыхТочекПоПериодам КАК ВТ_ПараметрыТорговыхТочекПоПериодам
	|		ПО ВТ_СуммаСбораЗаКвартал.Организация = ВТ_ПараметрыТорговыхТочекПоПериодам.Организация
	|			И ВТ_СуммаСбораЗаКвартал.ТорговаяТочка = ВТ_ПараметрыТорговыхТочекПоПериодам.ТорговаяТочка
	|			И ВТ_СуммаСбораЗаКвартал.ПериодРасчета = ВТ_ПараметрыТорговыхТочекПоПериодам.ПериодРасчета
	|			И ВТ_СуммаСбораЗаКвартал.СуммаСбора = ВТ_ПараметрыТорговыхТочекПоПериодам.СуммаСбора
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СуммаСбораЗаКвартал.Организация,
	|	ВТ_СуммаСбораЗаКвартал.ТорговаяТочка,
	|	ВТ_СуммаСбораЗаКвартал.ПериодРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РегламентныеОперации.РегламентнаяОперацияСсылка КАК РегламентнаяОперацияСсылка,
	|	ВТ_ПараметрыТорговыхТочекСрезПоследних.Организация КАК Организация,
	|	ВТ_ПараметрыТорговыхТочекСрезПоследних.ТорговаяТочка КАК ТорговаяТочка,
	|	ВТ_ПараметрыТорговыхТочекСрезПоследних.ПериодРасчета КАК ПериодРасчета, // период регоперации
	|	ВТ_ПараметрыТорговыхТочекСрезПоследних.ПериодЗаписи КАК ДатаРегистрационныхДанных, // дата когда были установлены параметры по которым произведен расчет
	|	ВТ_ПараметрыТорговыхТочекПоПериодам.Ставка КАК Ставка,
	|	ВТ_ПараметрыТорговыхТочекПоПериодам.СуммаЛьготы КАК СуммаЛьготы,
	|	ВТ_ПараметрыТорговыхТочекПоПериодам.СуммаСбора КАК СуммаСбора,
	|	ВТ_ПараметрыТорговыхТочекПоПериодам.РасшифровкаРасчета КАК РасшифровкаРасчета
	|ИЗ
	|	ВТ_ПараметрыТорговыхТочекСрезПоследних КАК ВТ_ПараметрыТорговыхТочекСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыТорговыхТочекПоПериодам КАК ВТ_ПараметрыТорговыхТочекПоПериодам
	|		ПО ВТ_ПараметрыТорговыхТочекСрезПоследних.Организация = ВТ_ПараметрыТорговыхТочекПоПериодам.Организация
	|			И ВТ_ПараметрыТорговыхТочекСрезПоследних.ТорговаяТочка = ВТ_ПараметрыТорговыхТочекПоПериодам.ТорговаяТочка
	|			И ВТ_ПараметрыТорговыхТочекСрезПоследних.ПериодРасчета = ВТ_ПараметрыТорговыхТочекПоПериодам.ПериодРасчета
	|			И ВТ_ПараметрыТорговыхТочекСрезПоследних.ПериодЗаписи = ВТ_ПараметрыТорговыхТочекПоПериодам.ПериодЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегламентныеОперации КАК ВТ_РегламентныеОперации
	|		ПО ВТ_ПараметрыТорговыхТочекСрезПоследних.Организация = ВТ_РегламентныеОперации.Организация
	|			И ВТ_ПараметрыТорговыхТочекСрезПоследних.ПериодРасчета = ВТ_РегламентныеОперации.ПериодРасчета
	|ИТОГИ ПО
	|	РегламентнаяОперацияСсылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция РасчетТорговогоСбора(Организация, ДатаНачалаПериода, ДатаОкончанияПериода)

	Запрос = Новый Запрос;
	
	// В случае, когда в текущем периоде происходит уменьшение суммы торгового сбора, то такое изменение начинает действовать только с начала следующего периода.
	// Для этого получаем сумму сбора на конец последней даты предыдущего периода, 
	// так как сумма полученная на начало текущего периода учитывает возможные изменения внесенные первой датой текущего периода
	// и может привести к занижению суммы торгового сбора.
	Запрос.УстановитьПараметр("ДатаОкончанияПредыдущегоПериода", ДатаНачалаПериода-1);
	Запрос.УстановитьПараметр("ДатаНачалаПериода", ДатаНачалаПериода);
	Запрос.УстановитьПараметр("ДатаОкончанияПериода", ДатаОкончанияПериода);
	Запрос.УстановитьПараметр("Организация", ОбщегоНазначенияБПВызовСервераПовтИсп.ВсяОрганизация(Организация));
	Запрос.УстановитьПараметр("ВидОперацииСнятиеСУчета", Перечисления.ВидыОперацийТорговыеТочки.СнятиеСУчета);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ДатаНачалаПериода КАК Период,
	|	ПараметрыТорговыхТочекНаНачалоПериода.Период КАК ДатаРегистрационныхДанных,
	|	ПараметрыТорговыхТочекНаНачалоПериода.Организация КАК Организация,
	|	ПараметрыТорговыхТочекНаНачалоПериода.ТорговаяТочка КАК ТорговаяТочка,
	|	ПараметрыТорговыхТочекНаНачалоПериода.КодПоОКТМО КАК КодПоОКТМО,
	|	ПараметрыТорговыхТочекНаНачалоПериода.ВидТорговойДеятельности КАК ВидТорговойДеятельности,
	|	ПараметрыТорговыхТочекНаНачалоПериода.ТипТорговойТочки КАК ТипТорговойТочки,
	|	ПараметрыТорговыхТочекНаНачалоПериода.ПлощадьТорговогоЗала КАК ПлощадьТорговогоЗала,
	|	ПараметрыТорговыхТочекНаНачалоПериода.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
	|	ИСТИНА КАК ЭтоЗаписьНачалоПериода
	|ИЗ
	|	РегистрСведений.ПараметрыТорговыхТочек.СрезПоследних(&ДатаОкончанияПредыдущегоПериода, Организация В (&Организация)) КАК ПараметрыТорговыхТочекНаНачалоПериода
	|ГДЕ
	|	НЕ ПараметрыТорговыхТочекНаНачалоПериода.ТорговаяТочка.ПометкаУдаления
	|	И НЕ ПараметрыТорговыхТочекНаНачалоПериода.ВидОперации = &ВидОперацииСнятиеСУчета
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ПараметрыТорговыхТочек.Период,
	|	ПараметрыТорговыхТочек.Период,
	|	ПараметрыТорговыхТочек.Организация,
	|	ПараметрыТорговыхТочек.ТорговаяТочка,
	|	ПараметрыТорговыхТочек.КодПоОКТМО,
	|	ПараметрыТорговыхТочек.ВидТорговойДеятельности,
	|	ПараметрыТорговыхТочек.ТипТорговойТочки,
	|	ПараметрыТорговыхТочек.ПлощадьТорговогоЗала,
	|	ПараметрыТорговыхТочек.КодНалоговойЛьготы,
	|	ЛОЖЬ
	|ИЗ
	|	РегистрСведений.ПараметрыТорговыхТочек КАК ПараметрыТорговыхТочек
	|ГДЕ
	|	ПараметрыТорговыхТочек.Период МЕЖДУ &ДатаНачалаПериода И &ДатаОкончанияПериода
	|	И ПараметрыТорговыхТочек.Организация В(&Организация)
	|	И (ПараметрыТорговыхТочек.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийТорговыеТочки.Регистрация)
	|			ИЛИ ПараметрыТорговыхТочек.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийТорговыеТочки.ИзменениеПараметров))
	|	И НЕ ПараметрыТорговыхТочек.ТорговаяТочка.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтоЗаписьНачалоПериода УБЫВ,
	|	Период";
	
	ТаблицаСтавок     = ТорговыйСбор.ПрочитатьТаблицуСтавок();
	ТаблицаТерриторий = ТорговыйСбор.ПрочитатьТаблицуТерриторий();
	
	// в соответствие сначала помещаем запись на конец предыдущего периода, 
	// а потом сравниваем ее с записьмю на начало текушего
	ПараметрыТорговыхТочек = Новый ТаблицаЗначений;
	ПараметрыТорговыхТочек.Колонки.Добавить("ТорговаяТочка",        Новый ОписаниеТипов("СправочникСсылка.ТорговыеТочки"));
	ПараметрыТорговыхТочек.Колонки.Добавить("СуммаСбора",           ОбщегоНазначения.ОписаниеТипаЧисло(15,3));
	ПараметрыТорговыхТочек.Колонки.Добавить("ПлощадьТорговогоЗала", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	ПараметрыТорговыхТочек.Колонки.Добавить("КодНалоговойЛьготы");
	ПараметрыТорговыхТочек.Колонки.Добавить("Период",               ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));

	ВыборкаПараметрыТорговыхТочек = Запрос.Выполнить().Выбрать();
	Пока ВыборкаПараметрыТорговыхТочек.Следующий() Цикл
			ПараметрыТорговойТочки = РегистрыСведений.ПараметрыТорговыхТочек.НовыйПараметрыТорговойТочки(); 
			ПараметрыТорговойТочки.Вставить("ТорговаяТочка",Справочники.ТорговыеТочки.ПустаяСсылка());
			ЗаполнитьЗначенияСвойств(ПараметрыТорговойТочки, ВыборкаПараметрыТорговыхТочек);
			
			РегистрыСведений.ПараметрыТорговыхТочек.РассчитатьСуммуСбора(ПараметрыТорговойТочки, ТаблицаСтавок, ТаблицаТерриторий);
			СтрокаПараметров = ПараметрыТорговыхТочек.Найти(ВыборкаПараметрыТорговыхТочек.ТорговаяТочка ,"ТорговаяТочка");
			Если СтрокаПараметров = Неопределено Тогда
				СтрокаПараметров = ПараметрыТорговыхТочек.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПараметров, ПараметрыТорговойТочки);
				// Увеличение суммы сбора, связанное с изменением физических показателей применяется с текущего квартала, уменьшение со следующего
			ИначеЕсли СтрокаПараметров.ПлощадьТорговогоЗала < ПараметрыТорговойТочки.ПлощадьТорговогоЗала
				// Изменение суммы сбора, связанное с применением льготы - применяется с текущего квартала
				ИЛИ СтрокаПараметров.КодНалоговойЛьготы <> ПараметрыТорговойТочки.КодНалоговойЛьготы Тогда
				ЗаполнитьЗначенияСвойств(СтрокаПараметров, ПараметрыТорговойТочки);
			КонецЕсли;
	КонецЦикла;
		
	Возврат ПараметрыТорговыхТочек;
	
КонецФункции

#КонецОбласти