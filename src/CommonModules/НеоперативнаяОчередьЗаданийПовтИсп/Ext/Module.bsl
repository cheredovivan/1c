#Область СлужебныйПрограммныйИнтерфейс

// Описание очередей заданий.
// Используется при формировании текстов запросов исполнения заданий неоперативной очереди.
// 
// Возвращаемое значение:
//  Структура - Очереди заданий:
// * ВидыЗаданий - Соответствие Из СправочникСсылка.ВидыНеоперативныхЗаданий - Описание видов заданий.
// * ТаблицаОчередей - ТаблицаЗначений - Описание очередей:
// ** Регистр - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Регистр очереди.
// ** ИмяОчереди - Строка - Имя очереди
// ** ПолноеИмяОчереди  - Строка - Полное имя очереди
// ** ПараметрЗапросаВидЗадания  - Строка - описание см. НеоперативнаяОчередьЗаданий.СтруктураОписанияПараметровОчереди.
// ** ЗначениеВидЗадания - Строка - описание см. НеоперативнаяОчередьЗаданий.СтруктураОписанияПараметровОчереди.
// ** ПараметрЗапросаНомерОчереди - Строка - описание см. НеоперативнаяОчередьЗаданий.СтруктураОписанияПараметровОчереди.
// ** ЗначениеНомерОчереди - Строка - описание см. НеоперативнаяОчередьЗаданий.СтруктураОписанияПараметровОчереди.
// ** ПолеНомерОчереди - Строка - описание см. НеоперативнаяОчередьЗаданий.СтруктураОписанияПараметровОчереди.
// ** ГранулыРасчета - ТаблицаЗначений - Таблица с описанием гранул расчета по заданиям.
// Определяется возвращаемым значением функции ТаблицаГранулыРасчетаВОбработке модуля менеджера регистра заданий.
// ** МенеджерРегистраЗаданий - РегистрСведенийМенеджер - Менеджер регистра заданий
// ** НаименованиеОчереди - Строка -Имя очереди заданий
// ** ТекстЗапросаГранулыРасчетаВОбработке - Строка - описание см. НеоперативнаяОчередьЗаданий.СтруктураОписанияПараметровОчереди.
// ** ТекстЗапросаИсключаемыеДокументы - Строка - описание см. НеоперативнаяОчередьЗаданий.СтруктураОписанияПараметровОчереди.
// ** ОбрабатываемыеГранулыИмяТаблицы - Строка - описание см. НеоперативнаяОчередьЗаданий.СтруктураОписанияПараметровОчереди.
//
Функция ОчередиЗаданий() Экспорт

	ОчередиЗаданий = Новый Структура;
	
	ТаблицаОчередей = Новый ТаблицаЗначений;
	ТаблицаОчередей.Колонки.Добавить("Регистр", Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных"));
	ТаблицаОчередей.Колонки.Добавить("ИмяОчереди", ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	ТаблицаОчередей.Колонки.Добавить("ПолноеИмяОчереди", ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	
	ТаблицаОчередей.Колонки.Добавить("ПараметрЗапросаВидЗадания", ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ТаблицаОчередей.Колонки.Добавить("ЗначениеВидЗадания", Новый ОписаниеТипов("СправочникСсылка.ВидыНеоперативныхЗаданий"));
	ТаблицаОчередей.Колонки.Добавить("ПараметрЗапросаНомерОчереди", ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ТаблицаОчередей.Колонки.Добавить("ЗначениеНомерОчереди", ОбщегоНазначения.ОписаниеТипаЧисло(10,0));
	
	ТаблицаОчередей.Колонки.Добавить("ПолеНомерОчереди", ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	ТаблицаОчередей.Колонки.Добавить("ГранулыРасчета",Новый ОписаниеТипов("ТаблицаЗначений"));
	ТаблицаОчередей.Колонки.Добавить("МенеджерРегистраЗаданий");
	ТаблицаОчередей.Колонки.Добавить("НаименованиеОчереди", ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	ТаблицаОчередей.Колонки.Добавить("ТекстЗапросаГранулыРасчетаВОбработке", Новый ОписаниеТипов("Строка"));
	ТаблицаОчередей.Колонки.Добавить("ТекстЗапросаИсключаемыеДокументы", Новый ОписаниеТипов("Строка"));
	ТаблицаОчередей.Колонки.Добавить("ОбрабатываемыеГранулыИмяТаблицы", ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	
	ТаблицаОчередей.Индексы.Добавить("Регистр");
	
	// Исключаемые виды заданий указаны в справочнике справочно.
	// В механизме неоперативной очереди по ним не должны регистрироваться и обрабатываться задания.
	ИсключаемыеВидыЗаданий = Новый Массив;
	
	//++ НЕ УТ
	
	//++ Локализация
	ИсключаемыеВидыЗаданий.Добавить(Справочники.ВидыНеоперативныхЗаданий.ОтражениеВРегламентированномУчете);
	//-- Локализация
	
	//-- НЕ УТ
	
	//++ НЕ УТКА
	ИсключаемыеВидыЗаданий.Добавить(Справочники.ВидыНеоперативныхЗаданий.ОтражениеВМеждународномУчете);
	//-- НЕ УТКА
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ВидыНеоперативныхЗаданий.Ссылка КАК ВидЗадания,
	|	ВидыНеоперативныхЗаданий.Регистр КАК РегистрОчереди,
	|	ВидыНеоперативныхЗаданий.МаксимальнаяПорция КАК МаксимальнаяПорция,
	|	ВидыНеоперативныхЗаданий.НомерОчереди КАК НомерОчереди
	|ИЗ
	|	Справочник.ВидыНеоперативныхЗаданий КАК ВидыНеоперативныхЗаданий
	|ГДЕ
	|	НЕ ВидыНеоперативныхЗаданий.Ссылка В (&ИсключаемыеВидыЗаданий)";
	Запрос.УстановитьПараметр("ИсключаемыеВидыЗаданий", ИсключаемыеВидыЗаданий);
	
	ВидыЗаданий = Новый Соответствие;
	УстановитьПривилегированныйРежим(Истина);
	ДанныеПоВидамЗаданий = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	Для каждого ТекущийВидЗадания Из ДанныеПоВидамЗаданий Цикл
		
		ВидыЗаданий.Вставить(ТекущийВидЗадания.ВидЗадания, ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТекущийВидЗадания));
		
	КонецЦикла;
		
	ОчередиЗаданий.Вставить("ВидыЗаданий", ВидыЗаданий);
	
	МассивРегистров = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ДанныеПоВидамЗаданий.ВыгрузитьКолонку(
		"РегистрОчереди"));
	
	Для Каждого РегистрОчереди Из МассивРегистров Цикл
		
		МетаданныеРегистраЗаданий = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(РегистрОчереди);
		ПолноеИмяОчереди = МетаданныеРегистраЗаданий.ПолноеИмя();
		ИмяОчереди = МетаданныеРегистраЗаданий.Имя;
		МенеджерРегистраЗаданий = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяОчереди);

		НоваяЗапись = ТаблицаОчередей.Добавить();
		НоваяЗапись.Регистр = РегистрОчереди;
		НоваяЗапись.ПолноеИмяОчереди = ПолноеИмяОчереди;
		НоваяЗапись.ИмяОчереди = ИмяОчереди;
		НоваяЗапись.МенеджерРегистраЗаданий = МенеджерРегистраЗаданий;
		НоваяЗапись.НаименованиеОчереди = МетаданныеРегистраЗаданий.Представление();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, МенеджерРегистраЗаданий.ОписаниеПараметровОчереди(ИмяОчереди));
		НоваяЗапись.ГранулыРасчета = МенеджерРегистраЗаданий.ТаблицаГранулыРасчетаВОбработке();
		
	КонецЦикла;
	
	ОчередиЗаданий.Вставить("ТаблицаОчередей", ТаблицаОчередей);
	
	Возврат ОчередиЗаданий;
	
КонецФункции

// Возвращает список доступных видов заданий для регистра очереди по его идентификатору.
// 
// Параметры:
//  ИдентификаторРегистра - СправочникСсылка.ИдентификаторыОбъектовМетаданных -Идентификатор регистра
// 
// Возвращаемое значение:
//  Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список доступных видов заданий для регистра очереди
Функция СписокДоступныхВидовЗаданийДляРегистраОчереди(ИдентификаторРегистра) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ВидыНеоперативныхЗаданий.Ссылка КАК ВидЗадания
	|ИЗ
	|	Справочник.ВидыНеоперативныхЗаданий КАК ВидыНеоперативныхЗаданий
	|ГДЕ
	|	ВидыНеоперативныхЗаданий.Регистр = &РегистрОчереди";
	Запрос.УстановитьПараметр("РегистрОчереди", ИдентификаторРегистра);
	УстановитьПривилегированныйРежим(Истина);
	СписокДоступныхВидовЗаданий = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВидЗадания");
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СписокДоступныхВидовЗаданий;
	
КонецФункции

// Размер порции распределение взаиморасчетов по срокам.
// 
// Возвращаемое значение:
//  Число - Максимальный размер порции распределения взаиморасчетов по срокам
Функция РазмерПорцииРаспределениеВзаиморасчетовПоСрокам() Экспорт
	
	РаспределениеВзаиморасчетовПоСрокам = Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам;
	ПорцияРасчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РаспределениеВзаиморасчетовПоСрокам, "МаксимальнаяПорция");
	
	Возврат ПорцияРасчета;
	
КонецФункции

#КонецОбласти
