#Область ПрограммныйИнтерфейс

// Открыть форму просмотра лога запросов
// 
// Параметры:
//  УникальныйИдентификаторФормы - УникальныйИдентификатор
//  Подсистема - Строка - Имя подсистемы
//  АдресДанных - Неопределено, УникальныйИдентификатор - Адрес данных
Процедура ОткрытьПросмотрЛогаЗапросов(УникальныйИдентификаторФормы, Подсистема, АдресДанных = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура();
	
	Если АдресДанных = Неопределено Тогда
		МодульЛогированияЗапросовВызовСервера = ОбщегоНазначенияКлиент.ОбщийМодуль(
			СтрШаблон("ЛогированиеЗапросов%1ВызовСервера", Подсистема));
		АдресДанных = МодульЛогированияЗапросовВызовСервера.АдресСодержанияЛоговЗапросов(УникальныйИдентификаторФормы);
		ДополнительныеПараметры.Вставить("АдресДанных", АдресДанных);
	КонецЕсли;
	
	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("АдресДанных", АдресДанных);
	ПараметрыОткрытияФормы.Вставить("Подсистема",  Подсистема);
	
	ОписаниеОповещенияЗакрытиеФормыПросмотраЛогов = Новый ОписаниеОповещения(
		"ОповещениеОбработчикЗакрытиеФормыПросмотраЛогов",
		ЛогированиеЗапросовИСКлиент,
		ДополнительныеПараметры);
	
	ОткрытьФорму(
		"ОбщаяФорма.ФормаПросмотраЛогаЗапросовИС",
		ПараметрыОткрытияФормы,
		УникальныйИдентификаторФормы,,,,
		ОписаниеОповещенияЗакрытиеФормыПросмотраЛогов);
		
КонецПроцедуры

// Открыть просмотр информации об окружении
// 
// Параметры:
//  ФормаВладелец - ФормаКлиентскогоПриложения
//  Подсистема - Строка - имя подсистемы
Процедура ОткрытьПросмотрИнформацииОбОкружении(ФормаВладелец, Подсистема) Экспорт
	
	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("ЭтоИнформацияОбОкружении", Истина);
	ПараметрыОткрытияФормы.Вставить("Подсистема",               Подсистема);
	
	ОткрытьФорму(
		"ОбщаяФорма.ФормаПросмотраЛогаЗапросовИС",
		ПараметрыОткрытияФормы,
		ФормаВладелец);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверят необходимость записи логов запросов по флагу и времени записи.
// 
// Параметры:
// 	ПараметрыЛогирования - см. ЛогированиеЗапросовИС.НовыеПараметрыЛогированияЗапросов
// Возвращаемое значение:
// 	Булево - Призак записи логов запросов.
Функция ВыполняетсяЛогированиеЗапросов(ПараметрыЛогирования) Экспорт
	
	Если Не ПараметрыЛогирования.Включено
		Или (ЗначениеЗаполнено(ПараметрыЛогирования.ОкончаниеЗаписи)
		И ПараметрыЛогирования.ОкончаниеЗаписи <= ТекущаяУниверсальнаяДатаВМиллисекундах()) Тогда
		Возврат Ложь;
	Иначе 
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

Процедура ОповещениеОбработчикЗакрытиеФормыПросмотраЛогов(Значение, ДополнительныеПараметры) Экспорт 
	
	Если ДополнительныеПараметры.Свойство("АдресДанных")
		И ЭтоАдресВременногоХранилища(ДополнительныеПараметры.АдресДанных) Тогда
		УдалитьИзВременногоХранилища(ДополнительныеПараметры.АдресДанных);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
