#Область СлужебныйПрограммныйИнтерфейс

// Связанный с решением приказ.
// 
// Параметры:
//  Решение - ОпределяемыйТип.КадровоеРешение -
// 
// Возвращаемое значение:
//  Неопределено, 
//	ДокументСсылка.КадровыйПеревод, 
//	ДокументСсылка.Увольнение, 
//	ДокументСсылка.ВосстановлениеВДолжности, 
//	ДокументСсылка.ВозвратИзОтпускаПоУходуЗаРебенком, 
//	ДокументСсылка.КадровыйПереводСписком, 
//	ДокументСсылка.ПриемНаРаботу, 
//	ДокументСсылка.УвольнениеСписком, 
//	ДокументСсылка.ОтпускПоУходуЗаРебенком, 
//	ДокументСсылка.ПриемНаРаботуСписком - кадровый документ.
Функция СвязанныйСРешениемПриказ(Решение) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Решение", Решение);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КадровыйДокумент
		|ИЗ
		|	РегистрСведений.КадровыеДокументыПоРешениям
		|ГДЕ
		|	Решение = &Решение";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.КадровыйДокумент;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ДоступноЧтениеРешенийОПриемеНаРаботу() Экспорт

	Возврат ПравоДоступа("Чтение", Метаданные.Документы.РешениеОПриемеНаРаботу);

КонецФункции

#Область КадровыеДокументы

Функция ИспользуетсяРешение(КадровыйДокумент) Экспорт
	
	ИспользуетсяРешение = Ложь;
	ТипДокумента = ТипЗнч(КадровыйДокумент);
	Если ТипДокумента = Тип("ДокументСсылка.ПриемНаРаботу")
		Или ТипДокумента = Тип("ДокументСсылка.ПриемНаРаботуСписком") Тогда
		ИспользуетсяРешение = ПолучитьФункциональнуюОпцию("ИспользоватьРешениеОПриемеНаРаботуИнтеграцияУправлениеПерсоналом");
	КонецЕсли;
	
	Если Не ИспользуетсяРешение И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КадровыеРешения") Тогда
		МодульКадровыеРешенияРасширенный = ОбщегоНазначения.ОбщийМодуль("КадровыеРешенияРасширенный");
		ИспользуетсяРешение = МодульКадровыеРешенияРасширенный.ИспользуетсяРешение(КадровыйДокумент);
	КонецЕсли;
	
	Возврат ИспользуетсяРешение;
	
КонецФункции

Процедура ПриЗаполненииПриемаНаРаботу(ПриемНаРаботуОбъект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт

	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		// Проверим, есть ли по этому сотруднику решение о приеме на работу?
		Решение = Документы.РешениеОПриемеНаРаботу.РешениеОПриемеНаРаботуПоСотруднику(ДанныеЗаполнения);
		Если Решение = Неопределено Тогда
			Возврат;
		КонецЕсли;
		Сотрудник = ДанныеЗаполнения;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РешениеОПриемеНаРаботу") Тогда
		Решение = ДанныеЗаполнения;
		Сотрудник = Документы.РешениеОПриемеНаРаботу.СотрудникПоРешениюОПриемеНаРаботу(Решение);
	Иначе
		Возврат;
	КонецЕсли;

	ФизическоеЛицо = КадровыйУчет.ФизическиеЛицаСотрудников(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник))[0];
	
	СтруктураЗаполнения = Документы.РешениеОПриемеНаРаботу.ДанныеЗаполненияПриемаНаРаботуПоДокументуРешения(
		Решение.ПолучитьОбъект(), Сотрудник, ФизическоеЛицо);
	
	ПриемНаРаботуОбъект.Заполнить(СтруктураЗаполнения);
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

// При проверке заполнения приема на работу.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект.ПриемНаРаботу, ДокументОбъект.ПриемНаРаботуСписком -
//  Отказ - Булево -
//  ПроверяемыеРеквизиты - Массив из Строка -
//  ПроверяемыеСтруктуры - Массив Из ДокументОбъект.ПриемНаРаботу, ДокументОбъект.ПриемНаРаботуСписком.Сотрудники - 
Процедура ПриПроверкеЗаполненияПриемаНаРаботу(ДокументОбъект, Отказ, ПроверяемыеРеквизиты, ПроверяемыеСтруктуры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	СотрудникиПоРешениям = РегистрыСведений.СотрудникиПоРешениямОПриемеНаРаботу.СотрудникиПоРешениямОПриемеНаРаботу(
		ОбщегоНазначения.ВыгрузитьКолонку(ПроверяемыеСтруктуры, "Решение", Истина));
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого ЭлементПроверки Из ПроверяемыеСтруктуры Цикл
		Если ЗначениеЗаполнено(ЭлементПроверки.Решение) 
			И ЭлементПроверки.Сотрудник <> СотрудникиПоРешениям[ЭлементПроверки.Решение] Тогда
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru = 'Принимается на работу не тот сотрудник, по которому принято решение о приеме на работу.
							   |Ожидается прием сотрудника %1 по решению %2';
							   |en = 'The employee to hire is not the one for whom the employment decision was made.
							   |Employee %1 is expected to be hired based on the %2 decision'"), 
					СотрудникиПоРешениям[ДокументОбъект.Решение], 
					ДокументОбъект.Решение),  
				ДокументОбъект.Ссылка, , , Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// См. УправлениеПечатьюПереопределяемый.ПриОпределенииОбъектовСКомандамиПечати.
Процедура ПриОпределенииОбъектовСКомандамиПечати(СписокОбъектов) Экспорт
	
	СписокОбъектов.Добавить(Документы.РешениеОПриемеНаРаботу);
	
КонецПроцедуры

#КонецОбласти

#Область Свойства

// См. УправлениеСвойствамиПереопределяемый.ПриПолученииПредопределенныхНаборовСвойств.
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, 
		"c091b4f1-9802-11e9-80cd-4cedfb43b11a", Метаданные.Документы.РешениеОПриемеНаРаботу);
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.Справочники.РешениеОПриемеНаРаботуПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Документы.РешениеОПриемеНаРаботу, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоСотруднику(ДокументОбъект, ДанныеЗаполнения, ЗаполнятьСотрудника = Истина, ЗаполнятьПозицию = Ложь) Экспорт
	
	Если ТипЗнч(ДанныеЗаполнения) <> Тип("СправочникСсылка.Сотрудники") Тогда
		Возврат;
	КонецЕсли;
	
	ПоляДанных = ОбщегоНазначенияБЗККлиентСервер.ЗначенияВМассиве(
		"Организация", "ФизическоеЛицо", "ДолжностьПоШтатномуРасписанию");
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, ДанныеЗаполнения, ПоляДанных);
	
	Если КадровыеДанные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	ТекущиеКадровыеДанныеСотрудника = КадровыеДанные[0];
	
	ДокументОбъект.ФизическоеЛицо = ТекущиеКадровыеДанныеСотрудника.ФизическоеЛицо;
	ДокументОбъект.Организация = ТекущиеКадровыеДанныеСотрудника.Организация;
	
	Если ЗаполнятьСотрудника Тогда
		ДокументОбъект.Сотрудник = ДанныеЗаполнения;
	КонецЕсли;
	
	Если ЗаполнятьПозицию Тогда
		ДокументОбъект.ДолжностьПоШтатномуРасписанию = ТекущиеКадровыеДанныеСотрудника.ДолжностьПоШтатномуРасписанию;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНаличиеДубляДокумента(ДокументОбъект, Отказ) Экспорт
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	
	ПолноеИмяДокумента = МетаданныеДокумента.ПолноеИмя();
	МодульМенеджера = Документы[МетаданныеДокумента.Имя];
	
	Запрос = Новый Запрос;
	
	Разделитель = "";
	ТекстУсловийПроверки = "";
	ШаблонУсловия = "%1Решение.%2 = &%2";
	
	Для Каждого Поле Из МодульМенеджера.ПоляПроверкиУникальности() Цикл
		
		Если ЗначениеЗаполнено(ТекстУсловийПроверки)
			И Не ЗначениеЗаполнено(Разделитель) Тогда
			Разделитель = "
			|	И ";
		КонецЕсли;
		
		ТекстУсловийПроверки = ТекстУсловийПроверки + СтрШаблон(ШаблонУсловия, Разделитель, Поле);
		Запрос.УстановитьПараметр(Поле, ДокументОбъект[Поле]);
		
	КонецЦикла;
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Решение.Ссылка
	|ИЗ
	|	&ПолноеИмяДокумента КАК Решение
	|ГДЕ
	|	НЕ Решение.ПометкаУдаления
	|	И Решение.Ссылка <> &Ссылка
	|	И &УсловияПроверки";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолноеИмяДокумента", ПолноеИмяДокумента);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловияПроверки", ТекстУсловийПроверки);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		ТекстОшибки = НСтр("ru = 'Документ с такими характеристиками уже есть: %1.';
							|en = 'There is already a document with these characteristics: %1.'", ОбщегоНазначения.КодОсновногоЯзыка());
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ТекстОшибки, Выборка.Ссылка), Выборка.Ссылка, , , Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти