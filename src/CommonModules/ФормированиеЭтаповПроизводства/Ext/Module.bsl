
#Область ПрограммныйИнтерфейс

Процедура СформироватьЭтапыКЗапуску(ГруппыИзделий, Изделия, КлючиПартий, Параметры, АдресХранилища) Экспорт
	
	Результат = РезультатФормирования();
	
	РазмерПорции = 500;
	
	Если ГруппыИзделий.Количество() <= РазмерПорции Тогда
		СформироватьПорциюЭтапов(ГруппыИзделий, Изделия, КлючиПартий, Параметры, Результат, АдресХранилища);
	Иначе
		Счетчик = 1;
		МаксимальныйИндекс = ГруппыИзделий.Количество()-1;
		
		ГруппыИзделийКопия = ГруппыИзделий.СкопироватьКолонки();
		ИзделияКопия = Изделия.СкопироватьКолонки();
		КлючиПартийКопия = КлючиПартий.СкопироватьКолонки();
	
		Изделия.Индексы.Добавить("НомерГруппы");
		КлючиПартий.Индексы.Добавить("НомерГруппы");
		
		СтруктураПоиска = Новый Структура("НомерГруппы");
	
		Для Индекс = 0 По МаксимальныйИндекс Цикл
			СтрокаГруппа = ГруппыИзделий[Индекс];
			ЗаполнитьЗначенияСвойств(ГруппыИзделийКопия.Добавить(), СтрокаГруппа);
			
			СтруктураПоиска.НомерГруппы = СтрокаГруппа.НомерГруппы;
			Для каждого СтрокаИзделие Из Изделия.НайтиСтроки(СтруктураПоиска) Цикл
				ЗаполнитьЗначенияСвойств(ИзделияКопия.Добавить(), СтрокаИзделие);
			КонецЦикла;
			Для каждого СтрокаКлюч Из КлючиПартий.НайтиСтроки(СтруктураПоиска) Цикл
				ЗаполнитьЗначенияСвойств(КлючиПартийКопия.Добавить(), СтрокаКлюч);
			КонецЦикла;
			
			Если Счетчик = РазмерПорции ИЛИ Индекс = МаксимальныйИндекс Тогда
				СформироватьПорциюЭтапов(ГруппыИзделийКопия, ИзделияКопия, КлючиПартийКопия, Параметры, Результат, АдресХранилища);
				
				ГруппыИзделийКопия.Очистить();
				ИзделияКопия.Очистить();
				КлючиПартийКопия.Очистить();
				Счетчик = 1;
			Иначе
				Счетчик = Счетчик+1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ОбеспечениеВДокументахВызовСервера.ЕстьЗаданияПоЗаказам(Результат.СозданныеОбъекты);
	СтруктураЗаказа.ЗапуститьРасчет();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция РезультатФормирования() Экспорт
	
	РезультатФормирования = Новый ТаблицаЗначений;
	РезультатФормирования.Колонки.Добавить("Партия", Новый ОписаниеТипов("СправочникСсылка.ПартииПроизводства"));
	РезультатФормирования.Колонки.Добавить("Документ", Новый ОписаниеТипов("ДокументСсылка.ЭтапПроизводства2_2"));
	
	СозданныеОбъекты = Новый Массив;
	
	Ошибки = Новый ТаблицаЗначений;
	Ошибки.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Ошибки.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Ошибки.Колонки.Добавить("НаправлениеДеятельности", Новый ОписаниеТипов("СправочникСсылка.НаправленияДеятельности"));
	Ошибки.Колонки.Добавить("Спецификация", Новый ОписаниеТипов("СправочникСсылка.РесурсныеСпецификации"));
	Ошибки.Колонки.Добавить("Сообщения", Новый ОписаниеТипов("Массив, ФиксированныйМассив"));
	
	Сообщения = Новый Массив;
	
	Результат = Новый Структура;
	Результат.Вставить("РезультатФормирования", РезультатФормирования);
	Результат.Вставить("СозданныеОбъекты", СозданныеОбъекты);
	Результат.Вставить("Сообщения", Сообщения);
	Результат.Вставить("Ошибки", Ошибки);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Сформировать порцию этапов.
// 
// Параметры:
//  ГруппыИзделий - ТаблицаЗначений
//  Изделия - ТаблицаЗначений
//  КлючиПартий - ТаблицаЗначений
//  Параметры - Структура
//  Результат - Структура
//  АдресХранилища - Строка
//
Процедура СформироватьПорциюЭтапов(ГруппыИзделий, Изделия, КлючиПартий, Параметры, Результат, АдресХранилища)
	
	РезультатФормирования = Результат.РезультатФормирования;
	СозданныеОбъекты = Результат.СозданныеОбъекты;
	Ошибки = Результат.Ошибки;
	Сообщения = Результат.Сообщения;
	
	ПередатьКВыполнениюВсе = Параметры.ВариантПередачиЭтаповКВыполнению = "Все";
	ПередатьКВыполнениюПервые = Параметры.ВариантПередачиЭтаповКВыполнению = "Первые";
	ИспользуетсяГрафикПроизводства = УправлениеПроизводствомПовтИсп.ИспользуетсяГрафикПроизводства();
	
	Изделия.Индексы.Добавить("НомерГруппы");
	
	ЗаданияКРасчетуГрафика = КлючиПартий.Скопировать(, "ЗаказНаПроизводство, КлючПартия");
	ЗаданияКРасчетуГрафика.Свернуть("ЗаказНаПроизводство, КлючПартия");
	ЗаданияКРасчетуГрафика.Колонки.Добавить("Очередь", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла()));
	ЗаданияКРасчетуГрафика.Индексы.Добавить("КлючПартия");
	
	СтруктураПоискаГруппа = Новый Структура("НомерГруппы");
	СтруктураПоискаРаспоряжение = Новый Структура("Распоряжение", Документы.ЗаказНаПроизводство2_2.ПустаяСсылка());
	СтруктураПоискаКлючПартия = Новый Структура("КлючПартия");
	
	ИменаКолонок = Новый Массив;
	Для каждого Колонка Из Изделия.Колонки Цикл
		Если Колонка.Имя <> "НомерГруппы"
			И Колонка.Имя <> "НомерПартииВГруппе"
			И Колонка.Имя <> "Количество" Тогда
			ИменаКолонок.Добавить(Колонка.Имя);
		КонецЕсли;
	КонецЦикла;
	КолонкиГруппировок = СтрСоединить(ИменаКолонок, ",");
	
	ГруппыКУдалению = Новый Массив;
	
	// Создание заказов
	УстановитьПривилегированныйРежим(Истина);
	Для каждого Группа Из ГруппыИзделий.НайтиСтроки(СтруктураПоискаРаспоряжение) Цикл
		
		СчетчикОшибок = 1;
		Пока Истина Цикл
			
			Попытка
				
				РеквизитыШапки = Новый Структура;
				
				РеквизитыШапки.Вставить("Подразделение", Группа.ПодразделениеДиспетчер);
				РеквизитыШапки.Вставить("Организация", Группа.Организация);
				РеквизитыШапки.Вставить("НаправлениеДеятельности", Группа.НаправлениеДеятельности);
				РеквизитыШапки.Вставить("ХозяйственнаяОперация", Группа.ХозяйственнаяОперация);
				РеквизитыШапки.Вставить("Партнер", Группа.Партнер);
				РеквизитыШапки.Вставить("Договор", Группа.Договор);
				РеквизитыШапки.Вставить("ЗаказПодДеятельность", Группа.ЗаказПодДеятельность);
				РеквизитыШапки.Вставить("РазмещениеВыпуска", Перечисления.СпособыПривязкиОперацийПроизводства.КОкончанию);
				РеквизитыШапки.Вставить("НачатьНеРанее", Группа.НачатьНеРанее);
				РеквизитыШапки.Вставить("ДатаПотребности", Группа.ДатаПотребности);
				
				Если ЗначениеЗаполнено(Группа.Приоритет) Тогда
					РеквизитыШапки.Вставить("Приоритет", Группа.Приоритет);
					РеквизитыШапки.Вставить("Очередь",
						Документы.ЗаказНаПроизводство2_2.НомерОчередиДляВставкиПередДокументами(Группа.СледующийЗаказВОчереди));
				КонецЕсли;
				
				РеквизитыШапки.Вставить("ДинамическаяСтруктура", Истина);
				РеквизитыШапки.Вставить("Дата", ТекущаяДатаСеанса());
				РеквизитыШапки.Вставить("Статус", Перечисления.СтатусыЗаказовНаПроизводство2_2.КПроизводству);
				РеквизитыШапки.Вставить("ВариантОбособления", Перечисления.ВариантыОбособленияМатериаловВПроизводстве.НазначениеПродукции);
				РеквизитыШапки.Вставить("Комментарий", НСтр("ru = 'Сформирован автоматически (запуск партий)';
															|en = 'Generated automatically (lot launch)'"));
				
				СтруктураПоискаГруппа.НомерГруппы = Группа.НомерГруппы;
				Товары = Изделия.Скопировать(СтруктураПоискаГруппа);
				Товары.Свернуть(КолонкиГруппировок, "Количество");
				
				ДанныеЗаполнения = Новый Структура;
				ДанныеЗаполнения.Вставить("РеквизитыШапки", РеквизитыШапки);
				ДанныеЗаполнения.Вставить("Товары", Товары);
				
				Заказ = Документы.ЗаказНаПроизводство2_2.СоздатьДокумент();
				Заказ.Заполнить(ДанныеЗаполнения);
				
				// Отказ от проверки заполнения дополнительных реквизитов
				ПараметрыСеанса.ИнтерактивнаяПроверкаЗаполненияСвойств = Истина;
				
				Если Заказ.ПроверитьЗаполнение() Тогда
					Заказ.Записать(РежимЗаписиДокумента.Проведение);
					
					Группа.Распоряжение = Заказ.Ссылка;
					Для каждого Изделие Из Изделия.НайтиСтроки(СтруктураПоискаГруппа) Цикл
						Изделие.Распоряжение = Заказ.Ссылка;
					КонецЦикла;
					СозданныеОбъекты.Добавить(Заказ.Ссылка);
				Иначе
					ДобавитьОшибку(Ошибки, Группа, Изделия);
					
					ГруппыКУдалению.Добавить(Группа);
					
					Для каждого Изделие Из Изделия.НайтиСтроки(СтруктураПоискаГруппа) Цикл
						Изделия.Удалить(Изделие);
					КонецЦикла;
				КонецЕсли;
				
				// Фиксация результата
				ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
				
			Исключение
				
				Событие = НСтр("ru = 'Этап производства';
								|en = 'Production stage'", ОбщегоНазначения.КодОсновногоЯзыка()) + "."
					+ НСтр("ru = 'Формирование этапов';
							|en = 'Generate stages'", ОбщегоНазначения.КодОсновногоЯзыка());
				
				Если СчетчикОшибок < 3 Тогда
					ТекстСообщения = СтрШаблон(НСтр("ru = 'Неудачная попытка записи (%1): %2';
													|en = 'Unsuccessful attempt to write (%1): %2'"),
						СчетчикОшибок, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Предупреждение,,, ТекстСообщения);
					
					СчетчикОшибок = СчетчикОшибок + 1;
					Продолжить;
				КонецЕсли;
				
				ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ДобавитьОшибку(Ошибки, Группа, Изделия, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			КонецПопытки;
			
			Прервать;
			
		КонецЦикла;
		
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
	Для каждого Группа Из ГруппыКУдалению Цикл
		ГруппыИзделий.Удалить(Группа);
	КонецЦикла;
	
	//
	ГруппыИзделий.Индексы.Добавить("НомерГруппы");
	Изделия.Индексы.Добавить("НомерГруппы");
	Изделия.Индексы.Добавить("НомерГруппы, НомерПартииВГруппе");
	СтруктураПоискаПартия = Новый Структура("НомерГруппы, НомерПартииВГруппе");
	
	//
	ДанныеПартии = Новый Структура;
	ДанныеПартии.Вставить("ДинамическаяСтруктура", Истина);
	ДанныеПартии.Вставить("ПартияПроизводства", Неопределено);
	ДанныеПартии.Вставить("НазначениеПродукция");
	ДанныеПартии.Вставить("Распоряжение");
	ДанныеПартии.Вставить("Спецификация");
	ДанныеПартии.Вставить("ТипПроизводственногоПроцесса");
	
	// Чтение данных спецификаций
	ПараметрыВыборки = Справочники.РесурсныеСпецификации.ПараметрыВыборкиДанных();
	ПараметрыВыборки.УчитыватьВероятностьБрака = Ложь;
	ПараметрыВыборки.РассчитыватьДолиСтоимостиВыходныхИзделий = Истина;
	ПараметрыВыборки.ПолучитьПромежуточныйВыпуск = Истина;
	
	ДанныеСпецификаций = Справочники.РесурсныеСпецификации.ДанныеСпецификацииПоСпискуНоменклатуры(
		Изделия,
		ПараметрыВыборки,
		Истина,
		"НомерГруппы,НомерПартииВГруппе");
	
	// Создание партий
	ПартииПроизводства = Новый ТаблицаЗначений();
	ПартииПроизводства.Колонки.Добавить("Код", Новый ОписаниеТипов("Число"));
	ПартииПроизводства.Колонки.Добавить("ПартияПроизводства", Новый ОписаниеТипов("СправочникСсылка.ПартииПроизводства"));
	ПартииПроизводства.Колонки.Добавить("ДанныеСпецификацииИндексСтроки", Новый ОписаниеТипов("Число"));
	
	Для Каждого Реквизит Из Метаданные.Справочники.ПартииПроизводства.Реквизиты Цикл
		ПартииПроизводства.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	КонецЦикла;
	
	Для Каждого ТабличнаяЧасть Из Метаданные.Справочники.ПартииПроизводства.ТабличныеЧасти Цикл
		Если ТабличнаяЧасть.Имя = "ДополнительныеРеквизиты" Тогда
			Продолжить;
		КонецЕсли;
		ПартииПроизводства.Колонки.Добавить(ТабличнаяЧасть.Имя, Новый ОписаниеТипов("ТаблицаЗначений"));
	КонецЦикла;
	
	Для Индекс = 0 По ДанныеСпецификаций.ВГраница() Цикл
		ДанныеСпецификации = ДанныеСпецификаций[Индекс];
		Группа = ГруппыИзделий.Найти(ДанныеСпецификации.НомерГруппы, "НомерГруппы");
		Если Группа.Распоряжение.Пустая() Тогда
			Продолжить; // Заказ не был проведен
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ДанныеПартии, Группа, "Распоряжение, НазначениеПродукция");
		ЗаполнитьЗначенияСвойств(ДанныеПартии, ДанныеСпецификации, "Спецификация, ТипПроизводственногоПроцесса");
		ПоляПартии = Документы.ЭтапПроизводства2_2.ПоляПартии(
			ДанныеПартии, ДанныеСпецификации.ОсновноеИзделие,, ДанныеСпецификации);
		
		СтрокаПартия = ПартииПроизводства.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПартия, ПоляПартии);
		СтрокаПартия.ДанныеСпецификацииИндексСтроки = Индекс;
	КонецЦикла;
	
	Если ПартииПроизводства.Количество() <> 0 Тогда
		
		// Формируем ключи аналитик учета затрат (партии производства)
		Справочники.ПартииПроизводства.СформироватьПартииПроизводстваДляСпискаЭтапов(ПартииПроизводства);

		// Кешируем место партии в структуре заказа
		МестоПартииВСтруктуреЗаказа = РегистрыСведений.МестоПартииВСтруктуреЗаказа.СоздатьНаборЗаписей().ВыгрузитьКолонки();
		Для Индекс = -ПартииПроизводства.Количество()+1 По 0 Цикл
			СтрокаПартия = ПартииПроизводства[-Индекс];
			Если СтрокаПартия.ПартияПроизводства.Пустая() Тогда
				ДанныеСпецификаций.Удалить(СтрокаПартия.ДанныеСпецификацииИндексСтроки);
			Иначе
				ДанныеСпецификации = ДанныеСпецификаций[СтрокаПартия.ДанныеСпецификацииИндексСтроки];
				ДанныеСпецификации.Вставить("ПартияПроизводства", СтрокаПартия.ПартияПроизводства);

				НоваяСтрока = МестоПартииВСтруктуреЗаказа.Добавить();
				НоваяСтрока.Партия = СтрокаПартия.ПартияПроизводства;
				НоваяСтрока.ГруппаИзделий = СтруктураЗаказа.ГруппаИзделийКонтрольнаяСумма(СтрокаПартия,
					"Спецификация", "Документ");
				
				ЗаполнитьЗначенияСвойств(СтруктураПоискаПартия, ДанныеСпецификации, "НомерГруппы, НомерПартииВГруппе");
				НайденныеСтроки = Изделия.НайтиСтроки(СтруктураПоискаПартия);
				ВсегоСтрок = НайденныеСтроки.Количество();
				Если ВсегоСтрок > 0 Тогда
					НоваяСтрока.Уровень = НайденныеСтроки[0].Уровень;
					НоваяСтрока.ДлительностьДоВыпуска = НайденныеСтроки[0].ДлительностьДоВыпуска;
					НоваяСтрока.ИдентификаторВГруппеИзделий = НайденныеСтроки[0].ИдентификаторВГруппеИзделий;
					Для ИндексИзделия = 1 По ВсегоСтрок-1 Цикл
						НайденнаяСтрока = НайденныеСтроки[ИндексИзделия];
						НоваяСтрока.Уровень = Макс(НоваяСтрока.Уровень, НайденнаяСтрока.Уровень);
						НоваяСтрока.ДлительностьДоВыпуска = Макс(НоваяСтрока.ДлительностьДоВыпуска, НайденнаяСтрока.ДлительностьДоВыпуска);
						НоваяСтрока.ИдентификаторВГруппеИзделий = Макс(НоваяСтрока.ИдентификаторВГруппеИзделий, НайденнаяСтрока.ИдентификаторВГруппеИзделий);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		
		КонецЦикла;
		РегистрыСведений.МестоПартииВСтруктуреЗаказа.ЗаписатьНовыеПартии(МестоПартииВСтруктуреЗаказа);
		
	КонецЕсли;
	
	//
	СсылкиПроведен = Новый Массив;
	ЭтапыКПереносуГрафика = Неопределено;
	
	Если ИспользуетсяГрафикПроизводства Тогда
		ЭтапыКПереносуГрафика = Новый ТаблицаЗначений;
		ЭтапыКПереносуГрафика.Колонки.Добавить("ЗаказНаПроизводство", Новый ОписаниеТипов("ДокументСсылка.ЗаказНаПроизводство2_2"));
		ЭтапыКПереносуГрафика.Колонки.Добавить("КлючПартияСтарый", Новый ОписаниеТипов("УникальныйИдентификатор"));
		ЭтапыКПереносуГрафика.Колонки.Добавить("КлючПартияНовый", Новый ОписаниеТипов("УникальныйИдентификатор"));
		ЭтапыКПереносуГрафика.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка.ЭтапПроизводства2_2"));
		ЭтапыКПереносуГрафика.Колонки.Добавить("Этап", Новый ОписаниеТипов("СправочникСсылка.ЭтапыПроизводства"));
	КонецЕсли;
	
	Для каждого ДанныеСпецификации Из ДанныеСпецификаций Цикл
		Группа = ГруппыИзделий.Найти(ДанныеСпецификации.НомерГруппы, "НомерГруппы");
		Если Группа.Распоряжение.Пустая() Тогда
			Продолжить; // Заказ не был проведен
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтруктураПоискаПартия, ДанныеСпецификации, "НомерГруппы, НомерПартииВГруппе");
		ИзделияПартии = Изделия.Скопировать(СтруктураПоискаПартия);
		Если ИзделияПартии.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Изделие = ИзделияПартии[0];
		КлючПартияНовый = ДанныеСпецификации.ПартияПроизводства.УникальныйИдентификатор();
		
		ПеренестиГрафик = ИспользуетсяГрафикПроизводства
			И ИзделияПартии.Количество() = 1
			И Изделие.КоличествоПартий = 1
			И Изделие.Количество = Изделие.КоличествоНорматив
			И НачалоДня(Изделие.ДатаЗапуска) = НачалоДня(Изделие.ДатаЗапускаНорматив)
			И ЗначениеЗаполнено(Изделие.КлючПартия);
		
		// Задания на расчет графика
		Если ПеренестиГрафик Тогда
			СтруктураПоискаКлючПартия.КлючПартия = Изделие.КлючПартия;
			НайденныеСтроки = ЗаданияКРасчетуГрафика.НайтиСтроки(СтруктураПоискаКлючПартия);
			Если НайденныеСтроки.Количество() <> 0 Тогда
				ЗаданияКРасчетуГрафика.Удалить(НайденныеСтроки[0]);
			КонецЕсли;
		Иначе
			НоваяСтрока = ЗаданияКРасчетуГрафика.Добавить();
			НоваяСтрока.ЗаказНаПроизводство = Группа.Распоряжение;
			НоваяСтрока.КлючПартия = КлючПартияНовый;
		КонецЕсли;
		
		// Ссылка на партию
		НоваяСтрока = РезультатФормирования.Добавить();
		НоваяСтрока.Партия = ДанныеСпецификации.ПартияПроизводства;
		СозданныеОбъекты.Добавить(ДанныеСпецификации.ПартияПроизводства);
		
		// Плановая дата отгрузки и производства
		ДанныеСпецификации.Этапы.Колонки.Добавить("ДатаОтгрузки", Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты));
		ДанныеСпецификации.Этапы.Колонки.Добавить("НеОтгружатьЧастями", Новый ОписаниеТипов("Булево"));
		ДанныеСпецификации.Этапы.Колонки.Добавить("ПланироватьНеРанее", 
			Новый ОписаниеТипов("ДокументСсылка.ЭтапПроизводства2_2, Дата",,, Новый КвалификаторыДаты));
		ДанныеСпецификации.Этапы.ЗаполнитьЗначения(Истина, "НеОтгружатьЧастями");
		ДанныеСпецификации.Этапы.ЗаполнитьЗначения(Группа.НачатьНеРанее, "ДатаОтгрузки");
		
		ДанныеСпецификации.Этапы.Колонки.Добавить("ДатаПроизводства", Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты));
		ДанныеСпецификации.Этапы.Колонки.Добавить("ПроизводствоОднойДатой", Новый ОписаниеТипов("Булево"));
		ДанныеСпецификации.Этапы.ЗаполнитьЗначения(Истина, "ПроизводствоОднойДатой");
		
		Если Не ИспользуетсяГрафикПроизводства Тогда
			ДанныеСпецификации.Этапы.ЗаполнитьЗначения(Группа.ДатаПотребности, "ДатаПроизводства"); // Желаемая дата выпуска
		КонецЕсли;
		
		// Наследование реквизитов выпуска
		ЗаполнитьИзделияСпецификацииПоИзделиямКЗапуску(ДанныеСпецификации, ИзделияПартии);
		
		// Объект-имитация партии
		ЗаполнитьЗначенияСвойств(ДанныеПартии, Группа, "Распоряжение, НазначениеПродукция");
		ЗаполнитьЗначенияСвойств(ДанныеПартии, ДанныеСпецификации, "Спецификация, ТипПроизводственногоПроцесса");
		
		// Назначение материалов
		НазначенияМатериалов = ОбеспечениеПроизводства.КоэффицентыНазначенийОбеспеченияВЭтапеПроизводства(
			ДанныеПартии,
			ДанныеСпецификации.ВыходныеИзделия);
		ДанныеСпецификации.Вставить("НазначенияМатериалов", НазначенияМатериалов);
		
		// Дополнение данных спецификации
		Документы.ЭтапПроизводства2_2.ПодготовитьДанныеСпецификацииКСозданиюЭтапов(ДанныеСпецификации, ДанныеПартии);
		
		// Статус этапов
		ДанныеСпецификации.Этапы.Колонки.Добавить(
			"Статус", Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыЭтаповПроизводства2_2"));
		
		Если ПередатьКВыполнениюВсе Тогда
			ДанныеСпецификации.Этапы.ЗаполнитьЗначения(Перечисления.СтатусыЭтаповПроизводства2_2.КВыполнению, "Статус");
		Иначе
			Для каждого Этап Из ДанныеСпецификации.Этапы Цикл
				Если ПередатьКВыполнениюПервые И Этап.НомерЭтапа = 1 Тогда
					Этап.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.КВыполнению;
				Иначе
					Этап.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Сформирован;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		// Очистка стека сообщений
		Для каждого Сообщение Из ПолучитьСообщенияПользователю(Истина) Цикл
			Сообщения.Добавить(Сообщение);
		КонецЦикла;
		
		// Создание этапов
		РеквизитыШапки = УправлениеПроизводствомВызовСервера.ЗначенияЗаполненияНовогоЭтапаПроизводства(
			ДанныеСпецификации.Распоряжение,
			ДанныеСпецификации.НазначениеПродукция,
			ДанныеСпецификации.ПартияПроизводства,
			ДанныеСпецификации.ТипПроизводственногоПроцесса,
			ДанныеСпецификации.Спецификация);
		
		Для Индекс = 0 По ДанныеСпецификации.Этапы.Количество()-1 Цикл
			
			ДанныеЭтапа = ДанныеСпецификации.Этапы[Индекс];
			
			Если ДанныеЭтапа.НомерЭтапа = 1
				И ИспользуетсяГрафикПроизводства Тогда
				ДанныеЭтапа.ПланироватьНеРанее = ИзделияПартии[0].ДатаЗапуска;
			КонецЕсли;
			
			ДанныеЗаполнения = Новый Структура;
			ДанныеЗаполнения.Вставить("РеквизитыШапки", РеквизитыШапки);
			ДанныеЗаполнения.Вставить("ДанныеСпецификации", ДанныеСпецификации);
			ДанныеЗаполнения.Вставить("ДанныеЭтапа", ДанныеЭтапа);
			
			ДокументОбъект = Документы.ЭтапПроизводства2_2.СоздатьДокумент();
			ДокументОбъект.УстановитьСсылкуНового(ДанныеЭтапа.ЭтапПроизводства);
			ДокументОбъект.УстановитьРежимПакетногоФормирования();
			ДокументОбъект.Заполнить(ДанныеЗаполнения);
			
			// Отказ от проверки заполнения дополнительных реквизитов
			ПараметрыСеанса.ИнтерактивнаяПроверкаЗаполненияСвойств = Истина;
			
			Если Не ДокументОбъект.ПроверитьЗаполнение() Тогда
				ДокументОбъект.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Формируется;
			КонецЕсли;
			
			Пока Истина Цикл
				
				Попытка
					ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
					СсылкиПроведен.Добавить(ДокументОбъект.Ссылка);
				Исключение
					
					Если ДокументОбъект.Статус <> Перечисления.СтатусыЭтаповПроизводства2_2.Формируется Тогда
						ДокументОбъект.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Формируется;
						Продолжить;
					КонецЕсли;
					
					ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
					
				КонецПопытки;
				
				Прервать;
			
			КонецЦикла;
			
			НоваяСтрока = РезультатФормирования.Добавить();
			НоваяСтрока.Партия = ДанныеСпецификации.ПартияПроизводства;
			НоваяСтрока.Документ = ДокументОбъект.Ссылка;
			СозданныеОбъекты.Добавить(ДокументОбъект.Ссылка);
			
			Если ПеренестиГрафик Тогда
				НоваяСтрока = ЭтапыКПереносуГрафика.Добавить();
				НоваяСтрока.ЗаказНаПроизводство = ДокументОбъект.Распоряжение;
				НоваяСтрока.КлючПартияСтарый = ИзделияПартии[0].КлючПартия;
				НоваяСтрока.КлючПартияНовый = КлючПартияНовый;
				НоваяСтрока.Ссылка = ДокументОбъект.Ссылка;
				НоваяСтрока.Этап = ДокументОбъект.Этап;
			КонецЕсли;
			
			Для каждого Сообщение Из ПолучитьСообщенияПользователю(Истина) Цикл
				Сообщение.КлючДанных = ДокументОбъект.Ссылка;
				Сообщения.Добавить(Сообщение);
			КонецЦикла;
			
		КонецЦикла;
		
		// Фиксация результата
		ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	КонецЦикла;
	
	// Наследование графика
	Если ЗначениеЗаполнено(ЭтапыКПереносуГрафика) Тогда
		ГрафикПроизводстваСтруктурыЗаказа.ПеренестиГрафикПартийПриЗапуске(ЭтапыКПереносуГрафика);
	КонецЕсли;
	
	// Очистка стека сообщений
	Для каждого Сообщение Из ПолучитьСообщенияПользователю(Истина) Цикл
		Сообщения.Добавить(Сообщение);
	КонецЦикла;
	
	// Запись в регистры
	Если СсылкиПроведен.ВГраница() <> -1 Тогда
		Распоряжения = ОбщегоНазначенияУТ.УникальныеЗначенияИзКолонкиТаблицы(ГруппыИзделий, "Распоряжение");
 		Очередь = Константы.ГраницаРасчетаСтруктурыЗаказов.ТекущаяОчередь();

		// Заполнение обеспечения и формирование движений 
		ПараметрыФормирования = Документы.ЭтапПроизводства2_2.ПараметрыФормированияЭтапов();
		ЗаполнитьЗначенияСвойств(ПараметрыФормирования, Параметры, "РезервироватьМатериалы, ЗаменятьМатериалыНаАналоги");
		ПараметрыФормирования.Распоряжения = Распоряжения;
		Документы.ЭтапПроизводства2_2.ЗаполнитьОбеспечениеИОтразитьДвиженияПоЭтапам(
			ПараметрыФормирования, СсылкиПроведен, Новый МенеджерВременныхТаблиц, Очередь);
		
		// Заполнение связанных регистров
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПооперационноеУправление")
				ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПооперационноеПланирование") Тогда
			ЭтапыДляРасчетаОчередиЗаданий = Документы.ЭтапПроизводства2_2.ТребуетсяРассчитатьОчередьОпераций(СсылкиПроведен);
			Если ЭтапыДляРасчетаОчередиЗаданий.Количество() > 0 Тогда
				ТаблицаДанныхЗаданий = 
					РегистрыСведений.ЗаданияКРасчетуОчередиПроизводственныхОпераций.ИнициализироватьТаблицуДанныхЗаданий();
				Для Каждого ЭтапДляРасчета Из ЭтапыДляРасчетаОчередиЗаданий Цикл
					НоваяСтрока = ТаблицаДанныхЗаданий.Добавить();
					НоваяСтрока.ОбъектРасчета = ЭтапДляРасчета;
				КонецЦикла;
				РегистрыСведений.ЗаданияКРасчетуОчередиПроизводственныхОпераций.ДобавитьЗадания(ТаблицаДанныхЗаданий);
			КонецЕсли;
		КонецЕсли;
		
		РегистрыКОтражению = СостоянияДокументов.РегистрыКОтражениюСостоянияЗаказов();
		РегистрыКОтражению.СостоянияЭтаповПроизводства = Истина;
		
		СостоянияДокументов.ДобавитьЗаданияКОтражениюСостоянияЗаказов(
			Неопределено,
			СсылкиПроведен,
			"ДЕЙСТВИЕ_ОТРАЗИТЬ_СОСТОЯНИЕ_ЭТАПОВ",
			РегистрыКОтражению);
		
		РегистрыКОтражению = СостоянияДокументов.РегистрыКОтражениюСостоянияЗаказов();
		РегистрыКОтражению.СостоянияЗаказовНаПроизводство = Истина;
		
		СостоянияДокументов.ДобавитьЗаданияКОтражениюСостоянияЗаказов(
			Неопределено,
			Распоряжения,,
			РегистрыКОтражению);
		
		Если ЗаданияКРасчетуГрафика.Количество() > 0 Тогда
			ЗаданияКРасчетуГрафика.ЗаполнитьЗначения(Очередь, "Очередь");
			РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства.ДобавитьЗадания(ЗаданияКРасчетуГрафика);
		КонецЕсли;
	КонецЕсли;
	
	// Очистка стека сообщений
	Для каждого Сообщение Из ПолучитьСообщенияПользователю(Истина) Цикл
		Сообщения.Добавить(Сообщение);
	КонецЦикла;
	
	// Фиксация результата
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

Процедура ЗаполнитьИзделияСпецификацииПоИзделиямКЗапуску(ДанныеСпецификации, Изделия)
	
	ТипыСтатей = Новый Массив;
	ТипыСтатей.Добавить(Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов"));
	ТипыСтатей.Добавить(Тип("ПланВидовХарактеристикСсылка.СтатьиАктивовПассивов"));
	ОТСтатьяРасходов = Новый ОписаниеТипов(ТипыСтатей);
	
	ТипыПолучателей = Новый Массив;
	ТипыПолучателей.Добавить(Тип("СправочникСсылка.СтруктураПредприятия"));
	ТипыПолучателей.Добавить(Тип("СправочникСсылка.Склады"));
	ОТПолучатель = Новый ОписаниеТипов(ТипыПолучателей);
	
	Для Сч = 1 По 2 Цикл
		
		Таблица = ДанныеСпецификации[?(Сч = 1, "ВыходныеИзделия", "ВозвратныеОтходы")]; // ТаблицаЗначений
		Таблица.Колонки.Добавить("Назначение", Новый ОписаниеТипов("СправочникСсылка.Назначения"));
		Таблица.Колонки.Добавить("Распределено", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла));
		Таблица.Колонки.Добавить("СписатьНаРасходы", Новый ОписаниеТипов("Булево"));
		Таблица.Колонки.Добавить("СтатьяРасходов", ОТСтатьяРасходов);
		Таблица.Колонки.Добавить("АналитикаРасходов", Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.Тип);
		Таблица.Колонки.Добавить("АналитикаАктивовПассивов",
			Метаданные.ПланыВидовХарактеристик.СтатьиАктивовПассивов.Тип);
		
		Таблица.Колонки.Добавить("Получатель", ОТПолучатель);
		Таблица.ЗагрузитьКолонку(Таблица.ВыгрузитьКолонку("Склад"), "Получатель");
		
	КонецЦикла;
	
	ИменаКолонок = СтрРазделить(
		"Характеристика,Назначение,СписатьНаРасходы,СтатьяРасходов,АналитикаРасходов,АналитикаАктивовПассивов", ",");
	
	ИндексИзделия = 0;
	Пока ИндексИзделия < Изделия.Количество() Цикл
		
		Изделие = Изделия[ИндексИзделия];
		ИндексИзделия = ИндексИзделия + 1;
		
		Для Сч = 1 По 2 Цикл
			
			Индекс = 0;
			Пока Индекс < ДанныеСпецификации[?(Сч = 1, "ВыходныеИзделия", "ВозвратныеОтходы")].Количество() Цикл
				
				Строка = ДанныеСпецификации[?(Сч = 1, "ВыходныеИзделия", "ВозвратныеОтходы")][Индекс];
				Индекс = Индекс + 1;
				
				Если Строка.Распределено = Строка.Количество
					ИЛИ Строка.Номенклатура <> Изделие.Номенклатура
					ИЛИ Строка.Характеристика <> Изделие.Характеристика
						И ЗначениеЗаполнено(Строка.Характеристика)
						И НЕ Строка.ЛюбаяХарактеристика Тогда
					Продолжить;
				КонецЕсли;
				
				Распределено = Мин(Строка.Количество-Строка.Распределено, Изделие.Количество);
				
				Если Строка.Распределено = 0
						И Изделие.Количество >= Строка.Количество
					ИЛИ СтрокиИдентичны(Строка, Изделие, ИменаКолонок)
						И (Строка.Получатель = Изделие.Получатель
							ИЛИ Строка.Распределено = 0 И Не ЗначениеЗаполнено(Строка.Получатель)) Тогда
					
					// Разделять строку не нужно
					СтрокаЗаполнить = Строка;
					
				ИначеЕсли Строка.Округлить
					И НЕ Строка.ЭтоЦеховаяКладовая
					И Окр(Строка.Количество - Распределено) <> (Строка.Количество - Распределено) Тогда
					
					// При разделении строки будут нарушены правила округления
					Если Строка.Распределено = 0 Тогда
						Строка.Округлить = Ложь;
						СтрокаЗаполнить = Строка;
					Иначе
						ВладелецСтроки = Строка.Владелец(); // ТаблицаЗначений
						НоваяСтрока = ВладелецСтроки.Вставить(Индекс-1);
						ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
						НоваяСтрока.Количество = Строка.Количество - Строка.Распределено;
						НоваяСтрока.КоличествоУпаковок = НоваяСтрока.Количество
							* НоваяСтрока.ДанныеУпаковки.Знаменатель / НоваяСтрока.ДанныеУпаковки.Числитель;
						НоваяСтрока.Распределено = 0;
						НоваяСтрока.Округлить = Ложь;
						СтрокаЗаполнить = НоваяСтрока;
						
						Строка.Количество = Строка.Распределено;
						Строка.КоличествоУпаковок = Строка.Количество
							* Строка.ДанныеУпаковки.Знаменатель / Строка.ДанныеУпаковки.Числитель;
						Если Сч = 1 Тогда
							ПроизводствоКлиентСервер.ПересчитатьДолюСтоимостиПриРазбиенииСтроки(
								НоваяСтрока, Строка, ДанныеСпецификации.СпособРаспределенияЗатратНаВыходныеИзделия);
						КонецЕсли;
						
						Индекс = Индекс + 1;
					КонецЕсли;
					
				Иначе
					
					// Разделение строки
					ВладелецСтроки = Строка.Владелец(); // ТаблицаЗначений
					НоваяСтрока = ВладелецСтроки.Вставить(Индекс-1);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
					НоваяСтрока.Количество = Распределено;
					НоваяСтрока.КоличествоУпаковок = НоваяСтрока.Количество
						* НоваяСтрока.ДанныеУпаковки.Знаменатель / НоваяСтрока.ДанныеУпаковки.Числитель;
					НоваяСтрока.Распределено = 0;
					СтрокаЗаполнить = НоваяСтрока;
					
					Строка.Количество = Строка.Количество - Распределено;
					Строка.КоличествоУпаковок = Строка.Количество
						* Строка.ДанныеУпаковки.Знаменатель / Строка.ДанныеУпаковки.Числитель;
					Если Сч = 1 Тогда
						ПроизводствоКлиентСервер.ПересчитатьДолюСтоимостиПриРазбиенииСтроки(
							НоваяСтрока, Строка, ДанныеСпецификации.СпособРаспределенияЗатратНаВыходныеИзделия);
					КонецЕсли;
					
					Индекс = Индекс + 1;
					
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(СтрокаЗаполнить, Изделие,
					"Характеристика,Получатель,Назначение,СписатьНаРасходы,СтатьяРасходов,АналитикаРасходов,АналитикаАктивовПассивов");
				СтрокаЗаполнить.Распределено = СтрокаЗаполнить.Распределено + Распределено;
				
				Изделие.Количество = Изделие.Количество - Распределено;
				Если Изделие.Количество = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			Если Изделие.Количество = 0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СтрокиИдентичны(Строка1, Строка2, ИменаКолонок)
	
	Для каждого Колонка Из ИменаКолонок Цикл
		Если Строка1[Колонка] <> Строка2[Колонка]
			И (ЗначениеЗаполнено(Строка1[Колонка])
				Или ЗначениеЗаполнено(Строка2[Колонка])) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Процедура ДобавитьОшибку(Ошибки, Группа, Изделия, ТекстИсключения = "")
	
	Изделие = Изделия.Найти(Группа.НомерГруппы, "НомерГруппы");
	
	НоваяСтрока = Ошибки.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Группа, "Спецификация, НаправлениеДеятельности");
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Изделие, "Номенклатура, Характеристика");
	
	Сообщения = ПолучитьСообщенияПользователю(Истина);
	Если Сообщения.ВГраница() = -1 И НЕ ПустаяСтрока(ТекстИсключения) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = ТекстИсключения;
		Сообщения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сообщение);
	КонецЕсли;
	НоваяСтрока.Сообщения = Сообщения;
	
КонецПроцедуры

#КонецОбласти
