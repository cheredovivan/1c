#Область СлужебныеПроцедурыИФункции

Функция КонтекстОтправкиПодтвержденияПолучения(Знач МассивСсылок) Экспорт
	СЭДОФСС.ПроверитьНаличиеПраваОбмена();
	УправлениеДоступом.ПроверитьИзменениеРазрешено(МассивСсылок[0]);
	
	ЗначенияРеквизитовОбъектов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		МассивСсылок,
		"Ссылка, Страхователь, ИдентификаторСообщения",
		Истина);
	
	Страхователи = Новый Массив;
	ИдентификаторыСообщенийСтрахователей = Новый Соответствие;
	
	Для Каждого КлючИЗначение Из ЗначенияРеквизитовОбъектов Цикл
		ЗначенияРеквизитов = КлючИЗначение.Значение;
		Идентификаторы = ИдентификаторыСообщенийСтрахователей[ЗначенияРеквизитов.Страхователь];
		Если Идентификаторы = Неопределено Тогда
			Идентификаторы = Новый Массив;
			ИдентификаторыСообщенийСтрахователей.Вставить(ЗначенияРеквизитов.Страхователь, Идентификаторы);
			Страхователи.Добавить(ЗначенияРеквизитов.Страхователь);
		КонецЕсли;
		Идентификаторы.Добавить(ЗначенияРеквизитов.ИдентификаторСообщения);
	КонецЦикла;
	
	Контекст = Новый Структура;
	Контекст.Вставить("Страхователи", Страхователи);
	Контекст.Вставить("ИдентификаторыСообщенийСтрахователей", ИдентификаторыСообщенийСтрахователей);
	
	Возврат Контекст;
КонецФункции

Процедура ПослеОтправкиПодтвержденияПолучения(Знач МассивСсылок) Экспорт
	СЭДОФСС.ПослеОтправкиПодтвержденияПолучения(МассивСсылок);
КонецПроцедуры

Функция СтрахователиИзОтборовПоОрганизациямФормы(Знач ОтборыПоОрганизациям) Экспорт
	Возврат СЭДОФСС.СтрахователиИзОтборовПоОрганизациямФормы(ОтборыПоОрганизациям);
КонецФункции

Функция СтрахователиОрганизаций(Знач Организации) Экспорт
	Возврат СЭДОФСС.СтрахователиОрганизаций(Организации);
КонецФункции

Функция ТекстXML(Знач Идентификатор) Экспорт
	Возврат СЭДОФСС.ТекстXML(Идентификатор);
КонецФункции

Функция КонтекстПолученияДанныхВходящегоВложения(Знач Ссылка, ИдентификаторФормы) Экспорт
	Результат = СЭДОФСС.КонтекстПодтвержденияПолучения(Ссылка);
	Результат.Вставить("СсылкаФайла", Неопределено);
	Результат.Вставить("ДанныеФайла", Неопределено);
	Результат.Вставить("Загружен", Неопределено);
	
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Ссылка);
	Менеджер.ЗаполнитьКонтекстПолученияДанныхВложения(Результат);
	
	Если ЗначениеЗаполнено(Результат.СсылкаФайла) Тогда
		ДополнительныеПараметры = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
		ДополнительныеПараметры.ИдентификаторФормы = ИдентификаторФормы;
		Результат.ДанныеФайла = РаботаСФайлами.ДанныеФайла(Результат.СсылкаФайла, ДополнительныеПараметры);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Процедура СохранитьОтветНаВопросОПодтвержденииПолученияВходящегоДокумента(ВариантОтвета) Экспорт
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ПособияФСС.СЭДО",
		"ОтветНаВопросОПодтвержденииПолучения",
		ВариантОтвета);
КонецПроцедуры

#КонецОбласти
