#Область ПрограммныйИнтерфейс

// Обработать данные штрихкода.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ДанныеШтрихкода - Неопределено, Структура - Результат анализа штрихкода, все данные, которые удалось по нему собрать.
//  ПараметрыСканирования - (См. ШтрихкодированиеИСКлиент.ПараметрыСканирования).
//  ВложенныеШтрихкоды - Неопределено - Вложенные штрихкоды
// 
// Возвращаемое значение:
//   см. ШтрихкодированиеИС.ИнициализироватьРезультатОбработкиШтрихкода
Функция ОбработатьДанныеШтрихкода(Форма, ДанныеШтрихкода, ПараметрыСканирования, ВложенныеШтрихкоды = Неопределено) Экспорт
	
	РезультатОбработки = ШтрихкодированиеОбщегоНазначенияИС.ИнициализироватьРезультатОбработкиШтрихкода(ПараметрыСканирования, ДанныеШтрихкода);
	
	Возврат РезультатОбработки;
	
КонецФункции

Функция ПоискДублейШКУпаковок(ШтрихкодыУпаковок, ШтрихкодУпаковки) Экспорт
	
	ТЗШтрихкодов = ШтрихкодыУпаковок.Выгрузить(,"ШтрихкодУпаковки"); 
	МассивШтрихкодов = ТЗШтрихкодов.ВыгрузитьКолонку("ШтрихкодУпаковки");
	
	ДеревоМаркированнойПродукции = ШтрихкодированиеИС.ВложенныеШтрихкодыИзвестныхУпаковок(МассивШтрихкодов);	
	ТЗМаркированныхТоваров = ДеревоМаркированнойПродукции.МаркированныеТовары;
	
	МассивВложенныхШтрихкодов = Новый Массив;
	МассивВложенныхШтрихкодов.Добавить(ШтрихкодУпаковки);
	МассивВложенныхШтрихкодов = ЗаполнитьВложенныеКоды(МассивВложенныхШтрихкодов, ШтрихкодУпаковки);
	
	ЕстьДубль = Ложь;
	
	Отбор = Новый Структура;
	
	Для Каждого ШКОтсканированный Из МассивВложенныхШтрихкодов Цикл  
		Отбор.Вставить("ШтрихкодУпаковки", ШКОтсканированный);
		Дубли = ТЗМаркированныхТоваров.НайтиСтроки(Отбор);
		Если ЗначениеЗаполнено(Дубли) Тогда
			ЕстьДубль = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЕстьДубль;
	
КонецФункции

Функция ОбработатьВводШтрихкода(ДокументСсылка, ШтрихкодКоличество, КэшированныеЗначения, ПараметрыСканирования = Неопределено) Экспорт
	
	ПараметрыСканирования.Склад                                         = ДокументСсылка.Склад;
	ПараметрыСканирования.СсылкаНаОбъект                                = ДокументСсылка;
	ПараметрыСканирования.ИспользуетсяСоответствиеШтрихкодовСтрокДерева = Истина;
	ПараметрыСканирования.Вставить("ОтключитьПоискСуществующего", Истина);
	Если Не ПараметрыСканирования.Свойство("ЭтоПроверкаКодовМаркировкиИСМП") Тогда		
		ПараметрыСканирования.Вставить("ЭтоПроверкаКодовМаркировкиИСМП", Ложь);
		ПараметрыСканирования.Вставить("ЭтоОтчетПроизводственнойЛинии", Ложь);
		ПараметрыСканирования.Вставить("ВидОперацииИСМП", ПредопределенноеЗначение("Перечисление.ВидыОперацийИСМП.ОтгрузкаПродажа"));
	КонецЕсли;
	
	ПараметрыСканирования.ДопустимыеВидыПродукции.Очистить();
	ПараметрыСканирования.ДопустимыеВидыПродукции = ВидыПродукцииИСМП();
	
	ЭтоОтборРазмещение = (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтборРазмещениеТоваров"));
	
	Если ЭтоОтборРазмещение Тогда
		Менеджер = Документы.ОтборРазмещениеТоваров;
	Иначе
		Менеджер = Документы.ПриходныйОрдерНаТовары;
	КонецЕсли;
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ДокументСсылка, Менеджер);
	Если ЭтоОтборРазмещение Тогда
		ПараметрыУказанияСерий = ПараметрыУказанияСерий.Отбор;
	КонецЕсли;
	ПараметрыУказанияСерий.ИмяПоляПомещение = Неопределено;
	
	ПараметрыСканирования.ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(ПараметрыУказанияСерий);
	
	ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ЗакодироватьШтрихкодДанныхBase64(ШтрихкодКоличество);
	
	РезультатОбработкиШтрихкода = ШтрихкодированиеОбщегоНазначенияИСВызовСервера.ОбработатьШтрихкод(
	ШтрихкодКоличество, ПараметрыСканирования, Неопределено, ДокументСсылка.УникальныйИдентификатор());
	
	Возврат РезультатОбработкиШтрихкода;
	
КонецФункции

Функция ВидыПродукцииИСМП() Экспорт
	
	ДопустимыеВидыПродукции = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиУчетаМаркируемойПродукцииИСМП.ВидПродукции КАК ВидПродукции
		|ИЗ
		|	РегистрСведений.НастройкиУчетаМаркируемойПродукцииИСМП КАК НастройкиУчетаМаркируемойПродукцииИСМП
		|ГДЕ
		|	НастройкиУчетаМаркируемойПродукцииИСМП.ВестиУчетПродукции";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ДопустимыеВидыПродукции.Добавить(ВыборкаДетальныеЗаписи.ВидПродукции);
	КонецЦикла;
	
	Возврат ДопустимыеВидыПродукции;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗаполнитьВложенныеКоды(МассивВложенныхШтрихкодов, ШтрихкодУпаковки)
	
	Для Каждого СтрокаШтрихкод Из ШтрихкодУпаковки.ВложенныеШтрихкоды Цикл
		МассивВложенныхШтрихкодов.Добавить(СтрокаШтрихкод.Штрихкод);
		ЗаполнитьВложенныеКоды(МассивВложенныхШтрихкодов, СтрокаШтрихкод.Штрихкод);
	КонецЦикла;
	
	Возврат МассивВложенныхШтрихкодов;
	
КонецФункции

#КонецОбласти