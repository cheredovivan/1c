#Область СлужебныеПроцедурыИФункции

// Процедура определяет тип документа и вид операции в зависимости от имени команды.
//
// Параметры:
//  ИмяКоманды - Строка - Имя команды, для которой необходимо определить тип документа и вид операции.
//  Результат  - Структура: 
//   * ТипДокумента - Строка - Тип документа, который будет создан.
//   * ВидОперации - ПеречислениеСсылка.ХозяйственныеОперации - Вид операции документа.
//
Процедура ТипДокументаИВидОперации(ИмяКоманды, Результат) Экспорт
	
	ИмяКоманды = СтрЗаменить(ИмяКоманды, "Форма", "");
	
	Если ИмяКоманды = "СоздатьРеализацияТовары" Или ИмяКоманды = "СоздатьРеализацияТоварыСчетФактура" Тогда
		Результат.ТипДокумента = "РеализацияТоваровУслуг";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту");
	ИначеЕсли ИмяКоманды = "СоздатьРеализацияУслуги" Или ИмяКоманды = "СоздатьРеализацияУслугиСчетФактура" Тогда
		Результат.ТипДокумента = "РеализацияУслугПрочихАктивов";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту");
	ИначеЕсли ИмяКоманды = "СоздатьРеализацияПродажаКомиссия" Тогда
		Результат.ТипДокумента = "РеализацияТоваровУслуг";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту");
	ИначеЕсли ИмяКоманды = "СоздатьПоступлениеПокупкаКомиссия" Тогда
		Результат.ТипДокумента = "ПриобретениеТоваровУслуг";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика");
	ИначеЕсли ИмяКоманды = "СоздатьПоступлениеТовары" Или ИмяКоманды = "СоздатьПоступлениеТоварыСчетФактура" Тогда
		Результат.ТипДокумента = "ПриобретениеТоваровУслуг";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика");
	ИначеЕсли ИмяКоманды = "СоздатьПоступлениеУслуги" Или ИмяКоманды = "СоздатьПоступлениеУслугиСчетФактура" Тогда
		Результат.ТипДокумента = "ПриобретениеУслугПрочихАктивов";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика");
	ИначеЕсли ИмяКоманды = "СоздатьАктВыполненныхРабот" Тогда
		Результат.ТипДокумента = "АктВыполненныхРабот";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту");
	Иначе
		Результат = Неопределено;
	КонецЕсли;
		
КонецПроцедуры

// Устанавливает тип документа и вид операции по умолчанию для документа в зависимости от направления.
//
// Параметры:
//  Направление - Перечисление.НаправленияРаспознанногоДокумента - Направление документа (входящий или исходящий).
//  ТипДокумента - Строка - Тип документа, который будет установлен.
//  Результат - Структура:
//   * ТипДокумента - Строка - Тип документа, который будет установлен.
//   * ВидОперации - ПеречислениеСсылка.ХозяйственныеОперации - Вид операции документа, который будет установлен.
//
Процедура ТипДокументаИВидОперацииДляДокументаПоУмолчанию(Направление, ТипДокумента, Результат) Экспорт
		
	Если Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Входящий") Тогда
		Результат.ТипДокумента = "ПоступлениеТоваровУслуг";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика");
	Иначе
		Результат.ТипДокумента = "РеализацияТоваровУслуг";
		Результат.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту");
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает тип документа и массив типов для доступных для создания документов на основании.
//
// Параметры:
//  ТипДокумента - Строка - Тип документа, который будет установлен.
//  Результат - Структура:
//  .* ТипДокумента - Строка - Тип документа, который будет установлен.
//  .* ПодходящиеТипы - Массив - Массив типов документов, доступных для создания на основании.
//
Процедура ПодходящиеТипыРаспознанногоДокумента(ТипДокумента, Результат) Экспорт
	
	Результат = Новый Массив;
	Если ТипДокумента = Тип("ДокументСсылка.СчетНаОплатуКлиенту")
		Или ТипДокумента = Тип("ДокументСсылка.ЗаказПоставщику")
		Или ТипДокумента = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату"));
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
		Или ТипДокумента = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов")
		Или ТипДокумента = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипДокумента = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.УПД"));
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.ТОРГ12"));
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг"));
		// Т.к. поступление или реализацию можно создать по распознанной СФ
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетФактура"));
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.СчетФактураПолученный")
		Или ТипДокумента = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг"));
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетФактура"));
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.УПД"));
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		
		Результат.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг"));
	
	КонецЕсли;
	
КонецПроцедуры

// Процедура - При заполнении параметров выбора договора
//
// Параметры:
//  ДокументОбъект - ДанныеФормыСтруктура
//  ПараметрыВыбораДоговора - Структура
//
Процедура ПриЗаполненииПараметровВыбораДоговора(ДокументОбъект, ПараметрыВыбораДоговора) Экспорт
	
	ВидыДоговоров = Новый Массив;
	Если ДокументОбъект.Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
		ВидыДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПокупателем"));
	Иначе
		ВидыДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПоставщиком"));
	КонецЕсли;
	
	ПараметрыВыбораДоговора.Вставить("ВидыДоговоров", ВидыДоговоров);
	ПараметрыВыбораДоговора.Вставить("Партнер", ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка"));
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДанныеФормыСтруктура") Тогда
		Отбор = Новый Структура("ИмяРеквизита", "Партнер");
		НайденныеСтроки = ДокументОбъект.РеквизитыДокумента.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() Тогда
			ПараметрыВыбораДоговора.Партнер = НайденныеСтроки[0].Значение;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПриЗаполненииПараметровВыбораДоговора()

// Процедура - Обязательные реквизиты подчиненного документа
//
// Параметры:
//  ЭтотТипДокумента1С - Тип
//  ТипИВид - Структура:
//...* ВидОперации - ПеречислениеСсылка.ХозяйственныеОперации
//  Реквизиты - Структура
// 
Процедура ОбязательныеРеквизитыПодчиненногоДокумента(ЭтотТипДокумента1С, ТипИВид, Реквизиты) Экспорт
	
	Если ЭтотТипДокумента1С = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
		Или ЭтотТипДокумента1С = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		Реквизиты.Вставить("НомерДокумента");
		Реквизиты.Вставить("ДатаДокумента");   
		
		Если ТипИВид.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту")
			Или ТипИВид.ВидОперации = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика") Тогда
			Реквизиты.Вставить("Склад");
		КонецЕсли;
		
	ИначеЕсли ЭтотТипДокумента1С = Тип("ДокументСсылка.СчетФактураПолученный")
		Или ЭтотТипДокумента1С = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
		Реквизиты.Вставить("НомерДокумента");
		Реквизиты.Вставить("ДатаДокумента");
	КонецЕсли;
		
КонецПроцедуры

// Процедура - Создаваемые документы комплекта
//
// Параметры:
//  ПропускатьТип - Булево 
//  Тип - Строка 
//  ВидДокумента - Строка 
//
Процедура СоздаваемыеДокументыКомплекта(ПропускатьТип, Тип, ВидДокумента) Экспорт

	Если Тип = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		Если ВидДокумента = "ПУИПА" Тогда
			ПропускатьТип = Истина;
		КонецЕсли;
	ИначеЕсли Тип = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов") Тогда
		Если ВидДокумента = "ПТИУ" Или ВидДокумента = "" Тогда
			ПропускатьТип = Истина;
		КонецЕсли;
	ИначеЕсли Тип = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Если ВидДокумента = "РУИПА" Тогда
			ПропускатьТип = Истина;
		КонецЕсли;
	ИначеЕсли Тип = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		Если ВидДокумента = "РТИУ" Или ВидДокумента = "" Тогда
			ПропускатьТип = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти