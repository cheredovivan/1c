
#Область ПрограммныйИнтерфейс

#Область Проведение

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - см. ПроведениеДокументов.СвойстваДокумента
//
// Возвращаемое значение:
//  Структура - см. ПроведениеДокументов.ПараметрыУчетногоМеханизма
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	// Проведение
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат);
		//++ Локализация
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.ПрослеживаемыеТоварыОтгруженныеВЕАЭС);
		//-- Локализация
		
	КонецЕсли;
	
	// Контроль
	Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		
		ТипДокумента = ТипЗнч(Документ);
		
		ИсправительныйДокумент = ИсправлениеДокументов.ЭтоИсправительныйДокумент(Документ);
		
		Если Не Свойства.ЭтоНовый
				И (ТипДокумента = Тип("ДокументОбъект.ЗаказКлиента")
				Или ТипДокумента = Тип("ДокументОбъект.ЗаявкаНаВозвратТоваровОтКлиента")
				//++ НЕ УТ
				
				//++ Устарело_Переработка24
				Или ТипДокумента = Тип("ДокументОбъект.ЗаказПереработчику")
				//-- Устарело_Переработка24
				Или ТипДокумента = Тип("ДокументОбъект.ЗаказПереработчику2_5")
				//-- НЕ УТ
				
				//++ НЕ УТКА
				
				//++ Устарело_Переработка24
				Или ТипДокумента = Тип("ДокументОбъект.ЗаказДавальца")
				//-- Устарело_Переработка24
				Или ТипДокумента = Тип("ДокументОбъект.ЗаказДавальца2_5")
				Или ТипДокумента = Тип("ДокументОбъект.ЭтапПроизводства2_2") И Документ.ПроизводствоНаСтороне
				//-- НЕ УТКА
				Или Ложь)
			Или Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение
				И (ТипДокумента = Тип("ДокументОбъект.АктВыполненныхРабот")
				Или ТипДокумента = Тип("ДокументОбъект.ВозвратТоваровОтКлиента")
				Или ТипДокумента = Тип("ДокументОбъект.ЗаявкаНаВозвратТоваровОтКлиента")
				Или ТипДокумента = Тип("ДокументОбъект.ПоступлениеТоваровОтХранителя")
				Или ТипДокумента = Тип("ДокументОбъект.РеализацияТоваровУслуг")
				Или ТипДокумента = Тип("ДокументОбъект.ОтчетКомитентуОЗакупках")
				Или ТипДокумента = Тип("ДокументОбъект.ОтгрузкаТоваровСХранения")
				//++ НЕ УТ
				
				//++ Устарело_Переработка24
				Или ТипДокумента = Тип("ДокументОбъект.ПередачаСырьяПереработчику")
				//-- Устарело_Переработка24
				
				//-- НЕ УТ
				Или ТипДокумента = Тип("ДокументОбъект.ПередачаТоваровХранителю")
				//++ НЕ УТКА
				
				//++ Устарело_Переработка24
				Или ТипДокумента = Тип("ДокументОбъект.ПередачаДавальцу")
				//-- Устарело_Переработка24
				Или ТипДокумента = Тип("ДокументОбъект.ОтчетДавальцу2_5")
				//-- НЕ УТКА
				Или Ложь)
			Или (ТипДокумента = Тип("ДокументОбъект.ОтгрузкаТоваровКлиенту")
				Или ТипДокумента = Тип("ДокументОбъект.СписаниеОтгруженныхТоваров")
				Или ТипДокумента = Тип("ДокументОбъект.ОприходованиеИзлишковОтгруженныхТоваров")
				Или ТипДокумента = Тип("ДокументОбъект.ПоступлениеТоваровОтКлиента")
				Или ТипДокумента = Тип("ДокументОбъект.РеализацияТоваровУслуг")
					И Документ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереходПраваСобственности)
			
			Или ИсправительныйДокумент Тогда
			
			Параметры.КонтрольныеРегистрыИзменений.Добавить(Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Контроль даты запрета
	Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		Параметры.КонтрольныеРегистрыДатаЗапрета.Добавить(Метаданные.РегистрыНакопления.ВыручкаИСебестоимостьПродаж);
		Параметры.КонтрольныеРегистрыДатаЗапрета.Добавить(Метаданные.РегистрыНакопления.ПрочаяВыручка);
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Возвращает тексты запросов для сторнирования движений при исправлении документов
// 
// Параметры:
// 	МетаданныеДокумента - ОбъектМетаданныхДокумент - Метаданные документа, который проводится.
// 
// Возвращаемое значение:
// 	Соответствие - Соответствие полного имени регистра тексту запроса сторнирования
//
Функция ТекстыЗапросовСторнирования(МетаданныеДокумента) Экспорт
	
	ДвиженияДокумента = МетаданныеДокумента.Движения;
	
	ТекстыЗапросов = Новый Соответствие();
	
	МетаданныеРегистра = Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат;
	Если ДвиженияДокумента.Содержит(МетаданныеРегистра) Тогда
		ТекстыЗапросов.Вставить(МетаданныеРегистра.ПолноеИмя(),
			ПроведениеДокументов.ТекстСторнирующегоЗапроса(
				МетаданныеРегистра, МетаданныеДокумента));
	КонецЕсли;
	
	//++ Локализация
	МетаданныеРегистра = Метаданные.РегистрыНакопления.ПрослеживаемыеТоварыОтгруженныеВЕАЭС;
	Если ДвиженияДокумента.Содержит(МетаданныеРегистра) Тогда
		ТекстыЗапросов.Вставить(МетаданныеРегистра.ПолноеИмя(),
			ПроведениеДокументов.ТекстСторнирующегоЗапроса(
				МетаданныеРегистра, МетаданныеДокумента));
	КонецЕсли;
	//-- Локализация
		
	Возврат ТекстыЗапросов;
	
КонецФункции

// Дополняет текст запроса механизма проверки даты запрета по таблице изменений.
// 
// Параметры:
// 	Запрос - Запрос - используется для установки параметров запроса.
// 
// Возвращаемое значение:
//	Соответствие - соответствие имен таблиц изменения регистров и текстов запросов.
//	
Функция ТекстыЗапросовКонтрольДатыЗапретаПоТаблицеИзменений(Запрос) Экспорт
	
	СоответствиеТекстовЗапросов = Новый Соответствие();
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийВыручкаИСебестоимостьПродаж", 
		РегистрыНакопления.ВыручкаИСебестоимостьПродаж.ТекстЗапросаКонтрольДатыЗапрета(Запрос));
	Возврат СоответствиеТекстовЗапросов;
	
КонецФункции

// Процедура формирования движений по подчиненным регистрам продаж.
//
// Параметры:
//	ТаблицыДляДвижений - Структура - таблицы данных документа
//	Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//	Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "РаспоряженияНаОтгрузкуИВозврат");
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "УдалитьЗаявкиНаВозвратТоваровОтКлиентов");
	//++ Локализация
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "ПрослеживаемыеТоварыОтгруженныеВЕАЭС");
	//-- Локализация
	
КонецПроцедуры

// Формирует тексты запросов для контроля изменений записанных движений регистров.
//
// Параметры:
//  Запрос - Запрос - запрос, хранящий параметры используемые в списке запросов
//  ТекстыЗапроса - СписокЗначений Из Строка - список текстов запросов и их имен.
//  Документ - ДокументОбъект - записываемый документ.
//
Процедура ИнициализироватьДанныеКонтроляИзменений(Запрос, ТекстыЗапроса, Документ) Экспорт
	
	#Область РаспоряженияНаОтгрузкуИВозврат
	
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "ДвиженияРаспоряженияНаОтгрузкуИзменение") Тогда
		
		ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
		ПараметрыФормированияТаблицы.ТекстУсловия =
			"(Распоряжение, Номенклатура, Характеристика) В
			|		(ВЫБРАТЬ
			|			Таблица.Распоряжение,
			|			Таблица.Номенклатура,
			|			Таблица.Характеристика
			|		ИЗ
			|			ДвиженияРаспоряженияНаОтгрузкуИзменение КАК Таблица)";
		ПараметрыФормированияТаблицы.Группировка = "Распоряжение, Номенклатура, Характеристика, КодСтроки, Склад, Серия";
		ПараметрыФормированияТаблицы.Показатели =
			"КОформлению, Оформлено, КОтгрузке, Отгружено, ОприходованоДоППС, СписаноДоППС,
			|ЗаявленоНаВозврат, КОформлениюПоВозвратам, ОформленоПоВозвратам, КПриемке, Принято, ПринятоПоВозвратам";
		ПараметрыФормированияТаблицы.Ресурсы = "Количество";
		ПараметрыФормированияТаблицы.ИмяВременнойТаблицы = "ВТ_РаспоряженияНаОтгрузкуКонтроль";
		
		ВТ_РаспоряженияНаОтгрузкуКонтроль =
			РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
				ПараметрыФормированияТаблицы);
		
		ТекстыЗапроса.Добавить(ВТ_РаспоряженияНаОтгрузкуКонтроль);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров") Тогда
			
			ТекстВременнойТаблицыДопустимыхОтклонений =
			"ВЫБРАТЬ
			|	ДопустимыеОтклонения.Распоряжение КАК Распоряжение,
			|	ДопустимыеОтклонения.Номенклатура КАК Номенклатура,
			|	ДопустимыеОтклонения.Характеристика КАК Характеристика,
			|	ДопустимыеОтклонения.Склад КАК Склад,
			|	ДопустимыеОтклонения.Серия КАК Серия,
			|	ДопустимыеОтклонения.ДопустимоеОтклонениеКОформлению КАК ДопустимоеОтклонениеКОформлению,
			|	ДопустимыеОтклонения.ДопустимоеОтклонениеКОтгрузке КАК ДопустимоеОтклонениеКОтгрузке,
			|	ДопустимыеОтклонения.ДопустимоеОтклонениеКВозврату КАК ДопустимоеОтклонениеКВозврату,
			|	ДопустимыеОтклонения.ДопустимоеОтклонениеКПриемке КАК ДопустимоеОтклонениеКПриемке
			|ПОМЕСТИТЬ ТаблицаДопустимыхОтклонений
			|ИЗ
			|	ВТДопустимыеОтклоненияРаспоряженияНаОтгрузку КАК ДопустимыеОтклонения
			|ГДЕ
			|	ТИПЗНАЧЕНИЯ(ДопустимыеОтклонения.Распоряжение) В
			|	(ТИП(Документ.ЗаказКлиента),
			|	ТИП(Документ.ОтгрузкаТоваровКлиенту),
			|	ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента),
			|	ТИП(Документ.КорректировкаРеализации))
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Распоряжение,
			|	Номенклатура,
			|	Характеристика,
			|	Склад,
			|	Серия";
			
			ТекстыЗапроса.Добавить(ТекстВременнойТаблицыДопустимыхОтклонений);
			
		Иначе
			
			ТекстВременнойТаблицыДопустимыхОтклонений =
			"ВЫБРАТЬ
			|	НЕОПРЕДЕЛЕНО КАК Распоряжение,
			|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
			|	НЕОПРЕДЕЛЕНО КАК Характеристика,
			|	НЕОПРЕДЕЛЕНО КАК Склад,
			|	НЕОПРЕДЕЛЕНО КАК Серия,
			|	0 КАК ДопустимоеОтклонениеКОформлению,
			|	0 КАК ДопустимоеОтклонениеКОтгрузке,
			|	0 КАК ДопустимоеОтклонениеКВозврату,
			|	0 КАК ДопустимоеОтклонениеКПриемке
			|ПОМЕСТИТЬ ТаблицаДопустимыхОтклонений";
			
			ТекстыЗапроса.Добавить(ТекстВременнойТаблицыДопустимыхОтклонений);
			
			ТекстВременнойТаблицыСкладыСерии =
			"ВЫБРАТЬ
			|	ТаблицаОборотов.Распоряжение КАК Распоряжение,
			|	ТаблицаОборотов.КодСтроки КАК КодСтроки,
			|	МАКСИМУМ(ТаблицаОборотов.Склад) КАК Склад,
			|	МАКСИМУМ(ТаблицаОборотов.Серия) КАК Серия
			|ПОМЕСТИТЬ ТаблицаСкладыСерии
			|ИЗ
			|	РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат.Обороты(,,, (Распоряжение, Номенклатура, Характеристика) В
			|		(ВЫБРАТЬ
			|			Таблица.Распоряжение,
			|			Таблица.Номенклатура,
			|			Таблица.Характеристика
			|		ИЗ
			|			ДвиженияРаспоряженияНаОтгрузкуИзменение КАК Таблица)
			|	И КодСтроки <> 0
			|	И Показатель В (ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Заказано),
			|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОтгрузке),
			|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Отгружено))) КАК ТаблицаОборотов
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаОборотов.Распоряжение,
			|	ТаблицаОборотов.КодСтроки
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Распоряжение,
			|	КодСтроки";
			
			ТекстыЗапроса.Добавить(ТекстВременнойТаблицыСкладыСерии);
			
		КонецЕсли;
		
		ТекстЗапросаГруппировкаПоПоказателям =
		"ВЫБРАТЬ
		|	Подзапрос.Распоряжение КАК Распоряжение,
		|	Подзапрос.Номенклатура КАК Номенклатура,
		|	Подзапрос.Характеристика КАК Характеристика,
		|	Подзапрос.Склад КАК Склад,
		|	Подзапрос.Серия КАК Серия,
		|	СУММА(Подзапрос.ЗаявленоНаВозвратКоличествоОборот) КАК ЗаявленоНаВозвратКоличествоОборот,
		|	СУММА(Подзапрос.КОформлениюКоличествоОборот)
		|		- СУММА(Подзапрос.ОприходованоДоППСБезСклада)
		|		+ СУММА(Подзапрос.СписаноДоППСБезСклада)
		|		+ СУММА(Подзапрос.ПринятоБезСклада) КАК КОформлениюКоличествоОборот,
		|	СУММА(Подзапрос.ОформленоКоличествоОборот)
		|		- СУММА(Подзапрос.ОприходованоДоППСБезСклада)
		|		+ СУММА(Подзапрос.СписаноДоППСБезСклада)
		|		+ СУММА(Подзапрос.ПринятоБезСклада) КАК ОформленоКоличествоОборот,
		|	СУММА(Подзапрос.ОформленоКоличествоОборот) КАК ТолькоОформленоКоличествоОборот,
		|	СУММА(Подзапрос.КОформлениюПоВозвратамКоличествоОборот) КАК КОформлениюПоВозвратамКоличествоОборот,
		|	СУММА(Подзапрос.ОформленоПоВозвратамКоличествоОборот) КАК ОформленоПоВозвратамКоличествоОборот,
		|	СУММА(Подзапрос.КОтгрузкеКоличествоОборот)
		|		+ СУММА(Подзапрос.ОприходованоДоППССклад)
		|		- СУММА(Подзапрос.СписаноДоППССклад)
		|		- СУММА(Подзапрос.ПринятоСклад) КАК КОтгрузкеКоличествоОборот,
		|	СУММА(Подзапрос.ОтгруженоКоличествоОборот)
		|		+ СУММА(Подзапрос.ОприходованоДоППССклад)
		|		- СУММА(Подзапрос.СписаноДоППССклад)
		|		- СУММА(Подзапрос.ПринятоСклад) КАК ОтгруженоКоличествоОборот,
		|	СУММА(Подзапрос.ОтгруженоКоличествоОборот) ТолькоОтгруженоКоличествоОборот,
		|	СУММА(Подзапрос.КПриемкеКоличествоОборот) КАК КПриемкеКоличествоОборот,
		|	СУММА(Подзапрос.ПринятоКоличествоОборот) КАК ПринятоКоличествоОборот,
		|	СУММА(Подзапрос.ПринятоПоВозвратамКоличествоОборот) КАК ПринятоПоВозвратамКоличествоОборот
		|ПОМЕСТИТЬ ВТ_РаспоряженияНаОтгрузкуКонтрольСклады
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаОборотов.Распоряжение КАК Распоряжение,
		|		ТаблицаОборотов.Номенклатура КАК Номенклатура,
		|		ТаблицаОборотов.Характеристика КАК Характеристика,
		|		ТаблицаОборотов.КодСтроки КАК КодСтроки,
		|		ВЫБОР
		|			КОГДА ТаблицаОборотов.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				ТОГДА ЕСТЬNULL(ТаблицаСкладыСерии.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
		|			ИНАЧЕ ТаблицаОборотов.Склад
		|		КОНЕЦ КАК Склад,
		|		ВЫБОР
		|			КОГДА ТаблицаОборотов.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|				ТОГДА ЕСТЬNULL(ТаблицаСкладыСерии.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
		|			ИНАЧЕ ТаблицаОборотов.Серия
		|		КОНЕЦ КАК Серия,
		|		ТаблицаОборотов.ЗаявленоНаВозвратКоличествоОборот КАК ЗаявленоНаВозвратКоличествоОборот,
		|		ТаблицаОборотов.КОформлениюКоличествоОборот КАК КОформлениюКоличествоОборот,
		|		ТаблицаОборотов.ОформленоКоличествоОборот КАК ОформленоКоличествоОборот,
		|		ТаблицаОборотов.КОформлениюПоВозвратамКоличествоОборот КАК КОформлениюПоВозвратамКоличествоОборот,
		|		ТаблицаОборотов.ОформленоПоВозвратамКоличествоОборот КАК ОформленоПоВозвратамКоличествоОборот,
		|		ТаблицаОборотов.КОтгрузкеКоличествоОборот КАК КОтгрузкеКоличествоОборот,
		|		ТаблицаОборотов.ОтгруженоКоличествоОборот КАК ОтгруженоКоличествоОборот,
		|		ТаблицаОборотов.КПриемкеКоличествоОборот КАК КПриемкеКоличествоОборот,
		|		ТаблицаОборотов.ПринятоКоличествоОборот КАК ПринятоКоличествоОборот,
		|		ТаблицаОборотов.ПринятоПоВозвратамКоличествоОборот КАК ПринятоПоВозвратамКоличествоОборот,
		|		ВЫБОР
		|			КОГДА ТаблицаОборотов.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				ТОГДА ТаблицаОборотов.ОприходованоДоППСКоличествоОборот
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ОприходованоДоППСБезСклада,
		|		ВЫБОР
		|			КОГДА ТаблицаОборотов.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				ТОГДА ТаблицаОборотов.СписаноДоППСКоличествоОборот
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СписаноДоППСБезСклада,
		|		ВЫБОР
		|			КОГДА ТаблицаОборотов.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				ТОГДА ТаблицаОборотов.ПринятоКоличествоОборот
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ПринятоБезСклада,
		|		ВЫБОР
		|			КОГДА ТаблицаОборотов.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				ТОГДА ТаблицаОборотов.ОприходованоДоППСКоличествоОборот
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ОприходованоДоППССклад,
		|		ВЫБОР
		|			КОГДА ТаблицаОборотов.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				ТОГДА ТаблицаОборотов.СписаноДоППСКоличествоОборот
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК СписаноДоППССклад,
		|		ВЫБОР
		|			КОГДА ТаблицаОборотов.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|				ТОГДА ТаблицаОборотов.ПринятоКоличествоОборот
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ПринятоСклад
		|	ИЗ
		|		ВТ_РаспоряженияНаОтгрузкуКонтроль КАК ТаблицаОборотов
		|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСкладыСерии КАК ТаблицаСкладыСерии
		|			ПО ТаблицаОборотов.Распоряжение = ТаблицаСкладыСерии.Распоряжение
		|			И ТаблицаОборотов.КодСтроки = ТаблицаСкладыСерии.КодСтроки
		|			И ТаблицаОборотов.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК Подзапрос
		|СГРУППИРОВАТЬ ПО
		|	Подзапрос.Распоряжение,
		|	Подзапрос.Номенклатура,
		|	Подзапрос.Характеристика,
		|	Подзапрос.Склад,
		|	Подзапрос.Серия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	Номенклатура,
		|	Характеристика,
		|	Склад,
		|	Серия";
		
		ТекстыЗапроса.Добавить(ТекстЗапросаГруппировкаПоПоказателям);
		
		ТекстЗапросаИсточникДанных =
		"ВЫБРАТЬ
		|	ПодзапросОбороты.Распоряжение КАК Распоряжение,
		|	ПодзапросОбороты.Номенклатура КАК Номенклатура,
		|	ПодзапросОбороты.Характеристика КАК Характеристика,
		|	ПодзапросОбороты.Склад КАК Склад,
		|	ПодзапросОбороты.Серия КАК Серия,
		|	СУММА(ПодзапросОбороты.ЗаявленоНаВозврат) КАК ЗаявленоНаВозврат,
		|	СУММА(ПодзапросОбороты.КОформлению) КАК КОформлению,
		|	СУММА(ПодзапросОбороты.Оформлено) КАК Оформлено,
		|	СУММА(ПодзапросОбороты.ТолькоОформлено) КАК ТолькоОформлено,
		|	СУММА(ПодзапросОбороты.КОформлениюПоВозвратам) КАК КОформлениюПоВозвратам,
		|	СУММА(ПодзапросОбороты.ОформленоПоВозвратам) КАК ОформленоПоВозвратам,
		|	СУММА(ПодзапросОбороты.КОтгрузке) КАК КОтгрузке,
		|	СУММА(ПодзапросОбороты.Отгружено) КАК Отгружено,
		|	СУММА(ПодзапросОбороты.ТолькоОтгружено) КАК ТолькоОтгружено,
		|	СУММА(ПодзапросОбороты.КПриемке) КАК КПриемке,
		|	СУММА(ПодзапросОбороты.Принято) КАК Принято,
		|	СУММА(ПодзапросОбороты.ПринятоПоВозвратам) КАК ПринятоПоВозвратам,
		|	СУММА(ПодзапросОбороты.КОформлению)
		|		+ СУММА(ПодзапросОбороты.ДопустимоеОтклонениеКОформлению) КАК КОформлениюСОтклонением,
		|	СУММА(ПодзапросОбороты.КОтгрузке)
		|		+ СУММА(ПодзапросОбороты.ДопустимоеОтклонениеКОтгрузке) КАК КОтгрузкеСОтклонением,
		|	СУММА(ПодзапросОбороты.ЗаявленоНаВозврат)
		|		+ СУММА(ПодзапросОбороты.ДопустимоеОтклонениеКВозврату) КАК ЗаявленоНаВозвратСОтклонением,
		|	СУММА(ПодзапросОбороты.КОформлениюПоВозвратам)
		|		+ СУММА(ПодзапросОбороты.ДопустимоеОтклонениеКВозврату) КАК КОформлениюПоВозвратамСОтклонением,
		|	СУММА(ПодзапросОбороты.КПриемке)
		|		+ СУММА(ПодзапросОбороты.ДопустимоеОтклонениеКПриемке) КАК КПриемкеСОтклонением
		|ПОМЕСТИТЬ ИсточникДанных
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаОборотов.Распоряжение КАК Распоряжение,
		|		ТаблицаОборотов.Номенклатура КАК Номенклатура,
		|		ТаблицаОборотов.Характеристика КАК Характеристика,
		|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
		|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
		|		ТаблицаОборотов.ЗаявленоНаВозвратКоличествоОборот КАК ЗаявленоНаВозврат,
		|		ТаблицаОборотов.КОформлениюКоличествоОборот КАК КОформлению,
		|		ТаблицаОборотов.ОформленоКоличествоОборот КАК Оформлено,
		|		ТаблицаОборотов.ТолькоОформленоКоличествоОборот КАК ТолькоОформлено,
		|		ТаблицаОборотов.КОформлениюПоВозвратамКоличествоОборот КАК КОформлениюПоВозвратам,
		|		ТаблицаОборотов.ОформленоПоВозвратамКоличествоОборот КАК ОформленоПоВозвратам,
		|		ТаблицаОборотов.КОтгрузкеКоличествоОборот КАК КОтгрузке,
		|		ТаблицаОборотов.ОтгруженоКоличествоОборот КАК Отгружено,
		|		ТаблицаОборотов.ТолькоОтгруженоКоличествоОборот КАК ТолькоОтгружено,
		|		ТаблицаОборотов.КПриемкеКоличествоОборот КАК КПриемке,
		|		ТаблицаОборотов.ПринятоКоличествоОборот КАК Принято,
		|		ТаблицаОборотов.ПринятоПоВозвратамКоличествоОборот КАК ПринятоПоВозвратам,
		|		ЕСТЬNULL(ДопустимыеОтклонения.ДопустимоеОтклонениеКОформлению, 0) КАК ДопустимоеОтклонениеКОформлению,
		|		ЕСТЬNULL(ДопустимыеОтклонения.ДопустимоеОтклонениеКОтгрузке, 0) КАК ДопустимоеОтклонениеКОтгрузке,
		|		ЕСТЬNULL(ДопустимыеОтклонения.ДопустимоеОтклонениеКВозврату, 0) КАК ДопустимоеОтклонениеКВозврату,
		|		ЕСТЬNULL(ДопустимыеОтклонения.ДопустимоеОтклонениеКПриемке, 0) КАК ДопустимоеОтклонениеКПриемке
		|	ИЗ
		|		ВТ_РаспоряженияНаОтгрузкуКонтрольСклады КАК ТаблицаОборотов
		|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДопустимыхОтклонений КАК ДопустимыеОтклонения
		|			ПО ТаблицаОборотов.Распоряжение = ДопустимыеОтклонения.Распоряжение
		|			И ТаблицаОборотов.Номенклатура = ДопустимыеОтклонения.Номенклатура
		|			И ТаблицаОборотов.Характеристика = ДопустимыеОтклонения.Характеристика
		|			И ТаблицаОборотов.Склад = ДопустимыеОтклонения.Склад
		|			И ТаблицаОборотов.Серия = ДопустимыеОтклонения.Серия) КАК ПодзапросОбороты
		|СГРУППИРОВАТЬ ПО
		|	ПодзапросОбороты.Распоряжение,
		|	ПодзапросОбороты.Номенклатура,
		|	ПодзапросОбороты.Характеристика,
		|	ПодзапросОбороты.Склад,
		|	ПодзапросОбороты.Серия";
		
		ТекстыЗапроса.Добавить(ТекстЗапросаИсточникДанных);
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаОстатков.Распоряжение КАК Распоряжение,
		|	ТаблицаОстатков.Номенклатура КАК Номенклатура,
		|	ТаблицаНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаОстатков.Характеристика КАК Характеристика,
		|	ТаблицаОстатков.Склад КАК Склад,
		|	ТаблицаОстатков.Серия КАК Серия,
		|	ВЫБОР
		|		КОГДА ТаблицаОстатков.ЗаявленоНаВозвратСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ""ЗаявленоНаВозврат""
		|		КОГДА ТаблицаОстатков.КОформлениюСОтклонением < ТаблицаОстатков.Оформлено
		|			ТОГДА ""КОформлению""
		|		КОГДА ТаблицаОстатков.КОформлениюПоВозвратамСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ""КОформлениюПоВозвратам""
		|		КОГДА ТаблицаОстатков.КОтгрузкеСОтклонением < ТаблицаОстатков.Отгружено
		|			ТОГДА ""КОтгрузке""
		|		КОГДА ТаблицаОстатков.КПриемкеСОтклонением < (ТаблицаОстатков.Принято + ТаблицаОстатков.ПринятоПоВозвратам)
		|			ТОГДА ""КПриемке""
		//
		// Формула контроля продаж:
		//
		// КОтгрузке + ОприходованоДоППС (со складом) - СписаноДоППС (склад) - Принято (склад) >=
		// КОформлению - ОприходованоДоППС (без склада) + СписаноДоППС (без склада) + Принято (без склада) >=
		// Отгружено + ОприходованоДоППС (со складом) - СписаноДоППС (склад) - Принято (склад) >=
		// Оформлено - ОприходованоДоППС (без склада) + СписаноДоППС (без склада) + Принято (без склада)
		//
		// И
		//
		// КОтгрузке >= Отгружено >= КПриемке >= Принято
		//
		|		КОГДА ТаблицаОстатков.КОтгрузке < ТаблицаОстатков.КОформлению
		|			ТОГДА ""Продажи_КОтгрузке""
		|		КОГДА ТаблицаОстатков.КОформлениюСОтклонением < ТаблицаОстатков.Отгружено
		|			ТОГДА ""Продажи_КОформлению""
		|		КОГДА ТаблицаОстатков.Отгружено < ТаблицаОстатков.Оформлено
		|			ТОГДА ""Продажи_Отгружено""
		|		КОГДА ТаблицаОстатков.ТолькоОформлено < 0
		|			ТОГДА ""Продажи_Оформлено""
		|		КОГДА ТаблицаОстатков.ТолькоОтгружено < ТаблицаОстатков.КПриемке
		|			ТОГДА ""Продажи_КПриемке""
		|	КОНЕЦ КАК КодОшибкиКонтроля,
		|	ВЫБОР
		|		КОГДА ТаблицаОстатков.ЗаявленоНаВозвратСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ТаблицаОстатков.ОформленоПоВозвратам - ТаблицаОстатков.ЗаявленоНаВозвратСОтклонением
		|		КОГДА ТаблицаОстатков.КОформлениюСОтклонением < ТаблицаОстатков.Оформлено
		|			ТОГДА ТаблицаОстатков.Оформлено - ТаблицаОстатков.КОформлениюСОтклонением
		|		КОГДА ТаблицаОстатков.КОформлениюПоВозвратамСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ТаблицаОстатков.ОформленоПоВозвратам - ТаблицаОстатков.КОформлениюПоВозвратамСОтклонением
		|		КОГДА ТаблицаОстатков.КОтгрузкеСОтклонением < ТаблицаОстатков.Отгружено
		|			ТОГДА ТаблицаОстатков.Отгружено - ТаблицаОстатков.КОтгрузкеСОтклонением
		|		КОГДА ТаблицаОстатков.КПриемкеСОтклонением < (ТаблицаОстатков.Принято + ТаблицаОстатков.ПринятоПоВозвратам)
		|			ТОГДА (ТаблицаОстатков.Принято + ТаблицаОстатков.ПринятоПоВозвратам) - ТаблицаОстатков.КПриемкеСОтклонением
		|		КОГДА ТаблицаОстатков.КОтгрузке < ТаблицаОстатков.КОформлению
		|			ТОГДА ТаблицаОстатков.КОформлению - ТаблицаОстатков.КОтгрузке
		|		КОГДА ТаблицаОстатков.КОформлениюСОтклонением < ТаблицаОстатков.Отгружено
		|			ТОГДА ТаблицаОстатков.Отгружено - ТаблицаОстатков.КОформлениюСОтклонением
		|		КОГДА ТаблицаОстатков.Отгружено < ТаблицаОстатков.Оформлено
		|			ТОГДА ТаблицаОстатков.Оформлено - ТаблицаОстатков.Отгружено
		|		КОГДА ТаблицаОстатков.ТолькоОформлено < 0
		|			ТОГДА ТаблицаОстатков.ТолькоОформлено
		|		КОГДА ТаблицаОстатков.ТолькоОтгружено < ТаблицаОстатков.КПриемке
		|			ТОГДА ТаблицаОстатков.КПриемке - ТаблицаОстатков.ТолькоОтгружено
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЗначениеРасхождения
		|ПОМЕСТИТЬ ТаблицаОшибокКонтроля
		|ИЗ
		|	ИсточникДанных КАК ТаблицаОстатков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
		|		ПО ТаблицаОстатков.Номенклатура = ТаблицаНоменклатуры.Ссылка
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ТаблицаОстатков.Распоряжение) = ТИП(Документ.ЗаказКлиента)
		|	ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаОстатков.Распоряжение) = ТИП(Документ.ОтгрузкаТоваровКлиенту)
		|	ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаОстатков.Распоряжение) = ТИП(Документ.ПервичныйДокумент)
		|		И ВЫРАЗИТЬ(ТаблицаОстатков.Распоряжение КАК Документ.ПервичныйДокумент).ТипПервичногоДокумента =
		|			ЗНАЧЕНИЕ(Перечисление.ТипыПервичныхДокументов.ОтгрузкаТоваровКлиенту)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаОстатков.Распоряжение КАК Распоряжение,
		|	ТаблицаОстатков.Номенклатура КАК Номенклатура,
		|	ТаблицаНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаОстатков.Характеристика КАК Характеристика,
		|	ТаблицаОстатков.Склад КАК Склад,
		|	ТаблицаОстатков.Серия КАК Серия,
		|	ВЫБОР
		|		КОГДА ТаблицаОстатков.ЗаявленоНаВозвратСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ""ЗаявленоНаВозврат""
		|		КОГДА ТаблицаОстатков.КОформлениюСОтклонением < ТаблицаОстатков.Оформлено
		|			ТОГДА ""КОформлению""
		|		КОГДА ТаблицаОстатков.КОформлениюПоВозвратамСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ""КОформлениюПоВозвратам""
		|		КОГДА ТаблицаОстатков.КОтгрузкеСОтклонением < ТаблицаОстатков.Отгружено
		|			ТОГДА ""КОтгрузке""
		|		КОГДА ТаблицаОстатков.КПриемкеСОтклонением < (ТаблицаОстатков.Принято + ТаблицаОстатков.ПринятоПоВозвратам)
		|			ТОГДА ""КПриемке""
		//
		// Формула контроля возвратов:
		//
		// ЗаявленоНаВозврат >= КОформлениюПоВозвратам >= КПриемке >= ОформленоПоВозвратам >= (Принято + ПринятоПоВозвратам)
		//
		|		КОГДА ТаблицаОстатков.ЗаявленоНаВозврат < ТаблицаОстатков.КОформлениюПоВозвратам
		|			ТОГДА ""Возвраты_ЗаявленоНаВозврат""
		|		КОГДА ТаблицаОстатков.КОформлениюПоВозвратам < ТаблицаОстатков.КПриемке
		|			ТОГДА ""Возвраты_КОформлениюПоВозвратам""
		|		КОГДА ТаблицаОстатков.КПриемкеСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ""Возвраты_КПриемке""
		|		КОГДА ТаблицаОстатков.ОформленоПоВозвратам < (ТаблицаОстатков.Принято + ТаблицаОстатков.ПринятоПоВозвратам)
		|			ТОГДА ""Возвраты_ОформленоПоВозвратам""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК КодОшибкиКонтроля,
		|	ВЫБОР
		|		КОГДА ТаблицаОстатков.ЗаявленоНаВозвратСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ТаблицаОстатков.ОформленоПоВозвратам - ТаблицаОстатков.ЗаявленоНаВозвратСОтклонением
		|		КОГДА ТаблицаОстатков.КОформлениюСОтклонением < ТаблицаОстатков.Оформлено
		|			ТОГДА ТаблицаОстатков.Оформлено - ТаблицаОстатков.КОформлениюСОтклонением
		|		КОГДА ТаблицаОстатков.КОформлениюПоВозвратамСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ТаблицаОстатков.ОформленоПоВозвратам - ТаблицаОстатков.КОформлениюПоВозвратамСОтклонением
		|		КОГДА ТаблицаОстатков.КОтгрузкеСОтклонением < ТаблицаОстатков.Отгружено
		|			ТОГДА ТаблицаОстатков.Отгружено - ТаблицаОстатков.КОтгрузкеСОтклонением
		|		КОГДА ТаблицаОстатков.КПриемкеСОтклонением < (ТаблицаОстатков.Принято + ТаблицаОстатков.ПринятоПоВозвратам)
		|			ТОГДА (ТаблицаОстатков.Принято + ТаблицаОстатков.ПринятоПоВозвратам) - ТаблицаОстатков.КПриемкеСОтклонением
		|		КОГДА ТаблицаОстатков.ЗаявленоНаВозврат < ТаблицаОстатков.КОформлениюПоВозвратам
		|			ТОГДА ТаблицаОстатков.КОформлениюПоВозвратам - ТаблицаОстатков.ЗаявленоНаВозврат
		|		КОГДА ТаблицаОстатков.КОформлениюПоВозвратам < ТаблицаОстатков.КПриемке
		|			ТОГДА ТаблицаОстатков.КПриемке - ТаблицаОстатков.КОформлениюПоВозвратам
		|		КОГДА ТаблицаОстатков.КПриемкеСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ТаблицаОстатков.ОформленоПоВозвратам - ТаблицаОстатков.КПриемкеСОтклонением
		|		КОГДА ТаблицаОстатков.ОформленоПоВозвратам < (ТаблицаОстатков.Принято + ТаблицаОстатков.ПринятоПоВозвратам)
		|			ТОГДА (ТаблицаОстатков.Принято + ТаблицаОстатков.ПринятоПоВозвратам) - ТаблицаОстатков.ОформленоПоВозвратам
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЗначениеРасхождения
		|ИЗ
		|	ИсточникДанных КАК ТаблицаОстатков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
		|		ПО ТаблицаОстатков.Номенклатура = ТаблицаНоменклатуры.Ссылка
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ТаблицаОстатков.Распоряжение) В
		|	(ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента), ТИП(Документ.КорректировкаРеализации))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаОстатков.Распоряжение КАК Распоряжение,
		|	ТаблицаОстатков.Номенклатура КАК Номенклатура,
		|	ТаблицаНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаОстатков.Характеристика КАК Характеристика,
		|	ТаблицаОстатков.Склад КАК Склад,
		|	ТаблицаОстатков.Серия КАК Серия,
		|	ВЫБОР
		|		КОГДА ТаблицаОстатков.ЗаявленоНаВозвратСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ""ЗаявленоНаВозврат""
		|		КОГДА ТаблицаОстатков.КОформлениюСОтклонением < ТаблицаОстатков.Оформлено
		|			ТОГДА ""КОформлению""
		|		КОГДА ТаблицаОстатков.КОформлениюПоВозвратамСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ""КОформлениюПоВозвратам""
		|		КОГДА ТаблицаОстатков.КОтгрузкеСОтклонением < ТаблицаОстатков.Отгружено
		|			ТОГДА ""КОтгрузке""
		|		КОГДА ТаблицаОстатков.КПриемкеСОтклонением < (ТаблицаОстатков.Принято + ТаблицаОстатков.ПринятоПоВозвратам)
		|			ТОГДА ""КПриемке""
		//
		// Формула контроля переработки:
		//
		// КОформлению >= Оформлено
		// И
		// КОтгрузке >= Отгружено
		//
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК КодОшибкиКонтроля,
		|	ВЫБОР
		|		КОГДА ТаблицаОстатков.ЗаявленоНаВозвратСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ТаблицаОстатков.ОформленоПоВозвратам - ТаблицаОстатков.ЗаявленоНаВозвратСОтклонением
		|		КОГДА ТаблицаОстатков.КОформлениюСОтклонением < ТаблицаОстатков.Оформлено
		|			ТОГДА ТаблицаОстатков.Оформлено - ТаблицаОстатков.КОформлениюСОтклонением
		|		КОГДА ТаблицаОстатков.КОформлениюПоВозвратамСОтклонением < ТаблицаОстатков.ОформленоПоВозвратам
		|			ТОГДА ТаблицаОстатков.ОформленоПоВозвратам - ТаблицаОстатков.КОформлениюПоВозвратамСОтклонением
		|		КОГДА ТаблицаОстатков.КОтгрузкеСОтклонением < ТаблицаОстатков.Отгружено
		|			ТОГДА ТаблицаОстатков.Отгружено - ТаблицаОстатков.КОтгрузкеСОтклонением
		|		КОГДА ТаблицаОстатков.КПриемкеСОтклонением < (ТаблицаОстатков.Принято + ТаблицаОстатков.ПринятоПоВозвратам)
		|			ТОГДА (ТаблицаОстатков.Принято + ТаблицаОстатков.ПринятоПоВозвратам) - ТаблицаОстатков.КПриемкеСОтклонением
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ЗначениеРасхождения
		|ИЗ
		|	ИсточникДанных КАК ТаблицаОстатков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
		|		ПО ТаблицаОстатков.Номенклатура = ТаблицаНоменклатуры.Ссылка
		//++ НЕ УТ
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ТаблицаОстатков.Распоряжение) В (
		//++ НЕ УТКА
		
		//++ Устарело_Переработка24
		|		ТИП(Документ.ЗаказДавальца),
		//-- Устарело_Переработка24
		|		ТИП(Документ.ЗаказДавальца2_5),
		//-- НЕ УТКА
		
		//++ Устарело_Переработка24
		|		ТИП(Документ.ЗаказПереработчику),
		//-- Устарело_Переработка24
		|		ТИП(Документ.ЗаказПереработчику2_5)
		|		)
		//-- НЕ УТ
		|";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ТаблицаКодыОшибок");
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаОшибокКонтроля.Распоряжение КАК Распоряжение,
		|	ТаблицаОшибокКонтроля.Номенклатура КАК Номенклатура,
		|	ТаблицаОшибокКонтроля.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаОшибокКонтроля.Характеристика КАК Характеристика,
		|	ТаблицаОшибокКонтроля.Серия КАК Серия,
		|	ТаблицаОшибокКонтроля.КодОшибкиКонтроля КАК КодОшибкиКонтроля,
		|	ТаблицаОшибокКонтроля.ЗначениеРасхождения КАК ЗначениеРасхождения
		|ИЗ
		|	ТаблицаОшибокКонтроля КАК ТаблицаОшибокКонтроля
		|ГДЕ
		|	ТаблицаОшибокКонтроля.ЗначениеРасхождения <> 0";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОшибкиРаспоряженияНаОтгрузку");
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ТоварыОрганизаций
	
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "ДвиженияТоварыОрганизацийИзменениеСводно") Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ТаблицаОстатков.Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Договор КАК Договор,
		|	ТаблицаОстатков.Номенклатура КАК Номенклатура,
		|	ТаблицаОстатков.Характеристика КАК Характеристика,
		|	(ТаблицаОстатков.КОформлениюПоВозвратамКоличествоОборот -
		|		ТаблицаОстатков.ОформленоПоВозвратамКоличествоОборот) КАК Количество
		|ПОМЕСТИТЬ ВТ_ОстатокКОформлениюПоЗаявкам
		|ИЗ
		|	#РаспоряженияНаОтгрузкуОборотыРазвернутая КАК ТаблицаОстатков";
		
		ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
		ПараметрыФормированияТаблицы.ТекстУсловия =
			"(ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Договор, Номенклатура, Характеристика) В
			|		(ВЫБРАТЬ
			|				Таблица.Договор,
			|				Таблица.Номенклатура,
			|				Таблица.Характеристика
			|			ИЗ
			|				ДвиженияТоварыОрганизацийИзменениеСводно КАК Таблица)";
		ПараметрыФормированияТаблицы.Группировка = "Распоряжение, Номенклатура, Характеристика";
		ПараметрыФормированияТаблицы.Показатели = "КОформлениюПоВозвратам, ОформленоПоВозвратам";
		ПараметрыФормированияТаблицы.Ресурсы = "Количество";
		
		РаспоряженияНаОтгрузкуОборотыРазвернутая =
			РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
				ПараметрыФормированияТаблицы);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"#РаспоряженияНаОтгрузкуОборотыРазвернутая",
			РаспоряженияНаОтгрузкуОборотыРазвернутая);
		
		ТекстыЗапроса.Добавить(ТекстЗапроса);
		
		ТекстыЗапроса.Добавить(
			"ВЫБРАТЬ
			|	Аналитики.КлючАналитики КАК Ссылка
			|ПОМЕСТИТЬ ВТ_ОтборАналитикУчетаНоменклатуры
			|ИЗ
			|	ДвиженияТоварыОрганизацийИзменениеСводно КАК ОстаткиПоЗаявкам
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитики
			|		ПО ОстаткиПоЗаявкам.Номенклатура = Аналитики.Номенклатура
			|		И ОстаткиПоЗаявкам.Характеристика = Аналитики.Характеристика
			|		И ОстаткиПоЗаявкам.Договор = Аналитики.МестоХранения");
		
		ТекстыЗапроса.Добавить(
			"ВЫБРАТЬ
			|	ТаблицаОстатков.АналитикаУчетаНоменклатуры.Договор КАК Договор,
			|	ТаблицаОстатков.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
			|	ТаблицаОстатков.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
			|	ТаблицаОстатков.КоличествоОстаток КАК Количество
			|ПОМЕСТИТЬ ВТ_ОстатокПоТоварамОрганизаций
			|ИЗ
			|	РегистрНакопления.ТоварыОрганизаций.Остатки(, (АналитикаУчетаНоменклатуры) В
			|		(ВЫБРАТЬ
			|			ОтборАналитик.Ссылка
			|		ИЗ
			|			ВТ_ОтборАналитикУчетаНоменклатуры КАК ОтборАналитик)) КАК ТаблицаОстатков");
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТаблицаОстатков.Договор КАК Договор,
			|	ТаблицаОстатков.Номенклатура КАК Номенклатура,
			|	ВЫРАЗИТЬ(ТаблицаОстатков.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ТаблицаОстатков.Характеристика КАК Характеристика,
			|	СУММА(ТаблицаОстатков.Количество) КАК Количество
			|ИЗ
			|	(ВЫБРАТЬ
			|		ТоварыОрганизаций.Договор КАК Договор,
			|		ТоварыОрганизаций.Номенклатура КАК Номенклатура,
			|		ТоварыОрганизаций.Характеристика КАК Характеристика,
			|		ТоварыОрганизаций.Количество КАК Количество
			|	ИЗ
			|		ВТ_ОстатокПоТоварамОрганизаций КАК ТоварыОрганизаций
			|
			|	ОБЪЕДИНИТЬ ВСЕ
			|
			|	ВЫБРАТЬ
			|		ЗаявкиНаВозврат.Договор КАК Договор,
			|		ЗаявкиНаВозврат.Номенклатура КАК Номенклатура,
			|		ЗаявкиНаВозврат.Характеристика КАК Характеристика,
			|		-ЗаявкиНаВозврат.Количество КАК Количество
			|	ИЗ
			|		ВТ_ОстатокКОформлениюПоЗаявкам КАК ЗаявкиНаВозврат) КАК ТаблицаОстатков
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаОстатков.Договор,
			|	ТаблицаОстатков.Номенклатура,
			|	ТаблицаОстатков.Характеристика,
			|	ВЫРАЗИТЬ(ТаблицаОстатков.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения
			|ИМЕЮЩИЕ
			|	СУММА(ТаблицаОстатков.Количество) < 0";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОшибкиВозвратТоваровОтХранителей");
		
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры

// Выводит сообщения пользователю при наличии ошибок контроля изменений записанных движений регистров.
//
// Параметры:
//  РезультатыКонтроля - Структура - таблицы с результатами контроля изменений
//  Документ - ДокументОбъект - записываемый документ
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура СообщитьОРезультатахКонтроляИзменений(РезультатыКонтроля, Документ, Отказ) Экспорт
	
	#Область РаспоряженияНаОтгрузкуИВозврат
	
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "ДвиженияРаспоряженияНаОтгрузкуИзменение") Тогда
		
		ТекстыОшибокКонтроля = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстыОшибокКонтроляОборотовПоПоказателям();
		
		ШаблонСообщения = НСтр("ru = 'Номенклатура %1';
								|en = '%1 item'");
		
		Для Каждого СтрокаОшибки Из РезультатыКонтроля.ОшибкиРаспоряженияНаОтгрузку Цикл
			
			ПредставлениеНоменклатуры = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
				СтрокаОшибки.Номенклатура,
				СтрокаОшибки.Характеристика);
			
			ЗаголовокСообщения = СтрШаблон(ШаблонСообщения,
				ПредставлениеНоменклатуры);
				
			ТекстСообщения = ЗаголовокСообщения
				+ Символы.ПС
				+ СтрШаблон(ТекстыОшибокКонтроля[СтрокаОшибки.КодОшибкиКонтроля],
					СтрокаОшибки.Распоряжение,
					СтрокаОшибки.ЗначениеРасхождения,
					СтрокаОшибки.ЕдиницаИзмерения);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
				Документ,,,
				Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ТоварыОрганизаций
	
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "ДвиженияТоварыОрганизацийИзменениеСводно") Тогда
		
		ШаблонСообщения = НСтр("ru = 'Номенклатура %1
			|Возвращено больше, чем указано по всем оформленным заявкам на возврат и поступлениям товаров от хранителя по договору %2, на %3 %4';
			|en = 'Items %1
			|Number of the returned ones exceeds the number of ones specified in all registered requests for return of goods and VMI returns under the %2 contract by %3 %4'");
		
		Для Каждого СтрокаОшибки Из РезультатыКонтроля.ОшибкиВозвратТоваровОтХранителей Цикл
			
			ПредставлениеНоменклатуры = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
				СтрокаОшибки.Номенклатура, СтрокаОшибки.Характеристика);
			
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПредставлениеНоменклатуры,
				СтрокаОшибки.Договор, -СтрокаОшибки.Количество, СтрокаОшибки.ЕдиницаИзмерения);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
				Документ,,,
				Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры

#КонецОбласти

#Область ОценкаРентабельностиПродаж

Процедура ДополнитьТекстыЗапросовОтчетРентабельностьПродажДаннымиДокументов(ТекстДополненияДанныеДокумента, ТекстДополненияСводныйРасчет) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКоммерческиеПредложенияКлиентам")
		И ПравоДоступа("Чтение", Метаданные.Документы.КоммерческоеПредложениеКлиенту) Тогда
		
		ТекстДополненияДанныеДокумента = "
			|ОБЪЕДИНИТЬ ВСЕ" + "
			|
			|ВЫБРАТЬ
			|	ТаблицаДокументов.Ссылка,
			|	ТаблицаДокументов.Дата,
			|	ТаблицаДокументов.Валюта,
			|	КоммерческоеПредложениеКлиентуТовары.НомерСтроки,
			|	КоммерческоеПредложениеКлиентуТовары.Номенклатура,
			|	КоммерческоеПредложениеКлиентуТовары.Характеристика,
			|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
			|	КоммерческоеПредложениеКлиентуТовары.ЕдиницаИзмерения,
			|	КоммерческоеПредложениеКлиентуТовары.ВидЦены,
			|	КоммерческоеПредложениеКлиентуТовары.Цена,
			|	КоммерческоеПредложениеКлиентуТовары.Сумма,
			|	КоммерческоеПредложениеКлиентуТовары.Количество,
			|	КоммерческоеПредложениеКлиентуТовары.ПроцентАвтоматическойСкидки,
			|	КоммерческоеПредложениеКлиентуТовары.ПроцентРучнойСкидки,
			|	КоммерческоеПредложениеКлиентуТовары.СуммаАвтоматическойСкидки,
			|	КоммерческоеПредложениеКлиентуТовары.СуммаРучнойСкидки,
			|	КоммерческоеПредложениеКлиентуТовары.СтавкаНДС,
			|	КоммерческоеПредложениеКлиентуТовары.СуммаНДС
			|ИЗ
			|	Документ.КоммерческоеПредложениеКлиенту.Товары КАК КоммерческоеПредложениеКлиентуТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КоммерческоеПредложениеКлиенту КАК ТаблицаДокументов
			|		ПО ТаблицаДокументов.Ссылка = &Документ
			|		И КоммерческоеПредложениеКлиентуТовары.Ссылка = ТаблицаДокументов.Ссылка
			|ГДЕ
			|	ТИПЗНАЧЕНИЯ(&Документ) = ТИП(Документ.КоммерческоеПредложениеКлиенту)";
			
		ТекстДополненияСводныйРасчет = "
			|ОБЪЕДИНИТЬ ВСЕ" + "
			|
			|ВЫБРАТЬ
			|	ТаблицаДокументов.Ссылка,
			|	ТаблицаДокументов.Дата,
			|	ТаблицаДокументов.Валюта,
			|	КоммерческоеПредложениеКлиентуТовары.НомерСтроки,
			|	КоммерческоеПредложениеКлиентуТовары.Номенклатура,
			|	КоммерческоеПредложениеКлиентуТовары.Характеристика,
			|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
			|	КоммерческоеПредложениеКлиентуТовары.ЕдиницаИзмерения,
			|	КоммерческоеПредложениеКлиентуТовары.ВидЦены,
			|	КоммерческоеПредложениеКлиентуТовары.Цена,
			|	КоммерческоеПредложениеКлиентуТовары.Сумма,
			|	КоммерческоеПредложениеКлиентуТовары.Количество,
			|	КоммерческоеПредложениеКлиентуТовары.ПроцентАвтоматическойСкидки,
			|	КоммерческоеПредложениеКлиентуТовары.ПроцентРучнойСкидки,
			|	КоммерческоеПредложениеКлиентуТовары.СуммаАвтоматическойСкидки,
			|	КоммерческоеПредложениеКлиентуТовары.СуммаРучнойСкидки,
			|	КоммерческоеПредложениеКлиентуТовары.СтавкаНДС,
			|	КоммерческоеПредложениеКлиентуТовары.СуммаНДС
			|ИЗ
			|	Документ.КоммерческоеПредложениеКлиенту.Товары КАК КоммерческоеПредложениеКлиентуТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КоммерческоеПредложениеКлиенту КАК ТаблицаДокументов
			|		ПО ТаблицаДокументов.Ссылка = &Документ
			|		И КоммерческоеПредложениеКлиентуТовары.Ссылка = ТаблицаДокументов.Ссылка
			|ГДЕ
			|	ТИПЗНАЧЕНИЯ(&Документ) = ТИП(Документ.КоммерческоеПредложениеКлиенту)";
		
	КонецЕсли;
	
КонецПроцедуры

// Настройка параметров отбора по контекту вызова.
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения.
// 	КомпоновщикНастроекФормы - КомпоновщикНастроекКомпоновкиДанных.
//
Процедура ПриНастройкеПараметровОтбораПоКонтекстуВызова(Форма, КомпоновщикНастроекФормы) Экспорт
	
	ЗначениеПараметраКоманды = Неопределено;
	Если Форма.Параметры.Свойство("ПараметрКоманды",ЗначениеПараметраКоманды) 
		И ТипЗнч(ЗначениеПараметраКоманды) = Тип("ДокументСсылка.КоммерческоеПредложениеКлиенту") 
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеПараметраКоманды,"ХозяйственнаяОперация") = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию") Тогда
		
		КомпоновкаДанныхСервер.УдалитьПараметрИзПользовательскихНастроекОтчета(КомпоновщикНастроекФормы.Настройки, "ИзменениеОбъемаРучныхСкидок");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаказКлиента

Процедура ПриОбработкеЗаполненияЗаказаКлиента(ДокументОбъект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.КоммерческоеПредложениеКлиенту") Тогда
		ЗаполнитьЗаказаКлиентаНаОснованииКоммерческогоПредложенияКлиенту(ДокументОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры
 
#КонецОбласти

#Область ПомощникПродаж

Процедура ПриИнициализацииПомощникаПродаж(Объект) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКоммерческиеПредложенияКлиентам") Тогда
		Объект.СоздаватьКоммерческоеПредложение = Истина;
		Объект.ПечататьКоммерческоеПредложение = Истина;
	КонецЕсли;
	
	Объект.СтатусКоммерческогоПредложения = Перечисления.СтатусыКоммерческихПредложенийКлиентам.Действует;
	
КонецПроцедуры

// Инициализация вариантов оформления документов продажи.
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма.
//
Процедура ПриИнициализацииВариантовОформленияДокументовПродажи(Форма) Экспорт
	
	ИспользоватьКоммерческиеПредложенияКлиентам = ПолучитьФункциональнуюОпцию("ИспользоватьКоммерческиеПредложенияКлиентам");
	
	Если ИспользоватьКоммерческиеПредложенияКлиентам Тогда
		Форма.Элементы.ВариантОформленияДокументов.СписокВыбора.Вставить(0, Перечисления.ВариантыОформленияДокументовПродажи.КоммерческоеПредложение);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриУстановкеВариантаОформленияДокументовПродажиПоУмолчанию(Форма) Экспорт
	
	ИспользоватьКоммерческиеПредложенияКлиентам = ПолучитьФункциональнуюОпцию("ИспользоватьКоммерческиеПредложенияКлиентам");
	
	Если Форма.Объект.СоздаватьКоммерческоеПредложение
		И ИспользоватьКоммерческиеПредложенияКлиентам Тогда
		
		Форма.Объект.ВариантОформленияДокументов = Перечисления.ВариантыОформленияДокументовПродажи.КоммерческоеПредложение;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриСозданииДокументовПомощникПродаж(Форма, МассивНайденныхДокументов, Отказ) Экспорт
	
	Если НЕ Отказ
		И Форма.Объект.ВариантОформленияДокументов = Перечисления.ВариантыОформленияДокументовПродажи.КоммерческоеПредложение Тогда
			
			СоздатьДокументКоммерческоеПредложениеКлиентуПомощникПродаж(Форма, МассивНайденныхДокументов, Отказ);
			
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриОпределенииТипаДокументаСоздаваемогоДокументаПомощникПродаж(Форма, ПустаяСсылкаДокумента) Экспорт
	
	Если Форма.Объект.ВариантОформленияДокументов = Перечисления.ВариантыОформленияДокументовПродажи.КоммерческоеПредложение Тогда
		ПустаяСсылкаДокумента = Документы.КоммерческоеПредложениеКлиенту.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьДоступностьДополнительныхРеквизитов(Форма) Экспорт
	
	НаборСвойств = УправлениеСвойствамиСлужебный.ПолучитьНаборыСвойствОбъекта(Документы.КоммерческоеПредложениеКлиенту.ПустаяСсылка());
	
	Если НаборСвойств.Количество() > 0 Тогда
		ДополнительныеРеквизиты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НаборСвойств[0].Набор, "ДополнительныеРеквизиты"); // РезультатЗапроса
		Если ДополнительныеРеквизиты <> Неопределено
				И Не ДополнительныеРеквизиты.Пустой() > 0 Тогда
			Форма.ЕстьДопРеквизитыКомПредложения = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииИмениТаблицыЗагрузкиДополнительныхРеквизитов(ВыбранноеЗначение, ИмяТаблицы) Экспорт
	
	Если ВыбранноеЗначение.ТипДокумента = "ДокументОбъект.КоммерческоеПредложениеКлиенту" Тогда
		ИмяТаблицы = "ДополнительныеРеквизитыКоммерческогоПредложения";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ПомощникПродаж

Функция ЗаблокироватьДокумент(ДокументСсылка) Экспорт
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(ДокументСсылка);
	Исключение
		ТекстОшибки = НСтр("ru = 'Не удалось заблокировать %Документ%. %ОписаниеОшибки%';
							|en = 'Failed to lock %Документ%. %ОписаниеОшибки%'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Документ%", ДокументСсылка);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ДокументСсылка);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция НайтиСозданныйДокументПоТипу(Форма, ТипДокумента, МассивНайденныхДокументов) Экспорт
	
	Для Каждого ТекСтрока Из Форма.Объект.Документы Цикл
		Если ТипЗнч(ТекСтрока.Документ) = ТипДокумента
			И МассивНайденныхДокументов.Найти(ТекСтрока.Документ) = Неопределено Тогда
			
			Возврат ТекСтрока;
			
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Параметры:
// 	Форма                     - ФормаКлиентскогоПриложения.
// 	МенеджерДокумента         - ДокументМенеджер.
// 	ТипДокумента              - Тип.
// 	МассивНайденныхДокументов - Массив Из ДокументСсылка.
// 	СозданНовыйДокумент       - Булево.
// 	СтрокаТаблицы             - СтрокаТаблицыЗначений.
// Возвращаемое значение:
// 	ДокументОбъект.
//
Функция НайтиСоздатьДокумент(Форма, МенеджерДокумента, ТипДокумента, МассивНайденныхДокументов, СозданНовыйДокумент, СтрокаТаблицы = Неопределено) Экспорт
	
	Если Форма.ДокументыСформированы Тогда
		СтрокаТаблицы = НайтиСозданныйДокументПоТипу(Форма, ТипДокумента, МассивНайденныхДокументов);
		
		Если СтрокаТаблицы <> Неопределено
			И ЗаблокироватьДокумент(СтрокаТаблицы.Документ) Тогда
			
			ДокументОбъект = СтрокаТаблицы.Документ.ПолучитьОбъект();
			Если НЕ ДокументОбъект = Неопределено И ДокументОбъект.ПометкаУдаления Тогда
				ДокументОбъект.УстановитьПометкуУдаления(Ложь);
			КонецЕсли;
			
			МассивНайденныхДокументов.Добавить(СтрокаТаблицы.Документ);
			
		КонецЕсли;
	КонецЕсли;
	
	Если ДокументОбъект = Неопределено Тогда
		СозданНовыйДокумент = Истина;
		
		ДокументОбъект = МенеджерДокумента.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Возврат ДокументОбъект;
	
КонецФункции

Процедура ДобавитьСтрокуВТаблицуСформированныхДокументов(Форма, ДокументСсылка, Порядок, Состояние = 0, Печать = Ложь) Экспорт
	
	ТаблицаДокументов = Форма.Объект.Документы; // ТабличнаяЧасть 
	
	НоваяСтрока = ТаблицаДокументов.Добавить();
	
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ДокументСсылка);
	
	НоваяСтрока.Документ  = ДокументСсылка;
	НоваяСтрока.Состояние = Состояние;
	НоваяСтрока.Порядок   = Порядок;
	НоваяСтрока.Печать    = Печать;
	
КонецПроцедуры

Процедура ОбновитьСтрокуВТаблицуСформированныхДокументов(СтрокаТаблицы, ДокументСсылка, Порядок, Состояние = 0) Экспорт
	
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДокументСсылка);
	
	СтрокаТаблицы.Состояние = Состояние;
	СтрокаТаблицы.Порядок   = Порядок;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаказКлиента

// Параметры:
// 	ДокументОбъект - ДокументОбъект.ЗаказКлиента.
// 	Основание - ДокументСсылка.КоммерческоеПредложениеКлиенту.
//
Процедура ЗаполнитьЗаказаКлиентаНаОснованииКоммерческогоПредложенияКлиенту(ДокументОбъект, Знач Основание)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КоммерческоеПредложениеКлиенту.Ссылка КАК ДокументОснование,
	|	КоммерческоеПредложениеКлиенту.Клиент КАК Партнер,
	|	КоммерческоеПредложениеКлиенту.Контрагент КАК Контрагент,
	|	КоммерческоеПредложениеКлиенту.КонтактноеЛицо КАК КонтактноеЛицо,
	|	КоммерческоеПредложениеКлиенту.Сделка КАК Сделка,
	|	КоммерческоеПредложениеКлиенту.Валюта КАК Валюта,
	|	КоммерческоеПредложениеКлиенту.СуммаДокумента КАК СуммаДокумента,
	|	КоммерческоеПредложениеКлиенту.Организация КАК Организация,
	|	КоммерческоеПредложениеКлиенту.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	КоммерческоеПредложениеКлиенту.Налогообложение КАК НалогообложениеНДС,
	|	КоммерческоеПредложениеКлиенту.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	КоммерческоеПредложениеКлиенту.Статус КАК СтатусДокумента,
	|	КоммерческоеПредложениеКлиенту.КартаЛояльности КАК КартаЛояльности,
	|	НЕ КоммерческоеПредложениеКлиенту.Проведен       КАК ЕстьОшибкиПроведен,
	|	ВЫБОР
	|		КОГДА КоммерческоеПредложениеКлиенту.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыКоммерческихПредложенийКлиентам.Действует)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьОшибкиСтатус
	|ИЗ
	|	Документ.КоммерческоеПредложениеКлиенту КАК КоммерческоеПредложениеКлиенту
	|ГДЕ
	|	КоммерческоеПредложениеКлиенту.Ссылка = &Основание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КоммерческоеПредложениеКлиентуТовары.НомерСтроки                          КАК НомерСтроки,
	|	КоммерческоеПредложениеКлиентуТовары.КлючСвязи                             КАК КлючСвязи,
	|	КоммерческоеПредложениеКлиентуТовары.Номенклатура                          КАК Номенклатура,
	|	КоммерческоеПредложениеКлиентуТовары.Номенклатура.НаименованиеПолное       КАК НаименованиеНоменклатурыПолное,
	|	КоммерческоеПредложениеКлиентуТовары.Характеристика                        КАК Характеристика,
	|	КоммерческоеПредложениеКлиентуТовары.Характеристика.НаименованиеПолное     КАК НаименованиеХарактеристикиПолное,
	|	КоммерческоеПредложениеКлиентуТовары.Номенклатура.ВариантОформленияПродажи КАК ВариантОформленияПродажи,
	|	КоммерческоеПредложениеКлиентуТовары.ЕдиницаИзмерения                      КАК Упаковка,
	|	КоммерческоеПредложениеКлиентуТовары.Количество                            КАК КоличествоУпаковок,
	|	КоммерческоеПредложениеКлиентуТовары.Количество * ВЫБОР
	|		КОГДА КоммерческоеПредложениеКлиентуТовары.ЕдиницаИзмерения = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА 1
	|		ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки
	|	КОНЕЦ                                                     КАК Количество,
	|	КоммерческоеПредложениеКлиентуТовары.ВидЦены              КАК ВидЦены,
	|	КоммерческоеПредложениеКлиентуТовары.Цена                 КАК Цена,
	|	КоммерческоеПредложениеКлиентуТовары.ПроцентРучнойСкидки  КАК ПроцентРучнойСкидки,
	|	КоммерческоеПредложениеКлиентуТовары.СуммаРучнойСкидки    КАК СуммаРучнойСкидки,
	|	КоммерческоеПредложениеКлиентуТовары.СтавкаНДС            КАК СтавкаНДС,
	|	КоммерческоеПредложениеКлиентуТовары.СуммаНДС             КАК СуммаНДС,
	|	КоммерческоеПредложениеКлиентуТовары.СуммаСНДС            КАК СуммаСНДС,
	|	КоммерческоеПредложениеКлиентуТовары.Сумма                КАК Сумма,
	|	КоммерческоеПредложениеКлиентуТовары.НоменклатураНабора   КАК НоменклатураНабора,
	|	КоммерческоеПредложениеКлиентуТовары.ХарактеристикаНабора КАК ХарактеристикаНабора,
	|	ВЫБОР
	|		КОГДА КоммерческоеПредложениеКлиентуТовары.Ссылка.ВариантУказанияСрокаПоставки = 1
	|			ТОГДА КоммерческоеПредложениеКлиентуТовары.СрокПоставки
	|		ИНАЧЕ РАЗНОСТЬДАТ(ВЫРАЗИТЬ(КоммерческоеПредложениеКлиентуТовары.СрокПоставки КАК ДАТА), &ТекущаяДата, ДЕНЬ)
	|	КОНЕЦ                                                     КАК СрокПоставки
	|ИЗ
	|	Документ.КоммерческоеПредложениеКлиенту.Товары КАК КоммерческоеПредложениеКлиентуТовары
	|ГДЕ
	|	КоммерческоеПредложениеКлиентуТовары.Ссылка = &Основание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КоммерческоеПредложениеКлиентуСкидкиНаценки.КлючСвязи КАК КлючСвязи,
	|	КоммерческоеПредложениеКлиентуСкидкиНаценки.СкидкаНаценка КАК СкидкаНаценка,
	|	КоммерческоеПредложениеКлиентуСкидкиНаценки.Сумма КАК Сумма
	|ИЗ
	|	Документ.КоммерческоеПредложениеКлиенту.СкидкиНаценки КАК КоммерческоеПредложениеКлиентуСкидкиНаценки
	|ГДЕ
	|	КоммерческоеПредложениеКлиентуСкидкиНаценки.Ссылка = &Основание";

	Запрос.УстановитьПараметр("Основание",Основание);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ТекстЗапросаКоэффициентУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"КоммерческоеПредложениеКлиентуТовары.ЕдиницаИзмерения",
			"КоммерческоеПредложениеКлиентуТовары.Номенклатура"));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаШапка = РезультатЗапроса[0].Выбрать();
	ВыборкаШапка.Следующий();
	
	МассивДопустимыхСтатусов = Новый Массив();
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыКоммерческихПредложенийКлиентам.Действует);
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
		ВыборкаШапка.ДокументОснование,
		ВыборкаШапка.СтатусДокумента,
		ВыборкаШапка.ЕстьОшибкиПроведен,
		ВыборкаШапка.ЕстьОшибкиСтатус,
		МассивДопустимыхСтатусов);
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, ВыборкаШапка);
	ДокументОбъект.НалогообложениеНДС = 
		КоммерческиеПредложенияДокументыКлиентСерверУТ.НалогообложениеНДСПоНалогообложениюКоммерческихПредложений(ВыборкаШапка.НалогообложениеНДС);
	ДокументОбъект.ХозяйственнаяОперация = 
		КоммерческиеПредложенияДокументыКлиентСерверУТ.ХозяйственнаяОперацияДокументаКоммерческоеПредложениеКлиенту(ВыборкаШапка.ХозяйственнаяОперация);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами") Тогда
		ЗаполнитьСоглашениеЗаказаКлиентаПоСтатистике(ДокументОбъект);
		Если Не ЗначениеЗаполнено(ДокументОбъект.Соглашение) Тогда
			
			ПараметрыОтбора = Новый Структура;
			МассивОпераций = Новый Массив;
			МассивОпераций.Добавить(ДокументОбъект.ХозяйственнаяОперация);
			ПараметрыОтбора.Вставить("ХозяйственныеОперации",             МассивОпераций);
			ПараметрыОтбора.Вставить("ИспользоватьПервоеУдовлетворяющее", Истина);
			УсловияПродажПоУмолчанию = ПродажиСервер.ПолучитьУсловияПродажПоУмолчанию(ДокументОбъект.Партнер, ПараметрыОтбора);
			
			Если ТипЗнч(УсловияПродажПоУмолчанию) = Тип("Структура")
				И УсловияПродажПоУмолчанию.Свойство("Соглашение") Тогда
				ДокументОбъект.Соглашение = УсловияПродажПоУмолчанию.Соглашение;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДокументОбъект.Соглашение) Тогда
			УсловияПродаж = ПродажиСервер.ПолучитьУсловияПродаж(ДокументОбъект.Соглашение, Истина);
			ДокументОбъект.ГрафикОплаты = УсловияПродаж.ГрафикОплаты;
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаТовары = РезультатЗапроса[1].Выгрузить();
	ТаблицаТовары.Сортировать("НомерСтроки Возр");
	
	Для Каждого ТекСтрока Из ТаблицаТовары Цикл
		
		НоваяСтрока = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
		
		Если ТекСтрока.ВариантОформленияПродажи = Перечисления.ВариантыОформленияПродажи.АктВыполненныхРабот Тогда
			НоваяСтрока.Содержание = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				ТекСтрока.НаименованиеНоменклатурыПолное, 
				ТекСтрока.НаименованиеХарактеристикиПолное);
		КонецЕсли;
		
	КонецЦикла;
	
	ДокументОбъект.Договор = ПродажиСервер.ПолучитьДоговорПоУмолчанию(ДокументОбъект, ДокументОбъект.ХозяйственнаяОперация, ДокументОбъект.Валюта);
	
	ПродажиСервер.ЗаполнитьБанковскиеСчетаПоДоговору(ДокументОбъект.Договор, ДокументОбъект.БанковскийСчет, ДокументОбъект.БанковскийСчетКонтрагента);
	
	Если ЗначениеЗаполнено(ДокументОбъект.Договор) Тогда
		ДокументОбъект.ГруппаФинансовогоУчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.Договор, "ГруппаФинансовогоУчета");
	КонецЕсли;
	
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("ПрименятьКОбъекту",                Истина);
	СтруктураПараметры.Вставить("ТолькоПредварительныйРасчет",      Ложь);
	СтруктураПараметры.Вставить("ВосстанавливатьУправляемыеСкидки", Истина);
	СтруктураПараметры.Вставить("УправляемыеСкидки", Новый СписокЗначений);
	
	ДокументОбъект.СкидкиНаценки.Загрузить(РезультатЗапроса[2].Выгрузить());
	СкидкиНаценкиСервер.Рассчитать(ДокументОбъект, СтруктураПараметры);
	
КонецПроцедуры

Процедура ЗаполнитьСоглашениеЗаказаКлиентаПоСтатистике(Объект)
	
	ОписаниеРеквизитов = Новый Структура;
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьТолькоЗаполненные = "Партнер, Валюта, ЦенаВключаетНДС";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "Соглашение", Параметры);
	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(Объект, Неопределено, ОписаниеРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ПомощникПродаж

// Параметры:
//  Форма - ФормаКлиентскогоПриложения.
//  МассивНайденныхДокументов - Массив Из ДокументОбъект.
//
Функция СоздатьДокументКоммерческоеПредложениеКлиентуПомощникПродаж(Форма, МассивНайденныхДокументов, Отказ)
	
	СозданНовыйДокумент = Ложь;
	СтрокаТаблицы = Неопределено;
	
	ДокументОбъект = Продажи.НайтиСоздатьДокумент(Форма,
		                                          Документы.КоммерческоеПредложениеКлиенту,
		                                          Тип("ДокументСсылка.КоммерческоеПредложениеКлиенту"),
		                                          МассивНайденныхДокументов,
		                                          СозданНовыйДокумент,
		                                          СтрокаТаблицы);
	
	ЗаполнитьЗначенияСвойств(ДокументОбъект, Форма.Объект, , "Дата");
	ДокументОбъект.Клиент                              = Форма.Объект.Партнер;
	ДокументОбъект.ВариантУказанияСрокаПоставки        = Перечисления.ВариантыСроковПоставкиКоммерческихПредложений.НеУказывается;
	ДокументОбъект.ПрочаяДополнительнаяИнформацияТекст = Форма.Объект.ДополнительнаяИнформация;
	ДокументОбъект.Налогообложение                     = КоммерческиеПредложенияДокументыКлиентСерверУТ.НалогообложениеКоммерческихПредложенийПоНалогообложениюНДС(Форма.Объект.НалогообложениеНДС);
	ДокументОбъект.Статус                              = Форма.Объект.СтатусКоммерческогоПредложения;
	ДокументОбъект.УсловияДоставкиТекст                = Форма.Объект.ДополнительнаяИнформацияПоДоставке;
	ДокументОбъект.ХозяйственнаяОперация               = КоммерческиеПредложенияДокументыКлиентСерверУТ.ХозяйственнаяОперацияДокументаКоммерческоеПредложениеКлиенту(Форма.Объект.ХозяйственнаяОперация);
	ДокументОбъект.МожетВыкупатьсяЧастично             = Не Форма.Объект.НеМожетВыкупатьсяЧастично;
	ДокументОбъект.Статус                              = Форма.Объект.СтатусКоммерческогоПредложения;
	
	Если Форма.Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию
		//++ НЕ УТ
		Или Форма.Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаХранениеСПравомПродажи
		//-- НЕ УТ
		Тогда
		ДокументОбъект.ХозяйственнаяОперация = Перечисления.ВидыОперацийКоммерческихПредложений.ПриемНаКомиссию;
	Иначе
		ДокументОбъект.ХозяйственнаяОперация = Перечисления.ВидыОперацийКоммерческихПредложений.ЗакупкаУПоставщика;
	КонецЕсли;
	
	Для Каждого СтрокаТовары Из Форма.Объект.Товары Цикл
		
		НоваяСтрока = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовары);
		
		НоваяСтрока.Количество      = СтрокаТовары.КоличествоУпаковок;
		НоваяСтрока.ЕдиницаИзмерения = СтрокаТовары.Упаковка;
		
	КонецЦикла;
	ДокументОбъект.Товары.Загрузить(Форма.Объект.Товары.Выгрузить());
	
	ДокументОбъект.СкидкиНаценки.Загрузить(Форма.Объект.СкидкиНаценки.Выгрузить());
	ДокументОбъект.ДополнительныеРеквизиты.Загрузить(Форма.Объект.ДополнительныеРеквизитыКоммерческогоПредложения.Выгрузить());
	
	Попытка
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Если СозданНовыйДокумент Тогда
			Продажи.ДобавитьСтрокуВТаблицуСформированныхДокументов(
				Форма,
				ДокументОбъект.Ссылка,
				1,
				,
				Ложь);
			МассивНайденныхДокументов.Добавить(ДокументОбъект.Ссылка);
		ИначеЕсли СтрокаТаблицы <> Неопределено Тогда
			Продажи.ОбновитьСтрокуВТаблицуСформированныхДокументов(
				СтрокаТаблицы,
				ДокументОбъект.Ссылка,
				1);
		КонецЕсли;
	Исключение
		Сообщение = Неопределено;
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Сообщение,"Объект",ОписаниеОшибки(), "");
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Сообщение, Отказ);
		Отказ = Истина;
	КонецПопытки;
	
	Возврат ДокументОбъект;
	
КонецФункции

#КонецОбласти

#КонецОбласти
