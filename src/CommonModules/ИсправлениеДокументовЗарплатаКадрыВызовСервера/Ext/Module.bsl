
#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьВыплатаПроизводиласьОтражениеВУчетеПроизводилось(Организация, Документ, ПериодРегистрации, ВыплатаПроизводилась, ОтражениеВУчетеПроизводилось) Экспорт
	Если ВыплатаПроизводилась = НеОпределено Тогда
		ВыплатаПроизводилась = ВзаиморасчетыССотрудникамиРасширенный.ВыплатаПроизводилась(Организация, Документ, ПериодРегистрации);
	КонецЕсли;
	Если ОтражениеВУчетеПроизводилось = НеОпределено Тогда
		ОтражениеВУчетеПроизводилось = ИсправлениеДокументовЗарплатаКадры.ОтражениеВУчетеПроизводилось(Организация, ПериодРегистрации);
	КонецЕсли;
КонецПроцедуры

Функция ПрочитатьПараметрыИсправленногоДокумента(ИсправленныйДокумент) Экспорт
	
	МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ИсправленныйДокумент);
	Результат = МенеджерДокумента.ПараметрыИсправляемогоДокумента(ИсправленныйДокумент);
	
	Возврат Результат;
	
КонецФункции

Функция НайтиСторнированныеОснования(ДокументСсылка, ЭтоПроведение) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 10
		|	НачисленияУдержанияПоСотрудникам.ДокументОснование КАК ДокументОснование,
		|	СторнированиеНачислений.Ссылка КАК СторнирующийДокумент
		|ИЗ
		|	РегистрНакопления.НачисленияУдержанияПоСотрудникам КАК НачисленияУдержанияПоСотрудникам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СторнированиеНачислений КАК СторнированиеНачислений
		|		ПО (СторнированиеНачислений.СторнируемыйДокумент = НачисленияУдержанияПоСотрудникам.ДокументОснование)
		|			И (СторнированиеНачислений.Проведен)
		|ГДЕ
		|	НачисленияУдержанияПоСотрудникам.Регистратор = &ДокументНачисления");
		
	Запрос.УстановитьПараметр("ДокументНачисления", ДокументСсылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат "";
	КонецЕсли;
	
	ЧастиСообщения = Новый Массив;
	
	Если ЭтоПроведение Тогда
		ЧастиСообщения.Добавить(НСтр("ru = 'Повторное проведение запрещено.';
									|en = 'Repeated posting is prohibited.'"));
	Иначе
		ЧастиСообщения.Добавить(НСтр("ru = 'Отмена проведения запрещена.';
									|en = 'Cannot cancel posting.'"));
	КонецЕсли;
	
	ЧастиСообщения.Добавить(Символы.ПС + НСтр("ru = 'Сторнированы некоторые документы-основания начислений:';
												|en = 'Some base documents of accruals are reversed:'"));
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЧастиСообщения.Добавить(Символы.ПС + " - ");
		ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(Строка(Выборка.ДокументОснование),,,, ПолучитьНавигационнуюСсылку(Выборка.ДокументОснование)));
	КонецЦикла;
	
	Если Выборка.Количество() = 10 Тогда
		ЧастиСообщения.Добавить(Символы.ПС + "...");
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(ЧастиСообщения);
	
КонецФункции

#КонецОбласти
