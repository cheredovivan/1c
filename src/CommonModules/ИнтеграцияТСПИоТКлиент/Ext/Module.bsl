
#Область ПрограммныйИнтерфейс

// Получение настроек экземпляра ТС ПИоТ по адресу подключения
// 
// Параметры:
//  ПараметрыНастройки      - см. ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияТСПИоТ
//  УникальныйИдентификатор - УникальныйИдентификатор, Неопределено - Уникальный идентификатор формы
// 
// Возвращаемое значение:
//  Структура:
//   * ЕстьОшибки - Булево - Истина, если есть ошибки
//   * НастройкиЭкземпляраТСПИоТ - см. ИнтеграцияТСПИоТКлиентСервер.КонструкторНастройкиЭкземляраТСПИоТ
//   * ТекстОшибки - Строка - полный текст ошибки
Функция ПолучитьДанныеНастройкиТСПИоТ(ПараметрыНастройки, УникальныйИдентификатор = Неопределено) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ЕстьОшибки",                Ложь);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",               "");
	ВозвращаемоеЗначение.Вставить("НастройкиЭкземпляраТСПИоТ", ИнтеграцияТСПИоТКлиентСервер.КонструкторНастройкиЭкземляраТСПИоТ());
	
	РезультатОбновления = ИнтеграцияТСПИоТКлиентСервер.ПолучитьНастройкиЭкземпляраТСПИоТ(ПараметрыНастройки);
	
	ВозвращаемоеЗначение.ЕстьОшибки                 = ЗначениеЗаполнено(РезультатОбновления.ТекстОшибки);
	ВозвращаемоеЗначение.ТекстОшибки                = РезультатОбновления.ТекстОшибки;
	ВозвращаемоеЗначение.НастройкиЭкземпляраТСПИоТ  = РезультатОбновления.НастройкиЭкземпляраТСПИоТ;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Функция возвращает результат асинхронного получения идентификаторов разрешительного режима на Локальном модуле Честного знака
//
// Параметры:
//  ПараметрыЗавершенияОбработкиШтрихкода - Структура - (См. ШтрихкодированиеОбщегоНазначенияИСКлиент.ПараметрыЗавершенияОбработкиШтрихкода)
//  ОписаниеПослеЗавершения               - ОписаниеОповещения - процедура, выполняемая после обработки штрихкода на ТС ПИоТ
//  ОперацияПробития                      - Булево - Истина, если обработка штрихкода на ТС ПИоТ осуществляется при пробитии чека в рамках обновления идентификаторов РР
// Возвращаемое значение:
//  Булево - Истина, если обработка штрихкода на ТС ПИоТ проведена успешно.
Асинх Функция ОбработатьАсинхронноШтрихкод(ПараметрыЗавершенияОбработкиШтрихкода, ОписаниеПослеЗавершения, ОперацияПробития = Ложь) Экспорт
	
	ПроверкаЗавершена     = Ложь;
	ПараметрыСканирования = ПараметрыЗавершенияОбработкиШтрихкода.ПараметрыСканирования;
	РезультатОбработки    = ПараметрыЗавершенияОбработкиШтрихкода.РезультатОбработкиШтрихкода;
	
	Если ШтрихкодированиеОбщегоНазначенияИСКлиент.ПрисутствуетПродукцияИСМП(ПараметрыСканирования.ДопустимыеВидыПродукции, Истина)
		И ШтрихкодированиеОбщегоНазначенияИСКлиент.ПрисутствуетПродукцияИСМП(РезультатОбработки.ВидыПродукции, Истина)
		И Не РезультатОбработки.ТребуетсяУточнениеДанных Тогда
		
		Результат = Ждать ОбработатьАсинхронноШтрихкодНаТСПИоТ(ПараметрыЗавершенияОбработкиШтрихкода);
		
		ДанныеШтрихкода = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Результат.ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода.ДанныеШтрихкода);
		Форма = Результат.ПараметрыЗавершенияВводаШтрихкода.Форма;
		
		РезультатОбработкиШтрихкода = ИнтеграцияТСПИоТВызовСервера.ОбработатьШтрихкодыПослеУточненияНаТСПИоТ(ДанныеШтрихкода, ПараметрыСканирования, Форма.УникальныйИдентификатор, Результат.АварийныйРежим);
		РезультатОбработкиШтрихкода.ТребуетсяПроверкаРазрешительногоРежимаНаТСПИоТ = Ложь;
		
		Если РезультатОбработкиШтрихкода <> Неопределено Тогда
			
			Если ПараметрыСканирования.ИспользуетсяСоответствиеШтрихкодовСтрокДерева
				И РезультатОбработкиШтрихкода.ТребуетсяВыборНоменклатуры Тогда
				
				Если Форма.СоответствиеШтрихкодовСтрокДерева.Получить(ДанныеШтрихкода.Штрихкод) <> Неопределено Тогда
					РезультатОбработкиШтрихкода.ТребуетсяВыборНоменклатуры = Ложь;
				КонецЕсли;
				
			КонецЕсли;
			
			ШтрихкодированиеОбщегоНазначенияИСМПКлиент.ДополнительныеПроверкиПередЗавершениемОбработкиШтрихкода(Форма, ПараметрыСканирования, РезультатОбработкиШтрихкода);
			
			ПроверкаЗавершена = Истина;
			ПараметрыЗавершенияОбработкиШтрихкода.РезультатОбработкиШтрихкода = РезультатОбработкиШтрихкода;
			
			Если ОперацияПробития Тогда
				Возврат ПроверкаЗавершена;
			КонецЕсли;
			
			Если РезультатОбработкиШтрихкода.ЕстьОшибкиВДеревеУпаковок
				Или Не ПустаяСтрока(РезультатОбработкиШтрихкода.ТекстОшибки)
				Или РезультатОбработкиШтрихкода.ТребуетсяУточнениеДанных
				Или РезультатОбработкиШтрихкода.ТребуетсяУточнениеСоставаУпаковки
				Или (РезультатОбработкиШтрихкода.ТребуетсяПроверкаСредствамиККТ
					И Не РезультатОбработкиШтрихкода.ПроверкаСредствамиККТЗавершена
				Или ШтрихкодированиеОбщегоНазначенияИСКлиент.ТребуетсяУточнениеДанныхУПользователя(Форма, РезультатОбработкиШтрихкода, ПараметрыСканирования)) Тогда
				
				Если Не ПараметрыСканирования.РасширеннаяВерсияГосИС Тогда
					ШтрихкодированиеОбщегоНазначенияИСКлиент.ДополнитьДанныеШтрихкодаМиниВерсия(РезультатОбработкиШтрихкода, ДанныеШтрихкода);
				КонецЕсли;
				
				ШтрихкодированиеОбщегоНазначенияИСКлиент.ЗавершитьОбработкуШтрихкода(ПараметрыЗавершенияОбработкиШтрихкода);
				
			Иначе
				
				ПараметрыЗавершения = Новый Структура("ДанныеВыбора, ЗапомнитьВыбор", РезультатОбработки, Ложь);
				ВыполнитьОбработкуОповещения(ОписаниеПослеЗавершения, ПараметрыЗавершения);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ДанныеШтрихкода = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(РезультатОбработки.ДанныеШтрихкода);
		РезультатОбработки.ТребуетсяПроверкаРазрешительногоРежимаНаТСПИоТ = Ложь;
		
		ВыполнитьОбработкуОповещения(ОписаниеПослеЗавершения, Новый Структура("ДанныеВыбора, ЗапомнитьВыбор", ДанныеШтрихкода, Ложь));
		
	КонецЕсли;
	
	Возврат ПроверкаЗавершена;
	
КонецФункции

// Обработка штрихкода на ТС ПИоТ.
// 
// Параметры:
//  ДанныеКМПроверки             - Массив из см. ШтрихкодированиеОбщегоНазначенияИС.ИнициализацияДанныхПоШтрихкодам
//  ПараметрыСканирования        - см. ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования
//  ПараметрыПодключенияТСПИоТ   - см. ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияТСПИоТ
//  УникальныйИдентификаторФормы - УникальныйИдентификатор - уникальный идентификатор формы вызывающего объекта
// 
// Возвращаемое значение:
//  Обещание - Обработать асинхронно штрихкоды на ТС ПИоТ
Асинх Функция ОбработатьАсинхронноШтрихкодыНаТСПИоТ(ДанныеКМПроверки, ПараметрыСканирования, ПараметрыПодключенияТСПИоТ, УникальныйИдентификаторФормы) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("АварийныйРежим",                    Ложь);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                       "");
	ВозвращаемоеЗначение.Вставить("РезультатыОбработкиШтрихкода",      Новый Соответствие());
	
	СтатусыКодовМаркировки = Новый Соответствие();
	
	Для Каждого КодМаркировкиНаПроверку Из ДанныеКМПроверки Цикл
		
		МассивКодовНаПроверку = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КодМаркировкиНаПроверку);
		
		#Если ВебКлиент Тогда
		
			СистемнаяИнфо = Новый СистемнаяИнформация;
			Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнфо.ВерсияПриложения, "8.3.21.0") < 0 Тогда
				ВозвращаемоеЗначение.ТекстОшибки = НСтр("ru = 'Обмен с ТС ПИоТ из веб-клиента недоступен для текущей версии платформы.
					|Используйте тонкий клиент.';
					|en = 'Обмен с ТС ПИоТ из веб-клиента недоступен для текущей версии платформы.
					|Используйте тонкий клиент.'");
				Возврат ВозвращаемоеЗначение;
			КонецЕсли;
			
			РезультатЗапросаТСПИоТ = Ждать ИнтеграцияТСПИоТВебКлиент.ДанныеПоКодуМаркировкиПриРозничнойПродаже(
				ПараметрыПодключенияТСПИоТ.НастройкиПодключенияТСПИоТ.НастройкиПодключения,
				МассивКодовНаПроверку,
				ПараметрыСканирования,
				ПараметрыПодключенияТСПИоТ.ДополнительныеПараметры);
		
		#Иначе
		
			РезультатЗапросаТСПИоТ = ИнтеграцияТСПИоТКлиентСервер.ДанныеПоКодуМаркировкиПриРозничнойПродаже(
				ПараметрыПодключенияТСПИоТ.НастройкиПодключенияТСПИоТ.НастройкиПодключения,
				МассивКодовНаПроверку,
				ПараметрыСканирования,
				ПараметрыПодключенияТСПИоТ.ДополнительныеПараметры);
		
		#КонецЕсли
		
		Если ЗначениеЗаполнено(РезультатЗапросаТСПИоТ.ТекстОшибки) Тогда
			
			КодМаркировкиНаПроверку.ТекстОшибки = РезультатЗапросаТСПИоТ.ТекстОшибки;
			Возврат ВозвращаемоеЗначение;
			
		КонецЕсли;
		
		Если РезультатЗапросаТСПИоТ.АварийныйРежим Тогда
			
			Для Каждого СтрокаОбработки Из ДанныеКМПроверки Цикл
				СтрокаОбработки.РазрешительныйРежимКодОтвета = ОбщегоНазначенияИСМПКлиентСерверПовтИсп.АварийныйРежимКодОтветаСервисаГИСМТ();
			КонецЦикла;
			
			ВозвращаемоеЗначение.АварийныйРежим          = РезультатЗапросаТСПИоТ.АварийныйРежим;
			
			Возврат ВозвращаемоеЗначение;
			
		КонецЕсли;
		
		Для Каждого СтатусКодаМаркировкиТСПИоТ Из РезультатЗапросаТСПИоТ.СтатусыКодовМаркировкиГИСМТ Цикл
			СтатусыКодовМаркировки.Вставить(СтатусКодаМаркировкиТСПИоТ.Ключ, СтатусКодаМаркировкиТСПИоТ.Значение);
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого СтрокаОбработки Из ДанныеКМПроверки Цикл
	
		ПараметрыКодаМаркировки = СтатусыКодовМаркировки.Получить(СтрокаОбработки);
		
		Если ПараметрыКодаМаркировки <> Неопределено Тогда
			
			СвойстваДляЗаполнения = ПараметрыКодаМаркировки;
			
			Для Каждого КлючИЗначение Из СвойстваДляЗаполнения Цикл
			
				Если Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
					Продолжить;
				КонецЕсли;
				
				Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаОбработки, КлючИЗначение.Ключ) Тогда
					Продолжить;
				КонецЕсли;
				
				СтрокаОбработки[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
				
			КонецЦикла;
		
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ДанныеШтрихкода Из ДанныеКМПроверки Цикл
		
		РезультатОбработкиШтрихкода = ИнтеграцияТСПИоТВызовСервера.ОбработатьШтрихкодыПослеУточненияНаТСПИоТ(ДанныеШтрихкода, ПараметрыСканирования, УникальныйИдентификаторФормы, РезультатЗапросаТСПИоТ.АварийныйРежим);
		ВозвращаемоеЗначение.РезультатыОбработкиШтрихкода.Вставить(ДанныеШтрихкода, РезультатОбработкиШтрихкода);
		
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Асинх Функция ОбработатьАсинхронноШтрихкодНаТСПИоТ(ПараметрыЗавершенияВводаШтрихкода)
	
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("ОбработкаНаТСПИоТ",                 Ложь);
	ВозвращаемоеЗначение.Вставить("ПараметрыЗавершенияВводаШтрихкода", ПараметрыЗавершенияВводаШтрихкода);
	ВозвращаемоеЗначение.Вставить("АварийныйРежим",                    Ложь);
	ВозвращаемоеЗначение.Вставить("Отказ",                             Ложь);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                       "");
	
	РезультатОбработкиШтрихкода = ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода;
	
	ТребуетсяПроверкаРазрешительногоРежимаНаТСПИоТ = РезультатОбработкиШтрихкода.Свойство("ТребуетсяПроверкаРазрешительногоРежимаНаТСПИоТ")
		И РезультатОбработкиШтрихкода.ТребуетсяПроверкаРазрешительногоРежимаНаТСПИоТ;
	
	ПроверкаНаТСПИоТЗавершена = РезультатОбработкиШтрихкода.Свойство("ПроверкаНаТСПИоТЗавершена")
		И РезультатОбработкиШтрихкода.ПроверкаНаТСПИоТЗавершена;
	
	Если Не ТребуетсяПроверкаРазрешительногоРежимаНаТСПИоТ Или ПроверкаНаТСПИоТЗавершена Тогда
		Возврат ВозвращаемоеЗначение;
	Иначе
		
		РезультатОбработки = Ждать ЗавершитьОбработкуКодаМаркировкиАсинхронноПослеВыполненияЗапросаНаТСПИоТ(ПараметрыЗавершенияВводаШтрихкода);
		
		ПараметрыЗавершенияВводаШтрихкода = РезультатОбработки.ПараметрыЗавершенияВводаШтрихкода;
		РезультатОбработкиШтрихкода       = ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода;
		
		ВозвращаемоеЗначение.ОбработкаНаТСПИоТ                 = Истина;
		ВозвращаемоеЗначение.ПараметрыЗавершенияВводаШтрихкода = РезультатОбработки.ПараметрыЗавершенияВводаШтрихкода;
		ВозвращаемоеЗначение.ТекстОшибки                       = РезультатОбработки.ТекстОшибки;
		ВозвращаемоеЗначение.АварийныйРежим                    = РезультатОбработки.АварийныйРежим;
		
		Возврат ВозвращаемоеЗначение;
		
	КонецЕсли;
	
КонецФункции

Асинх Функция ЗавершитьОбработкуКодаМаркировкиАсинхронноПослеВыполненияЗапросаНаТСПИоТ(ПараметрыЗавершенияВводаШтрихкода)
	
	СтрокаОбработки = ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода.ДанныеШтрихкода;
	
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("ОтсутствуютУстановленныеЛМЧЗ",      Ложь);
	ВозвращаемоеЗначение.Вставить("АварийныйРежим",                    Ложь);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                       "");
	ВозвращаемоеЗначение.Вставить("ПараметрыЗавершенияВводаШтрихкода", ПараметрыЗавершенияВводаШтрихкода);
	
	Если Не СтрокаОбработки.ТребуетсяПроверкаРазрешительногоРежимаНаТСПИоТ Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ПараметрыСканирования  = ПараметрыЗавершенияВводаШтрихкода.ПараметрыСканирования;
	ПараметрыЗапросаТСПИоТ = ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода.ДанныеНастройкиТСПИоТ;
	
	ДанныеКМПроверки = Новый Массив;
	ДанныеКМПроверки.Добавить(СтрокаОбработки);
	
	#Если ВебКлиент Тогда
	
		СистемнаяИнфо = Новый СистемнаяИнформация;
		Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(СистемнаяИнфо.ВерсияПриложения, "8.3.21.0") < 0 Тогда
			ВозвращаемоеЗначение.ТекстОшибки = НСтр("ru = 'Обмен с ТС ПИоТ из веб-клиента недоступен для текущей версии платформы.
				|Используйте тонкий клиент.';
				|en = 'Обмен с ТС ПИоТ из веб-клиента недоступен для текущей версии платформы.
				|Используйте тонкий клиент.'");
			Возврат ВозвращаемоеЗначение;
		КонецЕсли;
		
		РезультатЗапросаТСПИоТ = Ждать ИнтеграцияТСПИоТВебКлиент.ДанныеПоКодуМаркировкиПриРозничнойПродаже(
			ПараметрыЗапросаТСПИоТ.НастройкиПодключенияТСПИоТ.НастройкиПодключения,
			ДанныеКМПроверки,
			ПараметрыСканирования,
			ПараметрыЗапросаТСПИоТ.ДополнительныеПараметры);
	
	#Иначе
	
		РезультатЗапросаТСПИоТ = ИнтеграцияТСПИоТКлиентСервер.ДанныеПоКодуМаркировкиПриРозничнойПродаже(
			ПараметрыЗапросаТСПИоТ.НастройкиПодключенияТСПИоТ.НастройкиПодключения,
			ДанныеКМПроверки,
			ПараметрыСканирования,
			ПараметрыЗапросаТСПИоТ.ДополнительныеПараметры);
	
	#КонецЕсли
	
	Если ЗначениеЗаполнено(РезультатЗапросаТСПИоТ.ТекстОшибки) Тогда
	
		Если Не ПараметрыЗавершенияВводаШтрихкода = Неопределено Тогда
			
			ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода.ЕстьОшибки                = Истина;
			ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода.ТекстОшибки               = РезультатЗапросаТСПИоТ.ТекстОшибки;
			ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода.ПроверкаНаТСПИоТЗавершена = Истина;
			
		КонецЕсли;
	
		СтрокаОбработки.ТекстОшибки = РезультатЗапросаТСПИоТ.ТекстОшибки;

		Возврат ВозвращаемоеЗначение;
	
	КонецЕсли;
	
	Если РезультатЗапросаТСПИоТ.АварийныйРежим Тогда
		
		СтрокаОбработки.РазрешительныйРежимКодОтвета = ОбщегоНазначенияИСМПКлиентСерверПовтИсп.АварийныйРежимКодОтветаСервисаГИСМТ();
		ВозвращаемоеЗначение.АварийныйРежим          = РезультатЗапросаТСПИоТ.АварийныйРежим;
		
		Возврат ВозвращаемоеЗначение;
		
	КонецЕсли;
	
	ПараметрыКодаМаркировки = РезультатЗапросаТСПИоТ.СтатусыКодовМаркировкиГИСМТ.Получить(СтрокаОбработки);
	
	Если ПараметрыКодаМаркировки <> Неопределено Тогда
		
		СвойстваДляЗаполнения = ПараметрыКодаМаркировки;
		
		Для Каждого КлючИЗначение Из СвойстваДляЗаполнения Цикл
		
			Если Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
				Продолжить;
			КонецЕсли;
			
			Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаОбработки, КлючИЗначение.Ключ) Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаОбработки[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
			
		КонецЦикла;
	
	КонецЕсли;
	
	Если Не ПараметрыЗавершенияВводаШтрихкода = Неопределено Тогда
		
		ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода.ПараметрыОшибки = СтрокаОбработки.ПараметрыОшибки;
		ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода.ТекстОшибки     = СтрокаОбработки.ТекстОшибки;
		ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода.ЕстьОшибки      = ЗначениеЗаполнено(СтрокаОбработки.ТекстОшибки);
		
		ПараметрыЗавершенияВводаШтрихкода.РезультатОбработкиШтрихкода.ПроверкаНаТСПИоТЗавершена = Истина;
		
	КонецЕсли;
	
	ВозвращаемоеЗначение.ПараметрыЗавершенияВводаШтрихкода = ПараметрыЗавершенияВводаШтрихкода;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти
