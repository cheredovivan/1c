//@strict-types

#Область ПрограммныйИнтерфейс

// Заголовок документа.
// 
// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика, ДокументСсылка.НачислениеРетроБонусовКлиента, ДокументСсылка.НачислениеРетроБонусовПоставщика, ДокументСсылка.СписаниеРетроБонусовКлиента, ДокументСсылка.СписаниеРетроБонусовПоставщика - Обрабатываемый документ
//  НомерДокумента - Строка - Номер документа
//  ДатаДокумента - Дата - Дата документа
//  Исправление - Булево, Неопределено - Признак исправительного документа
// 
// Возвращаемое значение:
//  Строка - представление документа
//
Функция ЗаголовокДокумента(Знач Документ, Знач НомерДокумента, Знач ДатаДокумента, Знач Исправление = Неопределено) Экспорт
	
	Возврат РетроБонусыСервер.ЗаголовокДокумента(Документ, НомерДокумента, ДатаДокумента, Исправление);
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика -
// 
// Возвращаемое значение:
//	- Неопределено -
//	- см. ДлительныеОперации.ВыполнитьПроцедуру
//
Функция ДлительнаяОперацияФиксацииСоставаДокументаУсловий(Знач Документ) Экспорт
	
	ШаблонКлюча = "ФиксацияСостава_%1";
	ИдентификаторДокумента = Строка(Документ.УникальныйИдентификатор());
	Ключ = СтрШаблон(ШаблонКлюча, ИдентификаторДокумента);
	
	Отбор = Новый Структура;
	Отбор.Вставить("Ключ", Ключ);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ФоновыеЗаданияПоДокументу = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	ВыполняетсяФиксацияПоДокументу = (ФоновыеЗаданияПоДокументу.Количество() > 0);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ДлительнаяОперация = Неопределено;
	
	Если НЕ ВыполняетсяФиксацияПоДокументу Тогда
		
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
		ПараметрыВыполнения.КлючФоновогоЗадания = Ключ;
		ПараметрыВыполнения.УточнениеОшибки = НСтр("ru = 'Не удалось зафиксировать состав:';
													|en = 'Cannot record the content:'");
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.УсловияРетроБонусовКлиентов") Тогда
			
			ИмяПроцедуры = "РетроБонусыСервер.ЗафиксироватьСоставУРБКлиентов";
			
		Иначе
			
			ИмяПроцедуры = "РетроБонусыСервер.ЗафиксироватьСоставУРБПоставщиков";
			
		КонецЕсли;
		
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяПроцедуры, Документ);
		
	КонецЕсли;
	
	Возврат ДлительнаяОперация;
	
КонецФункции

// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика -
// 
// Возвращаемое значение:
//	- Неопределено -
//	- см. ДлительныеОперации.ВыполнитьПроцедуру 
//
Функция ДлительнаяОперацияОтменыФиксацииСоставаДокументаУсловий(Знач Документ) Экспорт
	
	ШаблонКлюча = "ФиксацияСостава_%1";
	ИдентификаторДокумента = Строка(Документ.УникальныйИдентификатор());
	Ключ = СтрШаблон(ШаблонКлюча, ИдентификаторДокумента);
	
	Отбор = Новый Структура;
	Отбор.Вставить("Ключ", Ключ);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ФоновыеЗаданияПоДокументу = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	ВыполняетсяОтменаФиксацииПоДокументу = (ФоновыеЗаданияПоДокументу.Количество() > 0);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ДлительнаяОперация = Неопределено;
	
	Если НЕ ВыполняетсяОтменаФиксацииПоДокументу Тогда
		
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
		ПараметрыВыполнения.КлючФоновогоЗадания = Ключ;
		ПараметрыВыполнения.УточнениеОшибки = НСтр("ru = 'Не удалось отменить фиксацию состава:';
													|en = 'Cannot cancel document recording:'");
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.УсловияРетроБонусовКлиентов") Тогда
			
			ИмяПроцедуры = "РетроБонусыСервер.ОтменитьФиксациюСоставаУРБКлиентов";
			
		Иначе
			
			ИмяПроцедуры = "РетроБонусыСервер.ОтменитьФиксациюСоставаУРБПоставщиков";
			
		КонецЕсли;
		
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяПроцедуры, Документ);
		
	КонецЕсли;
	
	Возврат ДлительнаяОперация;
	
КонецФункции

// Параметры:
//  ПараметрыРасчета - см. Отчеты.РасчетРетроБонусовКлиентов.ПараметрыРасчетаРетроБонусов
//  ИдентификаторФормы - см. ДлительныеОперации.ПараметрыВыполненияФункции.ИдентификаторФормы
// 
// Возвращаемое значение:
//	см. ДлительныеОперации.ВыполнитьФункцию 
//
Функция ДлительнаяОперацияПолученияРетроБонусовПоДокументамПродажи(ПараметрыРасчета, ИдентификаторФормы) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.УточнениеОшибки = НСтр("ru = 'Не удалось получить данные ретро-бонусов по документам продажи:';
												|en = 'Cannot get rebate data by sales documents:'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"Отчеты.РасчетРетроБонусовКлиентов.РетроБонусыЗаПериодПоДокументамПродажи",
		ПараметрыРасчета);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

// Параметры:
//  ПараметрыРасчета - см. Отчеты.РасчетРетроБонусовПоставщиков.ПараметрыРасчетаРетроБонусов
//  ИдентификаторФормы - см. ДлительныеОперации.ПараметрыВыполненияФункции.ИдентификаторФормы
// 
// Возвращаемое значение:
//	см. ДлительныеОперации.ВыполнитьФункцию 
//
Функция ДлительнаяОперацияПолученияРетроБонусовПоДокументамПриобретения(ПараметрыРасчета, ИдентификаторФормы) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.УточнениеОшибки = НСтр("ru = 'Не удалось получить данные ретро-бонусов по документам поступления:';
												|en = 'Cannot get rebate data by receipt documents:'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"Отчеты.РасчетРетроБонусовПоставщиков.РасчетРетроБонусовЗаПериод",
		ПараметрыРасчета);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

// Параметры:
//  АдресДанныхДляФормирования - Строка
//  ПараметрыРасчета - см. Отчеты.РасчетРетроБонусовКлиентов.ПараметрыРасчетаРетроБонусов
//  ПараметрыФормирования - см. Обработки.ГрупповоеНачислениеРетроБонусовКлиентов.ПараметрыФормированияДокументов
//  ИдентификаторФормы - см. ДлительныеОперации.ПараметрыВыполненияФункции.ИдентификаторФормы
// 
// Возвращаемое значение:
//  см. ДлительныеОперации.ВыполнитьФункцию
//
Функция ДлительнаяОперацияФормированияДокументовНачисления(АдресДанныхДляФормирования, ПараметрыРасчета, ПараметрыФормирования, ИдентификаторФормы) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.УточнениеОшибки = НСтр("ru = 'Не удалось сформировать документы начисления:';
												|en = 'Cannot generate the accrual documents:'");
	ДанныеДляФормирования = ПолучитьИзВременногоХранилища(АдресДанныхДляФормирования); // см. НовыеДанныеДляФормированияДокументовНачисления
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"Обработки.ГрупповоеНачислениеРетроБонусовКлиентов.СформироватьДокументыНачисления",
		ДанныеДляФормирования,
		ПараметрыРасчета,
		ПараметрыФормирования);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

// Параметры:
//  ОбластиСРасшифровками - Структура:
//   * МассивИдентификаторовРасшифровок - Массив Из ИдентификаторРасшифровкиКомпоновкиДанных
//   * РасшифровкаОбласть - Соответствие Из КлючИЗначение:
//     ** Ключ - ИдентификаторРасшифровкиКомпоновкиДанных
//     ** Значение - Строка - Имя области
//  АдресДанныхРасшифровки - Строка
//  ПоляРасшифровки - Массив Из Строка
//  ЗакрытьРасчеты - Булево
// 
// Возвращаемое значение:
//  Структура:
//   * РезультатАнализаОбластей - Структура:
//	   ** МассивДокументовУсловий - Массив Из ДокументСсылка.УсловияРетроБонусовПоставщика
//	   ** МассивИменОбластей - Массив Из Строка
//	   ** ДокументыУсловийСАдресами - Соответствие Из КлючИЗначение:
//		 *** Ключ - Строка 
//		 *** Значение - ДокументСсылка.УсловияРетроБонусовПоставщика
//   * РезультатИзмененияСтатуса - Структура:
//	   ** ЕстьОшибки - Булево 
//	   ** ИнформацияОбОшибке - Строка 
//
Функция ЗакрытьВозобновитьРасчетыПоБонусамИзФормыОтчета(ОбластиСРасшифровками, АдресДанныхРасшифровки, ПоляРасшифровки, ЗакрытьРасчеты) Экспорт
	
	РезультатИзмененияСтатуса = Новый Структура;
	РезультатИзмененияСтатуса.Вставить("ЕстьОшибки", Ложь);
	РезультатИзмененияСтатуса.Вставить("ИнформацияОбОшибке", "");
	
	РезультатАнализаОбластей = Отчеты.ВедомостьПоРетроБонусамПоставщиков.ДокументыУсловийВыделенныхОбластей(
		ОбластиСРасшифровками,
		АдресДанныхРасшифровки,
		ПоляРасшифровки);
		
	ДокументыУсловий = РезультатАнализаОбластей.МассивДокументовУсловий;
	
	Если ДокументыУсловий.Количество() > 0 Тогда
		
		РезультатИзмененияСтатуса = РегистрыСведений.ЗавершенныеУсловияРетроБонусовПоставщиков.ИзменитьСтатусРасчетов(
			ДокументыУсловий,
			ЗакрытьРасчеты);
			
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("РезультатАнализаОбластей", РезультатАнализаОбластей);
	Результат.Вставить("РезультатИзмененияСтатуса", РезультатИзмененияСтатуса);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти