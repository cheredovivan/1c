#Область ПрограммныйИнтерфейс

// Описывает состав прикладных данных таблицы отчета.
// Под таблицей понимается совокупность строк с одинаковым составом колонок.
// Обычно каждая таблица в отчете имеет отдельное название, номер и шапку с описанием колонок.
//
// Параметры:
//  Описание - см. ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйОписаниеТаблицыОтчета - заполняемая коллекция
//  ИмяТаблицыОтчета - Строка - имя таблицы из ЗаполнениеБухгалтерскойОтчетностиРазмещение.СоставТаблицОтчета
//
Процедура ОписатьТаблицуОтчета(Описание, ИмяТаблицыОтчета) Экспорт
	
	Если ИмяТаблицыОтчета = "ОценочныеОбязательства" Тогда
		ОписатьТаблицуОтчетаОценочныеОбязательства(Описание);
	ИначеЕсли ИмяТаблицыОтчета = "ОбеспеченияОбязательств" Тогда
		ОписатьТаблицуОтчетаОбеспеченияОбязательств(Описание);
	КонецЕсли;
	
КонецПроцедуры

// Определяет состав заполняемых пояснений конкретного отчета (за конкретный период, по конкретной организации)
//
// Параметры:
//  ТаблицыОтчета - Структура - Ключ - имя таблицы из ЗаполнениеБухгалтерскойОтчетностиРазмещение.СоставТаблицОтчета; Значение - Булево.
//                  Для заполняемых пояснений следует установить значение Истина, состав коллекции изменять не следует.
//  КонтекстОтчета - см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстОтчета
//
Процедура ОпределитьСоставЗаполняемыхПояснений(ТаблицыОтчета, КонтекстОтчета) Экспорт
	
	ТаблицыОтчета.ОценочныеОбязательства  = Истина;
	ТаблицыОтчета.ОбеспеченияОбязательств = Истина;
	
КонецПроцедуры

// Заполняет таблицы отчета за один период.
//
// Параметры:
//  ПолучениеДанныхТаблицОтчета - см. ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйПолучениеДанныхТаблицОтчета -
//     процессор, который содержит исходные параметры для получения данных,
//     и в который нужно разместить полученные данные.
//  Классификаторы - Структура - Ключ: имя таблицы отчета; Значение: Структура - Ключ и Значение: имя колонки, содержащей значения по классификатору
//  КонтекстЗаполненияОтчета - см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета
//
Процедура ЗаполнитьТаблицыОтчета(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета) Экспорт
	
	ЗаполнитьПояснениеОценочныеОбязательства(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета);
	ЗаполнитьПояснениеОбеспеченияОбязательств(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОписатьТаблицуОтчетаОценочныеОбязательства(Описание)
	
	Описание.Представление = НСтр("ru = 'Оценочные обязательства';
									|en = 'Оценочные обязательства'");
	
	Описание.Структура.Добавить("Обязательство");
	
	Описание.ЧисловыеЗначения.Добавить("Начало");
	Описание.ЧисловыеЗначения.Добавить("Признано");
	Описание.ЧисловыеЗначения.Добавить("Выполнено");
	Описание.ЧисловыеЗначения.Добавить("Прекращено");
	Описание.ЧисловыеЗначения.Добавить("Конец");
	
КонецПроцедуры

Процедура ОписатьТаблицуОтчетаОбеспеченияОбязательств(Описание)
	
	Описание.Представление = НСтр("ru = 'Обеспечения обязательств';
									|en = 'Guarantees for obligations'");
	
	Описание.Структура.Добавить("Роль");
	Описание.Структура.Добавить("Обеспечение");
	
	Описание.Классификаторы.Вставить("Роль", Новый СписокЗначений);
	Описание.Классификаторы.Роль.Добавить("Полученные", НСтр("ru = 'Полученные обеспечения обязательств';
															|en = 'Received guarantees for obligations'"));
	Описание.Классификаторы.Роль.Добавить("Выданные",   НСтр("ru = 'Выданные обеспечения обязательств';
															|en = 'Given guarantees for obligations'"));
	
	Описание.ВариантПериода = "НачалоКонец";
	Описание.ЧисловыеЗначения.Добавить("СуммаОбеспечения"); // НачалоСуммаОбеспечения, КонецСуммаОбеспечения
	
КонецПроцедуры

Процедура ЗаполнитьПояснениеОценочныеОбязательства(ЗаполняемыеДанные, Классификаторы, КонтекстЗаполненияОтчета)
	
	Если ЗаполнениеБухгалтерскойОтчетностиТабличное.ТаблицаОтчета(ЗаполняемыеДанные, "ОценочныеОбязательства") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПравилаУчета = Новый Структура;
	ПравилаУчета.Вставить("СчетаУчета", Новый Структура);
	ПравилаУчета.СчетаУчета.Вставить("ОценочныеОбязательства",  ПланыСчетов.Хозрасчетный.РезервыПредстоящихРасходов);
	ПравилаУчета.СчетаУчета.Вставить("ПрекращениеОбязательств", ПланыСчетов.Хозрасчетный.ПрочиеДоходы);
	ПравилаУчета.Вставить("СубконтоОбязательство", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Резервы);
	
	ИспользуемыеСчетаУчета = ИспользуемыеСчетаУчета(ПравилаУчета.СчетаУчета);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",  КонтекстЗаполненияОтчета.Период.Начало);
	Запрос.УстановитьПараметр("КонецПериода",   КонтекстЗаполненияОтчета.Период.Конец);
	Запрос.УстановитьПараметр("Организации",    КонтекстЗаполненияОтчета.Организации);
	Запрос.УстановитьПараметр("СчетОценочныеОбязательства",  ИспользуемыеСчетаУчета.ОценочныеОбязательства);
	Запрос.УстановитьПараметр("СчетПрекращениеОбязательств", ИспользуемыеСчетаУчета.ПрекращениеОбязательств);
	Запрос.УстановитьПараметр("СубконтоОбязательство",       ПравилаУчета.СубконтоОбязательство);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Обязательства.Счет КАК Счет,
	|	ВЫРАЗИТЬ(Обязательства.Субконто1 КАК Справочник.Резервы) КАК Обязательство,
	|	Обязательства.Организация КАК Организация,
	|	-Обязательства.СуммаНачальныйОстаток КАК Начало,
	|	Обязательства.СуммаОборотКт КАК Кт,
	|	Обязательства.СуммаОборотДт КАК Дт,
	|	0 КАК Прекращение,
	|	-Обязательства.СуммаКонечныйОстаток КАК Конец
	|ПОМЕСТИТЬ ОценочныеОбязательства
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, , , Счет В (&СчетОценочныеОбязательства), &СубконтоОбязательство, Организация В (&Организации)) КАК Обязательства
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Прекращение.СчетДт,
	|	ВЫРАЗИТЬ(Прекращение.СубконтоДт1 КАК Справочник.Резервы),
	|	Прекращение.Организация,
	|	NULL,
	|	NULL,
	|	NULL,
	|	Прекращение.СуммаОборот,
	|	NULL
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, , СчетДт В (&СчетОценочныеОбязательства), &СубконтоОбязательство, СчетКт В (&СчетПрекращениеОбязательств), , Организация В (&Организации)) КАК Прекращение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обязательства.Счет КАК Счет,
	|	ПРЕДСТАВЛЕНИЕ(Обязательства.Счет) КАК СчетПредставление,
	|	Обязательства.Обязательство КАК Обязательство,
	|	ПРЕДСТАВЛЕНИЕ(Обязательства.Обязательство) КАК ОбязательствоПредставление,
	|	Обязательства.Организация КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(Обязательства.Организация) КАК ОрганизацияПредставление,
	|	Обязательства.Начало КАК Начало,
	|	Обязательства.Кт КАК Признано,
	|	Обязательства.Дт КАК Выполнено,
	|	Обязательства.Прекращение КАК Прекращено,
	|	Обязательства.Конец КАК Конец,
	|	Обязательства.Начало + Обязательства.Кт КАК СуммаПорядок,
	|	Обязательства.Счет.Порядок КАК СчетПорядок
	|ИЗ
	|	ОценочныеОбязательства КАК Обязательства
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммаПорядок УБЫВ,
	|	СчетПорядок
	|ИТОГИ
	|	СУММА(Начало),
	|	СУММА(Признано),
	|	СУММА(Выполнено),
	|	СУММА(Прекращено),
	|	СУММА(Конец),
	|	СУММА(СуммаПорядок)
	|ПО
	|	Обязательство,
	|	Организация,
	|	Счет";
	
	ПараметрыТекстов = Новый Структура;
	ЗаполнениеБухгалтерскойОтчетностиТабличное.УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстов, КонтекстЗаполненияОтчета);
	ПараметрыТекстов.Вставить("СчетОценочныеОбязательства",  ПравилаУчета.СчетаУчета.ОценочныеОбязательства);
	ПараметрыТекстов.Вставить("СчетПрекращениеОбязательств", ПравилаУчета.СчетаУчета.ПрекращениеОбязательств);
	
	ШаблоныКолонок = Новый Соответствие; // Ключ - имя колонки; Значение - строка
	ШаблоныЗаголовковРасшифровки = Новый Соответствие; // Ключ - имя колонки; Значение - ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки
	
	ДобавитьОписаниеКолонки(ШаблоныКолонок, ШаблоныЗаголовковРасшифровки,
		"Начало",
		НСтр("ru = 'обязательство на [НачалоПериода]';
			|en = 'liability as of [НачалоПериода]'"),
		НСтр("ru = 'Сальдо по счету [СчетОценочныеОбязательства] - по наименованиям обязательств';
			|en = '[СчетОценочныеОбязательства] balance - by liability names'"));
	ДобавитьОписаниеКолонки(ШаблоныКолонок, ШаблоныЗаголовковРасшифровки,
		"Признано",
		НСтр("ru = 'признано обязательство за [Период]';
			|en = 'recognized liability for [Период]'"),
		НСтр("ru = 'Оборот по кредиту счета [СчетОценочныеОбязательства] - по наименованиям обязательств';
			|en = '[СчетОценочныеОбязательства] account credit turnover - by liability names'"));
	ДобавитьОписаниеКолонки(ШаблоныКолонок, ШаблоныЗаголовковРасшифровки,
		"Выполнено",
		НСтр("ru = 'выполнено обязательство за [Период]';
			|en = 'fulfilled liability for [Период]'"),
		НСтр("ru = 'Оборот по дебету счета [СчетОценочныеОбязательства], кроме корреспонденций со счетом [СчетПрекращениеОбязательств] - по наименованиям обязательств';
			|en = '[СчетОценочныеОбязательства] account debit turnover, except for correspondence with account [СчетПрекращениеОбязательств] - by liability names'"));
	ДобавитьОписаниеКолонки(ШаблоныКолонок, ШаблоныЗаголовковРасшифровки,
		"Прекращено",
		НСтр("ru = 'прекращено обязательство за [Период]';
			|en = 'terminated liability for [Период]'"),
		НСтр("ru = 'Оборот Дт [СчетОценочныеОбязательства] Кт [СчетПрекращениеОбязательств] - по наименованиям обязательств';
			|en = 'Dr [СчетОценочныеОбязательства] Cr [СчетПрекращениеОбязательств] turnover - by liability names'"));
	ДобавитьОписаниеКолонки(ШаблоныКолонок, ШаблоныЗаголовковРасшифровки,
		"Конец",
		НСтр("ru = 'обязательство на [КонецПериода]';
			|en = 'liability as of [КонецПериода]'"),
		НСтр("ru = 'Сальдо по счету [СчетОценочныеОбязательства] - по наименованиям обязательств';
			|en = '[СчетОценочныеОбязательства] balance - by liability names'"));
	
	ОценочныеОбязательства = ЗаполнениеБухгалтерскойОтчетностиТабличное.НачатьВыводТаблицы(
		ЗаполняемыеДанные,
		"ОценочныеОбязательства",
		ШаблоныКолонок,
		ПараметрыТекстов);
		
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаОбязательство = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОбязательство.Следующий() Цикл
		
		ПараметрыТекстов.Вставить("Обязательство", ВыборкаОбязательство.Обязательство);
		
		НачатьДобавлениеСтрокиОценочныеОбязательства(
			ОценочныеОбязательства,
			ВыборкаОбязательство.Обязательство,
			ШаблоныЗаголовковРасшифровки,
			ПараметрыТекстов);
		
		ВыборкаОрганизация = ВыборкаОбязательство.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаОрганизация.Следующий() Цикл
			
			ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьФилиал(
				ОценочныеОбязательства,
				ШаблоныЗаголовковРасшифровки,
				ВыборкаОрганизация.Организация,
				ВыборкаОрганизация.ОрганизацияПредставление);
			
			ВыборкаСчет = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаСчет.Следующий() Цикл
				ДобавитьСлагаемыеПоСчету(ОценочныеОбязательства, ВыборкаСчет, "Начало,Признано,Выполнено,Прекращено,Конец");
				Если Не ЗначениеЗаполнено(ВыборкаСчет.Прекращено) Тогда
					Продолжить;
				КонецЕсли;
				Отчет = Неопределено;
				ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(
					ОценочныеОбязательства,
					"Выполнено",
					СтрШаблон(НСтр("ru = 'Исключен оборот в корреспонденции со счетом %1';
									|en = 'Turnover in correspondence with %1 is excluded'"), ПравилаУчета.СчетаУчета.ПрекращениеОбязательств),
					-ВыборкаСчет.Прекращено,
					ПравилаУчета.СчетаУчета.ПрекращениеОбязательств,
					Отчет);
			КонецЦикла;
			
		КонецЦикла;
		
		ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(ОценочныеОбязательства, ШаблоныКолонок);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ИспользуемыеСчетаУчета(СчетаУчета)
	ИспользуемыеСчетаУчета = Новый Структура;
	Для Каждого ОписаниеСчета Из СчетаУчета Цикл
		ИспользуемыеСчетаУчета.Вставить(ОписаниеСчета.Ключ, БухгалтерскийУчетПовтИсп.СчетаВИерархии(ОписаниеСчета.Значение));
	КонецЦикла;
	Возврат ИспользуемыеСчетаУчета;
КонецФункции

Процедура ДобавитьОписаниеКолонки(ШаблоныКолонок, ШаблоныЗаголовковРасшифровки, ИмяКолонки, ШаблонЗаголовкаКолонки, ШаблонПорядкаРасчета)
	
	ШаблоныКолонок.Вставить(ИмяКолонки, ШаблонЗаголовкаКолонки);
	
	ШаблонКолонки = ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки();
	
	ШаблонКолонки.ЗаголовокГруппы = НСтр("ru = '[Обязательство]: [ПредставлениеКолонки]';
										|en = '[Обязательство]: [ПредставлениеКолонки]'");
	ШаблонКолонки.ПорядокРасчета  = ШаблонПорядкаРасчета;
	
	ШаблоныЗаголовковРасшифровки.Вставить(ИмяКолонки, ШаблонКолонки);
	
КонецПроцедуры

Процедура НачатьДобавлениеСтрокиОценочныеОбязательства(ОценочныеОбязательства, Обязательство, ШаблоныКолонок, ПараметрыТекстовРасшифровки)
	
	НоваяСтрока = ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСтрокуТаблицыОтчета(
		ОценочныеОбязательства,
		ШаблоныКолонок,
		ПараметрыТекстовРасшифровки);
		
	НоваяСтрока.Обязательство = Обязательство;
	
КонецПроцедуры

Процедура ДобавитьСлагаемыеПоСчету(ВыводТаблицы, Выборка, Слагаемые)
	Отчет = Неопределено;
	Для Каждого Слагаемое Из СтрРазделить(Слагаемые, ",") Цикл
		ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(
			ВыводТаблицы,
			Слагаемое,
			Выборка.СчетПредставление,
			Выборка[Слагаемое],
			Выборка.Счет,
			Отчет);
	КонецЦикла;
КонецПроцедуры

Процедура ЗаполнитьПояснениеОбеспеченияОбязательств(ЗаполняемыеДанные, Классификаторы, КонтекстЗаполненияОтчета)
	
	Если ЗаполнениеБухгалтерскойОтчетностиТабличное.ТаблицаОтчета(ЗаполняемыеДанные, "ОбеспеченияОбязательств") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Порядок учета
	
	ПравилаУчета = Новый Структура;
	ПравилаУчета.Вставить("СчетаУчета", Новый Структура);
	ПравилаУчета.СчетаУчета.Вставить("Полученные", ПланыСчетов.Хозрасчетный.ОбеспеченияОбязательствПолученные);
	ПравилаУчета.СчетаУчета.Вставить("Выданные",   ПланыСчетов.Хозрасчетный.ОбеспеченияОбязательствВыданные);
	ПравилаУчета.Вставить("СубконтоКонтрагенты", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ПравилаУчета.Вставить("СубконтоДоговоры",    ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	
	ДетальныйУчет = Ложь;
	
	ПараметрыТекстов = Новый Структура;
	ЗаполнениеБухгалтерскойОтчетностиТабличное.УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстов, КонтекстЗаполненияОтчета);
	ПараметрыТекстов.Вставить("СчетПолученные", ПравилаУчета.СчетаУчета.Полученные);
	ПараметрыТекстов.Вставить("СчетВыданные",   ПравилаУчета.СчетаУчета.Выданные);
	
	ШаблоныКолонок = Новый Соответствие; // Ключ - имя колонки; Значение - строка
	ШаблоныКолонок.Вставить("НачалоСуммаОбеспечения", НСтр("ru = 'на [КонецПредшествующегоПериода]';
															|en = 'as of [КонецПредшествующегоПериода]'"));
	ШаблоныКолонок.Вставить("КонецСуммаОбеспечения", НСтр("ru = 'на [КонецПериода]';
															|en = 'as of [КонецПериода]'"));
	
	ПорядокРасчета = Новый Структура;
	Если Ложь Тогда
	
		ШаблонПорядокРасчета = НСтр("ru = 'Сальдо по счету %1 - по видам %2 обеспечений и контрагентам, чьи обязательства обеспечиваются.
	                                          |Эти сведения указаны в %3 (аналитике %4 на счете)';
	                                          |en = '%1 balance - by on the types of %2 guarantees and counterparties whose obligations are guaranteed.
	                                          |This information is given in %3 (the %4 account dimension)'");
		
		ПорядокРасчета.Вставить("Полученные", СтрШаблон(ШаблонПорядокРасчета,
			ПравилаУчета.СчетаУчета.Полученные,
			НСтр("ru = 'полученных';
				|en = 'received'"),
			НСтр("ru = 'обеспеченных договорах';
				|en = 'secured contracts'"),
			Строка(ПравилаУчета.СубконтоДоговоры)));
		ПорядокРасчета.Вставить("Выданные", СтрШаблон(ШаблонПорядокРасчета,
			ПравилаУчета.СчетаУчета.Выданные,
			НСтр("ru = 'выданных';
				|en = 'issued'"),
			НСтр("ru = 'договорах предоставления обеспечения';
				|en = 'security contracts'"),
			Строка(ПравилаУчета.СубконтоДоговоры)));
			
	Иначе
		
		ШаблонПорядокРасчета = НСтр("ru = 'Сальдо по счету %1 - по контрагентам';
									|en = '%1 balance by counterparties'");
		
		ПорядокРасчета.Вставить("Полученные", СтрШаблон(ШаблонПорядокРасчета, ПравилаУчета.СчетаУчета.Полученные));
		ПорядокРасчета.Вставить("Выданные",   СтрШаблон(ШаблонПорядокРасчета, ПравилаУчета.СчетаУчета.Выданные));
		
	КонецЕсли;
	
	ШаблоныЗаголовковРасшифровки = Новый Структура;
	ДобавитьШаблонЗаголовковРасшифровкиОбеспеченияОбязательств(ШаблоныЗаголовковРасшифровки, "Полученные", ПорядокРасчета);
	ДобавитьШаблонЗаголовковРасшифровкиОбеспеченияОбязательств(ШаблоныЗаголовковРасшифровки, "Выданные", ПорядокРасчета);
	
	// Получение данных
	
	ИспользуемыеСчетаУчета = ИспользуемыеСчетаУчета(ПравилаУчета.СчетаУчета);
	
	ВсеСчета = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеСчета, ИспользуемыеСчетаУчета.Полученные);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВсеСчета, ИспользуемыеСчетаУчета.Выданные);
	
	ВсеСубконто = Новый Массив;
	ВсеСубконто.Добавить(ПравилаУчета.СубконтоДоговоры);
	ВсеСубконто.Добавить(ПравилаУчета.СубконтоКонтрагенты);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", КонтекстЗаполненияОтчета.Период.Начало);
	Запрос.УстановитьПараметр("КонецПериода",  Новый Граница(КонтекстЗаполненияОтчета.Период.Конец, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организации",   КонтекстЗаполненияОтчета.Организации);
	Запрос.УстановитьПараметр("ВсеСчета",      ВсеСчета);
	Запрос.УстановитьПараметр("СчетаВыданные", ИспользуемыеСчетаУчета.Выданные);
	Запрос.УстановитьПараметр("ВсеСубконто",   ВсеСубконто);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеУчета.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ДанныеУчета.Счет В (&СчетаВыданные)
	|			ТОГДА ""Выданные""
	|		ИНАЧЕ ""Полученные""
	|	КОНЕЦ КАК Роль,
	|	ДанныеУчета.Счет КАК Счет,
	|	ВЫРАЗИТЬ(ДанныеУчета.Договор КАК Справочник.ДоговорыКонтрагентов) КАК Договор,
	|	ВЫРАЗИТЬ(ДанныеУчета.Контрагент КАК Справочник.Контрагенты) КАК Контрагент,
	|	ДанныеУчета.Валюта КАК Валюта,
	|	СУММА(ДанныеУчета.Начало) КАК Начало,
	|	СУММА(ДанныеУчета.Конец) КАК Конец
	|ПОМЕСТИТЬ Обеспечения
	|ИЗ
	|	(ВЫБРАТЬ
	|		Начало.Организация КАК Организация,
	|		Начало.Счет КАК Счет,
	|		Начало.Субконто1 КАК Договор,
	|		Начало.Субконто2 КАК Контрагент,
	|		Начало.Валюта КАК Валюта,
	|		Начало.СуммаОстаток КАК Начало,
	|		0 КАК Конец
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет В (&ВсеСчета), &ВсеСубконто, Организация В (&Организации)) КАК Начало
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Конец.Организация,
	|		Конец.Счет,
	|		Конец.Субконто1,
	|		Конец.Субконто2,
	|		Конец.Валюта,
	|		0,
	|		Конец.СуммаОстаток
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецПериода, Счет В (&ВсеСчета), &ВсеСубконто, Организация В (&Организации)) КАК Конец) КАК ДанныеУчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеУчета.Организация,
	|	ДанныеУчета.Счет,
	|	ДанныеУчета.Валюта,
	|	ВЫРАЗИТЬ(ДанныеУчета.Договор КАК Справочник.ДоговорыКонтрагентов),
	|	ВЫРАЗИТЬ(ДанныеУчета.Контрагент КАК Справочник.Контрагенты)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обеспечения.Организация КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(Обеспечения.Организация) КАК ОрганизацияПредставление,
	|	Обеспечения.Роль КАК Роль,
	|	Обеспечения.Счет КАК Счет,
	|	Обеспечения.Счет.Порядок КАК СчетПорядок,
	|	ПРЕДСТАВЛЕНИЕ(Обеспечения.Счет) КАК СчетПредставление,
	|	Обеспечения.Договор КАК Договор,
	|	ПРЕДСТАВЛЕНИЕ(Обеспечения.Договор) КАК ДоговорПредставление,
	|	НЕОПРЕДЕЛЕНО КАК ВидОбеспечения,
	|	НЕОПРЕДЕЛЕНО КАК ОбеспечениеВыданоЗа,
	|	"""" КАК ОбеспечениеВыданоЗаПредставление,
	|	Обеспечения.Контрагент КАК Контрагент,
	|	ПРЕДСТАВЛЕНИЕ(Обеспечения.Контрагент) КАК КонтрагентПредставление,
	|	Обеспечения.Валюта КАК Валюта,
	|	ПРЕДСТАВЛЕНИЕ(Обеспечения.Валюта) КАК ВалютаПредставление,
	|	Обеспечения.Начало КАК Начало,
	|	Обеспечения.Конец КАК Конец
	|ИЗ
	|	Обеспечения КАК Обеспечения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО Обеспечения.Договор = ДоговорыКонтрагентов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Роль,
	|	Конец УБЫВ,
	|	Начало УБЫВ,
	|	СчетПорядок,
	|	Счет,
	|	Договор
	|ИТОГИ
	|	СУММА(Начало),
	|	СУММА(Конец)
	|ПО
	|	Роль,
	|	ВидОбеспечения,
	|	ОбеспечениеВыданоЗа,
	|	Контрагент,
	|	Организация";
	
	ПорядокРолейВЗапросе = СтрРазделить("Выданные,Полученные", ",");
	
	// Заполнение таблицы
	
	ОбеспеченияОбязательств = ЗаполнениеБухгалтерскойОтчетностиТабличное.НачатьВыводТаблицы(
		ЗаполняемыеДанные,
		"ОбеспеченияОбязательств",
		ШаблоныКолонок,
		ПараметрыТекстов);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаРоль = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Для Каждого Роль Из ПорядокРолейВЗапросе Цикл
		
		Если Не ВыборкаРоль.НайтиСледующий(Роль, "Роль") Тогда
			Продолжить;
		КонецЕсли;
		
		Если Роль = "Выданные" Тогда
			ПараметрыТекстов.Вставить("Роль", НСтр("ru = 'Выданное обеспечение обязательства';
													|en = 'Given guarantee for obligation'"));
		Иначе
			ПараметрыТекстов.Вставить("Роль", НСтр("ru = 'Полученное обеспечение обязательства';
													|en = 'Received guarantee for obligation'"));
		КонецЕсли;
		
		ВыборкаВидОбеспечения = ВыборкаРоль.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаВидОбеспечения.Следующий() Цикл
		
			ВыборкаОбеспечениеВыданоЗа = ВыборкаВидОбеспечения.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаОбеспечениеВыданоЗа.Следующий() Цикл
				
				ДанныеПоДоговору = ДетальныйУчет
					И ЗначениеЗаполнено(ВыборкаОбеспечениеВыданоЗа.ВидОбеспечения)
					И ЗначениеЗаполнено(ВыборкаОбеспечениеВыданоЗа.ОбеспечениеВыданоЗаПредставление);
				
				Если ДанныеПоДоговору Тогда
					
					ОписаниеОбеспечения = СтрШаблон(
						НСтр("ru = '%1 за %2';
							|en = '%1 for %2'"),
						ВыборкаОбеспечениеВыданоЗа.ВидОбеспечения,
						ВыборкаОбеспечениеВыданоЗа.ОбеспечениеВыданоЗаПредставление);
						
					ПараметрыТекстов.Вставить("Обеспечение", ОписаниеОбеспечения);
					
					НачатьДобавлениеСтрокиОбеспеченияОбязательств(
						ОбеспеченияОбязательств,
						Роль,
						ОписаниеОбеспечения,
						ШаблоныЗаголовковРасшифровки[Роль],
						ПараметрыТекстов);
						
				КонецЕсли;
				
				ВыборкаКонтрагент = ВыборкаОбеспечениеВыданоЗа.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаКонтрагент.Следующий() Цикл
					Если Не ДанныеПоДоговору Тогда
						
						ПараметрыТекстов.Вставить("Обеспечение", ВыборкаКонтрагент.КонтрагентПредставление);
						
						НачатьДобавлениеСтрокиОбеспеченияОбязательств(
							ОбеспеченияОбязательств,
							Роль,
							ВыборкаКонтрагент.КонтрагентПредставление,
							ШаблоныЗаголовковРасшифровки[Роль],
							ПараметрыТекстов);
					КонецЕсли;
					
					ВыборкаОрганизация = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаОрганизация.Следующий() Цикл
						
						ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьФилиал(
							ОбеспеченияОбязательств,
							ШаблоныЗаголовковРасшифровки[Роль],
							ВыборкаОрганизация.Организация,
							ВыборкаОрганизация.ОрганизацияПредставление);
			
						ВыборкаСлагаемое = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаСлагаемое.Следующий() Цикл
							
							ОписаниеДоговора = ВыборкаСлагаемое.ДоговорПредставление;
							Если Не ЗначениеЗаполнено(ВыборкаСлагаемое.Договор) Тогда
								ОписаниеДоговора = НСтр("ru = 'не указан';
														|en = 'not specified'");
							ИначеЕсли ЗначениеЗаполнено(ВыборкаСлагаемое.Валюта) Тогда
								ОписаниеДоговора = СтрШаблон(НСтр("ru = '%1 (%2)';
																	|en = '%1 (%2)'"), ОписаниеДоговора, ВыборкаСлагаемое.ВалютаПредставление);
							КонецЕсли;
							ОписаниеСлагаемого = СтрШаблон(НСтр("ru = 'Счет %1, договор %2';
																|en = 'Account %1, contract %2'"), ВыборкаСлагаемое.Счет, ОписаниеДоговора);
							
							Отчет = Неопределено;
							ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(
								ОбеспеченияОбязательств,
								"НачалоСуммаОбеспечения",
								ОписаниеСлагаемого,
								ВыборкаСлагаемое.Начало,
								ВыборкаСлагаемое.Договор,
								Отчет);
								
							ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(
								ОбеспеченияОбязательств,
								"КонецСуммаОбеспечения",
								ОписаниеСлагаемого,
								ВыборкаСлагаемое.Конец,
								ВыборкаСлагаемое.Договор,
								Отчет);
								
						КонецЦикла; // Слагаемое
					КонецЦикла; // Организация
					
					Если Не ДанныеПоДоговору Тогда
						ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(ОбеспеченияОбязательств, ШаблоныКолонок);
					КонецЕсли;
					
				КонецЦикла; // Контрагент
				
				Если ДанныеПоДоговору Тогда
					ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(ОбеспеченияОбязательств, ШаблоныКолонок);
				КонецЕсли;
				
			КонецЦикла; // ОбеспечениеВыданоЗа
		КонецЦикла; // ВидОбеспечения
	КонецЦикла; // Роль
	
КонецПроцедуры

Процедура НачатьДобавлениеСтрокиОбеспеченияОбязательств(ОбеспеченияОбязательств, Роль, ОписаниеОбеспечения, ШаблоныКолонок, ПараметрыТекстовРасшифровки)
	
	НоваяСтрока = ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСтрокуТаблицыОтчета(
		ОбеспеченияОбязательств,
		ШаблоныКолонок,
		ПараметрыТекстовРасшифровки);
		
	НоваяСтрока.Роль        = Роль;
	НоваяСтрока.Обеспечение = ОписаниеОбеспечения;
	
КонецПроцедуры

Процедура ДобавитьШаблонЗаголовковРасшифровкиОбеспеченияОбязательств(ШаблоныЗаголовковРасшифровки, Роль, ПорядокРасчета)
	
	ШаблоныЗаголовковРасшифровкиКолонки = Новый Соответствие; // Ключ - имя колонки; Значение - ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки
	ШаблонКолонки = ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки();
	
	ШаблонКолонки.ЗаголовокГруппы = НСтр("ru = '[Роль]: [Обеспечение], [ПредставлениеКолонки]';
										|en = '[Роль]: [Обеспечение], [ПредставлениеКолонки]'");
	ШаблонКолонки.ПорядокРасчета  = ПорядокРасчета[Роль];
	
	ШаблоныЗаголовковРасшифровкиКолонки.Вставить("НачалоСуммаОбеспечения", ШаблонКолонки);
	ШаблоныЗаголовковРасшифровкиКолонки.Вставить("КонецСуммаОбеспечения", ШаблонКолонки);
	
	ШаблоныЗаголовковРасшифровки.Вставить(Роль, ШаблоныЗаголовковРасшифровкиКолонки);
	
КонецПроцедуры

#КонецОбласти
