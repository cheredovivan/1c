
#Область ПрограммныйИнтерфейс

#Область ПреобразованиеТиповАналитики

// Возвращает таблицу видов аналитик с колонкой типов
// 
// Возвращаемое значение:
//	ТаблицаЗначений - все доступные виды аналитик. Содержит колонки:
//		*Ссылка - ПланВидовХарактеристикСсылка.АналитикиСтатейБюджетов - вид аналитики.
//		*ТипЗначения - ОписаниеТипов - тип вида аналитики.
// 
Функция ТипыВидовВидыАналитик() Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	АналитикиСтатейБюджетов.Ссылка,
	|	АналитикиСтатейБюджетов.ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.АналитикиСтатейБюджетов КАК АналитикиСтатейБюджетов");
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает таблицу видов аналитик с колонкой типов
// 
// Возвращаемое значение:
//	Массив из ПланВидовХарактеристикСсылка.АналитикиСтатейБюджетов - все доступные виды аналитик.
// 
Функция ВидыАналитик() Экспорт
	
	Возврат ТипыВидовВидыАналитик().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

// Возвращает таблицу видов аналитик с колонкой типов
// 
// Параметры:
// 	ВидАналитики - ПланВидовХарактеристикСсылка.АналитикиСтатейБюджетов, Строка - Ссылка на ПВХ АналитикиСтатейБюджетов или имя (идентификатор) вида аналитики.
//
// Возвращаемое значение:
//	Массив из Тип - Массив значений типа Тип, состоящий из используемых типов.
// 
Функция ТипыВидаАналитики(ВидАналитики) Экспорт
	ТипыВидаАналитики = Новый Массив;
	
	ИскомыйВидАналитики = Неопределено;
	ТипПараметра = ТипЗнч(ВидАналитики);
	// Уточним пустой вид и тип аналитики
	Если ТипПараметра = Тип("ПланВидовХарактеристикСсылка.АналитикиСтатейБюджетов")
		И ВидАналитики.Пустая() Тогда
		ВидАналитики = Неопределено;
		ТипПараметра = Тип("Неопределено");
	КонецЕсли;
	
	Если ТипПараметра = Тип("Строка") Тогда
		ИД_ВидаАналитики = МониторингЦелевыхПоказателей.СтрокуВУникальныйИдентификатор(ВидАналитики);
		
		ИскомыйВидАналитики = ПланыВидовХарактеристик.АналитикиСтатейБюджетов.ПолучитьСсылку(ИД_ВидаАналитики);
		
		Если НЕ ЗначениеЗаполнено(ИскомыйВидАналитики) Тогда
			ИскомыйВидАналитики = Неопределено;
		КонецЕсли;
	Иначе
		ИскомыйВидАналитики = ВидАналитики;
	КонецЕсли;
	
	// Неопределено - вид аналитики не используется 
	Если Не ТипПараметра = Тип("Неопределено") Тогда
		УстановитьПривилегированныйРежим(Истина);
		ТипЗначения = ИскомыйВидАналитики.ТипЗначения;
		УстановитьПривилегированныйРежим(Ложь);
		ТипыВидаАналитики = ТипЗначения.Типы();
	КонецЕсли;
		
	Возврат ТипыВидаАналитики;
КонецФункции

// Возвращает массив пустых ссылок плана видов характеристик АналитикиСтатейБюджетов и Неопределено.
//
// Возвращаемое значение:
// 	Массив из ЛюбаяСсылка - массив пустых ссылок и неопределено.
//
Функция ПустыеЗначенияАналитики() Экспорт
	Массив = Новый Массив;
	
	Типы = БюджетированиеСервер.ВсеТипыАналитик().Типы();
	
	Для каждого Тип Из Типы Цикл
		ПустаяСсылка = Новый (Тип);
		Массив.Добавить(ПустаяСсылка);
	КонецЦикла;
	
	Массив.Добавить(Неопределено);
	
	Возврат Массив;
КонецФункции

#КонецОбласти

#Область МассивноИспользуемыеКонстанты

// Возвращает значение одноименной опции и используется в массивных, многократно повторяющихся алгоритмах. 
//
// Возвращаемое значение:
//   Булево - значение функциональной опции.
// 
Функция ИспользоватьНесколькоВалют() Экспорт
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют");
КонецФункции

#КонецОбласти 

// Возвращает номер вида аналитики статьи или показателя бюджетов
//
// Параметры:
//  СтатьяПоказатель - СправочникСсылка.СтатьиБюджетов,
//						СправочникСсылка.ПоказателиБюджетов
//  ВидАналитики - ПланВидовХарактеристикСсылка.АналитикиСтатейБюджетов - вид аналитики статьи или показателя бюджетов
//
// Возвращаемое значение:
//  Число
//
Функция НомерВидаАналитикиСтатьиПоказателяБюджетов(СтатьяПоказатель, ВидАналитики) Экспорт
	
	ВидыАналитик = БюджетированиеСервер.ВидыАналитик(СтатьяПоказатель);
	
	НомерАналитики = 0;
	
	МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
	Для Счетчик = 1 По МаксимальноеКоличествоАналитик Цикл
		Если ВидыАналитик[СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ВидАналитики%1", Счетчик)]
			= ВидАналитики Тогда
			НомерАналитики = Счетчик;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НомерАналитики;
	
КонецФункции

// Возвращает соответствие аналитик статьи или показателя бюджетов с аналитиками нефинансового показателя
//
// Параметры:
//  СтатьяПоказатель - СправочникСсылка.СтатьиБюджетов
//						СправочникСсылка.ПоказателиБюджетов
//  НФП - СправочникСсылка.НефинансовыеПоказателиБюджетов
//
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//   *Ключ - Число - номер аналитики статьи (показателя) бюджетов
//   *Значение - Строка - номер аналитики нефинансового показателя
//  Неопределено, если аналитики статьи или показателя бюджетов не соответствуют аналитикам НФП.
//
Функция СоответствиеАналитикСтатьиПоказателяБюджетовСНФП(СтатьяПоказатель, НФП) Экспорт
	
	ВидыАналитикСтатьиПоказателя = БюджетированиеСервер.ВидыАналитик(СтатьяПоказатель);
	ВидыАналитикНФП = БюджетированиеСервер.ВидыАналитик(НФП);
	
	СоответствиеАналитик = Новый Соответствие;
	
	Для Каждого ВидАналитикиНФП Из ВидыАналитикНФП Цикл
		Если Не ЗначениеЗаполнено(ВидАналитикиНФП.Значение) Тогда
			Продолжить;
		КонецЕсли;
		НомерАналитикиНФП = НомерВидаАналитикиСтатьиПоказателяБюджетов(НФП, ВидАналитикиНФП.Значение);
		СоответствиеНайдено = Ложь;
		Для Каждого ВидАналитикиСтатьиПоказателя Из ВидыАналитикСтатьиПоказателя Цикл
			Если ВидАналитикиНФП.Значение = ВидАналитикиСтатьиПоказателя.Значение Тогда
				НомерАналитикиСтатьиПоказателя = НомерВидаАналитикиСтатьиПоказателяБюджетов(СтатьяПоказатель,
					ВидАналитикиСтатьиПоказателя.Значение);
				СоответствиеАналитик.Вставить(НомерАналитикиНФП, НомерАналитикиСтатьиПоказателя);
				СоответствиеНайдено = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Не СоответствиеНайдено Тогда
			СоответствиеАналитик = Неопределено;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СоответствиеАналитик;
	
КонецФункции

// Возвращает структуру полей заполнения валюты по виду аналитики.
// 
// Параметры:
// 	ВидАналитики - ПланВидовХарактеристикСсылка.АналитикиСтатейБюджетов - Вид аналитики.
// Возвращаемое значение:
// 	Структура - Описание:
// * УчитыватьПоВалюте - Булево - Флаг учета аналитики по валюте.
// * ЗаполнениеВалюты - Булево - Путь к полю валюты.
Функция ПараметрыЗаполненияВалютыВидаАналитики(ВидАналитики) Экспорт
	
	Если ЗначениеЗаполнено(ВидАналитики) Тогда
		ПараметрыЗаполненияВалюты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидАналитики, "УчитыватьПоВалюте, ЗаполнениеВалюты");
	Иначе
		ПараметрыЗаполненияВалюты = Новый Структура("УчитыватьПоВалюте, ЗаполнениеВалюты", Ложь, Ложь);
	КонецЕсли;
	
	Возврат ПараметрыЗаполненияВалюты;
КонецФункции

// Возвращает массив имен балансовых регистров для заданного вида документа
//
// Параметры:
//  ИмяВидаДокумента - Строка - имя вида документа
//  ТипДанныхУчета - Неопределено, ПеречислениеСсылка.ТипыДанныхУчета - отбор по типу данных учета
// Возвращаемое значение:
//  Соответствие из КлючИЗначение - массив балансовых регистров:
//   Ключ - Строка - имя балансового регистра
//   Значение - Структура - параметры отражения движений балансового регистра
//
Функция ОстаточныеФинансовыеРегистрыПоИмениВидаДокумента(ИмяВидаДокумента, ТипДанныхУчета = Неопределено) Экспорт
	
	КоллекцияДвиженийДокумента = Метаданные.Документы[ИмяВидаДокумента].Движения;
	
	РегистрыНакопленияМетаданные = Метаданные.РегистрыНакопления;
	
	ОстаточныеФинансовыеРегистры = Новый Соответствие;
	
	Для Каждого МетаданныеРегистра Из КоллекцияДвиженийДокумента Цикл
		
		Если Не РегистрыНакопленияМетаданные.Содержит(МетаданныеРегистра) Тогда
			Продолжить;
		ИначеЕсли МетаданныеРегистра.Реквизиты.Найти("ИдентификаторФинЗаписи") = Неопределено
			Или МетаданныеРегистра.Реквизиты.Найти("НастройкаХозяйственнойОперации") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если МетаданныеРегистра = РегистрыНакопленияМетаданные.РасчетыСКлиентами Тогда
			МетаданныеРегистра = РегистрыНакопленияМетаданные.РасчетыСКлиентамиПоСрокам;
		ИначеЕсли МетаданныеРегистра = РегистрыНакопленияМетаданные.РасчетыСПоставщиками Тогда
			МетаданныеРегистра = РегистрыНакопленияМетаданные.РасчетыСПоставщикамиПоСрокам;
		КонецЕсли;
		
		Попытка
			ПараметрыОтраженияДвижений = РегистрыНакопления[МетаданныеРегистра.Имя].ПараметрыОтраженияДвиженийВФинансовомУчете(
				МетаданныеРегистра);
		Исключение
			Продолжить;
		КонецПопытки;
		
		Если ТипДанныхУчета <> Неопределено И ПараметрыОтраженияДвижений.ТипДанныхУчета <> ТипДанныхУчета Тогда
			Продолжить;
		КонецЕсли;
		
		ОстаточныеФинансовыеРегистры.Вставить(МетаданныеРегистра.Имя, ПараметрыОтраженияДвижений);
		
	КонецЦикла;
	
	ДополнитьОстаточныеФинансовыеРегистрыПоИмениВидаДокумента(ИмяВидаДокумента, ОстаточныеФинансовыеРегистры);
	
	Возврат ОстаточныеФинансовыеРегистры;
	
КонецФункции

// Возвращает настройки ХО, которые необходимо заменить основной настройкой ХО из правила получения факта
//
// Возвращаемое значение:
//  Массив из СправочникСсылка.НастройкиХозяйственныхОпераций
//
Функция НастройкиХозяйственныхОперацийДляЗамены() Экспорт
	
	НастройкиХозяйственныхОпераций = Новый Массив;
	
	//++ Локализация
	НастройкиХозяйственныхОпераций.Добавить(Справочники.НастройкиХозяйственныхОпераций.ВходящийНДСПоПриобретению);
	//-- Локализация
	
	Возврат НастройкиХозяйственныхОпераций;
	
КонецФункции

// Возвращает признак наличия правил получения фактических данных по статьям бюджетов с хранением результата расчета правил.
//
// Возвращаемое значение:
//  Булево - Истина, если есть хотя бы одно правило
//
Функция ИспользуетсяХранимыйФактПоСтатьямБюджетов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПравилаПолученияФактаПоСтатьямБюджетов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПравилаПолученияФактаПоСтатьямБюджетов КАК ПравилаПолученияФактаПоСтатьямБюджетов
	|ГДЕ
	|	ПравилаПолученияФактаПоСтатьямБюджетов.ПромежуточноеКэшированиеРезультатовРаботыПравил
	|	И НЕ ПравилаПолученияФактаПоСтатьямБюджетов.ПометкаУдаления";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Запрос.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

// Возвращает массив имен объектов метаданных, связанных с видом аналитики, содержащих табличную часть с указанным именем.
//
// Параметры:
//  ВидАналитики - ПланВидовХарактеристикСсылка.АналитикиСтатейБюджетов - вид аналитики
//  ИмяТабличнойЧасти - Строка - имя табличной части
//
// Возвращаемое значение:
//  Массив Из Строка - массив имен объектов метаданных с указанной табличной частью
//
Функция ИменаТиповВидаАналитикиСодержащихТабличнуюЧасть(ВидАналитики, ИмяТабличнойЧасти) Экспорт
	
	ИменаТиповВидаАналитики = Новый Массив;
	
	Для Каждого ТипВидаАналитики Из ТипыВидаАналитики(ВидАналитики) Цикл
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипВидаАналитики);
		Если МетаданныеОбъекта <> Неопределено
			И Не Метаданные.Перечисления.Содержит(МетаданныеОбъекта)
			И МетаданныеОбъекта.ТабличныеЧасти.Найти(ИмяТабличнойЧасти) <> Неопределено Тогда
			ИменаТиповВидаАналитики.Добавить(МетаданныеОбъекта.ПолноеИмя());
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИменаТиповВидаАналитики;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьОстаточныеФинансовыеРегистрыПоИмениВидаДокумента(ИмяВидаДокумента, ОстаточныеФинансовыеРегистры)
	
	КоллекцияДвиженийДокумента = Новый Массив;
	
	Если ИмяВидаДокумента = Метаданные.Документы.РасчетКурсовыхРазниц.Имя Тогда
		РегистрыНакопленияМетаданные = Метаданные.РегистрыНакопления;
		КоллекцияДвиженийДокумента.Добавить(РегистрыНакопленияМетаданные.РасчетыСПоставщикамиПоСрокам);
		КоллекцияДвиженийДокумента.Добавить(РегистрыНакопленияМетаданные.РасчетыСКлиентамиПоСрокам);
	КонецЕсли;
	
	Для Каждого МетаданныеРегистра Из КоллекцияДвиженийДокумента Цикл // -ОбъектМетаданных-
		
		Попытка
			ПараметрыОтраженияДвижений = РегистрыНакопления[МетаданныеРегистра.Имя].ПараметрыОтраженияДвиженийВФинансовомУчете(
				МетаданныеРегистра);
		Исключение
			Продолжить;
		КонецПопытки;
		
		ОстаточныеФинансовыеРегистры.Вставить(МетаданныеРегистра.Имя, ПараметрыОтраженияДвижений);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
