#Область ПрограммныйИнтерфейс

// Описывает состав прикладных данных таблицы отчета.
// Под таблицей понимается совокупность строк с одинаковым составом колонок.
// Обычно каждая таблица в отчете имеет отдельное название, номер и шапку с описанием колонок.
//
// Параметры:
//  Описание - см. ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйОписаниеТаблицыОтчета - заполняемая коллекция
//  ИмяТаблицыОтчета - Строка - имя таблицы из ЗаполнениеБухгалтерскойОтчетностиРазмещение.СоставТаблицОтчета
//
Процедура ОписатьТаблицуОтчета(Описание, ИмяТаблицыОтчета) Экспорт
	
	Если ИмяТаблицыОтчета = "ЗапасыНаличиеДвижение" Тогда
		ОписатьТаблицуОтчетаЗапасыНаличиеДвижение(Описание);
	ИначеЕсли ИмяТаблицыОтчета = "ЗапасыОграничения" Тогда
		ОписатьТаблицуОтчетаЗапасыОграничения(Описание);
	КонецЕсли;
	
КонецПроцедуры

// Определяет состав заполняемых пояснений конкретного отчета (за конкретный период, по конкретной организации)
//
// Параметры:
//  ТаблицыОтчета - Структура - Ключ - имя таблицы из ЗаполнениеБухгалтерскойОтчетностиРазмещение.СоставТаблицОтчета; Значение - Булево.
//                  Для заполняемых пояснений следует установить значение Истина, состав коллекции изменять не следует.
//  КонтекстОтчета - см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстОтчета
//
Процедура ОпределитьСоставЗаполняемыхПояснений(ТаблицыОтчета, КонтекстОтчета) Экспорт
	
	ТаблицыОтчета.ЗапасыНаличиеДвижение = Истина;
	ТаблицыОтчета.ЗапасыОграничения = Истина;
	
КонецПроцедуры

// Заполняет таблицы отчета за один период.
//
// Параметры:
//  ПолучениеДанныхТаблицОтчета - см. ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйПолучениеДанныхТаблицОтчета -
//     процессор, который содержит исходные параметры для получения данных,
//     и в который нужно разместить полученные данные.
//  Классификаторы - Структура - Ключ: имя таблицы отчета; Значение: Структура - Ключ и Значение: имя колонки, содержащей значения по классификатору
//  КонтекстЗаполненияОтчета - см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета
//
Процедура ЗаполнитьТаблицыОтчета(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета) Экспорт
	
	ЗаполнитьПояснениеЗапасы(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОписатьТаблицуОтчетаЗапасыНаличиеДвижение(Описание)
	
	Описание.Представление = НСтр("ru = 'Запасы';
									|en = 'Inventory'");
	
	Описание.Структура.Добавить("Группа");
	
	Описание.ЧисловыеЗначения.Добавить("НачалоЗатраты");
	Описание.ЧисловыеЗначения.Добавить("НачалоОбесценение");
	Описание.ЧисловыеЗначения.Добавить("Затраты");
	Описание.ЧисловыеЗначения.Добавить("СписаноЗатраты");
	Описание.ЧисловыеЗначения.Добавить("СписаноОбесценение");
	Описание.ЧисловыеЗначения.Добавить("Обесценение");
	Описание.ЧисловыеЗначения.Добавить("ПреобразованиеЗатраты");
	Описание.ЧисловыеЗначения.Добавить("ПреобразованиеОбесценение");
	Описание.ЧисловыеЗначения.Добавить("КонецЗатраты");
	Описание.ЧисловыеЗначения.Добавить("КонецОбесценение");
	
КонецПроцедуры

Процедура ОписатьТаблицуОтчетаЗапасыОграничения(Описание)
	
	Описание.Представление = НСтр("ru = 'Запасы, в отношении которых имеются ограничения имущественных прав';
									|en = 'Inventory with limited property rights'");
	
	Описание.Структура.Добавить("Ограничение");
	Описание.Структура.Добавить("Группа");
	
	Описание.Классификаторы.Вставить("Ограничение", Новый СписокЗначений);
	Описание.Классификаторы.Ограничение.Добавить("ВПути",   НСтр("ru = 'Запасы, находящиеся в пути';
																|en = 'Inventory in transit'"));
	Описание.Классификаторы.Ограничение.Добавить("ВЗалоге", НСтр("ru = 'Запасы, находящиеся в залоге';
																|en = 'Inventory in deposit'"));
	
	Описание.ВариантПериода = "НачалоКонец";
	
	Описание.ЧисловыеЗначения.Добавить("БалансоваяСтоимость"); // НачалоБалансоваяСтоимость, КонецБалансоваяСтоимость
	
КонецПроцедуры

Процедура ЗаполнитьПояснениеЗапасы(ЗаполняемыеДанные, Классификаторы, КонтекстЗаполненияОтчета)
	
	Если ЗаполнениеБухгалтерскойОтчетностиТабличное.ТаблицаОтчета(ЗаполняемыеДанные, "ЗапасыНаличиеДвижение") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СчетаУчетаЗапасов = НовыйСчетаУчетаЗапасов();
	
	ГруппыЗапасов = НовыйГруппыЗапасов();
	
	ЗаполнитьГруппыЗапасов(ГруппыЗапасов, СчетаУчетаЗапасов, КонтекстЗаполненияОтчета);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОпределитьИспользуемыеСчетаУчетаЗапасов(СчетаУчетаЗапасов, МенеджерВременныхТаблиц, ГруппыЗапасов);
	
	СписаноОбесценениеПоСправкеРасчету = ВыбытиеОбесцененияОпределяетсяПоСправкеРасчету(КонтекстЗаполненияОтчета);
	СоздатьВыбытиеОбесцененияЗапасов(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, СчетаУчетаЗапасов.Обесценение);
	СоздатьАвансыВыданные(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, СчетаУчетаЗапасов.Авансы);
	
	СоздатьДанныеУчетаЗапасов(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, СчетаУчетаЗапасов);
	
	ПараметрыТекстовРасшифровки = ПараметрыТекстовРасшифровкиЗапасы(КонтекстЗаполненияОтчета);
	ШаблоныКолонок = Новый Структура; // Ключ - имя колонки; Значение - строка
	ШаблоныЗаголовковРасшифровки = Новый Соответствие; // Ключ - имя колонки; Значение - ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки
	ЗаполнитьШаблоныКолонокЗапасыНаличиеДвижение(ШаблоныКолонок, ШаблоныЗаголовковРасшифровки, СписаноОбесценениеПоСправкеРасчету);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	// Прикладной порядок характеристик совпадает с алфавитным
	"ВЫБРАТЬ
	|	Счета.Группа КАК Группа,
	|	Счета.ОтборПоАналитике КАК ОтборПоАналитике,
	|	Счета.Обесценение КАК ЭтоОбесценение,
	|	Счета.НазначениеСчета = ""Отгруженные"" КАК ЭтоОтгруженные,
	|	ДанныеУчета.Организация КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеУчета.Организация) КАК ОрганизацияПредставление,
	|	ДанныеУчета.Организация.Наименование КАК ОрганизацияПорядок,
	|	ДанныеУчета.Счет КАК Счет,
	|	ДанныеУчета.Счет.Порядок КАК СчетПорядок,
	|	ДанныеУчета.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА Счета.Группа = КорСчета.Группа
	|				И Счета.Обесценение
	|				И НЕ КорСчета.Обесценение
	|				И НЕ Счета.ПрименяетсяОтборПоАналитике
	|			ТОГДА ""СписаноОбесценение""
	|		КОГДА Счета.Группа = КорСчета.Группа
	|				И Счета.Обесценение = КорСчета.Обесценение
	|				И НЕ Счета.ПрименяетсяОтборПоАналитике
	|			ТОГДА ""ВнутриГруппы""
	|		КОГДА КорСчета.Группа ЕСТЬ НЕ NULL 
	|				И Счета.Обесценение = КорСчета.Обесценение
	|				И НЕ Счета.ПрименяетсяОтборПоАналитике
	|			ТОГДА ""ВнутриЗапасов""
	|	КОНЕЦ КАК Исключение,
	|	ДанныеУчета.КорСчет КАК КорСчет,
	|	ДанныеУчета.КорСчет.Порядок КАК КорСчетПорядок,
	|	ДанныеУчета.Сумма КАК Сумма
	|ИЗ
	|	ДанныеУчетаЗапасов КАК ДанныеУчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГруппыСчетов КАК Счета
	|		ПО ДанныеУчета.Счет = Счета.Счет
	|		ЛЕВОЕ СОЕДИНЕНИЕ ГруппыСчетов КАК КорСчета
	|		ПО ДанныеУчета.КорСчет = КорСчета.Счет
	|			И (НЕ КорСчета.ПрименяетсяОтборПоАналитике)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Группа,
	|	ОрганизацияПорядок,
	|	Организация,
	|	СчетПорядок,
	|	Счет,
	|	Характеристика,
	|	КорСчетПорядок,
	|	КорСчет
	|ИТОГИ
	|	МАКСИМУМ(ОтборПоАналитике),
	|	МАКСИМУМ(ЭтоОбесценение),
	|	МАКСИМУМ(ЭтоОтгруженные),
	|	МАКСИМУМ(СчетПорядок),
	|	МАКСИМУМ(Исключение),
	|	МАКСИМУМ(КорСчетПорядок),
	|	СУММА(Сумма)
	|ПО
	|	Группа,
	|	Организация,
	|	Счет,
	|	Характеристика,
	|	КорСчет";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаГруппа = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ЗапасыНаличиеДвижение = ЗаполнениеБухгалтерскойОтчетностиТабличное.НачатьВыводТаблицы(
		ЗаполняемыеДанные,
		"ЗапасыНаличиеДвижение",
		ШаблоныКолонок,
		ПараметрыТекстовРасшифровки);
		
	ЗапасыОграничения = Неопределено;
	Если ЗаполнениеБухгалтерскойОтчетностиТабличное.ТаблицаОтчета(ЗаполняемыеДанные, "ЗапасыОграничения") <> Неопределено Тогда
		ЗапасыОграничения = ЗаполнениеБухгалтерскойОтчетностиТабличное.НачатьВыводТаблицы(
			ЗаполняемыеДанные,
			"ЗапасыОграничения",
			ШаблоныКолонок,
			ПараметрыТекстовРасшифровки);
	КонецЕсли;
		
	Для Каждого ГруппаЗапасов Из ГруппыЗапасов Цикл
	
		УстановитьПараметрыТекстовРасшифровкиПоГруппеЗапасов(ПараметрыТекстовРасшифровки, ГруппаЗапасов);
		
		ШаблоныКолонокГруппыЗапасов = УточнитьПорядокЗаполненияПоГруппеЗапасов(ШаблоныЗаголовковРасшифровки, ГруппаЗапасов, СчетаУчетаЗапасов);
		
		ВсеВыводимыеТаблицы = Новый Массив;
		
		НачатьДобавлениеСтрокиЗапасыНаличиеДвижение(ЗапасыНаличиеДвижение, ГруппаЗапасов, ШаблоныКолонокГруппыЗапасов, ПараметрыТекстовРасшифровки);
		ВсеВыводимыеТаблицы.Добавить(ЗапасыНаличиеДвижение);
		
		ВыводитьЗапасыВПути = Ложь;
		Если ЗапасыОграничения <> Неопределено И ЗначениеЗаполнено(ГруппаЗапасов.СчетаОтгруженных) Тогда
			НачатьДобавлениеСтрокиЗапасыОграничения(
				ЗапасыОграничения,
				ГруппаЗапасов,
				ШаблоныКолонокГруппыЗапасов,
				ПараметрыТекстовРасшифровки,
				Классификаторы.ЗапасыОграничения.Ограничение);
			ВсеВыводимыеТаблицы.Добавить(ЗапасыОграничения);
			ВыводитьЗапасыВПути = Истина;
		КонецЕсли;
		
		Если ВыборкаГруппа.НайтиСледующий(ГруппыЗапасов.Индекс(ГруппаЗапасов), "Группа") Тогда
		
			ВыборкаОрганизация = ВыборкаГруппа.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаОрганизация.Следующий() Цикл
				
				ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьФилиал(
					ВсеВыводимыеТаблицы,
					ШаблоныКолонокГруппыЗапасов,
					ВыборкаОрганизация.Организация,
					ВыборкаОрганизация.ОрганизацияПредставление);
				
				ВыборкаСчет = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаСчет.Следующий() Цикл
					
					ВыборкаХарактеристика = ВыборкаСчет.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаХарактеристика.Следующий() Цикл
						ЗначенияНастроекОтчета = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.ЗначенияНастроекОтчетаЗапасы(ВыборкаХарактеристика);
						УстановитьОтборПоАналитике(ЗначенияНастроекОтчета, ВыборкаХарактеристика);
						Если ВыборкаХарактеристика.Характеристика = "Начало" Тогда
							Если ВыборкаХарактеристика.ЭтоОбесценение Тогда
								ДобавитьСлагаемоеСчет(ЗапасыНаличиеДвижение, "НачалоОбесценение", ВыборкаХарактеристика, ЗначенияНастроекОтчета);
							Иначе
								ДобавитьСлагаемоеСчет(ЗапасыНаличиеДвижение, "НачалоЗатраты", ВыборкаХарактеристика, ЗначенияНастроекОтчета);
							КонецЕсли;
							Если ВыводитьЗапасыВПути И ВыборкаСчет.ЭтоОтгруженные Тогда
								ДобавитьСлагаемоеСчет(ЗапасыОграничения, "НачалоБалансоваяСтоимость", ВыборкаХарактеристика, ЗначенияНастроекОтчета);
							КонецЕсли;
						ИначеЕсли ВыборкаХарактеристика.Характеристика = "Конец" Тогда
							Если ВыборкаХарактеристика.ЭтоОбесценение Тогда
								ДобавитьСлагаемоеСчет(ЗапасыНаличиеДвижение, "КонецОбесценение", ВыборкаХарактеристика, ЗначенияНастроекОтчета);
							Иначе
								ДобавитьСлагаемоеСчет(ЗапасыНаличиеДвижение, "КонецЗатраты", ВыборкаХарактеристика, ЗначенияНастроекОтчета);
							КонецЕсли;
							Если ВыводитьЗапасыВПути И ВыборкаСчет.ЭтоОтгруженные Тогда
								ДобавитьСлагаемоеСчет(ЗапасыОграничения, "КонецБалансоваяСтоимость", ВыборкаХарактеристика, ЗначенияНастроекОтчета);
							КонецЕсли;
						ИначеЕсли ВыборкаХарактеристика.Характеристика = "СправкаРасчетВыбытиеОбесцененияЗапасов" Тогда
							
							ВыбытиеОбесценения = ВыборкаХарактеристика.Сумма;
							Если ВыбытиеОбесценения <> 0 Тогда
								Описание = СтрШаблон(
									НСтр("ru = 'См. Справку-расчет ""Обесценение запасов"" по счету %1';
										|en = 'See the ""Impairment of inventory"" detailed calculation on account %1'"),
									ВыборкаХарактеристика.Счет);
								ЗначенияНастроекОтчетаСправкаРасчет = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.ЗначенияНастроекОтчетаЗапасы(ВыборкаХарактеристика);
								НастроитьОтчетСправкаРасчетОбесценениеЗапасов(ЗначенияНастроекОтчетаСправкаРасчет);
								Отчет = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастройкиОтчетаПоЗначениям(ЗначенияНастроекОтчетаСправкаРасчет, КонтекстЗаполненияОтчета.Период);
								ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(ЗапасыНаличиеДвижение, "СписаноОбесценение", Описание, ВыбытиеОбесценения, , Отчет);
								Описание = СтрШаблон(
									НСтр("ru = 'Исключено изменение обесценения, связанное со списанием запаса; см. Справку-расчету ""Обесценение запасов"" по счету %1';
										|en = 'Change in impairment related to inventory write-off is excluded. See the ""Impairment of inventory"" detailed calculation on account %1'"),
									ВыборкаХарактеристика.Счет);
								ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(ЗапасыНаличиеДвижение, "Обесценение", Описание, -ВыбытиеОбесценения, , Отчет);
							КонецЕсли;
						ИначеЕсли ВыборкаХарактеристика.Характеристика = "АвансыДт" Тогда
							ДобавитьСлагаемоеСчет(ЗапасыНаличиеДвижение, "Затраты", ВыборкаХарактеристика, ЗначенияНастроекОтчета);
						ИначеЕсли ВыборкаХарактеристика.Характеристика = "АвансыКт" Тогда
							ДобавитьСлагаемоеСчет(ЗапасыНаличиеДвижение, "СписаноЗатраты", ВыборкаХарактеристика, ЗначенияНастроекОтчета);
						Иначе
						
							ВыводОборотовСчета = НовыйВыводОборотовСчета(ЗапасыНаличиеДвижение);
							
							ОсновнаяКолонка = "Затраты";
							Если ВыборкаХарактеристика.ЭтоОбесценение Тогда
								ОсновнаяКолонка = "Обесценение";
							ИначеЕсли ВыборкаХарактеристика.Характеристика = "Кт" Тогда
								ОсновнаяКолонка = "СписаноЗатраты";
							КонецЕсли;
							
							ВыборкаКорСчет = ВыборкаХарактеристика.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							Пока ВыборкаКорСчет.Следующий() Цикл
								ДобавитьСлагаемоеОборотМеждуСчетами(ВыводОборотовСчета, ОсновнаяКолонка, ВыборкаКорСчет);
							КонецЦикла;
							
							// Исключения
							Если ОсновнаяКолонка = "Затраты" Или ОсновнаяКолонка = "СписаноЗатраты" Тогда
								ВывестиИсключенияПоСчету(ВыводОборотовСчета, "ВнутриГруппы",       ОсновнаяКолонка);
								ВывестиИсключенияПоСчету(ВыводОборотовСчета, "ВнутриЗапасов",      ОсновнаяКолонка, "ПреобразованиеЗатраты");
							ИначеЕсли ОсновнаяКолонка = "Обесценение" Тогда
								ВывестиИсключенияПоСчету(ВыводОборотовСчета, "ВнутриГруппы",       ОсновнаяКолонка);
								ВывестиИсключенияПоСчету(ВыводОборотовСчета, "ВнутриЗапасов",      ОсновнаяКолонка, "ПреобразованиеОбесценение");
								ВывестиИсключенияПоСчету(ВыводОборотовСчета, "СписаноОбесценение", ОсновнаяКолонка, "СписаноОбесценение");
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
			
		КонецЕсли;
		
		ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(ВсеВыводимыеТаблицы, ШаблоныКолонокГруппыЗапасов);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура НастроитьОтчетСправкаРасчетОбесценениеЗапасов(ЗначенияНастроекОтчета)
	ЗначенияНастроекОтчета.ИмяОтчета = "СправкаРасчетОбесцененияЗапасов";
КонецПроцедуры

#Область Тексты

Процедура ЗаполнитьШаблоныКолонокЗапасыНаличиеДвижение(ШаблоныКолонок, ШаблоныРасшифровки, СписаноОбесценениеПоСправкеРасчету)
	
	// Затраты
	
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"НачалоЗатраты",
		НСтр("ru = 'фактическая себестоимость на [НачалоПериода]';
			|en = 'actual cost as of [НачалоПериода]'"),
		НСтр("ru = 'Сальдо по [СчетуЗатрат]';
			|en = '[СчетуЗатрат] balance'"));
		
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"Затраты",
		НСтр("ru = 'затраты за [Период]';
			|en = 'costs for [Период]'"),
		НСтр("ru = 'Оборот по дебету [СчетаЗатрат], за исключением оборотов в корреспонденции со счетами запасов';
			|en = '[СчетаЗатрат] debit turnover, except for turnover in correspondence with inventory accounts'"));
		
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"СписаноЗатраты",
		НСтр("ru = 'списано за [Период] - фактическая себестоимость';
			|en = 'written off for [Период] - actual cost'"),
		НСтр("ru = 'Оборот по кредиту [СчетаЗатрат], за исключением оборотов в корреспонденции со счетами запасов';
			|en = '[СчетаЗатрат] credit turnover, except for turnover in correspondence with inventory accounts'"));
		
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"ПреобразованиеЗатраты",
		НСтр("ru = 'изменения видов запасов за [Период] - фактическая себестоимость';
			|en = 'changes in inventory owner attributes for [Период] - actual cost'"),
		НСтр("ru = 'Оборот по [СчетуЗатрат] в корреспонденции со счетами других видов запасов';
			|en = '[СчетуЗатрат] turnover in correspondence with accounts of other inventory types'"));
	
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"КонецЗатраты",
		НСтр("ru = 'фактическая себестоимость на [КонецПериода]';
			|en = 'actual cost as of [КонецПериода]'"),
		НСтр("ru = 'Сальдо по [СчетуЗатрат]';
			|en = '[СчетуЗатрат] balance'"));
		
	// Обесценение
	
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"НачалоОбесценение",
		НСтр("ru = 'резерв под обесценение на [НачалоПериода]';
			|en = 'provision for impairment as of [НачалоПериода]'"),
		НСтр("ru = 'Сальдо по [СчетуОбесценения]';
			|en = '[СчетуОбесценения] balance'"));
		
	ОписаниеСписаноОбесценение = НСтр("ru = 'Оборот по [СчетуОбесценения] в корреспонденции с соответствующим ему счетом запасов.';
										|en = '[СчетуОбесценения] turnover in correspondence with the corresponding inventory account.'");
	Если СписаноОбесценениеПоСправкеРасчету Тогда
		ОписаниеСписаноОбесценение = НСтр("ru = 'Расчет приведен в Cправке-расчете ""Обесценение запасов"".
                                           |К этому показателю также может относиться оборот по [СчетуОбесценения] в корреспонденции с соответствующим ему счетом запасов.';
                                           |en = 'The calculation is given in the ""Impairment of inventory"" detailed calculation.
                                           |This indicator may also include the [СчетуОбесценения] turnover in correspondence with the corresponding inventory account.'");
	КонецЕсли;
		
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"СписаноОбесценение", //справка
		НСтр("ru = 'изменение резерва под обесценение за [Период] - в связи со списанием запасов';
			|en = 'change in provision for impairment for [Период] - due to inventory write-off'"),
		ОписаниеСписаноОбесценение);
		
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"Обесценение",
		НСтр("ru = 'изменение резерва под обесценение за [Период] - кроме связанных со списанием запасов';
			|en = 'change in provision for impairment for [Период] - except for related to inventory write-off'"),
		НСтр("ru = 'Оборот по [СчетуОбесценения], кроме связанного со списанием запасов';
			|en = '[СчетуОбесценения] turnover, except for related to inventory write-off'"));
		
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"КонецОбесценение",
		НСтр("ru = 'резерв под обесценение на [КонецПериода]';
			|en = 'provision for impairment as for [КонецПериода]'"),
		НСтр("ru = 'Сальдо по [СчетуОбесценения]';
			|en = '[СчетуОбесценения] balance'"));
	
	// Ограничения
	
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"НачалоБалансоваяСтоимость",
		НСтр("ru = 'балансовая стоимость на [КонецПредшествующегоПериода]';
			|en = 'net book value as of [КонецПредшествующегоПериода]'"),
		НСтр("ru = 'Сальдо по [СчетамБалансовойСтоимости]';
			|en = '[СчетамБалансовойСтоимости] balance'"));
	
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"КонецБалансоваяСтоимость",
		НСтр("ru = 'балансовая стоимость на [КонецПериода]';
			|en = 'Net book value as of [КонецПериода]'"),
		НСтр("ru = 'Сальдо по [СчетамБалансовойСтоимости]';
			|en = '[СчетамБалансовойСтоимости] balance'"));
		
КонецПроцедуры

Функция УточнитьПорядокЗаполненияПоГруппеЗапасов(ШаблоныКолонок, ГруппаЗапасов, СчетаУчетаЗапасов)
	
	ГруппыЗапасов = ГруппаЗапасов.Владелец();
	ЭтоПрочиеЗапасы = (ГруппыЗапасов.Индекс(ГруппаЗапасов) = ГруппыЗапасов.Количество() - 1);
	Если ЭтоПрочиеЗапасы Тогда
		// Для таких порядок расчета не выводим, так как по умолчанию он "служебный",
		// включает только счета, не предназначенные для ведения учета
		УточненныеШаблоныКолонок = ОбщегоНазначения.СкопироватьРекурсивно(ШаблоныКолонок);
		Для Каждого ШаблонКолонки Из УточненныеШаблоныКолонок Цикл
			ШаблонКолонки.Значение.ПорядокРасчета = "";
		КонецЦикла;
		Возврат УточненныеШаблоныКолонок;
	КонецЕсли;
	
	ПорядокЗаполнения = Новый Структура;
	
	ПояснениеНеРассчитывается = "";
	
	Если Не ЗначениеЗаполнено(ГруппаЗапасов.СчетаОбесценения) Тогда
		ПорядокЗаполнения.Вставить("НачалоОбесценение",  ПояснениеНеРассчитывается);
		ПорядокЗаполнения.Вставить("СписаноОбесценение", ПояснениеНеРассчитывается);
		ПорядокЗаполнения.Вставить("Обесценение",        ПояснениеНеРассчитывается);
		ПорядокЗаполнения.Вставить("КонецОбесценение",   ПояснениеНеРассчитывается);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ГруппаЗапасов.ОтборПоАналитике) Тогда
		
		ПояснениеОтборПоАналитике = "";
		Если ГруппаЗапасов.ОтборПоАналитике = "ТранспортныеРасходы" Тогда
			
			ПредставлениеТранспортныеРасходыСтатьиЗатрат = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(СчетаУчетаЗапасов.ТранспортныеРасходыСтатьиЗатрат);
			
			Если ПредставлениеТранспортныеРасходыСтатьиЗатрат.Количество = 0 Тогда
				ПояснениеОтборПоАналитике = СтрШаблон(НСтр("ru = 'по статьям затрат вида %1';
															|en = 'by cost items of the %1 type'"), СчетаУчетаЗапасов.ТранспортныеРасходыВидАналитики);
			Иначе
				ПояснениеОтборПоАналитике = СтрШаблон(
					НСтр("ru = 'по статьям затрат вида %1 (%2)';
						|en = 'by cost items of the %1 (%2) type'"),
					СчетаУчетаЗапасов.ТранспортныеРасходыВидАналитики,
					ПредставлениеТранспортныеРасходыСтатьиЗатрат.Текст);
			КонецЕсли;
			
		ИначеЕсли ГруппаЗапасов.ОтборПоАналитике = "РБП" Тогда
			ПояснениеОтборПоАналитике = НСтр("ru = 'по статьям с указанным отражением в балансе в составе запасов';
											|en = 'by items that are recorded in the balance sheet as inventory'");
		ИначеЕсли ГруппаЗапасов.ОтборПоАналитике = "Авансы" Тогда
			ПояснениеОтборПоАналитике = НСтр("ru = 'по договорам с указанным отражением авансов в балансе в составе запасов';
											|en = 'under contracts that record advances in the balance sheet as inventory'");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПояснениеОтборПоАналитике) Тогда
			
			ПорядокЗаполнения.Вставить(
				"НачалоЗатраты",
				СтрШаблон(НСтр("ru = 'Сальдо по [СчетуЗатрат] - %1';
								|en = '[СчетуЗатрат] balance - %1'"), ПояснениеОтборПоАналитике));
			ПорядокЗаполнения.Вставить(
				"Затраты",
				СтрШаблон(НСтр("ru = 'Оборот по дебету [СчетаЗатрат] - %1';
								|en = '[СчетаЗатрат] debit turnover - %1'"), ПояснениеОтборПоАналитике));
			ПорядокЗаполнения.Вставить(
				"СписаноЗатраты",
				СтрШаблон(НСтр("ru = 'Оборот по кредиту [СчетаЗатрат] - %1';
								|en = '[СчетаЗатрат] credit turnover - %1'"), ПояснениеОтборПоАналитике));
			ПорядокЗаполнения.Вставить(
				"КонецЗатраты",
				СтрШаблон(НСтр("ru = 'Сальдо по [СчетуЗатрат] - %1';
								|en = '[СчетуЗатрат] balance - %1'"), ПояснениеОтборПоАналитике));
			ПорядокЗаполнения.Вставить(
				"НачалоБалансоваяСтоимость",
				СтрШаблон(НСтр("ru = 'Сальдо по [СчетамБалансовойСтоимости] - %1';
								|en = '[СчетамБалансовойСтоимости] balance - %1'"), ПояснениеОтборПоАналитике));
			ПорядокЗаполнения.Вставить(
				"КонецБалансоваяСтоимость",
				СтрШаблон(НСтр("ru = 'Сальдо по [СчетамБалансовойСтоимости] - %1';
								|en = '[СчетамБалансовойСтоимости] balance - %1'"), ПояснениеОтборПоАналитике));
			
			ПорядокЗаполнения.Вставить("ПреобразованиеЗатраты", ПояснениеНеРассчитывается);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПорядокЗаполнения) Тогда
		Возврат ШаблоныКолонок;
	КонецЕсли;
	
	УточненныеШаблоныКолонок = ОбщегоНазначения.СкопироватьРекурсивно(ШаблоныКолонок);
	
	Для Каждого ПорядокЗаполненияКолонки Из ПорядокЗаполнения Цикл
		
		ШаблонКолонки = УточненныеШаблоныКолонок[ПорядокЗаполненияКолонки.Ключ];
		
		Если ШаблонКолонки = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ПорядокЗаполненияКолонки.Значение = ПояснениеНеРассчитывается Тогда
			ШаблонКолонки.ПорядокРасчета = НСтр("ru = 'Не рассчитывается для этой строки';
												|en = 'Is not calculated for this row'");
			ШаблонКолонки.Определен = Ложь;
		Иначе
			ШаблонКолонки.ПорядокРасчета = ПорядокЗаполненияКолонки.Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат УточненныеШаблоныКолонок;
	
КонецФункции

Функция ПараметрыТекстовРасшифровкиЗапасы(КонтекстЗаполненияОтчета)
	
	ПараметрыТекстов = Новый Структура;
	
	ЗаполнениеБухгалтерскойОтчетностиТабличное.УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстов, КонтекстЗаполненияОтчета);
	
	// См. УстановитьПараметрыТекстовРасшифровкиПоГруппеЗапасов
	ПараметрыТекстов.Вставить("НаименованиеГруппы", "");
	ПараметрыТекстов.Вставить("СчетаЗатрат", ""); // включая счета отгруженных
	ПараметрыТекстов.Вставить("СчетуЗатрат", ""); // включая счета отгруженных
	ПараметрыТекстов.Вставить("СчетаОтгруженных", "");
	ПараметрыТекстов.Вставить("СчетуОтгруженных", "");
	ПараметрыТекстов.Вставить("СчетаОбесценения", "");
	ПараметрыТекстов.Вставить("СчетуОбесценения", "");
	ПараметрыТекстов.Вставить("СчетамБалансовойСтоимости", "");
	
	Возврат ПараметрыТекстов;
	
КонецФункции

Процедура УстановитьПараметрыТекстовРасшифровкиПоГруппеЗапасов(ПараметрыТекстовРасшифровки, ГруппаЗапасов)
	
	СчетаЗатрат = ОбщегоНазначения.СкопироватьРекурсивно(ГруппаЗапасов.СчетаЗатрат);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаЗатрат, ГруппаЗапасов.СчетаОтгруженных);
	ПредставленияСчетовЗапасов     = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(СчетаЗатрат);
	ПараметрыТекстовРасшифровки.СчетаЗатрат = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовРодительныйПадеж(
		ПредставленияСчетовЗапасов);
	ПараметрыТекстовРасшифровки.СчетуЗатрат = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовДательныйПадеж(
		ПредставленияСчетовЗапасов);
	
	ПредставленияСчетоОтгруженных = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(ГруппаЗапасов.СчетаОтгруженных);
	ПараметрыТекстовРасшифровки.СчетаОтгруженных = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовРодительныйПадеж(
		ПредставленияСчетоОтгруженных);
	ПараметрыТекстовРасшифровки.СчетуОтгруженных = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовДательныйПадеж(
		ПредставленияСчетоОтгруженных);
		
	ПредставленияСчетовОбесценения = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(ГруппаЗапасов.СчетаОбесценения);
	ПараметрыТекстовРасшифровки.СчетаОбесценения = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовРодительныйПадеж(
		ПредставленияСчетовОбесценения);
	ПараметрыТекстовРасшифровки.СчетуОбесценения = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовДательныйПадеж(
		ПредставленияСчетовОбесценения);
	
	СчетаБалансовойСтоимости = ОбщегоНазначения.СкопироватьРекурсивно(СчетаЗатрат);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаБалансовойСтоимости, ГруппаЗапасов.СчетаОбесценения);
	ПредставленияВсехСчетов = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(СчетаБалансовойСтоимости);
	ПараметрыТекстовРасшифровки.СчетамБалансовойСтоимости = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовДательныйПадеж(
		ПредставленияВсехСчетов);
	
	ПараметрыТекстовРасшифровки.НаименованиеГруппы = ГруппаЗапасов.Наименование;
	
КонецПроцедуры

#КонецОбласти

#Область Настройки

Функция НовыйГруппыЗапасов()
	
	ГруппыЗапасов = Новый ТаблицаЗначений;
	
	ГруппыЗапасов.Колонки.Добавить("Наименование",     Новый ОписаниеТипов("Строка"));
	ГруппыЗапасов.Колонки.Добавить("Вывод",            Новый ОписаниеТипов("Булево"));
	ГруппыЗапасов.Колонки.Добавить("СчетаЗатрат",      Новый ОписаниеТипов("Массив")); // Из ПланСчетовСсылка.Хозрасчетный
	ГруппыЗапасов.Колонки.Добавить("СчетаОбесценения", Новый ОписаниеТипов("Массив")); // Из ПланСчетовСсылка.Хозрасчетный
	ГруппыЗапасов.Колонки.Добавить("СчетаОтгруженных", Новый ОписаниеТипов("Массив")); // Из ПланСчетовСсылка.Хозрасчетный
	ГруппыЗапасов.Колонки.Добавить("ОтборПоАналитике", Новый ОписаниеТипов("Строка"));
	
	// Последняя группа считается "запасной" - она приводится для полноты картины и может содержать значения,
	// полученные в результате нарушения порядка учета или нецелостного добавления субсчетов.
	
	Возврат ГруппыЗапасов;

КонецФункции

Процедура ЗаполнитьГруппыЗапасов(ГруппыЗапасов, СчетаУчетаЗапасов, КонтекстЗаполненияОтчета)
	
	Группа = ДобавитьГруппуЗапасов(ГруппыЗапасов, НСтр("ru = 'Сырье и материалы';
														|en = 'Raw and consumable materials'"), ПланыСчетов.Хозрасчетный.Материалы);
	Группа.СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ЗаготовлениеИПриобретениеМатериалов);
	Группа.СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ОтклонениеВСтоимостиМатериалов);
	Группа.СчетаОбесценения.Добавить(ПланыСчетов.Хозрасчетный.РезервыПодСнижениеСтоимостиМатериалов);
	
	Группа = ДобавитьГруппуЗапасов(ГруппыЗапасов, НСтр("ru = 'Готовая продукция';
														|en = 'Finished products'"), ПланыСчетов.Хозрасчетный.ГотоваяПродукция);
	Группа.СчетаОбесценения.Добавить(ПланыСчетов.Хозрасчетный.РезервыПодСнижениеСтоимостиГотовойПродукции);
	Группа.СчетаОтгруженных.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукцияОтгруженная);
	
	Группа = ДобавитьГруппуЗапасов(ГруппыЗапасов, НСтр("ru = 'Товары';
														|en = 'Goods'"), ПланыСчетов.Хозрасчетный.Товары);
	Группа.СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ПриобретениеТоваров);
	Группа.СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ОтклонениеВСтоимостиТоваров);
	Группа.СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ТорговаяНаценка);
	Группа.СчетаОбесценения.Добавить(ПланыСчетов.Хозрасчетный.РезервыПодСнижениеСтоимостиТоваров);
	Группа.СчетаОтгруженных.Добавить(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные);
	
	// Строго говоря, это группа Товары, но выделена технически, так как сейчас ОтборПоАналитике привязан к группе, а не счету
	Группа = ДобавитьГруппуЗапасов(ГруппыЗапасов, НСтр("ru = 'Затраты на доставку товаров до складов организации';
														|en = 'Cost of goods delivery to the company warehouses'"));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Группа.СчетаЗатрат, СчетаУчетаЗапасов.ТранспортныеРасходы);
	Группа.ОтборПоАналитике = "ТранспортныеРасходы"; // Отбираются только затраты по статье, формирующей стоимость товаров
	
	Группа = ДобавитьГруппуЗапасов(ГруппыЗапасов, НСтр("ru = 'Незавершенное производство';
														|en = 'Work-in-progress'"), ПланыСчетов.Хозрасчетный.ОсновноеПроизводство);
	Группа.СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.Полуфабрикаты);
	Группа.СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ВспомогательныеПроизводства);
	Группа.СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ОбщепроизводственныеРасходы); // Относим к запасам, чтобы идентифицировать как оборот между видами запасов
	Группа.СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.БракВПроизводстве);
	Группа.СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ОбслуживающиеПроизводства);
	Группа.СчетаОбесценения.Добавить(ПланыСчетов.Хозрасчетный.РезервыПодСнижениеСтоимостиНезавершенногоПроизводства);
	
	Группа = ДобавитьГруппуЗапасов(ГруппыЗапасов, НСтр("ru = 'Расходы будущих периодов';
														|en = 'Deferred expenses'"));
	Группа.Вывод = Ложь;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Группа.СчетаЗатрат, СчетаУчетаЗапасов.РБП);
	Группа.ОтборПоАналитике = "РБП"; // Отбираются только статьи расходов будущих периодов с установленным отражением в балансе в составе запасов
	
	РазделятьАвансыПоВидамАктивов = РегламентированнаяОтчетность.АвансыРазделеныПоВидамАктивов(
		КонтекстЗаполненияОтчета.Организации[0],
		КонтекстЗаполненияОтчета.Период.КонецДата);
	Если РазделятьАвансыПоВидамАктивов Тогда
		Группа = ДобавитьГруппуЗапасов(ГруппыЗапасов, НСтр("ru = 'Авансы, выданные на приобретение запасов';
															|en = 'Advances issued for the purchase of inventory'"));
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Группа.СчетаЗатрат, СчетаУчетаЗапасов.Авансы);
		Группа.ОтборПоАналитике = "Авансы"; // Отбираются только авансы по договорам с установленным отражением в балансе в составе запасов
	КонецЕсли;
	
	Группа = ДобавитьГруппуЗапасов(ГруппыЗапасов, НСтр("ru = 'Иные виды запасов';
														|en = 'Other inventory owner attributes'"), ПланыСчетов.Хозрасчетный.ЗаготовлениеИПриобретениеМЦ);
	Группа.Вывод = Ложь;
	Группа.СчетаЗатрат.Добавить(ПланыСчетов.Хозрасчетный.ОтклонениеВСтоимостиМЦ);
	Группа.СчетаОбесценения.Добавить(ПланыСчетов.Хозрасчетный.РезервыПодСнижениеСтоимостиМЦ);
	// Исходя из действующих стандартов бухгалтерского учета, если такой актив и может существовать,
	// то он должен классифицироваться обособленно от запасов.
	// Однако, с учетом того, что сценариев использования этого счета сейчас нет,
	// условно отражаем данные по нему в запасах.
	Группа.СчетаОтгруженных.Добавить(ПланыСчетов.Хозрасчетный.ПереданныеОбъектыНедвижимости);
	
КонецПроцедуры

Функция ДобавитьГруппуЗапасов(ГруппыЗапасов, Наименование, Счет = Неопределено)
	
	Группа = ГруппыЗапасов.Добавить();
	
	Группа.Вывод = Истина;
	Группа.Наименование = Наименование;
	Если Счет <> Неопределено Тогда
		Группа.СчетаЗатрат.Добавить(Счет);
	КонецЕсли;
	
	Возврат Группа;
	
КонецФункции

Функция НовыйСчетаУчетаЗапасов()
	
	СчетаУчетаЗапасов = Новый Структура;
	
	СчетаУчетаЗапасов.Вставить("РБП",                СчетаУчетаРБП());
	СчетаУчетаЗапасов.Вставить("Авансы",             СчетаУчетаАвансов());
	СчетаУчетаЗапасов.Вставить("ВнеоборотныеАктивы", Новый Массив);
	
	СчетаУчетаЗапасов.Вставить("ТранспортныеРасходы",             СчетаУчетаТранспортныхРасходов()); // По модели учета затрат может учитываться часть стоимости товаров - затраты на их доставку в организацию
	СчетаУчетаЗапасов.Вставить("ТранспортныеРасходыВидАналитики", Перечисления.ВидыРасходовНУ.ТранспортныеРасходы);
	СчетаУчетаЗапасов.Вставить("ТранспортныеРасходыСтатьиЗатрат", ОписаниеСтатейЗатрат(СчетаУчетаЗапасов.ТранспортныеРасходыВидАналитики));
	
	// Заполняется в ОпределитьИспользуемыеСчетаУчетаЗапасов
	СчетаУчетаЗапасов.Вставить("ТМЦ",                Новый Массив);
	СчетаУчетаЗапасов.Вставить("Обесценение",        Новый Массив);
	
	Возврат СчетаУчетаЗапасов;

КонецФункции

Функция СчетаУчетаРБП()
	Возврат ЗаполнениеБухгалтерскойОтчетностиТабличное.СчетаПоСубконто(
		ЗаполнениеБухгалтерскойОтчетностиПереопределяемый.СчетаУчетаРасходовБудущихПериодов(),
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов);
КонецФункции

Функция СчетаУчетаАвансов()
	
	СчетаАвансов = Новый Массив;
	СчетаАвансов.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Счета", СчетаАвансов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйВидыСубконто.Ссылка КАК Ссылка
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|ГДЕ
	|	ХозрасчетныйВидыСубконто.Ссылка В ИЕРАРХИИ(&Счета)
	|	И ХозрасчетныйВидыСубконто.Ссылка.Вид = ЗНАЧЕНИЕ(ВидСчета.Активный)
	|	И ХозрасчетныйВидыСубконто.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры)
	|	И ХозрасчетныйВидыСубконто.Суммовой";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

Функция СчетаУчетаТранспортныхРасходов()
	Возврат ЗаполнениеБухгалтерскойОтчетностиТабличное.СчетаПоСубконто(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПланыСчетов.Хозрасчетный.РасходыНаПродажу),
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат);
КонецФункции

Процедура ОпределитьИспользуемыеСчетаУчетаЗапасов(СчетаУчетаЗапасов, МенеджерВременныхТаблиц, ГруппыЗапасов)
	
	СчетаУчетаТаблицей = Новый ТаблицаЗначений;
	СчетаУчетаТаблицей.Колонки.Добавить("Счет",             Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	СчетаУчетаТаблицей.Колонки.Добавить("ОтборПоАналитике", ОбщегоНазначения.ОписаниеТипаСтрока(255));
	СчетаУчетаТаблицей.Колонки.Добавить("Уровень",          Новый ОписаниеТипов("Число"));
	СчетаУчетаТаблицей.Колонки.Добавить("Группа",           Новый ОписаниеТипов("Число"));
	СчетаУчетаТаблицей.Колонки.Добавить("НазначениеСчета",  ОбщегоНазначения.ОписаниеТипаСтрока(255)); // Затраты, Обесценение, Отгруженные
	
	СчетаДругихПоказателейБаланса = Новый Соответствие;
	Для Каждого Счет Из СчетаУчетаЗапасов.ВнеоборотныеАктивы Цикл
		СчетаДругихПоказателейБаланса.Вставить(Счет, "ВнеоборотныеАктивы");
	КонецЦикла;
	
	Для Каждого Группа Из ГруппыЗапасов Цикл
		Для Каждого ИмяКолонки Из СтрРазделить("СчетаЗатрат,СчетаОбесценения,СчетаОтгруженных", ",") Цикл
			Для Каждого УстановленныйСчет Из Группа[ИмяКолонки] Цикл
				Счета = Новый Массив;
				Если ЗначениеЗаполнено(Группа.ОтборПоАналитике) Тогда
					Счета.Добавить(УстановленныйСчет); // не следует разворачивать по иерархии, так как в иерархии могут быть без вида субконто
				Иначе
					Счета = БухгалтерскийУчетПовтИсп.СчетаВИерархии(УстановленныйСчет);
				КонецЕсли;
				Для Каждого Счет Из Счета Цикл
					
					ДругойПоказательБаланса = СчетаДругихПоказателейБаланса[Счет];
					Если ДругойПоказательБаланса <> Неопределено Тогда
						Продолжить;
					КонецЕсли;
					
					ОписаниеСчета = СчетаУчетаТаблицей.Добавить();
					ОписаниеСчета.Счет = Счет;
					ОписаниеСчета.Группа = Группа.Владелец().Индекс(Группа);
					ОписаниеСчета.Уровень = УстановленныйСчет.Уровень();
					ОписаниеСчета.НазначениеСчета = "Затраты";
					Если ИмяКолонки = "СчетаОбесценения" Тогда
						ОписаниеСчета.НазначениеСчета = "Обесценение";
					ИначеЕсли ИмяКолонки = "СчетаОтгруженных" Тогда
						ОписаниеСчета.НазначениеСчета = "Отгруженные";
					КонецЕсли;
					ОписаниеСчета.ОтборПоАналитике = Группа.ОтборПоАналитике;
					
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СчетаУчетаЗапасов", СчетаУчетаТаблицей);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетаУчетаЗапасов.Счет КАК Счет,
	|	СчетаУчетаЗапасов.ОтборПоАналитике КАК ОтборПоАналитике,
	|	СчетаУчетаЗапасов.Уровень КАК Уровень,
	|	СчетаУчетаЗапасов.Группа КАК Группа,
	|	СчетаУчетаЗапасов.НазначениеСчета КАК НазначениеСчета
	|ПОМЕСТИТЬ ВТ_СчетаУчетаЗапасов
	|ИЗ
	|	&СчетаУчетаЗапасов КАК СчетаУчетаЗапасов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	Уровень
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаУчетаЗапасов.Счет КАК Счет,
	|	МАКСИМУМ(СчетаУчетаЗапасов.Уровень) КАК Уровень
	|ПОМЕСТИТЬ ВТ_УровниСчетов
	|ИЗ
	|	ВТ_СчетаУчетаЗапасов КАК СчетаУчетаЗапасов
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетаУчетаЗапасов.Счет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	Уровень
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаУчетаЗапасов.Счет КАК Счет,
	|	МАКСИМУМ(СчетаУчетаЗапасов.ОтборПоАналитике) КАК ОтборПоАналитике,
	|	МАКСИМУМ(СчетаУчетаЗапасов.ОтборПоАналитике <> """") КАК ПрименяетсяОтборПоАналитике,
	|	МАКСИМУМ(СчетаУчетаЗапасов.НазначениеСчета) КАК НазначениеСчета,
	|	МАКСИМУМ(СчетаУчетаЗапасов.НазначениеСчета = ""Обесценение"") КАК Обесценение,
	|	МИНИМУМ(СчетаУчетаЗапасов.Группа) КАК Группа
	|ПОМЕСТИТЬ ГруппыСчетов
	|ИЗ
	|	ВТ_СчетаУчетаЗапасов КАК СчетаУчетаЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УровниСчетов КАК УровниСчетов
	|		ПО СчетаУчетаЗапасов.Счет = УровниСчетов.Счет
	|			И СчетаУчетаЗапасов.Уровень = УровниСчетов.Уровень
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетаУчетаЗапасов.Счет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет,
	|	ОтборПоАналитике
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГруппыСчетов.Счет КАК Счет
	|ИЗ
	|	ГруппыСчетов КАК ГруппыСчетов
	|ГДЕ
	|	НЕ ГруппыСчетов.ПрименяетсяОтборПоАналитике";
	СчетаУчетаЗапасов.ТМЦ = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Счет");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ГруппыСчетов.Счет КАК Счет
	|ИЗ
	|	ГруппыСчетов КАК ГруппыСчетов
	|ГДЕ
	|	ГруппыСчетов.НазначениеСчета = ""Обесценение""";
	СчетаУчетаЗапасов.Обесценение = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Счет");
	
	ОбщегоНазначенияБП.УничтожитьВременныеТаблицы(МенеджерВременныхТаблиц, , "ГруппыСчетов");
	
КонецПроцедуры

Функция ОписаниеСтатейЗатрат(ВидСтатьи)
	
	ОписаниеСтатей = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидСтатьи", ВидСтатьи);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатьиЗатрат.Ссылка КАК Ссылка,
	|	СтатьиЗатрат.Представление КАК Представление,
	|	НЕ СтатьиЗатрат.ПометкаУдаления КАК Активный
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиЗатрат
	|ГДЕ
	|	СтатьиЗатрат.ВидРасходов = &ВидСтатьи
	|
	|УПОРЯДОЧИТЬ ПО
	|	СтатьиЗатрат.РеквизитДопУпорядочивания,
	|	СтатьиЗатрат.Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОписаниеСтатей.Добавить(Выборка.Ссылка, Выборка.Представление, Выборка.Активный);
	КонецЦикла;
	
	Возврат ОписаниеСтатей;
	
КонецФункции

#КонецОбласти

#Область ДанныеУчета

Процедура СоздатьДанныеУчетаЗапасов(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, СчетаУчетаЗапасов)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода", КонтекстЗаполненияОтчета.Период.Начало);
	Запрос.УстановитьПараметр("КонецПериода",  Новый Граница(КонтекстЗаполненияОтчета.Период.Конец, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организации",   КонтекстЗаполненияОтчета.Организации);
	Запрос.УстановитьПараметр("СчетаРБП",                        СчетаУчетаЗапасов.РБП);
	Запрос.УстановитьПараметр("СчетаТМЦ",                        СчетаУчетаЗапасов.ТМЦ);
	Запрос.УстановитьПараметр("СчетаТранспортныеРасходы",        СчетаУчетаЗапасов.ТранспортныеРасходы);
	Запрос.УстановитьПараметр("ТранспортныеРасходыСтатьиЗатрат", СчетаУчетаЗапасов.ТранспортныеРасходыСтатьиЗатрат);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходыБудущихПериодов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_РасходыБудущихПериодовЗапасы
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК РасходыБудущихПериодов
	|ГДЕ
	|	РасходыБудущихПериодов.ВидАктива = ЗНАЧЕНИЕ(Перечисление.ВидыАктивовДляРБП.Запасы)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОборотыТМЦ.Организация КАК Организация,
	|	ОборотыТМЦ.Счет КАК Счет,
	|	ОборотыТМЦ.СуммаОборотДт КАК СуммаОборотДт,
	|	ОборотыТМЦ.СуммаОборотКт КАК СуммаОборотКт,
	|	ОборотыТМЦ.КорСчет КАК КорСчет
	|ПОМЕСТИТЬ ВТ_Обороты
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаТМЦ), , Организация В (&Организации), , ) КАК ОборотыТМЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОборотыРБП.Организация,
	|	ОборотыРБП.Счет,
	|	ОборотыРБП.СуммаОборотДт,
	|	ОборотыРБП.СуммаОборотКт,
	|	ОборотыРБП.КорСчет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Счет В (&СчетаРБП),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов),
	|			Организация В (&Организации)
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						РБП.Ссылка
	|					ИЗ
	|						ВТ_РасходыБудущихПериодовЗапасы КАК РБП),
	|			,
	|			) КАК ОборотыРБП
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОборотыТранспортныеРасходы.Организация,
	|	ОборотыТранспортныеРасходы.Счет,
	|	ОборотыТранспортныеРасходы.СуммаОборотДт,
	|	ОборотыТранспортныеРасходы.СуммаОборотКт,
	|	ОборотыТранспортныеРасходы.КорСчет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Счет В (&СчетаТранспортныеРасходы),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат),
	|			Организация В (&Организации)
	|				И Субконто1 В (&ТранспортныеРасходыСтатьиЗатрат),
	|			,
	|			) КАК ОборотыТранспортныеРасходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Начало"" КАК Характеристика,
	|	НачалоТМЦ.Организация КАК Организация,
	|	НачалоТМЦ.Счет КАК Счет,
	|	НачалоТМЦ.СуммаОстаток КАК Сумма,
	|	NULL КАК КорСчет
	|ПОМЕСТИТЬ ДанныеУчетаЗапасов
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет В (&СчетаТМЦ), , Организация В (&Организации)) КАК НачалоТМЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Конец"",
	|	КонецТМЦ.Организация,
	|	КонецТМЦ.Счет,
	|	КонецТМЦ.СуммаОстаток,
	|	NULL
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецПериода, Счет В (&СчетаТМЦ), , Организация В (&Организации)) КАК КонецТМЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Начало"",
	|	НачалоРБП.Организация,
	|	НачалоРБП.Счет,
	|	НачалоРБП.СуммаОстаток,
	|	NULL
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&НачалоПериода,
	|			Счет В (&СчетаРБП),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов),
	|			Организация В (&Организации)
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						РБП.Ссылка
	|					ИЗ
	|						ВТ_РасходыБудущихПериодовЗапасы КАК РБП)) КАК НачалоРБП
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Конец"",
	|	КонецРБП.Организация,
	|	КонецРБП.Счет,
	|	КонецРБП.СуммаОстаток,
	|	NULL
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&КонецПериода,
	|			Счет В (&СчетаРБП),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов),
	|			Организация В (&Организации)
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						РБП.Ссылка
	|					ИЗ
	|						ВТ_РасходыБудущихПериодовЗапасы КАК РБП)) КАК КонецРБП
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Начало"",
	|	НачалоТранспортныеРасходы.Организация,
	|	НачалоТранспортныеРасходы.Счет,
	|	НачалоТранспортныеРасходы.СуммаОстаток,
	|	NULL
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&НачалоПериода,
	|			Счет В (&СчетаТранспортныеРасходы),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат),
	|			Организация В (&Организации)
	|				И Субконто1 В (&ТранспортныеРасходыСтатьиЗатрат)) КАК НачалоТранспортныеРасходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Конец"",
	|	КонецТранспортныеРасходы.Организация,
	|	КонецТранспортныеРасходы.Счет,
	|	КонецТранспортныеРасходы.СуммаОстаток,
	|	NULL
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&КонецПериода,
	|			Счет В (&СчетаТранспортныеРасходы),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат),
	|			Организация В (&Организации)
	|				И Субконто1 В (&ТранспортныеРасходыСтатьиЗатрат)) КАК КонецТранспортныеРасходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Дт"",
	|	Обороты.Организация,
	|	Обороты.Счет,
	|	Обороты.СуммаОборотДт,
	|	Обороты.КорСчет
	|ИЗ
	|	ВТ_Обороты КАК Обороты
	|ГДЕ
	|	Обороты.СуммаОборотДт <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Кт"",
	|	Обороты.Организация,
	|	Обороты.Счет,
	|	-Обороты.СуммаОборотКт,
	|	Обороты.КорСчет
	|ИЗ
	|	ВТ_Обороты КАК Обороты
	|ГДЕ
	|	Обороты.СуммаОборотКт <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""СправкаРасчетВыбытиеОбесцененияЗапасов"",
	|	Обороты.Организация,
	|	Обороты.Счет,
	|	Обороты.ВыбытиеОбесценения,
	|	NULL
	|ИЗ
	|	ВыбытиеОбесцененияЗапасов КАК Обороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Авансы.Характеристика,
	|	Авансы.Организация,
	|	Авансы.Счет,
	|	Авансы.Аванс,
	|	NULL
	|ИЗ
	|	АвансыВыданные КАК Авансы";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВыбытиеОбесцененияЗапасов(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, СчетаОбесценения)
	
	ВыбытиеОбесцененияЗапасов = Новый ТаблицаЗначений;
	ВыбытиеОбесцененияЗапасов.Колонки.Добавить("Организация",        Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ВыбытиеОбесцененияЗапасов.Колонки.Добавить("Счет",               Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	ВыбытиеОбесцененияЗапасов.Колонки.Добавить("ВыбытиеОбесценения", БухгалтерскаяОтчетностьБРО.ТипСумма());
	
	ЗаполнитьВыбытиеОбесцененияЗапасов(ВыбытиеОбесцененияЗапасов, КонтекстЗаполненияОтчета, СчетаОбесценения);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВыбытиеОбесцененияЗапасов", ВыбытиеОбесцененияЗапасов);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВыбытиеОбесцененияЗапасов.Организация КАК Организация,
	|	ВыбытиеОбесцененияЗапасов.Счет КАК Счет,
	|	ВыбытиеОбесцененияЗапасов.ВыбытиеОбесценения КАК ВыбытиеОбесценения
	|ПОМЕСТИТЬ ВыбытиеОбесцененияЗапасов
	|ИЗ
	|	&ВыбытиеОбесцененияЗапасов КАК ВыбытиеОбесцененияЗапасов";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ЗаполнитьВыбытиеОбесцененияЗапасов(ВыбытиеОбесцененияЗапасов, КонтекстЗаполненияОтчета, СчетаОбесценения)
	
	Если Не ВыбытиеОбесцененияОпределяетсяПоСправкеРасчету(КонтекстЗаполненияОтчета) Тогда
		Возврат;
	КонецЕсли;
		
	Для Каждого СчетОбесценения Из СчетаОбесценения Цикл
		
		Если БухгалтерскийУчетПовтИсп.СчетаВИерархии(СчетОбесценения).Количество() > 1 Тогда
			// РассчитатьВыбытиеОбесцененияЗапасов опирается на иерархию счетов, а в данном случае этого не нужно делать
			Продолжить;
		КонецЕсли;
		
		Для Каждого Организация Из КонтекстЗаполненияОтчета.Организации Цикл
		
			ВыбытиеОбесценения = Неопределено;
			// В ERP заполнение выбытия обесценения
				
			Если ТипЗнч(ВыбытиеОбесценения) <> Тип("Число") Тогда
				Продолжить;
			КонецЕсли;
			
			Запись = ВыбытиеОбесцененияЗапасов.Добавить();
			Запись.Организация        = Организация;
			Запись.Счет               = СчетОбесценения;
			Запись.ВыбытиеОбесценения = ВыбытиеОбесценения;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВыбытиеОбесцененияОпределяетсяПоСправкеРасчету(КонтекстЗаполненияОтчета)
	
	ФункциональностьДоступна = Ложь;
	
		
	Возврат ФункциональностьДоступна;
	
КонецФункции

Процедура СоздатьАвансыВыданные(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, СчетаАвансы)
	
	АвансыВыданные = Новый ТаблицаЗначений;
	АвансыВыданные.Колонки.Добавить("Организация",    Новый ОписаниеТипов("СправочникСсылка.Организации"));
	АвансыВыданные.Колонки.Добавить("Счет",           Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	АвансыВыданные.Колонки.Добавить("Характеристика", ОбщегоНазначения.ОписаниеТипаСтрока(255));
	АвансыВыданные.Колонки.Добавить("Аванс",          БухгалтерскаяОтчетностьБРО.ТипСумма());
	
	// В ERP авансы по видам актива не выделяются
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("АвансыВыданные", АвансыВыданные);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АвансыВыданные.Организация КАК Организация,
	|	АвансыВыданные.Счет КАК Счет,
	|	АвансыВыданные.Характеристика КАК Характеристика,
	|	АвансыВыданные.Аванс КАК Аванс
	|ПОМЕСТИТЬ АвансыВыданные
	|ИЗ
	|	&АвансыВыданные КАК АвансыВыданные";
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#Область Расшифровка

Процедура ДобавитьНастройкуОтчетаОборотыПоВсемКорСчетам(ЗначенияНастроекОтчета)
	ЗначенияНастроекОтчета.КорСчет = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	ЗначенияНастроекОтчета.ИмяОтчета = "АнализСчета";
	ЗначенияНастроекОтчета.ПоСубсчетамКорСчетов = Истина;
КонецПроцедуры

Процедура УстановитьОтборПоАналитике(ЗначенияНастроекОтчета, Выборка)

	Если ЗначениеЗаполнено(Выборка.ОтборПоАналитике) Тогда
		ШаблонПоляОтбора = "";
		ВидСубконтоОтбора = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка();
		ЗначениеОтбора = Неопределено;
		Если Выборка.ОтборПоАналитике = "ТранспортныеРасходы" Тогда
			ЗначениеОтбора = Перечисления.ВидыРасходовНУ.ТранспортныеРасходы;
			ШаблонПоляОтбора = "Субконто%1.ВидРасходов";
			ВидСубконтоОтбора = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат;
		ИначеЕсли Выборка.ОтборПоАналитике = "РБП" Тогда
			ЗначениеОтбора = Перечисления.ВидыАктивовДляРБП.Запасы;
			ШаблонПоляОтбора = "Субконто%1.ВидАктива";
			ВидСубконтоОтбора = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыБудущихПериодов;
		КонецЕсли;

		ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастроитьОтборПоВидуСубконто(
			ЗначенияНастроекОтчета, ШаблонПоляОтбора, Выборка.Счет, ВидСубконтоОтбора, ЗначениеОтбора);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Функция НачатьДобавлениеСтрокиЗапасы(ВыводТаблицы, ГруппаЗапасов, ШаблоныКолонок, ПараметрыТекстовРасшифровки)
	
	НоваяСтрока = ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСтрокуТаблицыОтчета(
		ВыводТаблицы,
		ШаблоныКолонок,
		ПараметрыТекстовРасшифровки);
	
	НоваяСтрока.Группа = ГруппаЗапасов.Наименование;
	
	Возврат НоваяСтрока;
	
КонецФункции

Процедура НачатьДобавлениеСтрокиЗапасыНаличиеДвижение(ВыводТаблицы, ГруппаЗапасов, ШаблоныКолонок, ПараметрыТекстовРасшифровки)
	
	НачатьДобавлениеСтрокиЗапасы(ВыводТаблицы, ГруппаЗапасов, ШаблоныКолонок, ПараметрыТекстовРасшифровки);
	
КонецПроцедуры

Процедура НачатьДобавлениеСтрокиЗапасыОграничения(ВыводТаблицы, ГруппаЗапасов, ШаблоныКолонок, ПараметрыТекстовРасшифровки, КлассификаторОграничение)
	
	// Используем тот же шаблон, что для запасов, но с другим перечнем счетов.
	// Для простоты не распределяем обесценение на отгруженные запасы.
	СохраненныеПараметрыТекстов = Новый Структура;
	СохраненныеПараметрыТекстов.Вставить("СчетамБалансовойСтоимости", "");
	
	ПараметрыТекстовРасшифровки.СчетамБалансовойСтоимости = ПараметрыТекстовРасшифровки.СчетуОтгруженных;
	
	НоваяСтрока = НачатьДобавлениеСтрокиЗапасы(ВыводТаблицы, ГруппаЗапасов, ШаблоныКолонок, ПараметрыТекстовРасшифровки);
	
	НоваяСтрока.Ограничение = КлассификаторОграничение.ВПути;
	
	ЗаполнитьЗначенияСвойств(ПараметрыТекстовРасшифровки, СохраненныеПараметрыТекстов);
	
КонецПроцедуры

Функция ДобавитьСлагаемоеСчет(ВыводТаблицы, ИмяКолонки, Выборка, ЗначенияНастроекОтчета)
	
	ПериодОтчета = ВыводТаблицы.КонтекстЗаполненияОтчета.Период;
	Сумма              = Выборка.Сумма;
	ПредметЗаписи      = Выборка.Счет;
	ПредставлениеСчета = Строка(ПредметЗаписи);
	Отчет = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастройкиОтчетаПоЗначениям(ЗначенияНастроекОтчета, ПериодОтчета);
	
	Возврат ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(ВыводТаблицы, ИмяКолонки, ПредставлениеСчета, Сумма, ПредметЗаписи, Отчет);
	
КонецФункции

Функция НовыйВыводОборотовСчета(ЗапасыНаличиеДвижение)
	
	// Выводится в виде записей такого вида:
	// Дт 26
	//       Кт 70
	//       Кт 60
	
	ВыводОборотовСчета = Новый Структура;
	
	ВыводОборотовСчета.Вставить("ВыводТаблицы",       ЗапасыНаличиеДвижение);
	ВыводОборотовСчета.Вставить("СчетаВыведеныИтоги", Новый Соответствие);
	ВыводОборотовСчета.Вставить("Исключения",         Новый Соответствие); // Ключ - Строка, вид исключения; Значение - Массив из НовыйИсключениеОборотов
	
	Возврат ВыводОборотовСчета;
	
КонецФункции

Функция ДобавитьСлагаемоеОборотМеждуСчетами(ВыводОборотовСчета, ИмяКолонки, Выборка)
	
	ПериодОтчета = ВыводОборотовСчета.ВыводТаблицы.КонтекстЗаполненияОтчета.Период;
	ВыведенИтог = (ВыводОборотовСчета.СчетаВыведеныИтоги[Выборка.Счет] = Истина);
	ЗначенияНастроекОтчета = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.ЗначенияНастроекОтчетаЗапасы(Выборка);
	Если Не ВыведенИтог Тогда
		
		Счет = Выборка.Счет;
		Описание = ПредставлениеСтороныПроводки(Счет, Выборка.Характеристика);
		
		ЗначенияНастроекОтчетаИерархия = ОбщегоНазначения.СкопироватьРекурсивно(ЗначенияНастроекОтчета);
		ДобавитьНастройкуОтчетаОборотыПоВсемКорСчетам(ЗначенияНастроекОтчетаИерархия);
		Отчет = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастройкиОтчетаПоЗначениям(ЗначенияНастроекОтчетаИерархия, ПериодОтчета);
		ДобавитьИерархияСлагаемых(ВыводОборотовСчета.ВыводТаблицы, ИмяКолонки, Описание, Счет, Отчет);
		
		ВыводОборотовСчета.СчетаВыведеныИтоги.Вставить(Выборка.Счет, Истина);
		
	КонецЕсли;
	
	Счет = Выборка.КорСчет;
	СторонаПроводки = "Дт";
	Если Выборка.Характеристика = "Дт" Тогда
		СторонаПроводки = "Кт";
	КонецЕсли;
	
	Описание = ПредставлениеСтороныПроводки(Счет, СторонаПроводки);
	
	Отчет = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастройкиОтчетаПоЗначениям(ЗначенияНастроекОтчета, ПериодОтчета);
	Слагаемое = ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(ВыводОборотовСчета.ВыводТаблицы, ИмяКолонки, Описание, Выборка.Сумма, Счет, Отчет, 1);
	
	Если Не ПустаяСтрока(Выборка.Исключение) Тогда
		ИсключениеОборотов = НовыйИсключениеОборотов();
		ИсключениеОборотов.ХарактеристикаСчета = Выборка.Характеристика;
		ИсключениеОборотов.Счет                = Выборка.Счет;
		ИсключениеОборотов.КорСчет             = Выборка.КорСчет;
		ИсключениеОборотов.Слагаемое           = Слагаемое;
		ОбщегоНазначенияБПКлиентСервер.ДополнитьРазделенныйМассив(ВыводОборотовСчета.Исключения, Выборка.Исключение, ИсключениеОборотов);
	КонецЕсли;
	
	Возврат Слагаемое;
	
КонецФункции

Функция НовыйИсключениеОборотов()
	
	ИсключениеОборотов = Новый Структура;
	
	ИсключениеОборотов.Вставить("ХарактеристикаСчета", "Дт"); // Дт или Кт
	ИсключениеОборотов.Вставить("Счет",                ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	ИсключениеОборотов.Вставить("КорСчет",             ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	ИсключениеОборотов.Вставить("Слагаемое"); // Запись расшифровки
	
	Возврат ИсключениеОборотов;
	
КонецФункции

Процедура ВывестиИсключенияПоСчету(ВыводОборотовСчета, ВидИсключения, ОсновнаяКолонка, КолонкаИсключения = "")
	
	ИсключенияПоВиду = ВыводОборотовСчета.Исключения[ВидИсключения];
	Если ИсключенияПоВиду = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидИсключения  = "ВнутриГруппы" Тогда
		Описание = СтрШаблон(НСтр("ru = 'Исключены обороты внутри группы запасов';
									|en = 'Turnovers within the inventory group are excluded'"));
	ИначеЕсли ВидИсключения  = "ВнутриЗапасов" Тогда
		Описание = СтрШаблон(НСтр("ru = 'Исключены обороты, связанные с изменением вида запасов';
									|en = 'Turnovers related to a change in inventory owner attributes are excluded'"));
	ИначеЕсли ВидИсключения  = "СписаноОбесценение" Тогда
		Описание = СтрШаблон(НСтр("ru = 'Исключена корректировка резерва под обесценение в связи со списанием запасов';
									|en = 'Adjustment of provision for impairment due to inventory write-off is excluded'"));
	Иначе
		Возврат;
	КонецЕсли;
	
	УровеньРасшифровки = 1; // На уровне кор. счетов - т.е. на уровень ниже счета
	
	ДобавитьИерархияСлагаемых(ВыводОборотовСчета.ВыводТаблицы, ОсновнаяКолонка, Описание, , , УровеньРасшифровки);
	
	УровеньРасшифровки = УровеньРасшифровки + 1; // На уровень ниже иерархии слагаемых
	
	Для Каждого ИсключениеОборотов Из ИсключенияПоВиду Цикл
		СуммаПроводки = ИсключениеОборотов.Слагаемое.Сумма;
		ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(
			ВыводОборотовСчета.ВыводТаблицы,
			ОсновнаяКолонка,
			ИсключениеОборотов.Слагаемое.Описание,
			-СуммаПроводки, ИсключениеОборотов.Слагаемое.ПредметЗаписи,
			ИсключениеОборотов.Слагаемое.Отчет,
			УровеньРасшифровки);
		Если ЗначениеЗаполнено(КолонкаИсключения) Тогда
			ТекстПроводки = ПредставлениеПроводки(ИсключениеОборотов.ХарактеристикаСчета, ИсключениеОборотов.Счет, ИсключениеОборотов.КорСчет);
			ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(ВыводОборотовСчета.ВыводТаблицы, КолонкаИсключения, ТекстПроводки, СуммаПроводки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьИерархияСлагаемых(ВыводТаблицы, ИмяКолонки, Описание, ПредметЗаписи = Неопределено, Отчет = Неопределено, Уровень = 0)
	
	Слагаемое = ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(ВыводТаблицы, ИмяКолонки, Описание, 0, ПредметЗаписи, Отчет, Уровень);
	Слагаемое.ТипЗаписи = "ИерархияСлагаемых";
	
КонецПроцедуры

Процедура ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки, ИмяКолонки, ШаблонЗаголовкаКолонки, ШаблонПорядкаРасчета)
	
	ШаблоныКолонок.Вставить(ИмяКолонки, ШаблонЗаголовкаКолонки);
	
	ШаблонЗаголовковРасшифровки = ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки();
	
	ШаблонЗаголовковРасшифровки.ЗаголовокГруппы = НСтр("ru = '[НаименованиеГруппы]: [ПредставлениеКолонки]';
														|en = '[НаименованиеГруппы]: [ПредставлениеКолонки]'");
	ШаблонЗаголовковРасшифровки.ПорядокРасчета  = ШаблонПорядкаРасчета;
	
	ШаблоныРасшифровки.Вставить(ИмяКолонки, ШаблонЗаголовковРасшифровки);
	
КонецПроцедуры

Функция ПредставлениеСтороныПроводки(Счет, СторонаПроводки)
	
	ПредставлениеСчета = Строка(Счет);
	
	ПредставлениеСтороныПроводки = НСтр("ru = 'Дт';
										|en = 'Dr'");
	Если СторонаПроводки = "Кт" Тогда
		ПредставлениеСтороныПроводки = НСтр("ru = 'Кт';
											|en = 'Cr'");
	КонецЕсли;
	
	Возврат СтрШаблон(НСтр("ru = '%1 %2';
							|en = '%1 %2'"), ПредставлениеСтороныПроводки, ПредставлениеСчета);
	
КонецФункции

Функция ПредставлениеПроводки(ХарактеристикаСчета, Счет, КорСчет)
	
	ДтКт = Новый Структура;
	ДтКт.Вставить("Дт", Счет);
	ДтКт.Вставить("Кт", КорСчет);
	Если ХарактеристикаСчета = "Кт" Тогда
		ДтКт.Дт = КорСчет;
		ДтКт.Кт = Счет;
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(НСтр("ru = 'Дт [Дт] Кт [Кт]';
																		|en = '[Дт] Dr [Кт] Cr'"), ДтКт);
	
КонецФункции

#КонецОбласти
