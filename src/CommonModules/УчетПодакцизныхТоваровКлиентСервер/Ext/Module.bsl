
#Область СлужебныйПрограммныйИнтерфейс

// Получает текст заголовка поля облагается акцизом
//
// Параметры:
//  КоличествоПодакцизныхТоваров - Число
//
// Возвращаемое значение:
//  Строка
//
Функция ЗаголовокПоляОблагаетсяАкцизом(КоличествоПодакцизныхТоваров) Экспорт
	
	ТекстЗаголовка = НСтр("ru = 'Подакцизный товар (%1) произведен организацией и облагается акцизом';
							|en = 'Excisable goods (%1) are produced by the company and are subject to excise'");
	
	ПараметрыПрописи = НСтр("ru = 'позиция, позиции, позиций, ж, , , , , 0';
							|en = 'position,positions,,,0'");
	ПараметрыПрописиТолькоЧисло = НСтр("ru = ', , , ж, , , , , 0';
										|en = ', , , , , , 0'");
	КоличествоПодакцизныхТоваровПрописью = СтрЗаменить(
		ЧислоПрописью(КоличествоПодакцизныхТоваров, , ПараметрыПрописи),
		СокрП(ЧислоПрописью(КоличествоПодакцизныхТоваров, , ПараметрыПрописиТолькоЧисло)),
		Формат(КоличествоПодакцизныхТоваров, "ЧН=0; ЧГ=;"));
	
	ТекстЗаголовка = СтрШаблон(ТекстЗаголовка, КоличествоПодакцизныхТоваровПрописью);
	
	Возврат ТекстЗаголовка;
	
КонецФункции

// Настраивает поле подразделение производства
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//
Процедура НастроитьПолеПодразделениеПроизводства(Форма) Экспорт
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Не Объект.ОблагаетсяАкцизом Тогда
		Объект.ПодразделениеПроизводства = Неопределено;
	КонецЕсли;
	
	Элементы.ПодразделениеПроизводства.Доступность = Объект.ОблагаетсяАкцизом;
	Элементы.ПодразделениеПроизводства.АвтоОтметкаНезаполненного = ?(Объект.ОблагаетсяАкцизом, Неопределено, Ложь);
	
КонецПроцедуры

#КонецОбласти
