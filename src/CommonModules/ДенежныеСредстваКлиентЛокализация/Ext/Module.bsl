#Область ПрограммныйИнтерфейс

//++ Локализация

//++ НЕ УТ

// Формирует список варианто заявлений на печать лицевых счетов. 
// Вызывается из УправлениеПечатьюСлужебныйКлиент.ВыполнитьПодключаемуюКомандуПечатиЗавершение.
// 
// Параметры:
//  ПараметрыПечати - Структура - Параметры печати
// 
// Возвращаемое значение:
//  Неопределено
//
Функция ВыбратьВариантЗаявления(ПараметрыПечати) Экспорт

	ВидыЗаявлений = Новый СписокЗначений;
	ВидыЗаявлений.Вставить(0, "Открытие", НСтр("ru = 'Открытие';
												|en = 'Open'"));
	ВидыЗаявлений.Вставить(1, "Резервирование", НСтр("ru = 'Резервирование';
													|en = 'Assignment'"));
	ВидыЗаявлений.Вставить(2, "Закрытие", НСтр("ru = 'Закрытие';
												|en = 'Closing'"));
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПараметрыПечати", ПараметрыПечати);
	ИмяМетода = "ВыбратьВариантЗаявленияЗавершение";
	ОповещениеПослеВыбораЗаявления = Новый ОписаниеОповещения(ИмяМетода, ЭтотОбъект, ДополнительныеПараметры);
	
	ВидыЗаявлений.ПоказатьВыборЭлемента(ОповещениеПослеВыбораЗаявления, НСтр("ru = 'Виды заявлений';
																			|en = 'Application kinds'"));
	
	Возврат Неопределено;

КонецФункции

//-- НЕ УТ

//-- Локализация

// Выполняет проверку документа "Приобретение товаров и услуг" и "Приобретение услуг и прочих активов" перед загрузкой 
// чека самозанятого.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа приобретения.
//
Процедура ПередЗагрузкойЧекаСамозанятогоВДокумент(Форма) Экспорт

	//++ Локализация
	Объект = Форма.Объект; // ДанныеФормыСтруктура
	
	ТекстПередЗаписью = "";
	
	ДополнительныеПараметры = ПараметрыЗагрузкиЧекаСамозанятого();
	
	ДополнительныеПараметры.Вставить("Форма", Форма);
	
	ТаблицаЧеков = Форма.ЧекиСамозанятого;
	ДанныеЧека = НовыеДанныеЧека();
	
	Если ТаблицаЧеков.Количество() <> 0 Тогда
		
		СтрокаТаблицы = ТаблицаЧеков.Получить(0);
		ЗаполнитьЗначенияСвойств(ДанныеЧека, СтрокаТаблицы);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ДополнительныеПараметры.Вставить("Записывать", Истина);
		ТекстПередЗаписью = НСтр("ru = 'Документ будет записан в процессе загрузки чека.';
								|en = 'The document will be saved when importing the receipt.'");
		
	ИначеЕсли ЗначениеЗаполнено(Объект.Ссылка)
		И Форма.Модифицированность Тогда
		
		ДополнительныеПараметры.Вставить("Записывать", Истина);
		ТекстПередЗаписью = НСтр("ru = 'Данные изменены и будут записаны в процессе загрузки чека.';
								|en = 'The data is changed and will be saved when importing the receipt.'");
		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекстПередЗаписью) Тогда
		ТекстВопроса = СтрШаблон(Нстр("ru = '%1
											|Продолжить загрузку чека?';
											|en = '%1
											|Continue the receipt import?'"), ТекстПередЗаписью);
	Иначе
		
		ДанныеЧека.Вставить("ДокументВладелец", Объект.Ссылка);
		Оповещение = Новый ОписаниеОповещения("ОткрытьФормуЧекаСамозанятого", ЭтотОбъект, ДополнительныеПараметры);
		ВыполнитьОбработкуОповещения(Оповещение, ДанныеЧека);
		
		Возврат;
		
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ТекущиеДанные", ДанныеЧека);
	ОповещенияОЗавершении = Новый ОписаниеОповещения(
		"ПередЗагрузкойЧекаСамозанятогоВДокументЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры);
	Кнопки = РежимДиалогаВопрос.ДаНет;
	
	ПоказатьВопрос(ОповещенияОЗавершении, ТекстВопроса, Кнопки);
	//-- Локализация
	
	Возврат;

КонецПроцедуры

// Возвращает признак иностранного банка по данным счета.
//
// Параметры:
//  МеждународныеРеквизитыБанковскихСчетов - Булево - настройка "Международные реквизиты банковских счетов".
//  ДанныеСчета - см. ДенежныеСредстваКлиентСервер.НовыеДанныеБанковскогоСчета.
//
// Возвращаемое значение:
//   Булево
//
Функция ЭтоИностранныйБанк(МеждународныеРеквизитыБанковскихСчетов, ДанныеСчета) Экспорт

	//++ Локализация
	Если Не ДанныеСчета.ЭтоIBAN
		И ((ДенежныеСредстваКлиентСерверЛокализация.СтрокаСоответствуетФорматуБанковскогоСчетаРФ(ДанныеСчета.НомерСчета)
		И Не ЗначениеЗаполнено(ДанныеСчета.СтранаБанка)
		И Не МеждународныеРеквизитыБанковскихСчетов)
		Или ДанныеСчета.СтранаБанка = ПредопределенноеЗначение("Справочник.СтраныМира.Россия")) Тогда
		Возврат Ложь;
	КонецЕсли;
	//-- Локализация
	
	Возврат Истина;

КонецФункции

// Выполняет проверку номера счета.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой выполняется проверка.
//  Суффикс - Строка - дополнение к наименованию реквизита на форме.
//  ДанныеСчета - см. ДенежныеСредстваКлиентСервер.НовыеДанныеБанковскогоСчета.
//
Процедура ПроверитьНомерСчета(Форма, Суффикс, ДанныеСчета)Экспорт

	//++ Локализация
	ДенежныеСредстваКлиентСерверЛокализация.ПроверитьФорматСчетаРФ(Форма, Суффикс, ДанныеСчета);
	//-- Локализация
	
	Возврат;

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПередСозданиемБанковскогоСчета(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат
	КонецЕсли;
	ОткрытьСозданиеБанковскогоСчетаЗавершение(ДополнительныеПараметры.Владелец, Результат, ДополнительныеПараметры.ОписаниеОповещенияОЗакрытииФормыСчета);
	
КонецПроцедуры

Функция ОткрытьРезультатОплатыСтрокГрафика(ЭлементФормы, Результат, Оповещение) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	Если Результат.ОткрыватьФормуПомощника И Результат.Свойство("ТребуетсяКонтрольГОЗ") Тогда
		
		СтруктураПараметры = Новый Структура;
		СтруктураПараметры.Вставить("ТребуетсяКонтрольГОЗ", Истина);
		СтруктураПараметры.Вставить("АдресСтрокГрафика", Результат.АдресСтрокГрафика);
		
		ОткрытьФорму("ОбщаяФорма.ПомощникФормированияПлатежныхДокументов",
			СтруктураПараметры,
			ЭлементФормы,,,,
			Оповещение,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		Возврат Истина;
	КонецЕсли;
	//-- НЕ УТ

	//-- Локализация
	
	Возврат Ложь;
	
КонецФункции

// Открывает форму создания банковского счета организации или контрагента
//
// Параметры:
//  Список          - ДинамическийСписок - список элементов справочника
//  Команда         - КомандаФормы - вызываемая команда создания.
//  СчетОрганизации - Булево - признак открытия справочника банковских счетов организаций
//                              при значении равным Ложь будет произведено открытие
//                              формы справочника банковских счетов контрагентов.
//
Процедура ОткрытьСозданиеБанковскогоСчета(Список, Команда, СчетОрганизации = Истина) Экспорт
	
	СозданиеСчетаЦифровогоРубля = Ложь;
	//++ Локализация
	СозданиеСчетаЦифровогоРубля = Команда.Имя = "СоздатьСчетЦифровогоРубля";
	//-- Локализация
	
	ВладелецСчета = ?(СчетОрганизации,
	                  ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"),
	                  ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));

	Для Каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл
		Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Владелец") И ЭлементОтбора.Использование
			И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
			ВладелецСчета = ЭлементОтбора.ПравоеЗначение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ВладелецСчета) Тогда
		
		Для Каждого ЭлементПараметра Из Список.Параметры.Элементы Цикл
			
			Если ЭлементПараметра.Параметр = Новый ПараметрКомпоновкиДанных("Владелец")
				И ЭлементПараметра.Использование
				И ТипЗнч(ЭлементПараметра.Значение) <> Тип("Массив")
				И ТипЗнч(ЭлементПараметра.Значение) <> Тип("СписокЗначений") Тогда
				ВладелецСчета = ЭлементПараметра.Значение;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;
	
	ОткрытьСозданиеБанковскогоСчетаЗавершение(ВладелецСчета, СозданиеСчетаЦифровогоРубля);
	
КонецПроцедуры

// Открывает форму создания банковского счета организации или контрагента
//
// Параметры:
//  Владелец             - СправочникСсылка.Организации,
//                         СправочникСсылка.Контрагенты,
//                         СправочникСсылка.ФизическиеЛица - список элементов справочника.
//  СтандартнаяОбработка - Булево - признак использования стандартной обработки.
//  ФормаДокумента       - ФормаКлиентскогоПриложения - форма, из которой вызывается создание банковского счета
//  ИмяЗаполняемогоРеквизита - Строка - имя реквизита объекта, в который должен быть записан вновь созданный банковский счет
//
Процедура НачатьСозданиеБанковскогоСчета(ВладелецСчета, СтандартнаяОбработка, ФормаДокумента, ИмяЗаполняемогоРеквизита) Экспорт
	
	//++ Локализация
	СтандартнаяОбработка = Ложь;
	
	ДополнительныеПараметрыЗакрытия = Новый Структура;
	ДополнительныеПараметрыЗакрытия.Вставить("ФормаДокумента", ФормаДокумента);
	ДополнительныеПараметрыЗакрытия.Вставить("СсылкаНаСчет", Неопределено);
	ДополнительныеПараметрыЗакрытия.Вставить("ИмяЗаполняемогоРеквизита", ИмяЗаполняемогоРеквизита);
	ОписаниеОповещенияОЗакрытииФормыСчета = Новый ОписаниеОповещения("ПослеЗакрытияФормыСозданияСчета",
	                                                                 ЭтотОбъект,
	                                                                 ДополнительныеПараметрыЗакрытия);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Владелец", ВладелецСчета);
	ДополнительныеПараметры.Вставить("ОписаниеОповещенияОЗакрытииФормыСчета", ОписаниеОповещенияОЗакрытииФормыСчета);
	ОписаниеОповещенияПередСозданием = Новый ОписаниеОповещения("ПередСозданиемБанковскогоСчета",
	                                                            ЭтотОбъект,
	                                                            ДополнительныеПараметры);
	
	КомандыСоздания = Новый СписокЗначений;
	КомандыСоздания.Добавить(Ложь,   НСтр("ru = 'Банковский счет';
											|en = 'Bank account'"));
	КомандыСоздания.Добавить(Истина, НСтр("ru = 'Счет цифрового рубля';
											|en = 'Digital rouble account'"));
	
	ПоказатьВопрос(ОписаниеОповещенияПередСозданием,
	               НСтр("ru = 'Какой счет нужно создать?';
						|en = 'What account to create?'"),
	               КомандыСоздания);
	//-- Локализация
	
Конецпроцедуры

// Добавляет в дополнительные параметры оповещения о закрытии формы ссылку на элемент справочника.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма элемента справочников БанковскиеСчетаОрганизаций, БанковскиеСчетаКонтрагентов.
//
Процедура ПередЗакрытиемФормыБанковскогоСчета(Форма) Экспорт
	
	//++ Локализация
	Если Форма.ОписаниеОповещенияОЗакрытии = Неопределено 
		 ИЛИ Форма.ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Форма.ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры.Свойство("СсылкаНаСчет") Тогда
		Форма.ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры.СсылкаНаСчет = Форма.Объект.Ссылка;
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

//++ Локализация

Процедура ПослеЗакрытияФормыСозданияСчета(Результат, ДополнительныеПараметры) Экспорт
	
	СсылкаНаСозданныйСчет = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "СсылкаНаСчет", Неопределено);
	Если Не ЗначениеЗаполнено(СсылкаНаСозданныйСчет) Тогда
		Возврат;
	КонецЕсли;
	ФормаДокумента = ДополнительныеПараметры.ФормаДокумента;
	ФормаДокумента.Объект[ДополнительныеПараметры.ИмяЗаполняемогоРеквизита] = СсылкаНаСозданныйСчет;
	
КонецПроцедуры

#Область РаботаСФайлами

Процедура ОткрытьФормуСпискаФайловДляПередачиВБанк(Форма) Экспорт
	
	Обработчик = Новый ОписаниеОповещения("ОткрытьФормуСпискаФайловДляПередачиВБанкЗавершение", ЭтотОбъект, Форма);
	
	Если Форма.Объект.Ссылка.Пустая() Тогда
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
			|Переход к файлам возможен только после записи данных.
			|Данные будут записаны.';
			|en = 'Data is not written yet.
			|You can use files only after data is written.
			|Data will be written.'");
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	Иначе
		ВыполнитьОбработкуОповещения(Обработчик, КодВозвратаДиалога.Пропустить);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормуСпискаФайловДляПередачиВБанкЗавершение(Ответ, Форма) Экспорт
	
	Если Ответ = КодВозвратаДиалога.ОК Тогда
		Форма.Записать();
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВладелецФайла",  Форма.Объект.Ссылка);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Форма.ТолькоПросмотр);
	
	Отбор = Новый Структура;
	Отбор.Вставить("ДляПередачиВБанк", Истина);
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму("Обработка.РаботаСФайлами.Форма.ПрисоединенныеФайлы",
		ПараметрыФормы,
		Форма,
		Истина,
		Форма.Окно);
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ

// Выбрать вариант заявления завершение.
// 
// Параметры:
//  ВыбранноеЗначение - ЭлементСпискаЗначений - выбранное значение.
//  ДополнительныеПараметры - Структура:
//   * ПараметрыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ВыбратьВариантЗаявленияЗавершение(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		
		Параметры = ДополнительныеПараметры.ПараметрыПечати; // Структура
		ПараметрыПечати = Новый Структура;
		ПараметрыПечати.Вставить("ВариантЗаявления", ВыбранноеЗначение.Значение);
		
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			Параметры.МенеджерПечати,
			Параметры.Идентификатор,
			Параметры.ОбъектыПечати,
			Параметры.Форма,
			ПараметрыПечати);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает признак заполненности поля/колонки "Ведомость" в платежных документах с операцией на выплату зарплаты.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма платежного документа.
// 
// Возвращаемое значение:
//  Булево - признак заполненности ведомости на выплату зарплаты.
//
Функция ВедомостьНаВыплатуЗарплатыЗаполнена(Форма) Экспорт

	Заполнена = Истина;
	
	Для Каждого СтрокаТабличнойЧасти Из Форма.Объект.РасшифровкаПлатежа Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.Ведомость) Тогда
			
			Если Форма.ПереключательРасшифровки = 1 Тогда
			
				ТекстСообщения = СтрШаблон(НСтр("ru = 'В строке %1 не заполнена ведомость на выплату зарплаты';
												|en = 'In line %1, paysheet is not filled'"), СтрокаТабличнойЧасти.НомерСтроки);
				Поле = "РасшифровкаПлатежа[" + (СтрокаТабличнойЧасти.НомерСтроки - 1) + "].Ведомость";
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , Поле, "Объект");
			
			Иначе
			
				ТекстСообщения = НСтр("ru = 'Не заполнена ведомость на выплату зарплаты';
										|en = 'Paysheet is not filled'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "РасшифровкаБезРазбиенияВедомость");
			
			КонецЕсли;
			
			Заполнена = Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Заполнена;

КонецФункции
//-- НЕ УТ

// Формирует дополнительный отбор для выбора объектов расчета.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Данные формы, в которой выполняется выбор объектов расчета.
//  ДополнительныйОтбор - Соответствие из Строка, Неопределено - дополнительный отбор
//  Подбор - Булево - признак выполнения подбора в форме.
//
Процедура ДополнитьОтборОбъектовРасчета(Форма, ДополнительныйОтбор, Подбор = Ложь) Экспорт

	Если Форма.Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ОплатаПоставщику")
		И Форма.Объект.ОперацияССамозанятым
		И Форма.Объект.СписокКонтрагентов Тогда
		
		Если ДополнительныйОтбор = Неопределено Тогда
			ДополнительныйОтбор = Новый Соответствие;
		КонецЕсли;
		
		ДополнительныйОтбор.Вставить(
			"Контрагент.ЮридическоеФизическоеЛицо",
			ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо"));
		ДобавитьОтборПоПризнакуОперацияССамозанятым(Истина, ДополнительныйОтбор, Подбор);
		
	КонецЕсли;
	
	Если Форма.Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ОплатаПоставщику")
		И (Форма.КонтрагентЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо")
		ИЛИ Форма.КонтрагентЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель")) Тогда
		ДобавитьОтборПоПризнакуОперацияССамозанятым(Форма.Объект.ОперацияССамозанятым, ДополнительныйОтбор, Подбор);
	КонецЕсли;

КонецПроцедуры

// Установливает параметры выбора контрагента в табличной части "Расшифровка платежа".
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа.
//
Процедура УстановитьПараметрыВыбораКонтрагентаРасшифровкиПлатежа(Форма) Экспорт

	СтрокаТабличнойЧасти = Форма.Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Партнер) Тогда
		ДенежныеСредстваКлиентСерверЛокализация.УстановитьПараметрыВыбораПолучателя(
			Форма, Форма.Объект, Форма.Элементы.РасшифровкаПлатежаКонтрагент, СтрокаТабличнойЧасти.Партнер);
	Иначе
		ДенежныеСредстваКлиентСерверЛокализация.УстановитьПараметрыВыбораПолучателя(
			Форма, Форма.Объект, Форма.Элементы.РасшифровкаПлатежаКонтрагент);
	КонецЕсли;

КонецПроцедуры

//-- Локализация

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОткрытьСозданиеБанковскогоСчетаЗавершение(ВладелецСчета, СозданиеСчетаЦифровогоРубля, ОписаниеОповещенияОЗакрытииФормыСчета = Неопределено)
	
	СчетОрганизации = ТипЗнч(ВладелецСчета) = Тип("СправочникСсылка.Организации");
	
	ПутьКФорме         = ?(СчетОрганизации,
		                   "Справочник.БанковскиеСчетаОрганизаций.Форма.ФормаЭлемента",
		                   "Справочник.БанковскиеСчетаКонтрагентов.Форма.ФормаЭлемента");
	
	ЗначенияЗаполнения = Новый Структура;
	
	//++ Локализация
	Если СозданиеСчетаЦифровогоРубля Тогда
		
		ЗначенияЗаполнения.Вставить("СчетЦифровогоРубля", Истина);
		Если СчетОрганизации Тогда
			ПутьКФорме = "Справочник.БанковскиеСчетаОрганизаций.Форма.ФормаЭлементаСчетЦифровогоРубля";
			ЗначенияЗаполнения.Вставить(
				"ТипСчета", ПредопределенноеЗначение("Перечисление.ТипыБанковскихСчетов.Расчетный"));
		Иначе
			ПутьКФорме = "Справочник.БанковскиеСчетаКонтрагентов.Форма.ФормаЭлементаСчетЦифровогоРубля";
		КонецЕсли;
		
	КонецЕсли;
	//-- Локализация
	
	ЗначенияЗаполнения.Вставить("Владелец", ВладелецСчета);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму(ПутьКФорме, ПараметрыФормы, , , , ,ОписаниеОповещенияОЗакрытииФормыСчета);
	
КонецПроцедуры

//++ Локализация

Процедура ДобавитьОтборПоПризнакуОперацияССамозанятым(Признак, Отбор, ЭтоПодбор)

	Если Отбор = Неопределено Тогда
		Отбор = Новый Соответствие;
	КонецЕсли;
	
	Отбор.Вставить("ОперацияССамозанятым", Признак);
	
	Если Признак Тогда
		
		РежимНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС");
		Отбор.Вставить("НалогообложениеНДС", РежимНалогообложения);
		
	КонецЕсли;

КонецПроцедуры

Процедура ОткрытьФормуЧекаСамозанятого(Чек, Параметры) Экспорт

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", Параметры.Форма);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОткрытиеФормыЧекаСамозанятогоЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Чек, Ложь);
	
	ОткрытьФорму("ОбщаяФорма.ФормаЧекаСамозанятого", 
		ПараметрыФормы, , , , ,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

Процедура ОткрытиеФормыЧекаСамозанятогоЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Отмена Или Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДополнительныеПараметры.Форма;
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗагрузкиЧекаСамозанятого", Форма);
	ВыполнитьОбработкуОповещения(Оповещение, Результат);

КонецПроцедуры

// Выполняет действия перед открытием формы загрузки чека в документе.
//
// Параметры:
//  Ответ - КодВозвратаДиалога - результат диалога с пользователем
//  ДополнительныеПараметры - см. ПараметрыЗагрузкиЧекаСамозанятого.
//
Процедура ПередЗагрузкойЧекаСамозанятогоВДокументЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Результат = Истина;
		
		Форма = ДополнительныеПараметры.Форма;
		Объект = Форма.Объект; // ДанныеФормыСтруктура
		
		Если ДополнительныеПараметры.Записывать Тогда
			
			ПараметрыЗаписи = Новый Структура;
			ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Запись);
			Результат = ОбщегоНазначенияУТКлиент.ОбработатьЗаписьОбъектаВФорме(Форма, ПараметрыЗаписи);
			
		КонецЕсли;
		
		Если Результат Тогда
		
			ДанныеЧека = ДополнительныеПараметры.ТекущиеДанные;
			ДанныеЧека.Вставить("ДокументВладелец", Объект.Ссылка);
			
			Оповещение = Новый ОписаниеОповещения(
				"ОткрытьФормуЧекаСамозанятого", ЭтотОбъект, ДополнительныеПараметры);
			ВыполнитьОбработкуОповещения(Оповещение, ДанныеЧека);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Параметры загрузки чека самозанятого в документы приобретения.
// 
// Возвращаемое значение:
//   Структура:
//   * Форма - Неопределено, ФормаКлиентскогоПриложения - форма документа, в которой выполняется привязка.
//   * Записывать - Булево - признак необходимости записи документа перед загрузкой файла чека в документ.
//   * ТекущиеДанные - см. НовыеДанныеЧека.
//
Функция ПараметрыЗагрузкиЧекаСамозанятого()

	Результат = Новый Структура;
	Результат.Вставить("Форма", Неопределено);
	Результат.Вставить("Записывать", Ложь);
	Результат.Вставить("ТекущиеДанные", НовыеДанныеЧека());
	
	Возврат Результат;

КонецФункции

Функция НовыеДанныеЧека()

	ДанныеЧека = Новый Структура;
	
	ДанныеЧека.Вставить("СсылкаНаЧек", "");
	ДанныеЧека.Вставить("НомерЧека", "");
	ДанныеЧека.Вставить("ФайлЧека", Неопределено);
	ДанныеЧека.Вставить("ЧекСамозанятого", Неопределено);
	ДанныеЧека.Вставить("ДокументВладелец", Неопределено);
	
	Возврат ДанныеЧека

КонецФункции

//-- Локализация

#КонецОбласти