#Область СлужебныеПроцедурыИФункции

#Область ВыгрузкаДокументацииКонтролируемойСделки

// Формирует пример файла выгрузки.
// 
// Параметры:
//  ПараметрыВыгрузки - см. НовыеПараметрыЭлектронногоПредставленияДокументацииКС
// 
// Возвращаемое значение:
//	 - Массив из Структура:
//		* АдресФайлаВыгрузки - Строка - адрес двоичных данных файла выгрузки во временном хранилище.
//		* ИмяФайлаВыгрузки - Строка - короткое имя файла выгрузки (с расширением).
//
Функция ФайлВыгрузкиДокументацииКС(ОсновныеСведенияВыгрузки, ДанныеДокументации, УникальныйИдентификатор) Экспорт
	
	СведенияИД = Новый Структура;
	СведенияИД.Вставить("Получатель", ОсновныеСведенияВыгрузки.КодНалоговогоОргана);
	СведенияИД.Вставить("КонечныйПолучатель", "0023");
	СведенияИД.Вставить("ИНН", ОсновныеСведенияВыгрузки.СведенияОНалогоплательщике.ИНН);
	СведенияИД.Вставить("КПП", ОсновныеСведенияВыгрузки.СведенияОНалогоплательщике.КПП);
	СведенияИД.Вставить("ДатаВыгрузки", ОсновныеСведенияВыгрузки.ДатаВыгрузки);
	
	ИдФайл = ИдФайлаВыгрузкиДокументацииКС(СведенияИД);
	
	//@skip-check missing-temporary-file-deletion
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	
	КомпоновщикXML = НовыйКомпоновщикXML(ИмяВременногоФайла);
	
	КомпоновщикXML.ЗаписатьНачалоЭлемента("Файл");
	
	ВывестиАтрибут(КомпоновщикXML, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ВывестиАтрибут(КомпоновщикXML, "ИдФайл", ИдФайл, "Т,255");
	ВывестиАтрибут(КомпоновщикXML, "ВерсПрог", РегламентированнаяОтчетность.НазваниеИВерсияПрограммы(), "Т,40");
	ВывестиАтрибут(КомпоновщикXML, "ВерсФорм", "5.01", "Т,5");
	ВывестиАтрибут(КомпоновщикXML, "ИдФайлОсн", ОсновныеСведенияВыгрузки.ИдентификаторФайлаОснования, "Т,200");
	
	КомпоновщикXML.ЗаписатьНачалоЭлемента("Документ");
	
	ВывестиАтрибут(КомпоновщикXML, "КНД", "1184076", "Т,7");
	ВывестиАтрибут(КомпоновщикXML, "ДатаДок", ОсновныеСведенияВыгрузки.ДатаВыгрузки, "Д");
	ВывестиАтрибут(КомпоновщикXML, "НомКорр", ОсновныеСведенияВыгрузки.НомерКорректировки, "Ч,3");
	// Код налогового органа в шапке всегда должен быть равен 0023. Это ограничение схемы.
	ВывестиАтрибут(КомпоновщикXML, "КодНО", "0023");
	ВывестиАтрибут(КомпоновщикXML, "ОтчетГод", ОсновныеСведенияВыгрузки.ОтчетныйГод, "Ч,4");
	
	ВывестиУзел_СвНП(КомпоновщикXML, ОсновныеСведенияВыгрузки.СведенияОНалогоплательщике);
	ВывестиУзел_Подписант(КомпоновщикXML, ОсновныеСведенияВыгрузки.Подписант);
	
	ВывестиУзлы_ДокУвКонтрСд(КомпоновщикXML, ДанныеДокументации.МетодыЦенообразования);
	
	Для Каждого Приложение Из ДанныеДокументации.Приложения Цикл
		КомпоновщикXML.ЗаписатьНачалоЭлемента("Прилож");
		
		ВывестиАтрибут(КомпоновщикXML, "НаимПрилДок", Приложение.НаименованиеДокумента, "Т,255");
		ВывестиАтрибут(КомпоновщикXML, "ИмяФайлПрил", Приложение.ИмяФайла, "Т,255");
		
		КомпоновщикXML.ЗаписатьКонецЭлемента(); // Прилож
		
	КонецЦикла;
	
	КомпоновщикXML.ЗаписатьКонецЭлемента(); // Документ
	
	КомпоновщикXML.ЗаписатьКонецЭлемента(); // Файл
	
	СсылкаНаХранилищеКомпоновщикаXML = СсылкаНаХранилищеКомпоновщикаXML(КомпоновщикXML,
		ИмяВременногоФайла, УникальныйИдентификатор);
	
	ФайловаяСистема.УдалитьВременныйФайл(ИмяВременногоФайла);
	
	ДанныеФайла = Новый Структура();
	ДанныеФайла.Вставить("АдресФайлаВыгрузки", СсылкаНаХранилищеКомпоновщикаXML);
	ДанныеФайла.Вставить("ИмяФайлаВыгрузки", ИдФайл + ".xml");
	
	Результат = Новый Массив;
	Результат.Добавить(ДанныеФайла);
	
	ДобавитьФайлыПриложенияКВыгрузке(Результат, ДанныеДокументации, УникальныйИдентификатор);
	
	Возврат Результат;
	
КонецФункции

// Возвращает значение ИД файла выгрузки документации по контролируеым сделкам по структуре сведений.
// 
// Параметры:
//  Сведения - Структура - сведения о файле выгрузки для формирования ИД документации по контролируемым сделкам:
//  * ДатаВыгрузки - Дата - дата выгрузки документации.
//  * ИНН - Строка - ИНН организации, подающей документацию.
//  * КонечныйПолучатель - Строка - конечный получатель документации.
//  * КПП - Строка - КПП организации, подающей документацию.
//  * Получатель - Строка - получатель документации.
// 
// Возвращаемое значение:
//	 - Строка - значение ИД файла выгрузки документации по контролируемым сделкам.
//
Функция ИдФайлаВыгрузкиДокументацииКС(Сведения)
	
	Составляющие = Новый Массив();
	// Префикс.
	Составляющие.Добавить("UT_DOKUVKSD");
	// Идентификатор получателя информации.
	Составляющие.Добавить(Сведения.Получатель);
	Составляющие.Добавить(Сведения.КонечныйПолучатель);
	// Идентификатор отправителя информации.
	Составляющие.Добавить(Сведения.ИНН + Сведения.КПП);
	// Дата формирования.
	Составляющие.Добавить(Формат(Сведения.ДатаВыгрузки, "ДФ=yyyyMMdd;"));
	// Идентификационный номер файла.
	Составляющие.Добавить(Строка(Новый УникальныйИдентификатор()));
	
	Возврат СтрСоединить(Составляющие, "_");
	
КонецФункции

Процедура ВывестиУзел_СвНП(КомпоновщикXML, СведенияНП)
	
	КомпоновщикXML.ЗаписатьНачалоЭлемента("СвНП");
	
	КомпоновщикXML.ЗаписатьНачалоЭлемента("НПЮЛ");
	
	ВывестиАтрибут(КомпоновщикXML, "НаимОрг", СведенияНП.Наименование, "Т,1000");
	ВывестиАтрибут(КомпоновщикXML, "ИННЮЛ", СведенияНП.ИНН, "Т,10");
	ВывестиАтрибут(КомпоновщикXML, "КПП", СведенияНП.КПП, "Т,9");
	
	КомпоновщикXML.ЗаписатьКонецЭлемента(); // НПЮЛ
	
	КомпоновщикXML.ЗаписатьКонецЭлемента(); // СвНП
	
КонецПроцедуры

Процедура ВывестиУзел_Подписант(КомпоновщикXML, Подписант)
	
	КомпоновщикXML.ЗаписатьНачалоЭлемента("Подписант");
	ВывестиАтрибут(КомпоновщикXML, "ПрПодп", Подписант.ПризнакПодписанта, "Т,1");
	
	КомпоновщикXML.ЗаписатьНачалоЭлемента("ФИО");
	
	ВывестиАтрибут(КомпоновщикXML, "Фамилия", Подписант.Фамилия, "Т,60");
	ВывестиАтрибут(КомпоновщикXML, "Имя", Подписант.Имя, "Т,60");
	ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", Подписант.Отчество, "Т,60");
	
	КомпоновщикXML.ЗаписатьКонецЭлемента(); // ФИО
	
	Если Подписант.ПризнакПодписанта = "2" Тогда
		КомпоновщикXML.ЗаписатьНачалоЭлемента("СвПред");
		
		ВывестиАтрибут(КомпоновщикXML, "НаимДок", Подписант.НаименованиеДокумента, "Т,120");
		
		КомпоновщикXML.ЗаписатьКонецЭлемента(); // СвПред
		
	КонецЕсли;
	
	КомпоновщикXML.ЗаписатьКонецЭлемента(); // Подписант
	
КонецПроцедуры

Процедура ВывестиУзлы_ДокУвКонтрСд(КомпоновщикXML, МетодыЦенообразования)
	
	Для Каждого СтрокаМетода Из МетодыЦенообразования Цикл
		
		ПараметрыМетода= Новый Структура;
		
		ПараметрыМетода.Вставить("ПорядковыеНомераСделок", СтрокаМетода.ПорядковыеНомераСделок);
		ПараметрыМетода.Вставить("СуммаДоходов", СтрокаМетода.СуммаДоходов);
		ПараметрыМетода.Вставить("СуммаРасходов", СтрокаМетода.СуммаРасходов);
		
		ПараметрыМетода.Вставить("КодМетодаЦенообразования", СтрокаМетода.КодМетодаЦенообразования);
		ПараметрыМетода.Вставить("СвойстваМетода", СтрокаМетода.СвойстваМетода);
		
		ПараметрыМетода.Вставить("Контрагенты", Новый Массив);
		
		ОписаниеКонтрагента = Новый Структура;
		ОписаниеКонтрагента.Вставить("НаименованиеОрганизации", СтрокаМетода.НаименованиеРус);
		ОписаниеКонтрагента.Вставить("НаименованиеОрганизацииЛатиницей", СтрокаМетода.НаименованиеЛат);
		ОписаниеКонтрагента.Вставить("ОКСМ", СтрокаМетода.КодСтраныРегистрации);
		ОписаниеКонтрагента.Вставить("КодСтороныСделки", СтрокаМетода.КодСтороныСделки);
		ОписаниеКонтрагента.Вставить("Взаимозависимость", СтрокаМетода.КодВзаимозависимости);
		ОписаниеКонтрагента.Вставить("ИНН", СтрокаМетода.ИНН);
		ОписаниеКонтрагента.Вставить("РегистрационныйНомер", СтрокаМетода.РегистрационныйНомер);
		ПараметрыМетода.Контрагенты.Добавить(ОписаниеКонтрагента);
		
		ВывестиУзел_ДокУвКонтрСд(КомпоновщикXML, ПараметрыМетода);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиУзел_ДокУвКонтрСд(КомпоновщикXML, ПараметрыМетода)
	
	КомпоновщикXML.ЗаписатьНачалоЭлемента("ДокУвКонтрСд");
	
	ВывестиАтрибут(КомпоновщикXML, "НомПорСд", ПараметрыМетода.ПорядковыеНомераСделок, "Т,5000");
	ВывестиАтрибут(КомпоновщикXML, "СумДохСд", ПараметрыМетода.СуммаДоходов, "Ч,15");
	ВывестиАтрибут(КомпоновщикXML, "СумРасхСд", ПараметрыМетода.СуммаРасходов, "Ч,15");
	
	КомпоновщикXML.ЗаписатьНачалоЭлемента("Метод");
	
	ВывестиАтрибут(КомпоновщикXML, "КодМетЦен", ПараметрыМетода.КодМетодаЦенообразования, "Т,2");
	КодМетода = КонтролируемыеСделкиЦенообразование.КодМетодаКомбинацияМетодов();
	Если ПараметрыМетода.КодМетодаЦенообразования = КодМетода Тогда
		Для Каждого Метод Из ПараметрыМетода.СвойстваМетода Цикл
			КомпоновщикXML.ЗаписатьНачалоЭлемента("КомбМетод");
			ВывестиАтрибут(КомпоновщикXML, "КодМетЦенКомб", Метод.КодМетодаЦенообразования, "Т,2");
			ВывестиУзлы_ИнфМетод01_05(КомпоновщикXML, Метод);
			КомпоновщикXML.ЗаписатьКонецЭлемента(); // КомбМетод
		КонецЦикла;
		
	ИначеЕсли ЗначениеЗаполнено(ПараметрыМетода.СвойстваМетода) Тогда
		
		Метод = ПараметрыМетода.СвойстваМетода[0];
		ВывестиУзлы_ИнфМетод01_05(КомпоновщикXML, Метод);
		
	КонецЕсли;
	
	КомпоновщикXML.ЗаписатьКонецЭлемента(); // Метод
	
	Для Каждого ОписаниеКонтрагента Из ПараметрыМетода.Контрагенты Цикл
		ВывестиУзел_ОснСведКонтр(КомпоновщикXML, ОписаниеКонтрагента);
	КонецЦикла;
	
	КомпоновщикXML.ЗаписатьКонецЭлемента(); // ДокУвКонтрСд
	
КонецПроцедуры

Процедура ВывестиУзлы_ИнфМетод01_05(КомпоновщикXML, Метод)
	
	МетодЦенообразования = Метод.КодМетодаЦенообразования;
	
	Если МетодЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаСопоставимыхРыночныхЦен() Тогда
		ВывестиУзел_ИнфМетодЦен(КомпоновщикXML, Метод);
		
	ИначеЕсли МетодЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаЦеныПоследующейРеализации()
		Или МетодЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаЗатратного()
		Или МетодЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаСопоставимойРентабельности() Тогда
		ВывестиУзел_ИнфМетод02_04(КомпоновщикXML, Метод);
		
	ИначеЕсли МетодЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаРаспределенияПрибыли() Тогда
		ВывестиЭлемент(КомпоновщикXML, "ОписРаспрПр", Метод.ОписаниеМетодаРаспределенияПрибыли);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиУзел_ИнфМетодЦен(КомпоновщикXML, Метод)
	
	КомпоновщикXML.ЗаписатьНачалоЭлемента("ИнфМетодЦен");
	
	ВывестиАтрибут(КомпоновщикXML, "НаимИстИнф", Метод.ИсточникИнформацииСопоставимыхЦен, "Т,100");
	ВывестиАтрибут(КомпоновщикXML, "НаимКотир", Метод.НаименованиеКотировкиСопоставимыхЦен, "Т,100");
	ВывестиАтрибут(КомпоновщикXML, "ПорПримКотир", Метод.ПорядокПримененияКотировкиСопоставимыхЦен, "Т,100");
	ВывестиАтрибут(КомпоновщикXML, "НаимДокЦена", Метод.НаименованиеДокументаЦеныСопоставимыхЦен, "Т,100");
	ВывестиАтрибут(КомпоновщикXML, "НаимКоррСопост",
		Метод.НаименованиеКорректировкиДляСопоставимостиСопоставимыхЦен, "Т,100");
	
	КомпоновщикXML.ЗаписатьКонецЭлемента(); // ИнфМетодЦен
	
КонецПроцедуры

Процедура ВывестиУзел_ИнфМетод02_04(КомпоновщикXML, Метод)
	
	КомпоновщикXML.ЗаписатьНачалоЭлемента("ИнфМетод02-04");
	
	ВывестиАтрибут(КомпоновщикXML, "ВыбПоказРент", Метод.ПоказательРентабельности, "Т,100");
	ВывестиАтрибут(КомпоновщикXML, "ТестСтор", Метод.ТестируемаяСторона, "Т,1");
	ВывестиАтрибут(КомпоновщикXML, "РентТестСтор", Метод.Рентабельность, "Ч,6,3");
	ВывестиАтрибут(КомпоновщикXML, "РынРент", Метод.РыночнаяРентабельность, "Ч,6,3");
	
	КомпоновщикXML.ЗаписатьКонецЭлемента(); // ИнфМетод02-04
	
КонецПроцедуры

Процедура ВывестиУзел_ОснСведКонтр(КомпоновщикXML, ОписаниеКонтрагента)
	
	КомпоновщикXML.ЗаписатьНачалоЭлемента("ОснСведКонтр");
	
	ВывестиАтрибут(КомпоновщикXML, "НаимОргРус", ОписаниеКонтрагента.НаименованиеОрганизации, "Т,1000");
	ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НаимОргЛат",
		ОписаниеКонтрагента.НаименованиеОрганизацииЛатиницей, "Т,1000");
	ВывестиАтрибут(КомпоновщикXML, "ОКСМ", ОписаниеКонтрагента.ОКСМ, "Т,3");
	ВывестиАтрибут(КомпоновщикXML, "КодСторСд", ОписаниеКонтрагента.КодСтороныСделки, "Т,3");
	ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ВзЗавис", ОписаниеКонтрагента.Взаимозависимость, "Т,1");
	
	Если ЗначениеЗаполнено(ОписаниеКонтрагента.ИНН) Тогда
		ВывестиЭлемент(КомпоновщикXML, "ИННЮЛ", ОписаниеКонтрагента.ИНН, "Т,10");
	ИначеЕсли ЗначениеЗаполнено(ОписаниеКонтрагента.РегистрационныйНомер) Тогда
		ВывестиЭлемент(КомпоновщикXML, "РегНомИн", ОписаниеКонтрагента.РегистрационныйНомер, "Т,50");
	КонецЕсли;
	
	КомпоновщикXML.ЗаписатьКонецЭлемента(); // ОснСведКонтр
	
КонецПроцедуры

#Область КомпоновщикXML

// Конструктор компоновщика XML для формирования файла электронного представления.
// 
// Параметры:
//  ИмяФайла - Строка - Имя файла
//  Кодировка - Строка - Кодировка
// 
// Возвращаемое значение:
//  ЗаписьXML - Новый компоновщик XML
Функция НовыйКомпоновщикXML(ИмяФайла, Кодировка = "windows-1251")
	
	КомпоновщикXML = Новый ЗаписьXML();
	КомпоновщикXML.ОткрытьФайл(ИмяФайла, Кодировка);
	КомпоновщикXML.ЗаписатьОбъявлениеXML();
	
	Возврат КомпоновщикXML;
	
КонецФункции

Процедура ВывестиЭлемент(КомпоновщикXML, ИмяЭлемента, ЗначениеЭлемента, ТипЭлемента = "Т")
	
	КомпоновщикXML.ЗаписатьНачалоЭлемента(ИмяЭлемента);
	КомпоновщикXML.ЗаписатьТекст(ПредставлениеЗначения(ЗначениеЭлемента, ТипЭлемента));
	КомпоновщикXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ВывестиАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута = "Т")
	
	КомпоновщикXML.ЗаписатьАтрибут(ИмяАтрибута, ПредставлениеЗначения(ЗначениеАтрибута, ТипАтрибута));
	
КонецПроцедуры

Процедура ВывестиНеобязательныйАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута = "Т")
	
	Если ЗначениеЗаполнено(ЗначениеАтрибута) Тогда
		ВывестиАтрибут(КомпоновщикXML, ИмяАтрибута, ПредставлениеЗначения(ЗначениеАтрибута, ТипАтрибута));
	КонецЕсли;
	
КонецПроцедуры

Функция ПредставлениеЗначения(ИсходноеЗначение, ОписаниеФормата)
	
	ОписаниеТипаАтрибута = СтрРазделить(ОписаниеФормата, ",");
	
	Если ОписаниеТипаАтрибута[0] = "Д" Тогда
		ПредставлениеЗначения = Формат(ИсходноеЗначение, "ДФ=dd.MM.yyyy");
		
	ИначеЕсли ОписаниеТипаАтрибута[0] = "Т" Тогда
		ПредставлениеЗначения = СокрЛП(ИсходноеЗначение);
		Если ОписаниеТипаАтрибута.Количество() >= 2 Тогда
			МаксимальнаяДлина = Число(ОписаниеТипаАтрибута[1]);
			ПредставлениеЗначения = Лев(ПредставлениеЗначения, МаксимальнаяДлина);
		КонецЕсли;
		
	ИначеЕсли ОписаниеТипаАтрибута[0] = "Ч" Тогда
		СтрокаФормата = "ЧРД=.;ЧН=0;ЧГ=;";
		
		Если ОписаниеТипаАтрибута.Количество() >= 2 Тогда
			РазмерностьПолная = Число(ОписаниеТипаАтрибута[1]);
			СтрокаФормата = СтрокаФормата + "ЧЦ=" + Формат(РазмерностьПолная, "ЧГ=") + ";";
		КонецЕсли;
		
		Если ОписаниеТипаАтрибута.Количество() >= 3 Тогда
			РазмерностьДробнойЧасти = Число(ОписаниеТипаАтрибута[2]);
			СтрокаФормата = СтрокаФормата + "ЧДЦ=" + Формат(РазмерностьДробнойЧасти, "ЧГ=") + ";";
		КонецЕсли;
		
		ПредставлениеЗначения = Формат(ИсходноеЗначение, СтрокаФормата);
		
	Иначе
		ИмяМетода = "Отчет.ПрототипВыгрузкаДокументацииКС.ПредставлениеЗначения()";
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Функция %1:
			|Представление значения по типу ""%2"" не поддерживается';
			|en = 'The %1 function:
			|Value presentation by the ""%2"" type is not supported'"),
			ИмяМетода, ОписаниеФормата);
		
	КонецЕсли;
	
	Возврат ПредставлениеЗначения;
	
КонецФункции

Функция СсылкаНаХранилищеКомпоновщикаXML(КомпоновщикXML, ИмяФайла, УникальныйИдентификатор)
	
	КомпоновщикXML.Закрыть();
	КомпоновщикXML = Неопределено;
	
	ДвоичныеДанныеФайла = Новый ДвоичныеДанные(ИмяФайла);
	АдресВХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор);
	
	Возврат АдресВХранилище;
	
КонецФункции

#КонецОбласти

Процедура ДобавитьФайлыПриложенияКВыгрузке(Результат, ДанныеДокументации, УникальныйИдентификатор)
	
	ПриложенныеФайлы = ДанныеДокументации.Приложения.ВыгрузитьКолонку("Файл");
	ДвоичныеДанныеФайлов = РаботаСФайлами.ДвоичныеДанныеФайлов(ПриложенныеФайлы);
	
	Для Каждого Приложение Из ДанныеДокументации.Приложения Цикл
		
		ДвоичныеДанныеФайла = ДвоичныеДанныеФайлов.Получить(Приложение.Файл);
		Если ДвоичныеДанныеФайла = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеФайла = Новый Структура();
		ДанныеФайла.Вставить("АдресФайлаВыгрузки",
			ПоместитьВоВременноеХранилище(ДвоичныеДанныеФайла, УникальныйИдентификатор));
		ДанныеФайла.Вставить("ИмяФайлаВыгрузки", Приложение.ИмяФайла + "." + Приложение.ФайлРасширение);
		Результат.Добавить(ДанныеФайла);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
