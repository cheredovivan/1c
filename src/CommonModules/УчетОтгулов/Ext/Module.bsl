#Область ПрограммныйИнтерфейс

// Регистрирует рабочее время в переданной коллекции движений.
//
// 	Параметры: 
//		Движения - коллекция движений, обязательно содержащая набор записей
//				   регистра накопления ДниЧасыОтгулов.
//		ДанныеОбОтгулах - таблица значений с колонками.
//			Сотрудник
//			ВидДвижения
//			Период
//			ОтработанныйДень
//			Дни
Процедура ЗарегистрироватьДанныеОбИспользованииОтгулов(Движения, ДанныеОбОтгулах, ЗаписыватьСразу = Ложь) Экспорт
	
	Движения.ДанныеОбИспользованииОтгулов.Записывать = Истина;
		
	Для Каждого СтрокаДанных Из ДанныеОбОтгулах Цикл
		Если СтрокаДанных.ОтработанныйДень >= НачалоУчетаОтработанныхДней() 
			И (СтрокаДанных.Дни <> 0 Или СтрокаДанных.Часы <> 0) Тогда 
			ЗаполнитьЗначенияСвойств(Движения.ДанныеОбИспользованииОтгулов.Добавить(), СтрокаДанных);
		КонецЕсли;
	КонецЦикла;	
	
	Если ЗаписыватьСразу Тогда
		Движения.ДанныеОбИспользованииОтгулов.БлокироватьДляИзменения = Истина;
		Движения.ДанныеОбИспользованииОтгулов.Записать();
		Движения.ДанныеОбИспользованииОтгулов.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НачалоУчетаОтработанныхДней() Экспорт
	
	Возврат Дата(2025, 3, 1)
	
КонецФункции

Функция СведенияОбОтработанныхДнях(Сотрудники, ДатаАктуальности, ИсключаемыйДокумент = Неопределено, ИсправленныйДокумент = Неопределено) Экспорт 
	
	ДанныеСотрудников = Новый Соответствие;
	
	ИсключаемыеДокументы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИсключаемыйДокумент);
	Если ЗначениеЗаполнено(ИсправленныйДокумент) Тогда
		ИсключаемыеДокументы.Добавить(ИсправленныйДокумент);
		ИсправленныеДокументы = ИсправлениеДокументовЗарплатаКадры.ПолучитьДокументыЦепочкиИсправлений(ИсправленныйДокумент);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИсключаемыеДокументы, ИсправленныеДокументы);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ИсключаемыеДокументы", ИсключаемыеДокументы);
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеОбИспользованииОтгуловОстатки.Организация КАК Организация,
		|	ДанныеОбИспользованииОтгуловОстатки.Сотрудник КАК Сотрудник,
		|	ДанныеОбИспользованииОтгуловОстатки.ОтработанныйДень КАК ОтработанныйДень,
		|	ДанныеОбИспользованииОтгуловОстатки.ДниОстаток КАК Дни
		|ПОМЕСТИТЬ ВТДанныеОбИспользованииОтгулов
		|ИЗ
		|	РегистрНакопления.ДанныеОбИспользованииОтгулов.Остатки(&ДатаАктуальности, Сотрудник В (&Сотрудники)) КАК ДанныеОбИспользованииОтгуловОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеОбИспользованииОтгулов.Организация,
		|	ДанныеОбИспользованииОтгулов.Сотрудник,
		|	ДанныеОбИспользованииОтгулов.ОтработанныйДень,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОбИспользованииОтгулов.Сторно
		|				ТОГДА -ДанныеОбИспользованииОтгулов.Дни
		|			ИНАЧЕ ДанныеОбИспользованииОтгулов.Дни
		|		КОНЕЦ)
		|ИЗ
		|	РегистрНакопления.ДанныеОбИспользованииОтгулов КАК ДанныеОбИспользованииОтгулов
		|ГДЕ
		|	ДанныеОбИспользованииОтгулов.Регистратор В(&ИсключаемыеДокументы)
		|	И ДанныеОбИспользованииОтгулов.Сотрудник В(&Сотрудники)
		|	И ДанныеОбИспользованииОтгулов.Период < &ДатаАктуальности
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеОбИспользованииОтгулов.Организация,
		|	ДанныеОбИспользованииОтгулов.Сотрудник,
		|	ДанныеОбИспользованииОтгулов.ОтработанныйДень
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОбИспользованииОтгулов.Сотрудник КАК Сотрудник,
		|	ДанныеОбИспользованииОтгулов.ОтработанныйДень КАК ОтработанныйДень
		|ИЗ
		|	ВТДанныеОбИспользованииОтгулов КАК ДанныеОбИспользованииОтгулов
		|ГДЕ
		|	ДанныеОбИспользованииОтгулов.Дни <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	ОтработанныйДень";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
		НеиспользованныеОтработанныеДни = Новый Массив;
		Пока Выборка.Следующий() Цикл 
			НеиспользованныеОтработанныеДни.Добавить(Выборка.ОтработанныйДень);
		КонецЦикла;
		ДанныеСотрудника = Новый Структура("ОстатокДоНачалаУчетаОтгулов, НеиспользованныеОтработанныеДни", 0, НеиспользованныеОтработанныеДни);
		ДанныеСотрудников.Вставить(Выборка.Сотрудник, ДанныеСотрудника);
	КонецЦикла;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДниЧасыОтгуловОстатки.Сотрудник КАК Сотрудник,
		|	ДниЧасыОтгуловОстатки.ДниОстаток КАК Дни
		|ПОМЕСТИТЬ ВТОстатокОтгулов
		|ИЗ
		|	РегистрНакопления.ДниЧасыОтгулов.Остатки(&ДатаАктуальности, Сотрудник В (&Сотрудники)) КАК ДниЧасыОтгуловОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДниЧасыОтгулов.Сотрудник,
		|	СУММА(ВЫБОР
		|			КОГДА ДниЧасыОтгулов.Сторно
		|				ТОГДА -ДниЧасыОтгулов.Дни
		|			ИНАЧЕ ДниЧасыОтгулов.Дни
		|		КОНЕЦ)
		|ИЗ
		|	РегистрНакопления.ДниЧасыОтгулов КАК ДниЧасыОтгулов
		|ГДЕ
		|	ДниЧасыОтгулов.Регистратор В(&ИсключаемыеДокументы)
		|	И ДниЧасыОтгулов.Сотрудник В(&Сотрудники)
		|	И ДниЧасыОтгулов.Период < &ДатаАктуальности
		|
		|СГРУППИРОВАТЬ ПО
		|	ДниЧасыОтгулов.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	СУММА(ЕСТЬNULL(ОстатокОтгулов.Дни, 0)) КАК Дни
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстатокОтгулов КАК ОстатокОтгулов
		|		ПО Сотрудники.Ссылка = ОстатокОтгулов.Сотрудник
		|ГДЕ
		|	Сотрудники.Ссылка В(&Сотрудники)
		|
		|СГРУППИРОВАТЬ ПО
		|	Сотрудники.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		ДанныеСотрудника = ДанныеСотрудников.Получить(Выборка.Сотрудник);
		Если ДанныеСотрудника = Неопределено Тогда 
			НеиспользованныеОтработанныеДни = Новый Массив;
			ДанныеСотрудника = Новый Структура("ОстатокДоНачалаУчетаОтгулов, НеиспользованныеОтработанныеДни", 0, НеиспользованныеОтработанныеДни);
		КонецЕсли;
		КоличествоНеиспользованныхДней = ДанныеСотрудника.НеиспользованныеОтработанныеДни.Количество();
		ДанныеСотрудника.ОстатокДоНачалаУчетаОтгулов = Макс(Выборка.Дни - КоличествоНеиспользованныхДней, 0);
		ДнейКорректировки = КоличествоНеиспользованныхДней - Выборка.Дни;
		Если ДнейКорректировки > 0 Тогда
			НеиспользованныеОтработанныеДни = Новый Массив;
			Для Сч = ДнейКорректировки По КоличествоНеиспользованныхДней - 1 Цикл 
				НеиспользованныеОтработанныеДни.Добавить(ДанныеСотрудника.НеиспользованныеОтработанныеДни[Сч]);
			КонецЦикла;	
			ДанныеСотрудника.НеиспользованныеОтработанныеДни = НеиспользованныеОтработанныеДни; 
		КонецЕсли;
		ДанныеСотрудников.Вставить(Выборка.Сотрудник, ДанныеСотрудника);
	КонецЦикла;
	
	Возврат ДанныеСотрудников;
	
КонецФункции

Функция СведенияОбОтработанныхЧасах(Сотрудники, ДатаАктуальности, ИсключаемыйДокумент = Неопределено, ИсправленныйДокумент = Неопределено) Экспорт 
	
	ДанныеСотрудников = Новый Соответствие;
	
	ИсключаемыеДокументы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИсключаемыйДокумент);
	Если ЗначениеЗаполнено(ИсправленныйДокумент) Тогда
		ИсключаемыеДокументы.Добавить(ИсправленныйДокумент);
		ИсправленныеДокументы = ИсправлениеДокументовЗарплатаКадры.ПолучитьДокументыЦепочкиИсправлений(ИсправленныйДокумент);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИсключаемыеДокументы, ИсправленныеДокументы);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ИсключаемыеДокументы", ИсключаемыеДокументы);
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеОбИспользованииОтгуловОстатки.Организация КАК Организация,
		|	ДанныеОбИспользованииОтгуловОстатки.Сотрудник КАК Сотрудник,
		|	ДанныеОбИспользованииОтгуловОстатки.ОтработанныйДень КАК ОтработанныйДень,
		|	ДанныеОбИспользованииОтгуловОстатки.ЧасыОстаток КАК Часы
		|ПОМЕСТИТЬ ВТДанныеОбИспользованииОтгулов
		|ИЗ
		|	РегистрНакопления.ДанныеОбИспользованииОтгулов.Остатки(&ДатаАктуальности, Сотрудник В (&Сотрудники)) КАК ДанныеОбИспользованииОтгуловОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеОбИспользованииОтгулов.Организация,
		|	ДанныеОбИспользованииОтгулов.Сотрудник,
		|	ДанныеОбИспользованииОтгулов.ОтработанныйДень,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеОбИспользованииОтгулов.Сторно
		|				ТОГДА -ДанныеОбИспользованииОтгулов.Часы
		|			ИНАЧЕ ДанныеОбИспользованииОтгулов.Часы
		|		КОНЕЦ)
		|ИЗ
		|	РегистрНакопления.ДанныеОбИспользованииОтгулов КАК ДанныеОбИспользованииОтгулов
		|ГДЕ
		|	ДанныеОбИспользованииОтгулов.Регистратор В(&ИсключаемыеДокументы)
		|	И ДанныеОбИспользованииОтгулов.Сотрудник В(&Сотрудники)
		|	И ДанныеОбИспользованииОтгулов.Период < &ДатаАктуальности
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеОбИспользованииОтгулов.Организация,
		|	ДанныеОбИспользованииОтгулов.Сотрудник,
		|	ДанныеОбИспользованииОтгулов.ОтработанныйДень
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеОбИспользованииОтгулов.Сотрудник КАК Сотрудник,
		|	ДанныеОбИспользованииОтгулов.ОтработанныйДень КАК ОтработанныйДень,
		|	СУММА(ДанныеОбИспользованииОтгулов.Часы) КАК Часы
		|ИЗ
		|	ВТДанныеОбИспользованииОтгулов КАК ДанныеОбИспользованииОтгулов
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеОбИспользованииОтгулов.Сотрудник,
		|	ДанныеОбИспользованииОтгулов.ОтработанныйДень
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	ОтработанныйДень";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
		НеиспользованныеОтработанныеДни = Новый Массив;
		ОтработанныеЧасы = Новый Соответствие;
		Пока Выборка.Следующий() Цикл 
			НеиспользованныеОтработанныеДни.Добавить(Выборка.ОтработанныйДень);
			ОтработанныеЧасы.Вставить(Выборка.ОтработанныйДень, Выборка.Часы);
		КонецЦикла;
		ДанныеСотрудника = Новый Структура("ОстатокДоНачалаУчетаОтгулов, НеиспользованныеОтработанныеДни, ОтработанныеЧасы", 0, НеиспользованныеОтработанныеДни, ОтработанныеЧасы);
		ДанныеСотрудников.Вставить(Выборка.Сотрудник, ДанныеСотрудника);
	КонецЦикла;
	
	Возврат ДанныеСотрудников;
	
КонецФункции

Функция ПредставлениеОтработанныхДней(ОтработанныеДни) Экспорт 
	
	ТаблицаДат = Новый ТаблицаЗначений;
	ТаблицаДат.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	ТаблицаДат.Колонки.Добавить("НачалоМесяца", Новый ОписаниеТипов("Дата"));
	
	Для Каждого ОтработанныйДень Из ОтработанныеДни Цикл
		СтрокаТаблицыДат = ТаблицаДат.Добавить();
		СтрокаТаблицыДат.Дата = ОтработанныйДень.Дата;
		СтрокаТаблицыДат.НачалоМесяца = НачалоМесяца(СтрокаТаблицыДат.Дата);
	КонецЦикла;
	
	ПредставленияПериодов = Новый Массив;
	ОтработанныеПериоды = ОбщегоНазначенияБЗК.ТаблицаПериодовИзТаблицыДат(ТаблицаДат, "НачалоМесяца");
	УникальныеМесяцы = КоллекцииБЗК.УникальныеЗначенияКолонки(ОтработанныеПериоды, "НачалоМесяца");
	
	Для Каждого НачалоМесяца Из УникальныеМесяцы Цикл
		ПериодыПоМесяцу = ОтработанныеПериоды.Скопировать(Новый Структура("НачалоМесяца", НачалоМесяца));
		ПериодыПоМесяцу.Сортировать("ДатаНачала");
		КоличествоПериодов = ПериодыПоМесяцу.Количество();
		НомерПериода = 0;
		Представления = Новый Массив;
		Для Каждого Период Из ПериодыПоМесяцу Цикл
			НомерПериода = НомерПериода + 1;
			ФорматДатыОкончания = ?(НомерПериода = КоличествоПериодов, "ДФ=""д ММММ""", "ДФ=""д""");
			Если Период.ДатаНачала = Период.ДатаОкончания Тогда
				Представление = Формат(Период.ДатаОкончания, ФорматДатыОкончания);
			Иначе
				Представление = Формат(Период.ДатаНачала, "ДФ=""д""") + "-" + Формат(Период.ДатаОкончания, ФорматДатыОкончания);
			КонецЕсли;
			Представления.Добавить(Представление);
		КонецЦикла;
		ПредставленияПериодов.Добавить(СтрСоединить(Представления, ", "));
	КонецЦикла;
	
	Возврат СтрСоединить(ПредставленияПериодов, ";  ");
	
КонецФункции

Процедура СторнироватьДанныеОбИспользованииОтгулов(Движения, ИсправленныйДокумент, Записывать = Ложь) Экспорт
	
	ИмяУчета = "УчетОтгулов";
	МетаданныеРегистра = Метаданные.РегистрыНакопления.ДанныеОбИспользованииОтгулов;

	НаборыЗаписей = ОбщегоНазначенияБЗК.ДвиженияВСтруктуру(Движения);
	
	НаборЗаписей = Неопределено;
	Если НаборыЗаписей.Свойство(МетаданныеРегистра.Имя, НаборЗаписей)
		И ИсправлениеДокументовЗарплатаКадры.ИзолироватьУчетом(НаборЗаписей, ИмяУчета) Тогда
		
		РегистрыНакопления.ДанныеОбИспользованииОтгулов.СторнироватьДвижения(НаборЗаписей, ИсправленныйДокумент, Записывать);
		
	КонецЕсли;
	
КонецПроцедуры

#Область БлокФункцийПервоначальногоЗаполненияИОбновленияИБ

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.34.72";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Процедура = "УчетОтгулов.ЗарегистрироватьОтработанныеДни";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("cd68b7c5-bbc2-42e0-81ee-b01d3a70d578");
	Обработчик.Комментарий = НСтр("ru = 'Корректировка записей регистра Данные об использовании отгулов.';
									|en = 'Adjust the ""Data on use of day offs"" register records.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.34.73";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Процедура = "УчетОтгулов.ДобавитьТаблицуДанныеОбИспользованииОтгуловВПереносДанных";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("ffbbadce-3b9a-4668-9a0a-18b63b6bbaf6");
	Обработчик.Комментарий = НСтр("ru = 'Корректировка документа Перенос данных.';
									|en = 'Adjust the ""Data transfer"" document.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.34.76";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Процедура = "УчетОтгулов.ЗарегистрироватьОтработанныеЧасы";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("85abaec9-9ed1-48fe-865c-6af49d553216");
	Обработчик.Комментарий = НСтр("ru = 'Корректировка записей регистра Данные об использовании отгулов.';
									|en = 'Adjust the ""Data on use of day offs"" register records.'");
	
КонецПроцедуры

Процедура ЗарегистрироватьОтработанныеДни(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НачалоУчета", УчетОтгулов.НачалоУчетаОтработанныхДней());
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДниЧасыОтгулов.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТРегистраторыОтработанныхДней
		|ИЗ
		|	РегистрНакопления.ДниЧасыОтгулов КАК ДниЧасыОтгулов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаботаВВыходныеИПраздничныеДни КАК РаботаВВыходныеИПраздничныеДни
		|		ПО ДниЧасыОтгулов.Регистратор = РаботаВВыходныеИПраздничныеДни.Ссылка
		|			И (ДниЧасыОтгулов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
		|			И (ДниЧасыОтгулов.Период >= &НачалоУчета)
		|			И (ДниЧасыОтгулов.Дни > 0)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеОбИспользованииОтгулов КАК ДанныеОбИспользованииОтгулов
		|		ПО ДниЧасыОтгулов.Период = ДанныеОбИспользованииОтгулов.Период
		|			И ДниЧасыОтгулов.Регистратор = ДанныеОбИспользованииОтгулов.Регистратор
		|			И ДниЧасыОтгулов.ВидДвижения = ДанныеОбИспользованииОтгулов.ВидДвижения
		|			И ДниЧасыОтгулов.Организация = ДанныеОбИспользованииОтгулов.Организация
		|			И ДниЧасыОтгулов.Сотрудник = ДанныеОбИспользованииОтгулов.Сотрудник
		|ГДЕ
		|	ДанныеОбИспользованииОтгулов.Сотрудник ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДниЧасыОтгулов.Регистратор
		|ИЗ
		|	РегистрНакопления.ДниЧасыОтгулов КАК ДниЧасыОтгулов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПереносДанных КАК ПереносДанных
		|		ПО ДниЧасыОтгулов.Регистратор = ПереносДанных.Ссылка
		|			И (ДниЧасыОтгулов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
		|			И (ДниЧасыОтгулов.Период >= &НачалоУчета)
		|			И (ДниЧасыОтгулов.Дни > 0)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеОбИспользованииОтгулов КАК ДанныеОбИспользованииОтгулов
		|		ПО ДниЧасыОтгулов.Период = ДанныеОбИспользованииОтгулов.Период
		|			И ДниЧасыОтгулов.Регистратор = ДанныеОбИспользованииОтгулов.Регистратор
		|			И ДниЧасыОтгулов.ВидДвижения = ДанныеОбИспользованииОтгулов.ВидДвижения
		|			И ДниЧасыОтгулов.Организация = ДанныеОбИспользованииОтгулов.Организация
		|			И ДниЧасыОтгулов.Сотрудник = ДанныеОбИспользованииОтгулов.Сотрудник
		|ГДЕ
		|	ДанныеОбИспользованииОтгулов.Сотрудник ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1000
		|	РегистраторыОтработанныхДней.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТВыбранныеДокументы
		|ИЗ
		|	ВТРегистраторыОтработанныхДней КАК РегистраторыОтработанныхДней
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыбранныеДокументы.Регистратор КАК Регистратор
		|ИЗ
		|	ВТВыбранныеДокументы КАК ВыбранныеДокументы";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДниЧасыОтгулов.Период КАК Период,
		|	ДниЧасыОтгулов.Регистратор КАК Регистратор,
		|	ДниЧасыОтгулов.ВидДвижения КАК ВидДвижения,
		|	ДниЧасыОтгулов.Организация КАК Организация,
		|	ДниЧасыОтгулов.Сотрудник КАК Сотрудник,
		|	ДниЧасыОтгулов.Период КАК ОтработанныйДень,
		|	ДниЧасыОтгулов.Дни КАК Дни
		|ИЗ
		|	РегистрНакопления.ДниЧасыОтгулов КАК ДниЧасыОтгулов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВыбранныеДокументы КАК ВыбранныеДокументы
		|		ПО ДниЧасыОтгулов.Регистратор = ВыбранныеДокументы.Регистратор
		|			И (ДниЧасыОтгулов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
		|			И (ДниЧасыОтгулов.Период >= &НачалоУчета)
		|			И (ДниЧасыОтгулов.Дни > 0)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл 
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.ДанныеОбИспользованииОтгулов.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		НаборЗаписей = РегистрыНакопления.ДанныеОбИспользованииОтгулов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		Пока Выборка.Следующий() Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьТаблицуДанныеОбИспользованииОтгуловВПереносДанных(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	ИмяТаблицыРегистра = "ДанныеОбИспользованииОтгулов";
	Запрос.УстановитьПараметр("ИмяТаблицыРегистра", ИмяТаблицыРегистра);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеОбИспользованииОтгулов.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТРегистраторы
		|ИЗ
		|	РегистрНакопления.ДанныеОбИспользованииОтгулов КАК ДанныеОбИспользованииОтгулов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПереносДанных КАК ПереносДанных
		|		ПО ДанныеОбИспользованииОтгулов.Регистратор = ПереносДанных.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Регистраторы.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТОбработанныеРегистраторы
		|ИЗ
		|	ВТРегистраторы КАК Регистраторы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПереносДанных.ТаблицаРегистров КАК ТаблицаРегистров
		|		ПО Регистраторы.Регистратор = ТаблицаРегистров.Ссылка
		|			И (ТаблицаРегистров.Имя = &ИмяТаблицыРегистра)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1000
		|	Регистраторы.Регистратор КАК Регистратор
		|ИЗ
		|	ВТРегистраторы КАК Регистраторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОбработанныеРегистраторы КАК ОбработанныеРегистраторы
		|		ПО Регистраторы.Регистратор = ОбработанныеРегистраторы.Регистратор
		|ГДЕ
		|	ОбработанныеРегистраторы.Регистратор ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Пока Выборка.Следующий() Цикл 
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Документ.ПереносДанных", "Ссылка", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = Выборка.Регистратор.ПолучитьОбъект();
		
		НоваяСтрока = ДокументОбъект.ТаблицаРегистров.Добавить();
		НоваяСтрока.Имя = ИмяТаблицыРегистра;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьОтработанныеЧасы(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НачалоУчета", УчетОтгулов.НачалоУчетаОтработанныхДней());
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДниЧасыОтгулов.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТРегистраторыОтработанныхДней
		|ИЗ
		|	РегистрНакопления.ДниЧасыОтгулов КАК ДниЧасыОтгулов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаботаВВыходныеИПраздничныеДни КАК РаботаВВыходныеИПраздничныеДни
		|		ПО ДниЧасыОтгулов.Регистратор = РаботаВВыходныеИПраздничныеДни.Ссылка
		|			И (ДниЧасыОтгулов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
		|			И (ДниЧасыОтгулов.Период >= &НачалоУчета)
		|			И (ДниЧасыОтгулов.Часы > 0)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеОбИспользованииОтгулов КАК ДанныеОбИспользованииОтгулов
		|		ПО ДниЧасыОтгулов.Период = ДанныеОбИспользованииОтгулов.Период
		|			И ДниЧасыОтгулов.Регистратор = ДанныеОбИспользованииОтгулов.Регистратор
		|			И ДниЧасыОтгулов.ВидДвижения = ДанныеОбИспользованииОтгулов.ВидДвижения
		|			И ДниЧасыОтгулов.Организация = ДанныеОбИспользованииОтгулов.Организация
		|			И ДниЧасыОтгулов.Сотрудник = ДанныеОбИспользованииОтгулов.Сотрудник
		|ГДЕ
		|	ДанныеОбИспользованииОтгулов.Сотрудник ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1000
		|	РегистраторыОтработанныхДней.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТВыбранныеДокументы
		|ИЗ
		|	ВТРегистраторыОтработанныхДней КАК РегистраторыОтработанныхДней
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыбранныеДокументы.Регистратор КАК Регистратор
		|ИЗ
		|	ВТВыбранныеДокументы КАК ВыбранныеДокументы";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДниЧасыОтгулов.Период КАК Период,
		|	ДниЧасыОтгулов.Регистратор КАК Регистратор,
		|	ДниЧасыОтгулов.ВидДвижения КАК ВидДвижения,
		|	ДниЧасыОтгулов.Организация КАК Организация,
		|	ДниЧасыОтгулов.Сотрудник КАК Сотрудник,
		|	ДниЧасыОтгулов.Период КАК ОтработанныйДень,
		|	ДниЧасыОтгулов.Часы КАК Часы
		|ИЗ
		|	РегистрНакопления.ДниЧасыОтгулов КАК ДниЧасыОтгулов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВыбранныеДокументы КАК ВыбранныеДокументы
		|		ПО ДниЧасыОтгулов.Регистратор = ВыбранныеДокументы.Регистратор
		|			И (ДниЧасыОтгулов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
		|			И (ДниЧасыОтгулов.Период >= &НачалоУчета)
		|			И (ДниЧасыОтгулов.Часы > 0)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл 
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрНакопления.ДанныеОбИспользованииОтгулов.НаборЗаписей", "Регистратор", Выборка.Регистратор) Тогда
			Продолжить;
		КонецЕсли;
		НаборЗаписей = РегистрыНакопления.ДанныеОбИспользованииОтгулов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		Пока Выборка.Следующий() Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
