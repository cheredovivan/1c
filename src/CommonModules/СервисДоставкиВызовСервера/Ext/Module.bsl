////////////////////////////////////////////////////////////////////////////////
// Подсистема "Сервис доставки".
// ОбщийМодуль.СервисДоставкиВызовСервера.
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Имя формы выбора метаданных.
//
// Параметры:
//  ИмяОпределяемогоТипа - Строка - наименование определяемого типа.
// 
// Возвращаемое значение:
//  Строка - имя формы выбора.
//
Функция ИмяФормыВыбораПоОпределяемомуТипу(ИмяОпределяемогоТипа) Экспорт
	
	ОпределяемыйТип = Метаданные.ОпределяемыеТипы.Найти(ИмяОпределяемогоТипа);
	Если ОпределяемыйТип <> Неопределено Тогда
		
		ИмяФормы = "";
		Тип = ОпределяемыйТип.Тип.Типы()[0];
		Менеджер = Метаданные.НайтиПоТипу(Тип);
		Если Справочники.ТипВсеСсылки().СодержитТип(Тип) Тогда
			ИмяФормы = "Справочник." + Менеджер.Имя + ".ФормаВыбора";
		ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(Тип) Тогда
			ИмяФормы = "Документ." + Менеджер.Имя + ".ФормаВыбора";
		КонецЕсли;
		
		Возврат ИмяФормы;
	КонецЕсли;
	
КонецФункции

// Имя формы выбора метаданных.
// 
// Параметры:
//  ИмяОпределяемогоТипа - Строка - наименование определяемого типа.
//  Значение - Неопределено, Произвольный - Значение
// 
// Возвращаемое значение:
//  Строка - имя формы выбора.
Функция ИмяФормыОбъектаПоОпределяемомуТипу(ИмяОпределяемогоТипа, Значение = Неопределено) Экспорт
	
	ИмяФормы = "";
	
	ОпределяемыйТип = Метаданные.ОпределяемыеТипы.Найти(ИмяОпределяемогоТипа);
	Если ОпределяемыйТип <> Неопределено Тогда
		
		ЗначениеТипа = ТипЗнч(Значение);
		
		Если ЗначениеЗаполнено(Значение) И ОпределяемыйТип.Тип.СодержитТип(ЗначениеТипа) Тогда
			Тип = ЗначениеТипа;
		Иначе
			Тип = ОпределяемыйТип.Тип.Типы()[0];
		КонецЕсли;
		
		Менеджер = Метаданные.НайтиПоТипу(Тип);
		Если Справочники.ТипВсеСсылки().СодержитТип(Тип) Тогда
			ИмяФормы = "Справочник." + Менеджер.Имя + ".ФормаОбъекта";
		ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(Тип) Тогда
			ИмяФормы = "Документ." + Менеджер.Имя + ".ФормаОбъекта";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИмяФормы;
	
КонецФункции

Функция ОрганизацияПоУмолчанию(ТипГрузоперевозки) Экспорт
	
	Возврат СервисДоставки.ОрганизацияПоУмолчанию(ТипГрузоперевозки);
	
КонецФункции

Функция ОрганизацияПодключена(Организация) Экспорт
	
	Возврат СервисДоставкиСлужебный.ОрганизацияПодключена(Организация)
	
КонецФункции

Функция СохранитьНастройкиФормыСпискаЗаказов(Настройки) Экспорт
	
	КлючНастроекФормы = "Обработка.СервисДоставки.Форма.СписокЗаказов/ТекущиеДанные";
	ХранилищеСистемныхНастроек.Сохранить(КлючНастроекФормы, , Настройки);
	
	Возврат Настройки;
	
КонецФункции

Функция КонтактнаяИнформацияПоПредставлению(Представление, ТипКонтактнойИнформацииИмя) Экспорт
	
	Если ТипКонтактнойИнформацииИмя = "Телефон" Тогда
		ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Телефон;
	Иначе
		ТипКонтактнойИнформации = Перечисления.ТипыКонтактнойИнформации.Адрес;
	КонецЕсли;
	
	Возврат УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Представление, ТипКонтактнойИнформации);
	
КонецФункции

Функция АдресСДополнительнымиПолями(Тип, Адрес) Экспорт
	
	Если Тип <> Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
		Возврат Адрес;
	КонецЕсли;
	
	Если Адрес = "" Тогда
		СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(Адрес);
		СведенияОбАдресе.ТипАдреса = РаботаСАдресамиКлиентСервер.АдминистративноТерриториальныйАдрес();
		Адрес = РаботаСАдресами.ПоляАдресаВJSON(СведенияОбАдресе);
	КонецЕсли;
	
	ПараметрыАдреса = СервисДоставки.ЗначениеИзСтрокиJSON(Адрес);
	
	Если ПараметрыАдреса.addressType = ""
		ИЛИ ПараметрыАдреса.addressType = "ВСвободнойФорме" Тогда
		ПараметрыАдреса.addressType = РаботаСАдресамиКлиентСервер.АдминистративноТерриториальныйАдрес();
	КонецЕсли;
	
	ДополнительныеПараметры = Новый ТаблицаЗначений();
	ДополнительныеПараметры.Колонки.Добавить("Тип", Новый ОписаниеТипов("Строка"));
	ДополнительныеПараметры.Колонки.Добавить("Значение", Новый ОписаниеТипов("Строка"));
	НоваяСтрока = ДополнительныеПараметры.Добавить();
	НоваяСтрока.Тип = НСтр("ru = 'Квартира';
							|en = 'Apartment'");
	НоваяСтрока = ДополнительныеПараметры.Добавить();
	НоваяСтрока.Тип = НСтр("ru = 'Этаж';
							|en = 'Floor'");
	НоваяСтрока = ДополнительныеПараметры.Добавить();
	НоваяСтрока.Тип = НСтр("ru = 'Подъезд';
							|en = 'Entrance'");
	НоваяСтрока = ДополнительныеПараметры.Добавить();
	НоваяСтрока.Тип = НСтр("ru = 'Код домофона';
							|en = 'Doorphone code'");
	
	Помещения = Новый Массив();
	Если ПараметрыАдреса.Свойство("apartments") Тогда
		Помещения = ПараметрыАдреса.apartments;
	КонецЕсли;
	
	МассивУдаления = Новый Массив();
	Ид = Помещения.Количество()-1;
	Пока Ид >= 0 Цикл
		СтрокаПараметра = ДополнительныеПараметры.Найти(Помещения[Ид].type, "Тип");
		Если СтрокаПараметра <> Неопределено Тогда
			СтрокаПараметра.Значение = Помещения[Ид].number;
			МассивУдаления.Добавить(Ид);
		КонецЕсли;
		Ид = Ид - 1;
	КонецЦикла;
	
	Для Каждого ТекИндекс Из МассивУдаления Цикл
		Помещения.Удалить(ТекИндекс);
	КонецЦикла;
	
	Для Каждого ТекСтрока Из ДополнительныеПараметры Цикл
		Параметр = Новый Структура("type,number", ТекСтрока.Тип, ТекСтрока.Значение);
		Помещения.Добавить(Параметр);
	КонецЦикла;
	
	ПараметрыАдреса.Вставить("apartments", Помещения);
	
	Возврат СервисДоставки.ЗначениеВСтрокуJSON(ПараметрыАдреса);
	
КонецФункции

Процедура ПроверитьПодключениеИнтернетПоддержки(ЕстьПодключениеКСервису) Экспорт
	
	СервисДоставки.ПроверитьПодключениеИнтернетПоддержки(ЕстьПодключениеКСервису);
	
КонецПроцедуры

Процедура ЗаполнитьНастройкиОбщиеСервисДоставкиПоУмолчанию(ТипГрузоперевозки) Экспорт
	СервисДоставки.ЗаполнитьНастройкиОбщиеСервисДоставкиПоУмолчанию(ТипГрузоперевозки);
КонецПроцедуры

// См. СервисДоставки.ПараметрыСервисаДоставки
Процедура ПараметрыСервисаДоставки(ПараметрыФормы) Экспорт
	СервисДоставки.ПараметрыСервисаДоставки(ПараметрыФормы);
КонецПроцедуры

// Доступные сервисы доставки.
// 
// Возвращаемое значение:
//  Массив Из Число - Доступные сервисы доставки
Функция ДоступныеСервисыДоставки() Экспорт
	
	ДоступныеСервисы = Новый Массив();
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСервис1СДоставка") = Истина Тогда
		ДоступныеСервисы.Добавить(СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СДоставка());
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСервис1СКурьер") = Истина Тогда
		ДоступныеСервисы.Добавить(СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьер());
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСервис1СКурьерика") = Истина Тогда
		ДоступныеСервисы.Добавить(СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьерика());
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСервис1ССДЭК") = Истина Тогда
		ДоступныеСервисы.Добавить(СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1ССДЭК());
	КонецЕсли;

	Возврат ДоступныеСервисы;
	
КонецФункции

// Устанавливает заголовок информационной надписи с состоянием опции Курьерика.
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта.
// 
Процедура УстановитьЗаголовокОпцияКурьерика(Форма) Экспорт
	
	АдресСтраницы = СервисДоставкиКлиентСервер.АдресСтраницыОпцияКурьерика();
	
	Если ЗначениеЗаполнено(Форма.КомментарийОпцияКурьерика) Тогда
		ТекстКомментария = СтрШаблон(" (%1)", Форма.КомментарийОпцияКурьерика);
	Иначе
		ТекстКомментария = "";
	КонецЕсли;
	
	МассивПараметров = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("УведомлятьОСостоянииПодписки");
	МассивПараметров.Добавить("КоличествоКалендарныхДней");
	МассивПараметров.Добавить("ОстатокДоступныхЗаказов");
		
	НастройкиПодсистемы = РегистрыСведений.НастройкиОбщиеСервисДоставки.ПолучитьЗначенияНастроек(МассивПараметров, Форма.ТипГрузоперевозки);
	
	УведомлятьОСостоянииПодписки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиПодсистемы,
		"УведомлятьОСостоянииПодписки", 0);
	КоличествоКалендарныхДней = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиПодсистемы,
		"КоличествоКалендарныхДней", 0);
	ОстатокДоступныхЗаказов = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НастройкиПодсистемы,
		"ОстатокДоступныхЗаказов", 0);
	
	СвойстваФормы = Новый Структура("ДоступенТестовыйПериод, ОпцияДоступнаКурьерика");
	ЗаполнитьЗначенияСвойств(СвойстваФормы, Форма);
	
	ТекстТестовогоПериода = "";
	
	Если СвойстваФормы.ОпцияДоступнаКурьерика = Истина Тогда
		
		Если УведомлятьОСостоянииПодписки
			И Форма.БалансОпцияКурьерика > 0
			И ОстатокДоступныхЗаказов > 0
			И Форма.БалансОпцияКурьерика <= ОстатокДоступныхЗаказов Тогда
			
			Картинка = БиблиотекаКартинок.ЖелтыйШарБЭД;
			ТекстСостояния = СтрШаблон(НСтр("ru = 'истекает. Доступно заказов: %1.';
											|en = 'expires. Available orders: %1.'"),
				Формат(Форма.БалансОпцияКурьерика, НСтр("ru = 'ЧДЦ=0; ЧН=; ЧГ=';
														|en = 'ЧДЦ=0; ЧН=; ЧГ='")));
			Форма.ОтображатьУведомлениеПоОпцииКурьерика = Истина;
			
		ИначеЕсли УведомлятьОСостоянииПодписки
			И ЗначениеЗаполнено(Форма.СрокДействияОпцияКурьерика)
			И КоличествоКалендарныхДней > 0
			И ТекущаяДатаСеанса() + КоличествоКалендарныхДней * 86400 >= Форма.СрокДействияОпцияКурьерика Тогда
			
			Картинка = БиблиотекаКартинок.ЖелтыйШарБЭД;
			ТекстСостояния = СтрШаблон(НСтр("ru = 'истекает %1.';
											|en = 'expires on %1.'"),
				Формат(Форма.СрокДействияОпцияКурьерика, "ДЛФ=Д"));
			Форма.ОтображатьУведомлениеПоОпцииКурьерика = Истина;
			
		Иначе
			
			Картинка = БиблиотекаКартинок.ЗеленыйШарБЭД;
			ТекстСостояния = НСтр("ru = 'активна';
									|en = 'active'");
			Форма.ОтображатьУведомлениеПоОпцииКурьерика = Ложь;
			
		КонецЕсли;
		
	Иначе
		
		Картинка = БиблиотекаКартинок.КрасныйШарБЭД;
		ТекстСостояния = НСтр("ru = 'неактивна';
								|en = 'inactive'");
		Форма.ОтображатьУведомлениеПоОпцииКурьерика = Истина;
		
		Если СвойстваФормы.ДоступенТестовыйПериод = Истина Тогда
			ЧастиТекстаТестовогоПериода = Новый массив;
			ЧастиТекстаТестовогоПериода.Добавить(" - ");
			ЧастиТекстаТестовогоПериода.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Подключить';
																					|en = 'Connect'"), ,
				ЦветаСтиля.ГиперссылкаЦвет, , "ПодключитьТестовыйПериод"));
			ЧастиТекстаТестовогоПериода.Добавить(" ");
			ЧастиТекстаТестовогоПериода.Добавить(НСтр("ru = 'тестовый период';
														|en = 'trial period'"));
			ТекстТестовогоПериода = Новый ФорматированнаяСтрока(ЧастиТекстаТестовогоПериода);
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстСостоянияПодписки = СтроковыеФункции.ФорматированнаяСтрока(СтрШаблон(
		НСтр("ru = '<a href = ""%1"">Подписка</a> %2%3';
			|en = '<a href = ""%1"">Subscription</a> %2%3'"),
		АдресСтраницы,
		ТекстСостояния,
		ТекстКомментария));
	
	ТекстОбновитьСостояние = Новый ФорматированнаяСтрока(НСтр("ru = 'Обновить состояние';
																|en = 'Refresh state'"), ,
		ЦветаСтиля.ГиперссылкаЦвет, , "ОбновитьСостояние");
	
	ЧастиТекстаЗаголовка = Новый Массив;
	ЧастиТекстаЗаголовка.Добавить(ТекстСостоянияПодписки);
	Если ТекстТестовогоПериода <> "" Тогда
		ЧастиТекстаЗаголовка.Добавить(" ");
		ЧастиТекстаЗаголовка.Добавить(ТекстТестовогоПериода);
	КонецЕсли;
	ЧастиТекстаЗаголовка.Добавить(" ");
	ЧастиТекстаЗаголовка.Добавить(ТекстОбновитьСостояние);
	
	ТекстЗаголовка = Новый ФорматированнаяСтрока(ЧастиТекстаЗаголовка);
	
	Форма.Элементы.ДекорацияКартинкаСостояниеОпцияКурьерика.Картинка = Картинка;
	Форма.Элементы.ДекорацияСостояниеОпцияКурьерика.Заголовок = ТекстЗаголовка;
	Форма.Элементы.ДекорацияПолучениеИнформацииПоОпции.Видимость = Ложь;
	Форма.Элементы.ДекорацияКартинкаСостояниеОпцияКурьерика.Видимость = Истина;
	Форма.Элементы.ДекорацияСостояниеОпцияКурьерика.Видимость = Истина;
	
КонецПроцедуры

// Ключ фонового задания.
// 
// Параметры:
//  МассивДанных - Массив Из Произвольный - Параметры операции
// 
// Возвращаемое значение:
//  Строка - Ключ фонового задания
Функция КлючФоновогоЗадания(МассивДанных) Экспорт
	
	МассивДанных.Добавить(Пользователи.ТекущийПользователь());
	
	Возврат ОбщегоНазначения.КонтрольнаяСуммаСтрокой(МассивДанных);
	
КонецФункции

// Цвет подсветки найденной подстроки. Используется в форматированных строках
// 
// Возвращаемое значение:
//  Цвет - ЦветСтиля - Цвет
Функция ЦветПодстроки() Экспорт
	
	Возврат ЦветаСтиля.ЦветТекстаПоложительногоЗначения;
	
КонецФункции

// Возвращает данные с количеством заказов на доставку и возможностью использования способа доставки из документа-основания
// 
// Параметры:
//	ДокументыОснования - Массив Из ОпределяемыйТип.ОснованиеЗаказаСервисДоставки
//	ПараметрыПроверки - Структура - содержит ключи:
//	 * ПроверитьСпособДоставки - Булево - 
//	 * ПолучитьКоличествоЗаказов - Булево - 
// 
// Возвращаемое значение:
//  См. СервисДоставкиСлужебный.ДанныеПроверкиОснованияЗаказаНаДоставку
Функция ДанныеПроверкиОснованияЗаказаНаДоставку(ДокументыОснования, ПараметрыПроверки) Экспорт
	Возврат СервисДоставкиСлужебный.ДанныеПроверкиОснованияЗаказаНаДоставку(ДокументыОснования, ПараметрыПроверки);
КонецФункции

#КонецОбласти
