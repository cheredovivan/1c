// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// См. ВыгрузкаЗагрузкаДанныхПереопределяемый.ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузки
//
Процедура ПриЗаполненииТиповИсключаемыхИзВыгрузкиЗагрузки(Типы) Экспорт
	
	Типы.Добавить(Метаданные.Константы.ИспользоватьАвтоматическоеПолучениеИзСервисаЭДО);
	Типы.Добавить(Метаданные.Константы.КлючДоступаКСервисуЭДО);
	Типы.Добавить(Метаданные.РегистрыСведений.ИспользованиеКлючаДоступаКСервисуЭДО);
	
КонецПроцедуры

// См. РегламентныеЗаданияПереопределяемый.ПриОпределенииНастроекРегламентныхЗаданий
// 
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	
	НоваяСтрока = Настройки.Добавить();
	НоваяСтрока.РегламентноеЗадание = МетаданныеРегламентногоЗадания();
	НоваяСтрока.ФункциональнаяОпция = Метаданные.ФункциональныеОпции.ИспользоватьОбменЭД;
	НоваяСтрока.Параметризуется = Истина;
	НоваяСтрока.РаботаетСВнешнимиРесурсами = Истина;
	НоваяСтрока.ДоступноВМоделиСервиса = Истина;
	НоваяСтрока.ВключатьПриВключенииФункциональнойОпции = Ложь;
	
КонецПроцедуры

// См. ОчередьЗаданийПереопределяемый.ПриОпределенииПсевдонимовОбработчиков
//
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить(МетаданныеРегламентногоЗадания().ИмяМетода);
	
КонецПроцедуры

// См. ОчередьЗаданийПереопределяемый.ПриПолученииСпискаШаблонов 
Процедура ПриПолученииСпискаШаблонов(ШаблоныЗаданий) Экспорт
	
	ШаблоныЗаданий.Добавить(АвтоматическоеПолучениеЭДОКлиентСервер.ИмяРегламентногоЗадания());
	
КонецПроцедуры

// См. ОбщегоНазначенияПереопределяемый.ПриПериодическомПолученииДанныхКлиентаНаСервере
//
Процедура ПриПериодическомПолученииДанныхКлиентаНаСервере(Параметры, Результаты) Экспорт
	
	ПараметрыОтправки = Параметры[АвтоматическоеПолучениеЭДОКлиентСервер.КлючПараметраПериодическойОтправкиДанных()]; // См. АвтоматическоеПолучениеЭДОКлиент.НовыеПараметрыПериодическойОтправкиДанных
	Если ПараметрыОтправки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Результат = НовыйРезультатПериодическойОтправкиДанных();
	Результаты.Вставить(АвтоматическоеПолучениеЭДОКлиентСервер.КлючРезультатаПериодическойОтправкиДанных(), Результат);
	Результат.Включено = ?(ПараметрыОтправки.Включено = Неопределено,
		ИспользоватьПолучениеЧерезИнтервал(), ПараметрыОтправки.Включено);
	
	Если Не Результат.Включено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТребуетсяПолучениеЧерезИнтервал() Тогда
		Если Не ПараметрыОтправки.ИспользоватьУведомленияКлиента Тогда
			РезультатПолучения = НовыйРезультатПолучения();
			Результат.РезультатПолучения = РезультатПолучения;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Попытка
		Если ПараметрыОтправки.ИспользоватьУведомленияКлиента Тогда
			ФоновыеЗадания.Выполнить("АвтоматическоеПолучениеЭДО.ПолучитьЧерезИнтервал",, КлючЗадания());
		Иначе
			ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор());
			ПараметрыВыполненияВФоне.ОжидатьЗавершение = 0;
			ПараметрыВыполненияВФоне.КлючФоновогоЗадания = КлючЗадания();
			Результат.ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
				"АвтоматическоеПолучениеЭДО.ПолучитьЧерезИнтервал");
		КонецЕсли;
	Исключение
		Подсистема = ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().АвтоматическоеПолучениеЭДО;
		ТекстСообщения = НСтр("ru = 'Не удалось запустить автоматическое получение ЭДО через интервал по причине:';
								|en = 'Cannot start automatic EDI receipt via interval due to:'")
			+ Символы.ПС + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияБЭД.ЗаписатьВЖурналРегистрации(ТекстСообщения, Подсистема);
	КонецПопытки;
	
КонецПроцедуры

// Параметры:
//  ОписаниеУчетнойЗаписи - см. УчетныеЗаписиЭДОКлиентСервер.НовоеОписаниеУчетнойЗаписи
Процедура ПриСозданииУчетнойЗаписиЭДО(ОписаниеУчетнойЗаписи) Экспорт
	
	Если ОписаниеУчетнойЗаписи.ЭтоОблачныйЭДО Тогда
		УстановитьАктивностьРегламентногоЗадания(Истина);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ИдентификаторЭДО - Строка
//  СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД
//  ЭтоОблачныйЭДО - Булево
Процедура ПриУдаленииУчетнойЗаписи(ИдентификаторЭДО, СпособОбмена, ЭтоОблачныйЭДО) Экспорт
	
	Если СпособОбмена <> Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО
		И Не ЭтоОблачныйЭДО Тогда
		Возврат;
	КонецЕсли;
	
	ОтключитьИспользованиеКлючаДоступаКСервисуЭДО(ИдентификаторЭДО);
	
	РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.Удалить(ИдентификаторЭДО);
	
	Если Не ЕстьУчетныеЗаписиДляПолучения() Тогда
		УстановитьАктивностьРегламентногоЗадания(Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  Значение - Булево
Процедура ПриЗаписиКонстантыИспользоватьОбменЭД(Значение) Экспорт
	
	Если Не Значение Тогда
		Отключить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РегламентноеЗадание

// Возвращаемое значение:
//  Строка
Функция КлючЗадания() Экспорт
	Возврат "АвтоматическоеПолучениеЭДО";
КонецФункции

#КонецОбласти

#Область Использование

// Возвращаемое значение:
//  Булево
Функция Использовать() Экспорт
	
	Возврат АвтоматическоеПолучениеЭДОПовтИсп.ИспользоватьПолучениеПоКлючуДоступа()
		Или АвтоматическоеПолучениеЭДОПовтИсп.ИспользоватьПолучениеПоТокенамДоступа()
		Или НастройкиЭДО.ИспользоватьИнтеграциюОблачногоЭДО();
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * СостояниеПоИдентификаторамЭДО - Соответствие из КлючИЗначение:
//    ** Ключ - Строка - Идентификатор учетной записи ЭДО.
//    ** Значение - См. УстановитьИспользованиеКлючаДоступаКСервисуЭДО
//  * КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
Функция НовыйРезультатВключенияПоУчетнымЗаписямЭДО() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СостояниеПоИдентификаторамЭДО", Новый Соответствие);
	Результат.Вставить("КонтекстДиагностики", ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики());
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ИдентификаторыЭДО - Массив из Строка
//  РасшифрованныеМаркеры - см. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
//  
// Возвращаемое значение:
//  См. НовыйРезультатВключенияПоУчетнымЗаписямЭДО
//
Функция ВключитьПоУчетнымЗаписямЭДО(ИдентификаторыЭДО, РасшифрованныеМаркеры) Экспорт
	
	РезультатВключения = НовыйРезультатВключенияПоУчетнымЗаписямЭДО();
	
	Если Не НастройкиЭДО.ЕстьПравоНастройкиОбмена(Истина) Тогда
		Возврат РезультатВключения;
	КонецЕсли;
	
	ИдентификаторыПоИнтеграциямЭДО = УчетныеЗаписиЭДО.ИдентификаторыПоИнтеграциямЭДО(ИдентификаторыЭДО);
	
	Если ЗначениеЗаполнено(ИдентификаторыПоИнтеграциямЭДО.ИдентификаторыДляЛокальногоЭДО) Тогда
		
		РасшифрованныеМаркерыДляЛокальногоЭДО = ВыбратьРасшифрованныеМаркеры(РасшифрованныеМаркеры, ИдентификаторыЭДО);
		
		Если АвтоматическоеПолучениеЭДОПовтИсп.ИспользоватьПолучениеПоКлючуДоступа() Тогда 
			
			РезультатВключения = ВключитьИспользованиеКлючаДоступаКСервисуЭДО(ИдентификаторыЭДО,
				РасшифрованныеМаркерыДляЛокальногоЭДО);
			
		ИначеЕсли АвтоматическоеПолучениеЭДОПовтИсп.ИспользоватьПолучениеПоТокенамДоступа() Тогда
			
			РезультатВключения = ВключитьИспользованиеТокеновДоступаПоУчетнымЗаписямЭДО(ИдентификаторыЭДО,
				РасшифрованныеМаркерыДляЛокальногоЭДО);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторыПоИнтеграциямЭДО.ИдентификаторыДляОблачногоЭДО)
		И НастройкиЭДО.ИспользоватьИнтеграциюОблачногоЭДО() Тогда
		
		РасшифрованныеМаркерыДляОблачногоЭДО = ВыбратьРасшифрованныеМаркеры(РасшифрованныеМаркеры, ИдентификаторыЭДО);
		
		ОбщийМодульАвтоматическоеПолучениеЭДОИнтеграцияОблака = ОбщегоНазначения.ОбщийМодуль(
			"АвтоматическоеПолучениеЭДОИнтеграцияОблака");
			
		РезультатВключенияВОблаке = ОбщийМодульАвтоматическоеПолучениеЭДОИнтеграцияОблака.ВключитьПоУчетнымЗаписямЭДО(
			ИдентификаторыПоИнтеграциямЭДО.ИдентификаторыДляОблачногоЭДО, РасшифрованныеМаркерыДляОблачногоЭДО);
		
		ДополнитьРезультатВключения(РезультатВключения, РезультатВключенияВОблаке);
	КонецЕсли;
	
	Возврат РезультатВключения;
	
КонецФункции

// Параметры:
//  ИдентификаторЭДО - Неопределено,Строка
Процедура Отключить(ИдентификаторЭДО = Неопределено) Экспорт
	
	Если Не НастройкиЭДО.ЕстьПравоНастройкиОбмена(Истина) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если АвтоматическоеПолучениеЭДОПовтИсп.ИспользоватьПолучениеПоКлючуДоступа() Тогда
		
		ОтключитьИспользованиеКлючаДоступаКСервисуЭДО(ИдентификаторЭДО);
		
	ИначеЕсли АвтоматическоеПолучениеЭДОПовтИсп.ИспользоватьПолучениеПоТокенамДоступа() Тогда
		
		ОтключитьИспользованиеТокеновДоступаКСервисуЭДО(ИдентификаторЭДО);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // Использование

#Область КлючДоступаКСервисуЭДО

// Параметры:
//  ПараметрыВыполненияОперации - см. СервисЭДО.НовыеПараметрыВыполненияОперацииСервиса
//  КлючСинхронизации - См. СинхронизацияЭДОКлиентСервер.НовыйКлючСинхронизации
// 
// Возвращаемое значение:
//  Булево
Функция ДобавитьЗаголовокАвторизацииПоКлючуДоступа(ПараметрыВыполненияОперации, КлючСинхронизации) Экспорт
	
	Если Не ЗначениеЗаполнено(КлючСинхронизации.КлючДоступаКСервисуЭДО) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Использование = РегистрыСведений.ИспользованиеКлючаДоступаКСервисуЭДО.ЕстьИдентификаторЭДО(
		КлючСинхронизации.ИдентификаторУчетнойЗаписи);
	Если Не Использование Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Комментарий = СтрШаблон(НСтр("ru = 'Выполнение запроса по ключу доступа к сервису ЭДО.
		|Операция: %1
		|Метод: %2
		|Идентификатор ЭДО: %3';
		|en = 'Query execution using the access key to the EDI service.
		|Operation: %1
		|Method: %2
		|EDI ID: %3'"),
		ПараметрыВыполненияОперации.ПредставлениеОперации,
		ПараметрыВыполненияОперации.АдресРесурса,
		КлючСинхронизации.ИдентификаторУчетнойЗаписи);
	
	ОбщегоНазначенияБЭД.ЗаписатьВЖурналРегистрации(Комментарий,
		ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().АвтоматическоеПолучениеЭДО,
		УровеньЖурналаРегистрации.Информация);
	
	ПараметрыВыполненияОперации.Заголовки.Вставить("Api-Key", КлючСинхронизации.КлючДоступаКСервисуЭДО);
	
	Возврат Истина;
КонецФункции

#КонецОбласти

#Область РаботаСФормами

#Если Не ВнешнееСоединение Тогда

// Возвращаемое значение:
//  Структура:
//  * ГруппаПредупреждение - Неопределено,ГруппаФормы
//  * Список - Неопределено,ТаблицаФормы
Функция НовыеПараметрыФормыСпискаПриСозданииНаСервере() Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("ГруппаПредупреждение", Неопределено);
	Параметры.Вставить("Список", Неопределено);
	Возврат Параметры;
КонецФункции

// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//  * ПараметрыАвтоматическогоПолученияЭДО - Произвольный
//  Параметры - См. НовыеПараметрыФормыСпискаПриСозданииНаСервере
Процедура ФормаСпискаПриСозданииНаСервере(Форма, Параметры) Экспорт
	ПараметрыРеквизита = НовыеПараметрыРеквизитаФормы();
	Если Параметры.ГруппаПредупреждение <> Неопределено Тогда
		ПараметрыРеквизита.ИмяГруппыПредупреждение = Параметры.ГруппаПредупреждение.Имя;
	КонецЕсли;
	Если Параметры.Список <> Неопределено Тогда
		ПараметрыРеквизита.ИмяСписка = Параметры.Список.Имя;
	КонецЕсли;
	Форма.ПараметрыАвтоматическогоПолученияЭДО = ПараметрыРеквизита;
	ЕстьПредупреждения = РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.ЕстьПредупреждения();
	АвтоматическоеПолучениеЭДОКлиентСервер.УстановитьВидимостьПредупрежденияВФормеСписка(
		Форма, ЕстьПредупреждения, ПараметрыРеквизита);
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
//  * ГруппаПредупреждение - Неопределено,ГруппаФормы
//  * ИдентификаторЭДО - Строка
Функция НовыеПараметрыФормыЭлементаПриЧтенииНаСервере() Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("ГруппаПредупреждение", Неопределено);
	Параметры.Вставить("ИдентификаторЭДО", "");
	Возврат Параметры;
КонецФункции

// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//  * ПараметрыАвтоматическогоПолученияЭДО - Произвольный
//  Параметры - См. НовыеПараметрыФормыЭлементаПриЧтенииНаСервере
Процедура ФормаЭлементаПриЧтенииНаСервере(Форма, Параметры) Экспорт
	ПараметрыРеквизита = НовыеПараметрыРеквизитаФормы();
	ПараметрыРеквизита.ИмяГруппыПредупреждение = Параметры.ГруппаПредупреждение.Имя;
	ПараметрыРеквизита.ИдентификаторЭДО = Параметры.ИдентификаторЭДО;
	Форма.ПараметрыАвтоматическогоПолученияЭДО = ПараметрыРеквизита;
	Состояние = РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.СостояниеПоИдентификаторуЭДО(
		ПараметрыРеквизита.ИдентификаторЭДО);
	АвтоматическоеПолучениеЭДОКлиентСервер.УстановитьВидимостьПредупрежденияВФормеЭлемента(
		Форма, Состояние, ПараметрыРеквизита);
КонецПроцедуры

#КонецЕсли

#КонецОбласти

#Область Предупреждение

// Параметры:
//  Состояние - ДанныеФормыСтруктура,Структура:
//  * ЕстьОшибки - Булево
//  * НомерПопытки - Число
//  * Остановлено - Булево
// 
// Возвращаемое значение:
//  Строка
Функция ПредупреждениеВСтрокеСписка(Состояние) Экспорт
	Если Состояние.ЕстьОшибки Или Состояние.Остановлено Тогда
		Возврат НСтр("ru = 'Не удалось выполнить автоматическое получение.';
					|en = 'Cannot execute automatic receipt.'");
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции

#КонецОбласти

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

#Область РегламентноеЗадание

// Параметры:
//  Использование - Булево
Процедура УстановитьАктивностьРегламентногоЗадания(Использование)
	УстановитьПривилегированныйРежим(Истина);
	ВыполняемоеЗадание = МетаданныеРегламентногоЗадания();
	ОбщегоНазначенияБЭД.УстановитьАктивностьРегламентногоЗадания(ВыполняемоеЗадание, Использование);
КонецПроцедуры

// Возвращаемое значение:
//  Число
Функция ИнтервалЗавершенияРегламентногоЗадания()
	Возврат 3600;
КонецФункции

// Возвращаемое значение:
//  ОбъектМетаданныхРегламентноеЗадание
Функция МетаданныеРегламентногоЗадания()
	Возврат Метаданные.РегламентныеЗадания.АвтоматическоеПолучениеЭДО;
КонецФункции

#КонецОбласти

#Область Использование

// Параметры:
//  РасшифрованныеМаркеры - см. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
//  ИдентификаторыЭДО - Массив из Строка
// 
// Возвращаемое значение:
//  См. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
Функция ВыбратьРасшифрованныеМаркеры(РасшифрованныеМаркеры, ИдентификаторыЭДО)
	
	Результат = КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных();
	
	Для Каждого ИдентификаторЭДО Из ИдентификаторыЭДО Цикл
		РасшифрованныйМаркер = РасшифрованныеМаркеры[ИдентификаторЭДО];
		Если РасшифрованныйМаркер <> Неопределено Тогда
			Результат.Вставить(ИдентификаторЭДО, РасшифрованныйМаркер);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  РезультатВключенияПриемник - См. НовыйРезультатВключенияПоУчетнымЗаписямЭДО
//  РезультатВключенияИсточник - См. НовыйРезультатВключенияПоУчетнымЗаписямЭДО
Процедура ДополнитьРезультатВключения(РезультатВключенияПриемник, РезультатВключенияИсточник)
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
		РезультатВключенияПриемник.СостояниеПоИдентификаторамЭДО, 
		РезультатВключенияИсточник.СостояниеПоИдентификаторамЭДО);
	
	ОбработкаНеисправностейБЭДКлиентСервер.ДополнитьКонтекстДиагностики(
		РезультатВключенияПриемник.КонтекстДиагностики,
		РезультатВключенияИсточник.КонтекстДиагностики);
	
КонецПроцедуры

#КонецОбласти // Использование

#Область ПроверкаДоступности

// Параметры:
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 
// Возвращаемое значение:
//  Булево
Функция ПроверитьДоступность(КонтекстДиагностики = Неопределено)
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Возврат ДоступнаПроверкаПодписиВОблаке(КонтекстДиагностики);
	Иначе
		Возврат ДоступнаПроверкаПодписиНаСервере(КонтекстДиагностики);
	КонецЕсли;
	
КонецФункции

// Параметры:
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 
// Возвращаемое значение:
//  Булево
Функция ДоступнаПроверкаПодписиВОблаке(КонтекстДиагностики = Неопределено)
	
	Если КриптографияБЭД.ДоступнаПроверкаПодписиВОблаке() Тогда
		Возврат Истина
	КонецЕсли;
	
	Если КонтекстДиагностики <> Неопределено Тогда
		ВидОшибки = ВидОшибкиСервисDSSНедоступен();
		ДобавитьОшибкуПроверкиДоступности(КонтекстДиагностики, ВидОшибки);
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Параметры:
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 
// Возвращаемое значение:
//  Булево
Функция ДоступнаПроверкаПодписиНаСервере(КонтекстДиагностики = Неопределено)
	
	МенеджерКриптографии = ЭлектроннаяПодпись.МенеджерКриптографии("", Ложь);
	Если МенеджерКриптографии = Неопределено Тогда
		Если КонтекстДиагностики <> Неопределено Тогда
			ВидОшибки = ВидОшибкиКриптопровайдерНаСервереНедоступен();
			ДобавитьОшибкуПроверкиДоступности(КонтекстДиагностики, ВидОшибки);
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЭлектроннаяПодпись.ПроверятьЭлектронныеПодписиНаСервере() Тогда
		Если КонтекстДиагностики <> Неопределено Тогда
			ВидОшибки = ВидОшибкиПроверкаПодписейНедоступна();
			ДобавитьОшибкуПроверкиДоступности(КонтекстДиагностики, ВидОшибки);
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Параметры:
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  ВидОшибки - см. ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки
Процедура ДобавитьОшибкуПроверкиДоступности(КонтекстДиагностики, ВидОшибки)
	
	ВидОперации = НСтр("ru = 'Проверка доступности автоматического получения ЭДО';
						|en = 'Check availability of automatic EDI receipt'");
	ТекстОшибки = НСтр("ru = 'Проверка электронных подписей на сервере недоступна';
						|en = 'Cannot check digital signatures on the server'");
	Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(ВидОперации, ВидОшибки, ТекстОшибки, ТекстОшибки);
	ОбработкаНеисправностейБЭД.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
		ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().АвтоматическоеПолучениеЭДО, Истина);
	
КонецПроцедуры

// Возвращаемое значение:
//  См. ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки
Функция ВидОшибкиПроверкаПодписейНедоступна()
	
	ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки();
	ВидОшибки.Идентификатор = "ПроверкаПодписейНедоступна";
	ВидОшибки.ЗаголовокПроблемы = НСтр("ru = 'Автоматическое получение ЭДО недоступно';
										|en = 'Automatic EDI receipt is not available'");
	ВидОшибки.ОписаниеПроблемы =
		НСтр("ru = 'Для корректной работы автоматического получения ЭДО
			|требуется проверка электронных подписей на сервере';
			|en = 'For automatic EDI receipt to work correctly,
			|digital signature check on the server is required'");
	ВидОшибки.ОписаниеРешения = НСтр("ru = 'Включите проверку электронных подписей на сервере
		|и повторите операцию';
		|en = 'Enable digital signature check on the server
		|and try again'");
	ВидОшибки.ОбработчикиНажатия.Вставить("Включить",
		"АвтоматическоеПолучениеЭДОКлиент.ОбработчикОшибкиПроверкаПодписейНедоступна");
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИдентификаторВидаОшибки", ВидОшибки.Идентификатор);
	ВидОшибки.ПараметрыОбработчиков.Вставить("Включить", ДополнительныеПараметры);
	
	Возврат ВидОшибки;
	
КонецФункции

// Возвращаемое значение:
//  См. ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки
Функция ВидОшибкиСервисDSSНедоступен()
	
	ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки();
	ВидОшибки.Идентификатор = "СервисDSSНедоступен";
	ВидОшибки.ЗаголовокПроблемы = НСтр("ru = 'Автоматическое получение ЭДО недоступно';
										|en = 'Automatic EDI receipt is not available'");
	ВидОшибки.ОписаниеПроблемы =
		НСтр("ru = 'Для корректной работы автоматического получения ЭДО
			|требуется доступ к сервису DSS';
			|en = 'For automatic EDI receipt to work correctly,
			|access to the DSS service is required'");
	ВидОшибки.ОписаниеРешения = НСтр("ru = 'Подключите сервис DSS и повторите операцию';
									|en = 'Connect the DSS service and try again'");
	Возврат ВидОшибки
	
КонецФункции

// Возвращаемое значение:
//  См. ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки
Функция ВидОшибкиКриптопровайдерНаСервереНедоступен()
	
	ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки();
	ВидОшибки.Идентификатор = "КриптопровайдерНаСервереНедоступен";
	ВидОшибки.ЗаголовокПроблемы = НСтр("ru = 'Автоматическое получение ЭДО недоступно';
										|en = 'Automatic EDI receipt is not available'");
	ВидОшибки.ОписаниеПроблемы =
		НСтр("ru = 'Для корректной работы автоматического получения ЭДО
			|требуется наличие криптопровайдера на сервере';
			|en = 'For automatic EDI receipt to work correctly,
			|cryptographic service provider on the server is required'");
	ВидОшибки.ОписаниеРешения = НСтр("ru = 'Установите криптопровайдер на сервер и повторите операцию';
									|en = 'Install the cryptographic service provider on the server and try again'");
	Возврат ВидОшибки
	
КонецФункции

#КонецОбласти

#Область КлючДоступаКСервисуЭДО

// Возвращаемое значение:
//  Булево
Функция ИспользоватьПолучениеПоКлючуДоступа() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат ЗначениеЗаполнено(Константы.КлючДоступаКСервисуЭДО.Получить()) 
		И Константы.ИспользоватьАвтоматическоеПолучениеИзСервисаЭДО.Получить();
	
КонецФункции

// Параметры:
//  КлючСинхронизации - см. СинхронизацияЭДОКлиентСервер.НовыйКлючСинхронизации
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 
// Возвращаемое значение:
//  Булево
Функция ЕстьВозможностьПолучитьДокументыИзСервисаЭДО(КлючСинхронизации, КонтекстДиагностики) Экспорт
	
	ПараметрыВыполненияОперации = СервисЭДО.НовыеПараметрыВыполненияОперацииСервиса(КлючСинхронизации);
	ПараметрыВыполненияОперации.ПредставлениеОперации =
		НСтр("ru = 'Включение автоматического получения ЭДО по ключу доступа';
			|en = 'Enable automatic EDI receipt using an access key'");
	ПараметрыВыполненияОперации.АдресРесурса = "GetMessageList";
	ПараметрыВыполненияОперации.Заголовки.Вставить("Error-Messages", "all");
	ПараметрыВыполненияОперации.Метод = ИнтернетСоединениеБЭД.HTTPМетоды().GET;
	ПараметрыВыполненияОперации.Таймаут = 7;
	
	ДатаЗапроса = ОбщегоНазначенияБЭД.УниверсальнаяДатаВМиллисекундахИзДаты(ТекущаяДатаСеанса());
	ДатаЗапросаСтрокой = СтрокаИзУниверсальнойДатыВМиллисекундах(ДатаЗапроса, КлючСинхронизации.СпособОбмена);
	ПараметрыВыполненияОперации.ПараметрыЗапроса.Вставить("date", ДатаЗапросаСтрокой);
	
	ВерсияБЭД = ОбновлениеИнформационнойБазыБЭД.ВерсияБиблиотеки();
	ПараметрыВыполненияОперации.Заголовки.Вставить("X-Platform-Version", ВерсияБЭД);
	
	ОписаниеСоединения = СервисЭДО.СоединениеССервисом(ПараметрыВыполненияОперации.СпособОбмена,
		ПараметрыВыполненияОперации.Таймаут);
	
	HTTPЗапрос = СервисЭДО.ЗапросКСервису(ПараметрыВыполненияОперации);
	
	РезультатВыполненияЗапроса = ИнтернетСоединениеБЭД.ВызватьHTTPМетод(ОписаниеСоединения, HTTPЗапрос,
		ПараметрыВыполненияОперации.Метод, ПараметрыВыполненияОперации.ПредставлениеОперации, КонтекстДиагностики);
		
	Если Не РезультатВыполненияЗапроса.Успех Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Ответ = РезультатВыполненияЗапроса.Ответ;
	
	Если Ответ.КодСостояния = 200 Тогда
		Возврат Истина;
	КонецЕсли;

	Попытка
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		Данные = ОбщегоНазначенияБЭД.JSONЗначение(ТелоОтвета, Истина); // Соответствие
		ПодробноеПредставлениеОшибки = Данные["Details"]; // Строка
		КраткоеПредставлениеОшибки = Данные["CommonDescription"]; // Строка
		Если Не ЗначениеЗаполнено(ПодробноеПредставлениеОшибки) Тогда
			ПодробноеПредставлениеОшибки = ТелоОтвета;
		КонецЕсли;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	КонецПопытки;
	
	ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка();
	Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(ПараметрыВыполненияОперации.ПредставлениеОперации, ВидОшибки,
		ПодробноеПредставлениеОшибки, КраткоеПредставлениеОшибки);
	ОбработкаНеисправностейБЭД.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
		ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().АвтоматическоеПолучениеЭДО, Истина);
	
	Возврат Ложь;
	
КонецФункции

// Параметры:
//  УниверсальнаяДата - Число
//  СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД
// 
// Возвращаемое значение:
//  Строка - Строка из универсальной даты в миллисекундах
Функция СтрокаИзУниверсальнойДатыВМиллисекундах(УниверсальнаяДата, СпособОбмена) 
	
	Если УниверсальнаяДата < 0 Тогда
		УниверсальнаяДата = СервисЭДО.ДатаЗапросаПоУмолчанию();
	КонецЕсли;
	ДробнаяУниверсальнаяДата = УниверсальнаяДата / 1000;
	УниверсальнаяДатаБезМиллисекунд = Цел(ДробнаяУниверсальнаяДата);
	Дата = Дата(1970, 1, 1) + УниверсальнаяДатаБезМиллисекунд;
	ДатаСтрокой = Формат(Дата, "ДФ='yyyy-MM-dd HH:mm:ss'");
	ДатаСтрокой = ДатаСтрокой + Формат(ДробнаяУниверсальнаяДата - УниверсальнаяДатаБезМиллисекунд, "ЧЦ=3; ЧДЦ=3; ЧРД=.");
	
	Возврат ДатаСтрокой;
	
КонецФункции

// Параметры:
//  ИдентификаторыЭДО - Массив из Строка
//  РасшифрованныеМаркеры - см. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
//  
// Возвращаемое значение:
//  См. НовыйРезультатВключенияПоУчетнымЗаписямЭДО
//
Функция ВключитьИспользованиеКлючаДоступаКСервисуЭДО(ИдентификаторыЭДО, РасшифрованныеМаркеры)

	КлючиСинхронизации = СинхронизацияЭДО.КлючиСинхронизацииИзРасшифрованныхМаркеров(РасшифрованныеМаркеры);
	Если Не ЗначениеЗаполнено(КлючиСинхронизации) Тогда
		Возврат НовыйРезультатВключенияПоУчетнымЗаписямЭДО();
	КонецЕсли;
	
	РезультатВключения = ВключитьИспользованиеКлючаДоступаКСервисуЭДОПоКлючамСинхронизации(
		ИдентификаторыЭДО, КлючиСинхронизации);
	
	Возврат РезультатВключения;
	
КонецФункции

// Параметры:
//  ИдентификаторыЭДО - Массив из Строка
//  КлючиСинхронизации - См. СинхронизацияЭДОКлиентСервер.НовыеКлючиСинхронизации
//  
// Возвращаемое значение:
//  См. НовыйРезультатВключенияПоУчетнымЗаписямЭДО
//
Функция ВключитьИспользованиеКлючаДоступаКСервисуЭДОПоКлючамСинхронизации(ИдентификаторыЭДО, КлючиСинхронизации)
	
	РезультатПодключения = НовыйРезультатВключенияПоУчетнымЗаписямЭДО();
	
	СостояниеПоИдентификаторамЭДО = РезультатПодключения.СостояниеПоИдентификаторамЭДО;
	КонтекстДиагностики = РезультатПодключения.КонтекстДиагностики;
	
	Для Каждого КлючСинхронизацииПоИдентификаторуЭДО Из КлючиСинхронизации Цикл
		ИдентификаторЭДО = КлючСинхронизацииПоИдентификаторуЭДО.Ключ;
		КлючСинхронизации = КлючСинхронизацииПоИдентификаторуЭДО.Значение;
		
		ЕстьВозможность = ЕстьВозможностьПолучитьДокументыИзСервисаЭДО(КлючСинхронизации, КонтекстДиагностики);
		Если Не ЕстьВозможность Тогда
			Продолжить;
		КонецЕсли;
		
		СостояниеПолучения = УстановитьИспользованиеКлючаДоступаКСервисуЭДО(ИдентификаторЭДО);
		
		СостояниеПоИдентификаторамЭДО.Вставить(ИдентификаторЭДО, СостояниеПолучения);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СостояниеПоИдентификаторамЭДО) Тогда
		УстановитьАктивностьРегламентногоЗадания(Истина);
	КонецЕсли;
	
	Возврат РезультатПодключения;
	
КонецФункции

// Параметры:
//  ИдентификаторЭДО - Неопределено,Строка
Процедура ОтключитьИспользованиеКлючаДоступаКСервисуЭДО(ИдентификаторЭДО = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	Если ИдентификаторЭДО = Неопределено Тогда
		РегистрыСведений.ИспользованиеКлючаДоступаКСервисуЭДО.Очистить();
		РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.Очистить();
	Иначе
		РегистрыСведений.ИспользованиеКлючаДоступаКСервисуЭДО.Удалить(ИдентификаторЭДО);
		РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.Удалить(ИдентификаторЭДО);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Параметры:
//  ИдентификаторЭДО - Строка
// 
// Возвращаемое значение:
//  См. РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.НовоеСостояниеПолучения
Функция УстановитьИспользованиеКлючаДоступаКСервисуЭДО(ИдентификаторЭДО)
	
	СостояниеПолучения = РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.НовоеСостояниеПолучения();
	
	НачатьТранзакцию();
	Попытка
		УстановитьПривилегированныйРежим(Истина);
		РегистрыСведений.ИспользованиеКлючаДоступаКСервисуЭДО.Записать(ИдентификаторЭДО);
		УстановитьПривилегированныйРежим(Ложь);
		
		НаборЗаписей = РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторЭДО.Установить(ИдентификаторЭДО);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() Тогда
			ЗаписьНабора = НаборЗаписей[0];
			ЗаписьНабора.Остановлено = Ложь;
			НаборЗаписей.Записать();
			ЗаполнитьЗначенияСвойств(СостояниеПолучения, ЗаписьНабора);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат СостояниеПолучения;
	
КонецФункции

#КонецОбласти // КлючДоступаКСервисуЭДО

#Область ТокеныДоступаКСервисуЭДО

// Возвращаемое значение:
//  Строка
Функция ВидОперацииВключениеИспользованияТокеновДоступаКСервисуЭДО() Экспорт
	
	Возврат НСтр("ru = 'Включение автоматического получения по токенам доступа к сервису ЭДО';
				|en = 'Enabling automatic access token receipt for EDI service'");
	
КонецФункции

// Возвращаемое значение:
//  Булево
Функция ИспользоватьПолучениеПоТокенамДоступа() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Константы.ИспользоватьАвтоматическоеПолучениеИзСервисаЭДОПоТокенам.Получить();
	
КонецФункции

// Параметры:
//  ИдентификаторыЭДО - Массив из Строка
//  РасшифрованныеМаркеры - см. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
//  
// Возвращаемое значение:
//  См. НовыйРезультатВключенияПоУчетнымЗаписямЭДО
Функция ВключитьИспользованиеТокеновДоступаПоУчетнымЗаписямЭДО(ИдентификаторыЭДО, РасшифрованныеМаркеры)
	
	РезультатВключения = НовыйРезультатВключенияПоУчетнымЗаписямЭДО();
	СостояниеПоИдентификаторамЭДО = РезультатВключения.СостояниеПоИдентификаторамЭДО;
	КонтекстДиагностики = РезультатВключения.КонтекстДиагностики;
	КонтекстДиагностики.ЗаголовокОперации = ВидОперацииВключениеИспользованияТокеновДоступаКСервисуЭДО();
	
	Использовать = ВключитьИспользованиеТокеновДоступа(КонтекстДиагностики);
	Если Не Использовать Тогда
		Возврат РезультатВключения;
	КонецЕсли;
	
	ЕстьТокеныДоступа = Ложь;
	
	Для Каждого ИдентификаторЭДО Из ИдентификаторыЭДО Цикл
		
		РасшифрованныйМаркер = РасшифрованныеМаркеры[ИдентификаторЭДО];
		Если РасшифрованныйМаркер = Неопределено Тогда
			Состояние = РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.УстановитьПризнакОстановки(
				ИдентификаторЭДО, Истина);
			СостояниеПоИдентификаторамЭДО.Вставить(ИдентификаторЭДО, Состояние);
			Продолжить;
		КонецЕсли;
		
		ТокенПолучен = СинхронизацияЭДО.ПолучитьНовыйТокенДоступаКСервисуЭДО(
			ИдентификаторЭДО, РасшифрованныйМаркер, КонтекстДиагностики);
		Если Не ТокенПолучен Тогда
			Продолжить;
		КонецЕсли;
		
		Состояние = РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.УстановитьПризнакОстановки(
			ИдентификаторЭДО, Ложь);
		
		СостояниеПоИдентификаторамЭДО.Вставить(ИдентификаторЭДО, Состояние);
		
		ЕстьТокеныДоступа = Истина;
		
	КонецЦикла;
	
	Если ЕстьТокеныДоступа Тогда
		УстановитьАктивностьРегламентногоЗадания(Истина);
	КонецЕсли;
	
	Возврат РезультатВключения;
	
КонецФункции

// Параметры:
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 
// Возвращаемое значение:
//  Булево
Функция ВключитьИспользованиеТокеновДоступа(КонтекстДиагностики = Неопределено) Экспорт
	
	Использовать = Ложь;
	
	ИспользованиеДоступно = ПроверитьДоступность(КонтекстДиагностики);
	Если Не ИспользованиеДоступно Тогда
		Возврат Использовать;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Использовать = Константы.ИспользоватьАвтоматическоеПолучениеИзСервисаЭДОПоТокенам.Получить();
	Если Не Использовать Тогда
		Использовать = Истина;
		Константы.ИспользоватьАвтоматическоеПолучениеИзСервисаЭДОПоТокенам.Установить(Использовать);
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Использовать;
	
КонецФункции

// Параметры:
//  ИдентификаторЭДО - Неопределено,Строка
Процедура ОтключитьИспользованиеТокеновДоступаКСервисуЭДО(ИдентификаторЭДО = Неопределено)
	
	НачатьТранзакцию();
	Попытка
		
		Если ИдентификаторЭДО = Неопределено Тогда
			ИдентификаторыЛокальногоЭДО = УчетныеЗаписиЭДО.ИдентификаторыУчетныхЗаписейСпособаОбмена(
				Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО, Ложь);
			РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.Удалить(ИдентификаторыЛокальногоЭДО);
			СинхронизацияЭДО.УдалитьТокеныДоступаКСервисуЭДО(ИдентификаторыЛокальногоЭДО);
			
			Использовать = ИспользоватьПолучениеПоТокенамДоступа();
			Если Использовать Тогда
				Константы.ИспользоватьАвтоматическоеПолучениеИзСервисаЭДОПоТокенам.Установить(Ложь);
				ОбновитьПовторноИспользуемыеЗначения();
			КонецЕсли;
		Иначе
			РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.Удалить(ИдентификаторЭДО);
			СинхронизацияЭДО.УдалитьТокеныДоступаКСервисуЭДО(
				ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторЭДО));
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти // ТокеныДоступаКСервисуЭДО

#Область РаботаСФормами

#Если Не ВнешнееСоединение Тогда

// Возвращаемое значение:
//  Структура:
//  * ИмяГруппыПредупреждение - Строка
//  * ИмяСписка - Строка
//  * ИдентификаторЭДО - Строка
Функция НовыеПараметрыРеквизитаФормы() Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("ИмяГруппыПредупреждение", "");
	Параметры.Вставить("ИмяСписка", "");
	Параметры.Вставить("ИдентификаторЭДО", "");
	Возврат Параметры;
КонецФункции

#КонецЕсли

#КонецОбласти

#Область Получение

// Возвращаемое значение:
//  Булево
Функция ИспользоватьПолучениеЧерезИнтервал()
	
	Возврат ЕстьУчетныеЗаписиДляПолучения()
		И Не РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована();
	
КонецФункции

// Возвращаемое значение:
//  Булево
Функция ЕстьУчетныеЗаписиДляПолучения()
	Возврат НастройкиЭДО.ИспользоватьИнтеграциюОблачногоЭДО()
			И УчетныеЗаписиЭДО.ЕстьУчетныеЗаписиЧерезОблачныйЭДО()
		Или АвтоматическоеПолучениеЭДОПовтИсп.ИспользоватьПолучениеПоКлючуДоступа()
			И РегистрыСведений.ИспользованиеКлючаДоступаКСервисуЭДО.ЕстьЗаписи()
		Или АвтоматическоеПолучениеЭДОПовтИсп.ИспользоватьПолучениеПоТокенамДоступа()
			И УчетныеЗаписиЭДО.ЕстьУчетныеЗаписиЧерезСервис1СЭДО();
КонецФункции

// Параметры:
//  ЭтоРегламентноеЗадание - Булево
//  ДатаПоследнегоПолучения - Дата
// 
// Возвращаемое значение:
//  Булево
Функция ТребуетсяПолучениеЧерезИнтервал(ЭтоРегламентноеЗадание = Ложь, ДатаПоследнегоПолучения = Неопределено) Экспорт
	Интервал = АвтоматическоеПолучениеЭДОКлиентСервер.Интервал();
	Если ДатаПоследнегоПолучения = Неопределено Тогда
		ДатаПоследнегоПолучения = Константы.ДатаАвтоматическогоПолученияЭДО.Получить();
	КонецЕсли;
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	ТекущийИнтервал = ТекущаяДатаСеанса - ДатаПоследнегоПолучения;
	Если Интервал > ТекущийИнтервал  Тогда
		Возврат Ложь;
	КонецЕсли;
	КлючФоновогоЗадания = КлючЗадания();
	Отбор = Новый Структура("Ключ, Состояние", КлючФоновогоЗадания, СостояниеФоновогоЗадания.Активно);
	АктивныеФоновыеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	КоличествоАктивных = АктивныеФоновыеЗадания.Количество();
	Если КоличествоАктивных = 0
		Или ЭтоРегламентноеЗадание И КоличествоАктивных = 1 Тогда
		Возврат Истина;
	ИначеЕсли ИнтервалЗавершенияРегламентногоЗадания() > ТекущийИнтервал Тогда
		Возврат Ложь;
	КонецЕсли;
	Для Каждого ФоновоеЗадание Из АктивныеФоновыеЗадания Цикл
		Если ЭтоРегламентноеЗадание И ФоновоеЗадание.РегламентноеЗадание <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ФоновоеЗадание.УникальныйИдентификатор);
	КонецЦикла;
	Возврат Истина;
КонецФункции

Процедура ОбновитьДатуПолучения()
	УстановитьПривилегированныйРежим(Истина);
	Константы.ДатаАвтоматическогоПолученияЭДО.Установить(ТекущаяДатаСеанса());
КонецПроцедуры

// Параметры:
//  РасшифрованныеМаркеры - См. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
// 
// Возвращаемое значение:
//  См. Получить
Функция ПолучитьЧерезИнтервал(РасшифрованныеМаркеры = Неопределено) Экспорт
	
	ВидОперации = НСтр("ru = 'Автоматическое получение ЭДО через интервал';
						|en = 'Automatic EDI receipt via interval'");
	Возврат Получить(ВидОперации, РасшифрованныеМаркеры);
	
КонецФункции

Процедура ПолучитьПоРасписанию() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.АвтоматическоеПолучениеЭДО);
	
	УстановитьПривилегированныйРежим(Истина);
	ТребуетсяПолучение = ТребуетсяПолучениеЧерезИнтервал(Истина);
	Если Не ТребуетсяПолучение Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЕстьУчетныеЗаписиДляПолучения() Тогда
		УстановитьАктивностьРегламентногоЗадания(Ложь);
		Возврат;
	КонецЕсли;
	
	ВидОперации = НСтр("ru = 'Автоматическое получение ЭДО по расписанию';
						|en = 'Automatic EDI receipt according to the schedule'");
	Получить(ВидОперации);
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
//  * ИзменениеСостояния - Неопределено,Структура:
//    ** ЕстьПредупреждения - Булево
//    ** СостояниеПоИдентификаторамЭДО - см. РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.СостояниеПоИдентификаторамЭДО
//  * ИтогДействийПоЭДО - см. ЭлектронныеДокументыЭДОКлиентСервер.НовыйИтогВыполненияДействийПоЭДО
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
Функция НовыйРезультатПолучения() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИзменениеСостояния", Неопределено);
	Результат.Вставить("ИтогДействийПоЭДО", ЭлектронныеДокументыЭДОКлиентСервер.НовыйИтогВыполненияДействийПоЭДО());
	Результат.Вставить("КонтекстДиагностики", ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики());
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ВидОперации - Строка
//  РасшифрованныеМаркеры - См. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
//
// Возвращаемое значение:
//  См. НовыйРезультатПолучения
Функция Получить(ВидОперации, РасшифрованныеМаркеры = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбновитьДатуПолучения();
	
	Результат = НовыйРезультатПолучения();
	
	РезультатВариантовПолучения = НовыйРезультатВариантаПолучения();
	ИтогДействийПоЭДО = Результат.ИтогДействийПоЭДО;
	КонтекстДиагностики = Результат.КонтекстДиагностики;
	
	Текст = СтрШаблон(НСтр("ru = 'Начало операции: %1.';
							|en = 'Operation start: %1.'"), ВидОперации);
	Подсистема = ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().АвтоматическоеПолучениеЭДО;
	ОбщегоНазначенияБЭД.ЗаписатьВЖурналРегистрации(Текст, Подсистема, УровеньЖурналаРегистрации.Информация);
	
	Попытка
		
		СостояниеПоИдентификаторамЭДОДоПолучения =
			РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.СостояниеПоИдентификаторамЭДО();
		
		Если ЗначениеЗаполнено(РасшифрованныеМаркеры) Тогда
			
			РезультатПоМаркерам = ПолучитьПоРасшифрованнымМаркерам(РасшифрованныеМаркеры);
			ДополнитьРезультатПолучения(РезультатВариантовПолучения, РезультатПоМаркерам);
			
		ИначеЕсли АвтоматическоеПолучениеЭДОПовтИсп.ИспользоватьПолучениеПоКлючуДоступа() Тогда
			
			РезультатПоКлючуДоступа = ПолучитьПоКлючуДоступаКСервисуЭДО(СостояниеПоИдентификаторамЭДОДоПолучения);
			ДополнитьРезультатПолучения(РезультатВариантовПолучения, РезультатПоКлючуДоступа);
			
		ИначеЕсли АвтоматическоеПолучениеЭДОПовтИсп.ИспользоватьПолучениеПоТокенамДоступа() Тогда
			
			РезультатПоТокенамДоступа = ПолучитьПоТокенамДоступаКСервисуЭДО(СостояниеПоИдентификаторамЭДОДоПолучения);
			ДополнитьРезультатПолучения(РезультатВариантовПолучения, РезультатПоТокенамДоступа);
			
		КонецЕсли;
		
		ИспользоватьИнтеграциюОблачногоЭДО = НастройкиЭДО.ИспользоватьИнтеграциюОблачногоЭДО();
		Если ИспользоватьИнтеграциюОблачногоЭДО Тогда
			
			РезультатПоОблачномуЭДО = ПолучитьПоУчетнымЗаписямОблачногоЭДО();
			ДополнитьРезультатПолучения(РезультатВариантовПолучения, РезультатПоОблачномуЭДО);
			
		КонецЕсли;
		
		ИзмененныеСостоянияПоИдентификаторамЭДО =
			РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.ИзмененныеСостоянияПоИдентификаторамЭДО(
				СостояниеПоИдентификаторамЭДОДоПолучения, РезультатВариантовПолучения.СостояниеПоИдентификаторамЭДО);
		
		ЗаполнитьЗначенияСвойств(Результат, РезультатВариантовПолучения);
		
		Если ЗначениеЗаполнено(ИзмененныеСостоянияПоИдентификаторамЭДО) Тогда
			ЕстьПредупреждения = РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.ЕстьПредупреждения();
			Результат.ИзменениеСостояния = Новый Структура("ЕстьПредупреждения, СостояниеПоИдентификаторамЭДО",
				ЕстьПредупреждения, ИзмененныеСостоянияПоИдентификаторамЭДО);
		КонецЕсли;
		
		ШаблонСообщения = НСтр("ru = 'Завершение операции: %1.
			|Получено транспортных контейнеров: %2. Распаковано: %3.';
			|en = 'Operation completion: %1.
			|Transport containers received: %2. Unpacked: %3.'");
		
		Загружено = ИтогДействийПоЭДО.ОбработаноПоДействиям[Перечисления.ДействияПоЭДО.Загрузить];
		Если Загружено = Неопределено Тогда
			Загружено = 0;
		КонецЕсли;
		
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ВидОперации, ИтогДействийПоЭДО.ПолученоКонтейнеров, Загружено);
		
		ОбщегоНазначенияБЭД.ЗаписатьВЖурналРегистрации(ТекстСообщения, Подсистема, УровеньЖурналаРегистрации.Информация);
		
		ДиагностикаЭДО.ОбработатьОшибки(КонтекстДиагностики);
		
		Если Не ЗначениеЗаполнено(ИтогДействийПоЭДО.ОбработанныеДокументы)
			И Не ЗначениеЗаполнено(ИзмененныеСостоянияПоИдентификаторамЭДО) Тогда
			Возврат Результат;
		КонецЕсли;
		
		Если ИнтеграцияБСПБЭДКлиентСервер.УведомленияКлиентаДоступны() Тогда
			МенеджерУведомлений = ОбщегоНазначения.ВычислитьВБезопасномРежиме("УведомленияКлиента");
			//@skip-check dynamic-access-method-not-found
			МенеджерУведомлений.ОтправитьУведомление(АвтоматическоеПолучениеЭДОКлиентСервер.КлючУведомленийКлиента(),
				Результат);
		КонецЕсли;
		
	Исключение
		ШаблонСообщения = НСтр("ru = 'В процессе выполнения операции ""%1"" произошла ошибка: %2';
								|en = 'An error occurred when executing the ""%1"" operation: %2'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ВидОперации,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(ВидОперации,
			ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка(),
			ТекстСообщения, ТекстСообщения);
		ОбработкаНеисправностейБЭД.ДобавитьОшибку(КонтекстДиагностики, Ошибка, Подсистема);
		ДиагностикаЭДО.ОбработатьОшибки(КонтекстДиагностики);
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * СостояниеПоИдентификаторамЭДО - см. РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.СостояниеПоИдентификаторамЭДО
//  * ИтогДействийПоЭДО - см. ЭлектронныеДокументыЭДОКлиентСервер.НовыйИтогВыполненияДействийПоЭДО
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
Функция НовыйРезультатВариантаПолучения() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("СостояниеПоИдентификаторамЭДО", Новый Соответствие);
	Результат.Вставить("ИтогДействийПоЭДО", ЭлектронныеДокументыЭДОКлиентСервер.НовыйИтогВыполненияДействийПоЭДО());
	Результат.Вставить("КонтекстДиагностики", ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики());
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  См. НовыйРезультатВариантаПолучения
Функция ПолучитьПоУчетнымЗаписямОблачногоЭДО()
	
	МодульИнтеграцияОблачногоЭДО = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияОблачногоЭДО");
	МодульЭлектронныеДокументыЭДОИнтеграцияОблака = ОбщегоНазначения.ОбщийМодуль(
		"ЭлектронныеДокументыЭДОИнтеграцияОблака");
	
	Результат = НовыйРезультатВариантаПолучения();
	ИтогДействийПоЭДО = Результат.ИтогДействийПоЭДО;
	КонтекстДиагностики = Результат.КонтекстДиагностики;
	СостояниеПоИдентификаторамЭДО = Результат.СостояниеПоИдентификаторамЭДО;
	
	ИдентификаторыЭДОПоУчетнымЗаписямОблачногоЭДО =
		МодульИнтеграцияОблачногоЭДО.ИдентификаторыЭДОПоУчетнымЗаписямОблачнымЭДО();
	
	Для Каждого ИдентификаторыЭДОПоУчетнойЗаписиОблачногоЭДО Из ИдентификаторыЭДОПоУчетнымЗаписямОблачногоЭДО Цикл
		
		ДатаНачала = ТекущаяДатаСеанса();
		
		УчетнаяЗаписьОблачногоЭДО = ИдентификаторыЭДОПоУчетнойЗаписиОблачногоЭДО.Ключ;
		ИдентификаторыЭДО = ИдентификаторыЭДОПоУчетнойЗаписиОблачногоЭДО.Значение;
		ПараметрыВыполнения = ЭлектронныеДокументыЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
		ПараметрыВыполнения.ОбъектыДействий.ИдентификаторыОрганизаций = ИдентификаторыЭДО;
		ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(ПараметрыВыполнения.НаборДействий,
			Перечисления.ДействияПоЭДО.ЗагрузитьПриглашения);
		ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(ПараметрыВыполнения.НаборДействий,
			Перечисления.ДействияПоЭДО.Загрузить);
		РезультатВыполнения = МодульЭлектронныеДокументыЭДОИнтеграцияОблака.ВыполнитьДействияЭДО(
			ПараметрыВыполнения, КонтекстДиагностики);
		РезультатДействийЭДО = РезультатВыполнения.РезультатыДействий[УчетнаяЗаписьОблачногоЭДО];
		
		ЭлектронныеДокументыЭДОКлиентСервер.ДополнитьИтогВыполненияДействийПоЭДО(
			ИтогДействийПоЭДО, РезультатДействийЭДО.Итог);
		
		ОбновитьДатуПолучения();
		
		Для Каждого ИдентификаторЭДО Из ИдентификаторыЭДО Цикл
			СостояниеПолучения = РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.Записать(
				ИдентификаторЭДО, Ложь, ДатаНачала);
			СостояниеПоИдентификаторамЭДО.Вставить(ИдентификаторЭДО, СостояниеПолучения);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  СостоянияПолучения - см. РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.СостояниеПоИдентификаторамЭДО
//
// Возвращаемое значение:
//  См. НовыйРезультатВариантаПолучения
Функция ПолучитьПоКлючуДоступаКСервисуЭДО(СостоянияПолучения)
	
	Результат = НовыйРезультатВариантаПолучения();
	
	УстановитьПривилегированныйРежим(Истина);
	КлючДоступаКСервисуЭДО = Константы.КлючДоступаКСервисуЭДО.Получить();
	УстановитьПривилегированныйРежим(Ложь);
	Если Не ЗначениеЗаполнено(КлючДоступаКСервисуЭДО) Тогда
		Возврат Результат;
	КонецЕсли;
	
	КонтекстДиагностики = Результат.КонтекстДиагностики;
	
	ИдентификаторыЧерезСервис1СЭДО = УчетныеЗаписиЭДО.ИдентификаторыУчетныхЗаписейСпособаОбмена(
		Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО, Ложь);
	УстановитьПривилегированныйРежим(Истина);
	СертификатыПоУчетнымЗаписямЭДО = УчетныеЗаписиЭДО.ДействующиеСертификатыПоУчетнымЗаписям(
		ИдентификаторыЧерезСервис1СЭДО);
	УстановитьПривилегированныйРежим(Ложь);
	ИдентификаторыЭДОСДействующимиСертификатами = ОбщегоНазначения.ВыгрузитьКолонку(
		СертификатыПоУчетнымЗаписямЭДО, "Ключ");
	ИдентификаторыСКлючомДоступа = РегистрыСведений.ИспользованиеКлючаДоступаКСервисуЭДО.ПодключенныеИдентификаторыЭДО(
		ИдентификаторыЭДОСДействующимиСертификатами);
	ИдентификаторыБезКлючаДоступа = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		ИдентификаторыЧерезСервис1СЭДО, ИдентификаторыСКлючомДоступа);
	УстановитьПризнакОстановленПоИдентификаторамЭДО(ИдентификаторыБезКлючаДоступа);
	
	ПриниматьПриглашенияЭДОАвтоматически = НастройкиЭДО.ПриниматьПриглашенияЭДОАвтоматически();
	ДоступнаПроверкаПодписи = ДоступнаПроверкаПодписи();
	
	Для Каждого ИдентификаторЭДО Из ИдентификаторыСКлючомДоступа Цикл
		
		Состояние = СостоянияПолучения[ИдентификаторЭДО];
		Если Состояние <> Неопределено И Состояние.Остановлено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ДоступнаПроверкаПодписи Тогда
			ЗаполнитьРезультатПолученияБезПроверкиПодписи(Результат, ИдентификаторЭДО);
			Продолжить;
		КонецЕсли;
		
		ДатаНачала = ТекущаяДатаСеанса();
		
		Сертификаты = СертификатыПоУчетнымЗаписямЭДО[ИдентификаторЭДО];
		Если Не ЗначениеЗаполнено(Сертификаты) Тогда
			Продолжить;
		КонецЕсли;
		
		РасшифрованныйМаркер = РасшифрованныйМаркерПоКлючуДоступа(
			КлючДоступаКСервисуЭДО, ИдентификаторЭДО, Сертификаты[0], КонтекстДиагностики);
		Если РасшифрованныйМаркер.Данные = Неопределено Тогда
			РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.Записать(ИдентификаторЭДО, Истина, ДатаНачала);
			Продолжить;
		КонецЕсли;
		
		РезультатДействийЭДО = ПолучитьПоДаннымРасшифрованногоМаркера(ИдентификаторЭДО, РасшифрованныйМаркер,
			ПриниматьПриглашенияЭДОАвтоматически);
		
		ЗаполнитьРезультатПолученияПоИдентификаторуЭДО(Результат, ИдентификаторЭДО, ДатаНачала, РезультатДействийЭДО);
		
		ОбновитьДатуПолучения();
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  СостоянияПолучения - см. РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.СостояниеПоИдентификаторамЭДО
// 
// Возвращаемое значение:
//  См. НовыйРезультатВариантаПолучения
Функция ПолучитьПоТокенамДоступаКСервисуЭДО(СостоянияПолучения)
	
	Результат = НовыйРезультатВариантаПолучения();
	
	ИдентификаторыЧерезСервис1СЭДО = УчетныеЗаписиЭДО.ИдентификаторыУчетныхЗаписейСпособаОбмена(
		Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО, Ложь);
	
	ПриниматьПриглашенияЭДОАвтоматически = НастройкиЭДО.ПриниматьПриглашенияЭДОАвтоматически();
	ДоступнаПроверкаПодписи = ДоступнаПроверкаПодписи();
	
	Для Каждого ИдентификаторЭДО Из ИдентификаторыЧерезСервис1СЭДО Цикл
		
		Состояние = СостоянияПолучения[ИдентификаторЭДО];
		Если Состояние <> Неопределено И Состояние.Остановлено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ДоступнаПроверкаПодписи Тогда
			ЗаполнитьРезультатПолученияБезПроверкиПодписи(Результат, ИдентификаторЭДО);
			Продолжить;
		КонецЕсли;
		
		ДатаНачала = ТекущаяДатаСеанса();
		
		ПараметрыВыполнения = ПараметрыВыполненияДействийДляПолученияПоЭДО(
			ИдентификаторЭДО, ПриниматьПриглашенияЭДОАвтоматически);
		ПараметрыВыполнения.ИспользоватьТокеныДоступа = Истина;
		ПараметрыВыполнения.РасшифрованныеМаркеры = КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных();
		
		РезультатДействийЭДО = ЭлектронныеДокументыЭДОСлужебный.ВыполнитьДействияЭДО(ПараметрыВыполнения);
		
		ЗаполнитьРезультатПолученияПоИдентификаторуЭДО(Результат, ИдентификаторЭДО, ДатаНачала, РезультатДействийЭДО);
		
		ОбновитьДатуПолучения();
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  РасшифрованныеМаркеры - См. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
// 
// Возвращаемое значение:
//  См. НовыйРезультатВариантаПолучения
Функция ПолучитьПоРасшифрованнымМаркерам(РасшифрованныеМаркеры)
	
	Результат = НовыйРезультатВариантаПолучения();
	
	ПриниматьПриглашенияЭДОАвтоматически = НастройкиЭДО.ПриниматьПриглашенияЭДОАвтоматически();
	
	Для Каждого РасшифрованныйМаркер Из РасшифрованныеМаркеры Цикл
		ИдентификаторЭДО = РасшифрованныйМаркер.Ключ;
		РасшифрованныеДанные = РасшифрованныйМаркер.Значение;
		ДатаНачала = ТекущаяДатаСеанса();
		
		РезультатДействийЭДО = ПолучитьПоДаннымРасшифрованногоМаркера(ИдентификаторЭДО, РасшифрованныеДанные,
			ПриниматьПриглашенияЭДОАвтоматически);
		
		ЗаполнитьРезультатПолученияПоИдентификаторуЭДО(Результат, ИдентификаторЭДО, ДатаНачала, РезультатДействийЭДО);
		
		ОбновитьДатуПолучения();
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ИдентификаторЭДО - Строка
//  РасшифрованныеДанные - См. КриптографияБЭДКлиентСервер.НовыеДанныеРасшифровки
//  ПриниматьПриглашенияЭДОАвтоматически - Булево
//
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОСлужебный.ВыполнитьДействияЭДО
Функция ПолучитьПоДаннымРасшифрованногоМаркера(ИдентификаторЭДО, РасшифрованныеДанные,
		ПриниматьПриглашенияЭДОАвтоматически)
	
	РасшифрованныеМаркеры = КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных();
	РасшифрованныеМаркеры.Вставить(ИдентификаторЭДО, РасшифрованныеДанные);
	
	ПараметрыВыполнения = ПараметрыВыполненияДействийДляПолученияПоЭДО(
		ИдентификаторЭДО, ПриниматьПриглашенияЭДОАвтоматически);
	ПараметрыВыполнения.РасшифрованныеМаркеры = РасшифрованныеМаркеры;
	
	Возврат ЭлектронныеДокументыЭДОСлужебный.ВыполнитьДействияЭДО(ПараметрыВыполнения);
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * Включено - Булево
//  * РезультатПолучения - Неопределено
//                       - См. НовыйРезультатПолучения
//  * ДлительнаяОперация - Неопределено
//                       - См. ДлительныеОперации.ВыполнитьФункцию
Функция НовыйРезультатПериодическойОтправкиДанных() Экспорт
	Результат = Новый Структура;
	Результат.Вставить("Включено", Ложь);
	Результат.Вставить("РезультатПолучения", Неопределено);
	Результат.Вставить("ДлительнаяОперация", Неопределено);
	Возврат Результат;
КонецФункции

// Параметры:
//  ИдентификаторыЭДО - Массив из Строка
Процедура УстановитьПризнакОстановленПоИдентификаторамЭДО(ИдентификаторыЭДО)
	Для Каждого ИдентификаторЭДО Из ИдентификаторыЭДО Цикл
		РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.УстановитьПризнакОстановки(ИдентификаторЭДО);
	КонецЦикла;
КонецПроцедуры

// Параметры:
//  КлючДоступа - Строка
//  ИдентификаторЭДО - Строка
//  Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 
// Возвращаемое значение:
//  См. КриптографияБЭДКлиентСервер.НовыеДанныеРасшифровки
Функция РасшифрованныйМаркерПоКлючуДоступа(КлючДоступа, ИдентификаторЭДО, Сертификат, КонтекстДиагностики)
	
	КлючСинхронизации = СинхронизацияЭДОКлиентСервер.НовыйКлючСинхронизации();
	КлючСинхронизации.ИдентификаторУчетнойЗаписи = ИдентификаторЭДО;
	КлючСинхронизации.СпособОбмена = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО;
	КлючСинхронизации.ВыбранныйСертификат = Сертификат;
	КлючСинхронизации.КлючДоступаКСервисуЭДО = КлючДоступа;
	
	Маркер = СервисЭДО.Авторизоваться(КлючСинхронизации, КонтекстДиагностики);
	
	РасшифрованныйМаркер = КриптографияБЭДКлиентСервер.НовыеДанныеРасшифровки();
	РасшифрованныйМаркер.Сертификат = Сертификат;
	РасшифрованныйМаркер.Данные = Маркер;
	
	Возврат РасшифрованныйМаркер;
	
КонецФункции

// Возвращаемое значение:
//  Булево
Функция ДоступнаПроверкаПодписи()
	
	Возврат КриптографияБЭД.ДоступнаПроверкаПодписиНаСервере()
		Или КриптографияБЭД.ДоступнаПроверкаПодписиВОблаке();
	
КонецФункции

// Параметры:
//  Результат - см. НовыйРезультатВариантаПолучения
//  ИдентификаторЭДО - Строка
Процедура ЗаполнитьРезультатПолученияБезПроверкиПодписи(Результат, ИдентификаторЭДО)
	
	Подсистема = ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().АвтоматическоеПолучениеЭДО;
	ТекстСообщения = СтрШаблон(
		НСтр("ru = 'Автоматическое получение недоступно для учетной записи ЭДО %1 по причине:';
			|en = 'Automatic receipt is not available for the %1 EDI account due to:'"), ИдентификаторЭДО)
		+ Символы.ПС + НСтр("ru = 'Недоступна проверка электронных подписей на сервере.';
							|en = 'Cannot check digital signatures on the server.'");
	ОбщегоНазначенияБЭД.ЗаписатьВЖурналРегистрации(ТекстСообщения, Подсистема);
	
	ЕстьОшибки = Истина;
	ДатаНачала = ТекущаяДатаСеанса();
	Остановлено = Ложь;
	СостояниеПолучения = РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.Записать(
		ИдентификаторЭДО, ЕстьОшибки, ДатаНачала, Остановлено);
	Результат.СостояниеПоИдентификаторамЭДО.Вставить(ИдентификаторЭДО, СостояниеПолучения);
	
КонецПроцедуры

// Параметры:
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 
// Возвращаемое значение:
//  Структура:
//  * ЕстьОшибки - Булево
//  * ЕстьКритичныеОшибки - Булево
//  * ОтсутствуетИнтернетСоединение - Булево
Функция ПроверитьНаличиеОшибокПолученияЭДО(КонтекстДиагностики) 
	
	Результат = Новый Структура;
	Результат.Вставить("ЕстьОшибки", Ложь);
	Результат.Вставить("ЕстьКритичныеОшибки", Ложь);
	Результат.Вставить("ОтсутствуетИнтернетСоединение", Ложь);
	
	Ошибки = ОбработкаНеисправностейБЭДКлиентСервер.ПолучитьОшибки(КонтекстДиагностики);
	Если Не ЗначениеЗаполнено(Ошибки) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого Ошибка Из Ошибки Цикл
		
		Если ИнтернетСоединениеБЭД.ЭтоОшибкаИнтернетСоединение(Ошибка) Тогда
			Результат.ОтсутствуетИнтернетСоединение = Истина;
		КонецЕсли;
		
		Если Не СинхронизацияЭДО.ЭтоОшибкаЕстьНеполученныеЭлектронныеДокументы(Ошибка)
			И Не СинхронизацияЭДО.ЭтоОшибкаЕстьНеполученныеПриглашения(Ошибка) Тогда
			Результат.ЕстьОшибки = Истина;
		КонецЕсли;
		
		Если СинхронизацияЭДО.ЭтоОшибкаНекорректныйТокенДоступаКСервисуЭДО(Ошибка) Тогда
			Результат.ЕстьКритичныеОшибки = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ИдентификаторЭДО - Строка
//  ПриниматьПриглашенияЭДОАвтоматически - Булево
// 
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО
Функция ПараметрыВыполненияДействийДляПолученияПоЭДО(ИдентификаторЭДО, ПриниматьПриглашенияЭДОАвтоматически)
	
	ПараметрыВыполнения = ЭлектронныеДокументыЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	ПараметрыВыполнения.ОбъектыДействий.ИдентификаторыОрганизаций.Добавить(ИдентификаторЭДО);
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(ПараметрыВыполнения.НаборДействий,
		Перечисления.ДействияПоЭДО.ЗагрузитьПриглашения);
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(ПараметрыВыполнения.НаборДействий,
		Перечисления.ДействияПоЭДО.Загрузить);
	
	Если ПриниматьПриглашенияЭДОАвтоматически Тогда
		ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(ПараметрыВыполнения.НаборДействий,
			Перечисления.ДействияПоЭДО.ПринятьПриглашения);
	КонецЕсли;
	
	Возврат ПараметрыВыполнения;
	
КонецФункции

// Параметры:
//  Результат - см. НовыйРезультатВариантаПолучения
//  ИдентификаторЭДО - Строка
//  ДатаНачала - Дата
//  РезультатДействийЭДО - см. ЭлектронныеДокументыЭДОСлужебный.ВыполнитьДействияЭДО
Процедура ЗаполнитьРезультатПолученияПоИдентификаторуЭДО(Результат, ИдентификаторЭДО, ДатаНачала, РезультатДействийЭДО)
	
	ЭлектронныеДокументыЭДОКлиентСервер.ДополнитьИтогВыполненияДействийПоЭДО(
		Результат.ИтогДействийПоЭДО, РезультатДействийЭДО.Итог);
	ОбработкаНеисправностейБЭДКлиентСервер.ДополнитьКонтекстДиагностики(
		Результат.КонтекстДиагностики, РезультатДействийЭДО.КонтекстДиагностики);
	
	РезультатПроверки = ПроверитьНаличиеОшибокПолученияЭДО(РезультатДействийЭДО.КонтекстДиагностики);
	
	Если РезультатПроверки.ОтсутствуетИнтернетСоединение Тогда
		Остановлено = Ложь;
	ИначеЕсли РезультатПроверки.ЕстьКритичныеОшибки Тогда
		Остановлено = Истина;
	Иначе
		Остановлено = Неопределено;
	КонецЕсли;
	
	СостояниеПолучения = РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.Записать(
		ИдентификаторЭДО, РезультатПроверки.ЕстьОшибки, ДатаНачала, Остановлено);
	Результат.СостояниеПоИдентификаторамЭДО.Вставить(ИдентификаторЭДО, СостояниеПолучения);
	
КонецПроцедуры

// Параметры:
//  Приемник - см. НовыйРезультатПолучения
//  Источник - см. НовыйРезультатПолучения
Процедура ДополнитьРезультатПолучения(Приемник, Источник)
	
	ЭлектронныеДокументыЭДОКлиентСервер.ДополнитьИтогВыполненияДействийПоЭДО(
		Приемник.ИтогДействийПоЭДО, Источник.ИтогДействийПоЭДО);
	ОбработкаНеисправностейБЭДКлиентСервер.ДополнитьКонтекстДиагностики(
		Приемник.КонтекстДиагностики, Источник.КонтекстДиагностики);
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
		Приемник.СостояниеПоИдентификаторамЭДО, Источник.СостояниеПоИдентификаторамЭДО);
	
КонецПроцедуры

#КонецОбласти

#Область Предупреждение

// Параметры:
//  ИдентификаторЭДО - Строка
// 
// Возвращаемое значение:
//  Структура:
//  * Уровень - Строка
//  * ДатаНачала - Дата
//  * ДатаОкончания - Дата
Функция ОтборЖурналаРегистрации(ИдентификаторЭДО) Экспорт
	
	Период = РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.ПериодПолученияПоИдентификаторуЭДО(ИдентификаторЭДО);
	Если Период = Неопределено Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Отсутствует состояние автоматического получения по идентификатору ЭДО %1';
				|en = 'There is no automatic receipt state by EDI ID %1'"),
			ИдентификаторЭДО);
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Уровень"       , "Ошибка");
	Отбор.Вставить("ДатаНачала"    , Период.ДатаНачала);
	Отбор.Вставить("ДатаОкончания" , Период.ДатаОкончания);
	Возврат Отбор;
	
КонецФункции

#КонецОбласти

#КонецОбласти // СлужебныеПроцедурыИФункции
