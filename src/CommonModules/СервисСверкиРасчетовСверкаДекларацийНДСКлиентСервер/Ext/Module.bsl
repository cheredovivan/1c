#Область ПрограммныйИнтерфейс

// В процедуре выводятся результаты сверки на форму
// 
//Параметры:
// Форма - ФормаКлиентскогоПриложения - форма отчета (книга покупок/продаж или декларация по НДС),
//		где необходимо вывести результаты сверки
// ДопЛисты - Булево - для книг покупок/продаж могут выводиться данные для основного раздела или для доп листов
// НомерРаздела - Строка - для декларации по НДС в одной форме выводятся сразу все разделы 8, 8.1, 9, 9.1
// ПервыйРаздел - Булево - для декларации по НДС необходимо определить какой раздел первый - чтобы сделать для него выделение
// 
Процедура ОтобразитьСверкуНаФорме(Форма, ДопЛисты = Ложь, НомерРаздела ="", ПервыйРаздел = Ложь) Экспорт
	
	Если Не Форма.ИспользуетсяСервисСверкиРасчетов Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы = Форма.Элементы;
	ЭлементыФормы["ГруппаСверкиРасхождения" + НомерРаздела].Видимость            = Ложь;
	ЭлементыФормы["ГруппаСверкиНеОтраженоУКонтрагента" + НомерРаздела].Видимость = Ложь;
	ЭлементыФормы["ГруппаСверкаНеСверялись" + НомерРаздела].Видимость            = Ложь;
	ЭлементыФормы["ГруппаСверкаБезРасхождений" + НомерРаздела].Видимость         = Ложь;
	ЭлементыФормы["ГруппаСверкаБезОшибок" + НомерРаздела].Видимость              = Ложь;
	
	// раздел "Нет в организации" есть только в основном разделе книги продаж или в 9 разделе Декларации по НДС
	Если Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "КнигаПродаж" Или НомерРаздела = "Раздел9" Тогда
		ЭлементыФормы["ГруппаСверкиНетВОрганизации" + НомерРаздела].Видимость        = Ложь;
	КонецЕсли;
	
	РезультатСверки = СервисСверкиРасчетовСверкаДекларацийНДСВызовСервера.ПодготовитьДанныеДляОтображенияСверки(
		Форма.ДанныеДляСверкиДеклараций.АдресРезультатаСверки,
		Форма.ДанныеДляСверкиДеклараций.ИмяОтчета, ДопЛисты, НомерРаздела);
		
	Если Не Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "Декларация" Тогда
		ЭлементыФормы.ГруппаОжиданиеСверки.Видимость = Ложь;
		Если РезультатСверки = Неопределено Тогда
			ЭлементыФормы.ГруппаНеПолучилиДанныеИзСервиса.Видимость = Истина;
			ЭлементыФормы.ТекстНеПолучилиДанныеИзСервиса.Заголовок = НСтр("ru = 'Не удалось получить данные из сервиса ""1С:Сверка 2.0"" или произвести сверку. ';
																			|en = 'Cannot get data from the 1C:Reconciliation 2.0 service or perform reconciliation.·'");
			Если Не Форма.РазворачиватьРезультатыСверкиДекларацийСразу Тогда
				ЭлементыФормы.ИзменитьВидимостьСверки.Картинка = БиблиотекаКартинок.СтрелкаВлево;
				Форма.ОтображатьСверку = Ложь;
				ЭлементыФормы.ГруппаИтогиСверки.Видимость = Ложь;
			Иначе
				Форма.ОтображатьСверку = Истина;
				ЭлементыФормы.ГруппаИтогиСверки.Видимость = Истина;
			КонецЕсли;
			
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОтображатьСверку = Ложь;
	СверкаПрошлаУспешно = Истина;
	ЕстьРасхожденияИлиНеСверенныеДокументы = Ложь;
	ТекущийРаздел = "";
	
	Если РезультатСверки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатСверки.ТаблицаРасхожденияКоличествоДокументов > 0 Тогда
		ОтображатьСверку = Истина;
		СверкаПрошлаУспешно = Ложь;
		ЕстьРасхожденияИлиНеСверенныеДокументы = Истина;
		ЭлементыФормы["ТекстРасхождения" + НомерРаздела].Заголовок = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'С расхождениями (%1)';
				|en = 'With discrepancies (%1)'"),
			РезультатСверки.ТаблицаРасхожденияКоличествоДокументов);

		ЭлементыФормы["ТекстРасхожденияРасшифровка" + НомерРаздела].Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'на сумму %1 руб. (НДС %2 руб.)';
				|en = 'in the amount of %1 rub. (%2 rub. VAT)'"),
			РезультатСверки.ТаблицаРасхожденияВсего,
			РезультатСверки.ТаблицаРасхожденияНДС);
			
		ЭлементыФормы["ГруппаСверкиРасхождения" + НомерРаздела].Видимость = Истина;
	
		Если ПервыйРаздел Тогда
			ТекущийРаздел = "ТаблицаРасхождения" + НомерРаздела;
		КонецЕсли;
		
	КонецЕсли;
	
	Если РезультатСверки.ТаблицаНетУКонтрагентаКоличествоДокументов > 0 Тогда
		ОтображатьСверку = Истина;
		СверкаПрошлаУспешно = Ложь;
		ЕстьРасхожденияИлиНеСверенныеДокументы = Истина;
		Если Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "КнигаПродаж" Или НомерРаздела = "Раздел9" Или НомерРаздела = "Раздел9_Прил1" Тогда
			ЭлементыФормы["ТекстНеОтражено" + НомерРаздела].Заголовок = 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не отражены у покупателей (%1)';
						|en = 'Not recorded by customers (%1)'"),
					РезультатСверки.ТаблицаНетУКонтрагентаКоличествоДокументов);
		ИначеЕсли Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "КнигаПокупок" Или НомерРаздела = "Раздел8" Или НомерРаздела = "Раздел8_Прил1"  Тогда
			ЭлементыФормы["ТекстНеОтражено" + НомерРаздела].Заголовок = 
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не отражены у продавцов (%1)';
						|en = 'Not recorded by vendors (%1)'"),
					РезультатСверки.ТаблицаНетУКонтрагентаКоличествоДокументов);
		КонецЕсли;
		
		ЭлементыФормы["ТекстНеОтраженоРасшифровка" + НомерРаздела].Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'на сумму %1 руб. (НДС %2 руб.)';
				|en = 'in the amount of %1 rub. (%2 rub. VAT)'"),
			РезультатСверки.ТаблицаНетУКонтрагентаВсего,
			РезультатСверки.ТаблицаНетУКонтрагентаНДС);
		ЭлементыФормы["ГруппаСверкиНеОтраженоУКонтрагента" + НомерРаздела].Видимость = Истина;
		
		Если ПервыйРаздел И ТекущийРаздел = "" Тогда
			ТекущийРаздел = "ТаблицаНетУКонтрагента" + НомерРаздела;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ((Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "КнигаПродаж" И Не ДопЛисты)
		Или НомерРаздела = "Раздел9")
		И РезультатСверки.ТаблицаНетВОрганизацииКоличествоДокументов > 0 Тогда
		ОтображатьСверку = Истина;
		СверкаПрошлаУспешно = Ложь;
		ЕстьРасхожденияИлиНеСверенныеДокументы = Истина;
		
		ЭлементыФормы["ТекстНетВОрганизации" + НомерРаздела].Заголовок = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не отражены в организации (%1)';
				|en = 'Not recorded by company (%1)'"),
			РезультатСверки.ТаблицаНетВОрганизацииКоличествоДокументов);

		ЭлементыФормы["ТекстРасшифровкаНетВОрганизации" + НомерРаздела].Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'на сумму %1 руб. (НДС %2 руб.)';
				|en = 'in the amount of %1 rub. (%2 rub. VAT)'"),
			РезультатСверки.ТаблицаНетВОрганизацииВсего,
			РезультатСверки.ТаблицаНетВОрганизацииНДС);
			ЭлементыФормы["ГруппаСверкиНетВОрганизации" + НомерРаздела].Видимость = Истина;
			
		Если ПервыйРаздел И ТекущийРаздел = "" Тогда
			ТекущийРаздел = "ТаблицаНетВОрганизации" + НомерРаздела;
		КонецЕсли;

	КонецЕсли;
	
	Если РезультатСверки.ТаблицаНеСверилиКоличествоДокументов > 0 Тогда
		ОтображатьСверку = Истина;
		СверкаПрошлаУспешно = Ложь;
		ЕстьРасхожденияИлиНеСверенныеДокументы = Истина;
		
		ЭлементыФормы["ТекстНеСверялись" + НомерРаздела].Заголовок = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не сверились (%1)';
				|en = 'Not reconciled (%1)'"),
			РезультатСверки.ТаблицаНеСверилиКоличествоДокументов);
			
		ЭлементыФормы["ТекстНеСверилисьРасшифровка" + НомерРаздела].Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'на сумму %1 руб. (НДС %2 руб.)';
				|en = 'in the amount of %1 rub. (%2 rub. VAT)'"),
			РезультатСверки.ТаблицаНеСверилиВсего,
			РезультатСверки.ТаблицаНеСверилиНДС);
			
		ЭлементыФормы["ГруппаСверкаНеСверялись" + НомерРаздела].Видимость = Истина;
		
		Если ПервыйРаздел И ТекущийРаздел = "" Тогда
			ТекущийРаздел = "ТаблицаНеСверили" + НомерРаздела;
		КонецЕсли;

	КонецЕсли;
	
	Если РезультатСверки.ТаблицаСверилиБезОшибокКоличествоДокументов > 0 Тогда
		ОтображатьСверку = Истина;
		СверкаПрошлаУспешно = Истина;
		
		ЭлементыФормы["ТекстБезРасхождений" + НомерРаздела].Заголовок = 
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Без расхождений (%1)';
				|en = 'Without discrepancies (%1)'"),
			РезультатСверки.ТаблицаСверилиБезОшибокКоличествоДокументов);
			
		ЭлементыФормы["ТекстБезРасхожденийРасшифровка" + НомерРаздела].Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'на сумму %1 руб. (НДС %2 руб.)';
				|en = 'in the amount of %1 rub. (%2 rub. VAT)'"),
			РезультатСверки.ТаблицаСверилиБезОшибокВсего,
			РезультатСверки.ТаблицаСверилиБезОшибокНДС);
		ЭлементыФормы["ГруппаСверкаБезРасхождений" + НомерРаздела].Видимость = Истина;
		
		Если ПервыйРаздел И ТекущийРаздел = "" Тогда
			ТекущийРаздел = "ТаблицаСверилиБезОшибок" + НомерРаздела;
		КонецЕсли;

	КонецЕсли;
	
	Если Не Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "Декларация" Тогда
		
		СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер.СделатьВыделениеТекущегоРаздела(Форма, "СамОтчет");
		ЭлементыФормы.ГруппаРезультатСверки.Видимость      = ОтображатьСверку;
		ЭлементыФормы.ГруппаСверкаБезОшибок.Видимость      = СверкаПрошлаУспешно И Не ЕстьРасхожденияИлиНеСверенныеДокументы;
		ЭлементыФормы.ГруппаСверкаСРасхождениями.Видимость = ЕстьРасхожденияИлиНеСверенныеДокументы;
		
		Если Не Форма.РазворачиватьРезультатыСверкиДекларацийСразу Тогда
			ЭлементыФормы.ИзменитьВидимостьСверки.Картинка = БиблиотекаКартинок.СтрелкаВлево;
			Форма.ОтображатьСверку = Ложь;
			ЭлементыФормы.ГруппаИтогиСверки.Видимость = Ложь;
		Иначе
			Форма.ОтображатьСверку = ОтображатьСверку;
			ЭлементыФормы.ГруппаИтогиСверки.Видимость = Истина;
		КонецЕсли;
		
		Если ДопЛисты Тогда
			Форма.ДанныеДляСверкиДеклараций.ДопЛисты = ПоместитьВоВременноеХранилище(Форма.Результат, Форма.УникальныйИдентификатор);
			Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел = "ДопЛисты";
			Если Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "КнигаПокупок" Тогда
				ЭлементыФормы.ТекстКнигаПокупок.Заголовок = "Все доп. листы";
			Иначе
				ЭлементыФормы.ТекстКнигаПродаж.Заголовок = "Все доп. листы";
			КонецЕсли;
		Иначе
			Форма.ДанныеДляСверкиДеклараций.ОсновнойРаздел = ПоместитьВоВременноеХранилище(Форма.Результат, Форма.УникальныйИдентификатор);
			Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел = "СамОтчет";
			Если Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "КнигаПокупок" Тогда
				ЭлементыФормы.ТекстКнигаПокупок.Заголовок = "Вся книга покупок";
			Иначе
				ЭлементыФормы.ТекстКнигаПродаж.Заголовок = "Вся книга продаж";
			КонецЕсли;
		КонецЕсли;
	Иначе
		ЭлементыФормы["ГруппаСверкаСРасхождениями"+ НомерРаздела].Видимость = ЕстьРасхожденияИлиНеСверенныеДокументы;
		ЭлементыФормы["ГруппаСверкаБезОшибок"+ НомерРаздела].Видимость      = СверкаПрошлаУспешно И Не ЕстьРасхожденияИлиНеСверенныеДокументы;
		
		Если ТекущийРаздел <> "" Тогда
			Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел = ТекущийРаздел;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// В процедуре заполняется пустая структура реквизитов, необходимых для сверки
// для книг/декларации/помощника НДС свой набор необходимых реквизитов,
// обусловлено это разным выводом результатов сверки на форму
// 
// Параметры:
// Форма - ФормаКлиентскогоПриложения - форма отчета (книга покупок/продаж, декларация по НДС, Помощника по НДС),
//			где необходимо вывести результаты сверки
// ИмяОтчета - Строка - имя отчета для коготорого производится подготовка реквизитов для сверки
// 
Процедура ИнициализацияРеквизитовСверкиДекларации(Форма, ИмяОтчета) Экспорт
	
	РеквизитыСверки = Новый Структура;
	РеквизитыСверки.Вставить("ВыводитьТолькоДопЛисты");
	РеквизитыСверки.Вставить("КонтрагентДляОтбора");
	
	// Наименование раздела, который открыт у пользователя, по умолчанию,
	// сразу после сверки отображается или книга покупок, или книга продаж
	// доступные значения зависят от того, откуда открываем их, из книги или из декларации
	РеквизитыСверки.Вставить("ТекущийРаздел");
	// Данные, которые получили в результате сверки
	РеквизитыСверки.Вставить("АдресРезультатаСверки");
	
	РеквизитыСверки.Вставить("ДопЛисты");
	РеквизитыСверки.Вставить("ИмяОтчета", ИмяОтчета);
	// Адрес, по которому размещены данные, используемые для сверки	

	Если ИмяОтчета = "КнигаПокупок" Или ИмяОтчета = "КнигаПродаж" Тогда
		РеквизитыСверки.Вставить("НедействующиеКонтрагенты");
		РеквизитыСверки.Вставить("ОсновнойРаздел");
		РеквизитыСверки.Вставить("АдресДанныхОтчета");
		РеквизитыСверки.Вставить("АдресДанныхДопЛиста");
	ИначеЕсли ИмяОтчета = "Декларация" Тогда
		РеквизитыСверки.Вставить("НеУдалосьВыполнитьСверку", Ложь);
		РеквизитыСверки.Вставить("ЕстьРасхождения", Ложь);
		РеквизитыСверки.Вставить("ЕстьНеСверенные", Ложь);
		РеквизитыСверки.Вставить("ПроверкаВыполнена", Ложь);
	ИначеЕсли ИмяОтчета = "ПомощникПоНДС" Тогда
		РеквизитыСверки.Вставить("ДанныеОтчета");
		РеквизитыСверки.Вставить("НеУдалосьВыполнитьСверку",           Ложь);
		РеквизитыСверки.Вставить("ЕстьРасхожденияВКнигеПокупок",       Ложь);
		РеквизитыСверки.Вставить("ЕстьРасхожденияВКнигеПродаж",        Ложь);
		РеквизитыСверки.Вставить("ЕстьНеСверенныеВКнигеПокупок",       Ложь);
		РеквизитыСверки.Вставить("ЕстьНеСверенныеВКнигеПродаж",        Ложь);
		РеквизитыСверки.Вставить("ЕстьДанныеВКнигеПокупок",            Ложь);
		РеквизитыСверки.Вставить("ЕстьДанныеВКнигеПродаж",             Ложь);
		РеквизитыСверки.Вставить("КоличествоРасхожденийВКнигеПокупок", 0);
		РеквизитыСверки.Вставить("КоличествоНеСверенныхВКнигеПокупок", 0);
		РеквизитыСверки.Вставить("КоличествоРасхожденийВКнигеПродаж",  0);
		РеквизитыСверки.Вставить("КоличествоНеСверенныхВКнигеПродаж",  0);
	КонецЕсли;
	
	Форма.ДанныеДляСверкиДеклараций = РеквизитыСверки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// В процедуре выделяется текущий раздел сверки - выделенный пользователем, жирным цветом
// 
//Параметры:
// Форма - ФормаКлиентскогоПриложения - форма отчета (книга покупок/продаж) где необходимо выделить выбранный раздел
// ИмяРаздела - Строка - раздел, который необходимо выделить жирным 
// 
Процедура СделатьВыделениеТекущегоРаздела(Форма, ИмяРаздела) Экспорт
	
	ЭлементыФормы = Форма.Элементы;
	
	ЖирныйШрифт  = Новый Шрифт(, , Истина);
	ОбычныйШрифт = Новый Шрифт();
	Если ИмяРаздела = "СамОтчет" Или ИмяРаздела = "ДопЛисты" Тогда
		Если Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "КнигаПродаж" Тогда
			ЭлементыФормы.ТекстКнигаПродаж.Шрифт = ЖирныйШрифт;
		ИначеЕсли Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "КнигаПокупок" Тогда
			ЭлементыФормы.ТекстКнигаПокупок.Шрифт = ЖирныйШрифт;
		КонецЕсли;
	Иначе
		Если Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "КнигаПродаж" Тогда
			ЭлементыФормы.ТекстКнигаПродаж.Шрифт = ОбычныйШрифт;
		ИначеЕсли Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "КнигаПокупок" Тогда
			ЭлементыФормы.ТекстКнигаПокупок.Шрифт = ОбычныйШрифт;
		КонецЕсли;
	КонецЕсли;

	Если ИмяРаздела = "ТаблицаРасхождения" Или ИмяРаздела = "ТаблицаРасхожденияДопЛист" Тогда
		ЭлементыФормы.ТекстРасхождения.Шрифт     = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстРасхождения.Шрифт     = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаНетУКонтрагента" Или ИмяРаздела = "ТаблицаНетУКонтрагентаДопЛист" Тогда
		ЭлементыФормы.ТекстНеОтражено.Шрифт      = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстНеОтражено.Шрифт      = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаНетВОрганизации" Тогда
		ЭлементыФормы.ТекстНетВОрганизации.Шрифт = ЖирныйШрифт;
	ИначеЕсли Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "КнигаПродаж" Тогда
		ЭлементыФормы.ТекстНетВОрганизации.Шрифт = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаНеСверили" Или ИмяРаздела = "ТаблицаНеСверилиДопЛист" Тогда
		ЭлементыФормы.ТекстНеСверялись.Шрифт     = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстНеСверялись.Шрифт     = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаСверилиБезОшибок" Или ИмяРаздела = "ТаблицаСверилиБезОшибокДопЛист" Тогда
		ЭлементыФормы.ТекстБезРасхождений.Шрифт  = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстБезРасхождений.Шрифт  = ОбычныйШрифт;
	КонецЕсли;
	
КонецПроцедуры

// В процедуре выделяется текущий раздел сверки в декларации - выделенный пользователем, жирным цветом
// так же выделяется группа, в которой выбирается раздел
// например Раздел 8 (группа или раздел самой декларации), С расхождениями (раздел сверки)
// 
//Параметры:
// Форма - ФормаКлиентскогоПриложения - форма отчета (книга покупок/продаж) где необходимо выделить выбранный раздел
// ИмяРаздела - Строка - раздел сверки, который необходимо выделить жирным
//               Например С расхождениями/Не сверились/Нет у контрагента/Без расхождений
// НомерРаздела - Строка - раздел декларации, который соответствует группе на форме
//               Например, раздел 8, раздел 9
// 
Процедура СделатьВыделениеТекущегоРазделаДляДекларации(Форма, ИмяРаздела, НомерРаздела) Экспорт
	
	ЭлементыФормы = Форма.Элементы;
	
	ЖирныйШрифт  = Новый Шрифт(, , Истина);
	ОбычныйШрифт = Новый Шрифт();
	
	// Раздел 8
	Если ИмяРаздела = "ТаблицаРасхожденияРаздел8" Тогда
		ЭлементыФормы.ТекстРасхожденияРаздел8.Шрифт     = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстРасхожденияРаздел8.Шрифт     = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаНетУКонтрагентаРаздел8" Тогда
		ЭлементыФормы.ТекстНеОтраженоРаздел8.Шрифт      = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстНеОтраженоРаздел8.Шрифт      = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаНеСверилиРаздел8" Тогда
		ЭлементыФормы.ТекстНеСверялисьРаздел8.Шрифт     = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстНеСверялисьРаздел8.Шрифт     = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаСверилиБезОшибокРаздел8" Тогда
		ЭлементыФормы.ТекстБезРасхожденийРаздел8.Шрифт  = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстБезРасхожденийРаздел8.Шрифт  = ОбычныйШрифт;
	КонецЕсли;
	
	// Раздел 8 Прил.1
	Если ИмяРаздела = "ТаблицаРасхожденияРаздел8_Прил1" Тогда
		ЭлементыФормы.ТекстРасхожденияРаздел8_Прил1.Шрифт     = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстРасхожденияРаздел8_Прил1.Шрифт     = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаНетУКонтрагентаРаздел8_Прил1" Тогда
		ЭлементыФормы.ТекстНеОтраженоРаздел8_Прил1.Шрифт      = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстНеОтраженоРаздел8_Прил1.Шрифт      = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаНеСверилиРаздел8_Прил1" Тогда
		ЭлементыФормы.ТекстНеСверялисьРаздел8_Прил1.Шрифт     = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстНеСверялисьРаздел8_Прил1.Шрифт     = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаСверилиБезОшибокРаздел8_Прил1" Тогда
		ЭлементыФормы.ТекстБезРасхожденийРаздел8_Прил1.Шрифт  = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстБезРасхожденийРаздел8_Прил1.Шрифт  = ОбычныйШрифт;
	КонецЕсли;
	
	// Раздел 9
	Если ИмяРаздела = "ТаблицаРасхожденияРаздел9" Тогда
		ЭлементыФормы.ТекстРасхожденияРаздел9.Шрифт     = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстРасхожденияРаздел9.Шрифт     = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаНетУКонтрагентаРаздел9" Тогда
		ЭлементыФормы.ТекстНеОтраженоРаздел9.Шрифт      = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстНеОтраженоРаздел9.Шрифт      = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаНеСверилиРаздел9" Тогда
		ЭлементыФормы.ТекстНеСверялисьРаздел9.Шрифт     = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстНеСверялисьРаздел9.Шрифт     = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаСверилиБезОшибокРаздел9" Тогда
		ЭлементыФормы.ТекстБезРасхожденийРаздел9.Шрифт  = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстБезРасхожденийРаздел9.Шрифт  = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаНетВОрганизацииРаздел9" Тогда
		ЭлементыФормы.ТекстНетВОрганизацииРаздел9.Шрифт = ЖирныйШрифт;
	ИначеЕсли НомерРаздела = "Раздел9" Тогда
		ЭлементыФормы.ТекстНетВОрганизацииРаздел9.Шрифт = ОбычныйШрифт;
	КонецЕсли;
	
	// Раздел 9 Прил.1
	Если ИмяРаздела = "ТаблицаРасхожденияРаздел9_Прил1" Тогда
		ЭлементыФормы.ТекстРасхожденияРаздел9_Прил1.Шрифт     = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстРасхожденияРаздел9_Прил1.Шрифт     = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаНетУКонтрагентаРаздел9_Прил1" Тогда
		ЭлементыФормы.ТекстНеОтраженоРаздел9_Прил1.Шрифт      = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстНеОтраженоРаздел9_Прил1.Шрифт      = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаНеСверилиРаздел9_Прил1" Тогда
		ЭлементыФормы.ТекстНеСверялисьРаздел9_Прил1.Шрифт     = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстНеСверялисьРаздел9_Прил1.Шрифт     = ОбычныйШрифт;
	КонецЕсли;
	
	Если ИмяРаздела = "ТаблицаСверилиБезОшибокРаздел9_Прил1" Тогда
		ЭлементыФормы.ТекстБезРасхожденийРаздел9_Прил1.Шрифт  = ЖирныйШрифт;
	Иначе
		ЭлементыФормы.ТекстБезРасхожденийРаздел9_Прил1.Шрифт  = ОбычныйШрифт;
	КонецЕсли;
	
	// выделение группы
	Если НомерРаздела = "Раздел8" Тогда
		ЭлементыФормы.ГруппаИтогиСверкиРаздел8.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
	Иначе
		ЭлементыФормы.ГруппаИтогиСверкиРаздел8.Отображение = ОтображениеОбычнойГруппы.Нет;
	КонецЕсли;
	
	Если НомерРаздела = "Раздел8_Прил1" Тогда
		ЭлементыФормы.ГруппаИтогиСверкиРаздел8_Прил1.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
	Иначе
		ЭлементыФормы.ГруппаИтогиСверкиРаздел8_Прил1.Отображение = ОтображениеОбычнойГруппы.Нет;
	КонецЕсли;
	
	Если НомерРаздела = "Раздел9" Тогда
		ЭлементыФормы.ГруппаИтогиСверкиРаздел9.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
	Иначе
		ЭлементыФормы.ГруппаИтогиСверкиРаздел9.Отображение = ОтображениеОбычнойГруппы.Нет;
	КонецЕсли;
	
	Если НомерРаздела = "Раздел9_Прил1" Тогда
		ЭлементыФормы.ГруппаИтогиСверкиРаздел9_Прил1.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
	Иначе
		ЭлементыФормы.ГруппаИтогиСверкиРаздел9_Прил1.Отображение = ОтображениеОбычнойГруппы.Нет;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
