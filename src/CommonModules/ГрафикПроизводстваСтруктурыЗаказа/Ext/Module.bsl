
#Область ПрограммныйИнтерфейс

// Рассчитать график заказа динамической структуры.
// 
// Параметры:
//  Заказ - ДокументСсылка.ЗаказНаПроизводство2_2
//  Настройки - Структура - Настройки:
//   * СтатусГрафика - Число
//   * ЗадействоватьРезервДоступности - Булево
//   * ВсеМатериалыВНаличии - Булево
//   * НеограниченныйПаркОборудования - Булево
//   * КруглосуточнаяРаботаБезВыходных - Булево
//   * ОтсутствиеПрочихЗаказов - Булево
//   * ОтменитьРучныеИзмененияГрафика - Булево
// 
// Возвращаемое значение:
//  Структура:
//   * ГрафикЗапланирован - Булево
//   * Ошибки - ТаблицаЗначений - :
//    ** Описание - Строка -
//    ** Расшифровка - Структура - :
//     *** КодОшибки - Строка
//   * Оповещения - Соответствие из Строка
//   * КоличествоЭтапов - Число
//
Функция РассчитатьГрафик(Заказ, Настройки = Неопределено) Экспорт
	
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"МежцеховоеУправление2_2.СтруктураЗаказаРасчетГрафикаПроизводства");
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = РассчитатьГрафикВнутриЗамераВремени(Заказ, Настройки);
	УстановитьПривилегированныйРежим(Ложь);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
		ОписаниеЗамера,
		?(Результат.КоличествоЭтапов > 100, Окр(Результат.КоличествоЭтапов/10), Результат.КоличествоЭтапов));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Рассчитать график сформированных партий.
// 
// Параметры:
//  ГраницаРасчета - Число - номер очереди по которую сделан расчет структуры заказа.
//
Процедура РассчитатьГрафикПартийПриЗапуске(ГраницаРасчета) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	#Область ВТВсеЗаданияНаЗапуск
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Задания.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	Задания.Разделитель         КАК Разделитель,
		|	Задания.ЭтапПроизводства    КАК ЭтапПроизводства,
		|	Задания.КлючПартия          КАК КлючПартия,
		|	Задания.Очередь             КАК Очередь
		|ПОМЕСТИТЬ ВТВсеЗаданияНаЗапуск
		|ИЗ
		|	РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства КАК Задания
		|ГДЕ
		|	Задания.Очередь > 0 И Задания.Очередь <= &ГраницаРасчета
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Очередь";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТВсеЗаданияНаЗапуск");
	
	#КонецОбласти
	
	#Область ВТТекущиеОчереди
	
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Задания.Очередь КАК Очередь
		|ПОМЕСТИТЬ ВТТекущиеОчереди
		|ИЗ
		|	ВТВсеЗаданияНаЗапуск КАК Задания
		|ИНДЕКСИРОВАТЬ ПО
		|	Очередь";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТТекущиеОчереди");
	
	#КонецОбласти
	
	#Область ВТЗаблокированныеОчереди
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТекущиеОчереди.Очередь КАК Очередь
		|ПОМЕСТИТЬ ВТЗаблокированныеОчереди
		|ИЗ
		|	ВТТекущиеОчереди КАК ТекущиеОчереди
		|ГДЕ
		|	(ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ 
		|			РегистрСведений.БуферЗаданийКРасчетуСтруктурыЗаказаРаспределениеЗапасов КАК Т
		|		ГДЕ
		|			ТекущиеОчереди.Очередь = Т.Очередь)
		|	ИЛИ ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ 
		|			РегистрСведений.БуферЗаданийКРасчетуСтруктурыЗаказаСпецификации КАК Т
		|		ГДЕ
		|			ТекущиеОчереди.Очередь = Т.Очередь)
		|	ИЛИ ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ 
		|			РегистрСведений.ЗаданияКРаспределениюЗапасов КАК Т
		|		ГДЕ
		|			ТекущиеОчереди.Очередь = Т.ОчередьРасчетаСтруктурыЗаказов))
		|ИНДЕКСИРОВАТЬ ПО
		|	Очередь";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТЗаблокированныеОчереди");
	
	#КонецОбласти
	
	#Область ВТЗаданияНаЗапуск
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Задания.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	Задания.КлючПартия          КАК КлючПартия,
		|	Задания.Разделитель         КАК Разделитель,
		|	Задания.ЭтапПроизводства    КАК ЭтапПроизводства
		|ПОМЕСТИТЬ ВТЗаданияНаЗапуск
		|ИЗ
		|	ВТВсеЗаданияНаЗапуск КАК Задания
		|ГДЕ
		|	Задания.Очередь НЕ В (
		|		ВЫБРАТЬ
		|			Т.Очередь
		|		ИЗ
		|			ВТЗаблокированныеОчереди КАК Т)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПроизводство,
		|	КлючПартия";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТЗаданияНаЗапуск");
	
	#КонецОбласти
	
	#Область ВТЗадания
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Задания.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	Задания.КлючПартия          КАК КлючПартия,
		|	Задания.Разделитель         КАК Разделитель,
		|	Задания.ЭтапПроизводства    КАК ЭтапПроизводства
		|ПОМЕСТИТЬ ВТЗадания
		|ИЗ
		|	ВТЗаданияНаЗапуск КАК Задания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Задания.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	Задания.КлючПартия          КАК КлючПартия,
		|	Задания.Разделитель         КАК Разделитель,
		|	Задания.ЭтапПроизводства    КАК ЭтапПроизводства
		|ИЗ
		|	РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства КАК Задания
		|ГДЕ
		|	(Задания.ЗаказНаПроизводство, Задания.КлючПартия) В (
		|		ВЫБРАТЬ
		|			Т.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|			Т.КлючПартия          КАК КлючПартия
		|		ИЗ
		|			ВТЗаданияНаЗапуск КАК Т)
		|	И Задания.Очередь = 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПроизводство,
		|	КлючПартия";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТЗадания");
	
	#КонецОбласти
	
	#Область ВТГраницыЗаказов
	
	// Правая граница планируемых заказов
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СтруктураЗаказа.ЗаказНаПроизводство            КАК ЗаказНаПроизводство,
		|	МИНИМУМ(СтруктураЗаказа.ДлительностьДоВыпуска) КАК ДлительностьДоВыпуска
		|ПОМЕСТИТЬ ВТГраницыЗаказов
		|ИЗ
		|	РегистрСведений.СтруктураЗаказа КАК СтруктураЗаказа
		|ГДЕ
		|	(СтруктураЗаказа.ЗаказНаПроизводство, СтруктураЗаказа.КлючПартия) В (
		|		ВЫБРАТЬ
		|			Задания.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|			Задания.КлючПартия КАК КлючПартия
		|		ИЗ
		|			ВТЗадания КАК Задания)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтруктураЗаказа.ЗаказНаПроизводство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПроизводство";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТГраницыЗаказов");
	
	#КонецОбласти
	
	#Область ОчередьЗаказов
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Заказы.Ссылка КАК ЗаказНаПроизводство
		|ИЗ
		|	Документ.ЗаказНаПроизводство2_2 КАК Заказы
		|ГДЕ
		|	Заказы.Ссылка В (
		|		ВЫБРАТЬ
		|			Т.ЗаказНаПроизводство КАК Ссылка
		|		ИЗ
		|			ВТЗадания КАК Т)
		|	И Заказы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство2_2.КПроизводству)
		|	И Заказы.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	Заказы.Приоритет.РеквизитДопУпорядочивания,
		|	Заказы.Подразделение.РеквизитДопУпорядочивания,
		|	Заказы.Очередь";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ОчередьЗаказов");
	
	#КонецОбласти
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ГраницаРасчета", ГраницаРасчета);
	
	УстановитьПривилегированныйРежим(Истина);
	Таблицы = ОбщегоНазначенияУТ.ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса,, Истина, Ложь);
	УстановитьПривилегированныйРежим(Ложь);
	
	ОчередьЗаказов = Таблицы.ОчередьЗаказов;
	Если ОчередьЗаказов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Данные = ПрочитатьЭтапыПартий(МенеджерВременныхТаблиц);
	
	Связи = Данные.Связи;
	ВидыРЦ = Данные.ВидыРЦ;
	ЗагрузкаВидовРЦ = Данные.ЗагрузкаВидовРЦ;
	ДатыПредшественников = Данные.ДатыПредшественников;
	ДатыПоследователей = Данные.ДатыПоследователей;
	
	ТекстыЗапроса.Очистить();
	
	#Область Этапы
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	&ТекстЗапросаВсеПоляТаблицыЭтапы,
		|	ЕСТЬNULL(ГраницыЭтапов.ПраваяГраница, Этапы.ПраваяГраница) КАК ПраваяГраница,
		|	ВЫБОР
		|		КОГДА ГраницыЭтапов.ПраваяГраница ЕСТЬ НЕ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ Этапы.ЕстьПраваяГраница
		|	КОНЕЦ                                                      КАК ЕстьПраваяГраница
		|ИЗ
		|	ВТЭтапы КАК Этапы
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТГраницыЭтапов КАК ГраницыЭтапов
		|	ПО Этапы.КлючПартия = ГраницыЭтапов.КлючПартия
		|		И Этапы.Этап = ГраницыЭтапов.Этап
		|ГДЕ
		|	Этапы.ЗаказНаПроизводство = &Заказ
		|	И Этапы.КлючПартия В (
		|		ВЫБРАТЬ
		|			Т.КлючПартия
		|		ИЗ
		|			ВТЗадания КАК Т
		|		ГДЕ
		|			Т.ЗаказНаПроизводство = &Заказ)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Этапы.НомерЗаписи";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаВсеПоляТаблицыЭтапы", "Этапы.*");
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "Этапы");
	
	#КонецОбласти
	
	#Область ВидыРЦСсылки
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВидыРЦ.ВидРабочегоЦентра КАК Ссылка
		|ИЗ
		|	ВТВидыРЦ КАК ВидыРЦ
		|ГДЕ
		|	ВидыРЦ.ЗаказНаПроизводство = &Заказ";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВидыРЦСсылки");
	
	#КонецОбласти
	
	#Область Подразделения
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Этапы.Подразделение КАК Ссылка
		|ИЗ
		|	ВТЭтапы КАК Этапы
		|ГДЕ
		|	Этапы.ЗаказНаПроизводство = &Заказ";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "Подразделения");
	
	#КонецОбласти
	
	ПоискКлючПартияЭтап = Новый Структура("КлючПартия, Этап");
	ПоискНомерЗаписи = Новый Структура("НомерЗаписи");
	ПоискВРЦ = Новый Структура("ЭтоИндекс, ВидРабочегоЦентра, ДатаИнтервала", Ложь);
	
	Для каждого СтрокаЗаказ Из ОчередьЗаказов Цикл
		Заказ = СтрокаЗаказ.ЗаказНаПроизводство;
		
		// Чтение данных заказа
		Запрос.УстановитьПараметр("Заказ", Заказ);
		Таблицы = ОбщегоНазначенияУТ.ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса,, Истина, Ложь);
		
		// Данные расчета
		ДанныеРасчета = НовыйДанныеРасчета();
		
		Этапы = Таблицы.Этапы; // ТаблицаЗначений
		Этапы.Индексы.Добавить("КлючПартия, Этап");
		ДанныеРасчета.Этапы = Этапы;
		
		ДанныеРасчета.Связи = Связи;
		ДанныеРасчета.ВидыРЦ = ВидыРЦ;
		ДанныеРасчета.ЭтапыСРучнымРазмещением = Новый Массив;
		
		// Параметры расчета
		ПараметрыРасчета = НовыйПараметрыРасчета();
		ПараметрыРасчета.Заказ = Заказ;
		
		// Чтение доступности
		ПараметрыРасчета.ДоступностьОтборПодразделения = Таблицы.Подразделения.ВыгрузитьКолонку("Ссылка");
		
		ОтборВРЦ = Таблицы.ВидыРЦСсылки.ВыгрузитьКолонку("Ссылка");
		Если ОтборВРЦ.Количество() = 0 Тогда
			ОтборВРЦ.Добавить(Справочники.ВидыРабочихЦентров.ПустаяСсылка());
		КонецЕсли;
		ПараметрыРасчета.ДоступностьОтборВРЦ = ОтборВРЦ;
		
		ПараметрыРасчета.ДоступностьИнтервалНачало = НачалоДня(ТекущаяДатаСеанса());
		ПараметрыРасчета.ДоступностьИнтервалОкончание = КонецМесяца(ДобавитьМесяц(ТекущаяДатаСеанса(), 3));
		
		ПрочитатьДоступность(Заказ, ДанныеРасчета, ПараметрыРасчета);
		
		// Расчет
		Для Индекс = 0 По Этапы.Количество()-1 Цикл
			СтрокаЭтап = Этапы[Индекс];
			
			// Корректировка доступности
			ПоискНомерЗаписи.НомерЗаписи = СтрокаЭтап.НомерЗаписи;
			Для каждого СтрокаЗагрузка Из ЗагрузкаВидовРЦ.НайтиСтроки(ПоискНомерЗаписи) Цикл
				ЗаполнитьЗначенияСвойств(ПоискВРЦ, СтрокаЗагрузка, "ВидРабочегоЦентра, ДатаИнтервала");
				СтрокиДоступность = ДанныеРасчета.ДоступностьВРЦ.НайтиСтроки(ПоискВРЦ);
				Если СтрокиДоступность.Количество() <> 0 Тогда
					СтрокиДоступность[0].ДоступноВремяЗагрузка = СтрокиДоступность[0].ДоступноВремяЗагрузка - СтрокаЗагрузка.ЗанятоВремяЗагрузка;
					СтрокиДоступность[0].ДоступноВремя = СтрокиДоступность[0].ДоступноВремя - СтрокаЗагрузка.ЗанятоВремя;
				КонецЕсли;
			КонецЦикла;
			
			РазмещатьЭтап = Истина;
			
			// Наследование дат предшественников
			ПоискКлючПартияЭтап.КлючПартия = СтрокаЭтап.КлючПартия;
			ПоискКлючПартияЭтап.Этап = СтрокаЭтап.Этап;
			Для каждого СтрокаСвязь Из Связи.НайтиСтроки(ПоискКлючПартияЭтап) Цикл
				ПоискКлючПартияЭтап.КлючПартия = СтрокаСвязь.КлючПартияПредшественник;
				ПоискКлючПартияЭтап.Этап = СтрокаСвязь.ЭтапПредшественник;
				
				СтрокиПредшественник = Этапы.НайтиСтроки(ПоискКлючПартияЭтап);
				Если СтрокиПредшественник.Количество() = 0 Тогда
					СтрокиПредшественник = ДатыПредшественников.НайтиСтроки(ПоискКлючПартияЭтап);
				КонецЕсли;
				Если СтрокиПредшественник.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
				ЭтапПредшественник = СтрокиПредшественник[0];
				
				Если НЕ ЗначениеЗаполнено(ЭтапПредшественник.Окончание) Тогда
					РазмещатьЭтап = Ложь;
					Прервать;
				Иначе
					СтрокаЭтап.НачалоРазмещения = Макс(
						СтрокаЭтап.НачалоРазмещения,
						?(ЭтапПредшественник.Начало <> ЭтапПредшественник.Окончание,
							ЭтапПредшественник.Окончание + 1,
							ЭтапПредшественник.Окончание));
				КонецЕсли;
			КонецЦикла;
			
			// Размещение этапов
			Если РазмещатьЭтап Тогда
				Если СтрокаЭтап.ПланироватьВидыРЦ Тогда
					РазместитьЭтапПоВидамРЦ(ДанныеРасчета, ПараметрыРасчета, СтрокаЭтап);
				Иначе
					РазместитьЭтапПоДлительности(ДанныеРасчета, ПараметрыРасчета, СтрокаЭтап, Истина);
				КонецЕсли;
			КонецЕсли;
			
			// Наследование начала следующего (непланируемого) этапа
			ПоискКлючПартияЭтап.КлючПартия = СтрокаЭтап.КлючПартия;
			ПоискКлючПартияЭтап.Этап = СтрокаЭтап.Этап;
			НайденныеСтроки = ДатыПоследователей.НайтиСтроки(ПоискКлючПартияЭтап);
			Если НайденныеСтроки.Количество() <> 0 Тогда
				СтрокаЭтап.НачалоСледующегоЭтапа = НайденныеСтроки[0].НачалоСледующегоЭтапа;
			КонецЕсли;
		КонецЦикла;
		
		ЭтапыКУдалению = Этапы.НайтиСтроки(Новый Структура("Окончание", '00010101'));
		КлючиПартийГрафикНеРассчитан = Этапы.Скопировать(ЭтапыКУдалению, "КлючПартия").ВыгрузитьКолонку("КлючПартия");
		Для каждого Этап Из ЭтапыКУдалению Цикл
			Если Этап.ПланироватьВидыРЦ Тогда
				ЗаполнитьЗначенияСвойств(ПоискКлючПартияЭтап, Этап);
				Для каждого Строка Из ДанныеРасчета.ЗагрузкаВРЦ.НайтиСтроки(ПоискКлючПартияЭтап) Цикл
					ДанныеРасчета.ЗагрузкаВРЦ.Удалить(Строка);
				КонецЦикла;
			КонецЕсли;
			Этапы.Удалить(Этап);
		КонецЦикла;
		
		Если Этапы.Количество() > 0 Тогда
			#Область Задания
			
			ТекстЗапроса = 
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	Задания.КлючПартия КАК КлючПартия
				|ПОМЕСТИТЬ ВТКлючиКРасчетуИтогов
				|ИЗ
				|	ВТЗадания КАК Задания
				|ГДЕ
				|	Задания.ЗаказНаПроизводство = &Заказ
				|	И Задания.КлючПартия В (&КлючиПартийГрафикРассчитан)
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	КлючПартия
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	Задания.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
				|	Задания.КлючПартия          КАК КлючПартия,
				|	Задания.Разделитель         КАК Разделитель,
				|	Задания.ЭтапПроизводства    КАК ЭтапПроизводства
				|ИЗ
				|	ВТЗадания КАК Задания
				|ГДЕ
				|	Задания.ЗаказНаПроизводство = &Заказ
				|	И Задания.КлючПартия НЕ В (&КлючиПартийГрафикНеРассчитан)";
			
			Запрос.Текст = ТекстЗапроса;
			Запрос.УстановитьПараметр("КлючиПартийГрафикРассчитан", Этапы.ВыгрузитьКолонку("КлючПартия"));
			Запрос.УстановитьПараметр("КлючиПартийГрафикНеРассчитан", КлючиПартийГрафикНеРассчитан);
			
			ЗаданияКУдалению = Запрос.Выполнить().Выгрузить();
			
			#КонецОбласти
			
			СместитьЭтапыПоДлительности(ДанныеРасчета, ПараметрыРасчета);
			
			// Запись графика этапов
			Набор = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьНаборЗаписей();
			Набор.Загрузить(Этапы);
			Набор.Записать(РежимЗамещения.Слияние);
			
			// Расчет и запись итогов
			РегистрыСведений.ГрафикЭтаповПроизводства2_2.РассчитатьИтогиПартий(
				Заказ, МенеджерВременныхТаблиц, "ВТКлючиКРасчетуИтогов");
			
			// Запись доступности
			ЗаписатьЗагрузкуВРЦ(ДанныеРасчета, ПараметрыРасчета, Истина);
			
			// Очистка заданий
			Если ЗаданияКУдалению.Количество() > 0 Тогда
				Набор = РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства.СоздатьНаборЗаписей();
				Набор.Загрузить(ЗаданияКУдалению);
				УстановитьПривилегированныйРежим(Истина);
				Набор.Записать(РежимЗамещения.Удаление);
				УстановитьПривилегированныйРежим(Ложь);
			КонецЕсли;
			
			// Связанные действия
			СтруктураПоиска = Новый Структура("ЭтоДокумент", Истина);
			ЭтапыДокументы = Этапы.Скопировать(СтруктураПоиска, "Этап").ВыгрузитьКолонку("Этап");
			УправлениеПроизводством.ОбновитьДвиженияДокументовПослеЗаписиГрафикаПроизводства(Заказ, ЭтапыДокументы, Истина);
			
			// Удаление временных таблиц
			Запрос.Текст = "УНИЧТОЖИТЬ ВТКлючиКРасчетуИтогов";
			Запрос.Выполнить();
		КонецЕсли;
		
		// Партии по которым не удалось выполнить расчет исключаются из дальнейшего
		// пересчета партий путем обнуления номера очереди
		Если КлючиПартийГрафикНеРассчитан.Количество() > 0 Тогда
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	Задания.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
				|	Задания.КлючПартия          КАК КлючПартия,
				|	Задания.Разделитель         КАК Разделитель,
				|	Задания.ЭтапПроизводства    КАК ЭтапПроизводства,
				|	0                           КАК Очередь
				|ИЗ
				|	ВТЗаданияНаЗапуск КАК Задания
				|ГДЕ
				|	Задания.ЗаказНаПроизводство = &Заказ
				|	И Задания.КлючПартия В (&КлючиПартийГрафикНеРассчитан)";
			Запрос.УстановитьПараметр("КлючиПартийГрафикНеРассчитан", КлючиПартийГрафикНеРассчитан);
			ЗаданияКИзменению = Запрос.Выполнить().Выгрузить();
			
			Набор = РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства.СоздатьНаборЗаписей();
			Набор.Загрузить(ЗаданияКИзменению);
			УстановитьПривилегированныйРежим(Истина);
			Набор.Записать(РежимЗамещения.Слияние);
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
	КонецЦикла;
	
	#Область КонтрольПорядкаСледования
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Связи.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	Связи.Этап                КАК ЭтапПроизводства,
		|	Связи.КлючПартия          КАК КлючПартия
		|ИЗ
		|	ВТСвязи КАК Связи
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикПредшественника
		|	ПО Связи.ЗаказНаПроизводствоПредшественник = ГрафикПредшественника.ЗаказНаПроизводство
		|		И Связи.КлючПартияПредшественник = ГрафикПредшественника.КлючПартия
		|		И Связи.ЭтапПредшественник = ГрафикПредшественника.Этап
		|		И ГрафикПредшественника.СтатусГрафика = 0
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|	ПО Связи.ЗаказНаПроизводство = График.ЗаказНаПроизводство
		|		И Связи.КлючПартия = График.КлючПартия
		|		И Связи.Этап = График.Этап
		|		И График.СтатусГрафика = 0
		|ГДЕ
		|	(Связи.ЗаказНаПроизводствоПредшественник, Связи.КлючПартияПредшественник) В (
		|		ВЫБРАТЬ
		|			Т.ЗаказНаПроизводство КАК ЗаказНаПроизводствоПредшественник,
		|			Т.КлючПартия          КАК КлючПартияПредшественник
		|		ИЗ
		|			ВТЗадания КАК Т)
		|	И ГрафикПредшественника.Окончание > График.Начало";
	
	НовыеЗадания = Запрос.Выполнить().Выгрузить();
	Если НовыеЗадания.Количество() > 0 Тогда
		РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства.ДобавитьЗадания(НовыеЗадания);
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры

// Перенести график "виртуальных" партий созданным документам Этап производства.
// Используется при формировании этапов в случаях, когда график можно унаследовать.
// 
// Параметры:
//  Этапы - ТаблицаЗначений
//
Процедура ПеренестиГрафикПартийПриЗапуске(Этапы) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	Таблица.КлючПартияСтарый    КАК КлючПартияСтарый,
		|	Таблица.КлючПартияНовый     КАК КлючПартияНовый,
		|	Таблица.Ссылка              КАК Ссылка,
		|	Таблица.Этап                КАК Этап
		|ПОМЕСТИТЬ ВТЭтапы
		|ИЗ
		|	&ЭтапыКПереносуГрафика КАК Таблица
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПроизводство,
		|	КлючПартияСтарый,
		|	Этап
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	Загрузка.Регистратор       КАК Регистратор,
		|	Загрузка.Период            КАК Период,
		|	Загрузка.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
		|	Загрузка.ДатаИнтервала     КАК ДатаИнтервала,
		|	СУММА(Загрузка.Занято)     КАК Занято,
		|	Загрузка.Распоряжение      КАК Распоряжение,
		|	Загрузка.Подразделение     КАК Подразделение,
		|	Загрузка.Смена             КАК Смена,
		|	Загрузка.КлючПартия        КАК КлючПартия,
		|	Загрузка.Этап              КАК Этап
		|ПОМЕСТИТЬ ВТЗагрузкаВидовРЦ
		|ИЗ
		|	РегистрНакопления.ДоступностьВидовРабочихЦентров КАК Загрузка
		|ГДЕ
		|	(Загрузка.Распоряжение, Загрузка.КлючПартия) В (
		|		ВЫБРАТЬ
		|			Т.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|			Т.КлючПартияСтарый    КАК КлючПартия
		|		ИЗ
		|			ВТЭтапы КАК Т)
		|СГРУППИРОВАТЬ ПО
		|	Загрузка.Регистратор,
		|	Загрузка.Период,
		|	Загрузка.ВидРабочегоЦентра,
		|	Загрузка.ДатаИнтервала,
		|	Загрузка.Распоряжение,
		|	Загрузка.Подразделение,
		|	Загрузка.Смена,
		|	Загрузка.КлючПартия,
		|	Загрузка.Этап
		|ИМЕЮЩИЕ
		|	СУММА(Загрузка.Занято) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	КлючПартия,
		|	Этап
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	График.ЗаказНаПроизводство             КАК ЗаказНаПроизводство,
		|	График.СтатусГрафика                   КАК СтатусГрафика,
		|	Этапы.КлючПартияНовый                  КАК КлючПартия,
		|	Этапы.Ссылка                           КАК Этап,
		|	График.ВидСтроки                       КАК ВидСтроки,
		|	График.Начало                          КАК Начало,
		|	График.Окончание                       КАК Окончание,
		|	График.ОкончаниеПредварительногоБуфера КАК ОкончаниеПредварительногоБуфера,
		|	График.НачалоЗавершающегоБуфера        КАК НачалоЗавершающегоБуфера,
		|	График.НачалоСледующегоЭтапа           КАК НачалоСледующегоЭтапа,
		|	График.НаКритическомПути               КАК НаКритическомПути,
		|	График.ОграничиваетСрокВыпуска         КАК ОграничиваетСрокВыпуска,
		|	График.ОграниченПоМатериалам           КАК ОграниченПоМатериалам,
		|	График.ОграниченПоОборудованию         КАК ОграниченПоОборудованию,
		|	График.РазмещениеВыпуска               КАК РазмещениеВыпуска,
		|	График.Подразделение                   КАК Подразделение,
		|	График.Спецификация                    КАК Спецификация,
		|	График.КоличествоПартий                КАК КоличествоПартий,
		|	График.Порядок                         КАК Порядок
		|ИЗ
		|	ВТЭтапы КАК Этапы
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|	ПО Этапы.ЗаказНаПроизводство = График.ЗаказНаПроизводство
		|		И График.СтатусГрафика = &СтатусРабочийГрафик
		|		И Этапы.КлючПартияСтарый = График.КлючПартия
		|		И Этапы.Этап = График.Этап
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	График.ЗаказНаПроизводство                          КАК ЗаказНаПроизводство,
		|	График.СтатусГрафика                                КАК СтатусГрафика,
		|	Этапы.КлючПартияНовый                               КАК КлючПартия,
		|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка) КАК Этап,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСтрокГрафикаПроизводства.Партия) КАК ВидСтроки,
		|	МИНИМУМ(График.Начало)                              КАК Начало,
		|	МАКСИМУМ(График.Окончание)                          КАК Окончание,
		|	НЕОПРЕДЕЛЕНО                                        КАК ОкончаниеПредварительногоБуфера,
		|	НЕОПРЕДЕЛЕНО                                        КАК НачалоЗавершающегоБуфера,
		|	НЕОПРЕДЕЛЕНО                                        КАК НачалоСледующегоЭтапа,
		|	НЕОПРЕДЕЛЕНО                                        КАК НаКритическомПути,
		|	НЕОПРЕДЕЛЕНО                                        КАК ОграничиваетСрокВыпуска,
		|	НЕОПРЕДЕЛЕНО                                        КАК ОграниченПоМатериалам,
		|	НЕОПРЕДЕЛЕНО                                        КАК ОграниченПоОборудованию,
		|	НЕОПРЕДЕЛЕНО                                        КАК РазмещениеВыпуска,
		|	НЕОПРЕДЕЛЕНО                                        КАК Подразделение,
		|	НЕОПРЕДЕЛЕНО                                        КАК Спецификация,
		|	НЕОПРЕДЕЛЕНО                                        КАК КоличествоПартий,
		|	МИНИМУМ(График.Порядок)                             КАК Порядок
		|ИЗ
		|	ВТЭтапы КАК Этапы
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|	ПО Этапы.ЗаказНаПроизводство = График.ЗаказНаПроизводство
		|		И График.СтатусГрафика = &СтатусРабочийГрафик
		|		И Этапы.КлючПартияСтарый = График.КлючПартия
		|		И Этапы.Этап = График.Этап
		|
		|СГРУППИРОВАТЬ ПО
		|	График.ЗаказНаПроизводство,
		|	График.СтатусГрафика,
		|	Этапы.КлючПартияНовый
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	Загрузка.Регистратор       КАК Регистратор,
		|	Загрузка.Период            КАК Период,
		|	Загрузка.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
		|	Загрузка.ДатаИнтервала     КАК ДатаИнтервала,
		|	-Загрузка.Занято           КАК Занято,
		|	Загрузка.Распоряжение      КАК Распоряжение,
		|	Загрузка.Подразделение     КАК Подразделение,
		|	Загрузка.Смена             КАК Смена,
		|	Загрузка.КлючПартия        КАК КлючПартия,
		|	Загрузка.Этап              КАК Этап
		|ИЗ
		|	ВТЗагрузкаВидовРЦ КАК Загрузка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Этапы.Ссылка               КАК Регистратор,
		|	Загрузка.Период            КАК Период,
		|	Загрузка.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
		|	Загрузка.ДатаИнтервала     КАК ДатаИнтервала,
		|	Загрузка.Занято            КАК Занято,
		|	Загрузка.Распоряжение      КАК Распоряжение,
		|	Загрузка.Подразделение     КАК Подразделение,
		|	Загрузка.Смена             КАК Смена,
		|	Этапы.КлючПартияНовый      КАК КлючПартия,
		|	Этапы.Ссылка               КАК Этап
		|ИЗ
		|	ВТЭтапы КАК Этапы
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗагрузкаВидовРЦ КАК Загрузка
		|	ПО Этапы.ЗаказНаПроизводство = Загрузка.Распоряжение
		|		И Этапы.КлючПартияСтарый = Загрузка.КлючПартия
		|		И Этапы.Этап = Загрузка.Этап
		|
		|ИТОГИ ПО
		|	Регистратор";
	
	СтатусРабочийГрафик = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик();
	Запрос.УстановитьПараметр("ЭтапыКПереносуГрафика", Этапы);
	Запрос.УстановитьПараметр("СтатусРабочийГрафик", СтатусРабочийГрафик);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = РезультатыЗапроса.Количество();
	
	// Новый график
	Набор = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьНаборЗаписей();
	Набор.Загрузить(РезультатыЗапроса[КоличествоРезультатов - 2].Выгрузить());
	Набор.Записать(Ложь);
	
	// Перенос доступности
	ВыборкаРегистратор = РезультатыЗапроса[КоличествоРезультатов - 1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаРегистратор.Следующий() Цикл
		Набор = РегистрыНакопления.ДоступностьВидовРабочихЦентров.СоздатьНаборЗаписей();
		Набор.Отбор.Регистратор.Установить(ВыборкаРегистратор.Регистратор);
		
		Выборка = ВыборкаРегистратор.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
		КонецЦикла;
		
		Набор.Записать(Ложь);
	КонецЦикла;
	
КонецПроцедуры

#Область ОберткиДляЗапускаРасчета

// Расчет графика в рабочем месте планирования заказа.
// 
// Параметры:
//  Настройки - см. Обработки.ПланированиеГрафикаПроизводства2_2.НастройкиПланированияПредварительногоГрафика
//  АдресХранилища - УникальныйИдентификатор, Строка - адрес во временном хранилище,
//		по которому надо поместить результаты планирования.
//
Процедура РассчитатьРабочийГрафикИМодель(Настройки, АдресХранилища) Экспорт
	
	ИнформационнаяБазаФайловая = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	ОповещениеСтатусРасчетаСтруктуры = "";
	Если НЕ ИнформационнаяБазаФайловая Тогда
		ОжидатьЗавершениеРасчетаСтруктурыЗаказа(Настройки.ЗаказНаПроизводство, ОповещениеСтатусРасчетаСтруктуры);
	КонецЕсли;
	
	РезультатыГрафик = Неопределено;
	НастройкиГрафик = Неопределено;
	Если Настройки.ПланироватьГрафик Тогда
		НастройкиГрафик = ОбщегоНазначения.СкопироватьРекурсивно(Настройки);
		НастройкиГрафик.Вставить("СтатусГрафика",
			РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик());
		НастройкиГрафик.Вставить("ЗадействоватьРезервДоступности",
			Настройки.ЗадействоватьРезервДоступностиГрафик);
	КонецЕсли;
	
	РезультатыМодель = Неопределено;
	НастройкиМодель = Неопределено;
	Если Настройки.ПланироватьМодель Тогда
		НастройкиМодель = ОбщегоНазначения.СкопироватьРекурсивно(Настройки);
		НастройкиМодель.Вставить("СтатусГрафика",
			РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусМодельГрафика());
		НастройкиМодель.Вставить("ЗадействоватьРезервДоступности",
			Настройки.ЗадействоватьРезервДоступностиМодель);
	КонецЕсли;
	
	ПараллельныйРасчет = НЕ ИнформационнаяБазаФайловая
		И Настройки.ПланироватьГрафик
		И Настройки.ПланироватьМодель;
	Если ПараллельныйРасчет Тогда
		// Запуск расчета
		ОчередьРасчета = Новый Массив;
		
		Для Сч = 1 По 2 Цикл
			ПараметрыПроцедуры = Новый Структура;
			ПараметрыПроцедуры.Вставить("Заказ", НастройкиГрафик.ЗаказНаПроизводство);
			ПараметрыПроцедуры.Вставить("Настройки", ?(Сч = 1, НастройкиГрафик, НастройкиМодель));
			
			ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Неопределено);
			ПараметрыВыполнения.НаименованиеФоновогоЗадания = ?(Сч = 1,
				НСтр("ru = 'Расчет графика (%1)';
					|en = 'Create schedule (%1)'"),
				НСтр("ru = 'Расчет модели графика (%1)';
					|en = 'Create schedule model (%1)'"));
			ПараметрыВыполнения.АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
			
			РезультатЗапуска = ДлительныеОперации.ВыполнитьВФоне(
				"ГрафикПроизводстваСтруктурыЗаказа.РассчитатьГрафикВФоне",
				ПараметрыПроцедуры,
				ПараметрыВыполнения);
			
			Если РезультатЗапуска.Статус = "Выполняется" Тогда
				ЭлементОчереди = Новый Структура;
				ЭлементОчереди.Вставить("Идентификатор", РезультатЗапуска.ИдентификаторЗадания);
				ЭлементОчереди.Вставить("АдресРезультата", РезультатЗапуска.АдресРезультата);
				ЭлементОчереди.Вставить("ЭтоРасчетГрафика", Сч = 1);
				ОчередьРасчета.Добавить(ЭлементОчереди);
			Иначе
				Если РезультатЗапуска.Статус = "Выполнено" Тогда
					Если Сч = 1 Тогда
						РезультатыГрафик = ПолучитьИзВременногоХранилища(РезультатЗапуска.АдресРезультата);
					Иначе
						РезультатыМодель = ПолучитьИзВременногоХранилища(РезультатЗапуска.АдресРезультата);
					КонецЕсли;
				Иначе
					ВызватьИсключение РезультатЗапуска.ПодробноеПредставлениеОшибки;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		// Ожидание завершения
		РасчетЗавершен = Ложь;
		ИнтервалПроверкиЗавершенияРасчета = 2;
		СтруктураОтбора = Новый Структура();
		
		УстановитьПривилегированныйРежим(Истина);
		Пока Не РасчетЗавершен Цикл
			АктивныеЗадания = Новый Массив();
			
			Для Поток = 0 По ОчередьРасчета.ВГраница() Цикл
				Если ОчередьРасчета[Поток] = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ЭлементОчереди = ОчередьРасчета[Поток]; // Структура
				СтруктураОтбора.Вставить("УникальныйИдентификатор", ЭлементОчереди.Идентификатор);
				СписокЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(СтруктураОтбора);
				
				Если СписокЗаданий.ВГраница() <> -1 Тогда
					Если СписокЗаданий[0].Состояние = СостояниеФоновогоЗадания.Активно Тогда
						АктивныеЗадания.Добавить(СписокЗаданий[0].УникальныйИдентификатор);
						Продолжить;
					КонецЕсли;
					
					Если СписокЗаданий[0].Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
						ВызватьИсключение ОбработкаОшибок.ПодробноеПредставлениеОшибки(СписокЗаданий[0].ИнформацияОбОшибке);
					КонецЕсли;
				КонецЕсли;
				
				Если ЭлементОчереди.ЭтоРасчетГрафика Тогда
					РезультатыГрафик = ПолучитьИзВременногоХранилища(ЭлементОчереди.АдресРезультата);
				Иначе
					РезультатыМодель = ПолучитьИзВременногоХранилища(ЭлементОчереди.АдресРезультата);
				КонецЕсли;
				
				ОчередьРасчета[Поток] = Неопределено;
			КонецЦикла;
			
			РасчетЗавершен = Не ЗначениеЗаполнено(АктивныеЗадания);
			Если Не РасчетЗавершен Тогда
				Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(АктивныеЗадания[0]);
				Если Задание <> Неопределено Тогда
					Задание.ОжидатьЗавершенияВыполнения(ИнтервалПроверкиЗавершенияРасчета);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		УстановитьПривилегированныйРежим(Ложь);
		
	Иначе
		Если Настройки.ПланироватьГрафик Тогда
			РезультатыГрафик = РассчитатьГрафик(НастройкиГрафик.ЗаказНаПроизводство, НастройкиГрафик);
		КонецЕсли;
		
		Если Настройки.ПланироватьМодель Тогда
			РезультатыМодель = РассчитатьГрафик(НастройкиМодель.ЗаказНаПроизводство, НастройкиМодель);
		КонецЕсли;
	КонецЕсли;
	
	Результат = Новый Структура;
	
	Ошибки = Новый ТаблицаЗначений;
	Ошибки.Колонки.Добавить("Описание", Новый ОписаниеТипов("Строка"));
	Ошибки.Колонки.Добавить("Расшифровка");
	Ошибки.Колонки.Добавить("Ключ");
	Ошибки.Колонки.Добавить("СтатусГрафика", Новый ОписаниеТипов("Число"));
	
	Оповещения = Новый Соответствие;
	
	Если Не ПустаяСтрока(ОповещениеСтатусРасчетаСтруктуры) Тогда
		Оповещения.Вставить(ОповещениеСтатусРасчетаСтруктуры, Настройки.ЗаказНаПроизводство);
	КонецЕсли;
	
	Если РезультатыГрафик <> Неопределено Тогда
		Результат.Вставить("ГрафикЗапланирован", РезультатыГрафик.ГрафикЗапланирован);
		
		Для каждого Строка Из РезультатыГрафик.Ошибки Цикл
			ЗаполнитьЗначенияСвойств(Ошибки.Добавить(), Строка);
		КонецЦикла;

		Для каждого КлючИЗначение Из РезультатыГрафик.Оповещения Цикл
			Оповещения.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
	Иначе
		Результат.Вставить("ГрафикЗапланирован", Ложь);
	КонецЕсли;
	
	Если РезультатыМодель <> Неопределено Тогда
		Результат.Вставить("МодельЗапланирована", РезультатыМодель.ГрафикЗапланирован);
		
		Для каждого Строка Из РезультатыМодель.Ошибки Цикл
			ЗаполнитьЗначенияСвойств(Ошибки.Добавить(), Строка);
		КонецЦикла;

		Для каждого КлючИЗначение Из РезультатыМодель.Оповещения Цикл
			Оповещения.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
	Иначе
		Результат.Вставить("МодельЗапланирована", Ложь);
	КонецЕсли;
	
	Результат.Вставить("Ошибки", Ошибки);
	Результат.Вставить("Оповещения", Оповещения);
	
	// Свойства для совместимости
	Результат.Вставить("РазмещенныеЭтапы", Новый Массив);
	Результат.Вставить("НомерЗаданияКРасчетуГрафикаПроизводства", 0);
	Результат.Вставить("ОтменитьРучныеИзмененияГрафика", Ложь);
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

Процедура РассчитатьГрафикВФоне(Параметры, АдресРезультата) Экспорт
	
	Результат = РассчитатьГрафик(Параметры.Заказ, Параметры.Настройки);
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецПроцедуры

// Запустить в фоновом режиме расчет графика сформированных партий.
// 
// Параметры:
//  ГраницаРасчета - Число - номер очереди по которую сделан расчет структуры заказа.
//
Процедура ЗапуститьРасчетГрафикаПартий(ГраницаРасчета) Экспорт
	
	ИмяЭкспортнойПроцедуры = "ГрафикПроизводстваСтруктурыЗаказа.РассчитатьГрафикПартийПриЗапуске";
	
	ПараметрыЭкспортнойПроцедуры = Новый Массив();
	ПараметрыЭкспортнойПроцедуры.Добавить(ГраницаРасчета);
	
	Если ОбщегоНазначения.РежимОтладки()
		ИЛИ ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ОбщегоНазначения.ВыполнитьМетодКонфигурации(ИмяЭкспортнойПроцедуры, ПараметрыЭкспортнойПроцедуры);
		Возврат;
	КонецЕсли;
	
	Ключ = "РасчетГрафикаПартийПриЗапуске";
	Отбор = Новый Структура("Ключ, Состояние", Ключ, СостояниеФоновогоЗадания.Активно);
	
	УстановитьПривилегированныйРежим(Истина);
	АктивныеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если АктивныеЗадания.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	НаименованиеЗадания = НСтр("ru = 'Расчет графика партий при запуске в производство';
								|en = 'Calculation of lot schedule at production launch'");
	
	Попытка
		ФоновыеЗадания.Выполнить(
			ИмяЭкспортнойПроцедуры,
			ПараметрыЭкспортнойПроцедуры,
			Ключ,
			НаименованиеЗадания);
	Исключение
		Возврат; // Задание уже запущено
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Основное

// Рассчитывает график всех этапов заказа.
//
Функция РассчитатьГрафикВнутриЗамераВремени(Заказ, Настройки)
	
	ПараметрыРасчета = НовыйПараметрыРасчета(Настройки);
	ПараметрыРасчета.Заказ = Заказ;
	
	// Чтение данных
	ДанныеРасчета = НовыйДанныеРасчета();
	ПрочитатьЭтапыЗаказа(Заказ, ДанныеРасчета, ПараметрыРасчета);
	ПрочитатьДоступность(Заказ, ДанныеРасчета, ПараметрыРасчета);
	
	Этапы = ДанныеРасчета.Этапы; // ТаблицаЗначений
	Связи = ДанныеРасчета.Связи; // ТаблицаЗначений
	
	// Отмена ручного размещения
	Если ПараметрыРасчета.ОтменитьРучныеИзмененияГрафика
		И ДанныеРасчета.ЭтапыСРучнымРазмещением.Количество() > 0 Тогда
		Документы.ЭтапПроизводства2_2.ОтменитьРучноеРазмещениеЭтаповВГрафике(
			ДанныеРасчета.ЭтапыСРучнымРазмещением);
	КонецЕсли;
	
	// Расчет
	СтруктураПоиска = ПараметрыРасчета.КлючПартияЭтап;
	
	Для Индекс = 0 По Этапы.Количество()-1 Цикл
		СтрокаЭтап = Этапы[Индекс];
		
		// Наследование дат предшественников
		СтруктураПоиска.КлючПартия = СтрокаЭтап.КлючПартия;
		СтруктураПоиска.Этап = СтрокаЭтап.Этап;
		Для каждого СтрокаСвязь Из Связи.НайтиСтроки(СтруктураПоиска) Цикл
			СтруктураПоиска.КлючПартия = СтрокаСвязь.КлючПартияПредшественник;
			СтруктураПоиска.Этап = СтрокаСвязь.ЭтапПредшественник;
			
			СтрокиПредшественник = Этапы.НайтиСтроки(СтруктураПоиска);
			Если СтрокиПредшественник.Количество() <> 0 Тогда
				СтрокаЭтап.НачалоРазмещения = Макс(
					СтрокаЭтап.НачалоРазмещения,
					?(СтрокиПредшественник[0].Начало <> СтрокиПредшественник[0].Окончание,
						СтрокиПредшественник[0].Окончание + 1,
						СтрокиПредшественник[0].Окончание));
			КонецЕсли;
		КонецЦикла;
		
		// Размещение этапов
		Если СтрокаЭтап.ПланироватьВидыРЦ Тогда
			РазместитьЭтапПоВидамРЦ(ДанныеРасчета, ПараметрыРасчета, СтрокаЭтап);
		Иначе
			РазместитьЭтапПоДлительности(ДанныеРасчета, ПараметрыРасчета, СтрокаЭтап, Истина);
		КонецЕсли;
	КонецЦикла;
	
	ГрафикЗапланирован = Этапы.Количество() > 0
		И ПараметрыРасчета.Ошибки.Количество() = 0;
	
	Если ГрафикЗапланирован Тогда
		СместитьЭтапыПоДлительности(ДанныеРасчета, ПараметрыРасчета);
		ЗаписатьГрафикЗаказа(ДанныеРасчета, ПараметрыРасчета);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ГрафикЗапланирован", ГрафикЗапланирован);
	Результат.Вставить("Ошибки", ПараметрыРасчета.Ошибки);
	Результат.Вставить("Оповещения", ПараметрыРасчета.Оповещения);
	Результат.Вставить("КоличествоЭтапов", Этапы.Количество());
	
	Возврат Результат;
	
КонецФункции

Процедура СместитьЭтапыПоДлительности(ДанныеРасчета, ПараметрыРасчета)
	
	Этапы = ДанныеРасчета.Этапы; // ТаблицаЗначений
	Связи = ДанныеРасчета.Связи; // ТаблицаЗначений
	
	Этапы.ЗаполнитьЗначения('00010101', "НачалоРазмещения");
	
	СтруктураПоиска = Новый Структура("ЕстьПраваяГраница, СмещатьВправо", Истина, Истина);
	Для каждого СтрокаЭтап Из Этапы.НайтиСтроки(СтруктураПоиска) Цикл
		СтрокаЭтап.НачалоРазмещения = СтрокаЭтап.ПраваяГраница;
	КонецЦикла;
	
	СтруктураПоиска = ПараметрыРасчета.КлючПартияЭтап;
	
	Для Индекс = -(Этапы.Количество()-1) По 0 Цикл
		СтрокаЭтап = Этапы[-Индекс];
		
		Если СтрокаЭтап.СмещатьВправо
			И СтрокаЭтап.НачалоРазмещения <> '00010101'
			И СтрокаЭтап.НачалоРазмещения > СтрокаЭтап.Окончание + 1 Тогда
			РазместитьЭтапПоДлительности(ДанныеРасчета, ПараметрыРасчета, СтрокаЭтап, Ложь);
		КонецЕсли;
		
		СтруктураПоиска.КлючПартия = СтрокаЭтап.КлючПартия;
		СтруктураПоиска.Этап = СтрокаЭтап.Этап;
		Для каждого СтрокаСвязь Из Связи.НайтиСтроки(СтруктураПоиска) Цикл
			СтруктураПоиска.КлючПартия = СтрокаСвязь.КлючПартияПредшественник;
			СтруктураПоиска.Этап = СтрокаСвязь.ЭтапПредшественник;
			
			СтрокиПредшественники = Этапы.НайтиСтроки(СтруктураПоиска);
			Если СтрокиПредшественники.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаПредшественник = СтрокиПредшественники[0];
			
			СтрокаПредшественник.НачалоСледующегоЭтапа = ?(СтрокаПредшественник.НачалоСледующегоЭтапа = '00010101',
				СтрокаЭтап.Начало,
				Мин(СтрокаПредшественник.НачалоСледующегоЭтапа, СтрокаЭтап.Начало));
			
			Если НЕ СтрокаПредшественник.СмещатьВправо Тогда
				Продолжить;
			ИначеЕсли СтрокаПредшественник.НачалоРазмещения = '00010101' Тогда
				СтрокаПредшественник.НачалоРазмещения = СтрокаЭтап.Начало;
			Иначе
				СтрокаПредшественник.НачалоРазмещения = Мин(
					СтрокаПредшественник.НачалоРазмещения,
					СтрокаЭтап.Начало);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьГрафикЗаказа(ДанныеРасчета, ПараметрыРасчета)
	
	Заказ = ПараметрыРасчета.Заказ;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Запрос.УстановитьПараметр("СтатусГрафика", ПараметрыРасчета.СтатусГрафика);
	Запрос.УстановитьПараметр("ПустойКлючСвязи", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	Запрос.УстановитьПараметр("ОтменитьРучныеИзмененияГрафика", ПараметрыРасчета.ОтменитьРучныеИзмененияГрафика);
	
	// Запись графика этапов
	Этапы = ДанныеРасчета.Этапы; //ТаблицаЗначений
	
	Набор = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьНаборЗаписей();
	Набор.Отбор.ЗаказНаПроизводство.Установить(Заказ);
	Набор.Отбор.СтатусГрафика.Установить(ПараметрыРасчета.СтатусГрафика);
	Набор.Загрузить(Этапы);
	
	Если ПараметрыРасчета.СтатусГрафика = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик() Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ 
			|	*
			|ИЗ
			|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
			|ГДЕ
			|	График.ЗаказНаПроизводство = &Заказ
			|	И График.СтатусГрафика = &СтатусГрафика
			|	И График.Этап В (
			|		ВЫБРАТЬ
			|			Т.Ссылка КАК ЭтапПроизводства
			|		ИЗ
			|			Документ.ЭтапПроизводства2_2 КАК Т
			|		ГДЕ
			|			Т.Распоряжение = &Заказ
			|			И Т.Проведен
			|			И (
			|				Т.Статус В (
			|					ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат),
			|					ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен))
			|				ИЛИ НЕ &ОтменитьРучныеИзмененияГрафика И Т.РучноеРазмещениеВГрафике
			|			)
			|		)";
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
		КонецЦикла;
	КонецЕсли;
	
	Набор.Записать();
	
	// Расчет и запись итогов
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	График.ЗаказНаПроизводство      КАК ЗаказНаПроизводство,
		|	График.СтатусГрафика            КАК СтатусГрафика,
		|	График.КлючПартия               КАК КлючПартия,
		|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка) КАК Этап,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСтрокГрафикаПроизводства.Партия) КАК ВидСтроки,
		|	МИНИМУМ(График.Начало)          КАК Начало,
		|	МАКСИМУМ(График.Окончание)      КАК Окончание,
		|	МИНИМУМ(График.Порядок)         КАК Порядок
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|ГДЕ
		|	График.ЗаказНаПроизводство = &Заказ
		|	И График.СтатусГрафика = &СтатусГрафика
		|	И График.Этап <> ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	График.ЗаказНаПроизводство,
		|	График.СтатусГрафика,
		|	График.КлючПартия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	График.ЗаказНаПроизводство      КАК ЗаказНаПроизводство,
		|	График.СтатусГрафика            КАК СтатусГрафика,
		|	&ПустойКлючСвязи                КАК КлючПартия,
		|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка) КАК Этап,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСтрокГрафикаПроизводства.Заказ) КАК ВидСтроки,
		|	МИНИМУМ(График.Начало)          КАК Начало,
		|	МАКСИМУМ(График.Окончание)      КАК Окончание,
		|	0                               КАК Порядок
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|ГДЕ
		|	График.ЗаказНаПроизводство = &Заказ
		|	И График.СтатусГрафика = &СтатусГрафика
		|
		|СГРУППИРОВАТЬ ПО
		|	График.ЗаказНаПроизводство,
		|	График.СтатусГрафика";
	
	Набор = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьНаборЗаписей();
	Набор.Загрузить(Запрос.Выполнить().Выгрузить());
	Набор.Записать(Ложь);
	
	// Запись доступности
	Если ПараметрыРасчета.СтатусГрафика = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик() Тогда
		ЗаписатьЗагрузкуВРЦ(ДанныеРасчета, ПараметрыРасчета, Ложь);
	Иначе
		ДанныеРасчета.ЗагрузкаВРЦ.Свернуть("Этап, ВидРабочегоЦентра, ДатаИнтервала, КлючПартия, Смена", "Занято");
		РегистрыСведений.ПланированиеЗагрузкиВидовРабочихЦентров.ЗаписатьРезультатыПланирования(
			Заказ, ПараметрыРасчета.СтатусГрафика, ДанныеРасчета.ЗагрузкаВРЦ);
	КонецЕсли;
	
	// Очистка заданий
	Для каждого Задание Из ДанныеРасчета.Задания Цикл
		Набор = РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства.СоздатьНаборЗаписей();
		Набор.Отбор.ЗаказНаПроизводство.Установить(Задание.ЗаказНаПроизводство);
		Набор.Отбор.Разделитель.Установить(Задание.Разделитель);
		УстановитьПривилегированныйРежим(Истина);
		Набор.Записать();
		УстановитьПривилегированныйРежим(Ложь);
	КонецЦикла;
	
	// Связанные действия
	Если ПараметрыРасчета.СтатусГрафика = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик() Тогда
		СтруктураПоиска = Новый Структура("ЭтоДокумент", Истина);
		ЭтапыДокументы = Этапы.Скопировать(СтруктураПоиска, "Этап").ВыгрузитьКолонку("Этап");
		УправлениеПроизводством.ОбновитьДвиженияДокументовПослеЗаписиГрафикаПроизводства(Заказ, ЭтапыДокументы, Истина);
	КонецЕсли;
	
КонецПроцедуры

// Записать загрузку ВРЦ.
// 
// Параметры:
//  ДанныеРасчета - см. НовыйДанныеРасчета
//  ПараметрыРасчета - см. НовыйПараметрыРасчета
//  ОтборПоЭтапам - Булево - если Ложь, то загрузка будет обновлена по всем этапам заказа,
//    если Истина, то только по этапам в таблице ДанныеРасчета.ЗагрузкаВРЦ.
//
Процедура ЗаписатьЗагрузкуВРЦ(ДанныеРасчета, ПараметрыРасчета, ОтборПоЭтапам)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗагрузкаВРЦ = ДанныеРасчета.ЗагрузкаВРЦ;
	ЭтапыСРучнымРазмещением = ДанныеРасчета.ЭтапыСРучнымРазмещением;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(Загрузка.ДатаИнтервала, ДЕНЬ) КАК Период,
		|	Загрузка.ВидРабочегоЦентра                  КАК ВидРабочегоЦентра,
		|	Загрузка.ДатаИнтервала                      КАК ДатаИнтервала,
		|	Загрузка.Занято                             КАК Занято,
		|	Загрузка.Распоряжение                       КАК Распоряжение,
		|	Загрузка.Подразделение                      КАК Подразделение,
		|	Загрузка.Смена                              КАК Смена,
		|	Загрузка.КлючПартия                         КАК КлючПартия,
		|	Загрузка.Этап                               КАК Этап
		|ПОМЕСТИТЬ ВТНовыйГрафик
		|ИЗ
		|	&ЗагрузкаВРЦ КАК Загрузка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Регистратор.Дата             КАК Период,
		|	МАКСИМУМ(Регистратор.Ссылка) КАК Ссылка
		|ПОМЕСТИТЬ ВТРегистраторы
		|ИЗ
		|	Документ.РегистраторГрафикаПроизводства КАК Регистратор
		|ГДЕ
		|	Регистратор.Проведен
		|	И Регистратор.Дата В (
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			Т.Период КАК Дата
		|		ИЗ
		|			ВТНовыйГрафик КАК Т)
		|
		|СГРУППИРОВАТЬ ПО
		|	Регистратор.Дата
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Регистратор       КАК Регистратор,
		|	ВложенныйЗапрос.Период            КАК Период,
		|	ВложенныйЗапрос.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
		|	ВложенныйЗапрос.ДатаИнтервала     КАК ДатаИнтервала,
		|	СУММА(ВложенныйЗапрос.Занято)     КАК Занято,
		|	ВложенныйЗапрос.Распоряжение      КАК Распоряжение,
		|	ВложенныйЗапрос.Подразделение     КАК Подразделение,
		|	ВложенныйЗапрос.Смена             КАК Смена,
		|	ВложенныйЗапрос.КлючПартия        КАК КлючПартия,
		|	ВложенныйЗапрос.Этап              КАК Этап
		|ИЗ
		|	(
		|		ВЫБРАТЬ
		|			ВЫБОР
		|				КОГДА НовыйГрафик.Этап ССЫЛКА Документ.ЭтапПроизводства2_2
		|					ТОГДА НовыйГрафик.Этап
		|				ИНАЧЕ ЕСТЬNULL(Регистраторы.Ссылка, ЗНАЧЕНИЕ(Документ.РегистраторГрафикаПроизводства.ПустаяСсылка))
		|			КОНЕЦ                         КАК Регистратор,
		|			НовыйГрафик.Период            КАК Период,
		|			НовыйГрафик.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
		|			НовыйГрафик.ДатаИнтервала     КАК ДатаИнтервала,
		|			НовыйГрафик.Занято            КАК Занято,
		|			НовыйГрафик.Распоряжение      КАК Распоряжение,
		|			НовыйГрафик.Подразделение     КАК Подразделение,
		|			НовыйГрафик.Смена             КАК Смена,
		|			НовыйГрафик.КлючПартия        КАК КлючПартия,
		|			НовыйГрафик.Этап              КАК Этап
		|		ИЗ
		|			ВТНовыйГрафик КАК НовыйГрафик
		|			
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТРегистраторы КАК Регистраторы
		|			ПО НовыйГрафик.Этап ССЫЛКА Справочник.ЭтапыПроизводства
		|				И НовыйГрафик.Период = Регистраторы.Период
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			СтарыйГрафик.Регистратор       КАК Регистратор,
		|			СтарыйГрафик.Период            КАК Период,
		|			СтарыйГрафик.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
		|			СтарыйГрафик.ДатаИнтервала     КАК ДатаИнтервала,
		|			-СтарыйГрафик.Занято           КАК Занято,
		|			СтарыйГрафик.Распоряжение      КАК Распоряжение,
		|			СтарыйГрафик.Подразделение     КАК Подразделение,
		|			СтарыйГрафик.Смена             КАК Смена,
		|			СтарыйГрафик.КлючПартия        КАК КлючПартия,
		|			СтарыйГрафик.Этап              КАК Этап
		|		ИЗ
		|			РегистрНакопления.ДоступностьВидовРабочихЦентров КАК СтарыйГрафик
		|		ГДЕ
		|			СтарыйГрафик.Распоряжение = &Заказ
		|			И &ТекстЗапросаОтборПоЭтапам
		|			И &ТекстЗапросаИсключитьЭтапыСРучнымРазмещением
		|	) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Регистратор,
		|	ВложенныйЗапрос.Период,
		|	ВложенныйЗапрос.ВидРабочегоЦентра,
		|	ВложенныйЗапрос.ДатаИнтервала,
		|	ВложенныйЗапрос.Распоряжение,
		|	ВложенныйЗапрос.Подразделение,
		|	ВложенныйЗапрос.Смена,
		|	ВложенныйЗапрос.КлючПартия,
		|	ВложенныйЗапрос.Этап
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВложенныйЗапрос.Занято) <> 0
		|
		|ИТОГИ ПО
		|	Регистратор,
		|	Период";
	
	Если ОтборПоЭтапам Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаОтборПоЭтапам",
			"(СтарыйГрафик.КлючПартия, СтарыйГрафик.Этап) В (
			|	ВЫБРАТЬ
			|		Т.КлючПартия,
			|		Т.Этап
			|	ИЗ
			|		ВТНовыйГрафик КАК Т)");
	Иначе
		ТекстЗапросаОтборПоЭтапам = 
			"НЕ СтарыйГрафик.Этап В (
			|	ВЫБРАТЬ
			|		Этапы.Ссылка КАК Этап
			|	ИЗ
			|		Документ.ЭтапПроизводства2_2 КАК Этапы
			|	ГДЕ
			|		Этапы.Распоряжение = &Заказ
			|		И Этапы.Проведен
			|		И Этапы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат))";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаОтборПоЭтапам",
			ТекстЗапросаОтборПоЭтапам);
	КонецЕсли;
	
	Если НЕ ПараметрыРасчета.ОтменитьРучныеИзмененияГрафика
		И ЭтапыСРучнымРазмещением.Количество() > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаИсключитьЭтапыСРучнымРазмещением",
			"СтарыйГрафик.Этап НЕ В (&ЭтапыСРучнымРазмещением)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаИсключитьЭтапыСРучнымРазмещением",
			"ИСТИНА");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ЗагрузкаВРЦ", ЗагрузкаВРЦ);
	Запрос.УстановитьПараметр("Заказ", ПараметрыРасчета.Заказ);
	Запрос.УстановитьПараметр("ЭтапыСРучнымРазмещением", ЭтапыСРучнымРазмещением);
	
	ВыборкаРегистратор = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаРегистратор.Следующий() Цикл
		ВыборкаПериод = ВыборкаРегистратор.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Если ВыборкаРегистратор.Регистратор.Пустая() Тогда
			Пока ВыборкаПериод.Следующий() Цикл
				ДокументОбъект = Документы.РегистраторГрафикаПроизводства.СоздатьДокумент();
				ДокументОбъект.Дата = ВыборкаПериод.Период;
				ДокументОбъект.Проведен = Истина;
				ДокументОбъект.ОбменДанными.Загрузка = Истина;
				ДокументОбъект.Записать();
				
				Набор = РегистрыНакопления.ДоступностьВидовРабочихЦентров.СоздатьНаборЗаписей();
				Набор.Отбор.Регистратор.Установить(ДокументОбъект.Ссылка);
				
				Выборка = ВыборкаПериод.Выбрать();
				Пока Выборка.Следующий() Цикл
					Запись = Набор.Добавить();
					ЗаполнитьЗначенияСвойств(Запись, Выборка);
					Запись.Регистратор = ДокументОбъект.Ссылка;
				КонецЦикла;
				
				Набор.Записать(Ложь);
			КонецЦикла;
		Иначе
			Набор = РегистрыНакопления.ДоступностьВидовРабочихЦентров.СоздатьНаборЗаписей();
			Набор.Отбор.Регистратор.Установить(ВыборкаРегистратор.Регистратор);
			
			Пока ВыборкаПериод.Следующий() Цикл
				Выборка = ВыборкаПериод.Выбрать();
				Пока Выборка.Следующий() Цикл
					ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
				КонецЦикла;
			КонецЦикла;
			
			Набор.Записать(Ложь);
		КонецЕсли;
	КонецЦикла;
	
	ЗагрузкаВРЦ.Свернуть("ВидРабочегоЦентра, ДатаИнтервала");
	РегистрыНакопления.ДоступностьВидовРабочихЦентров.ПроверитьЗагрузкаНеПревышаетДоступностьГрафика(ЗагрузкаВРЦ);
	
КонецПроцедуры

#КонецОбласти

#Область ЧтениеЭтапов

Процедура ПрочитатьЭтапыЗаказа(Заказ, ДанныеРасчета, ПараметрыРасчета)
	
	РазделительЗапросовВПакете = ОбщегоНазначения.РазделительПакетаЗапросов();
	ТекстыЗапроса = Новый СписокЗначений;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// ВТСтруктураЗаказа
	ТекстЗапроса = ТекстЗапросаВТСтруктураЗаказа("&ТекстИмяТаблицы.ЗаказНаПроизводство = &Заказ");
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТСтруктураЗаказа");
	
	// ВТВыпускПартий
	ТекстЗапроса = ТекстЗапросаВТВыпускПартий();
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТВыпускПартий");
	
	// ВТКоличествоПартий
	ТекстЗапроса = ТекстЗапросаВТКоличествоПартий();
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТКоличествоПартий");
	
	#Область ВТОграниченияГрафикаПроизводстваПоМатериалам
	
	Если ПараметрыРасчета.ВсеМатериалыВНаличии Тогда
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство2_2.ПустаяСсылка) КАК ЗаказНаПроизводство,
			|	&ПустойКлючСвязи                                       КАК КлючПартия,
			|	ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка)    КАК Этап,
			|	ДАТАВРЕМЯ(1, 1, 1)                                     КАК ДатаДоступности
			|ПОМЕСТИТЬ ВТОграниченияГрафикаПроизводстваПоМатериалам";
		ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТОграниченияГрафикаПроизводстваПоМатериалам");
		
	Иначе
		
		УправлениеПроизводством.СоздатьВТОграниченияГрафикаПроизводстваПоМатериаламДинамическаяСтруктура(
			МенеджерВременныхТаблиц,
			Заказ,
			Ложь,
			Истина);
		
	КонецЕсли;
	
	#КонецОбласти
	
	// ВТСвязиМеждуПартиями
	ТекстЗапроса = ТекстЗапросаВТСвязиМеждуПартиями();
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТСвязиМеждуПартиями");
	
	// ВТПартии
	ТекстЗапроса = ТекстЗапросаВТПартии();
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТПартии");
	
	// ВТСвязи
	ТекстЗапроса = ТекстЗапросаВТСвязи();
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТСвязи");
	
	// ВТЭтапыСРучнымРазмещением
	ТекстЗапроса = ТекстЗапросаВТЭтапыСРучнымРазмещением("&ТекстИмяТаблицы.Распоряжение = &Заказ");
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТЭтапыСРучнымРазмещением");
	
	// ВТДатыНепланируемыхПредшественников
	ТекстЗапроса = ТекстЗапросаВТДатыНепланируемыхПредшественников("&ТекстИмяТаблицы.Распоряжение = &Заказ");
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТДатыНепланируемыхПредшественников");
	
	// Этапы
	ТекстЗапроса = ТекстЗапросаЭтапы(Ложь);
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "Этапы");
	
	// Связи
	ТекстЗапроса = ТекстЗапросаСвязи(ПараметрыРасчета.ОтменитьРучныеИзмененияГрафика);
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "Связи");
	
	// ВидыРЦ
	ТекстЗапроса = ТекстЗапросаВидыРЦ(Ложь, "ИСТИНА");
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВидыРЦ");
	
	// ИзделияКРасчетуВидовРЦПоФормуле
	ТекстЗапроса = ТекстЗапросаИзделияКРасчетуВидовРЦПоФормуле();
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ИзделияКРасчетуВидовРЦПоФормуле");
	
	#Область Задания
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Задания.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	Задания.Разделитель         КАК Разделитель
		|ИЗ
		|	РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства КАК Задания
		|ГДЕ
		|	Задания.ЗаказНаПроизводство = &Заказ";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "Задания");
	
	#КонецОбласти
	
	#Область ЭтапыСРучнымРазмещением
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Этапы.Ссылка КАК Ссылка
		|ИЗ
		|	ВТЭтапыСРучнымРазмещением КАК Этапы";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ЭтапыСРучнымРазмещением");
	
	#КонецОбласти
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Запрос.УстановитьПараметр("СтатусГрафика", ПараметрыРасчета.СтатусГрафика);
	Запрос.УстановитьПараметр("ПустойКлючСвязи", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	Запрос.УстановитьПараметр("ОсновнойКалендарь", Константы.ОсновнойКалендарьПредприятия.Получить());
	Запрос.УстановитьПараметр("ТекущаяДата", ПараметрыРасчета.ТекущаяДата);
	Запрос.УстановитьПараметр("НеограниченныйПаркОборудования", ПараметрыРасчета.НеограниченныйПаркОборудования);
	Запрос.УстановитьПараметр("ОтменитьРучныеИзмененияГрафика", ПараметрыРасчета.ОтменитьРучныеИзмененияГрафика);
	
	УстановитьПривилегированныйРежим(Истина);
	Таблицы = ОбщегоНазначенияУТ.ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса, , Ложь, Ложь);
	УстановитьПривилегированныйРежим(Ложь);
	
	Этапы = Таблицы.Этапы; // ТаблицаЗначений
	Этапы.Индексы.Добавить("КлючПартия, Этап");
	ДанныеРасчета.Этапы = Этапы;
	
	Связи = Таблицы.Связи; // ТаблицаЗначений
	Связи.Индексы.Добавить("КлючПартия, Этап");
	ДанныеРасчета.Связи = Связи;
	
	ВидыРЦ = Таблицы.ВидыРЦ; // ТаблицаЗначений
	РассчитатьВремяВидовРЦПоФормулам(ВидыРЦ, Таблицы.ИзделияКРасчетуВидовРЦПоФормуле);
	ВидыРЦ.Индексы.Добавить("КлючПартия, Этап");
	ДанныеРасчета.ВидыРЦ = ВидыРЦ;
	
	ДанныеРасчета.Задания = Таблицы.Задания;
	ДанныеРасчета.ЭтапыСРучнымРазмещением = Таблицы.ЭтапыСРучнымРазмещением.ВыгрузитьКолонку("Ссылка");
	
	// Контроль видов РЦ
	СтруктураПоиска = Новый Структура("ЕстьОшибкаНеЗаполненаЗагрузка", Истина);
	ТаблицаОшибок = ВидыРЦ.Скопировать(СтруктураПоиска, "ВидРабочегоЦентра");
	ТаблицаОшибок.Свернуть("ВидРабочегоЦентра");
	Для каждого Строка Из ТаблицаОшибок Цикл
		ТекстОповещения = СтрШаблон(
			НСтр("ru = 'Для вида РЦ ""%1"" не задана максимальная загрузка. Реквизит обязателен к заполнению для видов РЦ с параллельной загрузкой.';
				|en = 'The maximum load is not set for the ""%1"" work center type. The attribute is required for work center types with parallel load.'"),
			Строка.ВидРабочегоЦентра);
		ПараметрыРасчета.Оповещения.Вставить(ТекстОповещения, Строка.ВидРабочегоЦентра);
	КонецЦикла;
	
КонецПроцедуры

Функция ПрочитатьЭтапыПартий(МенеджерВременныхТаблиц)
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	// ВТСтруктураЗаказа
	ТекстОтборСтруктура = 
		"ИСТИНА В (
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ИСТИНА
		|	ИЗ 
		|		ВТГраницыЗаказов КАК Границы
		|	ГДЕ
		|		Границы.ЗаказНаПроизводство = &ТекстИмяТаблицы.ЗаказНаПроизводство
		|		И Границы.ДлительностьДоВыпуска <= &ТекстИмяТаблицы.ДлительностьДоВыпуска)";
	ТекстЗапроса = ТекстЗапросаВТСтруктураЗаказа(ТекстОтборСтруктура);
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТСтруктураЗаказа");
	
	// ВТВыпускПартий
	ТекстЗапроса = ТекстЗапросаВТВыпускПартий();
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТВыпускПартий");
	
	// ВТКоличествоПартий
	ТекстЗапроса = ТекстЗапросаВТКоличествоПартий();
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТКоличествоПартий");
	
	// ВТОграниченияГрафикаПроизводстваПоМатериалам
	УправлениеПроизводством.СоздатьВТОграниченияГрафикаПроизводстваПоМатериаламДинамическаяСтруктура(
		МенеджерВременныхТаблиц, ТекстОтборСтруктура, Ложь, Истина);
	
	// ВТСвязиМеждуПартиями
	ТекстЗапроса = ТекстЗапросаВТСвязиМеждуПартиями();
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТСвязиМеждуПартиями");
	
	// ВТПартии
	ТекстЗапроса = ТекстЗапросаВТПартии();
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТПартии");
	
	// ВТСвязи
	ТекстЗапроса = ТекстЗапросаВТСвязи();
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТСвязи");
	
	// ВТЭтапыСРучнымРазмещением
	ТекстОтборЭтапы = 
		"&ТекстИмяТаблицы.Распоряжение В (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Т.ЗаказНаПроизводство
		|	ИЗ
		|		ВТЗадания КАК Т)";
	ТекстЗапроса = ТекстЗапросаВТЭтапыСРучнымРазмещением(ТекстОтборЭтапы);
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТЭтапыСРучнымРазмещением");
	
	// ВТДатыНепланируемыхПредшественников
	ТекстОтборЭтапы = 
		"&ТекстИмяТаблицы.Распоряжение В (
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		Т.ЗаказНаПроизводство
		|	ИЗ
		|		ВТЗадания КАК Т)";
	ТекстЗапроса = ТекстЗапросаВТДатыНепланируемыхПредшественников(ТекстОтборЭтапы);
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТДатыНепланируемыхПредшественников");
	
	// ВТЭтапы
	ТекстЗапроса = ТекстЗапросаЭтапы(Истина);
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТЭтапы");
	
	// ВТВидыРЦ
	ТекстОтборПартии = 
		"&ТекстИмяТаблицы.КлючПартия В (
		|	ВЫБРАТЬ
		|		Т.КлючПартия
		|	ИЗ
		|		ВТЗадания КАК Т)";
	ТекстЗапроса = ТекстЗапросаВидыРЦ(Истина, ТекстОтборПартии);
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТВидыРЦ");
	
	#Область ВТПромежуточныеЭтапы
	
	// Непланируемые этапы, привязаные к ближайшему планируемому этапу
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Этапы.ЗаказНаПроизводство           КАК ЗаказНаПроизводство,
		|	Этапы.КлючПартия                    КАК КлючПартия,
		|	Этапы.Этап                          КАК Этап,
		|	МИНИМУМ(СледующиеЭтапы.НомерЗаписи) КАК НомерЗаписи
		|ПОМЕСТИТЬ ВТПромежуточныеЭтапы
		|ИЗ
		|	ВТЭтапы КАК Этапы
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЭтапы КАК СледующиеЭтапы
		|	ПО Этапы.ЗаказНаПроизводство = СледующиеЭтапы.ЗаказНаПроизводство
		|		И Этапы.НомерЗаписи < СледующиеЭтапы.НомерЗаписи
		|ГДЕ
		|	НЕ ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			ВТЗадания КАК Задания
		|		ГДЕ
		|			Задания.ЗаказНаПроизводство = Этапы.ЗаказНаПроизводство
		|			И Задания.КлючПартия = Этапы.КлючПартия)
		|	И ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			ВТЗадания КАК Задания
		|		ГДЕ
		|			Задания.ЗаказНаПроизводство = СледующиеЭтапы.ЗаказНаПроизводство
		|			И Задания.КлючПартия = СледующиеЭтапы.КлючПартия)
		|
		|СГРУППИРОВАТЬ ПО
		|	Этапы.ЗаказНаПроизводство,
		|	Этапы.КлючПартия,
		|	Этапы.Этап
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КлючПартия, Этап";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТПромежуточныеЭтапы");
	
	#КонецОбласти
	
	#Область ВТГраницыЭтапов
	
	// Ограничение справа для притягивания этапов
	// Заполняется в случаях когда партия справа не попадает в текущий расчет
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Этапы.КлючПартия       КАК КлючПартия,
		|	Этапы.Этап             КАК Этап,
		|	МИНИМУМ(График.Начало) КАК ПраваяГраница
		|ПОМЕСТИТЬ ВТГраницыЭтапов
		|ИЗ
		|	ВТЭтапы КАК Этапы
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСвязи КАК Связи
		|	ПО Этапы.КлючПартия = Связи.КлючПартияПредшественник
		|		И Этапы.Этап = Связи.ЭтапПредшественник
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|	ПО Связи.ЗаказНаПроизводство = График.ЗаказНаПроизводство
		|		И График.СтатусГрафика = 0
		|		И Связи.Этап = График.Этап
		|		И Связи.КлючПартия = График.КлючПартия
		|ГДЕ
		|	(Этапы.ЗаказНаПроизводство, Этапы.КлючПартия) В (
		|		ВЫБРАТЬ
		|			Т.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|			Т.КлючПартия КАК КлючПартия
		|		ИЗ
		|			ВТЗадания КАК Т)
		|	И Этапы.НомерСледующегоЭтапа = 0
		|	И Этапы.ЕстьПраваяГраница = ЛОЖЬ
		|	И Этапы.СмещатьВправо
		|	И НЕ ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			ВТЗадания КАК Т
		|		ГДЕ
		|			Т.ЗаказНаПроизводство = Связи.ЗаказНаПроизводство
		|			И Т.КлючПартия = Связи.КлючПартия)
		|СГРУППИРОВАТЬ ПО
		|	Этапы.КлючПартия,
		|	Этапы.Этап
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КлючПартия, Этап";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТГраницыЭтапов");
	
	#КонецОбласти
	
	// Связи
	ТекстЗапроса = ТекстЗапросаСвязи(Ложь);
	ТекстыЗапроса.Добавить(ТекстЗапроса, "Связи");
	
	// ВидыРЦ
	ТекстЗапроса = "ВЫБРАТЬ * ИЗ ВТВидыРЦ";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВидыРЦ");
	
	// ИзделияКРасчетуВидовРЦПоФормуле
	ТекстЗапроса = ТекстЗапросаИзделияКРасчетуВидовРЦПоФормуле();
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ИзделияКРасчетуВидовРЦПоФормуле");
	
	#Область ЗагрузкаВидовРЦ
	
	// Загрузка непланируемых этапов, привязанная к ближайшему планируемому этапу
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Этапы.НомерЗаписи          КАК НомерЗаписи,
		|	Загрузка.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
		|	Загрузка.ДатаИнтервала     КАК ДатаИнтервала,
		|	СУММА(Загрузка.Занято)     КАК ЗанятоВремя,
		|	СУММА(Загрузка.Занято) * 
		|		ВЫБОР
		|			КОГДА Загрузка.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|					И Загрузка.ВидРабочегоЦентра.МаксимальнаяЗагрузка <> 0
		|				ТОГДА Загрузка.ВидРабочегоЦентра.МаксимальнаяЗагрузка
		|			ИНАЧЕ 1
		|		КОНЕЦ                  КАК ЗанятоВремяЗагрузка
		|ИЗ
		|	ВТПромежуточныеЭтапы КАК Этапы
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДоступностьВидовРабочихЦентров КАК Загрузка
		|	ПО Этапы.КлючПартия = Загрузка.КлючПартия
		|		И Этапы.Этап = Загрузка.Этап
		|
		|ГДЕ
		|	Загрузка.ВидРабочегоЦентра В (
		|		ВЫБРАТЬ РАЗЛИЧНЫЕ
		|			Т.ВидРабочегоЦентра КАК ВидРабочегоЦентра
		|		ИЗ
		|			ВТВидыРЦ КАК Т
		|		ГДЕ
		|			Т.ЗаказНаПроизводство = Этапы.ЗаказНаПроизводство
		|			И Т.УчитыватьДоступность = ИСТИНА)
		|
		|СГРУППИРОВАТЬ ПО
		|	Этапы.НомерЗаписи,
		|	Загрузка.ВидРабочегоЦентра,
		|	Загрузка.ДатаИнтервала";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ЗагрузкаВидовРЦ");
	
	#КонецОбласти
	
	#Область ДатыПредшественников
	
	// Даты непланируемых этапов, предшествующих планируемым этапам
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Связи.КлючПартияПредшественник КАК КлючПартия,
		|	Связи.ЭтапПредшественник       КАК Этап,
		|	График.Начало                  КАК Начало,
		|	График.Окончание               КАК Окончание
		|ИЗ
		|	ВТСвязи КАК Связи
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|	ПО Связи.ЗаказНаПроизводствоПредшественник = График.ЗаказНаПроизводство
		|		И График.СтатусГрафика = &СтатусГрафика
		|		И Связи.ЭтапПредшественник = График.Этап
		|		И Связи.КлючПартияПредшественник = График.КлючПартия
		|ГДЕ
		|	(Связи.ЗаказНаПроизводство, Связи.КлючПартия) В (
		|		ВЫБРАТЬ
		|			Т.ЗаказНаПроизводство,
		|			Т.КлючПартия
		|		ИЗ
		|			ВТЗадания КАК Т)
		|	И НЕ ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			ВТЗадания КАК Т
		|		ГДЕ
		|			Т.ЗаказНаПроизводство = Связи.ЗаказНаПроизводствоПредшественник
		|			И Т.КлючПартия = Связи.КлючПартияПредшественник)
		|	И Связи.КлючПартияПредшественник <> Связи.КлючПартия";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ДатыПредшественников");
	
	#КонецОбласти
	
	#Область ДатыПоследователей
	
	// Даты начала непланируемых этапов - последователей
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Связи.КлючПартияПредшественник КАК КлючПартия,
		|	Связи.ЭтапПредшественник       КАК Этап,
		|	МИНИМУМ(График.Начало)         КАК НачалоСледующегоЭтапа
		|ИЗ
		|	ВТСвязи КАК Связи
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|	ПО Связи.ЗаказНаПроизводство = График.ЗаказНаПроизводство
		|		И График.СтатусГрафика = &СтатусГрафика
		|		И Связи.Этап = График.Этап
		|		И Связи.КлючПартия = График.КлючПартия
		|ГДЕ
		|	(Связи.ЗаказНаПроизводствоПредшественник, Связи.КлючПартияПредшественник) В (
		|		ВЫБРАТЬ
		|			Т.ЗаказНаПроизводство,
		|			Т.КлючПартия
		|		ИЗ
		|			ВТЗадания КАК Т)
		|	И НЕ ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			ВТЗадания КАК Т
		|		ГДЕ
		|			Т.ЗаказНаПроизводство = Связи.ЗаказНаПроизводство
		|			И Т.КлючПартия = Связи.КлючПартия)
		|	И Связи.КлючПартияПредшественник <> Связи.КлючПартия
		|
		|СГРУППИРОВАТЬ ПО
		|	Связи.КлючПартияПредшественник,
		|	Связи.ЭтапПредшественник";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ДатыПоследователей");
	
	#КонецОбласти
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СтатусГрафика", 
		Регистрысведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик());
	Запрос.УстановитьПараметр("ПустойКлючСвязи", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	Запрос.УстановитьПараметр("ОсновнойКалендарь", Константы.ОсновнойКалендарьПредприятия.Получить());
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("НеограниченныйПаркОборудования", Ложь);
	Запрос.УстановитьПараметр("ОтменитьРучныеИзмененияГрафика", Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	Таблицы = ОбщегоНазначенияУТ.ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса, , Истина, Ложь);
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат = Новый Структура;
	
	Связи = Таблицы.Связи; // ТаблицаЗначений
	Связи.Индексы.Добавить("КлючПартия, Этап");
	Результат.Вставить("Связи", Связи);
	
	ВидыРЦ = Таблицы.ВидыРЦ; // ТаблицаЗначений
	РассчитатьВремяВидовРЦПоФормулам(ВидыРЦ, Таблицы.ИзделияКРасчетуВидовРЦПоФормуле);
	ВидыРЦ.Индексы.Добавить("КлючПартия, Этап");
	Результат.Вставить("ВидыРЦ", ВидыРЦ);
	
	ЗагрузкаВидовРЦ = Таблицы.ЗагрузкаВидовРЦ; // ТаблицаЗначений
	ЗагрузкаВидовРЦ.Индексы.Добавить("НомерЗаписи");
	Результат.Вставить("ЗагрузкаВидовРЦ", ЗагрузкаВидовРЦ);
	
	ДатыПредшественников = Таблицы.ДатыПредшественников; // ТаблицаЗначений
	ДатыПредшественников.Индексы.Добавить("КлючПартия, Этап");
	Результат.Вставить("ДатыПредшественников", ДатыПредшественников);
	
	ДатыПоследователей = Таблицы.ДатыПоследователей; // ТаблицаЗначений
	ДатыПоследователей.Индексы.Добавить("КлючПартия, Этап");
	Результат.Вставить("ДатыПоследователей", ДатыПоследователей);
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаВТСтруктураЗаказа(ТекстЗапросаОтбор)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СтруктураЗаказа.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	СтруктураЗаказа.КлючНоменклатура    КАК КлючНоменклатура,
		|	СтруктураЗаказа.КлючПартия          КАК КлючПартия,
		|	СтруктураЗаказа.Этап                КАК Этап,
		|	СтруктураЗаказа.Номенклатура        КАК Номенклатура,
		|	СтруктураЗаказа.Характеристика      КАК Характеристика,
		|	СтруктураЗаказа.ВидСтроки <> ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Потребление) КАК ЭтоВыпуск,
		|	СтруктураЗаказа.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Перемещение)  КАК ЭтоПеремещение,
		|	СтруктураЗаказа.Номенклатура.ВидНоменклатуры    КАК ВидНоменклатуры,
		|	МАКСИМУМ(СтруктураЗаказа.Спецификация)          КАК Спецификация,
		|	МАКСИМУМ(СтруктураЗаказа.Подразделение)         КАК Подразделение,
		|	МАКСИМУМ(
		|		ВЫБОР
		|			КОГДА СтруктураЗаказа.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Перемещение)
		|				ТОГДА СтруктураЗаказа.Склад
		|		КОНЕЦ)                                      КАК СкладПеремещения,
		|	МАКСИМУМ(
		|		ВЫБОР
		|			КОГДА СтруктураЗаказа.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Перемещение)
		|				ТОГДА СтруктураЗаказа.Назначение
		|		КОНЕЦ)                                      КАК НазначениеПеремещения,
		|	СУММА(СтруктураЗаказа.Запланировано)            КАК Запланировано,
		|	СУММА(СтруктураЗаказа.Запущено)                 КАК Запущено,
		|	МИНИМУМ(СтруктураЗаказа.ДлительностьДоВыпуска)  КАК ДлительностьДоВыпуска,
		|	МИНИМУМ(СтруктураЗаказа.ИдентификаторВГруппеИзделий) КАК ИдентификаторВГруппеИзделий
		|ПОМЕСТИТЬ ВТСтруктураЗаказа
		|ИЗ
		|	РегистрСведений.СтруктураЗаказа КАК СтруктураЗаказа
		|ГДЕ
		|	&ТекстЗапросаОтбор
		|	И СтруктураЗаказа.ВидСтроки <> ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Продукция)
		|
		|СГРУППИРОВАТЬ ПО
		|	СтруктураЗаказа.ЗаказНаПроизводство,
		|	СтруктураЗаказа.КлючНоменклатура,
		|	СтруктураЗаказа.КлючПартия,
		|	СтруктураЗаказа.Этап,
		|	СтруктураЗаказа.Номенклатура,
		|	СтруктураЗаказа.Характеристика,
		|	СтруктураЗаказа.ВидСтроки <> ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Потребление),
		|	СтруктураЗаказа.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Перемещение),
		|	СтруктураЗаказа.Номенклатура.ВидНоменклатуры
		|
		|ИМЕЮЩИЕ
		|	СУММА(СтруктураЗаказа.Запланировано) > 0
		|	ИЛИ СУММА(СтруктураЗаказа.Запущено) > 0
		|	ИЛИ СУММА(СтруктураЗаказа.РаспределеноИзПартий) > 0
		|	ИЛИ СУММА(СтруктураЗаказа.РаспределеноИзПартийОбособленно) > 0
		|	ИЛИ СУММА(СтруктураЗаказа.Требуется) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПроизводство,
		|	КлючНоменклатура";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаОтбор",
		СтрЗаменить(ТекстЗапросаОтбор, "&ТекстИмяТаблицы", "СтруктураЗаказа"));
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТВыпускПартий()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СтруктураЗаказа.КлючПартия              КАК КлючПартия,
		|	СтруктураЗаказа.Номенклатура            КАК Номенклатура,
		|	СтруктураЗаказа.Характеристика          КАК Характеристика,
		|	СУММА(СтруктураЗаказа.Запланировано + СтруктураЗаказа.Запущено) КАК КоличествоЕдиниц,
		|	МАКСИМУМ(
		|		СпецификацииИзделий.КоличествоУпаковокНаЕдиницуПартииЗапуска
		|		* ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1))            КАК КоличествоЕдиницВПартии,
		|	АВТОНОМЕРЗАПИСИ()                       КАК Порядок
		|ПОМЕСТИТЬ ВТВыпускПартий
		|ИЗ
		|	ВТСтруктураЗаказа КАК СтруктураЗаказа
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СпецификацииИзделий КАК СпецификацииИзделий
		|	ПО СтруктураЗаказа.Спецификация = СпецификацииИзделий.Спецификация
		|		И СтруктураЗаказа.ВидНоменклатуры = СпецификацииИзделий.ВидНоменклатуры
		|		И СпецификацииИзделий.Номенклатура   В (СтруктураЗаказа.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
		|		И СпецификацииИзделий.Характеристика В (СтруктураЗаказа.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
		|ГДЕ
		|	СтруктураЗаказа.ЭтоВыпуск
		|	И НЕ СтруктураЗаказа.ЭтоПеремещение
		|	
		|СГРУППИРОВАТЬ ПО
		|	СтруктураЗаказа.КлючПартия,
		|	СтруктураЗаказа.Номенклатура,
		|	СтруктураЗаказа.Характеристика
		|	
		|ИМЕЮЩИЕ
		|	СУММА(СтруктураЗаказа.Запланировано + СтруктураЗаказа.Запущено) <> 0
		|	И МАКСИМУМ(
		|		ЕСТЬNULL(СпецификацииИзделий.КоличествоУпаковокНаЕдиницуПартииЗапуска, 1)
		|		* ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)) <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КлючПартия";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"СпецификацииИзделий.Упаковка",
			"СпецификацииИзделий.Номенклатура"));
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТКоличествоПартий()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВыпускПартий.КлючПартия       КАК КлючПартия,
		|	ВыпускПартий.Номенклатура     КАК Номенклатура,
		|	ВыпускПартий.Характеристика   КАК Характеристика,
		|	ВыпускПартий.КоличествоЕдиниц КАК КоличествоЕдиниц,
		|	ВыпускПартий.КоличествоЕдиниц КАК КоличествоПартийЧислитель,
		|	ВЫБОР
		|		КОГДА ВыпускПартий.КоличествоЕдиницВПартии = 0
		|			ТОГДА 1
		|		ИНАЧЕ ВыпускПартий.КоличествоЕдиницВПартии
		|	КОНЕЦ                         КАК КоличествоПартийЗнаменатель
		|ПОМЕСТИТЬ ВТКоличествоПартий
		|ИЗ
		|	ВТВыпускПартий КАК ВыпускПартий
		|ГДЕ
		|	ВыпускПартий.Порядок В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			Т.Порядок
		|		ИЗ
		|			ВТВыпускПартий КАК Т
		|		ГДЕ
		|			Т.КлючПартия = ВыпускПартий.КлючПартия
		|		УПОРЯДОЧИТЬ ПО
		|			Т.КоличествоЕдиниц / Т.КоличествоЕдиницВПартии УБЫВ)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КлючПартия";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТСвязиМеждуПартиями()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Последователи.КлючПартия            КАК КлючПартия,
		|	Последователи.Этап                  КАК Этап,
		|	Последователи.ЗаказНаПроизводство   КАК ЗаказНаПроизводство,
		|	Предшественники.КлючПартия          КАК КлючПартияПредшественник,
		|	Предшественники.Этап                КАК ЭтапПредшественник,
		|	Предшественники.ЗаказНаПроизводство КАК ЗаказНаПроизводствоПредшественник
		|ПОМЕСТИТЬ ВТСвязиМеждуПартиями
		|ИЗ
		|	ВТСтруктураЗаказа КАК Последователи
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСтруктураЗаказа КАК Предшественники
		|	ПО Последователи.ЗаказНаПроизводство = Предшественники.ЗаказНаПроизводство
		|		И Последователи.КлючНоменклатура = Предшественники.КлючНоменклатура
		|ГДЕ
		|	Предшественники.ЭтоВыпуск = ИСТИНА
		|	И Последователи.ЭтоВыпуск = ЛОЖЬ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПроизводство, КлючПартия";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТПартии()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 10000000
		|	СтруктураЗаказа.ЗаказНаПроизводство                              КАК ЗаказНаПроизводство,
		|	СтруктураЗаказа.КлючПартия                                       КАК КлючПартия,
		|	ЕСТЬNULL(Этапы.ПартияПроизводства, СтруктураЗаказа.Спецификация) КАК Ссылка,
		|	МАКСИМУМ(СтруктураЗаказа.ЭтоПеремещение)                         КАК ЭтоПеремещение,
		|	МИНИМУМ(СтруктураЗаказа.ДлительностьДоВыпуска)                   КАК ДлительностьДоВыпуска,
		|	МИНИМУМ(СтруктураЗаказа.ИдентификаторВГруппеИзделий)             КАК ИдентификаторВГруппеИзделий,
		|	МАКСИМУМ(
		|		ВЫБОР
		|			КОГДА СтруктураЗаказа.ЭтоПеремещение
		|				ТОГДА СтруктураЗаказа.Номенклатура
		|		КОНЕЦ)                                                       КАК НоменклатураПеремещения,
		|	МАКСИМУМ(
		|		ВЫБОР
		|			КОГДА СтруктураЗаказа.ЭтоПеремещение
		|				ТОГДА СтруктураЗаказа.Характеристика
		|		КОНЕЦ)                                                       КАК ХарактеристикаПеремещения,
		|	МАКСИМУМ(
		|		ВЫБОР
		|			КОГДА СтруктураЗаказа.ЭтоПеремещение
		|				ТОГДА СтруктураЗаказа.СкладПеремещения
		|		КОНЕЦ)                                                       КАК СкладПеремещения,
		|	МАКСИМУМ(
		|		ВЫБОР
		|			КОГДА СтруктураЗаказа.ЭтоПеремещение
		|				ТОГДА СтруктураЗаказа.НазначениеПеремещения
		|		КОНЕЦ)                                                       КАК НазначениеПеремещения,
		|	МИНИМУМ(
		|		ВЫБОР
		|			КОГДА Этапы.Ссылка ЕСТЬ НЕ NULL
		|				ТОГДА 1
		|			ИНАЧЕ 2
		|		КОНЕЦ)                                                       КАК ПорядокЗапуска,
		|	МИНИМУМ(
		|		ВЫБОР
		|			КОГДА ПродукцияЗаказа.ДатаПотребности ЕСТЬ НЕ NULL
		|				ТОГДА ПродукцияЗаказа.ДатаПотребности
		|		КОНЕЦ)                                                       КАК ДатаПотребностиПродукции,
		|	МИНИМУМ(
		|		ВЫБОР
		|			КОГДА ПродукцияЗаказа.НомерСтроки ЕСТЬ НЕ NULL
		|				ТОГДА ПродукцияЗаказа.НомерСтроки
		|		КОНЕЦ)                                                       КАК НомерСтрокиПродукция,
		|	МАКСИМУМ(
		|		ВЫБОР
		|			КОГДА ЕСТЬNULL(ДанныеПартии.КоличествоПартийЗнаменатель, 0) <> 0
		|				ТОГДА ДанныеПартии.КоличествоПартийЧислитель / ДанныеПартии.КоличествоПартийЗнаменатель
		|			ИНАЧЕ 0
		|		КОНЕЦ)                                                       КАК КоличествоПартий,
		|	АВТОНОМЕРЗАПИСИ()                                                КАК Порядок
		|ПОМЕСТИТЬ ВТПартии
		|ИЗ
		|	ВТСтруктураЗаказа КАК СтруктураЗаказа
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Этапы
		|	ПО СтруктураЗаказа.Этап = Этапы.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2.Продукция КАК ПродукцияЗаказа
		|	ПО СтруктураЗаказа.ЗаказНаПроизводство = ПродукцияЗаказа.Ссылка
		|		И СтруктураЗаказа.КлючНоменклатура = ПродукцияЗаказа.КлючНоменклатура
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТКоличествоПартий КАК ДанныеПартии
		|	ПО СтруктураЗаказа.КлючПартия = ДанныеПартии.КлючПартия
		|
		|СГРУППИРОВАТЬ ПО
		|	СтруктураЗаказа.ЗаказНаПроизводство,
		|	СтруктураЗаказа.КлючПартия,
		|	ЕСТЬNULL(Этапы.ПартияПроизводства, СтруктураЗаказа.Спецификация)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДлительностьДоВыпуска УБЫВ,
		|	ПорядокЗапуска,
		|	НомерСтрокиПродукция,
		|	ИдентификаторВГруппеИзделий,
		|	КлючПартия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка, ЭтоПеремещение";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТСвязи()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Связи.КлючПартия                        КАК КлючПартия,
		|	Связи.Этап                              КАК Этап,
		|	Связи.ЗаказНаПроизводство               КАК ЗаказНаПроизводство,
		|	Связи.КлючПартияПредшественник          КАК КлючПартияПредшественник,
		|	Связи.ЭтапПредшественник                КАК ЭтапПредшественник,
		|	Связи.ЗаказНаПроизводствоПредшественник КАК ЗаказНаПроизводствоПредшественник
		|ПОМЕСТИТЬ ВТСвязи
		|ИЗ
		|	ВТСвязиМеждуПартиями КАК Связи
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Партии.КлючПартия          КАК КлючПартия,
		|	Этапы2.Ссылка              КАК Этап,
		|	Партии.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	Партии.КлючПартия          КАК КлючПартияПредшественник,
		|	Этапы1.Ссылка              КАК ЭтапПредшественник,
		|	Партии.ЗаказНаПроизводство КАК ЗаказНаПроизводствоПредшественник
		|ИЗ
		|	ВТПартии КАК Партии
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы1
		|	ПО Партии.Ссылка = Этапы1.Владелец
		|		И Партии.ЭтоПеремещение = ЛОЖЬ
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы2
		|	ПО Этапы1.Владелец = Этапы2.Владелец
		|		И Этапы1.НомерСледующегоЭтапа = Этапы2.НомерЭтапа
		|
		|ГДЕ
		|	НЕ Этапы1.ПометкаУдаления
		|	И НЕ Этапы2.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Партии.КлючПартия          КАК КлючПартия,
		|	ЭтапыПоследователи.Этап    КАК Этап,
		|	Партии.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	Партии.КлючПартия          КАК КлючПартияПредшественник,
		|	Этапы.Ссылка               КАК ЭтапПредшественник,
		|	Партии.ЗаказНаПроизводство КАК ЗаказНаПроизводствоПредшественник
		|ИЗ
		|	ВТПартии КАК Партии
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Этапы
		|	ПО Партии.Ссылка = Этапы.ПартияПроизводства
		|		И Партии.ЭтоПеремещение = ЛОЖЬ
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.Последователи КАК ЭтапыПоследователи
		|	ПО Этапы.Ссылка = ЭтапыПоследователи.Ссылка
		|		И ЭтапыПоследователи.ТипСвязи = 0
		|	
		|ГДЕ
		|	Этапы.Проведен
		|	И Этапы.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЭтапПредшественник";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСвязи(ОтменитьРучныеИзмененияГрафика)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Связи.КлючПартия               КАК КлючПартия,
		|	Связи.Этап                     КАК Этап,
		|	Связи.КлючПартияПредшественник КАК КлючПартияПредшественник,
		|	Связи.ЭтапПредшественник       КАК ЭтапПредшественник
		|ИЗ
		|	ВТСвязи КАК Связи
		|ГДЕ
		|	&ТекстЗапросаИсключитьЭтапыСРучнымРазмещением";
	Если ОтменитьРучныеИзмененияГрафика Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаИсключитьЭтапыСРучнымРазмещением",
			"ИСТИНА");
	Иначе
		// Даты предшественников с ручным размещением учитываются при выборке этапов
		// (см. ВТДатыНепланируемыхПредшественников)
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаИсключитьЭтапыСРучнымРазмещением",
			"НЕ ИСТИНА В (
			|	ВЫБРАТЬ ПЕРВЫЕ 1
			|		ИСТИНА
			|	ИЗ
			|		ВТЭтапыСРучнымРазмещением КАК Т
			|	ГДЕ 
			|		Т.Ссылка = Связи.ЭтапПредшественник)");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТЭтапыСРучнымРазмещением(ТекстЗапросаОтбор)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Этапы.Распоряжение КАК ЗаказНаПроизводство,
		|	Этапы.Ссылка       КАК Ссылка
		|ПОМЕСТИТЬ ВТЭтапыСРучнымРазмещением
		|ИЗ
		|	Документ.ЭтапПроизводства2_2 КАК Этапы
		|ГДЕ
		|	&ТекстЗапросаОтбор
		|	И Этапы.Проведен
		|	И НЕ Этапы.Статус В (
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен))
		|	И Этапы.РучноеРазмещениеВГрафике
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаОтбор",
		СтрЗаменить(ТекстЗапросаОтбор, "&ТекстИмяТаблицы", "Этапы"));
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТДатыНепланируемыхПредшественников(ТекстЗапросаОтбор)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Связи.КлючПартия           КАК КлючПартия,
		|	Связи.Этап                 КАК Этап,
		|	МАКСИМУМ(График.Окончание) КАК Окончание
		|ПОМЕСТИТЬ ВТДатыНепланируемыхПредшественников
		|ИЗ
		|	ВТСвязи КАК Связи
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|	ПО Связи.ЗаказНаПроизводствоПредшественник = График.ЗаказНаПроизводство
		|		И График.СтатусГрафика = 0
		|		И Связи.ЭтапПредшественник = График.Этап
		|		И Связи.КлючПартияПредшественник = График.КлючПартия
		|ГДЕ
		|	Связи.ЭтапПредшественник В (
		|		ВЫБРАТЬ
		|			Этапы.Ссылка
		|		ИЗ
		|			ВТЭтапыСРучнымРазмещением КАК Этапы
		|		ГДЕ
		|			НЕ &ОтменитьРучныеИзмененияГрафика
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			Этапы.Ссылка
		|		ИЗ
		|			Документ.ЭтапПроизводства2_2 КАК Этапы
		|		ГДЕ
		|			&ТекстЗапросаОтбор
		|			И Этапы.Проведен
		|			И Этапы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат))
		|
		|СГРУППИРОВАТЬ ПО
		|	Связи.КлючПартия,
		|	Связи.Этап
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КлючПартия, Этап";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаОтбор",
		СтрЗаменить(ТекстЗапросаОтбор, "&ТекстИмяТаблицы", "Этапы"));
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЭтапы(СоздатьВТ)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Партии.ЗаказНаПроизводство                         КАК ЗаказНаПроизводство,
		|	&СтатусГрафика                                     КАК СтатусГрафика,
		|	Партии.КлючПартия                                  КАК КлючПартия,
		|	Этапы.Ссылка                                       КАК Этап,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСтрокГрафикаПроизводства.Этап) КАК ВидСтроки,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)                        КАК Начало,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)                        КАК Окончание,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)                        КАК ОкончаниеПредварительногоБуфера,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)                        КАК НачалоЗавершающегоБуфера,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)                        КАК НачалоСледующегоЭтапа,
		|	ЛОЖЬ                                               КАК ОграниченПоОборудованию,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)                        КАК ЖелаемаяДатаОбеспечения,
		|	ЗНАЧЕНИЕ(Перечисление.СпособыПривязкиОперацийПроизводства.КНачалу) КАК РазмещениеВыпуска,
		|	Этапы.Подразделение                                КАК Подразделение,
		|	Этапы.Владелец                                     КАК Спецификация,
		|	Партии.КоличествоПартий                            КАК КоличествоПартий,
		|	Партии.ДлительностьДоВыпуска + ЕСТЬNULL(ДлительностьЭтапов.ДлительностьДоВыпуска, 0) КАК Порядок,
		|
		|	Этапы.Подразделение.ИнтервалПланирования           КАК ИнтервалПланирования,
		|	Этапы.Подразделение.НачалоИнтервалаПланирования    КАК ИнтервалНачало,
		|	Этапы.Подразделение.ОкончаниеИнтервалаПланирования КАК ИнтервалОкончание,
		|	ВЫБОР
		|		КОГДА Этапы.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
		|			ТОГДА НАЧАЛОПЕРИОДА(Этапы.Подразделение.НачалоИнтервалаПланирования, ДЕНЬ) <> Этапы.Подразделение.НачалоИнтервалаПланирования
		|								ИЛИ КОНЕЦПЕРИОДА(Этапы.Подразделение.ОкончаниеИнтервалаПланирования, ДЕНЬ) <> Этапы.Подразделение.ОкончаниеИнтервалаПланирования
		|		КОГДА Этапы.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
		|			ТОГДА НАЧАЛОПЕРИОДА(Этапы.Подразделение.НачалоИнтервалаПланирования, НЕДЕЛЯ) <> Этапы.Подразделение.НачалоИнтервалаПланирования
		|								ИЛИ КОНЕЦПЕРИОДА(Этапы.Подразделение.ОкончаниеИнтервалаПланирования, НЕДЕЛЯ) <> Этапы.Подразделение.ОкончаниеИнтервалаПланирования
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ                                              КАК ИнтервалИмеетНестандартныеГраницы,
		|	Этапы.НомерЭтапа                                   КАК НомерЭтапа,
		|	Этапы.НомерСледующегоЭтапа                         КАК НомерСледующегоЭтапа,
		|	Этапы.ПланироватьРаботуВидовРабочихЦентров         КАК ПланироватьВидыРЦ,
		|	Этапы.ПорядокРаботыВидовРабочихЦентров             КАК ПорядокРаботыВидовРЦ,
		|	ВЫБОР Этапы.ЕдиницаИзмеренияДлительностиЭтапа
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.День)
		|			ТОГДА Этапы.ДлительностьЭтапа
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
		|			ТОГДА Этапы.ДлительностьЭтапа * 60
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
		|			ТОГДА Этапы.ДлительностьЭтапа * 3600
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Секунда)
		|			ТОГДА Этапы.ДлительностьЭтапа
		|	КОНЕЦ                                              КАК ДлительностьЭтапа,
		|	Этапы.ЕдиницаИзмеренияДлительностиЭтапа = ЗНАЧЕНИЕ(
		|		Перечисление.ЕдиницыИзмеренияВремени.День)     КАК РазмещатьВДнях,
		|	ВЫБОР Этапы.ЕдиницаИзмеренияПредварительногоБуфера
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.День)
		|			ТОГДА Этапы.ПредварительныйБуфер
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
		|			ТОГДА Этапы.ПредварительныйБуфер * 60
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
		|			ТОГДА Этапы.ПредварительныйБуфер * 3600
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Секунда)
		|			ТОГДА Этапы.ПредварительныйБуфер
		|	КОНЕЦ                                              КАК БуферДо,
		|	Этапы.ЕдиницаИзмеренияПредварительногоБуфера = ЗНАЧЕНИЕ(
		|		Перечисление.ЕдиницыИзмеренияВремени.День)     КАК БуферДоРазмещатьВДнях,
		|	ВЫБОР Этапы.ЕдиницаИзмеренияЗавершающегоБуфера
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.День)
		|			ТОГДА Этапы.ЗавершающийБуфер
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
		|			ТОГДА Этапы.ЗавершающийБуфер * 60
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
		|			ТОГДА Этапы.ЗавершающийБуфер * 3600
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Секунда)
		|			ТОГДА Этапы.ЗавершающийБуфер
		|	КОНЕЦ                                              КАК БуферПосле,
		|	Этапы.ЕдиницаИзмеренияЗавершающегоБуфера = ЗНАЧЕНИЕ(
		|		Перечисление.ЕдиницыИзмеренияВремени.День)     КАК БуферПослеРазмещатьВДнях,
		|	Этапы.Непрерывный                                  КАК Непрерывный,
		|	ВЫБОР
		|		КОГДА &ТекстЗапросаДатаДоступности >= &ТекстЗапросаДатаПредшественников
		|			И &ТекстЗапросаДатаДоступности >= ДанныеЗаказа.НачатьНеРанее
		|			И &ТекстЗапросаДатаДоступности >= &ТекущаяДата
		|			ТОГДА &ТекстЗапросаДатаДоступности
		|		КОГДА &ТекстЗапросаДатаПредшественников >= ДанныеЗаказа.НачатьНеРанее
		|			И &ТекстЗапросаДатаПредшественников >= &ТекущаяДата
		|			ТОГДА &ТекстЗапросаДатаПредшественников
		|		КОГДА ДанныеЗаказа.НачатьНеРанее >= &ТекущаяДата
		|			ТОГДА ДанныеЗаказа.НачатьНеРанее
		|		ИНАЧЕ &ТекущаяДата
		|	КОНЕЦ                                              КАК НачалоРазмещения,
		|	ЛОЖЬ                                               КАК ЭтоДокумент,
		|	ЛОЖЬ                                               КАК ЭтоПеремещение,
		|	ВЫБОР
		|		КОГДА Этапы.НомерСледующегоЭтапа = 0
		|			ТОГДА ЕСТЬNULL(Партии.ДатаПотребностиПродукции, ДАТАВРЕМЯ(1,1,1))
		|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
		|	КОНЕЦ                                              КАК ПраваяГраница,
		|	Этапы.НомерСледующегоЭтапа = 0
		|		И ЕСТЬNULL(Партии.ДатаПотребностиПродукции, ДАТАВРЕМЯ(1,1,1)) <> ДАТАВРЕМЯ(1,1,1) КАК ЕстьПраваяГраница,
		|	НЕ Этапы.ПланироватьРаботуВидовРабочихЦентров
		|		И (
		|			ИСТИНА В (
		|				ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					Справочник.ЭтапыПроизводства КАК ЭтапыСправа
		|				ГДЕ
		|					ЭтапыСправа.Владелец = Этапы.Владелец
		|					И ЭтапыСправа.ПланироватьРаботуВидовРабочихЦентров
		|					И ЭтапыСправа.НомерЭтапа > Этапы.НомерЭтапа
		|					И НЕ ЭтапыСправа.ПометкаУдаления)
		|			ИЛИ НЕ ИСТИНА В (
		|				ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					Справочник.ЭтапыПроизводства КАК ЭтапыСлева
		|				ГДЕ
		|					ЭтапыСлева.Владелец = Этапы.Владелец
		|					И ЭтапыСлева.ПланироватьРаботуВидовРабочихЦентров
		|					И ЭтапыСлева.НомерЭтапа < Этапы.НомерЭтапа
		|					И НЕ ЭтапыСлева.ПометкаУдаления)
		|		)                                                КАК СмещатьВправо,
		|	Партии.Порядок                                       КАК ПорядокПартии
		|ИЗ
		|	ВТПартии КАК Партии
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|	ПО Партии.Ссылка = Этапы.Владелец
		|		И Партии.ЭтоПеремещение = ЛОЖЬ
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТОграниченияГрафикаПроизводстваПоМатериалам КАК Обеспечение
		|	ПО Партии.ЗаказНаПроизводство = Обеспечение.ЗаказНаПроизводство
		|		И Партии.КлючПартия = Обеспечение.КлючПартия
		|		И Этапы.Ссылка = Обеспечение.Этап
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативнаяДлительностьЭтаповПроизводства КАК ДлительностьЭтапов
		|	ПО Этапы.Ссылка = ДлительностьЭтапов.Этап
		|		И Партии.Ссылка = ДлительностьЭтапов.Спецификация
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыНепланируемыхПредшественников КАК ДатыПредшественников
		|	ПО Партии.КлючПартия = ДатыПредшественников.КлючПартия
		|		И Этапы.Ссылка = ДатыПредшественников.Этап
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2 КАК ДанныеЗаказа
		|	ПО Партии.ЗаказНаПроизводство = ДанныеЗаказа.Ссылка
		|
		|ГДЕ
		|	НЕ Этапы.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Партии.ЗаказНаПроизводство                         КАК ЗаказНаПроизводство,
		|	&СтатусГрафика                                     КАК СтатусГрафика,
		|	Партии.КлючПартия                                  КАК КлючПартия,
		|	Этапы.Ссылка                                       КАК Этап,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСтрокГрафикаПроизводства.Этап) КАК ВидСтроки,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)                        КАК Начало,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)                        КАК Окончание,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)                        КАК ОкончаниеПредварительногоБуфера,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)                        КАК НачалоЗавершающегоБуфера,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)                        КАК НачалоСледующегоЭтапа,
		|	ЛОЖЬ                                               КАК ОграниченПоОборудованию,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)                        КАК ЖелаемаяДатаОбеспечения,
		|	ЗНАЧЕНИЕ(Перечисление.СпособыПривязкиОперацийПроизводства.КНачалу) КАК РазмещениеВыпуска,
		|	Этапы.Подразделение                                КАК Подразделение,
		|	Этапы.Спецификация                                 КАК Спецификация,
		|	Партии.КоличествоПартий                            КАК КоличествоПартий,
		|	Партии.ДлительностьДоВыпуска + Этапы.ДлительностьДоВыпуска КАК Порядок,
		|
		|	Этапы.Подразделение.ИнтервалПланирования           КАК ИнтервалПланирования,
		|	Этапы.Подразделение.НачалоИнтервалаПланирования    КАК ИнтервалНачало,
		|	Этапы.Подразделение.ОкончаниеИнтервалаПланирования КАК ИнтервалОкончание,
		|	ВЫБОР
		|		КОГДА Этапы.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
		|			ТОГДА НАЧАЛОПЕРИОДА(Этапы.Подразделение.НачалоИнтервалаПланирования, ДЕНЬ) <> Этапы.Подразделение.НачалоИнтервалаПланирования
		|								ИЛИ КОНЕЦПЕРИОДА(Этапы.Подразделение.ОкончаниеИнтервалаПланирования, ДЕНЬ) <> Этапы.Подразделение.ОкончаниеИнтервалаПланирования
		|		КОГДА Этапы.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
		|			ТОГДА НАЧАЛОПЕРИОДА(Этапы.Подразделение.НачалоИнтервалаПланирования, НЕДЕЛЯ) <> Этапы.Подразделение.НачалоИнтервалаПланирования
		|								ИЛИ КОНЕЦПЕРИОДА(Этапы.Подразделение.ОкончаниеИнтервалаПланирования, НЕДЕЛЯ) <> Этапы.Подразделение.ОкончаниеИнтервалаПланирования
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ                                              КАК ИнтервалИмеетНестандартныеГраницы,
		|	Этапы.НомерЭтапа                                   КАК НомерЭтапа,
		|	Этапы.НомерСледующегоЭтапа                         КАК НомерСледующегоЭтапа,
		|	ВЫБОР
		|		КОГДА &ОтменитьРучныеИзмененияГрафика
		|			И Этапы.РучноеРазмещениеВГрафике
		|			ТОГДА ВидыРЦ.ВидРабочегоЦентра ЕСТЬ НЕ NULL
		|		ИНАЧЕ Этапы.ПланироватьРаботуВидовРабочихЦентров
		|	КОНЕЦ                                              КАК ПланироватьВидыРЦ,
		|	Этапы.ПорядокРаботыВидовРабочихЦентров             КАК ПорядокРаботыВидовРЦ,
		|	ВЫБОР Этапы.ЕдиницаИзмеренияДлительностиЭтапа
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.День)
		|			ТОГДА Этапы.ДлительностьЭтапа
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
		|			ТОГДА Этапы.ДлительностьЭтапа * 60
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
		|			ТОГДА Этапы.ДлительностьЭтапа * 3600
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Секунда)
		|			ТОГДА Этапы.ДлительностьЭтапа
		|	КОНЕЦ                                              КАК ДлительностьЭтапа,
		|	Этапы.ЕдиницаИзмеренияДлительностиЭтапа = ЗНАЧЕНИЕ(
		|		Перечисление.ЕдиницыИзмеренияВремени.День)     КАК РазмещатьВДнях,
		|	ВЫБОР Этапы.ЕдиницаИзмеренияПредварительногоБуфера
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.День)
		|			ТОГДА Этапы.ПредварительныйБуфер
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
		|			ТОГДА Этапы.ПредварительныйБуфер * 60
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
		|			ТОГДА Этапы.ПредварительныйБуфер * 3600
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Секунда)
		|			ТОГДА Этапы.ПредварительныйБуфер
		|	КОНЕЦ                                              КАК БуферДо,
		|	Этапы.ЕдиницаИзмеренияПредварительногоБуфера = ЗНАЧЕНИЕ(
		|		Перечисление.ЕдиницыИзмеренияВремени.День)     КАК БуферДоРазмещатьВДнях,
		|	ВЫБОР Этапы.ЕдиницаИзмеренияЗавершающегоБуфера
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.День)
		|			ТОГДА Этапы.ЗавершающийБуфер
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
		|			ТОГДА Этапы.ЗавершающийБуфер * 60
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
		|			ТОГДА Этапы.ЗавершающийБуфер * 3600
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Секунда)
		|			ТОГДА Этапы.ЗавершающийБуфер
		|	КОНЕЦ                                              КАК БуферПосле,
		|	Этапы.ЕдиницаИзмеренияЗавершающегоБуфера = ЗНАЧЕНИЕ(
		|		Перечисление.ЕдиницыИзмеренияВремени.День)     КАК БуферПослеРазмещатьВДнях,
		|	Этапы.Непрерывный                                  КАК Непрерывный,
		|	ВЫБОР
		|		КОГДА &ТекстЗапросаДатаДоступности >= &ТекстЗапросаЭтапПланироватьНеРанее
		|			И &ТекстЗапросаДатаДоступности >= &ТекстЗапросаДатаПредшественников
		|			И &ТекстЗапросаДатаДоступности >= ДанныеЗаказа.НачатьНеРанее
		|			И &ТекстЗапросаДатаДоступности >= &ТекущаяДата
		|			ТОГДА &ТекстЗапросаДатаДоступности
		|		КОГДА &ТекстЗапросаЭтапПланироватьНеРанее >= &ТекстЗапросаДатаПредшественников
		|			И &ТекстЗапросаЭтапПланироватьНеРанее >= ДанныеЗаказа.НачатьНеРанее
		|			И &ТекстЗапросаЭтапПланироватьНеРанее >= &ТекущаяДата
		|			ТОГДА &ТекстЗапросаЭтапПланироватьНеРанее
		|		КОГДА &ТекстЗапросаДатаПредшественников >= ДанныеЗаказа.НачатьНеРанее
		|			И &ТекстЗапросаДатаПредшественников >= &ТекущаяДата
		|			ТОГДА &ТекстЗапросаДатаПредшественников
		|		КОГДА ДанныеЗаказа.НачатьНеРанее >= &ТекущаяДата
		|			ТОГДА ДанныеЗаказа.НачатьНеРанее
		|		ИНАЧЕ &ТекущаяДата
		|	КОНЕЦ                                              КАК НачалоРазмещения,
		|	ИСТИНА                                             КАК ЭтоДокумент,
		|	ЛОЖЬ                                               КАК ЭтоПеремещение,
		|	ДАТАВРЕМЯ(1,1,1)                                   КАК ПраваяГраница,
		|	ЛОЖЬ                                               КАК ЕстьПраваяГраница,
		|	НЕ ВЫБОР
		|		КОГДА &ОтменитьРучныеИзмененияГрафика
		|			И Этапы.РучноеРазмещениеВГрафике
		|			ТОГДА ВидыРЦ.ВидРабочегоЦентра ЕСТЬ НЕ NULL
		|		ИНАЧЕ Этапы.ПланироватьРаботуВидовРабочихЦентров
		|	КОНЕЦ
		|		И ИСТИНА В (
		|			ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				Документ.ЭтапПроизводства2_2 КАК ЭтапыСправа
		|				
		|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВидыРабочихЦентров КАК ВидыРЦЭтапыСправа
		|				ПО &ОтменитьРучныеИзмененияГрафика
		|					И ЭтапыСправа.РучноеРазмещениеВГрафике
		|					И ЭтапыСправа.Ссылка = ВидыРЦЭтапыСправа.Ссылка
		|					И ВидыРЦЭтапыСправа.НомерСтроки = 1
		|			ГДЕ
		|				ЭтапыСправа.ПартияПроизводства = Этапы.ПартияПроизводства
		|				И ВЫБОР
		|					КОГДА &ОтменитьРучныеИзмененияГрафика
		|						И ЭтапыСправа.РучноеРазмещениеВГрафике
		|						ТОГДА ВидыРЦЭтапыСправа.ВидРабочегоЦентра ЕСТЬ НЕ NULL
		|					ИНАЧЕ ЭтапыСправа.ПланироватьРаботуВидовРабочихЦентров
		|				КОНЕЦ
		|				И ЭтапыСправа.НомерЭтапа > Этапы.НомерЭтапа
		|				И ЭтапыСправа.Проведен)                  КАК СмещатьВправо,
		|	Партии.Порядок                                       КАК ПорядокПартии
		|ИЗ
		|	ВТПартии КАК Партии
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Этапы
		|	ПО Партии.Ссылка = Этапы.ПартияПроизводства
		|		И Партии.ЭтоПеремещение = ЛОЖЬ
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТОграниченияГрафикаПроизводстваПоМатериалам КАК Обеспечение
		|	ПО Партии.ЗаказНаПроизводство = Обеспечение.ЗаказНаПроизводство
		|		И Партии.КлючПартия = Обеспечение.КлючПартия
		|		И Этапы.Ссылка = Обеспечение.Этап
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыНепланируемыхПредшественников КАК ДатыПредшественников
		|	ПО Партии.КлючПартия = ДатыПредшественников.КлючПартия
		|		И Этапы.Ссылка = ДатыПредшественников.Этап
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВидыРабочихЦентров КАК ВидыРЦ
		|	ПО &ОтменитьРучныеИзмененияГрафика
		|		И Этапы.РучноеРазмещениеВГрафике
		|		И Этапы.Ссылка = ВидыРЦ.Ссылка
		|		И ВидыРЦ.НомерСтроки = 1
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2 КАК ДанныеЗаказа
		|	ПО Партии.ЗаказНаПроизводство = ДанныеЗаказа.Ссылка
		|
		|ГДЕ
		|	Этапы.Проведен
		|	И НЕ Этапы.Статус В (
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен))
		|	И (&ОтменитьРучныеИзмененияГрафика ИЛИ НЕ Этапы.РучноеРазмещениеВГрафике)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Партии.ЗаказНаПроизводство    КАК ЗаказНаПроизводство,
		|	&СтатусГрафика                КАК СтатусГрафика,
		|	Партии.КлючПартия             КАК КлючПартия,
		|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)    КАК Этап,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСтрокГрафикаПроизводства.Перемещение) КАК ВидСтроки,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)   КАК Начало,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)   КАК Окончание,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)   КАК ОкончаниеПредварительногоБуфера,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)   КАК НачалоЗавершающегоБуфера,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)   КАК НачалоСледующегоЭтапа,
		|	ЛОЖЬ                          КАК ОграниченПоОборудованию,
		|	ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)   КАК ЖелаемаяДатаОбеспечения,
		|	ЗНАЧЕНИЕ(Перечисление.СпособыПривязкиОперацийПроизводства.КНачалу) КАК РазмещениеВыпуска,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)  КАК Подразделение,
		|	ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка) КАК Спецификация,
		|	0                             КАК КоличествоПартий,
		|	Партии.ДлительностьДоВыпуска  КАК Порядок,
		|
		|	НЕОПРЕДЕЛЕНО                  КАК ИнтервалПланирования,
		|	НЕОПРЕДЕЛЕНО                  КАК ИнтервалНачало,
		|	НЕОПРЕДЕЛЕНО                  КАК ИнтервалОкончание,
		|	ЛОЖЬ                          КАК ИнтервалИмеетНестандартныеГраницы,
		|	0                             КАК НомерЭтапа,
		|	0                             КАК НомерСледующегоЭтапа,
		|	ЛОЖЬ                          КАК ПланироватьВидыРЦ,
		|	НЕОПРЕДЕЛЕНО                  КАК ПорядокРаботыВидовРЦ,
		|	ЕСТЬNULL(КэшНСИ.СпособОбеспечения.ДлительностьВДнях, 1) КАК ДлительностьЭтапа,
		|	ИСТИНА                        КАК РазмещатьВДнях,
		|	0                             КАК БуферДо,
		|	ЛОЖЬ                          КАК БуферДоРазмещатьВДнях,
		|	0                             КАК БуферПосле,
		|	ЛОЖЬ                          КАК БуферПослеРазмещатьВДнях,
		|	ЛОЖЬ                          КАК Непрерывный,
		|	ВЫБОР
		|		КОГДА &ТекстЗапросаДатаДоступности >= &ТекстЗапросаДатаПредшественников
		|			И &ТекстЗапросаДатаДоступности >= ДанныеЗаказа.НачатьНеРанее
		|			И &ТекстЗапросаДатаДоступности >= &ТекущаяДата
		|			ТОГДА &ТекстЗапросаДатаДоступности
		|		КОГДА &ТекстЗапросаДатаПредшественников >= ДанныеЗаказа.НачатьНеРанее
		|			И &ТекстЗапросаДатаПредшественников >= &ТекущаяДата
		|			ТОГДА &ТекстЗапросаДатаПредшественников
		|		КОГДА ДанныеЗаказа.НачатьНеРанее >= &ТекущаяДата
		|			ТОГДА ДанныеЗаказа.НачатьНеРанее
		|		ИНАЧЕ &ТекущаяДата
		|	КОНЕЦ                              КАК НачалоРазмещения,
		|	ЛОЖЬ                               КАК ЭтоДокумент,
		|	ИСТИНА                             КАК ЭтоПеремещение,
		|	ДАТАВРЕМЯ(1,1,1)                   КАК ПраваяГраница,
		|	ЛОЖЬ                               КАК ЕстьПраваяГраница,
		|	ИСТИНА                             КАК СмещатьВправо,
		|	Партии.Порядок                     КАК ПорядокПартии
		|ИЗ
		|	ВТПартии КАК Партии
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КэшНСИСтруктурыЗаказа КАК КэшНСИ
		|	ПО Партии.НоменклатураПеремещения = КэшНСИ.Номенклатура
		|		И Партии.ХарактеристикаПеремещения = КэшНСИ.Характеристика
		|		И Партии.СкладПеремещения = КэшНСИ.Склад
		|		И Партии.НазначениеПеремещения = КэшНСИ.Назначение
		|		И Партии.ЗаказНаПроизводство = КэшНСИ.ЗаказНаПроизводство
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТОграниченияГрафикаПроизводстваПоМатериалам КАК Обеспечение
		|	ПО Партии.ЗаказНаПроизводство = Обеспечение.ЗаказНаПроизводство
		|		И Партии.КлючПартия = Обеспечение.КлючПартия
		|		И Обеспечение.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыНепланируемыхПредшественников КАК ДатыПредшественников
		|	ПО Партии.КлючПартия = ДатыПредшественников.КлючПартия
		|		И ДатыПредшественников.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2 КАК ДанныеЗаказа
		|	ПО Партии.ЗаказНаПроизводство = ДанныеЗаказа.Ссылка
		|
		|ГДЕ
		|	Партии.ЭтоПеремещение
		|	И Партии.Ссылка = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаДатаДоступности",
		"ЕСТЬNULL(Обеспечение.ДатаДоступности, ДАТАВРЕМЯ(1,1,1))");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаЭтапПланироватьНеРанее",
		"ЕСТЬNULL(ВЫРАЗИТЬ(Этапы.ПланироватьНеРанее КАК ДАТА), ДАТАВРЕМЯ(1,1,1))");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаДатаПредшественников",
		"ЕСТЬNULL(ДатыПредшественников.Окончание, ДАТАВРЕМЯ(1,1,1))");
	
	ТекстЗапросаПорядок = 
		"
		|УПОРЯДОЧИТЬ ПО
		|	ПорядокПартии,
		|	НомерЭтапа";//@Query-part
	
	Если СоздатьВТ Тогда
		ТекстЗапросаВТ = 
			"ВЫБРАТЬ ПЕРВЫЕ 10000000
			|	*,
			|	АВТОНОМЕРЗАПИСИ() КАК НомерЗаписи
			|ПОМЕСТИТЬ ВТЭтапы
			|ИЗ
			|	(&ТекстЗапросаВыборка) КАК ВложенныйЗапрос
			|
			|&ТекстЗапросаПорядок
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ЗаказНаПроизводство,
			|	НомерЗаписи
			|";//@Query-part
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапросаВТ,
			"&ТекстЗапросаВыборка",
			ТекстЗапроса);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаПорядок",
			ТекстЗапросаПорядок);
		
	Иначе
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаПорядок;
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВидыРЦ(СоздатьВТ, ТекстЗапросаОтбор)
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Партии.ЗаказНаПроизводство               КАК ЗаказНаПроизводство,
		|	Партии.КлючПартия                        КАК КлючПартия,
		|	Этапы.Ссылка                             КАК Этап,
		|	Этапы.НомерЭтапа                         КАК НомерЭтапа,
		|	Виды.НомерСтроки                         КАК НомерСтроки,
		|	Виды.ВидРабочегоЦентра                                     КАК ВидРабочегоЦентра,
		|	Виды.ВидРабочегоЦентра.УчитыватьДоступностьПоГрафикуРаботы КАК УчитыватьДоступность,
		|	Виды.ВидРабочегоЦентра.ПараллельнаяЗагрузка                КАК ПараллельнаяЗагрузка,
		|	ВЫБОР
		|		КОГДА Виды.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|				И Виды.ВидРабочегоЦентра.МаксимальнаяЗагрузка <> 0
		|			ТОГДА Виды.ВидРабочегоЦентра.МаксимальнаяЗагрузка
		|		КОГДА Виды.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|				И Виды.Загрузка <> 0
		|			ТОГДА Виды.Загрузка
		|		КОГДА Виды.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ                                    КАК МаксимальнаяЗагрузка,
		|	Виды.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|		И Виды.ВидРабочегоЦентра.МаксимальнаяЗагрузка = 0 КАК ЕстьОшибкаНеЗаполненаЗагрузка,
		|	ВЫБОР
		|		КОГДА Виды.ВидРабочегоЦентра.Календарь = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
		|			ТОГДА Этапы.Подразделение
		|		ИНАЧЕ Виды.ВидРабочегоЦентра.Календарь
		|	КОНЕЦ                                    КАК КлючГрафика,
		|	Этапы.Подразделение.ИнтервалПланирования КАК ИнтервалПланирования,
		|
		|	ВЫБОР
		|		КОГДА Виды.ВидРабочегоЦентра.ПараллельнаяЗагрузка ТОГДА
		|			
		|			ДанныеПартии.КоличествоПартийЧислитель * Виды.Загрузка * Виды.ВремяРаботы / ДанныеПартии.КоличествоПартийЗнаменатель
		|		
		|		КОГДА Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий <> 0
		|				И Этапы.Владелец.ВыпускПроизвольнымиПорциями ТОГДА
		|			
		|			ВЫБОР
		|				КОГДА ЦЕЛ(ДанныеПартии.КоличествоЕдиниц / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий)
		|					= ДанныеПартии.КоличествоЕдиниц / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий
		|					ТОГДА ДанныеПартии.КоличествоЕдиниц / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий * Виды.ВремяРаботы
		|				ИНАЧЕ (ЦЕЛ(ДанныеПартии.КоличествоЕдиниц / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий) + 1) * Виды.ВремяРаботы
		|			КОНЕЦ
		|		
		|		КОГДА Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий <> 0
		|				И НЕ Этапы.Владелец.ВыпускПроизвольнымиПорциями ТОГДА
		|			
		|			ВЫБОР
		|				КОГДА Цел(ЕСТЬNULL(ДанныеПартии.КоличествоПартийЧислитель / ДанныеПартии.КоличествоПартийЗнаменатель, 0) / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий)
		|						= ЕСТЬNULL(ДанныеПартии.КоличествоПартийЧислитель / ДанныеПартии.КоличествоПартийЗнаменатель, 0) / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий
		|					ТОГДА ЕСТЬNULL(ДанныеПартии.КоличествоПартийЧислитель / ДанныеПартии.КоличествоПартийЗнаменатель, 0) / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий * Виды.ВремяРаботы
		|				ИНАЧЕ (Цел(ЕСТЬNULL(ДанныеПартии.КоличествоПартийЧислитель / ДанныеПартии.КоличествоПартийЗнаменатель, 0) / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий) + 1) * Виды.ВремяРаботы
		|			КОНЕЦ
		|			
		|		ИНАЧЕ ДанныеПартии.КоличествоПартийЧислитель * Виды.ВремяРаботы / ДанныеПартии.КоличествоПартийЗнаменатель
		|	КОНЕЦ * ВЫБОР Виды.ЕдиницаИзмерения
		|				КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
		|					ТОГДА 60
		|				КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
		|					ТОГДА 3600
		|				КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
		|					ТОГДА 86400
		|				ИНАЧЕ 1
		|			КОНЕЦ                            КАК ВремяЗагрузка,
		|	ВЫБОР
		|		КОГДА ДЛИНАСТРОКИ(СОКРЛ(Виды.АлгоритмРасчетаКоличества)) <> 0 ТОГДА
		|			ВЫБОР
		|				КОГДА Виды.ВидРабочегоЦентра.ПараллельнаяЗагрузка ТОГДА
		|					Виды.Загрузка
		|				ИНАЧЕ 1
		|			КОНЕЦ * ВЫБОР Виды.ЕдиницаИзмерения
		|						КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
		|							ТОГДА 60
		|						КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
		|							ТОГДА 3600
		|						КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
		|							ТОГДА 86400
		|						ИНАЧЕ 1
		|					КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ                                    КАК КоэффициентРасчетаПоФормуле
		|ПОМЕСТИТЬ ВТВидыРЦ
		|ИЗ
		|	ВТПартии КАК Партии
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
		|	ПО Партии.Ссылка = Этапы.Владелец
		|		И Партии.ЭтоПеремещение = ЛОЖЬ
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства.ВидыРабочихЦентров КАК Виды
		|	ПО Этапы.Ссылка = Виды.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТКоличествоПартий КАК ДанныеПартии
		|	ПО Партии.КлючПартия = ДанныеПартии.КлючПартия
		|
		|ГДЕ
		|	&ТекстЗапросаОтбор
		|	И НЕ Этапы.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Партии.ЗаказНаПроизводство               КАК ЗаказНаПроизводство,
		|	Партии.КлючПартия                        КАК КлючПартия,
		|	Этапы.Ссылка                             КАК Этап,
		|	Этапы.НомерЭтапа                         КАК НомерЭтапа,
		|	Виды1.НомерСтроки                        КАК НомерСтроки,
		|	ЕСТЬNULL(Виды2.ВидРабочегоЦентра, Виды1.ВидРабочегоЦентра)  КАК ВидРабочегоЦентра,
		|	ВЫБОР
		|		КОГДА Виды2.ВидРабочегоЦентра ЕСТЬ НЕ NULL
		|			ТОГДА Виды2.ВидРабочегоЦентра.УчитыватьДоступностьПоГрафикуРаботы
		|		ИНАЧЕ Виды1.ВидРабочегоЦентра.УчитыватьДоступностьПоГрафикуРаботы
		|	КОНЕЦ КАК УчитыватьДоступность,
		|	ВЫБОР
		|		КОГДА Виды2.ВидРабочегоЦентра ЕСТЬ НЕ NULL
		|			ТОГДА Виды2.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|		ИНАЧЕ Виды1.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|	КОНЕЦ КАК ПараллельнаяЗагрузка,
		|	ВЫБОР
		|		КОГДА Виды2.ВидРабочегоЦентра ЕСТЬ НЕ NULL
		|			ТОГДА
		|				ВЫБОР
		|					КОГДА Виды2.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|							И Виды2.ВидРабочегоЦентра.МаксимальнаяЗагрузка <> 0
		|						ТОГДА Виды2.ВидРабочегоЦентра.МаксимальнаяЗагрузка
		|					КОГДА Виды2.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|							И Виды2.ЗагрузкаНорматив <> 0
		|						ТОГДА Виды2.ЗагрузкаНорматив
		|					КОГДА Виды2.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|						ТОГДА 1
		|					ИНАЧЕ 0
		|				КОНЕЦ
		|		ИНАЧЕ
		|			ВЫБОР
		|				КОГДА Виды1.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|						И Виды1.ВидРабочегоЦентра.МаксимальнаяЗагрузка <> 0
		|					ТОГДА Виды1.ВидРабочегоЦентра.МаксимальнаяЗагрузка
		|				КОГДА Виды1.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|						И Виды1.ЗагрузкаНорматив <> 0
		|					ТОГДА Виды1.ЗагрузкаНорматив
		|				КОГДА Виды1.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|					ТОГДА 1
		|				ИНАЧЕ 0
		|			КОНЕЦ
		|	КОНЕЦ КАК МаксимальнаяЗагрузка,
		|	ВЫБОР
		|		КОГДА Виды2.ВидРабочегоЦентра ЕСТЬ НЕ NULL
		|			ТОГДА Виды2.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|					И Виды2.ВидРабочегоЦентра.МаксимальнаяЗагрузка = 0
		|		ИНАЧЕ Виды1.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|				И Виды1.ВидРабочегоЦентра.МаксимальнаяЗагрузка = 0
		|	КОНЕЦ КАК ЕстьОшибкаНеЗаполненаЗагрузка,
		|	ВЫБОР
		|		КОГДА Виды2.ВидРабочегоЦентра ЕСТЬ НЕ NULL
		|			ТОГДА
		|				ВЫБОР
		|					КОГДА Виды2.ВидРабочегоЦентра.Календарь = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
		|						ТОГДА Этапы.Подразделение
		|					ИНАЧЕ Виды2.ВидРабочегоЦентра.Календарь
		|				КОНЕЦ
		|		ИНАЧЕ
		|			ВЫБОР
		|				КОГДА Виды1.ВидРабочегоЦентра.Календарь = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
		|					ТОГДА Этапы.Подразделение
		|				ИНАЧЕ Виды1.ВидРабочегоЦентра.Календарь
		|			КОНЕЦ
		|	КОНЕЦ                                    КАК КлючГрафика,
		|	Этапы.Подразделение.ИнтервалПланирования КАК ИнтервалПланирования,
		|	ВЫБОР
		|		КОГДА Виды2.ВидРабочегоЦентра ЕСТЬ НЕ NULL
		|			ТОГДА
		|				ВЫБОР
		|					КОГДА Виды2.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|						ТОГДА Виды2.Загрузка * Виды2.ВремяРаботыНорматив
		|					ИНАЧЕ Виды2.ВремяРаботы
		|				КОНЕЦ * ВЫБОР Виды2.ЕдиницаИзмерения
		|							КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
		|							ТОГДА 60
		|						КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
		|							ТОГДА 3600
		|						КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
		|							ТОГДА 86400
		|						ИНАЧЕ 1
		|					КОНЕЦ
		|		ИНАЧЕ
		|			ВЫБОР
		|				КОГДА Виды1.ВидРабочегоЦентра.ПараллельнаяЗагрузка
		|					ТОГДА Виды1.Загрузка * Виды1.ВремяРаботыНорматив
		|				ИНАЧЕ Виды1.ВремяРаботы
		|			КОНЕЦ * ВЫБОР Виды1.ЕдиницаИзмерения
		|						КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
		|						ТОГДА 60
		|					КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
		|						ТОГДА 3600
		|					КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
		|						ТОГДА 86400
		|					ИНАЧЕ 1
		|				КОНЕЦ
		|	КОНЕЦ                                    КАК ВремяЗагрузка,
		|	0                                        КАК КоэффициентРасчетаПоФормуле
		|ИЗ
		|	ВТПартии КАК Партии
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Этапы
		|	ПО Партии.Ссылка = Этапы.ПартияПроизводства
		|		И Партии.ЭтоПеремещение = ЛОЖЬ
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВидыРабочихЦентров КАК Виды1
		|	ПО Этапы.Ссылка = Виды1.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.АльтернативныеВидыРабочихЦентров КАК Виды2
		|	ПО Этапы.Ссылка = Виды2.Ссылка
		|		И Виды1.КлючСвязи = Виды2.КлючСвязиВидыРабочихЦентров
		|		И НЕ Виды1.Использовать
		|		И Виды2.Использовать
		|
		|ГДЕ
		|	&ТекстЗапросаОтбор
		|	И Этапы.Проведен
		|	И НЕ Этапы.Статус В (
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен))
		|	И (&ОтменитьРучныеИзмененияГрафика ИЛИ НЕ Этапы.РучноеРазмещениеВГрафике)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаОтбор",
		СтрЗаменить(ТекстЗапросаОтбор, "&ТекстИмяТаблицы", "Партии"));
	
	Если НЕ СоздатьВТ Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ВТВидыРЦ", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаИзделияКРасчетуВидовРЦПоФормуле()
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Партии.КлючПартия                 КАК КлючПартия,
		|	Партии.Ссылка                     КАК Спецификация,
		|	ДанныеПартии.Номенклатура         КАК Номенклатура,
		|	ДанныеПартии.Характеристика       КАК Характеристика,
		|	ДанныеПартии.КоличествоЕдиниц     КАК Количество,
		|	Партии.ЗаказНаПроизводство        КАК Распоряжение,
		|	Заказ.Подразделение               КАК ПодразделениеДиспетчер,
		|	Заказ.НаправлениеДеятельности     КАК НаправлениеДеятельности,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеИсполнитель,
		|	ДАТАВРЕМЯ(1,1,1)                  КАК НачалоПроизводства,
		|	0                                 КАК ДнейОтПотребности
		|ИЗ
		|	ВТПартии КАК Партии
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКоличествоПартий КАК ДанныеПартии
		|	ПО Партии.КлючПартия = ДанныеПартии.КлючПартия
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2 КАК Заказ
		|	ПО Партии.ЗаказНаПроизводство = Заказ.Ссылка
		|ГДЕ
		|	Партии.Ссылка ССЫЛКА Справочник.РесурсныеСпецификации
		|	И НЕ Партии.ЭтоПеремещение
		|	И ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			Справочник.ЭтапыПроизводства КАК Этапы
		|			
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства.ВидыРабочихЦентров КАК Виды
		|			ПО Этапы.Ссылка = Виды.Ссылка
		|		ГДЕ
		|			Этапы.Владелец = Партии.Ссылка
		|			И НЕ Этапы.ПометкаУдаления
		|			И ДЛИНАСТРОКИ(СОКРЛ(Виды.АлгоритмРасчетаКоличества)) <> 0)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура РассчитатьВремяВидовРЦПоФормулам(ВидыРЦ, ИзделияКРасчетуВидовРЦПоФормуле)
	
	Если ИзделияКРасчетуВидовРЦПоФормуле.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВидыРЦ.Индексы.Добавить("КлючПартия, Этап, ВидРабочегоЦентра");
	СтруктураПоиска = Новый Структура("КлючПартия, Этап, ВидРабочегоЦентра");
	
	ДанныеСпецификаций = Справочники.РесурсныеСпецификации.ДанныеСпецификацииПоСпискуНоменклатуры(
		ИзделияКРасчетуВидовРЦПоФормуле,
		Справочники.РесурсныеСпецификации.ПараметрыВыборкиДанных("ВидыРабочихЦентров"),
		Истина,
		"КлючПартия");
	
	Для каждого ДанныеСпецификации Из ДанныеСпецификаций Цикл
		СтруктураПоиска.КлючПартия = ДанныеСпецификации.КлючПартия;
		Для каждого Строка Из ДанныеСпецификации.ВидыРабочихЦентров Цикл
			Если ПустаяСтрока(Строка.АлгоритмРасчетаКоличества) Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, Строка, "Этап, ВидРабочегоЦентра");
			НайденныеСтроки = ВидыРЦ.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество() <> 0 Тогда
				НайденныеСтроки[0].ВремяЗагрузка = Строка.ВремяРаботы * НайденныеСтроки[0].КоэффициентРасчетаПоФормуле;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ЧтениеДоступности

Процедура ПрочитатьДоступность(Заказ, ДанныеРасчета, ПараметрыРасчета, Граница = Неопределено)
	
	Если ПараметрыРасчета.ДоступностьГраницаВыборки = '00010101' Тогда
		Если ЗначениеЗаполнено(ПараметрыРасчета.ДоступностьИнтервалНачало)
			И ЗначениеЗаполнено(ПараметрыРасчета.ДоступностьИнтервалОкончание) Тогда
			Начало = ПараметрыРасчета.ДоступностьИнтервалНачало;
			Окончание = ПараметрыРасчета.ДоступностьИнтервалОкончание;
		Иначе
			Интервал = ИнтервалВыборкиДоступности(Заказ); // Структура
			Начало = Интервал.Начало;
			Окончание = Интервал.Окончание;
		КонецЕсли;
		ЭтоДополнительноеЧтение = Ложь;
	Иначе
		Начало = ПараметрыРасчета.ДоступностьГраницаВыборки + 1;
		Окончание = ?(Граница <> Неопределено И Граница > Начало,
			Граница,
			КонецМесяца(ДобавитьМесяц(Начало, 2)));
		ЭтоДополнительноеЧтение = Истина;
	КонецЕсли;
	ПараметрыРасчета.ДоступностьГраницаВыборки = Окончание;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Запрос.УстановитьПараметр("Начало", Начало);
	Запрос.УстановитьПараметр("Окончание", Окончание);
	Запрос.УстановитьПараметр("ОкончаниеНачалоДня", НачалоДня(Окончание));
	Запрос.УстановитьПараметр("ЗадействоватьРезервДоступности", ПараметрыРасчета.ЗадействоватьРезервДоступности);
	Запрос.УстановитьПараметр("ОтменитьРучныеИзмененияГрафика", ПараметрыРасчета.ОтменитьРучныеИзмененияГрафика);
	Запрос.УстановитьПараметр("НеограниченныйПаркОборудования", ПараметрыРасчета.НеограниченныйПаркОборудования);
	
	Если ЭтоДополнительноеЧтение Тогда
		Запрос.УстановитьПараметр("РасписаниеПодразделенийДень", ДанныеРасчета.РасписаниеПодразделенийДень);
		Запрос.УстановитьПараметр("РасписаниеПодразделенийСекунда", ДанныеРасчета.РасписаниеПодразделенийСекунда);
		Запрос.УстановитьПараметр("ДоступностьВРЦ", ДанныеРасчета.ДоступностьВРЦ);
	КонецЕсли;
	
	Если ПараметрыРасчета.ДоступностьОтборПодразделения.Количество() <> 0 Тогда
		Запрос.УстановитьПараметр("Подразделения", ПараметрыРасчета.ДоступностьОтборПодразделения);
	КонецЕсли;
	
	Если ПараметрыРасчета.ДоступностьОтборВРЦ.Количество() <> 0 Тогда
		Запрос.УстановитьПараметр("ВидыРЦ", ПараметрыРасчета.ДоступностьОтборВРЦ);
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	#Область Графики
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Календари.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Календари КАК Календари
		|ГДЕ
		|	ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			Справочник.СтруктураПредприятия.ГрафикиРаботы КАК Графики
		|		ГДЕ
		|			Графики.ГрафикРаботы = Календари.Ссылка
		|			И &ТекстЗапросаОтборПодразделения)
		|	ИЛИ ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			Справочник.ВидыРабочихЦентров КАК ВидыРабочихЦентров
		|		ГДЕ
		|			ВидыРабочихЦентров.Календарь = Календари.Ссылка
		|			И &ТекстЗапросаОтборВидыРЦ)
		|	ИЛИ ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			Константа.ОсновнойКалендарьПредприятия КАК ОсновнойКалендарьПредприятия
		|		ГДЕ
		|			ОсновнойКалендарьПредприятия.Значение = Календари.Ссылка)";
	
	Если ПараметрыРасчета.ДоступностьОтборПодразделения.Количество() = 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаОтборПодразделения",
			"ИСТИНА");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаОтборПодразделения",
			"Графики.Ссылка В (&Подразделения)");
	КонецЕсли;
	
	Если ПараметрыРасчета.ДоступностьОтборВРЦ.Количество() = 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаОтборВидыРЦ",
			"ИСТИНА");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаОтборВидыРЦ",
			"ВидыРабочихЦентров.Ссылка В (&ВидыРЦ)");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Графики = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	#КонецОбласти
	
	КалендарныеГрафики.СоздатьВТРасписанияРаботыНаПериод(
		МенеджерВременныхТаблиц, Графики, Начало, Окончание, Ложь);
	
	РазделительЗапросовВПакете = ОбщегоНазначения.РазделительПакетаЗапросов();
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если ЭтоДополнительноеЧтение Тогда
		
		#Область ВТРасписаниеПодразделенийДеньТекущее
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	РасписаниеПодразделенийДень.Подразделение КАК Подразделение,
			|	РасписаниеПодразделенийДень.Дата          КАК Дата,
			|	РасписаниеПодразделенийДень.Начало        КАК Начало,
			|	РасписаниеПодразделенийДень.Окончание     КАК Окончание,
			|	РасписаниеПодразделенийДень.ЭтоИндекс     КАК ЭтоИндекс
			|ПОМЕСТИТЬ ВТРасписаниеПодразделенийДеньТекущее
			|ИЗ
			|	&РасписаниеПодразделенийДень КАК РасписаниеПодразделенийДень
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Подразделение,
			|	Начало";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТРасписаниеПодразделенийДеньТекущее");
		
		#КонецОбласти
		
		#Область ВТРасписаниеПодразделенийСекундаТекущее
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	РасписаниеПодразделенийСекунда.Подразделение КАК Подразделение,
			|	РасписаниеПодразделенийСекунда.Дата          КАК Дата,
			|	РасписаниеПодразделенийСекунда.Начало        КАК Начало,
			|	РасписаниеПодразделенийСекунда.Окончание     КАК Окончание,
			|	РасписаниеПодразделенийСекунда.Длительность  КАК Длительность,
			|	РасписаниеПодразделенийСекунда.ЭтоИндекс     КАК ЭтоИндекс
			|ПОМЕСТИТЬ ВТРасписаниеПодразделенийСекундаТекущее
			|ИЗ
			|	&РасписаниеПодразделенийСекунда КАК РасписаниеПодразделенийСекунда
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Подразделение,
			|	Начало";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТРасписаниеПодразделенийСекундаТекущее");
		
		#КонецОбласти
		
		#Область ВТДоступностьВРЦТекущая
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ДоступностьВРЦ.ВидРабочегоЦентра     КАК ВидРабочегоЦентра,
			|	ДоступностьВРЦ.ДатаИнтервала         КАК ДатаИнтервала,
			|	ДоступностьВРЦ.НачалоИнтервала       КАК НачалоИнтервала,
			|	ДоступностьВРЦ.ОкончаниеИнтервала    КАК ОкончаниеИнтервала,
			|	ДоступностьВРЦ.Смена                 КАК Смена,
			|	ДоступностьВРЦ.ДоступноВремяЗагрузка КАК ДоступноВремяЗагрузка,
			|	ДоступностьВРЦ.ДоступноВремя         КАК ДоступноВремя,
			|	ДоступностьВРЦ.МаксимумВремяЗагрузка КАК МаксимумВремяЗагрузка,
			|	ДоступностьВРЦ.ЭтоИндекс             КАК ЭтоИндекс
			|ПОМЕСТИТЬ ВТДоступностьВРЦТекущая
			|ИЗ
			|	&ДоступностьВРЦ КАК ДоступностьВРЦ
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ВидРабочегоЦентра,
			|	ДатаИнтервала,
			|	ЭтоИндекс";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТДоступностьВРЦТекущая");
		
		#КонецОбласти
		
	Иначе
		
		#Область ВТРасписаниеПодразделенийДеньТекущее
		
		ТекстЗапроса = 
			"ВЫБРАТЬ ПЕРВЫЕ 0
			|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
			|	ДАТАВРЕМЯ(1, 1, 1)                                     КАК Дата,
			|	ДАТАВРЕМЯ(1, 1, 1)                                     КАК Начало,
			|	ДАТАВРЕМЯ(1, 1, 1)                                     КАК Окончание,
			|	ЛОЖЬ                                                   КАК ЭтоИндекс
			|ПОМЕСТИТЬ ВТРасписаниеПодразделенийДеньТекущее";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТРасписаниеПодразделенийДеньТекущее");
		
		#КонецОбласти
		
		#Область ВТРасписаниеПодразделенийСекундаТекущее
		
		ТекстЗапроса = 
			"ВЫБРАТЬ ПЕРВЫЕ 0
			|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
			|	ДАТАВРЕМЯ(1, 1, 1)                                     КАК Дата,
			|	ДАТАВРЕМЯ(1, 1, 1)                                     КАК Начало,
			|	ДАТАВРЕМЯ(1, 1, 1)                                     КАК Окончание,
			|	0                                                      КАК Длительность,
			|	ЛОЖЬ                                                   КАК ЭтоИндекс
			|ПОМЕСТИТЬ ВТРасписаниеПодразделенийСекундаТекущее";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТРасписаниеПодразделенийСекундаТекущее");
		
		#КонецОбласти
		
		#Область ВТДоступностьВРЦТекущая
		
		ТекстЗапроса = 
			"ВЫБРАТЬ ПЕРВЫЕ 0
			|	ЗНАЧЕНИЕ(Справочник.ВидыРабочихЦентров.ПустаяСсылка) КАК ВидРабочегоЦентра,
			|	ДАТАВРЕМЯ(1, 1, 1)                                   КАК ДатаИнтервала,
			|	ДАТАВРЕМЯ(1, 1, 1)                                   КАК НачалоИнтервала,
			|	ДАТАВРЕМЯ(1, 1, 1)                                   КАК ОкончаниеИнтервала,
			|	ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)          КАК Смена,
			|	0                                                    КАК ДоступноВремяЗагрузка,
			|	0                                                    КАК ДоступноВремя,
			|	0                                                    КАК МаксимумВремяЗагрузка,
			|	ЛОЖЬ                                                 КАК ЭтоИндекс
			|ПОМЕСТИТЬ ВТДоступностьВРЦТекущая";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТДоступностьВРЦТекущая");
		
		#КонецОбласти
		
	КонецЕсли;
	
	#Область ВТСчетчикДней
	
	ТекстЗапроса = 
		"ВЫБРАТЬ 0 КАК Шаг
		|ПОМЕСТИТЬ ВТСчетчикДней
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 1
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 2
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 3
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 4
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 5
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 6
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 7
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 8
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 9";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТСчетчикДней");
	
	#КонецОбласти
	
	#Область ВТСчетчикЧасов
	
	ТекстЗапроса = 
		"ВЫБРАТЬ 0 КАК НомерЧаса
		|ПОМЕСТИТЬ ВТСчетчикЧасов
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 1
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 2
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 3
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 4
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 5
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 6
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 7
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 8
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 9
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 10
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 11
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 12
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 13
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 14
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 15
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 16
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 17
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 18
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 19
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 20
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 21
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 22
		|ОБЪЕДИНИТЬ ВСЕ
		|ВЫБРАТЬ 23";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТСчетчикЧасов");
	
	#КонецОбласти
	
	#Область ВТНомераДней
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Счетчик1.Шаг * 1000 + Счетчик2.Шаг * 100 + Счетчик3.Шаг * 10 + Счетчик4.Шаг КАК Номер
		|ПОМЕСТИТЬ ВТНомераДней
		|ИЗ
		|	ВТСчетчикДней КАК Счетчик1,
		|	ВТСчетчикДней КАК Счетчик2,
		|	ВТСчетчикДней КАК Счетчик3,
		|	ВТСчетчикДней КАК Счетчик4
		|ГДЕ
		|	(Счетчик1.Шаг * 1000 + Счетчик2.Шаг * 100 + Счетчик3.Шаг * 10 + Счетчик4.Шаг) <= РАЗНОСТЬДАТ(&Начало, &ОкончаниеНачалоДня, ДЕНЬ)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТНомераДней");
	
	#КонецОбласти
	
	#Область ВТДниЗаПериод
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДОБАВИТЬКДАТЕ(&Начало, ДЕНЬ, НомераДней.Номер) КАК Дата
		|ПОМЕСТИТЬ ВТДниЗаПериод
		|ИЗ
		|	ВТНомераДней КАК НомераДней
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Дата";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТДниЗаПериод");
	
	#КонецОбласти
	
	#Область ВТПриоритетныеЗаказы
	
	Если ПараметрыРасчета.ОтсутствиеПрочихЗаказов Тогда
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство2_2.ПустаяСсылка) КАК Ссылка
			|ПОМЕСТИТЬ ВТПриоритетныеЗаказы";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТПриоритетныеЗаказы");
		
	Иначе
		
		Документы.ЗаказНаПроизводство2_2.СоздатьВТЗаказыСБольшимПриоритетом(
			МенеджерВременныхТаблиц, "ВТПриоритетныеЗаказы", Заказ);
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ВТГрафикиПодразделений
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	СтруктураПредприятия.Ссылка                                           КАК Подразделение,
		|	ЕСТЬNULL(Графики.ГрафикРаботы, ОсновнойКалендарьПредприятия.Значение) КАК ГрафикРаботы
		|ПОМЕСТИТЬ ВТГрафикиПодразделений
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия.ГрафикиРаботы КАК Графики
		|	ПО СтруктураПредприятия.Ссылка = Графики.Ссылка
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Константа.ОсновнойКалендарьПредприятия КАК ОсновнойКалендарьПредприятия
		|	ПО (ИСТИНА)
		|ГДЕ
		|	НЕ СтруктураПредприятия.ПометкаУдаления
		|	И СтруктураПредприятия.ПроизводственноеПодразделение
		|	И &ТекстЗапросаОтборПодразделения
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|// График для расчет перемещений
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
		|	ОсновнойКалендарьПредприятия.Значение                  КАК ГрафикРаботы
		|ИЗ
		|	Константа.ОсновнойКалендарьПредприятия КАК ОсновнойКалендарьПредприятия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ГрафикРаботы";
	
	Если ПараметрыРасчета.ДоступностьОтборПодразделения.Количество() = 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаОтборПодразделения",
			"ИСТИНА");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаОтборПодразделения",
			"СтруктураПредприятия.Ссылка В (&Подразделения)");
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТГрафикиПодразделений");
	
	#КонецОбласти
	
	#Область РасписаниеПодразделенийДень
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Графики.Подразделение                  КАК Подразделение,
		|	ДниЗаПериод.Дата                       КАК Дата,
		|	МИНИМУМ(ВЫБОР
		|		КОГДА РасписанияРаботы.Период ЕСТЬ NULL
		|			ТОГДА ДниЗаПериод.Дата
		|		КОГДА ЕСТЬNULL(РасписанияРаботы.ВремяНачала, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА РасписанияРаботы.ДатаГрафика
		|		ИНАЧЕ ДОБАВИТЬКДАТЕ(РасписанияРаботы.ДатаГрафика, МИНУТА,
		|				ЧАС(РасписанияРаботы.ВремяНачала) * 60 + МИНУТА(РасписанияРаботы.ВремяНачала))
		|	КОНЕЦ)                                 КАК Начало,
		|	МАКСИМУМ(ВЫБОР
		|		КОГДА РасписанияРаботы.Период ЕСТЬ NULL
		|			ТОГДА ДниЗаПериод.Дата
		|		КОГДА ЕСТЬNULL(РасписанияРаботы.ВремяОкончания, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДОБАВИТЬКДАТЕ(РасписанияРаботы.ДатаГрафика, ДЕНЬ, 1)
		|		ИНАЧЕ ДОБАВИТЬКДАТЕ(РасписанияРаботы.ДатаГрафика, МИНУТА,
		|				ЧАС(РасписанияРаботы.ВремяОкончания) * 60
		|					+ МИНУТА(РасписанияРаботы.ВремяОкончания))
		|	КОНЕЦ)                                 КАК Окончание,
		|	РасписанияРаботы.Период ЕСТЬ NULL      КАК ЭтоИндекс
		|ИЗ
		|	ВТГрафикиПодразделений КАК Графики
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДниЗаПериод КАК ДниЗаПериод
		|	ПО ИСТИНА
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТРасписанияРаботы КАК РасписанияРаботы
		|	ПО Графики.ГрафикРаботы = РасписанияРаботы.ГрафикРаботы
		|		И ДниЗаПериод.Дата = РасписанияРаботы.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	Графики.Подразделение,
		|	ДниЗаПериод.Дата,
		|	РасписанияРаботы.Период ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасписаниеПодразделенийДень.Подразделение КАК Подразделение,
		|	РасписаниеПодразделенийДень.Дата          КАК Дата,
		|	РасписаниеПодразделенийДень.Начало        КАК Начало,
		|	РасписаниеПодразделенийДень.Окончание     КАК Окончание,
		|	РасписаниеПодразделенийДень.ЭтоИндекс     КАК ЭтоИндекс
		|ИЗ
		|	ВТРасписаниеПодразделенийДеньТекущее КАК РасписаниеПодразделенийДень
		|
		|УПОРЯДОЧИТЬ ПО
		|	Подразделение,
		|	Начало";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "РасписаниеПодразделенийДень");
	
	#КонецОбласти
	
	#Область РасписаниеПодразделенийСекунда
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Графики.Подразделение                  КАК Подразделение,
		|	ДниЗаПериод.Дата                       КАК Дата,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РасписанияРаботы.ВремяНачала, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДниЗаПериод.Дата
		|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ДниЗаПериод.Дата, МИНУТА,
		|				ЧАС(РасписанияРаботы.ВремяНачала) * 60 + МИНУТА(РасписанияРаботы.ВремяНачала))
		|	КОНЕЦ                                  КАК Начало,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РасписанияРаботы.ВремяОкончания, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДОБАВИТЬКДАТЕ(ДниЗаПериод.Дата, ДЕНЬ, 1)
		|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ДниЗаПериод.Дата, МИНУТА,
		|				ЧАС(РасписанияРаботы.ВремяОкончания) * 60
		|					+ МИНУТА(РасписанияРаботы.ВремяОкончания))
		|	КОНЕЦ                                  КАК Окончание,
		|	ВЫБОР
		|		КОГДА РасписанияРаботы.ДатаГрафика ЕСТЬ NULL
		|			ТОГДА 0
		|		КОГДА ЕСТЬNULL(РасписанияРаботы.ВремяОкончания, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			И ЕСТЬNULL(РасписанияРаботы.ВремяНачала, ДАТАВРЕМЯ(1, 1, 1)) <> ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА 86400 - РАЗНОСТЬДАТ(ДАТАВРЕМЯ(1, 1, 1), РасписанияРаботы.ВремяНачала, СЕКУНДА)
		|		КОГДА ЕСТЬNULL(РасписанияРаботы.ВремяОкончания, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА 86400
		|		ИНАЧЕ РАЗНОСТЬДАТ(ЕСТЬNULL(РасписанияРаботы.ВремяНачала, ДАТАВРЕМЯ(1, 1, 1)), РасписанияРаботы.ВремяОкончания, СЕКУНДА)
		|	КОНЕЦ                                  КАК Длительность,
		|	РасписанияРаботы.ДатаГрафика ЕСТЬ NULL КАК ЭтоИндекс
		|ИЗ
		|	ВТГрафикиПодразделений КАК Графики
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДниЗаПериод КАК ДниЗаПериод
		|	ПО ИСТИНА
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТРасписанияРаботы КАК РасписанияРаботы
		|	ПО Графики.ГрафикРаботы = РасписанияРаботы.ГрафикРаботы
		|		И ДниЗаПериод.Дата = РасписанияРаботы.ДатаГрафика
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РасписаниеПодразделенийСекунда.Подразделение КАК Подразделение,
		|	РасписаниеПодразделенийСекунда.Дата          КАК Дата,
		|	РасписаниеПодразделенийСекунда.Начало        КАК Начало,
		|	РасписаниеПодразделенийСекунда.Окончание     КАК Окончание,
		|	РасписаниеПодразделенийСекунда.Длительность  КАК Длительность,
		|	РасписаниеПодразделенийСекунда.ЭтоИндекс     КАК ЭтоИндекс
		|ИЗ
		|	ВТРасписаниеПодразделенийСекундаТекущее КАК РасписаниеПодразделенийСекунда
		|
		|УПОРЯДОЧИТЬ ПО
		|	Подразделение,
		|	Начало";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "РасписаниеПодразделенийСекунда");
	
	#КонецОбласти
	
	#Область ВТКоличествоРЦ
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	РабочиеЦентры.ВидРабочегоЦентра  КАК ВидРабочегоЦентра,
		|	КОЛИЧЕСТВО(РабочиеЦентры.Ссылка) КАК КоличествоРабочихЦентров
		|ПОМЕСТИТЬ ВТКоличествоРЦ
		|ИЗ
		|	Справочник.РабочиеЦентры КАК РабочиеЦентры
		|ГДЕ
		|	НЕ РабочиеЦентры.ПометкаУдаления
		|	И НЕ РабочиеЦентры.ВидРабочегоЦентра.ВводитьДоступностьДляВидаРЦ
		|	И &ТекстЗапросаОтборВидыРЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	РабочиеЦентры.ВидРабочегоЦентра
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидРабочегоЦентра";
	
	Если ПараметрыРасчета.ДоступностьОтборВРЦ.Количество() = 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаОтборВидыРЦ",
			"ИСТИНА");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаОтборВидыРЦ",
			"РабочиеЦентры.ВидРабочегоЦентра В (&ВидыРЦ)");
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТКоличествоРЦ");
	
	#КонецОбласти
	
	#Область ВТСправочникВидыРЦ
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВидыРЦ.Ссылка                                       КАК ВидРабочегоЦентра,
		|	ВидыРЦ.Подразделение                                КАК Подразделение,
		|	ВидыРЦ.Подразделение.ИнтервалПланирования           КАК ИнтервалПланирования,
		|	ВидыРЦ.Подразделение.НачалоИнтервалаПланирования    КАК НачалоИнтервалаПланирования,
		|	ВидыРЦ.Подразделение.ОкончаниеИнтервалаПланирования КАК ОкончаниеИнтервалаПланирования,
		|	ВидыРЦ.Календарь                                    КАК Календарь,
		|	ВидыРЦ.РезервДоступности                            КАК РезервДоступности,
		|	ВидыРЦ.ВводитьДоступностьДляВидаРЦ                  КАК ВводитьДоступностьДляВидаРЦ,
		|	ВЫБОР
		|		КОГДА &НеограниченныйПаркОборудования
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ВидыРЦ.УчитыватьДоступностьПоГрафикуРаботы
		|	КОНЕЦ                                               КАК УчитыватьДоступность,
		|	ВЫБОР
		|		КОГДА НЕ ВидыРЦ.УчитыватьДоступностьПоГрафикуРаботы
		|				ИЛИ &НеограниченныйПаркОборудования
		|			ТОГДА 100000000
		|		КОГДА ВидыРЦ.ВводитьДоступностьДляВидаРЦ
		|			ТОГДА ВидыРЦ.КоличествоРабочихЦентров
		|		ИНАЧЕ ЕСТЬNULL(КоличествоРЦ.КоличествоРабочихЦентров, 0)
		|	КОНЕЦ                                               КАК КоличествоРабочихЦентров,
		|	ВидыРЦ.ПараллельнаяЗагрузка                         КАК ПараллельнаяЗагрузка,
		|	ВЫБОР ВидыРЦ.ЕдиницаИзмеренияДоступностиРЦ
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
		|			ТОГДА ВидыРЦ.МаксимальнаяДоступностьРЦ * 60
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
		|			ТОГДА ВидыРЦ.МаксимальнаяДоступностьРЦ * 3600
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
		|			ТОГДА ВидыРЦ.МаксимальнаяДоступностьРЦ * 86400
		|	КОНЕЦ                                               КАК МаксимальнаяДоступность,
		|	ВидыРЦ.МаксимальнаяЗагрузка                         КАК МаксимальнаяЗагрузка
		|ПОМЕСТИТЬ ВТСправочникВидыРЦ
		|ИЗ
		|	Справочник.ВидыРабочихЦентров КАК ВидыРЦ
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТКоличествоРЦ КАК КоличествоРЦ
		|	ПО ВидыРЦ.Ссылка = КоличествоРЦ.ВидРабочегоЦентра
		|
		|ГДЕ
		|	НЕ ВидыРЦ.ЭтоГруппа
		|	И &ТекстЗапросаОтборВидыРЦ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидРабочегоЦентра";
	
	Если ПараметрыРасчета.ДоступностьОтборВРЦ.Количество() = 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаОтборВидыРЦ",
			"ИСТИНА");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаОтборВидыРЦ",
			"ВидыРЦ.Ссылка В (&ВидыРЦ)");
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТСправочникВидыРЦ");
	
	#КонецОбласти
	
	#Область ВТГрафикиВидовРЦ
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВидыРЦ.ВидРабочегоЦентра              КАК ВидРабочегоЦентра,
		|	ВидыРЦ.ИнтервалПланирования           КАК ИнтервалПланирования,
		|	ВидыРЦ.НачалоИнтервалаПланирования    КАК НачалоИнтервалаПланирования,
		|	ВидыРЦ.ОкончаниеИнтервалаПланирования КАК ОкончаниеИнтервалаПланирования,
		|	ВидыРЦ.УчитыватьДоступность           КАК УчитыватьДоступность,
		|	ВидыРЦ.ПараллельнаяЗагрузка           КАК ПараллельнаяЗагрузка,
		|	ВидыРЦ.МаксимальнаяДоступность        КАК МаксимальнаяДоступность,
		|	ВидыРЦ.МаксимальнаяЗагрузка           КАК МаксимальнаяЗагрузка,
		|	ВЫБОР
		|		КОГДА ВидыРЦ.Календарь <> ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
		|			ТОГДА ВидыРЦ.Календарь
		|		ИНАЧЕ ЕСТЬNULL(Графики.ГрафикРаботы, ОсновнойКалендарьПредприятия.Значение)
		|	КОНЕЦ                                 КАК ГрафикРаботы
		|ПОМЕСТИТЬ ВТГрафикиВидовРЦ
		|ИЗ
		|	ВТСправочникВидыРЦ КАК ВидыРЦ
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия.ГрафикиРаботы КАК Графики
		|	ПО ВидыРЦ.Подразделение = Графики.Ссылка
		|		И ВидыРЦ.Календарь = ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Константа.ОсновнойКалендарьПредприятия КАК ОсновнойКалендарьПредприятия
		|	ПО (ИСТИНА)
		|ГДЕ
		|	ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
		|	ИЛИ НЕ ВидыРЦ.УчитыватьДоступность
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ГрафикРаботы";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТГрафикиВидовРЦ");
	
	#КонецОбласти
	
	#Область ВТГраницыСмен
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ГрафикиРаботы.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
		|	ГрафикиРаботы.ГрафикРаботы      КАК Смена,
		|	МИНИМУМ(ДОБАВИТЬКДАТЕ(Расписание.ДатаГрафика, МИНУТА,
		|		ЧАС(ЕСТЬNULL(Расписание.ВремяНачала, ДАТАВРЕМЯ(1, 1, 1)))*60
		|		+ МИНУТА(ЕСТЬNULL(Расписание.ВремяНачала, ДАТАВРЕМЯ(1, 1, 1))))) КАК ДатаИнтервала,
		|	МАКСИМУМ(
		|		ВЫБОР
		|			КОГДА Расписание.ВремяОкончания ЕСТЬ NULL 
		|				ИЛИ Расписание.ВремяОкончания = ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ДОБАВИТЬКДАТЕ(Расписание.ДатаГрафика, ДЕНЬ, 1)
		|			ИНАЧЕ ДОБАВИТЬКДАТЕ(Расписание.ДатаГрафика, МИНУТА,
		|				ЧАС(Расписание.ВремяОкончания)*60 + МИНУТА(Расписание.ВремяОкончания))
		|		КОНЕЦ) КАК ОкончаниеИнтервала
		|ПОМЕСТИТЬ ВТГраницыСмен
		|ИЗ
		|	ВТГрафикиВидовРЦ КАК ГрафикиРаботы
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасписанияРаботы КАК Расписание
		|	ПО ГрафикиРаботы.ГрафикРаботы = Расписание.ГрафикРаботы
		|ГДЕ
		|	ГрафикиРаботы.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
		|
		|СГРУППИРОВАТЬ ПО
		|	ГрафикиРаботы.ВидРабочегоЦентра,
		|	ГрафикиРаботы.ГрафикРаботы,
		|	Расписание.Период
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидРабочегоЦентра,
		|	ДатаИнтервала";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТГраницыСмен");
	
	#КонецОбласти
	
	#Область ВТВидыРЦБезУчетаДоступностиЧас
	
	Если Не ПараметрыРасчета.КруглосуточнаяРаботаБезВыходных Тогда
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ГрафикиРаботы.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
			|	Расписание.ДатаГрафика КАК ДатаГрафика,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(Расписание.ВремяНачала, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА 0
			|		КОГДА НАЧАЛОПЕРИОДА(Расписание.ВремяНачала, ЧАС) = Расписание.ВремяНачала
			|			ТОГДА ЧАС(Расписание.ВремяНачала)
			|		ИНАЧЕ ЧАС(Расписание.ВремяНачала) + 1
			|	КОНЕЦ КАК ЧасНачало,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(Расписание.ВремяОкончания, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА 23
			|		КОГДА НАЧАЛОПЕРИОДА(Расписание.ВремяОкончания, ЧАС) = Расписание.ВремяОкончания
			|			ТОГДА ЧАС(Расписание.ВремяОкончания) - 1
			|		ИНАЧЕ ЧАС(Расписание.ВремяОкончания)
			|	КОНЕЦ КАК ЧасОкончание
			|ПОМЕСТИТЬ ВТВидыРЦБезУчетаДоступностиЧас
			|ИЗ
			|	ВТГрафикиВидовРЦ КАК ГрафикиРаботы
			|	
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасписанияРаботы КАК Расписание
			|	ПО ГрафикиРаботы.ГрафикРаботы = Расписание.ГрафикРаботы
			|ГДЕ
			|	НЕ ГрафикиРаботы.УчитыватьДоступность
			|	И ГрафикиРаботы.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТВидыРЦБезУчетаДоступностиЧас");
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ДоступностьВРЦ
	
	Если ПараметрыРасчета.КруглосуточнаяРаботаБезВыходных Тогда
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ВидыРЦ.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
			|	ВЫБОР ВидыРЦ.ИнтервалПланирования
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
			|			ТОГДА ДниЗаПериод.Дата
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
			|			ТОГДА ДниЗаПериод.Дата
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, НЕДЕЛЯ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, МЕСЯЦ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
			|			ТОГДА ДОБАВИТЬКДАТЕ(ДниЗаПериод.Дата, ЧАС, СчетчикЧасов.НомерЧаса)
			|	КОНЕЦ                    КАК ДатаИнтервала,
			|	ВЫБОР ВидыРЦ.ИнтервалПланирования
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
			|			ТОГДА ДниЗаПериод.Дата
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
			|			ТОГДА ДниЗаПериод.Дата
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, НЕДЕЛЯ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, МЕСЯЦ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
			|			ТОГДА ДОБАВИТЬКДАТЕ(ДниЗаПериод.Дата, ЧАС, СчетчикЧасов.НомерЧаса)
			|	КОНЕЦ                    КАК НачалоИнтервала,
			|	ВЫБОР ВидыРЦ.ИнтервалПланирования
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
			|			ТОГДА ДОБАВИТЬКДАТЕ(ДниЗаПериод.Дата, ДЕНЬ, 1)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
			|			ТОГДА ДОБАВИТЬКДАТЕ(ДниЗаПериод.Дата, ДЕНЬ, 1)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, НЕДЕЛЯ), НЕДЕЛЯ, 1)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, МЕСЯЦ), МЕСЯЦ, 1)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
			|			ТОГДА ДОБАВИТЬКДАТЕ(ДниЗаПериод.Дата, ЧАС, СчетчикЧасов.НомерЧаса + 1)
			|	КОНЕЦ                    КАК ОкончаниеИнтервала,
			|	ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка) КАК Смена,
			|	ВЫБОР
			|		КОГДА НЕ ВидыРЦ.УчитыватьДоступность
			|			ТОГДА 100000000
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
			|			ТОГДА 86400 * ВидыРЦ.КоличествоРабочихЦентров
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
			|			ТОГДА 86400 * ВидыРЦ.КоличествоРабочихЦентров
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА 604800 * ВидыРЦ.КоличествоРабочихЦентров
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА КОЛИЧЕСТВО(ДниЗаПериод.Дата) * 86400 * ВидыРЦ.КоличествоРабочихЦентров
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
			|			ТОГДА 3600 * ВидыРЦ.КоличествоРабочихЦентров
			|	КОНЕЦ * ВЫБОР
			|				КОГДА ВидыРЦ.ПараллельнаяЗагрузка
			|						И ВидыРЦ.МаксимальнаяЗагрузка <> 0
			|					ТОГДА ВидыРЦ.МаксимальнаяЗагрузка
			|				ИНАЧЕ 1
			|			КОНЕЦ            КАК ДоступноВремяЗагрузка,
			|	ВЫБОР
			|		КОГДА НЕ ВидыРЦ.УчитыватьДоступность
			|			ТОГДА 100000000
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
			|			ТОГДА 86400 * ВидыРЦ.КоличествоРабочихЦентров
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
			|			ТОГДА 86400 * ВидыРЦ.КоличествоРабочихЦентров
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА 604800 * ВидыРЦ.КоличествоРабочихЦентров
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА КОЛИЧЕСТВО(ДниЗаПериод.Дата) * 86400 * ВидыРЦ.КоличествоРабочихЦентров
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
			|			ТОГДА 3600 * ВидыРЦ.КоличествоРабочихЦентров
			|	КОНЕЦ                    КАК ДоступноВремя,
			|	ВЫБОР
			|		КОГДА ВидыРЦ.ИнтервалПланирования В (
			|				ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День),
			|				ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена))
			|			ТОГДА 86400
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА 604800
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА КОЛИЧЕСТВО(ДниЗаПериод.Дата) * 86400
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
			|			ТОГДА 3600
			|	КОНЕЦ * ВЫБОР
			|				КОГДА ВидыРЦ.ПараллельнаяЗагрузка
			|						И ВидыРЦ.МаксимальнаяЗагрузка <> 0
			|					ТОГДА ВидыРЦ.МаксимальнаяЗагрузка
			|				ИНАЧЕ 1
			|			КОНЕЦ            КАК МаксимумВремяЗагрузка,
			|	ЛОЖЬ                     КАК ЭтоИндекс
			|ИЗ
			|	ВТСправочникВидыРЦ КАК ВидыРЦ
			|	
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДниЗаПериод КАК ДниЗаПериод
			|	ПО ИСТИНА
			|	
			|	ЛЕВОЕ СОЕДИНЕНИЕ ВТСчетчикЧасов КАК СчетчикЧасов
			|	ПО ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
			|
			|СГРУППИРОВАТЬ ПО
			|	ВидыРЦ.ВидРабочегоЦентра,
			|	ВидыРЦ.УчитыватьДоступность,
			|	ВидыРЦ.ИнтервалПланирования,
			|	ВидыРЦ.КоличествоРабочихЦентров,
			|	ВидыРЦ.ПараллельнаяЗагрузка,
			|	ВидыРЦ.МаксимальнаяЗагрузка,
			|	ВЫБОР ВидыРЦ.ИнтервалПланирования
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
			|			ТОГДА ДниЗаПериод.Дата
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
			|			ТОГДА ДниЗаПериод.Дата
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, НЕДЕЛЯ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, МЕСЯЦ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
			|			ТОГДА ДОБАВИТЬКДАТЕ(ДниЗаПериод.Дата, ЧАС, СчетчикЧасов.НомерЧаса)
			|	КОНЕЦ,
			|	ВЫБОР ВидыРЦ.ИнтервалПланирования
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
			|			ТОГДА ДОБАВИТЬКДАТЕ(ДниЗаПериод.Дата, ДЕНЬ, 1)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
			|			ТОГДА ДОБАВИТЬКДАТЕ(ДниЗаПериод.Дата, ДЕНЬ, 1)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, НЕДЕЛЯ), НЕДЕЛЯ, 1)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, МЕСЯЦ), МЕСЯЦ, 1)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
			|			ТОГДА ДОБАВИТЬКДАТЕ(ДниЗаПериод.Дата, ЧАС, СчетчикЧасов.НомерЧаса + 1)
			|	КОНЕЦ
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|// Индекс для поиска записей
			|ВЫБРАТЬ
			|	ВидыРЦ.ВидРабочегоЦентра                    КАК ВидРабочегоЦентра,
			|	ВЫБОР ВидыРЦ.ИнтервалПланирования
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, НЕДЕЛЯ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, МЕСЯЦ)
			|		ИНАЧЕ ДниЗаПериод.Дата
			|	КОНЕЦ                                       КАК ДатаИнтервала,
			|	ДАТАВРЕМЯ(1,1,1,1,1,1)                      КАК НачалоИнтервала,
			|	ДАТАВРЕМЯ(1,1,1,1,1,1)                      КАК ОкончаниеИнтервала,
			|	ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка) КАК Смена,
			|	0                                           КАК ДоступноВремяЗагрузка,
			|	0                                           КАК ДоступноВремя,
			|	0                                           КАК МаксимумВремяЗагрузка,
			|	ИСТИНА                                      КАК ЭтоИндекс
			|ИЗ
			|	ВТСправочникВидыРЦ КАК ВидыРЦ
			|	
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДниЗаПериод КАК ДниЗаПериод
			|	ПО ИСТИНА
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДоступностьВРЦ.ВидРабочегоЦентра     КАК ВидРабочегоЦентра,
			|	ДоступностьВРЦ.ДатаИнтервала         КАК ДатаИнтервала,
			|	ДоступностьВРЦ.НачалоИнтервала       КАК НачалоИнтервала,
			|	ДоступностьВРЦ.ОкончаниеИнтервала    КАК ОкончаниеИнтервала,
			|	ДоступностьВРЦ.Смена                 КАК Смена,
			|	ДоступностьВРЦ.ДоступноВремяЗагрузка КАК ДоступноВремяЗагрузка,
			|	ДоступностьВРЦ.ДоступноВремя         КАК ДоступноВремя,
			|	ДоступностьВРЦ.МаксимумВремяЗагрузка КАК МаксимумВремяЗагрузка,
			|	ДоступностьВРЦ.ЭтоИндекс             КАК ЭтоИндекс
			|ИЗ
			|	ВТДоступностьВРЦТекущая КАК ДоступностьВРЦ
			|
			|УПОРЯДОЧИТЬ ПО
			|	ВидРабочегоЦентра,
			|	ДатаИнтервала,
			|	ЭтоИндекс УБЫВ";
		
	Иначе
		
		ТекстЗапроса = 
			"// Виды РЦ с ограничением доступности
			|ВЫБРАТЬ
			|	Доступность.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
			|	Доступность.ДатаИнтервала     КАК ДатаИнтервала,
			|	ВЫБОР
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.ДЕНЬ)
			|				И НАЧАЛОПЕРИОДА(ВидыРЦ.НачалоИнтервалаПланирования, ДЕНЬ) <> ВидыРЦ.НачалоИнтервалаПланирования
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, МИНУТА, 
			|					ЧАС(ВидыРЦ.НачалоИнтервалаПланирования) * 60
			|					+ МИНУТА(ВидыРЦ.НачалоИнтервалаПланирования))
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.НЕДЕЛЯ)
			|				И НАЧАЛОПЕРИОДА(ВидыРЦ.НачалоИнтервалаПланирования, НЕДЕЛЯ) <> ВидыРЦ.НачалоИнтервалаПланирования
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, МИНУТА,
			|					(ДЕНЬ(ВидыРЦ.НачалоИнтервалаПланирования)-1) * 1440
			|					+ ЧАС(ВидыРЦ.НачалоИнтервалаПланирования) * 60
			|					+ МИНУТА(ВидыРЦ.НачалоИнтервалаПланирования))
			|		ИНАЧЕ Доступность.ДатаИнтервала
			|	КОНЕЦ                         КАК НачалоИнтервала,
			|	ВЫБОР
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.ДЕНЬ)
			|				И КОНЕЦПЕРИОДА(ВидыРЦ.ОкончаниеИнтервалаПланирования, ДЕНЬ) <> ВидыРЦ.ОкончаниеИнтервалаПланирования
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, МИНУТА, 
			|					ЧАС(ВидыРЦ.ОкончаниеИнтервалаПланирования) * 60
			|					+ МИНУТА(ВидыРЦ.ОкончаниеИнтервалаПланирования))
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.НЕДЕЛЯ)
			|				И КОНЕЦПЕРИОДА(ВидыРЦ.ОкончаниеИнтервалаПланирования, НЕДЕЛЯ) <> ВидыРЦ.ОкончаниеИнтервалаПланирования
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, МИНУТА,
			|					(ДЕНЬ(ВидыРЦ.ОкончаниеИнтервалаПланирования)-1) * 1440
			|					+ ЧАС(ВидыРЦ.ОкончаниеИнтервалаПланирования) * 60
			|					+ МИНУТА(ВидыРЦ.ОкончаниеИнтервалаПланирования))
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.ДЕНЬ)
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, ДЕНЬ, 1)
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.СМЕНА)
			|			ТОГДА ЕСТЬNULL(ГраницыСмен.ОкончаниеИнтервала, КОНЕЦПЕРИОДА(Доступность.ДатаИнтервала, ДЕНЬ))
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.НЕДЕЛЯ)
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, НЕДЕЛЯ, 1)
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.МЕСЯЦ)
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, МЕСЯЦ, 1)
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.ЧАС)
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, ЧАС, 1)
			|		ИНАЧЕ Доступность.ДатаИнтервала
			|	КОНЕЦ                         КАК ОкончаниеИнтервала,
			|	Доступность.Смена             КАК Смена,
			|	ВЫБОР
			|		КОГДА &ЗадействоватьРезервДоступности
			|			ТОГДА СУММА(ВЫБОР
			|							КОГДА ВидыРЦ.ВводитьДоступностьДляВидаРЦ
			|								ТОГДА Доступность.ДоступностьПоВидуРЦ
			|							ИНАЧЕ Доступность.ДоступностьПоРЦ
			|						КОНЕЦ) - СУММА(Доступность.Занято)
			|		ИНАЧЕ 
			|			ВЫРАЗИТЬ(
			|				СУММА(ВЫБОР
			|							КОГДА ВидыРЦ.ВводитьДоступностьДляВидаРЦ
			|								ТОГДА Доступность.ДоступностьПоВидуРЦ
			|							ИНАЧЕ Доступность.ДоступностьПоРЦ
			|						КОНЕЦ) * (100 - МАКСИМУМ(ВидыРЦ.РезервДоступности)) / 100
			|				КАК ЧИСЛО(15, 0))
			|			- СУММА(Доступность.Занято)
			|	КОНЕЦ * ВЫБОР
			|				КОГДА ВидыРЦ.ПараллельнаяЗагрузка
			|						И ВидыРЦ.МаксимальнаяЗагрузка <> 0
			|					ТОГДА ВидыРЦ.МаксимальнаяЗагрузка
			|				ИНАЧЕ 1
			|			КОНЕЦ                 КАК ДоступноВремяЗагрузка,
			|	ВЫБОР
			|		КОГДА &ЗадействоватьРезервДоступности
			|			ТОГДА СУММА(ВЫБОР
			|							КОГДА ВидыРЦ.ВводитьДоступностьДляВидаРЦ
			|								ТОГДА Доступность.ДоступностьПоВидуРЦ
			|							ИНАЧЕ Доступность.ДоступностьПоРЦ
			|						КОНЕЦ) - СУММА(Доступность.Занято)
			|		ИНАЧЕ 
			|			ВЫРАЗИТЬ(
			|				СУММА(ВЫБОР
			|							КОГДА ВидыРЦ.ВводитьДоступностьДляВидаРЦ
			|								ТОГДА Доступность.ДоступностьПоВидуРЦ
			|							ИНАЧЕ Доступность.ДоступностьПоРЦ
			|						КОНЕЦ) * (100 - МАКСИМУМ(ВидыРЦ.РезервДоступности)) / 100
			|				КАК ЧИСЛО(15, 0))
			|			- СУММА(Доступность.Занято)
			|	КОНЕЦ                         КАК ДоступноВремя,
			|	СУММА(ВЫБОР
			|			КОГДА ВидыРЦ.ВводитьДоступностьДляВидаРЦ
			|				ТОГДА Доступность.МаксимальнаяДоступностьПоВидуРЦ
			|			ИНАЧЕ Доступность.МаксимальнаяДоступность
			|		КОНЕЦ) * ВЫБОР
			|				КОГДА ВидыРЦ.ПараллельнаяЗагрузка
			|						И ВидыРЦ.МаксимальнаяЗагрузка <> 0
			|					ТОГДА ВидыРЦ.МаксимальнаяЗагрузка
			|				ИНАЧЕ 1
			|			КОНЕЦ                 КАК МаксимумВремяЗагрузка,
			|	ЛОЖЬ                          КАК ЭтоИндекс
			|ИЗ
			|	РегистрНакопления.ДоступностьВидовРабочихЦентров КАК Доступность
			|	
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСправочникВидыРЦ КАК ВидыРЦ
			|	ПО Доступность.ВидРабочегоЦентра = ВидыРЦ.ВидРабочегоЦентра
			|	
			|	ЛЕВОЕ СОЕДИНЕНИЕ ВТПриоритетныеЗаказы КАК ПриоритетныеЗаказы
			|	ПО Доступность.Распоряжение = ПриоритетныеЗаказы.Ссылка
			|	
			|	ЛЕВОЕ СОЕДИНЕНИЕ ВТГраницыСмен КАК ГраницыСмен
			|	ПО Доступность.ВидРабочегоЦентра = ГраницыСмен.ВидРабочегоЦентра
			|		И Доступность.ДатаИнтервала = ГраницыСмен.ДатаИнтервала
			|
			|ГДЕ
			|	ВидыРЦ.УчитыватьДоступность
			|	И Доступность.ДатаИнтервала >= &Начало
			|	И Доступность.ДатаИнтервала < &Окончание
			|	И (Доступность.ЭтоДвижениеВводаДоступности
			|		ИЛИ ПриоритетныеЗаказы.Ссылка ЕСТЬ НЕ NULL
			|		ИЛИ Доступность.Регистратор ССЫЛКА Документ.ЭтапПроизводства2_2
			|			И ВЫРАЗИТЬ(Доступность.Регистратор КАК Документ.ЭтапПроизводства2_2).Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат)
			|		ИЛИ Доступность.Регистратор ССЫЛКА Документ.ЭтапПроизводства2_2
			|			И Доступность.Распоряжение = &Заказ
			|			И ВЫРАЗИТЬ(Доступность.Регистратор КАК Документ.ЭтапПроизводства2_2).РучноеРазмещениеВГрафике
			|			И НЕ &ОтменитьРучныеИзмененияГрафика
			|		ИЛИ Доступность.Регистратор ССЫЛКА Документ.ЭтапПроизводства2_2
			|			И Доступность.Распоряжение <> &Заказ
			|			И ВЫРАЗИТЬ(Доступность.Регистратор КАК Документ.ЭтапПроизводства2_2).РучноеРазмещениеВГрафике
			|		ИЛИ &УчитыватьЗагрузкуЗапущенныхПартий)
			|	И Доступность.Активность
			|
			|СГРУППИРОВАТЬ ПО
			|	Доступность.ВидРабочегоЦентра,
			|	Доступность.ДатаИнтервала,
			|	ВидыРЦ.ПараллельнаяЗагрузка,
			|	ВидыРЦ.МаксимальнаяЗагрузка,
			|	ВЫБОР
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.ДЕНЬ)
			|				И НАЧАЛОПЕРИОДА(ВидыРЦ.НачалоИнтервалаПланирования, ДЕНЬ) <> ВидыРЦ.НачалоИнтервалаПланирования
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, МИНУТА, 
			|					ЧАС(ВидыРЦ.НачалоИнтервалаПланирования) * 60
			|					+ МИНУТА(ВидыРЦ.НачалоИнтервалаПланирования))
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.НЕДЕЛЯ)
			|				И НАЧАЛОПЕРИОДА(ВидыРЦ.НачалоИнтервалаПланирования, НЕДЕЛЯ) <> ВидыРЦ.НачалоИнтервалаПланирования
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, МИНУТА,
			|					(ДЕНЬ(ВидыРЦ.НачалоИнтервалаПланирования)-1) * 1440
			|					+ ЧАС(ВидыРЦ.НачалоИнтервалаПланирования) * 60
			|					+ МИНУТА(ВидыРЦ.НачалоИнтервалаПланирования))
			|		ИНАЧЕ Доступность.ДатаИнтервала
			|	КОНЕЦ,
			|	ВЫБОР
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.ДЕНЬ)
			|				И КОНЕЦПЕРИОДА(ВидыРЦ.ОкончаниеИнтервалаПланирования, ДЕНЬ) <> ВидыРЦ.ОкончаниеИнтервалаПланирования
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, МИНУТА, 
			|					ЧАС(ВидыРЦ.ОкончаниеИнтервалаПланирования) * 60
			|					+ МИНУТА(ВидыРЦ.ОкончаниеИнтервалаПланирования))
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.НЕДЕЛЯ)
			|				И КОНЕЦПЕРИОДА(ВидыРЦ.ОкончаниеИнтервалаПланирования, НЕДЕЛЯ) <> ВидыРЦ.ОкончаниеИнтервалаПланирования
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, МИНУТА,
			|					(ДЕНЬ(ВидыРЦ.ОкончаниеИнтервалаПланирования)-1) * 1440
			|					+ ЧАС(ВидыРЦ.ОкончаниеИнтервалаПланирования) * 60
			|					+ МИНУТА(ВидыРЦ.ОкончаниеИнтервалаПланирования))
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.ДЕНЬ)
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, ДЕНЬ, 1)
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.СМЕНА)
			|			ТОГДА ЕСТЬNULL(ГраницыСмен.ОкончаниеИнтервала, КОНЕЦПЕРИОДА(Доступность.ДатаИнтервала, ДЕНЬ))
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.НЕДЕЛЯ)
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, НЕДЕЛЯ, 1)
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.МЕСЯЦ)
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, МЕСЯЦ, 1)
			|		КОГДА ВидыРЦ.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.ЧАС)
			|			ТОГДА ДОБАВИТЬКДАТЕ(Доступность.ДатаИнтервала, ЧАС, 1)
			|		ИНАЧЕ Доступность.ДатаИнтервала
			|	КОНЕЦ,
			|	Доступность.Смена
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|// Виды РЦ без ограничения доступности (День, Неделя, Месяц)
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ГрафикиРаботы.ВидРабочегоЦентра             КАК ВидРабочегоЦентра,
			|	ВЫБОР ГрафикиРаботы.ИнтервалПланирования
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
			|			ТОГДА НАЧАЛОПЕРИОДА(Расписание.ДатаГрафика, ДЕНЬ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА НАЧАЛОПЕРИОДА(Расписание.ДатаГрафика, НЕДЕЛЯ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА НАЧАЛОПЕРИОДА(Расписание.ДатаГрафика, МЕСЯЦ)
			|	КОНЕЦ                                       КАК ДатаИнтервала,
			|	ВЫБОР
			|		КОГДА ГрафикиРаботы.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.ДЕНЬ)
			|				И НАЧАЛОПЕРИОДА(ГрафикиРаботы.НачалоИнтервалаПланирования, ДЕНЬ) <> ГрафикиРаботы.НачалоИнтервалаПланирования
			|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Расписание.ДатаГрафика, ДЕНЬ), МИНУТА, 
			|					ЧАС(ГрафикиРаботы.НачалоИнтервалаПланирования) * 60
			|					+ МИНУТА(ГрафикиРаботы.НачалоИнтервалаПланирования))
			|		КОГДА ГрафикиРаботы.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.НЕДЕЛЯ)
			|				И НАЧАЛОПЕРИОДА(ГрафикиРаботы.НачалоИнтервалаПланирования, НЕДЕЛЯ) <> ГрафикиРаботы.НачалоИнтервалаПланирования
			|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Расписание.ДатаГрафика, НЕДЕЛЯ), МИНУТА,
			|					(ДЕНЬ(ГрафикиРаботы.НачалоИнтервалаПланирования)-1) * 1440
			|					+ ЧАС(ГрафикиРаботы.НачалоИнтервалаПланирования) * 60
			|					+ МИНУТА(ГрафикиРаботы.НачалоИнтервалаПланирования))
			|		КОГДА ГрафикиРаботы.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.ДЕНЬ)
			|			ТОГДА НАЧАЛОПЕРИОДА(Расписание.ДатаГрафика, ДЕНЬ)
			|		КОГДА ГрафикиРаботы.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.НЕДЕЛЯ)
			|			ТОГДА НАЧАЛОПЕРИОДА(Расписание.ДатаГрафика, НЕДЕЛЯ)
			|		КОГДА ГрафикиРаботы.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА НАЧАЛОПЕРИОДА(Расписание.ДатаГрафика, МЕСЯЦ)
			|	КОНЕЦ                                       КАК НачалоИнтервала,
			|	ВЫБОР
			|		КОГДА ГрафикиРаботы.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.ДЕНЬ)
			|				И КОНЕЦПЕРИОДА(ГрафикиРаботы.ОкончаниеИнтервалаПланирования, ДЕНЬ) <> ГрафикиРаботы.ОкончаниеИнтервалаПланирования
			|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Расписание.ДатаГрафика, ДЕНЬ), МИНУТА, 
			|					ЧАС(ГрафикиРаботы.ОкончаниеИнтервалаПланирования) * 60
			|					+ МИНУТА(ГрафикиРаботы.ОкончаниеИнтервалаПланирования))
			|		КОГДА ГрафикиРаботы.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.НЕДЕЛЯ)
			|				И КОНЕЦПЕРИОДА(ГрафикиРаботы.ОкончаниеИнтервалаПланирования, НЕДЕЛЯ) <> ГрафикиРаботы.ОкончаниеИнтервалаПланирования
			|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Расписание.ДатаГрафика, НЕДЕЛЯ), МИНУТА,
			|					(ДЕНЬ(ГрафикиРаботы.ОкончаниеИнтервалаПланирования)-1) * 1440
			|					+ ЧАС(ГрафикиРаботы.ОкончаниеИнтервалаПланирования) * 60
			|					+ МИНУТА(ГрафикиРаботы.ОкончаниеИнтервалаПланирования))
			|		КОГДА ГрафикиРаботы.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.ДЕНЬ)
			|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Расписание.ДатаГрафика, ДЕНЬ), ДЕНЬ, 1)
			|		КОГДА ГрафикиРаботы.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.НЕДЕЛЯ)
			|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Расписание.ДатаГрафика, НЕДЕЛЯ), НЕДЕЛЯ, 1)
			|		КОГДА ГрафикиРаботы.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.МЕСЯЦ)
			|			ТОГДА ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Расписание.ДатаГрафика, МЕСЯЦ), МЕСЯЦ, 1)
			|	КОНЕЦ                                       КАК ОкончаниеИнтервала,
			|	ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка) КАК Смена,
			|	100000000                                   КАК ДоступноВремяЗагрузка,
			|	100000000                                   КАК ДоступноВремя,
			|	ГрафикиРаботы.МаксимальнаяДоступность
			|		* ВЫБОР
			|			КОГДА ГрафикиРаботы.ПараллельнаяЗагрузка
			|					И ГрафикиРаботы.МаксимальнаяЗагрузка <> 0
			|				ТОГДА ГрафикиРаботы.МаксимальнаяЗагрузка
			|			ИНАЧЕ 1
			|		КОНЕЦ                                   КАК МаксимумВремяЗагрузка,
			|	ЛОЖЬ                                        КАК ЭтоИндекс
			|ИЗ
			|	ВТГрафикиВидовРЦ КАК ГрафикиРаботы
			|	
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасписанияРаботы КАК Расписание
			|	ПО ГрафикиРаботы.ГрафикРаботы = Расписание.ГрафикРаботы
			|ГДЕ
			|	НЕ ГрафикиРаботы.УчитыватьДоступность
			|	И ГрафикиРаботы.ИнтервалПланирования В (
			|		ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День),
			|		ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя),
			|		ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц))
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|// Виды РЦ без ограничения доступности (Смена)
			|ВЫБРАТЬ
			|	ГраницыСмен.ВидРабочегоЦентра  КАК ВидРабочегоЦентра,
			|	ГраницыСмен.ДатаИнтервала      КАК ДатаИнтервала,
			|	ГраницыСмен.ДатаИнтервала      КАК НачалоИнтервала,
			|	ГраницыСмен.ОкончаниеИнтервала КАК ОкончаниеИнтервала,
			|	ГраницыСмен.Смена              КАК Смена,
			|	100000000                      КАК ДоступноВремяЗагрузка,
			|	100000000                      КАК ДоступноВремя,
			|	ВидыРЦ.МаксимальнаяДоступность
			|		* ВЫБОР
			|			КОГДА ВидыРЦ.ПараллельнаяЗагрузка
			|					И ВидыРЦ.МаксимальнаяЗагрузка <> 0
			|				ТОГДА ВидыРЦ.МаксимальнаяЗагрузка
			|			ИНАЧЕ 1
			|		КОНЕЦ                      КАК МаксимумВремяЗагрузка,
			|	ЛОЖЬ                           КАК ЭтоИндекс
			|ИЗ
			|	ВТГраницыСмен КАК ГраницыСмен
			|	
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСправочникВидыРЦ КАК ВидыРЦ
			|	ПО ГраницыСмен.ВидРабочегоЦентра = ВидыРЦ.ВидРабочегоЦентра
			|ГДЕ
			|	НЕ ВидыРЦ.УчитыватьДоступность
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|// Виды РЦ без ограничения доступности (Час)
			|ВЫБРАТЬ
			|	ВидыРЦЧас.ВидРабочегоЦентра                 КАК ВидРабочегоЦентра,
			|	ДОБАВИТЬКДАТЕ(ВидыРЦЧас.ДатаГрафика, ЧАС, СчетчикЧасов.НомерЧаса)     КАК ДатаИнтервала,
			|	ДОБАВИТЬКДАТЕ(ВидыРЦЧас.ДатаГрафика, ЧАС, СчетчикЧасов.НомерЧаса)     КАК НачалоИнтервала,
			|	ДОБАВИТЬКДАТЕ(ВидыРЦЧас.ДатаГрафика, ЧАС, СчетчикЧасов.НомерЧаса + 1) КАК ОкончаниеИнтервала,
			|	ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка) КАК Смена,
			|	100000000                                   КАК ДоступноВремяЗагрузка,
			|	100000000                                   КАК ДоступноВремя,
			|	ВидыРЦ.МаксимальнаяДоступность
			|		* ВЫБОР
			|			КОГДА ВидыРЦ.ПараллельнаяЗагрузка
			|					И ВидыРЦ.МаксимальнаяЗагрузка <> 0
			|				ТОГДА ВидыРЦ.МаксимальнаяЗагрузка
			|			ИНАЧЕ 1
			|		КОНЕЦ                                   КАК МаксимумВремяЗагрузка,
			|	ЛОЖЬ                                        КАК ЭтоИндекс
			|ИЗ
			|	ВТВидыРЦБезУчетаДоступностиЧас КАК ВидыРЦЧас
			|	
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСчетчикЧасов КАК СчетчикЧасов
			|	ПО СчетчикЧасов.НомерЧаса МЕЖДУ ВидыРЦЧас.ЧасНачало И ВидыРЦЧас.ЧасОкончание
			|	
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСправочникВидыРЦ КАК ВидыРЦ
			|	ПО ВидыРЦЧас.ВидРабочегоЦентра = ВидыРЦ.ВидРабочегоЦентра
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|// Индекс для поиска записей
			|ВЫБРАТЬ
			|	ВидыРЦ.ВидРабочегоЦентра                    КАК ВидРабочегоЦентра,
			|	ВЫБОР ВидыРЦ.ИнтервалПланирования
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, НЕДЕЛЯ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, МЕСЯЦ)
			|		ИНАЧЕ ДниЗаПериод.Дата
			|	КОНЕЦ                                       КАК ДатаИнтервала,
			|	ДАТАВРЕМЯ(1,1,1,1,1,1)                      КАК НачалоИнтервала,
			|	ДАТАВРЕМЯ(1,1,1,1,1,1)                      КАК ОкончаниеИнтервала,
			|	ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка) КАК Смена,
			|	0                                           КАК ДоступноВремяЗагрузка,
			|	0                                           КАК ДоступноВремя,
			|	0                                           КАК МаксимумВремяЗагрузка,
			|	ИСТИНА                                      КАК ЭтоИндекс
			|ИЗ
			|	ВТСправочникВидыРЦ КАК ВидыРЦ
			|	
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДниЗаПериод КАК ДниЗаПериод
			|	ПО ИСТИНА
			|
			|СГРУППИРОВАТЬ ПО
			|	ВидыРЦ.ВидРабочегоЦентра,
			|	ВЫБОР ВидыРЦ.ИнтервалПланирования
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, НЕДЕЛЯ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДниЗаПериод.Дата, МЕСЯЦ)
			|		ИНАЧЕ ДниЗаПериод.Дата
			|	КОНЕЦ
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДоступностьВРЦ.ВидРабочегоЦентра     КАК ВидРабочегоЦентра,
			|	ДоступностьВРЦ.ДатаИнтервала         КАК ДатаИнтервала,
			|	ДоступностьВРЦ.НачалоИнтервала       КАК НачалоИнтервала,
			|	ДоступностьВРЦ.ОкончаниеИнтервала    КАК ОкончаниеИнтервала,
			|	ДоступностьВРЦ.Смена                 КАК Смена,
			|	ДоступностьВРЦ.ДоступноВремяЗагрузка КАК ДоступноВремяЗагрузка,
			|	ДоступностьВРЦ.ДоступноВремя         КАК ДоступноВремя,
			|	ДоступностьВРЦ.МаксимумВремяЗагрузка КАК МаксимумВремяЗагрузка,
			|	ДоступностьВРЦ.ЭтоИндекс             КАК ЭтоИндекс
			|ИЗ
			|	ВТДоступностьВРЦТекущая КАК ДоступностьВРЦ
			|
			|УПОРЯДОЧИТЬ ПО
			|	ВидРабочегоЦентра,
			|	ДатаИнтервала,
			|	ЭтоИндекс УБЫВ";
		
		Если ПараметрыРасчета.ДоступностьУчитыватьЗагрузкуЗапущенныхПартий Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"&УчитыватьЗагрузкуЗапущенныхПартий",
				"Доступность.Регистратор ССЫЛКА Документ.ЭтапПроизводства2_2 И Доступность.Распоряжение = &Заказ");
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"&УчитыватьЗагрузкуЗапущенныхПартий",
				"ЛОЖЬ");
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ДоступностьВРЦ");
	
	#КонецОбласти
	
	Если НЕ ЭтоДополнительноеЧтение Тогда
		
		#Область ВТГраницыГрафиков
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	КалендарныеГрафики.Календарь             КАК ГрафикРаботы,
			|	МАКСИМУМ(КалендарныеГрафики.ДатаГрафика) КАК Граница
			|ПОМЕСТИТЬ ВТГраницыГрафиков
			|ИЗ
			|	РегистрСведений.КалендарныеГрафики КАК КалендарныеГрафики
			|ГДЕ
			|	КалендарныеГрафики.Календарь В (
			|		ВЫБРАТЬ РАЗЛИЧНЫЕ
			|			Т.ГрафикРаботы КАК Календарь
			|		ИЗ
			|			ВТГрафикиВидовРЦ КАК Т
			|		ГДЕ
			|			НЕ Т.УчитыватьДоступность
			|		
			|		ОБЪЕДИНИТЬ ВСЕ
			|		
			|		ВЫБРАТЬ РАЗЛИЧНЫЕ
			|			Т.ГрафикРаботы КАК Календарь
			|		ИЗ
			|			ВТГрафикиПодразделений КАК Т)
			|	И КалендарныеГрафики.ДеньВключенВГрафик
			|	
			|СГРУППИРОВАТЬ ПО
			|	КалендарныеГрафики.Календарь
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ГрафикРаботы";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ВТГраницыГрафиков");
		
		#КонецОбласти
		
		#Область ГраницыДоступности
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ДоступностьОбороты.ВидРабочегоЦентра       КАК Ссылка,
			|	МАКСИМУМ(ДоступностьОбороты.ДатаИнтервала) КАК Граница
			|ИЗ
			|	РегистрНакопления.ДоступностьВидовРабочихЦентров.Обороты(,,, &ТекстЗапросаОтборВидыРЦ) КАК ДоступностьОбороты
			|
			|СГРУППИРОВАТЬ ПО
			|	ДоступностьОбороты.ВидРабочегоЦентра
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Графики.ВидРабочегоЦентра         КАК Ссылка,
			|	МАКСИМУМ(ГраницыГрафиков.Граница) КАК Граница
			|ИЗ
			|	ВТГрафикиВидовРЦ КАК Графики
			|	
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТГраницыГрафиков КАК ГраницыГрафиков
			|	ПО Графики.ГрафикРаботы = ГраницыГрафиков.ГрафикРаботы
			|ГДЕ
			|	НЕ Графики.УчитыватьДоступность
			|
			|СГРУППИРОВАТЬ ПО
			|	Графики.ВидРабочегоЦентра
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Графики.Подразделение             КАК Ссылка,
			|	МАКСИМУМ(ГраницыГрафиков.Граница) КАК Граница
			|ИЗ
			|	ВТГрафикиПодразделений КАК Графики
			|	
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТГраницыГрафиков КАК ГраницыГрафиков
			|	ПО Графики.ГрафикРаботы = ГраницыГрафиков.ГрафикРаботы
			|
			|СГРУППИРОВАТЬ ПО
			|	Графики.Подразделение";
		
		Если ПараметрыРасчета.ДоступностьОтборВРЦ.Количество() = 0 Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"&ТекстЗапросаОтборВидыРЦ",
				"ИСТИНА");
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"&ТекстЗапросаОтборВидыРЦ",
				"ВидРабочегоЦентра В (&ВидыРЦ)");
		КонецЕсли;
		
		ТекстыЗапроса.Добавить(ТекстЗапроса + РазделительЗапросовВПакете, "ГраницыДоступности");
		
		#КонецОбласти
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Таблицы = ОбщегоНазначенияУТ.ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса,, Ложь, Ложь);
	УстановитьПривилегированныйРежим(Ложь);
	
	РасписаниеПодразделенийДень = Таблицы.РасписаниеПодразделенийДень; // ТаблицаЗначений
	РасписаниеПодразделенийДень.Индексы.Добавить("Подразделение, Дата");
	ДанныеРасчета.РасписаниеПодразделенийДень = РасписаниеПодразделенийДень;
	
	РасписаниеПодразделенийСекунда = Таблицы.РасписаниеПодразделенийСекунда; // ТаблицаЗначений
	РасписаниеПодразделенийСекунда.Индексы.Добавить("Подразделение, Дата");
	ДанныеРасчета.РасписаниеПодразделенийСекунда = РасписаниеПодразделенийСекунда;
	
	ДоступностьВРЦ = Таблицы.ДоступностьВРЦ; // ТаблицаЗначений
	ДоступностьВРЦ.Индексы.Добавить("ВидРабочегоЦентра, ДатаИнтервала, ЭтоИндекс");
	ДанныеРасчета.ДоступностьВРЦ = ДоступностьВРЦ;
	
	Если НЕ ЭтоДополнительноеЧтение Тогда
		ГраницыДоступности = Таблицы.ГраницыДоступности; // ТаблицаЗначений
		ГраницыДоступности.Индексы.Добавить("Ссылка");
		ДанныеРасчета.ГраницыДоступности = ГраницыДоступности;
	КонецЕсли;
	
КонецПроцедуры

Функция ДочитатьДоступностьИНайтиИнтервал(ДанныеРасчета, ПараметрыРасчета, Ссылка, ИмяТаблицы, СтруктураПоиска, Дата)
	
	ГраницаДоступности = ДанныеРасчета.ГраницыДоступности.Найти(Ссылка, "Ссылка");
	Если ГраницаДоступности = Неопределено
		ИЛИ ГраницаДоступности.Граница < Дата Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Граница = Мин(КонецМесяца(ДобавитьМесяц(Дата, 2)), ГраницаДоступности.Граница);
	ПрочитатьДоступность(ПараметрыРасчета.Заказ, ДанныеРасчета, ПараметрыРасчета, Граница);
	
	НайденныеСтроки = ДанныеРасчета[ИмяТаблицы].НайтиСтроки(СтруктураПоиска);
	
	Возврат НайденныеСтроки;
	
КонецФункции

Функция ДочитатьДоступность(ДанныеРасчета, ПараметрыРасчета, Интервал, ИмяТаблицы)
	
	СтруктураПоиска = Новый Структура;
	ИмяПоляСсылка = "";
	Если ИмяТаблицы = "РасписаниеПодразделенийДень"
		Или ИмяТаблицы = "РасписаниеПодразделенийСекунда" Тогда
		СтруктураПоиска.Вставить("Подразделение");
		СтруктураПоиска.Вставить("Дата");
		СтруктураПоиска.Вставить("Начало");
		СтруктураПоиска.Вставить("Окончание");
		СтруктураПоиска.Вставить("ЭтоИндекс");
		ИмяПоляСсылка = "Подразделение";
	ИначеЕсли ИмяТаблицы = "ДоступностьВРЦ" Тогда
		СтруктураПоиска.Вставить("ВидРабочегоЦентра");
		СтруктураПоиска.Вставить("ДатаИнтервала");
		СтруктураПоиска.Вставить("НачалоИнтервала");
		СтруктураПоиска.Вставить("ОкончаниеИнтервала");
		СтруктураПоиска.Вставить("ЭтоИндекс");
		ИмяПоляСсылка = "ВидРабочегоЦентра"
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, Интервал);
	
	ГраницаДоступности = ДанныеРасчета.ГраницыДоступности.Найти(Интервал[ИмяПоляСсылка], "Ссылка");
	Если ГраницаДоступности = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Пока ГраницаДоступности.Граница > ПараметрыРасчета.ДоступностьГраницаВыборки Цикл
		ПрочитатьДоступность(ПараметрыРасчета.Заказ, ДанныеРасчета, ПараметрыРасчета);
		
		НайденныеСтроки = ДанныеРасчета[ИмяТаблицы].НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Интервал = НайденныеСтроки[0]; // Текущий интервал в новой таблице может использоваться в вызывающем методе
		Индекс = ДанныеРасчета[ИмяТаблицы].Индекс(Интервал) + 1;
		
		Пока Индекс < ДанныеРасчета[ИмяТаблицы].Количество() Цикл
			Если ДанныеРасчета[ИмяТаблицы][Индекс][ИмяПоляСсылка] <> Интервал[ИмяПоляСсылка] Тогда
				Прервать;
			ИначеЕсли Не ДанныеРасчета[ИмяТаблицы][Индекс].ЭтоИндекс Тогда
				Возврат Истина;
			КонецЕсли;
			Индекс = Индекс + 1;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция ИнтервалВыборкиДоступности(Заказ)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Заказ.НачатьНеРанее   КАК НачатьНеРанее,
		|	Заказ.ДатаПотребности КАК ДатаПотребности,
		|	ВЫБОР
		|		КОГДА Заказ.ДатаПотребности = ДАТАВРЕМЯ(1,1,1)
		|			ТОГДА 0
		|		ИНАЧЕ РАЗНОСТЬДАТ(Заказ.НачатьНеРанее, Заказ.ДатаПотребности, МЕСЯЦ)
		|	КОНЕЦ                 КАК РазностьДатВМесяцах
		|ИЗ
		|	Документ.ЗаказНаПроизводство2_2 КАК Заказ
		|ГДЕ
		|	Заказ.Ссылка = &Заказ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(СтруктураПредприятия.ИнтервалПланирования
		|		= ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц))  КАК ЕстьМесяц,
		|	МАКСИМУМ(СтруктураПредприятия.ИнтервалПланирования
		|		= ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)) КАК ЕстьНеделя,
		|	МАКСИМУМ(СтруктураПредприятия.ИнтервалПланирования
		|		= ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена))  КАК ЕстьСмена,
		|	МАКСИМУМ(СтруктураПредприятия.ИнтервалПланирования
		|		= ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час))    КАК ЕстьЧас
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	НЕ СтруктураПредприятия.ПометкаУдаления
		|	И СтруктураПредприятия.ПроизводственноеПодразделение
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(СтруктураПредприятия.Ссылка) > 0");
	Запрос.УстановитьПараметр("Заказ", Заказ);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаЗаказ = РезультатыЗапроса[0].Выбрать();
	ВыборкаЗаказ.Следующий();
	ВыборкаПодразделения = РезультатыЗапроса[1].Выбрать();
	
	Начало = ?(ВыборкаЗаказ.НачатьНеРанее < ТекущаяДатаСеанса(),
		НачалоДня(ТекущаяДатаСеанса()),
		НачалоДня(ВыборкаЗаказ.НачатьНеРанее));
	ЕстьИнтервалЧас = Ложь;
	
	Если ВыборкаПодразделения.Следующий() Тогда
		НачалоМесяца = ?(ВыборкаПодразделения.ЕстьМесяц, НачалоМесяца(Начало), Начало);
		НачалоНедели = ?(ВыборкаПодразделения.ЕстьНеделя, НачалоНедели(Начало), Начало);
		НачалоСмены = ?(ВыборкаПодразделения.ЕстьСмена, НачалоДня(Начало - 86400), Начало);
		Начало = Мин(НачалоМесяца, НачалоНедели, НачалоСмены);
		
		ЕстьИнтервалЧас = ВыборкаПодразделения.ЕстьЧас;
	КонецЕсли;
	
	Окончание = '00010101';
	Если Начало > ВыборкаЗаказ.ДатаПотребности Тогда
		Окончание = КонецМесяца(ДобавитьМесяц(Начало, 3));
	ИначеЕсли ЕстьИнтервалЧас
		И ВыборкаЗаказ.РазностьДатВМесяцах > 6 Тогда
		Окончание = КонецМесяца(ДобавитьМесяц(Начало, 6));
	ИначеЕсли ВыборкаЗаказ.РазностьДатВМесяцах > 18 Тогда
		Окончание = КонецМесяца(ДобавитьМесяц(Начало, 18));
	Иначе
		Окончание = КонецМесяца(ДобавитьМесяц(ВыборкаЗаказ.ДатаПотребности, 3));
	КонецЕсли;
	
	Возврат Новый Структура("Начало, Окончание", Начало, Окончание);
	
КонецФункции

#КонецОбласти

#Область ПланированиеПоДлительности

Процедура РазместитьЭтапПоДлительности(ДанныеРасчета, ПараметрыРасчета, Этап, РазмещатьКНачалу)
	
	Если Этап.ДлительностьЭтапа = 0 Тогда
		Этап.Начало = Этап.НачалоРазмещения;
		Этап.Окончание = Этап.НачалоРазмещения;
		Возврат;
	КонецЕсли;
	
	Если Этап.РазмещатьВДнях Тогда
		ЭтапРазмещен = РазместитьПоРасписаниюПодразделенияВДнях(
			ДанныеРасчета, ПараметрыРасчета, Этап, Этап.ДлительностьЭтапа, Этап.НачалоРазмещения, РазмещатьКНачалу);
	Иначе
		ЭтапРазмещен = РазместитьПоРасписаниюПодразделенияВСекундах(
			ДанныеРасчета, ПараметрыРасчета, Этап, Этап.ДлительностьЭтапа, Этап.НачалоРазмещения, РазмещатьКНачалу);
	КонецЕсли;
	
	Если НЕ ЭтапРазмещен Тогда
		Возврат;
	КонецЕсли;
	
	Этап.Начало = ПараметрыРасчета.НачалоОкончание.Начало;
	Этап.Окончание = ПараметрыРасчета.НачалоОкончание.Окончание - 1;
	
КонецПроцедуры

Функция РазместитьПоРасписаниюПодразделенияВДнях(ДанныеРасчета, ПараметрыРасчета, Этап, Знач Длительность, Дата, РазмещатьКНачалу)
	
	Начало = '00010101';
	Окончание = '00010101';
	
	Интервал = НачальныйИнтервалПодразделенияВДнях(
		ДанныеРасчета, ПараметрыРасчета, Этап.Подразделение, Дата, РазмещатьКНачалу); // СтрокаТаблицыЗначений
	Если Интервал = Неопределено Тогда
		ОбработатьОшибкуНеНайденИнтервалПодразделения(ПараметрыРасчета, Этап, Дата, РазмещатьКНачалу);
		Возврат Ложь;
	КонецЕсли;
	
	Если РазмещатьКНачалу Тогда
		Начало = Интервал.Начало;
	Иначе
		Окончание = Интервал.Окончание;
	КонецЕсли;
	
	Пока Истина Цикл
		Длительность = Длительность - 1;
		
		// Подбор следующего интервала
		Если РазмещатьКНачалу Тогда
			Если Длительность = 0 Тогда
				Окончание = Интервал.Окончание;
				Прервать;
			КонецЕсли;
			
			Индекс = ДанныеРасчета.РасписаниеПодразделенийДень.Индекс(Интервал);
			КоличествоСтрок = ДанныеРасчета.РасписаниеПодразделенийДень.Количество();
			Пока Индекс < КоличествоСтрок Цикл
				// Дочитать доступность
				Если Индекс = КоличествоСтрок - 1
					ИЛИ ДанныеРасчета.РасписаниеПодразделенийДень[Индекс + 1].Подразделение <> Этап.Подразделение Тогда
					ЕстьЗаписи = ДочитатьДоступность(ДанныеРасчета, ПараметрыРасчета, Интервал, "РасписаниеПодразделенийДень");
					Если Не ЕстьЗаписи Тогда
						ОбработатьОшибкуНеНайденИнтервалПодразделения(ПараметрыРасчета, Этап, Интервал.Окончание, РазмещатьКНачалу);
						Возврат Ложь;
					КонецЕсли;
					
					Индекс = ДанныеРасчета.РасписаниеПодразделенийДень.Индекс(Интервал);
					КоличествоСтрок = ДанныеРасчета.РасписаниеПодразделенийДень.Количество();
				КонецЕсли;
				
				Индекс = Индекс + 1;
				Интервал = ДанныеРасчета.РасписаниеПодразделенийДень[Индекс];
				Если НЕ Интервал.ЭтоИндекс Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Если Длительность = 0 Тогда
				Начало = Интервал.Начало;
				Прервать;
			КонецЕсли;
			
			Индекс = ДанныеРасчета.РасписаниеПодразделенийДень.Индекс(Интервал);
			Пока Индекс >= 0 Цикл
				// При обратном размещении должна быть прочитана полная доступность
				Если Индекс = 0
					ИЛИ ДанныеРасчета.РасписаниеПодразделенийДень[Индекс - 1].Подразделение <> Этап.Подразделение Тогда
					ОбработатьОшибкуНеНайденИнтервалПодразделения(ПараметрыРасчета, Этап, Интервал.Начало, РазмещатьКНачалу);
					Возврат Ложь;
				КонецЕсли;
				
				Индекс = Индекс - 1;
				Интервал = ДанныеРасчета.РасписаниеПодразделенийДень[Индекс];
				Если НЕ Интервал.ЭтоИндекс Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыРасчета.НачалоОкончание.Начало = Начало;
	ПараметрыРасчета.НачалоОкончание.Окончание = Окончание;
	
	Возврат Истина;
	
КонецФункции

Функция РазместитьПоРасписаниюПодразделенияВСекундах(ДанныеРасчета, ПараметрыРасчета, Этап, Знач Длительность, Дата, РазмещатьКНачалу)
	
	Начало = '00010101';
	Окончание = '00010101';
	
	Интервал = НачальныйИнтервалПодразделенияВСекундах(
		ДанныеРасчета, ПараметрыРасчета, Этап.Подразделение, Дата, РазмещатьКНачалу); // СтрокаТаблицыЗначений
	Если Интервал = Неопределено Тогда
		ОбработатьОшибкуНеНайденИнтервалПодразделения(ПараметрыРасчета, Этап, Дата, РазмещатьКНачалу);
		Возврат Ложь;
	КонецЕсли;
	
	Если Дата >= Интервал.Начало И Дата <= Интервал.Окончание Тогда
		Если РазмещатьКНачалу Тогда
			Начало = Дата;
			
			ВремяСмещения = Мин(Длительность, Интервал.Окончание - Начало);
			Длительность = Длительность - ВремяСмещения;
			
			Если Длительность = 0 Тогда
				Окончание = Начало + ВремяСмещения;
			КонецЕсли;
		Иначе
			Окончание = Дата;
			
			ВремяСмещения = Мин(Длительность, Окончание - Интервал.Начало);
			Длительность = Длительность - ВремяСмещения;
			
			Если Длительность = 0 Тогда
				Начало = Окончание - ВремяСмещения;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ВремяСмещения = Мин(Длительность, Интервал.Длительность);
		Длительность = Длительность - ВремяСмещения;
		
		Если РазмещатьКНачалу Тогда
			Начало = Интервал.Начало;
			
			Если Длительность = 0 Тогда
				Окончание = Начало + ВремяСмещения;
			КонецЕсли;
		Иначе
			Окончание = Интервал.Окончание;
			
			Если Длительность = 0 Тогда
				Начало = Окончание - ВремяСмещения;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Пока Длительность > 0 Цикл
		// Подбор следующего интервала
		Если РазмещатьКНачалу Тогда
			Индекс = ДанныеРасчета.РасписаниеПодразделенийСекунда.Индекс(Интервал);
			КоличествоСтрок = ДанныеРасчета.РасписаниеПодразделенийСекунда.Количество();
			Пока Индекс < КоличествоСтрок Цикл
				// Дочитать доступность
				Если Индекс = КоличествоСтрок - 1
					ИЛИ ДанныеРасчета.РасписаниеПодразделенийСекунда[Индекс + 1].Подразделение <> Этап.Подразделение Тогда
					ЕстьЗаписи = ДочитатьДоступность(ДанныеРасчета, ПараметрыРасчета, Интервал, "РасписаниеПодразделенийСекунда");
					Если Не ЕстьЗаписи Тогда
						ОбработатьОшибкуНеНайденИнтервалПодразделения(ПараметрыРасчета, Этап, Интервал.Окончание, РазмещатьКНачалу);
						Возврат Ложь;
					КонецЕсли;
					
					Индекс = ДанныеРасчета.РасписаниеПодразделенийСекунда.Индекс(Интервал);
					КоличествоСтрок = ДанныеРасчета.РасписаниеПодразделенийСекунда.Количество();
				КонецЕсли;
				
				Индекс = Индекс + 1;
				Интервал = ДанныеРасчета.РасписаниеПодразделенийСекунда[Индекс];
				Если НЕ Интервал.ЭтоИндекс Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			Индекс = ДанныеРасчета.РасписаниеПодразделенийСекунда.Индекс(Интервал);
			Пока Индекс >= 0 Цикл
				// При обратном размещении должна быть прочитана полная доступность
				Если Индекс = 0
					ИЛИ ДанныеРасчета.РасписаниеПодразделенийСекунда[Индекс - 1].Подразделение <> Этап.Подразделение Тогда
					ОбработатьОшибкуНеНайденИнтервалПодразделения(ПараметрыРасчета, Этап, Интервал.Начало, РазмещатьКНачалу);
					Возврат Ложь;
				КонецЕсли;
				
				Индекс = Индекс - 1;
				Интервал = ДанныеРасчета.РасписаниеПодразделенийСекунда[Индекс];
				Если НЕ Интервал.ЭтоИндекс Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		// Расчет окончания
		ВремяСмещения = Мин(Длительность, Интервал.Длительность);
		Длительность = Длительность - ВремяСмещения;
		
		Если Длительность = 0 Тогда
			Если РазмещатьКНачалу Тогда
				Окончание = Интервал.Начало + ВремяСмещения;
			Иначе
				Начало = Интервал.Окончание - ВремяСмещения;
			КонецЕсли;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыРасчета.НачалоОкончание.Начало = Начало;
	ПараметрыРасчета.НачалоОкончание.Окончание = Окончание;
	
	Возврат Истина;
	
КонецФункции

Функция НачальныйИнтервалПодразделенияВДнях(ДанныеРасчета, ПараметрыРасчета, Подразделение, НачалоРазмещения, РазмещатьКНачалу)
	
	Если РазмещатьКНачалу Тогда
		ПараметрыРасчета.ПодразделениеДата.Подразделение = Подразделение;
		ПараметрыРасчета.ПодразделениеДата.Дата = НачалоДня(НачалоРазмещения);
		
		НайденныеСтроки = ДанныеРасчета.РасписаниеПодразделенийДень.НайтиСтроки(ПараметрыРасчета.ПодразделениеДата);
		Если НайденныеСтроки.Количество() = 0 Тогда
			НайденныеСтроки = ДочитатьДоступностьИНайтиИнтервал(
				ДанныеРасчета, ПараметрыРасчета, Подразделение, "РасписаниеПодразделенийДень", ПараметрыРасчета.ПодразделениеДата, НачалоРазмещения);
			Если НайденныеСтроки = Неопределено
				ИЛИ НайденныеСтроки.Количество() = 0 Тогда
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ НайденныеСтроки[0].ЭтоИндекс
			И НачалоРазмещения <= НайденныеСтроки[0].Начало Тогда
			Возврат НайденныеСтроки[0];
		Иначе
			Интервал = НайденныеСтроки[0];
			Индекс = ДанныеРасчета.РасписаниеПодразделенийДень.Индекс(Интервал);
			КоличествоСтрок = ДанныеРасчета.РасписаниеПодразделенийДень.Количество();
			Пока Индекс < КоличествоСтрок Цикл
				// Дочитать доступность
				Если Индекс = КоличествоСтрок - 1
					ИЛИ ДанныеРасчета.РасписаниеПодразделенийДень[Индекс + 1].Подразделение <> Подразделение Тогда
					ЕстьЗаписи = ДочитатьДоступность(ДанныеРасчета, ПараметрыРасчета, Интервал, "РасписаниеПодразделенийДень");
					Если Не ЕстьЗаписи Тогда
						Возврат Неопределено;
					КонецЕсли;
					
					Индекс = ДанныеРасчета.РасписаниеПодразделенийДень.Индекс(Интервал);
					КоличествоСтрок = ДанныеРасчета.РасписаниеПодразделенийДень.Количество();
				КонецЕсли;
				
				Индекс = Индекс + 1;
				Интервал = ДанныеРасчета.РасписаниеПодразделенийДень[Индекс];
				Если НЕ Интервал.ЭтоИндекс
					И НачалоРазмещения <= Интервал.Начало Тогда
					Возврат Интервал;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		ПараметрыРасчета.ПодразделениеДата.Подразделение = Подразделение;
		ПараметрыРасчета.ПодразделениеДата.Дата = ?(НачалоРазмещения = НачалоДня(НачалоРазмещения),
			НачалоРазмещения - 86400,
			НачалоДня(НачалоРазмещения));
		
		НайденныеСтроки = ДанныеРасчета.РасписаниеПодразделенийДень.НайтиСтроки(ПараметрыРасчета.ПодразделениеДата);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Возврат Неопределено;
		ИначеЕсли НЕ НайденныеСтроки[НайденныеСтроки.ВГраница()].ЭтоИндекс
			И НачалоРазмещения >= НайденныеСтроки[НайденныеСтроки.ВГраница()].Окончание Тогда
			Возврат НайденныеСтроки[НайденныеСтроки.ВГраница()];
		Иначе
			Интервал = НайденныеСтроки[НайденныеСтроки.ВГраница()];
			Индекс = ДанныеРасчета.РасписаниеПодразделенийДень.Индекс(Интервал);
			Пока Индекс > 0 Цикл
				Индекс = Индекс - 1;
				Интервал = ДанныеРасчета.РасписаниеПодразделенийДень[Индекс];
				Если Интервал.Подразделение <> Подразделение Тогда
					Возврат Неопределено;
				ИначеЕсли НЕ Интервал.ЭтоИндекс
					И НачалоРазмещения >= Интервал.Окончание Тогда
					Возврат Интервал;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция НачальныйИнтервалПодразделенияВСекундах(ДанныеРасчета, ПараметрыРасчета, Подразделение, НачалоРазмещения, РазмещатьКНачалу)
	
	Если РазмещатьКНачалу Тогда
		ПараметрыРасчета.ПодразделениеДата.Подразделение = Подразделение;
		ПараметрыРасчета.ПодразделениеДата.Дата = НачалоДня(НачалоРазмещения);
		
		НайденныеСтроки = ДанныеРасчета.РасписаниеПодразделенийСекунда.НайтиСтроки(ПараметрыРасчета.ПодразделениеДата);
		Если НайденныеСтроки.Количество() = 0 Тогда
			НайденныеСтроки = ДочитатьДоступностьИНайтиИнтервал(
				ДанныеРасчета, ПараметрыРасчета, Подразделение, "РасписаниеПодразделенийСекунда", ПараметрыРасчета.ПодразделениеДата, НачалоРазмещения);
			Если НайденныеСтроки = Неопределено
				ИЛИ НайденныеСтроки.Количество() = 0 Тогда
				Возврат Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ НайденныеСтроки[0].ЭтоИндекс
			И НачалоРазмещения < НайденныеСтроки[0].Окончание Тогда
			Возврат НайденныеСтроки[0];
		Иначе
			Интервал = НайденныеСтроки[0];
			Индекс = ДанныеРасчета.РасписаниеПодразделенийСекунда.Индекс(Интервал);
			КоличествоСтрок = ДанныеРасчета.РасписаниеПодразделенийСекунда.Количество();
			Пока Индекс < КоличествоСтрок Цикл
				// Дочитать доступность
				Если Индекс = КоличествоСтрок - 1
					ИЛИ ДанныеРасчета.РасписаниеПодразделенийСекунда[Индекс + 1].Подразделение <> Подразделение Тогда
					ЕстьЗаписи = ДочитатьДоступность(ДанныеРасчета, ПараметрыРасчета, Интервал, "РасписаниеПодразделенийСекунда");
					Если Не ЕстьЗаписи Тогда
						Возврат Неопределено;
					КонецЕсли;
					
					Индекс = ДанныеРасчета.РасписаниеПодразделенийСекунда.Индекс(Интервал);
					КоличествоСтрок = ДанныеРасчета.РасписаниеПодразделенийСекунда.Количество();
				КонецЕсли;
				
				Индекс = Индекс + 1;
				Интервал = ДанныеРасчета.РасписаниеПодразделенийСекунда[Индекс];
				Если НЕ Интервал.ЭтоИндекс
					И НачалоРазмещения < Интервал.Окончание Тогда
					Возврат Интервал;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		ПараметрыРасчета.ПодразделениеДата.Подразделение = Подразделение;
		ПараметрыРасчета.ПодразделениеДата.Дата = ?(НачалоРазмещения = НачалоДня(НачалоРазмещения),
			НачалоРазмещения,
			КонецДня(НачалоРазмещения) + 1);
		
		НайденныеСтроки = ДанныеРасчета.РасписаниеПодразделенийСекунда.НайтиСтроки(ПараметрыРасчета.ПодразделениеДата);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Интервал = НайденныеСтроки[0];
		Индекс = ДанныеРасчета.РасписаниеПодразделенийСекунда.Индекс(Интервал);
		Пока Индекс > 0 Цикл
			Индекс = Индекс - 1;
			Интервал = ДанныеРасчета.РасписаниеПодразделенийСекунда[Индекс];
			Если Интервал.Подразделение <> Подразделение Тогда
				Возврат Неопределено;
			ИначеЕсли НЕ Интервал.ЭтоИндекс
				И НачалоРазмещения > Интервал.Начало Тогда
				Возврат Интервал;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ПланированиеПоВидамРЦ

Процедура РазместитьЭтапПоВидамРЦ(ДанныеРасчета, ПараметрыРасчета, Этап)
	
	// Смещение на буфер до
	Если ЗначениеЗаполнено(Этап.БуферДо) Тогда
		Если Этап.БуферДоРазмещатьВДнях Тогда
			ЭтапРазмещен = РазместитьПоРасписаниюПодразделенияВДнях(
				ДанныеРасчета, ПараметрыРасчета, Этап, Этап.БуферДо, Этап.НачалоРазмещения, Истина);
		Иначе
			ЭтапРазмещен = РазместитьПоРасписаниюПодразделенияВСекундах(
				ДанныеРасчета, ПараметрыРасчета, Этап, Этап.БуферДо, Этап.НачалоРазмещения, Истина);
		КонецЕсли;
		Если НЕ ЭтапРазмещен Тогда
			Возврат;
		КонецЕсли;
		Этап.НачалоРазмещения = ПараметрыРасчета.НачалоОкончание.Окончание;
	КонецЕсли;
	
	// Планирование ВРЦ
	ЗаполнитьЗначенияСвойств(ПараметрыРасчета.КлючПартияЭтап, Этап, "КлючПартия, Этап");
	ВидыРабочихЦентров = ДанныеРасчета.ВидыРЦ.НайтиСтроки(ПараметрыРасчета.КлючПартияЭтап);
	Если ВидыРабочихЦентров.Количество() = 1 Тогда
		ЭтапРазмещен = ЗагрузитьОдиночныйВРЦ(ДанныеРасчета, ПараметрыРасчета, Этап, ВидыРабочихЦентров[0]);
	ИначеЕсли ВидыРабочихЦентров.Количество() > 1
		И (Этап.ПорядокРаботыВидовРЦ = Перечисления.ПорядокРаботыВидовРабочихЦентров.Последовательно
			ИЛИ Этап.ПорядокРаботыВидовРЦ = Перечисления.ПорядокРаботыВидовРабочихЦентров.Независимо) Тогда
		Если ТипЗнч(Этап.Этап) = Тип("СправочникСсылка.ЭтапыПроизводства") Тогда
			ТекстОповещения = СтрШаблон(НСтр("ru = 'В спецификации ""%1"" используется порядок работы видов РЦ ""%2"". В динамической структуре заказа поддерживается только одновременная загрузка видов РЦ.';
											|en = 'In the ""%1"" bill of materials, the ""%2"" operating procedure of work center types is used. The dynamic order structure supports only simultaneous load of work center types.'"),
				Этап.Спецификация,
				Этап.ПорядокРаботыВидовРЦ);
			ПараметрыРасчета.Оповещения.Вставить(ТекстОповещения, Этап.Спецификация);
		Иначе
			ТекстОповещения = СтрШаблон(НСтр("ru = 'В документе %1 используется порядок работы видов РЦ ""%2"". В динамической структуре заказа поддерживается только одновременная загрузка видов РЦ.';
											|en = 'In the ""%1"" document, the ""%2"" operating procedure of work center types is used. The dynamic order structure supports only simultaneous load of work center types.'"),
				Этап.Этап,
				Этап.ПорядокРаботыВидовРЦ);
			ПараметрыРасчета.Оповещения.Вставить(ТекстОповещения, Этап.Этап);
		КонецЕсли;
		
		ЭтапРазмещен = ЗагрузитьОдиночныйВРЦ(ДанныеРасчета, ПараметрыРасчета, Этап, ВидыРабочихЦентров[0]);
	Иначе
		ЭтапРазмещен = ЗагрузитьГруппуВРЦ(ДанныеРасчета, ПараметрыРасчета, Этап, ВидыРабочихЦентров);
	КонецЕсли;
	
	Если НЕ ЭтапРазмещен Тогда
		Возврат;
	КонецЕсли;
	
	// Размещение буфера до
	Если ЗначениеЗаполнено(Этап.БуферДо) Тогда
		Если Этап.БуферДоРазмещатьВДнях Тогда
			ЭтапРазмещен = РазместитьПоРасписаниюПодразделенияВДнях(
				ДанныеРасчета, ПараметрыРасчета, Этап, Этап.БуферДо, Этап.ОкончаниеПредварительногоБуфера, Ложь);
		Иначе
			ЭтапРазмещен = РазместитьПоРасписаниюПодразделенияВСекундах(
				ДанныеРасчета, ПараметрыРасчета, Этап, Этап.БуферДо, Этап.ОкончаниеПредварительногоБуфера, Ложь);
		КонецЕсли;
		Если НЕ ЭтапРазмещен Тогда
			Возврат;
		КонецЕсли;
		
		Этап.Начало = ПараметрыРасчета.НачалоОкончание.Начало;
	Иначе
		Этап.Начало = Этап.ОкончаниеПредварительногоБуфера;
	КонецЕсли;
	
	// Размещение буфера после
	Если ЗначениеЗаполнено(Этап.БуферПосле) Тогда
		Если Этап.БуферПослеРазмещатьВДнях Тогда
			ЭтапРазмещен = РазместитьПоРасписаниюПодразделенияВДнях(
				ДанныеРасчета, ПараметрыРасчета, Этап, Этап.БуферПосле, Этап.НачалоЗавершающегоБуфера, Истина);
		Иначе
			ЭтапРазмещен = РазместитьПоРасписаниюПодразделенияВСекундах(
				ДанныеРасчета, ПараметрыРасчета, Этап, Этап.БуферПосле, Этап.НачалоЗавершающегоБуфера, Истина);
		КонецЕсли;
		Если НЕ ЭтапРазмещен Тогда
			Возврат;
		КонецЕсли;
		
		Этап.Окончание = ПараметрыРасчета.НачалоОкончание.Окончание - 1;
	Иначе
		Этап.Окончание = Этап.НачалоЗавершающегоБуфера - 1;
	КонецЕсли;
	
КонецПроцедуры

Функция ЗагрузитьОдиночныйВРЦ(ДанныеРасчета, ПараметрыРасчета, Этап, ВидРЦ)
	
	ИнтервалРазмещения = НачальныйИнтервалРазмещенияВРЦ(ДанныеРасчета, ПараметрыРасчета, Этап, ВидРЦ);
	Если ИнтервалРазмещения = Неопределено Тогда
		ОбработатьОшибкуНеНайденаДоступностьВРЦ(ПараметрыРасчета, Этап, ВидРЦ);
		Возврат Ложь;
	КонецЕсли;
	
	ОсталосьРазместить = ВидРЦ.ВремяЗагрузка;
	Начало = '00010101';
	Окончание = '00010101';
	КоличествоИнтервалов = 0;
	
	Пока Истина Цикл
		ВремяЗагрузка = МИН(ОсталосьРазместить,
			ИнтервалРазмещения.МаксимумВремяЗагрузка, ИнтервалРазмещения.ДоступноВремяЗагрузка);
		
		Если ИнтервалРазмещения.НачалоИнтервала < ПараметрыРасчета.ТекущаяДата
			И ИнтервалРазмещения.ОкончаниеИнтервала > ПараметрыРасчета.ТекущаяДата Тогда
			ВремяЗагрузка = Мин(ВремяЗагрузка,
				?(НЕ ВидРЦ.ПараллельнаяЗагрузка,
					ИнтервалРазмещения.ОкончаниеИнтервала - ПараметрыРасчета.ТекущаяДата,
					(ИнтервалРазмещения.ОкончаниеИнтервала - ПараметрыРасчета.ТекущаяДата) * ВидРЦ.МаксимальнаяЗагрузка));
		КонецЕсли;
		
		НоваяСтрока = ДанныеРасчета.ЗагрузкаВРЦ.Добавить();
		НоваяСтрока.Распоряжение = Этап.ЗаказНаПроизводство;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Этап, "Этап, КлючПартия, Подразделение");
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ИнтервалРазмещения, "ВидРабочегоЦентра, ДатаИнтервала, Смена");
		
		НоваяСтрока.ЗанятоВремяЗагрузка = ВремяЗагрузка;
		Если НЕ ВидРЦ.ПараллельнаяЗагрузка Тогда
			НоваяСтрока.Занято = ВремяЗагрузка;
		Иначе
			НоваяСтрока.Занято = ?(ВремяЗагрузка = ИнтервалРазмещения.ДоступноВремяЗагрузка,
				ИнтервалРазмещения.ДоступноВремя,
				Окр(ВремяЗагрузка / ВидРЦ.МаксимальнаяЗагрузка));
		КонецЕсли;
		
		НоваяСтрока.Интервал = ИнтервалРазмещения;
		КоличествоИнтервалов = КоличествоИнтервалов + 1;
		
		ИнтервалРазмещения.ДоступноВремяЗагрузка = ИнтервалРазмещения.ДоступноВремяЗагрузка - НоваяСтрока.ЗанятоВремяЗагрузка;
		ИнтервалРазмещения.ДоступноВремя = ИнтервалРазмещения.ДоступноВремя - НоваяСтрока.Занято;
		
		ОсталосьРазместить = ОсталосьРазместить - ВремяЗагрузка;
		Если Начало = '00010101' Тогда
			Начало = ИнтервалРазмещения.НачалоИнтервала;
		КонецЕсли;
		
		Если ОсталосьРазместить = 0 Тогда
			Окончание = ИнтервалРазмещения.ОкончаниеИнтервала;
			Прервать;
		КонецЕсли;
		
		// Подбор следующего интервала
		Индекс = ДанныеРасчета.ДоступностьВРЦ.Индекс(ИнтервалРазмещения);
		КоличествоСтрок = ДанныеРасчета.ДоступностьВРЦ.Количество();
		Пока Индекс < КоличествоСтрок Цикл
			// Дочитать доступность
			Если Индекс = КоличествоСтрок - 1
				ИЛИ ДанныеРасчета.ДоступностьВРЦ[Индекс + 1].ВидРабочегоЦентра <> ВидРЦ.ВидРабочегоЦентра Тогда
				ЕстьЗаписи = ДочитатьДоступность(ДанныеРасчета, ПараметрыРасчета, ИнтервалРазмещения, "ДоступностьВРЦ");
				Если Не ЕстьЗаписи Тогда
					ОбработатьОшибкуНеНайденаДоступностьВРЦ(ПараметрыРасчета, Этап, ВидРЦ);
					Возврат Ложь;
				КонецЕсли;
				
				Индекс = ДанныеРасчета.ДоступностьВРЦ.Индекс(ИнтервалРазмещения);
				КоличествоСтрок = ДанныеРасчета.ДоступностьВРЦ.Количество();
			КонецЕсли;
			
			Индекс = Индекс + 1;
			ИнтервалРазмещения = ДанныеРасчета.ДоступностьВРЦ[Индекс];
			Если ИнтервалРазмещения.ДоступноВремяЗагрузка > 0 Тогда
				Прервать;
			ИначеЕсли НЕ ИнтервалРазмещения.ЭтоИндекс И Этап.Непрерывный Тогда
				// Откат размещения
				Для Счетчик = 1 По КоличествоИнтервалов Цикл
					СтрокаЗагрузка = ДанныеРасчета.ЗагрузкаВРЦ[ДанныеРасчета.ЗагрузкаВРЦ.Количество()-1];
					СтрокаЗагрузка.Интервал.ДоступноВремяЗагрузка = СтрокаЗагрузка.Интервал.ДоступноВремяЗагрузка + СтрокаЗагрузка.ЗанятоВремяЗагрузка;
					СтрокаЗагрузка.Интервал.ДоступноВремя = СтрокаЗагрузка.Интервал.ДоступноВремя + СтрокаЗагрузка.Занято;
					ДанныеРасчета.ЗагрузкаВРЦ.Удалить(СтрокаЗагрузка);
				КонецЦикла;
				
				Этап.ОграниченПоОборудованию = Истина;
				
				ОсталосьРазместить = ВидРЦ.ВремяЗагрузка;
				Начало = '00010101';
				КоличествоИнтервалов = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Этап.ОкончаниеПредварительногоБуфера = Начало;
	Этап.НачалоЗавершающегоБуфера = Окончание;
	
	Возврат Истина;
	
КонецФункции

Функция ЗагрузитьГруппуВРЦ(ДанныеРасчета, ПараметрыРасчета, Этап, ВидыРабочихЦентров)
	
	ВидРЦ = ВидыРабочихЦентров[0];
	
	ИнтервалРазмещения = НачальныйИнтервалРазмещенияВРЦ(ДанныеРасчета, ПараметрыРасчета, Этап, ВидРЦ);
	Если ИнтервалРазмещения = Неопределено Тогда
		ОбработатьОшибкуНеНайденаДоступностьВРЦ(ПараметрыРасчета, Этап, ВидРЦ);
		Возврат Ложь;
	КонецЕсли;
	
	ОсталосьРазместить = ВидРЦ.ВремяЗагрузка;
	Начало = '00010101';
	Окончание = '00010101';
	КоличествоИнтервалов = 0;
	
	Интервалы = Новый Массив;
	ПоискВРЦ = Новый Структура("ЭтоИндекс, ВидРабочегоЦентра, ДатаИнтервала", Ложь);
	
	Пока Истина Цикл
		Интервалы.Добавить(ИнтервалРазмещения);
		ВремяЗагрузка = МИН(ОсталосьРазместить,
			ИнтервалРазмещения.МаксимумВремяЗагрузка, ИнтервалРазмещения.ДоступноВремяЗагрузка);
		
		// Подбор интервалов дополнительных видов РЦ
		Пока Интервалы.Количество() <> ВидыРабочихЦентров.Количество() Цикл
			Для ИндексВидыРЦ = 1 По ВидыРабочихЦентров.Количество()-1 Цикл
				ДопВидРЦ = ВидыРабочихЦентров[ИндексВидыРЦ];
				
				ПоискВРЦ.ВидРабочегоЦентра = ДопВидРЦ.ВидРабочегоЦентра;
				ПоискВРЦ.ДатаИнтервала = ИнтервалРазмещения.ДатаИнтервала;
				НайденныеСтроки = ДанныеРасчета.ДоступностьВРЦ.НайтиСтроки(ПоискВРЦ);
				
				Если НайденныеСтроки.Количество() > 0
					И НайденныеСтроки[0].ДоступноВремяЗагрузка > 0 Тогда
					// Интервал дополнительного вида найден и доступен
					ВремяЗагрузка = МИН(ВремяЗагрузка,
						НайденныеСтроки[0].МаксимумВремяЗагрузка, НайденныеСтроки[0].ДоступноВремяЗагрузка);
					Интервалы.Добавить(НайденныеСтроки[0]);
				Иначе
					// Интервал дополнительного вида не найден или недоступен
					Если Этап.Непрерывный Тогда
						// Откат размещения
						Для Счетчик = 1 По КоличествоИнтервалов Цикл
							СтрокаЗагрузка = ДанныеРасчета.ЗагрузкаВРЦ[ДанныеРасчета.ЗагрузкаВРЦ.Количество()-1];
							СтрокаЗагрузка.Интервал.ДоступноВремяЗагрузка = СтрокаЗагрузка.Интервал.ДоступноВремяЗагрузка + СтрокаЗагрузка.ЗанятоВремяЗагрузка;
							СтрокаЗагрузка.Интервал.ДоступноВремя = СтрокаЗагрузка.Интервал.ДоступноВремя + СтрокаЗагрузка.Занято;
							ДанныеРасчета.ЗагрузкаВРЦ.Удалить(СтрокаЗагрузка);
						КонецЦикла;
						
						Этап.ОграниченПоОборудованию = Истина;
						
						ОсталосьРазместить = ВидРЦ.ВремяЗагрузка;
						Начало = '00010101';
						КоличествоИнтервалов = 0;
					ИначеЕсли ОсталосьРазместить = ВидРЦ.ВремяЗагрузка Тогда
						Этап.ОграниченПоОборудованию = Истина;
					КонецЕсли;
					
					// Поиск нового интервала основного вида РЦ
					Индекс = ДанныеРасчета.ДоступностьВРЦ.Индекс(ИнтервалРазмещения);
					КоличествоСтрок = ДанныеРасчета.ДоступностьВРЦ.Количество();
					Пока Индекс < КоличествоСтрок Цикл
						// Дочитать доступность
						Если Индекс = КоличествоСтрок - 1
							ИЛИ ДанныеРасчета.ДоступностьВРЦ[Индекс + 1].ВидРабочегоЦентра <> ВидРЦ.ВидРабочегоЦентра Тогда
							ЕстьЗаписи = ДочитатьДоступность(ДанныеРасчета, ПараметрыРасчета, ИнтервалРазмещения, "ДоступностьВРЦ");
							Если Не ЕстьЗаписи Тогда
								ОбработатьОшибкуНеНайденаДоступностьВРЦ(ПараметрыРасчета, Этап, ВидРЦ);
								Возврат Ложь;
							КонецЕсли;
							
							Индекс = ДанныеРасчета.ДоступностьВРЦ.Индекс(ИнтервалРазмещения);
							КоличествоСтрок = ДанныеРасчета.ДоступностьВРЦ.Количество();
						КонецЕсли;
						
						Индекс = Индекс + 1;
						ИнтервалРазмещения = ДанныеРасчета.ДоступностьВРЦ[Индекс];
						Если ИнтервалРазмещения.ДоступноВремяЗагрузка > 0 Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
					Интервалы.Очистить();
					Интервалы.Добавить(ИнтервалРазмещения);
					
					ВремяЗагрузка = МИН(ОсталосьРазместить,
						ИнтервалРазмещения.МаксимумВремяЗагрузка, ИнтервалРазмещения.ДоступноВремяЗагрузка);
					
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		Для Индекс = 0 По ВидыРабочихЦентров.Количество()-1 Цикл
			ТекущийВидРЦ = ВидыРабочихЦентров[Индекс];
			Интервал = Интервалы[Индекс];
			
			НоваяСтрока = ДанныеРасчета.ЗагрузкаВРЦ.Добавить();
			НоваяСтрока.Распоряжение = Этап.ЗаказНаПроизводство;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Этап, "Этап, КлючПартия, Подразделение");
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Интервал, "ВидРабочегоЦентра, ДатаИнтервала, Смена");
			
			НоваяСтрока.ЗанятоВремяЗагрузка = ВремяЗагрузка;
			Если НЕ ТекущийВидРЦ.ПараллельнаяЗагрузка Тогда
				НоваяСтрока.Занято = ВремяЗагрузка;
			Иначе
				НоваяСтрока.Занято = ?(ВремяЗагрузка = Интервал.ДоступноВремяЗагрузка,
					Интервал.ДоступноВремя,
					Окр(ВремяЗагрузка / ТекущийВидРЦ.МаксимальнаяЗагрузка));
			КонецЕсли;
			
			НоваяСтрока.Интервал = Интервал;
			КоличествоИнтервалов = КоличествоИнтервалов + 1;
			
			Интервал.ДоступноВремяЗагрузка = Интервал.ДоступноВремяЗагрузка - НоваяСтрока.ЗанятоВремяЗагрузка;
			Интервал.ДоступноВремя = Интервал.ДоступноВремя - НоваяСтрока.Занято;
		КонецЦикла;
		Интервалы.Очистить();
		
		ОсталосьРазместить = ОсталосьРазместить - ВремяЗагрузка;
		Если Начало = '00010101' Тогда
			Начало = ИнтервалРазмещения.НачалоИнтервала;
		КонецЕсли;
		
		Если ОсталосьРазместить = 0 Тогда
			Окончание = ИнтервалРазмещения.ОкончаниеИнтервала;
			Прервать;
		КонецЕсли;
		
		// Подбор следующего интервала
		Индекс = ДанныеРасчета.ДоступностьВРЦ.Индекс(ИнтервалРазмещения);
		КоличествоСтрок = ДанныеРасчета.ДоступностьВРЦ.Количество();
		Пока Индекс < КоличествоСтрок Цикл
			// Дочитать доступность
			Если Индекс = КоличествоСтрок - 1
				ИЛИ ДанныеРасчета.ДоступностьВРЦ[Индекс + 1].ВидРабочегоЦентра <> ВидРЦ.ВидРабочегоЦентра Тогда
				ЕстьЗаписи = ДочитатьДоступность(ДанныеРасчета, ПараметрыРасчета, ИнтервалРазмещения, "ДоступностьВРЦ");
				Если Не ЕстьЗаписи Тогда
					ОбработатьОшибкуНеНайденаДоступностьВРЦ(ПараметрыРасчета, Этап, ВидРЦ);
					Возврат Ложь;
				КонецЕсли;
				
				Индекс = ДанныеРасчета.ДоступностьВРЦ.Индекс(ИнтервалРазмещения);
				КоличествоСтрок = ДанныеРасчета.ДоступностьВРЦ.Количество();
			КонецЕсли;
			
			Индекс = Индекс + 1;
			ИнтервалРазмещения = ДанныеРасчета.ДоступностьВРЦ[Индекс];
			Если ИнтервалРазмещения.ДоступноВремяЗагрузка > 0 Тогда
				Прервать;
			ИначеЕсли НЕ ИнтервалРазмещения.ЭтоИндекс И Этап.Непрерывный Тогда
				// Откат размещения
				Для Счетчик = 1 По КоличествоИнтервалов Цикл
					СтрокаЗагрузка = ДанныеРасчета.ЗагрузкаВРЦ[ДанныеРасчета.ЗагрузкаВРЦ.Количество()-1];
					СтрокаЗагрузка.Интервал.ДоступноВремяЗагрузка = СтрокаЗагрузка.Интервал.ДоступноВремяЗагрузка + СтрокаЗагрузка.ЗанятоВремяЗагрузка;
					СтрокаЗагрузка.Интервал.ДоступноВремя = СтрокаЗагрузка.Интервал.ДоступноВремя + СтрокаЗагрузка.Занято;
					ДанныеРасчета.ЗагрузкаВРЦ.Удалить(СтрокаЗагрузка);
				КонецЦикла;
				
				Этап.ОграниченПоОборудованию = Истина;
				
				ОсталосьРазместить = ВидРЦ.ВремяЗагрузка;
				Начало = '00010101';
				КоличествоИнтервалов = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Этап.ОкончаниеПредварительногоБуфера = Начало;
	Этап.НачалоЗавершающегоБуфера = Окончание;
	
	Возврат Истина;
	
КонецФункции

Функция НачальныйИнтервалРазмещенияВРЦ(ДанныеРасчета, ПараметрыРасчета, Этап, ВидРЦ)
	
	ПараметрыРасчета.ПоискВРЦ.ВидРабочегоЦентра = ВидРЦ.ВидРабочегоЦентра;
	Если ВидРЦ.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Неделя Тогда
		ПараметрыРасчета.ПоискВРЦ.ДатаИнтервала = НачалоНедели(Этап.НачалоРазмещения);
	ИначеЕсли ВидРЦ.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Месяц Тогда
		ПараметрыРасчета.ПоискВРЦ.ДатаИнтервала = НачалоМесяца(Этап.НачалоРазмещения);
	Иначе
		ПараметрыРасчета.ПоискВРЦ.ДатаИнтервала = НачалоДня(Этап.НачалоРазмещения);
	КонецЕсли;
	
	НайденныеСтроки = ДанныеРасчета.ДоступностьВРЦ.НайтиСтроки(ПараметрыРасчета.ПоискВРЦ);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		НайденныеСтроки = ДочитатьДоступностьИНайтиИнтервал(
			ДанныеРасчета, ПараметрыРасчета, ВидРЦ.ВидРабочегоЦентра, "ДоступностьВРЦ", ПараметрыРасчета.ПоискВРЦ, ПараметрыРасчета.ПоискВРЦ.ДатаИнтервала);
		Если НайденныеСтроки = Неопределено
			ИЛИ НайденныеСтроки.Количество() = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Интервал = НайденныеСтроки[0];
	Индекс = ДанныеРасчета.ДоступностьВРЦ.Индекс(Интервал);
	КоличествоСтрок = ДанныеРасчета.ДоступностьВРЦ.Количество();
	Пока Индекс < КоличествоСтрок Цикл
		// Дочитать доступность
		Если Индекс = КоличествоСтрок - 1
			ИЛИ ДанныеРасчета.ДоступностьВРЦ[Индекс + 1].ВидРабочегоЦентра <> ВидРЦ.ВидРабочегоЦентра Тогда
			ЕстьЗаписи = ДочитатьДоступность(ДанныеРасчета, ПараметрыРасчета, Интервал, "ДоступностьВРЦ");
			Если Не ЕстьЗаписи Тогда
				Возврат Неопределено;
			КонецЕсли;
			
			Индекс = ДанныеРасчета.ДоступностьВРЦ.Индекс(Интервал);
			КоличествоСтрок = ДанныеРасчета.ДоступностьВРЦ.Количество();
		КонецЕсли;
		
		Индекс = Индекс + 1;
		Интервал = ДанныеРасчета.ДоступностьВРЦ[Индекс];
		Если Интервал.ЭтоИндекс
			Или Интервал.НачалоИнтервала < Этап.НачалоРазмещения И Этап.НачалоРазмещения <> ПараметрыРасчета.ТекущаяДата
			Или Интервал.ОкончаниеИнтервала <= Этап.НачалоРазмещения Тогда
			Продолжить;
		ИначеЕсли Интервал.ДоступноВремяЗагрузка <= 0 Тогда
			Этап.ОграниченПоОборудованию = Истина;
			Продолжить;
		Иначе
			Возврат Интервал;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область Ошибки

Процедура ОбработатьОшибкуНеНайденИнтервалПодразделения(ПараметрыРасчета, Этап, Дата, РазмещатьКНачалу)
	
	Описание = "";
	Расшифровка = Новый Структура;
	Ключ = Неопределено;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("СтатусГрафика", ПараметрыРасчета.СтатусГрафика);
	
	Если Этап.ЭтоПеремещение Тогда
		Ключ = "ГрафикРаботыПредприятия";
		
		СтруктураПоиска.Вставить("Ключ", Ключ);
		НайденныеСтроки = ПараметрыРасчета.Ошибки.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() <> 0 Тогда
			Возврат;
		КонецЕсли;
		
		Расшифровка.Вставить("КодОшибки",
			Обработки.ПланированиеГрафикаПроизводства2_2.КодОшибкиГрафикРаботыПредприятия());
		
		Причина = ?(РазмещатьКНачалу,
			СтрШаблон(НСтр("ru = 'не определен график работы предприятия или данные графика не заполнены после %1';
							|en = 'enterprise schedule is not specified or schedule data is not filled in after %1'"),
				Формат(Дата, "ДЛФ=D;")),
			СтрШаблон(НСтр("ru = 'не определен график работы предприятия или данные графика не заполнены ранее %1';
							|en = 'enterprise schedule is not specified or schedule data is not filled in before %1'"),
				Формат(Дата, "ДЛФ=D;")));
		
		Если ПараметрыРасчета.СтатусГрафика = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусМодельГрафика() Тогда
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При планировании модели не размещено перемещение по причине: %1.';
					|en = 'When planning the model, the transfer is not placed. Reason: %1.'"),
				Причина);
		Иначе
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При планировании графика не размещено перемещение по причине: %1.';
					|en = 'When planning the schedule, the transfer is not placed. Reason: %1.'"),
				Причина);
		КонецЕсли;
	Иначе
		Ключ = Этап.Подразделение;
		
		СтруктураПоиска.Вставить("Ключ", Ключ);
		НайденныеСтроки = ПараметрыРасчета.Ошибки.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() <> 0 Тогда
			Возврат;
		КонецЕсли;
		
		Расшифровка.Вставить("КодОшибки",
			Обработки.ПланированиеГрафикаПроизводства2_2.КодОшибкиГрафикРаботыПодразделения());
		Расшифровка.Вставить("Подразделение", Этап.Подразделение);
		
		Причина = ?(РазмещатьКНачалу,
			СтрШаблон(НСтр("ru = 'не определен график работы подразделения ""%1"" или данные графика не заполнены после %2';
							|en = 'the work schedule of the ""%1"" business unit is not determined, or the schedule data is not filled after %2'"),
				Этап.Подразделение, Формат(Дата, "ДЛФ=D;")),
			СтрШаблон(НСтр("ru = 'не определен график работы подразделения ""%1"" или данные графика не заполнены ранее %2';
							|en = 'the work schedule of the ""%1"" business unit is not determined, or the schedule data is not filled before %2'"),
				Этап.Подразделение, Формат(Дата, "ДЛФ=D;")));
		
		ПредставлениеЭтапа = ?(ТипЗнч(Этап.Этап) = Тип("ДокументСсылка.ЭтапПроизводства2_2"),
			Документы.ЭтапПроизводства2_2.ПредставлениеЭтапа(Этап.Этап),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Этап.Этап, "Наименование"));
		
		Если ПараметрыРасчета.СтатусГрафика = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусМодельГрафика() Тогда
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При планировании модели не размещен этап ""%1"" по причине: %2.';
					|en = 'When planning the model, the ""%1"" stage is not placed. Reason: %1.'"),
				ПредставлениеЭтапа,
				Причина);
		Иначе
			Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При планировании графика не размещен этап ""%1"" по причине: %2.';
					|en = 'When planning the schedule, the ""%1"" stage is not placed. Reason: %1.'"),
				ПредставлениеЭтапа,
				Причина);
		КонецЕсли;
	КонецЕсли;
	
	НоваяСтрока = ПараметрыРасчета.Ошибки.Добавить(); //СтрокаТаблицыЗначений
	НоваяСтрока.Описание = Описание;
	НоваяСтрока.Расшифровка = Расшифровка;
	НоваяСтрока.Ключ = Ключ;
	НоваяСтрока.СтатусГрафика = ПараметрыРасчета.СтатусГрафика;
	
КонецПроцедуры

Процедура ОбработатьОшибкуНеНайденаДоступностьВРЦ(ПараметрыРасчета, Этап, ВидРЦ)
	
	Ключ = ВидРЦ.ВидРабочегоЦентра;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("СтатусГрафика", ПараметрыРасчета.СтатусГрафика);
	СтруктураПоиска.Вставить("Ключ", Ключ);
	НайденныеСтроки = ПараметрыРасчета.Ошибки.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	Расшифровка = Новый Структура;
	Расшифровка.Вставить("КодОшибки", Обработки.ПланированиеГрафикаПроизводства2_2.КодОшибкиДоступностьВРЦ());
	Расшифровка.Вставить("ЭтапПроизводства", Этап.Этап);
	Расшифровка.Вставить("Подразделение", Этап.Подразделение);
	Расшифровка.Вставить("Период", Этап.НачалоРазмещения);
	Расшифровка.Вставить("ВидРабочегоЦентра", ВидРЦ.ВидРабочегоЦентра);
	
	Причина = СтрШаблон(
		НСтр("ru = 'недостаточно доступного времени вида РЦ ""%1"" после %2';
			|en = 'insufficient available time of the ""%1"" work center type after %2'"),
		ВидРЦ.ВидРабочегоЦентра,
		Формат(Этап.НачалоРазмещения, "ДЛФ=D;"));
	
	ПредставлениеЭтапа = ?(ТипЗнч(Этап.Этап) = Тип("ДокументСсылка.ЭтапПроизводства2_2"),
		Документы.ЭтапПроизводства2_2.ПредставлениеЭтапа(Этап.Этап),
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Этап.Этап, "Наименование"));
	
	Описание = "";
	Если ПараметрыРасчета.СтатусГрафика = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусМодельГрафика() Тогда
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При планировании модели не размещен этап ""%1"" по причине: %2.';
				|en = 'When planning the model, the ""%1"" stage is not placed. Reason: %1.'"),
			ПредставлениеЭтапа,
			Причина);
	Иначе
		Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При планировании графика не размещен этап ""%1"" по причине: %2.';
				|en = 'When planning the schedule, the ""%1"" stage is not placed. Reason: %1.'"),
			ПредставлениеЭтапа,
			Причина);
	КонецЕсли;
	
	НоваяСтрока = ПараметрыРасчета.Ошибки.Добавить(); //СтрокаТаблицыЗначений
	НоваяСтрока.Описание = Описание;
	НоваяСтрока.Расшифровка = Расшифровка;
	НоваяСтрока.Ключ = Ключ;
	НоваяСтрока.СтатусГрафика = ПараметрыРасчета.СтатусГрафика;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция НовыйПараметрыРасчета(Настройки = Неопределено)
	
	ПараметрыРасчета = Новый Структура;
	
	ПараметрыРасчета.Вставить("СтатусГрафика", РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик());
	ПараметрыРасчета.Вставить("ЗадействоватьРезервДоступности", Ложь);
	ПараметрыРасчета.Вставить("ВсеМатериалыВНаличии", Ложь);
	ПараметрыРасчета.Вставить("НеограниченныйПаркОборудования", Ложь);
	ПараметрыРасчета.Вставить("КруглосуточнаяРаботаБезВыходных", Ложь);
	ПараметрыРасчета.Вставить("ОтсутствиеПрочихЗаказов", Ложь);
	ПараметрыРасчета.Вставить("ОтменитьРучныеИзмененияГрафика", Ложь);
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыРасчета, Настройки);
	КонецЕсли;
	
	Если ПараметрыРасчета.СтатусГрафика = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик() Тогда
		ПараметрыРасчета.ВсеМатериалыВНаличии = Ложь;
		ПараметрыРасчета.НеограниченныйПаркОборудования = Ложь;
		ПараметрыРасчета.КруглосуточнаяРаботаБезВыходных = Ложь;
		ПараметрыРасчета.ОтсутствиеПрочихЗаказов = Ложь;
	Иначе
		ПараметрыРасчета.ОтменитьРучныеИзмененияГрафика = Ложь;
	КонецЕсли;
	
	ПараметрыРасчета.Вставить("Заказ", Неопределено);
	ПараметрыРасчета.Вставить("ТекущаяДата", ТекущаяДатаСеанса());
	
	Ошибки = Новый ТаблицаЗначений();
	Ошибки.Колонки.Добавить("Описание", Новый ОписаниеТипов("Строка"));
	Ошибки.Колонки.Добавить("Расшифровка");
	Ошибки.Колонки.Добавить("Ключ");
	Ошибки.Колонки.Добавить("СтатусГрафика", Новый ОписаниеТипов("Число"));
	ПараметрыРасчета.Вставить("Ошибки", Ошибки);
	
	ПараметрыРасчета.Вставить("Оповещения", Новый Соответствие);
	
	// Структуры для поиска
	НачалоОкончание = Новый Структура("Начало, Окончание", '00010101', '00010101');
	ПараметрыРасчета.Вставить("НачалоОкончание", НачалоОкончание);
	
	КлючПартияЭтап = Новый Структура("КлючПартия, Этап");
	ПараметрыРасчета.Вставить("КлючПартияЭтап", КлючПартияЭтап);
	
	ПодразделениеДата = Новый Структура("Подразделение, Дата");
	ПараметрыРасчета.Вставить("ПодразделениеДата", ПодразделениеДата);
	
	ПоискВРЦ = Новый Структура("ЭтоИндекс, ВидРабочегоЦентра, ДатаИнтервала", Истина);
	ПараметрыРасчета.Вставить("ПоискВРЦ", ПоискВРЦ);
	
	// Параметры чтения доступности
	ПараметрыРасчета.Вставить("ДоступностьГраницаВыборки", '00010101');
	ПараметрыРасчета.Вставить("ДоступностьОтборПодразделения", Новый Массив);
	ПараметрыРасчета.Вставить("ДоступностьОтборВРЦ", Новый Массив);
	//  Доступность сокращается на загрузку ранее запущенных партий текущего заказа
	//  (используется при расчете запускаемых этапов)
	ПараметрыРасчета.Вставить("ДоступностьУчитыватьЗагрузкуЗапущенныхПартий", Ложь);
	//  Параметры, позволяющие переопределить интервал первичной выборки доступности
	ПараметрыРасчета.Вставить("ДоступностьИнтервалНачало", '00010101');
	ПараметрыРасчета.Вставить("ДоступностьИнтервалОкончание", '00010101');
	
	Возврат ПараметрыРасчета;
	
КонецФункции

Функция НовыйДанныеРасчета()
	
	ДанныеРасчета = Новый Структура;
	ДанныеРасчета.Вставить("Этапы", Неопределено);
	ДанныеРасчета.Вставить("Связи", Неопределено);
	ДанныеРасчета.Вставить("ВидыРЦ", Неопределено);
	ДанныеРасчета.Вставить("РасписаниеПодразделенийДень", Новый ТаблицаЗначений);
	ДанныеРасчета.Вставить("РасписаниеПодразделенийСекунда", Новый ТаблицаЗначений);
	ДанныеРасчета.Вставить("ДоступностьВРЦ", Новый ТаблицаЗначений);
	ДанныеРасчета.Вставить("ГраницыДоступности", Новый ТаблицаЗначений);
	ДанныеРасчета.Вставить("Задания", Неопределено);
	ДанныеРасчета.Вставить("ЭтапыСРучнымРазмещением", Неопределено);
	
	ЗагрузкаВРЦ = Новый ТаблицаЗначений;
	ЗагрузкаВРЦ.Колонки.Добавить("ВидРабочегоЦентра", Новый ОписаниеТипов("СправочникСсылка.ВидыРабочихЦентров"));
	ЗагрузкаВРЦ.Колонки.Добавить("ДатаИнтервала", Новый ОписаниеТипов("Дата"));
	ЗагрузкаВРЦ.Колонки.Добавить("Занято", Новый ОписаниеТипов("Число")); // Время
	ЗагрузкаВРЦ.Колонки.Добавить("Распоряжение", Новый ОписаниеТипов("ДокументСсылка.ЗаказНаПроизводство2_2"));
	ЗагрузкаВРЦ.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	ЗагрузкаВРЦ.Колонки.Добавить("Смена", Новый ОписаниеТипов("СправочникСсылка.Календари"));
	ЗагрузкаВРЦ.Колонки.Добавить("КлючПартия", Новый ОписаниеТипов("УникальныйИдентификатор"));
	ЗагрузкаВРЦ.Колонки.Добавить("Этап", Новый ОписаниеТипов("СправочникСсылка.ЭтапыПроизводства, ДокументСсылка.ЭтапПроизводства2_2"));
	ЗагрузкаВРЦ.Колонки.Добавить("Интервал", Новый ОписаниеТипов("СтрокаТаблицыЗначений"));
	
	// Служебное поле. Для обычных ВРЦ идентично Занято, для параллельных хранит данные как (Время * Загрузка).
	ЗагрузкаВРЦ.Колонки.Добавить("ЗанятоВремяЗагрузка", Новый ОписаниеТипов("Число"));
	
	ДанныеРасчета.Вставить("ЗагрузкаВРЦ", ЗагрузкаВРЦ);
	
	Возврат ДанныеРасчета;
	
КонецФункции

Процедура ОжидатьЗавершениеРасчетаСтруктурыЗаказа(ЗаказНаПроизводство, ОповещениеСтатусРасчетаСтруктуры)
	
	ПротоколРасчета = Документы.ЗаказНаПроизводство2_2.ПротоколРасчетаСтруктурыЗаказа(ЗаказНаПроизводство);
	
	ТекстСтруктураНеРассчитана =
		НСтр("ru = 'Структура заказа на производство не рассчитана. Рекомендуется пересчитать график после расчета структуры.';
			|en = 'Структура заказа на производство не рассчитана. Рекомендуется пересчитать график после расчета структуры.'");
	
	Если ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.Рассчитан Тогда
		Возврат;
	ИначеЕсли ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.НеРассчитан
		Или ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.РассчитанЕстьОшибки
		Или ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.НеРассчитанЕстьОшибки
		Или ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.Отменяется Тогда
		ОповещениеСтатусРасчетаСтруктуры = ТекстСтруктураНеРассчитана;
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	Отбор.Вставить("Наименование", Обработки.ПланированиеГрафикаПроизводства2_2.НаименованиеЗаданияРасчетаГрафика());
	
	Задания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	Если Задания.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
	ТекущееЗадание = Задания[0];
	
	СрокОжидания = ТекущаяДатаСеанса() + 600;
	Таймаут = 1;
	
	Пока ТекущаяДатаСеанса() < СрокОжидания Цикл
		ТекущееЗадание = ТекущееЗадание.ОжидатьЗавершенияВыполнения(Таймаут);
		
		ПротоколРасчета = Документы.ЗаказНаПроизводство2_2.ПротоколРасчетаСтруктурыЗаказа(ЗаказНаПроизводство);
		Если ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.Рассчитан Тогда
			Возврат;
		ИначеЕсли ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.НеРассчитан
			Или ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.РассчитанЕстьОшибки
			Или ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.НеРассчитанЕстьОшибки
			Или ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.Отменяется Тогда
			ОповещениеСтатусРасчетаСтруктуры = ТекстСтруктураНеРассчитана;
			Возврат;
		КонецЕсли;
		
		Если Таймаут < 14 Тогда
			Таймаут = Таймаут * 1.4;
		КонецЕсли;
	КонецЦикла;
	
	// Для состояний: ОжидаетРасчета, Рассчитывается
	ОповещениеСтатусРасчетаСтруктуры =
		НСтр("ru = 'Структура заказа на производство находится в процессе расчета. Рекомендуется пересчитать график после завершения расчета структуры.';
			|en = 'Структура заказа на производство находится в процессе расчета. Рекомендуется пересчитать график после завершения расчета структуры.'");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
