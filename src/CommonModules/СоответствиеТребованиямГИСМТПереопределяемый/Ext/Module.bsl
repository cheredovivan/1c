
#Область ПрограммныйИнтерфейс

// Перед определением параметров интеграции.
// Определяет необходимость встраивания гиперссылки в форме документа
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ВстроитьГиперссылку - Булево
Процедура ПередОпределениемПараметровИнтеграции(Форма, ВстроитьГиперссылку) Экспорт
	
	Если Форма.ИмяФормы = "Документ.ПриобретениеТоваровУслуг.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПриемкаТоваровНаХранение.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПоступлениеТоваровОтХранителя.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПоступлениеТоваровОтКлиента.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ВозвратТоваровМеждуОрганизациями.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ВозвратТоваровОтКлиента.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ВозвратТоваровПоставщику.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.КорректировкаРеализации.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПередачаТоваровМеждуОрганизациями.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПередачаТоваровХранителю.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПеремещениеТоваров.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.СборкаТоваров.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ОтгрузкаТоваровСХранения.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ОтгрузкаТоваровКлиенту.Форма.ФормаДокумента"

//++ НЕ УТ

		Или Форма.ИмяФормы = "Документ.ДвижениеПродукцииИМатериалов.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПроизводствоБезЗаказа.Форма.ФормаДокумента"

//-- НЕ УТ

//++ НЕ УТКА

		Или Форма.ИмяФормы = "Документ.ЭтапПроизводства2_2.Форма.ФормаДокумента"

//-- НЕ УТКА

	Тогда
		ВстроитьГиперссылку = Истина;
	КонецЕсли;
	
КонецПроцедуры

// При определении параметров интеграции.
// Определяет группу для размещения статуса проверки документа в ГИС МТ
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ПараметрыИнтеграции - Структура:
// * ИмяРеквизитаФормыОбъект - Строка - Имя реквизита объекта в форме
// * РазмещатьЭлементыИнтерфейса - Булево - Размещать элементы интерфейса
// * ИмяРодительскойГруппыФормы - Строка - Имя группы, в которую будет встроена гиперссылка
// * ИмяКомандыСоответствиеТребованиямГИСМТ - Строка - Дата документа
// * ЗапуститьПроверкуДокументаВФоне - Булево - значение константы ИспользоватьФоновуюПроверкуДокументовГИСМТ
// * ИнтервалОбработкиОжидания - Строка - интервал обработки ожидания при фоновой проверке
Процедура ПриОпределенииПараметровИнтеграции(Форма, ПараметрыИнтеграции) Экспорт
	
	Если Форма.ИмяФормы = "Документ.ПередачаТоваровХранителю.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПеремещениеТоваров.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.СборкаТоваров.Форма.ФормаДокумента"

//++ НЕ УТ

		Или Форма.ИмяФормы = "Документ.ДвижениеПродукцииИМатериалов.Форма.ФормаДокумента"
		Или Форма.ИмяФормы = "Документ.ПроизводствоБезЗаказа.Форма.ФормаДокумента"

//-- НЕ УТ

//++ НЕ УТКА

		Или Форма.ИмяФормы = "Документ.ЭтапПроизводства2_2.Форма.ФормаДокумента"

//-- НЕ УТКА

	Тогда
		ПараметрыИнтеграции.ИмяРодительскойГруппыФормы  = "";
	Иначе
		ПараметрыИнтеграции.ИмяРодительскойГруппыФормы  = "ГруппаСостояниеЭДО";
	КонецЕсли;
	
	ПараметрыИнтеграции.РазмещатьЭлементыИнтерфейса = Истина;
	
КонецПроцедуры

// При определении реквизитов документа.
// Необходимо заполнить в структуре РеквизитыДокумента из прикладного документа поля: Дата, Организация, Контрагент.
// Параметры:
//  Документ - ОпределяемыйТип.ПрикладныеДокументыГИСМТ
//  РеквизитыДокумента - Структура:
// * Дата - Дата - Дата документа
// * Организация - Неопределено, ОпределяемыйТип.Организация - Организация
// * Контрагент - Неопределено, ОпределяемыйТип.КонтрагентГосИС - Контрагент
// * ДанныеУчастниковМОД - Структура - см. ЭлектронноеВзаимодействиеИСМП.СтруктураАдресовУчастниковОбмена - структура с дополнительными данными реквизитов, влияющих
//		на расчет статусов МОДов - ИНН/КПП грузоотправителя/грузополучателя, адреса МОД индивидуальных предпринимателей
//		Заполнять только при указании параметра ДополнитьРеквизитамиРасчетаМОД = Истина для уменьшения нагрузки на систему.
// ДополнитьРеквизитамиРасчетаМОД - Булево - если Истина, заполнить в РеквизитыДокумента поле ДанныеУчастниковМОД
//		данными, влияющими на перерасчет статуса МОД (см. ЭлектронноеВзаимодействиеИСМП.СтруктураАдресовУчастниковОбмена).
Процедура ПриОпределенииРеквизитовДокумента(Документ, РеквизитыДокумента, ДополнитьРеквизитамиРасчетаМОД = Ложь) Экспорт
	
	ТипДокумента = ТипЗнч(Документ);
	Если ТипДокумента = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипДокумента = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
		Или ТипДокумента = Тип("ДокументСсылка.ПриемкаТоваровНаХранение")
		Или ТипДокумента = Тип("ДокументСсылка.ПоступлениеТоваровОтКлиента")
		Или ТипДокумента = Тип("ДокументСсылка.ПоступлениеТоваровОтХранителя")
		Или ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
		Или ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
		Или ТипДокумента = Тип("ДокументСсылка.КорректировкаРеализации")
		Или ТипДокумента = Тип("ДокументСсылка.КорректировкаПриобретения")
		Или ТипДокумента = Тип("ДокументСсылка.ПередачаТоваровХранителю")
		Или ТипДокумента = Тип("ДокументСсылка.ОтгрузкаТоваровСХранения")
		Или ТипДокумента = Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту")
	Тогда
		
		Реквизиты = "Дата, Организация, Контрагент";
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			Реквизиты = Реквизиты + ", ХозяйственнаяОперация, ВариантВыбытияМаркируемойПродукции";
		КонецЕсли;
		
		Если ДополнитьРеквизитамиРасчетаМОД Тогда
		
			Если ТипДокумента = Тип("ДокументСсылка.РеализацияТоваровУслуг")
				Или ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
				Или ТипДокумента = Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту")
				Или ТипДокумента = Тип("ДокументСсылка.ПередачаТоваровХранителю") Тогда
				Реквизиты = Реквизиты + ", Склад, АдресДоставки, АдресДоставкиЗначение, АдресДоставкиЗначенияПолей, Грузоотправитель, Грузополучатель";
			КонецЕсли;
			
		КонецЕсли;
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, Реквизиты);
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ЗначенияРеквизитов);
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
			И ЗначенияРеквизитов.ВариантВыбытияМаркируемойПродукции = Перечисления.ВариантыВыбытияМаркируемойПродукции.ИспользованиеПокупателемДляСобственныхНужд Тогда
				РеквизитыДокумента.Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		КонецЕсли;
		
		Если Не ДополнитьРеквизитамиРасчетаМОД Тогда
			Возврат;
		КонецЕсли;
		
		Если (ТипДокумента = Тип("ДокументСсылка.РеализацияТоваровУслуг")
				И ЗначенияРеквизитов.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПереходПраваСобственности)
			Или ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
			Или ТипДокумента = Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту")
			Или ТипДокумента = Тип("ДокументСсылка.ПередачаТоваровХранителю") Тогда
			
			РеквизитыДокумента.ДанныеУчастниковМОД = ЭлектронноеВзаимодействиеИСМП.СтруктураАдресовУчастниковОбмена();
			
			ДанныеПокупателя = РеквизитыДокумента.ДанныеУчастниковМОД.Покупатель;
			ДанныеПродавца   = РеквизитыДокумента.ДанныеУчастниковМОД.Продавец;
			
			ОбъектПокупатель = Неопределено;
			ОбъектПродавец   = Неопределено;
			ВидКИПокупателя  = Неопределено;
			
			Если ЗначениеЗаполнено(ЗначенияРеквизитов.Грузополучатель) Тогда
				
				ОбъектПокупатель                                   = ЗначенияРеквизитов.Грузополучатель;
				ДанныеПокупателя.УполномоченноеЛицоЗаПоставкуГруза = ОбъектПокупатель;
				ВидКИПокупателя                                    = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
				
			Иначе
				ОбъектПокупатель = ЗначенияРеквизитов.Контрагент;
				ВидКИПокупателя  = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗначенияРеквизитов.Грузоотправитель) И ЗначенияРеквизитов.Грузоотправитель <> ЗначенияРеквизитов.Организация Тогда
				
				ОбъектПродавец                                   = ЗначенияРеквизитов.Грузоотправитель;
				ДанныеПродавца.УполномоченноеЛицоЗаПоставкуГруза = ОбъектПродавец;
				
			Иначе
				ОбъектПродавец = ЗначенияРеквизитов.Организация;
			КонецЕсли;
			
			ДанныеПродавца.УчастникОбмена   = ЗначенияРеквизитов.Организация;
			ДанныеПокупателя.УчастникОбмена = ЗначенияРеквизитов.Контрагент;
			
			Если ЗначениеЗаполнено(ЗначенияРеквизитов.АдресДоставкиЗначенияПолей) Тогда
				
				ДанныеПокупателя.АвтоматическоеЗаполнение = Ложь;
				ДанныеПокупателя.АдресСтрокой             = ЗначенияРеквизитов.АдресДоставки;
				ДанныеПокупателя.АдресЗначенияПолей       = ЗначенияРеквизитов.АдресДоставкиЗначенияПолей;
				
			Иначе
				
				ДанныеПокупателя.АвтоматическоеЗаполнение     = Истина;
				ДанныеПокупателя.ВладелецКонтактнойИнформации = ОбъектПокупатель;
				ДанныеПокупателя.ВидКонтактнойИнформации      = ВидКИПокупателя;
				
			КонецЕсли;
			
			ДанныеПродавца.АвтоматическоеЗаполнение     = Истина;
			ДанныеПродавца.ВладелецКонтактнойИнформации = ЗначенияРеквизитов.Склад;
			ДанныеПродавца.ВидКонтактнойИнформации      = Справочники.ВидыКонтактнойИнформации.АдресСклада;
			
		КонецЕсли;
	
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
		Или ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
//++ НЕ УТ
		Или ТипДокумента = Тип("ДокументСсылка.ДвижениеПродукцииИМатериалов")
//-- НЕ УТ

	Тогда
		
		Реквизиты = "Дата, Организация, ОрганизацияПолучатель";
		
		Если ДополнитьРеквизитамиРасчетаМОД Тогда
			
			Если ТипДокумента = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
				Или ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
				Реквизиты = Реквизиты + ", Склад, Грузоотправитель, Грузополучатель";
			КонецЕсли;
			
		КонецЕсли;
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, Реквизиты);
		
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ЗначенияРеквизитов);
		
		РеквизитыДокумента.Контрагент = ЗначенияРеквизитов.ОрганизацияПолучатель;
		
		Если Не ДополнитьРеквизитамиРасчетаМОД Тогда
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
			
			РеквизитыДокумента.ДанныеУчастниковМОД = ЭлектронноеВзаимодействиеИСМП.СтруктураАдресовУчастниковОбмена();
			
			ДанныеПокупателя = РеквизитыДокумента.ДанныеУчастниковМОД.Покупатель;
			ДанныеПродавца   = РеквизитыДокумента.ДанныеУчастниковМОД.Продавец;
			
			ОбъектПокупатель = Неопределено;
			ОбъектПродавец   = Неопределено;
			ВидКИПокупателя  = Неопределено;
			
			Если ЗначениеЗаполнено(ЗначенияРеквизитов.Грузополучатель) Тогда
				
				ОбъектПокупатель                                   = ЗначенияРеквизитов.Грузополучатель;
				ДанныеПокупателя.УполномоченноеЛицоЗаПоставкуГруза = ОбъектПокупатель;
				ВидКИПокупателя                                    = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
				
			Иначе
				ОбъектПокупатель = ЗначенияРеквизитов.ОрганизацияПолучатель;
				ВидКИПокупателя  = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗначенияРеквизитов.Грузоотправитель) И ЗначенияРеквизитов.Грузоотправитель <> ЗначенияРеквизитов.Организация Тогда
				
				ОбъектПродавец                                   = ЗначенияРеквизитов.Грузоотправитель;
				ДанныеПродавца.УполномоченноеЛицоЗаПоставкуГруза = ОбъектПродавец;
				
			Иначе
				ОбъектПродавец = ЗначенияРеквизитов.Организация;
			КонецЕсли;
			
			ДанныеПродавца.УчастникОбмена   = ЗначенияРеквизитов.Организация;
			ДанныеПокупателя.УчастникОбмена = ЗначенияРеквизитов.ОрганизацияПолучатель;
			
			ДанныеПокупателя.АвтоматическоеЗаполнение     = Истина;
			ДанныеПокупателя.ВладелецКонтактнойИнформации = ЗначенияРеквизитов.Склад;
			ДанныеПокупателя.ВидКонтактнойИнформации      = Справочники.ВидыКонтактнойИнформации.АдресСклада;
			
			ДанныеПродавца.АвтоматическоеЗаполнение     = Истина;
			ДанныеПродавца.ВладелецКонтактнойИнформации = ЗначенияРеквизитов.Склад;
			ДанныеПродавца.ВидКонтактнойИнформации      = Справочники.ВидыКонтактнойИнформации.АдресСклада;
			
		ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
			
			РеквизитыДокумента.ДанныеУчастниковМОД = ЭлектронноеВзаимодействиеИСМП.СтруктураАдресовУчастниковОбмена();
			
			ДанныеПокупателя = РеквизитыДокумента.ДанныеУчастниковМОД.Покупатель;
			ДанныеПродавца   = РеквизитыДокумента.ДанныеУчастниковМОД.Продавец;
			
			ОбъектПокупатель = Неопределено;
			ОбъектПродавец   = Неопределено;
			ВидКИПокупателя  = Неопределено;
			
			Если ЗначениеЗаполнено(ЗначенияРеквизитов.Грузополучатель) Тогда
				
				ОбъектПокупатель                                   = ЗначенияРеквизитов.Грузополучатель;
				ДанныеПокупателя.УполномоченноеЛицоЗаПоставкуГруза = ОбъектПокупатель;
				ВидКИПокупателя                                    = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
				
			Иначе
				ОбъектПокупатель = ЗначенияРеквизитов.ОрганизацияПолучатель;
				ВидКИПокупателя  = Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗначенияРеквизитов.Грузоотправитель) И ЗначенияРеквизитов.Грузоотправитель <> ЗначенияРеквизитов.Организация Тогда
				
				ОбъектПродавец                                   = ЗначенияРеквизитов.Грузоотправитель;
				ДанныеПродавца.УполномоченноеЛицоЗаПоставкуГруза = ОбъектПродавец;
				
			Иначе
				ОбъектПродавец = ЗначенияРеквизитов.Организация;
			КонецЕсли;
			
			ДанныеПродавца.УчастникОбмена   = ЗначенияРеквизитов.Организация;
			ДанныеПокупателя.УчастникОбмена = ЗначенияРеквизитов.ОрганизацияПолучатель;
			
			ДанныеПокупателя.АвтоматическоеЗаполнение     = Истина;
			ДанныеПокупателя.ВладелецКонтактнойИнформации = ЗначенияРеквизитов.Склад;
			ДанныеПокупателя.ВидКонтактнойИнформации      = Справочники.ВидыКонтактнойИнформации.АдресСклада;
			
			ДанныеПродавца.АвтоматическоеЗаполнение     = Истина;
			ДанныеПродавца.ВладелецКонтактнойИнформации = ЗначенияРеквизитов.Склад;
			ДанныеПродавца.ВидКонтактнойИнформации      = Справочники.ВидыКонтактнойИнформации.АдресСклада;
			
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.СборкаТоваров")
		Или ТипДокумента = Тип("ДокументСсылка.ПеремещениеТоваров")
//++ НЕ УТ
		Или ТипДокумента = Тип("ДокументСсылка.ПроизводствоБезЗаказа")
//-- НЕ УТ

//++ НЕ УТКА
		Или ТипДокумента = Тип("ДокументСсылка.ЭтапПроизводства2_2")
//-- НЕ УТКА
		
		Тогда
		
		Реквизиты = "Дата, Организация";
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, Реквизиты);
		
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, ЗначенияРеквизитов);
		
	КонецЕсли;
	
КонецПроцедуры

// При определении типа документа.
// Необходимо заполнить тип документа ГИС МТ.
// Параметры:
//  Документ - ОпределяемыйТип.ПрикладныеДокументыГИСМТ
//  ТипДокументаГИСМТ - ПеречислениеСсылка.ТипыДокументовГИСМТ - Тип документа
Процедура ПриОпределенииТипаДокументаГИСМТ(Документ, ТипДокументаГИСМТ) Экспорт
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ПоступлениеТоваровОтКлиента")
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ПоступлениеТоваровОтХранителя")
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ПриемкаТоваровНаХранение")
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		
		ТипДокументаГИСМТ = Перечисления.ТипыДокументовГИСМТ.УПД;
		
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.КорректировкаРеализации")
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
		
		ХозяйственнаяОперация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ВидКорректировки");
		
		Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИсправлениеОшибок Тогда
			ТипДокументаГИСМТ = Перечисления.ТипыДокументовГИСМТ.УПДИсправительный;
		Иначе
			ТипДокументаГИСМТ = Перечисления.ТипыДокументовГИСМТ.УКД;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ПередачаТоваровХранителю")
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ОтгрузкаТоваровСХранения")
		Или ТипЗнч(Документ) = Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту")
	Тогда
		
		ИсправляемыйДокумент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ИсправляемыйДокумент");
		Если ЗначениеЗаполнено(ИсправляемыйДокумент) Тогда
			ТипДокументаГИСМТ = Перечисления.ТипыДокументовГИСМТ.УПДИсправительный;
		Иначе
			ТипДокументаГИСМТ = Перечисления.ТипыДокументовГИСМТ.УПД;
		КонецЕсли;
	Иначе
		ТипДокументаГИСМТ = Перечисления.ТипыДокументовГИСМТ.ВнутреннееПотребление;
	КонецЕсли;
	
КонецПроцедуры

// При выполнении отдельных проверок продавца и покупателя.
// Параметры:
//  ПараметрыОбработки - Структура
Процедура ПриВыполненииОтдельныхПроверокПродавцаИПокупателя(ПараметрыОбработки) Экспорт
	
	Если Не ОбщегоНазначенияУТВызовСервера.ЗначениеФункциональнойОпции("ИспользоватьПередачиТоваровМеждуОрганизациями") Тогда
		Возврат;
	КонецЕсли;
	
	ОрганизацияИНН = ПараметрыОбработки.РеквизитыДокумента.ОрганизацияИНН;
	ДанныеШтрихкодов = ПараметрыОбработки.ДанныеШтрихкодов.ДанныеШтрихкодовДокумента;
	
	МассивИНН = Новый Массив;
	Для Каждого Строка Из ДанныеШтрихкодов Цикл
		
		Если Строка.ДанныеСервиса = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИННПоДаннымСервиса = Строка.ДанныеСервиса.ИННВладельца;
		
		МассивИНН.Добавить(ИННПоДаннымСервиса);
		
	КонецЦикла;
	
	СоответствиеОрганизаций = ИнтеграцияИСМП.ОрганизацииПоСпискуИНН(МассивИНН);
	
	НачатьТранзакцию();
	
	Попытка
		
		Для Каждого Строка Из ДанныеШтрихкодов Цикл
			
			Если Строка.ДанныеСервиса = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ИННПоДаннымСервиса = Строка.ДанныеСервиса.ИННВладельца;
			
			ОрганизацияВладелец = СоответствиеОрганизаций[ИННПоДаннымСервиса];
			
			Если ИННПоДаннымСервиса <> ОрганизацияИНН И ОрганизацияВладелец <> Неопределено Тогда
				
				МенеджерЗаписи = РегистрыСведений.КодыМаркировкиКПередачеМеждуОрганизациями.СоздатьМенеджерЗаписи();
				МенеджерЗаписи.ШтрихкодУпаковки      = Строка.ШтрихкодУпаковки;
				МенеджерЗаписи.Организация           = ОрганизацияВладелец;
				МенеджерЗаписи.ОрганизацияПолучатель = ПараметрыОбработки.РеквизитыДокумента.Организация;
				МенеджерЗаписи.Записать();
				
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	
	Исключение
		
		ОтменитьТранзакцию();
	
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'При добавлении записи о передаче кодов маркировки между организациями по документу %1 возникла ошибка:
			           |Текст ошибки: %2';
			           |en = 'При добавлении записи о передаче кодов маркировки между организациями по документу %1 возникла ошибка:
			           |Текст ошибки: %2'"),
			ПараметрыОбработки.Документ,
			ПодробноеПредставлениеОшибки);
		
		ОбщегоНазначенияИСВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
			ТекстОшибки,
			НСтр("ru = 'ИСМП';
				|en = 'ИСМП'", ОбщегоНазначения.КодОсновногоЯзыка()),
			Метаданные.РегистрыСведений.КодыМаркировкиКПередачеМеждуОрганизациями);
		
		ВызватьИсключение ПодробноеПредставлениеОшибки;
	КонецПопытки;
	
КонецПроцедуры

// После проверки статуса ГИСМТ.
// Вызывается после проверки статуса документа в ГИС МТ. 
// Позволяет выполнить обработку, после получения статуса документа из сервиса ГИСМТ.
// Параметры:
//  ПараметрыОбработки - см. СоответствиеТребованиямГИСМТ.ИнициализироватьПараметрыОбработки
Процедура ПослеПроверкиСтатусаГИСМТ(ПараметрыОбработки) Экспорт
	
	Если Не ОбщегоНазначенияУТВызовСервера.ЗначениеФункциональнойОпции("ИспользоватьПередачиТоваровМеждуОрганизациями") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыОбработки.Статус = Перечисления.СтатусыДокументовИСМП.Проверен
		И ПараметрыОбработки.СтатусОтправки = Перечисления.СтатусОтправкиГИСМТ.ДоставленВГИСМТ Тогда
		ШтрихкодыДокумента = ПараметрыОбработки.ДанныеШтрихкодов.ДанныеШтрихкодовДокумента.ВыгрузитьКолонку("ШтрихкодУпаковки");
		
		ПараметрыПередачи = Новый Структура;
		ПараметрыПередачи.Вставить("ШтрихкодыДокумента", ШтрихкодыДокумента);
		ПараметрыПередачи.Вставить("Организация",        ПараметрыОбработки.Организация);
		ПараметрыПередачи.Вставить("Документ",           ПараметрыОбработки.Документ);
		
		ИнтеграцияИСМПУТ.ЗавершитьПередачуКодовМаркировкиМеждуОрганизациями(ПараметрыПередачи);
			
	КонецЕсли;
	
КонецПроцедуры

// При получении статуса документа из очереди.
// Вызывается после проверки статуса документа в ГИС МТ. 
// Позволяет выполнить обработку, после получения статуса документа из сервиса ГИСМТ.
// Параметры:
//  СтрокаОчереди - СтрокаТаблицыЗначений из см.СоответствиеТребованиямГИСМТ.ИнициализироватьОчередьДокументов.
Процедура ПриПолученииСтатусаДокументаИзОчереди(СтрокаОчереди) Экспорт
	
	Если Не ОбщегоНазначенияУТВызовСервера.ЗначениеФункциональнойОпции("ИспользоватьПередачиТоваровМеждуОрганизациями") Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрокаОчереди.Статус = Перечисления.СтатусыДокументовИСМП.Проверен
		И СтрокаОчереди.СтатусОтправки = Перечисления.СтатусОтправкиГИСМТ.ДоставленВГИСМТ Тогда
		
		ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования(СтрокаОчереди.Документ);
		
		ШтрихкодыУпаковокПоДокументу = ШтрихкодированиеИСМПСлужебный.ШтрихкодыУпаковокИзДокумента(
			СтрокаОчереди.Документ, ПараметрыСканирования);
		
		ШтрихкодыДокумента = ШтрихкодыУпаковокПоДокументу.ВыгрузитьКолонку("ШтрихкодУпаковки");
		
		ПараметрыПередачи = Новый Структура;
		ПараметрыПередачи.Вставить("ШтрихкодыДокумента", ШтрихкодыДокумента);
		ПараметрыПередачи.Вставить("Организация",        СтрокаОчереди.Организация);
		ПараметрыПередачи.Вставить("Документ",           СтрокаОчереди.Документ);
		
		ИнтеграцияИСМПУТ.ЗавершитьПередачуКодовМаркировкиМеждуОрганизациями(ПараметрыПередачи);
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти