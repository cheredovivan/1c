#Область СлужебныеПроцедурыИФункции

Процедура ЗавершитьЗаявкиСотрудниковПослеПодписанияСправок() Экспорт
	
	Если Не Константы.ЗавершатьЗаявкиСотрудниковПослеПодписанияСправки.Получить() Тогда
		Возврат;
	КонецЕсли;
	
	БизнесПроцессыЗаявокСотрудников.ЗавершитьЗаявкиСотрудникаСПодписаннымФайломОтвета();
	
КонецПроцедуры

Процедура ОтметитьОжидающиеПодписиЗаявкиСотрудника(Источник) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяЭДОБЗККабинетСотрудника") Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.Отбор.Действие.Значение <> Перечисления.ДействияСФайламиДокументовЭДОБЗК.Подписать Тогда
		Возврат;
	КонецЕсли;
	
	ПрисоединенныйФайл = Источник.Отбор.ПрисоединенныйФайл.Значение;
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗапланированныеДействияСФайламиДокументовЭДОБЗК.ПрисоединенныйФайл КАК ПрисоединенныйФайл
	               |ИЗ
	               |	РегистрСведений.ЗапланированныеДействияСФайламиДокументовЭДОБЗК КАК ЗапланированныеДействияСФайламиДокументовЭДОБЗК
	               |ГДЕ
	               |	ЗапланированныеДействияСФайламиДокументовЭДОБЗК.ПрисоединенныйФайл = &ПрисоединенныйФайл
	               |	И ЗапланированныеДействияСФайламиДокументовЭДОБЗК.Исполнитель <> &Исполнитель
	               |	И ЗапланированныеДействияСФайламиДокументовЭДОБЗК.Действие = &Действие";
	
	Запрос.УстановитьПараметр("ПрисоединенныйФайл", ПрисоединенныйФайл);
	Запрос.УстановитьПараметр("Исполнитель", Источник.Отбор.Исполнитель.Значение);
	Запрос.УстановитьПараметр("Действие", Источник.Отбор.Действие.Значение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ПолноеУдаление = (Выборка.Количество() = 0) И Источник.Количество() = 0;
	
	ТипПрисоединенныйФайл = ТипЗнч(ПрисоединенныйФайл);
	
	Если ТипПрисоединенныйФайл = Тип("СправочникСсылка.СправкаНДФЛПрисоединенныеФайлы") Тогда
		БизнесПроцессы.ЗаявкаСотрудникаСправка2НДФЛ.ОтметитьОжидаетПодписание(
			ПрисоединенныйФайл,
			ПолноеУдаление);
		Возврат;		
	КонецЕсли;
	
	ТипЗаявкаСотрудникаСправкаСМестаРаботы = БизнесПроцессыЗаявокСотрудниковВнутренний.ТипЗаявкаСотрудникаСправкаСМестаРаботы();
	Если ТипЗаявкаСотрудникаСправкаСМестаРаботы = Тип("Неопределено") Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПрисоединенныеФайлыСправкаСМестаРаботы = РаботаСФайламиСлужебный.ИмяСправочникаХраненияФайлов(
													Новый(ТипЗаявкаСотрудникаСправкаСМестаРаботы));
	Если ТипПрисоединенныйФайл = Тип("СправочникСсылка."+ИмяПрисоединенныеФайлыСправкаСМестаРаботы) Тогда	
		БизнесПроцессыЗаявокСотрудниковВнутренний.ОтметитьОжидаетПодписиЗаявкаСотрудникаСправкаСМестаРаботы(
			ПрисоединенныйФайл,
			ПолноеУдаление);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти