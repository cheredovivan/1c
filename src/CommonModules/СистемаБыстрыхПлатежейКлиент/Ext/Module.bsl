///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП".
// ОбщийМодуль.СистемаБыстрыхПлатежейКлиент.
//
// Клиентские процедуры настройки подключения к Системе быстрых платежей:
//  - открытие форм настройки параметров подключения;
//  - отправка сообщений в службу технической поддержки;
//  - переход в журнал регистрации для просмотра лога;
//  - создание новых настроек подключения;
//  - алгоритмы настройки формы "Интернет-поддержка и сервисы";
//  - переход к форме отправки платежной ссылки.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Открывает форму выбора настроек подключения с Системой быстрых платежей.
//
// Параметры:
//  ОписаниеОповещения - ОписаниеОповещения, Неопределено - оповещение, которое
//  необходимо вызвать выбора настройки. В качестве результата передается ссылка на выбранную
//    настройку или неопределенно;
//  НастройкиПодключения - Массив из СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - отбор
//  настроек подключения к Системе быстрых платежей;
//  Владелец - ФормаКлиентскогоПриложения - форма которая будет установлена в качестве владельца.
//
Процедура ПоказатьВыборНастройки(
		ОписаниеОповещения,
		НастройкиПодключения,
		Владелец) Экспорт
	
	// Проверка входных параметров
	ИмяФункции = "СистемаБыстрыхПлатежейКлиент.ПоказатьВыборНастройки";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"НастройкиПодключения",
		НастройкиПодключения,
		Тип("Массив"));
		
	Отбор = Новый Структура;
	Отбор.Вставить("Ссылка", НастройкиПодключения);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("Отображение", "Список");
	
	ОткрытьФорму(
		"Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей.ФормаВыбора",
		ПараметрыФормы,
		Владелец,
		,
		,
		,
		ОписаниеОповещения);
	
КонецПроцедуры

// Отправляет сообщение в службу технической поддержки.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ, который отражает
//    продажу в информационной базе;
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//    настройка выполнения оплаты;
//  ТекстСообщения - Строка - сообщение для технической поддержки.
//
Процедура ОтправитьСообщениеВСлужбуТехническойПоддержки(
		ДокументОперации,
		НастройкаПодключения,
		ТекстСообщения = "") Экспорт
	
		// Проверка входных параметров
	ИмяФункции = "СистемаБыстрыхПлатежейКлиент.ОтправитьСообщениеВСлужбуТехническойПоддержки";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"НастройкаПодключения",
		НастройкаПодключения,
		Тип("СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей"));
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СообщенияВСлужбуТехническойПоддержки") Тогда
		
		ДанныеОперации = СистемаБыстрыхПлатежейВызовСервера.ИнформацияДляТехническойПоддержки(
			ДокументОперации,
			НастройкаПодключения);
		
		Вложения = Новый Массив;
		Вложения.Добавить(
			Новый Структура(
				"Представление, ВидДанных, Данные",
				НСтр("ru = 'Служебные данные операции.txt';
					|en = 'Internal transaction data.txt'"),
				"Текст",
				ДанныеОперации));
		
		МодульСообщенияВСлужбуТехническойПоддержкиКлиентСервер =
			ОбщегоНазначенияКлиент.ОбщийМодуль("СообщенияВСлужбуТехническойПоддержкиКлиентСервер");
		ДанныеСообщения = МодульСообщенияВСлужбуТехническойПоддержкиКлиентСервер.ДанныеСообщения();
		ДанныеСообщения.Получатель = "webIts";
		ДанныеСообщения.Тема       = НСтр("ru = 'Интернет-поддержка. Система быстрых платежей';
											|en = 'Online support. Faster Payments System'");
		ДанныеСообщения.Сообщение  = ТекстСообщения;
		
		МодульСообщенияВСлужбуТехническойПоддержкиКлиент =
			ОбщегоНазначенияКлиент.ОбщийМодуль("СообщенияВСлужбуТехническойПоддержкиКлиент");
		МодульСообщенияВСлужбуТехническойПоддержкиКлиент.ОтправитьСообщение(
			ДанныеСообщения,
			Вложения);
	Иначе
		ВызватьИсключение НСтр("ru = 'Для отправки сообщений в техническую поддержку необходимо встроить подсистему ""СообщенияВСлужбуТехническойПоддержки""';
								|en = 'To send messages to the technical support, embed the ""СообщенияВСлужбуТехническойПоддержки"" subsystem'");
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Сформировано сообщение в техническую поддержку.';
			|en = 'Message to the technical support is created.'"),
		,
		,
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

// Формирует текст подсказки настройки подключения к Системе быстрых платежей.
//
// Параметры:
//  НаименованиеУчастника - Строка - наименование банка участника СБП;
//  ПараметрыПодсказки - Структура - результат создания параметров подсказки:
//   * АдресЛичногоКабинета - Строка - ссылка для перехода в личный кабинет;
//   * ПартнерАгентаСБП - Строка - признак партнера Агента СБП;
//   * АдресСтраницыЗаявки - Строка - адрес страницы отправки заявки;
//   * ИдентификаторУчастника - Строка - идентификатор участника СБП.
//
// Возвращаемое значение:
//  Строка, ФорматированнаяСтрока - подсказка при настройке подключения.
//
Функция ТекстПодсказкиПодключения(
		НаименованиеУчастника,
		ПараметрыПодсказки) Экспорт
	
	Возврат СтроковыеФункцииКлиент.ФорматированнаяСтрока(
		СистемаБыстрыхПлатежейКлиентСервер.ТекстПодсказкиПодключенияБезФорматирования(
			НаименованиеУчастника,
			ПараметрыПодсказки,
			ПредопределенноеЗначение("Перечисление.ВариантыНастройкиСБП.c2b")));
		
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать НастройкиПлатежныхСистемКлиент.НастройкиПодключения.
// Открывает форму списка настроек подключения с Системой быстрых платежей.
//
// Параметры:
//  Владелец - ФормаКлиентскогоПриложения - форма которая будет установлена в качестве владельца.
//
Процедура НастройкиПодключения(Владелец) Экспорт
	
	НастройкиПлатежныхСистемКлиент.НастройкиПодключения(
		Владелец,
		"СистемаБыстрыхПлатежей");
	
КонецПроцедуры

// Устарела. Следует использовать НастройкиПлатежныхСистемКлиент.ФормаПользовательскогоСоглашения.
// Открывает форму пользовательского соглашения.
//
// Параметры:
//  ВладелецФормы - ФормаКлиентскогоПриложения -  в качестве владельца может выступить форма или элемент
//  управления другой формы.
//
Процедура ФормаПользовательскогоСоглашения(ВладелецФормы = Неопределено) Экспорт
	
	НастройкиПлатежныхСистемКлиент.ФормаПользовательскогоСоглашения(ВладелецФормы);
	
КонецПроцедуры

// Устарела. Следует использовать НастройкиПлатежныхСистемКлиент.НовыйПараметрыПодключения.
// Открывает форму настройки подключения к Системой быстрых платежей.
//
// Параметры:
//  БИК - Строка, Неопределено - идентификатор банка. Используется для автоматического
//    выбора участника СБП;
//  ОтборУчастников - Строка, Неопределено -  отбор участников СБП. Для варианта настройки
//    "b2b" параметр игнорируется. Возможные значения:
//   ПлатежныеАгрегаторы - будут отображены только платежные агрегаторы в доступных настройках;
//   КассовыеСсылки - будут отображены только участники СБП с поддержкой кассовых ссылок.
//  ОтображатьОписаниеСервиса - Булево - Признак отображения страницы описания сервиса;
//  ВариантНастройки - Строка - вариант настройки подключения к Системе быстрых платежей.
//   Возможные значения:
//    c2b - подключение приема оплат от физических лиц;
//    b2b - подключение приема оплат от юридических лиц.
//
// Возвращаемое значение:
//  Структура - настройки открытия формы подключения:
//    * БИК - Строка, Неопределено - идентификатор банка. Используется для автоматического
//      выбора участника СБП;
//    * ОтборУчастников - Строка, Неопределено - Параметры отбора участников СБП;
//    * ОтображатьОписаниеСервиса - Булево - Признак отображения страницы описания сервиса;
//    * ВариантНастройки - Строка - вариант настройки подключения к Системе быстрых платежей.
//
Функция НовыйПараметрыПодключения(
		БИК,
		ОтборУчастников,
		ОтображатьОписаниеСервиса = Истина,
		ВариантНастройки = "") Экспорт
	
	ПараметрыПодключения = Новый Структура;
	
	ПараметрыПодключения.Вставить("БИК", БИК);
	ПараметрыПодключения.Вставить("ОтборУчастников", ОтборУчастников);
	ПараметрыПодключения.Вставить("ОтображатьОписаниеСервиса", ОтображатьОписаниеСервиса);
	ПараметрыПодключения.Вставить("ВариантНастройки", ВариантНастройки);
	
	Возврат ПараметрыПодключения;
	
КонецФункции

// Устарела. Следует использовать НастройкиПлатежныхСистемКлиент.ПодключитьПлатежнуюСистему.
// Открывает форму настройки подключения к Системой быстрых платежей.
//
// Параметры:
//  ПараметрыПодключения - Структура - см. СистемаБыстрыхПлатежейКлиент.НовыйПараметрыПодключения.
//  ОписаниеОповещения - ОписаниеОповещения, Неопределено - оповещение, которое
//    необходимо вызвать после завершения настройки подключения. В случае успешного
//    завершения настройки подключения в результате оповещения будет возвращено Истина;
//  ДополнительныеПараметры - Структура, Неопределено - дополнительные параметры подключения.
//    Значение будет передано в переопределяемые методы:
//     СистемаБыстрыхПлатежейПереопределяемый.ПриНастройкеЭлементовФормыПодключения;
//     СистемаБыстрыхПлатежейПереопределяемый.ПриЗаполненииФормыНастройкиПодключения.
//
//@skip-check doc-comment-complex-type-with-link
Процедура ПодключитьСистемуБыстрыхПлатежей(
		ПараметрыПодключения,
		ОписаниеОповещения = Неопределено,
		ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыПодключенияПроверка = НастройкиПлатежныхСистемКлиент.НовыйПараметрыПодключения();
	
	Если ПараметрыПодключения <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(
			ПараметрыПодключенияПроверка,
			ПараметрыПодключения);
	КонецЕсли;
	
	ПараметрыПодключенияПроверка.ДополнительныеПараметры = ДополнительныеПараметры;
	ПараметрыПодключенияПроверка.ОписаниеОповещения = ОписаниеОповещения;
	
	НастройкиПлатежныхСистемКлиент.ПодключитьПлатежнуюСистему(
		"СистемаБыстрыхПлатежей",
		ПараметрыПодключенияПроверка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Открывает форму списка шаблонов сообщений.
//
// Параметры:
//  УникальныйИдентификатор - УникальныйИдентификатор - ключ уникальности используемый при открытии формы.
//
Процедура ОткрытьШаблоныСообщений(УникальныйИдентификатор) Экспорт
	
	РезультатПроверки = СистемаБыстрыхПлатежейВызовСервера.ШаблоныСозданы();
	
	Если РезультатПроверки.ШаблоныСозданы Тогда
		ОткрытьФормуШаблонаСообщенийСОтбором(
			РезультатПроверки.Шаблоны,
			УникальныйИдентификатор);
	Иначе
		РезультатПроверки.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
		
		Оповещение = Новый ОписаниеОповещения(
			"ПослеОтветаНаВопросОСозданииШаблонов",
			ЭтотОбъект,
			РезультатПроверки);
		
		ПоказатьВопрос(
			Оповещение,
			НСтр("ru = 'Создать шаблоны сообщений для автоматического заполнения на основании данных документов?';
				|en = 'Do you want to create message templates for automatic population from document data?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму настройки подключения к с Системой быстрых платежей.
//
// Параметры:
//  НастройкиПодключения - Структура - параметры настройки подключения к Системе быстрых платежей:
//    * БИК - Строка, Неопределено - идентификатор банка. Используется для автоматического
//      выбора участника СБП.
//    * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей, Неопределено - настройка
//      для автоматического заполнения параметров подключения на основании;
//    * ДополнительныеПараметры - Структура, Неопределено - дополнительные параметры подключения.
//      Значение будет передано в переопределяемые методы:
//       СистемаБыстрыхПлатежейПереопределяемый.ПриНастройкеЭлементовФормыПодключения;
//       СистемаБыстрыхПлатежейПереопределяемый.ПриЗаполненииФормыНастройкиПодключения.
//  ОписаниеОповещения - ОписаниеОповещения, Неопределено - оповещение, которое
//    необходимо вызвать после завершения настройки подключения. В случае успешного
//    завершения настройки подключения в результате оповещения будет возвращено Истина;
//
Процедура СлужебнаяПодключитьСистемуБыстрыхПлатежей(
		НастройкиПодключения,
		ОписаниеОповещения = Неопределено) Экспорт
	
	Проверка = Новый Структура("ОписаниеОповещения", Неопределено);
	ЗаполнитьЗначенияСвойств(Проверка, НастройкиПодключения);
	Если Проверка.ОписаниеОповещения <> Неопределено Тогда
		НастройкиПодключения.Удалить("ОписаниеОповещения");
	КонецЕсли;
	
	ОткрытьФорму(
		"Обработка.ПодключениеКСБП.Форма",
		НастройкиПодключения,
		,
		,
		,
		,
		ОписаниеОповещения);
	
КонецПроцедуры

// Выполняет обработку выбора строки в общем списке настроек.
// 
// Параметры:
//  ПараметрыВыбора - см. НастройкиПлатежныхСистемКлиент.НовыйПараметрыОбработкиВыбораНастройки
// 
Процедура ПриОбработкеВыбораНастройки(ПараметрыВыбора) Экспорт

	ПараметрыВыбора.ИмяОткрываемойФормы = "Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей.ФормаОбъекта";

	Если ПараметрыВыбора.РежимВыбора Тогда
		
		ЭтоГруппа = ПараметрыВыбора.ТекущиеДанные.ЭтоГруппа;
		
		ТекстОшибки = "";
		
		Если ЭтоГруппа И ПараметрыВыбора.ТолькоЭлементы Тогда
			ТекстОшибки = НСтр("ru = 'Выбор групп запрещен.';
								|en = 'Cannot select groups.'");
		ИначеЕсли Не ЭтоГруппа И ПараметрыВыбора.ТолькоГруппы Тогда
			ТекстОшибки = НСтр("ru = 'Разрешено выбрать только группу.';
								|en = 'You can select only groups.'");
		КонецЕсли;
		
		Если Не ПустаяСтрока(ТекстОшибки) Тогда
			ПараметрыВыбора.Отказ = Истина;
			ПараметрыВыбора.СообщениеОбОшибке = ТекстОшибки;
		КонецЕсли;
		
		Если ТипЗнч(ПараметрыВыбора.РезультатВыбора) = Тип("СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей") Тогда
			
			Идентификатор = СистемаБыстрыхПлатежейВызовСервера.ИдентификаторУчастникаПоСсылке(
				ПараметрыВыбора.РезультатВыбора);
			
			Если Идентификатор = СистемаБыстрыхПлатежейКлиентСервер.ИдентификаторНеизвестногоУчастника() Тогда
				ПараметрыВыбора.ТребуетсяДополнительнаяОбработка = Истина;
				ПараметрыВыбора.ПараметрыДополнительнойОбработки = Новый Структура(
					"ПричинаОбработки",
					"НеУказанИдентификаторУчастника");
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Если ПараметрыВыбора.ТекущиеДанные.ЭтоГруппа Тогда
			ПараметрыВыбора.Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// См. НастройкиПлатежныхСистемКлиентСобытия.ПриДополнительнойОбработкеВыбораНастройки
// 
Процедура ПриДополнительнойОбработкеВыбораНастройки(ПараметрыВыбора, ОбработкаЗавершения) Экспорт

	Если ТипЗнч(ПараметрыВыбора.ПараметрыДополнительнойОбработки) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Эталон = Новый Структура("ПричинаОбработки", "");
	ЗаполнитьЗначенияСвойств(Эталон, ПараметрыВыбора.ПараметрыДополнительнойОбработки);
	
	Если Эталон.ПричинаОбработки = "НеУказанИдентификаторУчастника" Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("НастройкаПодключения", ПараметрыВыбора.РезультатВыбора);
		ДополнительныеПараметры.Вставить("ПараметрыВыбора", ПараметрыВыбора);
		ДополнительныеПараметры.Вставить("ОбработкаЗавершения", ОбработкаЗавершения);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПриОтветеНаВопросВыбораУчастника",
			ЭтотОбъект,
			ДополнительныеПараметры);
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(
			"Выбрать",
			НСтр("ru = 'Выбрать';
				|en = 'Select'"));
		Кнопки.Добавить(
			"Отмена",
			НСтр("ru = 'Отмена';
				|en = 'Cancel'"));
		
		ПоказатьВопрос(
			ОписаниеОповещения,
			НСтр("ru = 'Идентификатор настройки не определен, для продолжения необходимо выбрать участника СБП.';
				|en = 'The setting ID is undefined. To continue, select a Faster Payments System participant.'"),
			Кнопки);
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при ответе на вопрос о необходимости выбора участника СБП при выборе настройки подключения.
// 
// Параметры:
//  Ответ - Строка - идентификатор нажатой кнопки диалога.
//  ДополнительныеПараметры - Структура - дополнительные параметры. Должна содержать следующие параметры:
//   * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения, 
//       которую нужно обработать;
//   * ПараметрыВыбора - см. НастройкиПлатежныхСистемКлиент.НовыйПараметрыОбработкиВыбораНастройки;
//   * ОбработкаЗавершения - ОписаниеОповещения - описание оповещения, которое должно быть вызвано в случае выбора
//       нового участника.
//
Процедура ПриОтветеНаВопросВыбораУчастника(
		Ответ,
		ДополнительныеПараметры) Экспорт
	
	Если Ответ = "Выбрать" Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПослеВыбораУчастникаСБП",
			ЭтотОбъект,
			ДополнительныеПараметры);
		ОткрытьФорму(
			"РегистрСведений.НастройкиУчастниковСБП.Форма.ВыборУчастникаСБП",
			Неопределено,
			,
			,
			,
			,
			ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при выборе участника СБП для настройки.
// 
// Параметры:
//  Результат - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - выбранное значение.
//            - Неопределено - если пользователь отказался от выбора.
//  ДополнительныеПараметры - Структура - дополнительные параметры. Должна содержать следующие параметры:
//   * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения, 
//       которую нужно обработать;
//   * ПараметрыВыбора - см. НастройкиПлатежныхСистемКлиент.НовыйПараметрыОбработкиВыбораНастройки;
//   * ОбработкаЗавершения - ОписаниеОповещения - описание оповещения, которое должно быть вызвано в случае выбора
//       нового участника.
//
Процедура ПослеВыбораУчастникаСБП(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		СистемаБыстрыхПлатежейВызовСервера.ОбновитьИдентификаторУчастника(
			ДополнительныеПараметры.НастройкаПодключения,
			Результат);
		
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОбработкаЗавершения,
			ДополнительныеПараметры.ПараметрыВыбора);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет обработку выбранного значения в общем списке настроек при нажатии кнопки контекстного меню "Изменить".
// 
// Параметры:
//  ПараметрыВыбора - см. НастройкиПлатежныхСистемКлиент.НовыйПараметрыОбработкиВыбораНастройкиДляИзменения
// 
Процедура ПриОбработкеВыбораНастройкиДляИзменения(ПараметрыВыбора) Экспорт
	
	Если ПараметрыВыбора.ТекущиеДанные.ЭтоГруппа Тогда
		
		ПараметрыВыбора.Отказ = Истина;
		ПараметрыВыбора.СообщениеОбОшибке =
			НСтр("ru = 'Изменение групп запрещено.';
				|en = 'Cannot edit groups.'");
		
	Иначе
		
		ПараметрыВыбора.ИмяФормы = "Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей.ФормаОбъекта";
	
	КонецЕсли;
	
КонецПроцедуры

// Выполняет открытие кассовых ссылок на основе данных строки, переданных из формы общих настроек.
// 
// Параметры:
//  ДанныеСтроки - ДанныеФормыЭлементКоллекции - данные выделенной строки.
//
Процедура ОткрытьКассовыеСсылкиКоманда(ДанныеСтроки) Экспорт
	
	Если ДанныеСтроки.ЭтоГруппа Тогда
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru = 'Работа с кассовыми QR-кодами доступна только для настроек подключения.';
				|en = 'The cash account QR codes operations are available only for connection settings.'"));
		Возврат;
	КонецЕсли;
	
	НастройкаПодключения = ДанныеСтроки.Ссылка;
	Если Не СистемаБыстрыхПлатежейВызовСервера.ДоступнаНастройкаКассовойСсылки(НастройкаПодключения) Тогда
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru = 'Работа с кассовыми QR-кодами не поддерживается данным участником СБП.';
				|en = 'This FPS member does not support operations with the cash account QR codes.'"));
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПc2b") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыНастройки = Новый Структура;
	ПараметрыНастройки.Вставить("НастройкаПодключения", НастройкаПодключения);
	
	МодульПереводыСБПc2bКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПереводыСБПc2bКлиент");
	МодульПереводыСБПc2bКлиент.ПриНастройкеКассовыхСсылок(
		ПараметрыНастройки,
		Неопределено);
	
КонецПроцедуры

// Открывает мастер создания новой настройки на основании существующей.
// 
// Параметры:
//  ДанныеСтроки - ДанныеФормыЭлементКоллекции - данные выделенной строки.
//  ОписаниеОповещения - ОписаниеОповещения, Неопределено - оповещение, которое
//    необходимо вызвать после завершения настройки подключения.
//
Процедура СкопироватьНастройку(ДанныеСтроки, ОписаниеОповещения) Экспорт
	
	ТекстПредупреждения = НСтр("ru = 'Копирование доступно только для настроек подключения.';
								|en = 'Copying is available only for connection settings.'");
	
	Если ДанныеСтроки.ЭтоГруппа Тогда
		ПоказатьПредупреждение(
			Неопределено,
			ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ПараметрыПодключения = Новый Структура;
	
	ПараметрыПодключения.Вставить(
		"НастройкаПодключения",
		ДанныеСтроки.Ссылка);
	ПараметрыПодключения.Вставить(
		"ДополнительныеПараметры",
		Неопределено);
	ПараметрыПодключения.Вставить(
		"РежимРаботы",
		СистемаБыстрыхПлатежейКлиентСервер.ИдентификаторОперацииСозданиеНастройки());
	
	СлужебнаяПодключитьСистемуБыстрыхПлатежей(
		ПараметрыПодключения,
		ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НастройкиПодключения

// Вызывается при нажатии на гиперссылку форматированной строки, расположенной в элементе
// ДекорацияДополнительнаяИнформация.
//
// Параметры:
//  Элемент - Элемент - элемент формы;
//  НавигационнаяСсылкаФорматированнойСтроки - Строка - значение гиперссылки форматированной строки.
//    Параметр передается по ссылке.
//  СтандартнаяОбработка - передается признак выполнения стандартной (системной) обработки события.
//  ДанныеФормы - Структура - настройки подключения на форме:
//    * ПараметрыОплатыСБПc2b - Соответствие, Неопределено - содержит настройки выполнения оплаты.
//       Структура параметра соответствует структуре регистра, которая определена
//       в метаданных за исключением полей указанных в настройках в свойстве ИсключаемыеПоля
//       процедуры СистемаБыстрыхПлатежейПереопределяемый.ПриОпределенииНастроекПодключения
//       (см. Настройки.c2b.ОбъектМетаданных).
//    * НастройкаПодключения, Неопределено - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//       настройка выполнения оплаты. При настройке подключения будет возвращено Неопределено,
//       т.к. элемент настройки еще не создан.
//    * Модифицированность - Булево - признак модифицированности формы.
//
//@skip-warning
Процедура ПриОбработкеНавигационнойСсылкиДополнительнойИнформации(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка,
		ДанныеФормы) Экспорт
	
	ИнтеграцияПодсистемБИПКлиент.ПриОбработкеНавигационнойСсылкиДополнительнойИнформации(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка,
		ДанныеФормы);
	СистемаБыстрыхПлатежейКлиентПереопределяемый.ПриОбработкеНавигационнойСсылкиДополнительнойИнформации(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка,
		ДанныеФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ИнтернетПоддержкаПользователей

// См. СистемаБыстрыхПлатежейВызовСервера.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки
//
Функция ПроверитьПодключениеИнтернетПоддержкиПользователей() 
	
	Возврат СистемаБыстрыхПлатежейВызовСервера.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	
КонецФункции

// Выполняет вызов вопроса пользователю оповещения после окончания процесса подключения ИПП
//
// Параметры:
//  ОбработкаЗавершения - ОписаниеОповещения - Содержит описание оповещения,
//   которое необходимо вызвать после ответа пользователя.
//
Процедура ПоказатьВопросПодключенияИнтернетПоддержкиПользователей(
		Знач ОбработкаЗавершения = Неопределено)
	
	Параметры = Новый Структура;
	Параметры.Вставить("ОбработкаЗавершения", ОбработкаЗавершения);
	
	ОбработкаОтвета = Новый ОписаниеОповещения("ПоказатьВопросПодключенияИнтернетПоддержкиПользователейЗавершение", ЭтотОбъект, Параметры);
	
	ТекстВопроса = НСтр("ru = 'Для использования функций взаимодействия с сервисом СБП,
		|необходимо подключиться к Интернет-поддержке пользователей.
		|Подключиться сейчас?';
		|en = 'To use functions of interaction with FPS service,
		|connect to Online user support.
		|Connect now?'");
	ПоказатьВопрос(ОбработкаОтвета, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

// Выполняет вызов обработки оповещения после окончания процесса подключения ИПП
//
// Параметры:
//  Ответ - КодВозвратаДиалога - Содержит ответ пользователя.
//  Параметры - Структура - содержит описание оповещения при закрытии формы.
//
Процедура ПоказатьВопросПодключенияИнтернетПоддержкиПользователейЗавершение(
		Знач Ответ,
		Знач Параметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ОбработкаПодключения = Новый ОписаниеОповещения(
			"НачатьПодключениеИнтернетПоддержкиПользователейЗавершение",
			ЭтотОбъект,
			Параметры);
		НачатьПодключениеИнтернетПоддержкиПользователей(ОбработкаПодключения);
		
	Иначе
		
		Если Параметры.ОбработкаЗавершения <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(Параметры.ОбработкаЗавершения, Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// См. ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей
//
Процедура НачатьПодключениеИнтернетПоддержкиПользователей(
		Знач ОбработкаПодключения = Неопределено)
	
	ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(
		ОбработкаПодключения,
		ЭтотОбъект);
	
КонецПроцедуры

// Выполняет вызов обработки оповещения после окончания процесса подключения ИПП
//
// Параметры:
//  ДанныеПодключения - Структура - Содержит данные аутентификации пользователя,
//    пустая если они не заданы.
//  Параметры - Структура - содержит описание оповещения при закрытии формы
//
Процедура НачатьПодключениеИнтернетПоддержкиПользователейЗавершение(
		Знач ДанныеПодключения,
		Знач Параметры) Экспорт
	
	Если Параметры.ОбработкаЗавершения <> Неопределено Тогда
		
		Подключено = (ДанныеПодключения <> Неопределено);
		ВыполнитьОбработкуОповещения(Параметры.ОбработкаЗавершения, Подключено);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область НастройкаШаблоновСообщений

// Определяет поведение системы после ответа пользователя о создании шаблонов
//
// Параметры:
//  Результат - Структура - результат открытия окна с вопросом пользователю.
//  Параметры - Структура - содержит описание параметров открытия формы.
//
Процедура ПослеОтветаНаВопросОСозданииШаблонов(
		Результат,
		Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		
		СозданныеШаблоны = СистемаБыстрыхПлатежейВызовСервера.СоздатьПредопределенныеШаблоныСообщений(
			Истина);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			СозданныеШаблоны,
			Параметры.Шаблоны);
		
	КонецЕсли;
	
	ОткрытьФормуШаблонаСообщенийСОтбором(
		СозданныеШаблоны,
		Параметры.УникальныйИдентификатор);
	
КонецПроцедуры

// Осуществляет открытие формы шаблонов сообщений с отбором по предопределенным шаблонам подсистемы.
//
// Параметры:
//  ДанныеШаблонов - Массив из СправочникСсылка.ШаблоныСообщений - массив элементов по которым будет установлен отбор.
//  УникальныйИдентификатор - УникальныйИдентификатор - ключ уникальности используемый при открытии формы
//
Процедура ОткрытьФормуШаблонаСообщенийСОтбором(
		ДанныеШаблонов,
		УникальныйИдентификатор)
	
	ОтборФормы = Новый Структура();
	ОтборФормы.Вставить("Ссылка", ДанныеШаблонов);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Отбор", ОтборФормы);
	
	ИмяФормыСпискаШаблонов = "Справочник.ШаблоныСообщений.ФормаСписка";
	ОткрытьФорму(
		ИмяФормыСпискаШаблонов,
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ПодготовкаПлатежнойСсылки

// Выполнение проверки и подключение к Интернет-поддержке пользователей.
// Параметры:
//  ОбработкаЗавершения - ОписаниеОповещения - Процедура, которая будет либо выполнена сразу, если вход в ИПП выполнен,
//    либо после вывода пользователю диалога о необходимости подключения к ИПП и ввода учетных данных.
//    Параметр Результат процедуры-обработчика может принимать значение Ложь,если пользователь отказался от подключения
//    к ИПП или отменил ввод учетных данных, либо Истина, если вход в ИПП выполнен.
//
Процедура НачатьПроверкуИПодключениеИнтернетПоддержкиПользователей(
		Знач ОбработкаЗавершения = Неопределено)
	
	Если ПроверитьПодключениеИнтернетПоддержкиПользователей() Тогда
		Если ОбработкаЗавершения <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(
				ОбработкаЗавершения,
				Истина);
		КонецЕсли;
	Иначе
		
		ПоказатьВопросПодключенияИнтернетПоддержкиПользователей(
			ОбработкаЗавершения);
		
	КонецЕсли;
	
КонецПроцедуры

// См. СистемаБыстрыхПлатежейСлужебный.ПриОпределенииКомандПодключенныхКОбъекту
//
//@skip-check doc-comment-parameter-section
Процедура Подключаемый_ОткрытьФормуПлатежнойСсылкиСБП(
		ПараметрКоманды,
		ПараметрыВыполненияКоманды) Экспорт
	
	ИнтеграцияПодсистемБИПКлиент.ПередОткрытиемФормыОтправкиПлатежнойСсылки();
	СистемаБыстрыхПлатежейКлиентПереопределяемый.ПередОткрытиемФормыОтправкиПлатежнойСсылки();
	ОткрытьФормуПлатежнойСсылкиСБП(ПараметрКоманды);
	
КонецПроцедуры

// Открывает форму формирования ссылки на оплату.
// Позволяет: 
//  * Сформировать новую ссылку.
//  * Просмотреть сформированную ранее ссылку.
//  * Обновить данные в сервисе, если они изменились.
//  
// Параметры:
//  ОснованиеПлатежа - Произвольный - основание платежа, для которого нужно сформировать/открыть/обновить ссылку.
//
Процедура ОткрытьФормуПлатежнойСсылкиСБП(Знач ОснованиеПлатежа)
	
	Параметры = Новый Структура;
	Параметры.Вставить("ОснованиеПлатежа", ОснованиеПлатежа);
	
	ОбработкаПродолжения = Новый ОписаниеОповещения(
		"ОткрытьФормуПлатежнойСсылкиСБППродолжение",
		ЭтотОбъект,
		Параметры);
	
	НачатьПроверкуИПодключениеИнтернетПоддержкиПользователей(
		ОбработкаПродолжения);
	
КонецПроцедуры

// Подготавливает параметры открытия формы формирования платежной ссылки основания платежа.
// Перед открытием проверяется наличие подключенной интернет-поддержки пользователей.
//
// Параметры:
//  ПодключенаИПП - Булево - признак подключения ИПП.
//  Параметры - Структура - содержит описание параметров открытия формы:
//   * ОснованиеПлатежа - Произвольный - основание платежа, для которого будет формироваться ссылка.
//
Процедура ОткрытьФормуПлатежнойСсылкиСБППродолжение(
		Знач ПодключенаИПП,
		Знач Параметры) Экспорт
	
	Если Не ПодключенаИПП Тогда
		Возврат;
	КонецЕсли;
	
	РезультатПроверки = СистемаБыстрыхПлатежейВызовСервера.ПриОпределенииДоступностиПодключенияПоДокументуОперации(
		Параметры.ОснованиеПлатежа);
	
	Если Не РезультатПроверки.ИнтеграцияДоступна Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатПроверки.СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	ПараметрыНастройки =
		СистемаБыстрыхПлатежейВызовСервера.ПриОпределенииПараметровПодключенияДокументаОперации(
			Параметры.ОснованиеПлатежа);
	
	Если ПараметрыНастройки.ПараметрыВопроса.ТекстВопроса <> Неопределено Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПараметрыНастройки", ПараметрыНастройки);
		ДополнительныеПараметры.Вставить("Параметры", Параметры);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОткрытьФормуПлатежнойСсылкиСБППослеОтвета",
			ЭтотОбъект,
			ДополнительныеПараметры);
		
		ПоказатьВопрос(
			ОписаниеОповещения,
			ПараметрыНастройки.ПараметрыВопроса.ТекстВопроса,
			РежимДиалогаВопрос.ДаНет);
			
	Иначе
		ОткрытьФормуПлатежнойСсылкиСБППослеОтветаЗавершение(
			ПараметрыНастройки,
			Параметры)
	КонецЕсли;
	
КонецПроцедуры

// Определяет поведение системы после ответа пользователя о создании новой торговой точки.
//
// Параметры:
//  Ответ - Структура - результат открытия окна с вопросом пользователю.
//  ДополнительныеПараметры - Структура - содержит описание параметров открытия формы.
//
Процедура ОткрытьФормуПлатежнойСсылкиСБППослеОтвета(
		Ответ,
		ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОткрытьФормуПлатежнойСсылкиСБППослеОтветаЗавершение(
			ДополнительныеПараметры.ПараметрыНастройки,
			ДополнительныеПараметры.Параметры)
	КонецЕсли;
	
КонецПроцедуры

// Определяет поведение системы после ответа пользователя о создании новой торговой точки.
//
// Параметры:
//  ПараметрыНастройки - Структура - содержит описание параметров настройки.
//  Параметры - Структура - параметры открытия.
//
Процедура ОткрытьФормуПлатежнойСсылкиСБППослеОтветаЗавершение(
		ПараметрыНастройки,
		Параметры)
	
	Если ПараметрыНастройки.НастройкаНайдена Тогда
		
		ОткрытьФормуПодготовкиПлатежнойСсылкиСБП(
			Параметры.ОснованиеПлатежа,
			ПараметрыНастройки.НастройкиПодключения,
			ПараметрыНастройки.ДополнительныеНастройки.МаксимальнаяСуммаОплаты);
		
	ИначеЕсли СистемаБыстрыхПлатежейВызовСервера.НастройкаПодключенияДоступна() Тогда
		
		ПараметрыЗавершения = Новый Структура;
		ПараметрыЗавершения.Вставить("ОснованиеПлатежа", Параметры.ОснованиеПлатежа);
		ПараметрыЗавершения.Вставить("НоваяНастройкаПодключения", Неопределено);
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ПараметрыЗавершения", ПараметрыЗавершения);
		ПараметрыОповещения.Вставить("НастройкиПодключения", ПараметрыНастройки.НастройкиПодключения);
		ПараметрыОповещения.Вставить("ДополнительныеНастройки", ПараметрыНастройки.ДополнительныеНастройки);
		
		ОбработкаОтвета = Новый ОписаниеОповещения(
			"ПоказатьВопросСозданияНастройкиПодключенияЗавершение",
			ЭтотОбъект,
			ПараметрыОповещения);
		
		ТекстВопроса = НСтр("ru = 'Настройка подключения к Системе быстрых платежей не
			|найдена, необходимо создать новую настройку.
			|Создать сейчас?';
			|en = 'Seeing of Faster Payments System connection is not found.
			|Create a new setting.
			|Create it now?'");
		
		ПоказатьВопрос(
			ОбработкаОтвета,
			ТекстВопроса,
			РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не обнаружены настройки подключения к СБП';
				|en = 'FPS connection settings are not found'"));
	КонецЕсли;
	
КонецПроцедуры

// Определяет поведение системы после ответа пользователя о создании новой торговой точки.
//
// Параметры:
//  Ответ - Структура - результат открытия окна с вопросом пользователю.
//  Параметры - Структура - содержит описание параметров открытия формы.
//
Процедура ПоказатьВопросСозданияНастройкиПодключенияЗавершение(
		Знач Ответ,
		Знач Параметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"СозданиеНастройкиПодключенияЗавершение",
			ЭтотОбъект,
			Параметры.ПараметрыЗавершения);
		
		ПараметрыПодключения = НастройкиПлатежныхСистемКлиент.НовыйПараметрыПодключения(
			Параметры.ДополнительныеНастройки.БИК,
			Параметры.ДополнительныеНастройки.ДополнительныеПараметры,
			Неопределено,
			Параметры.ДополнительныеНастройки.ВариантНастройки,
			Параметры.ДополнительныеНастройки.ОтборУчастников);

		ПараметрыПодключения.Вставить(
			"ОснованиеПлатежа",
			Параметры.ПараметрыЗавершения.ОснованиеПлатежа);
		ПараметрыПодключения.Вставить(
			"РежимРаботы",
			СистемаБыстрыхПлатежейКлиентСервер.ИдентификаторОперацииСозданиеНастройки());
		
		СлужебнаяПодключитьСистемуБыстрыхПлатежей(
			ПараметрыПодключения,
			ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет вызов обработки оповещения после окончания процесса создания торговой точки
//
// Параметры:
//  Результат - КодВозвратаДиалога - Содержит ответ пользователя.
//  ДополнительныеПараметры - Структура - содержит описание оповещения при закрытии формы.
//
Процедура СозданиеНастройкиПодключенияЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат <> КодВозвратаДиалога.Отмена Тогда
		ПараметрыДокумента = 
			СистемаБыстрыхПлатежейВызовСервера.ПриОпределенииПараметровПодключенияДокументаОперации(
				ДополнительныеПараметры.ОснованиеПлатежа);
		Если ЗначениеЗаполнено(ПараметрыДокумента) Тогда
			ОткрытьФормуПодготовкиПлатежнойСсылкиСБП(
				ДополнительныеПараметры.ОснованиеПлатежа,
				ПараметрыДокумента.НастройкиПодключения,
				ПараметрыДокумента.ДополнительныеНастройки.МаксимальнаяСуммаОплаты);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму формирования платежной ссылки основания платежа.
// Перед открытием проверяется наличие подключенной интернет-поддержки пользователей.
//
// Параметры:
//  ОснованиеПлатежа - Произвольный - основание платежа, для которого будет формироваться ссылка.
//  НастройкиПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка выполнения операции;
//  МаксимальнаяСуммаОплаты - Число - сумма документа.
//
Процедура ОткрытьФормуПодготовкиПлатежнойСсылкиСБП(
		ОснованиеПлатежа,
		НастройкиПодключения,
		МаксимальнаяСуммаОплаты)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ОснованиеПлатежа",        ОснованиеПлатежа);
	ПараметрыФормы.Вставить("НастройкиПодключения",    НастройкиПодключения);
	ПараметрыФормы.Вставить("МаксимальнаяСуммаОплаты", МаксимальнаяСуммаОплаты);
	
	ОткрытьФорму(
		"ОбщаяФорма.ОтправкаПлатежнойСсылкиСБП",
		ПараметрыФормы);
		
КонецПроцедуры

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыФункции

// Открывает форму журнала регистрации с отбором
// по событию см. ИмяСобытияЖурналаРегистрации.
//
Процедура ОткрытьЖурналРегистрации() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("СобытиеЖурналаРегистрации", ИмяСобытияЖурналаРегистрации());
	ЖурналРегистрацииКлиент.ОткрытьЖурналРегистрации(Отбор);
	
КонецПроцедуры

// Возвращает имя события для журнала регистрации, которое используется
// для записи событий загрузки данных из внешних систем.
//
// Возвращаемое значение:
//  Строка - имя события.
//
Функция ИмяСобытияЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Переводы СБП';
				|en = 'FPS transfers'",
		ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	
КонецФункции

// Возвращает идентификатор режима смены участника для мастера подключения.
//
// Возвращаемое значение:
//  Строка - идентификатор.
//
Функция ИдентификаторОперацииСменаУчастника() Экспорт
	
	Возврат "СменаУчастника";
	
КонецФункции

#КонецОбласти

#КонецОбласти