#Область ПрограммныйИнтерфейс



#Область ПроверкаФормулы

// Возвращает шаблон параметров проверки формулы.
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * ФункцииОбщегоМодуля - Массив из см. РаботаСФормуламиКлиентСервер.ОписаниеФункцииОбщегоМодуля -
// * ФормулаДляВычисленияВЗапросе - Булево - Признак исполнения функции в качестве выражения текста запроса.
// * Поле - Строка - Путь к реквизиту формы, для которого было выведено сообщение, или к данным объекта.
// * ПутьКДанным - Строка - Содержит путь в форме, которая будет отображать сообщение, до объекта, связанного с этим сообщением.
// * СообщениеОбОшибке - Строка - Значение по умолчанию "". Если сообщение заполнено, то при наличии ошибки будет выводится указанное сообщение.
// * НеВыводитьСообщения - Булево - Флаг, позволяющий отключить вывод сообщений об ошибках.
// * ТипыОперандов - Соответствие - типы операндов
//
Функция ПараметрыПроверкиФормулы() Экспорт
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("ФункцииОбщегоМодуля", Новый Массив);
	//++ НЕ УТ
	ПараметрыПроверки.Вставить("ФормулаДляВычисленияВЗапросе", Ложь);
	//-- НЕ УТ
	ПараметрыПроверки.Вставить("Поле", "");
	ПараметрыПроверки.Вставить("ПутьКДанным", "");
	ПараметрыПроверки.Вставить("СообщениеОбОшибке", "");
	ПараметрыПроверки.Вставить("НеВыводитьСообщения", Ложь);
	ПараметрыПроверки.Вставить("ТипыОперандов", Новый Соответствие);
	
	Возврат ПараметрыПроверки;
	
КонецФункции

// Осуществляет проверку формулы при интерактивных действиях пользователя.
//
// Параметры:
//   Формула - Строка - текст формулы.
//   Операнды - Массив из Строка - операнды формулы.
//   ТипРезультата - ОписаниеТипов - ожидаемый тип результата вычисления.
//   ПараметрыПроверки - см. ПараметрыПроверкиФормулы.
//
Процедура ПроверитьФормулуИнтерактивно(Формула, Операнды, ТипРезультата, ПараметрыПроверки = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(Формула) Тогда
		Если ПроверитьФормулу(Формула, Операнды, ТипРезультата, ПараметрыПроверки) Тогда
			
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'В формуле ошибок не обнаружено';
					|en = 'No errors detected in the formula'"),
				,
				,
				БиблиотекаКартинок.Информация32);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Осуществляет проверку корректности формулы.
//
// Параметры:
//   Формула - Строка - текст формулы.
//   Операнды - Массив из Строка - операнды формулы.
//   ТипРезультата - ОписаниеТипов - ожидаемый тип результата вычисления.
//   ПараметрыПроверки - см. ПараметрыПроверкиФормулы.
//
// Возвращаемое значение:
//  Булево - Ложь, если есть ошибки, иначе Истина.
//
Функция ПроверитьФормулу(Формула, Операнды, ТипРезультата, Знач ПараметрыПроверки = Неопределено) Экспорт
	
	Результат = Истина;
	
	Если ПараметрыПроверки = Неопределено Тогда
		ПараметрыПроверки = ПараметрыПроверкиФормулы();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Формула) Тогда
		
		ТипыРезультата = ТипРезультата.Типы();
		Если ТипыРезультата.Количество() = 1
			И ТипыРезультата[0] = Тип("Строка") Тогда
			ТекстРасчета = """Строка"" + " + Формула;
			ЗначениеЗамены = """1""";
		ИначеЕсли ТипыРезультата.Количество() = 1
			И ТипыРезультата[0] = Тип("Дата") Тогда
			//++ НЕ УТ
			Если ПараметрыПроверки.ФормулаДляВычисленияВЗапросе Тогда
				ЗначениеЗамены = ОбщегоНазначенияКлиент.ДатаСеанса();
			Иначе
			//-- НЕ УТ
				ЗначениеЗамены = "1 ";
			//++ НЕ УТ
			КонецЕсли;
			//-- НЕ УТ
			ТекстРасчета = Формула;
		Иначе
			ЗначениеЗамены = "1 ";
			ТекстРасчета = Формула;
		КонецЕсли;
		
		РаботаСФормуламиВызовСервера.ПолучитьТекстРасчета(ТекстРасчета, Операнды, ЗначениеЗамены, ПараметрыПроверки.ТипыОперандов);
		Попытка
			
			//++ НЕ УТ
			Если ПараметрыПроверки.ФормулаДляВычисленияВЗапросе Тогда
				
				РезультатРасчета = РаботаСФормуламиВызовСервера.ПроверитьФормулуЗапроса(ТекстРасчета, ЗначениеЗамены);
				
			Иначе
			//-- НЕ УТ
				Если ПараметрыПроверки.ФункцииОбщегоМодуля.Количество() > 0 Тогда
					
					Для каждого СвойстваФункции Из ПараметрыПроверки.ФункцииОбщегоМодуля Цикл
						ТекстРасчета = СтрЗаменить(ТекстРасчета, СвойстваФункции.Идентификатор, СвойстваФункции.ПолныйПуть);
					КонецЦикла; 
					
				КонецЕсли;
				
				РезультатРасчета = Вычислить(ТекстРасчета);
			
			//++ НЕ УТ
			КонецЕсли;
			//-- НЕ УТ
			
			ТекстПроверки = СтрЗаменить(Формула, Символы.ПС, "");
			ТекстПроверки = СтрЗаменить(ТекстПроверки, " ", "");
			ОтсутствиеРазделителей = Найти(ТекстПроверки, "][")
				+ Найти(ТекстПроверки, """[")
				+ Найти(ТекстПроверки, "]""");
			Если ОтсутствиеРазделителей > 0 Тогда
				СообщитьОбОшибкеВФормуле(Формула,
					ТекстРасчета,
					НСтр("ru = 'Между операндами должен присутствовать оператор или разделитель';
						|en = 'An operator or a separator is to be between the operands'"),
					ПараметрыПроверки,
					Результат);
			КонецЕсли;
			
			ТипРезультатаРасчетаПравильный = Ложь;
			
			Для Каждого ДопустимыйТип Из ТипРезультата.Типы() Цикл
				Если ТипЗнч(РезультатРасчета) = ДопустимыйТип Тогда
					ТипРезультатаРасчетаПравильный = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если Не ТипРезультатаРасчетаПравильный Тогда
				ПричинаОшибки = НСтр("ru = 'Результат расчета должен быть типа %1';
									|en = 'Calculation result must be of the %1 type'");
				ПричинаОшибки = СтрШаблон(ПричинаОшибки, ТипРезультата);
				
				СообщитьОбОшибкеВФормуле(Формула,
					ТекстРасчета,
					ПричинаОшибки,
					ПараметрыПроверки,
					Результат);
			КонецЕсли;
		
		Исключение
			
			Результат = Ложь;
			
			Если ЗначениеЗаполнено(ПараметрыПроверки.СообщениеОбОшибке) Тогда
				ТекстСообщения = ПараметрыПроверки.СообщениеОбОшибке;
			Иначе
				ТекстСообщения = НСтр("ru = 'В формуле обнаружены ошибки. Проверьте формулу. Формулы должны составляться по правилам написания выражений на встроенном языке 1С:Предприятия.';
										|en = 'Errors are detected in the formula. Check the formula. Formulas should be created based on the expression rules of 1C:Enterprise script.'");
			КонецЕсли;
			
			СообщитьОбОшибкеВФормуле(Формула,
				ТекстРасчета,
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()),
				ПараметрыПроверки,
				Результат,
				ТекстСообщения);
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыводОшибок

Процедура СообщитьОбОшибкеВФормуле(Формула, ТекстРасчета, ПричинаОшибки, ПараметрыПроверки, Результат, Знач ТекстСообщенияПользователю = "")
	
	Результат = Ложь;
	КомментарийЖурналаРегистрации = СтрШаблон(
		НСтр("ru = 'В формуле: %1
			|Обнаружены ошибки:
			|%2
			|
			|Полученный текст расчета формулы: %3';
			|en = 'In the formula: %1
			|Errors found:
			|%2
			|
			|Received formula calculation text: %3'"),
		Формула,
		ПричинаОшибки,
		ТекстРасчета);
		
	ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
		НСтр("ru = 'Ошибка при проверке формулы (Клиент)';
			|en = 'Formula check error (Client)'"),
		"Предупреждение",
		КомментарийЖурналаРегистрации,
		,
		Истина);
		
	Если ПараметрыПроверки.НеВыводитьСообщения Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстСообщенияПользователю) Тогда
		ТекстСообщенияПользователю = СтрШаблон("В формуле обнаружены ошибки. %1", ПричинаОшибки);
	КонецЕсли;
	
	ОбщегоНазначенияКлиент.СообщитьПользователю(
		ТекстСообщенияПользователю,,
		ПараметрыПроверки.Поле,
		ПараметрыПроверки.ПутьКДанным);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
