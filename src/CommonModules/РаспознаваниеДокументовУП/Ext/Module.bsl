#Область ПрограммныйИнтерфейс

Функция ОпределитьСтавкуНДС(Всего, СуммаНДС) Экспорт
	
	Делитель = Всего - СуммаНДС;
	Если Делитель = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	Ставка = Окр(СуммаНДС * 100 / Делитель);
	СтруктураСтавкиНДС = Новый Структура("Числом", Ставка);
	
	Если Ставка = 20 Тогда
		СтавкаСсылкой = Справочники.СтавкиНДС.НайтиПоРеквизиту("ПеречислениеСтавкаНДС",Перечисления.СтавкиНДС.НДС20);
	ИначеЕсли Ставка = 18 Тогда
		СтавкаСсылкой = Справочники.СтавкиНДС.НайтиПоРеквизиту("ПеречислениеСтавкаНДС",Перечисления.СтавкиНДС.НДС18);
	ИначеЕсли Ставка = 10 Тогда
		СтавкаСсылкой = Справочники.СтавкиНДС.НайтиПоРеквизиту("ПеречислениеСтавкаНДС",Перечисления.СтавкиНДС.НДС10);
	ИначеЕсли Ставка = 0 Тогда
		СтавкаСсылкой = Справочники.СтавкиНДС.БезНДС;
	Иначе
		СтавкаСсылкой = Неопределено;
	КонецЕсли;
	
	Если СтавкаСсылкой = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		СтруктураСтавкиНДС.Вставить("Ссылкой", СтавкаСсылкой);
		Возврат СтруктураСтавкиНДС;
	КонецЕсли;
	
КонецФункции

Функция СтавкаНДСПоРаспознанномуТексту(Ставка, ТекущееЗначение) Экспорт
	
	Если Ставка = "НДС20" Тогда
		Возврат Справочники.СтавкиНДС.НайтиПоРеквизиту("ПеречислениеСтавкаНДС", Перечисления.СтавкиНДС.НДС20);
	ИначеЕсли Ставка = "НДС18" Тогда
		Возврат Справочники.СтавкиНДС.НайтиПоРеквизиту("ПеречислениеСтавкаНДС", Перечисления.СтавкиНДС.НДС18);
	ИначеЕсли  Ставка = "НДС10" Тогда
		Возврат Справочники.СтавкиНДС.НайтиПоРеквизиту("ПеречислениеСтавкаНДС", Перечисления.СтавкиНДС.НДС10);
	ИначеЕсли Ставка = "НДС18_118" Тогда
		Возврат Справочники.СтавкиНДС.НайтиПоРеквизиту("ПеречислениеСтавкаНДС", Перечисления.СтавкиНДС.НДС18_118);
	ИначеЕсли Ставка = "НДС10_110" Тогда
		Возврат Справочники.СтавкиНДС.НайтиПоРеквизиту("ПеречислениеСтавкаНДС", Перечисления.СтавкиНДС.НДС10_110);
	ИначеЕсли Ставка = "НДС20_120" Тогда
		Возврат Справочники.СтавкиНДС.НайтиПоРеквизиту("ПеречислениеСтавкаНДС", Перечисления.СтавкиНДС.НДС20_120);
	ИначеЕсли Ставка = "НДС0" Тогда
		Возврат Справочники.СтавкиНДС.НайтиПоРеквизиту("ПеречислениеСтавкаНДС", Перечисления.СтавкиНДС.НДС0);
	ИначеЕсли Ставка = "БезНДС" Тогда
		Возврат Справочники.СтавкиНДС.БезНДС;
	КонецЕсли;
	
	Возврат ТекущееЗначение;
	
КонецФункции

Процедура ЗаполнитьПартнераКонтрагента(ДокументОбъект) Экспорт
	
	//Параметры выбора договора для поиска партнера тоже подходят
	ПараметрыВыбораДоговора = РаспознаваниеДокументовСлужебный.ПараметрыВыбораДоговора(ДокументОбъект);
		
	Партнер = Справочники.Партнеры.ПустаяСсылка();
	
	// Используем заполнение без нечеткого поиска
	Если ЗначениеЗаполнено(ПараметрыВыбораДоговора.Контрагент) Тогда
		Партнер = ДенежныеСредстваСервер.ПолучитьПартнераПоКонтрагенту(ПараметрыВыбораДоговора.Контрагент);
	КонецЕсли;

	Если ЗначениеЗаполнено(Партнер) Тогда
		ВидПартнера = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Партнер, "Клиент, Поставщик");
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Партнер) 
		Или (ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий И ВидПартнера.Клиент)
		Или (ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий И ВидПартнера.Поставщик) Тогда
		
		Отбор = Новый Структура("ИмяРеквизита", "Партнер");
		СтрокиПоиска = ДокументОбъект.РеквизитыДокумента.НайтиСтроки(Отбор);
		Если СтрокиПоиска.Количество() <> 0 Тогда
			СтрокиПоиска[0].Значение = Партнер;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСоглашениеКонтрагента(ДокументОбъект) Экспорт
	
	ХозОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту;
	ПараметрыОтбора = Новый Структура("ХозяйственныеОперации",ХозОперация);

	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов") Тогда
		Отбор = Новый Структура("ИмяРеквизита", "Партнер");
		СтрокиПоиска = ДокументОбъект.РеквизитыДокумента.НайтиСтроки(Отбор);
		Если СтрокиПоиска.Количество() <> 0 Тогда
			Если ЗначениеЗаполнено(СтрокиПоиска[0].Значение) Тогда
				Партнер = СтрокиПоиска[0].Значение;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ПараметрыДоговора = РаспознаваниеДокументовСлужебный.ПараметрыВыбораДоговора(ДокументОбъект);
		Партнер = ПараметрыДоговора.Партнер;
	КонецЕсли;
	
	Если Партнер = Неопределено Тогда
		Партнер = Справочники.Партнеры.ПустаяСсылка();
	КонецЕсли;
	
	// Используем заполнение без нечеткого поиска
	Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
		Если Не УправлениеДоступом.ЧтениеРазрешено(Справочники.СоглашенияСКлиентами.ПустаяСсылка()) Тогда
			Возврат;
		КонецЕсли;
		
		ХозОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту;
		ПараметрыОтбора = Новый Структура("ХозяйственныеОперации",ХозОперация);

		Соглашение = ПродажиСервер.ПолучитьУсловияПродажПоУмолчанию(Партнер, ПараметрыОтбора); 
		//= СамообслуживаниеСервер.ПолучитьСоглашениеПартнераПоУмолчанию(Партнер, ХозОперация);

		Отбор = Новый Структура("ИмяРеквизита", "СоглашениеСКлиентом");
		СтрокиПоиска = ДокументОбъект.РеквизитыДокумента.НайтиСтроки(Отбор);
		Если (СтрокиПоиска.Количество() <> 0 И Соглашение <> Неопределено) Тогда
			СтрокиПоиска[0].Значение = Соглашение.Соглашение;
		Иначе
			СтрокиПоиска[0].Значение = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
		КонецЕсли;
	Иначе    
		Если Не УправлениеДоступом.ЧтениеРазрешено(Справочники.СоглашенияСПоставщиками.ПустаяСсылка()) Тогда
			Возврат;
		КонецЕсли;
		ХозОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
		ПараметрыОтбора = Новый Структура("ХозяйственныеОперации",ХозОперация);

		Соглашение = ЗакупкиСервер.ПолучитьУсловияЗакупокПоУмолчанию(Партнер, ПараметрыОтбора);

		Отбор = Новый Структура("ИмяРеквизита", "СоглашениеСПоставщиком");
		СтрокиПоиска = ДокументОбъект.РеквизитыДокумента.НайтиСтроки(Отбор);
		Если (СтрокиПоиска.Количество() <> 0 И Соглашение <> Неопределено) Тогда
			СтрокиПоиска[0].Значение = Соглашение.Соглашение;
		Иначе
			СтрокиПоиска[0].Значение = Справочники.СоглашенияСПоставщиками.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ДоговорКонтрагента(ВладелецДоговора, ОрганизацияДоговора, СписокВидовДоговора= Неопределено, СтруктураПараметров = Неопределено) Экспорт
	
	НовыйДоговор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();

	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ДоговорыКонтрагентов.Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	&ТекстФильтра";
	
	Запрос.УстановитьПараметр("ВладелецДоговора", ВладелецДоговора);
	Запрос.УстановитьПараметр("ОрганизацияДоговора", ОрганизацияДоговора);
	Запрос.УстановитьПараметр("СписокВидовДоговора", СписокВидовДоговора);
	
	ИмяРеквизитаКонтрагент = БухгалтерскийУчетКлиентСерверПереопределяемый.ПолучитьИмяРеквизитаКонтрагентДоговора();	
	
	ТекстФильтра = "
	|	ДоговорыКонтрагентов." + ИмяРеквизитаКонтрагент + " = &ВладелецДоговора
	|	И ДоговорыКонтрагентов.Организация = &ОрганизацияДоговора
	|	И ДоговорыКонтрагентов.ПометкаУдаления = ЛОЖЬ"
	+?(СписокВидовДоговора<>неопределено,"
	|	И ДоговорыКонтрагентов.ТипДоговора" + " В (&СписокВидовДоговора)","");
	
	Если ТипЗнч(СтруктураПараметров) = Тип("Структура") Тогда
		Для каждого Параметр Из СтруктураПараметров Цикл
			ИмяРеквизита = Параметр.Ключ;
			СтруктураОтбора = Параметр.Значение;
			
			ВидСравненияЗапроса = "";

			Если НЕ СтруктураОтбора.Свойство("ВидСравненияОтбора") Тогда
				ВидСравненияЗапроса = "=";
			ИначеЕсли СтруктураОтбора.ВидСравненияОтбора = ВидСравнения.Равно Тогда
				ВидСравненияЗапроса = "=";
			ИначеЕсли СтруктураОтбора.ВидСравненияОтбора = ВидСравнения.НеРавно Тогда
				ВидСравненияЗапроса = "<>";
			ИначеЕсли СтруктураОтбора.ВидСравненияОтбора = ВидСравнения.ВСписке Тогда
				ВидСравненияЗапроса = "В";
			ИначеЕсли СтруктураОтбора.ВидСравненияОтбора = ВидСравнения.НеВСписке Тогда
				ВидСравненияЗапроса = "НЕ В";
			ИначеЕсли СтруктураОтбора.ВидСравненияОтбора = ВидСравнения.ВИерархии Тогда
				ВидСравненияЗапроса = "В ИЕРАРХИИ";
			ИначеЕсли СтруктураОтбора.ВидСравненияОтбора = ВидСравнения.ВСпискеПоИерархии Тогда
				ВидСравненияЗапроса = "В ИЕРАРХИИ";
			ИначеЕсли СтруктураОтбора.ВидСравненияОтбора = ВидСравнения.НеВСпискеПоИерархии Тогда
				ВидСравненияЗапроса = "НЕ В ИЕРАРХИИ";
			ИначеЕсли СтруктураОтбора.ВидСравненияОтбора = ВидСравнения.НеВИерархии Тогда
				ВидСравненияЗапроса = "НЕ В ИЕРАРХИИ";
			ИначеЕсли СтруктураОтбора.ВидСравненияОтбора = ВидСравнения.Больше Тогда
				ВидСравненияЗапроса = ">";
			ИначеЕсли СтруктураОтбора.ВидСравненияОтбора = ВидСравнения.БольшеИлиРавно Тогда
				ВидСравненияЗапроса = ">=";
			ИначеЕсли СтруктураОтбора.ВидСравненияОтбора = ВидСравнения.Меньше Тогда
				ВидСравненияЗапроса = "<";
			ИначеЕсли СтруктураОтбора.ВидСравненияОтбора = ВидСравнения.МеньшеИлиРавно Тогда
				ВидСравненияЗапроса = "<=";
			Иначе // другие варианты 
				ВидСравненияЗапроса = "=";
			КонецЕсли;
			
			ТекстФильтра = ТекстФильтра + "
			|	И ДоговорыКонтрагентов." + ИмяРеквизита + " " + ВидСравненияЗапроса + " (&" + ИмяРеквизита + ")";
			Запрос.УстановитьПараметр(ИмяРеквизита, СтруктураОтбора.ЗначениеОтбора);
		КонецЦикла;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстФильтра", ТекстФильтра);
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Если Выборка.Количество() = 1 Тогда
			Возврат Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция УстановитьБанковскийСчет(Счет, ВладелецСчета, Валюта, СовпадениеВалюты = Истина, УчитыватьВалюту = Истина, НомерСчета = Неопределено) Экспорт
	
	Если ТипЗнч(Счет) <> Тип("СправочникСсылка.БанковскиеСчетаОрганизаций") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НовыйСчет = Справочники.БанковскиеСчетаОрганизаций.ПустаяСсылка();
	Если НЕ ЗначениеЗаполнено(ВладелецСчета) Тогда
		ПолучитьНовыйСчет = Счет <> НовыйСчет;
		Счет = НовыйСчет;
		Возврат ПолучитьНовыйСчет;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	БанковскиеСчета.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА СправочникВладелец.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникВладелец
	|		ПО БанковскиеСчета.Владелец = СправочникВладелец.Ссылка
	|ГДЕ
	|	БанковскиеСчета.Владелец = &ВладелецСчета
	|	И БанковскиеСчета.ПометкаУдаления = ЛОЖЬ
	|	И (&УчитыватьВалюту = ЛОЖЬ
	|			ИЛИ (БанковскиеСчета.ВалютаДенежныхСредств = &Валюта
	|					И &СовпадениеВалюты = ИСТИНА
	|				ИЛИ НЕ БанковскиеСчета.ВалютаДенежныхСредств = &Валюта
	|					И &СовпадениеВалюты = ЛОЖЬ))
	|	И (&УчитыватьНомерСчета = ЛОЖЬ
	|			ИЛИ БанковскиеСчета.НомерСчета = &НомерСчета)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	Запрос.УстановитьПараметр("ВладелецСчета",       ВладелецСчета);
	Запрос.УстановитьПараметр("Валюта",              Валюта);
	Запрос.УстановитьПараметр("СовпадениеВалюты",    СовпадениеВалюты);
	Запрос.УстановитьПараметр("УчитыватьВалюту",     УчитыватьВалюту);
	Запрос.УстановитьПараметр("УчитыватьНомерСчета", ЗначениеЗаполнено(НомерСчета));
	Запрос.УстановитьПараметр("НомерСчета",          НомерСчета);
	
	Запрос.Текст = ТекстЗапроса;
	Результат    = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		НайденОсновнойСчет = Выборка.Приоритет = 1;
		НайденОдинСчет     = Выборка.Количество() = 1;
		
		Если НайденОсновнойСчет ИЛИ НайденОдинСчет Тогда
			НовыйСчет = Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	ПолучитьНовыйСчет = Счет <> НовыйСчет;
	Если ПолучитьНовыйСчет Тогда
		Если НЕ ЗначениеЗаполнено(Счет) Тогда
			Счет = НовыйСчет;
		Иначе
			СвойствоСчета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Счет, "Владелец, ВалютаДенежныхСредств");
			Если СвойствоСчета.Владелец <> ВладелецСчета
				ИЛИ СовпадениеВалюты И СвойствоСчета.ВалютаДенежныхСредств <> Валюта Тогда
				Счет = НовыйСчет;
			Иначе
				ПолучитьНовыйСчет = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПолучитьНовыйСчет;
	
КонецФункции 

Функция ПодсказкаНеНайденПартнерКонтрагента() Экспорт
	
	Возврат НСтр("ru = 'Не найден основной партнер выбранного контрагента';
				|en = 'The main partner of the selected counterparty is not found'");
	
КонецФункции

Функция ПодсказкаНеНайденоСоглашениеСКонтрагентом() Экспорт
	
	Возврат НСтр("ru = 'Не найдено основное соглашение с выбранным контрагентом';
				|en = 'The main agreement with the selected counterparty is not found'");
	
КонецФункции

#КонецОбласти
