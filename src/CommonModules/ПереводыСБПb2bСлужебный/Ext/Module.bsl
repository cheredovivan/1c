///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПb2b".
// ОбщийМодуль.ПереводыСБПb2b.
//
// Серверные процедуры обмена данными выполнения оплат:
//  - полученные служебных данных оплат;
//  - подготовка платежных ссылок к отправке.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ИнтеграцияПодсистемИнтернетПоддержкиПользователей

#Область БазоваяФункциональностьСБП

// Возвращает список использованных для создания платежной ссылки настроек подключения по документу,
// включая частичные оплаты.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ, по данным которого
//    необходимо получить список настроек.
//
// Возвращаемое значение:
//  Массив Из СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - используемые
//   в документах настройки подключения.
//
Функция НастройкиПодключенияПоДокументуОперации(ДокументОперации) Экспорт
	
	Возврат СистемаБыстрыхПлатежейСлужебный.НастройкиПодключенияПоДокументуОперации(
		ДокументОперации,
		Метаданные.РегистрыСведений.ИдентификаторыОперацийСБПb2b.Имя);
	
КонецФункции

// Формирует структуру данных для формы платежной ссылки в Системе быстрых платежей.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ отражающий операцию
//    в информационной базе;
//
// Возвращаемое значение:
//  Структура, Неопределено - данные для формы платежной ссылки в Системе быстрых платежей
//
Функция ДанныеПлатежнойСсылки(ДокументОперации) Экспорт
	
	Возврат РегистрыСведений.ИдентификаторыОперацийСБПb2b.ДанныеПлатежнойСсылки(
		ДокументОперации);
	
КонецФункции

// Создает новый заказ на оплату и получает идентификатор оплаты для формирования QR-кода.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ, который отражает
//    оплату в информационной базе;
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//    настройка выполнения операции;
//  СценарийЗапроса - Строка - Вариант формирования платежной ссылки, может принимать значения:
//    API, FULL, PARTIAL;
//  ДополнительныеПараметры - Структура, Неопределено - дополнительные настройки формирования
//    заказа на оплату;
//  ДанныеЧастичнойОплаты - Структура - описание данных частичной оплаты;
//
// Возвращаемое значение:
//  Структура - результат создания динамической ссылки в Системе быстрых платежей.
//
Функция СлужебныйДинамическаяСсылка(
		ДокументОперации,
		НастройкаПодключения,
		СценарийЗапроса,
		ДополнительныеПараметры = Неопределено,
		ДанныеЧастичнойОплаты = Неопределено) Экспорт
	
	ПроверитьПраваНаВыполнениеОперации();
	
	РезультатОперации = СистемаБыстрыхПлатежейСлужебный.НовыйРезультатПлатежнаяСсылка();
	
	// Подготовка параметров настройки подключения.
	ПараметрыНастройкиПодключения = СистемаБыстрыхПлатежейСлужебный.ПараметрыНастройкиПодключения(
		НастройкаПодключения);
	
	// Проверка общих параметров подключения.
	СистемаБыстрыхПлатежейСлужебный.ПроверитьОбщиеНастройкиПодключения(
		ДокументОперации,
		НастройкаПодключения,
		ПараметрыНастройкиПодключения,
		РезультатОперации);
	
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	// Создание нового заказа на оплату.
	Возврат ПереводыСБПb2bСервис.ДинамическаяСсылка(
		ДокументОперации,
		ПараметрыНастройкиПодключения,
		СценарийЗапроса,
		ДополнительныеПараметры,
		ДанныеЧастичнойОплаты);
	
КонецФункции

// Выполняет актуализацию статуса оплаты по ранее
// сформированному идентификатору оплаты (QR-коду).
//
// Параметры:
//  ДокументОплаты - ОпределяемыйТип.ДокументОперацииСБП - документ, который отражает
//    продажу в информационной базе;
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//    настройка выполнения оплаты;
//  ПараметрыОперации - Структура - дополнительные данные по оплате:
//    * ДатаОперации - Дата - фактическая дата оплаты в UTC;
//    * СуммаОперации - Число - фактическая суммы операции по документу;
//    * ИдентификаторОперации - Строка - ключ контроля загрузки;
//  ДанныеЧастичнойОплаты - Структура - параметры частичной оплаты.
//
// Возвращаемое значение:
//  Структура - результат создания заказа на оплату в Системе быстрых платежей.
//
Функция АктуализироватьСтатусОплаты(
		ДокументОплаты,
		НастройкаПодключения,
		ПараметрыОперации,
		ДанныеЧастичнойОплаты) Экспорт
	
	РезультатОперации = ПереводыСБПb2b.СтатусОплаты(
		ДокументОплаты,
		НастройкаПодключения,
		Ложь);
	
	РезультатОперации.Вставить("НеобходимоАктуализировать", Ложь);
	РезультатОперации.Вставить("СуммаОплаты", РезультатОперации.ПараметрыОперации.СуммаОперации);
	
	Если РезультатОперации.СтатусОперации <> НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполняется()
		И РезультатОперации.СтатусОперации <> НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка() Тогда
		
		// Изменился статус операции, необходима обработка, выполним в попытке
		Попытка
			
			СтатусОперации = РезультатОперации.СтатусОперации;
			Если СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполнена() Тогда
				СтатусОперацииСервиса = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаВыполнена();
			ИначеЕсли СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОтменена() Тогда
				СтатусОперацииСервиса = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаОтменена();
			Иначе
				РезультатОперации.КодОшибки = "НеизвестнаяОшибка";
				РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Неизвестный статус операции сервиса';
															|en = 'Unknown service transaction status'");
				РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Неизвестный статус операции сервиса';
															|en = 'Unknown service transaction status'");
			КонецЕсли;
			
			ОписаниеПараметровОперации = НовыйОписаниеПараметровОперации();
			ЗаполнитьЗначенияСвойств(
				ОписаниеПараметровОперации,
				РезультатОперации.ПараметрыОперации);
				Если ДанныеЧастичнойОплаты <> Неопределено
					И ЗначениеЗаполнено(ДанныеЧастичнойОплаты) Тогда
				ОписаниеПараметровОперации.ДокументОснование = ДанныеЧастичнойОплаты.ОснованиеПлатежа;
			КонецЕсли;
			
			ПереводыСБПb2bСервис.ПриЗагрузкеСтатусаОперации(
				ДокументОплаты,
				НастройкаПодключения,
				ОписаниеПараметровОперации,
				СтатусОперацииСервиса,
				РезультатОперации.СообщениеОбОшибке,
				Новый Массив);
			
		Исключение
			// Исключение не обрабатываем, ожидаем что документы прикладной логики обработаются методом СтатусыОпераций.
			СистемаБыстрыхПлатежейСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(
					ИнформацияОбОшибке()),
				Истина);
		КонецПопытки;
		
		РезультатОперации.Вставить("ПлатежнаяСсылка", ПараметрыОперации.ПлатежнаяСсылка);
		
	ИначеЕсли РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполняется() Тогда
		
		// Проверка изменился ли документ оплаты
		ЗаказНаОплату = ПереводыСБПb2bСервис.ЗаказаНаОплату(
			ДокументОплаты,
			НастройкаПодключения,
			Неопределено,
			РезультатОперации,
			ДанныеЧастичнойОплаты,
			Ложь);
			
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			Возврат РезультатОперации;
		КонецЕсли;
		
		ПараметрыНастройкиПодключения = СистемаБыстрыхПлатежейСлужебный.ПараметрыНастройкиПодключения(
			НастройкаПодключения);
		ПараметрыОперации.Вставить(
			"ИдентификаторМерчанта",
			ПараметрыНастройкиПодключения.ИдентификаторМерчанта);
		ПараметрыОперации.Вставить(
			"ИдентификаторУчастника",
			ПараметрыНастройкиПодключения.ИдентификаторУчастника);
		
		АнализИсторическихДанных = ПереводыСБПb2bСервис.АнализИсторическихДанныхОплаты(
			ДокументОплаты,
			ЗаказНаОплату,
			ПараметрыНастройкиПодключения);
			
		Если Не АнализИсторическихДанных.ДанныеИдентичны Тогда
			РезультатОперации.Вставить("НеобходимоАктуализировать", Истина);
		КонецЕсли;
		
		РезультатОперации.Вставить("ПлатежнаяСсылка", ПараметрыОперации.ПлатежнаяСсылка);
		РезультатОперации.Вставить("СуммаОплаты", ЗаказНаОплату.СуммаОплаты);
		
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Получает данные по документу операции.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ отражающий оплату
//    в информационной базе;
//
// Возвращаемое значение:
//  Структура, Неопределено - данные операции.
//
Функция ДанныеОперации(ДокументОперации) Экспорт
	
	Возврат РегистрыСведений.ИдентификаторыОперацийСБПb2b.ДанныеОперации(ДокументОперации);
	
КонецФункции

#КонецОбласти

#Область ОнлайнЗаказы

// Возвращает параметры оплат СБП b2b документа операции.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения к СБП.
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ, для которого
//    формируются параметры оплат.
//
// Возвращаемое значение:
//  Структура - параметры оплат b2b:
//    * ИдентификаторПлатежа - Строка - уникальный Идентификатор Платежа, назначаемый Получателем.
//    * РасчетныйСчет - Строка - расчетный счет получателя платежа.
//    * ОблагаетсяНДС - Булево - Признак обложения операции НДС.
//    * СуммаНДС - Число - Сумма НДС операции.
//    * ШаблоныНазначения - см. НастройкиПлатежныхСистемСлужебный.НовыйШаблоныНазначений.
//    * НазначениеПлатежа - Строка - Назначение платежа.
//
Функция ПараметрыОплаты(НастройкаПодключения, ДокументОперации) Экспорт
	
	ПараметрыОплатыСБПb2b = Новый Структура;
	ПараметрыОплатыСБПb2b.Вставить("ОблагаетсяНДС", Ложь);
	ПараметрыОплатыСБПb2b.Вставить("СуммаНДС", 0);
	ПараметрыОплатыСБПb2b.Вставить("РасчетныйСчет", "");
	ПараметрыОплатыСБПb2b.Вставить("ИдентификаторПлатежа", "");
	ПараметрыОплатыСБПb2b.Вставить("ШаблоныНазначения", Неопределено);
	ПараметрыОплатыСБПb2b.Вставить("НазначениеПлатежа", "");
	
	ПараметрыОплатыСБПb2b.ШаблоныНазначения = СистемаБыстрыхПлатежейСлужебный.ПриОпределенииШаблоновНазначенияЗаказаСБП(
		НастройкаПодключения,
		ДокументОперации,
		Перечисления.ВариантыНастройкиСБП.b2b);
		
	Возврат ПараметрыОплатыСБПb2b;
	
КонецФункции

// Возвращает данные оплат по документам платежей.
//
// Параметры:
//  ДокументОперации - Массив Из ОпределяемыйТип.ДокументОперацииСБП - документы операций;
//
// Возвращаемое значение:
//  Соответствие - данные оплат по документам в Системе быстрых платежей:
//   * Ключ - ОпределяемыйТип.ДокументОперацииСБП - документ операции в СБП
//   * Значение - Структура - содержит описание данных оплаты по документу операции:
//     ** НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения к СБП.
//     ** РезультатОбработки - Структура - содержит результаты обработки статуса операции
//       *** СтатусОперации - Строка - текущее состояние операции оплаты. Возможные значения:
//           - "Выполняется" - подтверждение оплаты не получено;
//           - "Отменена" - оплата по ранее сформированному QR-коду невозможна;
//           - "Выполнена" - участник СБП подтвердил оплату;
//           - "Отклонена" - оплата по ранее сформированному QR-коду невозможна;
//           - "Ошибка" - неизвестный статус операции.
//       *** ПараметрыОперации - Структура - Содержит данные операции:
//         **** ДатаОперации - Дата - фактическая дата оплаты в UTC;
//         **** СуммаОперации - Число - Сумма оплаты в СБП
//         **** ИдентификаторОперации - Строка, Неопределено - идентификатор выполненной операции;
//         **** ИдентификаторОплаты - Строка, Неопределено - идентификатор оплаты;
//         **** ИдентификаторПлатежа - Строка - уникальный Идентификатор Платежа, назначаемый Получателем;
//         **** РасчетныйСчет - Строка - расчетный счет получателя платежа;
//         **** ДокументЧастичнойОплаты - Документ.ПлатежнаяСсылкаСБПc2b - документ частичной оплаты;
//         **** СуммаНДС - Число - Сумма НДС операции;
//         **** ОблагаетсяНДС - Булево - Признак обложения операции НДС;
//
Функция ДанныеОпераций(ДокументыОпераций) Экспорт
	
	ДанныеОпераций = РегистрыСведений.ИдентификаторыОперацийСБПb2b.ДанныеОпераций(ДокументыОпераций);
	
	Результат = Новый Соответствие;
	
	Для Каждого ДанныеОперации Из ДанныеОпераций Цикл
		
		Если ДанныеОперации.Значение = Неопределено Тогда
			Результат.Вставить(
				ДанныеОперации.Ключ,
				Неопределено);
		Иначе
			Результат.Вставить(
				ДанныеОперации.Ключ,
				ЗаполнитьПараметрыОперации(ДанныеОперации.Значение));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Подготавливает описание параметров оплаты по переданным данным заказа и настройке подключения,
// для дальнейшей записи в подсистему.
//
// Параметры:
//  ДанныеЗаказа - Структура - Содержит данные заказа
//    * ИдентификаторЗаказа - Строка - Идентификатор онлайн-заказа;
//    * Статус - Строка - Статус онлайн-заказа;
//    * СуммаЗаказа - Число - Сумма онлайн-заказа;
//    * СпособОплаты - Перечисление.СпособыОплатыОнлайнЗаказов - Способ оплаты онлайн-заказа;
//    * ДатаОплаты - дата получения данных об оплате сервисом;
//    * КонтрольнаяСумма - Строка - Значение контрольной суммы заказа;
//    * ВерсияКонтрольнойСуммы - Число - Версия расчета контрольной суммы заказа;
//    * ОплатаСБП - Структура, Неопределено - Содержит данные операции СБП, в том случае когда
//      значение Свойства СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b:
//      ** Идентификатор - Строка - идентификатор операции;
//      ** ИдентификаторОплаты - Строка - идентификатор оплаты в СБП;
//      ** ИдентификаторПлатежнойСистемы - Строка - идентификатор оплаты в Системе быстрых платежей;
//      ** ИдентификаторМерчанта - Строка - идентификатор мерчанта в Системе быстрых платежей;
//      ** НазначениеПлатежа - Строка - назначение платежа;
//      ** ДатаОперации - Дата - фактическая дата оплаты в UTC;
//      ** ИдентификаторПлатежа - Строка - уникальный Идентификатор Платежа, назначаемый Получателем;
//      ** РасчетныйСчет - Строка - расчетный счет получателя платежа;
//      ** СуммаНДС - Число - Сумма НДС операции;
//      ** ОблагаетсяНДС - Булево - Признак обложения операции НДС;
//  ПараметрыНастройкиОплаты - Соответствие - Содержит параметры настройки вида оплаты;
//
// Возвращаемое значение:
//  Структура - параметры оплаты.
//
Функция ЗаполнитьПараметрыОплаты(ДанныеЗаказа, ПараметрыНастройкиОплаты) Экспорт
	
	ПараметрыОперации = НовыйПараметрыОперации();
	
	ПараметрыОперации.ДатаОперации          = ДанныеЗаказа.ОплатаСБП.ДатаОперации;
	ПараметрыОперации.СуммаОперации         = ДанныеЗаказа.СуммаЗаказа;
	ПараметрыОперации.Идентификатор         = ДанныеЗаказа.ОплатаСБП.Идентификатор;
	ПараметрыОперации.ИдентификаторОперации = ДанныеЗаказа.ОплатаСБП.ИдентификаторПлатежнойСистемы;
	ПараметрыОперации.ИдентификаторОплаты   = ДанныеЗаказа.ОплатаСБП.ИдентификаторОплаты;
	ПараметрыОперации.ИдентификаторМерчанта = ДанныеЗаказа.ОплатаСБП.ИдентификаторМерчанта;
	ПараметрыОперации.НазначениеПлатежа     = ДанныеЗаказа.ОплатаСБП.НазначениеПлатежа;
	ПараметрыОперации.РасчетныйСчет         = ДанныеЗаказа.ОплатаСБП.РасчетныйСчет;
	ПараметрыОперации.ИдентификаторПлатежа  = ДанныеЗаказа.ОплатаСБП.ИдентификаторПлатежа;
	ПараметрыОперации.СуммаНДС              = ДанныеЗаказа.ОплатаСБП.СуммаНДС;
	ПараметрыОперации.ОблагаетсяНДС         = ДанныеЗаказа.ОплатаСБП.ОблагаетсяНДС;
	
	Результат = Новый Структура;
	
	РезультатОбработки = Новый Структура;
	РезультатОбработки.Вставить("ПараметрыОперации", ПараметрыОперации);
	РезультатОбработки.Вставить("СообщениеОбОшибке", "");
	РезультатОбработки.Вставить(
		"СтатусОперации",
		НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполнена());
	
	Результат.Вставить("НастройкаПодключения", ПараметрыНастройкиОплаты.НастройкаПодключения);
	Результат.Вставить("РезультатОбработки",   РезультатОбработки);
	
	Возврат Результат;
	
КонецФункции

// Выполняет запись данных оплаты заказа.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ по которому была выполнена оплата;
//  ПараметрыОплаты - Структура - Содержит параметры оплаты  см.ПереводыСБПc2bСлужебный.ПриЗаполненииПараметровОплаты;
//  ПараметрыНастройкиОплаты - Соответствие - параметры настройки оплаты.
//
Процедура ЗаписатьДанныеОплаты(
		ДокументОперации,
		ПараметрыОплаты,
		ПараметрыНастройкиОплаты) Экспорт
	
	ПараметрыОперацииСБП = ПараметрыОплаты.РезультатОбработки.ПараметрыОперации;
	
	РегистрыСведений.ИдентификаторыОперацийСБПb2b.ЗаписатьНовыеДанныеОплаты(
		ДокументОперации,
		ПараметрыОперацииСБП.СуммаОперации,
		ПараметрыНастройкиОплаты,
		ПараметрыОперацииСБП);
	
КонецПроцедуры

// Выполняет обработку назначения платежа.
//
// Параметры:
//  НазначениеПлатежа - Строка - Назначение платежа.
//
Процедура ПодготовитьНазначениеПлатежа(НазначениеПлатежа) Экспорт
	
	Если СтрДлина(НазначениеПлатежа) > 210 Тогда
		НазначениеПлатежа = Лев(НазначениеПлатежа, 210);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбщегоНазначения

// Создает описание результат длительной операции в сервисе.
//
// Возвращаемое значение:
//  Структура - результат длительной операции:
//    * СтатусОперации - Строка - текущее состояние операции оплаты;
//    * ПараметрыОперации - Структура - см. НовыйОписаниеПараметровОперации;
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция НовыйРезультатДлительнойОперации() Экспорт
	
	РезультатОперации = СистемаБыстрыхПлатежейСлужебный.НовыйРезультатДлительнойОперации();
	РезультатОперации.Вставить("ПараметрыОперации", НовыйОписаниеПараметровОперации());
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует новое описание параметров операции, используется в длительных операциях и переопределяемых методах.
//
// Возвращаемое значение:
//  Структура - содержит описание параметров операции:
//    * ДатаОперации - Дата - фактическая дата операции в UTC;
//    * СуммаОперации - Число, Неопределено - фактическая суммы возврата по документу;
//    * ИдентификаторОперации - Строка - ключ контроля загрузки;
//    * ИдентификаторОплаты - Строка - идентификатор оплаты;
//    * РасчетныйСчет - Строка - расчетный счет получения денежных средств;
//    * ИдентификаторПлатежа - Строка - уникальный Идентификатор Платежа, назначаемый Получателем;
//    * ДокументОснование - ОпределяемыйТип.ДокументОперацииСБП, Неопределено - документ основание платежа.
//
Функция НовыйОписаниеПараметровОперации() Экспорт
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("ДатаОперации", Дата(1, 1, 1));
	ПараметрыОперации.Вставить("СуммаОперации", Неопределено);
	ПараметрыОперации.Вставить("ИдентификаторОперации", "");
	ПараметрыОперации.Вставить("ИдентификаторОплаты", ""); 
	ПараметрыОперации.Вставить("РасчетныйСчет", "");
	ПараметрыОперации.Вставить("ИдентификаторПлатежа", "");
	ПараметрыОперации.Вставить("ДокументОснование", Неопределено);
	
	Возврат ПараметрыОперации;
	
КонецФункции

// Создает описание результат операции данные ссылки.
//
// Возвращаемое значение:
//  Структура - результат операции:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//    * ДанныеСсылки - Неопределено - описание данные функциональной ссылки.
//
Функция НовыйРезультатОперацииДанныеСсылки() Экспорт
	
	РезультатОперации = СистемаБыстрыхПлатежейСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("ДанныеСсылки", Неопределено);
	
	Возврат РезультатОперации;
	
КонецФункции

#КонецОбласти

#Область ОнлайнЗаказы

// Возвращает новое описание параметров операции
//
// Возвращаемое значение:
//  Структура - параметры операции.
//
Функция НовыйПараметрыОперации()
	
	Результат = Новый Структура;
	Результат.Вставить("ДатаОперации",            Дата(1, 1, 1));
	Результат.Вставить("СуммаОперации",           0);
	Результат.Вставить("Идентификатор",           "");
	Результат.Вставить("ИдентификаторОперации",   "");
	Результат.Вставить("ИдентификаторОплаты",     "");
	Результат.Вставить("ИдентификаторМерчанта",   "");
	Результат.Вставить("РасчетныйСчет",           "");
	Результат.Вставить("ИдентификаторПлатежа",    "");
	Результат.Вставить("ОблагаетсяНДС",           Ложь);
	Результат.Вставить("СуммаНДС",                0);
	Результат.Вставить("ДокументЧастичнойОплаты", Документы.ПлатежнаяСсылкаСБП.ПустаяСсылка());
	Результат.Вставить("НазначениеПлатежа",       "");
	
	Возврат Результат;
	
КонецФункции

// Подготавливает и возвращает параметры операции для передачи в отложенный обработчик.
//
// Параметры:
//  ДанныеОперации - Структура - данные операции, хранящиеся в кэше подсистемы;
//
// Возвращаемое значение:
// Структура - содержит заполненные параметры операции
//   * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения к СБП.
//   * РезультатОбработки - Структура - содержит результаты обработки статуса операции
//     ** СтатусОперации - Строка - текущее состояние операции оплаты. Возможные значения:
//        - "Выполняется" - подтверждение оплаты не получено;
//        - "Отменена" - оплата по ранее сформированному QR-коду невозможна;
//        - "Выполнена" - участник СБП подтвердил оплату;
//        - "Отклонена" - оплата по ранее сформированному QR-коду невозможна;
//        - "Ошибка" - неизвестный статус операции.
//     ** ПараметрыОперации - Структура - Содержит данные операции:
//       *** ДатаОперации - Дата - фактическая дата оплаты в UTC;
//       *** СуммаОперации - Число - Сумма оплаты в СБП
//       *** ИдентификаторОперации - Строка - идентификатор выполненной операции;
//       *** ИдентификаторОплаты - Строка, Неопределено - идентификатор оплаты;
//       *** РасчетныйСчет - Строка - расчетный счет получателя платежа;
//       *** ИдентификаторПлатежа - Строка - уникальный Идентификатор Платежа, назначаемый Получателем;
//       *** СуммаНДС - Число - Сумма НДС оплаты в СБП (присутствует в сценарии b2b);
//       *** ОблагаетсяНДС - Булево - Признак обложения НДС оплаты в СБП (присутствует в сценарии b2b);
 //      *** ДокументЧастичнойОплаты - ДокументСсылка.ПлатежнаяСсылкаСБП - документ частичной оплаты.
//
Функция ЗаполнитьПараметрыОперации(ДанныеОперации)
	
	Результат = Новый Структура;
	
	ПараметрыОперацииСБП = Новый Структура;
	ПараметрыОперацииСБП.Вставить("ДатаОперации",            ДанныеОперации.ДатаОперации);
	ПараметрыОперацииСБП.Вставить("СуммаОперации",           ДанныеОперации.СуммаОперации);
	ПараметрыОперацииСБП.Вставить("ИдентификаторОперации",   ДанныеОперации.ИдентификаторОперации);
	ПараметрыОперацииСБП.Вставить("ИдентификаторОплаты",     ДанныеОперации.ИдентификаторОплаты);
	ПараметрыОперацииСБП.Вставить("РасчетныйСчет",           ДанныеОперации.РасчетныйСчет);
	ПараметрыОперацииСБП.Вставить("ИдентификаторПлатежа",    ДанныеОперации.ИдентификаторПлатежа);
	ПараметрыОперацииСБП.Вставить("ОблагаетсяНДС",           ДанныеОперации.ОблагаетсяНДС);
	ПараметрыОперацииСБП.Вставить("СуммаНДС",                ДанныеОперации.СуммаНДС);
	ПараметрыОперацииСБП.Вставить("ДокументЧастичнойОплаты", Документы.ПлатежнаяСсылкаСБП.ПустаяСсылка());
	
	РезультатПолученияСтатуса = НастройкиПлатежныхСистемСлужебный.ПолучитьСтатусОперацииПоИдентификатору(
		ДанныеОперации.СтатусОперации);
	
	РезультатОбработки = Новый Структура;
	РезультатОбработки.Вставить("ПараметрыОперации", ПараметрыОперацииСБП);
	РезультатОбработки.Вставить("СообщениеОбОшибке", "");
	РезультатОбработки.Вставить("СтатусОперации", РезультатПолученияСтатуса.СтатусОперации);
	
	Результат.Вставить("НастройкаПодключения", ДанныеОперации.НастройкаПодключения);
	Результат.Вставить("РезультатОбработки",   РезультатОбработки);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПодготовкаПлатежнойСсылки

// Формирует настройки подключения для выполнения запросов.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ, по данным которого
//    необходимо получить список настроек.
//
// Возвращаемое значение:
//  Структура - см. СистемаБыстрыхПлатежейСлужебный.НовыйПараметрыНастройкиПодключения.
//
Функция ПараметрыНастройкиПодключенияПоДокументу(ДокументОперации) Экспорт
	
	// Заполнение настроек проведения оплаты.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИдентификаторыОперацийСБПb2b.ИдентификаторМерчанта КАК ИдентификаторМерчанта,
	|	ВЫБОР
	|		КОГДА ИдентификаторыОперацийСБПb2b.ИдентификаторУчастника <> """"
	|			ТОГДА ИдентификаторыОперацийСБПb2b.ИдентификаторУчастника
	|		ИНАЧЕ ИдентификаторыОперацийСБПb2b.НастройкаПодключения.ИдентификаторУчастника
	|	КОНЕЦ КАК ИдентификаторУчастника,
	|	ИдентификаторыОперацийСБПb2b.НастройкаПодключения.Ссылка КАК НастройкаПодключения,
	|	ИдентификаторыОперацийСБПb2b.НастройкаПодключения.Используется КАК Используется,
	|	ИдентификаторыОперацийСБПb2b.НастройкаПодключения.Ссылка КАК НастройкаПодключенияСсылка,
	|	ЕСТЬNULL(&ИмяПоляТипАутентификации, ""UNKNOW"") КАК ТипАутентификации,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НастройкиУчастниковСБП.ПеренаправлениеПоСсылке, ЛОЖЬ)
	|			ТОГДА ИдентификаторыОперацийСБПb2b.НастройкаПодключения.СсылкаПеренаправления
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК СсылкаПеренаправления
	|ИЗ
	|	РегистрСведений.ИдентификаторыОперацийСБПb2b КАК ИдентификаторыОперацийСБПb2b
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчастниковСБП КАК НастройкиУчастниковСБП
	|		ПО (ИдентификаторыОперацийСБПb2b.ДокументОперации = &ДокументОперации)
	|			И (ВЫБОР
	|				КОГДА ИдентификаторыОперацийСБПb2b.ИдентификаторУчастника <> """"
	|					ТОГДА ИдентификаторыОперацийСБПb2b.ИдентификаторУчастника
	|				ИНАЧЕ ИдентификаторыОперацийСБПb2b.НастройкаПодключения.ИдентификаторУчастника
	|			КОНЕЦ = НастройкиУчастниковСБП.Идентификатор)
	|ГДЕ
	|	ИдентификаторыОперацийСБПb2b.ДокументОперации = &ДокументОперации";
	
	Запрос.УстановитьПараметр("ДокументОперации", ДокументОперации);
	
	ИмяПоля = "";
	СистемаБыстрыхПлатежейСобытия.ПриОпределенииИмениПоляТипаАутентификации(
		Перечисления.ВариантыНастройкиСБП.b2b,
		ИмяПоля);
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ИмяПоляТипАутентификации",
		"НастройкиУчастниковСБП." + ИмяПоля);
	
	Возврат СистемаБыстрыхПлатежейСлужебный.НовыйПараметрыНастройкиПодключения(
		Запрос,
		"РегистрСведений");
	
КонецФункции

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыФункцииПрикладныхОпераций

// Проверяет права на вызов прикладных операций Системы быстрых платежей
// в случае отсутствия прав, вызывается исключение.
//
Процедура ПроверитьПраваНаВыполнениеОперации() Экспорт
	
	Если Не ПереводыСБПb2b.ПереводыСБПДоступны() Тогда
		ВызватьИсключение НСтр("ru = 'Нарушение прав доступа. Пользователю запрещено выполнение операций в Системе быстрых платежей.
			|Обратитесь к администратору.';
			|en = 'Access violation. FPS transactions are unavailable for the user.
			|Contact the administrator.'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
