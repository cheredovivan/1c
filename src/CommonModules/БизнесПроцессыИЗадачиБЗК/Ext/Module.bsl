#Область СлужебныйПрограммныйИнтерфейс

Процедура ПометитьНаУдалениеБизнесПроцессыПредмета(Предмет) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	ЗаПрос.УстановитьПараметр("Предмет", Предмет);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Задание.Ссылка КАК БизнесПроцесс,
		|	Задание.Завершен КАК БизнесПроцессЗавершен,
		|	Задание.ПометкаУдаления КАК БизнесПроцессПометкаУдаления,
		|	ЗадачаИсполнителя.Ссылка КАК ЗадачаИсполнителя,
		|	ЗадачаИсполнителя.Выполнена КАК ЗадачаИсполнителяВыполнена,
		|	ЗадачаИсполнителя.ПометкаУдаления КАК ЗадачаИсполнителяПометкаУдаления
		|ИЗ
		|	БизнесПроцесс.Задание КАК Задание
		|		ЛЕВОЕ СОЕДИНЕНИЕ Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|		ПО Задание.Ссылка = ЗадачаИсполнителя.БизнесПроцесс
		|ГДЕ
		|	Задание.Предмет = &Предмет";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("БизнесПроцесс") Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Если Не Выборка.БизнесПроцессЗавершен Или Не Выборка.БизнесПроцессПометкаУдаления Тогда
				БизнесПроцессыИЗадачиСервер.ЗаблокироватьБизнесПроцессы(Выборка.БизнесПроцесс);
				ОбъектБизнесПроцесс = Выборка.БизнесПроцесс.ПолучитьОбъект();
				ОбъектБизнесПроцесс.Завершен = Истина;
				ОбъектБизнесПроцесс.ПометкаУдаления = Истина;
				ОбъектБизнесПроцесс.Записать();
			КонецЕсли;
			
			Пока Выборка.Следующий() Цикл
				Если Не Выборка.ЗадачаИсполнителяВыполнена Или Не Выборка.ЗадачаИсполнителяПометкаУдаления Тогда
					БизнесПроцессыИЗадачиСервер.ЗаблокироватьЗадачи(Выборка.ЗадачаИсполнителя);
					ОбъектЗадачаИсполнителя = Выборка.ЗадачаИсполнителя.ПолучитьОбъект();
					ОбъектЗадачаИсполнителя.Заблокировать();
					ОбъектЗадачаИсполнителя.Выполнена = Истина;
					ОбъектЗадачаИсполнителя.ПометкаУдаления = Истина;
					ОбъектЗадачаИсполнителя.Записать();
				КонецЕсли;
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти