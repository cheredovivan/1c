
#Область ПрограммныйИнтерфейс

Функция ОшибкаНаРазмерПисьмаФНС(Размер) Экспорт
	
	ПредставлениеРазмера = ОбщегоНазначенияЭДКОКлиентСервер.ТекстовоеПредставлениеРазмераФайла(Размер, 2);
	
	СобытиеНаступило = ПисьмаВГосОрганыВызовСервера.СобытиеНаступилоПисьмаФНС1Мб();
	
	Если СобытиеНаступило Тогда
		ТекстОшибки = НСтр("ru = 'Размер письма после шифрования и архивирования составил %1. 
	                        |В соответствии с Приказом ФНС России от 06.06.2025 № ЕД-7-26/509@ размер не может превышать 1 Мб.';
	                        |en = 'Размер письма после шифрования и архивирования составил %1. 
	                        |В соответствии с Приказом ФНС России от 06.06.2025 № ЕД-7-26/509@ размер не может превышать 1 Мб.'");
	Иначе
		ТекстОшибки = НСтр("ru = 'Размер письма после шифрования и архивирования составил %1. 
	                        |В соответствии с Приказом ФНС России от 22.11.2021 № ЕД-7-26/1011@ размер не может превышать 5 Мб.';
	                        |en = 'Размер письма после шифрования и архивирования составил %1. 
	                        |В соответствии с Приказом ФНС России от 22.11.2021 № ЕД-7-26/1011@ размер не может превышать 5 Мб.'");
	КонецЕсли;

	ТекстОшибки = СтрШаблон(ТекстОшибки, ПредставлениеРазмера);
	
	Подстроки = Новый Массив;
	Подстроки.Добавить(ТекстОшибки);
	Подстроки.Добавить(Символы.ПС);
	Подстроки.Добавить(Символы.ПС);
	Подстроки.Добавить(НСтр("ru = 'Для отправки больших писем и ответов на требование используйте Пакет с дополнительными документами.';
							|en = 'Для отправки больших писем и ответов на требование используйте Пакет с дополнительными документами.'"));
	
	ТекстОшибки = Новый ФорматированнаяСтрока(Подстроки);
	
	Результат = ШаблонОшибкиПисьмаФНС();
	Результат.Вставить("ТекстОшибки", ТекстОшибки);
	Результат.Вставить("ТекстКнопки", НСтр("ru = 'Создать Пакет с дополнительными документами';
											|en = 'Создать Пакет с дополнительными документами'"));
	Результат.Вставить("БлокироватьОтправку", Истина);
	
	Возврат Результат;
	
КонецФункции

Функция ОшибкаНаОтветНаТребованиеФНСПисьмом() Экспорт
	
	ТекстОшибки = НСтр("ru = 'Ответ на требование документов (информации) не рекомендуется отправлять письмом, поскольку оно может попасть к другому инспектору ИФНС.
                        |
                        |Создать ответ в рекомендуемом формате по приказу ФНС от 18 января 2017 г. N ММВ-7-6/16@?';
                        |en = 'Ответ на требование документов (информации) не рекомендуется отправлять письмом, поскольку оно может попасть к другому инспектору ИФНС.
                        |
                        |Создать ответ в рекомендуемом формате по приказу ФНС от 18 января 2017 г. N ММВ-7-6/16@?'");

	Результат = ШаблонОшибкиПисьмаФНС();
	Результат.Вставить("ТекстОшибки", ТекстОшибки);
	Результат.Вставить("ТекстКнопки", НСтр("ru = 'Создать ответ в рекомендуемом формате';
											|en = 'Создать ответ в рекомендуемом формате'"));
	
	Возврат Результат;
	
КонецФункции

Функция ЭтоФормаПисьма(Форма) Экспорт
	
	Если ТипЗнч(Форма) = Тип("ФормаКлиентскогоПриложения")
		И ДлительнаяОтправкаКлиентСервер.ИмяФормыПоПолномуИмени(Форма.ИмяФормы) = "ФормаИсходящееПисьмо" Тогда
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

Функция ТекстИнформацииЗапретВложенийПисемФНС(Организация, ЗапретПисем, ЗапретПисемДатаНачала) Экспорт
	
	Результат = СтрШаблон(НСтр("ru = '<b>С %1 %2запрещено %3.</b>
	|
	|Для передачи подтверждающих документов к ранее отправленным отчетам, уведомлениям, запросам, пояснениям, а также ответов на требования, аудиторских заключений и банковских гарантий воспользуйтесь <a href = ""ОписьИсходящихДокументов"">пакетом с доп. документами</a>.
	|
	|В других случаях для отправки обращений %4воспользуйтесь одним из следующих способов:
	|• <a href = ""%5"">Личный кабинет налогоплательщика</a>
	|• Сервис <a href = ""https://www.nalog.gov.ru/rn47/service/obr_fts/"">Обратиться в ФНС</a>
	|• <a href = ""https://pos.gosuslugi.ru/landing/"">Платформа обратной связи</a> на Госуслугах';
	|en = '<b>С %1 %2запрещено %3.</b>
	|
	|Для передачи подтверждающих документов к ранее отправленным отчетам, уведомлениям, запросам, пояснениям, а также ответов на требования, аудиторских заключений и банковских гарантий воспользуйтесь <a href = ""ОписьИсходящихДокументов"">пакетом с доп. документами</a>.
	|
	|В других случаях для отправки обращений %4воспользуйтесь одним из следующих способов:
	|• <a href = ""%5"">Личный кабинет налогоплательщика</a>
	|• Сервис <a href = ""https://www.nalog.gov.ru/rn47/service/obr_fts/"">Обратиться в ФНС</a>
	|• <a href = ""https://pos.gosuslugi.ru/landing/"">Платформа обратной связи</a> на Госуслугах'"),
		?(ЗапретПисем, Формат(ЗапретПисемДатаНачала, "ДЛФ=DD"), "15 декабря 2025 г."),
		?(ЗапретПисем, "", "в соответствии с </b><a href = ""https://www.nalog.gov.ru/rn77/about_fts/docs/16585183/""><b>приказом</b></a><b> ФНС России от 27.11.2025 № ЕА-7-26/1024@ "),
		?(ЗапретПисем, "отправлять письма из программы", "из программы отправлять письма с вложениями"),
		?(ЗапретПисем, "", "с вложениями "),
		ПисьмаВГосОрганыВызовСервера.СсылкаНаЛичныйКабинетНалогоплательщикаПоВидуОрганизации(Организация));
		
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ШаблонОшибкиПисьмаФНС()
	
	Результат = Новый Структура();
	Результат.Вставить("Заголовок", "");
	Результат.Вставить("ТекстОшибки", "");
	Результат.Вставить("ТекстКнопки", "");
	Результат.Вставить("БлокироватьОтправку", Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция СобытияЗапретВложенийПисемФНС() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ЗапретПисемФНС", Ложь);
	Результат.Вставить("ЗапретВложенийФНС", Ложь);
	Результат.Вставить("ЗапретПисемФНСДатаНачала", Дата(1, 1, 1));
	
	// Поиск состояния события в общих настройках
	Результат.ЗапретПисемФНС = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(
		"ДокументооборотСКонтролирующимиОрганами_ИсходящиеПисьма_СобытиеЗапретПисемФНС", ,
		Результат.ЗапретПисемФНС);
	
	Если Результат.ЗапретПисемФНС = Истина Тогда
		
		// Поиск даты начала события с учетом общих настроек
		Результат.ЗапретПисемФНСДатаНачала = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(
			"ДокументооборотСКонтролирующимиОрганами_ИсходящиеПисьма_СобытиеЗапретПисемФНСДатаНачала", ,
			Результат.ЗапретПисемФНСДатаНачала);
			
	Иначе
		
		СтруктураСобытияЗапретПисемФНС = ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступилоРасширенная("ЗапретПисемФНС2");
		
		Результат.ЗапретПисемФНС = СтруктураСобытияЗапретПисемФНС.СобытиеНаступило;
		Результат.ЗапретПисемФНСДатаНачала = СтруктураСобытияЗапретПисемФНС.ДатаНачала;
		
	КонецЕсли;
	
	// Поиск состояния события с учетом общих настроек
	Результат.ЗапретВложенийФНС = ОнлайнСервисыРегламентированнойОтчетностиВызовСервера.СобытиеНаступило(
		"ЗапретВложенийПисемФНС2",
		"ДокументооборотСКонтролирующимиОрганами_ИсходящиеПисьма_СобытиеЗапретВложенийФНС");
	
	Возврат Результат; 
	
КонецФункции

#КонецОбласти    
