#Область СлужебныйПрограммныйИнтерфейс

Функция УстановленныеПодписи(Объект) Экспорт
	
	ПараметрыПолучения = ЭлектроннаяПодпись.НовыйПараметрыПолученияПодписейОбъекта();
	ПараметрыПолучения.ВозвращатьДанныеМЧД = Истина;
	ПараметрыПолучения.ВозвращатьСертификатыИзПодписей = Истина;
	
	Подписи = ЭлектроннаяПодпись.ПодписиОбъекта(Объект, ПараметрыПолучения);
	РеквизитыПодписей = РеквизитыУстановленныхПодписей(Объект);
	
	НомерПодписи = 1;
	Для Каждого СвойстваПодписи Из Подписи Цикл
		
		СвойстваПодписи.Вставить("НаборСвойствПодписиЭДОБЗК", РеквизитыПодписей.Получить(СвойстваПодписи.ИдентификаторПодписи));
		СвойстваПодписи.Вставить("ИмяФайла",
			ЭДОБЗКВызовСервера.ИмяФайлаПодписи(
				ИмяФайлаПодписиОбъекта(Объект),
				Подписи.Количество() > 1,
				НомерПодписи));
		НомерПодписи = НомерПодписи + 1;
	КонецЦикла;
	
	Возврат Подписи;
	
КонецФункции

Процедура ЗаполнитьПодписиВФорме(УправляемаяФорма, СсылкаНаОбъект) Экспорт
	
	УправляемаяФорма.ЭлектронныеПодписи.Очистить();
	ЦифровыеПодписиОбъекта = ЭлектронныеПодписиДокумента(СсылкаНаОбъект, УправляемаяФорма.УникальныйИдентификатор);
	Для Каждого ЦифроваяПодписьОбъекта Из ЦифровыеПодписиОбъекта Цикл
		СтрокаПодписи = УправляемаяФорма.ЭлектронныеПодписи.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПодписи, ЦифроваяПодписьОбъекта);
	КонецЦикла;
	
КонецПроцедуры

Функция ДанныеМЧДПодписи(МЧД, ИдентификаторХранилища) Экспорт
	
	Если МЧД <> Неопределено Тогда
		
		ОписаниеДоверенности = ОписаниеМЧД();
		
		УстановитьПривилегированныйРежим(Истина);
		ФайлДоверенности = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МЧД, "ФайлДоверенности");
		ДанныеФайла = РаботаСФайлами.ДанныеФайла(ФайлДоверенности);
		ДанныеДоверенности = ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
		УстановитьПривилегированныйРежим(Ложь);
		
		ОписаниеДоверенности.Ссылка = МЧД;
		ОписаниеДоверенности.Доверенность = ПоместитьВоВременноеХранилище(ДанныеДоверенности, ИдентификаторХранилища);
		ОписаниеДоверенности.ИмяФайлаСРасширением = ЭДОБЗКВызовСервера.ИмяВыгружаемогоФайлаМЧД(МЧД);
		
		ОписаниеДоверенности.Размер = ДанныеДоверенности.Размер();
		
		ПодписиМЧД = УстановленныеПодписи(ФайлДоверенности);
		Для Каждого ПодписьМЧД Из ПодписиМЧД Цикл
			ОписаниеПодписи = ОписаниеПодписиМЧД();
			ОписаниеПодписи.АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(
				ПодписьМЧД.Подпись, ИдентификаторХранилища);
			ОписаниеПодписи.ИмяФайлаСРасширением = ПодписьМЧД.ИмяФайла;
			Если ПодписьМЧД.НаборСвойствПодписиЭДОБЗК <> Неопределено Тогда
				ОписаниеПодписи.НаборСвойствПодписиЭДОБЗК = Новый Массив;
				Для Каждого ОписаниеСвойств Из ОписаниеПодписи.НаборСвойствПодписиЭДОБЗК Цикл
					ОписаниеСвойствПодписи = Новый Структура("МЧД,РольПодписанта");
					ОписаниеПодписи.НаборСвойствПодписиЭДОБЗК.Добавить(ОписаниеСвойствПодписи);
					ОписаниеСвойствПодписи.РольПодписанта = ОписаниеСвойств.РольПодписанта;
					Если ЗначениеЗаполнено(ОписаниеСвойств.МЧД) Тогда
						ОписаниеСвойствПодписи.МЧД = ДанныеМЧДПодписи(ОписаниеСвойств.МЧД, ИдентификаторХранилища);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ОписаниеДоверенности.Подписи.Добавить(ОписаниеПодписи);
		КонецЦикла;
		
		Возврат ОписаниеДоверенности;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ОрганизацияМЧД(МЧД) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Доверитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МЧД, "Доверитель");
	УстановитьПривилегированныйРежим(Ложь);
	Если ЗначениеЗаполнено(Доверитель)
		И ТипЗнч(Доверитель) = Тип("СправочникСсылка.Организации") Тогда
		
		Возврат Доверитель;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	УстановленныеПодписи = УстановленныеПодписи(МЧД);
	УстановитьПривилегированныйРежим(Ложь);
	Для Каждого СвойстваПодписи Из УстановленныеПодписи Цикл
		ДополнительныеСвойства = Неопределено;
		Если СвойстваПодписи.Свойство("ДополнительныеСвойства", ДополнительныеСвойства) Тогда
			Если ДополнительныеСвойства <> Неопределено Тогда
				Для Каждого ДанныеПодписи Из ДополнительныеСвойства Цикл
					Если ЗначениеЗаполнено(ДанныеПодписи.МЧД) Тогда
						Организация = ОрганизацияМЧД(МЧД);
						Если ЗначениеЗаполнено(Организация) Тогда
							Возврат Организация;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ЗарегистрироватьНаборСвойствПодписи(Знач Объект, Знач ИдентификаторПодписи, Знач РольПодписанта) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(ИдентификаторПодписи) = Тип("Строка") Тогда
		ИдентификаторПодписи = Новый УникальныйИдентификатор(ИдентификаторПодписи);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ЭлектронныеПодписиЭДОБЗК.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПодписанныйОбъект.Установить(Объект);
	НаборЗаписей.Отбор.ИдентификаторПодписи.Установить(ИдентификаторПодписи);
	Запись = НаборЗаписей.Добавить();
	Запись.ПодписанныйОбъект 	= Объект;
	Запись.ИдентификаторПодписи = ИдентификаторПодписи;
	Запись.РольПодписанта    = РольПодписанта;
	НаборЗаписей.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры
 
Функция ИмяФайлаПодписиОбъекта(Объект)
	
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.МашиночитаемыеДоверенности") Тогда
		Возврат ЭДОБЗКВызовСервера.ИмяВыгружаемогоФайлаМЧД(Объект);
	КонецЕсли;
	
	Возврат ЭДОБЗКВызовСервера.ИмяФайлаЭДОБЗК(Строка(Объект));
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытий

Процедура ЗапомнитьУстановленныеПодписи(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ОтпечаткиСертификатов = Новый Соответствие;
	ДанныеПодписей = Неопределено;
	РеквизитыУстановленныхПодписей = Новый Соответствие;
	ОтборНаборов = РегистрыБЗК.ОтборыЗаписейНабора(Источник, Замещение);
	Для Каждого ОтборНабора Из ОтборНаборов Цикл
		Если Не ОтборНабора["ПодписанныйОбъект_Использование"] Тогда
			Продолжить;
		КонецЕсли;
		
		ПодписанныйОбъект = ОтборНабора["ПодписанныйОбъект"];
		Если Не ЗарплатаКадры.ЭтоОбъектЗарплатноКадровойБиблиотеки(ПодписанныйОбъект.Метаданные().ПолноеИмя()) Тогда
			Продолжить;
		КонецЕсли;
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЭлектронныеПодписи.ПодписанныйОбъект КАК ПодписанныйОбъект,
			|	ЭлектронныеПодписи.ИдентификаторПодписи КАК ИдентификаторПодписи
			|ИЗ
			|	РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
			|ГДЕ
			|	&ТекстОтбора";
		
		ТекстОтбора = "(ИСТИНА)";
		ТекстыОтбора = Новый Массив;
		Для Каждого Колонка Из ОтборНаборов.Колонки Цикл
			Если СтрЗаканчиваетсяНа(Колонка.Имя, "_Использование")
				И ОтборНабора[Колонка.Имя] Тогда
				
				ЛевоеЗначение = СтрЗаменить(Колонка.Имя, "_Использование", "");
				Запрос.УстановитьПараметр(ЛевоеЗначение, ОтборНабора[ЛевоеЗначение]);
				ТекстыОтбора.Добавить("(ЭлектронныеПодписи." + ЛевоеЗначение + " = &" + ЛевоеЗначение + ")");
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеЗаполнено(ТекстыОтбора) Тогда
			ТекстОтбора = СтрСоединить(ТекстыОтбора, " И ");
		КонецЕсли;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстОтбора", ТекстОтбора);
		
		УстановитьПривилегированныйРежим(Истина);
		ДанныеПодписейБазы = Запрос.Выполнить().Выгрузить();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если ДанныеПодписей = Неопределено Тогда
			ДанныеПодписей = ДанныеПодписейБазы;
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеПодписейБазы, ДанныеПодписей);
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(РеквизитыУстановленныхПодписей,
			ЭлектроннаяПодписьЭДОБЗКПовтИсп.РеквизитыУстановленныхПодписей(ПодписанныйОбъект), Истина);
		
		Если ЭДОБЗК.ЭтоЭлектронныйДокумент(ПодписанныйОбъект) Тогда
			ОтпечаткиСертификатов.Вставить(ПодписанныйОбъект, ОтпечаткиСертификатовПодписейОбъекта(ПодписанныйОбъект));
		КонецЕсли;
		
	КонецЦикла;
	Если ЗначениеЗаполнено(ДанныеПодписей) Тогда
		Источник.ДополнительныеСвойства.Вставить("ДанныеПодписей", ДанныеПодписей);
		Источник.ДополнительныеСвойства.Вставить("РеквизитыУстановленныхПодписей", РеквизитыУстановленныхПодписей);
	КонецЕсли;
	Если ЗначениеЗаполнено(ОтпечаткиСертификатов) Тогда
		Источник.ДополнительныеСвойства.Вставить("ОтпечаткиСертификатов", ОтпечаткиСертификатов);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьДанныеЭлектронныхПодписей(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ПодписанныеОбъекты = РегистрыБЗК.ЗначенияОтбораЗаписейНабора(Источник, Замещение, "ПодписанныйОбъект");
	Если Источник.ДополнительныеСвойства.Свойство("ДанныеПодписей") Тогда
		Для Каждого ПодписанныйОбъект Из ПодписанныеОбъекты Цикл
			ЗаписиОбоихНаборов = Новый Массив;
			ТаблицаПодписей = Источник.Выгрузить();
			Для Каждого ДанныеПодписи Из Источник.ДополнительныеСвойства.ДанныеПодписей.НайтиСтроки(Новый Структура("ПодписанныйОбъект", ПодписанныйОбъект)) Цикл
				СтруктураПоиска = Новый Структура("ПодписанныйОбъект,ИдентификаторПодписи");
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, ДанныеПодписи);
				ЗаписиПодписи = ТаблицаПодписей.НайтиСтроки(СтруктураПоиска);
				Если ЗаписиПодписи.Количество() = 0 Тогда
					НаборЗаписей = РегистрыСведений.ЭлектронныеПодписиЭДОБЗК.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.ПодписанныйОбъект.Установить(ДанныеПодписи.ПодписанныйОбъект);
					НаборЗаписей.Отбор.ИдентификаторПодписи.Установить(ДанныеПодписи.ИдентификаторПодписи);
					НаборЗаписей.Записать();
				Иначе
					ЗаписиОбоихНаборов.Добавить(ЗаписиПодписи[0]);
					ДанныеИдентификатораПодписи = Источник.ДополнительныеСвойства.РеквизитыУстановленныхПодписей.Получить(ДанныеПодписи.ИдентификаторПодписи);
					Если ДанныеИдентификатораПодписи <> Неопределено Тогда
						Для Каждого ДанныеПодписи Из ДанныеИдентификатораПодписи Цикл
							ЗарегистрироватьНаборСвойствПодписи(
								ПодписанныйОбъект, ЗаписиПодписи[0].ИдентификаторПодписи, ДанныеПодписи.РольПодписанта);
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Для Каждого ДанныеПодписи Из ТаблицаПодписей Цикл
				Если ЗаписиОбоихНаборов.Найти(ДанныеПодписи) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				ДанныеИдентификатораПодписи = Источник.ДополнительныеСвойства.РеквизитыУстановленныхПодписей.Получить(ДанныеПодписи.ИдентификаторПодписи);
				Если ДанныеИдентификатораПодписи <> Неопределено Тогда
					Для Каждого ДанныеПодписи Из ДанныеИдентификатораПодписи Цикл
						ЗарегистрироватьНаборСвойствПодписи(
							ПодписанныйОбъект, ДанныеПодписи.ИдентификаторПодписи, ДанныеПодписи.РольПодписанта);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("ОтпечаткиСертификатов") Тогда
		Для Каждого ПодписанныйОбъект Из ПодписанныеОбъекты Цикл
			Если Не ОбщегоНазначения.КоллекцииИдентичны(Источник.ДополнительныеСвойства.ОтпечаткиСертификатов.Получить(ПодписанныйОбъект),
				ОтпечаткиСертификатовПодписейОбъекта(ПодписанныйОбъект)) Тогда
				
				ЭДОБЗК.ЗарегистрироватьФайлыДокументовЭДОБЗККПересчетуСостояний(ПодписанныйОбъект);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Функция РеквизитыУстановленныхПодписей(Объект, ИдентификаторПодписи = Неопределено) Экспорт
	
	РеквизитыПодписей = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодписанныйОбъект", Объект);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЭлектронныеПодписиЭДОБЗК.ИдентификаторПодписи КАК ИдентификаторПодписи,
		|	ЭлектронныеПодписиЭДОБЗК.РольПодписанта КАК РольПодписанта,
		|	ЕСТЬNULL(ЭлектронныеПодписиМЧД.МашиночитаемаяДоверенность, НЕОПРЕДЕЛЕНО) КАК МЧД
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписиЭДОБЗК КАК ЭлектронныеПодписиЭДОБЗК
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЭлектронныеПодписиМЧД КАК ЭлектронныеПодписиМЧД
		|			ПО ЭлектронныеПодписи.ПодписанныйОбъект = ЭлектронныеПодписиМЧД.ПодписанныйОбъект
		|				И ЭлектронныеПодписи.ИдентификаторПодписи = ЭлектронныеПодписиМЧД.ИдентификаторПодписи
		|		ПО ЭлектронныеПодписиЭДОБЗК.ПодписанныйОбъект = ЭлектронныеПодписи.ПодписанныйОбъект
		|			И ЭлектронныеПодписиЭДОБЗК.ИдентификаторПодписи = ЭлектронныеПодписи.ИдентификаторПодписи
		|ГДЕ
		|	ЭлектронныеПодписиЭДОБЗК.ПодписанныйОбъект = &ПодписанныйОбъект
		|	И &ОтборПоИдентификаторуПодписи
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИдентификаторПодписи,
		|	МЧД";
	
	Если ИдентификаторПодписи = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоИдентификаторуПодписи", "(ИСТИНА)");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоИдентификаторуПодписи", "ЭлектронныеПодписиЭДОБЗК.ИдентификаторПодписи = &ИдентификаторПодписи");
		Запрос.УстановитьПараметр("ИдентификаторПодписи", ИдентификаторПодписи);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока Выборка.СледующийПоЗначениюПоля("ИдентификаторПодписи") Цикл
		ДанныеИдентификатораПодписи = Новый Массив;
		РеквизитыПодписей.Вставить(Выборка.ИдентификаторПодписи, ДанныеИдентификатораПодписи);
		Пока Выборка.Следующий() Цикл
			ДанныеИдентификатораПодписи.Добавить(
				Новый Структура("МЧД,РольПодписанта",
					Выборка.МЧД,
					Выборка.РольПодписанта));
		КонецЦикла;
	КонецЦикла;
	
	Возврат РеквизитыПодписей;
	
КонецФункции

Функция ОписаниеМЧД()
	
	ОписаниеДоверенности = Новый Структура;
	ОписаниеДоверенности.Вставить("Ссылка");
	ОписаниеДоверенности.Вставить("Доверенность");
	ОписаниеДоверенности.Вставить("ИмяФайлаСРасширением", "");
	ОписаниеДоверенности.Вставить("Размер", 0);
	ОписаниеДоверенности.Вставить("Подписи", Новый Массив);
	
	Возврат ОписаниеДоверенности;
	
КонецФункции

Функция ОписаниеПодписиМЧД()
	
	ОписаниеПодписи = Новый Структура;
	ОписаниеПодписи.Вставить("АдресВоВременномХранилище");
	ОписаниеПодписи.Вставить("ИмяФайлаСРасширением", "");
	ОписаниеПодписи.Вставить("НаборСвойствПодписиЭДОБЗК");
	
	Возврат ОписаниеПодписи;
	
КонецФункции

Функция ОтпечаткиСертификатовПодписейОбъекта(Объект)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодписанныйОбъект", Объект);
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЭлектронныеПодписи.Отпечаток КАК Отпечаток
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|ГДЕ
		|	ЭлектронныеПодписи.ПодписанныйОбъект = &ПодписанныйОбъект";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Отпечаток");
	
КонецФункции

Функция ЭлектронныеПодписиДокумента(СсылкаНаОбъект, Знач ИдентификаторХранилища = Неопределено) 
	
	Если ИдентификаторХранилища = Неопределено Тогда
		ИдентификаторХранилища = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	ЦифровыеПодписи = Новый Массив;
	ЦифровыеПодписиОбъекта = УстановленныеПодписи(СсылкаНаОбъект);
	Для Каждого ЦифроваяПодписьОбъекта Из ЦифровыеПодписиОбъекта Цикл
		
		ЦифроваяПодписьОбъекта.Вставить("Объект", СсылкаНаОбъект);
		АдресПодписи = ПоместитьВоВременноеХранилище(
			ЦифроваяПодписьОбъекта.Подпись, ИдентификаторХранилища);
		ЦифроваяПодписьОбъекта.Вставить("АдресПодписи", АдресПодписи);
		
		ДвоичныеДанныеСертификата = ЦифроваяПодписьОбъекта.Сертификат.Получить();
		Если ДвоичныеДанныеСертификата <> Неопределено Тогда 
			ЦифроваяПодписьОбъекта.Вставить("АдресСертификата",  ПоместитьВоВременноеХранилище(
				ДвоичныеДанныеСертификата, ИдентификаторХранилища));
		КонецЕсли;
		
		ЦифроваяПодписьОбъекта.Вставить("Статус", "");
		ЭлектроннаяПодписьЭДОБЗККлиентСервер.ЗаполнитьСтатусПроверкиПодписиОбъекта(ЦифроваяПодписьОбъекта);
		
		ЦифровыеПодписи.Добавить(ЦифроваяПодписьОбъекта);
		
	КонецЦикла;
	
	Возврат ЦифровыеПодписи;
	
КонецФункции

#КонецОбласти
