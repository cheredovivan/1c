#Область СлужебныйПрограммныйИнтерфейс

// Конструктор параметров размещения команд ЭДО на форме.
// 
// Возвращаемое значение:
// 	Структура:
// * Форма - ФормаКлиентскогоПриложения 
// * МестоРазмещенияКоманд - РасширениеГруппыФормыДляПодменю 
// * Направление - ПеречислениеСсылка.НаправленияЭДО - направление, по которому из объекта могут формироваться документы.
// * ИсточникКомандЭДО - ДинамическийСписок - список, в котором будет храниться кеш видимости команд ЭДО
//
Функция ПараметрыРазместитьНаФормеКомандыЭДО() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("Форма", Неопределено);
	Результат.Вставить("МестоРазмещенияКоманд", Неопределено);
	Результат.Вставить("Направление", Перечисления.НаправленияЭДО.ПустаяСсылка());
	Результат.Вставить("ИсточникКомандЭДО", Неопределено);
	//@skip-check constructor-function-return-section
	Возврат Результат;

КонецФункции

// Формирует подключаемые команды ЭДО.
//
// Параметры:
//  ПараметрыПриСозданииНаСервере - см. ПараметрыРазместитьНаФормеКомандыЭДО
//   
Процедура РазместитьНаФормеКомандыЭДО(ПараметрыПриСозданииНаСервере) Экспорт
	ПодключаемыеКомандыЭДОСлужебный.РазместитьНаФормеКомандыЭДО(ПараметрыПриСозданииНаСервере);
КонецПроцедуры

// При получении данных на сервере.
// 
// Параметры:
//  ИмяЭлемента - Строка
//  Настройки - НастройкиКомпоновкиДанных
//  Строки - Соответствие Из КлючИЗначение:
//	* Ключ - Произвольный
//	* Значение - СтрокаДинамическогоСписка
//
Процедура ПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки) Экспорт
	ПараметрыУправленияВидимостьюЭДО = Неопределено;
	Настройки.ДополнительныеСвойства.Свойство("ПараметрыУправленияВидимостьюЭДО", ПараметрыУправленияВидимостьюЭДО);
	Если ПараметрыУправленияВидимостьюЭДО <> Неопределено И ПараметрыУправленияВидимостьюЭДО.ЕстьУсловияВидимости Тогда
		РезультатыВычисленияМассовыхАлгоритмов = ПодключаемыеКомандыЭДОСлужебный.ВычислитьЗначенияМассовыхАлгоритмов(ПараметрыУправленияВидимостьюЭДО.КомандыСУсловиямиВидимости, Строки);
		Для Каждого ОписаниеСтрокиДинамическогоСписка Из Строки Цикл
			ДанныеСтроки = ОписаниеСтрокиДинамическогоСписка.Значение.Данные;
			ВидимостьКомандЭДО = Новый Соответствие;
			Для Каждого Команда Из ПараметрыУправленияВидимостьюЭДО.КомандыСУсловиямиВидимости Цикл
				Если ДанныеСтроки.Свойство(Команда.ИмяРеквизитаУсловия) Тогда
					Видимость = Команда.ЗначениеУсловия = ДанныеСтроки[Команда.ИмяРеквизитаУсловия];
				ИначеЕсли ПодключаемыеКомандыЭДОСлужебный.УсловиеВидимостиВычисленоМассовымАлгоритмом(Команда, ДанныеСтроки, РезультатыВычисленияМассовыхАлгоритмов) Тогда
					Видимость = Команда.ЗначениеУсловия = РезультатыВычисленияМассовыхАлгоритмов[Команда.ИмяАлгоритмаМассовойПроверкиУсловия][ДанныеСтроки.Ссылка];
				ИначеЕсли ЗначениеЗаполнено(Команда.ИмяАлгоритмаПроверкиУсловия) Тогда
					ПараметрАлгоритма = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеСтроки.Ссылка);
					Видимость = Команда.ЗначениеУсловия = ПодключаемыеКомандыЭДОСлужебный.ВычислитьЗначениеАлгоритма(
						Команда.ИмяАлгоритмаПроверкиУсловия, ПараметрАлгоритма);
				Иначе
					Видимость = Истина;
				КонецЕсли;
				ВидимостьКомандЭДО.Вставить(Команда.ИмяВФорме, Видимость);
			КонецЦикла;
			ДанныеСтроки[ПодключаемыеКомандыЭДОКлиентСервер.ИмяПоляЗначенийВидимостиКомандЭДО()] =
				Новый ФиксированноеСоответствие(ВидимостьКомандЭДО);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#Область ЛегкиеКомандыЭДО

// Конструктор параметров размещения команд ЭДО легкого интерфейса на форме.
// 
// Возвращаемое значение:
// 	Структура:
// * Форма - Неопределено,ФормаКлиентскогоПриложения:
// ** УникальныйИдентификатор - УникальныйИдентификатор
// * МестоРазмещенияКоманд - Неопределено,РасширениеГруппыФормыДляПодменю 
//
Функция ПараметрыРазместитьНаФормеКомандыЭДОЛегкогоИнтерфейса() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("Форма", Неопределено);
	Результат.Вставить("МестоРазмещенияКоманд", Неопределено);
	Возврат Результат;

КонецФункции

#Если Не ВнешнееСоединение Тогда

// Формирует команд ЭДО легкого интерфейса, добавляет на форму.
//
// Параметры:
//  ПараметрыПриСозданииНаСервере - См. ПараметрыРазместитьНаФормеКомандыЭДОЛегкогоИнтерфейса
//
Процедура РазместитьНаФормеКомандыЭДОЛегкогоИнтерфейса(ПараметрыПриСозданииНаСервере) Экспорт
	ПодключаемыеКомандыЭДОСлужебный.РазместитьНаФормеКомандыЭДОЛегкогоИнтерфейса(ПараметрыПриСозданииНаСервере);
КонецПроцедуры

#КонецЕсли // ВнешнееСоединение

#КонецОбласти // ЛегкиеКомандыЭДО

#КонецОбласти
