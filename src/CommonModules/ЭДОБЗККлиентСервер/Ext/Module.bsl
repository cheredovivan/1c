#Область ПрограммныйИнтерфейс

// Обновляет подключаемые команды формы, меня пиктограммы у команд печати с присоединенным файлом.
//
// Параметры:
//  УправляемаяФорма               - УправляемаяФорма
//  ОбъектФормы                    - ДанныеФормыСтруктура, основной объект формы
//                                 - ДинамическийСписок, формы списка
//  ВыполнитьСтандартныйОбработчик - Булево
//  УправлятьВидимостью            - Булево
//
Процедура ОбновитьКоманды(УправляемаяФорма, ОбъектФормы, ВыполнитьСтандартныйОбработчик, УправлятьВидимостью = Истина) Экспорт
	
	Если ВыполнитьСтандартныйОбработчик Тогда
		ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(УправляемаяФорма, ОбъектФормы);
	КонецЕсли;
	
	Структура = Новый Структура("ПараметрыПодключаемыхКоманд,ИдентификаторыФайловСЭЦП", Null, Null);
	ЗаполнитьЗначенияСвойств(Структура, УправляемаяФорма);
	ПараметрыПодключаемыхКоманд = Структура.ПараметрыПодключаемыхКоманд;
	Если ТипЗнч(ПараметрыПодключаемыхКоманд) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ОбъектФормы) = Тип("ТаблицаФормы") Тогда
		КоллекцияДанных = Новый Массив;
		Для Каждого ВыделеннаяСтрока Из ОбъектФормы.ВыделенныеСтроки Цикл
			КоллекцияДанных.Добавить(ОбъектФормы.ДанныеСтроки(ВыделеннаяСтрока));
		КонецЦикла;
	Иначе
		КоллекцияДанных = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(УправляемаяФорма);
	КонецЕсли;
	
	ИдентификаторыФайловСЭЦП = ?(Структура.ИдентификаторыФайловСЭЦП = Null, Неопределено, Структура.ИдентификаторыФайловСЭЦП) ;
	Для Каждого Подменю Из ПараметрыПодключаемыхКоманд.ПодменюСУсловиямиВидимости Цикл
		
		Для Каждого ОписаниеКоманды Из Подменю.КомандыСУсловиямиВидимости Цикл
			
			ДоступностьЭлемента = Истина;
			ЭлементФормы = УправляемаяФорма.Элементы.Найти(ОписаниеКоманды.ИмяВФорме);
			УсловияБЗК = Ложь;
			Если ЭлементФормы <> Неопределено И
				(Не УправлятьВидимостью Или ЭлементФормы.Видимость) Тогда
				
				Для Каждого Условие Из ОписаниеКоманды.УсловияВидимости Цикл
					
					Если Не СтрНачинаетсяС(Условие.Реквизит, "БЗК_") Тогда
						Продолжить;
					КонецЕсли;
					УсловияБЗК = Истина;
					
					Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ОбъектФормы, Условие.Реквизит) Тогда
						Продолжить;
					КонецЕсли;
					
					ЗначениеКлюча = Неопределено;
					Если ИдентификаторыФайловСЭЦП <> Неопределено Тогда
						ЗначениеКлюча = ИдентификаторыФайловСЭЦП.Получить(Условие.Реквизит);
						Если ЗначениеКлюча <> Неопределено Тогда
							ЗначениеКлюча = ЗначениеКлюча = Истина;
						КонецЕсли;
					КонецЕсли;
					
					УсловиеВыполняется = Истина;
					Если ЗначениеКлюча = Неопределено Тогда
						Для Каждого ЭлементДанных Из КоллекцияДанных Цикл
							Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭлементДанных, Условие.Реквизит) Тогда
								ЗначениеКлюча = ЭлементДанных[Условие.Реквизит];
								Если ЗначениеКлюча <> Условие.Значение Тогда
									УсловиеВыполняется = Ложь;
									Прервать;
								КонецЕсли;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
					
					Если ЗначениеКлюча = Неопределено Тогда
						ЗначениеКлюча = Ложь;
					КонецЕсли;
					
					Если Не УсловиеВыполняется Или ЗначениеКлюча <> Условие.Значение Тогда
						
						Если УправлятьВидимостью Тогда
							ЭлементФормы.Видимость = Ложь;
						Иначе
							ДоступностьЭлемента = Ложь;
						КонецЕсли;
						Прервать;
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			Если УсловияБЗК И Не УправлятьВидимостью И ЭлементФормы <> Неопределено Тогда
				ЭлементФормы.Видимость = Истина;
				ЭлементФормы.Доступность = ДоступностьЭлемента;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ТолькоИдентификаторПечатнойФормы(ИдентификаторПечатнойФормы) Экспорт
	
	ПозицияТочки = СтрНайти(ИдентификаторПечатнойФормы, ".", НаправлениеПоиска.СКонца);
	Если ПозицияТочки > 0 Тогда
		Возврат Сред(ИдентификаторПечатнойФормы, ПозицияТочки + 1);
	КонецЕсли;
	
	Возврат ИдентификаторПечатнойФормы;
	
КонецФункции

Функция ЭтоИдентификаторыЭлектронногоДокумента(Знач ИдентификаторПечатнойФормы) Экспорт
	
	Возврат ЭДОБЗКВызовСервера.НастройкиПечатныхФорм().Получить(
		ТолькоИдентификаторПечатнойФормы(ИдентификаторПечатнойФормы)) <> Неопределено;
	
КонецФункции

Функция ИмяДействияУведомленияОзнакомитьсяИПодписать() Экспорт
	Возврат "ДействиеУведомленияОзнакомитьсяИПодписать";
КонецФункции

Функция ИмяДействияУведомленияОзнакомитьсяСОтказомПодписать() Экспорт
	Возврат "ДействиеУведомленияОзнакомитьсяСОтказомПодписать";
КонецФункции

Процедура ОбновитьИнформационнуюНадписьОВозможностиРедактированияПечатнойФормы(УправляемаяФорма) Экспорт
	
	Защита = Ложь;
	Для Каждого НастройкаПечатнойФормы Из УправляемаяФорма.НастройкиПечатныхФорм Цикл
		Если УправляемаяФорма[НастройкаПечатнойФормы.ИмяРеквизита].Защита Тогда
			Защита = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		ПрефиксЭлементовОВозможностиРедактирования() + "Группа",
		"Видимость",
		Защита);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		ПрефиксЭлементовОВозможностиРедактирования() + "КомандаРедактирования",
		"Видимость",
		Не Защита);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"КнопкаРедактирование",
		"Видимость",
		Ложь);
	
КонецПроцедуры

Функция ПрефиксЭлементовОВозможностиРедактирования() Экспорт
	Возврат "ИнформацияОДоступностиРедактирования";
КонецФункции

Функция ЭтоРасширениеФайлаЭлектронногоДокумента(Расширение) Экспорт
	
	Возврат СтрНайти(ВРег(РасширенияФайловЭлектронныхДокументов()), "*." + ВРег(Расширение)) > 0;
	
КонецФункции

Функция ЭтоРасширениеФайлаСПредставлением(Расширение) Экспорт
	
	Если ЭтоРасширениеMXLДокумента(Расширение) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат СтрНайти(ВРег(РасширенияФайловЭлектронныхДокументов()), "*." + ВРег(Расширение)) > 0;
	
КонецФункции

Функция ЭтоРасширениеPDFДокумента(Расширение) Экспорт
	
	Возврат СтрНайти(ВРег(РасширениеPDFДокумента()), "*." + ВРег(Расширение)) > 0;
	
КонецФункции

Функция ЭтоРасширениеHTMLДокумента(Расширение) Экспорт
	
	Возврат СтрНайти(ВРег(РасширениеHTMLДокумента()), "*." + ВРег(Расширение)) > 0;
	
КонецФункции

Функция ЭтоРасширениеTXTДокумента(Расширение) Экспорт
	
	Возврат СтрНайти(ВРег(РасширениеTXTДокумента()), "*." + ВРег(Расширение)) > 0;
	
КонецФункции

Функция ЭтоРасширениеИзображения(Расширение) Экспорт
	
	Возврат СтрНайти(ВРег(РасширенияИзображений()), "*." + ВРег(Расширение)) > 0;
	
КонецФункции

Функция ЭтоРасширениеMXLДокумента(Расширение) Экспорт
	
	Возврат СтрНайти(ВРег(РасширениеMXLДокумента()), "*." + ВРег(Расширение)) > 0;
	
КонецФункции

Функция ЭтоРасширениеJSONДокумента(Расширение) Экспорт
	
	Возврат СтрНайти(ВРег(РасширениеJSONДокумента()), "*." + ВРег(Расширение)) > 0;
	
КонецФункции

Функция РасширенияФайловЭлектронныхДокументов() Экспорт
	
	РасширенияФайлов = Новый Массив;
	РасширенияФайлов.Добавить(РасширениеPDFДокумента());
	РасширенияФайлов.Добавить(РасширениеHTMLДокумента());
	РасширенияФайлов.Добавить(РасширениеTXTДокумента());
	РасширенияФайлов.Добавить(РасширенияИзображений());
	
	Возврат СтрСоединить(РасширенияФайлов, ";");
	
КонецФункции

Функция РасширениеPDFДокумента() Экспорт
	Возврат "*.pdf";
КонецФункции

Функция РасширениеHTMLДокумента() Экспорт
	Возврат "*.html";
КонецФункции

Функция РасширениеTXTДокумента()
	Возврат "*.txt";
КонецФункции

Функция РасширенияИзображений()
	Возврат "*.jpg;*.jpeg;*.png;*.tiff";
КонецФункции

Функция РасширениеMXLДокумента()
	Возврат "*.mxl";
КонецФункции

Функция РасширениеJSONДокумента()
	Возврат "*.json";
КонецФункции

Функция ОписанияФайловПечатныхФормИзПараметровФормы(ПараметрыФормы) Экспорт
	
	Если Не ПараметрыФормы.Свойство("ПараметрыПечати") Тогда
		
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ОписанияФайловПечатныхФормИзПараметровПечати(ПараметрыФормы.ПараметрыПечати);
	
КонецФункции

Функция ОписанияФайловПечатныхФормИзПараметровПечати(ПараметрыПечати) Экспорт
	
	ОписанияФайловПечатныхФорм = Неопределено;
	Если ПараметрыПечати.Свойство("ДополнительныеПараметры")
		И ТипЗнч(ПараметрыПечати.ДополнительныеПараметры) = Тип("Структура") Тогда
		
		ПараметрыПечати.ДополнительныеПараметры.Свойство("ОписанияФайловПечатныхФорм", ОписанияФайловПечатныхФорм);
	Иначе
		ПараметрыПечати.Свойство("ОписанияФайловПечатныхФорм", ОписанияФайловПечатныхФорм);
	КонецЕсли;
	
	Возврат ОписанияФайловПечатныхФорм;
	
КонецФункции

Процедура ДобавитьВПараметрыПечатиОписанияФайловПечатныхФорм(ПараметрыПечати, ОписанияФайлов) Экспорт
	
	Если ПараметрыПечати.Свойство("ДополнительныеПараметры")
		И ТипЗнч(ПараметрыПечати.ДополнительныеПараметры) = Тип("Структура") Тогда
		
		ПараметрыПечати.ДополнительныеПараметры.Вставить("ОписанияФайловПечатныхФорм", ОписанияФайлов);
		Возврат;
	КонецЕсли;
	
	ПараметрыПечати.Вставить("ОписанияФайловПечатныхФорм", ОписанияФайлов);
	
КонецПроцедуры

Функция ТолькоРасширение(Знач Расширение) Экспорт
	
	Расширение = СтрЗаменить(Расширение, "*", "");
	Расширение = СтрЗаменить(Расширение, ".", "");
	
	Возврат Расширение;
	
КонецФункции

Процедура ДобавитьДанныеПодписи(УстановленныеПодписи, Сертификат, УстановленнаяПодпись) Экспорт
	Если УстановленныеПодписи = Неопределено Тогда
		УстановленныеПодписи = Новый Соответствие;
	КонецЕсли;                                    
	ДанныеПодписи = УстановленныеПодписи.Получить(Сертификат);
	Если ДанныеПодписи = Неопределено Тогда
		ДанныеПодписи = Новый Массив;
	КонецЕсли;
	Если УстановленнаяПодпись.Свойство("РезультатПроверкиПодписиПоМЧД")
		И УстановленнаяПодпись.РезультатПроверкиПодписиПоМЧД <> Неопределено Тогда
		
		Для Каждого РезультатПроверки Из УстановленнаяПодпись.РезультатПроверкиПодписиПоМЧД Цикл
			ДанныеПодписи.Добавить(РезультатПроверки.МашиночитаемаяДоверенность);
		КонецЦикла;
	ИначеЕсли УстановленнаяПодпись.Свойство("ВыбраннаяДоверенность")
		И УстановленнаяПодпись.ВыбраннаяДоверенность <> Неопределено Тогда
		
		ДанныеПодписи.Добавить(УстановленнаяПодпись.ВыбраннаяДоверенность);
	Иначе
		ДанныеПодписи.Добавить(Неопределено);
	КонецЕсли;
	УстановленныеПодписи.Вставить(Сертификат, ДанныеПодписи);
КонецПроцедуры

Функция УникальноеИмяФайлаСРасширением(Знач ИсходноеИмяФайлаСРасширением, ИменаФайлов) Экспорт
	
	Если ИменаФайлов.Получить(ВРег(ИсходноеИмяФайлаСРасширением)) = Неопределено Тогда
		ИменаФайлов.Вставить(Врег(ИсходноеИмяФайлаСРасширением), Истина);
		Возврат ИсходноеИмяФайлаСРасширением;
	КонецЕсли;
	
	Файл = Новый Файл(ИсходноеИмяФайлаСРасширением);
	ИмяФайлаБезРасширения = СтрЗаменить(ИсходноеИмяФайлаСРасширением, Файл.Расширение, "");
	
	Счетчик = 1;
	Пока Истина Цикл
		ИмяФайлаСНомером = СтрШаблон("%1%2",
			ИмяФайлаБезРасширения + "_" + Формат(Счетчик, "ЧГ="), Файл.Расширение);
		Если ИменаФайлов.Получить(ВРег(ИмяФайлаСНомером)) = Неопределено Тогда
			Прервать;
		КонецЕсли;
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
	ИменаФайлов.Вставить(Врег(ИмяФайлаСНомером), Истина);
	Возврат ИмяФайлаСНомером;
	
КонецФункции

Функция УникальноеИмяФайла(ИмяФайла, ИменаФайлов) Экспорт
	
	Если ИменаФайлов.Получить(ИмяФайла) = Неопределено Тогда
		ИменаФайлов.Вставить(ИмяФайла, Истина);
		Возврат ИмяФайла;
	КонецЕсли;
	
	Счетчик = 1;
	Пока Истина Цикл
		ИмяФайлаСНомером = ИмяФайла + "_" + Формат(Счетчик, "ЧГ=");
		Если ИменаФайлов.Получить(ИмяФайлаСНомером) = Неопределено Тогда
			Прервать;
		КонецЕсли;
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
	ИменаФайлов.Вставить(ИмяФайлаСНомером, Истина);
	Возврат ИмяФайлаСНомером;
	
КонецФункции

Процедура УстановитьПодсказкуВидуДокумента(УправляемаяФорма, ИмяРеквизита, ВидДокумента, КодКадровогоДокумента) Экспорт
	
	ТекстПодсказки = "";
	Если ЗначениеЗаполнено(ВидДокумента) Тогда
		Если ЗначениеЗаполнено(КодКадровогоДокумента) Тогда
			КодВидаДокумента = ЭДОБЗКВызовСервера.КодКадровогоДокументаВидаДокумента(ВидДокумента);
			Если ЗначениеЗаполнено(КодВидаДокумента) И КодВидаДокумента <> КодКадровогоДокумента Тогда
				ТекстПодсказки = СтрШаблон(
					НСтр("ru = 'Код вида документа (%1) не совпадает с ранее выбранным кодом документа (%2)';
						|en = 'The document kind code (%1) does not match the previously selected document code (%2)'"),
					КодВидаДокумента,
					КодКадровогоДокумента);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(КодКадровогоДокумента) Тогда
			ТекстПодсказки = СтрШаблон(
				НСтр("ru = 'Не выбран вид документа (ранее использовался код документа %1)';
					|en = 'The document kind is not selected (previously, document code %1 was used)'"), КодКадровогоДокумента);
		Иначе
			ТекстПодсказки = НСтр("ru = 'Не выбран вид документа';
									|en = 'The document kind is not selected'");
		КонецЕсли;
	КонецЕсли;
	Если ПустаяСтрока(ТекстПодсказки) Тогда
		УстановитьПодсказкуОКодеВидаДокумента(УправляемаяФорма, ИмяРеквизита, ВидДокумента);
	Иначе
		ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(
			УправляемаяФорма, ИмяРеквизита, ТекстПодсказки, WebЦвета.Красный);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПодсказкуОКодеВидаДокумента(УправляемаяФорма, ИмяРеквизита, ВидДокумента) Экспорт
	ТекстПодсказки = "";
	Если ЗначениеЗаполнено(ВидДокумента) Тогда
		СвойстваКода = ЭДОБЗКВызовСервера.СвойстваКодаВидаКадровогоДокумента(ВидДокумента);
		Если СвойстваКода <> Неопределено Тогда
			ТекстПодсказки = СтрШаблон(
				"%1: (%2)",
				СвойстваКода.Код,
				СвойстваКода.НаименованиеПолное);
		Иначе
			ТекстПодсказки = НСтр("ru = 'Не является документом, связанным с работой';
									|en = 'Not a document related to work'");
		КонецЕсли;
	КонецЕсли;
	ЗарплатаКадрыКлиентСервер.УстановитьРасширеннуюПодсказкуЭлементуФормы(
		УправляемаяФорма, ИмяРеквизита, ТекстПодсказки, Новый Цвет);
	
КонецПроцедуры

#КонецОбласти