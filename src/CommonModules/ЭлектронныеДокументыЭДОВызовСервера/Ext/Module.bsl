//@strict-types
#Область СлужебныйПрограммныйИнтерфейс

// Возвращает общий размер файлов массива документов учета в байтах
//
// Параметры:
//  ЭлектронныеДокументы - Массив из ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//
// Возвращаемое значение:
//  Число
Функция РазмерПрисоединенныхФайловДокументов(ЭлектронныеДокументы) Экспорт
	
	Возврат ЭлектронныеДокументыЭДО.РазмерПрисоединенныхФайловДокументов(ЭлектронныеДокументы);
	
КонецФункции

// Параметры:
//  Документ - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
// 
// Возвращаемое значение:
//  Структура:
//  * Организация - ОпределяемыйТип.Организация
//  * Контрагент - ОпределяемыйТип.КонтрагентБЭД
//  * ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
//  * ЭтоОблачныйЭДО - Булево
//  * ИдентификаторПакета - Неопределено,УникальныйИдентификатор
//
Функция РеквизитыДокументаДляФормированияПакета(Знач Документ) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Организация", Неопределено);
	Результат.Вставить("Контрагент", Неопределено);
	Результат.Вставить("ДоговорКонтрагента", Неопределено);
	Результат.Вставить("ЭтоОблачныйЭДО", Ложь);
	Результат.Вставить("ИдентификаторПакета", Неопределено);
	
	РеквизитыДокумента = ЭлектронныеДокументыЭДО.СвойстваДокумента(Документ, "Организация, Контрагент,
		|ДоговорКонтрагента, ЭтоОблачныйЭДО");
	ИдентификаторПакетаДокумента = ПакетыДокументовЭДО.ИдентификаторПакетаДокумента(Документ);
	
	ЗаполнитьЗначенияСвойств(Результат, РеквизитыДокумента);
	Результат.ИдентификаторПакета = ИдентификаторПакетаДокумента;
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  СправочникСсылка.ВидыДокументовЭДО
Функция ВидДокументаПрочее() Экспорт
	
	Возврат ЭлектронныеДокументыЭДО.ВидДокументаПоТипу(Перечисления.ТипыДокументовЭДО.Прочее);
	
КонецФункции

// Параметры:
//  ЭлектронныеДокументы - Массив из ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  ЭтоПакетнаяОбработка - Булево
// 
// Возвращаемое значение:
//  Булево
Функция КаждыйДокументБезПодтвержденияИлиОтветнаяПодписьОтправлена(ЭлектронныеДокументы, ЭтоПакетнаяОбработка) Экспорт
	
	ПереченьСостоянийОтправкиОтветнойПодписи = 
		ЭлектронныеДокументыЭДОСлужебный.ПереченьСостоянийОтправкиОтветнойПодписи();
	Если ЭтоПакетнаяОбработка Тогда
		СостоянияОтправкиОтветнойПодписи = 
			ЭлектронныеДокументыЭДОСлужебный.ПакетноеСостояниеОтправкиОтветнойПодписиВходящихДокументов(
				ЭлектронныеДокументы);
	Иначе
		СостоянияОтправкиОтветнойПодписи = 
			ЭлектронныеДокументыЭДОСлужебный.СостояниеОтправкиОтветнойПодписиВходящихДокументов(ЭлектронныеДокументы);
	КонецЕсли;

	ЕстьОтветныеПодписиОжидающиеОтправку = СостоянияОтправкиОтветнойПодписи.НайтиСледующий(Новый Структура("Состояние",
		ПереченьСостоянийОтправкиОтветнойПодписи.НеОтправлена));
	Возврат Не ЕстьОтветныеПодписиОжидающиеОтправку;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаДействийПоИнтеграциямЭДО

// Возвращает описание длительной операции выполнения действий по ЭДО.
// 
// Параметры:
//  ПараметрыВыполненияДействий - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО
// Возвращаемое значение:
//  См. ДлительныеОперации.ВыполнитьФункцию
Функция ВыполнитьДействияПоИнтеграциямЭДОВФоне(Знач ПараметрыВыполненияДействий) Экспорт
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	
	// См. ЭлектронныеДокументыЭДО.ВыполнитьДействияПоИнтеграциямЭДО
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
		"ЭлектронныеДокументыЭДО.ВыполнитьДействияПоИнтеграциямЭДО", ПараметрыВыполненияДействий);
	
КонецФункции

// Возвращает результат действий по интеграциям ЭДО из временного хранилища.
// 
// Параметры:
//  АдресРезультата - Строка
//
// Возвращаемое значение:
//  - Произвольный
//  - См. ЭлектронныеДокументыЭДО.НовыеРезультатыВыполненияДействийПоИнтеграциямЭДО
//
Функция РезультатыВыполненияДействийПоИнтеграциямЭДОДляКлиента(Знач АдресРезультата) Экспорт
	
	РезультатыПоИнтеграциямЭДО = ПолучитьИзВременногоХранилища(АдресРезультата); // См. ЭлектронныеДокументыЭДО.ВыполнитьДействияПоИнтеграциямЭДО
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	ВизуализацияЭДОСлужебный.ЗапуститьКэшированиеВФоне();
	
	Если Не ЗначениеЗаполнено(РезультатыПоИнтеграциямЭДО) Тогда
		Возврат РезультатыПоИнтеграциямЭДО;
	КонецЕсли;
	
	РезультатыПоИнтеграциямЭДОДляКлиента = ЭлектронныеДокументыЭДО.РезультатыВыполненияДействийПоИнтеграциямЭДОДляКлиента(
		РезультатыПоИнтеграциямЭДО);
	
	Возврат РезультатыПоИнтеграциямЭДОДляКлиента;
	
КонецФункции

// Параметры:
//  ПараметрыПродолженияДействийПоИнтеграциямЭДО - Соответствие из КлючИЗначение:
//  * Ключ - Строка - идентификатор менеджера действий интеграции ЭДО.
//  * Значение - Произвольный
//  ПараметрыВыполненияДействий - см. ЭлектронныеДокументыЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО
// 
// Возвращаемое значение:
//  См. ДлительныеОперации.ВыполнитьФункцию
Функция ПродолжитьВыполнениеДействийПоИнтеграциямЭДОВФоне(Знач ПараметрыПродолженияДействийПоИнтеграциямЭДО, Знач ПараметрыВыполненияДействий) Экспорт
	
	ПараметрыПродолженияВФоне = ЭлектронныеДокументыЭДО.ПараметрыПродолженияДействийПоИнтеграциямЭДОВФоне(
		ПараметрыПродолженияДействийПоИнтеграциямЭДО);
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(Новый УникальныйИдентификатор);
	
	// См. ЭлектронныеДокументыЭДО.ПродолжитьВыполнениеДействийПоИнтеграциямЭДОПослеОбработкиРезультатов
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
		"ЭлектронныеДокументыЭДО.ПродолжитьВыполнениеДействийПоИнтеграциямЭДОПослеОбработкиРезультатов",
		ПараметрыПродолженияВФоне, ПараметрыВыполненияДействий);
	
КонецФункции

#КонецОбласти

#Область ОбработкаДействийПоЭДО

// Возвращает результат заполнения доверенностей и подписантов по сертификату.
//
// Параметры:
//  ДанныеДляЗаполнения - Соответствие из КлючИЗначение:
//    * Ключ - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
//    * Значение - Структура:
//  	** ТребуетсяЗаполнитьПодписанта   - Булево
//  	** ТребуетсяЗаполнитьДоверенность - Булево
//  	** НаборМЧД						  - Массив из СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//  	** Данные						  - Неопределено, Строка, ДвоичныеДанные - данные файлов.
//  Сертификат          - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  РезультатУточненияМЧД - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОСлужебный.ЗаполнитьПодписантовИДоверенностиПоСертификату
//
Функция ЗаполнитьПодписантовИДоверенностиПоСертификату(Знач ДанныеДляЗаполнения,
	Знач Сертификат, КонтекстДиагностики, Знач РезультатУточненияМЧД) Экспорт
	
	РезультатыПоФайлам = ЭлектронныеДокументыЭДОСлужебный.ЗаполнитьПодписантовИДоверенностиПоСертификату(
		ДанныеДляЗаполнения, Сертификат, КонтекстДиагностики, РезультатУточненияМЧД);
	
	СоздаватьЭлектронныеПодписиНаСервере = ЭлектроннаяПодпись.СоздаватьЭлектронныеПодписиНаСервере();
	Если СоздаватьЭлектронныеПодписиНаСервере Тогда
		Для Каждого РезультатПоФайлу Из РезультатыПоФайлам Цикл
			РезультатЗаполнения = РезультатПоФайлу.Значение;
			РезультатЗаполнения.ДанныеДляПодписания = РезультатЗаполнения.АдресДанныхДляОбновления;
		КонецЦикла;
	КонецЕсли;
	
	Возврат РезультатыПоФайлам
	
КонецФункции

// Возвращает преобразованный результат действий по ЭДО.
// 
// Параметры:
//  АдресРезультата - Строка
//
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДО.ОбработатьРезультатДействийПоЭДОФоне
//
Функция РезультатДействийПоЭДО(Знач АдресРезультата) Экспорт
	
	РезультатДействийПоЭДОФоне = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	РезультатДействийПоЭДО = ЭлектронныеДокументыЭДОСлужебный.ОбработатьРезультатДействийПоЭДОФоне(РезультатДействийПоЭДОФоне);
	
	Возврат РезультатДействийПоЭДО;
	
КонецФункции

#КонецОбласти

#Область ВидыДокументов

// См. ОбщегоНазначения.ЭтоМобильныйКлиент
Функция ИспользоватьКраткоеПредставлениеВидовДокументов() Экспорт
	Возврат ОбщегоНазначения.ЭтоМобильныйКлиент();
КонецФункции

#КонецОбласти

#Область Подписи

// Возвращает результат проверки подписей электронного документа.
// 
// Параметры:
//  ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//                      - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДО.ПроверитьПодписиДокумента
//
Функция ПроверитьПодписиДокумента(Знач ЭлектронныйДокумент, КонтекстДиагностики) Экспорт
	Возврат ЭлектронныеДокументыЭДО.ПроверитьПодписиДокумента(ЭлектронныйДокумент, КонтекстДиагностики);
КонецФункции

// Возвращает результат проверки подписей сообщения.
// 
// Параметры:
//  СообщениеЭДО - ДокументСсылка.СообщениеЭДО
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДО.ПроверитьПодписиСообщения
//
Функция ПроверитьПодписиСообщения(Знач СообщениеЭДО, КонтекстДиагностики) Экспорт
	Возврат ЭлектронныеДокументыЭДО.ПроверитьПодписиСообщения(СообщениеЭДО, КонтекстДиагностики);
КонецФункции

// См. ЭлектронныеДокументыЭДО.ЗаписатьРезультатПроверкиПодписейДокумента
Функция ЗаписатьРезультатПроверкиПодписейДокумента(Знач ПроверенныеПодписи) Экспорт
	Возврат ЭлектронныеДокументыЭДО.ЗаписатьРезультатПроверкиПодписейДокумента(ПроверенныеПодписи);
КонецФункции

#КонецОбласти

#Область МашиночитаемыеДоверенности

// Возвращает результаты проверок подписей по МЧД при регистрации сертификатов.
// 
// Параметры:
//  НаборПараметровПроверок - Массив из См. ЭлектронныеДокументыЭДОКлиент.НовыеПараметрыПроверкиПодписиПоМЧДПриРегистрацииСертификата
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОСлужебный.ПроверитьПодписиПоМЧДПриРегистрацииСертификатов
Функция ПроверитьПодписиПоМЧДПриРегистрацииСертификатов(Знач НаборПараметровПроверок, КонтекстДиагностики) Экспорт
	Возврат ЭлектронныеДокументыЭДОСлужебный.ПроверитьПодписиПоМЧДПриРегистрацииСертификатов(
		НаборПараметровПроверок, КонтекстДиагностики)
КонецФункции

// Продолжает проверку подписей по МЧД при регистрации сертификатов
//
// Параметры:
//  РезультатыПроверокПодписейПоМЧД - См. ЭлектронныеДокументыЭДОСлужебный.ПроверитьПодписиПоМЧДПриРегистрацииСертификатов
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОСлужебный.ПроверитьПодписиПоМЧДПриРегистрацииСертификатовПродолжение
Функция ПроверитьПодписиПоМЧДПриРегистрацииСертификатовПродолжение(Знач РезультатыПроверокПодписейПоМЧД, 
	КонтекстДиагностики) Экспорт
	Возврат ЭлектронныеДокументыЭДОСлужебный.ПроверитьПодписиПоМЧДПриРегистрацииСертификатовПродолжение(
		РезультатыПроверокПодписейПоМЧД, КонтекстДиагностики);
КонецФункции

// Текст ошибки сертификатом можно подписать только по МЧД.
// 
// Параметры:
//  Сертификаты - Массив Из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификаты,
//                                                                                            по которым требуются МЧД
// 
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДО.ТекстОшибкиСертификатомМожноПодписатьТолькоПоМЧД
Функция ТекстОшибкиСертификатомМожноПодписатьТолькоПоМЧД(Знач Сертификаты) Экспорт
	
	Возврат ЭлектронныеДокументыЭДО.ТекстОшибкиСертификатомМожноПодписатьТолькоПоМЧД(Сертификаты);
	
КонецФункции

#КонецОбласти

// Параметры:
//  ИдентификаторКонтрагента - Строка
//  ИдентификаторОрганизации - Строка
//  АдресФайла - Строка
//
// Возвращаемое значение:
//  см. ПакетыДокументовЭДОКлиентСервер.НовыйРезультатПроверкиПревышения
Функция ПроверкаПревышенияМаксимальногоРазмераФайла(ИдентификаторКонтрагента, ИдентификаторОрганизации, АдресФайла) Экспорт
	
	Возврат ЭлектронныеДокументыЭДО.ПроверкаПревышенияМаксимальногоРазмераФайла(ИдентификаторКонтрагента,
		ИдентификаторОрганизации, АдресФайла);
	
КонецФункции

#КонецОбласти