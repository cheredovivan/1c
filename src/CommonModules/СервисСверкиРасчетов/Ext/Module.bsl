
#Область ПрограммныйИнтерфейс

// Возвращает признак того, что это часто используемый документ, который подлежит сверке в сервисе сверки расчетов.
// 
//Параметры:
// ДокументСсылка - ссылка на проверяемый документ
//
// Возвращаемое значение:
// Булево - Истина - документ подлежит сверке в сервисе.
//
Функция ДокументПодлежитСверке(ДокументСсылка) Экспорт
	
	ТипПроверяемогоДокумента = ТипЗнч(ДокументСсылка);
	ТипыДокументовПодлежащихСверке = Метаданные.ОпределяемыеТипы.СервисСверкиРасчетовДокументыПодлежащиеСверке.Тип.Типы();
	Для Каждого ТипДокумента Из ТипыДокументовПодлежащихСверке Цикл
		Если ТипПроверяемогоДокумента = ТипДокумента Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Отправляет сведения о документах на сервис сверки
//
//Параметры:
// ДатаНачала    - Дата - начало периода за который необходимо отправить документы
// ДатаОкончания - Дата - конец периода за который необходимо отправить документы
//
//Возвращаемое значение:
// ТаблицаЗначений - см.НовыйТаблицаОшибок()
//
Функция ОтправитьСообщения(ДатаНачала = '00010101', ДатаОкончания = '00010101') Экспорт
	
	ТаблицаОшибок                             = НовыйТаблицаОшибок();
	КонтрагентыСНекорректнымиИдентификаторами = Новый Массив();
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДатаНачала < ДатаНачалаСверкиРасчетов() Тогда
		ДатаНачала = ДатаНачалаСверкиРасчетов();
	КонецЕсли;
	
	ДанныеИнформационнойБазыКСверке = СервисСверкиРасчетовПереопределяемый.ДанныеИнформационнойБазыКСверке(ДатаНачала, ДатаОкончания);
	
	Если ДанныеИнформационнойБазыКСверке.Пустой() Тогда
		// Возможно отключено обновление идентификаторов контрагентов, необходимо включить.
		ИзменитьРасписаниеЗаданияБизнесСети();
	КонецЕсли;
	
	ВыборкаОрганизации = ДанныеИнформационнойБазыКСверке.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	МаксимальныйРазмерПакета = 50;
	
	Пока ВыборкаОрганизации.Следующий() Цикл
		
		// Сформируем новый пакет сообщений.
		КоличествоЗаписейВПакете = 0;
		ИдентификаторСообщения   = Новый УникальныйИдентификатор();
		НаборЗаписей             = РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.СоздатьНаборЗаписей();
		ДокументыСообщения       = НаборЗаписей.ВыгрузитьКолонки();
		ЗаписьJSON               = НовыйПакетСообщенийНаСервис(ВыборкаОрганизации);
		
		ВыборкаДокументы = ВыборкаОрганизации.Выбрать();
		
		ТаблицаКонтрагенты = НовыйТаблицаКонтрагентов();
		Пока ВыборкаДокументы.Следующий() Цикл
			
			Если Не СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ВыборкаДокументы.ИдентификаторКонтрагента) Тогда
				Если КонтрагентыСНекорректнымиИдентификаторами.Найти(ВыборкаДокументы.Контрагент) = Неопределено Тогда
					КонтрагентыСНекорректнымиИдентификаторами.Добавить(ВыборкаДокументы.Контрагент);
				КонецЕсли;
				Продолжить;
			КонецЕсли;
			
			КоличествоЗаписейВПакете = КоличествоЗаписейВПакете + 1;
			Если КоличествоЗаписейВПакете = МаксимальныйРазмерПакета Тогда
				// Отправим пакет, текущий документ будет отправлен в следующем пакете.
				ЗакрытьИОтправитьТекущийПакет(ВыборкаОрганизации,
					ДокументыСообщения,
					ЗаписьJSON,
					ИдентификаторСообщения,
					ТаблицаКонтрагенты,
					ТаблицаОшибок);
				// Сформируем новый пакет сообщений.
				КоличествоЗаписейВПакете = 1; // текущий документ попадает в новый пакет
				ИдентификаторСообщения   = Новый УникальныйИдентификатор();
				НаборЗаписей             = РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.СоздатьНаборЗаписей();
				ДокументыСообщения       = НаборЗаписей.ВыгрузитьКолонки();
				ЗаписьJSON               = НовыйПакетСообщенийНаСервис(ВыборкаОрганизации);
			КонецЕсли;
			
			КонтрагентДокумента = ТаблицаКонтрагенты.Добавить();
			ЗаполнитьЗначенияСвойств(КонтрагентДокумента, ВыборкаДокументы);
			
			НаименованиеОперации = НаименованиеОперации(ВыборкаДокументы);
			
			ДобавитьДокументВПакет(ВыборкаДокументы, ЗаписьJSON, НаименованиеОперации);
			
			ДокументСообщения = ДокументыСообщения.Добавить();
			ЗаполнитьЗначенияСвойств(ДокументСообщения, ВыборкаДокументы);
			ДокументСообщения.ИдентификаторСообщения = ИдентификаторСообщения;
			
		КонецЦикла;
		
		ЗакрытьИОтправитьТекущийПакет(ВыборкаОрганизации,
			ДокументыСообщения,
			ЗаписьJSON,
			ИдентификаторСообщения,
			ТаблицаКонтрагенты,
			ТаблицаОшибок);
		
	КонецЦикла;
	
	ЗаписатьЛогОтправкиСообщения(ТаблицаОшибок); 
	АктуализироватьКешКонтрагентов(КонтрагентыСНекорректнымиИдентификаторами);
	
	Возврат ТаблицаОшибок;
	
КонецФункции

Функция ИмяСлужебногоПользователя() Экспорт
	
	Возврат "1ССверка";
	
КонецФункции

// Возвращает признак установки функциональной опции ИспользуетсяСервисСверкиРасчетов.
//
// Возвращаемое значение:
//  Булево - признак установки функциональной опции ИспользуетсяСервисСверкиРасчетов.
//
Функция ИспользуетсяСервисСверкиРасчетов() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользуетсяСервисСверкиРасчетов");
	
КонецФункции

// Процедура регламентного задания, которая отправляет документы в сервис сверки
// и получает информацию о расхождениях.
//
Процедура СверитьРасчетыРегламентноеЗадание() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОбменССервисомСверкиРасчетов);
	
	Если СлужебныйПользовательДляОповещенияОРасхождениях() = Неопределено Тогда
		УстановитьПользователяРегЗадания();
	КонецЕсли;
	
	ОбменятьсяССервисом();
	
КонецПроцедуры

Процедура ОбменятьсяССервисом(Параметры = Неопределено) Экспорт
	
	Если РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОбмена = НовыеПараметрыОбменаССервисом(Параметры);
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Обновим данные авторизации если пользователь более недели не запускал программу.
	ПереподключитьОрганизацииКСервису();

	// При необходимости зарегистрируем к сверке ранее введенные документы.
	Если ПараметрыОбмена.РегистрироватьДокументыПередОбменом Тогда
		ЗарегистрироватьКСверкеРанееВведенныеДокументы();
	КонецЕсли;
	
	ОтправитьСообщения();
	
	// Расхождения получаем с начала предыдущего квартала либо за период формирования отчета.
	ПолучитьРасхождения(
		ПараметрыОбмена.НачальнаяДатаПолученияРасхождений,
		ПараметрыОбмена.КонечнаяДатаПолученияРасхождений,
		ПараметрыОбмена.ЭтоВызовИзОтчета);

КонецПроцедуры

// Возвращает дату начала сверки в сервисе
// Сверка сервисом 1С:Сверка 2.0 производится за период с начала предыдущего года, если по нему еще не сдана отчетность.
// Либо с начала текущего года, если срок представления отчетности за предыдущий год миновал.
//
// Возвращаемое значение:
//   Дата - дата начала сверки в сервисе
Функция ДатаНачалаСверкиРасчетов() Экспорт
	
	ТекущаяДата = ТекущаяДата();
	Если Месяц(ТекущаяДата) < 5 Тогда
		Возврат НачалоГода(НачалоГода(ТекущаяДата) - 1); // Начало предыдущего года
	Иначе
		Возврат НачалоГода(ТекущаяДата);
	КонецЕсли;
	
КонецФункции

Процедура ЗарегистрироватьКСверкеРанееВведенныеДокументы(МассивОрганизаций = Неопределено, МассивКонтрагентов = Неопределено) Экспорт
	
	Если Не ИспользуетсяСервисСверкиРасчетов() Тогда
		Возврат;
	КонецЕсли;
	
	СервисСверкиРасчетовПереопределяемый.ЗарегистрироватьКСверкеРанееВведенныеДокументы(МассивОрганизаций, МассивКонтрагентов);
	
КонецПроцедуры

Функция ИтогиСверки() Экспорт
	
	СтруктураИтогов = Новый Структура();
	СтруктураИтогов.Вставить("КоличествоНеподключенныхКонтрагентов", 0);
	СтруктураИтогов.Вставить("КоличествоНеотправленныхДокументов",   0);
	СтруктураИтогов.Вставить("КоличествоОтправленныхДокументов",     0);
	СтруктураИтогов.Вставить("КоличествоДокументовСРасхождениями",   0);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА РеестрДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.КОтправке)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НеОтправлено,
	|	ВЫБОР
	|		КОГДА РеестрДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.Получено)
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Отправлено
	|ПОМЕСТИТЬ Документы
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК РеестрДокументов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	1
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
	|ГДЕ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Документ.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СервисСверкиРасчетовРеестрДокументов.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ Контрагенты
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|ГДЕ
	|	СервисСверкиРасчетовРеестрДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.КОтправке)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Документ КАК Документ,
	|	1 КАК ДокументСРасхождением
	|ПОМЕСТИТЬ Расхождения
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	0 КАК КоличествоНеподключенныхКонтрагентов,
	|	Документы.НеОтправлено КАК КоличествоНеотправленныхДокументов,
	|	Документы.Отправлено КАК КоличествоОтправленныхДокументов,
	|	0 КАК КоличествоДокументовСРасхождениями
	|ИЗ
	|	Документы КАК Документы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1,
	|	0,
	|	0,
	|	0
	|ИЗ
	|	Контрагенты КАК Контрагенты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	0,
	|	Расхождения.ДокументСРасхождением
	|ИЗ
	|	Расхождения КАК Расхождения
	|ИТОГИ
	|	СУММА(КоличествоНеподключенныхКонтрагентов),
	|	СУММА(КоличествоНеотправленныхДокументов),
	|	СУММА(КоличествоОтправленныхДокументов),
	|	СУММА(КоличествоДокументовСРасхождениями)
	|ПО
	|	ОБЩИЕ";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат СтруктураИтогов;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(СтруктураИтогов, Выборка);
	
	Возврат СтруктураИтогов;
	
КонецФункции

Функция ИдентификаторОрганизацияЗарегистрированаПодИнымЛогиномИТС() Экспорт
	
	Возврат "ЗарегистрированаВБизнесСетиПодИнымЛогиномИТС";

КонецФункции

// Выполняет регистрацию организаций в сервисе 1С:Бизнес-Сеть
// Параметры:
//  АдресХранилища - Строка - для возврата результата.
//
Процедура ЗарегистрироватьОрганизацииВСервисе(АдресХранилища) Экспорт
	
	ТаблицаОшибокРегистрации = Новый ТаблицаЗначений;
	ТаблицаОшибокРегистрации.Колонки.Добавить("Организация");
	ТаблицаОшибокРегистрации.Колонки.Добавить("ТекстОшибки");
	
	СтатусыОрганизаций = СтатусыРегистрацииОрганизацийВСервисе();
	Если СтатусыОрганизаций.КоличествоПодключенных = СтатусыОрганизаций.Количество Тогда
		// Все организации подключены 
		Для каждого ПодключеннаяОрганизация Из БизнесСеть.ПодключенныеОрганизации() Цикл
			НоваяСтрокаОшибок = ТаблицаОшибокРегистрации.Добавить();
			НоваяСтрокаОшибок.Организация = ПодключеннаяОрганизация.Организация;
			НоваяСтрокаОшибок.ТекстОшибки = "";
		КонецЦикла;
		ПоместитьВоВременноеХранилище(ТаблицаОшибокРегистрации, АдресХранилища);
		Возврат;
	КонецЕсли;
	
	ОрганизацииДляПодключения = СервисСверкиРасчетовПереопределяемый.ОрганизацииДляПодключения(Истина);
	Для Каждого Организация Из ОрганизацииДляПодключения Цикл
		РезультатПодключенияОрганизациюКСервису = СервисСверкиРасчетовМенеджерОбмена.РезультатПодключенияОрганизациюКСервису(Организация);
		НоваяСтрокаОшибок = ТаблицаОшибокРегистрации.Добавить();
		НоваяСтрокаОшибок.Организация = Организация;
		НоваяСтрокаОшибок.ТекстОшибки = РезультатПодключенияОрганизациюКСервису.ТекстОшибки;
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ТаблицаОшибокРегистрации, АдресХранилища);
	
КонецПроцедуры

Функция СтатусыРегистрацииОрганизацийВСервисе(ТолькоДоступные = Истина) Экспорт
	
	Если Не ТолькоДоступные Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	НЕ Организации.ПометкаУдаления
	|	И Организации.Ссылка <> &УправленческаяОрганизация";
	Запрос.УстановитьПараметр("УправленческаяОрганизация", СервисСверкиРасчетовПереопределяемый.УправленческаяОрганизация());
	ТаблицаОрганизаций = Запрос.Выполнить().Выгрузить();
	
	КоличествоОрганизаций = ТаблицаОрганизаций.Количество();
	КоличествоНеПодключенныхОрганизаций = СервисСверкиРасчетовПереопределяемый.ОрганизацииДляПодключения().Количество();
	КоличествоПодключенныхОрганизаций = КоличествоОрганизаций - КоличествоНеПодключенныхОрганизаций;
	
	Если Не ТолькоДоступные Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат Новый Структура("Количество,КоличествоПодключенных",
		КоличествоОрганизаций,
		КоличествоПодключенныхОрганизаций);
		
КонецФункции

Функция ОрганизацияЗарегистрированаПодИнымЛогиномИТС(Организация) Экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИННОрганизации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ИНН");
	ДлинаИНН = СтрДлина(СокрЛП(ИННОрганизации));
	
	Если Не ЗначениеЗаполнено(ИННОрганизации)
		Или (ДлинаИНН <> 10
		И ДлинаИНН <> 12) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НатуральныйИдентификаторОрганизации = БизнесСеть.НатуральныйИдентификаторОрганизации(Организация);
		
	НатуральныеИдентификаторы = 
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НатуральныйИдентификаторОрганизации);
		
	Отказ = Ложь;
	ДанныеОрганизации = БизнесСеть.ДанныеОрганизацийПоНатуральнымИдентификаторам(
		НатуральныеИдентификаторы, Отказ);
		
	// Организация ранее не регистрировалась
	Если Не ЗначениеЗаполнено(ДанныеОрганизации) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЗарегистрированаПодИнымЛогиномИТС = СервисСверкиРасчетовПереопределяемый.ПрочитатьДанныеИзХранилища(Организация,
		ИдентификаторОрганизацияЗарегистрированаПодИнымЛогиномИТС());
	
	Если ЗначениеЗаполнено(ЗарегистрированаПодИнымЛогиномИТС) 
		И ЗарегистрированаПодИнымЛогиномИТС = Истина Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

// Включает константу "Использовать обмен Бизнес-сеть" если она еще не включена.
//
Процедура ВключитьИспользованиеОбменаБизнесСети() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не Константы.ИспользоватьОбменБизнесСеть.Получить() Тогда
		Константы.ИспользоватьОбменБизнесСеть.Установить(Истина);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбменССервисомСверкиРасчетовРегистрацияДокументаПередЗаписью(Источник, Отказ) Экспорт
	
	Если Не ИспользуетсяСервисСверкиРасчетов() Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ГрупповоеПерепроведение = Неопределено;
	Если Источник.ДополнительныеСвойства.Свойство("ГрупповоеПерепроведение", ГрупповоеПерепроведение)
		И ГрупповоеПерепроведение = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ОтказОтРегистрации = Ложь;
	СервисСверкиРасчетовПереопределяемый.ПередРегистрациейКОтправке(Источник, ОтказОтРегистрации);
	Если ОтказОтРегистрации Тогда
		Возврат;
	КонецЕсли;
	
	Если СервисСверкиРасчетовПереопределяемый.ЭтоСчетФактура(Источник) Тогда
		СервисСверкиРасчетовПереопределяемый.ЗарегистрироватьОснованияСФКОбмену(Источник);
	Иначе
		РеквизитыДокумента = Новый Структура("Организация, Контрагент");
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, Источник);
		СервисСверкиРасчетовПереопределяемый.УточнитьРеквизитыДокумента(Источник, РеквизитыДокумента);
		Если Источник.Проведен Тогда
			ЗарегистрироватьДокументКОтправке(РеквизитыДокумента.Организация, РеквизитыДокумента.Контрагент, Источник.Ссылка, Источник);
			// Если текущий документ после регистрации есть в реестре в статусе отличном от "К отправке" (по другому контрагенту,
			// например) значит нужно его повторно отметить к отправке для удаления на стороне сервиса.
			Если ДокументОтправлялсяНаСервис(Источник.Ссылка) Тогда
				УстановитьСтатусДокументаКОтправке(Источник.Ссылка, Источник.ПометкаУдаления, РеквизитыДокумента.Контрагент);
			КонецЕсли;
		Иначе
			УстановитьСтатусДокументаКОтправке(Источник.Ссылка, Источник.ПометкаУдаления, РеквизитыДокумента.Контрагент);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ЗарегистрироватьДокументКОтправке(Организация, Контрагент, ДокументСсылка, ДокументОбъект = Неопределено) Экспорт
	
	Если Не БизнесСеть.ОрганизацияПодключена(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не КонтрагентПодлежитСверке(Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоКорректныеРеквизиты(Организация, Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыДокумента = СервисСверкиРасчетовПереопределяемый.ДанныеПервичногоДокумента(ДокументСсылка, ДокументОбъект);
	
	Если РеквизитыДокумента = Неопределено Тогда
		Возврат;
	Иначе
		Если ЗначениеЗаполнено(РеквизитыДокумента.Дата) Тогда
			ДатаДокумента  = РеквизитыДокумента.Дата;
		Иначе
			ДатаДокумента  = РеквизитыДокумента.ДатаРегистратора;
		КонецЕсли;
		Если ДатаДокумента < ДатаНачалаСверкиРасчетов() Тогда
			Возврат;
		Конецесли;
	КонецЕсли;
		
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Организация);
	НаборЗаписей.Отбор.Контрагент.Установить(Контрагент);
	НаборЗаписей.Отбор.Документ.Установить(ДокументСсылка);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Организация   = Организация;
	Запись.Контрагент    = Контрагент;
	Запись.Документ      = ДокументСсылка;
	Запись.ДатаДокумента = ДатаДокумента;
	
	Если ЗначениеЗаполнено(РеквизитыДокумента.Номер) Тогда
		ПолныйНомер = РеквизитыДокумента.Номер;
	Иначе
		ПолныйНомер = РеквизитыДокумента.НомерРегистратора;
	КонецЕсли;
	Запись.НомерДокумента = НомерБезПрефиксовИЛидирующихНулей(ПолныйНомер);
	
	Запись.ИдентификаторДокумента = ДокументСсылка.УникальныйИдентификатор();
	Запись.ИдентификаторСообщения = "";
	Запись.Статус                 = Перечисления.СервисСверкиРасчетовСтатусыДокументов.КОтправке;
	
	НаборЗаписей.Записать();
	
	СервисСверкиРасчетовПереопределяемый.ПослеРегистрацииКОтправке(Организация, Контрагент, РеквизитыДокумента);
	
КонецПроцедуры

Функция ИмяСервиса() Экспорт
	
	Возврат НСтр("ru = '1С:Сверка 2.0';
				|en = '1C:Reconciliation 2.0'");
	
КонецФункции

Функция НазваниеОперацииОтправкаСообщений() Экспорт
	
	Возврат НСтр("ru = 'отправка сообщений';
				|en = 'send messages'");
	
КонецФункции

Функция НазваниеОперацииПолучениеРасхождений() Экспорт
	
	Возврат НСтр("ru = 'получение расхождений';
				|en = 'get discrepancies'");
	
КонецФункции

Функция СлужебныйПользовательДляОповещенияОРасхождениях(СоздаватьПриОтсутствии = Ложь) Экспорт
	
	Пользователь = Неопределено;
	
	УстановитьПривилегированныйРежим(Истина);
	Если ОбщегоНазначения.РазделениеВключено()
		И НЕ ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат Пользователь;
	КонецЕсли;
	
	Логин        = ИмяСлужебногоПользователя();
	Пароль       = "946135e8-a9a7-405f-a5df-19e734c712c7";
	
	Пользователь = Пользователи.НайтиПоИмени(Логин);
	
	Если Пользователь <> Неопределено 
		ИЛИ НЕ СоздаватьПриОтсутствии Тогда
		Возврат Пользователь;
	КонецЕсли;
	
	Если НЕ ОбщегоНазначения.РазделениеВключено()
		И НЕ ПользователиИнформационнойБазы.ПолучитьПользователей().Количество() Тогда
		Возврат Пользователь;
	КонецЕсли;
	
	Попытка
		
		ОписаниеПользователяИБ = Пользователи.НовоеОписаниеПользователяИБ();
		ОписаниеПользователяИБ.Имя = Логин;
		ОписаниеПользователяИБ.ПолноеИмя = СервисСверкиРасчетов.ИмяСервиса();
		ОписаниеПользователяИБ.АутентификацияСтандартная = Истина;
		ОписаниеПользователяИБ.ПоказыватьВСпискеВыбора = Ложь;
		ОписаниеПользователяИБ.Вставить("Действие", "Записать");
		ОписаниеПользователяИБ.Вставить("ВходВПрограммуРазрешен", Истина);
		ОписаниеПользователяИБ.ЗапрещеноИзменятьПароль = Истина;
		ОписаниеПользователяИБ.Пароль = Пароль;
		ОписаниеПользователяИБ.Роли = Новый Массив;
		
		Пользователь = Справочники.Пользователи.СоздатьЭлемент();
		Пользователь.Наименование = ОписаниеПользователяИБ.ПолноеИмя;
		Пользователь.Служебный = Истина;
		Пользователь.ДополнительныеСвойства.Вставить("ОписаниеПользователяИБ", ОписаниеПользователяИБ);
		Пользователь.Записать();
		
		СервисСверкиРасчетовПереопределяемый.УстановитьПраваСлужебногоПользователяСверкиРасчетов(Пользователь.Ссылка);
		
	Исключение
		
		МетаданныеОбъекта = Метаданные.Справочники.Пользователи;
		ТекстОшибки = НСтр("ru = 'Не удалось создать служебного пользователя по причине:';
							|en = 'Cannot create internal user. Reason:'")
			+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
			МетаданныеОбъекта,, ТекстОшибки);
		ТекстСообщения = СтрШаблон("%1: при создании служебного пользователя для получения уведомлений произошла ошибка. %2",
			СервисСверкиРасчетов.ИмяСервиса(),
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
	КонецПопытки;
	
	Возврат Пользователь;
	
КонецФункции

Процедура ПриСменеКППУчастникаБизнесСети(Владелец) Экспорт
	
	Если Не ИспользуетсяСервисСверкиРасчетов() Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Контрагенты") Тогда
		
		РегистрыСведений.КонтрагентыБизнесСеть.РезультатАктуализацииКешаКонтрагентов();
		
	ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.Организации") Тогда
		
		РезультатПодключенияОрганизациюКСервису = СервисСверкиРасчетовМенеджерОбмена.РезультатПодключенияОрганизациюКСервису(Владелец);
		
		Если ЗначениеЗаполнено(РезультатПодключенияОрганизациюКСервису.ТекстОшибки) Тогда
			МетаданныеОбъекта = Метаданные.Справочники.Организации;
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось обновить данные авторизации по организации %1 по причине: %2';
										|en = 'Cannot update the authorization data for the %1 company. Reason: %2'"),
				Владелец, РезультатПодключенияОрганизациюКСервису.ТекстОшибки);
			ЗаписьЖурналаРегистрации(ИмяСервиса(), УровеньЖурналаРегистрации.Ошибка, МетаданныеОбъекта,, ТекстОшибки);
		Иначе
			// Регистрируем заново все документы по организации к отправке на сервис сверки.
			МассивОрганизаций = Новый Массив();
			МассивОрганизаций.Добавить(Владелец);
			ЗарегистрироватьКСверкеРанееВведенныеДокументы(МассивОрганизаций);
		КонецЕсли;
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ИзменитьРасписаниеЗаданияБизнесСети() Экспорт
	
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Расписание.ПериодПовтораВТечениеДня = 3600;
	Расписание.ПериодПовтораДней        = 1;
	
	ПараметрыЗадания = Новый Структура("Метаданные", Метаданные.РегламентныеЗадания.ОбновлениеИдентификаторовКонтрагентовБизнесСети);
	СписокЗаданий    = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыЗадания);
	ПараметрыИзмененияЗадания = Новый Структура("Использование, Расписание", Истина, Расписание);
	
	ИспользованиеРегЗадания = Истина;
	Для Каждого РегЗадание Из СписокЗаданий Цикл
		
		Если ИспользованиеРегЗадания Тогда
			РегламентныеЗаданияСервер.ИзменитьЗадание(РегЗадание, ПараметрыИзмененияЗадания);
			ИспользованиеРегЗадания = Ложь;
		Иначе
			РегламентныеЗаданияСервер.УдалитьЗадание(РегЗадание);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстБаннераСервисСверкиРасчетовКнигаПокупок() Экспорт
	
	ШрифЗаголовка = Новый Шрифт(, 16);
	ШрифтТекста   = Новый Шрифт(, 12);
	
	Массив = Новый Массив;
	Массив.Добавить(Новый ФорматированнаяСтрока(Нстр("ru = 'Какие счета-фактуры ваши продавцы ввели в программу с ошибками?';
													|en = 'What tax invoices did your vendors enter into the system with errors?'"), ШрифЗаголовка));
	Массив.Добавить(Символы.ПС);
	Массив.Добавить(Новый ФорматированнаяСтрока(" ", Новый Шрифт(,5)));
	Массив.Добавить(Символы.ПС);
	Массив.Добавить(Новый ФорматированнаяСтрока(Нстр("ru = 'Включите сервис';
													|en = 'Enable the service'") + " ", ШрифтТекста));
	Массив.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = '1С:Сверка 2.0';
													|en = '1C:Reconciliation 2.0'"), ШрифтТекста, , , "ПодключитьсяКСервисуСверки"));
	Массив.Добавить(Новый ФорматированнаяСтрока(Нстр("ru = ' и узнавайте о расхождениях в книге покупок заранее';
													|en = ' and find out about discrepancies in the purchase ledger in advance'"), ШрифтТекста));
	
	Возврат Новый ФорматированнаяСтрока(Массив);
	
КонецФункции

Функция ТекстБаннераСервисСверкиРасчетовКнигаПродаж() Экспорт
	
	ШрифЗаголовка = Новый Шрифт(, 16);
	ШрифтТекста   = Новый Шрифт(, 12);
	
	Массив = Новый Массив;
	Массив.Добавить(Новый ФорматированнаяСтрока(Нстр("ru = 'Какие счета-фактуры ваши покупатели ввели в программу с ошибками?';
													|en = 'What tax invoices did your customers enter into the system with errors?'"), ШрифЗаголовка));
	Массив.Добавить(Символы.ПС);
	Массив.Добавить(Новый ФорматированнаяСтрока(" ", Новый Шрифт(,5)));
	Массив.Добавить(Символы.ПС);
	Массив.Добавить(Новый ФорматированнаяСтрока(Нстр("ru = 'Включите сервис';
													|en = 'Enable the service'") + " ", ШрифтТекста));
	Массив.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = '1С:Сверка 2.0';
													|en = '1C:Reconciliation 2.0'"), ШрифтТекста, , , "ПодключитьсяКСервисуСверки"));
	Массив.Добавить(Новый ФорматированнаяСтрока(Нстр("ru = ' и узнавайте о расхождениях в книге продаж заранее';
													|en = ' and find out about discrepancies in the sales  ledger in advance'"), ШрифтТекста));
	
	Возврат Новый ФорматированнаяСтрока(Массив);

КонецФункции

Функция ТекстБаннераСервисСверкиРасчетовДекларацияПоНДС() Экспорт
	
	ШрифЗаголовка = Новый Шрифт(, 16);
	ШрифтТекста   = Новый Шрифт(, 12);
	
	Массив = Новый Массив;
	Массив.Добавить(Новый ФорматированнаяСтрока(Нстр("ru = 'Какие счета-фактуры ваши контрагенты ввели в программу с ошибками?';
													|en = 'What tax invoices did your counterparties enter into the system with errors?'"), ШрифЗаголовка));
	Массив.Добавить(Символы.ПС);
	Массив.Добавить(Новый ФорматированнаяСтрока(" ", Новый Шрифт(,5)));
	Массив.Добавить(Символы.ПС);
	Массив.Добавить(Новый ФорматированнаяСтрока(Нстр("ru = 'Включите сервис';
													|en = 'Enable the service'") + " ", ШрифтТекста));
	Массив.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = '1С:Сверка 2.0';
													|en = '1C:Reconciliation 2.0'"), ШрифтТекста, , , "ПодключитьсяКСервисуСверки"));
	Массив.Добавить(Новый ФорматированнаяСтрока(Нстр("ru = ' и узнавайте о расхождениях в Декларации по НДС заранее';
													|en = ' and find out about discrepancies in the VAT declaration in advance'"), ШрифтТекста));
	
	Возврат Новый ФорматированнаяСтрока(Массив);
	
КонецФункции

Функция ТекстБаннераСервисСверкиРасчетовПомощникРасчетаНДС() Экспорт
	
	ШрифЗаголовка = Новый Шрифт(, 16);
	ШрифтТекста   = Новый Шрифт(, 12);
	
	Массив = Новый Массив;
	Массив.Добавить(Новый ФорматированнаяСтрока(Нстр("ru = 'Какие счета-фактуры ваши контрагенты ввели в программу с ошибками?';
													|en = 'What tax invoices did your counterparties enter into the system with errors?'"), ШрифЗаголовка));
	Массив.Добавить(Символы.ПС);
	Массив.Добавить(Новый ФорматированнаяСтрока(" ", Новый Шрифт(,5)));
	Массив.Добавить(Символы.ПС);
	Массив.Добавить(Новый ФорматированнаяСтрока(Нстр("ru = 'Включите сервис';
													|en = 'Enable the service'") + " ", ШрифтТекста));
	Массив.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = '1С:Сверка 2.0';
													|en = '1C:Reconciliation 2.0'"), ШрифтТекста, , , "ПодключитьсяКСервисуСверки"));
	Массив.Добавить(Новый ФорматированнаяСтрока(Нстр("ru = ' и узнавайте о расхождениях в книгах покупок, продаж и в Декларации по НДС заранее';
													|en = ' and find out about discrepancies in the sales  ledger, purchase ledger, and VAT declaration in advance'"), ШрифтТекста));
	
	Возврат Новый ФорматированнаяСтрока(Массив);
	
КонецФункции

Функция ЦветФонаБаннеровСервисаСверкиРасчетов() Экспорт
	Возврат Новый Цвет(215, 240, 199);
КонецФункции 

// Формирует список релевантных контрагентов информационной базы, подключенных к сервису
// и сохраняет кэш этих данных в хранилище.
// Вызывается из РегистрСведений.КонтрагентыБизнесСеть.РезультатАктуализацииКешаКонтрагентов()
Процедура ОбновитьКэшДанныхОРелевантныхКонтрагентах() Экспорт
	
	Если ИспользуетсяСервисСверкиРасчетов() Тогда
		Возврат;
	КонецЕсли;
	
	КэшДанныхОРелевантныхКонтрагентах = ДанныеОРелевантныхКонтрагентах();

	Если ЗначениеЗаполнено(КэшДанныхОРелевантныхКонтрагентах) Тогда
		СервисСверкиРасчетовПереопределяемый.ЗаписатьДанныеВХранилище(
			ИмяСервиса(),
			КэшДанныхОРелевантныхКонтрагентах,
			ИмяКлючаКэшДанныхОРелевантныхКонтрагентах());
	КонецЕсли;

КонецПроцедуры

// Возвращает кэш данных о релевантных контрагентах
// 
// Возвращаемое значение:
//   Неопределено - кэш данных отсутствует
//   Структура см. ДанныеОРелевантныхКонтрагентах()
Функция КэшДанныхОРелевантныхКонтрагентах() Экспорт
	
	КэшДанныхОРелевантныхКонтрагентах = СервисСверкиРасчетовПереопределяемый.ПрочитатьДанныеИзХранилища(
		ИмяСервиса(), ИмяКлючаКэшДанныхОРелевантныхКонтрагентах());

	Если ЗначениеЗаполнено(КэшДанныхОРелевантныхКонтрагентах) Тогда
		Возврат КэшДанныхОРелевантныхКонтрагентах;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

Функция КонтрагентПодключенКБизнесСети(Контрагент) Экспорт
	
	Подключен = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КонтрагентыБизнесСеть.Контрагент
	|ИЗ
	|	РегистрСведений.КонтрагентыБизнесСеть КАК КонтрагентыБизнесСеть
	|ГДЕ
	|	КонтрагентыБизнесСеть.Контрагент = &Контрагент";

	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	УстановитьПривилегированныйРежим(Истина);
	Подключен = НЕ Запрос.Выполнить().Пустой();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Подключен;
	
КонецФункции 

Функция НомерБезПрефиксовИЛидирующихНулей(ПолныйНомер) Экспорт

	СтрокаЦифровыхСимволов = "0123456789";
	НомерБезПрефиксов = "";
	
	// "УПД000-00001 123"
	Для Индекс = 1 По СтрДлина(ПолныйНомер) Цикл
		Символ = Сред(ПолныйНомер, Индекс, 1);
		Если СтрНайти(СтрокаЦифровыхСимволов, Символ) > 0 Тогда
			НомерБезПрефиксов = Сред(ПолныйНомер, Индекс);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	// "000-00001 123"
	СимволыКОчистке = "";
	Для Индекс = 1 По СтрДлина(НомерБезПрефиксов) Цикл
		Символ = Сред(НомерБезПрефиксов, Индекс, 1);
		Если СтрНайти(СтрокаЦифровыхСимволов, Символ) = 0 Тогда
			СимволыКОчистке = СимволыКОчистке + Символ;
		КонецЕсли;
	КонецЦикла;
	
	Если СимволыКОчистке <> "" Тогда
		Для Индекс = 1 По СтрДлина(СимволыКОчистке) Цикл
			Символ = Сред(СимволыКОчистке, Индекс, 1);
			НомерБезПрефиксов = СтрЗаменить(НомерБезПрефиксов, Символ, "");
		КонецЦикла;
	КонецЕсли;
	// "00000001123"
	НомерБезПрефиксов = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(НомерБезПрефиксов, "0");
	// "1123"
	
	Если ЗначениеЗаполнено(НомерБезПрефиксов) Тогда
		Возврат НомерБезПрефиксов;
	Иначе
		Возврат ПолныйНомер;
	КонецЕсли;

КонецФункции 

Функция ПодключенныеОрганизации() Экспорт
	
	Возврат БизнесСеть.ПодключенныеОрганизации();
	
КонецФункции

Функция КонтрагентыДляСверки() Экспорт
	
	Возврат СервисСверкиРасчетовПереопределяемый.КонтрагентыДляСверки();
	
КонецФункции

// Определяет является ли интерфейс "Такси" текущим
//
// Возвращаемое значение: 
//   Булево - Истина, если РежимСовместимостиИнтерфейса = Метаданные.СвойстваОбъектов.РежимСовместимостиИнтерфейса.Такси.
//
Функция ЭтоИнтерфейсТакси() Экспорт
	
	Возврат Метаданные.РежимСовместимостиИнтерфейса = Метаданные.СвойстваОбъектов.РежимСовместимостиИнтерфейса.Такси;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура АктуализироватьКешКонтрагентов(СписокКонтрагентов)
	
	Если СписокКонтрагентов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.КонтрагентыБизнесСеть.РезультатАктуализацииКешаКонтрагентов();
	
КонецПроцедуры

Функция НовыеПараметрыОбменаССервисом(Параметры)
	
	ТекущаяДата = ТекущаяДатаСеанса();
	КоличествоМесяцевКвартала = 3;
	НачалоПредыдущегоКвартала = НачалоКвартала(ДобавитьМесяц(ТекущаяДата, - КоличествоМесяцевКвартала));
	
	ПараметрыОбмена = Новый Структура();
	ПараметрыОбмена.Вставить("ЭтоВызовИзОтчета", Ложь);
	ПараметрыОбмена.Вставить("РегистрироватьДокументыПередОбменом", Истина);
	ПараметрыОбмена.Вставить("НачальнаяДатаПолученияРасхождений", НачалоПредыдущегоКвартала);
	ПараметрыОбмена.Вставить("КонечнаяДатаПолученияРасхождений", КонецКвартала(ТекущаяДата));
	
	Если ЗначениеЗаполнено(Параметры) Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыОбмена, Параметры);
	КонецЕсли;
	
	Возврат ПараметрыОбмена;
	
КонецФункции

Процедура УстановитьПользователяРегЗадания()
	
	Пользователь = СлужебныйПользовательДляОповещенияОРасхождениях(Истина);
	Если Пользователь <> Неопределено Тогда
		ПараметрыЗадания = Новый Структура("Метаданные", Метаданные.РегламентныеЗадания.ОбменССервисомСверкиРасчетов);
		СписокЗаданий    = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыЗадания);
		ИспользованиеРегЗадания = Истина;
		ПараметрыИзмененияЗадания = Новый Структура("Использование, ИмяПользователя", 
		ИспользованиеРегЗадания, ИмяСлужебногоПользователя());
		УстановитьПривилегированныйРежим(Истина);
		Для Каждого РегЗадание Из СписокЗаданий Цикл
			Если ИспользованиеРегЗадания Тогда
				РегламентныеЗаданияСервер.ИзменитьЗадание(РегЗадание, ПараметрыИзмененияЗадания);
				ИспользованиеРегЗадания = Ложь;
			Иначе
				РегламентныеЗаданияСервер.УдалитьЗадание(РегЗадание);
			КонецЕсли;
		КонецЦикла;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;

КонецПроцедуры

Функция НовыйПакетСообщенийНаСервис(ВыборкаОрганизации)
	
	ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON( , Символы.Таб);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку(ПараметрыЗаписиJSON);
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	
	ЗаписьJSON.ЗаписатьИмяСвойства("abonement");
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	
	ЗаписьJSON.ЗаписатьИмяСвойства("fullName");
	ЗаписьJSON.ЗаписатьЗначение(ВыборкаОрганизации.НаименованиеПолное);
	
	ЗаписьJSON.ЗаписатьИмяСвойства("name");
	ЗаписьJSON.ЗаписатьЗначение(ВыборкаОрганизации.Наименование);
	
	ЗаписьJSON.ЗаписатьИмяСвойства("inn");
	ЗаписьJSON.ЗаписатьЗначение(ВыборкаОрганизации.ИНН);
	
	ЗаписьJSON.ЗаписатьИмяСвойства("kpp");
	ЗаписьJSON.ЗаписатьЗначение(ВыборкаОрганизации.КПП);
	
	ЗаписьJSON.ЗаписатьИмяСвойства("idNetwork");
	ЗаписьJSON.ЗаписатьЗначение(ВыборкаОрганизации.ИдентификаторОрганизации);
	
	ЗаписьJSON.ЗаписатьИмяСвойства("InfoBaseID");
	ЗаписьJSON.ЗаписатьЗначение(СтандартныеПодсистемыСервер.ИдентификаторИнформационнойБазы());

	ЗаписьJSON.ЗаписатьИмяСвойства("ClientVersion");
	ЗаписьJSON.ЗаписатьЗначение(КонфигурацияИВерсияКлиента());
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	ЗаписьJSON.ЗаписатьИмяСвойства("itemLines");
	
	ЗаписьJSON.ЗаписатьНачалоМассива();
	
	Возврат ЗаписьJSON;

КонецФункции

Функция КонфигурацияИВерсияКлиента()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВерсииПодсистем.Версия КАК Версия,
	|	ВерсииПодсистем.ИмяПодсистемы КАК ИмяПодсистемы
	|ИЗ
	|	РегистрСведений.ВерсииПодсистем КАК ВерсииПодсистем
	|ГДЕ
	|	ВерсииПодсистем.ЭтоОсновнаяКонфигурация";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат СтрШаблон("%1 %2", Выборка.ИмяПодсистемы, Выборка.Версия);
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

Процедура ЗакрытьИОтправитьТекущийПакет(ВыборкаОрганизации, ДокументыСообщения, ЗаписьJSON, ИдентификаторСообщения, ТаблицаКонтрагенты, ТаблицаОшибок)
	
	ЗаписьJSON.ЗаписатьКонецМассива();
	
	// Контрагенты документов в пакете.
	ДополнитьКонтрагентами(ВыборкаОрганизации, ЗаписьJSON, ТаблицаКонтрагенты);
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	СтрокаJSON = ЗаписьJSON.Закрыть();
	
	ДанныеСервиса = ОтправитьСообщениеВСервисСверки(ВыборкаОрганизации.Организация, ИдентификаторСообщения, СтрокаJSON);
	
	Отказ = Ложь;
	ЗарегистрироватьТекущиеОшибки(ВыборкаОрганизации.Организация,
		ТаблицаОшибок,
		ДанныеСервиса,
		Отказ);
	
	Если Не Отказ Тогда
		// Отметить, что сообщение отправлено успешно.
		ОтметитьУспешнуюОтправку(ДокументыСообщения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьДокументВПакет(ВыборкаДокументы, ЗаписьJSON, НаименованиеОперации)
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	
	ЗаписьJSON.ЗаписатьИмяСвойства("document");
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	
	ЗаписьJSON.ЗаписатьИмяСвойства("UID");
	ЗаписьJSON.ЗаписатьЗначение(Строка(ВыборкаДокументы.Документ.УникальныйИдентификатор()));
	
	ЗаписьJSON.ЗаписатьИмяСвойства("date");
	Если ВыборкаДокументы.ЭтоКорректировочныйСчетФактура Тогда
		ЗаписьJSON.ЗаписатьЗначение(Формат(ВыборкаДокументы.ДатаСчетаФактуры, "ДФ=yyyy-MM-dd"));
	Иначе
		ЗаписьJSON.ЗаписатьЗначение(Формат(ВыборкаДокументы.ДатаДокумента, "ДФ=yyyy-MM-dd"));
	КонецЕсли;
	
	ЗаписьJSON.ЗаписатьИмяСвойства("number");
	ЗаписьJSON.ЗаписатьЗначение(ВыборкаДокументы.НомерРегистратора);
	
	ИмяДокументаМетаданные = ВыборкаДокументы.Документ.Метаданные().Имя;
	
	ЗаписьJSON.ЗаписатьИмяСвойства("type");
	ЗаписьJSON.ЗаписатьЗначение(ИмяДокументаМетаданные); 
	
	ЗаписьJSON.ЗаписатьИмяСвойства("operation");
	ЗаписьJSON.ЗаписатьЗначение(НаименованиеОперации);
	
	ЗаписьJSON.ЗаписатьИмяСвойства("view1C");
	ЗаписьJSON.ЗаписатьЗначение(ВыборкаДокументы.Документ.Метаданные().ПредставлениеОбъекта); 
	
	ЗаписьJSON.ЗаписатьИмяСвойства("idNetworkPartner");
	ЗаписьJSON.ЗаписатьЗначение(ВыборкаДокументы.ИдентификаторКонтрагента);
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	ЗаписьJSON.ЗаписатьИмяСвойства("documentIncomming");
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	
	НомерСФ = ВыборкаДокументы.НомерСчетаФактуры; 
	Если ВыборкаДокументы.ОбрабатыватьНомерСФ Тогда
		НомерСФ = НомерБезПрефиксовИЛидирующихНулей(НомерСФ);
	КонецЕсли;
	
	Если ВыборкаДокументы.ЭтоКорректировочныйСчетФактура Тогда // Для корректировочных документов идентифицирующий номер - номер корр СФ.
		
		ЗаписьJSON.ЗаписатьИмяСвойства("date");
		ЗаписьJSON.ЗаписатьЗначение(Формат(ВыборкаДокументы.ДатаСчетаФактуры, "ДФ=yyyy-MM-dd"));
		
		ЗаписьJSON.ЗаписатьИмяСвойства("number");
		ЗаписьJSON.ЗаписатьЗначение(НомерСФ);
		
	Иначе
		
		ЗаписьJSON.ЗаписатьИмяСвойства("date");
		ЗаписьJSON.ЗаписатьЗначение(Формат(ВыборкаДокументы.ДатаДокумента, "ДФ=yyyy-MM-dd"));
		
		ЗаписьJSON.ЗаписатьИмяСвойства("number");
		ЗаписьJSON.ЗаписатьЗначение(ВыборкаДокументы.НомерДокумента);
		
	КонецЕсли;
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	ЗаписьJSON.ЗаписатьИмяСвойства("documentInvoice");
	
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	
	ЗаписьJSON.ЗаписатьИмяСвойства("date");
	ЗаписьJSON.ЗаписатьЗначение(Формат(ВыборкаДокументы.ДатаСчетаФактуры, "ДФ=yyyy-MM-dd"));
	
	ЗаписьJSON.ЗаписатьИмяСвойства("number");
	ЗаписьJSON.ЗаписатьЗначение(НомерСФ);
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	ЗаписьJSON.ЗаписатьИмяСвойства("sum"); // Сумма всегда по модулю
	Если ВыборкаДокументы.Сумма < 0 Тогда
		ЗаписьJSON.ЗаписатьЗначение(Формат(-ВыборкаДокументы.Сумма,"ЧРД=.; ЧРГ=' '; ЧГ=0"));
	Иначе
		ЗаписьJSON.ЗаписатьЗначение(Формат(ВыборкаДокументы.Сумма,"ЧРД=.; ЧРГ=' '; ЧГ=0"));
	КонецЕсли;
	
	ЗаписьJSON.ЗаписатьИмяСвойства("sumNDS");
	Если ВыборкаДокументы.СуммаНДС < 0 Тогда
		ЗаписьJSON.ЗаписатьЗначение(Формат(-ВыборкаДокументы.СуммаНДС,"ЧРД=.; ЧРГ=' '; ЧГ=0"));
	Иначе
		ЗаписьJSON.ЗаписатьЗначение(Формат(ВыборкаДокументы.СуммаНДС,"ЧРД=.; ЧРГ=' '; ЧГ=0"));
	КонецЕсли;
	
	ЗаписьJSON.ЗаписатьИмяСвойства("sumVal");
	Если ВыборкаДокументы.ВалютнаяСумма < 0 Тогда
		ЗаписьJSON.ЗаписатьЗначение(Формат(-ВыборкаДокументы.ВалютнаяСумма,"ЧРД=.; ЧРГ=' '; ЧГ=0"));
	Иначе
		ЗаписьJSON.ЗаписатьЗначение(Формат(ВыборкаДокументы.ВалютнаяСумма,"ЧРД=.; ЧРГ=' '; ЧГ=0"));
	КонецЕсли;
	
	ЗаписьJSON.ЗаписатьИмяСвойства("command");
	
	// Если документ был отправлен к удалению, то ставим команду удалить
	Если ВыборкаДокументы.Статус = Перечисления.СервисСверкиРасчетовСтатусыДокументов.ОтправленоКУдалению Тогда
		
		ЗаписьJSON.ЗаписатьЗначение("Удалить");
		
	Иначе
		
		ЗаписьJSON.ЗаписатьЗначение("ДобавитьОбновить");
		
	КонецЕсли;
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();

КонецПроцедуры

Процедура ДополнитьКонтрагентами(ВыборкаОрганизации, ЗаписьJSON, ТаблицаКонтрагенты)
	
	ТаблицаКонтрагенты.Свернуть("Контрагент,НаименованиеКонтрагента,НаименованиеКонтрагентаПолное,КППКонтрагента,ИННКонтрагента,ИдентификаторКонтрагента");
	
	ЗаписьJSON.ЗаписатьИмяСвойства("partnersLines");
	
	ЗаписьJSON.ЗаписатьНачалоМассива();
	
	Для Каждого Контрагент Из ТаблицаКонтрагенты Цикл
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		
		ЗаписьJSON.ЗаписатьИмяСвойства("fullName");
		ЗаписьJSON.ЗаписатьЗначение(Контрагент.НаименованиеКонтрагентаПолное);
		
		ЗаписьJSON.ЗаписатьИмяСвойства("name");
		ЗаписьJSON.ЗаписатьЗначение(Контрагент.НаименованиеКонтрагента);
		
		ЗаписьJSON.ЗаписатьИмяСвойства("inn");
		ЗаписьJSON.ЗаписатьЗначение(Контрагент.ИННКонтрагента);
		
		ЗаписьJSON.ЗаписатьИмяСвойства("kpp");
		ЗаписьJSON.ЗаписатьЗначение(Контрагент.КППКонтрагента);
		
		ЗаписьJSON.ЗаписатьИмяСвойства("idNetwork");
		ЗаписьJSON.ЗаписатьЗначение(Контрагент.ИдентификаторКонтрагента);
		
		ЗаписьJSON.ЗаписатьКонецОбъекта();
		
	КонецЦикла;
	
	ЗаписьJSON.ЗаписатьКонецМассива();
	
	// Очистим таблицу контрагентов в пакете.
	ТаблицаКонтрагенты.Очистить();
	
КонецПроцедуры

Процедура ОтметитьУспешнуюОтправку(ДокументыСообщения)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого ДокументСообщения Из ДокументыСообщения Цикл
		
		Если ДокументСообщения.Статус = Перечисления.СервисСверкиРасчетовСтатусыДокументов.Получено Тогда
			
			НаборЗаписей = РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.СоздатьНаборЗаписей();
			НаборЗаписей.ВыгрузитьКолонки();
			НаборЗаписей.Отбор.Организация.Установить(ДокументСообщения.Организация);
			НаборЗаписей.Отбор.Контрагент.Установить(ДокументСообщения.Контрагент);
			НаборЗаписей.Отбор.Документ.Установить(ДокументСообщения.Документ);
			
			НоваяЗаписьРегистра = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗаписьРегистра, ДокументСообщения);
			НаборЗаписей.Записать();
			
		ИначеЕсли ДокументСообщения.Статус = Перечисления.СервисСверкиРасчетовСтатусыДокументов.ОтправленоКУдалению Тогда
			
			НаборЗаписей = РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.СоздатьНаборЗаписей();
			НаборЗаписей.ВыгрузитьКолонки();
			НаборЗаписей.Отбор.Организация.Установить(ДокументСообщения.Организация);
			НаборЗаписей.Отбор.Контрагент.Установить(ДокументСообщения.Контрагент);
			НаборЗаписей.Отбор.Документ.Установить(ДокументСообщения.Документ);
			НаборЗаписей.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ОтправитьСообщениеВСервисСверки(Организация, ИдентификаторСообщения, СтрокаJSON)
	
	ПараметрыМетода = Новый Структура();
	ПараметрыМетода.Вставить("Организация", Организация);
	ПараметрыМетода.Вставить("Данные", СтрокаJSON);
	ПараметрыМетода.Вставить("Заголовок", ИдентификаторСообщения);
	
	Возврат ВыполнитьКомандуСервисаОтправитьСообщения(ПараметрыМетода);
	
КонецФункции

Процедура ПолучитьРасхождения(ДатаНачала, ДатаОкончания, ЭтоВызовИзОтчета)
	
	Отказ                   = Ложь;
	Оповещать               = НЕ ЭтоВызовИзОтчета И ВключеныОповещенияСверки();
	ПодключенныеОрганизации = ПодключенныеОрганизации();
	
	Для Каждого ТекущаяОрганизация Из ПодключенныеОрганизации Цикл
		
		// Отправить сообщение в сервис сверки
		ПараметрыМетода = Новый Структура();
		ПараметрыМетода.Вставить("Организация",   ТекущаяОрганизация.Организация);
		ПараметрыМетода.Вставить("ДатаНачала",    ДатаНачала);
		ПараметрыМетода.Вставить("ДатаОкончания", ДатаОкончания);
		ПараметрыМетода.Вставить("Заголовок",     "");
	
		ДанныеСервиса = ВыполнитьКомандуСервисаПолучитьРасхождения(ПараметрыМетода);
		ЗаписатьЛогПолученияРасхождений(ДанныеСервиса, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		СтруктураДанных = ПреобразоватьТекстВТаблицу(ПараметрыМетода, ДанныеСервиса);
		РегистрыСведений.СервисСверкиРасчетовОбнаруженныеРасхождения.ЗаписатьДанные(ПараметрыМетода, СтруктураДанных);
		
	КонецЦикла;
	
	// Запишем дату и время получения данных из сервиса.
	ЗаписатьЛогПолученияРасхождений(Неопределено, Отказ, Истина);
	
	Если Оповещать Тогда
		ОповеститьПользователейОНаличииРасхождений();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьТекущиеОшибки(Организация, ТаблицаОшибок, ДанныеСервиса, Отказ)

	МассивОшибок = Новый Массив();
	
	Если ДанныеСервиса = Неопределено Тогда
		
		МассивОшибок.Добавить(НСтр("ru = 'Сервис не отвечает';
									|en = 'Service is not responding'"));
		
	ИначеЕсли ТипЗНЧ(ДанныеСервиса) = Тип("Строка") Тогда
		
		МассивОшибок.Добавить(ДанныеСервиса);
		
	ИначеЕсли ДанныеСервиса.Свойство("errors") Тогда
		
		МассивОшибок.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибки сервиса: %1';
				|en = 'Service error: %1'"), ДанныеСервиса.detail));
				
	ИначеЕсли ДанныеСервиса.Ошибка Тогда
		
		МассивОшибок.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибки сервиса: %1';
				|en = 'Service error: %1'"), ДанныеСервиса.ТекстОшибки));
		
	КонецЕсли;
	
	Если МассивОшибок.Количество() > 0 Тогда
	
		НоваяСтрока = ТаблицаОшибок.Добавить();
		НоваяСтрока.Организация = Организация;
		НоваяСтрока.Ошибки = МассивОшибок;
		
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьЛогПолученияРасхождений(ДанныеСервиса, Отказ, УспешноеПолучениеДанных = Ложь)
	
	Если УспешноеПолучениеДанных Тогда
		
		Константы.ДатаПолученияДанныхСервисаСверкиРасчетов.Установить(ТекущаяДатаСеанса());
		
	ИначеЕсли ТипЗнч(ДанныеСервиса) = Тип("Строка") Тогда
		
		ЗаписатьОшибкуВЖурналРегистрации(ДанныеСервиса);
		Отказ = Истина;
		
	ИначеЕсли ДанныеСервиса = Неопределено Тогда
		
		ЗаписатьОшибкуВЖурналРегистрации(СтрШаблон("%1 не отвечает", ИмяСервиса()));
		Отказ = Истина;
		
	ИначеЕсли ТипЗнч(ДанныеСервиса) <> Тип("Массив") Тогда
		
		ЗаписатьОшибкуВЖурналРегистрации(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка %1 type: %2';
				|en = 'Error %1 type: %2'"),ДанныеСервиса.title, ДанныеСервиса.type));
		Отказ = Истина;
	
	КонецЕсли;

КонецПроцедуры

Процедура ЗаписатьЛогОтправкиСообщения(ТаблицаОшибок)
	
	Если ТаблицаОшибок.Количество() > 0 Тогда
		Для Каждого ТекущаяСтрока Из ТаблицаОшибок Цикл
			Для Каждого ТекущееЗначение Из ТекущаяСтрока.Ошибки Цикл
				ЗаписатьОшибкуВЖурналРегистрации(
					СтрШаблон("%1: по организации %2 ошибка %3'", ИмяСервиса(), ТекущаяСтрока.Организация, ТекущееЗначение));
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьОшибкуВЖурналРегистрации(Комментарий)
	
	ЗаписьЖурналаРегистрации(ИмяСервиса(), УровеньЖурналаРегистрации.Ошибка, , , Комментарий);
	
КонецПроцедуры

Функция ДокументОтправлялсяНаСервис(ДокументСсылка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СервисСверкиРасчетовРеестрДокументов.Документ КАК Документ
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|ГДЕ
	|	СервисСверкиРасчетовРеестрДокументов.Документ = &Документ
	|	И СервисСверкиРасчетовРеестрДокументов.Статус <> ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.КОтправке)";
	
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Возвращает признак того, что контрагент подлежит сверке в сервисе сверки расчетов.
// 
//Параметры:
// Контрагент - СправочникСсылка.Контрагенты - ссылка на проверяемого контрагента
//
// Возвращаемое значение:
// Булево - Истина - контрагент подлежит сверке в сервисе.
//
Функция КонтрагентПодлежитСверке(Контрагент)
	
	Возврат СервисСверкиРасчетовПереопределяемый.КонтрагентПодлежитСверке(Контрагент);
	
КонецФункции

Процедура УстановитьСтатусДокументаКОтправке(ДокументСсылка, ПометкаУдаления, Контрагент)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(ДокументСсылка);
	Если ПометкаУдаления И 
		НЕ КонтрагентПодключенКБизнесСети(Контрагент) Тогда
		НаборЗаписей.Записать();
		Возврат;
	КонецЕсли;
	
	НаборЗаписей.Прочитать();
	ЗаписыватьНабор = Ложь;
	Для каждого Запись Из НаборЗаписей Цикл
		Если Запись.Статус = Перечисления.СервисСверкиРасчетовСтатусыДокументов.КОтправке Тогда
			Продолжить;
		КонецЕсли;
		Запись.Статус                 = Перечисления.СервисСверкиРасчетовСтатусыДокументов.КОтправке;
		Запись.ИдентификаторСообщения = "";
		ЗаписыватьНабор = Истина;
	КонецЦикла;
	
	Если ЗаписыватьНабор Тогда
		НаборЗаписей.Записать();
	КонецЕсли;

КонецПроцедуры

Функция ЭтоКорректныеРеквизиты(Организация, Контрагент)
	
	ИННКонтрагента = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ИНН"));
	Если Не ЗначениеЗаполнено(ИННКонтрагента) Тогда
		Возврат Ложь; // Не заполнен контрагент или его ИНН.
	КонецЕсли; 
	
	ИННОрганизации = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ИНН"));
	Если ИННОрганизации = ИННКонтрагента Тогда
		Возврат Ложь; // Сверка с самим собой не производится.
	КонецЕсли; 
	
	Возврат Истина;
	
КонецФункции

Функция ВыполнитьКомандуСервисаОтправитьСообщения(ПараметрыМетода)
	
	Возврат СервисСверкиРасчетовМенеджерОбмена.ВыполнитьЗапросОтправитьСообщения(ПараметрыМетода);
	
КонецФункции

Функция ВыполнитьКомандуСервисаПолучитьРасхождения(ПараметрыМетода)
	
	Возврат СервисСверкиРасчетовМенеджерОбмена.ВыполнитьЗапросПолучитьРасхождения(ПараметрыМетода);
	
КонецФункции

Функция НаименованиеОперации(Выборка)
	
	ДополнительныеСведенияДляОпределенияОперации = 
		СервисСверкиРасчетовПереопределяемый.ДополнительныеСведенияДляОпределенияОперации(Выборка);
		
	Возврат СервисСверкиРасчетовПереопределяемый.НаименованиеОперации(
		Выборка.Документ, ДополнительныеСведенияДляОпределенияОперации);
	
КонецФункции

Функция ПреобразоватьТекстВТаблицу(ПараметрыМетода, ДанныеСервиса)
	
	ТаблицаРасхождений = НовыйТаблицаРасхождений();
	
	Если ТипЗНЧ(ДанныеСервиса) <> Тип("Массив")
		ИЛИ ДанныеСервиса.Количество() = 0 Тогда
		Возврат ТаблицаРасхождений;
	КонецЕсли;
	
	Для Каждого ТекущаяСтрока Из ДанныеСервиса Цикл
		
		СуммаПоДаннымКонтрагента = 0;
		СуммаВалПоДаннымКонтрагента = 0;
		НДСПоДаннымКонтрагента =   0;
		СуммаПоДаннымОрганизации = 0;
		СуммаВалПоДаннымОрганизации = 0;
		НДСПоДаннымОрганизации =   0;
		
		НоваяСтрока = ТаблицаРасхождений.Добавить();
		
		НоваяСтрока.Организация     = ПараметрыМетода.Организация;
		СведенияОКонтрагенте        = ПолучитьЗначение(ТекущаяСтрока.Контрагент, "СправочникСсылка.Контрагенты");
		НоваяСтрока.Контрагент      = СведенияОКонтрагенте.Значение;
		НоваяСтрока.ИННКонтрагента  = СведенияОКонтрагенте.ИННКонтрагента;
		НоваяСтрока.КППКонтрагента  = СведенияОКонтрагенте.КППКонтрагента;
		НоваяСтрока.Дата            = ПолучитьЗначение(ТекущаяСтрока.Дата, "ДатаJSON");
		НоваяСтрока.ИдентификаторСверкиПоОрганизации = 
			ПолучитьЗначение(ТекущаяСтрока.ИдентификаторСтрокиПоДаннымОрганизации, "УникальныйИдентификатор");
		НоваяСтрока.ИдентификаторСверкиКонтрагенту = 
			ПолучитьЗначение(ТекущаяСтрока.ИдентификаторСтрокиПоДаннымКонтрагента, "УникальныйИдентификатор");
		НоваяСтрока.КонтрагентНеПроизводилСверку = ТекущаяСтрока.КонтрагентНеПроизводилСверку;
		МассивДокументовПоОрганизации = Новый Массив();
		МассивДокументовПоКонтрагенту = Новый Массив();
		
		ТаблицаДокументовОрганизации = НовыйТаблицаПредставленияДокумента();
		ТаблицаДокументовКонтрагента = НовыйТаблицаПредставленияДокумента();
		ТаблицаСФОрганизации         = НовыйТаблицаПредставленияДокумента();
		ТаблицаСФКонтрагента         = НовыйТаблицаПредставленияДокумента();
		
		// Добавим постфикс для того чтобы отличать расхождение по организации и по контрагенту,
		// когда в одной базе ведется учет по сверяемым между собой организациям.
		// ОК - есть ИД сверки у организации и контрагента
		// О_ - есть ИД сверки только по организации (у контрагента нет документа)
		// _К - есть ИД сверки только по контрагенту (у организации нет документа)
		ПредварительныйИдентификаторРасхождения = "";
		Постфикс = "ОК";
		Если ТекущаяСтрока.ИдентификаторСтрокиПоДаннымОрганизации  =  "00000000-0000-0000-0000-000000000000" Тогда
			Постфикс = "_К"; // у организации нет документа
			ПредварительныйИдентификаторРасхождения = ТекущаяСтрока.ИдентификаторСтрокиПоДаннымКонтрагента;
		Иначе
			Если ТекущаяСтрока.ИдентификаторСтрокиПоДаннымКонтрагента = "00000000-0000-0000-0000-000000000000" Тогда
				Постфикс = "О_"; // у контрагента нет документа
			КонецЕсли;
			ПредварительныйИдентификаторРасхождения = ТекущаяСтрока.ИдентификаторСтрокиПоДаннымОрганизации;
		КонецЕсли;
		НоваяСтрока.ИдентификаторРасхождения = ПредварительныйИдентификаторРасхождения + Постфикс;
		
		Для Каждого ТекущаяСтрокаДетально Из ТекущаяСтрока.ДокументыПоДаннымОрганизации Цикл
			
			Если Не ЗначениеЗаполнено(НоваяСтрока.ИдентификаторСверкиПоОрганизации) Тогда
				Продолжить;
			КонецЕсли;
			
			СтруктураДанныхДокумента = НовыйСтруктураДокумента();
			
			СтруктураДанныхДокумента.ПредставлениеДокумента =   
				ПолучитьЗначение(ТекущаяСтрокаДетально["ПредставлениеДокумента1СПоДаннымОрганизации"], "Строка", "");
			СтруктураДанныхДокумента.ВидДокумента =             
				ПолучитьЗначение(ТекущаяСтрокаДетально["ВидДокументаПоДаннымОрганизации"], "Строка", "");
				
			СтруктураДанныхДокумента.Операция =                 
				ПолучитьЗначение(ТекущаяСтрокаДетально["ОперацияПоДаннымОрганизации"], 
				"ПеречислениеСсылка.СервисСверкиРасчетовВидыОпераций", 
				Перечисления.СервисСверкиРасчетовВидыОпераций.ПустаяСсылка());
				
			СтруктураДанныхДокумента.ДатаИзменения =   ПолучитьЗначение(ТекущаяСтрокаДетально["ДатаИзмененияПоДаннымОрганизации"], "ДатаJSON", '00010101');
			СтруктураДанныхДокумента.НомерДокумента =           ПолучитьЗначение(ТекущаяСтрокаДетально["НомерДокументаПоДаннымОрганизации"], "Строка", "");
			СтруктураДанныхДокумента.ДатаДокумента =            ПолучитьЗначение(ТекущаяСтрокаДетально["ДатаДокументаПоДаннымОрганизации"], "ДатаJSON", '00010101');
			СтруктураДанныхДокумента.НомерСчетФактуры =         ПолучитьЗначение(ТекущаяСтрокаДетально["НомерСчетФактурыПоДаннымОрганизации"], "Строка", "");
			СтруктураДанныхДокумента.ДатаСчетФактуры =          ПолучитьЗначение(ТекущаяСтрокаДетально["ДатаСчетФактурыПоДаннымОрганизации"], "ДатаJSON", '00010101');
			СтруктураДанныхДокумента.ИсходныйНомерДокумента1С = ПолучитьЗначение(ТекущаяСтрокаДетально["ИсходныйНомерДокумента1СПоДаннымОрганизации"], "Строка", "");
			СтруктураДанныхДокумента.НДС =                      ПолучитьЗначение(ТекущаяСтрокаДетально["НДСПоДаннымОрганизации"], "Число", 0);
			СтруктураДанныхДокумента.Сумма =                    ПолучитьЗначение(ТекущаяСтрокаДетально["СуммаПоДаннымОрганизации"], "Число", 0);
			СтруктураДанныхДокумента.СуммаВал =                    ПолучитьЗначение(ТекущаяСтрокаДетально["СуммаВалПоДаннымОрганизации"], "Число", 0);
			СтруктураДанныхДокумента.ИдентификаторДокумента =  ПолучитьЗначение(ТекущаяСтрокаДетально["УИДДокументаПоДаннымОрганизации"], "УникальныйИдентификатор", "");
			
			МассивДокументовПоОрганизации.Добавить(СтруктураДанныхДокумента);
			
			НовыйДокументОрганизации = ТаблицаДокументовОрганизации.Добавить();
			НовыйДокументОрганизации.Номер = СтруктураДанныхДокумента.НомерДокумента;
			НовыйДокументОрганизации.Дата  = СтруктураДанныхДокумента.ДатаДокумента;
			
			НовыйСФОрганизации = ТаблицаСФОрганизации.Добавить();
			НовыйСФОрганизации.Номер = СтруктураДанныхДокумента.НомерСчетФактуры;
			НовыйСФОрганизации.Дата  = СтруктураДанныхДокумента.ДатаСчетФактуры;
			
			СуммаПоДаннымОрганизации = СуммаПоДаннымОрганизации + СтруктураДанныхДокумента.Сумма;
			СуммаВалПоДаннымОрганизации = СуммаВалПоДаннымОрганизации + СтруктураДанныхДокумента.СуммаВал;
			НДСПоДаннымОрганизации =   НДСПоДаннымОрганизации + СтруктураДанныхДокумента.НДС;
			
		КонецЦикла;
		
		Для Каждого ТекущаяСтрокаДетально Из ТекущаяСтрока.ДокументыПоДаннымКонтрагента Цикл
			
			Если Не ЗначениеЗаполнено(НоваяСтрока.ИдентификаторСверкиКонтрагенту) Тогда
				Продолжить;
			КонецЕсли;	
			
			СтруктураДанныхДокумента = НовыйСтруктураДокумента();
			
			СтруктураДанныхДокумента.ПредставлениеДокумента =   ПолучитьЗначение(ТекущаяСтрокаДетально["ПредставлениеДокумента1СПоДаннымКонтрагента"], "Строка", "");
			СтруктураДанныхДокумента.ВидДокумента =             ПолучитьЗначение(ТекущаяСтрокаДетально["ВидДокументаПоДаннымКонтрагента"], "Строка", "");
			СтруктураДанныхДокумента.Операция =                 ПолучитьЗначение(ТекущаяСтрокаДетально["ОперацияПоДаннымКонтрагента"], 
				"ПеречислениеСсылка.СервисСверкиРасчетовВидыОпераций", Перечисления.СервисСверкиРасчетовВидыОпераций.ПустаяСсылка());
			СтруктураДанныхДокумента.ДатаИзменения =            ПолучитьЗначение(ТекущаяСтрокаДетально["ДатаИзмененияПоДаннымКонтрагента"], "ДатаJSON", '00010101');
			СтруктураДанныхДокумента.НомерДокумента =           ПолучитьЗначение(ТекущаяСтрокаДетально["НомерДокументаПоДаннымКонтрагента"], "Строка", "");
			СтруктураДанныхДокумента.ДатаДокумента =            ПолучитьЗначение(ТекущаяСтрокаДетально["ДатаДокументаПоДаннымКонтрагента"], "ДатаJSON", '00010101');
			СтруктураДанныхДокумента.НомерСчетФактуры =         ПолучитьЗначение(ТекущаяСтрокаДетально["НомерСчетФактурыПоДаннымКонтрагента"], "Строка", "");
			СтруктураДанныхДокумента.ДатаСчетФактуры =          ПолучитьЗначение(ТекущаяСтрокаДетально["ДатаСчетФактурыПоДаннымКонтрагента"], "ДатаJSON", '00010101');
			СтруктураДанныхДокумента.ИсходныйНомерДокумента1С = ПолучитьЗначение(ТекущаяСтрокаДетально["ИсходныйНомерДокумента1СПоДаннымКонтрагента"], "Строка", "");
			СтруктураДанныхДокумента.НДС =                      ПолучитьЗначение(ТекущаяСтрокаДетально["НДСПоДаннымКонтрагента"], "Число", 0);
			СтруктураДанныхДокумента.Сумма =                    ПолучитьЗначение(ТекущаяСтрокаДетально["СуммаПоДаннымКонтрагента"], "Число", 0);
			СтруктураДанныхДокумента.СуммаВал =                 ПолучитьЗначение(ТекущаяСтрокаДетально["СуммаВалПоДаннымКонтрагента"], "Число", 0);
			СтруктураДанныхДокумента.ИдентификаторДокумента =   ПолучитьЗначение( ТекущаяСтрокаДетально["УИДДокументаПоДаннымКонтрагента"], "УникальныйИдентификатор", "");
			
			МассивДокументовПоКонтрагенту.Добавить(СтруктураДанныхДокумента);
			
			НовыйДокументКонтрагента = ТаблицаДокументовКонтрагента.Добавить();
			НовыйДокументКонтрагента.Номер = СтруктураДанныхДокумента.НомерДокумента;
			НовыйДокументКонтрагента.Дата  = СтруктураДанныхДокумента.ДатаДокумента;
			
			НовыйСФКонтрагента = ТаблицаСФКонтрагента.Добавить();
			НовыйСФКонтрагента.Номер = СтруктураДанныхДокумента.НомерСчетФактуры;
			НовыйСФКонтрагента.Дата  = СтруктураДанныхДокумента.ДатаСчетФактуры;
			
			СуммаПоДаннымКонтрагента = СуммаПоДаннымКонтрагента + СтруктураДанныхДокумента.Сумма;
			СуммаВалПоДаннымКонтрагента = СуммаВалПоДаннымКонтрагента + СтруктураДанныхДокумента.СуммаВал;
			НДСПоДаннымКонтрагента =   НДСПоДаннымКонтрагента + СтруктураДанныхДокумента.НДС;
			
		КонецЦикла;
		
		НоваяСтрока.МассивДокументовПоОрганизации = МассивДокументовПоОрганизации;
		НоваяСтрока.МассивДокументовПоКонтрагенту = МассивДокументовПоКонтрагенту;
		
		НоваяСтрока.СуммаПоДаннымКонтрагента =    СуммаПоДаннымКонтрагента;
		НоваяСтрока.СуммаВалПоДаннымКонтрагента = СуммаВалПоДаннымКонтрагента;
		НоваяСтрока.НДСПоДаннымКонтрагента =      НДСПоДаннымКонтрагента;
		НоваяСтрока.СуммаПоДаннымОрганизации =    СуммаПоДаннымОрганизации;
		НоваяСтрока.СуммаВалПоДаннымОрганизации = СуммаВалПоДаннымОрганизации;
		НоваяСтрока.НДСПоДаннымОрганизации =      НДСПоДаннымОрганизации;
		
		НоваяСтрока.ПервичныйДокументОрганизации = ПредставлениеДокументаПоТаблице(ТаблицаДокументовОрганизации);
		НоваяСтрока.ПервичныйДокументКонтрагента = ПредставлениеДокументаПоТаблице(ТаблицаДокументовКонтрагента);
		НоваяСтрока.СчетФактураОрганизации       = ПредставлениеДокументаПоТаблице(ТаблицаСФОрганизации);
		НоваяСтрока.СчетФактураКонтрагента       = ПредставлениеДокументаПоТаблице(ТаблицаСФКонтрагента);
		
	КонецЦикла;
	
	Возврат ТаблицаРасхождений;
	
КонецФункции

Функция ПолучитьЗначение(Знач ИсходноеЗначение, ТипЗначения = "", ЗначениеПоУмолчанию = Неопределено)
	
	Если НЕ ЗначениеЗаполнено(ИсходноеЗначение) Тогда
		Значение = ЗначениеПоУмолчанию;
	ИначеЕсли ТипЗначения = "СправочникСсылка.Контрагенты" Тогда
		СведенияОКонтрагенте = Новый Структура();
		СведенияОКонтрагенте.Вставить("Значение", Справочники.Контрагенты.ПустаяСсылка());
		СведенияОКонтрагенте.Вставить("ИННКонтрагента",      ""); // заполняется если не найден контрагент по ИНН
		СведенияОКонтрагенте.Вставить("КППКонтрагента",      ""); // заполняется если не найден контрагент по КПП
		СервисСверкиРасчетовПереопределяемый.СведенияКонтрагентаПоИНН_КПП(ИсходноеЗначение, СведенияОКонтрагенте);
		Возврат СведенияОКонтрагенте;
	ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("Структура") Тогда
		Значение = ИсходноеЗначение;
	ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("Массив") Тогда
		Значение = ИсходноеЗначение;
	ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("Дата") Тогда
		Значение = ИсходноеЗначение;
	ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("Строка") И ВРег(ИсходноеЗначение) = "NONE" Тогда
		Значение = ЗначениеПоУмолчанию;
	ИначеЕсли ТипЗначения = "" Тогда
		Значение = ИсходноеЗначение;
	ИначеЕсли ТипЗначения = "ДатаJSON" Тогда
		Значение = ПрочитатьДатуJSON(ИсходноеЗначение, ФорматДатыJSON.ISO);
	ИначеЕсли ТипЗначения = "Дата" Тогда
		Значение = СтрокаВДату(ИсходноеЗначение);
	ИначеЕсли ТипЗначения = "Число" Тогда
		Значение = СтрокаВЧисло(ИсходноеЗначение, ЗначениеПоУмолчанию);
	ИначеЕсли ТипЗначения = "ПеречислениеСсылка.СервисСверкиРасчетовВидыОпераций" Тогда
		Значение = Перечисления.СервисСверкиРасчетовВидыОпераций[ИсходноеЗначение];
	ИначеЕсли ТипЗначения = "УникальныйИдентификатор" И ЗначениеЗаполнено(ИсходноеЗначение) Тогда
		Значение = Новый УникальныйИдентификатор(ИсходноеЗначение);
	Иначе
		ТребуемыйТип	= Новый ОписаниеТипов(ТипЗначения);
		Значение		= ТребуемыйТип.ПривестиЗначение(ИсходноеЗначение);
	КонецЕсли;

	Если Значение = Неопределено Тогда
		Значение = ЗначениеПоУмолчанию;
	КонецЕсли;

	Возврат Значение;
	
КонецФункции

Функция НовыйТаблицаРасхождений()
	
	ТаблицаПараметров = Новый ТаблицаЗначений;
	ТаблицаПараметров.Колонки.Добавить("Контрагент",                             Метаданные.ОпределяемыеТипы.ОрганизацияКонтрагентEDI.Тип); 
	ТаблицаПараметров.Колонки.Добавить("ИННКонтрагента",                         Новый ОписаниеТипов("Строка"));
	ТаблицаПараметров.Колонки.Добавить("КППКонтрагента",                         Новый ОписаниеТипов("Строка"));
	ТаблицаПараметров.Колонки.Добавить("Организация",                            Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаПараметров.Колонки.Добавить("Дата",                                   Новый ОписаниеТипов("Дата"));
	ТаблицаПараметров.Колонки.Добавить("ИдентификаторСверкиПоОрганизации",       Новый ОписаниеТипов("УникальныйИдентификатор"));
	ТаблицаПараметров.Колонки.Добавить("ИдентификаторСверкиКонтрагенту",         Новый ОписаниеТипов("УникальныйИдентификатор"));
	ТаблицаПараметров.Колонки.Добавить("ИдентификаторРасхождения",               Новый ОписаниеТипов("Строка"));
	ТаблицаПараметров.Колонки.Добавить("СуммаПоДаннымКонтрагента",               РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	ТаблицаПараметров.Колонки.Добавить("СуммаВалПоДаннымКонтрагента",            РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	ТаблицаПараметров.Колонки.Добавить("НДСПоДаннымКонтрагента",                 РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	ТаблицаПараметров.Колонки.Добавить("СуммаПоДаннымОрганизации",               РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	ТаблицаПараметров.Колонки.Добавить("СуммаВалПоДаннымОрганизации",            РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	ТаблицаПараметров.Колонки.Добавить("НДСПоДаннымОрганизации",                 РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля());
	ТаблицаПараметров.Колонки.Добавить("МассивДокументовПоОрганизации",          Новый ОписаниеТипов("Массив"));
	ТаблицаПараметров.Колонки.Добавить("МассивДокументовПоКонтрагенту",          Новый ОписаниеТипов("Массив"));
	ТаблицаПараметров.Колонки.Добавить("КонтрагентНеПроизводилСверку",           Новый ОписаниеТипов("Булево"));
	ТаблицаПараметров.Колонки.Добавить("ПервичныйДокументОрганизации",           Новый ОписаниеТипов("Строка"));
	ТаблицаПараметров.Колонки.Добавить("ПервичныйДокументКонтрагента",           Новый ОписаниеТипов("Строка"));
	ТаблицаПараметров.Колонки.Добавить("СчетФактураОрганизации",                 Новый ОписаниеТипов("Строка"));
	ТаблицаПараметров.Колонки.Добавить("СчетФактураКонтрагента",                 Новый ОписаниеТипов("Строка"));
	
	Возврат ТаблицаПараметров;

КонецФункции

Функция НовыйТаблицаКонтрагентов()
	
	ТаблицаКонтрагентов = Новый ТаблицаЗначений;
	ТаблицаКонтрагентов.Колонки.Добавить("Контрагент", Метаданные.ОпределяемыеТипы.ОрганизацияКонтрагентEDI.Тип);
	ТаблицаКонтрагентов.Колонки.Добавить("НаименованиеКонтрагента",       Новый ОписаниеТипов("Строка"));
	ТаблицаКонтрагентов.Колонки.Добавить("НаименованиеКонтрагентаПолное", Новый ОписаниеТипов("Строка"));
	ТаблицаКонтрагентов.Колонки.Добавить("КППКонтрагента",                Новый ОписаниеТипов("Строка"));
	ТаблицаКонтрагентов.Колонки.Добавить("ИННКонтрагента",                Новый ОписаниеТипов("Строка"));
	ТаблицаКонтрагентов.Колонки.Добавить("ИдентификаторКонтрагента",      Новый ОписаниеТипов("Строка"));
	
	Возврат ТаблицаКонтрагентов;

КонецФункции

Функция НовыйСтруктураДокумента()
	
	ПараметрыДокумента = Новый Структура();
	ПараметрыДокумента.Вставить("ПредставлениеДокумента");
	ПараметрыДокумента.Вставить("ВидДокумента");
	ПараметрыДокумента.Вставить("Операция");
	ПараметрыДокумента.Вставить("ДатаИзменения");
	ПараметрыДокумента.Вставить("НомерДокумента");
	ПараметрыДокумента.Вставить("ДатаДокумента");
	ПараметрыДокумента.Вставить("НомерСчетФактуры");
	ПараметрыДокумента.Вставить("ДатаСчетФактуры");
	ПараметрыДокумента.Вставить("ИсходныйНомерДокумента1С");
	ПараметрыДокумента.Вставить("НДС");
	ПараметрыДокумента.Вставить("Сумма");
	ПараметрыДокумента.Вставить("СуммаВал");
	ПараметрыДокумента.Вставить("ИдентификаторДокумента");
	
	Возврат ПараметрыДокумента;
	
КонецФункции

Функция НовыйТаблицаОшибок()
	
	ТаблицаОшибок = Новый ТаблицаЗначений;
	ТаблицаОшибок.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаОшибок.Колонки.Добавить("Ошибки",      Новый ОписаниеТипов("Массив"));
	
	Возврат ТаблицаОшибок;
	
КонецФункции

Функция СтрокаВДату(Значение)
	
	Значение = СтрЗаменить(Значение, " ", "");
	Значение = СокрЛП(СтрЗаменить(Значение, ":", ""));
	Значение = СокрЛП(СтрЗаменить(Значение, "-", ""));
	Значение = СокрЛП(СтрЗаменить(Значение, "T", ""));
	Значение = СокрЛП(СтрЗаменить(Значение, ".", ""));
	
	ОписаниеТипа = Новый ОписаниеТипов("Дата");
	Результат = ОписаниеТипа.ПривестиЗначение(Значение);
	
	Возврат Результат;
	
КонецФункции

Функция СтрокаВЧисло(Значение, ЗначениеПоУмолчанию = Неопределено)
	
	Если ТипЗнч(Значение) = Тип("Число") Тогда
		Результат = Значение;
	ИначеЕсли ТипЗнч(Значение) = Тип("Строка") Тогда
		Результат = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(Значение);
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		Результат = ЗначениеПоУмолчанию;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция КлючОбсужденияСистемыВзаимодействияСверка(ИдентификаторПользователя)
	
	Возврат "ЕстьРасхожденияСверки" + Строка(ИдентификаторПользователя);
	
КонецФункции

Функция ЛичноеОбсуждениеПользователей(ИдентификаторПользователяСВ1, ИдентификаторПользователяСВ2)
	
	КлючЧата   = КлючОбсужденияСистемыВзаимодействияСверка(ИдентификаторПользователяСВ1);
	Отбор      = Новый ОтборОбсужденийСистемыВзаимодействия;
	Отбор.Ключ = КлючЧата;
	Отбор.ТекущийПользовательЯвляетсяУчастником = Истина;
	Чаты = СистемаВзаимодействия.ПолучитьОбсуждения(Отбор);
	
	Если Чаты.Количество() > 0 Тогда
		Возврат Чаты[0];
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЛичноеОбсуждениеПользователей = СистемаВзаимодействия.СоздатьОбсуждение();
	ЛичноеОбсуждениеПользователей.Групповое = Ложь;
	ЛичноеОбсуждениеПользователей.Ключ = КлючЧата;
	ЛичноеОбсуждениеПользователей.Участники.Добавить(ИдентификаторПользователяСВ1);
	ЛичноеОбсуждениеПользователей.Участники.Добавить(ИдентификаторПользователяСВ2);
	ЛичноеОбсуждениеПользователей.Записать();
	
	Возврат ЛичноеОбсуждениеПользователей;
	
КонецФункции

Функция ИдентификаторБотаВСистемеВзаимодействия() 
	
	ИдентификаторБота = Неопределено;
	Бот = Пользователи.СвойстваПользователяИБ(ИмяСлужебногоПользователя());
	Если Бот <> Неопределено Тогда
		ИдентификаторБота = ИдентификаторПользователяСистемыВзаимодействия(Бот.УникальныйИдентификатор);
	КонецЕсли;
	Если ИдентификаторБота = Неопределено Тогда
		Если Бот <> Неопределено Тогда
			Попытка
				ПользовательСистемыВзаимодействия = СистемаВзаимодействия.СоздатьПользователя(Бот.ПользовательИБ);
				ПользовательСистемыВзаимодействия.Картинка = БиблиотекаКартинок.СервисСверкиРасчетовЛогоМалое;
				ПользовательСистемыВзаимодействия.Записать();
				ИдентификаторБота = ПользовательСистемыВзаимодействия.Идентификатор;
			Исключение
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Создание пользователя системы взаимодействия';
												|en = 'Create collaboration system user'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ИдентификаторБота = Неопределено;
			КонецПопытки;
		КонецЕсли;
	Иначе
		ПользовательСистемыВзаимодействия = СистемаВзаимодействия.ПолучитьПользователя(ИдентификаторБота);
		Если ПользовательСистемыВзаимодействия.Картинка.Вид = ВидКартинки.Пустая Тогда
			ПользовательСистемыВзаимодействия.Картинка = БиблиотекаКартинок.СервисСверкиРасчетовЛогоМалое;
			ПользовательСистемыВзаимодействия.Записать();
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИдентификаторБота;
	
КонецФункции

Функция ИдентификаторПользователяСистемыВзаимодействия(ИдентификаторПользователяИБ) 
	
	Попытка
		Идентификатор = СистемаВзаимодействия.ПолучитьИдентификаторПользователя(ИдентификаторПользователяИБ);
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Получение идентификатора пользователя системы взаимодействия';
										|en = 'Receive collaboration system user ID'", ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Идентификатор = Неопределено;
	КонецПопытки;
	
	Возврат Идентификатор;
	
КонецФункции

Процедура УдалитьСтароеОбсуждение(ИдентификаторПользователя = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Ключ = КлючОбсужденияСистемыВзаимодействияСверка(ИдентификаторПользователя);
	
	Если Не СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		Возврат;
	КонецЕсли;
	
	Обсуждение = Неопределено;
	
	Попытка
		Обсуждение = СистемаВзаимодействия.ПолучитьОбсуждение(Ключ);
	Исключение
		ВидОперации = СтрШаблон("Поиск обсуждения ""%1"" системы взаимодействия по ключу", ИмяСервиса());
		ЭлектронноеВзаимодействие.ОбработатьОшибку(
			ВидОперации, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Если Обсуждение <> Неопределено Тогда
		Если Обсуждение.Участники.Количество() Тогда
			Обсуждение.Участники.Очистить();
			Попытка
				Обсуждение.Записать();
			Исключение
				ВидОперации = СтрШаблон("Очистка участников обсуждения ""%1""", ИмяСервиса());
				ЭлектронноеВзаимодействие.ОбработатьОшибку(
					ВидОперации, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ОбсуждениеПользователяСБотом(ИдентификаторПользователя)

	ПользовательСВ = ИдентификаторПользователяСистемыВзаимодействия(ИдентификаторПользователя);
	БотСВ = ИдентификаторБотаВСистемеВзаимодействия();
	
	Возврат ЛичноеОбсуждениеПользователей(ПользовательСВ, БотСВ);
	
КонецФункции

Процедура ОтправитьСообщенияОРасхождениях(Пользователи)
	
	// Описание пакетов запроса:
	// ПользователиКоторыхНеОповещать - пользователи, которые исключены в глобальной настройке или которые отказались от оповещений
	// ПользователиКоторыхОповещать - пользователи, которых нужно оповещать в соответствии с глобальной настройкой
	// ВсеПользователиДляРассылки - все пользователи, которым нужно отправить оповещения с исключениям по индивидуальным и глобальной настройкам
	// ИндивидуальныеНастройкиПользователей - настройки пользователей, которых с учетом ограничений нужно оповестить по их индивидуальным настройкам
	// ФильтрПоКонтрагентам - индивидуальные настройки по контрагентам
	// ФильтрПоОрганизациям - индивидуальные настройки по организациям
	// НовыеРасхождения - все новые расхождения
	// ИндивидуальныеРасхожденияПредварительная - предваритеьная таблица расхождений для каждого пользователя
	// УниверсальныеРасхожденияПредварительная - предваритеьная таблица расхождений для пользователей без индивидуальной настройки
	// Пакет[9]  - пользователи у которых нет индивидуальной настройки и которым нужно отправить оповещение в соответствии с глобальной настройкой
	// Пакет[10] - расхождения в соответствии с глобальной настройкой
	// Пакет[11]  - индивидуальные расхождения для каждого пользователя в соответствии с его настройкой
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователейПользователи.Пользователь КАК Пользователь
	|ПОМЕСТИТЬ ПользователиКоторыхНеОповещать
	|ИЗ
	|	Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей КАК СервисСверкиРасчетовНастройкиОповещенийПользователей
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей.Пользователи КАК СервисСверкиРасчетовНастройкиОповещенийПользователейПользователи
	|		ПО (СервисСверкиРасчетовНастройкиОповещенийПользователейПользователи.Ссылка = СервисСверкиРасчетовНастройкиОповещенийПользователей.Ссылка)
	|ГДЕ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ОтборПользователей = 1
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ПометкаУдаления = ЛОЖЬ
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ЭтоГлобальнаяНастройка = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.Пользователь
	|ИЗ
	|	Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей КАК СервисСверкиРасчетовНастройкиОповещенийПользователей
	|ГДЕ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ПометкаУдаления = ЛОЖЬ
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ЭтоГлобальнаяНастройка = ЛОЖЬ
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ОповещатьОРасхождениях = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователейПользователи.Пользователь КАК Пользователь
	|ПОМЕСТИТЬ ПользователиКоторыхОповещать
	|ИЗ
	|	Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей КАК СервисСверкиРасчетовНастройкиОповещенийПользователей
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей.Пользователи КАК СервисСверкиРасчетовНастройкиОповещенийПользователейПользователи
	|		ПО (СервисСверкиРасчетовНастройкиОповещенийПользователейПользователи.Ссылка = СервисСверкиРасчетовНастройкиОповещенийПользователей.Ссылка)
	|ГДЕ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ОтборПользователей = 2
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ПометкаУдаления = ЛОЖЬ
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ЭтоГлобальнаяНастройка = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Ссылка,
	|	Пользователи.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
	|ПОМЕСТИТЬ ВсеПользователиДляРассылки
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПользователиКоторыхНеОповещать КАК ПользователиКоторыхНеОповещать
	|		ПО (ПользователиКоторыхНеОповещать.Пользователь = Пользователи.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПользователиКоторыхОповещать КАК ПользователиКоторыхОповещать
	|		ПО (ПользователиКоторыхОповещать.Пользователь = Пользователи.Ссылка)
	|ГДЕ
	|	Пользователи.Ссылка В(&СписокПользователей)
	|	И ВЫБОР
	|			КОГДА &ЕстьФильтрТолькоПользователям
	|				ТОГДА НЕ ПользователиКоторыхОповещать.Пользователь ЕСТЬ NULL
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ПользователиКоторыхНеОповещать.Пользователь ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеПользователиДляРассылки.Ссылка КАК Пользователь,
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ОповещатьОРасхожденииСумм КАК ОповещатьОРасхожденииСумм,
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ОповещатьОРасхожденииПервичныхДокументов КАК ОповещатьОРасхожденииПервичныхДокументов,
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ОповещатьОРасхожденииСчетовФактур КАК ОповещатьОРасхожденииСчетовФактур,
	|	ВсеПользователиДляРассылки.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ,
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.Ссылка КАК Ссылка,
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ОтборКонтрагентов КАК ОтборКонтрагентов,
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ОтборОрганизаций КАК ОтборОрганизаций,
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.КоличествоДнейБезДокумента КАК КоличествоДнейБезДокумента,
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ОповещатьОбУстраненииРасхождений КАК ОповещатьОбУстраненииРасхождений
	|ПОМЕСТИТЬ ИндивидуальныеНастройкиПользователей
	|ИЗ
	|	ВсеПользователиДляРассылки КАК ВсеПользователиДляРассылки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей КАК СервисСверкиРасчетовНастройкиОповещенийПользователей
	|		ПО (СервисСверкиРасчетовНастройкиОповещенийПользователей.Пользователь = ВсеПользователиДляРассылки.Ссылка)
	|ГДЕ
	|	НЕ СервисСверкиРасчетовНастройкиОповещенийПользователей.Ссылка ЕСТЬ NULL
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ЭтоГлобальнаяНастройка = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователейКонтрагенты.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовНастройкиОповещенийПользователейКонтрагенты.Ссылка КАК Ссылка,
	|	СервисСверкиРасчетовНастройкиОповещенийПользователейКонтрагенты.Ссылка.ЭтоГлобальнаяНастройка КАК ЭтоГлобальнаяНастройка
	|ПОМЕСТИТЬ ФильтрПоКонтрагентам
	|ИЗ
	|	Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей.Контрагенты КАК СервисСверкиРасчетовНастройкиОповещенийПользователейКонтрагенты
	|ГДЕ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователейКонтрагенты.Ссылка.ПометкаУдаления = ЛОЖЬ
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователейКонтрагенты.Ссылка.ЭтоГлобальнаяНастройка = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователейОрганизации.Организация КАК Организация,
	|	СервисСверкиРасчетовНастройкиОповещенийПользователейОрганизации.Ссылка КАК Ссылка,
	|	СервисСверкиРасчетовНастройкиОповещенийПользователейОрганизации.Ссылка.ЭтоГлобальнаяНастройка КАК ЭтоГлобальнаяНастройка
	|ПОМЕСТИТЬ ФильтрПоОрганизации
	|ИЗ
	|	Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей.Организации КАК СервисСверкиРасчетовНастройкиОповещенийПользователейОрганизации
	|ГДЕ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователейОрганизации.Ссылка.ПометкаУдаления = ЛОЖЬ
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователейОрганизации.Ссылка.ЭтоГлобальнаяНастройка = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация КАК Организация,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Документ КАК Документ,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Дата КАК Дата,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации КАК ИдентификаторСверкиПоОрганизации,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту КАК ИдентификаторСверкиКонтрагенту,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ПервичныйДокументОрганизации КАК ПервичныйДокументОрганизации,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ПервичныйДокументКонтрагента КАК ПервичныйДокументКонтрагента,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.СчетФактураОрганизации КАК СчетФактураОрганизации,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.СчетФактураКонтрагента КАК СчетФактураКонтрагента,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации КАК СуммаПоДаннымОрганизации,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымКонтрагента КАК СуммаПоДаннымКонтрагента,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации КАК НДСПоДаннымОрганизации,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымКонтрагента КАК НДСПоДаннымКонтрагента,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаВалПоДаннымОрганизации КАК СуммаВалПоДаннымОрганизации,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаВалПоДаннымКонтрагента КАК СуммаВалПоДаннымКонтрагента,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИННКонтрагента КАК ИННКонтрагента,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.КППКонтрагента КАК КППКонтрагента,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку КАК КонтрагентНеПроизводилСверку,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ПредставлениеДокументовОрганизации КАК ПредставлениеДокументовОрганизации,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ПредставлениеДокументовКонтрагентов КАК ПредставлениеДокументовКонтрагентов,
	|	(СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации <> СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымКонтрагента
	|		ИЛИ СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации <> СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымКонтрагента)
	|		И СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации <> &ПустойИдентификатор
	|		И СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту <> &ПустойИдентификатор КАК ОтличаетсяСумма,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ПервичныйДокументОрганизации <> СервисСверкиРасчетовОбнаруженныеРасхождения.ПервичныйДокументКонтрагента
	|		И СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации <> &ПустойИдентификатор
	|		И СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту <> &ПустойИдентификатор КАК ОтличаетсяПервичныйДокумент,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.СчетФактураОрганизации <> СервисСверкиРасчетовОбнаруженныеРасхождения.СчетФактураКонтрагента
	|		И СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации <> &ПустойИдентификатор
	|		И СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту <> &ПустойИдентификатор КАК ОтличаетсяСчетФактура,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации = &ПустойИдентификатор
	|		ИЛИ СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = &ПустойИдентификатор КАК ОтсутствуетДокумент
	|ПОМЕСТИТЬ НовыеРасхождения
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовСтатусыРасхождений КАК СервисСверкиРасчетовСтатусыРасхождений
	|		ПО СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторРасхождения = СервисСверкиРасчетовСтатусыРасхождений.ИдентификаторРасхождения
	|ГДЕ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку = ЛОЖЬ
	|	И СервисСверкиРасчетовСтатусыРасхождений.ЭтоНовоеРасхождение = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИндивидуальныеНастройкиПользователей.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ,
	|	НовыеРасхождения.Организация КАК Организация,
	|	НовыеРасхождения.Контрагент КАК Контрагент,
	|	НовыеРасхождения.Документ КАК Документ,
	|	НовыеРасхождения.Дата КАК Дата,
	|	НовыеРасхождения.ИдентификаторСверкиПоОрганизации КАК ИдентификаторСверкиПоОрганизации,
	|	НовыеРасхождения.ИдентификаторСверкиКонтрагенту КАК ИдентификаторСверкиКонтрагенту,
	|	НовыеРасхождения.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	НовыеРасхождения.ПервичныйДокументОрганизации КАК ПервичныйДокументОрганизации,
	|	НовыеРасхождения.ПервичныйДокументКонтрагента КАК ПервичныйДокументКонтрагента,
	|	НовыеРасхождения.СчетФактураОрганизации КАК СчетФактураОрганизации,
	|	НовыеРасхождения.СчетФактураКонтрагента КАК СчетФактураКонтрагента,
	|	НовыеРасхождения.СуммаПоДаннымОрганизации КАК СуммаПоДаннымОрганизации,
	|	НовыеРасхождения.СуммаПоДаннымКонтрагента КАК СуммаПоДаннымКонтрагента,
	|	НовыеРасхождения.НДСПоДаннымОрганизации КАК НДСПоДаннымОрганизации,
	|	НовыеРасхождения.НДСПоДаннымКонтрагента КАК НДСПоДаннымКонтрагента,
	|	НовыеРасхождения.СуммаВалПоДаннымОрганизации КАК СуммаВалПоДаннымОрганизации,
	|	НовыеРасхождения.СуммаВалПоДаннымКонтрагента КАК СуммаВалПоДаннымКонтрагента,
	|	НовыеРасхождения.ИННКонтрагента КАК ИННКонтрагента,
	|	НовыеРасхождения.КППКонтрагента КАК КППКонтрагента,
	|	НовыеРасхождения.КонтрагентНеПроизводилСверку КАК КонтрагентНеПроизводилСверку,
	|	НовыеРасхождения.ПредставлениеДокументовОрганизации КАК ПредставлениеДокументовОрганизации,
	|	НовыеРасхождения.ПредставлениеДокументовКонтрагентов КАК ПредставлениеДокументовКонтрагентов,
	|	НовыеРасхождения.ОтличаетсяСумма КАК ОтличаетсяСумма,
	|	НовыеРасхождения.ОтличаетсяПервичныйДокумент КАК ОтличаетсяПервичныйДокумент,
	|	НовыеРасхождения.ОтличаетсяСчетФактура КАК ОтличаетсяСчетФактура,
	|	НовыеРасхождения.ОтсутствуетДокумент КАК ОтсутствуетДокумент
	|ПОМЕСТИТЬ ИндивидуальныеРасхожденияПредварительная
	|ИЗ
	|	ИндивидуальныеНастройкиПользователей КАК ИндивидуальныеНастройкиПользователей
	|		ЛЕВОЕ СОЕДИНЕНИЕ НовыеРасхождения КАК НовыеРасхождения
	|		ПО (ИндивидуальныеНастройкиПользователей.ОповещатьОРасхожденииСумм
	|					И НовыеРасхождения.ОтличаетсяСумма
	|				ИЛИ ИндивидуальныеНастройкиПользователей.ОповещатьОРасхожденииПервичныхДокументов
	|					И НовыеРасхождения.ОтличаетсяПервичныйДокумент
	|				ИЛИ ИндивидуальныеНастройкиПользователей.ОповещатьОРасхожденииСчетовФактур
	|					И НовыеРасхождения.ОтличаетсяСчетФактура
	|				ИЛИ НовыеРасхождения.ОтсутствуетДокумент
	|					И ИндивидуальныеНастройкиПользователей.КоличествоДнейБезДокумента > 0
	|					И РАЗНОСТЬДАТ(НовыеРасхождения.Дата, &ТекущаяДата, ДЕНЬ) > ИндивидуальныеНастройкиПользователей.КоличествоДнейБезДокумента)
	|ГДЕ
	|	НЕ НовыеРасхождения.Организация ЕСТЬ NULL
	|	И ВЫБОР
	|			КОГДА ИндивидуальныеНастройкиПользователей.ОтборКонтрагентов = 2
	|				ТОГДА НовыеРасхождения.Контрагент В
	|						(ВЫБРАТЬ
	|							ФильтрПоКонтрагентам.Контрагент
	|						ИЗ
	|							ФильтрПоКонтрагентам
	|						ГДЕ
	|							ФильтрПоКонтрагентам.Ссылка = ИндивидуальныеНастройкиПользователей.Ссылка)
	|			КОГДА ИндивидуальныеНастройкиПользователей.ОтборКонтрагентов = 1
	|				ТОГДА НЕ НовыеРасхождения.Контрагент В
	|							(ВЫБРАТЬ
	|								ФильтрПоКонтрагентам.Контрагент
	|							ИЗ
	|								ФильтрПоКонтрагентам
	|							ГДЕ
	|								ФильтрПоКонтрагентам.Ссылка = ИндивидуальныеНастройкиПользователей.Ссылка)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА ИндивидуальныеНастройкиПользователей.ОтборОрганизаций = 2
	|				ТОГДА НовыеРасхождения.Организация В
	|						(ВЫБРАТЬ
	|							ФильтрПоОрганизации.Организация
	|						ИЗ
	|							ФильтрПоОрганизации
	|						ГДЕ
	|							ФильтрПоОрганизации.Ссылка = ИндивидуальныеНастройкиПользователей.Ссылка)
	|			КОГДА ИндивидуальныеНастройкиПользователей.ОтборОрганизаций = 1
	|				ТОГДА НЕ НовыеРасхождения.Организация В
	|							(ВЫБРАТЬ
	|								ФильтрПоОрганизации.Организация
	|							ИЗ
	|								ФильтрПоОрганизации
	|							ГДЕ
	|								ФильтрПоОрганизации.Ссылка = ИндивидуальныеНастройкиПользователей.Ссылка)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеРасхождения.Организация КАК Организация,
	|	НовыеРасхождения.Контрагент КАК Контрагент,
	|	НовыеРасхождения.Документ КАК Документ,
	|	НовыеРасхождения.Дата КАК Дата,
	|	НовыеРасхождения.ИдентификаторСверкиПоОрганизации КАК ИдентификаторСверкиПоОрганизации,
	|	НовыеРасхождения.ИдентификаторСверкиКонтрагенту КАК ИдентификаторСверкиКонтрагенту,
	|	НовыеРасхождения.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	НовыеРасхождения.ПервичныйДокументОрганизации КАК ПервичныйДокументОрганизации,
	|	НовыеРасхождения.ПервичныйДокументКонтрагента КАК ПервичныйДокументКонтрагента,
	|	НовыеРасхождения.СчетФактураОрганизации КАК СчетФактураОрганизации,
	|	НовыеРасхождения.СчетФактураКонтрагента КАК СчетФактураКонтрагента,
	|	НовыеРасхождения.СуммаПоДаннымОрганизации КАК СуммаПоДаннымОрганизации,
	|	НовыеРасхождения.СуммаПоДаннымКонтрагента КАК СуммаПоДаннымКонтрагента,
	|	НовыеРасхождения.НДСПоДаннымОрганизации КАК НДСПоДаннымОрганизации,
	|	НовыеРасхождения.НДСПоДаннымКонтрагента КАК НДСПоДаннымКонтрагента,
	|	НовыеРасхождения.СуммаВалПоДаннымОрганизации КАК СуммаВалПоДаннымОрганизации,
	|	НовыеРасхождения.СуммаВалПоДаннымКонтрагента КАК СуммаВалПоДаннымКонтрагента,
	|	НовыеРасхождения.ИННКонтрагента КАК ИННКонтрагента,
	|	НовыеРасхождения.КППКонтрагента КАК КППКонтрагента,
	|	НовыеРасхождения.КонтрагентНеПроизводилСверку КАК КонтрагентНеПроизводилСверку,
	|	НовыеРасхождения.ПредставлениеДокументовОрганизации КАК ПредставлениеДокументовОрганизации,
	|	НовыеРасхождения.ПредставлениеДокументовКонтрагентов КАК ПредставлениеДокументовКонтрагентов,
	|	НовыеРасхождения.ОтличаетсяСумма КАК ОтличаетсяСумма,
	|	НовыеРасхождения.ОтличаетсяПервичныйДокумент КАК ОтличаетсяПервичныйДокумент,
	|	НовыеРасхождения.ОтличаетсяСчетФактура КАК ОтличаетсяСчетФактура,
	|	НовыеРасхождения.ОтсутствуетДокумент КАК ОтсутствуетДокумент
	|ПОМЕСТИТЬ УниверсальныеРасхожденияПредварительная
	|ИЗ
	|	НовыеРасхождения КАК НовыеРасхождения
	|ГДЕ
	|	ВЫБОР
	|			КОГДА НовыеРасхождения.ОтсутствуетДокумент
	|				ТОГДА РАЗНОСТЬДАТ(НовыеРасхождения.Дата, &ТекущаяДата, ДЕНЬ) > 30
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеПользователиДляРассылки.Ссылка КАК Ссылка,
	|	ВсеПользователиДляРассылки.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
	|ИЗ
	|	ВсеПользователиДляРассылки КАК ВсеПользователиДляРассылки
	|ГДЕ
	|	НЕ ВсеПользователиДляРассылки.Ссылка В
	|				(ВЫБРАТЬ
	|					ИндивидуальныеНастройкиПользователей.Пользователь
	|				ИЗ
	|					ИндивидуальныеНастройкиПользователей)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1200
	|	УниверсальныеРасхожденияПредварительная.Организация КАК Организация,
	|	УниверсальныеРасхожденияПредварительная.Контрагент КАК Контрагент,
	|	УниверсальныеРасхожденияПредварительная.Документ КАК Документ,
	|	УниверсальныеРасхожденияПредварительная.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	ВЫБОР
	|		КОГДА УниверсальныеРасхожденияПредварительная.СуммаПоДаннымОрганизации = 0
	|			ТОГДА УниверсальныеРасхожденияПредварительная.СуммаПоДаннымКонтрагента
	|		ИНАЧЕ УниверсальныеРасхожденияПредварительная.СуммаПоДаннымОрганизации
	|	КОНЕЦ КАК СуммаДокумента,
	|	ВЫБОР
	|		КОГДА УниверсальныеРасхожденияПредварительная.НДСПоДаннымОрганизации = 0
	|			ТОГДА УниверсальныеРасхожденияПредварительная.НДСПоДаннымКонтрагента
	|		ИНАЧЕ УниверсальныеРасхожденияПредварительная.НДСПоДаннымОрганизации
	|	КОНЕЦ КАК НДСДокумента,
	|	УниверсальныеРасхожденияПредварительная.ОтличаетсяСумма КАК ОтличаетсяСумма,
	|	УниверсальныеРасхожденияПредварительная.ОтличаетсяПервичныйДокумент КАК ОтличаетсяПервичныйДокумент,
	|	УниверсальныеРасхожденияПредварительная.ОтличаетсяСчетФактура КАК ОтличаетсяСчетФактура,
	|	УниверсальныеРасхожденияПредварительная.ОтсутствуетДокумент КАК ОтсутствуетДокумент,
	|	1 КАК КоличествоДокументов,
	|	УниверсальныеРасхожденияПредварительная.ПервичныйДокументОрганизации КАК ПервичныйДокументОрганизации,
	|	УниверсальныеРасхожденияПредварительная.ПервичныйДокументКонтрагента КАК ПервичныйДокументКонтрагента,
	|	УниверсальныеРасхожденияПредварительная.ИННКонтрагента КАК ИННКонтрагента,
	|	УниверсальныеРасхожденияПредварительная.КППКонтрагента КАК КППКонтрагента
	|ИЗ
	|	УниверсальныеРасхожденияПредварительная КАК УниверсальныеРасхожденияПредварительная
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОтличаетсяСумма УБЫВ,
	|	СуммаДокумента УБЫВ
	|ИТОГИ
	|	МАКСИМУМ(Документ),
	|	МАКСИМУМ(ИдентификаторРасхождения),
	|	СУММА(СуммаДокумента),
	|	СУММА(НДСДокумента),
	|	МАКСИМУМ(ОтличаетсяСумма),
	|	МАКСИМУМ(ОтличаетсяПервичныйДокумент),
	|	МАКСИМУМ(ОтличаетсяСчетФактура),
	|	МАКСИМУМ(ОтсутствуетДокумент),
	|	СУММА(КоличествоДокументов),
	|	МАКСИМУМ(ПервичныйДокументОрганизации),
	|	МАКСИМУМ(ПервичныйДокументКонтрагента),
	|	МАКСИМУМ(ИННКонтрагента),
	|	МАКСИМУМ(КППКонтрагента)
	|ПО
	|	ОБЩИЕ,
	|	Организация,
	|	Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1200
	|	ИндивидуальныеРасхожденияПредварительная.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ,
	|	ИндивидуальныеРасхожденияПредварительная.Организация КАК Организация,
	|	ИндивидуальныеРасхожденияПредварительная.Контрагент КАК Контрагент,
	|	ИндивидуальныеРасхожденияПредварительная.Документ КАК Документ,
	|	ИндивидуальныеРасхожденияПредварительная.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	ВЫБОР
	|		КОГДА ИндивидуальныеРасхожденияПредварительная.СуммаПоДаннымОрганизации = 0
	|			ТОГДА ИндивидуальныеРасхожденияПредварительная.СуммаПоДаннымКонтрагента
	|		ИНАЧЕ ИндивидуальныеРасхожденияПредварительная.СуммаПоДаннымОрганизации
	|	КОНЕЦ КАК СуммаДокумента,
	|	ВЫБОР
	|		КОГДА ИндивидуальныеРасхожденияПредварительная.НДСПоДаннымОрганизации = 0
	|			ТОГДА ИндивидуальныеРасхожденияПредварительная.НДСПоДаннымКонтрагента
	|		ИНАЧЕ ИндивидуальныеРасхожденияПредварительная.НДСПоДаннымОрганизации
	|	КОНЕЦ КАК НДСДокумента,
	|	ИндивидуальныеРасхожденияПредварительная.ОтличаетсяСумма КАК ОтличаетсяСумма,
	|	ИндивидуальныеРасхожденияПредварительная.ОтличаетсяПервичныйДокумент КАК ОтличаетсяПервичныйДокумент,
	|	ИндивидуальныеРасхожденияПредварительная.ОтличаетсяСчетФактура КАК ОтличаетсяСчетФактура,
	|	ИндивидуальныеРасхожденияПредварительная.ОтсутствуетДокумент КАК ОтсутствуетДокумент,
	|	1 КАК КоличествоДокументов,
	|	ИндивидуальныеРасхожденияПредварительная.ПервичныйДокументОрганизации КАК ПервичныйДокументОрганизации,
	|	ИндивидуальныеРасхожденияПредварительная.ПервичныйДокументКонтрагента КАК ПервичныйДокументКонтрагента,
	|	ИндивидуальныеРасхожденияПредварительная.ИННКонтрагента КАК ИННКонтрагента,
	|	ИндивидуальныеРасхожденияПредварительная.КППКонтрагента КАК КППКонтрагента
	|ИЗ
	|	ИндивидуальныеРасхожденияПредварительная КАК ИндивидуальныеРасхожденияПредварительная
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОтличаетсяСумма УБЫВ,
	|	СуммаДокумента УБЫВ
	|ИТОГИ
	|	МАКСИМУМ(Документ),
	|	МАКСИМУМ(ИдентификаторРасхождения),
	|	СУММА(СуммаДокумента),
	|	СУММА(НДСДокумента),
	|	МАКСИМУМ(ОтличаетсяСумма),
	|	МАКСИМУМ(ОтличаетсяПервичныйДокумент),
	|	МАКСИМУМ(ОтличаетсяСчетФактура),
	|	МАКСИМУМ(ОтсутствуетДокумент),
	|	СУММА(КоличествоДокументов),
	|	МАКСИМУМ(ПервичныйДокументОрганизации),
	|	МАКСИМУМ(ПервичныйДокументКонтрагента),
	|	МАКСИМУМ(ИННКонтрагента),
	|	МАКСИМУМ(КППКонтрагента)
	|ПО
	|	ИдентификаторПользователяИБ,
	|	Организация,
	|	Контрагент";
	
	Запрос.УстановитьПараметр("ПустойИдентификатор", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	Запрос.УстановитьПараметр("СписокПользователей", Пользователи); 
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("ЕстьФильтрТолькоПользователям", Справочники.СервисСверкиРасчетовНастройкиОповещенийПользователей.ЕстьФильтрПользователей(2));
	
	Результат = Запрос.ВыполнитьПакет();
	
	УстраненныеРасхожденияПоПользователям = УстраненныеРасхожденияПоПользователям(Запрос.МенеджерВременныхТаблиц);
	
	// Отправим оповещения пользователям с индивидуальной настройкой.
	ВыборкаПоПользователям = Результат[11].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоПользователям.Следующий() Цикл
		
		ВыборкаПоОрганизациям = ВыборкаПоПользователям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
		ПользовательВсегоРасхожденийУстранено = ВыборкаПоПользователям.КоличествоДокументов;
		ПользовательВсегоСумма                = ВыборкаПоПользователям.СуммаДокумента;
		ПользовательВсегоНДС                  = ВыборкаПоПользователям.НДСДокумента;
	
		СообщениеДляПользователя    = СообщениеОНовыхРасхожденияхДляПользователя(ВыборкаПоОрганизациям, ПользовательВсегоНДС, ПользовательВсегоРасхожденийУстранено, ПользовательВсегоСумма);
		ИдентификаторПользователяИБ = ВыборкаПоПользователям.ИдентификаторПользователяИБ;
		ДополнитьСообщениеУстраненнымиРасхождениямиИОтправить(ИдентификаторПользователяИБ, СообщениеДляПользователя, УстраненныеРасхожденияПоПользователям);
		
	КонецЦикла;
	
	// Отправим оповещения пользователям без индивидуальной настройки.
	ПользователиДляУниверсальнойРассылки = Результат[9].Выбрать();
	Если ПользователиДляУниверсальнойРассылки.Количество() > 0 Тогда
		
		ВыборкаОбщая = Результат[10].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Если ВыборкаОбщая.Следующий() Тогда
			ПользовательВсегоРасхожденийУстранено = ВыборкаОбщая.КоличествоДокументов;
			ПользовательВсегоСумма                = ВыборкаОбщая.СуммаДокумента;
			ПользовательВсегоНДС                  = ВыборкаОбщая.НДСДокумента;
			
			ВыборкаПоОрганизациям = ВыборкаОбщая.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			СообщениеДляПользователя = СообщениеОНовыхРасхожденияхДляПользователя(ВыборкаПоОрганизациям, ПользовательВсегоНДС, ПользовательВсегоРасхожденийУстранено, ПользовательВсегоСумма);
			Пока ПользователиДляУниверсальнойРассылки.Следующий() Цикл
				ИдентификаторПользователяИБ = ПользователиДляУниверсальнойРассылки.ИдентификаторПользователяИБ;
				ДополнитьСообщениеУстраненнымиРасхождениямиИОтправить(ИдентификаторПользователяИБ, СообщениеДляПользователя, УстраненныеРасхожденияПоПользователям);
			КонецЦикла; 
		КонецЕсли;
		
	КонецЕсли;
	
	// Оповестим пользователей, по которым в сообщении только устраненные расхождения.
	Для Каждого Пользователь Из УстраненныеРасхожденияПоПользователям Цикл
		ЧастиСообщения = Новый Массив;
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Обнаружены устраненные расхождения на %1';
				|en = 'Eliminated discrepancies are detected as of %1'"), Формат(ТекущаяДатаСеанса(), "ДЛФ=Д"));
		ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(ТекстСообщения, Новый Шрифт(Новый Шрифт,, 14)));
		ЧастиСообщения.Добавить(Символы.ПС);
		ЧастиСообщения.Добавить(Символы.ПС);
		ЧастиСообщения.Добавить(Пользователь.Значение);
		ИтоговоеСообщение = Новый ФорматированнаяСтрока(ЧастиСообщения);
		Действия = Новый СписокЗначений;
		Действия.Добавить("ПоказатьНастройки", "Настроить оповещения"); 
		ОтправитьСообщениеСистемыВзаимодействия(Пользователь.Ключ, ИтоговоеСообщение, Действия, Неопределено);
	КонецЦикла;

	// Очистим регистры, необходимые для отслеживания новых и устраненных расхождений.
	РегистрыСведений.СервисСверкиРасчетовСтатусыРасхождений.ОчиститьСтатусыРасхождений();
	РегистрыСведений.СервисСверкиРасчетовУстраненныеРасхождения.ОчиститьУстраненныеРасхождения();
	
КонецПроцедуры

Процедура ОтправитьСообщениеСистемыВзаимодействия(ИдентификаторПользователяИБ, ТекстСообщения, Действия, ИдентификаторыРасхождений)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Получатель = ИдентификаторПользователяСистемыВзаимодействия(ИдентификаторПользователяИБ);
	Если Получатель = Неопределено Тогда
		Возврат; // Пользователь не зарегистрирован в системе взаимодействия.
	КонецЕсли;
	
	УдалитьСтароеОбсуждение(ИдентификаторПользователяИБ);
	
	Обсуждение = ОбсуждениеПользователяСБотом(ИдентификаторПользователяИБ);
	
	Если Обсуждение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторБота = ИдентификаторБотаВСистемеВзаимодействия();
	Если ИдентификаторБота = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Сообщение = СистемаВзаимодействия.СоздатьСообщение(Обсуждение.Идентификатор);
	
	Сообщение.Автор  = ИдентификаторБота;
	Сообщение.Текст  = ТекстСообщения;
	Сообщение.Данные = ИдентификаторыРасхождений;
	Сообщение.Получатели.Добавить(Получатель);
	Для каждого Действие Из Действия Цикл
		Сообщение.Действия.Добавить(Действие.Значение, Действие.Представление);
	КонецЦикла;
	
	Сообщение.Записать();
	
КонецПроцедуры

Процедура ПереподключитьОрганизацииКСервису()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОрганизацииБизнесСеть.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|ГДЕ
	|	ОрганизацииБизнесСеть.ТребуетсяПовторноеПодключение";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ОрганизацииКПереподключению = Результат.Выгрузить();
	СписокОрганизаций = ОрганизацииКПереподключению.ВыгрузитьКолонку("Организация");
	
	Для Каждого СтрокаТаблицы Из ОрганизацииКПереподключению Цикл
		РезультатПодключенияОрганизациюКСервису = СервисСверкиРасчетовМенеджерОбмена.РезультатПодключенияОрганизациюКСервису(СтрокаТаблицы.Организация);
		Если ЗначениеЗаполнено(РезультатПодключенияОрганизациюКСервису.ТекстОшибки) Тогда
			МетаданныеОбъекта = Метаданные.Справочники.Организации;
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось обновить данные авторизации по организации %1 по причине: %2';
										|en = 'Cannot update the authorization data for the %1 company. Reason: %2'"),
				СтрокаТаблицы.Организация, РезультатПодключенияОрганизациюКСервису.ТекстОшибки);
			ЗаписьЖурналаРегистрации(ИмяСервиса(), УровеньЖурналаРегистрации.Ошибка, МетаданныеОбъекта,, ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ИмяКлючаКэшДанныхОРелевантныхКонтрагентах()
	
	Возврат "СервисСверкиРасчетовКэшДанныхОРелевантныхКонтрагентах";

КонецФункции

// Формирует список релевантных контрагентов информационной базы, подключенных к сервису
//
// Возвращаемое значение: 
//   Неопределено - список контрагентов пуст.
//   Структура с ключами
//   * РелевантныеКонтрагенты - ТаблицаЗначений - содержит перечень всех релевантных контрагентов, подключенных к сервису
//      * Контрагент - СправочникСсылка.Контрагенты
//      * КоличествоДокументов - Число
//      * Оборот - Число
//   * КоличествоПодключенныхКонтрагентов - Число - количество всех подключенных к сервису контрагентов.
Функция ДанныеОРелевантныхКонтрагентах()
	
	Возврат СервисСверкиРасчетовПереопределяемый.ДанныеОРелевантныхКонтрагентах();
	
КонецФункции

Функция ВключеныОповещенияСверки()
	
	Возврат СервисСверкиРасчетовВызовСервера.ОповещенияСверкиВключены();

КонецФункции

Функция НовыйТаблицаПредставленияДокумента() 
	
	ТаблицаПредставлениеДокумента= Новый ТаблицаЗначений();
	ТаблицаПредставлениеДокумента.Колонки.Добавить("Номер", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаПредставлениеДокумента.Колонки.Добавить("Дата", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	
	Возврат ТаблицаПредставлениеДокумента;
	
КонецФункции

Функция ПредставлениеДокументаПоТаблице(ТаблицаДокументов)
	
	ТаблицаДокументов.Свернуть("Номер, Дата");
	ШаблонПредставления = НСтр("ru = '%1 от %2 г.';
								|en = '%1 dated %2'"); 
	ПредставлениеДокумента = "";
	Для Каждого ТекущийДокумент из ТаблицаДокументов Цикл
		Если НЕ ЗначениеЗаполнено(ТекущийДокумент.Номер)
			И НЕ  ЗначениеЗаполнено(ТекущийДокумент.Дата) Тогда
			Продолжить;
		КонецЕсли;
		ТекущийДокументОбОтгрузке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
			ТекущийДокумент.Номер,
			Формат(ТекущийДокумент.Дата,"ДЛФ=Д"));
		Если ЗначениеЗаполнено(ПредставлениеДокумента) Тогда
			ПредставлениеДокумента = "; " + ТекущийДокументОбОтгрузке; 
		Иначе
			ПредставлениеДокумента = ТекущийДокументОбОтгрузке;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПредставлениеДокумента;
	
КонецФункции

Функция ОповеститьПользователейОНаличииРасхождений()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пользователи.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ,
	|	Пользователи.Наименование КАК Наименование,
	|	Пользователи.Ссылка КАК Пользователь
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.Служебный = ЛОЖЬ
	|	И Пользователи.Недействителен = ЛОЖЬ
	|	И Пользователи.ПометкаУдаления = ЛОЖЬ
	|	И Пользователи.Ссылка <> &ПользовательНеУказан";
	
	Запрос.УстановитьПараметр("ПользовательНеУказан", Пользователи.СсылкаНеуказанногоПользователя());
	
	ТаблицаВыборки = Запрос.Выполнить().Выгрузить();
	ТаблицаПользователей = ТаблицаВыборки.СкопироватьКолонки();
	Для каждого СтрокаВыборки Из ТаблицаВыборки Цикл
		Если СервисСверкиРасчетовПереопределяемый.ОповещатьПользователяОРасхождениях(СтрокаВыборки.Пользователь) Тогда
			НоваяСтрока = ТаблицаПользователей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаВыборки);
		КонецЕсли;
	КонецЦикла;
	
	ОтправитьСообщенияОРасхождениях(ТаблицаПользователей.ВыгрузитьКолонку("Пользователь"));

КонецФункции

Процедура ДополнитьСообщениеУстраненнымиРасхождениямиИОтправить(ИдентификаторПользователяИБ, СообщениеДляПользователя, УстраненныеРасхожденияПоПользователям)
	
	Действия = Новый СписокЗначений;
	Действия.Добавить("ПоказатьРасхождения", "Подробнее");
	Действия.Добавить("ПоказатьНастройки", "Настроить оповещения");
	
	ЧастиСообщения = Новый Массив;
	ЧастиСообщения.Добавить(СообщениеДляПользователя.Сообщение);
	СообщениеОбУстраненныхРасхожденийДляПользователя = УстраненныеРасхожденияПоПользователям.Получить(ИдентификаторПользователяИБ);
	Если СообщениеОбУстраненныхРасхожденийДляПользователя <> Неопределено Тогда
		ЧастиСообщения.Добавить(Символы.ПС);
		ЧастиСообщения.Добавить(СообщениеОбУстраненныхРасхожденийДляПользователя);
		УстраненныеРасхожденияПоПользователям.Удалить(ИдентификаторПользователяИБ);
	КонецЕсли;
	ИтоговоеСообщение = Новый ФорматированнаяСтрока(ЧастиСообщения);
	
	ОтправитьСообщениеСистемыВзаимодействия(ИдентификаторПользователяИБ, ИтоговоеСообщение, Действия, СообщениеДляПользователя.ИдентификаторыРасхождений);
	
КонецПроцедуры

Функция СообщениеОНовыхРасхожденияхДляПользователя(ВыборкаПоОрганизациям, ПользовательВсегоНДС, ПользовательВсегоРасхождений, ПользовательВсегоСумма)
	
	ЛимитСообщения = 3; // не больше 3 организаций, 3 контрагентов по организации, 3 расхождений по контрагенту
	ОбщийЛимитРасхождений = 5; // максимальное количество расхождений в сообщении, остальное в "еще"
	Сообщение                 = "";
	ИдентификаторыРасхождений = Новый Массив();
	
	ЧастиСообщения = Новый Массив;
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru = 'Обнаружены расхождения на %1';
		|en = 'Discrepancies are detected as of %1'"), Формат(ТекущаяДатаСеанса(), "ДЛФ=Д"));
	ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(ТекстСообщения, Новый Шрифт(Новый Шрифт,, 14)));
	ЧастиСообщения.Добавить(Символы.ПС);
	ЧастиСообщения.Добавить(Символы.ПС);
	
	СчетчикРасхожденийПоПользователю     = 0;
	СчетчикСуммРасхожденийПоПользователю = 0;
	СчетчикНДСРасхожденийПоПользователю  = 0;
	
	СчетчикПоказанныхРасхождений         = 0;
	
	ОбщийСчетчикРасхождений         = 0;
	ОбщийСчетчикСуммРасхождений     = 0;
	ОбщийСчетчикНДСРасхождений      = 0;
	
	
	ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Новые расхождения:';
															|en = 'New discrepancies'"), Новый Шрифт(Новый Шрифт,, 12)));
	ЧастиСообщения.Добавить(Символы.ПС);
	
	СчетчикОрганизаций              = 0;
	СообщениеПоПользователюСформировано = Ложь;
	Пока ВыборкаПоОрганизациям.Следующий() Цикл
		СчетчикОрганизаций = СчетчикОрганизаций + 1;
		Если СчетчикОрганизаций > ЛимитСообщения 
			ИЛИ СчетчикПоказанныхРасхождений >= ОбщийЛимитРасхождений Тогда
			Если НЕ СообщениеПоПользователюСформировано Тогда
				// Это последняя строка сообщения для пользователя.
				ОсталосьРасхождений = ПользовательВсегоРасхождений - СчетчикРасхожденийПоПользователю;
				ОсталасьСумма       = ПользовательВсегоСумма - СчетчикСуммРасхожденийПоПользователю;
				ОсталсяНДС          = ПользовательВсегоНДС - СчетчикНДСРасхожденийПоПользователю; 
				ДополнитьСообщениеОставшимисяРасхождениями(ОсталасьСумма, ОсталосьРасхождений, ОсталсяНДС, ЧастиСообщения);
				
				ОбщийСчетчикРасхождений          = ОбщийСчетчикРасхождений + ОсталосьРасхождений;
				ОбщийСчетчикСуммРасхождений      = ОбщийСчетчикСуммРасхождений + ОсталасьСумма;
				ОбщийСчетчикНДСРасхождений       = ОбщийСчетчикНДСРасхождений + ОсталсяНДС;
				
				СообщениеПоПользователюСформировано = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Не СообщениеПоПользователюСформировано Тогда
			ЧастиСообщения.Добавить(Символы.ПС);
			ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Наша организация:';
																	|en = 'Our company:'"),,ЦветаСтиля.СервисСверкиРасчетовЦветТекстаПодсказки));
			ЧастиСообщения.Добавить(Символы.ПС);
			ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(Строка(ВыборкаПоОрганизациям.Организация), Новый Шрифт(Новый Шрифт,, 12)));
			ЧастиСообщения.Добавить(Символы.ПС);
		КонецЕсли;
		
		СчетчикРасхожденийПоОрганизации     = 0;
		СчетчикСуммРасхожденийПоОрганизации = 0;
		СчетчикНДСРасхожденийПоОрганизации  = 0;
		
		ОрганизацияВсегоРасхождений          = ВыборкаПоОрганизациям.КоличествоДокументов;
		ОрганизацияВсегоСумма                = ВыборкаПоОрганизациям.СуммаДокумента;
		ОрганизацияВсегоНДС                  = ВыборкаПоОрганизациям.НДСДокумента;
		
		СчетчикКонтрагентов = 0;
		СообщениеПоОрганизацииСформировано = СообщениеПоПользователюСформировано;
		ВыборкаПоКонтрагентам = ВыборкаПоОрганизациям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоКонтрагентам.Следующий() Цикл
			
			СчетчикКонтрагентов = СчетчикКонтрагентов + 1;
			Если СчетчикКонтрагентов > ЛимитСообщения 
				ИЛИ СчетчикПоказанныхРасхождений >= ОбщийЛимитРасхождений Тогда
				Если НЕ СообщениеПоОрганизацииСформировано Тогда
					// Это последняя строка сообщения по организации.
					ОсталосьРасхождений = ОрганизацияВсегоРасхождений - СчетчикРасхожденийПоОрганизации;
					ОсталасьСумма       = ОрганизацияВсегоСумма - СчетчикСуммРасхожденийПоОрганизации;
					ОсталсяНДС          = ОрганизацияВсегоНДС - СчетчикНДСРасхожденийПоОрганизации; 
					ДополнитьСообщениеОставшимисяРасхождениями(ОсталасьСумма, ОсталосьРасхождений, ОсталсяНДС, ЧастиСообщения);
					
					СчетчикРасхожденийПоПользователю     = СчетчикРасхожденийПоПользователю + ОсталосьРасхождений;
					СчетчикСуммРасхожденийПоПользователю = СчетчикСуммРасхожденийПоПользователю + ОсталасьСумма;
					СчетчикНДСРасхожденийПоПользователю  = СчетчикНДСРасхожденийПоПользователю + ОсталсяНДС;
					
					ОбщийСчетчикРасхождений          = ОбщийСчетчикРасхождений + ОсталосьРасхождений;
					ОбщийСчетчикСуммРасхождений      = ОбщийСчетчикСуммРасхождений + ОсталасьСумма;
					ОбщийСчетчикНДСРасхождений       = ОбщийСчетчикНДСРасхождений + ОсталсяНДС;
					
					СообщениеПоОрганизацииСформировано = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ СообщениеПоОрганизацииСформировано Тогда
				ЧастиСообщения.Добавить(Символы.ПС);
				ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = '  Контрагент:';
																		|en = ' Counterparty:'"),,ЦветаСтиля.ЦветТекстаДополнительныеСведения));
				ЧастиСообщения.Добавить(Символы.ПС);
				ЧастиСообщения.Добавить("  ");
				Если ЗначениеЗаполнено(ВыборкаПоКонтрагентам.Контрагент) Тогда
					ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(Строка(ВыборкаПоКонтрагентам.Контрагент), Новый Шрифт(Новый Шрифт,, 12)));
				Иначе
					КонтрагентНеНайден = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Контрагент не найден ИНН:%1 КПП:%2';
							|en = 'Counterparty is not found TIN:%1 KPP:%2'"), ВыборкаПоКонтрагентам.ИННКонтрагента, ВыборкаПоКонтрагентам.КППКонтрагента);
					ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(КонтрагентНеНайден, Новый Шрифт(Новый Шрифт,, 12), ЦветаСтиля.ЦветТекстаВнимание));
				КонецЕсли;
				ЧастиСообщения.Добавить(Символы.ПС); 
			КонецЕсли;
			
			СчетчикРасхожденийПоКонтрагенту     = 0;
			СчетчикСуммРасхожденийПоКонтрагенту = 0;
			СчетчикНДСРасхожденийПоКонтрагенту  = 0;
			
			КонтрагентВсегоРасхождений          = ВыборкаПоКонтрагентам.КоличествоДокументов;
			КонтрагентВсегоСумма                = ВыборкаПоКонтрагентам.СуммаДокумента;
			КонтрагентВсегоНДС                  = ВыборкаПоКонтрагентам.НДСДокумента;
			
			СчетчикДокументов = 0;
			СообщениеПоКонтрагентуСформировано = СообщениеПоОрганизацииСформировано;
			ВыборкаПоДокументам = ВыборкаПоКонтрагентам.Выбрать();
			Пока ВыборкаПоДокументам.Следующий() Цикл
				
				ИдентификаторыРасхождений.Добавить(ВыборкаПоДокументам.ИдентификаторРасхождения);
				СчетчикДокументов = СчетчикДокументов + 1;
				Если СчетчикДокументов > ЛимитСообщения 
					ИЛИ СчетчикПоказанныхРасхождений >= ОбщийЛимитРасхождений Тогда
					Если НЕ СообщениеПоКонтрагентуСформировано Тогда
						// Это последняя строка сообщения по контрагенту.
						ОсталосьРасхождений = КонтрагентВсегоРасхождений - СчетчикРасхожденийПоКонтрагенту;
						ОсталасьСумма       = КонтрагентВсегоСумма - СчетчикСуммРасхожденийПоКонтрагенту;
						ОсталсяНДС          = КонтрагентВсегоНДС - СчетчикНДСРасхожденийПоКонтрагенту; 
						ДополнитьСообщениеОставшимисяРасхождениями(ОсталасьСумма, ОсталосьРасхождений, ОсталсяНДС, ЧастиСообщения);
						
						СчетчикРасхожденийПоПользователю     = СчетчикРасхожденийПоПользователю + ОсталосьРасхождений;
						СчетчикСуммРасхожденийПоПользователю = СчетчикСуммРасхожденийПоПользователю + ОсталасьСумма;
						СчетчикНДСРасхожденийПоПользователю  = СчетчикНДСРасхожденийПоПользователю + ОсталсяНДС;
						
						СчетчикРасхожденийПоОрганизации     = СчетчикРасхожденийПоОрганизации + ОсталосьРасхождений;
						СчетчикСуммРасхожденийПоОрганизации = СчетчикСуммРасхожденийПоОрганизации + ОсталасьСумма;
						СчетчикНДСРасхожденийПоОрганизации  = СчетчикНДСРасхожденийПоОрганизации + ОсталсяНДС;
						
						ОбщийСчетчикРасхождений          = ОбщийСчетчикРасхождений + ОсталосьРасхождений;
						ОбщийСчетчикСуммРасхождений      = ОбщийСчетчикСуммРасхождений + ОсталасьСумма;
						ОбщийСчетчикНДСРасхождений       = ОбщийСчетчикНДСРасхождений + ОсталсяНДС;
					
						СообщениеПоКонтрагентуСформировано = Истина;
					КонецЕсли;
				Иначе
					
					Если Не СообщениеПоОрганизацииСформировано Тогда
						Если ЗначениеЗаполнено(ВыборкаПоДокументам.Документ) Тогда
							ПредставлениеДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Документ %1';
								|en = '""%1"" document'"),ВыборкаПоДокументам.ПервичныйДокументОрганизации);
							Документ = Новый ФорматированнаяСтрока(ПредставлениеДокумента, , , , ПолучитьНавигационнуюСсылку(ВыборкаПоДокументам.Документ));
						Иначе
							ПредставлениеДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = '<Документ %1>';
								|en = '<%1 document>'"),ВыборкаПоДокументам.ПервичныйДокументКонтрагента);
							Документ = Новый ФорматированнаяСтрока(ПредставлениеДокумента,,ЦветаСтиля.ЦветТекстаВнимание);
						КонецЕсли;
						ЧастиСообщения.Добавить("  ");
						ЧастиСообщения.Добавить(Документ);
						ТекстСуммаДокумента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = ' на сумму %1 руб.';
								|en = ' in the amount of %1 rub.'"), Формат(ВыборкаПоДокументам.СуммаДокумента, "ЧЦ=10; ЧДЦ=2"));
						ТекстСуммаДокумента =
							ТекстСуммаДокумента + ?(ВыборкаПоДокументам.НДСДокумента > 0,
							" (" + Строка(Формат(ВыборкаПоДокументам.НДСДокумента, "ЧЦ=10; ЧДЦ=2")) + " НДС)",
							"");
						ЧастиСообщения.Добавить(ТекстСуммаДокумента);
						ЧастиСообщения.Добавить(Символы.ПС);
						ОписаниеРасхождения = ОписаниеРасхождения(ВыборкаПоДокументам);
						ЧастиСообщения.Добавить(ОписаниеРасхождения);
						ЧастиСообщения.Добавить(Символы.ПС);
					КонецЕсли;
					
					СчетчикПоказанныхРасхождений     = СчетчикПоказанныхРасхождений + 1;
					
					ОбщийСчетчикРасхождений          = ОбщийСчетчикРасхождений + 1;
					ОбщийСчетчикСуммРасхождений      = ОбщийСчетчикСуммРасхождений + ВыборкаПоДокументам.СуммаДокумента;
					ОбщийСчетчикНДСРасхождений       = ОбщийСчетчикНДСРасхождений + ВыборкаПоДокументам.НДСДокумента;
					
					Если СчетчикПоказанныхРасхождений >= ОбщийЛимитРасхождений Тогда
						// Выведено максимальное количество расхождений.
						ОсталосьРасхождений = ПользовательВсегоРасхождений - ОбщийСчетчикРасхождений;
						ОсталасьСумма       = ПользовательВсегоСумма - ОбщийСчетчикСуммРасхождений;
						ОсталсяНДС          = ПользовательВсегоНДС - ОбщийСчетчикНДСРасхождений;  
						Если ОсталосьРасхождений > 0 Тогда 
							ДополнитьСообщениеОставшимисяРасхождениями(ОсталасьСумма, ОсталосьРасхождений, ОсталсяНДС, ЧастиСообщения);
						КонецЕсли;
						СообщениеПоПользователюСформировано = Истина;
						СообщениеПоОрганизацииСформировано  = Истина;
						СообщениеПоКонтрагентуСформировано  = Истина;
					КонецЕсли;
					
					СчетчикРасхожденийПоПользователю     = СчетчикРасхожденийПоПользователю + 1;
					СчетчикСуммРасхожденийПоПользователю = СчетчикСуммРасхожденийПоПользователю + ВыборкаПоДокументам.СуммаДокумента;
					СчетчикНДСРасхожденийПоПользователю  = СчетчикНДСРасхожденийПоПользователю + ВыборкаПоДокументам.НДСДокумента;
					
					СчетчикРасхожденийПоОрганизации     = СчетчикРасхожденийПоОрганизации + 1;
					СчетчикСуммРасхожденийПоОрганизации = СчетчикСуммРасхожденийПоОрганизации + ВыборкаПоДокументам.СуммаДокумента;
					СчетчикНДСРасхожденийПоОрганизации  = СчетчикНДСРасхожденийПоОрганизации + ВыборкаПоДокументам.НДСДокумента;
				
					СчетчикРасхожденийПоКонтрагенту     = СчетчикРасхожденийПоКонтрагенту + 1;
					СчетчикСуммРасхожденийПоКонтрагенту = СчетчикСуммРасхожденийПоКонтрагенту + ВыборкаПоДокументам.СуммаДокумента;
					СчетчикНДСРасхожденийПоКонтрагенту  = СчетчикНДСРасхожденийПоКонтрагенту + ВыборкаПоДокументам.НДСДокумента; 
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Новый Структура("Сообщение, ИдентификаторыРасхождений", Новый ФорматированнаяСтрока(ЧастиСообщения), ИдентификаторыРасхождений);

КонецФункции

Процедура ДополнитьСообщениеОставшимисяРасхождениями(ОсталасьСумма, ОсталосьРасхождений, ОсталсяНДС, ЧастиСообщения)
	
	ШаблонСклонения = НСтр("ru = ';%1 расхождение;;%1 расхождения;%1 расхождений;%1 расхождений';
							|en = ';%1 discrepancy;;;;%1 discrepancies'");
	ПредставлениеКоличестваРасхождений = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		ШаблонСклонения,
		ОсталосьРасхождений);
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '  и еще %1 на сумму %2 руб.';
			|en = '  and %1 more in the amount of %2 rub.'"), ПредставлениеКоличестваРасхождений, Формат(ОсталасьСумма, "ЧЦ=10; ЧДЦ=2"));
		
	ТекстСообщения = ТекстСообщения + ?(ОсталсяНДС > 0, " (" + Строка(Формат(ОсталсяНДС, "ЧЦ=10; ЧДЦ=2")) + " НДС)", "");
	
	ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(ТекстСообщения));
	ЧастиСообщения.Добавить(Символы.ПС);

КонецПроцедуры

Функция ОписаниеРасхождения(СтрокаРасхождения) 
	
	ОписаниеРасхождения = НСтр("ru = '  выявлено расхождение';
								|en = ' discrepancy is found'");
	Если СтрокаРасхождения.ОтсутствуетДокумент Тогда
		Если ЗначениеЗаполнено(СтрокаРасхождения.Документ) Тогда
			ОписаниеРасхождения = НСтр("ru = '  не отражен в учете контрагента';
										|en = ' not recorded in counterparty accounting'");
		Иначе
			ОписаниеРасхождения = НСтр("ru = '  не отражен в нашем учете';
										|en = ' not recorded in our accounting'");
		КонецЕсли;
	ИначеЕсли СтрокаРасхождения.ОтличаетсяСумма Тогда
		ОписаниеРасхождения = НСтр("ru = '  расхождение суммы';
									|en = ' amount discrepancy'");
	ИначеЕсли СтрокаРасхождения.ОтличаетсяСчетФактура Тогда
		ОписаниеРасхождения = НСтр("ru = '  расхождение реквизитов счета-фактуры';
									|en = ' tax invoice attribute discrepancy'");
	ИначеЕсли СтрокаРасхождения.ОтличаетсяПервичныйДокумент Тогда
		ОписаниеРасхождения = НСтр("ru = '  расхождение реквизитов документа';
									|en = ' document attribute discrepancy'");
	КонецЕсли;
	
	Возврат ОписаниеРасхождения;
	
КонецФункции

Функция УстраненныеРасхожденияПоПользователям(МенеджерВременныхТаблиц)
	
	УстраненныеРасхожденияПоПользователям = Новый Соответствие();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИндивидуальныеНастройкиПользователей.Пользователь КАК Пользователь,
	|	ИндивидуальныеНастройкиПользователей.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ,
	|	СервисСверкиРасчетовУстраненныеРасхождения.Организация КАК Организация,
	|	СервисСверкиРасчетовУстраненныеРасхождения.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовУстраненныеРасхождения.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	СервисСверкиРасчетовУстраненныеРасхождения.СуммаДокумента КАК СуммаДокумента,
	|	СервисСверкиРасчетовУстраненныеРасхождения.НДСДокумента КАК НДСДокумента,
	|	1 КАК КоличествоДокументов
	|ПОМЕСТИТЬ УстраненныеРасхожденияПредварительная
	|ИЗ
	|	ИндивидуальныеНастройкиПользователей КАК ИндивидуальныеНастройкиПользователей
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовУстраненныеРасхождения КАК СервисСверкиРасчетовУстраненныеРасхождения
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ИндивидуальныеНастройкиПользователей.ОповещатьОбУстраненииРасхождений = ИСТИНА
	|	И ВЫБОР
	|			КОГДА ИндивидуальныеНастройкиПользователей.ОтборКонтрагентов = 2
	|				ТОГДА СервисСверкиРасчетовУстраненныеРасхождения.Контрагент В
	|						(ВЫБРАТЬ
	|							ФильтрПоКонтрагентам.Контрагент
	|						ИЗ
	|							ФильтрПоКонтрагентам
	|						ГДЕ
	|							ФильтрПоКонтрагентам.Ссылка = ИндивидуальныеНастройкиПользователей.Ссылка
	|							И ФильтрПоКонтрагентам.ЭтоГлобальнаяНастройка = ЛОЖЬ)
	|			КОГДА ИндивидуальныеНастройкиПользователей.ОтборКонтрагентов = 1
	|				ТОГДА НЕ СервисСверкиРасчетовУстраненныеРасхождения.Контрагент В
	|							(ВЫБРАТЬ
	|								ФильтрПоКонтрагентам.Контрагент
	|							ИЗ
	|								ФильтрПоКонтрагентам
	|							ГДЕ
	|								ФильтрПоКонтрагентам.Ссылка = ИндивидуальныеНастройкиПользователей.Ссылка
	|								И ФильтрПоКонтрагентам.ЭтоГлобальнаяНастройка = ЛОЖЬ)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА ИндивидуальныеНастройкиПользователей.ОтборОрганизаций = 2
	|				ТОГДА СервисСверкиРасчетовУстраненныеРасхождения.Организация В
	|						(ВЫБРАТЬ
	|							ФильтрПоОрганизации.Организация
	|						ИЗ
	|							ФильтрПоОрганизации
	|						ГДЕ
	|							ФильтрПоОрганизации.Ссылка = ИндивидуальныеНастройкиПользователей.Ссылка)
	|			КОГДА ИндивидуальныеНастройкиПользователей.ОтборОрганизаций = 1
	|				ТОГДА НЕ СервисСверкиРасчетовУстраненныеРасхождения.Организация В
	|							(ВЫБРАТЬ
	|								ФильтрПоОрганизации.Организация
	|							ИЗ
	|								ФильтрПоОрганизации
	|							ГДЕ
	|								ФильтрПоОрганизации.Ссылка = ИндивидуальныеНастройкиПользователей.Ссылка)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И НЕ СервисСверкиРасчетовУстраненныеРасхождения.Организация ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВсеПользователиДляРассылки.Ссылка,
	|	ВсеПользователиДляРассылки.ИдентификаторПользователяИБ,
	|	СервисСверкиРасчетовУстраненныеРасхождения.Организация,
	|	СервисСверкиРасчетовУстраненныеРасхождения.Контрагент,
	|	СервисСверкиРасчетовУстраненныеРасхождения.ИдентификаторРасхождения,
	|	СервисСверкиРасчетовУстраненныеРасхождения.СуммаДокумента,
	|	СервисСверкиРасчетовУстраненныеРасхождения.НДСДокумента,
	|	1
	|ИЗ
	|	ВсеПользователиДляРассылки КАК ВсеПользователиДляРассылки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовУстраненныеРасхождения КАК СервисСверкиРасчетовУстраненныеРасхождения
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ ВсеПользователиДляРассылки.Ссылка В
	|				(ВЫБРАТЬ
	|					ИндивидуальныеНастройкиПользователей.Пользователь
	|				ИЗ
	|					ИндивидуальныеНастройкиПользователей)
	|	И НЕ СервисСверкиРасчетовУстраненныеРасхождения.Организация ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УстраненныеРасхожденияПредварительная.Пользователь КАК Пользователь,
	|	УстраненныеРасхожденияПредварительная.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ,
	|	УстраненныеРасхожденияПредварительная.Организация КАК Организация,
	|	УстраненныеРасхожденияПредварительная.Контрагент КАК Контрагент,
	|	УстраненныеРасхожденияПредварительная.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	УстраненныеРасхожденияПредварительная.СуммаДокумента КАК СуммаДокумента,
	|	УстраненныеРасхожденияПредварительная.НДСДокумента КАК НДСДокумента,
	|	УстраненныеРасхожденияПредварительная.КоличествоДокументов КАК КоличествоДокументов
	|ИЗ
	|	УстраненныеРасхожденияПредварительная КАК УстраненныеРасхожденияПредварительная
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммаДокумента УБЫВ
	|ИТОГИ
	|	МАКСИМУМ(ИдентификаторПользователяИБ),
	|	МАКСИМУМ(ИдентификаторРасхождения),
	|	СУММА(СуммаДокумента),
	|	СУММА(НДСДокумента),
	|	СУММА(КоличествоДокументов)
	|ПО
	|	Пользователь,
	|	Организация,
	|	Контрагент";  
	
	Результат = Запрос.Выполнить();
	ВыборкаПоПользователям = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
	
	ЛимитСообщения = 3; // не больше 3 организаций, 3 контрагентов по организации, 3 расхождений по контрагенту
	
	Пока ВыборкаПоПользователям.Следующий() Цикл
		
		СчетчикРасхожденийПоПользователю     = 0;
		СчетчикСуммРасхожденийПоПользователю = 0;
		СчетчикНДСРасхожденийПоПользователю  = 0;
		
		ЧастиСообщения = Новый Массив;
		ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Устранены расхождения по контрагентам:';
																|en = 'Eliminated discrepancies for counterparties:'"), Новый Шрифт(Новый Шрифт,, 12)));
		ЧастиСообщения.Добавить(Символы.ПС);
		ЧастиСообщения.Добавить(Символы.ПС); 
		
		ПользовательВсегоРасхожденийУстранено = ВыборкаПоПользователям.КоличествоДокументов;
		ПользовательВсегоСумма                = ВыборкаПоПользователям.СуммаДокумента;
		ПользовательВсегоНДС                  = ВыборкаПоПользователям.НДСДокумента;
		
		СчетчикОрганизаций              = 0;
		ВыборкаПоОрганизациям = ВыборкаПоПользователям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам); 
		Пока ВыборкаПоОрганизациям.Следующий() Цикл
			СчетчикОрганизаций = СчетчикОрганизаций + 1;
			Если СчетчикОрганизаций > ЛимитСообщения Тогда
				// Это последняя строка сообщения для пользователя.
				ОсталосьРасхождений = ПользовательВсегоРасхожденийУстранено - СчетчикРасхожденийПоПользователю;
				ОсталасьСумма       = ПользовательВсегоСумма - СчетчикСуммРасхожденийПоПользователю;
				ОсталсяНДС          = ПользовательВсегоНДС - СчетчикНДСРасхожденийПоПользователю; 
				
				ДополнитьСообщениеОставшимисяРасхождениями(ОсталасьСумма, ОсталосьРасхождений, ОсталсяНДС, ЧастиСообщения);
				УстраненныеРасхожденияПоПользователям.Вставить(ВыборкаПоПользователям.ИдентификаторПользователяИБ, Новый ФорматированнаяСтрока(ЧастиСообщения));
				Прервать;
			КонецЕсли;
			
			СчетчикРасхожденийПоОрганизации     = 0;
			СчетчикСуммРасхожденийПоОрганизации = 0;
			СчетчикНДСРасхожденийПоОрганизации  = 0;
			
			ОрганизацияВсегоРасхожденийУстранено = ВыборкаПоОрганизациям.КоличествоДокументов;
			ОрганизацияВсегоСумма                = ВыборкаПоОрганизациям.СуммаДокумента;
			ОрганизацияВсегоНДС                  = ВыборкаПоОрганизациям.НДСДокумента;
			
			СчетчикКонтрагентов = 0;
			ВыборкаПоКонтрагентам = ВыборкаПоОрганизациям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоКонтрагентам.Следующий() Цикл
				СчетчикКонтрагентов = СчетчикКонтрагентов + 1;
				Если СчетчикКонтрагентов > ЛимитСообщения Тогда
					// Это последняя строка сообщения по организации
					ОсталосьРасхождений = ОрганизацияВсегоРасхожденийУстранено - СчетчикРасхожденийПоОрганизации;
					ОсталасьСумма       = ПользовательВсегоСумма - СчетчикСуммРасхожденийПоОрганизации;
					ОсталсяНДС          = ПользовательВсегоНДС - СчетчикНДСРасхожденийПоОрганизации; 
					
					ДополнитьСообщениеОставшимисяРасхождениями(ОсталасьСумма, ОсталосьРасхождений, ОсталсяНДС, ЧастиСообщения);
					
					СчетчикРасхожденийПоПользователю     = СчетчикРасхожденийПоПользователю + ОсталосьРасхождений;
					СчетчикСуммРасхожденийПоПользователю = СчетчикСуммРасхожденийПоПользователю + ОсталасьСумма;
					СчетчикНДСРасхожденийПоПользователю  = СчетчикНДСРасхожденийПоПользователю + ОсталсяНДС;
					
					СчетчикРасхожденийПоОрганизации     = СчетчикРасхожденийПоОрганизации + ОсталосьРасхождений;
					СчетчикСуммРасхожденийПоОрганизации = СчетчикСуммРасхожденийПоОрганизации + ОсталасьСумма;
					СчетчикНДСРасхожденийПоОрганизации  = СчетчикНДСРасхожденийПоОрганизации + ОсталсяНДС;
					
					Прервать;
				КонецЕсли;
				
				ШаблонСклонения = НСтр("ru = ';%1 расхождение;;%1 расхождения;%1 расхождений;%1 расхождений';
										|en = ';%1 discrepancy;;;;%1 discrepancies'");
				ПредставлениеКоличестваРасхождений = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					ШаблонСклонения,
					ВыборкаПоКонтрагентам.КоличествоДокументов);	
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '  ""%1"": %2 на сумму %3 руб. (%4 НДС)';
						|en = '  ""%1"": %2 in the amount of %3 rub. (VAT %4)'"),
					ВыборкаПоКонтрагентам.Контрагент,
					ПредставлениеКоличестваРасхождений, 
					Формат(ВыборкаПоКонтрагентам.СуммаДокумента, "ЧЦ=10; ЧДЦ=2"), 
					Формат(ВыборкаПоКонтрагентам.НДСДокумента, "ЧЦ=10; ЧДЦ=2"));
				ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(ТекстСообщения));
				ЧастиСообщения.Добавить(Символы.ПС);
				
				СчетчикРасхожденийПоПользователю     = СчетчикРасхожденийПоПользователю + 1;
				СчетчикСуммРасхожденийПоПользователю = СчетчикСуммРасхожденийПоПользователю + ВыборкаПоКонтрагентам.СуммаДокумента;
				СчетчикНДСРасхожденийПоПользователю  = СчетчикНДСРасхожденийПоПользователю + ВыборкаПоКонтрагентам.НДСДокумента;
				
				СчетчикРасхожденийПоОрганизации     = СчетчикРасхожденийПоОрганизации + 1;
				СчетчикСуммРасхожденийПоОрганизации = СчетчикСуммРасхожденийПоОрганизации + ВыборкаПоКонтрагентам.СуммаДокумента;
				СчетчикНДСРасхожденийПоОрганизации  = СчетчикНДСРасхожденийПоОрганизации + ВыборкаПоКонтрагентам.НДСДокумента;
				
			КонецЦикла;
			
		КонецЦикла;
		
		УстраненныеРасхожденияПоПользователям.Вставить(ВыборкаПоПользователям.ИдентификаторПользователяИБ, Новый ФорматированнаяСтрока(ЧастиСообщения));
		
	КонецЦикла;
		
	Возврат УстраненныеРасхожденияПоПользователям;
	
КонецФункции

#КонецОбласти