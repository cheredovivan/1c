///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Авторизация

// Определяет имя сервера авторизации для обновления ключей доступа к сервису.
//
// Возвращаемое значение:
//   Строка - имя сервера сервиса.
//
Функция СерверАвторизации() Экспорт
	
	Результат = "oauth.yandex.ru";
	
	Возврат Результат;
	
КонецФункции

// Определяет адрес ресурса авторизации.
//
// Возвращаемое значение:
//   Строка - адрес ресурса. 
//
Функция АдресАвторизации() Экспорт
	
	ДанныеПриложения = ДанныеПриложения();

	Результат = "https://oauth.yandex.ru/authorize?response_type=code"
		+ "&client_id=" + ДанныеПриложения.IDПриложения 
		+ "&client_secret=" + ДанныеПриложения.ПарольПриложения 
		+ "&redirect_uri=" + "https://oauth.yandex.ru/verification_code";
	
	Возврат Результат;
	
КонецФункции

// Определяет данные авторизации.
//
// Параметры:
//   СтруктураОтвета - Структура - содержит набор ключей, которые необходимо вернуть.
//
// Возвращаемое значение:
//   Структура - содержит запрошенный набор ключей:
//     * access_token         - Строка - токен авторизации;
//     * access_token_expires - Дата - период действия токена;
//     * refresh_token        - Строка - токен автообновления.
//                     
Функция ДанныеАвторизации(СтруктураОтвета) Экспорт
	
	СтруктураДанныхАвторизации = Новый Структура;
	
	Если СтруктураОтвета.Свойство("access_token") 
		И СтруктураОтвета.Свойство("expires_in") 
		И СтруктураОтвета.Свойство("refresh_token") Тогда
		
		expires_in = СтруктураОтвета.expires_in;
		
		Если ОбщегоНазначенияУТКлиентСервер.ЭтаСтрокаЯвляетсяЦелымНеотрицательнымЧислом(expires_in) Тогда
			СрокЖизниТокена = НачалоДня(ТекущаяДатаСеанса() + Число(expires_in));
			СтруктураДанныхАвторизации.Вставить("access_token",         СтруктураОтвета.access_token);
			СтруктураДанныхАвторизации.Вставить("access_token_expires", СрокЖизниТокена);
			СтруктураДанныхАвторизации.Вставить("refresh_token",        СтруктураОтвета.refresh_token);
			
			Если СрокЖизниТокена<ТекущаяДатаСеанса() Тогда   
				КлючиОбновлены = ОбновитьКлючиДоступа(СтруктураДанныхАвторизации);
				
				Если КлючиОбновлены.Результат Тогда  
					СтруктураОтвета = КлючиОбновлены.СтруктураОтвета;
					Если СтруктураОтвета.Свойство("access_token") 
						И СтруктураОтвета.Свойство("expires_in") 
						И СтруктураОтвета.Свойство("refresh_token") Тогда
						
						expires_in = СтруктураОтвета.expires_in;
						
						Если ОбщегоНазначенияУТКлиентСервер.ЭтаСтрокаЯвляетсяЦелымНеотрицательнымЧислом(expires_in) Тогда
							СрокЖизниТокена = НачалоДня(ТекущаяДатаСеанса() + Число(expires_in));
							СтруктураДанныхАвторизации.Вставить("access_token",         СтруктураОтвета.access_token);
							СтруктураДанныхАвторизации.Вставить("access_token_expires", СрокЖизниТокена);
							СтруктураДанныхАвторизации.Вставить("refresh_token",        СтруктураОтвета.refresh_token);
							Если СрокЖизниТокена>ТекущаяДатаСеанса() Тогда   
								КлючиОбновлены = ОбновитьКлючиДоступа(СтруктураДанныхАвторизации);
								Если КлючиОбновлены.Результат Тогда 
									
								КонецЕсли;
							КонецЕсли;
						Иначе
							ТекстОписанияСобытия = НСтр("ru = 'Ошибка сохранения данных в безопасном хранилище';
														|en = 'An error occurred when saving the data to the secure storage'", 
							ОбщегоНазначения.КодОсновногоЯзыка());
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Некорректное значение свойства expires_in при получении ключа авторизации: %1';
								|en = 'Incorrect value of the expires_in property when receiving an authorization key: %1'", 
							ОбщегоНазначения.КодОсновногоЯзыка()),
							СтруктураОтвета.expires_in);
							
							ЗаписьЖурналаРегистрации(ТекстОписанияСобытия, 
							УровеньЖурналаРегистрации.Ошибка,,, 
							ТекстОшибки);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			ТекстОписанияСобытия = НСтр("ru = 'Ошибка сохранения данных в безопасном хранилище';
										|en = 'An error occurred when saving the data to the secure storage'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Некорректное значение свойства expires_in при получении ключа авторизации: %1';
				|en = 'Incorrect value of the expires_in property when receiving an authorization key: %1'", 
			ОбщегоНазначения.КодОсновногоЯзыка()),
			СтруктураОтвета.expires_in);
			
			ЗаписьЖурналаРегистрации(ТекстОписанияСобытия, 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтруктураДанныхАвторизации;
	
КонецФункции

// Функция возвращает http запрос для обновления ключей доступа к сервису для организации.
//
// Параметры:
//   ИдентификаторАккаунта - Строка - идентификатор аккаунта для подключения к сервису.
//
// Возвращаемое значение:
//   HTTPЗапрос - запрос для получения ключей доступа.
//
Функция ЗапросОбновитьТокеныДоступа(ИдентификаторАккаунта) Экспорт
	
	ДанныеПриложения = ДанныеПриложения();

	СтруктураДанныхАвторизации = ДанныеАвторизацииПоИдентификаторуАккаунта(ИдентификаторАккаунта);
	
	Если СтруктураДанныхАвторизации <> Неопределено Тогда
		refresh_token = СтруктураДанныхАвторизации.refresh_token;
	КонецЕсли;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");

	ПараметрыURL = Новый Массив;
	ПараметрыURL.Добавить("grant_type=refresh_token");
	ПараметрыURL.Добавить("refresh_token" + refresh_token);
	ПараметрыURL.Добавить("client_id=" + ДанныеПриложения.IDПриложения);
	ПараметрыURL.Добавить("client_secret=" + ДанныеПриложения.ПарольПриложения);
	
	Адрес = "/token?"; 
	HTTPЗапрос = Новый HTTPЗапрос(Адрес, Заголовки);
	Тело = СтрСоединить(ПараметрыURL, "&");
	
	HTTPЗапрос.УстановитьТелоИзСтроки(Тело,"windows-1251");
	
	Возврат HTTPЗапрос;
	
КонецФункции

// Функция возвращает http запрос для получения ключей доступа к сервису по временному коду.
//
// Параметры:
//   ВременныйКод - Строка - временный код для подключения к сервису.
//
// Возвращаемое значение:
//   HTTPЗапрос - запрос для получения ключей доступа.
//
Функция ЗапросПолучитьТокеныПоКоду(ВременныйКод) Экспорт
	
	ДанныеПриложения = ДанныеПриложения();
	
    Заголовки = Новый Соответствие;
    Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");

	ПараметрыURL = Новый Массив;
	ПараметрыURL.Добавить("grant_type=authorization_code");
	ПараметрыURL.Добавить("code=" + ВременныйКод);
    ПараметрыURL.Добавить("client_id=" + ДанныеПриложения.IDПриложения);    
    ПараметрыURL.Добавить("client_secret=" + ДанныеПриложения.ПарольПриложения);
	
	Адрес = "/token?"; 
    HTTPЗапрос = Новый HTTPЗапрос(Адрес, Заголовки);
	Тело = СтрСоединить(ПараметрыURL, "&");
	
	HTTPЗапрос.УстановитьТелоИзСтроки(Тело,"windows-1251");
    
	Возврат HTTPЗапрос;
	
КонецФункции

// Функция возвращает информацию о пользователе Яндекс ID (логин и идентификатор пользователя).
//
// Параметры:
//   КлючДоступа - Строка - ключ доступа, выданный для логина пользователя.
// 
// Возвращаемое значение:
//   - Структура - информацию о пользователе:
//     * Имя           - Строка - логин пользователя на Яндекс ID;
//     * Идентификатор - Строка - уникальный идентификатор пользователя на Яндекс ID.
//   - Неопределено - ошибка выполнения.
//
Функция ПолучитьДанныеЛогина(КлючДоступа) Экспорт
		
	ДанныеПриложения = ДанныеПриложения();
	Сервер = СерверДанныхАвторизации();
	ИмяМетода = "/info";
	Адрес = ИмяМетода +"?";
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Authorization", "OAuth " + КлючДоступа);
	
	ПараметрыURL = Новый Массив;
	ПараметрыURL.Добавить("format=" + "json");
	ПараметрыURL.Добавить("jwt_secret=" + ДанныеПриложения.ПарольПриложения);
	
	Попытка	
		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,,ИнтернетПрокси,200,СоединениеOpenSSL);
	Исключение
		ТекстОписанияСобытия = НСтр("ru = 'Отсутствует соединение с сервером';
									|en = 'There is no connection with the server'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером: %1';
				|en = 'There is no connection with the server: %1'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер);
		
		ЗаписьЖурналаРегистрации(ТекстОписанияСобытия,
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
			
		Возврат Неопределено;
	КонецПопытки;
	
	ТелоЗапроса = СтрСоединить(ПараметрыURL, "&");
	Адрес = Адрес + ТелоЗапроса;
	
	HTTPЗапрос = Новый HTTPЗапрос(Адрес, Заголовки); 
	СтруктураОтвета = Новый Структура();
	СтруктураДанныхЛогина = Новый Структура();
		
	Попытка
		HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос);
		СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
		СтруктураОтвета = ИзJSON(СтрокаОтвета);
		КодСостояния = HTTPОтвет.КодСостояния;
	Исключение 
		ТекстОписанияСобытия = НСтр("ru = 'Ошибка выполнения запроса';
									|en = 'Request execution error'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		ТекстОшибки = НСтр("ru = 'Ошибка выполнения запроса получения данных о пользователе Яндекс ID (логина и идентификатора пользователя)';
							|en = 'An error occurred when executing the request to receive data about the Yandex ID user (username and user ID)'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(ТекстОписанияСобытия,
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
			
		Возврат Неопределено;
	КонецПопытки;
	
	Если КодСостояния = 200 Тогда
		СтруктураДанныхЛогина.Вставить("Имя",СтруктураОтвета.login);
		СтруктураДанныхЛогина.Вставить("Идентификатор",СтруктураОтвета.id);
	Иначе
		Если СтруктураОтвета.Свойство("status") И СтруктураОтвета.status = "ERROR" Тогда
			ОписаниеОшибок = ТекстОшибки(СтруктураОтвета.errors);
		Иначе
			ОписаниеОшибок = ""; 
		КонецЕсли;
		
		ТекстОписанияСобытия = НСтр("ru = 'Ошибка выполнения запроса';
									|en = 'Request execution error'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка вызова API Яндекс Маркет: %1; %2; %3';
				|en = 'An error occurred when calling Yandex.Market API: %1; %2; %3'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			КодСостояния,
			HTTPОтвет.ПолучитьТелоКакСтроку(),
			ОписаниеОшибок);

		ЗаписьЖурналаРегистрации(ТекстОписанияСобытия,
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
	КонецЕсли;
	
	Возврат СтруктураДанныхЛогина;
	
КонецФункции

// Получает данные авторизации из безопасного хранилища. 
//
// Параметры:
//   ДанныеУчетнойЗаписи - Структура - данные учетной записи (см. ДанныеУчетнойЗаписи).
//
// Возвращаемое значение:
//   - Структура    - данные авторизации для подключения к сервису (см. ДанныеАвторизации);
//   - Неопределено - данные авторизации не найдены.
//
Функция ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи) Экспорт
	
	Владелец = "ЯндексМаркет";
	Ключ     = "_" + СокрЛП(ДанныеУчетнойЗаписи.ИдентификаторКабинета);
	
	УстановитьПривилегированныйРежим(Истина);
	ТекущиеДанные = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Владелец, Ключ, Ложь);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если НЕ (ТекущиеДанные<>Неопределено И ТекущиеДанные.Свойство("apikey_token")) Тогда 
		
		Ключ     = "_" + СокрЛП(ДанныеУчетнойЗаписи.ИдентификаторАккаунта);
		
		УстановитьПривилегированныйРежим(Истина);
		ТекущиеДанные = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Владелец, Ключ, Ложь);
		УстановитьПривилегированныйРежим(Ложь);

	КонецЕсли;
	
	Возврат ТекущиеДанные;
	
КонецФункции

// Получает данные авторизации из безопасного хранилища. 
//
// Параметры:
//   ИдентификаторАккаунта - Строка - идентификатор аккаунта для подключения к сервису.
//
// Возвращаемое значение:
//   - Структура    - данные авторизации для подключения к сервису (см. ДанныеАвторизации);
//   - Неопределено - данные авторизации не найдены.
//
Функция ДанныеАвторизацииПоИдентификаторуАккаунта(ИдентификаторАккаунта) Экспорт
	
	Владелец = "ЯндексМаркет";
	Ключ     = "_" + СокрЛП(ИдентификаторАккаунта);
	
	УстановитьПривилегированныйРежим(Истина);
	ТекущиеДанные = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Владелец, Ключ, Ложь);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТекущиеДанные;
	
КонецФункции     

// Получает данные авторизации из безопасного хранилища. 
//
// Параметры:
//   ИдентификаторКабинета - Строка - идентификатор бизнес-кабинета для подключения к сервису.
//
// Возвращаемое значение:
//   - Структура    - данные авторизации для подключения к сервису:
//             * Идентификатор - Строка - ключ доступа к методам кабинета.
//   - Неопределено - данные авторизации не найдены.
//
Функция ДанныеАвторизацииПоИдентификаторуКабинета(ИдентификаторКабинета) Экспорт
	
	Владелец = "ЯндексМаркет";
	Ключ     = "_" + СокрЛП(ИдентификаторКабинета);
	
	УстановитьПривилегированныйРежим(Истина);
	ТекущиеДанные = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Владелец, Ключ, Ложь);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТекущиеДанные;
	
КонецФункции

// Определяет данные авторизации по указанному аккаунту. 
//
// Параметры:
//   ИдентификаторАккаунта - Строка - идентификатор аккаунта для подключения к сервису.
//
// Возвращаемое значение:
//   Строка - имя аккаунта (см. ПолучитьДанныеЛогина).
//
Функция ДанныеАвторизацииЛогин(ИдентификаторАккаунта) Экспорт
	
	ТекущиеДанные = ДанныеАвторизацииПоИдентификаторуАккаунта(ИдентификаторАккаунта);
	
	Если ТекущиеДанные <> Неопределено И ТекущиеДанные.Свойство("login") Тогда   
		Логин = ТекущиеДанные.login;
	КонецЕсли;
	
	Возврат Логин;
	
КонецФункции

// Добавляет данные авторизации в безопасное хранилище. 
//
// Параметры:
//   ДанныеАвторизации      - Структура - данные авторизации для подключения к сервису (см. ДанныеАвторизации);
//   ИдентификаторКабинета  - Строка - идентификатор о кабинете.
//
// Возвращаемое значение:
//   Булево - результат успешного выполнения операции.
//
Функция УстановитьНастройкиАвторизации(ДанныеАвторизации, ИдентификаторКабинета) Экспорт
	
	Результат = Ложь;
	Владелец  = "ЯндексМаркет";
	
	Если ИдентификаторКабинета <> Неопределено 
		 И ДанныеАвторизации.Свойство("apikey_token") Тогда
		 
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Владелец, ДанныеАвторизации, "_" + ИдентификаторКабинета);  
		УстановитьПривилегированныйРежим(Ложь);
		
		Результат = Истина;
	КонецЕсли;
	
	Если Не Результат Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось сохранить данные авторизации в безопасном хранилище для владельца ""%1"".';
				|en = 'Cannot save the authorization data to the secure storage for owner ""%1"".'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Владелец);
			
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции 

// Удаляет данные авторизации из безопасного хранилища по идентификатору аккаунта. 
//
// Параметры:
//   ИдентификаторАккаунта - Строка - идентификатор аккаунта для подключения к сервису.
//
Процедура УдалитьДанныеАвторизации(ИдентификаторАккаунта) Экспорт

	Владелец = "ЯндексМаркет";
	Ключ     = "_" + СокрЛП(ИдентификаторАккаунта);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторАккаунта", ИдентификаторАккаунта);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетныеЗаписиМаркетплейсов.Ссылка КАК УчетнаяЗаписьМаркетплеса
		|ИЗ
		|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
		|ГДЕ
		|	УчетныеЗаписиМаркетплейсов.ИдентификаторАккаунта = &ИдентификаторАккаунта";

	УстановитьПривилегированныйРежим(Истина);
	ВыборкаУчетныхЗаписей = Запрос.Выполнить().Выбрать();

	ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(Владелец, Ключ);
	УстановитьПривилегированныйРежим(Ложь);

	Префиксы = ПрефиксыРегламентныхЗаданий();
	Пока ВыборкаУчетныхЗаписей.Следующий() Цикл
		Идентификатор = ВыборкаУчетныхЗаписей.УчетнаяЗаписьМаркетплеса.УникальныйИдентификатор();
		Для Каждого Префикс Из Префиксы Цикл
			Справочники.УчетныеЗаписиМаркетплейсов.УстановитьИспользованиеРегламентногоЗадания(Префикс.Значение + Идентификатор, Ложь);
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры  

// Удаляет данные авторизации из безопасного хранилища по идентификатору кабинета. 
//
// Параметры:
//   ИдентификаторКабинета  - Строка - идентификатор о кабинете.
//
Процедура УдалитьДанныеАвторизацииКабинета(ИдентификаторКабинета) Экспорт

	Владелец = "ЯндексМаркет";
	Ключ     = "_" + СокрЛП(ИдентификаторКабинета);
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторКабинета", ИдентификаторКабинета);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетныеЗаписиМаркетплейсов.Ссылка КАК УчетнаяЗаписьМаркетплеса
		|ИЗ
		|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
		|ГДЕ
		|	УчетныеЗаписиМаркетплейсов.ИдентификаторКабинета = &ИдентификаторКабинета";

	УстановитьПривилегированныйРежим(Истина);
	ВыборкаУчетныхЗаписей = Запрос.Выполнить().Выбрать();

	ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(Владелец, Ключ);
	УстановитьПривилегированныйРежим(Ложь);

	Префиксы = ПрефиксыРегламентныхЗаданий();
	Пока ВыборкаУчетныхЗаписей.Следующий() Цикл
		Идентификатор = ВыборкаУчетныхЗаписей.УчетнаяЗаписьМаркетплеса.УникальныйИдентификатор();
		Для Каждого Префикс Из Префиксы Цикл
			Справочники.УчетныеЗаписиМаркетплейсов.УстановитьИспользованиеРегламентногоЗадания(Префикс.Значение + Идентификатор, Ложь);
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

// Получает данные обо всех зарегистрированных аккаунтах Яндекс Маркет.
//
// Возвращаемое значение:
//   Массив Из Структура - данные об аккаунтах:
//     * Идентификатор        - Строка - идентификатор аккаунта подключения;
//     * Наименование         - Строка - наименование аккаунта;
//     * ТребуетсяАвторизация - Булево - признак успешно завершенной авторизации (действующего токена доступа).
//
Функция ПолучитьСписокАккаунтов() Экспорт

	Результат = Новый Массив;
	
	Если ОбщегоНазначения.РазделениеВключено() И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		ИмяБезопасногоХранилищаДанных = "БезопасноеХранилищеДанныхОбластейДанных";
	Иначе
		ИмяБезопасногоХранилищаДанных = "БезопасноеХранилищеДанных";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БезопасноеХранилищеДанных.Данные КАК Данные
		|ИЗ
		|	#ИмяБезопасногоХранилищаДанных КАК БезопасноеХранилищеДанных
		|ГДЕ
		|	БезопасноеХранилищеДанных.Владелец = &Владелец";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяБезопасногоХранилищаДанных", "РегистрСведений." + ИмяБезопасногоХранилищаДанных);
	Запрос.УстановитьПараметр("Владелец", "ЯндексМаркет");
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ВыборкаДанных.Следующий() Тогда
		Если ЗначениеЗаполнено(ВыборкаДанных.Данные) Тогда
			СохраненныеДанные = ВыборкаДанных.Данные.Получить();
			
			Если ЗначениеЗаполнено(СохраненныеДанные) Тогда
				Для Каждого КлючЗначение Из СохраненныеДанные Цикл
					Если ТипЗнч(КлючЗначение.Значение) = Тип("Структура")
						 И КлючЗначение.Значение.Свойство("id") 
					 	 И КлючЗначение.Значение.Свойство("login") 
					 	 И КлючЗначение.Значение.Свойство("access_token") 
					 	 И КлючЗначение.Значение.Свойство("access_token_expires") Тогда
						Аккаунт = Новый Структура;
						Аккаунт.Вставить("Идентификатор",        КлючЗначение.Значение.id);
						Аккаунт.Вставить("Наименование",         КлючЗначение.Значение.login);
						Аккаунт.Вставить("ТребуетсяАвторизация", Истина); 
						
						Если ТипЗнч(КлючЗначение.Значение.access_token_expires) = Тип("Дата") 
							 И КлючЗначение.Значение.access_token_expires > ТекущаяДатаСеанса() Тогда
							Аккаунт.Вставить("ТребуетсяАвторизация", Ложь); 
						КонецЕсли;
						
						Результат.Добавить(Аккаунт);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Добавляет подпись интеграции в заголовки вызовов методов API
//
// Параметры:
//   Заголовки - Соответствие Из Строка - заголовки вызовов методов API.   
//
Процедура ДобавитьПодписьИнтеграцииВЗаголовки(Заголовки) Экспорт
	Постфикс = "_ut";
	
	//++ НЕ УТ
	
	Постфикс = "_ka";
	
	//-- НЕ УТ
	
	//++ НЕ УТКА
	
	Постфикс = "_erp";
	
	//-- НЕ УТКА

	Заголовки.Вставить("X-Market-Integration",  "vendor_1c" + Постфикс);
	
КонецПроцедуры

#КонецОбласти

#Область УчетныеЗаписи

// Определяет настройки учетной записи торговой площадки Яндекс Маркет.
//
// Параметры:
//   УчетнаяЗапись      - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису; 
//   СлужебныеПараметры - Булево - признак необходимости возврата в результате дополнительных параметров.
// 
// Возвращаемое значение:
//   Структура - настройки учетной записи:
//     * УникальныйИдентификатор           - УникальныйИдентификатор - идентификатор учетной записи;
//     * УчетнаяЗапись                     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//     * Наименование                      - Строка - наименование учетной записи (магазина);
//     * Представление                     - Строка - представление учетной записи;
//     * ПометкаУдаления                   - Булево - признак недействующего магазина;
//     * ВидМаркетплейса                   - ПеречислениеСсылка.ВидыМаркетплейсов - вид маркетплейса для получения учетной записи;
//     * Организация                       - СправочникСсылка.Организации - организация, связанная с магазином;
//     * ИсточникКатегории                 - ПеречислениеСсылка.ИсточникиКатегорийДляМаркетплейса - источник категории;
//     * ЦенаПродажи                       - СправочникСсылка.ВидыЦен - вид цены продажи;
//     * СхемаРаботы                       - ПеречислениеСсылка.СхемыРаботыТорговыхПлощадок - схема работы магазина;
//     * ИдентификаторАккаунта             - Строка - идентификатор аккаунта;
//     * ИдентификаторКабинета             - Строка - идентификатор кабинета;
//     * ИдентификаторМагазина             - Строка - идентификатор кампании;
//     * ИдентификаторКлиента              - Строка - номер магазина;
//     * НеОбновлятьДанныеТорговойПлощадки - Булево - признак запрещенного обмена данными с торговой площадкой;
//     * ОбязательныеРеквизитыЗаполнены    - Булево - признак заполненности обязательных реквизитов учетной записи;
//     * ОбязательныеЗаданияВключены       - Булево - признак включенных обязательных регламентных заданий;
//     * Партнер                           - СправочникСсылка.Партнеры - партнер торговой площадки;
//     * Контрагент                        - СправочникСсылка.Контрагенты - контрагент торговой площадки;
//     * СоглашениеКомиссия                - СправочникСсылка.СоглашенияСКлиентами - соглашение с комиссионером;
//     * ДоговорКомиссия                   - СправочникСсылка.ДоговорыКонтрагентов - договор с комиссионером;
//     * ДополнительныеНастройки           - Структура Из КлючИЗначение - настройки из табличной части ДополнительныеНастройки.
//
Функция ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись, СлужебныеПараметры = Истина) Экспорт
	
	ДанныеУчетнойЗаписи = Новый Структура;
	ДанныеУчетнойЗаписи.Вставить("УникальныйИдентификатор",           ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	ДанныеУчетнойЗаписи.Вставить("УчетнаяЗапись",                     Справочники.УчетныеЗаписиМаркетплейсов.ПустаяСсылка());
	ДанныеУчетнойЗаписи.Вставить("Наименование",                      "");
	ДанныеУчетнойЗаписи.Вставить("Представление",                     "");
	ДанныеУчетнойЗаписи.Вставить("ВидМаркетплейса",                   Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет);
	ДанныеУчетнойЗаписи.Вставить("ПометкаУдаления",                   Ложь);
	ДанныеУчетнойЗаписи.Вставить("Организация",                       Справочники.Организации.ПустаяСсылка());
	ДанныеУчетнойЗаписи.Вставить("ЦенаПродажи",                       Справочники.ВидыЦен.ПустаяСсылка());
	ДанныеУчетнойЗаписи.Вставить("ИсточникКатегории",                 Перечисления.ИсточникиКатегорийДляМаркетплейса.ПустаяСсылка());
	ДанныеУчетнойЗаписи.Вставить("СхемаРаботы",                       Перечисления.СхемыРаботыТорговыхПлощадок.ПустаяСсылка());
	ДанныеУчетнойЗаписи.Вставить("ИдентификаторАккаунта",             "");
	ДанныеУчетнойЗаписи.Вставить("ИдентификаторКабинета",             "");
	ДанныеУчетнойЗаписи.Вставить("ИдентификаторМагазина",             "");
	ДанныеУчетнойЗаписи.Вставить("ИдентификаторКлиента",              "");
	ДанныеУчетнойЗаписи.Вставить("НеОбновлятьДанныеТорговойПлощадки", Истина);
	ДанныеУчетнойЗаписи.Вставить("Партнер",                           Справочники.Партнеры.ПустаяСсылка());
	ДанныеУчетнойЗаписи.Вставить("Контрагент",                        Справочники.Контрагенты.ПустаяСсылка());
	ДанныеУчетнойЗаписи.Вставить("СоглашениеКомиссия",                Справочники.СоглашенияСКлиентами.ПустаяСсылка());
	ДанныеУчетнойЗаписи.Вставить("ДоговорКомиссия",                   Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	ДанныеУчетнойЗаписи.Вставить("ДополнительныеНастройки",           Новый Структура);
	
	Если СлужебныеПараметры Тогда
		ДанныеУчетнойЗаписи.Вставить("ОбязательныеРеквизитыЗаполнены", Ложь);
		ДанныеУчетнойЗаписи.Вставить("ОбязательныеЗаданияВключены",    Ложь);
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	УчетныеЗаписиМаркетплейсов.Ссылка КАК УчетнаяЗапись,
		|	УчетныеЗаписиМаркетплейсов.Наименование КАК Наименование,
		|	УчетныеЗаписиМаркетплейсов.ПометкаУдаления КАК ПометкаУдаления,
		|	УчетныеЗаписиМаркетплейсов.Организация КАК Организация,
		|	УчетныеЗаписиМаркетплейсов.ИсточникКатегории КАК ИсточникКатегории,
		|	ЕСТЬNULL(ВидыЦенМаркетплейсов.ВидЦены, ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)) КАК ЦенаПродажи,
		|	УчетныеЗаписиМаркетплейсов.СхемаРаботы КАК СхемаРаботы,
		|	УчетныеЗаписиМаркетплейсов.ИдентификаторАккаунта КАК ИдентификаторАккаунта,
		|	УчетныеЗаписиМаркетплейсов.ИдентификаторКабинета КАК ИдентификаторКабинета,
		|	УчетныеЗаписиМаркетплейсов.ИдентификаторМагазина КАК ИдентификаторМагазина,
		|	УчетныеЗаписиМаркетплейсов.ИдентификаторКлиента КАК ИдентификаторКлиента,
		|	УчетныеЗаписиМаркетплейсов.НеОбновлятьДанныеТорговойПлощадки КАК НеОбновлятьДанныеТорговойПлощадки,
		|	УчетныеЗаписиМаркетплейсов.Партнер КАК Партнер,
		|	УчетныеЗаписиМаркетплейсов.Контрагент КАК Контрагент,
		|	УчетныеЗаписиМаркетплейсов.Соглашение КАК СоглашениеКомиссия,
		|	УчетныеЗаписиМаркетплейсов.ДоговорПродажиЧерезСкладыТорговойПлощадки КАК ДоговорКомиссия
		|ИЗ
		|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УчетныеЗаписиМаркетплейсов.ВидыЦен КАК ВидыЦенМаркетплейсов
		|		ПО УчетныеЗаписиМаркетплейсов.Ссылка = ВидыЦенМаркетплейсов.Ссылка
		|			И (ВидыЦенМаркетплейсов.ИмяНастройки = &ИмяНастройки)
		|ГДЕ
		|	УчетныеЗаписиМаркетплейсов.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет)
		|	И УчетныеЗаписиМаркетплейсов.Ссылка = &УчетнаяЗапись"; 

	ИмяНастройки = НСтр("ru = 'Цена продажи';
						|en = 'Selling price'");
	Запрос.Параметры.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	Запрос.Параметры.Вставить("ИмяНастройки",  ИмяНастройки);
	
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаДанных.Следующий() Тогда
		ДанныеУчетнойЗаписи.УникальныйИдентификатор           = ВыборкаДанных.УчетнаяЗапись.УникальныйИдентификатор();
		ДанныеУчетнойЗаписи.УчетнаяЗапись                     = ВыборкаДанных.УчетнаяЗапись;
		ДанныеУчетнойЗаписи.Наименование                      = ВыборкаДанных.Наименование;
		ДанныеУчетнойЗаписи.Представление                     = Строка(ВыборкаДанных.УчетнаяЗапись);
		ДанныеУчетнойЗаписи.ПометкаУдаления                   = ВыборкаДанных.ПометкаУдаления;
		ДанныеУчетнойЗаписи.Организация                       = ВыборкаДанных.Организация;
		ДанныеУчетнойЗаписи.ЦенаПродажи                       = ВыборкаДанных.ЦенаПродажи;
		ДанныеУчетнойЗаписи.ИсточникКатегории                 = ВыборкаДанных.ИсточникКатегории;
		ДанныеУчетнойЗаписи.СхемаРаботы                       = ВыборкаДанных.СхемаРаботы;
		ДанныеУчетнойЗаписи.ИдентификаторАккаунта             = ВыборкаДанных.ИдентификаторАккаунта;
		ДанныеУчетнойЗаписи.ИдентификаторКабинета             = ВыборкаДанных.ИдентификаторКабинета;
		ДанныеУчетнойЗаписи.ИдентификаторМагазина             = ВыборкаДанных.ИдентификаторМагазина;
		ДанныеУчетнойЗаписи.ИдентификаторКлиента              = ВыборкаДанных.ИдентификаторКлиента;
		ДанныеУчетнойЗаписи.НеОбновлятьДанныеТорговойПлощадки = ВыборкаДанных.НеОбновлятьДанныеТорговойПлощадки;
		ДанныеУчетнойЗаписи.Партнер                           = ВыборкаДанных.Партнер;
		ДанныеУчетнойЗаписи.Контрагент                        = ВыборкаДанных.Контрагент;
		ДанныеУчетнойЗаписи.СоглашениеКомиссия                = ВыборкаДанных.СоглашениеКомиссия;
		ДанныеУчетнойЗаписи.ДоговорКомиссия                   = ВыборкаДанных.ДоговорКомиссия; 
		
		Если СлужебныеПараметры Тогда
			Если ЗначениеЗаполнено(ДанныеУчетнойЗаписи.Организация)
					И ЗначениеЗаполнено(ДанныеУчетнойЗаписи.ИдентификаторКабинета)
					И ЗначениеЗаполнено(ДанныеУчетнойЗаписи.ИдентификаторМагазина)
					И ЗначениеЗаполнено(ДанныеУчетнойЗаписи.ИдентификаторКлиента)
					И ЗначениеЗаполнено(ДанныеУчетнойЗаписи.СхемаРаботы)
					И ЗначениеЗаполнено(ДанныеУчетнойЗаписи.ЦенаПродажи) Тогда
				ДанныеУчетнойЗаписи.Вставить("ОбязательныеРеквизитыЗаполнены", Истина);
			КонецЕсли;
			
			ДоступнаРаботаСОстатками     = Справочники.УчетныеЗаписиМаркетплейсов.ДоступнаРаботаСОстаткамиДляУчетнойЗаписи(УчетнаяЗапись);
			ПрефиксыРегламентныхЗаданий  = ПрефиксыРегламентныхЗаданий(Истина, ДоступнаРаботаСОстатками);
			СостоянияРегламентныхЗаданий = ПолучитьСостоянияРегламентныхЗаданийМагазина(УчетнаяЗапись, ПрефиксыРегламентныхЗаданий);
			
			ДанныеУчетнойЗаписи.Вставить("ОбязательныеЗаданияВключены", Не СостоянияРегламентныхЗаданий.ЕстьОтключенныеРегламентныеЗадания); 
		КонецЕсли;
		
		ДополнительныеНастройки = Справочники.УчетныеЗаписиМаркетплейсов.ОписаниеДополнительныхНастроекУчетнойЗаписи(УчетнаяЗапись,
			"СпособОтраженияПродаж, СкладОтгрузки, ДоговорВзаимозачеты,
			|РозничныйПартнер, РозничныйКонтрагент,
			|СоглашениеРеализация, ДоговорРеализация,
			|ПричинаОтменыПоУмолчанию, СоздаватьЗадачуОповещение, ТестовыйРежимЗаказовFBS");
		ДанныеУчетнойЗаписи.ДополнительныеНастройки = ДополнительныеНастройки; 
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДанныеУчетнойЗаписи, ДополнительныеНастройки, Истина);
		
		Если ДанныеУчетнойЗаписи.СхемаРаботы <> Перечисления.СхемыРаботыТорговыхПлощадок.FBO Тогда
			ДанныеСклада = СкладУчетнойЗаписиДляВыгрузкиОстатков(УчетнаяЗапись);
			ДанныеУчетнойЗаписи.СкладОтгрузки = ДанныеСклада.Склад;
		КонецЕсли;
		
		Если ДанныеУчетнойЗаписи.ТестовыйРежимЗаказовFBS = Неопределено Тогда
			ДанныеУчетнойЗаписи.ТестовыйРежимЗаказовFBS = Ложь;
		КонецЕсли; 
		Если ДанныеУчетнойЗаписи.СоздаватьЗадачуОповещение = Неопределено Тогда
			ДанныеУчетнойЗаписи.СоздаватьЗадачуОповещение = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеУчетнойЗаписи;
		
КонецФункции

// Возвращает склад для выгрузки остатков.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//
// Возвращаемое значение:
//   Структура - данные по складу:
//     * ИдентификаторСклада - Строка - идентификатор склада.
//     * Склад - СправочникСсылка.Склады - склад по идентификатору склада.
//
Функция СкладУчетнойЗаписиДляВыгрузкиОстатков(УчетнаяЗапись) Экспорт
	
	ДанныеСклада = Новый Структура;
	ДанныеСклада.Вставить("ИдентификаторСклада", "");
	ДанныеСклада.Вставить("Склад",               Справочники.Склады.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторОбъектаМаркетплейса,
		|	СоответствияОбъектовМаркетплейсов.НаименованиеОбъектаМаркетплейса КАК НаименованиеОбъектаМаркетплейса,
		|	СоответствияОбъектовМаркетплейсов.Объект1С КАК Склад
		|ИЗ
		|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
		|ГДЕ
		|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись
		|	И СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Склад)";
	
	Запрос.Параметры.Вставить("УчетнаяЗапись",          УчетнаяЗапись);
	Запрос.УстановитьПараметр("ВидОбъектаМаркетплейса", Перечисления.ВидыОбъектовМаркетплейсов.Склад);
	
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	Если ВыборкаДанных.Следующий() Тогда
		ДанныеСклада.Склад               = ВыборкаДанных.Склад;
		ДанныеСклада.ИдентификаторСклада = ВыборкаДанных.ИдентификаторОбъектаМаркетплейса;
	КонецЕсли;
	
	Возврат ДанныеСклада;
	
КонецФункции

// Выбирает учетные записи, для которых разрешен обмен данными.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//                 - Неопределено - выбрать все учетные записи (по умолчанию).
//
// Возвращаемое значение:
//   ВыборкаИзРезультатаЗапроса - результат выборки. Поля выборки соответствуют результату функции ДанныеУчетнойЗаписиЯндексМаркет.
//
Функция ВыбратьУчетныеЗаписиЯндексМаркет(УчетнаяЗапись = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетныеЗаписиМаркетплейсов.Ссылка КАК УчетнаяЗапись,
		|	УчетныеЗаписиМаркетплейсов.Наименование КАК Наименование,
		|	УчетныеЗаписиМаркетплейсов.ПометкаУдаления КАК ПометкаУдаления,
		|	УчетныеЗаписиМаркетплейсов.Организация КАК Организация,
		|	УчетныеЗаписиМаркетплейсов.ИсточникКатегории КАК ИсточникКатегории,
		|	ЕСТЬNULL(ВидыЦенМаркетплейсов.ВидЦены, ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)) КАК ЦенаПродажи,
		|	УчетныеЗаписиМаркетплейсов.СхемаРаботы КАК СхемаРаботы,
		|	УчетныеЗаписиМаркетплейсов.ИдентификаторАккаунта КАК ИдентификаторАккаунта,
		|	УчетныеЗаписиМаркетплейсов.ИдентификаторКабинета КАК ИдентификаторКабинета,
		|	УчетныеЗаписиМаркетплейсов.ИдентификаторМагазина КАК ИдентификаторМагазина,
		|	УчетныеЗаписиМаркетплейсов.ИдентификаторКлиента КАК ИдентификаторКлиента,
		|	УчетныеЗаписиМаркетплейсов.НеОбновлятьДанныеТорговойПлощадки КАК НеОбновлятьДанныеТорговойПлощадки,
		|	ВЫБОР
		|		КОГДА УчетныеЗаписиМаркетплейсов.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|				И УчетныеЗаписиМаркетплейсов.ИдентификаторАккаунта <> """"
		|				И УчетныеЗаписиМаркетплейсов.ИдентификаторКабинета <> """"
		|				И УчетныеЗаписиМаркетплейсов.ИдентификаторМагазина <> """"
		|				И УчетныеЗаписиМаркетплейсов.ИдентификаторКлиента <> """"
		|				И ВидыЦенМаркетплейсов.ВидЦены <> ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ОбязательныеРеквизитыЗаполнены
		|ИЗ
		|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УчетныеЗаписиМаркетплейсов.ВидыЦен КАК ВидыЦенМаркетплейсов
		|		ПО УчетныеЗаписиМаркетплейсов.Ссылка = ВидыЦенМаркетплейсов.Ссылка
		|			И (ВидыЦенМаркетплейсов.ИмяНастройки = &ИмяНастройки)
		|ГДЕ
		|	(УчетныеЗаписиМаркетплейсов.Ссылка = &УчетнаяЗапись
		|			ИЛИ &УчетнаяЗапись = НЕОПРЕДЕЛЕНО)
		|	И УчетныеЗаписиМаркетплейсов.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет)
		|	И НЕ УчетныеЗаписиМаркетплейсов.НеОбновлятьДанныеТорговойПлощадки"; 

	ИмяНастройки = НСтр("ru = 'Цена продажи';
						|en = 'Selling price'");
	Запрос.Параметры.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	Запрос.Параметры.Вставить("ИмяНастройки",  ИмяНастройки);
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВыборкаДанных;
		
КонецФункции

#КонецОбласти

#Область УправлениеКаталогом

// Получает рекомендации по заполнению реквизитов товарной позиции с Яндекс Маркет.
//
// Параметры:
//   УчетнаяЗапись           - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//                           - Неопределено - выполнить регламентное задание по всем учетным записям, с которыми разрешен обмен данными.
//   ДополнительныеПараметры - Произвольный - произвольные данные, переданные в функцию;
//                           - Структура - возможные ключи дополнительных параметров:
//     * ПоРасписанию             - Булево - признак автоматического или ручного запуска регламентного задания;
//     * ИдентификаторПредложения - Строка - идентификатор предложения товарной позиции.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ПолучитьРекомендацииПоСклейкеТовараЯндексМаркет(УчетнаяЗапись = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ПолучениеРекомендацийПоСклейкеТовараЯндексМаркет);
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет") Тогда
		ТекстОшибки = НСтр("ru = 'Интеграция с Яндекс Маркет не используется.';
							|en = 'Integration with Yandex.Market is not used.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
				
		Ошибка.КодОшибки      = "ЯндексМаркет_ИспользоватьИнтеграциюСЯндексМаркет";
		Ошибка.ОписаниеОшибки = ТекстОшибки;

		Возврат Ошибка;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись);
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		
		Если ДанныеАвторизации = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" отсутствует информация об аккаунте.';
					|en = 'There is no information for the ""%1"" account.'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_ОтсутствуетИнформацияОбАккаунте";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
		
		Если ДанныеУчетнойЗаписи.НеОбновлятьДанныеТорговойПлощадки Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" запрещен обмен данными с торговой площадкой.';
					|en = 'Data exchange with the trading platform is not allowed for the ""%1"" account.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_НеОбновлятьДанныеТорговойПлощадки";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
	КонецЕсли;
		
	ПоРасписанию = Истина;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") 
		 И ДополнительныеПараметры.Свойство("ПоРасписанию") Тогда
		ПоРасписанию = ДополнительныеПараметры.ПоРасписанию;
	КонецЕсли;
	
	ИдентификаторПредложения = Неопределено;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда 
		ДополнительныеПараметры.Свойство("ИдентификаторПредложения", ИдентификаторПредложения);
	КонецЕсли;
	
	Если ПоРасписанию Тогда
		Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Запуск по расписанию: получить рекомендации при выгрузке товарного каталога для учетной записи ""%1"" на торговую площадку.';
						|en = 'Run on schedule: receive recommendations when exporting the goods directory to the trading platform for the ""%1"" account.'", 
						ОбщегоНазначения.КодОсновногоЯзыка()),
					УчетнаяЗапись));
		Иначе
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				НСтр("ru = 'Запуск по расписанию: получить рекомендации при выгрузке товарного каталога на торговую площадку.';
					|en = 'Run on schedule: receive recommendations when exporting the goods directory to the trading platform.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецЕсли;
	КонецЕсли;
			
	ВыборкаДанных = ВыбратьУчетныеЗаписиЯндексМаркет(УчетнаяЗапись);
	
	Пока ВыборкаДанных.Следующий() Цикл
		Попытка
			Отказ = Ложь;
			ПредварительныйПросмотрКарточекТоваровИзСервиса(ВыборкаДанных, ИдентификаторПредложения, Отказ);
			
			Если Отказ Тогда
				Ошибка.КодОшибки      = "ЯндексМаркет_API_ПолучитьРекомендацииПоСклейкеТовараЯндексМаркет";
				Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'При получении рекомендаций при выгрузке товарного каталога для учетной записи ""%1"" на торговую площадку возникли ошибки. Подробности см. в журнале регистрации.';
						|en = 'Errors occurred when receiving recommendations upon exporting the goods directory to the trading platform for the ""%1"" account. For more information, see the event log.'"),
					ВыборкаДанных.Наименование);
					
			Иначе
				УстановитьПривилегированныйРежим(Истина);
				СерверныеОповещения.ОтправитьСерверноеОповещение(
					"ИнтеграцияСЯндексМаркетСервер.ОбновитьДанные",
					ВыборкаДанных.УчетнаяЗапись,
					Неопределено,
					Истина);
				УстановитьПривилегированныйРежим(Ложь);
			КонецЕсли;
			
		Исключение
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При получении рекомендаций при выгрузке товарного каталога для учетной записи ""%1"" на торговую площадку возникли ошибки: %2';
					|en = 'Errors occurred when receiving recommendations upon exporting the goods directory to the trading platform for the ""%1"" account: %2'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				ВыборкаДанных.УчетнаяЗапись,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстОшибки);
					
			Ошибка.КодОшибки      = "ЯндексМаркет_ПолучитьРекомендацииПоСклейкеТовараЯндексМаркет";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
		КонецПопытки;
	КонецЦикла;
	
	Если ПоРасписанию Тогда 
		Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Завершено получение рекомендаций при выгрузке товарного каталога для учетной записи ""%1"" на торговую площадку.';
						|en = 'Recommendations are received when exporting the goods directory to the trading platform for the ""%1"" account.'", 
						ОбщегоНазначения.КодОсновногоЯзыка()),
					УчетнаяЗапись));
		Иначе
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				НСтр("ru = 'Завершено получение рекомендаций при выгрузке товарного каталога на торговую площадку.';
					|en = 'Recommendations are received when exporting the goods directory to the trading platform.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Ошибка;
	
КонецФункции

// Отправляет товарные позиции на модерацию.
//
// Параметры:
//   УчетнаяЗапись           - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//                           - Неопределено - выполнить регламентное задание по всем учетным записям, с которыми разрешен обмен данными.
//   ДополнительныеПараметры - Произвольный - произвольные данные, переданные в функцию;
//                           - Структура - возможные ключи дополнительных параметров:
//     * ПоРасписанию          - Булево - признак автоматического или ручного запуска регламентного задания.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ОтправитьНаМодерациюСвязиТоваровЯндексМаркет(УчетнаяЗапись = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ОтправкаНаМодерациюСвязейТоваровЯндексМаркет);
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет") Тогда
		ТекстОшибки = НСтр("ru = 'Интеграция с Яндекс Маркет не используется.';
							|en = 'Integration with Yandex.Market is not used.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
				
		Ошибка.КодОшибки      = "ЯндексМаркет_ИспользоватьИнтеграциюСЯндексМаркет";
		Ошибка.ОписаниеОшибки = ТекстОшибки;

		Возврат Ошибка;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись);
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		
		Если ДанныеАвторизации = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" отсутствует информация об аккаунте.';
					|en = 'There is no information for the ""%1"" account.'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_ОтсутствуетИнформацияОбАккаунте";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
		
		Если ДанныеУчетнойЗаписи.НеОбновлятьДанныеТорговойПлощадки Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" запрещен обмен данными с торговой площадкой.';
					|en = 'Data exchange with the trading platform is not allowed for the ""%1"" account.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_НеОбновлятьДанныеТорговойПлощадки";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
	КонецЕсли;
		
	ПоРасписанию = Истина;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") 
		 И ДополнительныеПараметры.Свойство("ПоРасписанию") Тогда
		ПоРасписанию = ДополнительныеПараметры.ПоРасписанию;
	КонецЕсли;
	
	Если ПоРасписанию Тогда
		Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Запуск по расписанию: отправить товарные позиции на модерацию для учетной записи ""%1"".';
						|en = 'Run on schedule: send the items for moderation for the ""%1"" account.'", 
						ОбщегоНазначения.КодОсновногоЯзыка()),
					УчетнаяЗапись));
		Иначе
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				НСтр("ru = 'Запуск по расписанию: отправить товарные позиции на модерацию.';
					|en = 'Run on schedule: send the items for moderation.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецЕсли;
	КонецЕсли;
			
	ВыборкаДанных = ВыбратьУчетныеЗаписиЯндексМаркет(УчетнаяЗапись);
	
	Пока ВыборкаДанных.Следующий() Цикл
		Попытка     
			Отказ = Ложь;
			ДобавитьИзменитьТоварыВСервисе(ВыборкаДанных, Отказ);
			
			Если Отказ Тогда
				Ошибка.КодОшибки      = "ЯндексМаркет_API_ОтправитьНаМодерациюСвязиТоваровЯндексМаркет";
				Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'При отправке товарных позиций на модерацию для учетной записи ""%1"" возникли ошибки. Подробности см. в журнале регистрации.';
						|en = 'Errors occurred when sending the items for moderation for the ""%1"" account. For more information, see the event log.'"),
					ВыборкаДанных.Наименование);
					
			Иначе
				УстановитьПривилегированныйРежим(Истина);
				СерверныеОповещения.ОтправитьСерверноеОповещение(
					"ИнтеграцияСЯндексМаркетСервер.ОбновитьДанные",
					ВыборкаДанных.УчетнаяЗапись,
					Неопределено,
					Истина);
				УстановитьПривилегированныйРежим(Ложь);
			КонецЕсли;
			
		Исключение
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При отправке товарных позиций на модерацию для учетной записи ""%1"" возникли ошибки: %2';
					|en = 'Errors occurred when sending the items for moderation for the ""%1"" account: %2'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				ВыборкаДанных.УчетнаяЗапись,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстОшибки);
					
			Ошибка.КодОшибки      = "ЯндексМаркет_ОтправитьНаМодерациюСвязиТоваровЯндексМаркет";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
		КонецПопытки;
	КонецЦикла;
	
	Если ПоРасписанию Тогда 
		Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Завершена отправка товарных позиций на модерацию для учетной записи ""%1"".';
						|en = 'The items are sent for moderation for the ""%1"" account.'", 
						ОбщегоНазначения.КодОсновногоЯзыка()),
					УчетнаяЗапись));
		Иначе
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				НСтр("ru = 'Завершена отправка товарных позиций на модерацию.';
					|en = 'The items are sent for moderation.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Ошибка;

КонецФункции

// Получает статусы модерации товарных позиций с торговой площадки.
//
// Параметры:
//   УчетнаяЗапись           - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//                           - Неопределено - выполнить регламентное задание по всем учетным записям, с которыми разрешен обмен данными.
//   ДополнительныеПараметры - Произвольный - произвольные данные, переданные в функцию;
//                           - Структура - возможные ключи дополнительных параметров:
//     * ПоРасписанию          - Булево - признак автоматического или ручного запуска регламентного задания.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ПолучитьСтатусыМодерацииТоваровЯндексМаркет(УчетнаяЗапись = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ПолучениеСтатусовМодерацииТоваровЯндексМаркет);
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет") Тогда
		ТекстОшибки = НСтр("ru = 'Интеграция с Яндекс Маркет не используется.';
							|en = 'Integration with Yandex.Market is not used.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
				
		Ошибка.КодОшибки      = "ЯндексМаркет_ИспользоватьИнтеграциюСЯндексМаркет";
		Ошибка.ОписаниеОшибки = ТекстОшибки;

		Возврат Ошибка;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись);
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		
		Если ДанныеАвторизации = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" отсутствует информация об аккаунте.';
					|en = 'There is no information for the ""%1"" account.'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_ОтсутствуетИнформацияОбАккаунте";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
		
		Если ДанныеУчетнойЗаписи.НеОбновлятьДанныеТорговойПлощадки Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" запрещен обмен данными с торговой площадкой.';
					|en = 'Data exchange with the trading platform is not allowed for the ""%1"" account.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_НеОбновлятьДанныеТорговойПлощадки";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
	КонецЕсли;
		
	ПоРасписанию = Истина;
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") 
		 И ДополнительныеПараметры.Свойство("ПоРасписанию") Тогда
		ПоРасписанию = ДополнительныеПараметры.ПоРасписанию;
	КонецЕсли;
	
	Если ПоРасписанию Тогда
		Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Запуск по расписанию: получение статусов модерации товаров для учетной записи ""%1"" с торговой площадки.';
						|en = 'Run on schedule: receive item moderation statuses for the ""%1"" account from the trading platform.'", 
						ОбщегоНазначения.КодОсновногоЯзыка()),
					УчетнаяЗапись));
		Иначе
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				НСтр("ru = 'Запуск по расписанию: получение статусов модерации товаров с торговой площадки.';
					|en = 'Run on schedule: receive item moderation statuses from the trading platform.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецЕсли;
	КонецЕсли;
					
	ВыборкаДанных = ВыбратьУчетныеЗаписиЯндексМаркет(УчетнаяЗапись);
	
	Пока ВыборкаДанных.Следующий() Цикл
		Попытка
			Отказ = Ложь;
			ИнформацияОТоварахВКаталогеИзСервиса(ВыборкаДанных, Отказ);
			
			Если Отказ Тогда
				Ошибка.КодОшибки      = "ЯндексМаркет_API_ПолучитьСтатусыМодерацииТоваровЯндексМаркет";
				Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'При получении статусов модерации товаров для учетной записи ""%1"" с торговой площадки возникли ошибки. Подробности см. в журнале регистрации.';
						|en = 'Errors occurred when receiving item moderation statuses for the ""%1"" account from the trading platform. For more information, see the event log.'"),
					ВыборкаДанных.УчетнаяЗапись);
					
			Иначе
				УстановитьПривилегированныйРежим(Истина);
				СерверныеОповещения.ОтправитьСерверноеОповещение(
					"ИнтеграцияСЯндексМаркетСервер.ОбновитьДанные",
					ВыборкаДанных.УчетнаяЗапись,
					Неопределено,
					Истина);
				УстановитьПривилегированныйРежим(Ложь);
			КонецЕсли;
			
		Исключение
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При получении статусов модерации товаров для учетной записи ""%1"" с торговой площадки возникли ошибки: %2';
					|en = 'Errors occurred when receiving item moderation statuses for the ""%1"" account from the trading platform: %2'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				ВыборкаДанных.УчетнаяЗапись,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстОшибки);
					
			Ошибка.КодОшибки      = "ЯндексМаркет_ПолучитьСтатусыМодерацииТоваровЯндексМаркет";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
		КонецПопытки;
	КонецЦикла;
	
	Если ПоРасписанию Тогда
		Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Завершено получение статусов модерации товаров для учетной записи ""%1"" с торговой площадки.';
						|en = 'Item moderation statuses for the ""%1"" account are received from the trading platform.'", 
						ОбщегоНазначения.КодОсновногоЯзыка()),
					УчетнаяЗапись));
		Иначе
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				НСтр("ru = 'Завершено получение статусов модерации товаров с торговой площадки.';
					|en = 'Item moderation statuses are received from the trading platform.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Ошибка;

КонецФункции

// Определяет категорию товара в зависимости от настройки учетной записи.
//
// Параметры:
//   Номенклатура  - СправочникСсылка.Номенклатура - ссылка на номенклатуру, для которой определяется категория;
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//
// Возвращаемое значение:
//   Строка - наименование категории товара.
//
Функция КатегорияТовара(Номенклатура, УчетнаяЗапись) Экспорт 
	
	ИспользоватьТоварныеКатегории   = ПолучитьФункциональнуюОпцию("ИспользоватьТоварныеКатегории");
	ДанныеУчетнойЗаписиЯндексМаркет = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись, Ложь);
	ИсточникКатегории               = ДанныеУчетнойЗаписиЯндексМаркет.ИсточникКатегории;
	КатегорияТовараСтрокой          = "";
	
	Если ИсточникКатегории = Перечисления.ИсточникиКатегорийДляМаркетплейса.ВидНоменклатуры Тогда
		КатегорияТовара        = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВидНоменклатуры");
		КатегорияТовараСтрокой = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КатегорияТовара, "Наименование");

	ИначеЕсли ИсточникКатегории = Перечисления.ИсточникиКатегорийДляМаркетплейса.ИерархияНоменклатуры Тогда
		КатегорияТовара        = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "Родитель");
		КатегорияТовараСтрокой = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КатегорияТовара, "Наименование");
			
	ИначеЕсли ИсточникКатегории = Перечисления.ИсточникиКатегорийДляМаркетплейса.ТоварнаяКатегория
			  И ИспользоватьТоварныеКатегории Тогда
		КатегорияТовара        = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ТоварнаяКатегория");
		КатегорияТовараСтрокой = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КатегорияТовара, "Наименование");
		
	Иначе
		КатегорияТовараСтрокой = "";
	КонецЕсли;
	
	Возврат КатегорияТовараСтрокой;
	
КонецФункции

// Определяет весогабаритные характеристики для упаковки номенклатуры.
//
// Параметры:
//   Упаковка                     - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка товарной позиции;
//   Номенклатура                 - СправочникСсылка.Номенклатура - номенклатура товарной позиции;
//   ОтличатьВесогабаритыУпаковки - Булево - необходимость указания признака упаковки.
//
// Возвращаемое значение:
//   Структура - весогабаритные характеристики:
//	   * length  - Строка - длина;
//	   * width   - Строка - ширина;
//	   * height  - Строка - высота;
//	   * weight  - Строка - вес;
//	   * package - Число - признак базовой единицы измерения или упаковки номенклатуры (0/1).
//
Функция ПолучитьВесогабариты(Упаковка, Номенклатура, ОтличатьВесогабаритыУпаковки = Ложь) Экспорт
	
	Если ОтличатьВесогабаритыУпаковки Тогда
		СтруктураВесогабаритов = Новый Структура("length, width, height, weight, package");
	Иначе
		СтруктураВесогабаритов = Новый Структура("length, width, height, weight");
	КонецЕсли;
	
	СтруктураВесогабаритов.length = "0"; 
	СтруктураВесогабаритов.width  = "0"; 
	СтруктураВесогабаритов.height = "0"; 
	СтруктураВесогабаритов.weight = "0";
	
	Если ОтличатьВесогабаритыУпаковки Тогда
		СтруктураВесогабаритов.package = 0;  
	КонецЕсли;
	
	ЕдиницаИзмеренияНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения");
	
	Если ЗначениеЗаполнено(Упаковка)
		 И Упаковка <> ЕдиницаИзмеренияНоменклатуры Тогда 
		ВесогабаритыУпаковки = 1;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	УпаковкиЕдиницыИзмерения.Вес КАК Вес,
			|	УпаковкиЕдиницыИзмерения.Высота КАК Высота,
			|	УпаковкиЕдиницыИзмерения.Глубина КАК Длина,
			|	УпаковкиЕдиницыИзмерения.Ширина КАК Ширина,
			|	УпаковкиЕдиницыИзмерения.ВесЕдиницаИзмерения КАК ВесЕдиницаИзмерения,
			|	УпаковкиЕдиницыИзмерения.ГлубинаЕдиницаИзмерения КАК ДлинаЕдиницаИзмерения,
			|	УпаковкиЕдиницыИзмерения.ШиринаЕдиницаИзмерения КАК ШиринаЕдиницаИзмерения,
			|	УпаковкиЕдиницыИзмерения.ВысотаЕдиницаИзмерения КАК ВысотаЕдиницаИзмерения
			|ИЗ
			|	Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
			|ГДЕ
			|	УпаковкиЕдиницыИзмерения.Ссылка = &УпаковкаЕдиницаИзмерения
			|	И УпаковкиЕдиницыИзмерения.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Упаковка)";
		
		Запрос.Параметры.Вставить("УпаковкаЕдиницаИзмерения", Упаковка);
		ВыборкаДанных = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаДанных.Следующий() Цикл   
			СокращенияЕдИзмДлина = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаДанных.ДлинаЕдиницаИзмерения, "МеждународноеСокращение");
			Если СокращенияЕдИзмДлина = "MTR" Тогда //м 
				Длина = ВыборкаДанных.Длина * 100;  
			ИначеЕсли СокращенияЕдИзмДлина = "KMT" Тогда //км 
				Длина = ВыборкаДанных.Длина * 100000;
			ИначеЕсли СокращенияЕдИзмДлина = "CMT" Тогда //см
				Длина = ВыборкаДанных.Длина;
			ИначеЕсли СокращенияЕдИзмДлина = "MMT" Тогда //мм
				Длина = ВыборкаДанных.Длина/10; 
			Иначе
				Длина = 0; 
			КонецЕсли;
			
			СокращенияЕдИзмШирина = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаДанных.ШиринаЕдиницаИзмерения, "МеждународноеСокращение");
			Если СокращенияЕдИзмШирина = "MTR" Тогда //м 
				Ширина = ВыборкаДанных.Ширина * 100;  
			ИначеЕсли СокращенияЕдИзмШирина = "KMT" Тогда //км 
				Ширина = ВыборкаДанных.Ширина * 100000;
			ИначеЕсли СокращенияЕдИзмШирина = "CMT" Тогда //см
				Ширина = ВыборкаДанных.Ширина; 
			ИначеЕсли СокращенияЕдИзмШирина = "MMT" Тогда //мм
				Ширина = ВыборкаДанных.Ширина/10; 
			Иначе
				Ширина = 0;
			КонецЕсли;   
			
			СокращенияЕдИзмВысота = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаДанных.ВысотаЕдиницаИзмерения, "МеждународноеСокращение");
			Если СокращенияЕдИзмВысота = "MTR" Тогда //м 
				Высота = ВыборкаДанных.Высота * 100;  
			ИначеЕсли СокращенияЕдИзмВысота = "KMT" Тогда //км 
				Высота = ВыборкаДанных.Высота * 100000;
			ИначеЕсли СокращенияЕдИзмВысота = "CMT" Тогда //см
				Высота = ВыборкаДанных.Высота;
			ИначеЕсли СокращенияЕдИзмВысота = "MMT" Тогда //мм
				Высота = ВыборкаДанных.Высота/10;  	
			Иначе
				Высота = 0;
			КонецЕсли;
			
			СокращенияЕдИзмВес = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаДанных.ВесЕдиницаИзмерения, "МеждународноеСокращение");
			Если СокращенияЕдИзмВес = "GRM" Тогда //г 
				Вес = ВыборкаДанных.Вес / 1000;  
			ИначеЕсли СокращенияЕдИзмВес = "CTM" Тогда //кар 
				Вес = ВыборкаДанных.Вес / 5000;
			ИначеЕсли СокращенияЕдИзмВес = "KGM" Тогда //кг
				Вес = ВыборкаДанных.Вес;   
			ИначеЕсли СокращенияЕдИзмВес = "TNE" Тогда //т
				Вес = ВыборкаДанных.Вес * 1000;
			Иначе 
				Вес = 0;
			КонецЕсли;
		КонецЦикла;   
		
	Иначе 
		ВесогабаритыУпаковки = 0;
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Номенклатура.ВесЕдиницаИзмерения КАК ВесЕдиницаИзмерения,
			|	Номенклатура.ВесЧислитель КАК ВесЧислитель,
			|	Номенклатура.ВесЗнаменатель КАК ВесЗнаменатель,
			|	Номенклатура.ВесИспользовать КАК ВесИспользовать,
			|	Номенклатура.ДлинаЕдиницаИзмерения КАК ДлинаЕдиницаИзмерения,
			|	Номенклатура.ДлинаЧислитель КАК ДлинаЧислитель,
			|	Номенклатура.ДлинаЗнаменатель КАК ДлинаЗнаменатель,
			|	Номенклатура.ДлинаИспользовать КАК ДлинаИспользовать,
			|	Номенклатура.ПлощадьЕдиницаИзмерения КАК ПлощадьЕдиницаИзмерения,
			|	Номенклатура.ПлощадьЧислитель КАК ПлощадьЧислитель,
			|	Номенклатура.ПлощадьЗнаменатель КАК ПлощадьЗнаменатель,
			|	Номенклатура.ПлощадьИспользовать КАК ПлощадьИспользовать,
			|	Номенклатура.ОбъемЕдиницаИзмерения КАК ОбъемЕдиницаИзмерения,
			|	Номенклатура.ОбъемЧислитель КАК ОбъемЧислитель,
			|	Номенклатура.ОбъемЗнаменатель КАК ОбъемЗнаменатель,
			|	Номенклатура.ОбъемИспользовать КАК ОбъемИспользовать
			|ИЗ
			|	Справочник.Номенклатура КАК Номенклатура
			|ГДЕ
			|	Номенклатура.Ссылка = &Номенклатура";  
		
		Запрос.Параметры.Вставить("Номенклатура", Номенклатура);  
		ВыборкаДанных = Запрос.Выполнить().Выбрать();
		
		Пока ВыборкаДанных.Следующий() Цикл  
			Если ВыборкаДанных.ВесИспользовать Тогда 
				СокращенияЕдИзмВес = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаДанных.ВесЕдиницаИзмерения, "МеждународноеСокращение");
				Если СокращенияЕдИзмВес = "GRM" Тогда //г   
					ВесЧислитель   = ВыборкаДанных.ВесЧислитель / 1000; 
					ВесЗнаменатель = ВыборкаДанных.ВесЗнаменатель; 
					Вес            = ВесЧислитель / ВесЗнаменатель;
				ИначеЕсли СокращенияЕдИзмВес = "CTM" Тогда //кар 
					ВесЧислитель   = ВыборкаДанных.ВесЧислитель / 5000; 
					ВесЗнаменатель = ВыборкаДанных.ВесЗнаменатель;
					Вес            = ВесЧислитель / ВесЗнаменатель;
				ИначеЕсли СокращенияЕдИзмВес = "KGM" Тогда //кг        
					ВесЧислитель   = ВыборкаДанных.ВесЧислитель;
					ВесЗнаменатель = ВыборкаДанных.ВесЗнаменатель; 
					Вес            = ВесЧислитель / ВесЗнаменатель;
				ИначеЕсли СокращенияЕдИзмВес = "TNE" Тогда //т
					ВесЧислитель   = ВыборкаДанных.ВесЧислитель * 1000;
					ВесЗнаменатель = ВыборкаДанных.ВесЗнаменатель;
					Вес            = ВесЧислитель / ВесЗнаменатель;
				Иначе 
					Вес = 0;
				КонецЕсли;
			Иначе  
				Вес = 0;
			КонецЕсли;
			
			Если ВыборкаДанных.ДлинаИспользовать Тогда
				СокращенияЕдИзмДлина = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаДанных.ДлинаЕдиницаИзмерения, "МеждународноеСокращение");
				Если СокращенияЕдИзмДлина = "MTR" Тогда //м 
					ДлинаЧислитель   = ВыборкаДанных.ДлинаЧислитель * 100; 
					ДлинаЗнаменатель = ВыборкаДанных.ДлинаЗнаменатель; 
					Длина            = ДлинаЧислитель / ДлинаЗнаменатель;
				ИначеЕсли СокращенияЕдИзмДлина = "KMT" Тогда //км 
					ДлинаЧислитель   = ВыборкаДанных.ДлинаЧислитель * 100000; 
					ДлинаЗнаменатель = ВыборкаДанных.ДлинаЗнаменатель;
					Длина            = ДлинаЧислитель / ДлинаЗнаменатель;
				ИначеЕсли СокращенияЕдИзмДлина = "CMT" Тогда //см
					ДлинаЧислитель   = ВыборкаДанных.ДлинаЧислитель;  
					ДлинаЗнаменатель = ВыборкаДанных.ДлинаЗнаменатель;
					Длина            = ДлинаЧислитель / ДлинаЗнаменатель;
				Иначе
					Длина = 0; 
				КонецЕсли;
			Иначе
				Длина = 0;
			КонецЕсли;
			
			Если ВыборкаДанных.ПлощадьИспользовать 
				 И Длина <> 0 Тогда      
				СокращенияЕдИзмПлощадь = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаДанных.ПлощадьЕдиницаИзмерения, "МеждународноеСокращение");
				Если СокращенияЕдИзмПлощадь = "MTK" Тогда //м2 
					ПлощадьЧислитель   = ВыборкаДанных.ПлощадьЧислитель;  
					ПлощадьЗнаменатель = ВыборкаДанных.ПлощадьЗнаменатель;
					Площадь            = ПлощадьЧислитель / ПлощадьЗнаменатель;  
					ПлощадьВСм2 	   = Площадь*10000;
					Ширина             = ПлощадьВСм2 /Длина;
				ИначеЕсли СокращенияЕдИзмПлощадь = "CMK" Тогда //см2 
					ПлощадьЧислитель   = ВыборкаДанных.ПлощадьЧислитель;  
					ПлощадьЗнаменатель = ВыборкаДанных.ПлощадьЗнаменатель;
					Площадь            = ПлощадьЧислитель / ПлощадьЗнаменатель;    
					Ширина = Площадь / Длина;
				Иначе
					Ширина = 0; 
				КонецЕсли; 
			Иначе 
				Ширина = 0;
			КонецЕсли;
			
			Если ВыборкаДанных.ОбъемИспользовать 
				 И Длина <> 0 
				 И Ширина <> 0 Тогда  
				СокращенияЕдИзмОбъем = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаДанных.ОбъемЕдиницаИзмерения, "МеждународноеСокращение");
				Если СокращенияЕдИзмОбъем = "MTQ" Тогда //м3 
					ОбъемЧислитель   = ВыборкаДанных.ОбъемЧислитель;  
					ОбъемЗнаменатель = ВыборкаДанных.ОбъемЗнаменатель;
					Объем            = ОбъемЧислитель / ОбъемЗнаменатель;
					ОбъемВСм3        = Объем*1000000;
					Высота           = ОбъемВСм3 /(Длина*Ширина);
				ИначеЕсли СокращенияЕдИзмОбъем = "CMQ" Тогда //см3 
					ОбъемЧислитель   = ВыборкаДанных.ОбъемЧислитель;  
					ОбъемЗнаменатель = ВыборкаДанных.ОбъемЗнаменатель;
					Объем            = ОбъемЧислитель / ОбъемЗнаменатель; 
					Высота           = Объем /(Длина*Ширина);
				ИначеЕсли СокращенияЕдИзмОбъем = "LTR" Тогда //л (дм3) 
					ОбъемЧислитель   = ВыборкаДанных.ОбъемЧислитель;
					ОбъемЗнаменатель = ВыборкаДанных.ОбъемЗнаменатель;
					ОбъемВСм3        = Объем*1000;
					Высота           = ОбъемВСм3 /(Длина*Ширина);
				Иначе
					Высота = 0; 
				КонецЕсли; 
			Иначе 
				Высота = 0;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	СтруктураВесогабаритов.length = ?(ЗначениеЗаполнено(Длина),  Формат(Длина, "ЧДЦ=2; ЧРД=."),  "0");
	СтруктураВесогабаритов.width  = ?(ЗначениеЗаполнено(Ширина), Формат(Ширина, "ЧДЦ=2; ЧРД=."), "0"); 
	СтруктураВесогабаритов.height = ?(ЗначениеЗаполнено(Высота), Формат(Высота, "ЧДЦ=2; ЧРД=."), "0"); 
	СтруктураВесогабаритов.weight = ?(ЗначениеЗаполнено(Вес),    Формат(Вес, "ЧДЦ=2; ЧРД=."),    "0");
	
	Если ОтличатьВесогабаритыУпаковки Тогда
		СтруктураВесогабаритов.package = ВесогабаритыУпаковки;
	КонецЕсли;
	
	Возврат СтруктураВесогабаритов;
	
КонецФункции

// Добавляет выбранный ассортимент магазина-источника в магазин приемник.
//
// Параметры:
//   МагазинИсточник               - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись источник ассортимента;
//   МагазинПриемник               - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись приемник ассортимента;
//   ПараметрыКомпоновщикаНастроек - см. ПараметрыДобавленияАссортимента.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ДобавитьАссортимент(МагазинИсточник, МагазинПриемник, ПараметрыКомпоновщикаНастроек) Экспорт

	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ИнтеграцияСЯндексМаркетСервер.ДобавитьАссортимент");

	СхемаКомпоновкиДанных                = ПараметрыКомпоновщикаНастроек.СхемаКомпоновкиДанных;
	НастройкиКомпоновщика                = ПараметрыКомпоновщикаНастроек.НастройкиКомпоновкиДанных;
	ИдентификаторыПредложенияВыбранные   = ПараметрыКомпоновщикаНастроек.ИдентификаторыПредложенияВыбранные;
	ИдентификаторыПредложенияИсключенные = ПараметрыКомпоновщикаНастроек.ИдентификаторыПредложенияИсключенные;

	Если ИдентификаторыПредложенияВыбранные <> Неопределено Тогда
		КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(НастройкиКомпоновщика.Отбор, "ИдентификаторПредложения",
			ИдентификаторыПредложенияВыбранные, ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;

	Если ИдентификаторыПредложенияИсключенные <> Неопределено Тогда
		КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(НастройкиКомпоновщика.Отбор, "ИдентификаторПредложения",
			ИдентификаторыПредложенияИсключенные, ВидСравненияКомпоновкиДанных.НеВСписке);
	КонецЕсли;

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;

	МакетКомпоновки =
			КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновщика,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);

	ТаблицаАссортимента = Новый ТаблицаЗначений;

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаАссортимента);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);

	ИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	ИспользоватьУпаковкиНоменклатуры       = ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");
	
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет");
		ЭлементБлокировкиДанных.УстановитьЗначение("УчетнаяЗапись", МагазинПриемник);
		ЭлементБлокировкиДанных.ИсточникДанных = ТаблицаАссортимента;
		ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
		Если ИспользоватьХарактеристикиНоменклатуры Тогда
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
		КонецЕсли;
		Если ИспользоватьУпаковкиНоменклатуры Тогда
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Упаковка", "Упаковка");
		КонецЕсли;
		БлокировкаДанных.Заблокировать();

		НаборЗаписей = РегистрыСведений.СтатусыПубликацииТоваровЯндексМаркет.СоздатьНаборЗаписей(); 
		НаборЗаписей.Отбор.УчетнаяЗапись.Установить(МагазинПриемник);
		
		Для Каждого СтрокаТаблицыЗначений Из ТаблицаАссортимента Цикл
			Если Не СтрокаТаблицыЗначений.ЗначениеФлагаПометкаУстановлена Тогда
				Продолжить;
			КонецЕсли;

			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицыЗначений,, "УчетнаяЗапись");
			Запись.УчетнаяЗапись = МагазинПриемник;
			
			Если Запись.Статус = ПредопределенноеЗначение("Перечисление.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияПройдена") Тогда
				Запись.Статус = ПредопределенноеЗначение("Перечисление.СтатусыВыгрузкиТоваровЯндексМаркет.УтвержденаРекомендация");
			КонецЕсли;
		КонецЦикла;
		
		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.Записать(Ложь);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить копирование ассортимента из магазина ""%1"" в магазин ""%2"" по причине: %3.';
				|en = 'Cannot copy the product range from the ""%1"" store to the ""%2"" store. Reason: %3.'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			МагазинИсточник,
			МагазинПриемник,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
				
		Ошибка.КодОшибки      = "ЯндексМаркет_ДобавлениеАссортиментаИзМагазинаИсточника";
		Ошибка.ОписаниеОшибки = ТекстОшибки;
	КонецПопытки;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ТаблицаАссортимента.Количество() / 1000);

	УстановитьПривилегированныйРежим(Истина);
	СерверныеОповещения.ОтправитьСерверноеОповещение(
		"ИнтеграцияСЯндексМаркетСервер.ОбновитьДанные",
		МагазинПриемник,
		Неопределено,
		Истина);
	УстановитьПривилегированныйРежим(Ложь);
		
	Возврат Ошибка;

КонецФункции

// Конструктор параметров добавления ассортимента.
//
// Возвращаемое значение:
//   Структура - параметры для копирования ассортимента:
//     * НастройкиКомпоновкиДанных            - НастройкиКомпоновкиДанных - используемые в динамическом списке настройки компоновки данных.
//     * СхемаКомпоновкиДанных                - СхемаКомпоновкиДанных - используемая в динамическом списке схема компоновки данных.
//     * ИдентификаторыПредложенияВыбранные   - СписокЗначений Из Строка, Неопределено - список идентификаторов публикации,
//                                                которые выбраны для копирования.
//     * ИдентификаторыПредложенияИсключенные - СписокЗначений Из Строка, Неопределено - список идентификаторов публикации,
//                                                которые исключаются из копирования.
//
Функция ПараметрыДобавленияАссортимента() Экспорт

	ПараметрыКомпоновщикаНастроек = Новый Структура;
	ПараметрыКомпоновщикаНастроек.Вставить("НастройкиКомпоновкиДанных",            Новый НастройкиКомпоновкиДанных);
	ПараметрыКомпоновщикаНастроек.Вставить("СхемаКомпоновкиДанных",                Новый СхемаКомпоновкиДанных);
	ПараметрыКомпоновщикаНастроек.Вставить("ИдентификаторыПредложенияВыбранные",   Неопределено);
	ПараметрыКомпоновщикаНастроек.Вставить("ИдентификаторыПредложенияИсключенные", Неопределено);

	Возврат ПараметрыКомпоновщикаНастроек;

КонецФункции

// Заполняет товарный каталог магазина по данным прайс-листа.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//   ЦенаПродажи   - СправочникСсылка.ВидыЦен - вид цены продажи.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ЗаполнитьПоДаннымПрайсЛиста(УчетнаяЗапись, ЦенаПродажи) Экспорт
	
	Ошибка       = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	МассивОшибок = Новый Массив;
	
	ИспользуетсяЦенообразование25          = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25(); 
	ИспользоватьУпаковкиНоменклатуры       = ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");
	ИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ИнтеграцияСЯндексМаркетСервер.ЗаполнитьПоДаннымПрайсЛиста");
			
	Запрос = Новый Запрос;

	Если ИспользуетсяЦенообразование25 Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
			|	ЕСТЬNULL(ХарактеристикиНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
			|	ВЫБОР
			|		КОГДА ЦеныНоменклатурыСрезПоследних.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|			ТОГДА ЦеныНоменклатурыСрезПоследних.Номенклатура.ЕдиницаИзмерения
			|		ИНАЧЕ ЦеныНоменклатурыСрезПоследних.Упаковка
			|	КОНЕЦ КАК Упаковка,
			|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
			|ИЗ
			|	РегистрСведений.ЦеныНоменклатуры25.СрезПоследних(&ТекущаяДата, ВидЦены = &ЦенаПродажи) КАК ЦеныНоменклатурыСрезПоследних
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
			|		ПО (ХарактеристикиНоменклатуры.Владелец = ВЫБОР
			|				КОГДА ЦеныНоменклатурыСрезПоследних.Номенклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры)
			|					ТОГДА ЦеныНоменклатурыСрезПоследних.Номенклатура.ВидНоменклатуры
			|				КОГДА ЦеныНоменклатурыСрезПоследних.Номенклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)
			|					ТОГДА ЦеныНоменклатурыСрезПоследних.Номенклатура
			|				КОГДА ЦеныНоменклатурыСрезПоследних.Номенклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры)
			|					ТОГДА ЦеныНоменклатурыСрезПоследних.Номенклатура.ВладелецХарактеристик
			|				ИНАЧЕ НЕОПРЕДЕЛЕНО
			|			КОНЕЦ)
			|			И ЦеныНоменклатурыСрезПоследних.ХарактеристикаЦО = ХарактеристикиНоменклатуры.ХарактеристикаНоменклатурыДляЦенообразования
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
			|		ПО (СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись)
			|			И ЦеныНоменклатурыСрезПоследних.Номенклатура = СтатусыПубликацииТоваровЯндексМаркет.Номенклатура
			|			И (ЕСТЬNULL(ХарактеристикиНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) = СтатусыПубликацииТоваровЯндексМаркет.Характеристика)
			|ГДЕ
			|	СтатусыПубликацииТоваровЯндексМаркет.Номенклатура ЕСТЬ NULL";
		
	Иначе
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
			|	ЦеныНоменклатурыСрезПоследних.Характеристика КАК Характеристика,
			|	ВЫБОР
			|		КОГДА ЦеныНоменклатурыСрезПоследних.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|			ТОГДА ЦеныНоменклатурыСрезПоследних.Номенклатура.ЕдиницаИзмерения
			|		ИНАЧЕ ЦеныНоменклатурыСрезПоследних.Упаковка
			|	КОНЕЦ КАК Упаковка,
			|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
			|ИЗ
			|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ТекущаяДата, ВидЦены = &ЦенаПродажи) КАК ЦеныНоменклатурыСрезПоследних
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
			|		ПО (СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись)
			|			И ЦеныНоменклатурыСрезПоследних.Номенклатура = СтатусыПубликацииТоваровЯндексМаркет.Номенклатура
			|			И ЦеныНоменклатурыСрезПоследних.Характеристика = СтатусыПубликацииТоваровЯндексМаркет.Характеристика
			|ГДЕ
			|	СтатусыПубликацииТоваровЯндексМаркет.Номенклатура ЕСТЬ NULL";
	КонецЕсли;
	
	Запрос.Параметры.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	Запрос.Параметры.Вставить("ЦенаПродажи",   ЦенаПродажи);
	Запрос.Параметры.Вставить("ТекущаяДата",   ТекущаяДатаСеанса());
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока ВыборкаДанных.Следующий() Цикл
		НачатьТранзакцию();
		
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет");
			ЭлементБлокировкиДанных.УстановитьЗначение("УчетнаяЗапись",  УчетнаяЗапись);
			ЭлементБлокировкиДанных.УстановитьЗначение("Номенклатура",   ВыборкаДанных.Номенклатура);
			Если ИспользоватьХарактеристикиНоменклатуры Тогда
				ЭлементБлокировкиДанных.УстановитьЗначение("Характеристика", ВыборкаДанных.Характеристика);
			КонецЕсли;
			Если ИспользоватьУпаковкиНоменклатуры Тогда
				ЭлементБлокировкиДанных.УстановитьЗначение("Упаковка", ВыборкаДанных.Упаковка);
			КонецЕсли;
			БлокировкаДанных.Заблокировать();
	
			НаборЗаписей = РегистрыСведений.СтатусыПубликацииТоваровЯндексМаркет.СоздатьНаборЗаписей(); 
			НаборЗаписей.Отбор.УчетнаяЗапись.Установить(УчетнаяЗапись);
			НаборЗаписей.Отбор.Номенклатура.Установить(ВыборкаДанных.Номенклатура);
			Если ИспользоватьХарактеристикиНоменклатуры Тогда
				НаборЗаписей.Отбор.Характеристика.Установить(ВыборкаДанных.Характеристика);
			КонецЕсли;                                 
			Если ИспользоватьУпаковкиНоменклатуры Тогда
				НаборЗаписей.Отбор.Упаковка.Установить(ВыборкаДанных.Упаковка);
			КонецЕсли;
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() > 0 Тогда
				Запись             = НаборЗаписей[0];
				Запись.ЦенаПродажи = ВыборкаДанных.Цена;
				
			Иначе 
				Запись                = НаборЗаписей.Добавить(); 
				Запись.УчетнаяЗапись  = УчетнаяЗапись; 
				Запись.Номенклатура   = ВыборкаДанных.Номенклатура; 
				Запись.ЦенаПродажи    = ВыборкаДанных.Цена;
				
				Если ИспользоватьХарактеристикиНоменклатуры Тогда
					Запись.Характеристика = ВыборкаДанных.Характеристика; 
				КонецЕсли;
				Если ИспользоватьУпаковкиНоменклатуры Тогда
					Запись.Упаковка = ВыборкаДанных.Упаковка; 
				КонецЕсли;
			КонецЕсли; 
			
			НаборЗаписей.Записать();
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось записать товарную позицию %1, %2, %3';
					|en = 'Cannot save the item: %1, %2, %3'", ОбщегоНазначения.КодОсновногоЯзыка()),
				ВыборкаДанных.Номенклатура,
				ВыборкаДанных.Характеристика,
				ВыборкаДанных.Упаковка);

			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстСообщения);

			МассивОшибок.Добавить(ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ВыборкаДанных.Количество() / 1000);

	Если МассивОшибок.Количество() > 0 Тогда
		Ошибка.КодОшибки      = "ЯндексМаркет_ОбновитьПоДаннымПрайсЛиста";
		Ошибка.ОписаниеОшибки = СтрСоединить(МассивОшибок, Символы.ПС);
	КонецЕсли;

	Возврат Ошибка;

КонецФункции

// Выполняет загрузку товарного каталога с торговой площадки.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ИмпортироватьТоварныйКаталог(УчетнаяЗапись) Экспорт
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет") Тогда
		ТекстОшибки = НСтр("ru = 'Интеграция с Яндекс Маркет не используется.';
							|en = 'Integration with Yandex.Market is not used.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
				
		Ошибка.КодОшибки      = "ЯндексМаркет_ИспользоватьИнтеграциюСЯндексМаркет";
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНоменклатуруПартнеров") Тогда
		ТекстОшибки = НСтр("ru = 'Для импорта данных о товарах из личного кабинета Яндекс Маркет включите возможность использования номенклатуры поставщиков в настройках раздела ""НСИ и администрирование"" - ""Настройка НСИ и разделов"" - ""Номенклатура"" - ""Дополнительная информация"".';
							|en = 'To import goods data from the Yandex.Market personal account, enable vendor part numbers in Master data and settings - Master data and sections - Items - Additional information.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
			
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
				
		Ошибка.КодОшибки      = "ЯндексМаркет_ИмпортироватьТоварныйКаталог";
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись, Ложь);
	ПараметрыЗапроса    = НовыеПараметрыЗапросаИмпортаТоварногоКаталога();
	УчетныеЗаписи       = Неопределено;
	
	Пока Истина Цикл
		Попытка
			Отказ = Ложь;
			ТаблицаТоваров = ИмпортироватьПорциюТоварногоКаталогаИзСервиса(ДанныеУчетнойЗаписи, 
				ПараметрыЗапроса,
				УчетныеЗаписи,
				Отказ);
			
			Если Отказ Тогда
				Ошибка.КодОшибки      = "ЯндексМаркет_API_ИмпортироватьТоварныйКаталог";
				Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'При импорте товарного каталога с торговой площадки для кабинета ""%1"" возникли ошибки. Подробности см. в журнале регистрации.';
						|en = 'Errors occurred while importing the goods directory from the trading platform for the %1 account. For more information, see the event log.'"),
					ДанныеУчетнойЗаписи.ИдентификаторКабинета);
				Прервать;
				
			Иначе
				ЗаписатьПорциюТоварногоКаталога(ТаблицаТоваров, Истина);
				
				Если Не ЗначениеЗаполнено(ПараметрыЗапроса.ИдентификаторСтраницы)
						Или ТаблицаТоваров.Количество() < ПараметрыЗапроса.КоличествоНаСтранице Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
		
		Исключение
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При импорте товарного каталога с торговой площадки для кабинета ""%1"" возникли ошибки: %2';
					|en = 'Errors occurred while importing the goods directory from the trading platform for the %1 account: %2'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				ДанныеУчетнойЗаписи.ИдентификаторКабинета,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстОшибки);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_ИмпортироватьТоварныйКаталог";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Прервать;
		КонецПопытки;
	КонецЦикла;
	
	Возврат Ошибка;
	
КонецФункции

// Получает товарный каталог с торговой площадки.
//
// Параметры:
//   УчетнаяЗапись         - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   ИдентификаторыТоваров - Массив Из Строка - идентификаторы запрашиваемых товаров.
//
// Возвращаемое значение:
//   - ТаблицаЗначений - см. НоваяТаблицаОписанияТоваров.
//   - Неопределено - при выполнении запроса к сервису обнаружены ошибки.
//
Функция ПолучитьТоварныйКаталог(УчетнаяЗапись, ИдентификаторыТоваров) Экспорт
	
	ТаблицаТоваров = НоваяТаблицаОписанияТоваров();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет") Тогда
		ТекстОшибки = НСтр("ru = 'Интеграция с Яндекс Маркет не используется.';
							|en = 'Integration with Yandex.Market is not used.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
				
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись, Ложь);
	ПараметрыЗапроса    = НовыеПараметрыЗапросаИмпортаТоварногоКаталога();
	УчетныеЗаписи       = Неопределено;
	КоличествоПорций    = Цел((ИдентификаторыТоваров.Количество() - 1) / ПараметрыЗапроса.КоличествоНаСтранице) + 1;
	
	Для Порция = 1 По КоличествоПорций Цикл
		Попытка
			ПараметрыЗапроса.ИдентификаторыТоваров = Новый Массив;
			Для Индекс = (Порция - 1) * ПараметрыЗапроса.КоличествоНаСтранице По Мин(ИдентификаторыТоваров.ВГраница(), (Порция * ПараметрыЗапроса.КоличествоНаСтранице - 1)) Цикл
				ПараметрыЗапроса.ИдентификаторыТоваров.Добавить(ИдентификаторыТоваров[Индекс]);
			КонецЦикла;
			
			Отказ = Ложь;
			ПорцияТоваров = ИмпортироватьПорциюТоварногоКаталогаИзСервиса(ДанныеУчетнойЗаписи, 
				ПараметрыЗапроса,
				УчетныеЗаписи,
				Отказ);
			
			Если Отказ Тогда
				Возврат Неопределено;
				
			Иначе
				ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ПорцияТоваров, ТаблицаТоваров);
			КонецЕсли;
		
		Исключение
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При получении товарного каталога с торговой площадки для кабинета ""%1"" возникли ошибки: %2';
					|en = 'Errors occurred while receiving the goods directory from the trading platform for the %1 account: %2'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				ДанныеУчетнойЗаписи.ИдентификаторКабинета,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстОшибки);
			
			Возврат Неопределено;
		КонецПопытки;
	КонецЦикла;
	
	Возврат ТаблицаТоваров;
	
КонецФункции

// Определяет список штрихкодов для указанной пары номенклатуры и характеристики.
//
// Параметры:
//   Номенклатура   - СправочникСсылка.Номенклатура - номенклатура товарной позиции;
//   Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика товарной позиции;
//   Упаковка       - СправочникСсылка.УпаковкиЕдиницыИзмерения   - упаковка товарной позиции.
// Возвращаемое значение:
//   Массив Из Строка - список штрихкодов.
//
Функция ПолучитьШтрихКоды(Номенклатура, Характеристика, Упаковка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	ШтрихкодыНоменклатуры.Штрихкод КАК barcode
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Номенклатура = &Номенклатура
		|	И ШтрихкодыНоменклатуры.Характеристика = &Характеристика 
		|	И ШтрихкодыНоменклатуры.Упаковка = &Упаковка
		|	И ШтрихкодыНоменклатуры.Номенклатура.ЕдиницаИзмерения <> &Упаковка
		|
		|ОБЪЕДИНИТЬ ВСЕ 
		|
		|ВЫБРАТЬ
		|	ШтрихкодыНоменклатуры.Штрихкод КАК barcode
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Номенклатура = &Номенклатура
		|	И ШтрихкодыНоменклатуры.Характеристика = &Характеристика 
		|	И ШтрихкодыНоменклатуры.Номенклатура.ЕдиницаИзмерения = &Упаковка
		|	И ШтрихкодыНоменклатуры.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)";   

	Запрос.Параметры.Вставить("Номенклатура",   Номенклатура);
	Запрос.Параметры.Вставить("Характеристика", Характеристика);   
	Запрос.Параметры.Вставить("Упаковка", Упаковка);
	
	Результат = Запрос.Выполнить().Выгрузить();
	ШтрихКоды = Новый Массив;
	
	Если Результат.Количество() > 0 Тогда
		ШтрихКоды = Результат.ВыгрузитьКолонку("barcode");
	КонецЕсли;
	
	Возврат ШтрихКоды;
	
КонецФункции

#КонецОбласти

#Область УправлениеОстаткамиТоваров

// Устарело. Выгружает остатки товарных позиций из информационной базы на торговую площадку.
//
// Параметры:
//   УчетнаяЗапись           - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//                           - Неопределено - выполнить регламентное задание по всем учетным записям, с которыми разрешен обмен данными.
//   ДополнительныеПараметры - Произвольный - произвольные данные, переданные в функцию;
//                           - Структура - возможные ключи дополнительных параметров:
//     * ПоРасписанию          - Булево - признак автоматического или ручного запуска регламентного задания.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ВыгрузитьОстаткиТоваровЯндексМаркет(УчетнаяЗапись = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.УдалитьВыгрузкаОстатковТоваровЯндексМаркет);
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	ПараметрыИзменения = ИнтеграцияСМаркетплейсамиСервер.ПараметрыИзмененияЭкземпляраРегламентногоЗадания();
	ПараметрыИзменения.МетаданныеДо = Метаданные.РегламентныеЗадания.УдалитьВыгрузкаОстатковТоваровЯндексМаркет;
	ПараметрыИзменения.МетаданныеПосле = Метаданные.РегламентныеЗадания.ВыгрузкаОстатковНаМаркетплейсы;
	ПараметрыИзменения.УчетнаяЗапись = УчетнаяЗапись;
	ПараметрыИзменения.Ключ = "ЯндексМаркет_ВыгрузкаОстатковТоваровЯндексМаркет_" + XMLСтрока(УчетнаяЗапись);
	
	ИнтеграцияСМаркетплейсамиСервер.ИзменитьЭкземплярРегламентногоЗадания(ПараметрыИзменения, ДополнительныеПараметры);
	
	Возврат Ошибка;
	
КонецФункции

#КонецОбласти

#Область УправлениеЦенамиНаТовары

// Загружает рекомендованные цены на товары c торговой площадки.
//
// Параметры:
//   УчетнаяЗапись           - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//                           - Неопределено - выполнить регламентное задание по всем учетным записям, с которыми разрешен обмен данными.
//   ДополнительныеПараметры - Произвольный - произвольные данные, переданные в функцию;
//                           - Структура - возможные ключи дополнительных параметров:
//     * ПоРасписанию          - Булево - признак автоматического или ручного запуска регламентного задания;
//     * ТаблицаВидовЦен       - ТаблицаЗначений - загружаемые виды цен:
//       ** ВидЦены              - СправочникСсылка.ВидыЦен - вид цены.
//     * ТаблицаТоваров        - ТаблицаЗначений - список товаров:
//       ** УчетнаяЗапись        - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//       ** Номенклатура         - СправочникСсылка.Номенклатура - номенклатура;
//       ** Характеристика       - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика;
//       ** Упаковка             - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ЗагрузитьРекомендованныеЦеныЯндексМаркет(УчетнаяЗапись = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ЗагрузкаРекомендованныхЦенЯндексМаркет);
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет") Тогда
		ТекстОшибки = НСтр("ru = 'Интеграция с Яндекс Маркет не используется.';
							|en = 'Integration with Yandex.Market is not used.'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
				
		Ошибка.КодОшибки      = "ЯндексМаркет_ИспользоватьИнтеграциюСЯндексМаркет";
		Ошибка.ОписаниеОшибки = ТекстОшибки;

		Возврат Ошибка;
	КонецЕсли;
		
	ПоРасписанию = Истина;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		 И ДополнительныеПараметры.Свойство("ПоРасписанию") Тогда
		ПоРасписанию = ДополнительныеПараметры.ПоРасписанию;
	КонецЕсли;
	
	Если ПоРасписанию Тогда
		СтрокаИнформацииПоУЗ = НСтр("ru = 'Запуск по расписанию: загрузка рекомендованных цен товаров для учетной записи ""%1"" с торговой площадки.';
									|en = 'Run on schedule: import recommended item prices from the trading platform for the ""%1"" account.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		СтрокаИнформацииБезУЗ = НСтр("ru = 'Запуск по расписанию: загрузка рекомендованных цен товаров с торговой площадки.';
									|en = 'Run on schedule: import recommended item prices from the trading platform.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
	Иначе
		СтрокаИнформацииПоУЗ = НСтр("ru = 'Запуск по инициативе пользователя: загрузка рекомендованных цен товаров для учетной записи ""%1"" с торговой площадки.';
									|en = 'Run by user: import recommended item prices from the trading platform for the ""%1"" account.'", 
			ОбщегоНазначения.КодОсновногоЯзыка()); 
		СтрокаИнформацииБезУЗ = НСтр("ru = 'Запуск по инициативе пользователя: загрузка рекомендованных цен товаров с торговой площадки.';
									|en = 'Run by user: import recommended item prices from the trading platform.'", 
			ОбщегоНазначения.КодОсновногоЯзыка()); 
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				СтрокаИнформацииПоУЗ, 
				УчетнаяЗапись));
	Иначе
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			СтрокаИнформацииБезУЗ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись);
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		
		Если ДанныеАвторизации = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" отсутствует информация об аккаунте.';
					|en = 'There is no information for the ""%1"" account.'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_ОтсутствуетИнформацияОбАккаунте";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
		
		Если ДанныеУчетнойЗаписи.НеОбновлятьДанныеТорговойПлощадки Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" запрещен обмен данными с торговой площадкой.';
					|en = 'Data exchange with the trading platform is not allowed for the ""%1"" account.'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_НеОбновлятьДанныеТорговойПлощадки";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
	КонецЕсли;
		
	ВидыЦен = ПолучитьМассивРекомендованныхЦен();
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		 И ДополнительныеПараметры.Свойство("ТаблицаВидовЦен") Тогда
		ВидыЦен = ДополнительныеПараметры.ТаблицаВидовЦен.ВыгрузитьКолонку("ВидЦены");
	КонецЕсли;
	
	Если ВидыЦен.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Не определены виды цен с признаком ""Загружается из Яндекс Маркет"".';
							|en = 'Price types with the ""Import from Yandex.Market"" specification method are not specified.'",
				ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
				
		Ошибка.КодОшибки      = "ЯндексМаркет_ЗагрузитьРекомендованныеЦеныЯндексМаркет";
		Ошибка.ОписаниеОшибки = ТекстОшибки;
			
		Возврат Ошибка;
	КонецЕсли;
	
	ТаблицаТоваров = Неопределено;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		 И ДополнительныеПараметры.Свойство("ТаблицаТоваров") Тогда
		ТаблицаТоваров = ДополнительныеПараметры.ТаблицаТоваров;
	КонецЕсли;
	
	ВыборкаДанных = ВыбратьУчетныеЗаписиЯндексМаркет(УчетнаяЗапись);
	
	Пока ВыборкаДанных.Следующий() Цикл
		Попытка
			ИдентификаторыОтработанные = Новый СписокЗначений;
			
			Пока Истина Цикл
				ИдентификаторыТоваровПлощадки = ПолучитьИдентификаторыТоваровПлощадки(ВыборкаДанных.УчетнаяЗапись, ВидыЦен, ИдентификаторыОтработанные, ТаблицаТоваров);
				Если ИдентификаторыТоваровПлощадки.Количество() = 0 Тогда
					Прервать;
				КонецЕсли;
				
				Отказ = Ложь;
				ПолучитьРекомендованныеЦеныИзСервиса(ВыборкаДанных, ИдентификаторыТоваровПлощадки, ВидыЦен, Отказ);
				
				Если Отказ Тогда
					Ошибка.КодОшибки      = "ЯндексМаркет_API_ЗагрузитьРекомендованныеЦеныЯндексМаркет";
					Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'При загрузке рекомендованных цен товаров для учетной записи ""%1"" с торговой площадки возникли ошибки. Подробности см. в журнале регистрации.';
							|en = 'Errors occurred when importing the recommended item prices from the trading platform for account ""%1"". For more information, see the event log.'"),
						ВыборкаДанных.УчетнаяЗапись);
				КонецЕсли;
				
				Для Каждого ЭлементКоллекции Из ИдентификаторыТоваровПлощадки Цикл
					ИдентификаторыОтработанные.Добавить(ЭлементКоллекции.Значение);
				КонецЦикла;
			КонецЦикла;
			
		Исключение
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При загрузке рекомендованных цен товаров для учетной записи ""%1"" с торговой площадки возникли ошибки: %2';
					|en = 'Errors occurred when importing the recommended item prices from the trading platform for account ""%1"": %2'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				ВыборкаДанных.УчетнаяЗапись,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстОшибки);
					
			Ошибка.КодОшибки      = "ЯндексМаркет_ЗагрузитьРекомендованныеЦеныЯндексМаркет";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
		КонецПопытки;
	КонецЦикла;

	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Завершена загрузка рекомендованных цен товаров для учетной записи ""%1"" с торговой площадки.';
					|en = 'The recommended item prices are imported from the trading platform for account ""%1"".'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись));
	Иначе
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			НСтр("ru = 'Завершена загрузка рекомендованных цен товаров с торговой площадки.';
				|en = 'The recommended item prices are imported from the trading platform.'",
				ОбщегоНазначения.КодОсновногоЯзыка()));
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Ошибка;

КонецФункции

// Выгружает рекомендованные цены на товары c торговой площадки.
//
// Параметры:
//   УчетнаяЗапись           - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//                           - Неопределено - выполнить регламентное задание по всем учетным записям, с которыми разрешен обмен данными.
//   ДополнительныеПараметры - Произвольный - произвольные данные, переданные в функцию;
//                           - Структура - возможные ключи дополнительных параметров:
//     * ПоРасписанию          - Булево - признак автоматического или ручного запуска регламентного задания.
//     * ТаблицаТоваров        - ТаблицаЗначений - список товаров:
//       ** УчетнаяЗапись        - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//       ** Номенклатура         - СправочникСсылка.Номенклатура - номенклатура;
//       ** Характеристика       - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика;
//       ** Упаковка             - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ВыгрузитьЦеныТоваровРегламентнымЗаданием(УчетнаяЗапись = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет") Тогда
		ТекстОшибки = НСтр("ru = 'Интеграция с Яндекс Маркет не используется.';
							|en = 'Integration with Yandex.Market is not used.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
				
		Ошибка.КодОшибки      = "ЯндексМаркет_ИспользоватьИнтеграциюСЯндексМаркет";
		Ошибка.ОписаниеОшибки = ТекстОшибки;

		Возврат Ошибка;
	КонецЕсли;
			
	ПоРасписанию = Истина;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") 
		 И ДополнительныеПараметры.Свойство("ПоРасписанию") Тогда
		ПоРасписанию = ДополнительныеПараметры.ПоРасписанию;
	КонецЕсли;
	
	Если ПоРасписанию Тогда
		СтрокаИнформацииПоУЗ = НСтр("ru = 'Запуск по расписанию: выгрузка текущих цен товаров для учетной записи ""%1"" на торговую площадку.';
									|en = 'Run on schedule: export the current item prices to the trading platform for account ""%1"".'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		СтрокаИнформацииБезУЗ = НСтр("ru = 'Запуск по расписанию: выгрузка текущих цен товаров на торговую площадку.';
									|en = 'Run on schedule: export the current item prices to the trading platform.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
	Иначе
		СтрокаИнформацииПоУЗ = НСтр("ru = 'Запуск по инициативе пользователя: выгрузка текущих цен товаров для учетной записи ""%1"" на торговую площадку.';
									|en = 'Run by user: export the current item prices to the trading platform for account ""%1"".'", 
			ОбщегоНазначения.КодОсновногоЯзыка()); 
		СтрокаИнформацииБезУЗ = НСтр("ru = 'Запуск по инициативе пользователя: выгрузка текущих цен товаров на торговую площадку.';
									|en = 'Run by user: export the current item prices to the trading platform.'", 
			ОбщегоНазначения.КодОсновногоЯзыка()); 
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				СтрокаИнформацииПоУЗ, 
				УчетнаяЗапись));
	Иначе
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			СтрокаИнформацииБезУЗ);
	КонецЕсли;
		
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись);
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		
		Если ДанныеАвторизации = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" отсутствует информация об аккаунте.';
					|en = 'There is no information for the ""%1"" account.'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_ОтсутствуетИнформацияОбАккаунте";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
		
		Если ДанныеУчетнойЗаписи.НеОбновлятьДанныеТорговойПлощадки Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" запрещен обмен данными с торговой площадкой.';
					|en = 'Data exchange with the trading platform is not allowed for account ""%1"".'", 
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_НеОбновлятьДанныеТорговойПлощадки";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаТоваров = Неопределено;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		 И ДополнительныеПараметры.Свойство("ТаблицаТоваров") Тогда
		ТаблицаТоваров = ДополнительныеПараметры.ТаблицаТоваров;
	КонецЕсли;
	
	ВыборкаДанных = ВыбратьУчетныеЗаписиЯндексМаркет(УчетнаяЗапись);
	
	Пока ВыборкаДанных.Следующий() Цикл
		Попытка
			Отказ = Ложь;
	    	УстановитьЦеныВСервисе(ВыборкаДанных, ТаблицаТоваров, Отказ);
			
			Если Отказ Тогда
				Ошибка.КодОшибки      = "ЯндексМаркет_API_ВыгрузитьУстановленныеЦеныЯндексМаркет";
				Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'При выгрузке текущих цен товаров для учетной записи ""%1"" на торговую площадку возникли ошибки. Подробности см. в журнале регистрации.';
						|en = 'Errors occurred when exporting the current item prices to the trading platform for account ""%1"". For more information, see the event log.'"),
					ВыборкаДанных.УчетнаяЗапись);
					
			Иначе
				УстановитьПривилегированныйРежим(Истина);
				СерверныеОповещения.ОтправитьСерверноеОповещение(
					"ИнтеграцияСЯндексМаркетСервер.ОбновитьДанные",
					ВыборкаДанных.УчетнаяЗапись,
					Неопределено,
					Истина);
				УстановитьПривилегированныйРежим(Ложь);
			КонецЕсли;
			
		Исключение
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При выгрузке текущих цен товаров для учетной записи ""%1"" на торговую площадку возникли ошибки: %2';
					|en = 'Errors occurred when exporting the current item prices to the trading platform for account ""%1"": %2'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				ВыборкаДанных.УчетнаяЗапись,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстОшибки);
					
			Ошибка.КодОшибки      = "ЯндексМаркет_ВыгрузитьУстановленныеЦеныЯндексМаркет";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
		КонецПопытки;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Завершена выгрузка текущих цен товаров для учетной записи ""%1"" на торговую площадку.';
					|en = 'The current item prices are exported to the trading platform for account ""%1"".'", 
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись));
	Иначе
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			НСтр("ru = 'Завершена выгрузка текущих цен товаров для учетной записи ""%1"" на торговую площадку.';
				|en = 'The current item prices are exported to the trading platform for account ""%1"".'", 
				ОбщегоНазначения.КодОсновногоЯзыка()));
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Ошибка;

КонецФункции

// Устарела. Следует использовать ВыгрузитьЦеныТоваровРегламентнымЗаданием
// Выгружает рекомендованные цены на товары c торговой площадки.
//
// Параметры:
//   УчетнаяЗапись           - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//                           - Неопределено - выполнить регламентное задание по всем учетным записям, с которыми разрешен обмен данными.
//   ДополнительныеПараметры - Произвольный - произвольные данные, переданные в функцию;
//                           - Структура - возможные ключи дополнительных параметров:
//     * ПоРасписанию          - Булево - признак автоматического или ручного запуска регламентного задания.
//     * ТаблицаТоваров        - ТаблицаЗначений - список товаров:
//       ** УчетнаяЗапись        - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//       ** Номенклатура         - СправочникСсылка.Номенклатура - номенклатура;
//       ** Характеристика       - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика;
//       ** Упаковка             - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ВыгрузитьУстановленныеЦеныЯндексМаркет(УчетнаяЗапись = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.УдалитьВыгрузкаУстановленныхЦенЯндексМаркет);
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	ПараметрыИзменения = ИнтеграцияСМаркетплейсамиСервер.ПараметрыИзмененияЭкземпляраРегламентногоЗадания();
	ПараметрыИзменения.МетаданныеДо = Метаданные.РегламентныеЗадания.УдалитьВыгрузкаУстановленныхЦенЯндексМаркет;
	ПараметрыИзменения.МетаданныеПосле = Метаданные.РегламентныеЗадания.ВыгрузкаЦенНаМаркетплейсы;
	ПараметрыИзменения.УчетнаяЗапись = УчетнаяЗапись;
	ПараметрыИзменения.Ключ = "ЯндексМаркет_ВыгрузкаУстановленныхЦенЯндексМаркет_" + XMLСтрока(УчетнаяЗапись);
	
	ИнтеграцияСМаркетплейсамиСервер.ИзменитьЭкземплярРегламентногоЗадания(ПараметрыИзменения, ДополнительныеПараметры);
	
	Возврат Ошибка;
	
КонецФункции

// Определяет список рекомендованных цен.
// 
// Возвращаемое значение:
//    Массив Из СправочникСсылка.ВидыЦен - рекомендованные виды цен.
//
Функция ПолучитьМассивРекомендованныхЦен() Экспорт  
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыЦен.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВидыЦен КАК ВидыЦен
		|ГДЕ
		|	НЕ ВидыЦен.ПометкаУдаления
		|	И ВидыЦен.СпособЗаданияЦены = ЗНАЧЕНИЕ(Перечисление.СпособыЗаданияЦен.ЗагружаетсяИзЯндексМаркет)";
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции  

// Конструктор настроек по способу задания цены.
//
// Возвращаемое значение:
//   Структура - настройки:
//     * ИмяПараметра      - Строка - имя параметра.
//     * СписокВыбора      - Строка - список выбора (строка с разделителем).
//     * ЗначениеПараметра - Строка - значение параметра.
//
Функция СтруктураПараметровСпособаЗаданияЦены() Экспорт

	СтруктураПараметровСпособаЗаданияЦены              = Новый Структура("ИмяПараметра, СписокВыбора, ЗначениеПараметра");
	СтруктураПараметровСпособаЗаданияЦены.ИмяПараметра = НСтр("ru = 'Тип цены Яндекс Маркет';
																|en = 'Yandex.Market price type'");
	СтруктураПараметровСпособаЗаданияЦены.СписокВыбора = "Минимальная цена продажи на Маркете;Рекомендованная Маркетом;Минимальная среди всех предложений товара на Маркете;Максимальная без скидки;Максимальная для показов на Маркете";

	Возврат СтруктураПараметровСпособаЗаданияЦены;
	
КонецФункции

// Обновляет актуальные цены торговой площадки.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//   ЦенаПродажи   - СправочникСсылка.ВидыЦен - вид цены продажи;
//   ТаблицаЦен    - ТаблицаЗначений - список рекомендованных цен:
//     * ВидЦены     - СправочникСсылка.ВидыЦен - вид цены;
//     * ПолеТаблицы - Строка - обозначение вида цены в Яндекс Маркет.
//
// Возвращаемое значение:
//   Структура - описание ошибки при выполнении операции (см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка):
//     * КодОшибки      - Строка - код ошибки;
//     * ОписаниеОшибки - Строка - текстовое описание ошибки;
//     * Детализация    - Массив Из Строка - детализированная информация по ошибке;
//                      - Неопределено - детализация не используется (по умолчанию).
//     * АктуальныеЦены - Неопределено - при возникновении ошибки;
//                      - Массив Из Структура - актуальные цены, описания полей см. Обработка.УправлениеПродажамиНаЯндексМаркет.ВыгрузкаТоварногоКаталога.АктуальныеЦены.
//
Функция ОбновитьАктуальныеЦеныТорговойПлощадки(УчетнаяЗапись, ЦенаПродажи, ТаблицаЦен) Экспорт
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	Ошибка.Вставить("АктуальныеЦены", Неопределено);
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
	"ОбщийМодуль.ИнтеграцияСЯндексМаркетСервер.ОбновитьАктуальныеЦеныТорговойПлощадки");  
	
	Попытка     
		ТекстЗапроса = "ВЫБРАТЬ
		|	ОтносительныеКурсыВСрезПоследних.Валюта КАК Валюта,
		|	ОтносительныеКурсыВСрезПоследних.КурсЧислитель КАК КурсЧислитель,
		|	ОтносительныеКурсыВСрезПоследних.КурсЗнаменатель КАК КурсЗнаменатель
		|ПОМЕСТИТЬ КурсыВалют
		|ИЗ
		|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(, БазоваяВалюта = &БазоваяВалюта) КАК ОтносительныеКурсыВСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		| 
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЦен.ВидЦены КАК ВидЦены,
		|	ТаблицаЦен.ПолеТаблицы КАК ПолеТаблицы
		|ПОМЕСТИТЬ ВнутренняяТаблицаЦен
		|ИЗ
		|	&ТаблицаЦен КАК ТаблицаЦен
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидЦены
		|;  
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись КАК УчетнаяЗапись,
		|	СтатусыПубликацииТоваровЯндексМаркет.Номенклатура КАК Номенклатура,
		|	СтатусыПубликацииТоваровЯндексМаркет.Характеристика КАК Характеристика,
		|	СтатусыПубликацииТоваровЯндексМаркет.Упаковка КАК Упаковка, 
		|	СтатусыПубликацииТоваровЯндексМаркет.ПредставлениеТовара КАК ПредставлениеТовара, 
		|	&УпаковкаТовара КАК УпаковкаТовара,
		|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторПредложения КАК ИдентификаторПредложения,
		|	СтатусыПубликацииТоваровЯндексМаркет.ЦенаПродажи КАК ЦенаПродажи,
		|	СтатусыПубликацииТоваровЯндексМаркет.ДатаУстановкиЦены КАК ДатаУстановкиЦены,
		|	ВнутренняяТаблицаЦен.ВидЦены КАК ВидЦены,
		|	&ПоляДляЦенообразованияВыборка КАК ПоляДляЦенообразованияВыборка             
		|ПОМЕСТИТЬ ТаблицаТоварныхПозиций
		|ИЗ
		|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправНоменклатура
		|		ПО СтатусыПубликацииТоваровЯндексМаркет.Номенклатура = СправНоменклатура.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|			ПО (СправНоменклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка) 
		|				ЛЕВОЕ СОЕДИНЕНИЕ ВнутренняяТаблицаЦен
		|				ПО ИСТИНА
		|ГДЕ
		|	СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись
		|	И СтатусыПубликацииТоваровЯндексМаркет.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияПройдена)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,  
		|	ПоляДляЦенообразованияВыборка,
		|	ВидЦены
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
		|	&ПоляДляЦенообразованияСрез КАК ПоляДляЦенообразованияСрез,
		|	ЦеныНоменклатуры.Цена КАК Цена,  
		|   ЦеныНоменклатуры.ВидЦены КАК ВидЦены, 
		|	ЦеныНоменклатуры.Упаковка КАК Упаковка,
		|	ЦеныНоменклатуры.Валюта КАК Валюта,
		|	ЦеныНоменклатуры.Период КАК Период
		|ПОМЕСТИТЬ ЦеныНоменклатуры
		|ИЗ
		|	ИсточникЦенНоменклатуры КАК ЦеныНоменклатуры
		|ГДЕ ВидЦены В (&ВидыЦен)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	ПоляДляЦенообразованияСрез
		|;   
		|
		|////////////////////////////////////////////////////////////////////////////////   
		|ВЫБРАТЬ
		|	АктуальныеЦены.Номенклатура КАК Номенклатура,
		|	АктуальныеЦены.Характеристика КАК Характеристика,
		|	АктуальныеЦены.Упаковка КАК Упаковка,
		|	АктуальныеЦены.ПредставлениеТовара КАК ПредставлениеТовара,
		|	МАКСИМУМ(АктуальныеЦены.Период) КАК ДатаОбновления,
		|	МАКСИМУМ(АктуальныеЦены.ЦенаПродажи) КАК ЦенаПродажи,
		|	МАКСИМУМ(АктуальныеЦены.МинимальнаяЦенаПродажи) КАК МинимальнаяЦенаПродажи,
		|	МАКСИМУМ(АктуальныеЦены.РекомендованнаяЦена) КАК РекомендованнаяЦена,
		|	МАКСИМУМ(АктуальныеЦены.МинимальнаяСредиВсехПредложений) КАК МинимальнаяСредиВсехПредложений,
		|	МАКСИМУМ(АктуальныеЦены.МаксимальнаяБезСкидки) КАК МаксимальнаяБезСкидки,
		|	МАКСИМУМ(АктуальныеЦены.МаксимальнаяДляПоказов) КАК МаксимальнаяДляПоказов
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ 
		|		ТаблицаТоварныхПозиций.УчетнаяЗапись КАК УчетнаяЗапись,
		|		ТаблицаТоварныхПозиций.Номенклатура КАК Номенклатура,
		|		ТаблицаТоварныхПозиций.Характеристика КАК Характеристика,
		|		ТаблицаТоварныхПозиций.УпаковкаТовара КАК Упаковка,
		|		ТаблицаТоварныхПозиций.ПредставлениеТовара КАК ПредставлениеТовара,
		|		ЦеныНоменклатуры.Период КАК Период,
		|			ВЫБОР
		|				КОГДА ВнутренняяТаблицаЦен.ВидЦены = &ВидЦен
		|					ТОГДА &ЦенаПродажи
		|				ИНАЧЕ 0
		|			КОНЕЦ КАК ЦенаПродажи,
		|			ВЫБОР
		|				КОГДА ВнутренняяТаблицаЦен.ПолеТаблицы = ""МинимальнаяЦенаПродажи""
		|					ТОГДА &ЦенаПродажи
		|				ИНАЧЕ 0
		|			КОНЕЦ КАК МинимальнаяЦенаПродажи,
		|			ВЫБОР
		|				КОГДА ВнутренняяТаблицаЦен.ПолеТаблицы = ""РекомендованнаяЦена""
		|					ТОГДА &ЦенаПродажи
		|				ИНАЧЕ 0
		|			КОНЕЦ КАК РекомендованнаяЦена,
		|			ВЫБОР
		|				КОГДА ВнутренняяТаблицаЦен.ПолеТаблицы = ""МинимальнаяСредиВсехПредложений""
		|					ТОГДА &ЦенаПродажи
		|				ИНАЧЕ 0
		|			КОНЕЦ КАК МинимальнаяСредиВсехПредложений,
		|			ВЫБОР
		|				КОГДА ВнутренняяТаблицаЦен.ПолеТаблицы = ""МаксимальнаяБезСкидки""
		|					ТОГДА &ЦенаПродажи
		|				ИНАЧЕ 0
		|			КОНЕЦ КАК МаксимальнаяБезСкидки,
		|			ВЫБОР
		|				КОГДА ВнутренняяТаблицаЦен.ПолеТаблицы = ""МаксимальнаяДляПоказов""
		|					ТОГДА &ЦенаПродажи
		|				ИНАЧЕ 0
		|			КОНЕЦ КАК МаксимальнаяДляПоказов
		|
		|ИЗ
		|	ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправНоменклатура
		|		ПО ЦеныНоменклатуры.Номенклатура = СправНоменклатура.Ссылка
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВнутренняяТаблицаЦен КАК ВнутренняяТаблицаЦен
		|				ПО ЦеныНоменклатуры.ВидЦены = ВнутренняяТаблицаЦен.ВидЦены
		|					ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
		|					ПО ЦеныНоменклатуры.Валюта = КурсыВалют.Валюта
		|						ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУчета
		|						ПО (КурсыВалютУчета.Валюта = &БазоваяВалюта)
		|							ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоварныхПозиций КАК ТаблицаТоварныхПозиций
		|							ПО (&УсловиеСоединенияЦеныНоменклатурыСрезПоследних)) КАК АктуальныеЦены
		|
		|СГРУППИРОВАТЬ ПО
		|	АктуальныеЦены.Номенклатура,
		|	АктуальныеЦены.Характеристика,
		|	АктуальныеЦены.Упаковка,
		|	АктуальныеЦены.ПредставлениеТовара
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КурсыВалют
		|;
		|   
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВнутренняяТаблицаЦен
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаТоварныхПозиций
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЦеныНоменклатуры";   
		ТекущаяДата                   = ТекущаяДатаСеанса();
		ИспользуетсяЦенообразование25 = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25(ТекущаяДата);
		
		НастройкаЦенообразования                 = ЦенообразованиеКлиентСервер.НастройкиДляВременнойТаблицыСДополнениемДляЦенообразования();
		НастройкаЦенообразования.ИсточникТоваров = "СтатусыПубликацииТоваровЯндексМаркет";
		НастройкаЦенообразования.ПолеСерия       = "";
		
		ТекстЗамены = ЦенообразованиеКлиентСервер.ТекстПолейДляЦенообразования(НастройкаЦенообразования);
		Если ЗначениеЗаполнено(ТекстЗамены) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияВыборка КАК ПоляДляЦенообразованияВыборка", ТекстЗамены);
			ПоляДляЦенообразования =
			"ХарактеристикаЦО,
			|	СерияЦО,
			|	УпаковкаЦО";
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияВыборка КАК ПоляДляЦенообразованияВыборка", "ИСТИНА");
			ПоляДляЦенообразования =
			"Характеристика";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПоляДляЦенообразованияВыборка", ПоляДляЦенообразования);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"СтатусыПубликацииТоваровЯндексМаркет.Упаковка",
		"ВЫБОР
		|		КОГДА СтатусыПубликацииТоваровЯндексМаркет.Упаковка = СправНоменклатура.ЕдиницаИзмерения
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|		ИНАЧЕ СтатусыПубликацииТоваровЯндексМаркет.Упаковка
		|	КОНЕЦ");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УпаковкаТовара", "СтатусыПубликацииТоваровЯндексМаркет.Упаковка");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"ИсточникЦенНоменклатуры",
		ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатуры("ТаблицаТоварныхПозиций",
		"&ДатаЦен",
		Новый Структура("ВТаблице", "ВидЦены"),
		ИспользуетсяЦенообразование25));
		
		Если ИспользуетсяЦенообразование25 Тогда
			ПоляДляЦенообразованияСрез = 
			"ЦеныНоменклатуры.ХарактеристикаЦО КАК ХарактеристикаЦО,
			|	ЦеныНоменклатуры.СерияЦО КАК СерияЦО,
			|	ЦеныНоменклатуры.УпаковкаЦО КАК УпаковкаЦО";
		Иначе
			ПоляДляЦенообразованияСрез = 
			"ЦеныНоменклатуры.Характеристика КАК Характеристика";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияСрез КАК ПоляДляЦенообразованияСрез", ПоляДляЦенообразованияСрез);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПоляДляЦенообразованияСрез", ПоляДляЦенообразования);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&УсловиеСоединенияЦеныНоменклатурыСрезПоследних",
		ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатурыУсловиеСоединения(
		"ТаблицаТоварныхПозиций",
		"ЦеныНоменклатуры",,
		ИспользуетсяЦенообразование25));
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ЦенаПродажи",
		"ВЫРАЗИТЬ(ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) 
		|			* ВЫБОР
		|				КОГДА ТаблицаТоварныхПозиций.Упаковка <> ЦеныНоменклатуры.Упаковка
		|					ТОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) 
		|							/ ВЫБОР
		|								КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) = 0
		|									ТОГДА 1
		|								ИНАЧЕ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1)
		|							КОНЕЦ
		|				ИНАЧЕ 1
		|			КОНЕЦ 
		|			* ВЫБОР
		|				КОГДА ЦеныНоменклатуры.Валюта = &БазоваяВалюта
		|					ТОГДА 1
		|				ИНАЧЕ ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютУчета.КурсЗнаменатель, 1) / (ЕСТЬNULL(КурсыВалютУчета.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1))
		|			КОНЕЦ КАК ЧИСЛО(31, 2))");
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"(ВЫБОР
		|	КОГДА ТаблицаТоварныхПозиций.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|		ТОГДА СправНоменклатура.ЕдиницаИзмерения
		|	ИНАЧЕ ТаблицаТоварныхПозиций.Упаковка
		|КОНЕЦ)",
		"ТаблицаТоварныхПозиций.Номенклатура"));
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"(ВЫБОР
		|	КОГДА ЦеныНоменклатуры.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|		ТОГДА СправНоменклатура.ЕдиницаИзмерения
		|	ИНАЧЕ ЦеныНоменклатуры.Упаковка
		|КОНЕЦ)",
		"ЦеныНоменклатуры.Номенклатура"));
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;    
		
		ВидыЦен = ТаблицаЦен.ВыгрузитьКолонку("ВидЦены");
		ВидыЦен.Добавить(ЦенаПродажи);   
		СтрокаТаблицыЦен = ТаблицаЦен.Добавить();
		СтрокаТаблицыЦен.ВидЦены  = ЦенаПродажи;
		СтрокаТаблицыЦен.ПолеТаблицы  = "ЦенаПродажи";
		
		Запрос.Параметры.Вставить("УчетнаяЗапись",  УчетнаяЗапись);
		Запрос.Параметры.Вставить("ВидыЦен",        ВидыЦен); 
		Запрос.Параметры.Вставить("ВидЦен",         ЦенаПродажи);
		Запрос.УстановитьПараметр("ДатаЦен",        ТекущаяДата);
		Запрос.УстановитьПараметр("БазоваяВалюта",  ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию());
		Запрос.Параметры.Вставить("ТаблицаЦен",     ТаблицаЦен);
		
		УстановитьПривилегированныйРежим(Истина);
		ВыборкаДанных = ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
		УстановитьПривилегированныйРежим(Ложь);
		
		Ошибка.Вставить("АктуальныеЦены", ВыборкаДанных);
		ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ВыборкаДанных.Количество() / 1000);
		
	Исключение
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При обновлении актуальных цен товарных позиций для учетной записи ""%1"" возникли ошибки: %2';
				|en = 'Errors occurred when updating item prices for the ""%1"" account: %2'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Ошибка,,,
		ТекстОшибки);
		
		Ошибка.КодОшибки      = "ЯндексМаркет_АктуальныеЦеныТорговойПлощадки";
		Ошибка.ОписаниеОшибки = ТекстОшибки;
	КонецПопытки;
	
	Возврат Ошибка;
	
КонецФункции

#КонецОбласти

#Область УправлениеМагазинами

// Возвращает список магазинов, к которым имеет доступ пользователь - владелец авторизационного токена. 
//
// Параметры:
//   ИдентификаторКабинета - Строка - идентификатор бизнес-кабинета для подключения к сервису.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ПолучитьМагазиныЯндексМаркет(ИдентификаторКабинета) Экспорт
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();  
	Отказ  = Ложь; 
	ИдентификаторАккаунта = "";
	
	ДанныеАвторизации = ДанныеАвторизацииПоИдентификаторуКабинета(ИдентификаторКабинета);  
	// если нового ключа не найдено получим данные аккаунта по кабинету
	Если НЕ (ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token")) Тогда 
		Запрос = Новый Запрос();
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			|УчетныеЗаписиМаркетплейсов.ИдентификаторАккаунта КАК ИдентификаторАккаунта
			|ИЗ
			|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
			|ГДЕ
			|	УчетныеЗаписиМаркетплейсов.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет)
			|	И УчетныеЗаписиМаркетплейсов.ИдентификаторКабинета = &ИдентификаторКабинета
			|	И НЕ УчетныеЗаписиМаркетплейсов.ПометкаУдаления
			|	И УчетныеЗаписиМаркетплейсов.ИдентификаторАккаунта<>""""
			|";  
		Запрос.УстановитьПараметр("ИдентификаторКабинета", ИдентификаторКабинета);
		УстановитьПривилегированныйРежим(Истина);
		Результат = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		Пока Результат.Следующий() Цикл 
			ИдентификаторАккаунта = Результат.ИдентификаторАккаунта;
		КонецЦикла;  
		ДанныеАвторизации = ДанныеАвторизацииПоИдентификаторуАккаунта(ИдентификаторАккаунта); 	
	КонецЕсли;
	
	Если ДанныеАвторизации <> Неопределено Тогда
		Магазины = ПолучитьМагазиныПользователяИзСервиса(ДанныеАвторизации, Отказ);
	КонецЕсли;	
		
	Если Не Отказ Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	УчетныеЗаписиМаркетплейсов.Ссылка КАК Ссылка,
			|	УчетныеЗаписиМаркетплейсов.Наименование КАК Наименование,
			|	УчетныеЗаписиМаркетплейсов.ПометкаУдаления КАК ПометкаУдаления,
			|	УчетныеЗаписиМаркетплейсов.ИдентификаторАккаунта КАК ИдентификаторАккаунта,
			|	УчетныеЗаписиМаркетплейсов.ИдентификаторМагазина КАК ИдентификаторМагазина,
			|	УчетныеЗаписиМаркетплейсов.ИдентификаторКабинета КАК ИдентификаторКабинета,
			|	УчетныеЗаписиМаркетплейсов.СхемаРаботы КАК СхемаРаботы,
			|	ЛОЖЬ КАК Активность
			|ИЗ
			|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
			|ГДЕ
			|	УчетныеЗаписиМаркетплейсов.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет)
			|	И УчетныеЗаписиМаркетплейсов.ИдентификаторКабинета = &ИдентификаторКабинета";
		
		Запрос.УстановитьПараметр("ИдентификаторКабинета", ИдентификаторКабинета);
		
		УстановитьПривилегированныйРежим(Истина);
		РезультатЗапроса = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		УчетныеЗаписи = РезультатЗапроса.Выгрузить();
		
		Для Каждого ЭлементКоллекции Из Магазины.campaigns Цикл
			Если ЭлементКоллекции.Свойство("placementType") Тогда 
				СхемаРаботы = ОпределитьСхемуРаботы(ЭлементКоллекции.placementType); 
			Иначе     
				СхемаРаботы = Перечисления.СхемыРаботыТорговыхПлощадок.ПустаяСсылка();
			КонецЕсли;
			
			Если СокрЛП(Формат(ЭлементКоллекции.business.id, "ЧН=; ЧГ=0"))=СокрЛП(ИдентификаторКабинета) Тогда
				СтрокаТаблицыЗначений = УчетныеЗаписи.Найти(Формат(ЭлементКоллекции.id, "ЧН=; ЧГ=0"), "ИдентификаторМагазина");
				Если СтрокаТаблицыЗначений = Неопределено Тогда
					НачатьТранзакцию();
					Попытка
						// блокировка изменения учетных записей
						БлокировкаДанных = Новый БлокировкаДанных;  
						ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.УчетныеЗаписиМаркетплейсов");
						ЭлементБлокировкиДанных.ИсточникДанных = РезультатЗапроса;
						ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
						БлокировкаДанных.Заблокировать();
						
						НоваяУчетнаяЗапись                                   = Справочники.УчетныеЗаписиМаркетплейсов.СоздатьЭлемент();
						НоваяУчетнаяЗапись.Наименование                      = ЭлементКоллекции.domain;
						НоваяУчетнаяЗапись.ВидМаркетплейса                   = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет;
						НоваяУчетнаяЗапись.ИдентификаторАккаунта             = ИдентификаторАккаунта;
						НоваяУчетнаяЗапись.ИдентификаторМагазина             = Формат(ЭлементКоллекции.id, "ЧН=; ЧГ=0");
						НоваяУчетнаяЗапись.ИдентификаторКабинета             = Формат(ЭлементКоллекции.business.id, "ЧН=; ЧГ=0");
						НоваяУчетнаяЗапись.СхемаРаботы                       = СхемаРаботы;
						НоваяУчетнаяЗапись.ПометкаУдаления                   = Ложь;
						НоваяУчетнаяЗапись.НеОбновлятьДанныеТорговойПлощадки = Истина;
						НоваяУчетнаяЗапись.Записать();
						ЗафиксироватьТранзакцию();
					Исключение
						ОтменитьТранзакцию();
						ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
						УровеньЖурналаРегистрации.Ошибка,,,
						ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					КонецПопытки;
					
				Иначе
					СтрокаТаблицыЗначений.Активность = Истина;
					
					Если ЭлементКоллекции.Свойство("placementType") И ЭлементКоллекции.placementType = "FBS"
						И СтрокаТаблицыЗначений.СхемаРаботы = Перечисления.СхемыРаботыТорговыхПлощадок.Express Тогда
						СхемаРаботы = Перечисления.СхемыРаботыТорговыхПлощадок.Express;
					КонецЕсли;
					
					Если СтрокаТаблицыЗначений.Наименование <> ЭлементКоллекции.domain
						Или СтрокаТаблицыЗначений.ИдентификаторАккаунта <> ИдентификаторАккаунта
						Или СтрокаТаблицыЗначений.ИдентификаторКабинета <> Формат(ЭлементКоллекции.business.id, "ЧН=; ЧГ=0")
						Или СтрокаТаблицыЗначений.СхемаРаботы <> СхемаРаботы Тогда
						НачатьТранзакцию();
						Попытка
						// блокировка изменения учетных записей
							БлокировкаДанных = Новый БлокировкаДанных;  
							ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.УчетныеЗаписиМаркетплейсов");
							ЭлементБлокировкиДанных.ИсточникДанных = РезультатЗапроса;
							ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
							БлокировкаДанных.Заблокировать();
						
							УчетнаяЗапись = СтрокаТаблицыЗначений.Ссылка.ПолучитьОбъект();
							УчетнаяЗапись.Наименование          = ЭлементКоллекции.domain; 
							УчетнаяЗапись.ИдентификаторАккаунта = ИдентификаторАккаунта;
							УчетнаяЗапись.ИдентификаторКабинета = Формат(ЭлементКоллекции.business.id, "ЧН=; ЧГ=0");
							УчетнаяЗапись.СхемаРаботы           = СхемаРаботы;
							УчетнаяЗапись.ПометкаУдаления       = Ложь;
							УчетнаяЗапись.Записать();
							ЗафиксироватьТранзакцию();
						Исключение
							ОтменитьТранзакцию();
							ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
							УровеньЖурналаРегистрации.Ошибка,,,
							ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
						КонецПопытки;
					КонецЕсли;
				КонецЕсли;  
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого СтрокаТаблицыЗначений Из УчетныеЗаписи Цикл
			Если Не СтрокаТаблицыЗначений.Активность И Не СтрокаТаблицыЗначений.ПометкаУдаления Тогда
				Попытка
					УчетнаяЗапись = СтрокаТаблицыЗначений.Ссылка.ПолучитьОбъект();
					УчетнаяЗапись.ИдентификаторКабинета = "";
					УчетнаяЗапись.ИдентификаторМагазина = "";
					УчетнаяЗапись.УстановитьПометкуУдаления(Истина);
					
				Исключение
					ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
						УровеньЖурналаРегистрации.Ошибка,,,
						ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				КонецПопытки;     
				
			ИначеЕсли СтрокаТаблицыЗначений.Активность И СтрокаТаблицыЗначений.ПометкаУдаления Тогда
				Попытка
					УчетнаяЗапись = СтрокаТаблицыЗначений.Ссылка.ПолучитьОбъект();
					УчетнаяЗапись.УстановитьПометкуУдаления(Ложь);
					
				Исключение
					ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
						УровеньЖурналаРегистрации.Ошибка,,,
						ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				КонецПопытки;   
			КонецЕсли; 
		КонецЦикла;
		
		Если Магазины.campaigns.Количество() = 0 Тогда
			Ошибка.КодОшибки      = "ЯндексМаркет_ПолучитьМагазиныЯндексМаркет";
			Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В личном кабинете торговой площадки для кабинета ""%1"" магазины не зарегистрированы.';
					|en = 'No stores are registered in the personal account of the trading platform for the %1 account.'"),
				ИдентификаторКабинета);
		КонецЕсли;
		
	Иначе
		Ошибка.КодОшибки      = "ЯндексМаркет_API_ПолучитьМагазиныЯндексМаркет";
		Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При получении списка магазинов для кабинета ""%1"" возникли ошибки. Подробности см. в журнале регистрации.';
				|en = 'Errors occurred when receiving the list of stores for the %1 account. For more information, see the event log.'"),
			ИдентификаторКабинета);
	КонецЕсли;
	
	Возврат Ошибка;
	
КонецФункции

#КонецОбласти

#Область УправлениеПродажами

// Выполняет загрузку информации о продажах товаров через торговую площадку. 
//
// Параметры:
//   УчетнаяЗапись           - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//                           - Неопределено - выполнить регламентное задание по всем учетным записям, с которыми разрешен обмен данными.
//   ДополнительныеПараметры - Произвольный - произвольные данные, переданные в функцию;
//                           - Структура - возможные ключи дополнительных параметров:
//     * ПоРасписанию          - Булево - признак автоматического или ручного запуска регламентного задания.
//     * НачалоПериода         - Дата, Неопределено - начало периода загрузки данных.
//     * ОкончаниеПериода      - Дата, Неопределено - окончание периода загрузки данных.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ЗагрузитьИнформациюОПродажахТоваровРегламентнымЗаданием(УчетнаяЗапись = Неопределено, 
			ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ЗагрузкаИнформацииОПродажахТоваровЧерезТорговуюПлощадку);
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет") Тогда
		ТекстОшибки = НСтр("ru = 'Интеграция с Яндекс Маркет не используется.';
							|en = 'Integration with Yandex.Market is not used.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
				
		Ошибка.КодОшибки      = "ЯндексМаркет_ИспользоватьИнтеграциюСЯндексМаркет";
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись);
		ДанныеАвторизации   = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		
		Если ДанныеАвторизации = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" отсутствует информация об аккаунте.';
					|en = 'There is no information for the ""%1"" account.'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_ОтсутствуетИнформацияОбАккаунте";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
		
		Если ДанныеУчетнойЗаписи.НеОбновлятьДанныеТорговойПлощадки Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" запрещен обмен данными с торговой площадкой.';
					|en = 'Data exchange with the trading platform is not allowed for account ""%1"".'", 
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_НеОбновлятьДанныеТорговойПлощадки";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
	КонецЕсли;
	
	ПоРасписанию = Истина;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") 
			И ДополнительныеПараметры.Свойство("ПоРасписанию") Тогда
		ПоРасписанию = ДополнительныеПараметры.ПоРасписанию;
	КонецЕсли;
	
	НачалоПериода    = НачалоМесяца(ДобавитьМесяц(ТекущаяДатаСеанса(), -1));
	ОкончаниеПериода = КонецМесяца(НачалоПериода);
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		Если ДополнительныеПараметры.Свойство("НачалоПериода") Тогда
			НачалоПериода = ?(ЗначениеЗаполнено(ДополнительныеПараметры.НачалоПериода), 
				ДополнительныеПараметры.НачалоПериода, 
				НачалоПериода);
		КонецЕсли;
		Если ДополнительныеПараметры.Свойство("ОкончаниеПериода") Тогда
			ОкончаниеПериода = ?(ЗначениеЗаполнено(ДополнительныеПараметры.ОкончаниеПериода), 
				ДополнительныеПараметры.ОкончаниеПериода, 
				ОкончаниеПериода);
		КонецЕсли;
	КонецЕсли;
	
	Если НачалоПериода > ОкончаниеПериода Тогда
		ТекстОшибки = НСтр("ru = 'Период загрузки информации о продажах товаров через торговую площадку указан неверно.';
							|en = 'Period of importing information about goods sales via the marketplace is incorrectly specified.'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
				
		Ошибка.КодОшибки      = "ЯндексМаркет_API_ЗагрузитьИнформациюОПродажахТоваров";
		Ошибка.ОписаниеОшибки = ТекстОшибки;

		Возврат Ошибка;
	КонецЕсли;
	
	Если ПоРасписанию Тогда
		Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Запуск по расписанию: загрузка информации о продажах товаров через торговую площадку для учетной записи ""%1"".';
						|en = 'Run on schedule: import the information about sales via the marketplace for the ""%1"" account.'", 
						ОбщегоНазначения.КодОсновногоЯзыка()),
					УчетнаяЗапись));
		Иначе
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				НСтр("ru = 'Запуск по расписанию: загрузка информации о продажах товаров через торговую площадку.';
					|en = 'Run on schedule: import the information about sales via the marketplace.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецЕсли;
	КонецЕсли;
	
	ВыборкаДанных = ВыбратьУчетныеЗаписиЯндексМаркет(УчетнаяЗапись);
	
	Пока ВыборкаДанных.Следующий() Цикл
		// Получить файлы отчетов
		Файлы = Новый Массив;
		
		Попытка
			Отказ = Ложь;
			Файлы = ПолучитьОтчетыОРеализованныхТоварахИзСервиса(ВыборкаДанных, 
				НачалоПериода, 
				ОкончаниеПериода, 
				Отказ);
			
			Если Отказ Тогда
				Ошибка.КодОшибки      = "ЯндексМаркет_API_ЗагрузитьИнформациюОПродажахТоваров";
				Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'При загрузке информации о продажах товаров через торговую площадку для учетной записи ""%1"" возникли ошибки. Подробности см. в журнале регистрации.';
						|en = 'Errors occurred when importing the information about sales via the marketplace for the ""%1"" account. For more information, see the Event log.'"),
					ВыборкаДанных.Наименование);
				Продолжить;
			КонецЕсли;
			
		Исключение
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При загрузке информации о продажах товаров через торговую площадку для учетной записи ""%1"" возникли ошибки: %2';
					|en = 'Errors occurred when importing the information about sales via the marketplace for the ""%1"" account: %2'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				ВыборкаДанных.УчетнаяЗапись,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстОшибки);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_ЗагрузкаИнформацииОПродажахТоваровЧерезТорговуюПлощадку";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Продолжить;
		КонецПопытки;
		
		// Обработать полученные файлы
		Если Файлы.Количество() = 0 Тогда
			Отказ = Истина;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При загрузке информации о продажах товаров через торговую площадку для учетной записи ""%1"" возникли ошибки: за период с %2 по %3 нет данных.';
					|en = 'Errors occurred when importing the information about sales via the marketplace for the ""%1"" account: there is no data for the period from %2 to %3.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()), 
				ВыборкаДанных.УчетнаяЗапись,
				Формат(НачалоПериода, "ДЛФ=D;"),
				Формат(ОкончаниеПериода, "ДЛФ=D;"));
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
				
			Ошибка.КодОшибки      = "ЯндексМаркет_ЗагрузкаИнформацииОПродажахТоваровЧерезТорговуюПлощадку";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Продолжить;
			
		Иначе  
			// Инициализация шаблонов
			ЛистыШаблона = Новый Структура;
			ЛистыШаблона.Вставить("Товары__переданные_в_доставку", Новый Структура("ИмяЛиста, ИмяМакетаСШаблоном, Служебный", 
																		"Товары, переданные в доставку", "ПФ_MXL_ТоварыПереданныеВДоставку", 1));
			ЛистыШаблона.Вставить("Доставленные_товары",           Новый Структура("ИмяЛиста, ИмяМакетаСШаблоном, Служебный", 
																		"Доставленные товары", "ПФ_MXL_ДоставленныеТовары", 2));
			ЛистыШаблона.Вставить("Невыкупленные_товары",          Новый Структура("ИмяЛиста, ИмяМакетаСШаблоном, Служебный", 
																		"Невыкупленные товары", "ПФ_MXL_НевыкупленныеТовары", 3));
			ЛистыШаблона.Вставить("Возвращенные_товары",           Новый Структура("ИмяЛиста, ИмяМакетаСШаблоном, Служебный", 
																		"Возвращенные товары", "ПФ_MXL_ВозвращенныеТовары", 4));
			
			ПараметрыЗагрузкиВТабличнуюЧасть = ИнтеграцияСМаркетплейсамиСервер.ПараметрыЗагрузкиИзФайлаВТабличнуюЧасть();
			ПараметрыЗагрузкиВТабличнуюЧасть.ИмяОбъектаМетаданных = "Обработка.УправлениеПродажамиНаЯндексМаркет";
			ПараметрыЗагрузкиВТабличнуюЧасть.ИмяТабличнойЧасти    = "ДанныеОРеализованныхТоварах";
			ПараметрыЗагрузкиВТабличнуюЧасть.ИмяМакетаСШаблоном   = "";
			
			Для Каждого КлючИЗначение Из ЛистыШаблона Цикл
				ПараметрыЗагрузкиВТабличнуюЧасть.ИмяМакетаСШаблоном = КлючИЗначение.Значение.ИмяМакетаСШаблоном;
				
				ПараметрыШапки       = ИнтеграцияСМаркетплейсамиСервер.ПараметрыШапки();
				ИнформацияПоКолонкам = ИнтеграцияСМаркетплейсамиСервер.ИнициализироватьТаблицуИнформацииПоКолонкам();
				
				ИнтеграцияСМаркетплейсамиСервер.ОпределитьИнформациюПоКолонкамИПараметрамШапки(
					ПараметрыЗагрузкиВТабличнуюЧасть,
					ИнформацияПоКолонкам,
					ПараметрыШапки);
					
				КлючИЗначение.Значение.Вставить("ПараметрыШапки",                           ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыШапки));
				КлючИЗначение.Значение.Вставить("ИнформацияПоКолонкам",                     ОбщегоНазначения.СкопироватьРекурсивно(ИнформацияПоКолонкам));
				КлючИЗначение.Значение.Вставить("ВысотаЗаголовкаТаблицыМакетаСШаблоном",    ПараметрыЗагрузкиВТабличнуюЧасть.ВысотаЗаголовкаТаблицыМакетаСШаблоном);
				КлючИЗначение.Значение.Вставить("ВысотаЗаголовкаТаблицыЗагружаемогоМакета", ПараметрыЗагрузкиВТабличнуюЧасть.ВысотаЗаголовкаТаблицыЗагружаемогоМакета);
				КлючИЗначение.Значение.Вставить("ШаблонСДанными",                           Новый ТабличныйДокумент);
				КлючИЗначение.Значение.Вставить("ЗаполняемыйШаблон",                        Неопределено);
			КонецЦикла;
			
			// Чтение данных
			ТаблицаЗагруженныхДанных = Новый ТаблицаЗначений;
			
			Для Каждого Файл Из Файлы Цикл
				ДанныеФайла = Новый ТабличныйДокумент;
				ДанныеФайла.Прочитать(Файл, СпособЧтенияЗначенийТабличногоДокумента.Значение);
				ФайловаяСистема.УдалитьВременныйФайл(Файлы[0]);
				
				Для Каждого Область Из ДанныеФайла.Области Цикл
					Если ЛистыШаблона.Свойство(Область.Имя) Тогда
						ШаблонСДанными = Новый ТабличныйДокумент;
						ШаблонСДанными.Вывести(ДанныеФайла.ПолучитьОбласть(Область.Имя));
						
						ЛистыШаблона[Область.Имя].ШаблонСДанными = ШаблонСДанными;
					КонецЕсли;
				КонецЦикла;
				
				ПараметрыЗагрузки = ИнтеграцияСМаркетплейсамиСервер.ПараметрыЗагрузкиТабличногоДокумента(Новый УникальныйИдентификатор);
				ПараметрыЗагрузки.ЛистыШаблона = ОбщегоНазначения.СкопироватьРекурсивно(ЛистыШаблона);
				
				РезультатЗагрузки = ИнтеграцияСМаркетплейсамиСервер.ТабличныйДокументВТаблицу(ДанныеФайла, 
					ПараметрыЗагрузки, 
					Истина);
				
				Для Каждого ЭлементКоллекции Из РезультатЗагрузки.Колонки Цикл
					Если ТаблицаЗагруженныхДанных.Колонки.Найти(ЭлементКоллекции.Имя) = Неопределено Тогда
						ТаблицаЗагруженныхДанных.Колонки.Добавить(ЭлементКоллекции.Имя, 
							Новый ОписаниеТипов(ЭлементКоллекции.ТипЗначения), 
							ЭлементКоллекции.Заголовок);
					КонецЕсли;
				КонецЦикла;
				
				Для Каждого СтрокаТаблицыЗначений Из РезультатЗагрузки Цикл
					Если СтрокаТаблицыЗначений.Служебный = 1
							И (СтрокаТаблицыЗначений.ДатаПередачиВДоставку < НачалоПериода
								Или СтрокаТаблицыЗначений.ДатаПередачиВДоставку > ОкончаниеПериода) Тогда
						Продолжить;
					ИначеЕсли СтрокаТаблицыЗначений.Служебный = 2
								И (СтрокаТаблицыЗначений.ДатаДоставки < НачалоПериода
									Или СтрокаТаблицыЗначений.ДатаДоставки > ОкончаниеПериода) Тогда
						Продолжить;
					ИначеЕсли (СтрокаТаблицыЗначений.Служебный = 3
									Или СтрокаТаблицыЗначений.Служебный = 4)
								И (СтрокаТаблицыЗначений.ДатаНевыкупаВозврата < НачалоПериода
									Или СтрокаТаблицыЗначений.ДатаНевыкупаВозврата > ОкончаниеПериода) Тогда
						Продолжить;
					КонецЕсли;
					
					ЗаполнитьЗначенияСвойств(ТаблицаЗагруженныхДанных.Добавить(), СтрокаТаблицыЗначений);
				КонецЦикла;
			КонецЦикла;
			
			Для Каждого СтрокаТаблицыЗначений Из ТаблицаЗагруженныхДанных Цикл
				СтрокаТаблицыЗначений.НомерСтроки = ТаблицаЗагруженныхДанных.Индекс(СтрокаТаблицыЗначений) + 1;
			КонецЦикла;
			
			// Сопоставление данных
			ТаблицаСопоставленияДанных = ТаблицаЗагруженныхДанных.СкопироватьКолонки();
			
			ПараметрыСопоставления = Новый Структура;
			ПараметрыСопоставления.Вставить("УчетнаяЗапись",                    ВыборкаДанных.УчетнаяЗапись);
			ПараметрыСопоставления.Вставить("ИмяТабличнойЧасти",                "ДанныеОРеализованныхТоварах");
			ПараметрыСопоставления.Вставить("КолонкиОбязательныеПриПроверке",   Новый СписокЗначений);
			ПараметрыСопоставления.Вставить("КолонкиДополнительныеПриПроверке", Новый СписокЗначений);
			
			Обработки.УправлениеПродажамиНаЯндексМаркет.СопоставитьЗагружаемыеДанные(ТаблицаСопоставленияДанных, 
				ТаблицаЗагруженныхДанных, 
				ПараметрыСопоставления);
				
			// Создание документов
			ПараметрыЗаписи = Новый Структура;
			ПараметрыЗаписи.Вставить("УчетнаяЗапись",                ВыборкаДанных.УчетнаяЗапись);
			ПараметрыЗаписи.Вставить("ДополнительныеПараметры",      Неопределено);
			ПараметрыЗаписи.Вставить("ИмяТабличнойЧасти",            "ДанныеОРеализованныхТоварах");
			ПараметрыЗаписи.Вставить("ЗагружатьНезаполненныеСтроки", Истина);
			
			РезультатЗаписи = Обработки.УправлениеПродажамиНаЯндексМаркет.ЗаписатьЗагружаемыеДанные(ТаблицаСопоставленияДанных, 
				ПараметрыЗаписи);
			
			Если Не ПустаяСтрока(РезультатЗаписи.ОписаниеОшибки) Тогда
				Отказ = Истина;
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'При загрузке информации о продажах товаров через торговую площадку для учетной записи ""%1"" возникли ошибки: %2';
						|en = 'Errors occurred when importing the information about sales via the marketplace for the ""%1"" account: %2'",
						ОбщегоНазначения.КодОсновногоЯзыка()), 
					ВыборкаДанных.УчетнаяЗапись,
					РезультатЗаписи.ОписаниеОшибки);
				
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
					УровеньЖурналаРегистрации.Ошибка,,, 
					ТекстОшибки);
					
				Ошибка.КодОшибки      = "ЯндексМаркет_ЗагрузкаИнформацииОПродажахТоваровЧерезТорговуюПлощадку";
				Ошибка.ОписаниеОшибки = ТекстОшибки;
				
				Продолжить;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ПоРасписанию Тогда 
		Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Завершена загрузка информации о продажах товаров через торговую площадку для учетной записи ""%1"".';
						|en = 'Information about sales via the marketplace for the ""%1"" account is imported.'", 
						ОбщегоНазначения.КодОсновногоЯзыка()),
					УчетнаяЗапись));
		Иначе
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				НСтр("ru = 'Завершена загрузка информации о продажах товаров через торговую площадку.';
					|en = 'Information about sales via the marketplace is imported.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Ошибка;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область РегламентныеЗадания

// Выгружает остатки товарных позиций из информационной базы на торговую площадку.
//
// Параметры:
//   УчетнаяЗапись           - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//                           - Неопределено - выполнить регламентное задание по всем учетным записям, с которыми разрешен обмен данными.
//   ДополнительныеПараметры - Произвольный - произвольные данные, переданные в функцию;
//                           - Структура - возможные ключи дополнительных параметров:
//     * ПоРасписанию          - Булево - признак автоматического или ручного запуска регламентного задания.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
Функция ВыгрузитьОстаткиТоваровРегламентнымЗаданием(УчетнаяЗапись = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет") Тогда
		ТекстОшибки = НСтр("ru = 'Интеграция с Яндекс Маркет не используется.';
							|en = 'Integration with Yandex.Market is not used.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
				
		Ошибка.КодОшибки      = "ЯндексМаркет_ИспользоватьИнтеграциюСЯндексМаркет";
		Ошибка.ОписаниеОшибки = ТекстОшибки;

		Возврат Ошибка;
	КонецЕсли;

	ПоРасписанию = Истина;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") 
		 И ДополнительныеПараметры.Свойство("ПоРасписанию") Тогда
		ПоРасписанию = ДополнительныеПараметры.ПоРасписанию;
	КонецЕсли;
	
	Если ПоРасписанию Тогда
		СтрокаИнформацииПоУЗ = НСтр("ru = 'Запуск по расписанию: выгрузка остатков товаров для учетной записи ""%1"" на торговую площадку.';
									|en = 'Run on schedule: export the stock balance for the ""%1"" account to the trading platform.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		СтрокаИнформацииБезУЗ = НСтр("ru = 'Запуск по расписанию: выгрузка остатков товаров на торговую площадку.';
									|en = 'Run on schedule: export the stock balance to the trading platform.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
	Иначе
		СтрокаИнформацииПоУЗ = НСтр("ru = 'Запуск по инициативе пользователя: выгрузка остатков товаров для учетной записи ""%1"" на торговую площадку.';
									|en = 'Run by user: export the stock balance for the ""%1"" account to the trading platform.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		СтрокаИнформацииБезУЗ = НСтр("ru = 'Запуск по инициативе пользователя: выгрузка остатков товаров на торговую площадку.';
									|en = 'Run by user: export the stock balance to the trading platform.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись);
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		
		Если ДанныеАвторизации = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" отсутствует информация об аккаунте.';
					|en = 'There is no information for the ""%1"" account.'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_ОтсутствуетИнформацияОбАккаунте";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
		
		Если ДанныеУчетнойЗаписи.НеОбновлятьДанныеТорговойПлощадки Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для учетной записи ""%1"" запрещен обмен данными с торговой площадкой.';
					|en = 'Data exchange with the trading platform is not allowed for the ""%1"" account.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = "ЯндексМаркет_НеОбновлятьДанныеТорговойПлощадки";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
			
			Возврат Ошибка;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				СтрокаИнформацииПоУЗ, 
				УчетнаяЗапись));
	Иначе
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			СтрокаИнформацииБезУЗ);
	КонецЕсли;
	
	ВыборкаДанных = ВыбратьУчетныеЗаписиЯндексМаркет(УчетнаяЗапись);
	
	Пока ВыборкаДанных.Следующий() Цикл
		Попытка
			Отказ = Ложь;
			ВыгрузитьОстаткиТоваровВСервис(ВыборкаДанных, Отказ);
			
			Если Отказ Тогда
				Ошибка.КодОшибки      = "ЯндексМаркет_API_ВыгрузитьОстаткиТоваровЯндексМаркет";
				Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'При выгрузке остатков товаров для учетной записи ""%1"" на торговую площадку возникли ошибки. Подробности см. в журнале регистрации.';
						|en = 'Errors occurred when exporting the stock balance for the ""%1"" account to the trading platform. For more information, see the event log.'"),
					ВыборкаДанных.УчетнаяЗапись);
					
			Иначе
				УстановитьПривилегированныйРежим(Истина);
				СерверныеОповещения.ОтправитьСерверноеОповещение(
					"ИнтеграцияСЯндексМаркетСервер.ОбновитьДанные",
					ВыборкаДанных.УчетнаяЗапись,
					Неопределено,
					Истина);
				УстановитьПривилегированныйРежим(Ложь);
			КонецЕсли;
			
		Исключение
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При выгрузке остатков товаров для учетной записи ""%1"" на торговую площадку возникли ошибки: %2';
					|en = 'Errors occurred when exporting the stock balance for the ""%1"" account to the trading platform: %2'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				ВыборкаДанных.УчетнаяЗапись,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстОшибки);
					
			Ошибка.КодОшибки      = "ЯндексМаркет_ВыгрузитьОстаткиТоваровЯндексМаркет";
			Ошибка.ОписаниеОшибки = ТекстОшибки;
		КонецПопытки;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Завершена выгрузка остатков товаров для учетной записи ""%1"" на торговую площадку.';
					|en = 'The stock balance for the ""%1"" account is exported to the trading platform.'", 
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись));
	Иначе
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,,,
			НСтр("ru = 'Завершена выгрузка остатков товаров на торговую площадку.';
				|en = 'The stock balance is exported to the trading platform.'", 
				ОбщегоНазначения.КодОсновногоЯзыка()));
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Ошибка;
	
КонецФункции

// Определяет префиксы и представления регламентных заданий.
//
// Параметры:
//   ТолькоОбязательные       - Булево - признак получения только обязательных регламентных заданий;
//   ДоступнаРаботаСОстатками - Булево - см. Справочники.УчетныеЗаписиМаркетплейсов.ДоступнаРаботаСОстатками.
// 
// Возвращаемое значение:
//   СписокЗначений Из Строка - префиксы и представления регламентных заданий:
//     * Значение      - Строка - префикс регламентного задания;
//     * Представление - Строка - представление регламентного задания.
//
Функция ПрефиксыРегламентныхЗаданий(ТолькоОбязательные = Ложь, ДоступнаРаботаСОстатками = Истина) Экспорт

	Префиксы = Новый СписокЗначений;
	
	Префиксы.Добавить("ЯндексМаркет_ОтправкаНаМодерациюСвязейТоваровЯндексМаркет_",
		НСтр("ru = 'Яндекс Маркет. Отправка на модерацию связей товаров';
			|en = 'Yandex.Market. Send goods links to moderation'"));
	Префиксы.Добавить("ЯндексМаркет_ПолучениеСтатусовМодерацииТоваровЯндексМаркет_",
		НСтр("ru = 'Яндекс Маркет. Получение статусов модерации товаров';
			|en = 'Yandex.Market. Receive goods moderation statuses'"));

	Если Не ТолькоОбязательные Тогда
		Префиксы.Добавить("ЯндексМаркет_ВыгрузкаУстановленныхЦенЯндексМаркет_",
			НСтр("ru = 'Яндекс Маркет. Выгрузка установленных цен товаров';
				|en = 'Yandex.Market. Export set goods prices'"));

		Если ДоступнаРаботаСОстатками Тогда
			Префиксы.Добавить("ЯндексМаркет_ВыгрузкаОстатковТоваровЯндексМаркет_",
				НСтр("ru = 'Яндекс Маркет. Выгрузка остатков товаров';
					|en = 'Yandex.Market. Export stock balance'"));
		КонецЕсли;

		Префиксы.Добавить("ЯндексМаркет_ЗагрузкаДанныхОПродажах_",
			НСтр("ru = 'Яндекс Маркет. Загрузка информации о продажах товаров через торговую площадку';
				|en = 'Yandex.Market. Import information about goods sales via marketplace'"));  
			
		Префиксы.Добавить("ЯндексМаркет_ЗагрузкаНеобработанныхЗаказовСТорговойПлощадки_",
			НСтр("ru = 'Яндекс Маркет. Загрузка информации о необработанных заказах с торговой площадки.';
				|en = 'Yandex.Market. Import data on unprocessed orders from the trading platform.'"));	
			
		Префиксы.Добавить("ЯндексМаркет_ОбновлениеСтатусовЗаказовТорговойПлощадки_",
			НСтр("ru = 'Яндекс Маркет. Обновление статусов заказов торговой площадки.';
				|en = 'Yandex.Market. Update trading platform order statuses.'"));   	

	КонецЕсли;

	Возврат Префиксы;

КонецФункции

// Заполняет соответствие имен методов их псевдонимам для вызова из очереди заданий.
// См. ОчередьЗаданийЛокализация.ПриОпределенииПсевдонимовОбработчиков.
//
// Параметры:
//   СоответствиеИменПсевдонимам - Соответствие из КлючИЗначение:
//     * Ключ - Строка - псевдоним метода, например "ОчиститьОбластьДанных".
//     * Значение - Строка - Имя метода для вызова, например "РаботаВМоделиСервиса.ОчиститьОбластьДанных".
//         В качестве значения можно указать Неопределено, в этом случае считается что имя совпадает с псевдонимом.
//
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ВыгрузкаОстатковНаМаркетплейсы.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ВыгрузкаЦенНаМаркетплейсы.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ЗагрузкаРекомендованныхЦенЯндексМаркет.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ОтправкаНаМодерациюСвязейТоваровЯндексМаркет.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ПолучениеРекомендацийПоСклейкеТовараЯндексМаркет.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ПолучениеСтатусовМодерацииТоваровЯндексМаркет.ИмяМетода);
	
КонецПроцедуры

// Определяет состояния регламентных заданий.
//
// Параметры:
//   УчетнаяЗапись               - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//   ПрефиксыРегламентныхЗаданий - СписокЗначений Из Строка, Неопределено - см. ПрефиксыРегламентныхЗаданий.
// 
// Возвращаемое значение:
//   Структура - состояния. Содержит поля:
//       * ЕстьОтключенныеРегламентныеЗадания - Булево - признак наличия отключенных регламентных заданий;
//       * ОтключенныеРегламентныеЗадания     - СписокЗначений Из Строка - содержит данные для выбора. Колонки:
//           ** Значение      - Строка - префикс регламентного задания;
//           ** Представление - Строка - представление регламентного задания.
//
Функция ПолучитьСостоянияРегламентныхЗаданийМагазина(УчетнаяЗапись, ПрефиксыРегламентныхЗаданий = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СостоянияРегламентныхЗаданийМагазина = Новый Структура;
	СостоянияРегламентныхЗаданийМагазина.Вставить("ЕстьОтключенныеРегламентныеЗадания", Ложь);
	СостоянияРегламентныхЗаданийМагазина.Вставить("ОтключенныеРегламентныеЗадания",     Новый СписокЗначений);

	ДоступнаРаботаСОстатками = Справочники.УчетныеЗаписиМаркетплейсов.ДоступнаРаботаСОстаткамиДляУчетнойЗаписи(УчетнаяЗапись);

	Если ПрефиксыРегламентныхЗаданий = Неопределено Тогда
		ПрефиксыРегламентныхЗаданий = ПрефиксыРегламентныхЗаданий(Ложь, ДоступнаРаботаСОстатками);
	КонецЕсли;

	Для Каждого Префикс Из ПрефиксыРегламентныхЗаданий Цикл
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 (%2)", Префикс.Представление, УчетнаяЗапись);
		Ключ          = Префикс.Значение + Строка(УчетнаяЗапись.УникальныйИдентификатор());

		Отбор = Новый Структура("Ключ", Ключ);
		Результат = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
		
		Если Результат.Количество() = 0 Тогда
			СостоянияРегламентныхЗаданийМагазина.ЕстьОтключенныеРегламентныеЗадания = Истина;
			СостоянияРегламентныхЗаданийМагазина.ОтключенныеРегламентныеЗадания.Добавить(Префикс.Значение, Представление);
		Иначе
			Задание = Результат[0];
			Если Не Задание.Использование Тогда
				СостоянияРегламентныхЗаданийМагазина.ЕстьОтключенныеРегламентныеЗадания = Истина;
				СостоянияРегламентныхЗаданийМагазина.ОтключенныеРегламентныеЗадания.Добавить(Префикс.Значение, Представление);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Возврат СостоянияРегламентныхЗаданийМагазина;

КонецФункции

// Определяет возможность выполнения регламентного задания.
//
// Параметры:
//   УчетнаяЗаписьТорговойПлощадки            - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения
//                                                к сервису.
//   ЗаписатьСобытие                          - Булево - признак записи события в журнал регистрации.
//
// Возвращаемое значение:
//   Булево - разрешение или запрет на выполнение регламентного задания.
//
Функция МожноВыполнитьРегламентноеЗадание(УчетнаяЗаписьТорговойПлощадки, ЗаписатьСобытие = Истина) Экспорт
	
	Если Не ЗначениеЗаполнено(УчетнаяЗаписьТорговойПлощадки)
			Или Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет") Тогда
		Если ЗаписатьСобытие Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				НСтр("ru = 'Не используется интеграция с Яндекс Маркет';
					|en = 'Integration with Yandex.Market is not used'", 
					ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецЕсли;
		
		Возврат Ложь;
	КонецЕсли;

	УчетнаяЗаписьПомеченаНаУдаление = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗаписьТорговойПлощадки, "ПометкаУдаления");
	Если УчетнаяЗаписьПомеченаНаУдаление Тогда
		Если ЗаписатьСобытие Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не используется интеграция с Яндекс Маркет для учетной записи <%1>, т.к. очищены настройки авторизации';
						|en = 'Integration with Yandex.Market is not used for the <%1> account as authorization settings are cleared'", 
						ОбщегоНазначения.КодОсновногоЯзыка()),
					УчетнаяЗаписьТорговойПлощадки));
		КонецЕсли;
		
		Возврат Ложь;
	КонецЕсли;
	
	Если Не Справочники.УчетныеЗаписиМаркетплейсов.ОбновленияДанныхТорговойПлощадкиРазрешено(УчетнаяЗаписьТорговойПлощадки) Тогда
		Если ЗаписатьСобытие Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Информация,,,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Для учетной записи <%1> запрещено обновление данных торговой площадки';
						|en = 'The trading platform data update is not allowed for the <%1> account'", 
						ОбщегоНазначения.КодОсновногоЯзыка()),
					УчетнаяЗаписьТорговойПлощадки));
		КонецЕсли;
		
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

#КонецОбласти

#Область СервисныеФункции

// Возвращает имя события журнала регистрации работы с торговой площадкой.
//
// Возвращаемое значение:
//   Строка - Наименование события для записей в журнале регистрации.
//
Функция СобытиеЖурналаРегистрации() Экспорт

	Возврат НСтр("ru = 'Интеграция с Яндекс Маркет';
				|en = 'Integration with Yandex.Market'", 
				ОбщегоНазначения.КодОсновногоЯзыка());

КонецФункции

// Преобразует строку, содержащую текст JSON, в структуру данных.
//
// Параметры:
//   СтрокаJSON                   - Строка - строка, содержащая текст в формате JSON.
//   ИменаСвойствСоЗначениямиДата - Массив Из Строка, Строка - массив, элементы которого содержат имена свойств JSON, для 
//                                    которых нужно вызывать восстановление даты из строки.
//   ПрочитатьВСоответствие       - Булево - если установлено Истина, чтение объекта JSON будет выполнено в Соответствие. Если установлено
//                                    Ложь, объекты будут считываться в объект типа Структура. Значение по умолчанию: Ложь.
//
// Возвращаемое значение:
//   Произвольный - результат преобразования строки JSON.
//
Функция ИзJSON(СтрокаJSON, ИменаСвойствСоЗначениямиДата = "", ПрочитатьВСоответствие = Ложь) Экспорт

	Если ЗначениеЗаполнено(ИменаСвойствСоЗначениямиДата) Тогда
		Если ТипЗнч(ИменаСвойствСоЗначениямиДата) = Тип("Строка") Тогда
			ИменаСвойствСоЗначениямиДата = СтрЗаменить(ИменаСвойствСоЗначениямиДата, " ", "");
			ИменаСвойствСоЗначениямиДата = СтрРазделить(ИменаСвойствСоЗначениямиДата, ",", Ложь);
		КонецЕсли;
	КонецЕсли;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	Результат = ПрочитатьJSON(ЧтениеJSON, 
					ПрочитатьВСоответствие, 
					ИменаСвойствСоЗначениямиДата, 
					ФорматДатыJSON.ISO,
					?(ЗначениеЗаполнено(ИменаСвойствСоЗначениямиДата), "ВосстановитьДатуJSON", Неопределено),
					?(ЗначениеЗаполнено(ИменаСвойствСоЗначениямиДата), ИнтеграцияСЯндексМаркетСервер, Неопределено),
					,
					?(ЗначениеЗаполнено(ИменаСвойствСоЗначениямиДата), ИменаСвойствСоЗначениямиДата, Неопределено));
	ЧтениеJSON.Закрыть();
	
	Возврат Результат;

КонецФункции

// Восстанавливает значение даты из JSON.
//
// Параметры:
//   Свойство                - Строка - имя свойства.
//   Значение                - Строка, Неопределено - значение свойства.
//   ДополнительныеПараметры - Структура, Неопределено - параметры восстановления.
//
// Возвращаемое значение:
//   Дата - результат восстановления.
//
Функция ВосстановитьДатуJSON(Свойство, Значение, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Значение) Тогда
		Если ТипЗнч(Значение) = Тип("Строка") Тогда
			Значение = СтрЗаменить(Значение, " ", "T");
		КонецЕсли;
		
		Попытка
			Результат = ПрочитатьДатуJSON(Значение, ФорматДатыJSON.ISO);
		Исключение
			Результат = Дата(1, 1, 1);
		КонецПопытки;
		
	Иначе
		Результат = Дата(1, 1, 1);
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Формирует текст ошибки по результатам работы метода сервиса.
//
// Параметры:
//   Ошибки - Массив Из Структура - ошибки сервиса:
//     * code    - Строка - код ошибки;
//     * message - Строка - описание ошибки.
//
// Возвращаемое значение:
//   Строка - описание ошибок.
//
Функция ТекстОшибки(Ошибки) Экспорт
	
	Результат = "";
	
	Для Каждого ЭлементКоллекции Из Ошибки Цикл 
		Если ЭлементКоллекции.code = "FORBIDDEN" Тогда
			Если СтрНайти(ЭлементКоллекции.message,"Token does not have") > 0 Тогда
				СообщениеОграниченияДоступа = Нстр("ru = 'Нарушение прав доступа. Выданный ключ не поддерживает обращение к вызываемому методу. Ответ сервиса:';
													|en = 'Access right violation. The issued key does not support accessing the called method. Service response:'");
			ИначеЕсли  СтрНайти(ЭлементКоллекции.message,"The method is deprecated") > 0 Тогда
				СообщениеОграниченияДоступа = Нстр("ru = 'Функциональность временно не доступна. Использование вызываемого метода запрещено, метод устарел. Ответ сервиса:';
													|en = 'The functionality is temporarily unavailable. The called method is outdated and not allowed. Service response:'"); 	
			Иначе
				СообщениеОграниченияДоступа = "";
			КонецЕсли;
			Результат = Результат + СообщениеОграниченияДоступа + " (" + ЭлементКоллекции.code + ") " + ЭлементКоллекции.message + "; " + Символы.ПС; 
		Иначе
			Результат = Результат + "(" + ЭлементКоллекции.code + ") " + ЭлементКоллекции.message + "; " + Символы.ПС; 
		КонецЕсли;
	КонецЦикла;
		
	Возврат СокрЛП(Результат);
	
КонецФункции

// Получает список причин отмены.
//
// Параметры:
//   ИдентификаторПричиныОтмены - Строка - идентификатор причины отмены.
//
// Возвращаемое значение:
//   Структура - описание причин отмены:
//     * КодОшибки      - Строка - код ошибки.
//     * ОписаниеОшибки - Строка - описание ошибки.
//     * Детализация    - Неопределено
//                      - Массив из см. НовыйОписаниеПричиныОтмены
//
Функция ПолучитьПричиныОтмены(ИдентификаторПричиныОтмены = Неопределено) Экспорт
	
	Ошибка = Новый Структура;
	Ошибка.Вставить("КодОшибки",      "");
	Ошибка.Вставить("ОписаниеОшибки", "");
	Ошибка.Вставить("Детализация",    Неопределено);   
	
	Детализация = Новый Массив; // Массив из см. НовыйОписаниеПричиныОтмены
	
	Если ИдентификаторПричиныОтмены  = Неопределено 
		Или ИдентификаторПричиныОтмены = "RESERVATION_EXPIRED" Тогда
		Данные = НовыйОписаниеПричиныОтмены();
		Данные.Идентификатор = "RESERVATION_EXPIRED";
		Данные.Наименование = НСтр("ru = 'Покупатель не завершил оформление';
									|en = 'The customer did not complete the registration'");
		Данные.Инициатор = "customer";
		Данные.Доступность = Истина; 
		Детализация.Добавить(Данные);  
	КонецЕсли;
	
	Если ИдентификаторПричиныОтмены  = Неопределено 
		Или ИдентификаторПричиныОтмены = "USER_NOT_PAID" Тогда
		Данные = НовыйОписаниеПричиныОтмены();	
		Данные.Идентификатор = "USER_NOT_PAID";
		Данные.Наименование = НСтр("ru = 'Покупатель не оплатил заказ';
									|en = 'The customer did not pay for the order'");
		Данные.Инициатор = "customer";
		Данные.Доступность = Истина; 
		Детализация.Добавить(Данные);  
	КонецЕсли;
	
	Если ИдентификаторПричиныОтмены  = Неопределено 
		Или ИдентификаторПричиныОтмены = "USER_UNREACHABLE" Тогда
		Данные = НовыйОписаниеПричиныОтмены();
		Данные.Идентификатор = "USER_UNREACHABLE"; 
		Данные.Наименование = НСтр("ru = 'Не удалось связаться с покупателем';
									|en = 'Could not contact the customer'");
		Данные.Инициатор =  "customer";
		Данные.Доступность = Истина; 
		Детализация.Добавить(Данные);
	КонецЕсли;
	
	Если ИдентификаторПричиныОтмены  = Неопределено 
		Или ИдентификаторПричиныОтмены = "USER_CHANGED_MIND" Тогда
		Данные = НовыйОписаниеПричиныОтмены();
		Данные.Идентификатор = "USER_CHANGED_MIND"; 
		Данные.Наименование = НСтр("ru = 'Покупатель передумал';
									|en = 'The customer changed their mind'");
		Данные.Инициатор =  "customer";
		Данные.Доступность = Истина; 
		Детализация.Добавить(Данные);
	КонецЕсли;
	
	Если ИдентификаторПричиныОтмены  = Неопределено 
		Или ИдентификаторПричиныОтмены = "USER_REFUSED_DELIVERY" Тогда
		Данные = НовыйОписаниеПричиныОтмены();
		Данные.Идентификатор = "USER_REFUSED_DELIVERY";
		Данные.Наименование = НСтр("ru = 'Покупателя не устроили условия доставки';
									|en = 'The customer was not satisfied with the delivery terms'"); 
		Данные.Инициатор =  "customer";
		Данные.Доступность = Истина; 
		Детализация.Добавить(Данные); 
	КонецЕсли;
	
	Если ИдентификаторПричиныОтмены  = Неопределено 
		Или ИдентификаторПричиныОтмены = "USER_REFUSED_PRODUCT" Тогда
		Данные = НовыйОписаниеПричиныОтмены();
		Данные.Идентификатор = "USER_REFUSED_PRODUCT"; 
		Данные.Наименование = НСтр("ru = 'Покупателю не подошел продукт';
									|en = 'The item did not fit the customer'"); 
		Данные.Инициатор = "customer";
		Данные.Доступность = Истина; 
		Детализация.Добавить(Данные);
	КонецЕсли;
	
	Если ИдентификаторПричиныОтмены  = Неопределено 
		Или ИдентификаторПричиныОтмены = "SHOP_FAILED" Тогда
		Данные = НовыйОписаниеПричиныОтмены();
		Данные.Идентификатор = "SHOP_FAILED";
		Данные.Наименование = НСтр("ru = 'Магазин не может выполнить заказ';
									|en = 'The store cannot fulfill the order'"); 
		Данные.Инициатор = "seller";
		Данные.Доступность = Истина; 
		Детализация.Добавить(Данные);
	КонецЕсли;
		
	Если ИдентификаторПричиныОтмены  = Неопределено 
		Или ИдентификаторПричиныОтмены = "USER_REFUSED_QUALITY" Тогда
		Данные = НовыйОписаниеПричиныОтмены();
		Данные.Идентификатор = "USER_REFUSED_QUALITY";
		Данные.Наименование = НСтр("ru = 'Покупателя не устроило качество товара';
									|en = 'The customer was not satisfied with the item quality'");
		Данные.Инициатор =  "customer";
		Данные.Доступность = Истина; 
		Детализация.Добавить(Данные);    
	КонецЕсли;
	
	Если ИдентификаторПричиныОтмены  = Неопределено 
		Или ИдентификаторПричиныОтмены = "REPLACING_ORDER" Тогда
		Данные = НовыйОписаниеПричиныОтмены();
		Данные.Идентификатор = "REPLACING_ORDER";
		Данные.Наименование = НСтр("ru = 'Покупатель решил заменить товар';
									|en = 'The customer decided to replace the item'");
		Данные.Инициатор = "customer";
		Данные.Доступность = Истина; 
		Детализация.Добавить(Данные);
	КонецЕсли;
		
	Если ИдентификаторПричиныОтмены  = Неопределено 
		Или ИдентификаторПричиныОтмены = "PICKUP_EXPIRED" Тогда
		Данные = НовыйОписаниеПричиныОтмены();
		Данные.Идентификатор = "PICKUP_EXPIRED";
		Данные.Наименование = НСтр("ru = 'Закончился срок хранения товара на ПВЗ';
									|en = 'The goods retention period at the order issuing point ended'");
		Данные.Инициатор = "market";
		Данные.Доступность = Истина;  
		Детализация.Добавить(Данные);
	КонецЕсли;
	
	Если ИдентификаторПричиныОтмены  = Неопределено 
		Или ИдентификаторПричиныОтмены = "TOO_MANY_DELIVERY_DATE_CHANGES" Тогда
		Данные = НовыйОписаниеПричиныОтмены();
		Данные.Идентификатор = "TOO_MANY_DELIVERY_DATE_CHANGES";
		Данные.Наименование = НСтр("ru = 'Заказ переносили слишком много раз';
									|en = 'The order was postponed too many times'");
		Данные.Инициатор = "market"; 
		Данные.Доступность = Истина; 
		Детализация.Добавить(Данные);
	КонецЕсли;
	
	Если ИдентификаторПричиныОтмены  = Неопределено 
		Или ИдентификаторПричиныОтмены = "TOO_LONG_DELIVERY" Тогда
		Данные = НовыйОписаниеПричиныОтмены();
		Данные.Идентификатор = "TOO_LONG_DELIVERY";
		Данные.Наименование = НСтр("ru = 'Заказ доставляется слишком долго';
									|en = 'The order is being delivered for too long'");
		Данные.Инициатор = "market";
		Данные.Доступность = Истина; 
		Детализация.Добавить(Данные);
	КонецЕсли;
	
	Ошибка.Детализация = Детализация;
	
	Возврат Ошибка;
	
КонецФункции

// Получает причину отмены по умолчанию.
//
// Возвращаемое значение:
//   Строка, СправочникСсылка.ПричиныОтменыЗаказовКлиентов - причина отмены заказа по умолчанию.
//
Функция ПолучитьПричинуОтменыПоУмолчанию() Экспорт  
	
	ИспользоватьПричиныОтменыЗаказовКлиентов = ПолучитьФункциональнуюОпцию("ИспользоватьПричиныОтменыЗаказовКлиентов");
	
	Если ИспользоватьПричиныОтменыЗаказовКлиентов Тогда		
		ПричинаПоУмолчанию = Справочники.ПричиныОтменыЗаказовКлиентов.ПолучитьСсылку(Новый УникальныйИдентификатор("0d43fb45-b3e8-43b4-9aec-3632d0f597d8"));
		Если Не ОбщегоНазначения.СсылкаСуществует(ПричинаПоУмолчанию) Тогда
			СправочникОбъект = Справочники.ПричиныОтменыЗаказовКлиентов.СоздатьЭлемент();
			СправочникОбъект.УстановитьСсылкуНового(ПричинаПоУмолчанию);
			СправочникОбъект.Наименование = НСтр("ru = 'Магазин не может выполнить заказ';
												|en = 'The store cannot fulfill the order'");
			СправочникОбъект.Записать();  
		КонецЕсли;
	Иначе
		ПричинаПоУмолчанию = НСтр("ru = 'Магазин не может выполнить заказ';
									|en = 'The store cannot fulfill the order'");
	КонецЕсли;
	
	Возврат ПричинаПоУмолчанию; 
	
КонецФункции

// Заполняет массив параметров запроса
//
// Параметры:
//   ПараметрыЗапроса    - Структура - параметры запроса, см. НовыйПараметрыПолученияЗаказов.
//
// Возвращаемое значение:
//   Строка, СправочникСсылка.ПричиныОтменыЗаказовКлиентов - причина отмены заказа по умолчанию.
//
Функция ЗаполнитьПараметрыURL(ПараметрыЗапроса) Экспорт  
	
	ПараметрыURL = Новый Массив; 
	
	Для Каждого ПараметрЗапроса Из ПараметрыЗапроса Цикл 
		Если ЗначениеЗаполнено(ПараметрЗапроса.Значение) Тогда   
			Если ТипЗнч(ПараметрЗапроса.Значение) = Тип("Массив") Тогда
				СтрокаЗначения = СтрСоединить(ПараметрЗапроса.Значение,",");
                ПараметрыURL.Добавить("" + ПараметрЗапроса.Ключ + "=" + СтрокаЗначения);
			Иначе 
				ПараметрыURL.Добавить("" + ПараметрЗапроса.Ключ + "=" + ПараметрЗапроса.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПараметрыURL;
	
КонецФункции  

// Преобразует исходную строку в число без вызова исключений
//
// Параметры:
//   Значение           - Строка - значение, которое необходимо преобразовать;
//   АбсолютноеЗначение - Булево - признак приведения результата к абсолютной величине.
//
// Возвращаемое значение:
//   Число - преобразованное значение.
//
Функция СтрокаВЧисло(Знач Значение, АбсолютноеЗначение = Ложь) Экспорт
	
	Если Значение = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("Число") Тогда
		Если АбсолютноеЗначение Тогда
			Значение = ?(Значение >= 0, Значение, -Значение);
		КонецЕсли;
		
		Возврат Значение;
	КонецЕсли;
	
	Если Значение = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("Число") Тогда
		Возврат Значение;
	КонецЕсли;
	
	Значение = СтрЗаменить(Значение, " ", "");
	Если СтрНачинаетсяС(Значение, "(") Тогда
		Значение = СтрЗаменить(Значение, "(", "-");
		Значение = СтрЗаменить(Значение, ")", "");
	КонецЕсли;
	
	СтрокаБезНулей = СтрЗаменить(Значение, "0", "");
	Если ПустаяСтрока(СтрокаБезНулей) Или СтрокаБезНулей = "-" Или СтрокаБезНулей = "." Или СтрокаБезНулей = "," Тогда
		Возврат 0;
	КонецЕсли;
	
	ТипЧисло  = Новый ОписаниеТипов("Число");
	Результат = ТипЧисло.ПривестиЗначение(Значение);
	
	Если АбсолютноеЗначение Тогда
		Результат = ?(Результат >= 0, Результат, -Результат);
	КонецЕсли;
	
	Возврат ?(Результат <> 0 И Не ПустаяСтрока(СтрокаБезНулей), Результат, Неопределено);
	
КонецФункции    

// Преобразует исходное число в строку в фиксированном формате обмена с сервисом.
//
// Параметры:
//   Значение                  - Число - форматируемое значение.
//   ВыводитьПредставлениеНуля - Булево - признак включения в результат нулевых значений.
//
// Возвращаемое значение:
//   Строка - строка в фиксированном формате.
//
Функция ЧислоВСтроку(Знач Значение, ВыводитьПредставлениеНуля = Ложь) Экспорт

	Если Значение = Неопределено Тогда
		Значение = 0;
	КонецЕсли;

	ФорматнаяСтрока = ?(ВыводитьПредставлениеНуля, "ЧН=; ЧРД=.; ЧГ=", "ЧРД=.; ЧГ=");
	Возврат Формат(Значение, ФорматнаяСтрока);  

КонецФункции

#КонецОбласти

#Область ВнешниеРесурсы

// Определяет адрес для отображения заказов FBO в личном кабинете торговой площадки.
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису
//  ИдентификаторКабинета - Строка - идентификатор кабинета учетной записи. Если передан пустой - будет заполнен.
// 
// Возвращаемое значение:
//  Строка
//
Функция АдресЗаказовFBO(УчетнаяЗапись, ИдентификаторКабинета = "") Экспорт
	
	Если Не ЗначениеЗаполнено(ИдентификаторКабинета) Тогда
		ИдентификаторКабинета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ИдентификаторКабинета");
	КонецЕсли;
	
	АдресЗаказов = СтрШаблон(
		"https://partner.market.yandex.ru/business/%1/orders?tabId=fbyProcessing&tabGroupId=fby",
		ИдентификаторКабинета);
	
	Возврат АдресЗаказов;
	
КонецФункции

// Определяет адрес для отображения заказов FBS в личном кабинете торговой площадки.
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису
//  ИдентификаторКабинета - Строка - идентификатор кабинета учетной записи. Если передан пустой - будет заполнен.
// 
// Возвращаемое значение:
//  Строка
//
Функция АдресЗаказовFBS(УчетнаяЗапись, ИдентификаторКабинета = "") Экспорт
	
	Если Не ЗначениеЗаполнено(ИдентификаторКабинета) Тогда
		ИдентификаторКабинета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ИдентификаторКабинета");
	КонецЕсли;
	
	АдресЗаказов = СтрШаблон(
		"https://partner.market.yandex.ru/business/%1/orders?tabId=processing&tabGroupId=fbsAndExpress",
		ИдентификаторКабинета);
	
	Возврат АдресЗаказов;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область АвторизацияСлужебный

// Конструктор данных для авторизации приложения.
//
// Возвращаемое значение:
//   Структура - данные для авторизации приложения:
//     * IDПриложения     - Строка - идентификатор приложения;
//     * ПарольПриложения - Строка - пароль приложения.
//
Функция ДанныеПриложения()
	
	Результат = Новый Структура;
	Результат.Вставить("IDПриложения",     "cc9b7be075074e6caaff515a3c5ef0e5");
	Результат.Вставить("ПарольПриложения", "dc807379134444cca2a27e21c9bf9d7d");
	
	Возврат Результат;
	
КонецФункции

// Определяет имя сервиса данных авторизации Яндекс.
//
// Возвращаемое значение:
//   Строка - имя сервера сервиса данных авторизации.
//
Функция СерверДанныхАвторизации()
	
	Результат = "login.yandex.ru";
	
	Возврат Результат;
	
КонецФункции

// Определяет имя сервиса партнерского API.
//
// Параметры:
//   ПоМагазину - Булево - определение имени сервиса для магазина (Истина, по умолчанию) или для кабинета (Ложь).
//
// Возвращаемое значение:
//   Строка - имя сервера сервиса.
//
Функция СерверПартнерскогоAPI(ПоМагазину = Истина) Экспорт
	
	Если ПоМагазину Тогда
		Результат = "api.partner.market.yandex.ru/v2/campaigns";
	Иначе
		Результат = "api.partner.market.yandex.ru/businesses";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции  

// Добавляет данные авторизации в безопасное хранилище по ключу обновления. 
//
// Параметры:
//   ДанныеАвторизации - см. ДанныеАвторизации.
//
// Возвращаемое значение:
//   Структура - содержит данные авторизации:
//     * СтруктураОтвета - Строка - ответ сервера с информацией по ключам;
//     * Результат       - Булево - токены обновлены.
//
Функция ОбновитьКлючиДоступа(ДанныеАвторизации)
	
	ДанныеЛогина      = ПолучитьДанныеЛогина(ДанныеАвторизации.access_token);
	Идентификатор     = ДанныеЛогина.Идентификатор;  
	Результат = Ложь;
	
	Попытка
		Сервер         = СерверАвторизации();
		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,,ИнтернетПрокси,60,СоединениеOpenSSL);

		HTTPЗапрос = ЗапросОбновитьТокеныДоступа(Идентификатор);
		
		HTTPОтвет    = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);  
		КодСостояния = HTTPОтвет.КодСостояния; 
		СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
	
		Если КодСостояния = 200  Тогда
			// обработать раздел errors в структуре ответа
	    	СтруктураОтвета   = ИзJSON(СтрокаОтвета);
			ДанныеАвторизации = ДанныеАвторизации(СтруктураОтвета);  
			ДанныеЛогина      = ПолучитьДанныеЛогина(ДанныеАвторизации.access_token);
			Идентификатор     = ДанныеЛогина.Идентификатор;
			
			Если УстановитьНастройкиАвторизации(ДанныеАвторизации, ДанныеЛогина) Тогда	
				Результат = Истина;
			Иначе 
				Результат = Ложь;
			КонецЕсли;
			
		Иначе
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка выполнения запроса %1: %2';
					|en = 'An error occurred when executing the %1 request: %2'", 
					ОбщегоНазначения.КодОсновногоЯзыка()), 
				Сервер, 
				СтрокаОтвета);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
		КонецЕсли;
			
	Исключение
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
	КонецПопытки;   
	
	КлючиОбновлены = Новый Структура();
	КлючиОбновлены.Вставить("Результат",Результат);  
	КлючиОбновлены.Вставить("СтруктураОтвета",СтруктураОтвета);
	
	Возврат КлючиОбновлены;
	
КонецФункции

#КонецОбласти

#Область УправлениеКаталогомСлужебный

// Возвращает идентификаторы карточек на Маркете, которые соответствуют товарам с заданными параметрами.
// Описание метода см. https://yandex.ru/dev/market/partner-api/doc/ru/reference/business-assortment/getSuggestedOfferMappings.
//
// Параметры:
//   ДанныеУчетнойЗаписи      - Структура - данные учетной записи (см. ДанныеУчетнойЗаписиЯндексМаркет);
//   ИдентификаторПредложения - Строка - идентификатор предложения;
//                            - Неопределено - получить рекомендации по всем товарным позициям;
//   Отказ                    - Булево - выходной параметр, определяет наличие ошибки при выполнении запросов к сервису.
//
Процедура ПредварительныйПросмотрКарточекТоваровИзСервиса(ДанныеУчетнойЗаписи, ИдентификаторПредложения = Неопределено, Отказ = Ложь)

	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ИнтеграцияСЯндексМаркетСервер.ПредварительныйПросмотрКарточекТоваровИзСервиса");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 500
		|	СтатусыПубликацииТоваровЯндексМаркет.Номенклатура КАК Номенклатура,
		|	СтатусыПубликацииТоваровЯндексМаркет.Характеристика КАК Характеристика,
		|	СтатусыПубликацииТоваровЯндексМаркет.Упаковка КАК Упаковка,
		|	СтатусыПубликацииТоваровЯндексМаркет.offerId КАК offerId,
		|	СтатусыПубликацииТоваровЯндексМаркет.name КАК name,
		|	СтатусыПубликацииТоваровЯндексМаркет.category КАК category,
		|	СтатусыПубликацииТоваровЯндексМаркет.vendor КАК vendor,
		|	СтатусыПубликацииТоваровЯндексМаркет.vendorCode КАК vendorCode
		|ИЗ
		|	(ВЫБРАТЬ
		|		СтатусыПубликацииТоваровЯндексМаркет.Номенклатура КАК Номенклатура,
		|		СтатусыПубликацииТоваровЯндексМаркет.Характеристика КАК Характеристика,
		|		СтатусыПубликацииТоваровЯндексМаркет.Упаковка КАК Упаковка,
		|		СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторПредложения КАК offerId,
		|		ВЫБОР
		|			КОГДА ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.Наименование, """") = """"
		|				ТОГДА СтатусыПубликацииТоваровЯндексМаркет.ПредставлениеТовара
		|			ИНАЧЕ СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.Наименование + ВЫБОР
		|					КОГДА ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Характеристика.Наименование, """") = """"
		|						ТОГДА """"
		|					ИНАЧЕ "", "" + СтатусыПубликацииТоваровЯндексМаркет.Характеристика.Наименование
		|				КОНЕЦ
		|		КОНЕЦ КАК name,
		|		СтатусыПубликацииТоваровЯндексМаркет.ТоварнаяКатегория КАК category,
		|		ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.Марка.Наименование, """") КАК vendor,
		|		ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.Артикул, """") КАК vendorCode
		|	ИЗ                                              
		|		РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
		|	ГДЕ
		|		СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись
		|		И СтатусыПубликацииТоваровЯндексМаркет.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыВыгрузкиТоваровЯндексМаркет.Новый)
		|		И (СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторПредложения = &ИдентификаторПредложения
		|				ИЛИ &ИдентификаторПредложения = НЕОПРЕДЕЛЕНО)) КАК СтатусыПубликацииТоваровЯндексМаркет";
	
	Запрос.Параметры.Вставить("УчетнаяЗапись",            ДанныеУчетнойЗаписи.УчетнаяЗапись);
	Запрос.Параметры.Вставить("ИдентификаторПредложения", ИдентификаторПредложения);
	
	ВыборкаДанных = Запрос.Выполнить().Выбрать();  
		
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ВыборкаДанных.Количество() / 10000);

	Попытка	  
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		ДанныеПриложения  = ДанныеПриложения();
		Сервер            = СерверПартнерскогоAPI(Ложь);
		ИмяМетода         = "/offer-mappings/suggestions.json";
		АдресРесурса      = "/" + ДанныеУчетнойЗаписи.ИдентификаторКабинета + ИмяМетода;
		
		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 180, СоединениеOpenSSL);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type",  "application/json");
		Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
			Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
		ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
			Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
		КонецЕсли;
		ДобавитьПодписьИнтеграцииВЗаголовки(Заголовки);
		
		Пока ВыборкаДанных.Следующий() Цикл
			SuggestedOfferDTO = Новый Структура("offerId, name, category, vendor, vendorCode, barcodes");
			ЗаполнитьЗначенияСвойств(SuggestedOfferDTO, ВыборкаДанных);
			
			barcodes = ПолучитьШтрихКоды(ВыборкаДанных.Номенклатура, ВыборкаДанных.Характеристика, ВыборкаДанных.Упаковка);
			SuggestedOfferDTO.barcodes = barcodes; 
			
			offers = Новый Массив;
			offers.Добавить(SuggestedOfferDTO);
			
			ДанныеЗапроса = Новый Структура;
			ДанныеЗапроса.Вставить("offers", offers);
			ТелоЗапроса = ВJSON(ДанныеЗапроса);
			
			HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
			HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, "UTF-8");
			
			HTTPОтвет    = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);  
			КодСостояния = HTTPОтвет.КодСостояния;
			СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
			Результат    = ИзJSON(СтрокаОтвета);
			
			Если КодСостояния = 200 И Результат.status = "OK" Тогда
				Если Результат.result.offers.Количество() > 0 Тогда
					Рекомендация = Неопределено;
					Результат.result.offers[0].Свойство("mapping", Рекомендация);
					
					ОбновитьСтатусыПубликацииПоКабинету(ДанныеУчетнойЗаписи.УчетнаяЗапись, 
						ВыборкаДанных.Номенклатура, 
						ВыборкаДанных.Характеристика, 
						ВыборкаДанных.Упаковка, 
						Рекомендация);
				КонецЕсли;
				
			Иначе	
				Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
					ОбработатьОшибки(ДанныеУчетнойЗаписи.УчетнаяЗапись, 
						ВыборкаДанных.Номенклатура, 
						ВыборкаДанных.Характеристика, 
						ВыборкаДанных.Упаковка,
						Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.ОшибкаПриПолученииРекомендации,
						Результат.errors);
				Иначе
					ОбработатьОшибки(ДанныеУчетнойЗаписи.УчетнаяЗапись, 
						ВыборкаДанных.Номенклатура, 
						ВыборкаДанных.Характеристика, 
						Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.ОшибкаПриПолученииРекомендации,
						ВыборкаДанных.Упаковка);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	Исключение 
		Отказ = Истина;
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
	КонецПопытки;

КонецПроцедуры

// Добавляет товары в каталог или редактирует информацию об уже имеющихся товарах.
// Описание метода см. https://yandex.ru/dev/market/partner-api/doc/ru/reference/business-assortment/updateOfferMappings.
//
// Параметры:
//   ДанныеУчетнойЗаписи - Структура - данные учетной записи (см. ДанныеУчетнойЗаписиЯндексМаркет);
//   Отказ               - Булево - выходной параметр, определяет наличие ошибки при выполнении запросов к сервису.
//
Процедура ДобавитьИзменитьТоварыВСервисе(ДанныеУчетнойЗаписи, Отказ = Ложь)

	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ИнтеграцияСЯндексМаркетСервер.ДобавитьИзменитьТоварыВСервисе");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 500
		|	СтатусыПубликацииТоваровЯндексМаркет.Номенклатура КАК Номенклатура,
		|	СтатусыПубликацииТоваровЯндексМаркет.Характеристика КАК Характеристика,
		|	СтатусыПубликацииТоваровЯндексМаркет.Упаковка КАК Упаковка,
		|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторПредложения КАК offerId,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.Марка.Наименование, """") КАК vendor,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.Артикул, """") КАК vendorCode,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.Производитель.Наименование, """") КАК manufacturer,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.СтранаПроисхождения.Наименование, """") КАК manufacturerCountry,
		|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторТовараПлощадки КАК marketSku,
		|	ВЫРАЗИТЬ(ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.СрокГодности, 0) КАК ЧИСЛО(15,3)) КАК СрокГодности,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.ЕдиницаИзмеренияСрокаГодности, """") КАК ЕдиницаИзмеренияСрокаГодности,
		|	ВЫБОР КОГДА ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.НаименованиеТовараПлощадки,"""")<>"""" ТОГДА
		|		СтатусыПубликацииТоваровЯндексМаркет.НаименованиеТовараПлощадки
		|	ИНАЧЕ
		|       СтатусыПубликацииТоваровЯндексМаркет.ПредставлениеТовара
		|	КОНЕЦ  КАК name,   
		|	ВЫБОР КОГДА ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.НаименованиеКатегорииПлощадки,"""")<>"""" ТОГДА
		|		СтатусыПубликацииТоваровЯндексМаркет.НаименованиеКатегорииПлощадки
		|	ИНАЧЕ
		|       СтатусыПубликацииТоваровЯндексМаркет.ТоварнаяКатегория
		|	КОНЕЦ  КАК category
		|ИЗ
		|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
		|ГДЕ
		|	СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись
		|	И СтатусыПубликацииТоваровЯндексМаркет.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыВыгрузкиТоваровЯндексМаркет.УтвержденаРекомендация)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 500
		|	СтатусыПубликацииТоваровЯндексМаркет.Номенклатура КАК Номенклатура,
		|	СтатусыПубликацииТоваровЯндексМаркет.Характеристика КАК Характеристика,
		|	СтатусыПубликацииТоваровЯндексМаркет.Упаковка КАК Упаковка,
		|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторПредложения КАК offerId,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.Марка.Наименование, """") КАК vendor,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.Артикул, """") КАК vendorCode,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.Производитель.Наименование, """") КАК manufacturer,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.СтранаПроисхождения.Наименование, """") КАК manufacturerCountry,
		|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторТовараПлощадки КАК marketSku,
		|	ВЫРАЗИТЬ(ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.СрокГодности, 0) КАК ЧИСЛО(15,3)) КАК СрокГодности,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.ЕдиницаИзмеренияСрокаГодности, """") КАК ЕдиницаИзмеренияСрокаГодности,
		|	ВЫБОР КОГДА ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.НаименованиеТовараПлощадки,"""")<>"""" ТОГДА
		|		СтатусыПубликацииТоваровЯндексМаркет.НаименованиеТовараПлощадки
		|	ИНАЧЕ
		|       СтатусыПубликацииТоваровЯндексМаркет.ПредставлениеТовара
		|	КОНЕЦ  КАК name,   
		|	ВЫБОР КОГДА ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.НаименованиеКатегорииПлощадки,"""")<>"""" ТОГДА
		|		СтатусыПубликацииТоваровЯндексМаркет.НаименованиеКатегорииПлощадки
		|	ИНАЧЕ
		|       СтатусыПубликацииТоваровЯндексМаркет.ТоварнаяКатегория
		|	КОНЕЦ  КАК category
		|ИЗ
		|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
		|ГДЕ
		|	СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись
		|	И СтатусыПубликацииТоваровЯндексМаркет.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыВыгрузкиТоваровЯндексМаркет.СозданиеНового)";
	
	Запрос.УстановитьПараметр("УчетнаяЗапись", ДанныеУчетнойЗаписи.УчетнаяЗапись);  
	РезультатВыполнения = Запрос.Выполнить();   
	
	ТаблицаРезультата = РезультатВыполнения.Выгрузить();	
	ТаблицаТоваров = ТаблицаРезультата.Скопировать(,"Номенклатура,Характеристика,Упаковка");  
	ТаблицаТоваров.Колонки.Добавить("УчетнаяЗапись", Новый ОписаниеТипов("СправочникСсылка.УчетныеЗаписиМаркетплейсов"));
	ТаблицаТоваров.ЗаполнитьЗначения(ДанныеУчетнойЗаписи.УчетнаяЗапись,"УчетнаяЗапись");
	ЦеныТоваров = ПолучитьЦены(ДанныеУчетнойЗаписи, ТаблицаТоваров, Истина); 
	ЦеныТоваров = ЦеныТоваров.Выгрузить();
	
	СписокИдентификаторовПредложений = ТаблицаРезультата.ВыгрузитьКолонку("offerId"); 
	Если СписокИдентификаторовПредложений.Количество()>0 Тогда
		СсылкиИзображений = ПолучитьПубличныеСсылкиИзображений(ДанныеУчетнойЗаписи.УчетнаяЗапись, СписокИдентификаторовПредложений);
	КонецЕсли;
	ВыборкаДанных = РезультатВыполнения.Выбрать();
		
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ВыборкаДанных.Количество() / 10000);

	Попытка
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		ДанныеПриложения  = ДанныеПриложения();
		Сервер            = СерверПартнерскогоAPI(Ложь);
		ИмяМетода         = "/offer-mappings/update.json";
		АдресРесурса      = "/" + ДанныеУчетнойЗаписи.ИдентификаторКабинета + ИмяМетода;
		
		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 180, СоединениеOpenSSL);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type",  "application/json");
		Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
			Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
		ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
			Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
		КонецЕсли;
		ДобавитьПодписьИнтеграцииВЗаголовки(Заголовки);
		
		Пока ВыборкаДанных.Следующий() Цикл
	 		//@skip-check structure-consructor-too-many-keys
	 		UpdateOfferDTO = Новый Структура("offerId, name, category, pictures, vendor, vendorCode, barcodes, basicPrice, manufacturer, manufacturerCountries, weightDimensions, shelfLife");
			ЗаполнитьЗначенияСвойств(UpdateOfferDTO, ВыборкаДанных);   
			
			pictures = Новый Массив;    
			СсылкиИзображения = СсылкиИзображений.Найти(ВыборкаДанных.offerId,"ИдентификаторПредложения");
			Если СсылкиИзображения <> Неопределено Тогда 
				Если СсылкиИзображения.НеВсеФайлыВыгружены Тогда
					Продолжить;
				Иначе
					pictures = СтрРазделить(СсылкиИзображения.ПубличныеСсылки,";"); 
				КонецЕсли;
			КонецЕсли;
			UpdateOfferDTO.pictures = pictures;
			
			barcodes = ПолучитьШтрихКоды(ВыборкаДанных.Номенклатура, ВыборкаДанных.Характеристика, ВыборкаДанных.Упаковка);
			UpdateOfferDTO.barcodes = barcodes;       
			
			ОтборПоТаблицеЦен = Новый Структура("Номенклатура,Характеристика,Упаковка");
			ОтборПоТаблицеЦен.Номенклатура = ВыборкаДанных.Номенклатура; 
			ОтборПоТаблицеЦен.Характеристика = ВыборкаДанных.Характеристика; 
			ОтборПоТаблицеЦен.Упаковка = ВыборкаДанных.Упаковка;
			Цены = ЦеныТоваров.НайтиСтроки(ОтборПоТаблицеЦен);
			basicPrice = Новый Структура("value,currencyId,discountBase");
			Если Цены.Количество()>0 Тогда 
				ЦенаПродажи = Цены[0].Цена;  
			Иначе
				ЦенаПродажи = 0; 
			КонецЕсли;
			basicPrice.value = ЦенаПродажи;
			basicPrice.currencyId = "RUR";
			UpdateOfferDTO.basicPrice = basicPrice; 
			
			manufacturerCountries = Новый Массив;
			manufacturerCountries.Добавить(ВыборкаДанных.manufacturerCountry);
			UpdateOfferDTO.manufacturerCountries = manufacturerCountries;
			
			Весогабариты = ПолучитьВесогабариты(ВыборкаДанных.Упаковка, ВыборкаДанных.Номенклатура);
			UpdateOfferDTO.weightDimensions = Весогабариты;
			
			Если ВыборкаДанных.СрокГодности<>0
				И ВыборкаДанных.ЕдиницаИзмеренияСрокаГодности <> "" Тогда
				TimePeriodDTO = Новый Структура("timePeriod, timeUnit"); 
				TimePeriodDTO.timePeriod = ВыборкаДанных.СрокГодности;   
				TimeUnitType = ПолучитьПредставлениеПериода(ВыборкаДанных.ЕдиницаИзмеренияСрокаГодности);                          
				TimePeriodDTO.timeUnit = TimeUnitType;
				UpdateOfferDTO.shelfLife = TimePeriodDTO;
			КонецЕсли;
			
			UpdateMappingDTO           = Новый Структура("marketSku");
			UpdateMappingDTO.marketSku = ВыборкаДанных.marketSku;
			
			UpdateOfferMappingDTO         = Новый Структура("offer, mapping");
			UpdateOfferMappingDTO.offer   = UpdateOfferDTO; 
			UpdateOfferMappingDTO.mapping = UpdateMappingDTO;
			
			offerMappings = Новый Массив;
			offerMappings.Добавить(UpdateOfferMappingDTO);

			ДанныеЗапроса = Новый Структура;
			ДанныеЗапроса.Вставить("offerMappings", offerMappings);
			ТелоЗапроса = ВJSON(ДанныеЗапроса);
			
			HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
			HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, "UTF-8");
			
			HTTPОтвет    = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);  
			КодСостояния = HTTPОтвет.КодСостояния;
			СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
			Результат    = ИзJSON(СтрокаОтвета);
			
			Если КодСостояния = 200 И Результат.status = "OK" Тогда
				ЗаписатьСтатусНаМодерации(ДанныеУчетнойЗаписи.УчетнаяЗапись, 
					ВыборкаДанных.Номенклатура, 
					ВыборкаДанных.Характеристика, 
					ВыборкаДанных.Упаковка,
					ЦенаПродажи);
				
			Иначе	
				Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
					ОбработатьОшибки(ДанныеУчетнойЗаписи.УчетнаяЗапись, 
						ВыборкаДанных.Номенклатура, 
						ВыборкаДанных.Характеристика, 
						ВыборкаДанных.Упаковка,   
						Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.ОшибкаПриОтправкеНаМодерацию,
						Результат.errors);
				Иначе
					ОбработатьОшибки(ДанныеУчетнойЗаписи.УчетнаяЗапись, 
						ВыборкаДанных.Номенклатура, 
						ВыборкаДанных.Характеристика, 
						Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.ОшибкаПриОтправкеНаМодерацию,
						ВыборкаДанных.Упаковка);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	Исключение 
		Отказ = Истина;
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
	КонецПопытки;

КонецПроцедуры

// Возвращает список товаров в каталоге с параметрами каждого товара.
// Описание метода см. https://yandex.ru/dev/market/partner-api/doc/ru/reference/business-assortment/getOfferMappings.
//
// Параметры:
//   ДанныеУчетнойЗаписи - Структура - данные учетной записи (см. ДанныеУчетнойЗаписиЯндексМаркет);
//   Отказ               - Булево - выходной параметр, определяет наличие ошибки при выполнении запросов к сервису.
//
Процедура ИнформацияОТоварахВКаталогеИзСервиса(ДанныеУчетнойЗаписи, Отказ = Ложь)
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ИнтеграцияСЯндексМаркетСервер.ИнформацияОТоварахВКаталогеИзСервиса");

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтатусыПубликацииТоваровЯндексМаркет.Номенклатура КАК Номенклатура,
		|	СтатусыПубликацииТоваровЯндексМаркет.Характеристика КАК Характеристика,
		|	СтатусыПубликацииТоваровЯндексМаркет.Упаковка КАК Упаковка,
		|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторПредложения КАК ИдентификаторПредложения
		|ИЗ
		|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
		|ГДЕ
		|	СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись
		|	И СтатусыПубликацииТоваровЯндексМаркет.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыВыгрузкиТоваровЯндексМаркет.НаМодерации)";
	
	Запрос.УстановитьПараметр("УчетнаяЗапись", ДанныеУчетнойЗаписи.УчетнаяЗапись);
	
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ВыборкаДанных.Количество() / 10000);

	offerIds    = Новый Массив;
	Предложения = Новый Соответствие;
	
	Пока ВыборкаДанных.Следующий() Цикл
		offerIds.Добавить(ВыборкаДанных.ИдентификаторПредложения);
		
		КлючЗаписи = Новый Структура;
		КлючЗаписи.Вставить("Номенклатура",   ВыборкаДанных.Номенклатура);
		КлючЗаписи.Вставить("Характеристика", ВыборкаДанных.Характеристика);
		КлючЗаписи.Вставить("Упаковка",       ВыборкаДанных.Упаковка);
		
		Предложения.Вставить(ВыборкаДанных.ИдентификаторПредложения, КлючЗаписи);
	КонецЦикла;
	
	Если offerIds.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Попытка	  
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		ДанныеПриложения  = ДанныеПриложения();
		Сервер            = СерверПартнерскогоAPI(Ложь);
		ИмяМетода         = "/offer-mappings.json";
		АдресРесурса      = "/" + ДанныеУчетнойЗаписи.ИдентификаторКабинета + ИмяМетода;
		
		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 180, СоединениеOpenSSL);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type",  "application/json");
		Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
			Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
		ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
			Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
		КонецЕсли;
		ДобавитьПодписьИнтеграцииВЗаголовки(Заголовки);
		
		ДанныеЗапроса = Новый Структура;
		ДанныеЗапроса.Вставить("offerIds", offerIds);
		ТелоЗапроса = ВJSON(ДанныеЗапроса);
		
		HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, "UTF-8");
		
		HTTPОтвет    = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);  
		КодСостояния = HTTPОтвет.КодСостояния;
		СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
		Результат    = ИзJSON(СтрокаОтвета);
		
		Если КодСостояния = 200 И Результат.status = "OK" Тогда
			Для Каждого ЭлементКоллекции Из Результат.result.offerMappings Цикл
				КлючЗаписи = Предложения.Получить(ЭлементКоллекции.offer.offerId);
				
				Если КлючЗаписи <> Неопределено Тогда
					ОбновитьСтатусыНаМодерацииПоКабинету(ДанныеУчетнойЗаписи.УчетнаяЗапись, 
						КлючЗаписи.Номенклатура, 
						КлючЗаписи.Характеристика, 
						КлючЗаписи.Упаковка,
					    ЭлементКоллекции);
				КонецЕсли;
			КонецЦикла;
			
		Иначе	
			Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
				ОписаниеОшибок = ТекстОшибки(Результат.errors);
			Иначе
				ОписаниеОшибок = ""; 
			КонецЕсли; 	
			
			Отказ = Истина;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка выполнения запроса %1: %2%3';
					|en = 'An error occurred when executing the %1 request: %2%3'", 
					ОбщегоНазначения.КодОсновногоЯзыка()), 
				Сервер + АдресРесурса, 
				ОписаниеОшибок,
				СтрокаОтвета);
				
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
		КонецЕсли;
		
	Исключение 
		Отказ = Истина;
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
	КонецПопытки;

КонецПроцедуры

// Устанавливает статус "На модерации" для товарной позиции.
//
// Параметры:
//   УчетнаяЗапись  - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//   Номенклатура   - СправочникСсылка.Номенклатура - номенклатура товарной позиции;
//   Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика товарной позиции;
//   Упаковка       - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка товарной позиции;
//   ЦенаПродажи    - Число - базовая цена товара.
//
Процедура ЗаписатьСтатусНаМодерации(УчетнаяЗапись, Номенклатура, Характеристика, Упаковка, ЦенаПродажи)
	
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет");
		ЭлементБлокировкиДанных.УстановитьЗначение("УчетнаяЗапись",  УчетнаяЗапись);
		ЭлементБлокировкиДанных.УстановитьЗначение("Номенклатура",   Номенклатура);
		ЭлементБлокировкиДанных.УстановитьЗначение("Характеристика", Характеристика);
		ЭлементБлокировкиДанных.УстановитьЗначение("Упаковка",       Упаковка);
		БлокировкаДанных.Заблокировать();

		НаборЗаписей = РегистрыСведений.СтатусыПубликацииТоваровЯндексМаркет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УчетнаяЗапись.Установить(УчетнаяЗапись);
		НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);
		НаборЗаписей.Отбор.Характеристика.Установить(Характеристика);
		НаборЗаписей.Отбор.Упаковка.Установить(Упаковка);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() > 0 Тогда
			Запись        = НаборЗаписей[0];
			Запись.Статус = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.НаМодерации; 
			Запись.ЦенаПродажи = ЦенаПродажи;
			НаборЗаписей.Записать();
		КонецЕсли;

		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
		
КонецПроцедуры

// Обновляет статусы публикации товарных позиций.
//
// Параметры:
//   УчетнаяЗапись  - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//   Номенклатура   - СправочникСсылка.Номенклатура - номенклатура товарной позиции;
//   Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика товарной позиции;
//   Упаковка       - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка товарной позиции.
//   Рекомендация   - Неопределено - рекомендация не найдена;
//                  - Структура - информация о рекомендованной карточке товара (GetMappingDTO):
//     * marketSku          - Число - идентификатор карточки на Маркете;
//     * marketSkuName      - Строка - название карточки на Маркете. Может отсутствовать в ответе;
//     * marketModelId      - Число - идентификатор модели на Маркете. Может отсутствовать в ответе;
//     * marketModelName    - Строка - название модели на Маркете. Может отсутствовать в ответе;
//     * marketCategoryId   - Число - идентификатор категории карточки на Маркете. Может отсутствовать в ответе;
//     * marketCategoryName - Строка - название категории карточки на Маркете. Может отсутствовать в ответе.
//
Процедура ОбновитьСтатусыПубликацииПоКабинету(УчетнаяЗапись, Номенклатура, Характеристика, Упаковка, Рекомендация)
	
	Если Рекомендация = Неопределено Тогда
		Рекомендация = Новый Структура;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет");
		ЭлементБлокировкиДанных.УстановитьЗначение("УчетнаяЗапись",  УчетнаяЗапись);
		ЭлементБлокировкиДанных.УстановитьЗначение("Номенклатура",   Номенклатура);
		ЭлементБлокировкиДанных.УстановитьЗначение("Характеристика", Характеристика);
		ЭлементБлокировкиДанных.УстановитьЗначение("Упаковка",       Упаковка);
		БлокировкаДанных.Заблокировать();

		НаборЗаписей = РегистрыСведений.СтатусыПубликацииТоваровЯндексМаркет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УчетнаяЗапись.Установить(УчетнаяЗапись);
		НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);
		НаборЗаписей.Отбор.Характеристика.Установить(Характеристика);
		НаборЗаписей.Отбор.Упаковка.Установить(Упаковка);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() > 0 Тогда
			Запись = НаборЗаписей[0];
			Если Рекомендация.Свойство("marketSku") Тогда
				Запись.ИдентификаторТовараПлощадки = Формат(Рекомендация.marketSku, "ЧН=; ЧГ=0");
				Запись.Статус                      = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.ПолученаРекомендация;
				Запись.ЕстьИдентификаторПлощадки   = Истина;
			Иначе
				Запись.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.РекомендацияНеНайдена;
				Запись.ОписаниеОшибки = НСтр("ru = 'Для товара с указанными свойствами не была подобрана рекомендованная карточка товара на Яндекс Маркет. 
											 |Для получения рекомендации укажите в запросе как можно больше информации о товаре (категорию, бренд (марку),штрих-коды товара, ориентировочную цену товара). После заполнения данных отправьте повторно публикацию на получение рекомендации (кнопка ""Получить рекомендацию"").
											 |Попробуйте найти товар в поиске по разделу ""Покупки"" на Маркете. 
											 |Если товар найдется, SKU на Яндексе можно взять из URL его страницы. 
											 |Например, если URL страницы товара — https://market.yandex.ru/product--pir-2821ak/649875256?sponsored=1&sku=100848394231, то его SKU на Яндексе — 100848394231.
											 |Если вы смогли самостоятельно подобрать SKU на Яндексе - заполните его и отправьте товар на модерацию (кнопка ""Отправить на модерацию"").
											 |Если SKU не удалось подобрать - наиболее подробно опишите товар и отправьте запрос без указания SKU (кнопка ""Отправить на модерацию"") - сотрудники Маркета могут подобрать или создать карточки для ваших товаров в личном кабинете, если товар еще не продается на Маркете.';
											 |en = 'A recommended item card on Yandex.Market was not selected for the item with the specified properties. 
											 |To get a recommendation, specify as much information about the item as possible in the request: a category, a brand, item barcodes, and an approximate item price. Once the data is filled, send the publication for recommendation receipt again (click ""Receive recommendation"").
											 |Try to find the item by the search in the ""Purchases"" section on Yandex.Market.
											 |If the item is found, take its SKU on Yandex from its page URL. 
											 |For example, if the item page URL is https://market.yandex.ru/product--pir-2821ak/649875256?sponsored=1&sku=100848394231, the item SKU on Yandex is 100848394231.
											 |If you managed to find the SKU on Yandex, fill it and send the item to moderation (click ""Send to moderation"").
											 |If you could not find the SKU, describe the item in detail and send the request without specifying the SKU (click ""Send to moderation""). Yandex.Market employees can pick up or create cards for your items in your personal account if the item is not published for sale on Yandex.Market yet.'"); 
			КонецЕсли;
			Если Рекомендация.Свойство("marketSkuName") Тогда 
				Запись.НаименованиеТовараПлощадки = Рекомендация.marketSkuName;
			КонецЕсли;
			Если Рекомендация.Свойство("marketCategoryName") Тогда 
				Запись.НаименованиеКатегорииПлощадки = Рекомендация.marketCategoryName;
			КонецЕсли;
			Если Рекомендация.Свойство("marketModelName") Тогда 
				Запись.НаименованиеМоделиПлощадки = Рекомендация.marketModelName;
			КонецЕсли;
		
			НаборЗаписей.Записать();
		КонецЕсли;

		ЗафиксироватьТранзакцию();

	Исключение
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

// Возвращает перевод распространенных ошибок в ответе сервиса.
//
// Параметры:
//   Сообщение - Строка - сообщение об ошибке.
//
// Возвращаемое значение:
//   Строка - перевод сообщения об ошибке. 
//
Функция ПереводОшибки(Сообщение)
	
	Если Сообщение = "Offer at position 1 should not have empty value of vendor" Тогда   
		ПереводОшибки = НСтр("ru = 'Для товара не заполнен бренд (марка)';
							|en = 'A brand is not specified for the item'");
	ИначеЕсли Сообщение = "Offer at position 1 should not have empty value of category" Тогда
		ПереводОшибки = НСтр("ru = 'Для товара не заполнена категория';
							|en = 'The item category is not specified'");
	ИначеЕсли Сообщение = "Offer at position 1 should not have empty values in list of manufacturerCountries" Тогда
		ПереводОшибки = НСтр("ru = 'Для товара не заполнена страна происхождения';
							|en = 'The item''s country of origin is not specified'");
	ИначеЕсли Сообщение = "Offer at position 1 should have at least one barcode" Тогда  
		ПереводОшибки = НСтр("ru = 'Для товара не заполнено ни одного штрих-кода';
							|en = 'A barcode is not specified for the item'"); 
	Иначе
		ПереводОшибки = "";
	КонецЕсли;
	
	Возврат ПереводОшибки;
КонецФункции

// Устанавливает признаки ошибки при отправке статусов товарной позиции.
//
// Параметры:
//   УчетнаяЗапись  - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//   Номенклатура   - СправочникСсылка.Номенклатура - номенклатура товарной позиции;
//   Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика товарной позиции;
//   Упаковка       - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка товарной позиции;
//   ТипОшибки      - ПеречислениеСсылка.СтатусыВыгрузкиТоваровЯндексМаркет - статус публикации;
//   Ошибки         - Массив Из Структура - ошибки сервиса:
//     * code         - Строка - код ошибки;
//     * message      - Строка - описание ошибки.
//                  - Неопределено - сведения об ошибках сервиса отсутствуют.
//
Процедура ОбработатьОшибки(УчетнаяЗапись, Номенклатура, Характеристика, Упаковка, ТипОшибки, Ошибки = Неопределено)
	
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет");
		ЭлементБлокировкиДанных.УстановитьЗначение("УчетнаяЗапись",  УчетнаяЗапись);
		ЭлементБлокировкиДанных.УстановитьЗначение("Номенклатура",   Номенклатура);
		ЭлементБлокировкиДанных.УстановитьЗначение("Характеристика", Характеристика);
		ЭлементБлокировкиДанных.УстановитьЗначение("Упаковка",       Упаковка);
		БлокировкаДанных.Заблокировать();

		НаборЗаписей = РегистрыСведений.СтатусыПубликацииТоваровЯндексМаркет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УчетнаяЗапись.Установить(УчетнаяЗапись);
		НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);
		НаборЗаписей.Отбор.Характеристика.Установить(Характеристика);
		НаборЗаписей.Отбор.Упаковка.Установить(Упаковка);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() > 0 Тогда
			Запись                = НаборЗаписей[0];
			Запись.КодОшибки      = ""; 
			Запись.ОписаниеОшибки = "";
			Запись.Статус         = ТипОшибки;
			
			Если Ошибки <> Неопределено Тогда
				Для Каждого ЭлементКоллекции Из Ошибки Цикл
					ПереводОшибки         = ПереводОшибки(ЭлементКоллекции.message);
					Запись.КодОшибки      = Запись.КодОшибки + ?(ЗначениеЗаполнено(Запись.КодОшибки), "; ", "") 
												+ ЭлементКоллекции.code; 
					Если ЭлементКоллекции.code = "FORBIDDEN" Тогда 
						Если СтрНайти(ЭлементКоллекции.message,"Token does not have") > 0 Тогда
							СообщениеОграниченияДоступа = Нстр("ru = 'Нарушение прав доступа. Выданный ключ не поддерживает обращение к вызываемому методу. Ответ сервиса:';
																|en = 'Access right violation. The issued key does not support accessing the called method. Service response:'");
						Иначе
							СообщениеОграниченияДоступа = "";
						КонецЕсли;
					КонецЕсли;
					Запись.ОписаниеОшибки = Запись.ОписаниеОшибки + СообщениеОграниченияДоступа + ?(ЗначениеЗаполнено(Запись.ОписаниеОшибки), "; " + Символы.ПС, "")
												+ ?(ЗначениеЗаполнено(ПереводОшибки), ПереводОшибки, ЭлементКоллекции.message);
				КонецЦикла;
			КонецЕсли;
				
			НаборЗаписей.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

// Обновляет статусы публикации товарных позиций.
//
// Параметры:
//   УчетнаяЗапись     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//   Номенклатура      - СправочникСсылка.Номенклатура - номенклатура товарной позиции;
//   Характеристика    - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика товарной позиции;
//   Упаковка          - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка товарной позиции.
//   ИнформацияОТоваре - Массив Из Структура - информация о товаре (GetOfferMappingDTO):
//     * offer           - Структура - основные параметры товара (GetOfferDTO):
//       ** offerId        - Строка - идентификатор предложения;
//       ** cardStatus     - Строка - статус карточки товара (OfferCardStatusType);
//       ** campaigns      - Массив Из Структура - список магазинов, в которых размещен товар (OfferCampaignStatusDTO):
//          *** campaignId   - Число - идентификатор кампании;
//          *** status       - Строка - статус карточки товара (OfferCardStatusType).
//     * mapping         - Структура - информация о карточке товара на Маркете (GetMappingDTO):
//       ** marketSku          - Число - идентификатор карточки на Маркете;
//       ** marketSkuName      - Строка - название карточки на Маркете. Может отсутствовать в ответе;
//       ** marketModelId      - Число - идентификатор модели на Маркете. Может отсутствовать в ответе;
//       ** marketModelName    - Строка - название модели на Маркете. Может отсутствовать в ответе;
//       ** marketCategoryId   - Число - идентификатор категории карточки на Маркете. Может отсутствовать в ответе;
//       ** marketCategoryName - Строка - название категории карточки на Маркете. Может отсутствовать в ответе.
//
Процедура ОбновитьСтатусыНаМодерацииПоКабинету(УчетнаяЗапись, Номенклатура, Характеристика, Упаковка, ИнформацияОТоваре)
	
	Если Не ИнформацияОТоваре.offer.Свойство("cardStatus") Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет");
		ЭлементБлокировкиДанных.УстановитьЗначение("УчетнаяЗапись",  УчетнаяЗапись);
		ЭлементБлокировкиДанных.УстановитьЗначение("Номенклатура",   Номенклатура);
		ЭлементБлокировкиДанных.УстановитьЗначение("Характеристика", Характеристика);
		ЭлементБлокировкиДанных.УстановитьЗначение("Упаковка",       Упаковка);
		БлокировкаДанных.Заблокировать();

		НаборЗаписей = РегистрыСведений.СтатусыПубликацииТоваровЯндексМаркет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УчетнаяЗапись.Установить(УчетнаяЗапись);
		НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);
		НаборЗаписей.Отбор.Характеристика.Установить(Характеристика);
		НаборЗаписей.Отбор.Упаковка.Установить(Упаковка);
		НаборЗаписей.Прочитать();

		Если НаборЗаписей.Количество() > 0 Тогда
			Запись = НаборЗаписей[0];
			Если ИнформацияОТоваре.Свойство("mapping") Тогда   
				Если ИнформацияОТоваре.mapping.Свойство("marketSku") Тогда  
					Запись.ИдентификаторТовараПлощадки = Формат(ИнформацияОТоваре.mapping.marketSku, "ЧН=; ЧГ=0"); 
				КонецЕсли;
				Если ИнформацияОТоваре.mapping.Свойство("marketSkuName") Тогда 
					Запись.НаименованиеТовараПлощадки = ИнформацияОТоваре.mapping.marketSkuName;
				КонецЕсли;
				Если ИнформацияОТоваре.mapping.Свойство("marketCategoryName") Тогда 
					Запись.НаименованиеКатегорииПлощадки = ИнформацияОТоваре.mapping.marketCategoryName;
				КонецЕсли;
				Если ИнформацияОТоваре.mapping.Свойство("marketModelName") Тогда 
					Запись.НаименованиеМоделиПлощадки = ИнформацияОТоваре.mapping.marketModelName;
				КонецЕсли;
			КонецЕсли;
			
			СтатусСОписанием = ПолучитьСтатусМодерацииСОписанием(ИнформацияОТоваре.offer.cardStatus, Запись.Статус);
			
			Если СтатусСОписанием.Статус = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияПройдена 
					И Запись.Статус <> Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияПройдена Тогда
				РегистрыСведений.ОстаткиТоваровМаркетплейсов.ОчиститьЗаписиПоНоменклатуреИХарактеристике(
					Запись.УчетнаяЗапись, 
					Запись.Номенклатура,
					Запись.Характеристика);
			КонецЕсли;
			
			Запись.Статус         = СтатусСОписанием.Статус;
			Запись.ОписаниеОшибки = СтатусСОписанием.ОписаниеОшибки;
			
			НаборЗаписей.Записать();
		КонецЕсли;   
		
		ДанныеФайловИзображений = Запись.ГиперссылкаНаРекомендованныеТовар;

		ЗафиксироватьТранзакцию(); 
		
	Исключение
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Попытка
		Если СтатусСОписанием <> Неопределено И СтатусСОписанием.Статус = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияПройдена Тогда
			Если СтрНайти(ДанныеФайловИзображений,";")>0 Тогда   
					МассивДанныхФайловИзображений = СтрРазделить(ДанныеФайловИзображений,";");
					Для каждого ЭлементМассива Из МассивДанныхФайловИзображений Цикл   
						ТокенДоступа = ИнтеграцияСМаркетплейсамиСервер.ПолучитьТокенДоступаЯндексДиск(УчетнаяЗапись);
						ИнтеграцияСМаркетплейсамиСервер.УдалитьФайлССервиса(ТокенДоступа, ЭлементМассива);
					КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	Исключение
		СообщениеПользователю = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Не удалось удалить публичную ссылку по адресу %1 по причине: %2';
			|en = 'Cannot delete the public link (%1) due to: %2'", 
		ОбщегоНазначения.КодОсновногоЯзыка()),
		ЭлементМассива,
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Ошибка,,,
		СообщениеПользователю);
	КонецПопытки;
	
КонецПроцедуры

// Определяет расширенное описание статуса публикации товарной позиции.
//
// Параметры:
//   СтатусПубликации - Строка - статус публикации товара (OfferProcessingStatusType);
//   ТекущийСтатус    - ПеречислениеСсылка.СтатусыВыгрузкиТоваровЯндексМаркет - текущий статус товарной позиции;
//   ПричиныОтказа    - Массив Из Структура - причины, по которым товар не прошел модерацию (OfferProcessingNoteDTO):
//     * type           - Строка - тип причины, по которой товар не прошел модерацию;
//     * payload        - Строка - дополнительная информация о причине отклонения товара.
//
Функция ПолучитьСтатусМодерацииСОписанием(СтатусПубликации, ТекущийСтатус, ПричиныОтказа = Неопределено)
	
	Структура = Новый Структура("Статус, ОписаниеОшибки");
	
	Если СтатусПубликации = "READY" Тогда
		// Товар прошел модерацию. Чтобы разместить его на Маркете, установите для него цену
		Структура.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияПройдена; 
		Структура.ОписаниеОшибки = "";
		
	ИначеЕсли СтатусПубликации = "IN_WORK" Тогда
		// Товар проходит модерацию. Это занимает несколько дней
		Если ТекущийСтатус = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.НаМодерации Тогда
			Структура.Статус = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.НаМодерации; 
		
		ИначеЕсли ТекущийСтатус = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.ОжидаетМодерации Тогда
			Структура.Статус = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.НаМодерации; 
		КонецЕсли;
		
	ИначеЕсли СтатусПубликации = "NEED_CONTENT" Тогда
		// Для товара без SKU на Маркете marketSku нужно найти карточку самостоятельно (через API или личный кабинет магазина) или создать ее, если товар еще не продается на Маркете
		Структура.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияНеПройдена;
		Структура.ОписаниеОшибки = НСтр("ru = 'Для товара не указан SKU на Яндексе, и сотрудники Маркета не смогли привязать его к карточке. Найдите карточку товара самостоятельно в поиске по разделу ""Покупки"" или создайте ее, если товар еще не продается на Маркете.';
										|en = 'SKU is not specified for the item on Yandex.Market. Trading platform employees could not bind it to the item card. Find the item card in the ""Purchases"" section or create it if the item is not published for sale on Yandex.Market yet.'");
		
	ИначеЕсли СтатусПубликации = "NEED_INFO" Тогда
		// Товар не прошел модерацию из-за ошибок или недостающих сведений в описании товара. Информация о причинах отклонения возвращается в параметре notes
		ОписаниеОшибки = НСтр("ru = 'Вы указали для товара неверную или недостаточную информацию.';
								|en = 'Incorrect or insufficient information is provided for the item.'");
		
		Если ЗначениеЗаполнено(ПричиныОтказа) Тогда
			Если ТипЗнч(ПричиныОтказа) = Тип("Строка") Тогда 
				ОписаниеОшибки = ОписаниеОшибки + " " + ПричиныОтказа;
				
			ИначеЕсли ТипЗнч(ПричиныОтказа) = Тип("Массив") Тогда 
				Для Каждого ЭлементКоллекции Из ПричиныОтказа Цикл
					Если ЭлементКоллекции.type = "ASSORTMENT" Тогда
						ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Товар производится в разных вариантах. Каждый из них нужно описать как отдельный товар';
																	|en = 'The item is produced in different variants. Describe each variant as a separate item'");
					ИначеЕсли ЭлементКоллекции.type = "CANCELLED" Тогда
						ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Товар отозван с модерации по вашей инициативе';
																	|en = 'The item was withdrawn from moderation at your request'");
					ИначеЕсли ЭлементКоллекции.type = "CONFLICTING_INFORMATION" Тогда
						ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Вы предоставили противоречивую информацию о товаре';
																	|en = 'Inconsistent information is provided for the item'");
					ИначеЕсли ЭлементКоллекции.type = "DEPARTMENT_FROZEN" Тогда
						ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Правила размещения товаров в данной категории перерабатываются, поэтому товар пока не может пройти модерацию';
																	|en = 'The rules for adding items to this category are under revision. The item cannot pass moderation at the moment'");
					ИначеЕсли ЭлементКоллекции.type = "INCORRECT_INFORMATION" Тогда
						ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Информация о товаре, которую вы предоставили, противоречит описанию от производителя';
																	|en = 'The item information provided does not correspond to the manufacturer''s description'");
					ИначеЕсли ЭлементКоллекции.type = "LEGAL_CONFLICT" Тогда
						ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Товар не прошел модерацию по юридическим причинам. Например, он официально не продается в России или у вас нет разрешения на его продажу';
																	|en = 'The item did not pass moderation for legal reasons. It might not be sold in Russia officially or you might not have a permission to sell it'");
					ИначеЕсли ЭлементКоллекции.type = "NEED_CLASSIFICATION_INFORMATION" Тогда
						ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Информации о товаре, которую вы предоставили, не хватает, чтобы отнести его к категории. Проверьте, что правильно указали название, категорию, производителя и страны производства товара, а также URL изображений или страниц с описанием, по которым можно идентифицировать товар';
																	|en = 'The item information provided is not enough to categorize the item. Make sure you correctly specified the name, the category, the manufacturer, and the country of origin. Besides, check the URL of images or pages with the description that can be used to identify the item'");
					ИначеЕсли ЭлементКоллекции.type = "NEED_INFORMATION" Тогда
						ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Товар раньше не продавался в России и пока не размещается на Маркете. Для него можно создать карточку';
																	|en = 'The item has not been sold in Russia before and is not published on Yandex.Market for now. You can create a card for it'");
					ИначеЕсли ЭлементКоллекции.type = "NEED_PICTURES" Тогда
						ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Для идентификации товара нужны его изображения. Отправьте URL изображений товара или загрузите обновленный каталог через личный кабинет магазина';
																	|en = 'Item images are required for its identification. Send the URL of the item images or import the updated goods directory from the store account'");
					ИначеЕсли ЭлементКоллекции.type = "NEED_VENDOR" Тогда
						ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Неверно указан производитель товара';
																	|en = 'Incorrect item manufacturer is specified'");
					ИначеЕсли ЭлементКоллекции.type = "NO_CATEGORY, NO_KNOWLEDGE" Тогда
						ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Товары из указанной категории пока не размещаются на Маркете. Если категория появится, товар будет снова отправлен на модерацию';
																	|en = 'Items from this category are not published on Yandex.Market for now. If the category becomes available, the item will be sent for moderation again'");
					ИначеЕсли ЭлементКоллекции.type = "NO_PARAMETERS_IN_SHOP_TITLE" Тогда
						ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Товар производится в разных вариантах, и из указанного названия непонятно, о каком идет речь';
																	|en = 'The item is produced in different variants. The specified name does not help to distinguish between the variants'");
					ИначеЕсли ЭлементКоллекции.type = "NO_SIZE_MEASURE" Тогда
						ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Для этого товара нужна размерная сетка. Отправьте ее в службу поддержки или вашему менеджеру';
																	|en = 'Add a size chart for the item. Send it to the technical support or your manager'");
					ИначеЕсли ЭлементКоллекции.type = "UNKNOWN" Тогда
						ОписаниеОшибки = НСтр("ru = 'Товар не прошел модерацию по другой причине. Обратитесь в службу поддержки или к вашему менеджеру';
												|en = 'The item did not pass moderation for another reason. Please contact the technical support or your manager'");
					КонецЕсли;
					
					Если ЭлементКоллекции.Свойство("payload") И ЗначениеЗаполнено(ЭлементКоллекции.payload) Тогда
						ОписаниеОшибки = ОписаниеОшибки + " (" + ЭлементКоллекции.payload + ")";
					КонецЕсли;
					ОписаниеОшибки = ОписаниеОшибки + ".";
				КонецЦикла;
			Иначе
				ОписаниеОшибки = ОписаниеОшибки + " " + НСтр("ru = 'Уточните входные данные запроса и отправьте связь на модерацию повторно.';
															|en = 'Specify additional input request data and send the link for moderation again.'");
			КонецЕсли;
		КонецЕсли;
			
		Структура.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияНеПройдена;
		Структура.ОписаниеОшибки = ОписаниеОшибки;
		
	ИначеЕсли СтатусПубликации = "REJECTED" Тогда   
		// Товар не прошел модерацию, так как Маркет не планирует размещать подобные товары
		Структура.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияНеПройдена;
		Структура.ОписаниеОшибки = НСтр("ru = 'В ближайшее время Маркет не планирует размещать товары данной категории. Если у вас есть вопросы по категориям товаров, размещаемым на Маркете, обратитесь в службу поддержки.';
										|en = 'In the near future, Yandex.Market does not plan to publish items of this category. If you have any questions about item categories published on Yandex.Market, contact the technical support.'");
		
	ИначеЕсли СтатусПубликации = "SUSPENDED" Тогда                                                   
		// Товар не прошел модерацию, так как Маркет пока не размещает подобные товары
		Структура.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.ОжидаетМодерации;
		Структура.ОписаниеОшибки = НСтр("ru = 'Маркет пока не размещает товары данной категории, но начнет в ближайшее время. После этого товар автоматически отправится на модерацию, и статус изменится на IN_WORK. Чтобы уточнить сроки появления категорий товаров на Маркете, обратитесь в службу поддержки.';
										|en = 'Yandex.Market does not publish items of this category for now. This category will be available for publication soon. After that, the item will be automatically sent for moderation, and its status will change to IN_WORK. To find out when the item categories will become available on Yandex.Market, contact the technical support.'");
	КонецЕсли;
	
	Если СтатусПубликации = "HAS_CARD_CAN_UPDATE_PROCESSING" 
			Или СтатусПубликации = "NO_CARD_PROCESSING" Тогда 
		// Изменения на проверке
		Если ТекущийСтатус = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.НаМодерации Тогда
			Структура.Статус = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.НаМодерации; 
		
		ИначеЕсли ТекущийСтатус = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.ОжидаетМодерации Тогда
			Структура.Статус = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.НаМодерации; 
		КонецЕсли;

	ИначеЕсли СтатусПубликации = "NO_CARD_ADD_TO_CAMPAIGN" Тогда 
		// Разместите товар в магазине
		Структура.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияПройдена; 
		Структура.ОписаниеОшибки = "";

	ИначеЕсли СтатусПубликации = "NO_CARD_MARKET_WILL_CREATE" Тогда 
		// Создаст Маркет
		Структура.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.ОжидаетМодерации;
		Структура.ОписаниеОшибки = НСтр("ru = 'Вы добавили товар, который пока не продавался на Маркете. Карточки для товаров этой категории могут создавать только сотрудники Маркета. Обычно создание карточки занимает не больше двух дней.';
										|en = 'You have added an item that has not been sold on Yandex.Market before. Only Yandex.Market employees can create cards for items of this category. It usually takes no more than two days to create a card.'");

	ИначеЕсли СтатусПубликации = "HAS_CARD_CAN_NOT_UPDATE" Тогда
		// Карточка Маркета
		Структура.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияНеПройдена;
		Структура.ОписаниеОшибки = НСтр("ru = 'Напрямую управлять карточкой с таким статусом нельзя - вы можете менять только условия продажи товара, его вес и габариты.
											  |Если нужно изменить описание, изображения или другие характеристики товара — напишите в службу поддержки с помощью кнопки Помощь с карточкой на странице товара.';
											  |en = 'You cannot manage the card with this status. You can only change the sales terms for the item, its weight and dimensions.
											  |To change the description, images, or other item details, contact the technical support using the ""Help with card"" button on the item page.'");
		
	ИначеЕсли СтатусПубликации = "NO_CARD_NEED_CONTENT" Тогда 
		// Создайте карточку
		Структура.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.НаМодерации;
		Структура.ОписаниеОшибки = НСтр("ru = 'В категории есть дополнительные поля, без которых карточку нельзя создать. Заполните характеристики на странице товара или загрузите информацию для всех товаров, используя Excel-шаблон категории или конвертер контента.';
										|en = 'The category contains additional fields that must be filled to create a card. Fill the item details on the item page or import information for all items using a Microsoft Excel category template or a content converter.'");

	ИначеЕсли СтатусПубликации = "NO_CARD_ERRORS" Тогда 
		// Не создана из-за ошибки
		Структура.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияНеПройдена;
		Структура.ОписаниеОшибки = НСтр("ru = 'Маркет обнаружил ошибку в данных о товаре, поэтому создать карточку не удалось.';
										|en = 'Cannot create the card as Yandex.Market found an error in the item data.'");

	ИначеЕсли СтатусПубликации = "HAS_CARD_CAN_UPDATE" Тогда 
		// Можно дополнить
		Структура.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияПройдена; 
		Структура.ОписаниеОшибки = НСтр("ru = 'Карточку можно дополнить недостающей информацией. 
											  |Если на карточке мало данных - товар сложно найти в поиске и с помощью фильтров на Маркете, а покупателям будет трудно принять решение о покупке.
											  |Дополните карточку товара и Маркет за свой счет предложит покупателям кешбэк на ваши товары.';
											  |en = 'You can add more information to the card. 
											  |If the card contains insufficient item information, the item will be difficult to find in the search or using filters on Yandex.Market. Besides, customers will hesitate when making a decision on whether to buy or not.
											  |Add more information to the item card, and Yandex.Market will offer customers cash back on your items at the expense of the trading platform.'");
		
	ИначеЕсли СтатусПубликации = "HAS_CARD_CAN_UPDATE_ERRORS" Тогда 
		// Изменения не приняты
		Структура.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияНеПройдена;
		Структура.ОписаниеОшибки = НСтр("ru = 'Вы отредактировали карточку, но Маркет нашел ошибки в измененных данных - их нужно исправить. Карточка пока остается в том же виде, что была до редактирования.';
										|en = 'You edited the card, but Yandex.Market found errors in the changed data. Please correct the changed data. Until the data is corrected, the card will remain the same as it was before editing.'");
	КонецЕсли;
	
	// Прочее
	Если Не ЗначениеЗаполнено(Структура.Статус) Тогда
		Структура.Статус         = Перечисления.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияНеПройдена; 
		Структура.ОписаниеОшибки = НСтр("ru = 'Товар не прошел модерацию по неустановленной причине. Обратитесь в службу поддержки или к вашему менеджеру.';
										|en = 'The item did not pass moderation for an unknown reason. Please contact the technical support or your manager.'");
	КонецЕсли;
	
	Возврат Структура;

КонецФункции

Функция ПолучитьПредставлениеПериода(ЕдиницаИзмеренияСрокаГодности) 
	
	ПредставлениеПериода = "";
	
	Если ЕдиницаИзмеренияСрокаГодности = Перечисления.ЕдиницыИзмеренияВремени.Час Тогда 
		ПредставлениеПериода = "HOUR";
	ИначеЕсли  ЕдиницаИзмеренияСрокаГодности = Перечисления.ЕдиницыИзмеренияВремени.День Тогда 
		ПредставлениеПериода = "DAY";
	ИначеЕсли  ЕдиницаИзмеренияСрокаГодности = Перечисления.ЕдиницыИзмеренияВремени.Сутки Тогда     
		ПредставлениеПериода = "DAY";
	ИначеЕсли   ЕдиницаИзмеренияСрокаГодности = Перечисления.ЕдиницыИзмеренияВремени.Месяц Тогда  
		ПредставлениеПериода = "MONTH";
	ИначеЕсли   ЕдиницаИзмеренияСрокаГодности = Перечисления.ЕдиницыИзмеренияВремени.Год Тогда   
		ПредставлениеПериода = "YEAR";
	КонецЕсли;
	
	Возврат  ПредставлениеПериода;
		
КонецФункции

// Возвращает порцию товарного каталога из сервиса.
// Описание метода см. https://yandex.ru/dev/market/partner-api/doc/ru/reference/business-assortment/getOfferMappings.
//
// Параметры:
//   ДанныеУчетнойЗаписи - Структура - данные учетной записи (см. ДанныеУчетнойЗаписиЯндексМаркет);
//   Параметры           - Структура - список параметров запроса, 
//                           см. НовыеПараметрыЗапросаИмпортаТоварногоКаталога.
//   УчетныеЗаписи       - Неопределено, Соответствие Из КлючИЗначение - список магазинов для кабинета, 
//                           где ключ - идентификатор магазина; значение - ссылка.
//   Отказ               - Булево - выходной параметр, определяет наличие ошибки при выполнении запросов к сервису.
//
// Возвращаемое значение:
//   - ТаблицаЗначений - см. НоваяТаблицаОписанияТоваров.
//   - Неопределено - при выполнении запроса к сервису обнаружены ошибки.
//
Функция ИмпортироватьПорциюТоварногоКаталогаИзСервиса(ДанныеУчетнойЗаписи, Параметры, УчетныеЗаписи = Неопределено, 
			Отказ = Ложь)
	
	Если УчетныеЗаписи = Неопределено Тогда
		УчетныеЗаписи = Новый Соответствие;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	УчетныеЗаписиМаркетплейсов.ИдентификаторМагазина КАК ИдентификаторМагазина,
			|	УчетныеЗаписиМаркетплейсов.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
			|ГДЕ
			|	УчетныеЗаписиМаркетплейсов.ИдентификаторКабинета = &ИдентификаторКабинета
			|	И НЕ УчетныеЗаписиМаркетплейсов.ПометкаУдаления";
			
		Запрос.УстановитьПараметр("ИдентификаторКабинета", ДанныеУчетнойЗаписи.ИдентификаторКабинета);
		
		УстановитьПривилегированныйРежим(Истина);
		ВыборкаДанных = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		
		Пока ВыборкаДанных.Следующий() Цикл
			УчетныеЗаписи.Вставить(ВыборкаДанных.ИдентификаторМагазина, ВыборкаДанных.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
	Попытка
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		ДанныеПриложения  = ДанныеПриложения();
		
		ПараметрыURL = Новый Массив;
		Если Не ЗначениеЗаполнено(Параметры.ИдентификаторыТоваров) Тогда
			ПараметрыURL.Добавить("limit=" + Параметры.КоличествоНаСтранице);
			Если ЗначениеЗаполнено(Параметры.ИдентификаторСтраницы) Тогда
				ПараметрыURL.Добавить("page_token=" + Параметры.ИдентификаторСтраницы);
			КонецЕсли;
		КонецЕсли;
		
		Сервер       = СерверПартнерскогоAPI(Ложь);
		ИмяМетода    = "/offer-mappings.json";
		АдресРесурса = "/" + ДанныеУчетнойЗаписи.ИдентификаторКабинета + ИмяМетода;
		Если Не ЗначениеЗаполнено(Параметры.ИдентификаторыТоваров) Тогда
			АдресРесурса = АдресРесурса + "?" + СтрСоединить(ПараметрыURL, "&");
		КонецЕсли;
		
		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 180, СоединениеOpenSSL);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type",  "application/json");
		Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
			Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
		ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
			Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
		КонецЕсли;
		ДобавитьПодписьИнтеграцииВЗаголовки(Заголовки);
		
		ДанныеЗапроса = Новый Структура;
		Если ЗначениеЗаполнено(Параметры.ИдентификаторыТоваров) Тогда
			ДанныеЗапроса.Вставить("offerIds", Параметры.ИдентификаторыТоваров);
		Иначе
			ДанныеЗапроса.Вставить("archived", Параметры.Архивный);
			Если ЗначениеЗаполнено(Параметры.СтатусыКарточек) Тогда
				ДанныеЗапроса.Вставить("cardStatuses", Параметры.СтатусыКарточек);
			КонецЕсли;
			Если ЗначениеЗаполнено(Параметры.ИдентификаторыКатегорий) Тогда
				ДанныеЗапроса.Вставить("categoryIds", Параметры.ИдентификаторыКатегорий);
			КонецЕсли;
			Если ЗначениеЗаполнено(Параметры.Теги) Тогда
				ДанныеЗапроса.Вставить("tags", Параметры.Теги);
			КонецЕсли;
			Если ЗначениеЗаполнено(Параметры.Бренды) Тогда
				ДанныеЗапроса.Вставить("vendorNames", Параметры.Бренды);
			КонецЕсли;
		КонецЕсли;
		ТелоЗапроса = ВJSON(ДанныеЗапроса);
		
		ПоляТипаДата = Новый Массив;
		ПоляТипаДата.Добавить("updatedAt");
		
		HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, "UTF-8");
		
		HTTPОтвет    = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);  
		КодСостояния = HTTPОтвет.КодСостояния;
		СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
		ОтветСервиса = ИзJSON(СтрокаОтвета, ПоляТипаДата);
		
		Если КодСостояния = 200 И ОтветСервиса.status = "OK" Тогда
			Параметры.ИдентификаторСтраницы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОтветСервиса.result.paging, "nextPageToken", "");
			ТаблицаТоваров = НоваяТаблицаОписанияТоваров();
			
			Для Каждого ЭлементОтветаСервиса Из ОтветСервиса.result.offerMappings Цикл
				ДобавитьОтветСервисаВТаблицуОписанияТоваров(ТаблицаТоваров, ЭлементОтветаСервиса, УчетныеЗаписи);
			КонецЦикла;
			
			Возврат ТаблицаТоваров;
			
		Иначе
			Если ОтветСервиса.Свойство("status") И ОтветСервиса.status = "ERROR" Тогда
				ОписаниеОшибок = ТекстОшибки(ОтветСервиса.errors);
			Иначе
				ОписаниеОшибок = ""; 
			КонецЕсли;
			
			Отказ = Истина;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка выполнения запроса %1: %2%3';
					|en = 'An error occurred when executing the %1 request: %2%3'", 
					ОбщегоНазначения.КодОсновногоЯзыка()), 
				Сервер + АдресРесурса, 
				ОписаниеОшибок,
				СтрокаОтвета);
				
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
		КонецЕсли;
		
	Исключение 
		Отказ = Истина;
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
	КонецПопытки;
	
	Возврат Неопределено;
	
КонецФункции

// Заполняет таблицу описания товаров значениями из сервиса.
//
// Параметры:
//   ТаблицаТоваров       - См. НоваяТаблицаОписанияТоваров.
//   ЭлементОтветаСервиса - Структура Из КлючИЗначение - описание товаров из сервиса.
//   УчетныеЗаписи        - Соответствие Из КлючИЗначение - список магазинов для кабинета, 
//                            где ключ - идентификатор магазина; значение - ссылка.
//
Процедура ДобавитьОтветСервисаВТаблицуОписанияТоваров(ТаблицаТоваров, ЭлементОтветаСервиса, УчетныеЗаписи)
	
	Если Не ЭлементОтветаСервиса.offer.Свойство("campaigns") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементКоллекции Из ЭлементОтветаСервиса.offer.campaigns Цикл
		НоваяСтрока = ТаблицаТоваров.Добавить();
		
		НоваяСтрока.НомерСтроки           = ТаблицаТоваров.Количество() - 1;
		НоваяСтрока.ИдентификаторМагазина = Формат(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементКоллекции, "campaignId", 0), "ЧН=; ЧГ=0");
		НоваяСтрока.СтатусВМагазине       = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементКоллекции, "status", "");
		НоваяСтрока.УчетнаяЗапись         = УчетныеЗаписи.Получить(НоваяСтрока.ИдентификаторМагазина);
		
		// Информация о карточке товара на Маркете
		Если ЭлементОтветаСервиса.Свойство("mapping") Тогда
			НоваяСтрока.ИдентификаторКатегории      = Формат(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.mapping, "marketCategoryId", 0), "ЧГ=0");
			НоваяСтрока.НаименованиеКатегории       = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.mapping, "marketCategoryName", "");
			НоваяСтрока.ИдентификаторМодели         = Формат(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.mapping, "marketModelId", 0), "ЧГ=0");
			НоваяСтрока.НаименованиеМодели          = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.mapping, "marketModelName", "");
			НоваяСтрока.ИдентификаторКарточкиТовара = Формат(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.mapping, "marketSku", 0), "ЧГ=0");
			НоваяСтрока.НаименованиеКарточкиТовара  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.mapping, "marketSkuName", "");
		КонецЕсли;
		
		// Основные параметры товара
		СтатусСОписанием = ПолучитьСтатусМодерацииСОписанием(
			ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "cardStatus", "READY"), Неопределено);
		
		НоваяСтрока.ИдентификаторПредложения = Формат(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "offerId", 0), "ЧН=; ЧГ=0");
		НоваяСтрока.ПредставлениеТовара      = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "name", "");
		НоваяСтрока.ОписаниеТовара           = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "description", "");
		НоваяСтрока.Статус                   = СтатусСОписанием.Статус;
		НоваяСтрока.ОписаниеОшибки           = СтатусСОписанием.ОписаниеОшибки;
		НоваяСтрока.Архивный                 = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "archived", Ложь);
		НоваяСтрока.Штрихкоды                = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "barcodes", Новый Массив);
		НоваяСтрока.Штрихкод                 = СтрСоединить(НоваяСтрока.Штрихкоды, "; ");
		НоваяСтрока.СтраныПроисхождения      = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "manufacturerCountries", Новый Массив);
		НоваяСтрока.ТоварнаяКатегория        = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "category", "");
		НоваяСтрока.Артикул                  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "vendorCode", "");
		НоваяСтрока.Бренд                    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "vendor", "");
		Если ЭлементОтветаСервиса.offer.Свойство("weightDimensions") Тогда
			НоваяСтрока.Длина  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.weightDimensions, "length", 0);
			НоваяСтрока.Ширина = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.weightDimensions, "width", 0);
			НоваяСтрока.Высота = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.weightDimensions, "height", 0);
			НоваяСтрока.Вес    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.weightDimensions, "weight", 0);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(НоваяСтрока.ИдентификаторКарточкиТовара) Тогда
			НоваяСтрока.ИдентификаторКарточкиТовара = НоваяСтрока.ИдентификаторПредложения;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(НоваяСтрока.ПредставлениеТовара) Тогда
			НоваяСтрока.ПредставлениеТовара = НоваяСтрока.НаименованиеКарточкиТовара;
		ИначеЕсли Не ЗначениеЗаполнено(НоваяСтрока.НаименованиеКарточкиТовара) Тогда
			НоваяСтрока.НаименованиеКарточкиТовара = НоваяСтрока.ПредставлениеТовара;
		КонецЕсли;
		Если НоваяСтрока.СтраныПроисхождения.Количество() > 0 Тогда
			НоваяСтрока.СтранаПроисхождения = НоваяСтрока.СтраныПроисхождения[0];
		КонецЕсли;
		
		// Ценовые показатели
		Если ЭлементОтветаСервиса.offer.Свойство("basicPrice") Тогда
			НоваяСтрока.КодВалюты          = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.basicPrice, "currencyId", "RUR");
			НоваяСтрока.ЦенаДоСкидки       = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.basicPrice, "discountBase", 0);
			НоваяСтрока.ЦенаСоСкидкой      = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.basicPrice, "value", 0);
			НоваяСтрока.ДатаОбновленияЦены = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.basicPrice, "updatedAt", ТекущаяДатаСеанса());
		КонецЕсли;
		
		// Прочее
		НоваяСтрока.ТолькоДляВзрослых      = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "adult", Ложь);
		НоваяСтрока.КоличествоГрузовыхМест = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "boxCount", 0);
		НоваяСтрока.Сертификаты            = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "certificates", Новый Массив);
		НоваяСтрока.КодТНВЭД               = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "customsCommodityCode", "");
		НоваяСтрока.Цифровой               = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "downloadable", Ложь);
		НоваяСтрока.Изображения            = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "pictures", Новый Массив);
		НоваяСтрока.Видео                  = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "videos", Новый Массив);
		НоваяСтрока.Теги                   = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "tags", Новый Массив);
		НоваяСтрока.Инструкции             = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "manuals", Новый Массив);
		НоваяСтрока.ТипТовара              = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer, "type", "");
		Если ЭлементОтветаСервиса.offer.Свойство("age") Тогда
			НоваяСтрока.ОграничениеПоВозрасту = Новый Структура("ЕдиницаИзмерения, Значение",
													ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.age, "ageUnit", "YEAR"),
													ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.age, "value", 0));
		КонецЕсли;
		Если ЭлементОтветаСервиса.offer.Свойство("guaranteePeriod") Тогда
			НоваяСтрока.ГарантийныйПериод = Новый Структура("ЕдиницаИзмерения, Значение, Комментарий",
												ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.guaranteePeriod, "timeUnit", "YEAR"),
												ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.guaranteePeriod, "timePeriod", 0),
												ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.guaranteePeriod, "comment", ""));
		КонецЕсли;
		Если ЭлементОтветаСервиса.offer.Свойство("lifeTime") Тогда
			НоваяСтрока.СрокСлужбы = Новый Структура("ЕдиницаИзмерения, Значение, Комментарий",
											ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.lifeTime, "timeUnit", "YEAR"),
											ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.lifeTime, "timePeriod", 0),
											ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.lifeTime, "comment", ""));
		КонецЕсли;
		Если ЭлементОтветаСервиса.offer.Свойство("shelfLife") Тогда
			НоваяСтрока.СрокГодности = Новый Структура("ЕдиницаИзмерения, Значение, Комментарий",
											ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.shelfLife, "timeUnit", "MONTH"),
											ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.shelfLife, "timePeriod", 0),
											ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ЭлементОтветаСервиса.offer.shelfLife, "comment", ""));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Записывает порцию товарного каталога.
//
// Параметры:
//   ТаблицаТоваров       - См. НоваяТаблицаОписанияТоваров.
//   ПредварительныйПоиск - Булево - выполнять ли предварительный поиск номенклатуры, характеристики, упаковки.
//
Процедура ЗаписатьПорциюТоварногоКаталога(ТаблицаТоваров, ПредварительныйПоиск = Истина)
	
	Если ПредварительныйПоиск Тогда
		РазделителиСловПоиска = ",/(";
		ВыборкаПоиска = Обработки.УправлениеПродажамиНаЯндексМаркет.НайтиНоменклатурыХарактеристикиУпаковки(
			ТаблицаТоваров, 
			РазделителиСловПоиска, 
			Истина);
	КонецЕсли;
		
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ИнтеграцияСЯндексМаркетСервер.ЗаписатьПорциюТоварногоКаталога");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТоваров.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ТаблицаТоваров.ИдентификаторПредложения КАК ИдентификаторПредложения,
		|	ТаблицаТоваров.Статус КАК Статус,
		|	ТаблицаТоваров.ПредставлениеТовара КАК ПредставлениеТовара,
		|	ТаблицаТоваров.ИдентификаторКарточкиТовара КАК ИдентификаторКарточкиТовара,
		|	ТаблицаТоваров.НаименованиеКарточкиТовара КАК НаименованиеКарточкиТовара,
		|	ТаблицаТоваров.НаименованиеКатегории КАК НаименованиеКатегории,
		|	ТаблицаТоваров.НаименованиеМодели КАК НаименованиеМодели,
		|	ТаблицаТоваров.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	ТаблицаТоваров.ЦенаСоСкидкой КАК ЦенаСоСкидкой,
		|	ТаблицаТоваров.ДатаОбновленияЦены КАК ДатаОбновленияЦены,
		|	ТаблицаТоваров.СтранаПроисхождения КАК СтранаПроисхождения,
		|	ТаблицаТоваров.Артикул КАК Артикул
		|ПОМЕСТИТЬ ВТ_ТаблицаТоваров
		|ИЗ
		|	&ТаблицаТоваров КАК ТаблицаТоваров
		|ГДЕ
		|	ТаблицаТоваров.УчетнаяЗапись <> &ПустаяУчетнаяЗапись
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	УчетнаяЗапись,
		|	ИдентификаторПредложения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТоваров.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ЕСТЬNULL(СправочникУчетныеЗаписиМаркетплейсов.Партнер, &ПустойПартнер) КАК Владелец,
		|	ТаблицаТоваров.ИдентификаторПредложения КАК ИдентификаторПредложения,
		|	ТаблицаТоваров.Статус КАК Статус,
		|	ТаблицаТоваров.ПредставлениеТовара КАК ПредставлениеТовара,
		|	ТаблицаТоваров.ИдентификаторКарточкиТовара КАК ИдентификаторКарточкиТовара,
		|	ТаблицаТоваров.НаименованиеКарточкиТовара КАК НаименованиеКарточкиТовара,
		|	ТаблицаТоваров.НаименованиеКатегории КАК НаименованиеКатегории,
		|	ТаблицаТоваров.НаименованиеМодели КАК НаименованиеМодели,
		|	ТаблицаТоваров.ТоварнаяКатегория КАК ТоварнаяКатегория,
		|	ТаблицаТоваров.ЦенаСоСкидкой КАК ЦенаСоСкидкой,
		|	ТаблицаТоваров.ДатаОбновленияЦены КАК ДатаОбновленияЦены,
		|	ВЫРАЗИТЬ(ТаблицаТоваров.СтранаПроисхождения КАК СТРОКА(60)) КАК СтранаПроисхождения,
		|	ТаблицаТоваров.Артикул КАК Артикул,
		|	НЕ СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись ЕСТЬ NULL КАК ЕстьЗаписьПубликации,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Упаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК Упаковка,
		|	СтатусыПубликацииТоваровЯндексМаркет.Статус КАК СтатусыПубликацииСтатус,
		|	СтатусыПубликацииТоваровЯндексМаркет.ПредставлениеТовара КАК СтатусыПубликацииПредставлениеТовара,
		|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторТовараПлощадки КАК СтатусыПубликацииИдентификаторТовараПлощадки,
		|	СтатусыПубликацииТоваровЯндексМаркет.НаименованиеТовараПлощадки КАК СтатусыПубликацииНаименованиеТовараПлощадки,
		|	СтатусыПубликацииТоваровЯндексМаркет.НаименованиеКатегорииПлощадки КАК СтатусыПубликацииНаименованиеКатегорииПлощадки,
		|	СтатусыПубликацииТоваровЯндексМаркет.НаименованиеМоделиПлощадки КАК СтатусыПубликацииНаименованиеМоделиПлощадки,
		|	СтатусыПубликацииТоваровЯндексМаркет.ТоварнаяКатегория КАК СтатусыПубликацииТоварнаяКатегория,
		|	СтатусыПубликацииТоваровЯндексМаркет.НоменклатураПартнера КАК СтатусыПубликацииНоменклатураПартнера
		|ПОМЕСТИТЬ ВТ_ТаблицаТоваровСВладельцем
		|ИЗ
		|	ВТ_ТаблицаТоваров КАК ТаблицаТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УчетныеЗаписиМаркетплейсов КАК СправочникУчетныеЗаписиМаркетплейсов
		|		ПО (СправочникУчетныеЗаписиМаркетплейсов.Ссылка = ТаблицаТоваров.УчетнаяЗапись)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
		|		ПО (СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = ТаблицаТоваров.УчетнаяЗапись)
		|			И (СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторПредложения = ТаблицаТоваров.ИдентификаторПредложения)
		|ГДЕ
		|	ЕСТЬNULL(СправочникУчетныеЗаписиМаркетплейсов.Партнер, &ПустойПартнер) <> &ПустойПартнер
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Владелец,
		|	ИдентификаторПредложения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ТаблицаТоваров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТоваров.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ТаблицаТоваров.Владелец КАК Владелец,
		|	ТаблицаТоваров.ИдентификаторПредложения КАК ИдентификаторПредложения,
		|	ТаблицаТоваров.Статус КАК Статус,
		|	ЕСТЬNULL(СправочникНоменклатураКонтрагентов.Ссылка, ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка)) КАК НоменклатураПартнера,
		|	СправочникНоменклатураКонтрагентов.Наименование КАК НоменклатураПартнераНаименование,
		|	СправочникНоменклатураКонтрагентов.НаименованиеПолное КАК НоменклатураПартнераНаименованиеПолное,
		|	СправочникНоменклатураКонтрагентов.Артикул КАК НоменклатураПартнераАртикул,
		|	СправочникНоменклатураКонтрагентов.ИдентификаторНоменклатуры КАК НоменклатураПартнераИдентификаторНоменклатуры,
		|	СправочникНоменклатураКонтрагентов.НаименованиеНоменклатуры КАК НоменклатураПартнераНаименованиеНоменклатуры,
		|	СправочникНоменклатураКонтрагентов.Штрихкод КАК НоменклатураПартнераШтрихкод,
		|	СправочникНоменклатураКонтрагентов.КодТНВЭД КАК НоменклатураПартнераКодТНВЭД,
		|	СправочникНоменклатураКонтрагентов.СтранаПроисхожденияКод КАК НоменклатураПартнераСтранаПроисхожденияКод,
		|	СправочникНоменклатураКонтрагентов.Недействителен КАК НоменклатураПартнераНедействителен,
		|	ЕСТЬNULL(СправочникСтраныМира.Код, ""   "") КАК СтранаПроисхожденияКод,
		|	НЕ ИмпортируемыеТоварыТорговыхПлощадок.УчетнаяЗапись ЕСТЬ NULL КАК ЕстьЗаписьИмпорта,
		|	ТаблицаТоваров.ЕстьЗаписьПубликации КАК ЕстьЗаписьПубликации,
		|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваров.Характеристика КАК Характеристика,
		|	ТаблицаТоваров.Упаковка КАК Упаковка,
		|	ПРЕДСТАВЛЕНИЕ(ТаблицаТоваров.Упаковка) КАК НаименованиеУпаковки,
		|	ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК УпаковкаКоэффициент,
		|	ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения.Наименование, ЕСТЬNULL(ДанныеЕдиницыПоУмолчанию.Наименование, """")) КАК ЕдиницаИзмеренияНаименование,
		|	ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения.Код, ЕСТЬNULL(ДанныеЕдиницыПоУмолчанию.Код, """")) КАК ЕдиницаИзмеренияКод,
		|	ЕСТЬNULL(ТаблицаТоваров.СтатусыПубликацииНоменклатураПартнера, ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка))<> ЕСТЬNULL(СправочникНоменклатураКонтрагентов.Ссылка, ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка)) КАК ТребуетсяЗаписьПубликации,
		|	ТаблицаТоваров.ЕстьЗаписьПубликации
		|		И (ТаблицаТоваров.Статус <> ТаблицаТоваров.СтатусыПубликацииСтатус
		|			ИЛИ (ВЫРАЗИТЬ(ТаблицаТоваров.ПредставлениеТовара КАК СТРОКА(300))) <> ТаблицаТоваров.СтатусыПубликацииПредставлениеТовара
		|			ИЛИ (ВЫРАЗИТЬ(ТаблицаТоваров.ИдентификаторКарточкиТовара КАК СТРОКА(300))) <> ТаблицаТоваров.СтатусыПубликацииИдентификаторТовараПлощадки
		|			ИЛИ (ВЫРАЗИТЬ(ТаблицаТоваров.НаименованиеКарточкиТовара КАК СТРОКА(200))) <> ТаблицаТоваров.СтатусыПубликацииНаименованиеТовараПлощадки
		|			ИЛИ (ВЫРАЗИТЬ(ТаблицаТоваров.НаименованиеКатегории КАК СТРОКА(250))) <> ТаблицаТоваров.СтатусыПубликацииНаименованиеКатегорииПлощадки
		|			ИЛИ (ВЫРАЗИТЬ(ТаблицаТоваров.НаименованиеМодели КАК СТРОКА(250))) <> ТаблицаТоваров.СтатусыПубликацииНаименованиеМоделиПлощадки
		|			ИЛИ (ВЫРАЗИТЬ(ТаблицаТоваров.Артикул КАК СТРОКА(50))) <> ЕСТЬNULL(СправочникНоменклатура.Артикул, """")) КАК ЕслиОтличияПубликации
		|ИЗ
		|	ВТ_ТаблицаТоваровСВладельцем КАК ТаблицаТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК СправочникНоменклатураКонтрагентов
		|		ПО (СправочникНоменклатураКонтрагентов.Владелец = ТаблицаТоваров.Владелец)
		|			И (СправочникНоменклатураКонтрагентов.Идентификатор = ТаблицаТоваров.ИдентификаторПредложения)
		|			И (НЕ СправочникНоменклатураКонтрагентов.ПометкаУдаления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИмпортируемыеТоварыТорговыхПлощадок КАК ИмпортируемыеТоварыТорговыхПлощадок
		|		ПО (ИмпортируемыеТоварыТорговыхПлощадок.УчетнаяЗапись = ТаблицаТоваров.УчетнаяЗапись)
		|			И (ИмпортируемыеТоварыТорговыхПлощадок.НоменклатураПартнера = СправочникНоменклатураКонтрагентов.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО (СправочникНоменклатура.Ссылка = ТаблицаТоваров.Номенклатура)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СправочникСтраныМира
		|		ПО (СправочникСтраныМира.Наименование = ТаблицаТоваров.СтранаПроисхождения)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ДанныеЕдиницыПоУмолчанию
		|		ПО (ДанныеЕдиницыПоУмолчанию.Ссылка = &ЕдиницаИзмеренияКоличестваШтук)";
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"(ВЫБОР
			|	КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|		ТОГДА СправочникНоменклатура.ЕдиницаИзмерения
			|	ИНАЧЕ ТаблицаТоваров.Упаковка
			|КОНЕЦ)",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.УстановитьПараметр("ТаблицаТоваров",      ТаблицаТоваров);
	Запрос.УстановитьПараметр("ПустаяУчетнаяЗапись", Справочники.УчетныеЗаписиМаркетплейсов.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустойПартнер",       Справочники.Партнеры.ПустаяСсылка());
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияКоличестваШтук", Константы.ЕдиницаИзмеренияКоличестваШтук.Получить());
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	
	ИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	ИспользоватьУпаковкиНоменклатуры       = ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");
	
	Дубли = Новый Соответствие;
	
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.НоменклатураКонтрагентов");
		ЭлементБлокировкиДанных.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("ВладелецНоменклатуры", "Владелец");
		ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Ссылка", "НоменклатураПартнера");
		
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ИмпортируемыеТоварыТорговыхПлощадок");
		ЭлементБлокировкиДанных.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("УчетнаяЗапись", "УчетнаяЗапись");
		ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("НоменклатураПартнера", "НоменклатураПартнера");
		
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет");
		ЭлементБлокировкиДанных.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("УчетнаяЗапись", "УчетнаяЗапись");
		ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
		Если ИспользоватьХарактеристикиНоменклатуры Тогда
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
		КонецЕсли;
		Если ИспользоватьУпаковкиНоменклатуры Тогда
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Упаковка", "Упаковка");
		КонецЕсли;
		
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписейИмпорт = РегистрыСведений.ИмпортируемыеТоварыТорговыхПлощадок.СоздатьНаборЗаписей();

		ВыборкаДанных = РезультатЗапроса.Выбрать();
		Пока ВыборкаДанных.Следующий() Цикл
			НоменклатураКонтрагентовСсылка = Дубли.Получить(XMLСтрока(ВыборкаДанных.Владелец) + ВыборкаДанных.ИдентификаторПредложения);
			
			Если Не ЗначениеЗаполнено(НоменклатураКонтрагентовСсылка) Тогда
				
				ИндексСтроки = ВыборкаДанных.НомерСтроки; // Число
				ВладелецТоварногоКаталога = ВыборкаДанных.Владелец; // СправочникСсылка.Партнеры
				
				ДанныеТовара = ТаблицаТоваров[ИндексСтроки]; // СтрокаТаблицыЗначений из см. НоваяТаблицаОписанияТоваров
				ДанныеНоменклатуры = ДанныеНоменклатурыДляОбновленияПриИмпортеПорцииТоварногоКаталога(
					ВыборкаДанных,
					ДанныеТовара,
					ПредварительныйПоиск,
					ВыборкаПоиска);
				
				РезультатСоздания = ИнтеграцияСМаркетплейсамиСервер.СоздатьОбновитьНоменклатуруПартнера(
					ВладелецТоварногоКаталога,
					ДанныеНоменклатуры);
				
				Если РезультатСоздания.Отказ Тогда
					ВызватьИсключение РезультатСоздания.ТекстОшибки;
				КонецЕсли;
				
				НоменклатураКонтрагентовСсылка = РезультатСоздания.НоменклатураПартнера;
				Дубли.Вставить(XMLСтрока(ВыборкаДанных.Владелец) + ВыборкаДанных.ИдентификаторПредложения, НоменклатураКонтрагентовСсылка);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НоменклатураКонтрагентовСсылка) Тогда
				Если Не ВыборкаДанных.ЕстьЗаписьИмпорта
						Или ВыборкаДанных.ЕстьЗаписьПубликации Тогда
					НаборЗаписейИмпорт.Отбор.УчетнаяЗапись.Установить(ВыборкаДанных.УчетнаяЗапись);
					НаборЗаписейИмпорт.Отбор.НоменклатураПартнера.Установить(НоменклатураКонтрагентовСсылка);
					НаборЗаписейИмпорт.Очистить();
					
					Если (Не ВыборкаДанных.ЕстьЗаписьПубликации И Не ВыборкаДанных.ЕстьЗаписьИмпорта)
							Или (ВыборкаДанных.ЕстьЗаписьПубликации И ВыборкаДанных.ЕслиОтличияПубликации) Тогда
						Запись = НаборЗаписейИмпорт.Добавить();
						Запись.УчетнаяЗапись        = ВыборкаДанных.УчетнаяЗапись;
						Запись.НоменклатураПартнера = НоменклатураКонтрагентовСсылка;
						Запись.Статус               = ВыборкаДанных.Статус;
						Запись.ЕстьИзменения        = ВыборкаДанных.ЕслиОтличияПубликации;
					КонецЕсли;
					
					НаборЗаписейИмпорт.Записать(Истина);
				КонецЕсли;
				
				Если ВыборкаДанных.ЕстьЗаписьПубликации И ВыборкаДанных.ТребуетсяЗаписьПубликации Тогда
					НаборЗаписей = РегистрыСведений.СтатусыПубликацииТоваровЯндексМаркет.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.УчетнаяЗапись.Установить(ВыборкаДанных.УчетнаяЗапись);
					НаборЗаписей.Отбор.Номенклатура.Установить(ВыборкаДанных.Номенклатура);
					Если ИспользоватьХарактеристикиНоменклатуры Тогда
						НаборЗаписей.Отбор.Характеристика.Установить(ВыборкаДанных.Характеристика);
					КонецЕсли;
					Если ИспользоватьУпаковкиНоменклатуры Тогда
						НаборЗаписей.Отбор.Упаковка.Установить(ВыборкаДанных.Упаковка);
					КонецЕсли;
					НаборЗаписей.Прочитать();
					
					Если НаборЗаписей.Количество() > 0 Тогда
						Запись = НаборЗаписей[0];
						Запись.НоменклатураПартнера = НоменклатураКонтрагентовСсылка;
						
						НаборЗаписей.Записать(Истина);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
		Замер, 
		ТаблицаТоваров.Количество() / 100);
	
КонецПроцедуры

// Параметры:
//  ВыборкаДанных - ВыборкаИзРезультатаЗапроса:
//   * ЕдиницаИзмерения - СправочникСсылка.УпаковкиЕдиницыИзмерения
//   * ЕдиницаИзмеренияНаименование - Строка
//   * УпаковкаКоэффициент - Число
//   * ИдентификаторПредложения - Строка
//  ДанныеТовара - СтрокаТаблицыЗначений из см. НоваяТаблицаОписанияТоваров
//  ПредварительныйПоиск - Булево
//  ВыборкаПоиска - ВыборкаИзРезультатаЗапроса
// 
// Возвращаемое значение:
//  См. ИнтеграцияСМаркетплейсамиСервер.НовыеДанныеНоменклатурыПартнераДляОбновления
//
Функция ДанныеНоменклатурыДляОбновленияПриИмпортеПорцииТоварногоКаталога(ВыборкаДанных, ДанныеТовара,
			ПредварительныйПоиск, ВыборкаПоиска)
	
	ДанныеНоменклатуры = ИнтеграцияСМаркетплейсамиСервер.НовыеДанныеНоменклатурыПартнераДляОбновления();
	
	ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры, ВыборкаДанных);
	ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры, ДанныеТовара);
	
	ДанныеНоменклатуры.НаименованиеТовара = ДанныеТовара.НаименованиеКарточкиТовара;
	ДанныеНоменклатуры.ИдентификаторТовара = ДанныеТовара.ИдентификаторПредложения;
	ДанныеНоменклатуры.ИдентификаторКакАртикул = ДанныеТовара.Артикул;
	
	Если ДанныеТовара.Штрихкоды.Количество() > 0 Тогда
		ДанныеНоменклатуры.Штрихкод = ДанныеТовара.Штрихкоды[0];
		
		ШтрихкодыНоменклатуры = Новый Массив; // Массив из Строка
		Для Индекс = 1 По ДанныеТовара.Штрихкоды.ВГраница() Цикл
			ШтрихкодыНоменклатуры.Добавить(ДанныеТовара.Штрихкоды[Индекс]);
		КонецЦикла;
		ДанныеНоменклатуры.ШтрихкодыНоменклатуры = СтрСоединить(ШтрихкодыНоменклатуры, ",");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеНоменклатуры.Упаковка) Тогда
		ДанныеНоменклатуры.Упаковка = ВыборкаДанных.ЕдиницаИзмерения;
	КонецЕсли;
	
	ДанныеНоменклатуры.ЕдиницаИзмерения = ВыборкаДанных.ЕдиницаИзмеренияНаименование;
	ДанныеНоменклатуры.КоэффициентУпаковки = ВыборкаДанных.УпаковкаКоэффициент;
	ДанныеНоменклатуры.Недействителен = ДанныеТовара.Архивный;
	
	Если ПредварительныйПоиск Тогда
		РезультатПоиска = Обработки.УправлениеПродажамиНаЯндексМаркет.ОпределитьНоменклатуруХарактеристикуУпаковку(
			ВыборкаПоиска, 
			ВыборкаДанных.ИдентификаторПредложения,
			"ИдентификаторПредложения");
		
		Если ЗначениеЗаполнено(РезультатПоиска.Номенклатура) Тогда
			ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры, РезультатПоиска);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеНоменклатуры;
	
КонецФункции

// Возвращает перечень свойств товара для дальнейшего сравнения значений этих свойств.
//
// Возвращаемое значение:
//   Соответствие Из КлючИЗначение - перечень свойств товара для дальнейшего сравнения, 
//                            где ключ - идентификатор свойства; значение - наименование свойства. 
//
Функция ПолучитьСвойстваТовараДляСравнения() Экспорт
	
	СоответствиеСвойствТовара = Новый Соответствие;
	СоответствиеСвойствТовара.Вставить("Наименование",                         НСтр("ru = 'Наименование';
																					|en = 'Description'"));
	СоответствиеСвойствТовара.Вставить("Артикул",                              НСтр("ru = 'Артикул';
																					|en = 'Product ID'"));
	СоответствиеСвойствТовара.Вставить("Производитель",                        НСтр("ru = 'Производитель';
																					|en = 'Manufacturer'"));
	СоответствиеСвойствТовара.Вставить("СтранаПроизводства",                   НСтр("ru = 'Страна производства';
																					|en = 'Сountry of origin'"));
	СоответствиеСвойствТовара.Вставить("ШтрихКоды",                            НСтр("ru = 'Штрих-коды';
																					|en = 'Barcodes'"));
	СоответствиеСвойствТовара.Вставить("НаименованиеТовараВКаталогеЯндекса",   НСтр("ru = 'Наименование товара в каталоге Яндекса';
																					|en = 'Item name in the Yandex directory'"));
	СоответствиеСвойствТовара.Вставить("ИдентификаторТовараВКаталогеЯндекса",  НСтр("ru = 'Идентификатор товара в каталоге Яндекса';
																					|en = 'Item ID in the Yandex directory'"));
	СоответствиеСвойствТовара.Вставить("КатегорияТовараВКаталогеЯндекса",      НСтр("ru = 'Категория товара в каталоге Яндекса';
																					|en = 'Item category in the Yandex directory'"));
	СоответствиеСвойствТовара.Вставить("МодельТовараВКаталогеЯндекс",          НСтр("ru = 'Модель товара в каталоге Яндекс';
																					|en = 'Item model in the Yandex directory'"));
	
	Возврат СоответствиеСвойствТовара;   
	
КонецФункции

// Возвращает свойства товара по данным учетной системы.
//
// Параметры:
//   УчетнаяЗапись        - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//   НоменклатураПартнера - СправочникСсылка.НоменклатураКонтрагентов - данные номенклатуры партнера;
//   
// Возвращаемое значение:
//   Соответствие Из КлючИЗначение - значения свойств товара по данным учетной системы, 
//                            где ключ - идентификатор свойства; значение  - значение свойства по данным учетной системы. 
//
Функция ПолучитьДанныеУчетнойСистемыДляСравнения(УчетнаяЗапись, НоменклатураПартнера) Экспорт  
	
	СоответствиеДанныхУчетнойСистемы = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СтатусыПубликацииТоваровЯндексМаркет.Номенклатура КАК Номенклатура,
		|	СтатусыПубликацииТоваровЯндексМаркет.Характеристика КАК Характеристика,
		|	СтатусыПубликацииТоваровЯндексМаркет.Упаковка КАК Упаковка,
		|	СтатусыПубликацииТоваровЯндексМаркет.ПредставлениеТовара КАК Наименование,
		|	ЕСТЬNULL(СправочникНоменклатура.Артикул, """") КАК Артикул,
		|	ЕСТЬNULL(СправочникНоменклатура.СтранаПроисхождения, ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК СтранаПроизводства,
		|	ЕСТЬNULL(СправочникНоменклатура.Производитель, ЗНАЧЕНИЕ(Справочник.Производители.ПустаяСсылка)) КАК Производитель,
		|	СтатусыПубликацииТоваровЯндексМаркет.НаименованиеТовараПлощадки КАК НаименованиеТовараВКаталогеЯндекса,
		|	СтатусыПубликацииТоваровЯндексМаркет.НаименованиеКатегорииПлощадки КАК КатегорияТовараВКаталогеЯндекса,
		|	СтатусыПубликацииТоваровЯндексМаркет.НаименованиеМоделиПлощадки КАК МодельТовараВКаталогеЯндекс,
		|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторТовараПлощадки КАК ИдентификаторТоварногоПредложения
		|ИЗ
		|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО (СправочникНоменклатура.Ссылка = СтатусыПубликацииТоваровЯндексМаркет.Номенклатура)
		|ГДЕ
		|	СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись
		|	И СтатусыПубликацииТоваровЯндексМаркет.НоменклатураПартнера = &НоменклатураПартнера";
	
	Запрос.Параметры.Вставить("УчетнаяЗапись",        УчетнаяЗапись);
	Запрос.Параметры.Вставить("НоменклатураПартнера", НоменклатураПартнера);
	
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаДанных.Следующий() Тогда
		СоответствиеДанныхУчетнойСистемы.Вставить("Наименование",                        ВыборкаДанных.Наименование);
		СоответствиеДанныхУчетнойСистемы.Вставить("Артикул",                             ВыборкаДанных.Артикул);
		СоответствиеДанныхУчетнойСистемы.Вставить("Производитель",                       ВыборкаДанных.Производитель);
		СоответствиеДанныхУчетнойСистемы.Вставить("СтранаПроизводства",                  ВыборкаДанных.СтранаПроизводства);
		СоответствиеДанныхУчетнойСистемы.Вставить("НаименованиеТовараВКаталогеЯндекса",  ВыборкаДанных.НаименованиеТовараВКаталогеЯндекса);
		СоответствиеДанныхУчетнойСистемы.Вставить("ИдентификаторТовараВКаталогеЯндекса", ВыборкаДанных.ИдентификаторТоварногоПредложения);
		СоответствиеДанныхУчетнойСистемы.Вставить("КатегорияТовараВКаталогеЯндекса",     ВыборкаДанных.КатегорияТовараВКаталогеЯндекса);
		СоответствиеДанныхУчетнойСистемы.Вставить("МодельТовараВКаталогеЯндекс",         ВыборкаДанных.МодельТовараВКаталогеЯндекс);
		
		ШтрихКоды = ПолучитьШтрихКоды(ВыборкаДанных.Номенклатура, ВыборкаДанных.Характеристика, ВыборкаДанных.Упаковка);
		ОбщегоНазначенияУТКлиентСервер.СортироватьМассив(ШтрихКоды);
		СоответствиеДанныхУчетнойСистемы.Вставить("ШтрихКоды", СтрСоединить(ШтрихКоды, "; "));
	КонецЕсли;
	
	Возврат СоответствиеДанныхУчетнойСистемы;
	
КонецФункции

// Возвращает свойства товара по данным торговой площадки.
//
// Параметры:
//   УчетнаяЗапись                     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//   ИдентификаторТоварногоПредложения - Строка - идентификатор товарного предложения;
//   
// Возвращаемое значение:
//   Соответствие Из КлючИЗначение - значения свойств товара по данным торговой площадки 
//                            где ключ - идентификатор свойства; значение  - значение свойства по данным торговой площадки. 
//
Функция ПолучитьДанныеТорговойПлощадкиДляСравнения(УчетнаяЗапись, ИдентификаторТоварногоПредложения) Экспорт 
	
	СоответствиеДанныхТорговойПлощадки = Новый Соответствие;
	
	Если УчетнаяЗапись <> Неопределено Тогда
		ИдентификаторыТоваров = Новый Массив;
		ИдентификаторыТоваров.Добавить(ИдентификаторТоварногоПредложения);
		
		ТаблицаТоваров = ПолучитьТоварныйКаталог(УчетнаяЗапись, ИдентификаторыТоваров);
		
		Если ТаблицаТоваров.Количество() > 0 Тогда
			СоответствиеДанныхТорговойПлощадки.Вставить("Наименование",                        ТаблицаТоваров[0].ПредставлениеТовара);
			СоответствиеДанныхТорговойПлощадки.Вставить("Артикул",                             ТаблицаТоваров[0].Артикул);
			СоответствиеДанныхТорговойПлощадки.Вставить("Производитель",                       ТаблицаТоваров[0].Бренд);
			СоответствиеДанныхТорговойПлощадки.Вставить("СтранаПроизводства",                  ТаблицаТоваров[0].СтранаПроисхождения);
			СоответствиеДанныхТорговойПлощадки.Вставить("НаименованиеТовараВКаталогеЯндекса",  ТаблицаТоваров[0].НаименованиеКарточкиТовара);
			СоответствиеДанныхТорговойПлощадки.Вставить("ИдентификаторТовараВКаталогеЯндекса", ТаблицаТоваров[0].ИдентификаторКарточкиТовара);
			СоответствиеДанныхТорговойПлощадки.Вставить("КатегорияТовараВКаталогеЯндекса",     ТаблицаТоваров[0].НаименованиеКатегории);
			СоответствиеДанныхТорговойПлощадки.Вставить("МодельТовараВКаталогеЯндекс",         ТаблицаТоваров[0].НаименованиеМодели); 
			
			ШтрихКоды = ТаблицаТоваров[0].Штрихкоды;
			ОбщегоНазначенияУТКлиентСервер.СортироватьМассив(ШтрихКоды);
			СоответствиеДанныхТорговойПлощадки.Вставить("ШтрихКоды", СтрСоединить(ШтрихКоды, "; "));
		КонецЕсли;
	КонецЕсли;  
	
	Возврат СоответствиеДанныхТорговойПлощадки;
	
КонецФункции
// Конструктор списка расширений для изображений.
//
// Параметры:
//   ИспользоватьPDF - Булево - необходимость включать файлы PDF.
// 
// Возвращаемое значение:
//   Массив Из Строка - список расширений изображений.
//
Функция РасширенияИзображений(ИспользоватьPDF = Ложь) Экспорт

	РасширенияИзображений = Новый Массив;
	РасширенияИзображений.Добавить("jpg");
	РасширенияИзображений.Добавить("jpeg");
	РасширенияИзображений.Добавить("png");

	Если ИспользоватьPDF Тогда
		РасширенияИзображений.Добавить("pdf");
	КонецЕсли;

	Возврат РасширенияИзображений;

КонецФункции

// Сохраняет данные о публичных ссылках изображений товара.
//
// Параметры:
//   УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису. 
//   ИдентификаторыТовараПлощадки - СписокЗначений Из Строка - список идентификаторов товарных позиций;
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ПолучитьПубличныеСсылкиИзображений(УчетнаяЗапись, ИдентификаторыПредложения) 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	Запрос.УстановитьПараметр("РасширенияИзображенийТоваров", РасширенияИзображений());  
	Запрос.УстановитьПараметр("ИдентификаторыПредложения",ИдентификаторыПредложения);
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтатусыПубликацииТоваровЯндексМаркет.Номенклатура КАК Номенклатура,
		|	СтатусыПубликацииТоваровЯндексМаркет.Характеристика КАК Характеристика,
		|	СтатусыПубликацииТоваровЯндексМаркет.Упаковка КАК Упаковка,
		|	НоменклатураПрисоединенныеФайлы.Ссылка КАК ФайлИзображения,
		|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторПредложения КАК ИдентификаторПредложения
		|ИЗ
		|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураПрисоединенныеФайлы КАК НоменклатураПрисоединенныеФайлы
		|		ПО СтатусыПубликацииТоваровЯндексМаркет.Номенклатура = НоменклатураПрисоединенныеФайлы.ВладелецФайла
		|			И (НоменклатураПрисоединенныеФайлы.ПубликуетсяВСервисах)
		|			И (НЕ НоменклатураПрисоединенныеФайлы.ПометкаУдаления)
		|			И (НоменклатураПрисоединенныеФайлы.Расширение В (&РасширенияИзображенийТоваров))
		|ГДЕ
		|	СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись
		|	И СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторПредложения В(&ИдентификаторыПредложения)
		|ИТОГИ ПО
		|	ИдентификаторПредложения";

	ТаблицаЗаписиИзменений = Новый ТаблицаЗначений;
	ТаблицаЗаписиИзменений.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаЗаписиИзменений.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаЗаписиИзменений.Колонки.Добавить("Упаковка", Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения")); 
	ТаблицаЗаписиИзменений.Колонки.Добавить("ИдентификаторПредложения", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(300))); 
	ТаблицаЗаписиИзменений.Колонки.Добавить("ПубличныеСсылки", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(500)));       
	ТаблицаЗаписиИзменений.Колонки.Добавить("ДанныеФайлов", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(500)));   
	ТаблицаЗаписиИзменений.Колонки.Добавить("НеВсеФайлыВыгружены", Новый ОписаниеТипов("Булево"));

	ТокенДоступа = ИнтеграцияСМаркетплейсамиСервер.ПолучитьТокенДоступаЯндексДиск(УчетнаяЗапись);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);

	ВыборкаИдентификаторовТоваров = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаИдентификаторовТоваров.Следующий() Цикл
		МассивИзображений = Новый Массив;
		МассивДанныхИзображений = Новый Массив;
		ВыборкаИзображений = ВыборкаИдентификаторовТоваров.Выбрать();  
		НеВсеФайлыВыгружены = Ложь;
		Пока ВыборкаИзображений.Следующий() Цикл
			ОсновнаяКартинка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаИзображений.Номенклатура,"ФайлКартинки"); 
			Если ЗначениеЗаполнено(ОсновнаяКартинка) Тогда
				ОсновнаяКартинкаПубликуется = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнаяКартинка, "ПубликуетсяВСервисах");
			Иначе
				ОсновнаяКартинкаПубликуется = Ложь;
			КонецЕсли;
			Номенклатура = ВыборкаИзображений.Номенклатура;
			Характеристика = ВыборкаИзображений.Характеристика;
			Упаковка = ВыборкаИзображений.Упаковка; 
			ДанныеПубличнойСсылки = ИнтеграцияСМаркетплейсамиСервер.ЗагрузитьФайлНаСервис(ТокенДоступа, ВыборкаИзображений.ФайлИзображения);
			ПубличнаяСсылка       = ДанныеПубличнойСсылки.ПубличнаяСсылка;  
			ПутьКФайлуНаЯДиске    = ДанныеПубличнойСсылки.ПутьКФайлуНаЯДиске;
						
			Если ДанныеПубличнойСсылки.Отказ Тогда
				НеВсеФайлыВыгружены = Истина;
				Продолжить;   
			КонецЕсли;   
			
			Если НЕ ВыборкаИзображений.ФайлИзображения = ОсновнаяКартинка Тогда
				МассивИзображений.Добавить(ПубличнаяСсылка); 
				МассивДанныхИзображений.Добавить(ПутьКФайлуНаЯДиске);
			КонецЕсли;
		КонецЦикла;

		Если ЗначениеЗаполнено(ОсновнаяКартинка) И ОсновнаяКартинкаПубликуется Тогда
			ДанныеПубличнойСсылкиОсновнойКартинки = ИнтеграцияСМаркетплейсамиСервер.ЗагрузитьФайлНаСервис(ТокенДоступа, ОсновнаяКартинка);
			ПубличнаяСсылкаОсновнойКартинки       = ДанныеПубличнойСсылкиОсновнойКартинки.ПубличнаяСсылка;      
			ПутьКФайлуНаЯДискеОсновнойКартинки    =  ДанныеПубличнойСсылки.ПутьКФайлуНаЯДиске;
			Если ДанныеПубличнойСсылкиОсновнойКартинки.Отказ Тогда               
				ПубличнаяСсылкаОсновнойКартинки = "";  
				ПутьКФайлуНаЯДискеОсновнойКартинки = "";
			Иначе
				ПубличнаяСсылкаОсновнойКартинки = ПубличнаяСсылкаОсновнойКартинки;  
				ПутьКФайлуНаЯДискеОсновнойКартинки = ПутьКФайлуНаЯДискеОсновнойКартинки; 
			КонецЕсли;
		Иначе
			ПубличнаяСсылкаОсновнойКартинки = "";  
			ПутьКФайлуНаЯДискеОсновнойКартинки = "";
		КонецЕсли;
		Если  ПубличнаяСсылкаОсновнойКартинки <> "" ИЛИ  МассивИзображений.Количество()>0 Тогда
			Если  МассивИзображений.Количество()>0 Тогда
				Если ПубличнаяСсылкаОсновнойКартинки <> "" Тогда
					ПубличныеСсылки = ПубличнаяСсылкаОсновнойКартинки +";"+ СтрСоединить(МассивИзображений, ";"); 
					ДанныеФайлов = ПутьКФайлуНаЯДискеОсновнойКартинки +";"+ СтрСоединить(МассивИзображений, ";");
				Иначе
					ПубличныеСсылки = СтрСоединить(МассивИзображений, ";"); 
					ДанныеФайлов = СтрСоединить(МассивДанныхИзображений, ";");
				КонецЕсли;
			Иначе
				ПубличныеСсылки = ПубличнаяСсылкаОсновнойКартинки; 
				ДанныеФайлов = ПутьКФайлуНаЯДискеОсновнойКартинки;
			КонецЕсли;
			СтрокаИзменений = ТаблицаЗаписиИзменений.Добавить();  
			СтрокаИзменений.Номенклатура = Номенклатура;
			СтрокаИзменений.Характеристика = Характеристика;
			СтрокаИзменений.Упаковка = Упаковка;
			СтрокаИзменений.ИдентификаторПредложения = ВыборкаИдентификаторовТоваров.ИдентификаторПредложения;
			СтрокаИзменений.ПубличныеСсылки = ПубличныеСсылки;  
			СтрокаИзменений.ДанныеФайлов = ДанныеФайлов;   
			СтрокаИзменений.НеВсеФайлыВыгружены = НеВсеФайлыВыгружены;
		КонецЕсли;
	КонецЦикла;     
	
	Для каждого СтрокаИзменений Из ТаблицаЗаписиИзменений Цикл   
		
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет");
		ЭлементБлокировкиДанных.УстановитьЗначение("УчетнаяЗапись",  УчетнаяЗапись);
		ЭлементБлокировкиДанных.УстановитьЗначение("Номенклатура",   СтрокаИзменений.Номенклатура);
		ЭлементБлокировкиДанных.УстановитьЗначение("Характеристика", СтрокаИзменений.Характеристика);
		ЭлементБлокировкиДанных.УстановитьЗначение("Упаковка",       СтрокаИзменений.Упаковка);
		БлокировкаДанных.Заблокировать();

		НаборЗаписей = РегистрыСведений.СтатусыПубликацииТоваровЯндексМаркет.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УчетнаяЗапись.Установить(УчетнаяЗапись);
		НаборЗаписей.Отбор.Номенклатура.Установить(СтрокаИзменений.Номенклатура);
		НаборЗаписей.Отбор.Характеристика.Установить(СтрокаИзменений.Характеристика);
		НаборЗаписей.Отбор.Упаковка.Установить(СтрокаИзменений.Упаковка);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() > 0 Тогда
			Запись = НаборЗаписей[0];
            Запись.ГиперссылкаНаРекомендованныеТовар = СтрокаИзменений.ДанныеФайлов;	
			НаборЗаписей.Записать(); 	
		КонецЕсли;

		ЗафиксироватьТранзакцию();

	Исключение
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
		
	КонецЦикла;
	
	Возврат ТаблицаЗаписиИзменений;
	
КонецФункции

#КонецОбласти

#Область УправлениеОстаткамиТоваровСлужебный

// Передает данные об остатках товаров на витрине. 
// Описание метода см. https://yandex.ru/dev/market/partner-api/doc/ru/reference/stocks/updateStocks.
//
// Параметры:
//   ДанныеУчетнойЗаписи - Структура - данные учетной записи (см. ДанныеУчетнойЗаписиЯндексМаркет);
//   Отказ               - Булево - выходной параметр, определяет наличие ошибки при выполнении запросов к сервису.
//
Процедура ВыгрузитьОстаткиТоваровВСервис(ДанныеУчетнойЗаписи, Отказ = Ложь) 
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ИнтеграцияСЯндексМаркетСервер.ВыгрузитьОстаткиТоваровВСервис");
	
	ТаблицаСкладов = ИнтеграцияСМаркетплейсамиСервер.ПолучитьСопоставленныеСклады(
		ДанныеУчетнойЗаписи.УчетнаяЗапись,
		Ложь);
	Если ТаблицаСкладов.Количество() = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не обнаружено сопоставленных складов Яндекс Маркет по учетной записи <%1>';
				|en = 'No mapped Yandex.Market warehouses are found for the <%1> account'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			ДанныеУчетнойЗаписи.УчетнаяЗапись);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,,,
			ТекстСообщения);
	
		Отказ = Истина;
	
		Возврат;
	КонецЕсли;
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ТаблицаСкладов.Группа КАК Справочник.Склады) КАК Группа,
		|	ВЫРАЗИТЬ(ТаблицаСкладов.Склад КАК Справочник.Склады) КАК Склад,
		|	ВЫРАЗИТЬ(ТаблицаСкладов.ИдентификаторСклада КАК СТРОКА(50)) КАК ИдентификаторСклада
		|ПОМЕСТИТЬ ТаблицаСкладов
		|ИЗ
		|	&ТаблицаСкладов КАК ТаблицаСкладов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Склад,
		|	ИдентификаторСклада
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись КАК УчетнаяЗапись,
		|	СтатусыПубликацииТоваровЯндексМаркет.Номенклатура КАК Номенклатура,
		|	СтатусыПубликацииТоваровЯндексМаркет.Характеристика КАК Характеристика,
		|	СтатусыПубликацииТоваровЯндексМаркет.Упаковка КАК Упаковка,
		|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторПредложения КАК ИдентификаторПредложения,
		|	ЕСТЬNULL(ТаблицаСкладов.ИдентификаторСклада, """") КАК ИдентификаторОбъектаМаркетплейса,
		|	ЕСТЬNULL(ТаблицаСкладов.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК Склад
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСкладов КАК ТаблицаСкладов
		|		ПО (ИСТИНА)
		|ГДЕ
		|	СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись
		|	И СтатусыПубликацииТоваровЯндексМаркет.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияПройдена)
		|	И ЕСТЬNULL(ТаблицаСкладов.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|	И ЕСТЬNULL(ТаблицаСкладов.ИдентификаторСклада, """") <> """"
		|	И НЕ ЕСТЬNULL(ТаблицаСкладов.Склад.ЭтоГруппа, ЛОЖЬ)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаСкладов";
	
	Запрос.Параметры.Вставить("УчетнаяЗапись",  ДанныеУчетнойЗаписи.УчетнаяЗапись);
	Запрос.Параметры.Вставить("ТаблицаСкладов", ТаблицаСкладов);
	Запрос.Параметры.Вставить("ТекущаяДата",    ТекущаяДатаСеанса());
	Запрос.Выполнить();
	
	РезультатЗапроса = ПолучитьРазницуОстатковТоваровДляВыгрузки(ДанныеУчетнойЗаписи.УчетнаяЗапись, МенеджерВТ);
	ВыборкаSKU       = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ВыборкаSKU.Количество() / 10000);

	skus   = Новый Массив;
	Порция = Новый ТаблицаЗначений;
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Порция.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения,, Колонка.Ширина);
	КонецЦикла;
	
	Пока ВыборкаSKU.Следующий() Цикл
		ВыборкаОстатковSKU = ВыборкаSKU.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаОстатковSKU.Следующий() Цикл
			Если skus.Количество() >= 2000 Тогда
				ВыгрузитьПорциюОстатковТоваровВСервис(ДанныеУчетнойЗаписи, skus, Порция, Отказ);
				
				skus.Очистить();
				Порция.Очистить();
				
				Если Отказ Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
			
			// Для записей в регистры
			ВыборкаСкладов = ВыборкаОстатковSKU.Выбрать();
			Пока ВыборкаСкладов.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(Порция.Добавить(), ВыборкаСкладов);
			КонецЦикла;
			
			// Для запроса HTTP
			items = Новый Массив;
			
			StockItemDTO           = Новый Структура("type, count, updatedAt");
			StockItemDTO.type      = "FIT";
			StockItemDTO.count     = ВыборкаОстатковSKU.count;
			StockItemDTO.updatedAt = ЗаписатьДатуJSON(ТекущаяДатаСеанса(), ФорматДатыJSON.ISO, ВариантЗаписиДатыJSON.УниверсальнаяДата);
			
			items.Добавить(StockItemDTO);
			
			StockDTO             = Новый Структура("sku, warehouseId, items");
			StockDTO.sku         = ВыборкаОстатковSKU.sku;
			StockDTO.warehouseId = ВыборкаОстатковSKU.warehouseId;
			StockDTO.items       = items;
			
			skus.Добавить(StockDTO);
		КонецЦикла;
	КонецЦикла;

	Если skus.Количество() > 0 Тогда
		ВыгрузитьПорциюОстатковТоваровВСервис(ДанныеУчетнойЗаписи, skus, Порция, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыгрузитьПорциюОстатковТоваровВСервис(ДанныеУчетнойЗаписи, skus, Порция, Отказ = Ложь)
	
	Если skus.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Попытка	  
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		ДанныеПриложения  = ДанныеПриложения();
		Сервер            = СерверПартнерскогоAPI();
		ИмяМетода         = "/offers/stocks.json";
		АдресРесурса      = "/" + ДанныеУчетнойЗаписи.ИдентификаторМагазина + ИмяМетода;
		
		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 180, СоединениеOpenSSL);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type",  "application/json");
		Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
			Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
		ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
			Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
		КонецЕсли;
		ДобавитьПодписьИнтеграцииВЗаголовки(Заголовки);
		
		ДанныеЗапроса = Новый Структура;
		ДанныеЗапроса.Вставить("skus", skus);
		ТелоЗапроса = ВJSON(ДанныеЗапроса);
		
		HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, "UTF-8");
		
		HTTPОтвет    = HTTPСоединение.Записать(HTTPЗапрос);  
		КодСостояния = HTTPОтвет.КодСостояния;
		СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
		Результат    = ИзJSON(СтрокаОтвета);
		
		Если КодСостояния = 200 И Результат.status = "OK" Тогда
			ОбновитьДанныеОбОстатках(Порция);
			
		Иначе
			Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
				ОписаниеОшибок = ТекстОшибки(Результат.errors);
			Иначе
				ОписаниеОшибок = ""; 
			КонецЕсли; 	
			
			Отказ = Истина;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка выполнения запроса %1: %2%3';
					|en = 'An error occurred when executing the %1 request: %2%3'", 
					ОбщегоНазначения.КодОсновногоЯзыка()), 
				Сервер + АдресРесурса, 
				ОписаниеОшибок,
				СтрокаОтвета);
				
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
		КонецЕсли;
		
	Исключение 
		Отказ = Истина;
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
	КонецПопытки;
				
КонецПроцедуры    

// Определяет наличие разницы в остатках товаров между количеством рассчитанным по данным информационной базы и количеством по данным торговой площадки.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//   МенеджерВТ    - МенеджерВременныхТаблиц - менеджер временных таблиц.
//
// Возвращаемое значение:
//   ТаблицаЗначений - данные об остатках товарных позиций:
//     * УчетнаяЗапись  - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//     * Номенклатура   - СправочникСсылка.Номенклатура - номенклатура товарной позиции;
//     * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика товарной позиции;
//     * Упаковка       - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка товарной позиции;
//     * sku            - Строка - идентификатор публикации;
//     * Склад          - СправочникСсылка.Склады - склад, для которого определяются остатки товарных позиций;
//     * warehouseId    - Строка - идентификатор объекта маркетплейса (склада);
//     * Остаток        - Число - свободный остаток товарной позиции на складе;
//     * count          - Число - остаток в указанной единице измерения.
//
Функция ПолучитьРазницуОстатковТоваровДляВыгрузки(УчетнаяЗапись, МенеджерВТ) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА Запасы.ВНаличииОстаток - Запасы.РезервироватьНаСкладеОстаток - Запасы.РезервироватьПоМереПоступленияОстаток < 0
		|				ТОГДА 0
		|			ИНАЧЕ Запасы.ВНаличииОстаток - Запасы.РезервироватьНаСкладеОстаток - Запасы.РезервироватьПоМереПоступленияОстаток
		|		КОНЕЦ, 0) КАК Остаток
		|ПОМЕСТИТЬ ВТОстатки
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(
		|			,
		|			(Номенклатура, Характеристика, Склад, Назначение) В
		|				(ВЫБРАТЬ
		|					Фильтр.Номенклатура КАК Номенклатура,
		|					Фильтр.Характеристика КАК Характеристика,
		|					Фильтр.Склад КАК Склад,
		|					ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение
		|				ИЗ
		|					ВтТовары КАК Фильтр)) КАК Запасы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Остатки.Номенклатура КАК Номенклатура,
		|	Остатки.Характеристика КАК Характеристика,
		|	СУММА(Остатки.Остаток) КАК Остаток
		|ПОМЕСТИТЬ ВТОстаткиИтого
		|ИЗ
		|	ВТОстатки КАК Остатки
		|
		|СГРУППИРОВАТЬ ПО
		|	Остатки.Номенклатура,
		|	Остатки.Характеристика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиТоваровМаркетплейсов.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗапись,
		|	ОстаткиТоваровМаркетплейсов.Номенклатура КАК Номенклатура,
		|	ОстаткиТоваровМаркетплейсов.Характеристика КАК Характеристика,
		|	СУММА(ОстаткиТоваровМаркетплейсов.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТОстаткиОтправленные
		|ИЗ
		|	РегистрСведений.ОстаткиТоваровМаркетплейсов КАК ОстаткиТоваровМаркетплейсов
		|ГДЕ
		|	(ОстаткиТоваровМаркетплейсов.УчетнаяЗаписьМаркетплейса, ОстаткиТоваровМаркетплейсов.Склад, ОстаткиТоваровМаркетплейсов.Номенклатура, ОстаткиТоваровМаркетплейсов.Характеристика) В
		|			(ВЫБРАТЬ
		|				Фильтр.УчетнаяЗапись КАК УчетнаяЗапись,
		|				Фильтр.Склад КАК Склад,
		|				Фильтр.Номенклатура КАК Номенклатура,
		|				Фильтр.Характеристика КАК Характеристика
		|			ИЗ
		|				ВтТовары КАК Фильтр)
		|
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиТоваровМаркетплейсов.УчетнаяЗаписьМаркетплейса,
		|	ОстаткиТоваровМаркетплейсов.Номенклатура,
		|	ОстаткиТоваровМаркетплейсов.Характеристика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	УчетнаяЗапись,
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.УчетнаяЗапись КАК УчетнаяЗапись,
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Упаковка КАК Упаковка,
		|	Товары.ИдентификаторПредложения КАК sku,
		|	Товары.Склад КАК Склад,
		|	Товары.ИдентификаторОбъектаМаркетплейса КАК warehouseId,
		|	ЕСТЬNULL(Остатки.Остаток, 0) КАК Остаток,
		|	ВЫРАЗИТЬ(ЕСТЬNULL(Остатки.Остаток, 0) / ВЫБОР
		|			КОГДА Товары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|				ТОГДА 1
		|			ИНАЧЕ ВЫБОР
		|					КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 0
		|						ТОГДА 1
		|					ИНАЧЕ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)
		|				КОНЕЦ
		|		КОНЕЦ КАК ЧИСЛО(15, 0)) КАК count
		|ИЗ
		|	ВтТовары КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстатки КАК Остатки
		|		ПО Товары.Номенклатура = Остатки.Номенклатура
		|			И Товары.Характеристика = Остатки.Характеристика
		|			И Товары.Склад = Остатки.Склад
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиИтого КАК ОстаткиИтого
		|		ПО Товары.Номенклатура = ОстаткиИтого.Номенклатура
		|			И Товары.Характеристика = ОстаткиИтого.Характеристика
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиОтправленные КАК ОстаткиОтправленные
		|		ПО Товары.УчетнаяЗапись = ОстаткиОтправленные.УчетнаяЗапись
		|			И Товары.Номенклатура = ОстаткиОтправленные.Номенклатура
		|			И Товары.Характеристика = ОстаткиОтправленные.Характеристика
		|ГДЕ
		|	(ЕСТЬNULL(ОстаткиИтого.Остаток, 0) <> ЕСТЬNULL(ОстаткиОтправленные.Количество, 0)
		|			ИЛИ ОстаткиОтправленные.Количество ЕСТЬ NULL)
		|ИТОГИ
		|	СУММА(count)
		|ПО
		|	sku,
		|	warehouseId
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтТовары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОстаткиИтого
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОстаткиОтправленные";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"Товары.Упаковка",
			"Товары.Номенклатура"));
	
	Запрос.Параметры.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	Запрос.Параметры.Вставить("ТекущаяДата",   ТекущаяДатаСеанса());
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
	
КонецФункции

// Записывает актуальные данные об остатках товаров в регистры сведений ОстаткиТоваровМаркетплейсов и СтатусыПубликацииТоваровЯндексМаркет.
//
// Параметры:
//   ОстаткиТоваров - см. ПолучитьРазницуОстатковТоваровДляВыгрузки.
//
Процедура ОбновитьДанныеОбОстатках(ОстаткиТоваров)  
	
	Для Каждого СтрокаТаблицыЗначений Из ОстаткиТоваров Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаблицыЗначений.Склад) Тогда
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ОстаткиТоваровМаркетплейсов");
			ЭлементБлокировкиДанных.УстановитьЗначение("УчетнаяЗаписьМаркетплейса", СтрокаТаблицыЗначений.УчетнаяЗапись);
			ЭлементБлокировкиДанных.УстановитьЗначение("Склад",                     СтрокаТаблицыЗначений.Склад);
			ЭлементБлокировкиДанных.УстановитьЗначение("Номенклатура",              СтрокаТаблицыЗначений.Номенклатура);
			ЭлементБлокировкиДанных.УстановитьЗначение("Характеристика",            СтрокаТаблицыЗначений.Характеристика);
			БлокировкаДанных.Заблокировать();

			НаборЗаписей = РегистрыСведений.ОстаткиТоваровМаркетплейсов.СоздатьНаборЗаписей(); 
			НаборЗаписей.Отбор.УчетнаяЗаписьМаркетплейса.Установить(СтрокаТаблицыЗначений.УчетнаяЗапись);
			НаборЗаписей.Отбор.Склад.Установить(СтрокаТаблицыЗначений.Склад);
			НаборЗаписей.Отбор.Номенклатура.Установить(СтрокаТаблицыЗначений.Номенклатура);
			НаборЗаписей.Отбор.Характеристика.Установить(СтрокаТаблицыЗначений.Характеристика);
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() > 0 Тогда
				Запись                      = НаборЗаписей[0];
				Запись.Количество           = СтрокаТаблицыЗначений.Остаток; 
				Запись.ДатаВыгрузкиОстатков = ТекущаяДатаСеанса();
				
			Иначе
				Запись                           = НаборЗаписей.Добавить();  
				Запись.УчетнаяЗаписьМаркетплейса = СтрокаТаблицыЗначений.УчетнаяЗапись; 
				Запись.Склад                     = СтрокаТаблицыЗначений.Склад;
				Запись.Номенклатура              = СтрокаТаблицыЗначений.Номенклатура;  
				Запись.Характеристика            = СтрокаТаблицыЗначений.Характеристика;
				Запись.Количество                = СтрокаТаблицыЗначений.Остаток;
				Запись.ДатаВыгрузкиОстатков      = ТекущаяДатаСеанса();
			КонецЕсли;
			
			НаборЗаписей.Записать();
			ЗафиксироватьТранзакцию();
							
		Исключение
			ОтменитьТранзакцию();
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
	
	ОстаткиТоваров.Свернуть("УчетнаяЗапись, Номенклатура, Характеристика, Упаковка", "count");
	
	Для Каждого СтрокаТаблицыЗначений Из ОстаткиТоваров Цикл	
		НачатьТранзакцию();
		
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет");
			ЭлементБлокировкиДанных.УстановитьЗначение("УчетнаяЗапись",  СтрокаТаблицыЗначений.УчетнаяЗапись);
			ЭлементБлокировкиДанных.УстановитьЗначение("Номенклатура",   СтрокаТаблицыЗначений.Номенклатура);
			ЭлементБлокировкиДанных.УстановитьЗначение("Характеристика", СтрокаТаблицыЗначений.Характеристика);
			ЭлементБлокировкиДанных.УстановитьЗначение("Упаковка",       СтрокаТаблицыЗначений.Упаковка);
			БлокировкаДанных.Заблокировать();

			НаборЗаписей = РегистрыСведений.СтатусыПубликацииТоваровЯндексМаркет.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.УчетнаяЗапись.Установить(СтрокаТаблицыЗначений.УчетнаяЗапись);
			НаборЗаписей.Отбор.Номенклатура.Установить(СтрокаТаблицыЗначений.Номенклатура);
			НаборЗаписей.Отбор.Характеристика.Установить(СтрокаТаблицыЗначений.Характеристика);
			НаборЗаписей.Отбор.Упаковка.Установить(СтрокаТаблицыЗначений.Упаковка);
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() > 0 Тогда
				Запись                   = НаборЗаписей[0];
				Запись.ОстатокВУпаковках = СтрокаТаблицыЗначений.count;
				
				НаборЗаписей.Записать();
			КонецЕсли; 

			ЗафиксироватьТранзакцию();
				
		Исключение
			ОтменитьТранзакцию();
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеЦенамиНаТоварыСлужебный

// Устарел. Возвращает цены для продвижения товаров, которые вы размещаете на Маркете.
// Описание метода см. https://yandex.ru/dev/market/partner-api/doc/ru/reference/prices/getSuggestedPrices.
//
// Параметры:
//   ДанныеУчетнойЗаписи           - Структура - данные учетной записи (см. ДанныеУчетнойЗаписиЯндексМаркет);
//   ИдентификаторыТоваровПлощадки - СписокЗначений Из Строка - список идентификаторов товарных позиций;
//   ВидыЦен                       - Массив Из СправочникСсылка.ВидыЦен - рекомендованные виды цен, (см. ПолучитьМассивРекомендованныхЦен);
//   Отказ                         - Булево - выходной параметр, определяет наличие ошибки при выполнении запросов к сервису.
//
Процедура ПолучитьРекомендованныеЦеныИзСервиса(ДанныеУчетнойЗаписи, ИдентификаторыТоваровПлощадки, ВидыЦен, Отказ = Ложь)   
	
	Если ИдентификаторыТоваровПлощадки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	offers = Новый Массив;
	
	Для Каждого ЭлементКоллекции Из ИдентификаторыТоваровПлощадки Цикл
		SuggestOfferPriceDTO = Новый Структура("marketSku");
		SuggestOfferPriceDTO.Вставить("marketSku", ЭлементКоллекции.Значение);
		
		offers.Добавить(SuggestOfferPriceDTO);
	КонецЦикла;

	Если offers.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Попытка	  
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		ДанныеПриложения  = ДанныеПриложения();
		Сервер            = СерверПартнерскогоAPI();
		ИмяМетода         = "/offer-prices/suggestions.json";
		АдресРесурса      = "/" + ДанныеУчетнойЗаписи.ИдентификаторМагазина + ИмяМетода;
		
		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 180, СоединениеOpenSSL);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type",  "application/json");
		Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
			Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
		ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
			Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
		КонецЕсли;
		ДобавитьПодписьИнтеграцииВЗаголовки(Заголовки);
		
		ДанныеЗапроса = Новый Структура;
		ДанныеЗапроса.Вставить("offers", offers);
		ТелоЗапроса = ВJSON(ДанныеЗапроса);
		
		HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, "UTF-8");
		
		HTTPОтвет    = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
		КодСостояния = HTTPОтвет.КодСостояния;
		СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
		Результат    = ИзJSON(СтрокаОтвета);
		
		Если КодСостояния = 200 И Результат.status = "OK" Тогда
			ОбновитьРекомендованныеЦены(ДанныеУчетнойЗаписи.УчетнаяЗапись, Результат.result.offers, ВидыЦен);
			
		Иначе
			Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
				ОписаниеОшибок = ТекстОшибки(Результат.errors);
			Иначе
				ОписаниеОшибок = ""; 
			КонецЕсли; 	
			
			Отказ = Истина;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка выполнения запроса %1: %2%3';
					|en = 'An error occurred when executing the %1 request: %2%3'", 
					ОбщегоНазначения.КодОсновногоЯзыка()), 
				Сервер + АдресРесурса, 
				ОписаниеОшибок,
				СтрокаОтвета);
				
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
		КонецЕсли;
		
	Исключение 
		Отказ = Истина;
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

// Возвращает цену продажи на товары.
//
// Параметры:
//   ДанныеУчетнойЗаписи - Структура - данные учетной записи (см. ДанныеУчетнойЗаписиЯндексМаркет);
//   ТаблицаТоваров      - Неопределено - без дополнительных ограничений по списку товаров;
//                       - ТаблицаЗначений - список товаров ограничения выборки:
//     * УчетнаяЗапись     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//     * Номенклатура      - СправочникСсылка.Номенклатура - номенклатура;
//     * Характеристика    - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика;
//     * Упаковка          - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка.
//
// Возвращаемое значение:
//   Результат запроса - цены продажи по таблице товаров.
//
Функция ПолучитьЦены(ДанныеУчетнойЗаписи, ТаблицаТоваров = Неопределено, ВыгрузкаБазовойЦены = Ложь)  
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ИнтеграцияСЯндексМаркетСервер.УстановитьЦеныВСервисе");

	ТекстЗапроса = "";
	
	Если ТаблицаТоваров <> Неопределено Тогда
		ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ВЫРАЗИТЬ(ТаблицаТоваров.УчетнаяЗапись КАК Справочник.УчетныеЗаписиМаркетплейсов) КАК УчетнаяЗапись,
			|	ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
			|	ВЫРАЗИТЬ(ТаблицаТоваров.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
			|	ВЫРАЗИТЬ(ТаблицаТоваров.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения) КАК Упаковка
			|ПОМЕСТИТЬ ВТ_Товары
			|ИЗ
			|	&ТаблицаТоваров КАК ТаблицаТоваров
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	УчетнаяЗапись,
			|	Номенклатура,
			|	Характеристика,
			|	Упаковка"
			+ ОбщегоНазначения.РазделительПакетаЗапросов();
	КонецЕсли;
		
	ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ОтносительныеКурсыВСрезПоследних.Валюта КАК Валюта,
		|	ОтносительныеКурсыВСрезПоследних.КурсЧислитель КАК КурсЧислитель,
		|	ОтносительныеКурсыВСрезПоследних.КурсЗнаменатель КАК КурсЗнаменатель
		|ПОМЕСТИТЬ КурсыВалют
		|ИЗ
		|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(, БазоваяВалюта = &БазоваяВалюта) КАК ОтносительныеКурсыВСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись КАК УчетнаяЗапись,
		|	СтатусыПубликацииТоваровЯндексМаркет.Номенклатура КАК Номенклатура,
		|	СтатусыПубликацииТоваровЯндексМаркет.Характеристика КАК Характеристика,
		|	СтатусыПубликацииТоваровЯндексМаркет.Упаковка КАК Упаковка,
		|	&УпаковкаТовара КАК УпаковкаТовара,
		|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторПредложения КАК ИдентификаторПредложения,
		|	СтатусыПубликацииТоваровЯндексМаркет.ЦенаПродажи КАК ЦенаПродажи,
		|	СтатусыПубликацииТоваровЯндексМаркет.ДатаУстановкиЦены КАК ДатаУстановкиЦены,
		|	&ВидЦен КАК ВидЦены,
		|	&ПоляДляЦенообразованияВыборка КАК ПоляДляЦенообразованияВыборка
		|ПОМЕСТИТЬ ТаблицаТоварныхПозиций
		|ИЗ
		|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправНоменклатура
		|		ПО СтатусыПубликацииТоваровЯндексМаркет.Номенклатура = СправНоменклатура.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО (СправНоменклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка)
		|ГДЕ
		|	СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись
		|	И &ТекстУсловияПоТоварам
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	ПоляДляЦенообразованияВыборка,
		|	ВидЦены
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
		|	&ПоляДляЦенообразованияСрез КАК ПоляДляЦенообразованияСрез,
		|	ЦеныНоменклатуры.Цена КАК Цена,
		|	ЦеныНоменклатуры.Упаковка КАК Упаковка,
		|	ЦеныНоменклатуры.Валюта КАК Валюта,
		|	ЦеныНоменклатуры.Период КАК Период
		|ПОМЕСТИТЬ ЦеныНоменклатуры
		|ИЗ
		|	ИсточникЦенНоменклатуры КАК ЦеныНоменклатуры
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	ПоляДляЦенообразованияСрез
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ 
		|	ТаблицаТоварныхПозиций.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ТаблицаТоварныхПозиций.Номенклатура КАК Номенклатура,
		|	ТаблицаТоварныхПозиций.Характеристика КАК Характеристика,
		|	ТаблицаТоварныхПозиций.УпаковкаТовара КАК Упаковка,
		|	ТаблицаТоварныхПозиций.ИдентификаторПредложения КАК ИдентификаторПредложения,
		|	ЦеныНоменклатуры.Период КАК Период,
		|	&ЦенаПродажи КАК Цена
		|ИЗ
		|	ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправНоменклатура
		|		ПО ЦеныНоменклатуры.Номенклатура = СправНоменклатура.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
		|		ПО ЦеныНоменклатуры.Валюта = КурсыВалют.Валюта
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУчета
		|		ПО (КурсыВалютУчета.Валюта = &БазоваяВалюта)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоварныхПозиций КАК ТаблицаТоварныхПозиций
		|		ПО (&УсловиеСоединенияЦеныНоменклатурыСрезПоследних)
		|ГДЕ
		|	(&ЦенаПродажи <> ТаблицаТоварныхПозиций.ЦенаПродажи
		|			ИЛИ РАЗНОСТЬДАТ(ТаблицаТоварныхПозиций.ДатаУстановкиЦены, &ДатаЦен, ДЕНЬ) > 30)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КурсыВалют
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаТоварныхПозиций
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЦеныНоменклатуры";

	ТекущаяДата                   = ТекущаяДатаСеанса();
	ИспользуетсяЦенообразование25 = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25(ТекущаяДата);
	Если ВыгрузкаБазовойЦены Тогда 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
		"	(&ЦенаПродажи <> ТаблицаТоварныхПозиций.ЦенаПродажи
		|			ИЛИ РАЗНОСТЬДАТ(ТаблицаТоварныхПозиций.ДатаУстановкиЦены, &ДатаЦен, ДЕНЬ) > 30)",
		"	ИСТИНА");
	КонецЕсли;
	
	НастройкаЦенообразования                 = ЦенообразованиеКлиентСервер.НастройкиДляВременнойТаблицыСДополнениемДляЦенообразования();
	НастройкаЦенообразования.ИсточникТоваров = "СтатусыПубликацииТоваровЯндексМаркет";
	НастройкаЦенообразования.ПолеСерия       = "";

	ТекстЗамены = ЦенообразованиеКлиентСервер.ТекстПолейДляЦенообразования(НастройкаЦенообразования);
	Если ЗначениеЗаполнено(ТекстЗамены) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияВыборка КАК ПоляДляЦенообразованияВыборка", ТекстЗамены);
		ПоляДляЦенообразования =
			"ХарактеристикаЦО,
			|	СерияЦО,
			|	УпаковкаЦО";
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияВыборка КАК ПоляДляЦенообразованияВыборка", "ИСТИНА");
		ПоляДляЦенообразования =
			"Характеристика";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПоляДляЦенообразованияВыборка", ПоляДляЦенообразования);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"СтатусыПубликацииТоваровЯндексМаркет.Упаковка",
		"ВЫБОР
		|		КОГДА СтатусыПубликацииТоваровЯндексМаркет.Упаковка = СправНоменклатура.ЕдиницаИзмерения
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|		ИНАЧЕ СтатусыПубликацииТоваровЯндексМаркет.Упаковка
		|	КОНЕЦ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УпаковкаТовара", "СтатусыПубликацииТоваровЯндексМаркет.Упаковка");

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"ИсточникЦенНоменклатуры",
		ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатуры("ТаблицаТоварныхПозиций",
			"&ДатаЦен",
			Новый Структура("ВТаблице", "ВидЦены"),
			ИспользуетсяЦенообразование25));

	Если ИспользуетсяЦенообразование25 Тогда
		ПоляДляЦенообразованияСрез = 
			"ЦеныНоменклатуры.ХарактеристикаЦО КАК ХарактеристикаЦО,
			|	ЦеныНоменклатуры.СерияЦО КАК СерияЦО,
			|	ЦеныНоменклатуры.УпаковкаЦО КАК УпаковкаЦО";
	Иначе
		ПоляДляЦенообразованияСрез = 
			"ЦеныНоменклатуры.Характеристика КАК Характеристика";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияСрез КАК ПоляДляЦенообразованияСрез", ПоляДляЦенообразованияСрез);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПоляДляЦенообразованияСрез", ПоляДляЦенообразования);

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&УсловиеСоединенияЦеныНоменклатурыСрезПоследних",
		ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатурыУсловиеСоединения(
			"ТаблицаТоварныхПозиций",
			"ЦеныНоменклатуры",,
			ИспользуетсяЦенообразование25));

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ЦенаПродажи",
		"ВЫРАЗИТЬ(ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) 
		|			* ВЫБОР
		|				КОГДА ТаблицаТоварныхПозиций.Упаковка <> ЦеныНоменклатуры.Упаковка
		|					ТОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) 
		|							/ ВЫБОР
		|								КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) = 0
		|									ТОГДА 1
		|								ИНАЧЕ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1)
		|							КОНЕЦ
		|				ИНАЧЕ 1
		|			КОНЕЦ 
		|			* ВЫБОР
		|				КОГДА ЦеныНоменклатуры.Валюта = &БазоваяВалюта
		|					ТОГДА 1
		|				ИНАЧЕ ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютУчета.КурсЗнаменатель, 1) / (ЕСТЬNULL(КурсыВалютУчета.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1))
		|			КОНЕЦ КАК ЧИСЛО(31, 2))");

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"(ВЫБОР
			|	КОГДА ТаблицаТоварныхПозиций.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|		ТОГДА СправНоменклатура.ЕдиницаИзмерения
			|	ИНАЧЕ ТаблицаТоварныхПозиций.Упаковка
			|КОНЕЦ)",
			"ТаблицаТоварныхПозиций.Номенклатура"));

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"(ВЫБОР
			|	КОГДА ЦеныНоменклатуры.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|		ТОГДА СправНоменклатура.ЕдиницаИзмерения
			|	ИНАЧЕ ЦеныНоменклатуры.Упаковка
			|КОНЕЦ)",
			"ЦеныНоменклатуры.Номенклатура"));

	Если ТаблицаТоваров = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловияПоТоварам", "ИСТИНА");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловияПоТоварам", 
			"	(СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись, СтатусыПубликацииТоваровЯндексМаркет.Номенклатура, СтатусыПубликацииТоваровЯндексМаркет.Характеристика, СтатусыПубликацииТоваровЯндексМаркет.Упаковка) В
			|			(ВЫБРАТЬ
			|				ВТ_Товары.УчетнаяЗапись,
			|				ВТ_Товары.Номенклатура,
			|				ВТ_Товары.Характеристика,
			|				ВТ_Товары.Упаковка
			|			ИЗ
			|				ВТ_Товары КАК ВТ_Товары)");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.Параметры.Вставить("УчетнаяЗапись",  ДанныеУчетнойЗаписи.УчетнаяЗапись);
	Запрос.Параметры.Вставить("ВидЦен",         ДанныеУчетнойЗаписи.ЦенаПродажи);
	Запрос.УстановитьПараметр("ДатаЦен",        ТекущаяДата);
	Запрос.УстановитьПараметр("БазоваяВалюта",  ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию());
	Запрос.Параметры.Вставить("ТаблицаТоваров", ТаблицаТоваров);
	
	ЦеныТоваров = Запрос.Выполнить();
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ЦеныТоваров.Выгрузить().Количество() / 1000);
	
	Возврат ЦеныТоваров;
	
КонецФункции

// Устанавливает цены на товары в магазине.
// Описание метода см. https://yandex.ru/dev/market/partner-api/doc/ru/api/prices/updateBusinessPrices.
//
// Параметры:
//   ДанныеУчетнойЗаписи - Структура - данные учетной записи (см. ДанныеУчетнойЗаписиЯндексМаркет);
//   ТаблицаТоваров      - Неопределено - без дополнительных ограничений по списку товаров;
//                       - ТаблицаЗначений - список товаров ограничения выборки:
//     * УчетнаяЗапись     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//     * Номенклатура      - СправочникСсылка.Номенклатура - номенклатура;
//     * Характеристика    - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика;
//     * Упаковка          - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка.
//   Отказ               - Булево - выходной параметр, определяет наличие ошибки при выполнении запросов к сервису.
//
Процедура УстановитьЦеныВСервисе(ДанныеУчетнойЗаписи, ТаблицаТоваров = Неопределено, Отказ = Ложь)
	
	ЦеныТоваров = ПолучитьЦены(ДанныеУчетнойЗаписи, ТаблицаТоваров); 
	ЦеныТоваров = ЦеныТоваров.Выбрать();
	
	Если ЦеныТоваров.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Порция = Новый Массив;
	offers = Новый Массив;
	
	Пока ЦеныТоваров.Следующий() Цикл
		Если ЦеныТоваров.Цена <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если offers.Количество() >= 500 Тогда
			УстановитьПорциюЦенВСервисе(ДанныеУчетнойЗаписи, offers, Порция, Отказ);
			
			Порция = Новый Массив;
			offers = Новый Массив;
				
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
		КонецЕсли;
	
		// Для записей в регистры
		Позиция = Новый Структура;
		Позиция.Вставить("УчетнаяЗапись",	ЦеныТоваров.УчетнаяЗапись);
		Позиция.Вставить("Номенклатура",	ЦеныТоваров.Номенклатура);
		Позиция.Вставить("Характеристика",	ЦеныТоваров.Характеристика);
		Позиция.Вставить("Упаковка",		ЦеныТоваров.Упаковка);
		Позиция.Вставить("Цена",			ЦеныТоваров.Цена);
		Позиция.Вставить("Период",			ЦеныТоваров.Период);
		
        Порция.Добавить(Позиция);
		
		// Для запроса HTTP
		UpdatePriceWithDiscountDTO            = Новый Структура("currencyId, value");
		UpdatePriceWithDiscountDTO.currencyId = "RUR";
		UpdatePriceWithDiscountDTO.value      = Формат(ЦеныТоваров.Цена, "ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
		
		UpdateBusinessOfferPriceDTO         = Новый Структура("offerId, price");
		UpdateBusinessOfferPriceDTO.offerId = ЦеныТоваров.ИдентификаторПредложения;
		UpdateBusinessOfferPriceDTO.price   = UpdatePriceWithDiscountDTO;
		
		offers.Добавить(UpdateBusinessOfferPriceDTO);
	КонецЦикла;
	
	Если offers.Количество() > 0 Тогда
		УстановитьПорциюЦенВСервисе(ДанныеУчетнойЗаписи, offers, Порция, Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПорциюЦенВСервисе(ДанныеУчетнойЗаписи, offers, Порция, Отказ = Ложь)
	
	Если offers.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Попытка
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		ДанныеПриложения  = ДанныеПриложения();
		Сервер            = СерверПартнерскогоAPI(Ложь);
		ИмяМетода         = "/offer-prices/updates.json";
		АдресРесурса      = "/" + ДанныеУчетнойЗаписи.ИдентификаторКабинета + ИмяМетода;
		
		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 180, СоединениеOpenSSL);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type",  "application/json");
		Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
			Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
		ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
			Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
		КонецЕсли;
		ДобавитьПодписьИнтеграцииВЗаголовки(Заголовки);
		
		ДанныеЗапроса = Новый Структура;
		ДанныеЗапроса.Вставить("offers", offers);
		ТелоЗапроса = ВJSON(ДанныеЗапроса);
		
		HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, "UTF-8");
		
		HTTPОтвет    = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
		КодСостояния = HTTPОтвет.КодСостояния;
		СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
		Результат    = ИзJSON(СтрокаОтвета);
		
		Если КодСостояния = 200 И Результат.status = "OK" Тогда
			ОбновитьДатуУстановкиЦены(Порция);
			
		Иначе
			Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
				ОписаниеОшибок = ТекстОшибки(Результат.errors);
			Иначе
				ОписаниеОшибок = ""; 
			КонецЕсли;
			
			Отказ = Истина;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка выполнения запроса %1: %2%3';
					|en = 'An error occurred when executing the %1 request: %2%3'", 
					ОбщегоНазначения.КодОсновногоЯзыка()), 
				Сервер + АдресРесурса, 
				ОписаниеОшибок,
				СтрокаОтвета);
				
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
		КонецЕсли;
		
	Исключение 
		Отказ = Истина;
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

// Определяет следующую порцию идентификаторов товарных позиций.
//
// Параметры:
//   УчетнаяЗапись              - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//   ВидыЦен                    - Массив Из СправочникСсылка.ВидыЦен - рекомендованные виды цен (см. ПолучитьМассивРекомендованныхЦен);
//   ИдентификаторыОтработанные - СписокЗначений Из Строка - список отработанных идентификаторов товарных позиций;
//   ТаблицаТоваров             - Неопределено - без дополнительных ограничений по списку товаров;
//                              - ТаблицаЗначений - список товаров ограничения выборки:
//     * УчетнаяЗапись            - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//     * Номенклатура             - СправочникСсылка.Номенклатура - номенклатура;
//     * Характеристика           - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика;
//     * Упаковка                 - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка.
//
// Возвращаемое значение:
//   СписокЗначений Из Строка - список идентификаторов товарных позиций.
//
Функция ПолучитьИдентификаторыТоваровПлощадки(УчетнаяЗапись, ВидыЦен, ИдентификаторыОтработанные, ТаблицаТоваров = Неопределено) 
	
	Результат = Новый СписокЗначений;
	
	Если ВидыЦен = Неопределено 
			Или ВидыЦен.Количество() = 0 Тогда
		ВидыЦен = ПолучитьМассивРекомендованныхЦен();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "";
	
	Если ТаблицаТоваров <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст +
			"ВЫБРАТЬ
			|	ВЫРАЗИТЬ(ТаблицаТоваров.УчетнаяЗапись КАК Справочник.УчетныеЗаписиМаркетплейсов) КАК УчетнаяЗапись,
			|	ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
			|	ВЫРАЗИТЬ(ТаблицаТоваров.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
			|	ВЫРАЗИТЬ(ТаблицаТоваров.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения) КАК Упаковка
			|ПОМЕСТИТЬ ВТ_Товары
			|ИЗ
			|	&ТаблицаТоваров КАК ТаблицаТоваров
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	УчетнаяЗапись,
			|	Номенклатура,
			|	Характеристика,
			|	Упаковка"
			+ ОбщегоНазначения.РазделительПакетаЗапросов();
	КонецЕсли;
	
	ИспользуетсяЦенообразование25 = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25(); 
	Если ИспользуетсяЦенообразование25 Тогда
		Запрос.Текст = Запрос.Текст +
			"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
			|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторТовараПлощадки КАК ИдентификаторТовараПлощадки
			|ИЗ
			|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25.СрезПоследних КАК ЦеныНоменклатуры25СрезПоследних
			|		ПО СтатусыПубликацииТоваровЯндексМаркет.Номенклатура = ЦеныНоменклатуры25СрезПоследних.Номенклатура
			|ГДЕ
			|	СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись
			|	И СтатусыПубликацииТоваровЯндексМаркет.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияПройдена)
			|	И НЕ СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторТовараПлощадки В (&ИдентификаторыОтработанные)
			|	И (ЦеныНоменклатуры25СрезПоследних.Номенклатура ЕСТЬ NULL
			|			ИЛИ ЦеныНоменклатуры25СрезПоследних.ВидЦены В (&ЗагружаемыеВидыЦен)
			|				И ЦеныНоменклатуры25СрезПоследних.Период < &ТекущаяДата
			|				И НЕ ЦеныНоменклатуры25СрезПоследних.ВидЦены.ПометкаУдаления)
			|	И &ТекстУсловияПоТоварам";
		
	Иначе 
		Запрос.Текст = Запрос.Текст +
			"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
			|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторТовараПлощадки КАК ИдентификаторТовараПлощадки
			|ИЗ
			|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних КАК ЦеныНоменклатурыСрезПоследних
			|		ПО СтатусыПубликацииТоваровЯндексМаркет.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
			|ГДЕ
			|	СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись
			|	И СтатусыПубликацииТоваровЯндексМаркет.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыВыгрузкиТоваровЯндексМаркет.МодерацияПройдена)
			|	И НЕ СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторТовараПлощадки В (&ИдентификаторыОтработанные)
			|	И (ЦеныНоменклатурыСрезПоследних.Номенклатура ЕСТЬ NULL
			|			ИЛИ ЦеныНоменклатурыСрезПоследних.ВидЦены В (&ЗагружаемыеВидыЦен)
			|				И ЦеныНоменклатурыСрезПоследних.Период < &ТекущаяДата
			|				И НЕ ЦеныНоменклатурыСрезПоследних.ВидЦены.ПометкаУдаления)
			|	И &ТекстУсловияПоТоварам";
	КонецЕсли; 
	
	Если ТаблицаТоваров = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстУсловияПоТоварам", "ИСТИНА");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстУсловияПоТоварам", 
			"	(СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись, СтатусыПубликацииТоваровЯндексМаркет.Номенклатура, СтатусыПубликацииТоваровЯндексМаркет.Характеристика, СтатусыПубликацииТоваровЯндексМаркет.Упаковка) В
			|			(ВЫБРАТЬ
			|				ВТ_Товары.УчетнаяЗапись,
			|				ВТ_Товары.Номенклатура,
			|				ВТ_Товары.Характеристика,
			|				ВТ_Товары.Упаковка
			|			ИЗ
			|				ВТ_Товары КАК ВТ_Товары)");
	КонецЕсли;
	
	Запрос.Параметры.Вставить("УчетнаяЗапись",              УчетнаяЗапись);
	Запрос.Параметры.Вставить("ТекущаяДата",                НачалоДня(ТекущаяДатаСеанса()));
	Запрос.Параметры.Вставить("ЗагружаемыеВидыЦен",         ВидыЦен);
	Запрос.Параметры.Вставить("ИдентификаторыОтработанные", ИдентификаторыОтработанные);
	Запрос.Параметры.Вставить("ТаблицаТоваров",             ТаблицаТоваров);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Результат.ЗагрузитьЗначения(РезультатЗапроса.ВыгрузитьКолонку("ИдентификаторТовараПлощадки"));
	
	Возврат Результат; 
	
КонецФункции

// Обновляет цены на товары в информационной базе.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   Рекомендации  - Массив Из Структура - список товаров с ценами для продвижения (PriceSuggestOfferDTO):
//     * marketSku       - Число - SKU на Маркете;
//     * offerId         - Строка - идентификатор предложения из прайс-листа;
//     * priceSuggestion - Массив Из Структура - цены для продвижения (PriceSuggestDTO):
//       ** type           - Строка - тип цены;
//       ** price          - Число - цена в рублях.
//   ВидыЦен       - Массив Из СправочникСсылка.ВидыЦен - рекомендованные виды цен (см. ПолучитьМассивРекомендованныхЦен).
//
Процедура ОбновитьРекомендованныеЦены(УчетнаяЗапись, Рекомендации, Знач ВидыЦен)

	ИспользуетсяЦенообразование25                            = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25();
	ИспользоватьХарактеристикиНоменклатуры                   = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	ИспользоватьХарактеристикиНоменклатурыДляЦенообразования = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатурыДляЦенообразования");
	ИспользоватьУпаковкиНоменклатуры                         = ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");
	
	СтруктураУстановкиЦен = Новый Структура;
	СтруктураУстановкиЦен.Вставить("Дата",                          ТекущаяДатаСеанса());
	СтруктураУстановкиЦен.Вставить("ИспользуетсяЦенообразование25", ИспользуетсяЦенообразование25);
	СтруктураУстановкиЦен.Вставить("КодФормы",                      "");
	СтруктураУстановкиЦен.Вставить("Модифицированность",            "");
	
	ВидыВсехРекомендованныхЦен = ПолучитьМассивРекомендованныхЦен();
	Если ВидыЦен = Неопределено
			Или ВидыЦен.Количество() = 0 Тогда
		ВидыРекомендованныхЦен = ВидыВсехРекомендованныхЦен; 
	Иначе
		ВидыРекомендованныхЦен = ОбщегоНазначения.СкопироватьРекурсивно(ВидыЦен);
	КонецЕсли;

	ВидыЦен = Новый Массив;
	Для Каждого ЭлементКоллекции Из ВидыРекомендованныхЦен Цикл
		ВыбранныеЦены = Новый Структура();
		ВыбранныеЦены.Вставить("Выбрана", Истина);
		ВыбранныеЦены.Вставить("Влияет",  Ложь);
		ВыбранныеЦены.Вставить("Ссылка",  ЭлементКоллекции);
		ВыбранныеЦены.Вставить("ВидЦены", ЭлементКоллекции);
		ВидыЦен.Добавить(ВыбранныеЦены);
	КонецЦикла;
	
	СтруктураУстановкиЦен.Вставить("ВыбранныеЦены", ВидыЦен);
	
	ТаблицаЦен = Новый ТаблицаЗначений();
	ТаблицаЦен.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Если ИспользуетсяЦенообразование25 Тогда
		ТаблицаЦен.Колонки.Добавить("ХарактеристикаЦО", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатурыДляЦенообразования"));
		ТаблицаЦен.Колонки.Добавить("УпаковкаЦО",       Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
	Иначе
		ТаблицаЦен.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ТаблицаЦен.Колонки.Добавить("Упаковка",       Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
	КонецЕсли;
	ТаблицаЦен.Колонки.Добавить("ВидЦены",             Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
	ТаблицаЦен.Колонки.Добавить("Цена",                ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля(ДопустимыйЗнак.Неотрицательный));
	ТаблицаЦен.Колонки.Добавить("ЦенаИзмененаВручную", Новый ОписаниеТипов("Булево"));
	ТаблицаЦен.Колонки.Добавить("Валюта",              Новый ОписаниеТипов("СправочникСсылка.Валюты")); 
	
	Для Каждого СтруктураПредложения Из Рекомендации Цикл
		Если СтруктураПредложения.Свойство("marketSku")
			 И СтруктураПредложения.Свойство("priceSuggestion") Тогда
			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	СтатусыПубликацииТоваровЯндексМаркет.Номенклатура КАК Номенклатура,
				|	СтатусыПубликацииТоваровЯндексМаркет.Характеристика КАК Характеристика,
				|	СтатусыПубликацииТоваровЯндексМаркет.Упаковка КАК Упаковка,
				|	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторТовараПлощадки КАК ИдентификаторТовараПлощадки,
				|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьХарактеристики, ЛОЖЬ) КАК ИспользоватьХарактеристики,
				|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьУпаковки, ЛОЖЬ) КАК ИспользоватьУпаковки
				|ИЗ
				|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
				|		ПО СтатусыПубликацииТоваровЯндексМаркет.Номенклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка
				|ГДЕ
			    |	СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторТовараПлощадки = &ИдентификаторТовараПлощадки";  
			
			Запрос.УстановитьПараметр(
				"ИдентификаторТовараПлощадки", ФорматированныйИдентификаторЗаказа(СтруктураПредложения.marketSku));
			
			ВыборкаДанных = Запрос.Выполнить().Выбрать();
			
			Пока ВыборкаДанных.Следующий() Цикл
				Для Каждого ЭлементМассива Из СтруктураПредложения.priceSuggestion Цикл 
					ВидыЦенПоИмениПоля = ВидыЦенПоИмениПоля(ЭлементМассива.type, ВидыВсехРекомендованныхЦен);
					
					Для Каждого ВидЦены Из ВидыЦенПоИмениПоля Цикл
						Если ВидыРекомендованныхЦен.Найти(ВидЦены) <> Неопределено Тогда
							СтрокаТаблицыЗначений = ТаблицаЦен.Добавить();
							Если ИспользуетсяЦенообразование25 Тогда
								СтрокаТаблицыЗначений.Номенклатура = ВыборкаДанных.Номенклатура;
								Если ИспользоватьХарактеристикиНоменклатурыДляЦенообразования
									 И ВыборкаДанных.ИспользоватьХарактеристики Тогда
									ХарактеристикаЦО                       = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаДанных.Характеристика, "ХарактеристикаНоменклатурыДляЦенообразования");
									СтрокаТаблицыЗначений.ХарактеристикаЦО = ХарактеристикаЦО;
								КонецЕсли;
								Если ИспользоватьУпаковкиНоменклатуры 
									 И ВыборкаДанных.ИспользоватьУпаковки Тогда
									СтрокаТаблицыЗначений.УпаковкаЦО = ВыборкаДанных.Упаковка;
								КонецЕсли;
							Иначе
								СтрокаСвойств = "Номенклатура";
								Если ИспользоватьХарактеристикиНоменклатуры
									 И ВыборкаДанных.ИспользоватьХарактеристики Тогда 
									СтрокаСвойств = СтрокаСвойств + ", Характеристика";
								КонецЕсли;
								Если ИспользоватьУпаковкиНоменклатуры 
									 И ВыборкаДанных.ИспользоватьУпаковки Тогда
									СтрокаСвойств = СтрокаСвойств + ", Упаковка";
								КонецЕсли;
								ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗначений, ВыборкаДанных, СтрокаСвойств);
							КонецЕсли;
							СтрокаТаблицыЗначений.Цена    = ЭлементМассива.price;
							СтрокаТаблицыЗначений.Валюта  = Справочники.Валюты.НайтиПоКоду("643");
							СтрокаТаблицыЗначений.ВидЦены = ВидЦены;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураУстановкиЦен.Вставить("ТаблицаЦен", ТаблицаЦен);
	МассивДокументов = Документы.УстановкаЦенНоменклатуры.ЗаписатьИзмененияЦенНаСервере(СтруктураУстановкиЦен);
	
	Для Каждого ЭлементКоллекции Из МассивДокументов Цикл 
		Попытка
			ДокументОбъект = ЭлементКоллекции.УстановкаЦенНоменклатуры.ПолучитьОбъект();
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
		Исключение
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

// Конструктор типов цен (PriceSuggestType).
//
// Возвращаемое значение:
//   Соответствие Из Строка - типы цен.
//
Функция СоответствиеИдентификаторов()
	
	СоответствиеИдентификаторов = Новый Соответствие;
	
	СоответствиеИдентификаторов.Вставить("BUYBOX",               "Минимальная цена продажи на Маркете");
	СоответствиеИдентификаторов.Вставить("DEFAULT_OFFER",        "Рекомендованная Маркетом");
	СоответствиеИдентификаторов.Вставить("MIN_PRICE_MARKET",     "Минимальная среди всех предложений товара на Маркете");
	СоответствиеИдентификаторов.Вставить("MAX_DISCOUNT_BASE",    "Максимальная без скидки");
	СоответствиеИдентификаторов.Вставить("MARKET_OUTLIER_PRICE", "Максимальная для показов на Маркете");
	
	Возврат СоответствиеИдентификаторов; 

КонецФункции 

Функция ВидыЦенПоИмениПоля(ИмяПоля, МассивРекомендованныхЦен)   
	
	ВидыЦенПоИмениПоля = Новый Массив;
	
	СоответствиеИдентификаторов = СоответствиеИдентификаторов(); 
	ИмяТипа = СоответствиеИдентификаторов.Получить(ИмяПоля);  
	
	Если ИмяТипа = Неопределено Тогда 
		// Зафиксировать, что на стороне сервиса появился не поддерживаемый в текущей конфигурации вид цены
	Иначе
		Для Каждого ЭлементМассива Из МассивРекомендованныхЦен Цикл 
			ЗначениеИзХранилища = ЭлементМассива.ХранилищеНастроекПараметровСпособаЗаданияЦены.Получить();
			Если ЗначениеИзХранилища.ЗначениеПараметра = ИмяТипа Тогда    
				ВидыЦенПоИмениПоля.Добавить(ЭлементМассива);
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
	Возврат ВидыЦенПоИмениПоля;
	
КонецФункции

// Записывает актуальные данные об остатках товаров в регистры сведений ОстаткиТоваровМаркетплейсов и СтатусыПубликацииТоваровЯндексМаркет.
//
// Параметры:
//   ЦеныТоваров - Массив Из Структура - выборка данных о ценах товарных позиций:
//     * УчетнаяЗапись               - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//     * Номенклатура                - СправочникСсылка.Номенклатура - номенклатура товарной позиции;
//     * Характеристика              - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика товарной позиции;
//     * Упаковка                    - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка товарной позиции;
//     * Цена                        - Число - установленная цена товара;
//     * Период                      - Дата - период установки цены.
//
Процедура ОбновитьДатуУстановкиЦены(ЦеныТоваров)
	
	Для Каждого ЭлементКоллекции Из ЦеныТоваров Цикл
		НачатьТранзакцию();
		 
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет");
			ЭлементБлокировкиДанных.УстановитьЗначение("УчетнаяЗапись",  ЭлементКоллекции.УчетнаяЗапись);
			ЭлементБлокировкиДанных.УстановитьЗначение("Номенклатура",   ЭлементКоллекции.Номенклатура);
			ЭлементБлокировкиДанных.УстановитьЗначение("Характеристика", ЭлементКоллекции.Характеристика);
			ЭлементБлокировкиДанных.УстановитьЗначение("Упаковка",       ЭлементКоллекции.Упаковка);
			БлокировкаДанных.Заблокировать();

			НаборЗаписей = РегистрыСведений.СтатусыПубликацииТоваровЯндексМаркет.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.УчетнаяЗапись.Установить(ЭлементКоллекции.УчетнаяЗапись);
			НаборЗаписей.Отбор.Номенклатура.Установить(ЭлементКоллекции.Номенклатура);
			НаборЗаписей.Отбор.Характеристика.Установить(ЭлементКоллекции.Характеристика);
			НаборЗаписей.Отбор.Упаковка.Установить(ЭлементКоллекции.Упаковка);
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() > 0 Тогда
				Запись                   = НаборЗаписей[0];
				Запись.ДатаУстановкиЦены = ЭлементКоллекции.Период;
				Запись.ЦенаПродажи       = ЭлементКоллекции.Цена;
				
				НаборЗаписей.Записать();
			КонецЕсли;

			ЗафиксироватьТранзакцию();

		Исключение
			ОтменитьТранзакцию();
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеМагазинамиСлужебный

// Определяет список магазинов из сервиса, к которым имеет доступ пользователь - владелец авторизационного токена, использованного в запросе. 
// Для агентских пользователей список состоит из подчиненных магазинов.
// Описание метода см. https://yandex.ru/dev/market/partner-api/doc/ru/reference/campaigns/getCampaigns.
//
// Параметры:
//   ДанныеАвторизации - Структура - данные авторизации для подключения к сервису (см. ДанныеАвторизацииПоИдентификаторуАккаунта);
//   Отказ             - Булево - выходной параметр, определяет наличие ошибки при выполнении запросов к сервису.
//
// Возвращаемое значение:
//   - Неопределено - при выполнении запроса к внешнему ресурсу возникли ошибки.
//   - Структура Из КлючИЗначение - результат выполнения запроса:
//       * campaigns  - Массив Из Структура - список с информацией по каждому магазину (CampaignDTO):
//         ** domain        - Строка - URL или наименование магазина;
//         ** id            - Число - идентификатор кампании;
//         ** clientId      - Число - идентификатор плательщика в Яндекс Балансе;
//         ** business      - Структура - информацию о бизнесе (BusinessDTO):
//           *** id           - Число - идентификатор бизнеса;
//           *** name         - Строка - название бизнеса.
//         ** placementType - Строка - модель, по которой работает магазин.
//       * pager      - Структура - Модель для пагинации (FlippingPagerDTO):
//         ** total       - Число - сколько всего найдено элементов;
//         ** from        - Число - начальный номер найденного элемента на странице;
//         ** to          - Число - конечный номер найденного элемента на странице;
//         ** currentPage - Число - текущая страница;
//         ** pagesCount  - Число - общее количество страниц;
//         ** pageSize    - Число - размер страницы.
//
Функция ПолучитьМагазиныПользователяИзСервиса(ДанныеАвторизации, Отказ = Ложь)
	
	Результат = Неопределено;
	
	Попытка
		ДанныеПриложения = ДанныеПриложения();
		Сервер           = СерверПартнерскогоAPI();
		АдресРесурса     = "";

		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 60, СоединениеOpenSSL);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type",  "application/json");
		Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
			Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
		ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
			Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
		КонецЕсли;
		ДобавитьПодписьИнтеграцииВЗаголовки(Заголовки);
		
		HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		
		HTTPОтвет    = HTTPСоединение.Получить(HTTPЗапрос);  
		КодСостояния = HTTPОтвет.КодСостояния;
		СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
		Результат    = ИзJSON(СтрокаОтвета);
		
		Если КодСостояния <> 200 Тогда
			Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
				ОписаниеОшибок = ТекстОшибки(Результат.errors);
			Иначе
				ОписаниеОшибок = ""; 
			КонецЕсли;
			
			Отказ = Истина;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка выполнения запроса %1: %2%3';
					|en = 'An error occurred when executing the %1 request: %2%3'", 
					ОбщегоНазначения.КодОсновногоЯзыка()), 
				Сервер + АдресРесурса, 
				ОписаниеОшибок,
				СтрокаОтвета);
				
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
		КонецЕсли;

	Исключение 
		Отказ = Истина;
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
	КонецПопытки;

	Возврат Результат;

КонецФункции

// Определяет схему работы магазина по его представлению из сервиса. 
//
// Параметры:
//   Представление - Строка - представление схемы работы в сервисе.
//
// Возвращаемое значение:
//   ПеречислениеСсылка.СхемыРаботыТорговыхПлощадок - схема работы магазина.
//
Функция ОпределитьСхемуРаботы(Представление)
	
	Если Представление = "FBS" Тогда
		Возврат Перечисления.СхемыРаботыТорговыхПлощадок.FBS;
	ИначеЕсли Представление = "Express" Или Представление = "Экспресс" Тогда
		Возврат Перечисления.СхемыРаботыТорговыхПлощадок.Express;
	ИначеЕсли Представление = "FBY" Тогда
		Возврат Перечисления.СхемыРаботыТорговыхПлощадок.FBO;
	ИначеЕсли Представление = "DBS" Тогда
		Возврат Перечисления.СхемыРаботыТорговыхПлощадок.DBS;
	КонецЕсли;
	
	Возврат Перечисления.СхемыРаботыТорговыхПлощадок.ПустаяСсылка();
	
КонецФункции

#КонецОбласти

#Область ЗаказыСлужебный

// Возвращаемое значение:
//  Структура:
//   * buyerType - Строка - тип покупателя.
//   * orderIds - Строка, Массив из Строка - номера заказов.
//   * fake - Булево - признак тестовых заказов.
//   * status - Строка - статус.
//   * substatus - Строка - подстатус.
//   * fromDate - Строка - начало периода оформления заказа.
//   * toDate - Строка - конец периода оформления заказа.
//   * updatedAtFrom - Строка - начало периода обновления заказа.
//   * updatedAtTo - Строка - конец периода обновления заказа.
//   * supplierShipmentDateFrom - Строка - начало периода передачи в отгрузку.
//   * supplierShipmentDateTo - Строка - конец периода передачи в отгрузку.
//   * page_token - Строка - идентификатор страницы c результатами.
//
Функция НовыйПараметрыПолученияЗаказов() 
	
	ПараметрыЗапроса = Новый Структура;
	
	ПараметрыЗапроса.Вставить("buyerType", "");
	ПараметрыЗапроса.Вставить("orderIds", "");
	ПараметрыЗапроса.Вставить("fake", Ложь);
	ПараметрыЗапроса.Вставить("status", "");
	ПараметрыЗапроса.Вставить("substatus", "");
	ПараметрыЗапроса.Вставить("fromDate", "");
	ПараметрыЗапроса.Вставить("toDate", "");
	ПараметрыЗапроса.Вставить("updatedAtFrom", "");
	ПараметрыЗапроса.Вставить("updatedAtTo", "");
	ПараметрыЗапроса.Вставить("supplierShipmentDateFrom", "");
	ПараметрыЗапроса.Вставить("supplierShipmentDateTo", "");
	ПараметрыЗапроса.Вставить("page_token", "");
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

// Возвращаемое значение:
//  ТаблицаЗначений:
//   * ИдентификаторЗаказа - Строка
//   * НомерЗаказа - Строка
//   * КомментарийКЗаказу - Строка
//   * ИдентификаторОтправления - Строка
//   * НомерОтправления - Строка
//   * ИдентификаторОтгрузки - Строка
//   * ДатаОтправления - Дата
//   * СтатусОтправления - Строка
//   * ПодстатусОтправления - Строка
//   * ТрекНомерОтправления - Строка
//   * НомерРодительскогоОтправления - Строка
//   * ДатаОтгрузки - Дата
//   * ИдентификаторСклада - Строка
//   * НаименованиеСклада - Строка
//   * ДатаПередачиВДоставку - Дата
//   * ДатаДоставки - Дата
//   * СпособДоставки - Строка
//   * РегионДоставки - Строка
//   * ГородДоставки - Строка
//   * АдресДоставки - Строка
//   * СлужбаДоставки - Строка
//   * СтатусПокупателя - Булево
//   * СпособОплаты - Строка
//   * ИдентификаторПокупателя - Строка
//   * НаименованиеПокупателя - Строка
//   * ИдентификаторПричиныОтмены - Строка
//   * ПричинаОтмены - Строка
//   * ТипПричиныОтмены - Строка
//   * ИнициаторОтмены - Строка
//   * ИдентификаторПубликации - Строка
//   * ИдентификаторОбъектаМаркетплейса - Строка
//   * ИдентификаторSKU - Строка
//   * ПредставлениеОбъектаМаркетплейса - Строка
//   * Количество - Число
//   * Валюта - Строка
//   * ЦенаДоСкидки - Число
//   * Цена - Число
//   * ЦенаДляКлиента - Число
//   * ПроцентСкидки - Число
//   * СуммаСкидки - Число
//   * ПроцентКомиссии - Число
//   * СуммаКомиссии - Число
//   * ВыплатаПродавцу - Число
//   * ТребуетсяГТД - Булево
//   * ТребуетсяРНПТ - Булево
//   * ТребуетсяСтранаПроисхождения - Булево
//   * ТребуетсяМаркировка - Булево
//   * ТребуетсяУИНЮвелирногоИзделия - Булево
//   * ДополнительныеСведения - Строка
//   * СписокДоступныхДействий - Структура
//
Функция НоваяТаблицаЗаказов()
	
	ОписаниеТипаБулево     = Новый ОписаниеТипов("Булево");
	ОписаниеТипаДата       = Новый ОписаниеТипов("Дата");
	ОписаниеТипаСтрока3    = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(3));
	ОписаниеТипаСтрока50   = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50));
	ОписаниеТипаСтрока100  = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(100));
	ОписаниеТипаСтрока250  = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(250));
	ОписаниеТипаСтрока1024 = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(1024));
	ОписаниеТипаЧисло15_2  = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой));
	ОписаниеТипаЧисло15_3  = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Любой));

	ТаблицаЗаказов = Новый ТаблицаЗначений;
	
	ТаблицаЗаказов.Колонки.Добавить("ИдентификаторЗаказа",              ОписаниеТипаСтрока50);
	ТаблицаЗаказов.Колонки.Добавить("НомерЗаказа",                      ОписаниеТипаСтрока50);
	ТаблицаЗаказов.Колонки.Добавить("КомментарийКЗаказу",               ОписаниеТипаСтрока1024);

	ТаблицаЗаказов.Колонки.Добавить("ИдентификаторОтправления",         ОписаниеТипаСтрока50);
	ТаблицаЗаказов.Колонки.Добавить("НомерОтправления",                 ОписаниеТипаСтрока50);
	ТаблицаЗаказов.Колонки.Добавить("ИдентификаторОтгрузки",            ОписаниеТипаСтрока50);
	ТаблицаЗаказов.Колонки.Добавить("ДатаОтправления",                  ОписаниеТипаДата);
	ТаблицаЗаказов.Колонки.Добавить("СтатусОтправления",                ОписаниеТипаСтрока100);
	ТаблицаЗаказов.Колонки.Добавить("ПодстатусОтправления",             ОписаниеТипаСтрока100);
	ТаблицаЗаказов.Колонки.Добавить("ТрекНомерОтправления",             ОписаниеТипаСтрока50);
	ТаблицаЗаказов.Колонки.Добавить("НомерРодительскогоОтправления",    ОписаниеТипаСтрока50);

	ТаблицаЗаказов.Колонки.Добавить("ДатаОтгрузки",                     ОписаниеТипаДата);
	ТаблицаЗаказов.Колонки.Добавить("ИдентификаторСклада",              ОписаниеТипаСтрока50);
	ТаблицаЗаказов.Колонки.Добавить("НаименованиеСклада",               ОписаниеТипаСтрока250);

	ТаблицаЗаказов.Колонки.Добавить("ДатаПередачиВДоставку",            ОписаниеТипаДата);
	ТаблицаЗаказов.Колонки.Добавить("ДатаДоставки",                     ОписаниеТипаДата);
	ТаблицаЗаказов.Колонки.Добавить("СпособДоставки",                   ОписаниеТипаСтрока100);
	ТаблицаЗаказов.Колонки.Добавить("РегионДоставки",                   ОписаниеТипаСтрока100);
	ТаблицаЗаказов.Колонки.Добавить("ГородДоставки",                    ОписаниеТипаСтрока100);
	ТаблицаЗаказов.Колонки.Добавить("АдресДоставки",                    ОписаниеТипаСтрока250);
	ТаблицаЗаказов.Колонки.Добавить("СлужбаДоставки",                   ОписаниеТипаСтрока100);
	ТаблицаЗаказов.Колонки.Добавить("СтатусПокупателя",                 ОписаниеТипаБулево);
	ТаблицаЗаказов.Колонки.Добавить("СпособОплаты",                     ОписаниеТипаСтрока100);

	ТаблицаЗаказов.Колонки.Добавить("ИдентификаторПокупателя",          ОписаниеТипаСтрока50);
	ТаблицаЗаказов.Колонки.Добавить("НаименованиеПокупателя",           ОписаниеТипаСтрока250);

	ТаблицаЗаказов.Колонки.Добавить("ИдентификаторПричиныОтмены",       ОписаниеТипаСтрока50);
	ТаблицаЗаказов.Колонки.Добавить("ПричинаОтмены",                    ОписаниеТипаСтрока250);
	ТаблицаЗаказов.Колонки.Добавить("ТипПричиныОтмены",                 ОписаниеТипаСтрока50);
	ТаблицаЗаказов.Колонки.Добавить("ИнициаторОтмены",                  ОписаниеТипаСтрока50);

	ТаблицаЗаказов.Колонки.Добавить("ИдентификаторПубликации",          ОписаниеТипаСтрока50);
	ТаблицаЗаказов.Колонки.Добавить("ИдентификаторОбъектаМаркетплейса", ОписаниеТипаСтрока50);
	ТаблицаЗаказов.Колонки.Добавить("ИдентификаторSKU",                 ОписаниеТипаСтрока50);
	ТаблицаЗаказов.Колонки.Добавить("ПредставлениеОбъектаМаркетплейса", ОписаниеТипаСтрока250);
	ТаблицаЗаказов.Колонки.Добавить("Количество",                       ОписаниеТипаЧисло15_3);
	ТаблицаЗаказов.Колонки.Добавить("Валюта",                           ОписаниеТипаСтрока3);
	ТаблицаЗаказов.Колонки.Добавить("ЦенаДоСкидки",                     ОписаниеТипаЧисло15_2);
	ТаблицаЗаказов.Колонки.Добавить("Цена",                             ОписаниеТипаЧисло15_2);
	ТаблицаЗаказов.Колонки.Добавить("ЦенаДляКлиента",                   ОписаниеТипаЧисло15_2);
	ТаблицаЗаказов.Колонки.Добавить("ПроцентСкидки",                    ОписаниеТипаЧисло15_2);
	ТаблицаЗаказов.Колонки.Добавить("СуммаСкидки",                      ОписаниеТипаЧисло15_2);
	ТаблицаЗаказов.Колонки.Добавить("ПроцентКомиссии",                  ОписаниеТипаЧисло15_2);
	ТаблицаЗаказов.Колонки.Добавить("СуммаКомиссии",                    ОписаниеТипаЧисло15_2);
	ТаблицаЗаказов.Колонки.Добавить("ВыплатаПродавцу",                  ОписаниеТипаЧисло15_2);

	ТаблицаЗаказов.Колонки.Добавить("ТребуетсяГТД",                     ОписаниеТипаБулево);
	ТаблицаЗаказов.Колонки.Добавить("ТребуетсяРНПТ",                    ОписаниеТипаБулево);
	ТаблицаЗаказов.Колонки.Добавить("ТребуетсяСтранаПроисхождения",     ОписаниеТипаБулево);
	ТаблицаЗаказов.Колонки.Добавить("ТребуетсяМаркировка",              ОписаниеТипаБулево);
	ТаблицаЗаказов.Колонки.Добавить("ТребуетсяУИНЮвелирногоИзделия",    ОписаниеТипаБулево);
	
	ТаблицаЗаказов.Колонки.Добавить("ДополнительныеСведения",           ОписаниеТипаСтрока1024);
	
	ТаблицаЗаказов.Колонки.Добавить("СписокДоступныхДействий",          Новый ОписаниеТипов("Структура"));
	
	ТаблицаЗаказов.Индексы.Добавить("ИдентификаторSKU");
	
	Возврат ТаблицаЗаказов;
	
КонецФункции

Функция ТекстЗапросаЭкземпляровПоОтправлению()
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.Заказ КАК Заказ,      
		|	ЗаказыТорговыхПлощадок.НомерЗаказа КАК НомерЗаказа, 
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ИдентификаторТовара КАК ИдентификаторТовара,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ИдентификаторЭкземпляра КАК ИдентификаторЭкземпляра,
		|	ВЫБОР
		|		КОГДА НЕ &БезРодительскогоОтправления
		|				И ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.НомерОтправления ПОДОБНО &ПостфиксОтправления
		|			ТОГДА ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.НомерРодительскогоОтправления
		|		ИНАЧЕ ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.НомерОтправления
		|	КОНЕЦ КАК НомерОтправленияДляГруппировки,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.НомерОтправления КАК НомерОтправления,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.НомерРодительскогоОтправления КАК НомерРодительскогоОтправления,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.КодСтроки КАК КодСтроки,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ТребуетсяСтранаПроисхождения КАК ТребуетсяСтранаПроисхождения,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.СтранаПроисхождения КАК СтранаПроисхожденияСсылка,
		|	ЕСТЬNULL(СправочникСтраныМира.КодАльфа2, """") КАК СтранаПроисхожденияКод,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ТребуетсяНомерГТД КАК ТребуетсяНомерГТД,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ТребуетсяРНПТ КАК ТребуетсяРНПТ,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.НомерГТД КАК НомерГТДСсылка,
		|	ЕСТЬNULL(СправочникНомераГТД.ТипНомераГТД, ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.ПустаяСсылка)) КАК НомерГТДТип,
		|	ЕСТЬNULL(СправочникНомераГТД.Код, """") КАК НомерГТДКод,
		|	ЕСТЬNULL(СправочникНомераГТД.РегистрационныйНомер, """") КАК НомерГТДРегистрационныйНомер,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ТребуетсяМаркировка КАК ТребуетсяМаркировка,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ШтрихкодУпаковки КАК КодМаркировкиСсылка,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ПолныйКодМаркировки КАК КодМаркировки,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ТребуетсяУИН КАК ТребуетсяУИН,
		|	ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.УИН КАК УИН,
		|	ВЫБОР
		|		КОГДА ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.Количество = 0
		|			ТОГДА 1
		|		ИНАЧЕ ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.Количество
		|	КОНЕЦ КАК Количество
		|ПОМЕСТИТЬ ВТ_Экземпляры
		|ИЗ
		|	РегистрСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок КАК ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ПО (ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись)
		|			И (ЗаказыТорговыхПлощадок.Заказ = ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.Заказ)
		|			И (ЗаказыТорговыхПлощадок.НомерОтправления = ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.НомерОтправления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СправочникСтраныМира
		|		ПО (СправочникСтраныМира.Ссылка = ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.СтранаПроисхождения)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК СправочникНомераГТД
		|		ПО (СправочникНомераГТД.Ссылка = ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.НомерГТД)
		|ГДЕ
		|   ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.Заказ В(&Заказы)
		|	И &СтатусОтправления
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АВТОНОМЕРЗАПИСИ() КАК НомерЗаписи,
		|	Экземпляры.Заказ КАК Заказ,     
		|	Экземпляры.НомерЗаказа КАК НомерЗаказа,
		|	Экземпляры.ИдентификаторТовара КАК ИдентификаторТовара,
		|	Экземпляры.ИдентификаторЭкземпляра КАК ИдентификаторЭкземпляра,
		|	Экземпляры.НомерОтправленияДляГруппировки КАК НомерОтправленияДляГруппировки,
		|	Экземпляры.НомерОтправления КАК НомерОтправления,
		|	Экземпляры.НомерРодительскогоОтправления КАК НомерРодительскогоОтправления,
		|	Экземпляры.КодСтроки КАК КодСтроки,
		|	Экземпляры.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Экземпляры.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ЕСТЬNULL(ДокументЗаказКлиентаТовары.Отменено, ИСТИНА) КАК Отменено,
		|	ЕСТЬNULL(ДокументЗаказКлиентаТовары.ПричинаОтмены, ЗНАЧЕНИЕ(Справочник.ПричиныОтменыЗаказовКлиентов.ПустаяСсылка)) КАК ПричинаОтмены,
		|	ЕСТЬNULL(СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса, """") КАК КодПричиныОтмены,
		|	ЕСТЬNULL(СоответствияОбъектовМаркетплейсов.НаименованиеОбъектаМаркетплейса, """") КАК НаименованиеПричиныОтмены,
		|	Экземпляры.ТребуетсяСтранаПроисхождения КАК ТребуетсяСтранаПроисхождения,
		|	Экземпляры.СтранаПроисхожденияСсылка КАК СтранаПроисхожденияСсылка,
		|	Экземпляры.СтранаПроисхожденияКод КАК СтранаПроисхожденияКод,
		|	Экземпляры.ТребуетсяНомерГТД КАК ТребуетсяГТД,
		|	Экземпляры.ТребуетсяРНПТ КАК ТребуетсяРНПТ,
		|	Экземпляры.НомерГТДСсылка КАК НомерГТДСсылка,
		|	Экземпляры.НомерГТДТип КАК НомерГТДТип,
		|	ВЫБОР
		|		КОГДА Экземпляры.НомерГТДТип = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерГТД)
		|				ИЛИ Экземпляры.НомерГТДТип = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.ПустаяСсылка)
		|			ТОГДА Экземпляры.НомерГТДКод
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК НомерГТДКод,
		|	ВЫБОР
		|		КОГДА Экземпляры.НомерГТДТип <> ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерГТД)
		|				И Экземпляры.НомерГТДТип = ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.ПустаяСсылка)
		|			ТОГДА Экземпляры.НомерГТДРегистрационныйНомер
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК НомерГТДРегистрационныйНомер,
		|	Экземпляры.ТребуетсяМаркировка КАК ТребуетсяМаркировка,
		|	Экземпляры.КодМаркировкиСсылка КАК КодМаркировкиСсылка,
		|	Экземпляры.КодМаркировки КАК КодМаркировки,
		|	Экземпляры.ТребуетсяУИН КАК ТребуетсяУИН,
		|	Экземпляры.УИН КАК УИН,
		|	Экземпляры.Количество КАК Количество,
		|	ЛОЖЬ КАК ПроверкаПройдена,
		|	"""" КАК КодОшибки,
		|	"""" КАК ОписаниеОшибки
		|ПОМЕСТИТЬ ВТ_Результат
		|ИЗ
		|	ВТ_Экземпляры КАК Экземпляры
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ДокументЗаказКлиентаТовары
		|		ПО (ДокументЗаказКлиентаТовары.Ссылка = Экземпляры.Заказ)
		|			И (ДокументЗаказКлиентаТовары.КодСтроки = Экземпляры.КодСтроки)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
		|		ПО (СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись)
		|			И (СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.ПричинаОтмены))
		|			И (СоответствияОбъектовМаркетплейсов.Объект1С = ДокументЗаказКлиентаТовары.ПричинаОтмены)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Заказ,
		|	ИдентификаторТовара,
		|	ИдентификаторЭкземпляра
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Результат.Заказ КАК Заказ,
		|	Результат.НомерЗаказа КАК НомерЗаказа,
		|	Результат.ИдентификаторТовара КАК ИдентификаторТовара,
		|	Результат.ИдентификаторЭкземпляра КАК ИдентификаторЭкземпляра,
		|	Результат.НомерОтправленияДляГруппировки КАК НомерОтправленияДляГруппировки,
		|	Результат.НомерОтправления КАК НомерОтправления,
		|	Результат.НомерРодительскогоОтправления КАК НомерРодительскогоОтправления,
		|	Результат.КодСтроки КАК КодСтроки,
		|	Результат.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Результат.ДокументОтгрузки КАК ДокументОтгрузки,
		|	Результат.Отменено КАК Отменено,
		|	Результат.ПричинаОтмены КАК ПричинаОтмены,
		|	Результат.КодПричиныОтмены КАК КодПричиныОтмены,
		|	Результат.НаименованиеПричиныОтмены КАК НаименованиеПричиныОтмены,
		|	Результат.ТребуетсяСтранаПроисхождения КАК ТребуетсяСтранаПроисхождения,
		|	Результат.СтранаПроисхожденияСсылка КАК СтранаПроисхожденияСсылка,
		|	Результат.СтранаПроисхожденияКод КАК СтранаПроисхожденияКод,
		|	Результат.ТребуетсяГТД КАК ТребуетсяГТД,
		|	Результат.ТребуетсяРНПТ КАК ТребуетсяРНПТ,
		|	Результат.НомерГТДСсылка КАК НомерГТДСсылка,
		|	Результат.НомерГТДТип КАК НомерГТДТип,
		|	Результат.НомерГТДКод КАК НомерГТДКод,
		|	Результат.НомерГТДРегистрационныйНомер КАК НомерГТДРегистрационныйНомер,
		|	Результат.ТребуетсяМаркировка КАК ТребуетсяМаркировка,
		|	Результат.КодМаркировкиСсылка КАК КодМаркировкиСсылка,
		|	Результат.КодМаркировки КАК КодМаркировки,
		|	Результат.ТребуетсяУИН КАК ТребуетсяУИН,
		|	Результат.УИН КАК УИН,
		|	Результат.Количество КАК Количество,
		|	Результат.ПроверкаПройдена КАК ПроверкаПройдена,
		|	Результат.КодОшибки КАК КодОшибки,
		|	Результат.ОписаниеОшибки КАК ОписаниеОшибки
		|ИЗ
		|	ВТ_Результат КАК Результат
		|ГДЕ
		|	(Результат.НомерЗаписи, ИСТИНА) В
		|			(ВЫБРАТЬ
		|				МИНИМУМ(НомераСтрок.НомерЗаписи),
		|				ИСТИНА
		|			ИЗ
		|				ВТ_Результат КАК НомераСтрок
		|			ГДЕ
		|				Результат.Заказ = НомераСтрок.Заказ
		|				И Результат.ИдентификаторТовара = НомераСтрок.ИдентификаторТовара
		|				И Результат.ИдентификаторЭкземпляра = НомераСтрок.ИдентификаторЭкземпляра)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерЗаказа УБЫВ,
		|	ИдентификаторЭкземпляра
		|ИТОГИ      
		|	МАКСИМУМ(НомерЗаказа),
		|	МАКСИМУМ(ТребуетсяСтранаПроисхождения),
		|	МАКСИМУМ(ТребуетсяГТД),
		|	МАКСИМУМ(ТребуетсяРНПТ),
		|	МАКСИМУМ(ТребуетсяМаркировка),
		|	МАКСИМУМ(ТребуетсяУИН),
		|	СУММА(Количество)
		|ПО 
		|	Заказ,   
		|	НомерОтправленияДляГруппировки,
		|	ИдентификаторТовара";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПреобразоватьСпособОплаты(СпособОплатыКод, ТипОплаты)
	
	СпособОплаты = "";
	
	Если ТипОплаты = "PREPAID" Тогда  
		
		Если СпособОплатыКод = "YANDEX" Тогда 
			СпособОплаты = НСтр("ru = 'Банковской картой при оформлении';
								|en = 'Bank card at registration'");
			
		ИначеЕсли СпособОплатыКод = "GOOGLE_PAY" Тогда
			СпособОплаты = НСтр("ru = 'Apple Pay при оформлении';
								|en = 'Apple Pay at registration'");        
			
		ИначеЕсли СпособОплатыКод = "GOOGLE_PAY" Тогда
			СпособОплаты = НСтр("ru = 'Google Pay при оформлении';
								|en = 'Google Pay at registration'");  
			
		ИначеЕсли СпособОплатыКод = "CREDIT" Тогда
			СпособОплаты = НСтр("ru = 'Предоплата в кредит';
								|en = 'Prepayment on credit'");     
			
		ИначеЕсли СпособОплатыКод = "TINKOFF_CREDIT" Тогда
			СпособОплаты = НСтр("ru = 'Предоплата в кредит в Тинькофф Банке';
								|en = 'Prepayment on credit at Tinkoff Bank'");     
			
		ИначеЕсли СпособОплатыКод = "TINKOFF_INSTALLMENTS" Тогда
			СпособОплаты = НСтр("ru = 'Рассрочка в Тинькофф Банке при оформлении';
								|en = 'Installment payment at Tinkoff Bank at registration'");    
			
		ИначеЕсли СпособОплатыКод = "EXTERNAL_CERTIFICATE" Тогда
			СпособОплаты = НСтр("ru = 'Подарочным сертификатом';
								|en = 'Gift card'"); 
			
		ИначеЕсли СпособОплатыКод = "SBP" Тогда
			СпособОплаты = НСтр("ru = 'Предоплата через СБП';
								|en = 'Prepayment via FPS'");         
			
		ИначеЕсли СпособОплатыКод = "B2B_ACCOUNT_PREPAYMENT" Тогда
			СпособОплаты = НСтр("ru = 'Заказ оплачивает организация при оформлении';
								|en = 'Order is paid by company at registration'");		
		КонецЕсли;    
				
	ИначеЕсли  ТипОплаты = "POSTPAID" Тогда

		Если СпособОплатыКод = "CARD_ON_DELIVERY" Тогда 
			СпособОплаты = НСтр("ru = 'Банковской картой при получении';
								|en = 'Bank card upon receipt'");	
			
		ИначеЕсли СпособОплатыКод = "BOUND_CARD_ON_DELIVERY" Тогда
			СпособОплаты = НСтр("ru = 'Привязанной картой при получении';
								|en = 'Linked bank card upon receipt'"); 
			
		ИначеЕсли СпособОплатыКод = "BNPL_BANK_ON_DELIVERY" Тогда
			СпособОплаты = НСтр("ru = 'Супер Сплитом при получении';
								|en = 'Super Split upon receipt'");
			
		ИначеЕсли СпособОплатыКод = "BNPL_ON_DELIVERY" Тогда
			СпособОплаты = НСтр("ru = 'Сплитом при получении';
								|en = 'Split upon receipt'");
			
		ИначеЕсли СпособОплатыКод = "CASH_ON_DELIVERY" Тогда
			СпособОплаты = НСтр("ru = 'Наличными при получении';
								|en = 'Cash upon receipt'"); 
			
		ИначеЕсли СпособОплатыКод = "B2B_ACCOUNT_POSTPAYMENT" Тогда
			СпособОплаты = НСтр("ru = 'Заказ оплачивает организация после доставки';
								|en = 'Order is paid by company upon receipt'"); 
			
		КонецЕсли; 
		
	КонецЕсли;
	
	Возврат СпособОплаты; 
	
КонецФункции 

Функция ПреобразоватьСпособДоставки(СпособДоставкиКод) 
	
	Если СпособДоставкиКод = "DELIVERY" Тогда 
		СпособДоставки = НСтр("ru = 'Курьерская доставка';
								|en = 'Courier delivery'");	
		
	ИначеЕсли СпособДоставкиКод = "PICKUP" Тогда
		СпособДоставки = НСтр("ru = 'Самовывоз';
								|en = 'Self-pickup'"); 
		
	ИначеЕсли СпособДоставкиКод = "POST" Тогда
		СпособДоставки = НСтр("ru = 'Почта';
								|en = 'Post'");
		
	ИначеЕсли СпособДоставкиКод = "DIGITAL" Тогда
		СпособДоставки = НСтр("ru = 'Доставка цифровых товаров';
								|en = 'Digital goods delivery'");
				
	КонецЕсли;              
	
	Возврат СпособДоставки;	
	
КонецФункции

// Конструктор таблицы значений по товарам отправления.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица с колонками:
//     * ИдентификаторТовара          - Строка - идентификатор товара на торговой площадке.
//     * ТребуетсяСтранаПроисхождения - Булево - признак того, что необходимо передать страну происхождения товара.
//     * ТребуетсяГТД                 - Булево - признак того, что необходимо передать номер грузовой таможенной декларации (ГТД) для продукта и отправления.
//     * ТребуетсяРНПТ                - Булево - признак того, что необходимо передать номер партии товара (РНПТ).
//     * ТребуетсяМаркировка          - Булево - признак того, что необходимо передать маркировку "Честный ЗНАК".
//     * ТребуетсяУИН                 - Булево - признак того, что необходимо передать уникальный идентификационный номер (УИН) ювелирного изделия.
//     * Экземпляры                   - ТаблицаЗначений - экземпляры товара.
//     * ПроверкаПройдена             - Булево - результат прохождения проверки. Истина, если коды экземпляра соответствуют требованиям.
//     * КодПричиныОтмены             - Строка - идентификатор причины отмены.
//     * НаименованиеПричиныОтмены    - Строка - наименование причины отмены.
//     * Количество                   - Число - количество товара.
//
Функция НоваяТаблицаТоваровПоОтправлению()
	
	ОписаниеТипаБулево    = Новый ОписаниеТипов("Булево");
	ОписаниеТипаСтрока50  = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50));
	ОписаниеТипаСтрока250 = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(250));
	ОписаниеТипаЧисло15   = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 0, ДопустимыйЗнак.Неотрицательный));
	
	Результат = Новый ТаблицаЗначений;
	
	Результат.Колонки.Добавить("ИдентификаторТовара",          ОписаниеТипаСтрока50);
	Результат.Колонки.Добавить("ТребуетсяСтранаПроисхождения", ОписаниеТипаБулево);
	Результат.Колонки.Добавить("ТребуетсяГТД",                 ОписаниеТипаБулево);
	Результат.Колонки.Добавить("ТребуетсяРНПТ",                ОписаниеТипаБулево);
	Результат.Колонки.Добавить("ТребуетсяМаркировка",          ОписаниеТипаБулево);
	Результат.Колонки.Добавить("ТребуетсяУИН",                 ОписаниеТипаБулево);
	Результат.Колонки.Добавить("Экземпляры",                   Новый ОписаниеТипов("ТаблицаЗначений"));
	Результат.Колонки.Добавить("ПроверкаПройдена",             ОписаниеТипаБулево);
	Результат.Колонки.Добавить("КодПричиныОтмены",             ОписаниеТипаСтрока50);
	Результат.Колонки.Добавить("НаименованиеПричиныОтмены",    ОписаниеТипаСтрока250);
	Результат.Колонки.Добавить("Количество",                   ОписаниеТипаЧисло15);
	
	Возврат Результат;
	
КонецФункции

// Конструктор таблицы значений по экземплярам товаров отправления.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица с колонками:
//     * ИдентификаторЭкземпляра       - Число - идентификатор экземпляра товара.
//     * ТребуетсяСтранаПроисхождения  - Булево - признак того, что необходимо передать страну происхождения товара.
//     * СтранаПроисхожденияСсылка     - СправочникСсылка.СтраныМира - страна происхождения.
//     * СтранаПроисхожденияКод        - Строка - код страны (Альфа 2).
//     * ТребуетсяГТД                  - Булево - признак того, что необходимо передать номер грузовой таможенной декларации (ГТД) для продукта и отправления.
//     * ТребуетсяРНПТ                 - Булево - признак того, что необходимо передать номер партии товара (РНПТ).
//     * НомерГТДСсылка                - СправочникСсылка.НомераГТД - номер грузовой таможенной декларации (ГТД).
//     * НомерГТДТип                   - ПеречислениеСсылка.ТипыНомеровГТД - тип номера ГТД.
//     * НомерГТДКод                   - Строка - номер грузовой таможенной декларации (ГТД).
//     * НомерГТДРегистрационныйНомер  - Строка - регистрационный номер партии товара.
//     * ТребуетсяМаркировка           - Булево - признак того, что необходимо передать маркировку "Честный ЗНАК".
//     * КодМаркировкиСсылка           - СправочникСсылка.ШтрихкодыУпаковокТоваров - коды маркировки "Честный ЗНАК".
//     * КодМаркировки                 - Строка - представление кодов маркировки.
//     * ТребуетсяУИН                  - Булево - признак того, что необходимо передать уникальный идентификационный номер (УИН) ювелирного изделия.
//     * УИН                           - Строка - уникальный идентификационный номер ювелирного изделия.
//     * НомерОтправления              - Строка - номер отправления.
//     * НомерРодительскогоОтправления - Строка - номер родительского отправления.
//     * КодСтроки                     - Строка - код строки заказа.
//     * ИдентификаторСтроки           - Строка - идентификатор строки документа отгрузки.
//     * ДокументОтгрузки              - ДокументСсылка.ПередачаТоваровХранителю, ДокументСсылка.РеализацияТоваровУслуг - документ отгрузки.
//     * Отменено                      - Булево - признак отмены.
//     * ПричинаОтмены                 - СправочникСсылка.ПричиныОтменыЗаказовКлиентов - причина отмены.
//     * КодПричиныОтмены              - Строка - идентификатор причины отмены.
//     * НаименованиеПричиныОтмены     - Строка - наименование причины отмены.
//     * ПроверкаПройдена              - Булево - результат прохождения проверки. Истина, если коды экземпляра соответствуют требованиям.
//     * КодОшибки                     - Строка - код ошибки.
//     * ОписаниеОшибки                - Строка - описание ошибки.
//     * ИдентификаторУпаковки         - Строка - идентификатор упаковки, в которой будет размещен экземпляр.
//     * Количество                    - Число - количество экземпляров.
//
Функция НоваяТаблицаЭкземпляровТоваровПоОтправлению()
	
	ОписаниеТипаБулево    = Новый ОписаниеТипов("Булево");
	ОписаниеТипаСтрока    = Новый ОписаниеТипов("Строка");
	ОписаниеТипаСтрока3   = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(3));
	ОписаниеТипаСтрока30  = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(30));
	ОписаниеТипаСтрока36  = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36));
	ОписаниеТипаСтрока50  = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50));
	ОписаниеТипаСтрока250 = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(250));
	ОписаниеТипаЧисло15   = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 0, ДопустимыйЗнак.Неотрицательный));
	
	ТипыДокументовОтгрузки = Новый Массив;
	ТипыДокументовОтгрузки.Добавить(Тип("ДокументСсылка.ПередачаТоваровХранителю"));
	ТипыДокументовОтгрузки.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ИдентификаторЭкземпляра",       ОписаниеТипаЧисло15);
	Результат.Колонки.Добавить("ТребуетсяСтранаПроисхождения",  ОписаниеТипаБулево);
	Результат.Колонки.Добавить("СтранаПроисхожденияСсылка",     Новый ОписаниеТипов("СправочникСсылка.СтраныМира"));
	Результат.Колонки.Добавить("СтранаПроисхожденияКод",        ОписаниеТипаСтрока3);
	Результат.Колонки.Добавить("ТребуетсяГТД",                  ОписаниеТипаБулево);
	Результат.Колонки.Добавить("ТребуетсяРНПТ",                 ОписаниеТипаБулево);
	Результат.Колонки.Добавить("НомерГТДСсылка",                Новый ОписаниеТипов("СправочникСсылка.НомераГТД"));
	Результат.Колонки.Добавить("НомерГТДТип",                   Новый ОписаниеТипов("ПеречислениеСсылка.ТипыНомеровГТД"));
	Результат.Колонки.Добавить("НомерГТДКод",                   ОписаниеТипаСтрока30);
	Результат.Колонки.Добавить("НомерГТДРегистрационныйНомер",  ОписаниеТипаСтрока30);
	Результат.Колонки.Добавить("ТребуетсяМаркировка",           ОписаниеТипаБулево);
	Результат.Колонки.Добавить("КодМаркировкиСсылка",           Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	Результат.Колонки.Добавить("КодМаркировки",                 ОписаниеТипаСтрока);
	Результат.Колонки.Добавить("ТребуетсяУИН",                  ОписаниеТипаБулево);
	Результат.Колонки.Добавить("УИН",                           ОписаниеТипаСтрока);
	Результат.Колонки.Добавить("НомерОтправления",              ОписаниеТипаСтрока50);
	Результат.Колонки.Добавить("НомерРодительскогоОтправления", ОписаниеТипаСтрока50);
	Результат.Колонки.Добавить("КодСтроки",                     ОписаниеТипаЧисло15);
	Результат.Колонки.Добавить("ИдентификаторСтроки",           ОписаниеТипаСтрока36);
	Результат.Колонки.Добавить("ДокументОтгрузки",              Новый ОписаниеТипов(ТипыДокументовОтгрузки));
	Результат.Колонки.Добавить("Отменено",                      ОписаниеТипаБулево);
	Результат.Колонки.Добавить("ПричинаОтмены",                 Новый ОписаниеТипов("СправочникСсылка.ПричиныОтменыЗаказовКлиентов"));
	Результат.Колонки.Добавить("КодПричиныОтмены",              ОписаниеТипаСтрока50);
	Результат.Колонки.Добавить("НаименованиеПричиныОтмены",     ОписаниеТипаСтрока250);
	Результат.Колонки.Добавить("ПроверкаПройдена",              ОписаниеТипаБулево);
	Результат.Колонки.Добавить("КодОшибки",                     ОписаниеТипаСтрока);
	Результат.Колонки.Добавить("ОписаниеОшибки",                ОписаниеТипаСтрока);
	Результат.Колонки.Добавить("ИдентификаторУпаковки",         ОписаниеТипаСтрока36);
	Результат.Колонки.Добавить("Количество",                    ОписаниеТипаЧисло15);
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыЗапросаПоФильтруПоПериоду(ПараметрыЗапроса, ВидФильтраПоПериоду, НачалоПериода, ОкончаниеПериода)
	
	Если ВидФильтраПоПериоду = ВидФильтраПоДатеСоздания() Тогда 
		ОкончаниеПериода = ОкончаниеПериода + 86400; // Не включается в интервал фильтрации
		ПараметрыЗапроса.fromDate = Формат(НачалоПериода,"ДФ=dd-MM-yyyy");
		ПараметрыЗапроса.toDate = Формат(ОкончаниеПериода,"ДФ=dd-MM-yyyy");
		
	ИначеЕсли ВидФильтраПоПериоду = ВидФильтраПоДатеОтгрузки() Тогда 
		ОкончаниеПериода = ОкончаниеПериода + 86400; // Не включается в интервал фильтрации
		ПараметрыЗапроса.supplierShipmentDateFrom = Формат(НачалоПериода,"ДФ=dd-MM-yyyy");
		ПараметрыЗапроса.supplierShipmentDateTo = Формат(ОкончаниеПериода,"ДФ=dd-MM-yyyy");
		
	ИначеЕсли ВидФильтраПоПериоду = ВидФильтраПоДатеОбновленияСтатуса() Тогда
		ОкончаниеПериода = ОкончаниеПериода + 1; // Не включается в интервал фильтрации
		ПараметрыЗапроса.updatedAtFrom = ДатаUTC(НачалоПериода);
		ПараметрыЗапроса.updatedAtTo = ДатаUTC(ОкончаниеПериода);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыЗапросаПоФильтруПоСтатусу(ПараметрыЗапроса, СтатусЗаказа)  
	
	Если СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки Тогда
		ПараметрыЗапроса.status = "PROCESSING"; 
		ПараметрыЗапроса.substatus = "STARTED"; 
		
	ИначеЕсли СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке Тогда
		ПараметрыЗапроса.status = "PROCESSING"; 
		ПараметрыЗапроса.substatus = "READY_TO_SHIP";

	ИначеЕсли СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетОплату Тогда
		ПараметрыЗапроса.status = "UNPAID"; 
		ПараметрыЗапроса.substatus = "";

	ИначеЕсли СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен Тогда
		ПараметрыЗапроса.status = "CANCELLED"; 
		ПараметрыЗапроса.substatus = "";

	ИначеЕсли СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставляется Тогда
		ПараметрыЗапроса.status = "DELIVERY"; 
		ПараметрыЗапроса.substatus = "";

	ИначеЕсли СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.ВПунктеВыдачи Тогда
		ПараметрыЗапроса.status = "PICKUP"; 
		ПараметрыЗапроса.substatus = "";

	ИначеЕсли СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставлен Тогда
		ПараметрыЗапроса.status = "DELIVERED"; 
		ПараметрыЗапроса.substatus = "";

	ИначеЕсли СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.Возвращен Тогда
		ПараметрыЗапроса.status = "RETURNED"; 
		ПараметрыЗапроса.substatus = "";
		
	Иначе
		ПараметрыЗапроса.status = "";
		ПараметрыЗапроса.status = "";
	КонецЕсли;   
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыЗапросаПоТестовомуРежиму(ПараметрыЗапроса, ТестовыеЗаказы)    
	
	Если ТестовыеЗаказы Тогда
		ПараметрыЗапроса.fake = "true";
	Иначе   
		ПараметрыЗапроса.fake = "false";
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область УправлениеПродажами

// Загружает информацию о продажах товаров из сервиса.
// Описание метода см. https://yandex.ru/dev/market/partner-api/doc/ru/reference/reports/generateGoodsRealizationReport.
//
// Параметры:
//   ДанныеУчетнойЗаписи - Структура - данные учетной записи, см. ДанныеУчетнойЗаписиЯндексМаркет.
//   НачалоПериода       - Дата - начало периода загрузки данных.
//   ОкончаниеПериода    - Дата - окончание периода загрузки данных.
//   Отказ               - Булево - выходной параметр, определяет наличие ошибки при выполнении запросов к сервису.
//
// Возвращаемое значение:
//   Массив Из Строка - пути к полученным файлам отчетов о реализованных товарах.
//
Функция ПолучитьОтчетыОРеализованныхТоварахИзСервиса(ДанныеУчетнойЗаписи, Знач НачалоПериода, Знач ОкончаниеПериода, 
			Отказ = Ложь) Экспорт
	
	ОтчетыСервиса = Новый Соответствие;
	Файлы         = Новый Массив;
	
	// Генерация отчетов по реализации за указанный период помесячно
	Пока НачалоПериода < ОкончаниеПериода Цикл
		Попытка
			ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
			ДанныеПриложения  = ДанныеПриложения();

			ПараметрыURL = Новый Массив;
			ПараметрыURL.Добавить("format=" + "FILE");
			
			Сервер       = "api.partner.market.yandex.ru/reports";
			АдресРесурса = "/goods-realization/generate?" + СтрСоединить(ПараметрыURL, "&");
			
			ИнтернетПрокси = Неопределено;
			Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
				МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
				ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
			КонецЕсли;
			СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
			
			HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 180, СоединениеOpenSSL);
			
			Заголовки = Новый Соответствие;
			Заголовки.Вставить("Content-Type",  "application/json");
			Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
				Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
			ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
				Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
			КонецЕсли;
			ДобавитьПодписьИнтеграцииВЗаголовки(Заголовки);
			
			ДанныеЗапроса = Новый Структура;
			ДанныеЗапроса.Вставить("campaignId", ДанныеУчетнойЗаписи.ИдентификаторМагазина);
			ДанныеЗапроса.Вставить("year",       Год(НачалоПериода));
			ДанныеЗапроса.Вставить("month",      Месяц(НачалоПериода));
			ТелоЗапроса = ВJSON(ДанныеЗапроса);
			
			HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
			HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, "UTF-8");
			
			HTTPОтвет    = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);  
			КодСостояния = HTTPОтвет.КодСостояния;
			СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
			Результат    = ИзJSON(СтрокаОтвета);
			
			Если КодСостояния = 200 И Результат.status = "OK" Тогда
				ДанныеОтчета = Новый Структура;
				ДанныеОтчета.Вставить("Период",         НачалоПериода);
				ДанныеОтчета.Вставить("ВремяГенерации", ТекущаяУниверсальнаяДатаВМиллисекундах() 
															+ Результат.result.estimatedGenerationTime);
				ДанныеОтчета.Вставить("Статус",         "PENDING");
				ДанныеОтчета.Вставить("Повторы",        0);
				ДанныеОтчета.Вставить("ПутьКФайлу",     "");
				
				ОтчетыСервиса.Вставить(Результат.result.reportId, ДанныеОтчета);
				
			Иначе
				Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
					ОписаниеОшибок = ТекстОшибки(Результат.errors);
				Иначе
					ОписаниеОшибок = ""; 
				КонецЕсли;
				
				Отказ = Истина;
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка выполнения запроса %1 за период %2 г.: %3%4';
						|en = 'An error occurred when executing the %1 request for the period of %2: %3%4'", 
						ОбщегоНазначения.КодОсновногоЯзыка()), 
					Сервер + АдресРесурса, 
					Формат(НачалоПериода, "ДФ=MMMM.yyyy"),
					ОписаниеОшибок,
					СтрокаОтвета);
				
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
					УровеньЖурналаРегистрации.Ошибка,,, 
					ТекстОшибки);
			КонецЕсли;
			
		Исключение 
			Отказ = Истина;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
					|en = 'No connection with the %1 server. Reason: %2'", 
					ОбщегоНазначения.КодОсновногоЯзыка()),
				Сервер,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
			Прервать;
		КонецПопытки;
		
		НачалоПериода = ДобавитьМесяц(НачалоПериода, 1);
	КонецЦикла;
	
	// Проверить статусы генерации отчетов
	Пока Истина Цикл
		ОжидатьДалее = Ложь;
		
		Для Каждого КлючИЗначение Из ОтчетыСервиса Цикл
			Если КлючИЗначение.Значение.Статус = "PENDING"
					Или КлючИЗначение.Значение.Статус = "PROCESSING" Тогда
				ОжидатьДалее = Истина;
				
				Если КлючИЗначение.Значение.Повторы >= 5 Тогда
					КлючИЗначение.Значение.Статус = "FAILED";
					ОписаниеОшибки                = НСтр("ru = 'Не удалось получить отчет за ограниченное количество попыток.';
														|en = 'Cannot get the report for a limited number of attempts.'");
					
					Отказ = Истина;
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Ошибка генерации отчета о реализованных товарах за период %1 г. %2';
							|en = 'An error occurred when generating the sold item report for %1 %2'", 
							ОбщегоНазначения.КодОсновногоЯзыка()), 
						Формат(КлючИЗначение.Значение.Период, "ДФ=MMMM.yyyy"),
						ОписаниеОшибки);
					
					ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
						УровеньЖурналаРегистрации.Ошибка,,, 
						ТекстОшибки);
					
				ИначеЕсли КлючИЗначение.Значение.ВремяГенерации <= ТекущаяУниверсальнаяДатаВМиллисекундах() Тогда
					ОписаниеОшибки = "";
					ПутьКФайлу     = ПолучитьСтатусГенерацииОтчетаИзСервиса(ДанныеУчетнойЗаписи, 
						КлючИЗначение.Ключ, 
						КлючИЗначение.Значение.Статус,
						ОписаниеОшибки);
						
					Если КлючИЗначение.Значение.Статус = "FAILED" Тогда
						Отказ = Истина;
						ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Ошибка генерации отчета о реализованных товарах за период %1 г. %2';
								|en = 'An error occurred when generating the sold item report for %1 %2'", 
								ОбщегоНазначения.КодОсновногоЯзыка()), 
							Формат(КлючИЗначение.Значение.Период, "ДФ=MMMM.yyyy"),
							ОписаниеОшибки);
						
						ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
							УровеньЖурналаРегистрации.Ошибка,,, 
							ТекстОшибки);
							
					ИначеЕсли КлючИЗначение.Значение.Статус = "DONE" Тогда
						Файлы.Добавить(ПутьКФайлу);
						
					Иначе
						КлючИЗначение.Значение.Статус         = "PROCESSING";
						КлючИЗначение.Значение.ВремяГенерации = ТекущаяУниверсальнаяДатаВМиллисекундах() + 1000;
						КлючИЗначение.Значение.Повторы        = КлючИЗначение.Значение.Повторы + 1;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ОжидатьДалее Тогда
			Прервать;
		КонецЕсли;
		
		ОбщегоНазначенияБТС.Пауза(5);
	КонецЦикла;
	
	Возврат Файлы;
	
КонецФункции

#КонецОбласти

#Область Отчеты

// Получает статус генерации заданного отчета и, если отчет готов, ссылку для скачивания.
// Описание метода см. https://yandex.ru/dev/market/partner-api/doc/ru/reference/reports/getReportInfo.
//
// Параметры:
//   ДанныеУчетнойЗаписи - Структура - данные учетной записи, см. ДанныеУчетнойЗаписиЯндексМаркет.
//   ИдентификаторОтчета - Строка - идентификатор отчета, который получен после запуска его генерации.                           
//   Статус              - Строка - выходной параметр, определяет текущий статус генерации отчета.
//   ОписаниеОшибки      - Строка - описание ошибки генерации отчета.
//
// Возвращаемое значение:
//   Строка - путь к файлу отчета на сервере, если он уже сформирован, иначе - пустая строка.
//
Функция ПолучитьСтатусГенерацииОтчетаИзСервиса(ДанныеУчетнойЗаписи, ИдентификаторОтчета, Статус, ОписаниеОшибки = "")
	
	Попытка
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		ДанныеПриложения  = ДанныеПриложения();
		
		Сервер       = "api.partner.market.yandex.ru/reports";
		АдресРесурса = "/info/" + ИдентификаторОтчета;
		
		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 180, СоединениеOpenSSL);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type",  "application/json");
		Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
			Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
		ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
			Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
		КонецЕсли;
		ДобавитьПодписьИнтеграцииВЗаголовки(Заголовки);
		
		HTTPЗапрос   = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		HTTPОтвет    = HTTPСоединение.Получить(HTTPЗапрос);  
		КодСостояния = HTTPОтвет.КодСостояния;
		СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
		Результат    = ИзJSON(СтрокаОтвета);
		
		Если КодСостояния = 200 И Результат.status = "OK" Тогда
			Статус = Результат.result.status;
			
			Если Статус = "FAILED" Тогда
				Если Результат.result.Свойство("subStatus") И Результат.result.subStatus = "NO_DATA" Тогда
					ОписаниеОшибки = НСтр("ru = 'Для такого отчета нет данных.';
											|en = 'No data for this report.'", 
						ОбщегоНазначения.КодОсновногоЯзыка());
				ИначеЕсли Результат.result.Свойство("subStatus") И Результат.result.subStatus = "TOO_LARGE" Тогда
					ОписаниеОшибки = НСтр("ru = 'Отчет превысил допустимый размер - укажите меньший период времени или уточните условия запроса.';
											|en = 'The report exceeded the maximum size. Specify a shorter period or edit the filters.'", 
						ОбщегоНазначения.КодОсновногоЯзыка());
				Иначе
					ОписаниеОшибки = НСтр("ru = 'Во время генерации произошла ошибка.';
											|en = 'A generation error occurred.'", 
						ОбщегоНазначения.КодОсновногоЯзыка());
				КонецЕсли;
				
			ИначеЕсли Статус = "DONE" Тогда
				Если Результат.result.Свойство("subStatus") И Результат.result.subStatus = "NO_DATA" Тогда
					Статус         = "FAILED";
					ОписаниеОшибки = НСтр("ru = 'Для такого отчета нет данных.';
											|en = 'No data for this report.'", 
						ОбщегоНазначения.КодОсновногоЯзыка());
				ИначеЕсли Результат.result.Свойство("subStatus") И Результат.result.subStatus = "TOO_LARGE" Тогда
					Статус         = "FAILED";
					ОписаниеОшибки = НСтр("ru = 'Отчет превысил допустимый размер - укажите меньший период времени или уточните условия запроса.';
											|en = 'The report exceeded the maximum size. Specify a shorter period or edit the filters.'", 
						ОбщегоНазначения.КодОсновногоЯзыка());
				Иначе
					ПараметрыПолучения = ПолучениеФайловИзИнтернетаКлиентСервер.ПараметрыПолученияФайла();
					ПараметрыПолучения.ПутьДляСохранения = ПолучитьИмяВременногоФайла("xlsx");
					
					РезультатПолучения = ПолучениеФайловИзИнтернета.СкачатьФайлНаСервере(Результат.result.file, 
						ПараметрыПолучения, 
						Ложь);
					
					Если РезультатПолучения.Статус Тогда
						Возврат РезультатПолучения.Путь;
					Иначе
						Статус         = "FAILED";
						ОписаниеОшибки = РезультатПолучения.СообщениеОбОшибке;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
				ОписаниеОшибок = ТекстОшибки(Результат.errors);
			Иначе
				ОписаниеОшибок = ""; 
			КонецЕсли;
			
			Статус         = "FAILED";
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка выполнения запроса %1: %2%3';
					|en = 'An error occurred when executing the %1 request: %2%3'", 
					ОбщегоНазначения.КодОсновногоЯзыка()), 
				Сервер + АдресРесурса, 
				ОписаниеОшибок,
				СтрокаОтвета);
		КонецЕсли;
		
	Исключение 
		Статус         = "FAILED";
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;  
	
	Возврат "";
	
КонецФункции

#КонецОбласти    

#Область Заказы

// Конструктор параметров загрузки заказов.
//
// Возвращаемое значение:
//   Структура - параметры загрузки:
//     * НачалоПериода       - Дата - начало периода загрузки данных.
//     * ОкончаниеПериода    - Дата - окончание периода загрузки данных.
//     * ВидФильтраПоПериоду - Строка - вид фильтра по периоду. Для указания вида рекомендуется использовать функции:
//                             ИнтеграцияСЯндексМаркетКлиентСервер.ВидФильтра<ВидФильтра>.
//     * СтатусыОтправлений  - Массив из Строка - статусы отправлений в терминах торговой площадки,
//                             см. Перечисления.СтатусыЗаказовТорговыхПлощадок.ПолучитьПоПредставлению.
//     * ТолькоНовые         - Булево - признак загрузки только новых заказов.
//     * ИдентификаторыЗаказов - Строка, Массив Из Строка, Неопределено - идентификаторы заказов для выборочного
//                               получения данных.
//
Функция НовыйПараметрыЗагрузкиЗаказов() Экспорт

	Параметры = Новый Структура;
	Параметры.Вставить("НачалоПериода",       Дата(1, 1, 1));
	Параметры.Вставить("ОкончаниеПериода",    Дата(1, 1, 1));
	Параметры.Вставить("ВидФильтраПоПериоду", ВидФильтраПоДатеСоздания());
	Параметры.Вставить("СтатусыОтправлений",  Новый Массив);
	Параметры.Вставить("ТолькоНовые", Ложь);
	Параметры.Вставить("ИдентификаторыЗаказов", "");
	
	Возврат Параметры;

КонецФункции

// Определяет вид фильтра заказов: выбираются заказы с датой создания в указанном периоде.
//
// Возвращаемое значение:
//  Строка
//
Функция ВидФильтраПоДатеСоздания() Экспорт
	Возврат "ДатаСоздания";
КонецФункции

// Определяет вид фильтра заказов: выбираются заказы с датой отгрузки в доставку до торговой площадки
// в указанном периоде.
//
// Возвращаемое значение:
//  Строка
//
Функция ВидФильтраПоДатеОтгрузки() Экспорт
	Возврат "ДатаОтгрузки";
КонецФункции

// Определяет вид фильтра заказов: выбираются заказы, переданные в доставку до покупателя, с датой
// последнего обновления статуса в указанном периоде.
//
// Возвращаемое значение:
//  Строка
//
Функция ВидФильтраПоДатеОбновленияСтатуса() Экспорт
	Возврат "ДатаОбновленияСтатуса";
КонецФункции

// Получает все необработанные заказы с торговой площадки.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - данные текущего магазина.
//   Параметры - см. НовыйПараметрыЗагрузкиЗаказов
//   Ошибка - см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка
//
// Возвращаемое значение:
//   см. НоваяТаблицаЗаказов
//
Функция ПолучитьНеобработанныеЗаказы(УчетнаяЗапись, Параметры, Ошибка) Экспорт
	
	ТаблицаЗаказов = НоваяТаблицаЗаказов();
	Отказ = Ложь;
	ПараметрыПолучения = НовыеПараметрыПолученияДанныхЗаказов();
	ПараметрыПолучения.РазвернутьДоТоваров = Истина;
	
	Если Параметры.ТолькоНовые Тогда
		ПараметрыПолучения.ПараметрыЗапроса.status = "PROCESSING";
		ПараметрыПолучения.ПараметрыЗапроса.substatus = "STARTED";
		ПолучитьДанныеЗаказов(УчетнаяЗапись, ТаблицаЗаказов, ПараметрыПолучения, Отказ);
		
	ИначеЕсли ЗначениеЗаполнено(Параметры.ИдентификаторыЗаказов) Тогда
		ПорцииЗаказов = РазбитьМассивИдентификаторовНаПорции(Параметры.ИдентификаторыЗаказов);
		Для Каждого ПорцияЗаказов Из ПорцииЗаказов Цикл
			ПараметрыПолучения.ПараметрыЗапроса.orderIds = ПорцияЗаказов;
			//@skip-check query-in-loop - Обработка порции данных
			ПолучитьДанныеЗаказов(УчетнаяЗапись, ТаблицаЗаказов, ПараметрыПолучения, Отказ);
		КонецЦикла;
	Иначе
		ПолучитьДанныеЗаказов(УчетнаяЗапись, ТаблицаЗаказов, ПараметрыПолучения, Отказ);
	КонецЕсли;
	
	Возврат ТаблицаЗаказов;
	
КонецФункции

// Получает подробный список отправлений по заказам FBS.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   НачалоПериода - Дата - начало периода, за который необходимо получить данные заказов.
//   ОкончаниеПериода - Дата - окончание периода, за который необходимо получить данные заказов.
//   Заказы - Неопределено, Массив из ДокументСсылка.ЗаказКлиента - заказы для обновления статусов.
//   Ошибка - см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка
//
// Возвращаемое значение:
//   см. НоваяТаблицаЗаказов
//
Функция ПолучитьСтатусыЗаказовНаТорговойПлощадке(УчетнаяЗапись, НачалоПериода, ОкончаниеПериода, Заказы, Ошибка) Экспорт  
	
	ТаблицаЗаказов = НоваяТаблицаЗаказов();
	Отказ = Ложь;
	ПараметрыПолучения = НовыеПараметрыПолученияДанныхЗаказов();
	ПараметрыПолучения.РазвернутьДоТоваров = Истина;
	
	Если ТипЗнч(Заказы) = Тип("Массив") И Заказы.Количество() > 0 Тогда
		ИдентификаторыЗаказов = Новый Массив; // Массив из Строка
		НомераЗаказов = РегистрыСведений.ЗаказыТорговыхПлощадок.НомераЗаказовНаТорговойПлощадке(Заказы);
		
		Для Каждого КлючЗначение Из НомераЗаказов Цикл
			ИдентификаторыЗаказов.Добавить(КлючЗначение.Значение);
		КонецЦикла;
		
		ПорцииЗаказов = РазбитьМассивИдентификаторовНаПорции(ИдентификаторыЗаказов);
		Для Каждого ПорцияЗаказов Из ПорцииЗаказов Цикл
			ПараметрыПолучения.ПараметрыЗапроса.orderIds = ПорцияЗаказов;
			//@skip-check query-in-loop - Обработка порции данных
			ПолучитьДанныеЗаказов(УчетнаяЗапись, ТаблицаЗаказов, ПараметрыПолучения, Отказ);
		КонецЦикла;
	Иначе
		ПараметрыПолучения.ПараметрыЗапроса.updatedAtFrom = ДатаUTC(НачалоПериода);
		ПараметрыПолучения.ПараметрыЗапроса.updatedAtTo = ДатаUTC(ОкончаниеПериода);
		ПолучитьДанныеЗаказов(УчетнаяЗапись, ТаблицаЗаказов, ПараметрыПолучения, Отказ);
	КонецЕсли;
	
	Возврат ТаблицаЗаказов;

КонецФункции	

// Получает подробный список отправлений по заказам.
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//  Параметры - Структура - параметры выполнения функции:
//   * НачалоПериода - Дата - начало периода, за который необходимо получить данные заказов.
//   * КонецПериода - Дата - окончание периода, за который необходимо получить данные заказов.
//   * ВидФильтраПоПериоду - Строка - вид фильтра по периоду.  Для указания вида рекомендуется использовать функции:
//                           ИнтеграцияСЯндексМаркетКлиентСервер.ВидФильтра<ВидФильтра>.
//   * СтатусЗаказа - ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - представление статуса заказа, по которому необходимо осуществлять отбор;
//  ТаблицаЗаказов - ТаблицаЗначений - таблица, используемая в качестве шаблона для заполнения,
//                   см. РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПросмотрИЗагрузкаЗаказов.ТаблицаЗаказов.
//
// Возвращаемое значение:
//  Структура - результат выполнения функции:
//   * Ошибка - см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка
//   * ТаблицаЗаказов - см. НоваяТаблицаЗаказов
//
Функция ПолучитьЗаказыFBS(Знач УчетнаяЗапись, Знач Параметры, ТаблицаЗаказов) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ошибка", ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка());
	Результат.Вставить("ТаблицаЗаказов", ТаблицаЗаказов.СкопироватьКолонки());
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Результат.Ошибка.ОписаниеОшибки = НСтр("ru = 'Не указана учетная запись торговой площадки.';
												|en = 'The marketplace account is not specified.'");
		Возврат Результат;
	КонецЕсли;
	
	НачалоПериода = Параметры.НачалоПериода;
	КонецПериода = Параметры.КонецПериода;
	СекундВСутках = 24 * 60 * 60;
	МаксимальныйПериод = 30 * СекундВСутках;
	
	ПараметрыПолучения = НовыеПараметрыПолученияДанныхЗаказов();
	ПараметрыПолучения.ВидФильтраПоПериоду = Параметры.ВидФильтраПоПериоду;
	ПараметрыПолучения.СтатусЗаказа = Параметры.СтатусЗаказа;
	
	Пока НачалоПериода < КонецПериода Цикл
		
		Если Параметры.ВидФильтраПоПериоду = ВидФильтраПоДатеОбновленияСтатуса() Тогда
			
			// Конец 30 дня от начала периода, еще секунда будет добавлена при получении заказов, всего 30 дней
			ОкончаниеПериода = НачалоПериода + МаксимальныйПериод - 1;
			ОкончаниеПериода = Мин(ОкончаниеПериода, КонецДня(КонецПериода));
			Итератор = 1;
			
		Иначе
			
			// 30 дней от начала периода, еще день будет добавлен при получении заказов, всего 30 дней
			ОкончаниеПериода = НачалоПериода + МаксимальныйПериод - СекундВСутках;
			ОкончаниеПериода = Мин(ОкончаниеПериода, КонецПериода);
			Итератор = СекундВСутках;
			
		КонецЕсли;
		
		Отказ = Ложь;
		ПараметрыПолучения.НачалоПериода = НачалоПериода;
		ПараметрыПолучения.ОкончаниеПериода = ОкончаниеПериода;
		
		//@skip-check query-in-loop - Обработка порции данных
		ПолучитьДанныеЗаказов(УчетнаяЗапись, Результат.ТаблицаЗаказов, ПараметрыПолучения, Отказ);
		
		Если Отказ Тогда
			Результат.Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При получении информации о заказах товаров через торговую площадку <%1> за период с %2 по %3 возникли ошибки. Подробности см. в журнале регистрации.';
					|en = 'Errors occurred when getting information about ordering goods via the <%1> marketplace from %2 to %3. For more information, see the Event log.'"),
				Строка(УчетнаяЗапись),
				Формат(НачалоПериода, "ДЛФ=D;"),
				Формат(ОкончаниеПериода, "ДЛФ=D;"));
			Прервать;
		КонецЕсли;
		
		НачалоПериода = ОкончаниеПериода + Итератор;
	КонецЦикла;
	
	Результат.ТаблицаЗаказов.Сортировать("ДатаОтправления, НомерЗаказа");
	
	Возврат Результат;
	
КонецФункции

// Получает подробный список отправлений по заказам FBS.
//
// Параметры:
//   УчетнаяЗапись  - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   ТаблицаЗаказов - см. РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПросмотрИЗагрузкаЗаказов.ТаблицаЗаказов
//                  - см. НоваяТаблицаЗаказов
//   ПараметрыПолучения - см. НовыеПараметрыПолученияДанныхЗаказов
//   Отказ              - Булево - выходной параметр, определяет наличие ошибки при выполнении запросов к сервису.
//
Процедура ПолучитьДанныеЗаказов(Знач УчетнаяЗапись, ТаблицаЗаказов, ПараметрыПолучения, Отказ = Ложь)
	
	НачалоПериода       = ПараметрыПолучения.НачалоПериода;
	ОкончаниеПериода    = ПараметрыПолучения.ОкончаниеПериода;
	ВидФильтраПоПериоду = ПараметрыПолучения.ВидФильтраПоПериоду;
	СтатусЗаказа        = ПараметрыПолучения.СтатусЗаказа;
	ПараметрыЗапроса    = ПараметрыПолучения.ПараметрыЗапроса;
	РазвернутьДоТоваров = ПараметрыПолучения.РазвернутьДоТоваров;
	
	ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись);
	Если ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(ОкончаниеПериода) И ЗначениеЗаполнено(ВидФильтраПоПериоду) Тогда
		ЗаполнитьПараметрыЗапросаПоФильтруПоПериоду(ПараметрыЗапроса, ВидФильтраПоПериоду, НачалоПериода, ОкончаниеПериода);
	КонецЕсли;
	Если ЗначениеЗаполнено(СтатусЗаказа) Тогда
		ЗаполнитьПараметрыЗапросаПоФильтруПоСтатусу(ПараметрыЗапроса, СтатусЗаказа);
	КонецЕсли;
	ЗаполнитьПараметрыЗапросаПоТестовомуРежиму(ПараметрыЗапроса, ДанныеУчетнойЗаписи.ТестовыйРежимЗаказовFBS);
	
	ЕстьСледующаяСтраница = Истина;
	
	Пока ЕстьСледующаяСтраница Цикл
		ОтветСервиса = ПолучитьЗаказыИзСервиса(ДанныеУчетнойЗаписи, ПараметрыЗапроса, , Отказ);
		
		Если Отказ Тогда
			Прервать;
			
		ИначеЕсли ОтветСервиса.orders.Количество() = 0 Тогда
			Прервать;
			
		Иначе
			Для Каждого Заказ Из ОтветСервиса.orders Цикл
				Для Каждого Отгрузка Из Заказ["delivery"]["shipments"] Цикл
					
					Если Не Отгрузка.Свойство("boxes") Тогда
						Продолжить;
					ИначеЕсли Отгрузка["boxes"].Количество() = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					Отправление = Отгрузка["boxes"][0];
					
					Если Не РазвернутьДоТоваров Тогда
						ДанныеЗаказа = ДанныеЗаказаИзСервиса(Заказ, Отгрузка, Отправление, Неопределено);
						ЗаполнитьЗначенияСвойств(ТаблицаЗаказов.Добавить(), ДанныеЗаказа);
					Иначе
						Для Каждого Товар Из Заказ["items"] Цикл
							ДанныеЗаказа = ДанныеЗаказаИзСервиса(Заказ, Отгрузка, Отправление, Товар);
							ДанныеСтроки = ДанныеСтрокиЗаказаИзСервиса(Заказ, Товар);
							ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДанныеЗаказа, ДанныеСтроки);
							ЗаполнитьЗначенияСвойств(ТаблицаЗаказов.Добавить(), ДанныеЗаказа);
						КонецЦикла;
					КонецЕсли;
					
				КонецЦикла;
			КонецЦикла;
			
			Если ОтветСервиса.Свойство("paging") Тогда
				Если Не ОтветСервиса["paging"].Свойство("nextPageToken") Тогда
					ЕстьСледующаяСтраница = Ложь;
				Иначе
					ПараметрыЗапроса.page_token = ОтветСервиса["paging"]["nextPageToken"];
					Если ПустаяСтрока(ПараметрыЗапроса.page_token) Тогда
						ЕстьСледующаяСтраница = Ложь;
					КонецЕсли;
				КонецЕсли;
			Иначе
				ЕстьСледующаяСтраница = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// Получает заказы из сервиса.
// Описание метода см. https://api.partner.market.yandex.ru/campaigns/{campaignId}/orders.
//
// Параметры:
//   ДанныеУчетнойЗаписи - см. ДанныеУчетнойЗаписиЯндексМаркет
//	 ПараметрыЗапроса    - см. НовыйПараметрыПолученияЗаказов
//   ОписаниеОшибки      - Строка
//   Отказ               - Булево
//
// Возвращаемое значение:
//   Структура:
//    * pager - Структура - данные для пагинации:
//       ** total - Число - сколько всего найдено элементов.
//       ** from - Число - начальный номер найденного элемента на странице.
//       ** to - Число - конечный номер найденного элемента на странице.
//       ** currentPage - Число - текущая страница.
//       ** pagesCount - Число - общее количество страниц.
//       ** pageSize - Число - размер страницы.
//    * orders - Массив из Структура - данные заказов:
//       ** id - Число - идентификатор заказа.
//       ** externalOrderId - Строка - внешний идентификатор заказа.
//       ** status - Строка - статус заказа.
//       ** substatus - Строка - этап обработки заказа.
//       ** creationDate - Строка - дата и время оформления заказа.
//       ** updatedAt - Строка - дата и время последнего обновления заказа.
//       ** currency - Строка - валюта, в которой указаны цены на товары в заказе.
//       ** itemsTotal - Число - платеж покупателя.
//       ** deliveryTotal - Число - стоимость доставки.
//       ** buyerItemsTotal - Число - стоимость всех товаров в заказе в валюте покупателя после применения скидок и
//                       без учета стоимости доставки.
//       ** buyerTotal - Число - стоимость всех товаров в заказе в валюте покупателя после применения скидок и
//                       с учетом стоимости доставки.
//       ** buyerItemsTotalBeforeDiscount - Число - стоимость всех товаров в заказе в валюте покупателя без учета
//                       стоимости доставки и до применения скидок по акциям, купонам, промокодам.
//       ** buyerTotalBeforeDiscount - Число - стоимость всех товаров в заказе в валюте покупателя до применения скидок
//                       и с учетом стоимости доставки (buyerItemsTotalBeforeDiscount + стоимость доставки).
//       ** paymentType - Строка - тип оплаты заказа.
//       ** paymentMethod - Строка - способ оплаты заказа.
//       ** fake - Булево - тип заказа: настоящий заказ покупателя или тестовый заказ Маркета.
//       ** items - Массив из Структура - список товаров в заказе:
//           *** id - Число - идентификатор товара в заказе. Позволяет идентифицировать товар в рамках данного заказа.
//           *** offerId - Строка - идентификатор товарного предложения для определенного товара.
//           *** offerName - Строка - название товара.
//           *** price - Число - цена товара в валюте заказа без учета вознаграждения продавцу за скидки по промокодам,
//                       купонам и акциям (параметр subsidies). Включает НДС.
//           *** buyerPrice - Число - цена товара в валюте покупателя. В цене уже учтены скидки по акциям,
//                       купонам, промокодам.
//           *** buyerPriceBeforeDiscount - Число - стоимость товара в валюте покупателя до применения скидок по акциям
//                       купонам, промокодам.
//           *** priceBeforeDiscount - Число - стоимость товара в валюте магазина до применения скидок.
//           *** count - Число - количество единиц товара.
//           *** vat - Строка - налог на добавленную стоимость (НДС) на товар.
//           *** shopSku - Строка - идентификатор товара в системе продавца.
//           *** subsidy - Число - общее вознаграждение продавцу за DBS-доставку и все скидки на товар.
//           *** partnerWarehouseId - Строка - идентификатор склада в системе магазина, на который сформирован заказ.
//           *** promos - Массив из Структура - информация о вознаграждениях продавцу за скидки на товар:
//                **** type - Строка - тип скидки.
//                **** discount - Число - размер пользовательской скидки в валюте покупателя.
//                **** subsidy - Число - вознаграждение продавцу от Маркета за товар, проданный в рамках акции.
//                **** shopPromoId - Строка - идентификатор акции поставщика.
//                **** marketPromoId - Строка - идентификатор акции в рамках соглашения на оказание услуг по продвижению
//                       сервиса между Маркетом и продавцом.
//           *** instances - Массив из Структура - информация о маркировке единиц товара:
//                **** cis - Строка - код идентификации единицы товара в системе "Честный ЗНАК" без криптохвоста или
//                       "ASL BELGISI" (для продавцов Market Yandex Go).
//                **** cisFull - Строка - код идентификации единицы товара в системе "Честный ЗНАК" с криптохвостом.
//                **** uin - Строка - УИН ювелирного изделия.
//                **** rnpt - Строка - регистрационный номер партии товара.
//                **** gtd - Строка - грузовая таможенная декларация.
//                **** countryCode - Строка - страна производства в формате ISO 3166-1 alpha-2.
//           *** details - Массив из Структура - информация о невыкупленных или возвращенных товарах в заказе:
//               **** itemCount - Число - количество единиц товара.
//               **** itemStatus - Строка - невыкупленный или возвращенный товар.
//               **** updateDate - Строка
//           *** subsidies - Массив из Структура - список субсидий по типам:
//                **** type - Строка - тип субсидии.
//                **** amount - Число - сумма субсидии.
//           *** requiredInstanceTypes - Массив из Строка - список необходимых маркировок товара.
//           *** tags - Массив из Строка - признаки товара.
//       ** subsidies - Массив из Структура - список субсидий по типам:
//           *** type - Строка - тип субсидии.
//           *** amount - Число - сумма субсидии.
//       ** delivery - Структура - информация о доставке:
//           *** id - Строка - идентификатор доставки, присвоенный магазином.
//           *** type - Строка - способ доставки заказа.
//           *** serviceName - Строка - наименование службы доставки.
//           *** price - Число - стоимость доставки в валюте заказа.
//           *** deliveryPartnerType - Строка - тип сотрудничества со службой доставки в рамках конкретного заказа.
//           *** courier - Структура - информация о курьере:
//                **** fullName - Строка - полное имя курьера.
//                **** phone - Строка - номер телефона курьера.
//                **** phoneExtension - Строка - добавочный номер телефона.
//                **** vehicleNumber - Строка - номер транспортного средства.
//                **** vehicleDescription - Строка - описание машины. Например, модель и цвет.
//           *** dates - Структура - диапазон дат доставки:
//                **** fromDate - Строка
//                **** toDate - Строка
//                **** fromTime - Строка - начало интервала времени доставки.
//                **** toTime - Строка - конец интервала времени доставки.
//                **** realDeliveryDate - Строка
//           *** region - Структура - регион доставки:
//                **** id - Число - идентификатор региона.
//                **** name - Строка - название региона.
//                **** type - Строка - тип региона.
//                **** parent - Структура - вышестоящий регион:
//                      ***** id - Число - идентификатор региона.
//                      ***** name - Строка - название региона.
//                      ***** type - Строка - тип региона.
//           *** address - Структура - адрес доставки:
//                **** country - Строка - страна.
//                **** postcode - Строка - почтовый индекс.
//                **** city - Строка - город или населенный пункт.
//                **** district - Строка - район.
//                **** subway - Строка - станция метро.
//                **** street - Строка - улица.
//                **** house - Строка - номер дома.
//                **** estate - Строка - номер владения.
//                **** block - Строка - корпус.
//                **** building - Строка - строение.
//                **** entrance - Строка - номер подъезда.
//                **** entryphone - Строка - код домофона.
//                **** floor - Строка - этаж.
//                **** apartment - Строка - номер квартиры или офиса.
//                **** phone - Строка - нелефон получателя заказа.
//                **** recipient - Строка - фамилия, имя и отчество получателя заказа.
//                **** gps - Структура - GPS-координаты:
//                      ***** latitude - Число - широта.
//                      ***** longitude - Число - долгота.
//           *** vat - Строка - налог на добавленную стоимость (НДС) на товар.
//           *** deliveryServiceId - Число - идентификатор службы доставки.
//           *** liftType - Строка - тип подъема заказа на этаж.
//           *** liftPrice - Число - стоимость подъема на этаж.
//           *** outletCode - Строка - идентификатор пункта самовывоза, присвоенный магазином.
//           *** outletStorageLimitDate - Строка
//           *** dispatchType - Строка - способ отгрузки.
//           *** tracks - Массив из Структура - информация для отслеживания перемещений посылки:
//                **** trackCode - Строка - трек-номер посылки.
//                **** deliveryServiceId - Число - идентификатор службы доставки.
//           *** shipments - Массив из Структура - информация о посылках:
//                **** id - Число - идентификатор посылки, присвоенный Маркетом.
//                **** shipmentDate - Строка
//                **** shipmentTime - Строка - Только для модели Экспресс. Время, к которому магазин должен упаковать
//                       заказ и перевести его в статус READY_TO_SHIP. После смены статуса за заказом приедет курьер.
//                **** tracks - Массив из Структура - информация для отслеживания перемещений посылки:
//                      ***** trackCode - Строка - трек-номер посылки.
//                      ***** deliveryServiceId - Число - идентификатор службы доставки.
//                **** boxes - Массив из Структура - список грузовых мест:
//                      ***** id - Число - идентификатор грузоместа.
//                      ***** fulfilmentId - Строка - идентификатор грузового места в информационной системе магазина.
//           *** estimated - Булево - приблизительная ли дата доставки.
//           *** eacType - Строка - тип кода подтверждения ЭАПП.
//           *** eacCode - Строка - код подтверждения ЭАПП.
//       ** buyer - Структура - информация о покупателе:
//           *** id - Строка - идентификатор покупателя.
//           *** lastName - Строка - фамилия покупателя.
//           *** firstName - Строка - имя покупателя.
//           *** middleName - Строка - отчество покупателя.
//           *** type - Строка - тип покупателя: физическое лицо или организация.
//       ** notes - Строка - комментарий к заказу.
//       ** taxSystem - Строка - система налогообложения (СНО) магазина на момент оформления заказа.
//       ** cancelRequested - Булево - запрошена ли отмена.
//       ** expiryDate - Строка - дата, после которой заказ будет отменен, если не сменит статус.
//    * paging - Структура - информация о страницах с результатами:
//       ** nextPageToken - Строка - идентификатор следующей страницы результатов.
//   
//
Функция ПолучитьЗаказыИзСервиса(ДанныеУчетнойЗаписи, ПараметрыЗапроса = Неопределено, ОписаниеОшибки = "",
			Отказ = Ложь) Экспорт

	Если ПараметрыЗапроса = Неопределено Тогда 
		ПараметрыЗапроса = НовыйПараметрыПолученияЗаказов();	
	КонецЕсли;    
	
	ПараметрыURL = ЗаполнитьПараметрыURL(ПараметрыЗапроса);
		
	Попытка
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		ДанныеПриложения = ДанныеПриложения();
		Сервер           = СерверПартнерскогоAPI();
		ИмяМетода         = "/orders";
		АдресРесурса      = "/" + ДанныеУчетнойЗаписи.ИдентификаторМагазина + ИмяМетода;
		Если ПараметрыURL.Количество()>0 Тогда 
			АдресРесурса  = АдресРесурса + "?" + СтрСоединить(ПараметрыURL, "&");
        КонецЕсли;

		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 60, СоединениеOpenSSL);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type",  "application/json");
		Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
			Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
		ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
			Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
		КонецЕсли;
		
		HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		
		HTTPОтвет    = HTTPСоединение.Получить(HTTPЗапрос);  
		КодСостояния = HTTPОтвет.КодСостояния;
		СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
		Результат    = ИзJSON(СтрокаОтвета);
		
		Если КодСостояния <> 200 Тогда
			Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
				ОписаниеОшибок = ТекстОшибки(Результат.errors);
			Иначе
				ОписаниеОшибок = ""; 
			КонецЕсли;
			
			Отказ = Истина;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка выполнения запроса %1: %2%3';
					|en = 'An error occurred when executing the %1 request: %2%3'", 
					ОбщегоНазначения.КодОсновногоЯзыка()), 
				Сервер + АдресРесурса, 
				ОписаниеОшибок,
				СтрокаОтвета);
				
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
		КонецЕсли;

	Исключение 
		Отказ = Истина;
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
	КонецПопытки;  
		
	Возврат Результат;		

КонецФункции

// Получает данные о покупателе для каждого заказа.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   ДанныеЗаказов - см. НоваяТаблицаЗаказов.
//   Ошибка - см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
// Возвращаемое значение:
//   см. ИнтеграцияСМаркетплейсамиСервер.НоваяТаблицаДанныхПокупателейИзСервиса.
//
Функция ПолучитьДанныеПокупателейПоЗаказам(УчетнаяЗапись, ДанныеЗаказов, Ошибка) Экспорт
	
	СхемаРаботы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "СхемаРаботы");
	
	ТаблицаДанныхПокупателей = ИнтеграцияСМаркетплейсамиСервер.НоваяТаблицаДанныхПокупателейИзСервиса();
	
	Для Каждого ДанныеЗаказа Из ДанныеЗаказов Цикл
		ОшибкаЗапроса = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
		
		ДанныеПокупателяЗаказаИзСервиса = ПолучитьДанныеПокупателяПоЗаказуИзСервиса(
			УчетнаяЗапись,
			СхемаРаботы,
			ДанныеЗаказа.ИдентификаторЗаказа,
			ДанныеЗаказа.СтатусПокупателя,
			ОшибкаЗапроса);
		
		ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(Ошибка, ОшибкаЗапроса);
		
		Если ДанныеЗаказа.СтатусПокупателя
				И (Не ПустаяСтрока(ДанныеПокупателяЗаказаИзСервиса.id)
					Или Не ПустаяСтрока(ДанныеПокупателяЗаказаИзСервиса.organizationName)) Тогда
			СтрокаДанных = ТаблицаДанныхПокупателей.Добавить();
			СтрокаДанных.ИдентификаторЗаказа         = ДанныеЗаказа.ИдентификаторЗаказа;
			СтрокаДанных.ИдентификаторКонтрагента    = ДанныеПокупателяЗаказаИзСервиса.id;
			
			Если ПустаяСтрока(СтрокаДанных.ИдентификаторКонтрагента) Тогда
				СтрокаДанных.ТипКонтрагента          = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
				СтрокаДанных.КонтрагентПроверен      = Истина;
			Иначе
				Если ДанныеПокупателяЗаказаИзСервиса.type = "BUSINESS" Тогда
					СтрокаДанных.ТипКонтрагента      = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
				Иначе
					СтрокаДанных.ТипКонтрагента      = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
				КонецЕсли;
				
				СтрокаДанных.КонтрагентПроверен      = ДанныеПокупателяЗаказаИзСервиса.trusted;
			КонецЕсли;
			
			СтрокаДанных.ФамилияКонтрагента          = ДанныеПокупателяЗаказаИзСервиса.lastName;
			СтрокаДанных.ИмяКонтрагента              = ДанныеПокупателяЗаказаИзСервиса.firstName;
			СтрокаДанных.ОтчествоКонтрагента         = ДанныеПокупателяЗаказаИзСервиса.middleName;
			СтрокаДанных.ТелефонКонтрагента          = ДанныеПокупателяЗаказаИзСервиса.phone;
			
			Если Не ПустаяСтрока(ДанныеПокупателяЗаказаИзСервиса.organizationName) Тогда
				СтрокаДанных.НаименованиеКонтрагента = ДанныеПокупателяЗаказаИзСервиса.organizationName;
			Иначе
				ДанныеПокупателя = Новый Массив;
				ДанныеПокупателя.Добавить(СтрокаДанных.ФамилияКонтрагента);
				ДанныеПокупателя.Добавить(СтрокаДанных.ИмяКонтрагента);
				ДанныеПокупателя.Добавить(СтрокаДанных.ОтчествоКонтрагента);
				
				СтрокаДанных.НаименованиеКонтрагента = СтрСоединить(ДанныеПокупателя, " ");
				
				Если Не ПустаяСтрока(СтрокаДанных.ТелефонКонтрагента) Тогда
					СтрокаДанных.НаименованиеКонтрагента =
						СтрокаДанных.НаименованиеКонтрагента
						+ ", " + СтрШаблон(НСтр("ru = 'тел. %1';
												|en = 'phone %1'"), СтрокаДанных.ТелефонКонтрагента);
				КонецЕсли;
			КонецЕсли;
			
			СтрокаДанных.ИННКонтрагента              = ДанныеПокупателяЗаказаИзСервиса.inn;
			СтрокаДанных.КППКонтрагента              = ДанныеПокупателяЗаказаИзСервиса.kpp;
			СтрокаДанных.ЮридическийАдресКонтрагента = ДанныеПокупателяЗаказаИзСервиса.organizationJurAddress;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаДанныхПокупателей;
	
КонецФункции

// Получает данные о покупателе для заказа из сервиса.
// (Описание метода - https://api.partner.market.yandex.ru/campaigns/{campaignId}/orders/{orderId}/business-buyer).
// 
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   СхемаРаботы - ПеречислениеСсылка.СхемыРаботыТорговыхПлощадок - схема работы учетной записи.
//   ИдентификаторЗаказа - Число, Строка - идентификатор заказа.
//   ЭтоЮридическоеЛицо - Булево - признак юридического лица.
//   Ошибка - см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
// Возвращаемое значение:
//   Структура - данные покупателя по заказу из сервиса:
//     * organizationName - Строка - наименование организации;
//     * inn - Строка - ИНН организации;
//     * kpp - Строка - КПП организации;
//     * organizationJurAddress - Строка - юридический адрес организации;
//
Функция ПолучитьДанныеПокупателяПоЗаказуИзСервиса(УчетнаяЗапись, СхемаРаботы, Знач ИдентификаторЗаказа,
			ЭтоЮридическоеЛицо, Ошибка)
	
	ДанныеПокупателяЗаказа = Новый Структура;
	ДанныеПокупателяЗаказа.Вставить("type",                   "");
	ДанныеПокупателяЗаказа.Вставить("id",                     "");
	ДанныеПокупателяЗаказа.Вставить("firstName",              "");
	ДанныеПокупателяЗаказа.Вставить("lastName",               "");
	ДанныеПокупателяЗаказа.Вставить("middleName",             "");
	ДанныеПокупателяЗаказа.Вставить("phone",                  "");
	ДанныеПокупателяЗаказа.Вставить("trusted",                Ложь);
	ДанныеПокупателяЗаказа.Вставить("organizationName",       "");
	ДанныеПокупателяЗаказа.Вставить("inn",                    "");
	ДанныеПокупателяЗаказа.Вставить("kpp",                    "");
	ДанныеПокупателяЗаказа.Вставить("organizationJurAddress", "");
	
	Если ТипЗнч(ИдентификаторЗаказа) = Тип("Число") Тогда
		ИдентификаторЗаказа = ЧислоВСтроку(ИдентификаторЗаказа);
	КонецЕсли;
	
	Если СхемаРаботы = Перечисления.СхемыРаботыТорговыхПлощадок.DBS Тогда
		ТекстЗапроса = СтрШаблон("orders/%1/buyer", ИдентификаторЗаказа);
		
		ОтветСервиса = ВыполнитьЗапросВСервис(УчетнаяЗапись, ТекстЗапроса); // GET
		
		Если ОтветСервиса.КодСостояния = 200 Тогда
			Если ВозможныеСтатусыОтветаСервиса()[ВРег(ОтветСервиса.Статус)] <> "Ошибка" Тогда
				ЗаполнитьЗначенияСвойств(ДанныеПокупателяЗаказа, ОтветСервиса.Результат);
			КонецЕсли;
		Иначе
			ОшибкаВыполненияЗапроса = ОписаниеОшибкиИзСервиса(ОтветСервиса, Истина);
			ОшибкаВыполненияЗапроса.ОписаниеОшибки = ОшибкаВыполненияЗапроса.ОписаниеОшибки + " "
				+ НСтр("ru = 'Проверьте наличие прав обработки или просмотра информации о заказах.';
						|en = 'Check if you have rights to process or view order information.'");
			ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(Ошибка, ОшибкаВыполненияЗапроса);
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоЮридическоеЛицо Тогда
		ТекстЗапроса = СтрШаблон("orders/%1/business-buyer", ИдентификаторЗаказа);
		
		ОтветСервиса = ВыполнитьЗапросВСервис(УчетнаяЗапись, ТекстЗапроса, Ложь); // POST
		
		Если ОтветСервиса.КодСостояния = 200 Тогда
			Если ВозможныеСтатусыОтветаСервиса()[ВРег(ОтветСервиса.Статус)] <> "Ошибка" Тогда
				ЗаполнитьЗначенияСвойств(ДанныеПокупателяЗаказа, ОтветСервиса.Результат);
			КонецЕсли;
		Иначе
			ОшибкаВыполненияЗапроса = ОписаниеОшибкиИзСервиса(ОтветСервиса, Истина);
			ОшибкаВыполненияЗапроса.ОписаниеОшибки = ОшибкаВыполненияЗапроса.ОписаниеОшибки + " "
				+ НСтр("ru = 'Проверьте наличие прав обработки или просмотра информации о заказах.';
						|en = 'Check if you have rights to process or view order information.'");
			ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(Ошибка, ОшибкаВыполненияЗапроса);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДанныеПокупателяЗаказа;
	
КонецФункции

// Возвращает текст запроса по необработанным заказам.
//
// Возвращаемое значение:
//   Строка - текст запроса.
//
Функция ТекстЗапросаНеобработанныхЗаказовТорговойПлощадки() Экспорт
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	АВТОНОМЕРЗАПИСИ() КАК УникальныйИдентификатор,
		|	ТаблицаЗаказов.ИдентификаторЗаказа КАК ИдентификаторЗаказа,
		|	ТаблицаЗаказов.НомерОтправления КАК НомерДокумента,
		|	ТаблицаЗаказов.НомерЗаказа КАК НомерЗаказа,
		|	ТаблицаЗаказов.КомментарийКЗаказу КАК КомментарийКЗаказу,
		|	ТаблицаЗаказов.ИдентификаторОтправления КАК ИдентификаторОтправления,
		|	ТаблицаЗаказов.НомерОтправления КАК НомерОтправления,
		|	ТаблицаЗаказов.ИдентификаторОтгрузки КАК ИдентификаторОтгрузки,
		|	ТаблицаЗаказов.ДатаОтправления КАК ДатаОтправления,
		|	ТаблицаЗаказов.СтатусОтправления КАК СтатусОтправления,
		|	ТаблицаЗаказов.ПодстатусОтправления КАК ПодстатусОтправления,
		|	ТаблицаЗаказов.НомерРодительскогоОтправления КАК НомерРодительскогоОтправления,
		|	ТаблицаЗаказов.ИдентификаторСклада КАК ИдентификаторСклада,
		|	ТаблицаЗаказов.НаименованиеСклада КАК НаименованиеСклада,
		|	ТаблицаЗаказов.ДатаОтгрузки КАК ДатаОтгрузки,
		|	ТаблицаЗаказов.ДатаПередачиВДоставку КАК ДатаПередачиВДоставку,
		|	ТаблицаЗаказов.ДатаДоставки КАК ДатаДоставки,
		|	ТаблицаЗаказов.РегионДоставки КАК РегионДоставки,
		|	ТаблицаЗаказов.ГородДоставки КАК ГородДоставки,
		|	ТаблицаЗаказов.АдресДоставки КАК АдресДоставки,
		|	ТаблицаЗаказов.ИдентификаторПричиныОтмены КАК ИдентификаторПричиныОтмены,
		|	ТаблицаЗаказов.ПричинаОтмены КАК ПричинаОтмены,
		|	ТаблицаЗаказов.ИдентификаторПубликации КАК ИдентификаторПубликации,
		|	ТаблицаЗаказов.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторОбъектаМаркетплейса,
		|	ТаблицаЗаказов.ИдентификаторSKU КАК ИдентификаторSKU,
		|	ТаблицаЗаказов.ПредставлениеОбъектаМаркетплейса КАК ПредставлениеОбъектаМаркетплейса,
		|	ТаблицаЗаказов.Количество КАК Количество,
		|	ТаблицаЗаказов.Цена КАК Цена,
		|	ТаблицаЗаказов.ЦенаДоСкидки КАК ЦенаДоСкидки,
		|	ТаблицаЗаказов.ЦенаДляКлиента КАК ЦенаДляКлиента,
		|	ТаблицаЗаказов.ПроцентСкидки КАК ПроцентСкидки,
		|	ТаблицаЗаказов.СуммаСкидки КАК СуммаСкидки,
		|	ТаблицаЗаказов.ПроцентКомиссии КАК ПроцентКомиссии,
		|	ТаблицаЗаказов.СуммаКомиссии КАК СуммаКомиссии,
		|	ТаблицаЗаказов.ВыплатаПродавцу КАК ВыплатаПродавцу,
		|	ТаблицаЗаказов.ДополнительныеСведения КАК ДополнительныеСведения,
		|	ТаблицаЗаказов.ТребуетсяГТД КАК ТребуетсяГТД,
		|	ТаблицаЗаказов.ТребуетсяРНПТ КАК ТребуетсяРНПТ,
		|	ТаблицаЗаказов.ТребуетсяСтранаПроисхождения КАК ТребуетсяСтранаПроисхождения,
		|	ТаблицаЗаказов.ТребуетсяМаркировка КАК ТребуетсяМаркировка,
		|	ТаблицаЗаказов.ТребуетсяУИНЮвелирногоИзделия КАК ТребуетсяУИНЮвелирногоИзделия
		|ПОМЕСТИТЬ ВТ_ТаблицаЗаказов
		|ИЗ
		|	&ТаблицаЗаказов КАК ТаблицаЗаказов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторПубликации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСкладов.Группа КАК ГруппаСкладов,
		|	ТаблицаСкладов.Склад КАК Склад,
		|	ТаблицаСкладов.ИдентификаторСклада КАК ИдентификаторСклада,
		|	ТаблицаСкладов.НаименованиеСклада КАК НаименованиеСклада
		|ПОМЕСТИТЬ ВТ_ТаблицаСкладов
		|ИЗ
		|	&ТаблицаСкладов КАК ТаблицаСкладов
		|
		|ИНДЕКСИРОВАТЬ ПО НАБОРАМ
		|(
		|	(Склад),
		|	(ИдентификаторСклада)
		|)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторОбъектаМаркетплейса,
		|	СоответствияОбъектовМаркетплейсов.Объект1С КАК Объект1С
		|ПОМЕСТИТЬ ВТ_СоответствияОбъектовМаркетплейсовСклады
		|ИЗ
		|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
		|ГДЕ
		|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись
		|	И СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Склад)
		|	И СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса В
		|			(ВЫБРАТЬ
		|				Отбор.ИдентификаторСклада КАК ИдентификаторСклада
		|			ИЗ
		|				ВТ_ТаблицаСкладов КАК Отбор)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторОбъектаМаркетплейса,
		|	СоответствияОбъектовМаркетплейсов.Объект1С КАК Объект1С
		|ПОМЕСТИТЬ ВТ_СоответствияОбъектовМаркетплейсовПричиныОтмены
		|ИЗ
		|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
		|ГДЕ
		|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись
		|	И СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.ПричинаОтмены)
		|	И СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса В
		|			(ВЫБРАТЬ
		|				ТаблицаЗаказов.ИдентификаторПричиныОтмены КАК ИдентификаторПричиныОтмены
		|			ИЗ
		|				ВТ_ТаблицаЗаказов КАК ТаблицаЗаказов)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторОбъектаМаркетплейса
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЗаказов.УникальныйИдентификатор КАК УникальныйИдентификатор,
		|	АВТОНОМЕРЗАПИСИ() КАК НомерЗаписи,
		|	ТаблицаЗаказов.ИдентификаторЗаказа КАК ИдентификаторЗаказа,
		|	ТаблицаЗаказов.НомерДокумента КАК НомерДокумента,
		|	ТаблицаЗаказов.НомерЗаказа КАК НомерЗаказа,
		|	ТаблицаЗаказов.КомментарийКЗаказу КАК КомментарийКЗаказу,
		|	ТаблицаЗаказов.ИдентификаторОтправления КАК ИдентификаторОтправления,
		|	ТаблицаЗаказов.НомерОтправления КАК НомерОтправления,
		|	ТаблицаЗаказов.ДатаОтправления КАК ДатаОтправления,
		|	ТаблицаЗаказов.СтатусОтправления КАК СтатусОтправления,
		|	ТаблицаЗаказов.ПодстатусОтправления КАК ПодстатусОтправления,
		|	ТаблицаЗаказов.НомерРодительскогоОтправления КАК НомерРодительскогоОтправления,
		|	ЕСТЬNULL(СоответствияОбъектовМаркетплейсовСклады.ИдентификаторОбъектаМаркетплейса, ТаблицаЗаказов.ИдентификаторСклада) КАК ИдентификаторСклада,
		|	ТаблицаЗаказов.НаименованиеСклада КАК НаименованиеСклада,
		|	ЕСТЬNULL(СоответствияОбъектовМаркетплейсовСклады.Объект1С, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК Склад,
		|	ТаблицаЗаказов.ДатаОтгрузки КАК ДатаОтгрузки,
		|	ТаблицаЗаказов.ДатаПередачиВДоставку КАК ДатаПередачиВДоставку,
		|	ТаблицаЗаказов.ДатаДоставки КАК ДатаДоставки,
		|	ТаблицаЗаказов.РегионДоставки КАК РегионДоставки,
		|	ТаблицаЗаказов.ГородДоставки КАК ГородДоставки,
		|	ТаблицаЗаказов.АдресДоставки КАК АдресДоставки,
		|	ТаблицаЗаказов.ИдентификаторПричиныОтмены КАК ИдентификаторПричиныОтмены,
		|	ТаблицаЗаказов.ПричинаОтмены КАК НаименованиеПричиныОтмены,
		|	ЕСТЬNULL(СоответствияОбъектовМаркетплейсовПричиныОтмены.Объект1С, ЗНАЧЕНИЕ(Справочник.ПричиныОтменыЗаказовКлиентов.ПустаяСсылка)) КАК ПричинаОтмены,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.Упаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК Упаковка,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.НоменклатураПартнера, ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка)) КАК НоменклатураПартнера,
		|	ЕСТЬNULL(СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторПредложения, ТаблицаЗаказов.ИдентификаторПубликации) КАК ИдентификаторПубликации,
		|	ТаблицаЗаказов.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторОбъектаМаркетплейса,
		|	ТаблицаЗаказов.ИдентификаторSKU КАК ИдентификаторSKU,
		|	СтатусыПубликацииТоваровЯндексМаркет.ПредставлениеТовара КАК ПредставлениеОбъектаМаркетплейса,
		|	ТаблицаЗаказов.Количество КАК Количество,
		|	ТаблицаЗаказов.Цена КАК Цена,
		|	ТаблицаЗаказов.ЦенаДоСкидки КАК ЦенаДоСкидки,
		|	ТаблицаЗаказов.ЦенаДляКлиента КАК ЦенаДляКлиента,
		|	ТаблицаЗаказов.Цена * ТаблицаЗаказов.Количество КАК Сумма,
		|	ТаблицаЗаказов.ПроцентСкидки КАК ПроцентСкидки,
		|	ТаблицаЗаказов.СуммаСкидки КАК СуммаСкидки,
		|	ТаблицаЗаказов.ПроцентКомиссии КАК ПроцентКомиссии,
		|	ТаблицаЗаказов.СуммаКомиссии КАК СуммаКомиссии,
		|	ТаблицаЗаказов.ВыплатаПродавцу КАК ВыплатаПродавцу,
		|	ТаблицаЗаказов.ДополнительныеСведения КАК ДополнительныеСведения,
		|	ТаблицаЗаказов.ТребуетсяГТД КАК ТребуетсяГТД,
		|	ТаблицаЗаказов.ТребуетсяРНПТ КАК ТребуетсяРНПТ,
		|	ТаблицаЗаказов.ТребуетсяСтранаПроисхождения КАК ТребуетсяСтранаПроисхождения,
		|	ТаблицаЗаказов.ТребуетсяМаркировка КАК ТребуетсяМаркировка,
		|	ТаблицаЗаказов.ТребуетсяУИНЮвелирногоИзделия КАК ТребуетсяУИНЮвелирногоИзделия,
		|	ЕСТЬNULL(ЗаказыТорговыхПлощадок.Заказ, ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)) КАК ЗаказКлиентаСсылка,
		|	ЕСТЬNULL(ДокументЗаказКлиента.ПометкаУдаления, ЛОЖЬ) КАК ЗаказКлиентаПометкаУдаления,
		|	ЕСТЬNULL(ЗаказыТорговыхПлощадок.ДокументОтгрузки, НЕОПРЕДЕЛЕНО) КАК ДокументОтгрузкиСсылка,
		|	ЕСТЬNULL(ЗаказыТорговыхПлощадок.НомерЗаданияПолученияЭтикеток, """") КАК НомерЗаданияПолученияЭтикеток,
		|	ВЫБОР
		|		КОГДА ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки ЕСТЬ NULL
		|			ТОГДА ТаблицаЗаказов.ИдентификаторОтгрузки
		|		КОГДА ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки = """"
		|			ТОГДА ТаблицаЗаказов.ИдентификаторОтгрузки
		|		ИНАЧЕ ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки
		|	КОНЕЦ КАК ИдентификаторОтгрузки
		|ПОМЕСТИТЬ ВТ_Результат
		|ИЗ
		|	ВТ_ТаблицаЗаказов КАК ТаблицаЗаказов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииТоваровЯндексМаркет
		|		ПО ТаблицаЗаказов.ИдентификаторПубликации = СтатусыПубликацииТоваровЯндексМаркет.ИдентификаторПредложения
		|			И (СтатусыПубликацииТоваровЯндексМаркет.УчетнаяЗапись = &УчетнаяЗапись)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СоответствияОбъектовМаркетплейсовСклады КАК СоответствияОбъектовМаркетплейсовСклады
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СоответствияОбъектовМаркетплейсовПричиныОтмены КАК СоответствияОбъектовМаркетплейсовПричиныОтмены
		|		ПО (СоответствияОбъектовМаркетплейсовПричиныОтмены.ИдентификаторОбъектаМаркетплейса = ТаблицаЗаказов.ИдентификаторПричиныОтмены)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ПО ТаблицаЗаказов.НомерДокумента = ЗаказыТорговыхПлощадок.НомерОтправления
		|			И (ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДокументЗаказКлиента
		|		ПО (ЗаказыТорговыхПлощадок.Заказ = ДокументЗаказКлиента.Ссылка)
		|ГДЕ
		|	(ЗаказыТорговыхПлощадок.Заказ ЕСТЬ NULL
		|			ИЛИ ДокументЗаказКлиента.ПометкаУдаления)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ТаблицаЗаказов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_СоответствияОбъектовМаркетплейсовСклады
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_СоответствияОбъектовМаркетплейсовПричиныОтмены
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Результат.ИдентификаторЗаказа КАК ИдентификаторЗаказа,
		|	Результат.НомерДокумента КАК НомерДокумента,
		|	Результат.НомерЗаказа КАК НомерЗаказа,
		|	Результат.КомментарийКЗаказу КАК КомментарийКЗаказу,
		|	Результат.ИдентификаторОтправления КАК ИдентификаторОтправления,
		|	Результат.НомерОтправления КАК НомерОтправления,
		|	Результат.ДатаОтправления КАК ДатаОтправления,
		|	Результат.СтатусОтправления КАК СтатусОтправления,
		|	Результат.ПодстатусОтправления КАК ПодстатусОтправления,
		|	Результат.НомерРодительскогоОтправления КАК НомерРодительскогоОтправления,
		|	Результат.ИдентификаторСклада КАК ИдентификаторСклада,
		|	Результат.НаименованиеСклада КАК НаименованиеСклада,
		|	Результат.Склад КАК Склад,
		|	Результат.ДатаОтгрузки КАК ДатаОтгрузки,
		|	Результат.ДатаПередачиВДоставку КАК ДатаПередачиВДоставку,
		|	Результат.ДатаДоставки КАК ДатаДоставки,
		|	Результат.РегионДоставки КАК РегионДоставки,
		|	Результат.ГородДоставки КАК ГородДоставки,
		|	Результат.АдресДоставки КАК АдресДоставки,
		|	Результат.ИдентификаторПричиныОтмены КАК ИдентификаторПричиныОтмены,
		|	Результат.НаименованиеПричиныОтмены КАК НаименованиеПричиныОтмены,
		|	Результат.ПричинаОтмены КАК ПричинаОтмены,
		|	Результат.Номенклатура КАК Номенклатура,
		|	ЕСТЬNULL(СправочникНоменклатура.ПрослеживаемыйТовар, ЛОЖЬ) КАК ПрослеживаемыйТовар,
		|	ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(СправочникНоменклатура.ТипНоменклатуры, ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)) КАК ТипНоменклатуры,
		|	Результат.Характеристика КАК Характеристика,
		|	Результат.Упаковка КАК Упаковка,
		|	Результат.НоменклатураПартнера КАК НоменклатураПартнера,
		|	ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК УпаковкаКоэффициент,
		|	Результат.ИдентификаторПубликации КАК ИдентификаторПубликации,
		|	Результат.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторОбъектаМаркетплейса,
		|	Результат.ИдентификаторSKU КАК ИдентификаторSKU,
		|	Результат.ПредставлениеОбъектаМаркетплейса КАК ПредставлениеОбъектаМаркетплейса,
		|	Результат.Количество КАК Количество,
		|	Результат.Цена КАК Цена,
		|	Результат.ЦенаДоСкидки КАК ЦенаДоСкидки,
		|	Результат.ЦенаДляКлиента КАК ЦенаДляКлиента,
		|	Результат.Сумма КАК Сумма,
		|	Результат.ПроцентСкидки КАК ПроцентСкидки,
		|	Результат.СуммаСкидки КАК СуммаСкидки,
		|	Результат.ПроцентКомиссии КАК ПроцентКомиссии,
		|	Результат.СуммаКомиссии КАК СуммаКомиссии,
		|	Результат.ВыплатаПродавцу КАК ВыплатаПродавцу,
		|	Результат.ДополнительныеСведения КАК ДополнительныеСведения,
		|	Результат.ТребуетсяГТД КАК ТребуетсяГТД,
		|	Результат.ТребуетсяРНПТ КАК ТребуетсяРНПТ,
		|	Результат.ТребуетсяСтранаПроисхождения КАК ТребуетсяСтранаПроисхождения,
		|	Результат.ТребуетсяМаркировка КАК ТребуетсяМаркировка,
		|	Результат.ТребуетсяУИНЮвелирногоИзделия КАК ТребуетсяУИНЮвелирногоИзделия,
		|	ЛОЖЬ КАК ЕстьНесколькоОтправленийПоЗаказу,
		|	Результат.ЗаказКлиентаСсылка КАК ЗаказКлиентаСсылка,
		|	Результат.ЗаказКлиентаПометкаУдаления КАК ЗаказКлиентаПометкаУдаления,
		|	Результат.ДокументОтгрузкиСсылка КАК ДокументОтгрузкиСсылка,
		|	Результат.НомерЗаданияПолученияЭтикеток КАК НомерЗаданияПолученияЭтикеток,
		|	Результат.ИдентификаторОтгрузки КАК ИдентификаторОтгрузки
		|ИЗ
		|	ВТ_Результат КАК Результат
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО (СправочникНоменклатура.Ссылка = Результат.Номенклатура)
		|ГДЕ
		|	(Результат.НомерЗаписи, ИСТИНА) В
		|			(ВЫБРАТЬ
		|				МИНИМУМ(НомераСтрок.НомерЗаписи),
		|				ИСТИНА
		|			ИЗ
		|				ВТ_Результат КАК НомераСтрок
		|			ГДЕ
		|				Результат.УникальныйИдентификатор = НомераСтрок.УникальныйИдентификатор)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерДокумента,
		|	ДатаОтправления,
		|	УпаковкаКоэффициент УБЫВ
		|ИТОГИ
		|	СУММА(Сумма),
		|	МАКСИМУМ(ТребуетсяГТД),
		|	МАКСИМУМ(ТребуетсяРНПТ),
		|	МАКСИМУМ(ТребуетсяСтранаПроисхождения),
		|	МАКСИМУМ(ТребуетсяМаркировка),
		|	МАКСИМУМ(ТребуетсяУИНЮвелирногоИзделия)
		|ПО
		|	НомерДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗапасыИПотребности.Номенклатура КАК Номенклатура,
		|	ЗапасыИПотребности.Характеристика КАК Характеристика,
		|	ТаблицаСкладов.ГруппаСкладов КАК ГруппаСкладов,
		|	ЗапасыИПотребности.Склад КАК Склад,
		|	СУММА(ЗапасыИПотребности.ВНаличииОстаток - ЗапасыИПотребности.РезервироватьНаСкладеОстаток - ЗапасыИПотребности.РезервироватьПоМереПоступленияОстаток) КАК Остаток
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(
		|			,
		|			(Номенклатура, Характеристика, Склад, Назначение) В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					Результат.Номенклатура КАК Номенклатура,
		|					Результат.Характеристика КАК Характеристика,
		|					ТаблицаСкладов.Склад КАК Склад,
		|					ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение
		|				ИЗ
		|					ВТ_Результат КАК Результат
		|						ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаСкладов КАК ТаблицаСкладов
		|						ПО
		|							ИСТИНА)) КАК ЗапасыИПотребности
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаСкладов КАК ТаблицаСкладов
		|		ПО (ТаблицаСкладов.Склад = ЗапасыИПотребности.Склад)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыИПотребности.Номенклатура,
		|	ЗапасыИПотребности.Характеристика,
		|	ТаблицаСкладов.ГруппаСкладов,
		|	ЗапасыИПотребности.Склад";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"(ВЫБОР
			|	КОГДА Результат.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|		ТОГДА СправочникНоменклатура.ЕдиницаИзмерения
			|	ИНАЧЕ Результат.Упаковка
			|КОНЕЦ)",
			"Результат.Номенклатура"));
		
	Возврат ТекстЗапроса;
	
КонецФункции

// Устанавливает определенному заказу в сервисе статус "Отменен".
//
// Параметры: 
//   УчетнаяЗапись       - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   ИдентификаторЗаказа - Строка - идентификатор заказа по данным торговой площадки.
//	 Отказ				 - Булево - признак наличия ошибок в процессе отмены заказа.
//
// Возвращаемое значение:
//   Булево - результат отмены заказа в сервисе.
// 
Функция ОтменитьЗаказ(УчетнаяЗапись, ИдентификаторЗаказа, Отказ) Экспорт 
	
	ЗаказОтменен = Ложь;
	ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись); 
	Статус				= "CANCELLED";
	Подстатус           = "SHOP_FAILED";	
	ЗаказОтменен		= ИзменитьСтатусЗаказа(ДанныеУчетнойЗаписи, ИдентификаторЗаказа, Статус, Подстатус, Отказ);
		
	Возврат ЗаказОтменен;
	
КонецФункции

// Изменяет статус одного заказа. 
// (Описание метода - https://yandex.ru/dev/market/partner-api/doc/ru/reference/orders/updateOrderStatus).
//
// Параметры:
//   ДанныеУчетнойЗаписи - Структура - данные учетной записи, см. ДанныеУчетнойЗаписиЯндексМаркет. 
//   ИдентификаторЗаказа - Строка - идентификатор заказа по данным торговой площадки.
//	 Статус    			 - Строка - статус заказа, который необходимо установить. 
//	 Подстатус    	  	 - Строка - подстатус заказа, который необходимо установить.    
//	 Отказ				 - Булево - признак наличия ошибок в процессе отмены заказа.
//   ДанныеОтправлений   - Неопределено - не используется, если не передан.
//                       - Массив из см. НовыеДанныеОтправления - если передан, то он будет заполнен данными
//                         отправлений, которые будут указаны в заказе после изменения статуса.
//
// Возвращаемое значение:
//   Булево - результат отмены заказа в сервисе.
//                                              
Функция ИзменитьСтатусЗаказа(ДанныеУчетнойЗаписи, ИдентификаторЗаказа, Статус, Подстатус, Отказ,
			ДанныеОтправлений = Неопределено) Экспорт
	
	СтатусИзменен = Ложь; 
	ИдентификаторЗаказа = ФорматированныйИдентификаторЗаказа(ИдентификаторЗаказа);
	
	Попытка	  
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		ДанныеПриложения  = ДанныеПриложения();
		Сервер            = СерверПартнерскогоAPI();
		ИмяМетода         = "/status";
		АдресРесурса      = "/" + ДанныеУчетнойЗаписи.ИдентификаторМагазина + "/orders/" + ИдентификаторЗаказа + ИмяМетода;

		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 180, СоединениеOpenSSL);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type",  "application/json");
		Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
			Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
		ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
			Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
		КонецЕсли;
		 
		ДанныеСтатуса = Новый Структура;
		ДанныеСтатуса.Вставить("status", Статус);
		ДанныеСтатуса.Вставить("substatus", Подстатус);  
		
		ДанныеЗапроса = Новый Структура;
		ДанныеЗапроса.Вставить("order", ДанныеСтатуса); 
		
		ТелоЗапроса = ВJSON(ДанныеЗапроса);
		
		HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, "UTF-8");
		
		HTTPОтвет    = HTTPСоединение.Записать(HTTPЗапрос);  
		КодСостояния = HTTPОтвет.КодСостояния;
		СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
		Результат    = ИзJSON(СтрокаОтвета);
		
		Если КодСостояния = 200 И Результат.order.status = Статус Тогда
			
			СтатусИзменен = Истина;
			ЗаполнитьДанныеОтправленийПослеПодтвержденияЗаказа(Результат.order, ДанныеОтправлений);
			
		Иначе
			Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
				ОписаниеОшибок = ТекстОшибки(Результат.errors);
			Иначе
				ОписаниеОшибок = ""; 
			КонецЕсли; 	
			
			СтатусИзменен = Ложь; 
			Отказ = Истина;

			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка выполнения запроса %1: %2%3';
					|en = 'An error occurred when executing the %1 request: %2%3'", 
					ОбщегоНазначения.КодОсновногоЯзыка()), 
				Сервер + АдресРесурса, 
				ОписаниеОшибок,
				СтрокаОтвета);
				
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
		КонецЕсли;
		
	Исключение 
		СтатусИзменен = Ложь; 
		Отказ = Истина;
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
	КонецПопытки;
	
	Возврат СтатусИзменен; 
	
КонецФункции                            

// Отменяет сборку заказов на торговой площадке.
//
// Параметры:
//   УчетнаяЗапись               - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   Заказы                      - Массив Из ДокументСсылка.ЗаказКлиента - список обрабатываемых заказов.
//   ОтмененныеНомераОтправлений - Неопределено, Массив из Строка - список Обработанных заказов.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ОтменитьСборкуЗаказов(УчетнаяЗапись, Знач Заказы, ОтмененныеНомераОтправлений = Неопределено) Экспорт
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ТекстОшибки = НСтр("ru = 'Не указана учетная запись торговой площадки.';
							|en = 'The marketplace account is not specified.'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	Если Не Справочники.УчетныеЗаписиМаркетплейсов.ОбновленияДанныхТорговойПлощадкиРазрешено(УчетнаяЗапись) Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для учетной записи <%1> запрещено обновление данных торговой площадки';
				|en = 'The trading platform data update is not allowed for the <%1> account'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УчетнаяЗапись);
		Ошибка.ОписаниеОшибки = ТекстОшибки;
			
		Возврат Ошибка;
	КонецЕсли;
	
	Если ОтмененныеНомераОтправлений = Неопределено Тогда
		ОтмененныеНомераОтправлений = Новый Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
		|	ЗаказыТорговыхПлощадок.Заказ КАК Заказ,
		|	ЗаказыТорговыхПлощадок.НомерОтправления КАК НомерОтправления,
		|	ЗаказыТорговыхПлощадок.НомерЗаказа КАК НомерЗаказа
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.Заказ В(&Заказы)
		|	И ЗаказыТорговыхПлощадок.Статус НЕ В(&СтатусыНеДляОтмены)";
	
	СтатусыНеДляОтмены  = Новый Массив();
	СтатусыНеДляОтмены.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставлен);
	СтатусыНеДляОтмены.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен);
	Запрос.Параметры.Вставить("Заказы", Заказы);    
	Запрос.Параметры.Вставить("СтатусыНеДляОтмены", СтатусыНеДляОтмены);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	ДанныеЗаказов = РезультатЗапроса.Скопировать(,"Заказ, НомерЗаказа");    
	ДанныеЗаказов.Свернуть("Заказ, НомерЗаказа");
		
	Для Каждого ДанныеЗаказа Из ДанныеЗаказов Цикл 		
		
		Отказ = Ложь;
		Результат = ОтменитьЗаказ(УчетнаяЗапись, ДанныеЗаказа.НомерЗаказа, Отказ);
		
		Если Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При отмене заказа %1 для торговой площадки <%2> возникли ошибки. Подробнее см. журнал регистрации.';
				|en = 'Errors occurred when canceling the %1 order for the <%2> marketplace. For more information, see the event log.'",
			ОбщегоНазначения.КодОсновногоЯзыка()),
			ДанныеЗаказа.Заказ,
			УчетнаяЗапись);
			
			Если Ошибка.Детализация = Неопределено Тогда
				Ошибка.Детализация = Новый Массив;
			КонецЕсли;
			Ошибка.Детализация.Добавить(ТекстОшибки);
			
		ИначеЕсли ТипЗнч(Результат) = Тип("Булево") И Не Результат 
			Или Не ЗначениеЗаполнено(Результат) Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Запрос на отмену заказа %1 для торговой площадки <%2> не обработан.';
				|en = 'The request to cancel the %1 order for the <%2> marketplace is not processed.'",
			ОбщегоНазначения.КодОсновногоЯзыка()),
			ДанныеЗаказа.Заказ,
			УчетнаяЗапись);
			
			Если Ошибка.Детализация = Неопределено Тогда
				Ошибка.Детализация = Новый Массив;
			КонецЕсли;
			Ошибка.Детализация.Добавить(ТекстОшибки);
			
		Иначе  
			ОтборПоЗаказу = Новый Структура();
			ОтборПоЗаказу.Вставить("Заказ", ДанныеЗаказа.Заказ);
			ОтправленияЗаказов = РезультатЗапроса.НайтиСтроки(ОтборПоЗаказу);
			Если ОтправленияЗаказов.Количество()>0 Тогда  
				Для Каждого Отправление Из ОтправленияЗаказов Цикл
					ТекстОшибки = РегистрыСведений.ЗаказыТорговыхПлощадок.УстановитьСтатусОтправления(
						Отправление.Заказ,
						Отправление.НомерОтправления,
						Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен,
						СобытиеЖурналаРегистрации());
					
					ОтмененныеНомераОтправлений.Добавить(Отправление.НомерОтправления);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
	Возврат Ошибка;
	
КонецФункции

// Подтверждает сборку заказов (отправлений) на торговой площадке.
//
// Параметры:
//   УчетнаяЗапись     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   Заказы - Строка, Массив Из Строка - номера отправлений, для которых нужно создать экземпляры товаров.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ПодтвердитьСборкуЗаказовНаТорговойПлощадке(УчетнаяЗапись, Знач Заказы) Экспорт
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();  
	
	НомерОтправления = ПолучитьОтправленияЗаказов(Заказы);
	
	СобытиеЖурналаРегистрации  = СобытиеЖурналаРегистрации();
	
	Ошибка = ПолучитьЭкземплярыТоваровИзОтправлений(УчетнаяЗапись, Заказы, Ложь, Ложь);

	Если Не ПустаяСтрока(Ошибка.ОписаниеОшибки) Тогда
		Возврат Ошибка;
	КонецЕсли;          

	Если Не ПустаяСтрока(Ошибка.ОписаниеОшибки) Тогда
		Возврат Ошибка;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЭкземпляровПоОтправлению();
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "И &СтатусОтправления", "И ЗаказыТорговыхПлощадок.Статус = &СтатусОтправления");
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",               УчетнаяЗапись); 
	Запрос.УстановитьПараметр("Заказы",               Заказы);
	Запрос.УстановитьПараметр("ПостфиксОтправления",         "%" + РегистрыСведений.ЗаказыТорговыхПлощадок.ПостфиксНовогоОтправления() + "%");
	Запрос.УстановитьПараметр("БезРодительскогоОтправления", Истина);
	Запрос.УстановитьПараметр("СтатусОтправления",           Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);  
	
	ВыборкаЗаказ = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЗаказ.Следующий() Цикл
		НомерКоробки = 0;   
		НомерЗаказа = "";
		СоставЗаказа = Новый Структура;  
		СоставЗаказа.Вставить("КоличествоКоробок",0); 
		СоставЗаказаИзменен = Ложь;
		ВыборкаНомерОтправления = ВыборкаЗаказ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);  
		Пока ВыборкаНомерОтправления.Следующий() Цикл
			НомерКоробки = НомерКоробки + 1;	
			НомерРодительскогоОтправления = ВыборкаНомерОтправления.НомерОтправленияДляГруппировки;
			НомерРодительскогоОтправления = ВыборкаНомерОтправления.Заказ;
			Товары           = НоваяТаблицаТоваровПоОтправлению();
			ТоварыОтмененные = НоваяТаблицаТоваровПоОтправлению();
			
			ВыборкаИдентификаторТовара = ВыборкаНомерОтправления.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаИдентификаторТовара.Следующий() Цикл
				Экземпляры = НоваяТаблицаЭкземпляровТоваровПоОтправлению();
				КоличествоЭкземпляров = ВыборкаИдентификаторТовара.Количество;
				
				ВыборкаДанных = ВыборкаИдентификаторТовара.Выбрать();
				Пока ВыборкаДанных.Следующий() Цикл
					НомерРодительскогоОтправления = ВыборкаДанных.НомерРодительскогоОтправления;
					
					Если ВыборкаДанных.Отменено Тогда
						СтрокаТаблицыЗначений = ТоварыОтмененные.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗначений, ВыборкаДанных);
						КоличествоЭкземпляров = КоличествоЭкземпляров - ВыборкаДанных.Количество;
					Иначе
						Для Индекс = 1 По ВыборкаДанных.Количество Цикл
							СтрокаТаблицыЗначений = Экземпляры.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗначений, ВыборкаДанных);
							СтрокаТаблицыЗначений.Количество = 1;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
				
				Если Экземпляры.Количество() > 0 Тогда
					СтрокаТаблицыЗначений = Товары.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗначений, ВыборкаИдентификаторТовара);
					СтрокаТаблицыЗначений.Количество = КоличествоЭкземпляров;
					СтрокаТаблицыЗначений.Экземпляры = Экземпляры;
				КонецЕсли;
			КонецЦикла;
			
			// Отмена отправлений полная или частичная
			Если ТоварыОтмененные.Количество() > 0 Тогда
				ТоварыОтмененные.Свернуть("ИдентификаторТовара, КодПричиныОтмены, НаименованиеПричиныОтмены", "Количество");
				ТоварыОтмененные.Сортировать("КодПричиныОтмены, НаименованиеПричиныОтмены");
				
				СтрокаТаблицыЗначений = ТоварыОтмененные.Добавить();
				СтрокаТаблицыЗначений.КодПричиныОтмены = "-";
				
				Порция = НоваяТаблицаТоваровПоОтправлению();
				
				Для Каждого СтрокаТаблицыЗначений Из ТоварыОтмененные Цикл
					Если Порция.Количество() = 0 
						Или (Порция[0].КодПричиныОтмены = СтрокаТаблицыЗначений.КодПричиныОтмены
						И Порция[0].НаименованиеПричиныОтмены = СтрокаТаблицыЗначений.НаименованиеПричиныОтмены) Тогда
						ЗаполнитьЗначенияСвойств(Порция.Добавить(), СтрокаТаблицыЗначений);
						
					ИначеЕсли Порция.Количество() > 0 Тогда     
						
						СоставЗаказаИзменен = Истина;
						
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
	
			Если Товары.Количество() > 0 Тогда

				СоставЗаказа.Вставить(
					ИмяКоробкиДляСоставаЗаказа(НомерКоробки),
					ВыборкаНомерОтправления.НомерОтправленияДляГруппировки);
				СоставЗаказа.Вставить("ТоварыКоробки" + НомерКоробки, Товары); 
				СоставЗаказа.КоличествоКоробок = НомерКоробки; 
				НомерЗаказа = ВыборкаНомерОтправления.НомерЗаказа; 
			КонецЕсли;   				
		КонецЦикла;   
		
		ТребуетсяУИН = Ложь;
		Отказ = Ложь;
		
		Результат = ПодготовитьЗаказ(УчетнаяЗапись, НомерЗаказа, СоставЗаказа, СоставЗаказаИзменен, ТребуетсяУИН, Отказ); 
		Если Не Отказ Тогда 
			Если ТребуетсяУИН И Не Отказ Тогда 
				РезультатПроверкиУИН = ПолучитьСтатусыПроверкиУИН(УчетнаяЗапись, НомерЗаказа, Отказ);  
				Если РезультатПроверкиУИН.ВсеУИНПрошлиПроверку Тогда 
					Результат = ПодтвердитьЗаказ(УчетнаяЗапись, ВыборкаЗаказ.Заказ, СоставЗаказа, НомерЗаказа, Отказ);
				Иначе
					ОбщегоНазначения.СообщитьПользователю(Результат.СообщениеПользователю);
				КонецЕсли; 
			Иначе
				Результат = ПодтвердитьЗаказ(УчетнаяЗапись, ВыборкаЗаказ.Заказ, СоставЗаказа, НомерЗаказа, Отказ);	
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;
	
	Если Ошибка.Детализация <> Неопределено Тогда		
		Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При сборке заказов по отправлениям для торговой площадки <%1> возникли ошибки.';
				|en = 'Errors occurred when picking orders for shipments for the <%1> marketplace.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УчетнаяЗапись);
	КонецЕсли;
	
	Возврат Ошибка;
КонецФункции   

Функция ИмяКоробкиДляСоставаЗаказа(НомерКоробки)
	Возврат СтрШаблон("Коробка%1", ЧислоВСтроку(НомерКоробки));
КонецФункции

// Получает и проверяет экземпляры товаров по отправлениям.
//
// Параметры:
//   УчетнаяЗапись             - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//   НомераОтправлений         - Строка, Массив Из Строка - номера отправлений, для которых нужно создать экземпляры товаров;
//   ОбновитьДанныеЭкземпляров - Булево - обновлять ли данные регистра сведений;
//   ПрерыватьПриОшибках       - Булево - прерывать ли выполнение при, например, ошибках доступа (Api-Key is missing a required role for a method).
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ПолучитьЭкземплярыТоваровИзОтправлений(УчетнаяЗапись, Заказы, ОбновитьДанныеЭкземпляров = Истина, ПрерыватьПриОшибках = Истина) Экспорт
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ТекстОшибки = НСтр("ru = 'Не указана учетная запись торговой площадки.';
							|en = 'The marketplace account is not specified.'", 
		ОбщегоНазначения.КодОсновногоЯзыка());
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли; 
	
	НомераОтправлений = ПолучитьОтправленияЗаказов(Заказы);
	
	// Выбрать данные по отправлениям
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЭкземпляровПоОтправлению();
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",               УчетнаяЗапись);
	Запрос.УстановитьПараметр("Заказы",           Заказы);
	Запрос.УстановитьПараметр("ПостфиксОтправления",         "%" + РегистрыСведений.ЗаказыТорговыхПлощадок.ПостфиксНовогоОтправления() + "%");
	Запрос.УстановитьПараметр("БезРодительскогоОтправления", Ложь);
	Запрос.УстановитьПараметр("СтатусОтправления",           Истина);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаНомерОтправления = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаНомерОтправления.Следующий() Цикл
		Товары = НоваяТаблицаТоваровПоОтправлению();
		
		ВыборкаИдентификаторТовара = ВыборкаНомерОтправления.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаИдентификаторТовара.Следующий() Цикл
			Экземпляры = НоваяТаблицаЭкземпляровТоваровПоОтправлению();
			
			ВыборкаДанных = ВыборкаИдентификаторТовара.Выбрать();
			Пока ВыборкаДанных.Следующий() Цикл
				Для Индекс = 1 По ВыборкаДанных.Количество Цикл
					СтрокаТаблицыЗначений = Экземпляры.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗначений, ВыборкаДанных);
					СтрокаТаблицыЗначений.Количество = 1;
				КонецЦикла;
			КонецЦикла;
			
			СтрокаТаблицыЗначений = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗначений, ВыборкаИдентификаторТовара);
			СтрокаТаблицыЗначений.Экземпляры = Экземпляры;
		КонецЦикла;
		
		Если ТипЗнч(НомераОтправлений) <> Тип("Соответствие") Тогда
			НомераОтправлений = Новый Соответствие;
		КонецЕсли;
			
		ДанныеОбработки = Новый Структура;
		ДанныеОбработки.Вставить("Товары",          Товары);
		ДанныеОбработки.Вставить("Заказ",           ВыборкаНомерОтправления.Заказ);
		
		НомераОтправлений.Вставить(ВыборкаНомерОтправления.НомерОтправленияДляГруппировки, ДанныеОбработки);

	КонецЦикла;
	
	Если Ошибка.Детализация <> Неопределено Тогда
		
		Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'При проверке экземпляров товаров по отправлениям для торговой площадки <%1> возникли ошибки.';
			|en = 'Errors occurred when checking goods instances for shipments for the <%1> marketplace.'",
		ОбщегоНазначения.КодОсновногоЯзыка()),
		УчетнаяЗапись);
	КонецЕсли;
	
	Возврат Ошибка;
	
КонецФункции

// Получает все отправления заказов.
//
// Параметры:
//   Заказы            - Массив Из ДокументСсылка.ЗаказКлиента - список обрабатываемых заказов.
//
// Возвращаемое значение:
//   Массив Из Строка - номера отправлений заказов.
//
Функция ПолучитьОтправленияЗаказов(Заказы) Экспорт 
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказыТорговыхПлощадок.НомерОтправления КАК НомерОтправления
	               |ИЗ
	               |	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
	               |ГДЕ
	               |	ЗаказыТорговыхПлощадок.Заказ В(&Заказы)";  
	Запрос.Параметры.Вставить("Заказы", Заказы);
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("НомерОтправления");   

	Возврат Результат;
	
КонецФункции  

// Устанавливает определенному заказу в сервисе статус "Готов к отгрузке".
//
// Параметры: 
//   УчетнаяЗапись       - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   Заказ               - Документссылка.ЗаказКлиента - заказ, для которого необходимо установить статус.
//   СоставЗаказа        - Структура:
//    * КоличествоКоробок - Число
//    * Коробка1          - Строка - номер отправления коробки из учетной системы. В составе заказа имеется столько
//                          коробок, сколько указано в свойстве КоличествоКоробок.
//   ИдентификаторЗаказа - Строка - идентификатор заказа по данным торговой площадки.
//	 Отказ				 - Булево - признак наличия ошибок в процессе отмены заказа.
//
// Возвращаемое значение:
//   Булево - результат подтверждения заказа в сервисе.
// 
Функция ПодтвердитьЗаказ(УчетнаяЗапись, Заказ, СоставЗаказа, ИдентификаторЗаказа, Отказ) 
	
	ЗаказПодтвержден = Ложь;
	ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись);
	Статус = "PROCESSING";
	Подстатус = "READY_TO_SHIP";
	
	ДанныеОтправленийИзСервиса = Новый Массив;
	
	ЗаказПодтвержден = ИзменитьСтатусЗаказа(
		ДанныеУчетнойЗаписи,
		ИдентификаторЗаказа,
		Статус,
		Подстатус,
		Отказ,
		ДанныеОтправленийИзСервиса);
	
	ПараметрыЗамены = ИнтеграцияСМаркетплейсамиСервер.НовыеПараметрыЗаменыНомеровОтправленийПоЗаказу();
	
	ПараметрыЗамены.ИмяРегистра = Метаданные.РегистрыСведений.ЗаказыТорговыхПлощадок.ПолноеИмя();
	ПараметрыЗамены.ИмяСобытияЖурналаРегистрации = СобытиеЖурналаРегистрации();
	ПараметрыЗамены.Заказ = Заказ;
	
	ДатаУстановкиСтатуса = ТекущаяДатаСеанса();
	
	Для Индекс = 0 По ДанныеОтправленийИзСервиса.ВГраница() Цикл
		
		ДанныеОтправления = ДанныеОтправленийИзСервиса[Индекс]; // см. НовыеДанныеОтправления
		ИмяКоробки = ИмяКоробкиДляСоставаЗаказа(Индекс + 1);
		
		СтарыйНомерОтправления = СоставЗаказа[ИмяКоробки]; // Строка
		
		ДанныеЗаписиДляЗамены = ИнтеграцияСМаркетплейсамиСервер.НовыеДанныеЗаписиДляЗаменыНомераОтправленияПоЗаказу();
		
		ЗаполнитьЗначенияСвойств(ДанныеЗаписиДляЗамены, ДанныеОтправления);
		ДанныеЗаписиДляЗамены.Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке;
		ДанныеЗаписиДляЗамены.ДатаУстановкиСтатуса = ДатаУстановкиСтатуса;
		ДанныеЗаписиДляЗамены.ТребуетсяПолучениеЭтикеток = Истина;
		
		ПараметрыЗамены.ДанныеДляЗамены.Вставить(СтарыйНомерОтправления, ДанныеЗаписиДляЗамены);
		
	КонецЦикла;
	
	ИнтеграцияСМаркетплейсамиСервер.ЗаменитьНомераОтправленийПоЗаказу(ПараметрыЗамены);
	
	ПараметрыЗамены.ИмяРегистра =
		Метаданные.РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ПолноеИмя();
	ИнтеграцияСМаркетплейсамиСервер.ЗаменитьНомераОтправленийПоЗаказу(ПараметрыЗамены);
	
	Возврат ЗаказПодтвержден;
	
КонецФункции 

// Позволяет выполнить три операции: 
//		передать Маркету информацию о распределении товаров по коробкам;
//		передать Маркету коды маркировки для товаров;
//		удалить товар из заказа, если его не оказалось на складе.  
//      (см. https://yandex.ru/dev/market/partner-api/doc/ru/reference/orders/setOrderBoxLayout)
// Параметры: 
//   УчетнаяЗапись       - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   ИдентификаторЗаказа - Строка - идентификатор заказа по данным торговой площадки. 
//   СоставЗаказа        - Структура   - данные о распределении товаров по коробкам;
// 						    * Коробка1 - Строка - номер первого отправления в заказе; 
//                            Коробка...
//                          * КоробкаN - Строка - номер последнего отправления в заказе; 
// 						    * ТоварыКоробки1 - ТаблицаЗначений - состав первой коробки заказа (см. НоваяТаблицаТоваровПоОтправлению()); 
//                            ТоварыКоробки...
//                          * ТоварыКоробкиN - ТаблицаЗначений - состав последней коробки заказа (см. НоваяТаблицаТоваровПоОтправлению()); 
//     						* КоличествоКоробок - Чиало - Количество коробок  заказе.
//	 СоставЗаказаИзменен - Булево - признак удаление позиций из заказа.    
//	 ТребуетсяУИН        - Булево - признак наличия ювелирных изделий в заказе.
//	 Отказ				 - Булево - признак наличия ошибок в процессе отмены заказа.
//
// Возвращаемое значение:
//   Булево - результат подтверждения заказа в сервисе.
// 
Функция ПодготовитьЗаказ(УчетнаяЗапись, ИдентификаторЗаказа, СоставЗаказа, СоставЗаказаИзменен, ТребуетсяУИН = Ложь, Отказ)
	
	ЗаказПодготовлен = Ложь; 
	Отказ = Ложь; 
	ИдентификаторЗаказа = СтрЗаменить(ИдентификаторЗаказа,Символ(160),"");
	
	Попытка	  
		ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись);
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		ДанныеПриложения  = ДанныеПриложения();
		Сервер            = СерверПартнерскогоAPI();
		ИмяМетода         = "/boxes";
		АдресРесурса      = "/" + ДанныеУчетнойЗаписи.ИдентификаторМагазина + "/orders/" + ИдентификаторЗаказа + ИмяМетода;

		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 180, СоединениеOpenSSL);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type",  "application/json");
		Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
			Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
		ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
			Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
		КонецЕсли;
		
		ДанныеКоробок = Новый Массив();
		Для Инд = 1 По СоставЗаказа.КоличествоКоробок Цикл
			ДанныеКоробки = Новый Структура("items");
			ТоварыКоробки = СоставЗаказа["ТоварыКоробки" + Инд];
			ДанныеТоваров = Новый Массив();
			Для Каждого Стр Из ТоварыКоробки Цикл   
				Товар = Новый Структура("id,fullCount"); 
				Товар.id = Стр.ИдентификаторТовара; 
				Товар.fullCount = Стр.Количество; 
				ЕстьТребованияКТовару = Стр.ТребуетсяМаркировка
				Или Стр.ТребуетсяУИН
				Или Стр.ТребуетсяРНПТ
				Или Стр.ТребуетсяГТД
				Или Стр.ТребуетсяСтранаПроисхождения;
				
				Если ЕстьТребованияКТовару Тогда	
					
					ДанныеЭкземпляров = Новый Массив();
					
					Для Каждого Экземпляр Из Стр.Экземпляры Цикл
						
						ДанныеЭкземпляра = Новый Структура;
						ДанныеЭкземпляра.Вставить("countryCode", "RU");
						
						Если Стр.ТребуетсяМаркировка Тогда
							ДанныеЭкземпляра.Вставить("cis",ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.Base64ВШтрихкод(Экземпляр.КодМаркировки));
						КонецЕсли;
						Если Стр.ТребуетсяУИН Тогда  
							ДанныеЭкземпляра.Вставить("uin",Экземпляр.УИН);
						КонецЕсли;
						Если Стр.ТребуетсяРНПТ Тогда
							ДанныеЭкземпляра.Вставить("rnpt",Экземпляр.РНПТ);
						КонецЕсли;
						Если Стр.ТребуетсяГТД Тогда 
							ДанныеЭкземпляра.Вставить("gtd",Экземпляр.НомерГТДКод);
						КонецЕсли;
						Если Стр.ТребуетсяСтранаПроисхождения Тогда 
							ДанныеЭкземпляра.countryCode = Экземпляр.СтранаПроисхожденияКод;
						КонецЕсли;
						
						ДанныеЭкземпляров.Добавить(ДанныеЭкземпляра);
						
					КонецЦикла;
					
					Если Не ТребуетсяУИН И Стр.ТребуетсяУИН Тогда
						 ТребуетсяУИН = Истина;
					КонецЕсли;
					
					Товар.Вставить("instances",ДанныеЭкземпляров);
					
				КонецЕсли;
				ДанныеТоваров.Добавить(Товар);
			КонецЦикла; 
			ДанныеКоробки.items = ДанныеТоваров; 
			ДанныеКоробок.Добавить(ДанныеКоробки);
		КонецЦикла;
		
		ДанныеЗапроса = Новый Структура("boxes,allowRemove");
		ДанныеЗапроса.allowRemove = СоставЗаказаИзменен; 
		ДанныеЗапроса.boxes = ДанныеКоробок; 
		
		ТелоЗапроса = ВJSON(ДанныеЗапроса);
		
		HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоЗапроса, "UTF-8");
		
		HTTPОтвет    = HTTPСоединение.Записать(HTTPЗапрос);  
		КодСостояния = HTTPОтвет.КодСостояния;
		СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
		Результат    = ИзJSON(СтрокаОтвета);
		
		Если КодСостояния = 200  Тогда
			
			ЗаказПодготовлен = Истина; 	
		Иначе
			Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
				ОписаниеОшибок = ТекстОшибки(Результат.errors);
			Иначе
				ОписаниеОшибок = ""; 
			КонецЕсли; 	
			
			ЗаказПодготовлен = Ложь; 
			Отказ = Истина;

			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка выполнения запроса %1: %2%3';
					|en = 'An error occurred when executing the %1 request: %2%3'", 
					ОбщегоНазначения.КодОсновногоЯзыка()), 
				Сервер + АдресРесурса, 
				ОписаниеОшибок,
				СтрокаОтвета);
				
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
		КонецЕсли;
		
	Исключение 
		ЗаказПодготовлен = Ложь; 
		Отказ = Истина;
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
	КонецПопытки;
	
	Возврат ЗаказПодготовлен; 
	
КонецФункции 

// Получает статусы проверки УИН ювелирных изделий (см. https://api.partner.market.yandex.ru/campaigns/{campaignId}/orders/{orderId}/identifiers/status)
//
// Параметры: 
//   УчетнаяЗапись       - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   ИдентификаторЗаказа - Строка - идентификатор заказа по данным торговой площадки.
//
// Возвращаемое значение: 
//						Структура - результаты проверки УИН; 
//							*ЕстьУИНВОбработке - Булево - признак того, что по некоторым УИН проверка не завершена;
//							*ВсеУИНПрошлиПроверку - Булево - признак того, что все УИН подтверждены; 
//   				    	*ТаблицаЗначений - статусы проверки товаров заказа;
//								**id - Строка - идентификатор товара; 
//								**uin - Строка - УИН ювелирного изделия;
//								**status - Строка - статус проверки УИН.
//									***Возможные значения поля "status"
//										FAILED — не прошел проверку;
//										IN_PROGRESS — в процессе проверки;
//										NOT_ON_VALIDATION — УИН не отправлен на проверку или переданы не все УИНы в заказе;
//										OK — проверка успешно пройдена.
//
Функция ПолучитьСтатусыПроверкиУИН(УчетнаяЗапись, ИдентификаторЗаказа, Отказ = Ложь) Экспорт
		
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ТекстОшибки = НСтр("ru = 'Не указана учетная запись торговой площадки.';
							|en = 'The marketplace account is not specified.'",
		ОбщегоНазначения.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Ошибка,,, 
		ТекстОшибки);
		
	КонецЕсли;
	
	Если Не Справочники.УчетныеЗаписиМаркетплейсов.ОбновленияДанныхТорговойПлощадкиРазрешено(УчетнаяЗапись) Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Для учетной записи <%1> запрещено обновление данных торговой площадки';
			|en = 'The trading platform data update is not allowed for the <%1> account'",
		ОбщегоНазначения.КодОсновногоЯзыка()),
		УчетнаяЗапись);
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Ошибка,,, 
		ТекстОшибки);
	КонецЕсли;   
	
	Для Инд = 1 По 5 Цикл
		ОписаниеТипаСтрока50      = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50));
		
		ТаблицаТоваров = Новый ТаблицаЗначений;
		ТаблицаТоваров.Колонки.Добавить("id", ОписаниеТипаСтрока50);	
		ТаблицаТоваров.Колонки.Добавить("uin", ОписаниеТипаСтрока50);
		ТаблицаТоваров.Колонки.Добавить("status", ОписаниеТипаСтрока50);
		
		ЕстьУИНВОбработке = Ложь;
		ВсеУИНПрошлиПроверку = Истина;
		
		Попытка	  
			ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись, Ложь);
			ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
			ДанныеПриложения  = ДанныеПриложения();
			Сервер            = СерверПартнерскогоAPI();
			ИмяМетода         = "/identifiers/status";
			АдресРесурса      = "/" + ДанныеУчетнойЗаписи.ИдентификаторМагазина + "/orders/" + ИдентификаторЗаказа + ИмяМетода;
			
			ИнтернетПрокси = Неопределено;
			Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
				МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
				ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
			КонецЕсли;
			СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
			
			HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 180, СоединениеOpenSSL);
			
			Заголовки = Новый Соответствие;
			Заголовки.Вставить("Content-Type",  "application/json");
			Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
				Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
			ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
				Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
			КонецЕсли;
			
			HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
			HTTPЗапрос.УстановитьТелоИзСтроки("", "UTF-8");
			
			HTTPОтвет    = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
			КодСостояния = HTTPОтвет.КодСостояния;
			СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
			Результат    = ИзJSON(СтрокаОтвета);    
			СообщениеПользователю = "";
			
			Если КодСостояния = 200 И Результат.Свойство("result") Тогда
				
				Для Каждого Товар Из Результат.result.items Цикл 		 
					Если Товар.Свойство("uin") Тогда    
						Для Каждого УИН Из Товар.uin Цикл
							СтрокаРезультата = ТаблицаТоваров.Добавить();
							СтрокаРезультата.id = Товар.id;
							СтрокаРезультата.uin = УИН.value;
							СтрокаРезультата.status = УИН.status;
							Если УИН.status = "IN_PROGRESS" Тогда 
								ЕстьУИНВОбработке = Истина;
							КонецЕсли; 
							Если УИН.status <> "ОК" Тогда
								ВсеУИНПрошлиПроверку = Ложь;    
								
								Если УИН.status = "FAILED" Тогда 
									Статус = НСтр("ru = 'не прошел проверку.';
													|en = 'failed check.'");
								ИначеЕсли УИН.status = "" Тогда
									Статус = НСтр("ru = 'в процессе проверки. Повторите попытку позже.';
													|en = 'under check. Please try again later.'");
								ИначеЕсли УИН.status = "NOT_ON_VALIDATION" Тогда 
									Статус = НСтр("ru = 'не отправлен на проверку или переданы не все УИНы в заказе.';
													|en = 'not sent for check or not all payment IDs in the order were transferred.'");
								КонецЕсли;   
								СообщениеПользователю = СообщениеПользователю + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = 'Заказ %1 не может быть подтвержден. УИН %2 товара %3 %4
					          							|.';
					          							|en = 'The %1 order cannot be confirmed. Payment ID %2 of the %3 item is %4
					          							|.'"),
											ИдентификаторЗаказа,
											УИН.value,
											Товар.id,
											Статус);
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла                     
				
			Иначе
				Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
					ОписаниеОшибок = ТекстОшибки(Результат.errors);
				Иначе
					ОписаниеОшибок = ""; 
				КонецЕсли; 	
				
				СтатусИзменен = Ложь; 
				Отказ = Истина;
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка выполнения запроса %1: %2%3';
					|en = 'An error occurred when executing the %1 request: %2%3'", 
				ОбщегоНазначения.КодОсновногоЯзыка()), 
				Сервер + АдресРесурса, 
				ОписаниеОшибок,
				СтрокаОтвета);
				
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
			КонецЕсли;
			
		Исключение 
			Отказ = Истина;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
			ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
		КонецПопытки; 
		
		Если Не ЕстьУИНВОбработке Тогда
			Прервать;
		КонецЕсли;
		
		ОбщегоНазначенияБТС.Пауза(5);
	КонецЦикла;
			
	РезультатыПроверки = Новый Структура();
	РезультатыПроверки.Вставить("ЕстьУИНВОбработке", ЕстьУИНВОбработке);
	РезультатыПроверки.Вставить("ВсеУИНПрошлиПроверку", ВсеУИНПрошлиПроверку);   
	РезультатыПроверки.Вставить("СообщениеПользователю", СообщениеПользователю);
	РезультатыПроверки.Вставить("ТаблицаТоваров",ТаблицаТоваров);
	
	Возврат РезультатыПроверки;
		
КонецФункции

// Параметры:
//  ДанныеЗаказа  - Структура:
//   * delivery - Структура:
//      ** shipments - Массив из Структура:
//          *** boxes - Массив из Структура:
//               **** id - Число
//               **** fulfilmentId - Строка
//  ДанныеОтправлений - Неопределено
//                    - Массив из см. НовыеДанныеОтправления
//
Процедура ЗаполнитьДанныеОтправленийПослеПодтвержденияЗаказа(ДанныеЗаказа, ДанныеОтправлений)
	
	Если ДанныеОтправлений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ДанныеОтгрузки Из ДанныеЗаказа["delivery"]["shipments"] Цикл
		
		Если Не ДанныеОтгрузки.Свойство("boxes") Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ДанныеГрузовогоМеста Из ДанныеОтгрузки["boxes"] Цикл
			ДанныеОтправления = НовыеДанныеОтправления();
			ДанныеОтправления.ИдентификаторОтправления = ЧислоВСтроку(ДанныеГрузовогоМеста["id"]);
			ДанныеОтправления.НомерОтправления = ДанныеГрузовогоМеста["fulfilmentId"];
			ДанныеОтправления.ИдентификаторОтгрузки = ЧислоВСтроку(ДанныеОтгрузки["id"]); 
			ДанныеОтправлений.Добавить(ДанныеОтправления);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Конструктор данных отправления, полученных из сервиса.
// 
// Возвращаемое значение:
//  Структура:
//   * ИдентификаторОтправления - Строка
//   * НомерОтправления - Строка
//
Функция НовыеДанныеОтправления()
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторОтправления", "");
	Результат.Вставить("НомерОтправления", ""); 
	Результат.Вставить("ИдентификаторОтгрузки", "");
	Возврат Результат;
КонецФункции

Функция ПолучитьЭтикеткиОтправлений(УчетнаяЗапись, НомераОтправлений, ТребуетсяПолучениеЭтикеток = Ложь) Экспорт

	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ТекстОшибки = НСтр("ru = 'Не указана учетная запись торговой площадки.';
							|en = 'Marketplace personal account not specified.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		Ошибка.КодОшибки      = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаНеУказанаУчетнаяЗапись;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	Если НомераОтправлений <> Неопределено
			И ТипЗнч(НомераОтправлений) = Тип("Строка") Тогда
		НомерОтправления = НомераОтправлений;
		
		НомераОтправлений = Новый Массив;
		НомераОтправлений.Добавить(НомерОтправления);
	КонецЕсли;
	
	СтатусыОтправлений = РегистрыСведений.ЗаказыТорговыхПлощадок.СтатусыПечатиЭтикеток();

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ЗаказыТорговыхПлощадок.Заказ КАК Заказ,
		|	ЗаказыТорговыхПлощадок.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ЗаказыТорговыхПлощадок.НомерОтправления КАК НомерОтправления,
		|	ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки КАК ИдентификаторОтгрузки,
		|	ЗаказыТорговыхПлощадок.ИдентификаторОтправления КАК ИдентификаторОтправления,
		|	ЗаказыТорговыхПлощадок.НомерЗаказа КАК НомерЗаказа
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись
		|	И ЗаказыТорговыхПлощадок.Статус В(&СтатусыОтправлений)
		|	И ЗаказыТорговыхПлощадок.НомерОтправления <> &ПустаяСтрока";
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",     УчетнаяЗапись);
	Запрос.УстановитьПараметр("СтатусыОтправлений", СтатусыОтправлений);
	Запрос.УстановитьПараметр("ПустаяСтрока",      "");
	
	Если НомераОтправлений <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"И ЗаказыТорговыхПлощадок.НомерОтправления <> &ПустаяСтрока",
			"И ЗаказыТорговыхПлощадок.НомерОтправления В (&НомераОтправлений)");
		Запрос.УстановитьПараметр("НомераОтправлений", НомераОтправлений);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	ДанныеОтправлений = РезультатЗапроса.Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);       
	
	ДанныеОтправлений.Колонки.Добавить("ПутьКФайлу",      Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная)));
	ДанныеОтправлений.Колонки.Добавить("ОписаниеОшибки",  Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(0, ДопустимаяДлина.Переменная)));
		
	Отказ = Ложь;
	ПолучитьЭтикеткиВСервисе(УчетнаяЗапись, ДанныеОтправлений, Отказ);
	
	Если Отказ Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При получении этикеток с торговой площадки <%1> возникли ошибки. Подробнее см. журнал регистрации.';
				|en = 'Errors occurred when getting labels from the <%1> marketplace. For more information, see the event log.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УчетнаяЗапись);
		
		Ошибка.КодОшибки      = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = Ошибка.ОписаниеОшибки + ?(ЗначениеЗаполнено(Ошибка.ОписаниеОшибки), Символы.ПС, "") + ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	МассивОтправленийБезДокументовОтгрузки = Новый Массив;
		
	Для Каждого СтрокаТаблицыЗначений Из ДанныеОтправлений Цикл
		Если ЗначениеЗаполнено(СтрокаТаблицыЗначений.ОписаниеОшибки) Тогда
			Если Ошибка.Детализация = Неопределено Тогда
				Ошибка.Детализация = Новый Массив;
			КонецЕсли;
			Ошибка.Детализация.Добавить(СтрокаТаблицыЗначений.ОписаниеОшибки);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицыЗначений.ПутьКФайлу)
				И Не ЗначениеЗаполнено(СтрокаТаблицыЗначений.ОписаниеОшибки) Тогда
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ЗаказыТорговыхПлощадок");
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Разделяемый;
			ЭлементБлокировкиДанных.УстановитьЗначение("Заказ",            СтрокаТаблицыЗначений.Заказ);
			ЭлементБлокировкиДанных.УстановитьЗначение("ДокументОтгрузки", СтрокаТаблицыЗначений.ДокументОтгрузки);
			БлокировкаДанных.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ЗаказыТорговыхПлощадок.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Заказ.Установить(СтрокаТаблицыЗначений.Заказ);
			НаборЗаписей.Отбор.ДокументОтгрузки.Установить(СтрокаТаблицыЗначений.ДокументОтгрузки);
			НаборЗаписей.Прочитать();
			
			Если НаборЗаписей.Количество() > 0 Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицыЗначений.ПутьКФайлу) Тогда
					НаборЗаписей[0].ТребуетсяПолучениеЭтикеток = Ложь;
				ИначеЕсли ЗначениеЗаполнено(СтрокаТаблицыЗначений.ОписаниеОшибки) Тогда
					НаборЗаписей[0].ОписаниеОшибки = СтрокаТаблицыЗначений.ОписаниеОшибки;
				КонецЕсли;
				
				НаборЗаписей.Записать(Истина);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТаблицыЗначений.ПутьКФайлу) 
					И ЗначениеЗаполнено(СтрокаТаблицыЗначений.ДокументОтгрузки) Тогда
				АдресВременногоХранилищаФайла = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(СтрокаТаблицыЗначений.ПутьКФайлу));
				
				Если ЗначениеЗаполнено(АдресВременногоХранилищаФайла) Тогда
					ИмяФайла              = СтрЗаменить(СтрокаТаблицыЗначений.ПутьКФайлу, "\", "/");
					ИмяФайла              = Сред(ИмяФайла, СтрНайти(ИмяФайла, "/", НаправлениеПоиска.СКонца) + 1);
					ИмяФайлаБезРасширения = Сред(ИмяФайла, 1, СтрНайти(ИмяФайла, ".") - 1);
					РасширениеФайла       = НРег(Сред(ИмяФайла, СтрНайти(ИмяФайла, ".") + 1));
					
					ПараметрыФайла                    = РаботаСФайлами.ПараметрыДобавленияФайла();
					ПараметрыФайла.Автор              = Пользователи.АвторизованныйПользователь();
					ПараметрыФайла.ВладелецФайлов     = СтрокаТаблицыЗначений.ДокументОтгрузки;
					ПараметрыФайла.ИмяБезРасширения   = ИмяФайлаБезРасширения;
					ПараметрыФайла.РасширениеБезТочки = РасширениеФайла;
					ПараметрыФайла.ГруппаФайлов       = Неопределено;
					
					РаботаСФайлами.ДобавитьФайл(
						ПараметрыФайла, 
						АдресВременногоХранилищаФайла,
						Неопределено, 
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(   
							НСтр("ru = 'Этикетки по отправлению %1';
								|en = 'Labels for the %1 shipment'"),
							СтрокаТаблицыЗначений.НомерОтправления));
				КонецЕсли;
				
				УдалитьИзВременногоХранилища(АдресВременногоХранилищаФайла);
				ФайловаяСистема.УдалитьВременныйФайл(СтрокаТаблицыЗначений.ПутьКФайлу);
			ИначеЕсли Не ЗначениеЗаполнено(СтрокаТаблицыЗначений.ДокументОтгрузки) Тогда
				МассивОтправленийБезДокументовОтгрузки.Добавить(СтрокаТаблицыЗначений.НомерОтправления);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			Отказ = Истина;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При записи признака получения этикетки %1, %2, %3 возникла ошибка: %4';
					|en = 'An error occurred when saving the %1, %2, %3 label receipt flag: %4'", 
					ОбщегоНазначения.КодОсновногоЯзыка()),
				СтрокаТаблицыЗначений.УчетнаяЗапись, 
				СтрокаТаблицыЗначений.Заказ, 
				СтрокаТаблицыЗначений.ДокументОтгрузки,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстСообщения);
		КонецПопытки;
	КонецЦикла;
	
	Если МассивОтправленийБезДокументовОтгрузки.Количество() > 0 Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Полученные с торговой площадки <%1> этикетки не сохранены для отправлений: %2. Требуется оформить документы отгрузки.';
				|en = 'Labels received from the <%1> marketplace are not saved for shipments: %2. Register shipping documents.'"),
			УчетнаяЗапись,
			СтрСоединить(МассивОтправленийБезДокументовОтгрузки, ", "));
		
		Ошибка.КодОшибки      = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = Ошибка.ОписаниеОшибки + ?(ЗначениеЗаполнено(Ошибка.ОписаниеОшибки), Символы.ПС, "") + ТекстОшибки;
	КонецЕсли;
	
	Если Отказ Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При получении этикеток с торговой площадки <%1> возникли ошибки. Подробнее см. журнал регистрации.';
				|en = 'Errors occurred when getting labels from the <%1> marketplace. For more information, see the event log.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УчетнаяЗапись);
			
		Ошибка.КодОшибки      = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = Ошибка.ОписаниеОшибки + ?(ЗначениеЗаполнено(Ошибка.ОписаниеОшибки), Символы.ПС, "") + ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	Если Ошибка.Детализация <> Неопределено Тогда
		Ошибка.КодОшибки      = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При получении этикеток с торговой площадки <%1> возникли ошибки.';
				|en = 'Errors occurred when receiving labels from the <%1> marketplace.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УчетнаяЗапись);
	КонецЕсли;
	
	Возврат Ошибка;
	
КонецФункции   

// https://api.partner.market.yandex.ru/campaigns/{campaignId}/orders/{orderId}/delivery/shipments/{shipmentId}/boxes/{boxId}/label 
Процедура ПолучитьЭтикеткиВСервисе(УчетнаяЗапись, ДанныеОтправлений, Отказ)  

	ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись, Ложь);
	ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
	ДанныеПриложения = ДанныеПриложения();
	Сервер           = СерверПартнерскогоAPI();	
	
	Для каждого СтрокаТаблицы Из ДанныеОтправлений Цикл 
		
		ПараметрыURL = Новый Массив;
		ПараметрыURL.Добавить("format=A9");		
		
		Попытка
			
			АдресРесурса      = "/" + ДанныеУчетнойЗаписи.ИдентификаторМагазина + "/orders/" 
									+ СтрокаТаблицы.НомерЗаказа + "/delivery/shipments/"
									+ СтрокаТаблицы.ИдентификаторОтгрузки + "/boxes/"
									+ СтрокаТаблицы.ИдентификаторОтправления + "/label";	

			ИнтернетПрокси = Неопределено;
			Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
				МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
				ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
			КонецЕсли;
			СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
			
			HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 60, СоединениеOpenSSL);
			
			Заголовки = Новый Соответствие;
			Заголовки.Вставить("Content-Type",  "application/json");
			Если ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("apikey_token") Тогда
				Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
			ИначеЕсли ДанныеАвторизации<>Неопределено И ДанныеАвторизации.Свойство("access_token") Тогда
				Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token + ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
			КонецЕсли;
			
			HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
			
			HTTPОтвет    = HTTPСоединение.Получить(HTTPЗапрос);  
			КодСостояния = HTTPОтвет.КодСостояния;
			
			Если КодСостояния <> 200 Тогда 
				СтрокаОтвета = HTTPОтвет.ПолучитьТелоКакСтроку();
				Результат    = ИзJSON(СтрокаОтвета);

				Если Результат.Свойство("status") И Результат.status = "ERROR" Тогда
					ОписаниеОшибок = ТекстОшибки(Результат.errors);
				Иначе
					ОписаниеОшибок = ""; 
				КонецЕсли;     
				
				СтрокаТаблицы.ОписаниеОшибки = ОписаниеОшибок; 
				
				Отказ = Истина;
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка выполнения запроса %1: %2%3';
					|en = 'An error occurred when executing the %1 request: %2%3'", 
				ОбщегоНазначения.КодОсновногоЯзыка()), 
				Сервер + АдресРесурса, 
				ОписаниеОшибок,
				СтрокаОтвета);
				
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);   
			Иначе   
				ИмяВременногоФайла = ПолучитьИмяВременногоФайла("pdf");
    		    ДвоичныеДанные = HTTPОтвет.ПолучитьТелоКакДвоичныеДанные();
       			ДвоичныеДанные.Записать(ИмяВременногоФайла);
				СтрокаТаблицы.ПутьКФайлу = ИмяВременногоФайла;					
			КонецЕсли;
			
		Исключение 
			Отказ = Истина;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'", 
			ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ТекстОшибки);
		КонецПопытки;  
		
	КонецЦикла;
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
//   * НачалоПериода - Дата
//   * ОкончаниеПериода - Дата
//   * ВидФильтраПоПериоду - Строка
//   * СтатусЗаказа - ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок 
//   * ПараметрыЗапроса - см. НовыйПараметрыПолученияЗаказов
//   * РазвернутьДоТоваров - Булево
//
Функция НовыеПараметрыПолученияДанныхЗаказов()
	
	Результат = Новый Структура;
	
	Результат.Вставить("НачалоПериода", Дата(1, 1, 1));
	Результат.Вставить("ОкончаниеПериода", Дата(1, 1, 1));
	Результат.Вставить("ВидФильтраПоПериоду", "");
	Результат.Вставить("СтатусЗаказа", Перечисления.СтатусыЗаказовТорговыхПлощадок.ПустаяСсылка());
	Результат.Вставить("ПараметрыЗапроса", НовыйПараметрыПолученияЗаказов());
	Результат.Вставить("РазвернутьДоТоваров", Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция РазбитьМассивИдентификаторовНаПорции(МассивИдентификаторов, РазмерПорции = 50)
	
	Результат = Новый Массив; // Массив из Массив
	ТекущаяПорция = Новый Массив; // Массив из Строка
	
	Для Индекс = 0 По МассивИдентификаторов.ВГраница() Цикл
		
		Идентификатор = МассивИдентификаторов[Индекс];
		Идентификатор = ФорматированныйИдентификаторЗаказа(Идентификатор);
		ТекущаяПорция.Добавить(Идентификатор);
		
		Если ТекущаяПорция.Количество() = РазмерПорции Тогда
			Результат.Добавить(ТекущаяПорция);
			ТекущаяПорция = Новый Массив;
		КонецЕсли;
	КонецЦикла;
	
	Если ТекущаяПорция.Количество() > 0 Тогда
		Результат.Добавить(ТекущаяПорция);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ФорматированныйИдентификаторЗаказа(Знач ИдентификаторЗаказа)
	
	ТипИдентификатора = ТипЗнч(ИдентификаторЗаказа);
	
	Если ТипИдентификатора = Тип("Число") Тогда
		ИдентификаторЗаказа = ЧислоВСтроку(ИдентификаторЗаказа);
	Иначе
		Если ТипИдентификатора <> Тип("Строка") Тогда
			ИдентификаторЗаказа = Строка(ИдентификаторЗаказа);
		КонецЕсли;
		ИдентификаторЗаказа = СтрЗаменить(ИдентификаторЗаказа, Символ(160), "");
	КонецЕсли;
	
	Возврат ИдентификаторЗаказа;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//   * ИдентификаторЗаказа - Строка
//   * НомерЗаказа - Строка
//   * ИдентификаторОтправления - Строка
//   * НомерОтправления - Строка
//   * ИдентификаторОтгрузки - Строка
//   * ДатаОтправления - Дата
//   * ДатаНачалаОбработкиОтправления - Дата
//   * СтатусОтправления - Строка
//                       - ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок
//   * ПодстатусОтправления - Строка
//   * ДатаОтгрузки - Дата
//   * ТипДоставки - Строка
//   * СпособДоставки - Строка
//   * РегионДоставки - Строка
//   * ГородДоставки - Строка
//   * ДатаДоставки - Дата
//   * СтатусПокупателя - Булево
//   * СпособОплаты - Строка
//   * ИдентификаторПричиныОтмены - Строка
//   * ПричинаОтмены - Строка
//   * ПричинаОтменыОтправления - Строка
//   * ИнициаторОтмены - Строка
//   * КомментарийКЗаказу - Строка
//   * ТребуетсяМаркировка - Булево
//   * ТребуетсяУИНЮвелирногоИзделия - Булево
//   * ТребуетсяРНПТ - Булево
//   * ТребуетсяГТД - Булево
//   * СписокТоваровВОтправлении - Неопределено
//                               - см. НовыйСписокТоваровВОтправлении
//
Функция НовыеДанныеЗаказаИзСервиса()
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторЗаказа", "");
	Результат.Вставить("НомерЗаказа", "");
	Результат.Вставить("ИдентификаторОтправления", "");
	Результат.Вставить("НомерОтправления", "");
	Результат.Вставить("ИдентификаторОтгрузки", "");
	Результат.Вставить("ДатаОтправления", Дата(1, 1, 1));
	Результат.Вставить("ДатаНачалаОбработкиОтправления", Дата(1, 1, 1));
	Результат.Вставить("СтатусОтправления", "");
	Результат.Вставить("ПодстатусОтправления", "");
	Результат.Вставить("ДатаОтгрузки", Дата(1, 1, 1));
	
	Результат.Вставить("ТипДоставки", "");
	Результат.Вставить("СпособДоставки", "");
	Результат.Вставить("РегионДоставки", "");
	Результат.Вставить("ГородДоставки", "");
	Результат.Вставить("ДатаДоставки", Дата(1, 1, 1));
	
	Результат.Вставить("СтатусПокупателя", Ложь);
	Результат.Вставить("СпособОплаты", "");
	
	Результат.Вставить("ИдентификаторПричиныОтмены", "");
	Результат.Вставить("ПричинаОтмены", "");
	Результат.Вставить("ПричинаОтменыОтправления", "");
	Результат.Вставить("ИнициаторОтмены", "");
	
	Результат.Вставить("КомментарийКЗаказу", "");
	
	Результат.Вставить("ТребуетсяМаркировка", Ложь);
	Результат.Вставить("ТребуетсяУИНЮвелирногоИзделия", Ложь);
	Результат.Вставить("ТребуетсяРНПТ", Ложь);
	Результат.Вставить("ТребуетсяГТД", Ложь);
	
	Результат.Вставить("СписокТоваровВОтправлении", Неопределено);
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  ТаблицаЗначений:
//   * ИдентификаторПубликации - Строка
//   * ИдентификаторОбъектаМаркетплейса - Строка
//   * ИдентификаторSKU - Строка
//   * ПредставлениеОбъектаМаркетплейса - Строка
//   * Количество -Число
//   * Цена - Число
//   * Валюта - Строка
//
Функция НовыйСписокТоваровВОтправлении()
	
	ОписаниеТипаСтрока3   = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(3));
	ОписаниеТипаСтрока50  = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50));
	ОписаниеТипаСтрока250 = Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(250));
	ОписаниеТипаЧисло15_2 = Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ДенежнаяСуммаНеотрицательная.Тип);
	ОписаниеТипаЧисло15_3 = Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Любой));
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ИдентификаторПубликации",          ОписаниеТипаСтрока50);
	Результат.Колонки.Добавить("ИдентификаторОбъектаМаркетплейса", ОписаниеТипаСтрока50);
	Результат.Колонки.Добавить("ИдентификаторSKU",                 ОписаниеТипаСтрока50);
	Результат.Колонки.Добавить("ПредставлениеОбъектаМаркетплейса", ОписаниеТипаСтрока250);
	Результат.Колонки.Добавить("Количество",                       ОписаниеТипаЧисло15_3);
	Результат.Колонки.Добавить("Цена",                             ОписаниеТипаЧисло15_2);
	Результат.Колонки.Добавить("Валюта",                           ОписаниеТипаСтрока3);
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеЗаказаИзСервиса(Заказ, Отгрузка, Отправление, Товар)
	
	Результат = НовыеДанныеЗаказаИзСервиса();
	Результат.ИдентификаторЗаказа      = ЧислоВСтроку(Заказ["id"]);
	Результат.НомерЗаказа              = Результат.ИдентификаторЗаказа;
	Результат.ИдентификаторОтправления = ЧислоВСтроку(Отправление["id"]);
	Результат.НомерОтправления         = Отправление["fulfilmentId"];
	Результат.ИдентификаторОтгрузки    = ЧислоВСтроку(Отгрузка["id"]);
	
	Результат.ТипДоставки      = Заказ["delivery"]["serviceName"];
	Результат.СтатусПокупателя = (Заказ["buyer"]["type"] = "BUSINESS");
	Результат.СпособОплаты     = ПреобразоватьСпособОплаты(Заказ["paymentMethod"], Заказ["paymentType"]);
	
	Результат.ДатаОтправления = СтроковыеФункцииКлиентСервер.СтрокаВДату(
		Заказ["creationDate"], ЧастиДаты.ДатаВремя);
	Результат.ДатаНачалаОбработкиОтправления = СтроковыеФункцииКлиентСервер.СтрокаВДату(
		Заказ["updatedAt"], ЧастиДаты.ДатаВремя);
	Результат.ДатаОтгрузки = СтроковыеФункцииКлиентСервер.СтрокаВДату(
		Отгрузка["shipmentDate"], ЧастиДаты.Дата);
	Результат.ДатаДоставки = СтроковыеФункцииКлиентСервер.СтрокаВДату(
		Заказ["delivery"]["dates"]["toDate"], ЧастиДаты.Дата);
	
	Если Заказ["delivery"]["region"]["parent"]["type"] = "REPUBLIC" Тогда
		Результат.РегионДоставки = Заказ["delivery"]["region"]["parent"]["name"];
	КонецЕсли;
	Если Заказ ["delivery"]["region"]["type"] = "CITY" Тогда
		Результат.ГородДоставки = Заказ["delivery"]["region"]["name"];
	КонецЕсли;
	Если Заказ.Свойство("notes") Тогда
		Результат.КомментарийКЗаказу = Заказ["notes"];
	КонецЕсли;
	
	Если Заказ["status"] = "CANCELLED" Тогда
		Результат.ИдентификаторПричиныОтмены = Заказ["substatus"];
		ПричиныОтмены = ПолучитьПричиныОтмены(Результат.ИдентификаторПричиныОтмены);

		Если ПричиныОтмены.Детализация.Количество() > 0 Тогда
			ПричинаОтмены = ПричиныОтмены.Детализация[0];
			Если ПричинаОтмены.Доступность Тогда
				Результат.ПричинаОтмены   = ПричинаОтмены.Наименование;
				Результат.ИнициаторОтмены = ПричинаОтмены.Инициатор;
			КонецЕсли;
		КонецЕсли;
		Результат.ПричинаОтменыОтправления = Результат.ПричинаОтмены;
	КонецЕсли;
	
	Если Товар <> Неопределено Тогда
		Результат.СтатусОтправления    = Заказ["status"];
		Результат.ПодстатусОтправления = Заказ["substatus"];
		Результат.СпособДоставки       = Заказ["delivery"]["type"];
		
		Если Товар.Свойство("requiredInstanceTypes") Тогда // Свойства может не быть, если нет требований к маркировке
			ТребованияПоМаркировке = Товар["requiredInstanceTypes"];
			ЗаполнитьТребованиеПоМаркировке(Результат, ТребованияПоМаркировке, "CIS", "ТребуетсяМаркировка");
			ЗаполнитьТребованиеПоМаркировке(Результат, ТребованияПоМаркировке, "CIS_OPTIONAL", "ТребуетсяМаркировка");
			ЗаполнитьТребованиеПоМаркировке(Результат, ТребованияПоМаркировке, "UIN", "ТребуетсяУИНЮвелирногоИзделия");
			ЗаполнитьТребованиеПоМаркировке(Результат, ТребованияПоМаркировке, "RNPT", "ТребуетсяРНПТ");
			ЗаполнитьТребованиеПоМаркировке(Результат, ТребованияПоМаркировке, "GTD", "ТребуетсяГТД");
		КонецЕсли;
	Иначе
		Результат.СтатусОтправления = Перечисления.СтатусыЗаказовТорговыхПлощадок.ПолучитьПоПредставлению(
			Заказ["status"],
			Заказ["substatus"],
			Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет);
		Результат.СпособДоставки = ПреобразоватьСпособДоставки(Заказ["delivery"]["type"]);
		
		СписокТоваровВОтправлении = НовыйСписокТоваровВОтправлении();
		Для Каждого Товар Из Заказ["items"] Цикл
			ДанныеСтроки = ДанныеСтрокиЗаказаИзСервиса(Заказ, Товар);
			ЗаполнитьЗначенияСвойств(СписокТоваровВОтправлении.Добавить(), ДанныеСтроки);
		КонецЦикла;
		
		Результат.СписокТоваровВОтправлении = СписокТоваровВОтправлении;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьТребованиеПоМаркировке(ДанныеЗаказа, ТребованияПоМаркировке, ИмяТребования, ИмяЗаполняемогоПоля)
	
	ЕстьТребование = Ложь;
	Если ТребованияПоМаркировке.Найти(ИмяТребования) <> Неопределено Тогда
		ЕстьТребование = Истина;
	КонецЕсли;
	
	ДанныеЗаказа[ИмяЗаполняемогоПоля] = ЕстьТребование;
	
КонецПроцедуры

Функция НовыеДанныеСтрокиЗаказаИзСервиса()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ИдентификаторПубликации", "");
	Результат.Вставить("ИдентификаторОбъектаМаркетплейса", "");
	Результат.Вставить("ИдентификаторSKU", "");
	Результат.Вставить("ПредставлениеОбъектаМаркетплейса", "");
	
	Результат.Вставить("Количество", 0);
	Результат.Вставить("Цена", 0);
	Результат.Вставить("Валюта", "");
	Результат.Вставить("ЦенаДляКлиента", 0);
	Результат.Вставить("ЦенаДоСкидки", 0);
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеСтрокиЗаказаИзСервиса(Заказ, Товар)
	
	Результат = НовыеДанныеСтрокиЗаказаИзСервиса();
	
	Результат.ИдентификаторПубликации          = Товар["offerId"];
	Результат.ИдентификаторОбъектаМаркетплейса = ЧислоВСтроку(Товар["id"]);
	Результат.ИдентификаторSKU                 = Товар["offerId"];
	Результат.ПредставлениеОбъектаМаркетплейса = Товар["offerName"];
	Результат.Количество                       = Товар["count"];
	Результат.Цена                             = Товар["price"];
	Результат.Валюта                           = Заказ["currency"];
	Результат.ЦенаДляКлиента                   = Товар["buyerPrice"];
	Результат.ЦенаДоСкидки                     = Товар["buyerPriceBeforeDiscount"];
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СервисныеФункцииСлужебный

// Выполняет запрос в сервис.
//
// Параметры:
//   УчетнаяЗапись   - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   ТекстЗапроса    - Строка - текст запроса.
//   ЭтоЗапросДанных - Булево - для GET-методов Истина, для POST-методов Ложь.
//   ПоМагазину      - Булево - признак запроса: по магазину - Истина, по кабинету - Ложь.
//   ДанныеЗапроса   - Неопределено, Структура из Строка - данные тела запроса.
//
// Возвращаемое значение:
//   Структура - ответ сервиса:
//     * КодСостояния - Число, Строка - код состояния запроса.
//     * Статус       - Строка - статус запроса, см. ВозможныеСтатусыОтветаСервиса.
//     * Результат    - Неопределено, Произвольный - результат выполнения метода API.
//     * Ошибки       - Неопределено, Произвольный - описание ошибок выполнения метода API.
//
Функция ВыполнитьЗапросВСервис(УчетнаяЗапись, ТекстЗапроса, ЭтоЗапросДанных = Истина,
			ПоМагазину = Истина, ДанныеЗапроса = Неопределено)
	
	ОтветСервиса = Новый Структура;
	ОтветСервиса.Вставить("КодСостояния", "");
	ОтветСервиса.Вставить("Статус",       "");
	ОтветСервиса.Вставить("Результат",    Неопределено);
	ОтветСервиса.Вставить("Ошибки",       Неопределено);
	
	ДанныеУчетнойЗаписи = ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись, Ложь);
	
	Попытка
		ДанныеАвторизации = ДанныеАвторизацииПоУчетнойЗаписи(ДанныеУчетнойЗаписи);
		ДанныеПриложения  = ДанныеПриложения();
		Сервер            = СерверПартнерскогоAPI(ПоМагазину);
		
		АдресРесурса = СтрШаблон("/%1/%2",
			?(ПоМагазину, ДанныеУчетнойЗаписи.ИдентификаторМагазина, ДанныеУчетнойЗаписи.ИдентификаторКабинета),
			ТекстЗапроса);
		
		ИнтернетПрокси = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
			ИнтернетПрокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
		КонецЕсли;
		СоединениеOpenSSL = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
		
		HTTPСоединение = Новый HTTPСоединение(Сервер,,,, ИнтернетПрокси, 60, СоединениеOpenSSL);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type", "application/json");
		Если ДанныеАвторизации <> Неопределено Тогда
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеАвторизации, "apikey_token") Тогда
				Заголовки.Вставить("Api-Key", ДанныеАвторизации.apikey_token);
			ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеАвторизации, "access_token") Тогда
				Заголовки.Вставить("Authorization", "OAuth oauth_token=" + ДанныеАвторизации.access_token
					+ ", oauth_client_id=" + ДанныеПриложения.IDПриложения);
			КонецЕсли;
		КонецЕсли;
		
		HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
		Если ЗначениеЗаполнено(ДанныеЗапроса) Тогда
			HTTPЗапрос.УстановитьТелоИзСтроки(ВJSON(ДанныеЗапроса), "UTF-8");
		КонецЕсли;
		
		Если ЭтоЗапросДанных Тогда
			HTTPОтвет = HTTPСоединение.Получить(HTTPЗапрос);
		Иначе
			HTTPОтвет = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
		КонецЕсли;
		
		Результат = ИзJSON(HTTPОтвет.ПолучитьТелоКакСтроку());
		
		ОжидаемыйСоставОтвета = Новый Структура;
		ОжидаемыйСоставОтвета.Вставить("status", ""); // см. ВозможныеСтатусыОтветаСервиса.
		ОжидаемыйСоставОтвета.Вставить("result", Неопределено);
		ОжидаемыйСоставОтвета.Вставить("errors", Неопределено);
		ЗаполнитьЗначенияСвойств(ОжидаемыйСоставОтвета, Результат);
		
		ОтветСервиса.КодСостояния = HTTPОтвет.КодСостояния;
		ОтветСервиса.Статус       = ОжидаемыйСоставОтвета.status;
		ОтветСервиса.Результат    = ОжидаемыйСоставОтвета.result;
		ОтветСервиса.Ошибки       = ОжидаемыйСоставОтвета.errors;
		
	Исключение
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отсутствует соединение с сервером %1 по причине: %2';
				|en = 'No connection with the %1 server. Reason: %2'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
	КонецПопытки;
	
	Возврат ОтветСервиса;
	
КонецФункции

// Преобразует данные в строку JSON.
//
// Параметры:
//   Значение - Произвольный - объект записи JSON. Представляет собой значение произвольного типа.  
//                Ограничения см. в описании функции глобального контекста ЗаписатьJSON.
//
// Возвращаемое значение:
//   Строка - строка JSON.
//
Функция ВJSON(Значение)

	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписьJSON.ПроверятьСтруктуру = Истина;
	ЗаписатьJSON(ЗаписьJSON, Значение);
	
	Возврат ЗаписьJSON.Закрыть();

КонецФункции

// Формирует представление даты в формате Coordinated Universal Time.
//
// Параметры:
//   Дата        - Дата - дата/время, представление которой необходимо получить.
//   Миллисекунд - Число - миллисекунд во времени.
//   ЧасовойПояс - Строка, Неопределено - идентификатор часового пояса.
//
// Возвращаемое значение:
//   Строка - представление даты в формате Coordinated Universal Time (UTC).
//
Функция ДатаUTC(Знач Дата, Миллисекунд = 0, ЧасовойПояс = Неопределено)
	
	Миллисекунд = Мин(Миллисекунд, 999);
	Миллисекунд = Макс(Миллисекунд, 0);
	
	Если ЧасовойПояс = Неопределено Тогда
		ЧасовойПояс = ЧасовойПоясСеанса();
	КонецЕсли;
	
	Дата = УниверсальноеВремя(Дата, ЧасовойПояс);
	
	Результат = Формат(Дата, "ДФ=yyyy-MM-ddTЧЧ:мм:сс") + "." + Формат(Миллисекунд, "ЧН=; ЧГ=0") + "Z";
	
	Возврат Результат;
	
КонецФункции

// Список возможных статусов ответа сервиса.
//
// Возвращаемое значение:
//   Соответствие из Строка - соответствие статусов сервиса идентификаторам анализа.
//
Функция ВозможныеСтатусыОтветаСервиса()
	
	ВозможныеСтатусы = Новый Соответствие;
	ВозможныеСтатусы.Вставить("OK",    "БезОшибки");
	ВозможныеСтатусы.Вставить("ERROR", "Ошибка");
	
	Возврат ВозможныеСтатусы;
	
КонецФункции

// Возвращает описание ошибки из сервиса.
//
// Параметры:
//   ОтветСервиса - см. ВыполнитьЗапросВСервис.
//   ЗаписатьВЖурналРегистрации - Булево - признак записи в журнал регистрации.
//   Ошибка - Неопределено,
//          - см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
// Возвращаемое значение:
//   см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ОписаниеОшибкиИзСервиса(ОтветСервиса, ЗаписатьВЖурналРегистрации = Ложь, Знач Ошибка = Неопределено)
	
	Если Не ЗначениеЗаполнено(Ошибка) Тогда
		Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	КонецЕсли;
	
	ТекстыОшибок = Новый Массив;
	Если ВозможныеСтатусыОтветаСервиса()[ВРег(ОтветСервиса.Статус)] = "Ошибка" Тогда
		МассивОшибокОтвета = Новый Массив;
		Если ТипЗнч(ОтветСервиса.Ошибки) = Тип("Массив") Тогда
			МассивОшибокОтвета = ОтветСервиса.Ошибки;
		ИначеЕсли ЗначениеЗаполнено(ОтветСервиса.Ошибки) Тогда
			МассивОшибокОтвета.Добавить(ОтветСервиса.Ошибки);
		КонецЕсли;
		
		Для Каждого ОписаниеОшибкиОтвета Из МассивОшибокОтвета Цикл
			СоставОшибки = Новый Структура;
			СоставОшибки.Вставить("code",    "");
			СоставОшибки.Вставить("message", "");
			ЗаполнитьЗначенияСвойств(СоставОшибки, ОписаниеОшибкиОтвета);
			
			Если ЗначениеЗаполнено(СоставОшибки.message) Тогда
				Если ЗначениеЗаполнено(СоставОшибки.code) Тогда
					ШаблонОшибки = НСтр("ru = '%1 (код ошибки %2)';
										|en = '%1 (код ошибки %2)'");
				Иначе
					ШаблонОшибки = НСтр("ru = '%1';
										|en = '%1'");
				КонецЕсли;
				
				ТекстыОшибок.Добавить(СтрШаблон(ШаблонОшибки, СоставОшибки.message, СоставОшибки.code));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ТекстыОшибок.Количество() > 0 Тогда
		Если ПустаяСтрока(Ошибка.КодОшибки) Тогда
			Ошибка.КодОшибки = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
		КонецЕсли;
		
		Если ПустаяСтрока(Ошибка.ОписаниеОшибки) Тогда
			Если ТекстыОшибок.Количество() = 1 Тогда
				Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось выполнить запрос в сервис по причине: %1.';
						|en = 'Cannot execute the request to the service. Reason: %1'"),
					ТекстыОшибок[0]);
			Иначе
				Ошибка.ОписаниеОшибки = НСтр("ru = 'Не удалось выполнить запрос в сервис по причине:';
											|en = 'Cannot execute the request to the service. Reason:'");
				Ошибка.Детализация = ТекстыОшибок;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗаписатьВЖурналРегистрации Тогда
			Если ЗначениеЗаполнено(Ошибка.Детализация) Тогда
				Шаблон = "%1:
						 |	- %2.";
				ТекстОшибки = СтрШаблон(Шаблон, СтрСоединить(Ошибка.Детализация, Символы.ПС + Символы.Таб + "- "));
			Иначе
				ТекстОшибки = Ошибка.ОписаниеОшибки;
			КонецЕсли;
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ошибка;
	
КонецФункции

#Область КонструкторыПараметровЗапросов

// Конструктор списка параметров для импорта товарного каталога.
// 
// Возвращаемое значение:
//   Структура - список параметров:
//     * КоличествоНаСтранице    - Число - количество товаров на одной странице (limit);
//     * ИдентификаторСтраницы   - Строка - идентификатор страницы c результатами (page_token);
//     * Архивный                - Булево - фильтр по нахождению в архиве (archived);
//     * СтатусыКарточек         - Неопределено, Массив Из Строка - фильтр по статусам карточек (cardStatuses);
//     * ИдентификаторыКатегорий - Неопределено, Массив Из Число - фильтр по категориям на Маркете (categoryIds);
//     * ИдентификаторыТоваров   - Неопределено, Массив Из Строка - идентификаторы товаров, информация о которых нужна (offerIds);
//     * Теги                    - Неопределено, Массив Из Строка - фильтр по тегам (tags);
//     * Бренды                  - Неопределено, Массив Из Строка - фильтр по брендам (vendorNames).
//
Функция НовыеПараметрыЗапросаИмпортаТоварногоКаталога()
	
	Результат = Новый Структура;
	Результат.Вставить("КоличествоНаСтранице",    200);
	Результат.Вставить("ИдентификаторСтраницы",   "");
	Результат.Вставить("Архивный",                Ложь);
	Результат.Вставить("СтатусыКарточек",         Неопределено);
	Результат.Вставить("ИдентификаторыКатегорий", Неопределено);
	Результат.Вставить("ИдентификаторыТоваров",   Неопределено);
	Результат.Вставить("Теги",                    Неопределено);
	Результат.Вставить("Бренды",                  Неопределено);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область КонструкторыСтруктурДанных

// Конструктор таблицы значений для описания товаров, полученных из сервиса.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица с колонками:
//     * УчетнаяЗапись               - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//     * ИдентификаторМагазина       - Строка - идентификатор магазина;
//     * СтатусВМагазине             - Строка - статус товара в магазине;
//     * ИдентификаторКатегории      - Строка - идентификатор категории на Маркете, в которую попал товар;
//     * НаименованиеКатегории       - Строка - название категории карточки на Маркете;
//     * ИдентификаторМодели         - Строка - идентификатор модели на Маркете;
//     * НаименованиеМодели          - Строка - название модели на Маркете;
//     * ИдентификаторКарточкиТовара - Строка - идентификатор карточки на Маркете;
//     * НаименованиеКарточкиТовара  - Строка - название карточки товара;
//     * ИдентификаторПредложения    - Строка - идентификатор товара в вашей системе;
//     * ПредставлениеТовара         - Строка - наименование товара;
//     * ОписаниеТовара              - Строка - подробное описание товара: например, его преимущества и особенности;
//     * Статус                      - ПеречислениеСсылка.СтатусыВыгрузкиТоваровЯндексМаркет - статус карточки товара;
//     * ОписаниеОшибки              - Строка - описание ошибки;
//     * Архивный                    - Булево - товар помещен в архив;
//     * Штрихкоды                   - Массив Из Строка - штрихкоды;
//     * Штрихкод                    - Строка - штрихкоды строкой;
//     * СтраныПроисхождения         - Массив Из Строка - страны, где был произведен товар;
//     * СтранаПроисхождения         - Строка - страна, где был произведен товар;
//     * ТоварнаяКатегория           - Строка - категория товара в вашем магазине;
//     * Артикул                     - Строка - артикул товара от производителя;
//     * Бренд                       - Строка - название бренда или производителя;
//     * Длина                       - Число - длина упаковки в см;
//     * Ширина                      - Число - ширина упаковки в см;
//     * Высота                      - Число - высота упаковки в см;
//     * Вес                         - Число - вес товара в кг с учетом упаковки (брутто);
//     * КодВалюты                   - Строка - код валюты; 
//     * ЦенаДоСкидки                - ОпределяемыйТип.ДенежнаяСуммаНеотрицательная - цена до скидки;
//     * ЦенаСоСкидкой               - ОпределяемыйТип.ДенежнаяСуммаНеотрицательная - цена со скидкой;
//     * ДатаОбновленияЦены          - Дата - время последнего обновления.
//     * ТолькоДляВзрослых           - Булево - включает для товара пометку 18+;
//     * ОграничениеПоВозрасту       - Неопределено, Структура - товар не предназначен для детей младше определенного возраста:
//       ** ЕдиницаИзмерения           - Строка - единица измерения;
//       ** Значение                   - Число - значение.
//     * КоличествоГрузовыхМест      - Число - количество грузовых мест;
//     * Сертификаты                 - Массив Из Строка - номера документов на товар: сертификата, декларации соответствия и т.п.;
//     * КодТНВЭД                    - Строка - код товара в единой Товарной номенклатуре внешнеэкономической деятельности (ТН ВЭД);
//     * Цифровой                    - Булево - признак цифрового товара. Истина, если товар доставляется по электронной почте;
//     * ГарантийныйПериод           - Неопределено, Структура - гарантийный срок, в течение которого можно бесплатно заменить или починить товар:
//       ** ЕдиницаИзмерения           - Строка - единица измерения;
//       ** Значение                   - Число - значение;
//       ** Комментарий                - Строка - комментарий.
//     * СрокСлужбы                  - Неопределено, Структура - период, в течение которого товар должен исправно выполнять свою функцию:
//       ** ЕдиницаИзмерения           - Строка - единица измерения;
//       ** Значение                   - Число - значение;
//       ** Комментарий                - Строка - комментарий.
//     * СрокГодности                - Неопределено, Структура - период, по прошествии которого товар становится непригоден:
//       ** ЕдиницаИзмерения           - Строка - единица измерения;
//       ** Значение                   - Число - значение;
//       ** Комментарий                - Строка - комментарий.
//     * Инструкции                  - Массив Из Структура - список инструкций по использованию товара:
//       ** Ссылка                     - Строка - ссылка на инструкцию;
//       ** Наименование               - Строка - название инструкции, которое будет отображаться на карточке товара.
//     * Изображения                 - Массив Из Строка - ссылки на изображения товара. Изображение по первой ссылке считается основным, остальные дополнительными;
//     * Видео                       - Массив Из Строка - ссылки (URL) на видео товара;
//     * Теги                        - Массив Из Строка - метки товара, используемые магазином;
//     * ТипТовара                   - Строка -Особый тип товара. 
//
Функция НоваяТаблицаОписанияТоваров()
	
	ОписаниеТипаБулево        = Новый ОписаниеТипов("Булево");
	ОписаниеТипаДата          = Новый ОписаниеТипов("Дата");
	ОписаниеТипаСтрока        = Новый ОписаниеТипов("Строка");
	ОписаниеТипаСтрока3       = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(3));
	ОписаниеТипаСтрока50      = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50));
	ОписаниеТипаСтрока100     = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(100));
	ОписаниеТипаСтрока250     = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(250));
	ОписаниеТипаЧисло         = Новый ОписаниеТипов("Число");
	ОписаниеТипаДенежнаяСумма = Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ДенежнаяСуммаНеотрицательная.Тип);
	ОписаниеТипаМассив        = Новый ОписаниеТипов("Массив");
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("НомерСтроки",           ОписаниеТипаЧисло);
	Результат.Колонки.Добавить("УчетнаяЗапись",         Новый ОписаниеТипов("СправочникСсылка.УчетныеЗаписиМаркетплейсов"));
	Результат.Колонки.Добавить("ИдентификаторМагазина", ОписаниеТипаСтрока50);
	Результат.Колонки.Добавить("СтатусВМагазине",       ОписаниеТипаСтрока50);
	
	// Информация о карточке товара на Маркете
	Результат.Колонки.Добавить("ИдентификаторКатегории",      ОписаниеТипаСтрока50);
	Результат.Колонки.Добавить("НаименованиеКатегории",       ОписаниеТипаСтрока250);
	Результат.Колонки.Добавить("ИдентификаторМодели",         ОписаниеТипаСтрока50);
	Результат.Колонки.Добавить("НаименованиеМодели",          ОписаниеТипаСтрока250);
	Результат.Колонки.Добавить("ИдентификаторКарточкиТовара", ОписаниеТипаСтрока250);
	Результат.Колонки.Добавить("НаименованиеКарточкиТовара",  ОписаниеТипаСтрока250);
	
	// Основные параметры товара
	Результат.Колонки.Добавить("ИдентификаторПредложения", ОписаниеТипаСтрока250);
	Результат.Колонки.Добавить("ПредставлениеТовара",      ОписаниеТипаСтрока250);
	Результат.Колонки.Добавить("ОписаниеТовара",           ОписаниеТипаСтрока);
	Результат.Колонки.Добавить("Статус",                   Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыВыгрузкиТоваровЯндексМаркет"));
	Результат.Колонки.Добавить("ОписаниеОшибки",           ОписаниеТипаСтрока);
	Результат.Колонки.Добавить("Архивный",                 ОписаниеТипаБулево);
	Результат.Колонки.Добавить("Штрихкоды",                ОписаниеТипаМассив);
	Результат.Колонки.Добавить("Штрихкод",                 ОписаниеТипаСтрока);
	Результат.Колонки.Добавить("СтраныПроисхождения",      ОписаниеТипаМассив);
	Результат.Колонки.Добавить("СтранаПроисхождения",      ОписаниеТипаСтрока100);
	Результат.Колонки.Добавить("ТоварнаяКатегория",        ОписаниеТипаСтрока250);
	Результат.Колонки.Добавить("Артикул",                  ОписаниеТипаСтрока100);
	Результат.Колонки.Добавить("Бренд",                    ОписаниеТипаСтрока250);
	Результат.Колонки.Добавить("Длина",                    ОписаниеТипаЧисло);
	Результат.Колонки.Добавить("Ширина",                   ОписаниеТипаЧисло);
	Результат.Колонки.Добавить("Высота",                   ОписаниеТипаЧисло);
	Результат.Колонки.Добавить("Вес",                      ОписаниеТипаЧисло);
	
	// Ценовые показатели
	Результат.Колонки.Добавить("КодВалюты",          ОписаниеТипаСтрока3);
	Результат.Колонки.Добавить("ЦенаДоСкидки",       ОписаниеТипаДенежнаяСумма);
	Результат.Колонки.Добавить("ЦенаСоСкидкой",      ОписаниеТипаДенежнаяСумма);
	Результат.Колонки.Добавить("ДатаОбновленияЦены", ОписаниеТипаДата);
	
	// Прочее
	Результат.Колонки.Добавить("ТолькоДляВзрослых",      ОписаниеТипаБулево);
	Результат.Колонки.Добавить("ОграничениеПоВозрасту",  Неопределено);
	Результат.Колонки.Добавить("КоличествоГрузовыхМест", ОписаниеТипаЧисло);
	Результат.Колонки.Добавить("Сертификаты",            ОписаниеТипаМассив);
	Результат.Колонки.Добавить("КодТНВЭД",               ОписаниеТипаСтрока50);
	Результат.Колонки.Добавить("Цифровой",               ОписаниеТипаБулево);
	Результат.Колонки.Добавить("ГарантийныйПериод",      Неопределено);
	Результат.Колонки.Добавить("СрокСлужбы",             Неопределено);
	Результат.Колонки.Добавить("СрокГодности",           Неопределено);
	Результат.Колонки.Добавить("Инструкции",             ОписаниеТипаМассив);
	Результат.Колонки.Добавить("Изображения",            ОписаниеТипаМассив);
	Результат.Колонки.Добавить("Видео",                  ОписаниеТипаМассив);
	Результат.Колонки.Добавить("Теги",                   ОписаниеТипаМассив);
	Результат.Колонки.Добавить("ТипТовара",              ОписаниеТипаСтрока50);
	
	Возврат Результат;

КонецФункции

// Возвращает пустую структуру для описания причины отмены заказа.
//
// Возвращаемое значение:
//   Структура:
//     * Идентификатор - Строка
//     * Наименование - Строка
//     * Инициатор - Строка
//     * Доступность - Булево
//
Функция НовыйОписаниеПричиныОтмены()
	
	Данные = Новый Структура;
	
	Данные.Вставить("Идентификатор", "");
	Данные.Вставить("Наименование", "");
	Данные.Вставить("Инициатор", "");
	Данные.Вставить("Доступность", Ложь);
	
	Возврат Данные;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли