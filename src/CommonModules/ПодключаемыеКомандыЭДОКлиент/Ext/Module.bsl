
#Область ПрограммныйИнтерфейс

// см. ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО
//
Процедура ВыполнитьПодключаемуюКомандуЭДО(Знач Команда, Знач Форма, Знач Источник) Экспорт
	
	ОписаниеКоманды = Команда;
	Если ТипЗнч(Команда) = Тип("КомандаФормы") Тогда
		
		АдресКомандВоВременномХранилище = Форма.Команды.Найти("АдресКомандЭДОВоВременномХранилище").Действие;
		ОписаниеКоманды = ПодключаемыйКомандыЭДОСлужебныйВызовСервера.ОписаниеКомандыЭДО(Команда.Имя, 
																							АдресКомандВоВременномХранилище);
																							
	КонецЕсли;
	
	Если ПрерватьВыполнениеОперацииДляПодключенияКЭДО(ОписаниеКоманды, Форма) Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОписаниеКоманды", ОписаниеКоманды);
	ДополнительныеПараметры.Вставить("Форма", Форма);
	ДополнительныеПараметры.Вставить("Источник", Источник);
	
	Если ТипЗнч(Источник) = Тип("ДанныеФормыСтруктура")
		И (Источник.Ссылка.Пустая() Или Форма.Модифицированность) Тогда
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Данные еще не записаны.
				|Выполнение действия ""%1"" возможно только после записи данных.
				|Данные будут записаны.';
				|en = 'Data is not saved.
				|You can run ""%1"" only for saved data.
				|Data will be saved.'"),
			ОписаниеКоманды.Представление);
			
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьПодключаемуюКомандуЭДОПодтверждениеЗаписи",
			ПодключаемыеКомандыЭДОСлужебныйКлиент, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат;
	КонецЕсли;
	
	ПодключаемыеКомандыЭДОСлужебныйКлиент.ВыполнитьПодключаемуюКомандуЭДОПодтверждениеЗаписи(
		Неопределено, ДополнительныеПараметры);
	
КонецПроцедуры

// См. ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО_ЛегкийКлиентЭДО
//
Процедура ВыполнитьПодключаемуюКомандуЭДО_ЛегкийКлиентЭДО(Знач Команда, Знач Форма, Знач Источник) Экспорт
	
	Если ТипЗнч(Источник) = Тип("ДанныеФормыСтруктура")
		И (Источник.Ссылка.Пустая() Или Форма.Модифицированность) Тогда
		Если Не Форма.Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение)) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
		
	ВыполнитьПодключаемуюКомандуЭДО(Команда, Форма, Источник);
	
КонецПроцедуры

// Обновляет список команд в зависимости от текущего контекста.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения
//   Источник - ДанныеФормыСтруктура, ТаблицаФормы - контекст для проверки условий (Форма.Объект или Форма.Элементы.Список)
//
Процедура ОбновитьКоманды(Форма, Источник) Экспорт

	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПараметрыУправленияВидимостьюЭДО") Тогда
		ПараметрыУправленияВидимостью = Форма.ПараметрыУправленияВидимостьюЭДО;
		Если ТипЗнч(ПараметрыУправленияВидимостью) <> Тип("Структура")
			Или Не ПараметрыУправленияВидимостью.ЕстьУсловияВидимости Тогда
			Возврат;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;

	Если ТипЗнч(Источник) = Тип("ТаблицаФормы") Тогда
		Если ПараметрыУправленияВидимостью.УсловияВидимостиВТаблицеФормы Тогда
			ПодключаемыеКомандыЭДОСлужебныйКлиент.ОбновитьКомандыСпискаНаКлиенте(Форма, ПараметрыУправленияВидимостью, Источник);
		Иначе
			ПодключаемыеКомандыЭДОСлужебныйКлиент.ОбновитьКомандыСписка(Форма, ПараметрыУправленияВидимостью, Источник);
		КонецЕсли;
	Иначе
		ПодключаемыеКомандыЭДОСлужебныйКлиент.ОбновитьКомандыОбъекта(Форма, ПараметрыУправленияВидимостью, Источник);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет параметр формы ОтсутствуютУчетныеЗаписиЭДО и возможность выполнения команды без учетных записей
// Параметры:
//  ОписаниеКоманды - см. ПодключаемыйКомандыЭДОСлужебныйВызовСервера.ОписаниеКомандыЭДО
//  Форма - ФормаКлиентскогоПриложения
// Возвращаемое значение:
//  Булево
Функция ПрерватьВыполнениеОперацииДляПодключенияКЭДО(ОписаниеКоманды, Форма)
	
	Если Не ОписаниеКоманды.ПроверятьНаличиеУчетныхЗаписейЭДО Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Текст = НСтр("ru = 'Для работы с ЭДО необходимо выполнить настройку подключения.';
				|en = 'To use EDI, set up the connection.'");
	Заголовок = ОписаниеКоманды.Представление;
	
	Если ОписаниеКоманды.Идентификатор = ПодключаемыеКомандыЭДОКлиентСервер.ИдентификаторКомандыДобавитьФайлыКЭДЛегкийИнтерфейс() Тогда
		Текст = НСтр("ru = 'Для продолжения работы с электронным документом необходимо завершить настройку подключения к ЭДО и повторить действие.';
					|en = 'To continue operations with the electronic document, set up the connection to EDI and try again.'");
		Заголовок = НСтр("ru = 'Добавление в пакет произвольного документа';
						|en = 'Add an arbitrary document to a package'");
	ИначеЕсли ОписаниеКоманды.Идентификатор = ПодключаемыеКомандыЭДОКлиентСервер.ИдентификаторКомандыОткрытьАктуальныйЭДЛегкийИнтерфейс() Тогда
		Текст = НСтр("ru = 'Для продолжения работы с электронным документом необходимо завершить настройку подключения к ЭДО и повторить действие.';
					|en = 'To continue operations with the electronic document, set up the connection to EDI and try again.'");
		Заголовок = НСтр("ru = 'Предварительный просмотр электронного документа';
						|en = 'Electronic document preview'");
	ИначеЕсли ОписаниеКоманды.Идентификатор = ПодключаемыеКомандыЭДОКлиентСервер.ИдентификаторКомандыСформироватьПодписатьОтправитьЭДЛегкийИнтерфейс() Тогда
		Текст = НСтр("ru = 'Для формирования электронного документа необходимо завершить настройку подключения к ЭДО и повторить действие.';
					|en = 'To generate an electronic document, set up the connection to EDI and try again.'");
		Заголовок = НСтр("ru = 'Подписание электронного документа';
						|en = 'Sign electronic document'");
	ИначеЕсли ОписаниеКоманды.Идентификатор = ПодключаемыеКомандыЭДОКлиентСервер.ИдентификаторКомандыПригласитьКОбменуЭДО() Тогда
		Текст = НСтр("ru = 'Для продолжения отправки приглашений необходимо завершить подключение к ЭДО и повторить действие.';
					|en = 'To continue sending invitations, terminate the connection to EDI and try again.'");
		Заголовок = НСтр("ru = 'Создание приглашений';
						|en = 'Create invitations'");
	КонецЕсли;
	
	Результат = ИнтерфейсДокументовЭДОКлиент.ПрерватьВыполнениеОперацииДляПодключенияКЭДО(Форма, Текст, Заголовок);

	Возврат Результат;
	
КонецФункции

#КонецОбласти
