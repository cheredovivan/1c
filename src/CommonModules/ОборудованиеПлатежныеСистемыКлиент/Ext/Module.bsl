
#Область ПрограммныйИнтерфейс

// АПК: 142-выкл обратная совместимость

// Выполнить операции на эквайринговом терминале.
// Если эквайринговый терминал не поддерживает печать квитанций на терминале, для печати подключается печатающее устройство.
// 
// Параметры:
//  ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//  ИдентификаторКлиента - ФормаКлиентскогоПриложения -идентификатор формы.
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//  ПараметрыОперации - Структура - параметры выполнения операции.
//  ДополнительныеПараметры - Структура - дополнительные команды.
//  ПечатающееУстройство - Неопределено - Печатающее устройство.    
//
Процедура НачатьВыполнениеОперацииНаЭквайринговомТерминале(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства = Неопределено, ПараметрыОперации = Неопределено, ДополнительныеПараметры = Неопределено, ПечатающееУстройство = Неопределено) Экспорт
	
	ДанныеДляДополнения = МенеджерОборудованияКлиент.ПечатьСлипЧекаЭквайринговойОперации();
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		Для Каждого Элемент Из ДополнительныеПараметры Цикл
			ДанныеДляДополнения.Вставить(Элемент.Ключ, Элемент.Значение);
		КонецЦикла;
	КонецЕсли;
	
	КонтекстОперации = КонтекстОперацииНаЭквайринговомТерминале();
	КонтекстОперации.ОповещениеПриЗавершении = ОповещениеПриЗавершении;
	КонтекстОперации.ИдентификаторКлиента    = ИдентификаторКлиента;   
	КонтекстОперации.ИдентификаторУстройства = ИдентификаторУстройства;
	КонтекстОперации.ПечатающееУстройство    = ПечатающееУстройство;
	КонтекстОперации.ПараметрыОперации       = ПараметрыОперации;
	КонтекстОперации.ДополнительныеПараметры = ДополнительныеПараметры; 
	КонтекстОперации.ТипТранзакции           = ПараметрыОперации.ТипТранзакции; 
	КонтекстОперации.ПодготовитьДанные       = Ложь; 
	КонтекстОперации.ОбработатьДанные        = Истина;        
	КонтекстОперации.ПодготовитьДанныеКлиент = Истина;
	КонтекстОперации.ОбработатьДанныеКлиент  = Ложь;
	КонтекстОперации.ОбработатьДанныеПриОшибке = Истина;
	КонтекстОперации.ИспользоватьПечатающееУстройство = ПараметрыОперации.ИспользоватьПечатающееУстройство;
	КонтекстОперации.ДополнительныеПараметры = ДанныеДляДополнения;
	ПараметрыОперации.Вставить("ПолныйСлипЧек", КонтекстОперации.ДополнительныеПараметры.ПолныйСлипЧек);
	
	ВыполнениеОперацииНаЭквайринговомТерминале(ИдентификаторУстройства, КонтекстОперации);
	
КонецПроцедуры

// Выполнить сверку итогов на эквайринговом терминале.
// Если эквайринговый терминал не поддерживает печать квитанций на терминале, для печати подключается печатающее устройство.
// 
// Параметры:
//  ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//  ИдентификаторКлиента - ФормаКлиентскогоПриложения -идентификатор формы.
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//  ПараметрыОперации - Структура - параметры выполнения операции.
//  ДополнительныеПараметры - Структура - дополнительные команды.
//  ПечатающееУстройство - Неопределено - Печатающее устройство.      
//
Процедура НачатьВыполнениеСверкиИтоговНаЭквайринговомТерминале(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства = Неопределено, ПараметрыОперации = Неопределено, ДополнительныеПараметры = Неопределено, ПечатающееУстройство = Неопределено) Экспорт
	
	КонтекстОперации = КонтекстОперацииНаЭквайринговомТерминале();
	КонтекстОперации.ОповещениеПриЗавершении = ОповещениеПриЗавершении;
	КонтекстОперации.ИдентификаторКлиента    = ИдентификаторКлиента;   
	КонтекстОперации.ИдентификаторУстройства = ИдентификаторУстройства;
	КонтекстОперации.ПечатающееУстройство    = ПечатающееУстройство;
	КонтекстОперации.ПараметрыОперации       = ПараметрыОперации;
	КонтекстОперации.ДополнительныеПараметры = ДополнительныеПараметры; 
	КонтекстОперации.ТипТранзакции           = "Settlement"; 
	КонтекстОперации.ПодготовитьДанные       = Ложь; 
	КонтекстОперации.ОбработатьДанные        = Истина;
	КонтекстОперации.ИспользоватьПечатающееУстройство = Истина;
	
	ВыполнениеОперацииНаЭквайринговомТерминале(ИдентификаторУстройства, КонтекстОперации);
	
КонецПроцедуры

// Начать получение операции по картам.
// 
// Параметры:
//  ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//  ИдентификаторКлиента - ФормаКлиентскогоПриложения -идентификатор формы.
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//  ПараметрыОперации - Структура - параметры выполнения операции.
//  ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьПолучениеОперацииПоКартам(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства = Неопределено, ПараметрыОперации = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	КонтекстОперации = КонтекстОперацииНаЭквайринговомТерминале();
	КонтекстОперации.ОповещениеПриЗавершении = ОповещениеПриЗавершении;
	КонтекстОперации.ИдентификаторКлиента    = ИдентификаторКлиента;   
	КонтекстОперации.ИдентификаторУстройства = ИдентификаторУстройства;
	КонтекстОперации.ПараметрыОперации       = ПараметрыОперации;
	КонтекстОперации.ДополнительныеПараметры = ДополнительныеПараметры; 
	КонтекстОперации.ТипТранзакции           = "GetOperationByCards"; 
	КонтекстОперации.ПодготовитьДанные       = Ложь; 
	КонтекстОперации.ОбработатьДанные        = Истина;
	
	ВыполнениеОперацииНаЭквайринговомТерминале(ИдентификаторУстройства, КонтекстОперации);
	
КонецПроцедуры

// АПК: 142-вкл

// Начать получение параметров карты.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьПолучениеПараметровКарты(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства = Неопределено, ПараметрыОперации = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	КонтекстОперации = КонтекстОперацииНаЭквайринговомТерминале();
	КонтекстОперации.ОповещениеПриЗавершении = ОповещениеПриЗавершении;
	КонтекстОперации.ИдентификаторКлиента    = ИдентификаторКлиента;   
	КонтекстОперации.ИдентификаторУстройства = ИдентификаторУстройства;
	КонтекстОперации.ПечатающееУстройство    = Неопределено;
	КонтекстОперации.ПараметрыОперации       = ПараметрыОперации;
	КонтекстОперации.ДополнительныеПараметры = ДополнительныеПараметры; 
	КонтекстОперации.ТипТранзакции           = "GetCardParametrs"; 
	КонтекстОперации.ПодготовитьДанные       = Ложь; 
	КонтекстОперации.ОбработатьДанные        = Ложь;
	КонтекстОперации.ИспользоватьПечатающееУстройство = Ложь;
	
	ВыполнениеОперацииНаЭквайринговомТерминале(ИдентификаторУстройства, КонтекстОперации);
	
КонецПроцедуры

// Начать выполнение аварийной отмена операции.
//
// Параметры:
//   ОповещениеПриЗавершении - ОписаниеОповещения - оповещение при завершении.
//   ИдентификаторКлиента    - ФормаКлиентскогоПриложения -идентификатор формы.
//   ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - идентификатор устройства, если неопределенно - будет предложен выбор.
//   ПараметрыОперации       - Структура - параметры выполнения операции.
//   ДополнительныеПараметры - Структура - дополнительные команды.
//
Процедура НачатьВыполнениеАварийнойОтменыОперации(ОповещениеПриЗавершении, ИдентификаторКлиента, ИдентификаторУстройства = Неопределено, ПараметрыОперации = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	КонтекстОперации = КонтекстОперацииНаЭквайринговомТерминале();
	КонтекстОперации.ОповещениеПриЗавершении = ОповещениеПриЗавершении;
	КонтекстОперации.ИдентификаторКлиента    = ИдентификаторКлиента;   
	КонтекстОперации.ИдентификаторУстройства = ИдентификаторУстройства;
	КонтекстОперации.ПечатающееУстройство    = Неопределено;
	КонтекстОперации.ПараметрыОперации       = ПараметрыОперации;
	КонтекстОперации.ДополнительныеПараметры = ДополнительныеПараметры; 
	КонтекстОперации.ТипТранзакции           = "EmergencyVoid"; 
	КонтекстОперации.ПодготовитьДанные       = Ложь; 
	КонтекстОперации.ОбработатьДанные        = Ложь;
	КонтекстОперации.ОбработатьДанныеПриОшибке = Ложь;
	КонтекстОперации.ИспользоватьПечатающееУстройство = Ложь;
	
	ВыполнениеОперацииНаЭквайринговомТерминале(ИдентификаторУстройства, КонтекстОперации);
	
КонецПроцедуры

// Возвращает строку последнего слип-чека
//
// Возвращаемое значение:
//  Строка
Функция ПоследнийСлипЧек() Экспорт
	
	ПодключаемоеОборудование = МенеджерОборудованияКлиент.ПодключаемоеОборудование();
	Возврат ПодключаемоеОборудование.ПоследнийСлипЧек;    
	
КонецФункции

// Возвращает возможные типы пакетной операции
// 
// Возвращаемое значение:
//  Соответствие
Функция ТипыПакетнойОперацииВОчередиЧеков() Экспорт
	
	Результат = Новый Соответствие();
	Результат.Вставить("НеПакетнаяОперация", ПредопределенноеЗначение("Перечисление.ТипыПакетнойОперацииВОчередиЧеков.НеПакетнаяОперация"));
	Результат.Вставить("ФискализацияЧекаСОплатойКартой", ПредопределенноеЗначение("Перечисление.ТипыПакетнойОперацииВОчередиЧеков.ФискализацияЧекаСОплатойКартой"));
	Результат.Вставить("ПродажаСВыдачейНаличных", ПредопределенноеЗначение("Перечисление.ТипыПакетнойОперацииВОчередиЧеков.ПродажаСВыдачейНаличных"));
	Результат.Вставить("ПокупкаСЗачислением", ПредопределенноеЗначение("Перечисление.ТипыПакетнойОперацииВОчередиЧеков.ПокупкаСЗачислением"));
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Устанавливает строку последнего слип-чека
//
// Параметры:
//  ТекстСлипЧека - Строка
Процедура УстановитьПоследнийСлипЧек(ТекстСлипЧека) Экспорт
	ПодключаемоеОборудование = МенеджерОборудованияКлиент.ПодключаемоеОборудование();
	ПодключаемоеОборудование.ПоследнийСлипЧек = ТекстСлипЧека;
КонецПроцедуры

 // Подготовить данные операции, перед выполнением команды.
// 
// Параметры:
//  ПараметрыПодключения - Структура - см. МенеджерОборудованияКлиентСервер.ПараметрыПодключения()
//  Команда - Строка - команда для выполнения
//  ПараметрыОперации - Структура - 
// 
// Возвращаемое значение:
//  Структура - Подготовить данные операции:
//
Функция ПодготовитьДанныеОперации(ПараметрыПодключения, Команда, ПараметрыОперации, ДанныеОперации) Экспорт
	
	ПараметрыОперации.Вставить("Результат", Истина);
	ПараметрыОперации.Вставить("ТекстОшибки");
	
	Если Команда = "PayElectronicCertificate" Или Команда = "ReturnElectronicCertificate" Тогда
		Если ПустаяСтрока(ПараметрыОперации.ИдентификаторКорзины) Или ПараметрыОперации.СуммаЭлектронногоСертификата = 0 Тогда  
			ПараметрыОперации.ТекстОшибки = НСтр("ru = 'Неверные параметры в операции.';
												|en = 'Incorrect parameters in the transaction.'");
			ПараметрыОперации.Результат = Ложь; 
			Возврат ПараметрыОперации;
		КонецЕсли;
	Иначе
		Если Команда <> "Settlement" Тогда
			Если Не (ПараметрыОперации.СуммаОперации > 0) Тогда   
				ПараметрыОперации.ТекстОшибки =  НСтр("ru = 'Не корректная сумма операции.';
														|en = 'Incorrect operation amount.'");
				ПараметрыОперации.Результат = Ложь; 
				Возврат ПараметрыОперации;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ПараметрыОперации.РеквизитыКартыQR) И НЕ ПараметрыПодключения.ConsumerPresentedQR Тогда
		ПараметрыОперации.ТекстОшибки = НСтр("ru = 'Consumer-Presented QR не поддерживается драйвером.';
											|en = 'The driver does not support Consumer-Presented QR.'");
		ПараметрыОперации.Результат = Ложь; 
		Возврат ПараметрыОперации;
	КонецЕсли;
	
	Если Команда = "AuthorizeSales" Или Команда = "AuthorizeRefund" Или Команда = "AuthorizeVoid" 
		Или Команда = "PayByPaymentCardWithCashWithdrawal" Или Команда = "PurchaseWithEnrollment"       
		Или Команда = "PayElectronicCertificate" Или Команда = "ReturnElectronicCertificate"
		Или Команда = "AuthorizePreSales" Или Команда = "AuthorizeCompletion" Или Команда = "AuthorizeVoidPreSales"  Тогда      
		ПараметрыОперации.Вставить("ВходящиеПараметрыXML", ПолучитьXMLПакетДляОперации(ПараметрыОперации)); 
	КонецЕсли;
	
	Возврат ПараметрыОперации;  

КонецФункции

// Обработать данные операции после выполнения команды.
// 
// Параметры:
//  ПараметрыПодключения - Структура - см. МенеджерОборудованияКлиентСервер.ПараметрыПодключения()
//  Команда - Строка - команда для выполнения
//  РезультатВыполнения - Структура - 
//  ДанныеОперации - Структура - 
Процедура ОбработатьДанныеОперации(ПараметрыПодключения, Команда, РезультатВыполнения, ДанныеОперации) Экспорт

КонецПроцедуры

#Область Классификаторы

// Код вида оплаты на терминале.
// 
// Параметры:    
//   ВидОплаты -  ПеречислениеСсылка.ВидОплатыНаТерминале - Вид оплаты на терминале
// Возвращаемое значение:
//   Число - Код вида оплаты   
//                                
Функция КодВидаОплатыНаТерминале(ВидОплаты) Экспорт
	
	КлассификаторыБиблиотеки = МенеджерОборудованияКлиент.КлассификаторыБиблиотеки();
	Результат = КлассификаторыБиблиотеки.КодВидаОплатыНаТерминале;
	Возврат Результат.Получить(ВидОплаты);
	
КонецФункции

// Вид оплаты на терминале по коду.
// 
// Параметры:    
//   ВидОплатыКод - Число - Код вида оплаты.
// Возвращаемое значение:
//   ПеречислениеСсылка.ВидОплатыНаТерминале - Вид оплаты на терминале
//
Функция ВидОплатыНаТерминалеПоКоду(ВидОплатыКод) Экспорт
	
	КлассификаторыБиблиотеки = МенеджерОборудованияКлиент.КлассификаторыБиблиотеки();
	Результат = КлассификаторыБиблиотеки.ВидОплатыНаТерминалеПоКоду;
	Возврат Результат.Получить(ВидОплатыКод);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получить XMLПакет для операции.
// 
// Параметры:
//  ПараметрыОперации - Структура - Параметры
// 
// Возвращаемое значение:
//  Строка - Получить XMLПакет для операции
//
Функция ПолучитьXMLПакетДляОперации(ПараметрыОперации) Экспорт
	
#Если ВебКлиент Тогда
	Возврат ОборудованиеПлатежныеСистемыВызовСервера.ПолучитьXMLПакетДляОперации(ПараметрыОперации);
#Иначе
	Возврат ОборудованиеПлатежныеСистемыКлиентСервер.ПолучитьXMLПакетДляОперации(ПараметрыОперации);
#КонецЕсли

КонецФункции

Функция КонтекстОперацииНаЭквайринговомТерминале()
	
	КонтекстОперации = Новый Структура();
	КонтекстОперации.Вставить("ОповещениеПриЗавершении", Неопределено);
	КонтекстОперации.Вставить("ИдентификаторКлиента"   , Неопределено);   
	КонтекстОперации.Вставить("ИдентификаторУстройства", Неопределено);
	КонтекстОперации.Вставить("ПечатающееУстройство"   , Неопределено);
	КонтекстОперации.Вставить("ПараметрыОперации"      , Неопределено);
	КонтекстОперации.Вставить("ДополнительныеПараметры", Неопределено); 
	КонтекстОперации.Вставить("ТипТранзакции"          , Неопределено); 
	КонтекстОперации.Вставить("ПодготовитьДанные"        , Ложь); 
	КонтекстОперации.Вставить("ОбработатьДанные"         , Ложь);
	КонтекстОперации.Вставить("ОбработатьДанныеПриОшибке", Ложь);   
	КонтекстОперации.Вставить("ПодготовитьДанныеКлиент"  , Ложь);
	КонтекстОперации.Вставить("ОбработатьДанныеКлиент"   , Ложь);
	КонтекстОперации.Вставить("ИспользоватьПечатающееУстройство", Ложь);  
	КонтекстОперации.Вставить("Результат"     , Истина);
	Возврат КонтекстОперации;
	
КонецФункции

Процедура ВыполнениеОперацииНаЭквайринговомТерминале(ИдентификаторУстройства, КонтекстОперации)
	
	Если ИдентификаторУстройства = Неопределено Или ПустаяСтрока(ИдентификаторУстройства) Тогда       
		
		ПоддерживаемыеТипы = МенеджерОборудованияКлиентСервер.ПараметрыТипыОборудования();
		ПоддерживаемыеТипы.ЭквайринговыйТерминал = Истина;
		
		Оповещение = Новый ОписаниеОповещения("ВыполнениеОперацииНаЭквайринговомТерминалеЗавершение", ЭтотОбъект, КонтекстОперации);
		МенеджерОборудованияКлиент.ВыбратьУстройство(Оповещение, ПоддерживаемыеТипы,
			НСтр("ru = 'Выберите эквайринговый терминал';
				|en = 'Select a POS terminal'"), 
			НСтр("ru = 'Эквайринговый терминал не подключен.';
				|en = 'POS terminal is not connected.'"),
			НСтр("ru = 'Эквайринговый терминал не выбран.';
				|en = 'POS terminal is not selected.'"));
	Иначе
		ВыполнениеОперацииНаЭквайринговомТерминалеЗавершение(КонтекстОперации, КонтекстОперации);
	КонецЕсли
	
КонецПроцедуры

Процедура ВыполнениеОперацииНаЭквайринговомТерминалеЗавершение(РезультатВыбора, КонтекстОперации) Экспорт
	
	Если РезультатВыбора.Результат Тогда                                                   
		КонтекстОперации.ИдентификаторУстройства = РезультатВыбора.ИдентификаторУстройства;
		ПараметрыОперации = КонтекстОперации.ПараметрыОперации;
		Если КонтекстОперации.ТипТранзакции = "ReturnElectronicCertificate" И ПустаяСтрока(ПараметрыОперации.СсылочныйНомер) Тогда
			Оповещение = Новый ОписаниеОповещения("ВыполнениеОперацииНаЭквайринговомТерминалеПослеВводаRRN", ЭтотОбъект, КонтекстОперации);
			Подсказка = НСтр("ru = 'Введите RRN операции cо слип-чека покупки ЭС';
							|en = 'Enter the operation RRN from the digital sales slip'"); 
			ПоказатьВводСтроки(Оповещение, "", Подсказка, 12); 
		Иначе                            
			ВыполнениеОперацииНаЭквайринговомТерминалеКоманда(КонтекстОперации);
		КонецЕсли;
	Иначе
		ВыполнитьОбработкуОповещения(КонтекстОперации.ОповещениеПриЗавершении, РезультатВыбора);
	КонецЕсли;
	
КонецПроцедуры           

Процедура ВыполнениеОперацииНаЭквайринговомТерминалеПослеВводаRRN(СтрокаRRN, КонтекстОперации) Экспорт
	
	КонтекстОперации.ПараметрыОперации.СсылочныйНомер = СтрокаRRN;
	ВыполнениеОперацииНаЭквайринговомТерминалеКоманда(КонтекстОперации);
	
КонецПроцедуры

Процедура ВыполнениеОперацииНаЭквайринговомТерминалеКоманда(КонтекстОперации) Экспорт
	
	Если КонтекстОперации.ПараметрыОперации <> Неопределено Тогда
		Если КонтекстОперации.ПараметрыОперации.Свойство("РеквизитыКартыQR") Тогда
			РеквизитыКартыQR = КонтекстОперации.ПараметрыОперации.РеквизитыКартыQR;
			Если Не ПустаяСтрока(РеквизитыКартыQR) Тогда
				Если Не ОборудованиеПлатежныеСистемыКлиентСервер.ЭтоРеквизитыКартыQR(РеквизитыКартыQR) Тогда     
					ТекстСообщения = НСтр("ru = 'Операция по указанному QR-коду невозможна.';
											|en = 'Cannot carry out a transaction by the specified QR code.'");
					РезультатВыполнения = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперацииНаОборудовании(Ложь, ТекстСообщения);
					ВыполнитьОбработкуОповещения(КонтекстОперации.ОповещениеПриЗавершении, РезультатВыполнения);
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	ПараметрыВыполнениеКоманды = МенеджерОборудованияКлиентСервер.ПараметрыВыполнениеКоманды(КонтекстОперации.ТипТранзакции,
		"ОборудованиеПлатежныеСистемы", КонтекстОперации.ДополнительныеПараметры, КонтекстОперации.ПодготовитьДанные, КонтекстОперации.ОбработатьДанные, 
		КонтекстОперации.ИспользоватьПечатающееУстройство, КонтекстОперации.ОбработатьДанныеПриОшибке);   
	ПараметрыВыполнениеКоманды.ПодготовитьДанныеКлиент = КонтекстОперации.ПодготовитьДанныеКлиент;
		
	МенеджерОборудованияКлиент.НачатьВыполнениеКоманды(КонтекстОперации.ОповещениеПриЗавершении, КонтекстОперации.ИдентификаторКлиента, 
		КонтекстОперации.ИдентификаторУстройства, КонтекстОперации.ПараметрыОперации, ПараметрыВыполнениеКоманды, КонтекстОперации.ПечатающееУстройство);
	
КонецПроцедуры

#КонецОбласти  