#Область СлужебныйПрограммныйИнтерфейс

// Возвращает код метода ценообразования сопоставимых рыночных цен.
// 
// Возвращаемое значение:
//  Строка - код метода ценообразования для контролируемых сделок.
Функция КодМетодаСопоставимыхРыночныхЦен() Экспорт
	
	Возврат "01";
	
КонецФункции

// Возвращает код метода ценообразования цены последующей реализации.
// 
// Возвращаемое значение:
//  Строка - код метода ценообразования для контролируемых сделок.
Функция КодМетодаЦеныПоследующейРеализации() Экспорт
	
	Возврат "02";
	
КонецФункции

// Возвращает код затратного метода ценообразования.
// 
// Возвращаемое значение:
//  Строка - код метода ценообразования для контролируемых сделок.
Функция КодМетодаЗатратного() Экспорт
	
	Возврат "03";
	
КонецФункции

// Возвращает код метода ценообразования сопоставимой рентабельности.
// 
// Возвращаемое значение:
//  Строка - код метода ценообразования для контролируемых сделок.
Функция КодМетодаСопоставимойРентабельности() Экспорт
	
	Возврат "04";
	
КонецФункции

// Возвращает код метода ценообразования распределения прибыли.
// 
// Возвращаемое значение:
//  Строка - код метода ценообразования для контролируемых сделок.
Функция КодМетодаРаспределенияПрибыли() Экспорт
	
	Возврат "05";
	
КонецФункции

// Возвращает код метода ценообразования, применяющий комбинацию методов.
// 
// Возвращаемое значение:
//  Строка - код метода ценообразования для контролируемых сделок.
Функция КодМетодаКомбинацияМетодов() Экспорт
	
	Возврат "06";
	
КонецФункции

// Возвращает таблицу значений свойств метода ценообразования.
//
// Параметры:
//  КодМетодаЦенообразования - Строка - код метода цеообразования.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - свойств метода ценообразования:
//  * ИмяПоля - Строка - имя поля.
//  * ОписаниеПоля - Строка - описание поля.
//  * Обязательный - Строка - Истина, если обязательное поле для заполнения.
//  Далее могут быть поля добавлены в зависимости от значения кода метода ценообразования.
//
Функция ПоляСвойствМетодаЦенообразования(КодМетодаЦенообразования) Экспорт
	
	ПоляСвойств = Новый ТаблицаЗначений;
	ПоляСвойств.Колонки.Добавить("ИмяПоля", Новый ОписаниеТипов("Строка"));
	ПоляСвойств.Колонки.Добавить("ОписаниеПоля", Новый ОписаниеТипов("Строка"));
	ПоляСвойств.Колонки.Добавить("Обязательный", Новый ОписаниеТипов("Булево"));
	
	Если КодМетодаЦенообразования = КодМетодаСопоставимыхРыночныхЦен() Тогда
		
		ДобавитьПолеСвойств(ПоляСвойств, "ИсточникИнформацииСопоставимыхЦен", НСтр("ru = 'Источник информации';
																					|en = 'Information source'"), Истина);
		ДобавитьПолеСвойств(ПоляСвойств, "НаименованиеКотировкиСопоставимыхЦен", НСтр("ru = 'Наименование котировки';
																						|en = 'Quotation name'"), Истина);
		ДобавитьПолеСвойств(ПоляСвойств, "ПорядокПримененияКотировкиСопоставимыхЦен", НСтр("ru = 'Порядок применения котировки';
																							|en = 'Quotation application procedure'"), Истина);
		ДобавитьПолеСвойств(ПоляСвойств, "НаименованиеДокументаЦеныСопоставимыхЦен", НСтр("ru = 'Наименование документа, которым фиксируется цена';
																							|en = 'Name of document that fixes price'"), Истина);
		ДобавитьПолеСвойств(ПоляСвойств, "НаименованиеКорректировкиДляСопоставимостиСопоставимыхЦен", НСтр("ru = 'Наименование корректировки для сопоставимости';
																											|en = 'Name of adjustment for comparability'"), Истина);
	ИначеЕсли КодМетодаЦенообразования = КодМетодаЦеныПоследующейРеализации()
		Или КодМетодаЦенообразования = КодМетодаЗатратного()
		Или КодМетодаЦенообразования = КодМетодаСопоставимойРентабельности()Тогда
		
		ДобавитьПолеСвойств(ПоляСвойств, "ПоказательРентабельности", НСтр("ru = 'Показатель рентабельности';
																			|en = 'Profitability indicator'"), Истина);
		ДобавитьПолеСвойств(ПоляСвойств, "ТестируемаяСторона", НСтр("ru = 'Тестируемая сторона';
																	|en = 'Tested party'"), Истина);
		ДобавитьПолеСвойств(ПоляСвойств, "Рентабельность", НСтр("ru = 'Рентабельность';
																|en = 'Profitability'"), Ложь);
		ДобавитьПолеСвойств(ПоляСвойств, "РыночнаяРентабельность", НСтр("ru = 'Рыночная рентабельность';
																		|en = 'Market profitability'"), Ложь);
	ИначеЕсли КодМетодаЦенообразования = КодМетодаРаспределенияПрибыли() Тогда
		ДобавитьПолеСвойств(ПоляСвойств, "ОписаниеМетодаРаспределенияПрибыли", НСтр("ru = 'Описание метода';
																					|en = 'Method details'"), Истина);
	КонецЕсли;
	
	Возврат ПоляСвойств;
	
КонецФункции

Функция ОписаниеСвойствМетодаЦенообразования(КодМетодаЦенообразования, СвойстваМетода) Экспорт
	
	ОписаниеСвойств = Новый Структура;
	ОписаниеСвойств.Вставить("ЗначенияСвойствСтрокой", "");
	ОписаниеСвойств.Вставить("ЕстьОшибкиЗаполнения", Ложь);
	ОписаниеСвойств.Вставить("ТекстОшибкиЗаполнения", "");
	
	КодыМетодовЦенообразования = Документы.ДокументацияПоКонтролируемымСделкам.КодыМетодовЦенообразования();
	Если КодыМетодовЦенообразования.НайтиПоЗначению(КодМетодаЦенообразования) = Неопределено Тогда
		
		ОписаниеСвойств.ТекстОшибкиЗаполнения = СтрШаблон(
			НСтр("ru = 'Код метода ценообразования %1 нельзя использовать в документации';
				|en = 'Cannot use pricing method code %1 in the documentation'"),
			КодМетодаЦенообразования);
		ОписаниеСвойств.ЕстьОшибкиЗаполнения = Истина;
		Возврат ОписаниеСвойств;
		
	КонецЕсли;
	
	Если СвойстваМетода.Количество() = 0 Тогда
		
		Если КодМетодаЦенообразования = КодМетодаКомбинацияМетодов() Тогда
			ОписаниеСвойств.ТекстОшибкиЗаполнения = НСтр("ru = 'Не заданы методы ценообразования для комбинации методов';
														|en = 'No pricing methods are specified for the method combination'");
		Иначе
			ОписаниеСвойств.ТекстОшибкиЗаполнения = НСтр("ru = 'Не заполнены поля свойств метода ценообразования';
														|en = 'Pricing method property fields are required'");
		КонецЕсли;
		
		ОписаниеСвойств.ЕстьОшибкиЗаполнения = Истина;
		Возврат ОписаниеСвойств;
		
	КонецЕсли;
	
	ЧастиОписания = Новый Массив;
	НезаполненныеПоля = Новый Массив;
	
	Для Каждого СтрокаСвойств Из СвойстваМетода Цикл
		ОписаниеКода = "";
		Если КодМетодаЦенообразования = КодМетодаКомбинацияМетодов() Тогда
			ОписаниеКода = СтрокаСвойств.КодМетодаЦенообразования + ":" + " ";
		КонецЕсли;
		
		ПоляМетода = ПоляСвойствМетодаЦенообразования(СтрокаСвойств.КодМетодаЦенообразования);
		
		ЧастиОписания.Добавить(ОписаниеКода + ОписаниеМетодаСтрокиСвойствЦенообразования(СтрокаСвойств, ПоляМетода));
		
		ДополнитьНезаполненныеПоля(НезаполненныеПоля, СтрокаСвойств, ПоляМетода);
		
	КонецЦикла;
	
	ОписаниеСвойств.ЗначенияСвойствСтрокой = СтрСоединить(ЧастиОписания, Символы.ПС);
	Если НезаполненныеПоля.Количество() > 0 Тогда
		ОписаниеСвойств.ТекстОшибкиЗаполнения = СтрШаблон(
			НСтр("ru = 'Нужно заполнить: %1';
				|en = 'Fill in the following: %1'"),
			СтрСоединить(НезаполненныеПоля, ", "));
		ОписаниеСвойств.ЕстьОшибкиЗаполнения = Истина;
	КонецЕсли;
	
	Возврат ОписаниеСвойств;
	
КонецФункции

Функция ОписаниеМетодаСтрокиСвойствЦенообразования(Строка, ПоляМетода) Экспорт
	
	Если Строка = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	ПредставленияСвойств = Новый Массив;
	Для Каждого СтрокаПоля Из ПоляМетода Цикл
		ИмяПоля = СтрокаПоля.ИмяПоля;
		ЗначениеПоля = Строка[ИмяПоля];
		
		Если Не ЗначениеЗаполнено(ЗначениеПоля) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ИмяПоля = "ТестируемаяСторона" Тогда
			
			СписокВыбораТестируемойСтороны = Документы.ДокументацияПоКонтролируемымСделкам.СписокВыбораТестируемойСтороны();
			ЭлементСписка = СписокВыбораТестируемойСтороны.НайтиПоЗначению(ЗначениеПоля);
			Если ЭлементСписка <> Неопределено Тогда
				ПредставленияСвойств.Добавить(ЭлементСписка.Представление);
			КонецЕсли;
			
		ИначеЕсли ИмяПоля = "Рентабельность"
			Или ИмяПоля = "РыночнаяРентабельность" Тогда
			
			ПредставленияСвойств.Добавить(Формат(ЗначениеПоля, "ЧЦ=6; ЧДЦ=3"));
			
		Иначе
			
			ПредставленияСвойств.Добавить(ЗначениеПоля);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтрСоединить(ПредставленияСвойств, ", ");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеСвойств(ПоляСвойств, ИмяПоля, ОписаниеПоля, Обязательный)
	
	НоваяСтрока = ПоляСвойств.Добавить();
	НоваяСтрока.ИмяПоля = ИмяПоля;
	НоваяСтрока.ОписаниеПоля = ОписаниеПоля;
	НоваяСтрока.Обязательный = Обязательный;
	
КонецПроцедуры

Процедура ДополнитьНезаполненныеПоля(НезаполненныеПоля, СтрокаСвойств, ПоляМетода)
	
	Для Каждого СтрокаПоля Из ПоляМетода Цикл
		Если Не СтрокаПоля.Обязательный Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяПоля = СтрокаПоля.ИмяПоля;
		ЗначениеПоля = СтрокаСвойств[ИмяПоля];
		
		Если Не ЗначениеЗаполнено(ЗначениеПоля) Тогда
			НезаполненныеПоля.Добавить(СтрокаПоля.ОписаниеПоля);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

