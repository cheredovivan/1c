// Работа с расшифровками включает в себя две части: прикладную - получение настроек для расшифровки и техническую -
// преобразование полученных настроек в структуру, с которой умеет работать общая форма "РасшифровкаПоказателейРегламентированныхОтчетов".
// Первая часть описана в области "ЗначенияНастроек", вторая - "ПрименениеНастроек".
#Область СлужебныйПрограммныйИнтерфейс

#Область ЗначенияНастроек

// Значения настроек отчета таблиц 3.1 "Наличие и движение НМА" и 3.2 "НМА, созданные организацией".
// 
// Параметры:
//  ИмяКолонки - Строка
//  СчетаРасшифровки - см. ЗаполнениеБухгалтерскойОтчетностиПоясненияНематериальныеАктивы.СчетаРасшифровки
//  Выборка - ВыборкаИзРезультатаЗапроса
// 
// Возвращаемое значение:
//  Структура - см. НовыйЗначенияНастроекОтчета
//
Функция ЗначенияНастроекОтчетаНематериальныеАктивы(ИмяКолонки, СчетаРасшифровки, Выборка) Экспорт
	
	ЗначенияНастроекОтчета = НовыйЗначенияНастроекОтчета();
	ЗначенияНастроекОтчета.Организация = Выборка.Организация;
	
	СчетСтоимость = СчетаРасшифровки.СчетСтоимость.Ссылка;
	Если СтрНайти(ИмяКолонки, "Амортизация") <> 0 Тогда
		СчетСтоимость = СчетаРасшифровки.СчетАмортизацияОбесценение.Ссылка;
	КонецЕсли;
	
	Если Выборка.Расшифровка = "СтоимостьНМА" Тогда
		ЗначенияНастроекОтчета.Счет = СчетСтоимость;
	ИначеЕсли Выборка.Расшифровка = "ВыбытиеАмортизации" Тогда
		// Выбытие амортизации может быть отражено не только проводкой Дт 05 Кт 04.09,
		// но и Дт 05 Кт 04.01: такая схема проводок была до появления счета выбытия НМА 04.09.
		ЗначенияНастроекОтчета.Счет = СчетаРасшифровки.СчетАмортизацияОбесценение.Ссылка;
		ЗначенияНастроекОтчета.КорСчет = СчетаРасшифровки.СчетСтоимость.Ссылка;
	ИначеЕсли Выборка.Расшифровка = "СтоимостьАмортизацияОбесценение" Тогда
		ЗначенияНастроекОтчета.Счет = СчетаРасшифровки.СчетАмортизацияОбесценение.Ссылка;
	ИначеЕсли Выборка.Расшифровка = "КапитальныеВложения" Тогда
		ЗначенияНастроекОтчета.Счет = СчетаРасшифровки.СчетСтоимость.Ссылка;
		ЗначенияНастроекОтчета.КорСчет = СчетаРасшифровки.СчетКапитальныеВложения.Ссылка;
	ИначеЕсли Выборка.Расшифровка = "Переоценка" Тогда
		ЗначенияНастроекОтчета.Счет = СчетСтоимость;
		ЗначенияНастроекОтчета.КорСчет = СчетаРасшифровки.СчетПереоценка.Ссылка;
	ИначеЕсли Выборка.Расшифровка = "КорректировкаПереоценкиРасходы" Или Выборка.Расшифровка = "КорректировкаПереоценкиДоходы" Тогда
		ЗначенияНастроекОтчета.Счет = СчетСтоимость;
		Если Выборка.Расшифровка = "КорректировкаПереоценкиРасходы" Тогда
			ЗначенияНастроекОтчета.КорСчет = СчетаРасшифровки.СчетПрочиеРасходы.Ссылка;
		Иначе
			ЗначенияНастроекОтчета.КорСчет = СчетаРасшифровки.СчетПрочиеДоходы.Ссылка;
		КонецЕсли;
		УстановитьОтборВидДоходовРасходовПереоценка(ЗначенияНастроекОтчета);
	ИначеЕсли Выборка.Расшифровка = "Переклассификация" Тогда
	ИначеЕсли Выборка.Расшифровка = "МежфилиальныйОборот" Тогда
		ЗначенияНастроекОтчета.Счет = СчетСтоимость;
		ЗначенияНастроекОтчета.КорСчет = СчетаРасшифровки.СчетПередачи.Ссылка;
	КонецЕсли;
	
	Возврат ЗначенияНастроекОтчета;
	
КонецФункции

// Значения настроек отчета таблицы 6.1 "Наличие и движение запасов".
// 
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса
// 
// Возвращаемое значение:
//  Структура - см. НовыйЗначенияНастроекОтчета
//
Функция ЗначенияНастроекОтчетаЗапасы(Выборка) Экспорт
	
	ЗначенияНастроекОтчета = НовыйЗначенияНастроекОтчета();
	ЗаполнитьЗначенияСвойств(ЗначенияНастроекОтчета, Выборка);
	
	Возврат ЗначенияНастроекОтчета;
	
КонецФункции

// Значения настроек отчета таблиц 4.1 "Наличие и движение основных средств" и 4.2 "Наличие и движение прав пользования активами".
// 
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса
// 
// Возвращаемое значение:
//  Структура - см. НовыйЗначенияНастроекОтчета
//
Функция ЗначенияНастроекОтчетаОсновныеСредства(Выборка) Экспорт
	
	ЗначенияНастроекОтчета = НовыйЗначенияНастроекОтчета();
	ЗаполнитьЗначенияСвойств(ЗначенияНастроекОтчета, Выборка);
	
	НастроитьОтборПоВидуСубконто(
		ЗначенияНастроекОтчета,
		"Субконто%1.ГруппаОС",
		ЗначенияНастроекОтчета.Счет,
		ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства,
		Выборка.Группа);
	
	Возврат ЗначенияНастроекОтчета;
	
КонецФункции

// Значения настроек отчета таблиц 3.5 "Кап. вложения в НМА" и 4.5 "Кап. вложения в ОС".
// 
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса
// 
// Возвращаемое значение:
//  Структура - см. НовыйЗначенияНастроекОтчета
//
Функция ЗначенияНастроекОтчетаКапитальныеВложения(Выборка) Экспорт
	
	ЗначенияНастроекРасшифровки = НовыйЗначенияНастроекОтчета();
	ЗаполнитьЗначенияСвойств(ЗначенияНастроекРасшифровки, Выборка);
	
	Возврат ЗначенияНастроекРасшифровки;
КонецФункции

Процедура НастроитьОтборПоВидуСубконто(ЗначенияНастроекОтчета, ШаблонПоляОтбора, Счет, ВидСубконтоОтбора, ЗначениеОтбора, ВидСравнения = Неопределено) Экспорт
	
	НомерСубконто = НомерСубконтоСтрокой(Счет, ВидСубконтоОтбора);
	Если НомерСубконто <> Неопределено Тогда
		Поле = СтрШаблон(ШаблонПоляОтбора, НомерСубконто);
		Если ВидСравнения = Неопределено Тогда
			ЗначенияНастроекОтчета.Отбор.Вставить(Поле, ЗначениеОтбора);
		Иначе
			ЭлементОтбора = ЗаполнениеРасшифровкаРегламентированнойОтчетности.НовыйЗначениеЭлементаОтбора();
			ЭлементОтбора.ВидСравненияКомпоновкиДанных = ВидСравнения;
			ЭлементОтбора.Значение = ЗначениеОтбора;
			ЗначенияНастроекОтчета.Отбор.Вставить(Поле, ЭлементОтбора);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура УстановитьОтборВидДоходовРасходовПереоценка(ЗначенияНастроекОтчета) Экспорт

	ЗначениеОтбора = Перечисления.ВидыПрочихДоходовИРасходов.ПереоценкаВнеоборотныхАктивов;
	ШаблонПоляОтбора = "КорСубконто%1.ВидПрочихДоходовИРасходов";
	ВидСубконтоОтбора = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы;
	НастроитьОтборПоВидуСубконто(ЗначенияНастроекОтчета, ШаблонПоляОтбора, ЗначенияНастроекОтчета.КорСчет, ВидСубконтоОтбора, ЗначениеОтбора);
	
КонецПроцедуры

#КонецОбласти

#Область ПрименениеНастроек

// Преобразует полученные ранее значения настроек к структуре, используемой при расшифровке,
// для сохранения их в информационную базу.
// 
// Параметры:
//  ЗначенияНастроек - см. НовыйЗначенияНастроекОтчета
//  ПериодОтчета - см. БухгалтерскаяОтчетностьБРО.НовыйПериодОтчета
// 
// Возвращаемое значение:
//  Структура -см. НовыйКонтейнерНастроекОтчета
//
Функция НастройкиОтчетаПоЗначениям(ЗначенияНастроек, ПериодОтчета) Экспорт
	
	// Типовые отчеты - отчеты, настройки которых сохраняются в виде структуры:
	// оборотно-сальдовая ведомость по счету, анализ счета, обороты счета.
	// См. ОбщаяФорма.РасшифровкаПоказателейРегламентированныхОтчетов.НастройкиДляТиповогоОтчета
	Если ЭтоТиповойОтчет(ЗначенияНастроек) Тогда
		Возврат НастройкиТиповогоОтчетаПоЗначениям(ЗначенияНастроек, ПериодОтчета);
	КонецЕсли;
	
	// Произвольный отчет - как правило, это справки-расчеты. Обычно настройки таких отчетов могут быть сохранены в виде параметров формы.
	Возврат НастройкиПроизвольногоОтчетаПоЗначениям(ЗначенияНастроек, ПериодОтчета);
		
КонецФункции

// Выводимые данные отчета ОборотыСчета
// 
// Возвращаемое значение:
//  Структура:
// * СальдоНаНачалоДт - Булево
// * СальдоНаНачалоКт - Булево
// * ОборотыЗаПериодДт - Булево
// * ОборотыЗаПериодКт - Булево
// * ОборотыСоСчетамиДт - Булево
// * ОборотыСоСчетамиКт - Булево
// * СальдоНаКонецДт - Булево
// * СальдоНаКонецКт - Булево
//
Функция НовыйНастройкиПоказателей() Экспорт

	НастройкиПоказателей = Новый Структура;
	
	НастройкиПоказателей.Вставить("СальдоНаНачалоДт", Ложь);
	НастройкиПоказателей.Вставить("СальдоНаНачалоКт", Ложь);
	НастройкиПоказателей.Вставить("ОборотыЗаПериодДт", Ложь);
	НастройкиПоказателей.Вставить("ОборотыЗаПериодКт", Ложь);
	НастройкиПоказателей.Вставить("ОборотыСоСчетамиДт", Ложь);
	НастройкиПоказателей.Вставить("ОборотыСоСчетамиКт", Ложь);
	НастройкиПоказателей.Вставить("СальдоНаКонецДт", Ложь);
	НастройкиПоказателей.Вставить("СальдоНаКонецКт", Ложь);
	
	Возврат НастройкиПоказателей;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗначенияНастроек

Функция НовыйЗначенияНастроекОтчета()

	ЗначенияНастроекОтчета = Новый Структура();
	ЗначенияНастроекОтчета.Вставить("ИмяОтчета", "ОборотноСальдоваяВедомостьПоСчету"); // Имя метаданных отчета
	ЗначенияНастроекОтчета.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ЗначенияНастроекОтчета.Вставить("Счет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	ЗначенияНастроекОтчета.Вставить("КорСчет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	ЗначенияНастроекОтчета.Вставить("Отбор", Новый Соответствие); // Ключ - Строка - выражение левого поля компоновки,
	                                                              // Значение - см. ЗаполнениеРасшифровкаРегламентированнойОтчетности.НовыйЗначениеЭлементаОтбора
	ЗначенияНастроекОтчета.Вставить("Показатели", НовыйНастройкиПоказателей());
	ЗначенияНастроекОтчета.Вставить("ГруппировкаСубконто", Новый Массив); // из Число, ПланыСубконтоХарактеристикаСсылка.ВидыСубконтоХозрасчетные
	ЗначенияНастроекОтчета.Вставить("ПоСубсчетам", Истина);
	ЗначенияНастроекОтчета.Вставить("ПоСубсчетамКорСчетов", Ложь);
	
	Возврат ЗначенияНастроекОтчета;
	
КонецФункции

Функция НомерСубконтоСтрокой(Счет, ВидСубконто)
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
	НомерСубконто = СвойстваСчета.ИдентификаторыСубконто[ВидСубконто];
	Если НомерСубконто = Неопределено Тогда
		Возврат НомерСубконто;
	КонецЕсли;
	Возврат XMLСтрока(НомерСубконто);
КонецФункции

#КонецОбласти

#Область ПрименениеНастроек

Функция НастройкиТиповогоОтчетаПоЗначениям(ЗначенияНастроек, ПериодОтчета)
	
	ИсправитьНеприменимыеНастройкиТиповогоОтчета(ЗначенияНастроек);
	
	НастройкиРасшифровки = НовыйКонтейнерНастроекОтчета();
	
	НастройкиРасшифровки.Отчет = НовыйНастройкиРасшифровкиТиповогоОтчета();
	НастройкиТиповогоОтчета = НастройкиРасшифровки.Отчет.НастройкиТиповогоОтчета;
	ЗаполнитьЗначенияСвойств(НастройкиТиповогоОтчета, ЗначенияНастроек);
	УстановитьПериодОтчета(НастройкиТиповогоОтчета, ПериодОтчета);
	НастройкиТиповогоОтчета.ОтборПоАналитике = ЗначенияНастроек.Отбор;
	
	НастройкиТиповогоОтчета.Отчет = ТиповыеОтчеты()[ЗначенияНастроек.ИмяОтчета];
	НастройкиРасшифровки.Отчет.ИмяОтчета = ИмяФормыОтчета(ЗначенияНастроек.ИмяОтчета);
	
	Если НастройкиТиповогоОтчета.Отчет = "ОборотыСчета" Тогда
		НастройкиТиповогоОтчета.НастройкиПоказателей = НовыйНастройкиПоказателей();
		ЗаполнитьЗначенияСвойств(НастройкиТиповогоОтчета.НастройкиПоказателей, ЗначенияНастроек.Показатели);
	КонецЕсли;
	
	Если НастройкиТиповогоОтчета.Отчет = "АнализСубконто" Тогда
	
		НастройкиТиповогоОтчета.ВидыСубконто = ЗначенияНастроек.ГруппировкаСубконто;
		Для НомерСубконто = 1 По ЗначенияНастроек.ГруппировкаСубконто.Количество() Цикл
			ДобавитьГруппировкуПоСубконто(
				НастройкиТиповогоОтчета.Группировка,
				ЗначенияНастроек.Счет,
				НомерСубконто);
		КонецЦикла;

	Иначе
		
		Для Каждого ЭлементГруппировки Из ЗначенияНастроек.ГруппировкаСубконто Цикл
			
			ДобавитьГруппировкуПоСубконто(
				НастройкиТиповогоОтчета.Группировка,
				НастройкиТиповогоОтчета.Счет,
				ЭлементГруппировки);
				
		КонецЦикла;
	КонецЕсли;

	Возврат НастройкиРасшифровки;
	
КонецФункции

Функция НастройкиПроизвольногоОтчетаПоЗначениям(ЗначенияНастроекОтчета, ПериодОтчета)
	
	НастройкиРасшифровки = НовыйКонтейнерНастроекОтчета();
	НастройкиРасшифровки.Отчет = НовыйНастройкиРасшифровкиПроизвольногоОтчета();
	ПараметрыФормы = НастройкиРасшифровки.Отчет.ПараметрыФормы;
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, ЗначенияНастроекОтчета);
	УстановитьПериодОтчета(ПараметрыФормы, ПериодОтчета);
	НастройкиРасшифровки.Отчет.ИмяОтчета = ИмяФормыОтчета(ЗначенияНастроекОтчета.ИмяОтчета); // Имя формы отчета
	
	Возврат НастройкиРасшифровки;
	
КонецФункции

// Конструктор структуры, в которой сохраняются настройки расшифровки.
// Настройки, сохраненные в ключе Отчет, используются в качестве параметра Расшифровка в общей форме
// РасшифровкаПоказателейРегламентированныхОтчетов, процедура ОткрытьФормуРасшифровки.
// 
// Возвращаемое значение:
//  Структура - Новый контейнер настроек отчета:
//   * Отчет - см. НовыйНастройкиРасшифровкиТиповогоОтчета, см. НовыйНастройкиРасшифровкиПроизвольногоОтчета
//
Функция НовыйКонтейнерНастроекОтчета()
	КонтейнерНастроекОтчета = Новый Структура;
	КонтейнерНастроекОтчета.Вставить("Отчет", Новый Структура);
	Возврат КонтейнерНастроекОтчета;
КонецФункции

// Структура должна быть совместима с методом, пробразующим сохраненные настройки расшифровки
// в пользовательские настройки отчета.
// См. ТиповыеОтчеты, ОбщаяФорма.РасшифровкаПоказателейРегламентированныхОтчетов.НастройкиДляТиповогоОтчета
// 
// Возвращаемое значение:
//  Структура - Новый настройки расшифровки типового отчета:
// * ИмяОтчета - Строка - Имя формы отчета. Например, "Отчет.АнализСчета.ФормаОбъекта"
// * ПараметрыФормы - см. НовыйПараметрыФормыТиповогоОтчета
// * НастройкиТиповогоОтчета - см. НовыйНастройкиТиповогоОтчета
//
Функция НовыйНастройкиРасшифровкиТиповогоОтчета()
	
	НастройкиОтчета = Новый Структура;
	НастройкиОтчета.Вставить("ИмяОтчета", ""); //  Имя формы отчета
	НастройкиОтчета.Вставить("ПараметрыФормы", НовыйПараметрыФормыТиповогоОтчета());
	НастройкиОтчета.Вставить("НастройкиТиповогоОтчета", НовыйНастройкиТиповогоОтчета());
	
	Возврат НастройкиОтчета;
	
КонецФункции

Функция НовыйПараметрыФормыТиповогоОтчета()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРасшифровки", Истина);
	ПараметрыФормы.Вставить("ВидРасшифровки", 2); // 1 - из временного хранилища, 2 - из параметров формы
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	Возврат ПараметрыФормы;
КонецФункции

Функция НовыйНастройкиТиповогоОтчета()
	
	НастройкиТиповогоОтчета = Новый Структура;
	НастройкиТиповогоОтчета.Вставить("Отчет", "");
	НастройкиТиповогоОтчета.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	
	НастройкиТиповогоОтчета.Вставить("НачалоПериода", Дата(1, 1, 1));
	НастройкиТиповогоОтчета.Вставить("КонецПериода",  Дата(1, 1, 1));
	НастройкиТиповогоОтчета.Вставить("ВидПериода",    0); // Значение Структуры - см. БухгалтерскиеОтчетыКлиентСервер.ВидыПериода()
	
	НастройкиТиповогоОтчета.Вставить("Счет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	
	НастройкиТиповогоОтчета.Вставить("Группировка", Новый Массив); // из НовыйОписаниеГруппировки
	
	// ОтборПоАналитике - Соответствие: Ключ - Строка, устанавливаемая в левое значение отбора
	// в качестве параметра конструктора Новый ПолеКомпоновкиДанных(),
	// а Значение - любое значение, которое устанавливается в правое значение элемента отбора
	// или Структура - см. ЗаполнениеРасшифровкаРегламентированнойОтчетности.НовыйЗначениеЭлементаОтбора
	НастройкиТиповогоОтчета.Вставить("ОтборПоАналитике", Новый Соответствие); 
	
	НастройкиТиповогоОтчета.Вставить("ПоСубсчетам", Истина);
	НастройкиТиповогоОтчета.Вставить("ПоСубсчетамКорСчетов", Ложь);
	
	НастройкиТиповогоОтчета.Вставить("РазвернутоеСальдо", Ложь);
	
	НастройкиТиповогоОтчета.Вставить("КорСчет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	
	// Выводимые данные отчета Обороты счета, заполняются только для этого вида отчета
	// в целях оптимизации объема сохраняемых данных.
	НастройкиТиповогоОтчета.Вставить("НастройкиПоказателей", Новый Структура); // см. НовыйНастройкиПоказателей
	НастройкиТиповогоОтчета.Вставить("ВидыСубконто", Новый Массив); // из ПланВидовРасчетаСсылка.ВидыСубконтоХозрасчетные

	Возврат НастройкиТиповогоОтчета;
	
КонецФункции

Функция НовыйНастройкиРасшифровкиПроизвольногоОтчета()
	НастройкиОтчета = Новый Структура;
	НастройкиОтчета.Вставить("ИмяОтчета", "");
	НастройкиОтчета.Вставить("ПараметрыФормы", НовыйПараметрыФормыПроизвольногоОтчета());
	
	Возврат НастройкиОтчета;
КонецФункции

Функция НовыйПараметрыФормыПроизвольногоОтчета()
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НачалоПериода", Дата(1, 1, 1));
	ПараметрыФормы.Вставить("КонецПериода", Дата(1, 1, 1));
	ПараметрыФормы.Вставить("ВидПериода", 0); // Значение Структуры - см. БухгалтерскиеОтчетыКлиентСервер.ВидыПериода()
	ПараметрыФормы.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	Возврат ПараметрыФормы;
КонецФункции

Функция НовыйОписаниеГруппировки()
	
	ОписаниеГруппировки = Новый Структура;
	ОписаниеГруппировки.Вставить("Использование", Истина);
	ОписаниеГруппировки.Вставить("Поле", "");// Субконто1
	ОписаниеГруппировки.Вставить("Представление", "");
	ОписаниеГруппировки.Вставить("ТипГруппировки", 0); // см. БухгалтерскиеОтчетыКлиентСервер.ТипГруппировки
	Возврат ОписаниеГруппировки;
КонецФункции

Процедура ДобавитьГруппировкуПоСубконто(Настройка, СчетУчета, Субконто)
	
	НомерСубконто = Неопределено;
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СчетУчета);
	Если ТипЗнч(Субконто) = Тип("Число") Тогда
		Если Субконто > СвойстваСчета.КоличествоСубконто Тогда
			Возврат;
		КонецЕсли;
		НомерСубконто =  XMLСтрока(Субконто);
	ИначеЕсли ТипЗнч(Субконто) = Тип("ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные") Тогда
		НомерСубконто = НомерСубконтоСтрокой(СчетУчета, Субконто);
	КонецЕсли;
	
	Если НомерСубконто = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Представление = СвойстваСчета[СтрШаблон("ВидСубконто%1Наименование", XMLСтрока(НомерСубконто))];
	Поле = СтрШаблон("Субконто%1",НомерСубконто);
	
	Группировка = НовыйОписаниеГруппировки();
	Группировка.Поле = Поле;
	Группировка.Представление = Представление;
	Настройка.Добавить(Группировка);
	
КонецПроцедуры

Процедура ИсправитьНеприменимыеНастройкиТиповогоОтчета(ЗначенияНастроек)
	Если ИмяОтчетаУстановленоЗначениемПоУмолчанию(ЗначенияНастроек.ИмяОтчета)
		И Не НастройкиПрименимыВОСВ(ЗначенияНастроек) Тогда
		ЗначенияНастроек.ИмяОтчета = "АнализСчета";
	КонецЕсли;
КонецПроцедуры

Функция НастройкиПрименимыВОСВ(ЗначенияНастроекОтчета)
	
	Если ЗначениеЗаполнено(ЗначенияНастроекОтчета.КорСчет) Тогда
		Возврат Ложь;
	КонецЕсли;

	Для Каждого ЭлементОтбора Из ЗначенияНастроекОтчета.Отбор Цикл
		Если СтрНачинаетсяС(ЭлементОтбора.Ключ, "Счет") Или СтрНачинаетсяС(ЭлементОтбора.Ключ, "Кор") Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ИмяОтчетаУстановленоЗначениемПоУмолчанию(ИмяОтчета)
	Возврат ИмяОтчета = "ОборотноСальдоваяВедомостьПоСчету";
КонецФункции

Процедура УстановитьПериодОтчета(НастройкиОтчета, ПериодОтчета)
	НастройкиОтчета.НачалоПериода = ПериодОтчета.Начало;
	НастройкиОтчета.КонецПериода  = ПериодОтчета.КонецДата;
	НастройкиОтчета.ВидПериода    = ЗаполнениеБухгалтерскойОтчетности.ВидПериодаСтандартногоОтчета(ПериодОтчета.Конец);
КонецПроцедуры

Функция ИмяФормыОтчета(Имя)
	
	Возврат СтрШаблон("Отчет.%1.ФормаОбъекта", Имя);

КонецФункции

Функция ЭтоТиповойОтчет(ЗначенияНастроек)
	Возврат ТиповыеОтчеты().Свойство(ЗначенияНастроек.ИмяОтчета);
КонецФункции

// Возвращает отчеты, настройки которых могут быть сохранены и прочитаны в виде структуры.
// Ключ - ИмяОтчета - имя метаданных, указанное в НовыйЗначенияНастроекОтчета, Значение - идентификатор отчета, используемый
// в ОбщаяФорма.РасшифровкаПоказателейРегламентированныхОтчетов.НастройкиДляТиповогоОтчета.
// 
// Возвращаемое значение:
//  Структура:
// * ОборотноСальдоваяВедомостьПоСчету - Строка
// * АнализСчета - Строка
// * ОборотыСчета - Строка
//
Функция ТиповыеОтчеты()
	ТиповыеОтчеты = Новый Структура;
	ТиповыеОтчеты.Вставить("ОборотноСальдоваяВедомостьПоСчету", "ОСВ");
	ТиповыеОтчеты.Вставить("АнализСчета", "Анализ");
	ТиповыеОтчеты.Вставить("ОборотыСчета", "ОборотыСчета");
	ТиповыеОтчеты.Вставить("АнализСубконто", "АнализСубконто");
	Возврат ТиповыеОтчеты;
КонецФункции
#КонецОбласти

#КонецОбласти