
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("ПрослеживаемостьИмпортныхТоваров");
	
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	//++ Локализация
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("АктОРасхождениях")
		И ДанныеЗаполнения.Свойство("ОснованиеАкта")
		И Не ДанныеЗаполнения.Допоставка Тогда
		ИнтеграцияИСУТ.ЗаполнитьШтрихкодыУпаковокПоАктуОРасхождениях(Объект, ДанныеЗаполнения.АктОРасхождениях);
		
	КонецЕсли;
	
	//++ НЕ УТ
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Объект.СтранаРеализации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "СтранаРегистрации");
	КонецЕсли;
	//-- НЕ УТ
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	//++ Локализация
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		УчетПрослеживаемыхТоваровЛокализация.ПроверитьДанныеПрослеживаемостиНомеровГТД(Объект, Объект.ВидыЗапасов, Объект.Дата, "ВидыЗапасов");
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	//++ Локализация
	
	Документы.ТранспортнаяНакладная.ОбновитьРеквизитыТранспортныхНакладныхПриЗаписиДокументаОснования(Объект, Отказ);
	
	//-- Локализация
	
	Возврат;
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
//++ Локализация

	Объект.ШтрихкодыУпаковок.Очистить();

//-- Локализация

	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	
КонецПроцедуры

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	//++ Локализация
	
	Документы.СчетФактураВыданный.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	//++ НЕ УТ
	
	Документы.ТаможеннаяДекларацияЭкспорт.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Документы.ЗаявлениеОВвозеТоваровПолученное.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	//-- НЕ УТ
	
	ОбменСГИСЭПДПереопределяемый.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	//-- Локализация
	
КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	//++ Локализация
	
	ЭтоПартнер = ПраваПользователяПовтИсп.ЭтоПартнер();
	
	Если Не ЭтоПартнер Тогда

		// Товарная накладная (ТОРГ-12)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "ТОРГ12";
		КомандаПечати.Представление = НСтр("ru = 'Товарная накладная (ТОРГ-12)';
											|en = 'Invoice (TORG-12)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Ложь);
		КомандаПечати.Порядок = 17;
		
		// Товарная накладная с номерами ГТД (ТОРГ-12)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "ТОРГ12";
		КомандаПечати.Представление = НСтр("ru = 'Товарная накладная с номерами ГТД (ТОРГ-12)';
											|en = 'Invoice with CCD numbers (TORG-12)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьГТД",    Истина);
		КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Ложь);
		КомандаПечати.Порядок = 19;

	КонецЕсли;
	
	// Счет-фактура
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "СчетФактура";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура';
										|en = 'Tax invoice'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Ложь);
	КомандаПечати.Порядок = 20;

	Если НЕ ЭтоПартнер Тогда
		// Счет-фактура (в валюте)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "СчетФактураВВалюте";
		КомандаПечати.Представление = НСтр("ru = 'Счет-фактура (в валюте)';
											|en = 'Tax invoice (in currency)'");
		КомандаПечати.ФункциональныеОпции = "ИспользоватьНесколькоВалют";
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ПечатьВВалюте", Истина);
		КомандаПечати.Порядок = 21;
	КонецЕсли;
	
	Если ПравоДоступа("Изменение", Метаданные.Документы.ОтгрузкаТоваровКлиенту) Тогда

		// Реестр номеров ГТД
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "РеестрНомеровГТД";
		КомандаПечати.Представление = НСтр("ru = 'Реестр номеров ГТД';
											|en = 'CCD number registry'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 22;
		КомандаПечати.ФункциональныеОпции = "ИспользоватьИмпортныеТовары";
	КонецЕсли;
	
	// Порядок 23 - ТТН, 24 - Транспортная накладная
	Документы.ТранспортнаяНакладная.ДобавитьКомандыПечати(КомандыПечати, 23);
	
	Если Не ЭтоПартнер Тогда

		// Накладная (М-15)
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "М15";
		КомандаПечати.Представление = НСтр("ru = 'Накладная (М-15)';
											|en = 'Invoice (M-15)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 25;

	КонецЕсли;
	
	Если НЕ ЭтоПартнер
		 И ПолучитьФункциональнуюОпцию("ИспользоватьОтветственноеХранение") Тогда
		// МХ-3 
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
		КомандаПечати.Идентификатор = "МХ3";
		КомандаПечати.Представление = НСтр("ru = 'Акт о возврате ТМЦ, сданных на хранение (МХ-3)';
											|en = 'Certificate of stored inventory return (MH-3)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.Порядок = 50;
	КонецЕсли;
	
	// Универсальный передаточный документ (УПД)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "УПД";
	КомандаПечати.Представление = НСтр("ru = 'Универсальный передаточный документ (УПД)';
										|en = 'Universal transfer document (UTD)'");
	КомандаПечати.Порядок = 40;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// Универсальный корректировочный документ (УКД)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "УКД";
	КомандаПечати.Представление = НСтр("ru = 'Универсальный корректировочный документ (УКД)';
										|en = 'Universal adjustment document (UAD)'");
	КомандаПечати.Порядок = 42;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(
		КомандаПечати,
		"Исправление",
		Истина,
		ВидСравнения.Равно);
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив Из ДокументСсылка - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений Из ДокументСсылка - значение - ссылка на объект, представление - имя области.
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	//++ Локализация
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РеестрНомеровГТД") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"РеестрНомеровГТД",
			НСтр("ru = 'Реестр номеров ГТД';
				|en = 'CCD number registry'"),
			СформироватьПечатнуюФормуРеестрНомеровГТД(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.ОтгрузкаТоваровКлиенту.ПФ_MXL_РеестрНомеровГТД");
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

// Формирует комплект печатных форм по переданным объектам.
//
// Параметры:
// 	МассивОбъектов - см. УправлениеПечатьюПереопределяемый.ПриПечати.МассивОбъектов.
// 	ПараметрыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ПараметрыПечати.
// 	КоллекцияПечатныхФорм - см. УправлениеПечатьюПереопределяемый.ПриПечати.КоллекцияПечатныхФорм.
// 	ОбъектыПечати - см. УправлениеПечатьюПереопределяемый.ПриПечати.ОбъектыПечати.
// 	КомплектПечатныхФорм - см. РегистрыСведений.НастройкиПечатиОбъектов.ПодготовитьКомплектПечатныхФорм.
// 	
Процедура СформироватьКомплектПечатныхФорм(МассивОбъектов,
										ПараметрыПечати,
										КоллекцияПечатныхФорм,
										ОбъектыПечати,
										КомплектПечатныхФорм) Экспорт
	
	//++ Локализация
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
																									КомплектПечатныхФорм,
																									МассивОбъектов,
																									"ТОРГ12");
	
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтгрузкаТоваровКлиенту", КомплектПечати.Объекты);
		
		ПараметрыПечатиФормы = Новый Структура("ВыводитьУслуги", Ложь);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуТОРГ12(СтруктураТипов, ОбъектыПечати, ПараметрыПечатиФормы));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
																									КомплектПечатныхФорм,
																									МассивОбъектов,
																									"ТОРГ12_ГТД");
		
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтгрузкаТоваровКлиенту", КомплектПечати.Объекты);
		
		ПараметрыПечатиФормы = Новый Структура("ВыводитьУслуги, ВыводитьГТД", Ложь, Истина);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуТОРГ12(СтруктураТипов, ОбъектыПечати, ПараметрыПечатиФормы));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
																									КомплектПечатныхФорм,
																									МассивОбъектов,
																									"СчетФактура");
		
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтгрузкаТоваровКлиенту", КомплектПечати.Объекты);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуСчетФактура(СтруктураТипов, ОбъектыПечати, Новый Структура("ПечатьВВалюте", Ложь)));
	КонецЦикла;
		
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
																									КомплектПечатныхФорм,
																									МассивОбъектов,
																									"СчетФактураВВалюте");
		
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтгрузкаТоваровКлиенту", КомплектПечати.Объекты);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуСчетФактура(СтруктураТипов, ОбъектыПечати, Новый Структура("ПечатьВВалюте", Истина)));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
																									КомплектПечатныхФорм,
																									МассивОбъектов,
																									"РеестрНомеровГТД");
	
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			СформироватьПечатнуюФормуРеестрНомеровГТД(КомплектПечати.Объекты, ОбъектыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
																									КомплектПечатныхФорм,
																									МассивОбъектов,
																									"ТТН");
	
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		ОбъектыКоторыеМожноПечатать = Документы.ТранспортнаяНакладная.ОбъектыКоторыеМожноПечатать(КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Документы.ТранспортнаяНакладная.СформироватьПечатнуюФормуТТН(ОбъектыКоторыеМожноПечатать, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
																									КомплектПечатныхФорм,
																									МассивОбъектов,
																									"ТранспортнаяНакладная");
	
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		ОбъектыКоторыеМожноПечатать = Документы.ТранспортнаяНакладная.ОбъектыКоторыеМожноПечатать(КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Документы.ТранспортнаяНакладная.СформироватьПечатнуюФормуТранспортнойНакладной(ОбъектыКоторыеМожноПечатать,
																								ОбъектыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
																									КомплектПечатныхФорм,
																									МассивОбъектов,
																									"М15");
	
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтгрузкаТоваровКлиенту", КомплектПечати.Объекты);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуМ15(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
																									КомплектПечатныхФорм,
																									МассивОбъектов,
																									"МХ1");
	
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтгрузкаТоваровКлиенту", КомплектПечати.Объекты);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуМХ1(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(
		КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"УПД");
		
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтгрузкаТоваровКлиенту", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуУПД(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;

	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(
		КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"УКД");
		
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтгрузкаТоваровКлиенту", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуУКД(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;
	
	//-- Локализация
	
КонецПроцедуры

// Дополняет комплект печатных форм локализованными объектами.
//
// Параметры:
// 	КомплектПечатныхФорм - см. РегистрыСведений.НастройкиПечатиОбъектов.ПодготовитьКомплектПечатныхФорм.
//
Процедура КомплектПечатныхФорм(КомплектПечатныхФорм) Экспорт
	
	//++ Локализация
	
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(
		КомплектПечатныхФорм,
		"ТОРГ12",
		НСтр("ru = 'Товарная накладная (ТОРГ-12)';
			|en = 'Invoice (TORG-12)'"),
		0);
	
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(
		КомплектПечатныхФорм,
		"ТОРГ12_ГТД",
		НСтр("ru = 'Товарная накладная с номерами ГТД (ТОРГ-12)';
			|en = 'Invoice with CCD numbers (TORG-12)'"),
		0);
		
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(
		КомплектПечатныхФорм,
		"СчетФактура",
		НСтр("ru = 'Счет-фактура';
			|en = 'Tax invoice'"),
		2);
		
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют") Тогда
		РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(
			КомплектПечатныхФорм,
			"СчетФактураВВалюте",
			НСтр("ru = 'Счет-фактура (в валюте)';
				|en = 'Tax invoice (in currency)'"),
			0);
	КонецЕсли;
	
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(
		КомплектПечатныхФорм,
		"РеестрНомеровГТД",
		НСтр("ru = 'Реестр номеров ГТД';
			|en = 'CCD number registry'"),
		0);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьТТН") Тогда
		РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(
			КомплектПечатныхФорм,
			"ТТН",
			НСтр("ru = 'Товарно-транспортная накладная (1-Т)';
				|en = 'Waybill (1-T)'"),
			0);
		
		РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(
			КомплектПечатныхФорм,
			"ТранспортнаяНакладная",
			НСтр("ru = 'Транспортная накладная';
				|en = 'Waybill'"),
			0);
	КонецЕсли;
	
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(
		КомплектПечатныхФорм,
		"М15",
		НСтр("ru = 'Накладная (М-15)';
			|en = 'Invoice (M-15)'"),
		0);
	
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(
		КомплектПечатныхФорм,
		"МХ1",
		НСтр("ru = 'Акт о приеме-передаче ТМЦ на хранение (МХ-1)';
			|en = 'Certificate of inventory handover for storage (MH-1)'"),
		0);

	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(
		КомплектПечатныхФорм,
		"УПД",
		НСтр("ru = 'Универсальный передаточный документ (УПД)';
			|en = 'Universal transfer document (UTD)'"),
		0);

	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(
		КомплектПечатныхФорм,
		"УКД",
		НСтр("ru = 'Универсальный корректировочный документ (УКД)';
			|en = 'Universal adjustment document (UAD)'"),
		0);

	//-- Локализация
	
КонецПроцедуры

//++ Локализация

// Функция получает данные для формирования печатной формы ТОРГ - 12
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив - Массив ссылок на документы, по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыТОРГ12(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Валюта КАК Валюта,
	|	ДанныеДокументов.ИсправляемыйДокумент КАК ИсправляемыйДокумент
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	Если ПараметрыПечати.Свойство("ВыводитьГТД") Тогда
		ПараметрыЗаполнения.ВключаяНомераГТД = ПараметрыПечати.ВыводитьГТД;
	КонецЕсли;
	ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов = Истина;
	
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	ПродажиСервер.ПоместитьВременнуюТаблицуКоэффициентыУпаковок(МенеджерВременныхТаблиц,
		"ОтгрузкаТоваровКлиентуТаблицаТоваров");
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов,
		МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтгрузкаТоваровКлиенту.Ссылка                         КАК Ссылка,
	|	ОтгрузкаТоваровКлиенту.Номер                          КАК Номер,
	|	ОтгрузкаТоваровКлиенту.Дата                           КАК Дата,
	|	НЕОПРЕДЕЛЕНО                                          КАК Статус,
	|	ОтгрузкаТоваровКлиенту.Партнер                        КАК Партнер,
	|	ОтгрузкаТоваровКлиенту.Контрагент                     КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ОтгрузкаТоваровКлиенту.Организация.ОбособленноеПодразделение
	|			ТОГДА ОтгрузкаТоваровКлиенту.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ ОтгрузкаТоваровКлиенту.Организация
	|	КОНЕЦ                                                 КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование     КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность        КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ОтгрузкаТоваровКлиенту.Отпустил                       КАК Кладовщик,
	|	ОтгрузкаТоваровКлиенту.ОтпустилДолжность              КАК ДолжностьКладовщика,
	|	ОтгрузкаТоваровКлиенту.Организация.Префикс            КАК Префикс,
	|	ОтгрузкаТоваровКлиенту.Основание                      КАК Основание,
	|	ОтгрузкаТоваровКлиенту.ОснованиеДата                  КАК ОснованиеДата,
	|	ОтгрузкаТоваровКлиенту.ОснованиеНомер                 КАК ОснованиеНомер,
	|	ВЫБОР
	|		КОГДА ОтгрузкаТоваровКлиенту.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ОтгрузкаТоваровКлиенту.Контрагент
	|		ИНАЧЕ ОтгрузкаТоваровКлиенту.Грузополучатель
	|	КОНЕЦ                                                 КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА ОтгрузкаТоваровКлиенту.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ОтгрузкаТоваровКлиенту.Организация
	|		ИНАЧЕ ОтгрузкаТоваровКлиенту.Грузоотправитель
	|	КОНЕЦ                                                 КАК Грузоотправитель,
	|	ОтгрузкаТоваровКлиенту.БанковскийСчетОрганизации      КАК БанковскийСчетОрганизации,
	|	ОтгрузкаТоваровКлиенту.БанковскийСчетКонтрагента      КАК БанковскийСчетКонтрагента,
	|	ОтгрузкаТоваровКлиенту.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	ОтгрузкаТоваровКлиенту.БанковскийСчетГрузополучателя  КАК БанковскийСчетГрузополучателя,
	|	ОтгрузкаТоваровКлиенту.АдресДоставки                  КАК АдресДоставки,
	|	НЕОПРЕДЕЛЕНО                                          КАК Подразделение,
	|	ОтгрузкаТоваровКлиенту.Валюта                         КАК Валюта,
	|	ОтгрузкаТоваровКлиенту.НалогообложениеНДС             КАК НалогообложениеНДС,
	|	ОтгрузкаТоваровКлиенту.ДоверенностьНомер              КАК ДоверенностьНомер,
	|	ОтгрузкаТоваровКлиенту.ДоверенностьДата               КАК ДоверенностьДата,
	|	ОтгрузкаТоваровКлиенту.ДоверенностьВыдана             КАК ДоверенностьВыдана,
	|	ОтгрузкаТоваровКлиенту.ДоверенностьЛицо               КАК ДоверенностьЛицо,
	|	&ЕдиницаИзмеренияВеса                                 КАК ЕдиницаИзмеренияВеса
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ОтгрузкаТоваровКлиенту
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ОтгрузкаТоваровКлиенту.Ссылка = ДанныеДокументов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ОтгрузкаТоваровКлиенту.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка                     КАК Ссылка,
	|	ТаблицаТоваров.НомерСтроки                КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ
	|			&ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ                                     КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|	КОНЕЦ                                     КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ
	|			&ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ                                     КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТоваров.Упаковка                   КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
	|			ТОГДА """"
	|		ИНАЧЕ
	|			ТаблицаТоваров.Упаковка.Наименование
	|	КОНЕЦ                                     КАК УпаковкаНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА &ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|		ИНАЧЕ
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения2
	|	КОНЕЦ                                     КАК ВидУпаковки,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ
	|			ТаблицаТоваров.Количество
	|	КОНЕЦ                                     КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			И НЕ ТаблицаТоваров.ЭтоНабор
	|			ТОГДА ВЫБОР
	|				КОГДА ТаблицаТоваров.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				И ((ТаблицаТоваров.Номенклатура.ВесИспользовать
	|				И ТаблицаТоваров.Номенклатура.ВесМожноУказыватьВДокументах
	|				И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ВесЕдиницаИзмерения)
	|				ИЛИ (ТаблицаТоваров.Номенклатура.ОбъемИспользовать
	|				И ТаблицаТоваров.Номенклатура.ОбъемМожноУказыватьВДокументах
	|				И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ОбъемЕдиницаИзмерения)
	|				ИЛИ (ТаблицаТоваров.Номенклатура.ДлинаИспользовать
	|				И ТаблицаТоваров.Номенклатура.ДлинаМожноУказыватьВДокументах
	|				И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ДлинаЕдиницаИзмерения)
	|				ИЛИ (ТаблицаТоваров.Номенклатура.ПлощадьИспользовать
	|				И ТаблицаТоваров.Номенклатура.ПлощадьМожноУказыватьВДокументах
	|				И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ПлощадьЕдиницаИзмерения))
	|					ТОГДА КоэффициентыУпаковок.КоличествоУпаковок
	|				ИНАЧЕ ТаблицаТоваров.Количество / КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА ТаблицаТоваров.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			И &ВыводитьБазовыеЕдиницыИзмерения
	|			И ((ТаблицаТоваров.Номенклатура.ВесИспользовать
	|			И ТаблицаТоваров.Номенклатура.ВесМожноУказыватьВДокументах
	|			И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ВесЕдиницаИзмерения)
	|			ИЛИ (ТаблицаТоваров.Номенклатура.ОбъемИспользовать
	|			И ТаблицаТоваров.Номенклатура.ОбъемМожноУказыватьВДокументах
	|			И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ОбъемЕдиницаИзмерения)
	|			ИЛИ (ТаблицаТоваров.Номенклатура.ДлинаИспользовать
	|			И ТаблицаТоваров.Номенклатура.ДлинаМожноУказыватьВДокументах
	|			И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ДлинаЕдиницаИзмерения)
	|			ИЛИ (ТаблицаТоваров.Номенклатура.ПлощадьИспользовать
	|			И ТаблицаТоваров.Номенклатура.ПлощадьМожноУказыватьВДокументах
	|			И ТаблицаТоваров.Упаковка = ТаблицаТоваров.Номенклатура.ПлощадьЕдиницаИзмерения))
	|				ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|			КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ИЛИ ТаблицаТоваров.Упаковка = (ВЫБОР
	|				КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|					ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|				ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|			КОНЕЦ)
	|				ТОГДА ТаблицаТоваров.Количество
	|			ИНАЧЕ ТаблицаТоваров.Количество / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)
	|		КОНЕЦ
	|	КОНЕЦ                                     КАК КоличествоМестДробное,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|		И НЕ ТаблицаТоваров.ЭтоНабор
	|			ТОГДА ВЫБОР
	|				КОГДА ТаблицаТоваров.Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|					И ТаблицаТоваров.Упаковка = &ТекстЗапросаЕдиницаИзмерения
	|					ТОГДА 1
	|				ИНАЧЕ
	|					КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ИЛИ ТаблицаТоваров.Упаковка = (ВЫБОР
	|				КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|					ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
	|				ИНАЧЕ
	|					&ТекстЗапросаЕдиницаИзмерения
	|			КОНЕЦ)
	|				ТОГДА 1
	|			ИНАЧЕ
	|				&ТекстЗапросаКоэффициентУпаковки
	|		КОНЕЦ
	|	КОНЕЦ                                     КАК КоличествоВОдномМесте,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА &ТекстЗапросаВесУпаковки
	|		ИНАЧЕ
	|			&ТекстЗапросаВесВидаУпаковки
	|	КОНЕЦ                                     КАК МассаБруттоОдногоМеста,
	|	ЕСТЬNULL(КоэффициентыУпаковок.КоэффициентУпаковки, 1) *
	|	    ТаблицаТоваров.МассаНеттоНоменклатуры КАК МассаНеттоОднойУпаковки,
	|	ТаблицаТоваров.НомерГТД                   КАК НомерГТД,
	|	ТаблицаТоваров.Номенклатура               КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика             КАК Характеристика,
	|	1                                         КАК КоэффициентУпаковки
	|ПОМЕСТИТЬ РассчитанныеДанныеУпаковокЕдиницИзмерения
	|ИЗ
	|	ОтгрузкаТоваровКлиентуТаблицаТоваров КАК ТаблицаТоваров
	|	ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыУпаковок КАК КоэффициентыУпаковок
	|		ПО ТаблицаТоваров.Ссылка = КоэффициентыУпаковок.Ссылка
	|		И ТаблицаТоваров.НомерСтроки = КоэффициентыУпаковок.НомерСтроки
	|		И ТаблицаТоваров.Номенклатура = КоэффициентыУпаковок.Номенклатура
	|		И ТаблицаТоваров.КоличествоУпаковок = КоэффициентыУпаковок.КоличествоУпаковок
	|		И НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|ГДЕ
	|	ТаблицаТоваров.ЭтоТовар
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НомерСтроки,
	|	Номенклатура,
	|	Характеристика,
	|	НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка                                    КАК Ссылка,
	|	ТаблицаТоваров.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаТоваров.ВариантРасчетаЦеныНабора                  КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаТоваров.НоменклатураНабора                        КАК НоменклатураНабора,
	|	ТаблицаТоваров.ХарактеристикаНабора                      КАК ХарактеристикаНабора,
	|	ТаблицаТоваров.ЭтоКомплектующие                          КАК ЭтоКомплектующие,
	|	ТаблицаТоваров.ЭтоНабор                                  КАК ЭтоНабор,
	|	ТаблицаТоваров.ПолныйНабор                               КАК ПолныйНабор,
	|	ТаблицаТоваров.Номенклатура                              КАК Номенклатура,
	|	ТаблицаТоваров.Номенклатура.НаименованиеПолное           КАК НоменклатураНаименование,
	|	ТаблицаТоваров.Номенклатура.Наименование                 КАК НоменклатураНаименованиеКраткое,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаТоваров.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаТоваров.Номенклатура.Код
	|	КОНЕЦ                                                    КАК НоменклатураКод,
	|	ДанныеУпаковок.ЕдиницаИзмерения                          КАК ЕдиницаИзмерения,
	|	ДанныеУпаковок.ЕдиницаИзмеренияНаименование              КАК ЕдиницаИзмеренияНаименование,
	|	ДанныеУпаковок.ЕдиницаИзмеренияКод                       КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТоваров.Характеристика                            КАК Характеристика,
	|	ТаблицаТоваров.Характеристика.НаименованиеПолное         КАК ХарактеристикаНаименование,
	|	ТаблицаТоваров.Серия.Наименование                        КАК СерияНаименование,
	|	ДанныеУпаковок.Упаковка                                  КАК Упаковка,
	|	ДанныеУпаковок.УпаковкаНаименование                      КАК УпаковкаНаименование,
	|	ДанныеУпаковок.ВидУпаковки                               КАК ВидУпаковки,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)            КАК СтавкаНДС,
	|	ТаблицаТоваров.НомерГТД                                  КАК НомерГТД,
	|	ТаблицаТоваров.НомерГТД.СтранаПроисхождения              КАК СтранаПроисхождения,
	|	ДанныеУпаковок.Количество                                КАК Количество,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|			ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|		ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|	КОНЕЦ                                                    КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.ЕдиницаИзмерения = ДанныеУпаковок.Упаковка.ЕдиницаИзмерения
	|			ТОГДА 1
	|		ИНАЧЕ ДанныеУпаковок.КоличествоВОдномМесте
	|	КОНЕЦ                                                    КАК КоличествоВОдномМесте,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ВЫБОР
	|				КОГДА НЕ ТаблицаТоваров.КоличествоУпаковок = 0
	|					ТОГДА ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА НЕ ТаблицаТоваров.Количество = 0
	|				ТОГДА ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ                                                    КАК Цена,
	|	0                                                        КАК СуммаБезНДС,
	|	0                                                        КАК СуммаНДС,
	|	ТаблицаТоваров.СуммаБезНДС + ТаблицаТоваров.СуммаНДС     КАК СуммаСНДС,
	|	ТаблицаТоваров.МассаНетто                                КАК МассаНетто,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|			ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|		ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|	КОНЕЦ * ЕСТЬNULL(ДанныеУпаковок.МассаБруттоОдногоМеста, 0) - (ВЫБОР
	|		КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|			ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|		ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|	КОНЕЦ * ЕСТЬNULL(ДанныеУпаковок.КоличествоВОдномМесте, 1) - ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.Количество
	|		ИНАЧЕ (ВЫБОР 
	|				КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				ТОГДА (ВЫБОР
	|						КОГДА ДанныеУпаковок.КоличествоМестДробное = (ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное КАК ЧИСЛО(12, 0)))
	|							ТОГДА ДанныеУпаковок.КоличествоМестДробное
	|						ИНАЧЕ ВЫРАЗИТЬ(ДанныеУпаковок.КоличествоМестДробное + 0.5 КАК ЧИСЛО(12, 0))
	|						КОНЕЦ) * ЕСТЬNULL(ДанныеУпаковок.КоличествоВОдномМесте, 1)
	|				ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок * ЕСТЬNULL(ДанныеУпаковок.КоличествоВОдномМесте, 1)
	|			КОНЕЦ)
	|	КОНЕЦ) * ДанныеУпаковок.МассаНеттоОднойУпаковки / ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(ДанныеУпаковок.КоличествоВОдномМесте, 1)
	|	КОНЕЦ                                                    КАК МассаБрутто,
	|	ТаблицаТоваров.НомерСтроки                               КАК НомерСтроки,
	|	ТаблицаТоваров.НомерСтрокиНаборы                         КАК НомерСтрокиНаборы,
	|	ЛОЖЬ                                                     КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ОтгрузкаТоваровКлиентуТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ РассчитанныеДанныеУпаковокЕдиницИзмерения КАК ДанныеУпаковок
	|		ПО ТаблицаТоваров.Ссылка = ДанныеУпаковок.Ссылка
	|		И ТаблицаТоваров.НомерСтроки = ДанныеУпаковок.НомерСтроки
	|		И ТаблицаТоваров.Номенклатура = ДанныеУпаковок.Номенклатура
	|		И ТаблицаТоваров.Характеристика = ДанныеУпаковок.Характеристика
	|		И ТаблицаТоваров.НомерГТД = ДанныеУпаковок.НомерГТД
	|ГДЕ
	|	ТаблицаТоваров.ЭтоТовар
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтрокиНаборы,
	|	ЭтоНабор УБЫВ,
	|	НомерСтроки
	|ИТОГИ
	|ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВесУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));

	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВесВидаУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"КоэффициентыУпаковок.ВидУпаковки",
			"ТаблицаТоваров.Номенклатура"));

	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"КоэффициентыУпаковок.ВидУпаковки",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.УстановитьПараметр("КолонкаКодов",         КолонкаКодов);
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияВеса", Константы.ЕдиницаИзмеренияВеса.Получить());
	Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса",
		ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения",
		Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить() ИЛИ ПараметрыЗаполнения.ВключаяНомераГТД);
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[МассивРезультатов.ВГраница()];
	
	СтруктураДанныхДляПечати 	= Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
	                                            РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Формирует временную таблицу, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц, содержащий таблицу ТаблицаДанныхДокументов с полями:
//		* Ссылка 				- ДокументСсылка.ОтгрузкаТоваровКлиенту 	- ссылка на документ.
//		* Валюта 				- СправочникСсылка.Валюты 					- валюта документа.
//		* ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации 	- операция документа.
//
//	ПараметрыЗаполнения - Структура - структура, возвращаемая функцией ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров
//
Процедура ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтгрузкаТоваровКлиенту.Ссылка               КАК Документ,
	|	ОтгрузкаТоваровКлиенту.ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	ОтгрузкаТоваровКлиенту.Дата                 КАК ДатаДокумента,
	|	ОтгрузкаТоваровКлиенту.МоментВремени        КАК МоментВремени
	|ПОМЕСТИТЬ ЦепочкаДокументовБезПредыдущего
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ОтгрузкаТоваровКлиенту
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|ПО
	|	ОтгрузкаТоваровКлиенту.Ссылка = ДанныеДокументов.Ссылка
	|ГДЕ
	|	ОтгрузкаТоваровКлиенту.Проведен
	|	И &ВключаяДоКорректировки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтгрузкаТоваровКлиенту.Ссылка               КАК Документ,
	|	ОтгрузкаТоваровКлиенту.ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	ОтгрузкаТоваровКлиенту.Дата                 КАК ДатаДокумента,
	|	ОтгрузкаТоваровКлиенту.МоментВремени        КАК МоментВремени
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ОтгрузкаТоваровКлиенту
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|ПО
	|	ОтгрузкаТоваровКлиенту.ИсправляемыйДокумент = ДанныеДокументов.Ссылка.ИсправляемыйДокумент
	|ГДЕ
	|	ДанныеДокументов.Ссылка.ИсправляемыйДокумент <> ЗНАЧЕНИЕ(Документ.ОтгрузкаТоваровКлиенту.ПустаяСсылка)
	|	И ОтгрузкаТоваровКлиенту.Проведен
	|	И &ВключаяДоКорректировки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтгрузкаТоваровКлиенту.Ссылка               КАК Документ,
	|	ОтгрузкаТоваровКлиенту.ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	ОтгрузкаТоваровКлиенту.Дата                 КАК ДатаДокумента,
	|	ОтгрузкаТоваровКлиенту.МоментВремени        КАК МоментВремени
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ОтгрузкаТоваровКлиенту
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|ПО
	|	ОтгрузкаТоваровКлиенту.Ссылка = ДанныеДокументов.Ссылка.ИсправляемыйДокумент
	|ГДЕ
	|	ОтгрузкаТоваровКлиенту.Проведен
	|	И &ВключаяДоКорректировки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЦепочкаДокументовБезПредыдущего.Документ                                  КАК Документ,
	|	ЦепочкаДокументовБезПредыдущего.ИсправляемыйДокумент                      КАК ИсправляемыйДокумент,
	|	ЦепочкаДокументовБезПредыдущего.ДатаДокумента                             КАК ДатаДокумента,
	|	МАКСИМУМ(ЕСТЬNULL(ПредыдущиеДокументы.ДатаДокумента, ДАТАВРЕМЯ(1, 1, 1))) КАК ДатаДокументаПредыдущегоДокумента
	|ПОМЕСТИТЬ ЦепочкаДокументов
	|ИЗ
	|	ЦепочкаДокументовБезПредыдущего КАК ЦепочкаДокументовБезПредыдущего
	|	ЛЕВОЕ СОЕДИНЕНИЕ ЦепочкаДокументовБезПредыдущего КАК ПредыдущиеДокументы
	|		ПО ЦепочкаДокументовБезПредыдущего.МоментВремени > ПредыдущиеДокументы.МоментВремени
	|ГДЕ
	|	&ВключаяДоКорректировки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦепочкаДокументовБезПредыдущего.Документ,
	|	ЦепочкаДокументовБезПредыдущего.ИсправляемыйДокумент,
	|	ЦепочкаДокументовБезПредыдущего.ДатаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦепочкаДокументов.Документ                                  КАК Основной,
	|	ЕСТЬNULL(ЦепочкаПредыдущихДокументов.Документ,
	|		ЗНАЧЕНИЕ(Документ.ОтгрузкаТоваровКлиенту.ПустаяСсылка)) КАК Предыдущий
	|ПОМЕСТИТЬ ПредыдущиеДокументы
	|ИЗ
	|	ЦепочкаДокументов КАК ЦепочкаДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ЦепочкаДокументов.Документ = ДанныеДокументов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ЦепочкаДокументов КАК ЦепочкаПредыдущихДокументов
	|		ПО ЦепочкаДокументов.ДатаДокументаПредыдущегоДокумента = ЦепочкаПредыдущихДокументов.ДатаДокумента
	|ГДЕ
	|	&ВключаяДоКорректировки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка                     КАК Ссылка,
	|	ЛОЖЬ                                   КАК ДоКорректировки,
	|	ВидыЗапасов.НомерСтроки                КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.АналитикаУчетаНаборов      КАК АналитикаУчетаНаборов,
	|	ВидыЗапасов.Упаковка                   КАК Упаковка,
	|	ВидыЗапасов.Цена                       КАК Цена,
	|	ВидыЗапасов.ВидЗапасов                 КАК ВидЗапасов,
	|	ВидыЗапасов.НомерГТД                   КАК НомерГТД,
	|	ВидыЗапасов.ИдентификаторСтроки        КАК ИдентификаторСтроки,
	|	ВидыЗапасов.КодТНВЭД                   КАК КодТНВЭД,
	|	ВидыЗапасов.КоличествоУпаковок         КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество                 КАК Количество,
	|	ВидыЗапасов.КоличествоПоРНПТ           КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, 0) КАК СуммаБезНДС,
	|	ЕСТЬNULL(СуммыРегл.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)) КАК СтавкаНДС,
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, 0)    КАК СуммаНДС,
	|	ВидыЗапасов.ЗаказКлиента               КАК ЗаказКлиента
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.ВидыЗапасов КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ВидыЗапасов.Ссылка = ДанныеДокументов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|		И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|		И СуммыРегл.Активность
	|		И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                                                КАК Ссылка,
	|	ИСТИНА                                                                 КАК ДоКорректировки,
	|	ЕСТЬNULL(ВидыЗапасов.НомерСтроки, 0)                                   КАК НомерСтроки,
	|	ЕСТЬNULL(ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|		ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)) КАК АналитикаУчетаНоменклатуры,
	|	ЕСТЬNULL(ВидыЗапасов.АналитикаУчетаНаборов,
	|		ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНаборов.ПустаяСсылка))      КАК АналитикаУчетаНаборов,
	|	ЕСТЬNULL(ВидыЗапасов.Упаковка,
	|		ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка))        КАК Упаковка,
	|	ЕСТЬNULL(ВидыЗапасов.Цена, 0)                                          КАК Цена,
	|	ЕСТЬNULL(ВидыЗапасов.ВидЗапасов,
	|		ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка))                     КАК ВидЗапасов,
	|	ЕСТЬNULL(ВидыЗапасов.НомерГТД,
	|		ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка))                       КАК НомерГТД,
	|	ЕСТЬNULL(ВидыЗапасов.ИдентификаторСтроки, """")                        КАК ИдентификаторСтроки,
	|	ЕСТЬNULL(ВидыЗапасов.КодТНВЭД,
	|		ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка))              КАК КодТНВЭД,
	|	ЕСТЬNULL(ВидыЗапасов.КоличествоУпаковок, 0)                            КАК КоличествоУпаковок,
	|	ЕСТЬNULL(ВидыЗапасов.Количество, 0)                                    КАК Количество,
	|	ЕСТЬNULL(ВидыЗапасов.КоличествоПоРНПТ, 0)                              КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, 0)                                 КАК СуммаБезНДС,
	|	ЕСТЬNULL(СуммыРегл.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС))   КАК СтавкаНДС,
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, 0)                                    КАК СуммаНДС,
	|	ЕСТЬNULL(ВидыЗапасов.ЗаказКлиента, НЕОПРЕДЕЛЕНО)                       КАК ЗаказКлиента
	|ИЗ
	|	ЦепочкаДокументов КАК ЦепочкаДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ЦепочкаДокументов.Документ = ДанныеДокументов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровКлиенту.ВидыЗапасов КАК ВидыЗапасов
	|		ПО (ЦепочкаДокументов.ИсправляемыйДокумент = ВидыЗапасов.Ссылка.ИсправляемыйДокумент
	|			ИЛИ ЦепочкаДокументов.ИсправляемыйДокумент = ВидыЗапасов.Ссылка)
	|		И ЦепочкаДокументов.ДатаДокументаПредыдущегоДокумента = ВидыЗапасов.Ссылка.Дата
	|		И ВидыЗапасов.Ссылка.Проведен
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|		И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|		И СуммыРегл.Активность
	|		И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка                КАК Ссылка,
	|	ТаблицаТоваров.Номенклатура          КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика        КАК Характеристика,
	|	ТаблицаТоваров.Упаковка              КАК Упаковка,
	|	МАКСИМУМ(ТаблицаТоваров.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ СтрокиТоваров
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ТаблицаТоваров
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ТаблицаТоваров.Ссылка = ДанныеДокументов.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	ТаблицаТоваров.Упаковка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Номенклатура,
	|	Характеристика,
	|	Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                    КАК Ссылка,
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.Ссылка,
	|		ЗНАЧЕНИЕ(Справочник.ВариантыКомплектацииНоменклатуры.ПустаяСсылка)) КАК ВариантКомплектацииНоменклатуры,
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.ВариантПредставленияНабораВПечатныхФормах,
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка)) КАК
	|		ВариантПредставленияНабораВПечатныхФормах,
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора,
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка)) КАК ВариантРасчетаЦеныНабора,
	|	ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНаборов.НоменклатураНабора,
	|		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК НоменклатураНабора,
	|	ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНаборов.ХарактеристикаНабора,
	|		ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ХарактеристикаНабора,
	|	ЛОЖЬ                                                       КАК ЭтоКомплектующие,
	|	ЛОЖЬ                                                       КАК ЭтоНабор,
	|	0                                                          КАК НомерСтрокиНаборы,
	|	ЛОЖЬ                                                       КАК ПолныйНабор,
	|	ЛОЖЬ                                                       КАК ВернутьМногооборотнуюТару,
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999)                 КАК НомерСтроки,
	|	""""                                                       КАК Содержание,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка) КАК НоменклатураПартнера,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Серия          КАК Серия,
	|	ТаблицаДокумента.Упаковка                                  КАК Упаковка,
	|	ВЫБОР
	|		КОГДА &ВключаяНомераГТД
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ
	|			&ПустаяГТД
	|	КОНЕЦ                                                      КАК НомерГТД,
	|	ВЫБОР
	|		КОГДА СУММА(ТаблицаДокумента.Количество) > 0
	|			ТОГДА СУММА(ТаблицаДокумента.Количество)
	|		ИНАЧЕ
	|			-СУММА(ТаблицаДокумента.Количество)
	|	КОНЕЦ                                                      КАК Количество,
	|	ВЫБОР
	|		КОГДА СУММА(ТаблицаДокумента.КоличествоУпаковок) > 0
	|			ТОГДА СУММА(ТаблицаДокумента.КоличествоУпаковок)
	|		ИНАЧЕ
	|			-СУММА(ТаблицаДокумента.КоличествоУпаковок)
	|	КОНЕЦ                                                      КАК КоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА НЕ &ВключаяНомераГТД
	|			ТОГДА 0
	|		КОГДА СУММА(ТаблицаДокумента.КоличествоПоРНПТ) > 0
	|			ТОГДА СУММА(ТаблицаДокумента.КоличествоПоРНПТ)
	|		ИНАЧЕ
	|			-СУММА(ТаблицаДокумента.КоличествоПоРНПТ)
	|	КОНЕЦ                                                      КАК КоличествоПоРНПТ,
	|	ВЫБОР
	|		КОГДА СУММА(ТаблицаДокумента.СуммаБезНДС) > 0
	|			ТОГДА СУММА(ТаблицаДокумента.СуммаБезНДС)
	|		ИНАЧЕ
	|			-СУММА(ТаблицаДокумента.СуммаБезНДС)
	|	КОНЕЦ                                                      КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС                                 КАК СтавкаНДС,
	|	ТаблицаДокумента.КодТНВЭД                                  КАК КодТНВЭД,
	|	ВЫБОР
	|		КОГДА СУММА(ТаблицаДокумента.СуммаНДС) > 0
	|			ТОГДА СУММА(ТаблицаДокумента.СуммаНДС)
	|		ИНАЧЕ
	|			-СУММА(ТаблицаДокумента.СуммаНДС)
	|	КОНЕЦ                                                      КАК СуммаНДС,
	|	ИСТИНА                                                     КАК ЭтоТовар,
	|	ИСТИНА                                                     КАК ЭтоНеВозвратнаяТара,
	|	ТаблицаДокумента.ДоКорректировки                           КАК ДоКорректировки,
	|	&ТекстЗапросаВесНоменклатуры                               КАК МассаНеттоНоменклатуры
	|ПОМЕСТИТЬ ОтгрузкаТоваровКлиентуТаблицаТоваров
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ СтрокиТоваров КАК СтрокиТоваров
	|		ПО ТаблицаДокумента.Ссылка = СтрокиТоваров.Ссылка
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура = СтрокиТоваров.Номенклатура
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика = СтрокиТоваров.Характеристика
	|		И ТаблицаДокумента.Упаковка = СтрокиТоваров.Упаковка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыКомплектацииНоменклатуры КАК ВариантыКомплектацииНоменклатуры
	|		ПО ВариантыКомплектацииНоменклатуры.Владелец = ТаблицаДокумента.АналитикаУчетаНаборов.НоменклатураНабора
	|		И ВариантыКомплектацииНоменклатуры.Характеристика = ТаблицаДокумента.АналитикаУчетаНаборов.ХарактеристикаНабора
	|		И ВариантыКомплектацииНоменклатуры.Основной
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.Ссылка,
	|		ЗНАЧЕНИЕ(Справочник.ВариантыКомплектацииНоменклатуры.ПустаяСсылка)),
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.ВариантПредставленияНабораВПечатныхФормах,
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка)),
	|	ЕСТЬNULL(ВариантыКомплектацииНоменклатуры.ВариантРасчетаЦеныНабора,
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка)),
	|	ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНаборов.НоменклатураНабора,
	|		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)),
	|	ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНаборов.ХарактеристикаНабора,
	|		ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)),
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка),
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Серия,
	|	ТаблицаДокумента.Упаковка,
	|	ВЫБОР
	|		КОГДА &ВключаяНомераГТД
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ
	|			&ПустаяГТД
	|	КОНЕЦ,
	|	ТаблицаДокумента.СтавкаНДС,
	|	ТаблицаДокумента.КодТНВЭД,
	|	ТаблицаДокумента.ДоКорректировки
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаДокумента.Количество) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ЦепочкаДокументовБезПредыдущего
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ЦепочкаДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтВидыЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СтрокиТоваров
	|";
	
	Если ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов Тогда
		
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"ПОМЕСТИТЬ ОтгрузкаТоваровКлиентуТаблицаТоваров",
			"ПОМЕСТИТЬ ОтгрузкаТоваровКлиентуТаблицаТоваровДляПреобразования");
			
		Запрос.Текст = Запрос.Текст + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ
		|	ТаблицаТоваров.Ссылка                         КАК Ссылка,
		|	ТаблицаТоваров.НоменклатураНабора             КАК НоменклатураНабора,
		|	ТаблицаТоваров.ХарактеристикаНабора           КАК ХарактеристикаНабора,
		|	МАКСИМУМ(ТаблицаТоваров.ЭтоТовар)             КАК ЭтоТовар,
		|	МИНИМУМ(ТаблицаТоваров.НомерСтроки)           КАК НомерСтроки,
		|	СУММА(ВЫБОР
		|			КОГДА &ЗаполненаЕдиницаИзмеренияВеса
		|				ТОГДА ВЫБОР
		|						КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL
		|							ТОГДА ТаблицаТоваров.Количество
		|						ИНАЧЕ
		|							ВЫБОР
		|								КОГДА ТаблицаТоваров.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Вес)
		|									ТОГДА 0
		|								ИНАЧЕ
		|									ТаблицаТоваров.КоличествоУпаковок
		|							КОНЕЦ
		|					КОНЕЦ * &ТекстЗапросаВесУпаковки
		|			ИНАЧЕ
		|				0
		|	КОНЕЦ)                                        КАК МассаБрутто,
		|	СУММА(ТаблицаТоваров.Количество *
		|	       ТаблицаТоваров.МассаНеттоНоменклатуры) КАК МассаНетто,
		|	СУММА(ТаблицаТоваров.МассаНеттоНоменклатуры)  КАК МассаНеттоНоменклатуры,
		|	СУММА(ТаблицаТоваров.СуммаБезНДС)             КАК СуммаБезНДС,
		|	СУММА(ТаблицаТоваров.СуммаНДС)                КАК СуммаНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаНаборыПодготовка
		|ИЗ
		|	ОтгрузкаТоваровКлиентуТаблицаТоваровДляПреобразования КАК ТаблицаТоваров
		|ГДЕ
		|	ТаблицаТоваров.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаТоваров.Ссылка,
		|	ТаблицаТоваров.НоменклатураНабора,
		|	ТаблицаТоваров.ХарактеристикаНабора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Результат.Ссылка                                    КАК Ссылка,
		|	Результат.ВариантКомплектацииНоменклатуры           КАК ВариантКомплектацииНоменклатуры,
		|	Результат.ВариантРасчетаЦеныНабора                  КАК ВариантРасчетаЦеныНабора,
		|	Результат.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|	Результат.НоменклатураНабора                        КАК НоменклатураНабора,
		|	Результат.ХарактеристикаНабора                      КАК ХарактеристикаНабора,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА Результат.ОсновнаяКомплектующая
		|				ТОГДА Результат.СтавкаНДС
		|			ИНАЧЕ NULL
		|	КОНЕЦ)                                              КАК СтавкаНДС,
		|	МАКСИМУМ(Результат.НомерГТД)                        КАК НомерГТД,
		|	МАКСИМУМ(Результат.КодТНВЭД)                        КАК КодТНВЭД,
		|	(ВЫРАЗИТЬ(МИНИМУМ(ВЫБОР
		|				КОГДА Результат.КоличествоПоУмолчанию <> 0
		|						И Результат.ОсновнаяКомплектующая
		|					ТОГДА Результат.Количество / Результат.КоличествоПоУмолчанию
		|				ИНАЧЕ NULL
		|	        КОНЕЦ) + 0.5 КАК ЧИСЛО(10, 0))) - 1         КАК Количество,
		|	МАКСИМУМ(Результат.КоличествоПоРНПТ)                КАК КоличествоПоРНПТ,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА Результат.КоличествоПоУмолчанию <> 0
		|				ТОГДА Результат.Количество / Результат.КоличествоПоУмолчанию
		|			ИНАЧЕ NULL
		|	    КОНЕЦ)                                          КАК КоэффициентМаксимум,
		|	(ВЫРАЗИТЬ(МИНИМУМ(ВЫБОР
		|				КОГДА Результат.КоличествоПоУмолчанию <> 0
		|					ТОГДА Результат.Количество / Результат.КоличествоПоУмолчанию
		|				ИНАЧЕ NULL
		|	        КОНЕЦ) + 0.5 КАК ЧИСЛО(10, 0))) - 1         КАК КоэффициентМинимум
		|ПОМЕСТИТЬ ВременнаяТаблицаНаборыДополнительно
		|ИЗ
		|	(ВЫБРАТЬ
		|		Таблица.Ссылка КАК Ссылка,
		|		Таблица.ВариантКомплектацииНоменклатуры           КАК ВариантКомплектацииНоменклатуры,
		|		Таблица.ВариантРасчетаЦеныНабора                  КАК ВариантРасчетаЦеныНабора,
		|		Таблица.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|		Таблица.НоменклатураНабора                        КАК НоменклатураНабора,
		|		Таблица.ХарактеристикаНабора                      КАК ХарактеристикаНабора,
		|		Таблица.Номенклатура                              КАК Номенклатура,
		|		Таблица.Характеристика                            КАК Характеристика,
		|		МАКСИМУМ(Таблица.СтавкаНДС)                       КАК СтавкаНДС,
		|		МАКСИМУМ(Таблица.НомерГТД)                        КАК НомерГТД,
		|		МАКСИМУМ(Таблица.КодТНВЭД)                        КАК КодТНВЭД,
		|		МАКСИМУМ(Таблица.ОсновнаяКомплектующая)           КАК ОсновнаяКомплектующая,
		|		СУММА(Таблица.КоличествоПоУмолчанию)              КАК КоличествоПоУмолчанию,
		|		СУММА(Таблица.Количество)                         КАК Количество,
		|		СУММА(Таблица.КоличествоПоРНПТ)                   КАК КоличествоПоРНПТ
		|	ИЗ
		|		(ВЫБРАТЬ
		|			Товары.Ссылка                                    КАК Ссылка,
		|			Товары.ВариантКомплектацииНоменклатуры           КАК ВариантКомплектацииНоменклатуры,
		|			Товары.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|			Товары.ВариантРасчетаЦеныНабора                  КАК ВариантРасчетаЦеныНабора,
		|			Товары.НоменклатураНабора                        КАК НоменклатураНабора,
		|			Товары.ХарактеристикаНабора                      КАК ХарактеристикаНабора,
		|			Товары.Номенклатура                              КАК Номенклатура,
		|			Товары.Характеристика                            КАК Характеристика,
		|			ВЫБОР
		|				КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
		|						И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ
		|					ЛОЖЬ
		|			КОНЕЦ                                            КАК ОсновнаяКомплектующая,
		|			Товары.СтавкаНДС                                 КАК СтавкаНДС,
		|			ВЫБОР
		|				КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
		|						И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика
		|					ТОГДА Товары.НомерГТД
		|				ИНАЧЕ
		|					NULL
		|			КОНЕЦ                                            КАК НомерГТД,
		|			ВЫБОР
		|				КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
		|						И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика
		|					ТОГДА Товары.КодТНВЭД
		|				ИНАЧЕ
		|					NULL
		|			КОНЕЦ                                            КАК КодТНВЭД,
		|			0                                                КАК КоличествоПоУмолчанию,
		|			Товары.Количество                                КАК Количество,
		|			ВЫБОР
		|				КОГДА Товары.ВариантКомплектацииНоменклатуры.НоменклатураОсновногоКомпонента = Товары.Номенклатура
		|						И Товары.ВариантКомплектацииНоменклатуры.ХарактеристикаОсновногоКомпонента = Товары.Характеристика
		|					ТОГДА Товары.КоличествоПоРНПТ
		|				ИНАЧЕ
		|					0
		|			КОНЕЦ                                            КАК КоличествоПоРНПТ
		|		ИЗ
		|			ОтгрузкаТоваровКлиентуТаблицаТоваровДляПреобразования КАК Товары
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ТаблицаДанныхДокументов.Ссылка                               КАК Ссылка,
		|			ВариантыКомплектацииНоменклатурыТовары.Ссылка                КАК ВариантКомплектацииНоменклатуры,
		|			ВариантыКомплектацииНоменклатурыТовары.Ссылка.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|			ВариантыКомплектацииНоменклатурыТовары.Ссылка.ВариантРасчетаЦеныНабора КАК ВариантРасчетаЦеныНабора,
		|			ВариантыКомплектацииНоменклатурыТовары.Ссылка.Владелец       КАК НоменклатураНабора,
		|			ВариантыКомплектацииНоменклатурыТовары.Ссылка.Характеристика КАК ХарактеристикаНабора,
		|			ВариантыКомплектацииНоменклатурыТовары.Номенклатура          КАК Номенклатура,
		|			ВариантыКомплектацииНоменклатурыТовары.Характеристика        КАК Характеристика,
		|			ЛОЖЬ                                                         КАК ОсновнаяКомплектующая,
		|			NULL                                                         КАК СтавкаНДС,
		|			NULL                                                         КАК НомерГТД,
		|			NULL                                                         КАК КодТНВЭД,
		|			ВариантыКомплектацииНоменклатурыТовары.Количество            КАК КоличествоПоУмолчанию,
		|			0                                                            КАК Количество,
		|			0                                                            КАК КоличествоПоРНПТ
		|		ИЗ
		|			Справочник.ВариантыКомплектацииНоменклатуры.Товары КАК ВариантыКомплектацииНоменклатурыТовары
		|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
		|				ПО ИСТИНА
		|		ГДЕ
		|			ВариантыКомплектацииНоменклатурыТовары.Ссылка В
		|				(ВЫБРАТЬ
		|					ОтгрузкаТоваровКлиентуТаблицаТоваровДляПреобразования.ВариантКомплектацииНоменклатуры
		|				ИЗ
		|					ОтгрузкаТоваровКлиентуТаблицаТоваровДляПреобразования КАК ОтгрузкаТоваровКлиентуТаблицаТоваровДляПреобразования)) КАК Таблица
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Таблица.Ссылка,
		|		Таблица.ВариантКомплектацииНоменклатуры,
		|		Таблица.ВариантРасчетаЦеныНабора,
		|		Таблица.ВариантПредставленияНабораВПечатныхФормах,
		|		Таблица.НоменклатураНабора,
		|		Таблица.ХарактеристикаНабора,
		|		Таблица.Номенклатура,
		|		Таблица.Характеристика) КАК Результат
		|
		|СГРУППИРОВАТЬ ПО
		|	Результат.Ссылка,
		|	Результат.ВариантКомплектацииНоменклатуры,
		|	Результат.ВариантРасчетаЦеныНабора,
		|	Результат.ВариантПредставленияНабораВПечатныхФормах,
		|	Результат.НоменклатураНабора,
		|	Результат.ХарактеристикаНабора
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	НоменклатураНабора,
		|	ХарактеристикаНабора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаНаборыДополнительно.ВариантКомплектацииНоменклатуры           КАК ВариантКомплектацииНоменклатуры,
		|	ВременнаяТаблицаНаборыДополнительно.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|	ВременнаяТаблицаНаборыДополнительно.ВариантРасчетаЦеныНабора                  КАК ВариантРасчетаЦеныНабора,
		|	Таблица.Ссылка                                                                КАК Ссылка,
		|	Таблица.НоменклатураНабора                                                    КАК НоменклатураНабора,
		|	Таблица.ХарактеристикаНабора                                                  КАК ХарактеристикаНабора,
		|	Таблица.НомерСтроки                                                           КАК НомерСтроки,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.НомерГТД, ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)) КАК НомерГТД,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.КодТНВЭД, ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)) КАК КодТНВЭД,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.Количество, 1)                   КАК КоличествоУпаковок,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.Количество, 1)                   КАК Количество,
		|	ЕСТЬNULL(ВременнаяТаблицаНаборыДополнительно.КоличествоПоРНПТ, 1)             КАК КоличествоПоРНПТ,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаНаборыДополнительно.КоэффициентМинимум = ВременнаяТаблицаНаборыДополнительно.КоэффициентМаксимум
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ                                                                         КАК ПолныйНабор,
		|	Таблица.МассаБрутто                                                           КАК МассаБрутто,
		|	Таблица.МассаНетто                                                            КАК МассаНетто,
		|	Таблица.МассаНеттоНоменклатуры                                                КАК МассаНеттоНоменклатуры,
		|	Таблица.СуммаБезНДС                                                           КАК СуммаБезНДС,
		|	Таблица.СуммаНДС                                                              КАК СуммаНДС,
		|	Таблица.ЭтоТовар                                                              КАК ЭтоТовар,
		|	ВременнаяТаблицаНаборыДополнительно.СтавкаНДС                                 КАК СтавкаНДС
		|ПОМЕСТИТЬ ВременнаяТаблицаНаборы
		|ИЗ
		|	ВременнаяТаблицаНаборыПодготовка КАК Таблица
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборыДополнительно КАК ВременнаяТаблицаНаборыДополнительно
		|		ПО Таблица.НоменклатураНабора = ВременнаяТаблицаНаборыДополнительно.НоменклатураНабора
		|		И Таблица.ХарактеристикаНабора = ВременнаяТаблицаНаборыДополнительно.ХарактеристикаНабора
		|		И Таблица.Ссылка = ВременнаяТаблицаНаборыДополнительно.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	НоменклатураНабора,
		|	ХарактеристикаНабора
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваров.Ссылка                     КАК Ссылка,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка)
		|	КОНЕЦ                                     КАК ВариантПредставленияНабораВПечатныхФормах,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.ВариантРасчетаЦеныНабора
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка)
		|	КОНЕЦ                                     КАК ВариантРасчетаЦеныНабора,
		|	ТаблицаТоваров.НоменклатураНабора         КАК НоменклатураНабора,
		|	ТаблицаТоваров.ХарактеристикаНабора       КАК ХарактеристикаНабора,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ                                     КАК ЭтоКомплектующие,
		|	ЛОЖЬ                                      КАК ЭтоНабор,
		|	ТаблицаТоваров.НомерСтроки                КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.НомерСтроки
		|		ИНАЧЕ ТаблицаТоваров.НомерСтроки
		|	КОНЕЦ                                     КАК НомерСтрокиНаборы,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.ПолныйНабор
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ                                     КАК ПолныйНабор,
		|	ТаблицаТоваров.Номенклатура               КАК Номенклатура,
		|	ТаблицаТоваров.НоменклатураПартнера       КАК НоменклатураПартнера,
		|	ТаблицаТоваров.Количество                 КАК Количество,
		|	ТаблицаТоваров.КоличествоУпаковок         КАК КоличествоУпаковок,
		|	ТаблицаТоваров.КоличествоПоРНПТ           КАК КоличествоПоРНПТ,
		|	ТаблицаТоваров.СтавкаНДС                  КАК СтавкаНДС,
		|	ТаблицаТоваров.НомерГТД                   КАК НомерГТД,
		|	ТаблицаТоваров.КодТНВЭД                   КАК КодТНВЭД,
		|	ТаблицаТоваров.СуммаБезНДС                КАК СуммаБезНДС,
		|	ТаблицаТоваров.СуммаНДС                   КАК СуммаНДС,
		|	ТаблицаТоваров.Характеристика             КАК Характеристика,
		|	ТаблицаТоваров.Серия                      КАК Серия,
		|	ТаблицаТоваров.Упаковка                   КАК Упаковка,
		|	ВЫБОР
		|		КОГДА &ЗаполненаЕдиницаИзмеренияВеса
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL
		|						ТОГДА ТаблицаТоваров.Количество
		|					ИНАЧЕ ВЫБОР
		|							КОГДА ТаблицаТоваров.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Вес)
		|								ТОГДА 0
		|							ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок
		|						КОНЕЦ
		|				КОНЕЦ * &ТекстЗапросаВесУпаковки
		|		ИНАЧЕ 0
		|	КОНЕЦ                                     КАК МассаБрутто,
		|	ТаблицаТоваров.Количество *
		|	    ТаблицаТоваров.МассаНеттоНоменклатуры КАК МассаНетто,
		|	ТаблицаТоваров.МассаНеттоНоменклатуры     КАК МассаНеттоНоменклатуры,
		|	ТаблицаТоваров.ЭтоТовар                   КАК ЭтоТовар
		|ПОМЕСТИТЬ ОтгрузкаТоваровКлиентуТаблицаТоваров
		|ИЗ
		|	ОтгрузкаТоваровКлиентуТаблицаТоваровДляПреобразования КАК ТаблицаТоваров
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборы КАК ВременнаяТаблицаНаборы
		|		ПО ВременнаяТаблицаНаборы.НоменклатураНабора = ТаблицаТоваров.НоменклатураНабора
		|		И ВременнаяТаблицаНаборы.ХарактеристикаНабора = ТаблицаТоваров.ХарактеристикаНабора
		|		И ВременнаяТаблицаНаборы.Ссылка = ТаблицаТоваров.Ссылка
		|ГДЕ
		|	ТаблицаТоваров.НоменклатураНабора = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаТоваров.Ссылка                     КАК Ссылка,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ПустаяСсылка)
		|	КОНЕЦ                                     КАК ВариантПредставленияНабораВПечатныхФормах,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.ВариантРасчетаЦеныНабора
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаЦенНаборов.ПустаяСсылка)
		|	КОНЕЦ                                     КАК ВариантРасчетаЦеныНабора,
		|	ТаблицаТоваров.НоменклатураНабора         КАК НоменклатураНабора,
		|	ТаблицаТоваров.ХарактеристикаНабора       КАК ХарактеристикаНабора,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ                                     КАК ЭтоКомплектующие,
		|	ЛОЖЬ                                      КАК ЭтоНабор,
		|	ТаблицаТоваров.НомерСтроки                КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.НомерСтроки
		|		ИНАЧЕ ТаблицаТоваров.НомерСтроки
		|	КОНЕЦ                                     КАК НомерСтрокиНаборы,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВременнаяТаблицаНаборы.НомерСтроки, 0) <> 0
		|			ТОГДА ВременнаяТаблицаНаборы.ПолныйНабор
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ                                     КАК ПолныйНабор,
		|	ТаблицаТоваров.Номенклатура               КАК Номенклатура,
		|	ТаблицаТоваров.НоменклатураПартнера       КАК НоменклатураПартнера,
		|	ТаблицаТоваров.Количество                 КАК Количество,
		|	ТаблицаТоваров.КоличествоУпаковок         КАК КоличествоУпаковок,
		|	ТаблицаТоваров.КоличествоПоРНПТ           КАК КоличествоПоРНПТ,
		|	ТаблицаТоваров.СтавкаНДС                  КАК СтавкаНДС,
		|	ТаблицаТоваров.НомерГТД                   КАК НомерГТД,
		|	ТаблицаТоваров.КодТНВЭД                   КАК КодТНВЭД,
		|	ТаблицаТоваров.СуммаБезНДС                КАК СуммаБезНДС,
		|	ТаблицаТоваров.СуммаНДС                   КАК СуммаНДС,
		|	ТаблицаТоваров.Характеристика             КАК Характеристика,
		|	ТаблицаТоваров.Серия                      КАК Серия,
		|	ТаблицаТоваров.Упаковка                   КАК Упаковка,
		|	ВЫБОР
		|		КОГДА &ЗаполненаЕдиницаИзмеренияВеса
		|			ТОГДА ВЫБОР
		|					КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL
		|						ТОГДА ТаблицаТоваров.Количество
		|					ИНАЧЕ ВЫБОР
		|							КОГДА ТаблицаТоваров.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Вес)
		|								ТОГДА 0
		|							ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок
		|						КОНЕЦ
		|				КОНЕЦ * &ТекстЗапросаВесУпаковки
		|		ИНАЧЕ 0
		|	КОНЕЦ                                     КАК МассаБрутто,
		|	ТаблицаТоваров.Количество *
		|	    ТаблицаТоваров.МассаНеттоНоменклатуры КАК МассаНетто,
		|	ТаблицаТоваров.МассаНеттоНоменклатуры     КАК МассаНеттоНоменклатуры,
		|	ТаблицаТоваров.ЭтоТовар                   КАК ЭтоТовар
		|ИЗ
		|	ОтгрузкаТоваровКлиентуТаблицаТоваровДляПреобразования КАК ТаблицаТоваров
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаНаборы КАК ВременнаяТаблицаНаборы
		|		ПО ВременнаяТаблицаНаборы.НоменклатураНабора = ТаблицаТоваров.НоменклатураНабора
		|		И ВременнаяТаблицаНаборы.ХарактеристикаНабора = ТаблицаТоваров.ХарактеристикаНабора
		|		И ВременнаяТаблицаНаборы.Ссылка = ТаблицаТоваров.Ссылка
		|ГДЕ
		|	ТаблицаТоваров.НоменклатураНабора <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	И ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах В (
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоКомплектующие),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие)
		|				)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВременнаяТаблицаНаборы.Ссылка                                    КАК Ссылка,
		|	ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах КАК ВариантПредставленияНабораВПечатныхФормах,
		|	ВременнаяТаблицаНаборы.ВариантРасчетаЦеныНабора                  КАК ВариантРасчетаЦеныНабора,
		|	ВременнаяТаблицаНаборы.НоменклатураНабора                        КАК НоменклатураНабора,
		|	ВременнаяТаблицаНаборы.ХарактеристикаНабора                      КАК ХарактеристикаНабора,
		|	ЛОЖЬ                                                             КАК ЭтоКомплектующие,
		|	ИСТИНА                                                           КАК ЭтоНабор,
		|	ВременнаяТаблицаНаборы.НомерСтроки                               КАК НомерСтроки,
		|	ВременнаяТаблицаНаборы.НомерСтроки                               КАК НомерСтрокиНаборы,
		|	ВременнаяТаблицаНаборы.ПолныйНабор                               КАК ПолныйНабор,
		|	ВременнаяТаблицаНаборы.НоменклатураНабора                        КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка)       КАК НоменклатураПартнера,
		|	ВременнаяТаблицаНаборы.Количество                                КАК Количество,
		|	ВременнаяТаблицаНаборы.КоличествоУпаковок                        КАК КоличествоУпаковок,
		|	ВременнаяТаблицаНаборы.КоличествоПоРНПТ                          КАК КоличествоПоРНПТ,
		|	ВременнаяТаблицаНаборы.СтавкаНДС                                 КАК СтавкаНДС,
		|	ВременнаяТаблицаНаборы.НомерГТД                                  КАК НомерГТД,
		|	ВременнаяТаблицаНаборы.КодТНВЭД                                  КАК КодТНВЭД,
		|	ВременнаяТаблицаНаборы.СуммаБезНДС                               КАК СуммаБезНДС,
		|	ВременнаяТаблицаНаборы.СуммаНДС                                  КАК СуммаНДС,
		|	ВременнаяТаблицаНаборы.ХарактеристикаНабора                      КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)              КАК Серия,
		|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)       КАК Упаковка,
		|	ВременнаяТаблицаНаборы.МассаБрутто                               КАК МассаБрутто,
		|	ВременнаяТаблицаНаборы.МассаНетто                                КАК МассаНетто,
		|	ВременнаяТаблицаНаборы.МассаНеттоНоменклатуры                    КАК МассаНеттоНоменклатуры,
		|	ВременнаяТаблицаНаборы.ЭтоТовар                                  КАК ЭтоТовар
		|ИЗ
		|	ВременнаяТаблицаНаборы КАК ВременнаяТаблицаНаборы
		|ГДЕ
		|	ВременнаяТаблицаНаборы.ВариантПредставленияНабораВПечатныхФормах В (
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.ТолькоНабор),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыПредставленияНаборовВПечатныхФормах.НаборИКомплектующие)
		|		)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаНаборыПодготовка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаНаборыДополнительно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВременнаяТаблицаНаборы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОтгрузкаТоваровКлиентуТаблицаТоваровДляПреобразования";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаВесУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
				"ТаблицаТоваров.Упаковка",
				"ТаблицаТоваров.Номенклатура"));
			
		Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса", ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
		
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВесНоменклатуры",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения",
			"ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура"));
	
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл", ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	Запрос.УстановитьПараметр("ВключаяНомераГТД",         ПараметрыЗаполнения.ВключаяНомераГТД);
	Запрос.УстановитьПараметр("ВключаяДоКорректировки",   ПараметрыЗаполнения.ВключаяДоКорректировки);
	Запрос.УстановитьПараметр("ПустаяГТД",                Справочники.НомераГТД.ПустаяСсылка());
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Функция получает данные для формирования печатной формы М15
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ОтгрузкаТоваровКлиенту - Массив ссылок на документы, по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыМ15(ПараметрыПечати, МассивОбъектов) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Валюта КАК Валюта,
	|	ДанныеДокументов.ИсправляемыйДокумент КАК ИсправляемыйДокумент
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц);
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтгрузкаТоваровКлиенту.Ссылка                         КАК Ссылка,
	|	ОтгрузкаТоваровКлиенту.Дата                           КАК Дата,
	|	ЕСТЬNULL(ОтгрузкаТоваровКлиенту.ИсправляемыйДокумент.Номер, ОтгрузкаТоваровКлиенту.Номер) КАК Номер,
	|	ЕСТЬNULL(ОтгрузкаТоваровКлиенту.ИсправляемыйДокумент.Дата, ОтгрузкаТоваровКлиенту.Дата) КАК ДатаДокумента,
	|	ЕСТЬNULL(ОтгрузкаТоваровКлиенту.ИсправляемыйДокумент.Дата, ОтгрузкаТоваровКлиенту.Дата) КАК ДатаСоставления,
	|	ОтгрузкаТоваровКлиенту.Партнер                        КАК Партнер,
	|	ОтгрузкаТоваровКлиенту.Контрагент                     КАК Контрагент,
	|	ОтгрузкаТоваровКлиенту.Организация                    КАК Организация,
	|	ОтгрузкаТоваровКлиенту.Организация.Префикс            КАК Префикс,
	|	ОтгрузкаТоваровКлиенту.Склад                          КАК Склад,
	|	ОтгрузкаТоваровКлиенту.Склад.Наименование             КАК СкладНаименование,
	|	ОтгрузкаТоваровКлиенту.Валюта                         КАК Валюта,
	|	ОтгрузкаТоваровКлиенту.Подразделение                  КАК СтруктурноеПодразделение,
	|	ОтгрузкаТоваровКлиенту.Основание                      КАК Основание,
	|	ОтгрузкаТоваровКлиенту.БанковскийСчетОрганизации      КАК БанковскийСчетОрганизации,
	|	ОтгрузкаТоваровКлиенту.Отпустил                       КАК Кладовщик,
	|	ОтгрузкаТоваровКлиенту.ОтпустилДолжность              КАК ДолжностьКладовщика,
	|	ТаблицаОтветственныеЛица.РуководительНаименование     КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность        КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ОтгрузкаТоваровКлиенту
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ОтгрузкаТоваровКлиенту.Ссылка = ДанныеДокументов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ОтгрузкаТоваровКлиенту.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка                            КАК Ссылка,
	|	ТаблицаТоваров.НомерСтроки                       КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура                      КАК Номенклатура,
	|	ТаблицаТоваров.Номенклатура.Код                  КАК НоменклатураКод,
	|	ТаблицаТоваров.Номенклатура.НаименованиеПолное   КАК НоменклатураНаименование,
	|	ТаблицаТоваров.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаТоваров.Серия.Наименование                КАК СерияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения                 КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения        КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаТоваров.Упаковка
	|	КОНЕЦ                                            КАК Упаковка,
	|	ТаблицаТоваров.КоличествоУпаковок                КАК Количество,
	|	0                                                КАК КоличествоМест,
	|	ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок КАК Цена,
	|	ТаблицаТоваров.СуммаБезНДС                       КАК СуммаБезНДС,
	|	0                                                КАК СуммаНДС,
	|	ТаблицаТоваров.СуммаБезНДС                       КАК СуммаСНДС,
	|	ЛОЖЬ                                             КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ОтгрузкаТоваровКлиентуТаблицаТоваров КАК ТаблицаТоваров
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
	
	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[1];
	
	СтруктураДанныхДляПечати = Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
												РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Формирует текст запроса для получения данных основания при печати Счет-фактуры.
// Возвращаемое значение:
//	Строка
Функция ТекстЗапросаДанныхОснованияДляПечатнойФормыСчетФактура() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                                   КАК Ссылка,
	|	ДанныеДокументов.ХозяйственнаяОперация                    КАК ХозяйственнаяОперация,
	|	ДанныеДокументов.Валюта                                   КАК Валюта,
	|	ДанныеДокументов.Организация                              КАК Организация,
	|	ДанныеДокументов.НалогообложениеНДС                       КАК НалогообложениеНДС,
	|	ДанныеДокументов.Подразделение                            КАК Подразделение,
	|	ДанныеДокументов.Склад                                    КАК Склад,
	|	ДанныеДокументов.Грузоотправитель                         КАК Грузоотправитель,
	|	ДанныеДокументов.Грузополучатель                          КАК Грузополучатель,
	|	ЛОЖЬ                                                      КАК РасчетыЧерезОтдельногоКонтрагента,
	|	НЕОПРЕДЕЛЕНО                                              КАК Номенклатура,
	|	""""                                                      КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)             КАК Комиссионер,
	|	ДанныеДокументов.Основание                                КАК Основание,
	|	ДанныеДокументов.ОснованиеДата                            КАК ОснованиеДата,
	|	ДанныеДокументов.ОснованиеНомер                           КАК ОснованиеНомер,
	|	ДанныеДокументов.БанковскийСчетОрганизации                КАК БанковскийСчетОрганизации,
	|	ДанныеДокументов.БанковскийСчетКонтрагента                КАК БанковскийСчетКонтрагента,
	|	ДанныеДокументов.БанковскийСчетГрузоотправителя           КАК БанковскийСчетГрузоотправителя,
	|	ДанныеДокументов.БанковскийСчетГрузополучателя            КАК БанковскийСчетГрузополучателя,
	|	ДанныеДокументов.ДоверенностьНомер                        КАК ДоверенностьНомер,
	|	ДанныеДокументов.ДоверенностьДата                         КАК ДоверенностьДата,
	|	ДанныеДокументов.ДоверенностьВыдана                       КАК ДоверенностьВыдана,
	|	ДанныеДокументов.ДоверенностьЛицо                         КАК ДоверенностьЛицо,
	|	ДанныеДокументов.Менеджер                                 КАК Менеджер,
	|	ДанныеДокументов.Отпустил                                 КАК Кладовщик,
	|	ДанныеДокументов.ОтпустилДолжность                        КАК ДолжностьКладовщика,
	|	ДанныеДокументов.АдресДоставки                            КАК АдресДоставки,
	|	ДанныеДокументов.АдресДоставкиЗначенияПолей               КАК АдресДоставкиЗначенияПолей,
	|	ДанныеДокументов.АдресДоставкиЗначение                    КАК АдресДоставкиЗначение,
	|	ДанныеДокументов.СведенияОТранспортировкеИГрузе           КАК ДанныеТранспортнаяНакладная,
	|	ДанныеДокументов.СопроводительныеДокументы                КАК СопроводительныеДокументы,
	|	ДанныеДокументов.Дата                                     КАК Период,
	|	ДанныеДокументов.КодСпециальныхОбстоятельств              КАК КодСпециальныхОбстоятельств
	|
	|//ОператорПОМЕСТИТЬ
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&ДокументОснование_ОтгрузкаТоваровКлиенту)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

//// Возвращает данные для формирования печатной формы УПД
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ОтгрузкаТоваровКлиенту - ссылки на документы, по которым необходимо
//																			получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемая при печати УПД, содержащая следующие свойства:
//		* РезультатПоШапке - РезультатЗапроса - общие данные накладной.
//		* РезультатПоТабличнойЧасти - РезультатЗапроса - данные табличной части накладной.
//
Функция ПолучитьДанныеДляПечатнойФормыУПД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка               КАК Ссылка,
	|	ДанныеДокументов.Подразделение        КАК Подразделение,
	|	ДанныеДокументов.Организация          КАК Организация,
	|	ДанныеДокументов.Дата                 КАК Период,
	|	ДанныеДокументов.Склад                КАК Склад,
	|	ДанныеДокументов.ИсправляемыйДокумент КАК ИсправляемыйДокумент
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВключаяНомераГТД                 = Истина;
	ПараметрыЗаполнения.ОбработатьНастройкиПечатиНаборов = Истина;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	Обработки.ПечатьОбщихФорм.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов,
		МенеджерВременныхТаблиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                КАК Ссылка,
	|	&ПредставлениеСчетФактура                             КАК ПредставлениеДокумента,
	|	2                                                     КАК СтатусУПД,
	|	ДанныеДокумента.Номер                                 КАК Номер,
	|	ДанныеДокумента.Дата                                  КАК Дата,
	|	НЕОПРЕДЕЛЕНО                                          КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО                                          КАК ДатаИсправления,
	|	ЛОЖЬ                                                  КАК Исправление,
	|	НЕОПРЕДЕЛЕНО                                          КАК НомерСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                          КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                          КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                          КАК ДатаИсправленияСчетаФактуры,
	|	ЛОЖЬ                                                  КАК КорректировочныйСчетФактура,
	|	НЕОПРЕДЕЛЕНО                                          КАК СтрокаПоДокументу,
	|	НЕОПРЕДЕЛЕНО                                          КАК ВалютаСчетаФактуры,
	|	ДанныеДокумента.Партнер                               КАК Партнер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Контрагент.ОбособленноеПодразделение
	|			ТОГДА ДанныеДокумента.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ
	|			ДанныеДокумента.Контрагент
	|	КОНЕЦ                                                 КАК Контрагент,
	|	ДанныеДокумента.НалогообложениеНДС                    КАК НалогообложениеНДС,
	|	ДанныеПоставщика.ГоловнаяОрганизация                  КАК Организация,
	|	ДанныеДокумента.Организация.Префикс                   КАК Префикс,
	|	ЕСТЬNULL(ДанныеПоставщика.ИндексПодразделения, 0)     КАК ИндексПодразделения,
	|	ТаблицаОтветственныеЛица.РуководительНаименование     КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность        КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.Контрагент
	|		ИНАЧЕ
	|			ДанныеДокумента.Грузополучатель
	|	КОНЕЦ                                                 КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.Организация
	|		ИНАЧЕ
	|			ДанныеДокумента.Грузоотправитель
	|	КОНЕЦ                                                 КАК Грузоотправитель,
	|	ДанныеДокумента.БанковскийСчетОрганизации             КАК БанковскийСчетОрганизации,
	|	ДанныеДокумента.БанковскийСчетКонтрагента             КАК БанковскийСчетКонтрагента,
	|	ДанныеДокумента.БанковскийСчетГрузоотправителя        КАК БанковскийСчетГрузоотправителя,
	|	ДанныеДокумента.БанковскийСчетГрузополучателя         КАК БанковскийСчетГрузополучателя,
	|	ДанныеПоставщика.КПППоставщика                        КАК КПППоставщика,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузополучатель <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			И ДанныеДокумента.Грузополучатель.ГоловнойКонтрагент = ДанныеДокумента.Контрагент
	|		ТОГДА
	|			ДанныеДокумента.Грузополучатель.КПП
	|		ИНАЧЕ
	|			ДанныеДокумента.Контрагент.КПП
	|	КОНЕЦ                                                 КАК КПППокупателя,
	|	ДанныеДокумента.Контрагент.ИНН                        КАК ИННПокупателя,
	|	ДанныеДокумента.АдресДоставки                         КАК АдресДоставки,
	|	ДанныеДокумента.АдресДоставкиЗначенияПолей            КАК АдресДоставкиЗначенияПолей,
	|	ДанныеДокумента.АдресДоставкиЗначение                 КАК АдресДоставкиЗначение,
	|	ДанныеДокумента.Склад                                 КАК Склад,
	|	ДанныеДокумента.Валюта                                КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное             КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код                            КАК ВалютаКод,
	|	ЛОЖЬ                                                  КАК ТолькоУслуги,
	|	ЛОЖЬ                                                  КАК ЕстьПрослеживаемыеТовары,
	|	ЛОЖЬ                                                  КАК ЭтоПередачаНаКомиссию,
	|	ДанныеДокумента.Основание                             КАК Основание,
	|	ДанныеДокумента.ДокументОснование                     КАК ДокументОснование,
	|	ДанныеДокумента.ОснованиеДата                         КАК ОснованиеДата,
	|	ДанныеДокумента.ОснованиеНомер                        КАК ОснованиеНомер,
	|	ДанныеДокумента.ДоверенностьНомер                     КАК ДоверенностьНомер,
	|	ДанныеДокумента.ДоверенностьДата                      КАК ДоверенностьДата,
	|	ДанныеДокумента.ДоверенностьВыдана                    КАК ДоверенностьВыдана,
	|	ДанныеДокумента.ДоверенностьЛицо                      КАК ДоверенностьЛицо,
	|	ДанныеДокумента.Отпустил                              КАК Кладовщик,
	|	ДанныеДокумента.ОтпустилДолжность                     КАК ДолжностьКладовщика,
	|	ДанныеДокумента.СведенияОТранспортировкеИГрузе        КАК ДанныеТранспортнаяНакладная,
	|	ДанныеДокумента.СопроводительныеДокументы             КАК СопроводительныеДокументы,
	|	ДанныеДокумента.КодСпециальныхОбстоятельств           КАК КодСпециальныхОбстоятельств,
	|	ИСТИНА                                                КАК ТребуетсяНаличиеСФ,
	|	""ПредставлениеСтроки5а""                             КАК ПредставлениеСтроки5а
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|		ПО ДанныеДокумента.Ссылка = ДанныеПоставщика.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                        КАК Ссылка,
	|	ТаблицаДокумента.ВариантПредставленияНабораВПечатныхФормах     КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаДокумента.ВариантРасчетаЦеныНабора                      КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаДокумента.НоменклатураНабора                            КАК НоменклатураНабора,
	|	ТаблицаДокумента.ХарактеристикаНабора                          КАК ХарактеристикаНабора,
	|	ТаблицаДокумента.ЭтоКомплектующие                              КАК ЭтоКомплектующие,
	|	ТаблицаДокумента.ЭтоНабор                                      КАК ЭтоНабор,
	|	ТаблицаДокумента.ПолныйНабор                                   КАК ПолныйНабор,
	|	ТаблицаДокумента.Номенклатура                                  КАК Номенклатура,
	|	ТаблицаДокумента.НоменклатураПартнера                          КАК НоменклатураПартнера,
	|	ТаблицаДокумента.Номенклатура.НаименованиеПолное               КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ
	|			ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ                                                          КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ
	|			&ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ                                                          КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения
	|	КОНЕЦ                                                          КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ
	|			&ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ                                                          КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения                     КАК ЕдиницаИзмеренияТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Представление       КАК ЕдиницаИзмеренияТНВЭДНаименование,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Код                 КАК ЕдиницаИзмеренияТНВЭДКод,
	|	ТаблицаДокумента.Характеристика                                КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное             КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.Серия.Наименование                            КАК СерияНаименование,
	|	ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)                          КАК СтавкаНДС,
	|	ТаблицаДокумента.НомерГТД                                      КАК НомерГТДСсылка,
	|	ЕСТЬNULL(ТаблицаДокумента.НомерГТД.ТипНомераГТД, НЕОПРЕДЕЛЕНО) КАК ТипНомераГТД,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НомерГТД.РегистрационныйНомер = """"
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ
	|			ТаблицаДокумента.НомерГТД.РегистрационныйНомер
	|	КОНЕЦ                                                          КАК НомерГТД,
	|	ТаблицаДокумента.КодТНВЭД                                      КАК КодТНВЭД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения                  КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код              КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Количество
	|		ИНАЧЕ
	|			ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ                                                          КАК Количество,
	|	ТаблицаДокумента.Количество                                    КАК КоличествоБазовыхЕдиниц,
	|	ТаблицаДокумента.КоличествоПоРНПТ                              КАК КоличествоПоРНПТ,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество
	|		ИНАЧЕ
	|			ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ                                                          КАК Цена,
	|	ТаблицаДокумента.СуммаБезНДС                                   КАК СуммаБезНДС,
	|	0                                                              КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаБезНДС                                   КАК СуммаСНДС,
	|	ТаблицаДокумента.НомерСтроки                                   КАК НомерСтроки,
	|	ТаблицаДокумента.Серия                                         КАК Серия,
	|	ТаблицаДокумента.Упаковка                                      КАК Упаковка,
	|	ЛОЖЬ                                                           КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ОтгрузкаТоваровКлиентуТаблицаТоваров КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.НомерСтрокиНаборы,
	|	ТаблицаДокумента.ЭтоНабор УБЫВ,
	|	ТаблицаДокумента.НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.ШтрихкодыУпаковок КАК ТаблицаДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДокумента.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|		И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(&МассивОбъектов)
	|	И ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL";
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	Запрос.УстановитьПараметр("ПредставлениеСчетФактура", НСтр("ru = 'счет-фактура';
																|en = 'tax invoice'"));
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[0];
	РезультатПоТабличнойЧасти = МассивРезультатов[1];
	ШтрихкодыУпаковок         = МассивРезультатов[2].Выгрузить();
	
	Если ПараметрыПечати.Свойство("ЗаполнениеЭлектронногоДокумента") Тогда
		ТаблицаТоварыДляЭДО = ИнтеграцияИСМПУТ.ТаблицаТоварыДляЭДО(РезультатПоТабличнойЧасти.Выгрузить(), "КоличествоБазовыхЕдиниц");
		Если ТаблицаТоварыДляЭДО.Количество() Тогда
			ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования(ТаблицаТоварыДляЭДО[0].Ссылка);
		КонецЕсли;
		Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(
			ШтрихкодыУпаковок,
			ТаблицаТоварыДляЭДО,
			ПараметрыСканирования);
	Иначе
		Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(ШтрихкодыУпаковок);
	КонецЕсли;
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке",          РезультатПоШапке);
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти", РезультатПоТабличнойЧасти);
	ДанныеДляПечати.Вставить("Маркировка",                Маркировка);
	
	Возврат ДанныеДляПечати;
	
КонецФункции

// Функция получает данные для формирования печатной формы УКД
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.КорректировкаРеализации - Массив ссылок на документы, 
//																	по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоИсходнымДанным - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
// 		* Маркировка - см. ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое
// 		* МаркировкаДо - см. ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое
// 		* ПрослеживаемыеКомплектующие - см. УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных
//
Функция ПолучитьДанныеДляПечатнойФормыУКД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка               КАК Ссылка,
	|	ДанныеДокументов.Исправление          КАК Исправление,
	|	ДанныеДокументов.Подразделение        КАК Подразделение,
	|	ДанныеДокументов.Организация          КАК Организация,
	|	ДанныеДокументов.Дата                 КАК Период,
	|	ДанныеДокументов.Склад                КАК Склад,
	|	ДанныеДокументов.ИсправляемыйДокумент КАК ИсправляемыйДокумент
	|ПОМЕСТИТЬ ТаблицаДанныхВсехДокументов
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка               КАК Ссылка,
	|	ДанныеДокументов.Подразделение        КАК Подразделение,
	|	ДанныеДокументов.Организация          КАК Организация,
	|	ДанныеДокументов.Период               КАК Период,
	|	ДанныеДокументов.Склад                КАК Склад,
	|	ДанныеДокументов.ИсправляемыйДокумент КАК ИсправляемыйДокумент
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	ТаблицаДанныхВсехДокументов КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Исправление
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка                КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДанныхДокументов.Ссылка) КАК ПредставлениеСсылки
	|ИЗ
	|	ТаблицаДанныхВсехДокументов КАК ТаблицаДанныхДокументов
	|ГДЕ
	|	НЕ ТаблицаДанныхДокументов.Исправление
	|";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	ВыборкаОшибки = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаОшибки.Следующий() Цикл
		
		ТекстСообщения = НСтр("ru = 'Документ %1 не является исправлением. Печать универсального корректировочного документа (УКД) не требуется.';
								|en = 'The ""%1"" document is not a correction document. Printing the universal adjustment document (UAD) is not required.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ВыборкаОшибки.ПредставлениеСсылки);
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстСообщения,
			ВыборкаОшибки.Ссылка);
		
	КонецЦикла;
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВключаяНомераГТД       = Истина;
	ПараметрыЗаполнения.ВключаяДоКорректировки = Истина;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	Обработки.ПечатьОбщихФорм.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов,
		МенеджерВременныхТаблиц);
	
	СформироватьВременнуюТаблицуНоменклатураДокументов(МенеджерВременныхТаблиц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка КАК Ссылка,
	|	ДанныеОснования.Номер          КАК НомерСчетаФактуры,
	|	ДанныеОснования.Дата           КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                   КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                   КАК ДатаИсправленияСчетаФактуры
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровКлиенту КАК ДанныеОснования
	|		ПО ТаблицаДанныхДокументов.Ссылка.ИсправляемыйДокумент = ДанныеОснования.Ссылка
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                                КАК Ссылка,
	|	2                                                                     КАК СтатусУПД,
	|	ДанныеДокумента.Номер                                                 КАК Номер,
	|	ДанныеДокумента.Дата                                                  КАК Дата,
	|	НЕОПРЕДЕЛЕНО                                                          КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО                                                          КАК ДатаИсправления,
	|	ДанныеДокумента.Исправление                                           КАК Исправление,
	|	ДанныеДокумента.ИсправляемыйДокумент                                  КАК ИсправляемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                                                          КАК НомерСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                                          КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                                          КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                                          КАК ДатаИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                                          КАК ВалютаСчетаФактуры,
	|	ДанныеДокумента.Партнер                                               КАК Партнер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Контрагент.ОбособленноеПодразделение
	|			ТОГДА ДанныеДокумента.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ
	|			ДанныеДокумента.Контрагент
	|	КОНЕЦ                                                                 КАК Контрагент,
	|	ЕСТЬNULL(ДанныеПоставщика.ГоловнаяОрганизация,
	|		ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))                    КАК Организация,
	|	ДанныеДокумента.Организация.Префикс                                   КАК Префикс,
	|	ДанныеДокумента.БанковскийСчетОрганизации                             КАК БанковскийСчетОрганизации,
	|	ДанныеДокумента.БанковскийСчетКонтрагента                             КАК БанковскийСчетКонтрагента,
	|	ДанныеДокумента.БанковскийСчетГрузоотправителя                        КАК БанковскийСчетГрузоотправителя,
	|	ДанныеДокумента.БанковскийСчетГрузополучателя                         КАК БанковскийСчетГрузополучателя,
	|	0                                                                     КАК ИндексПодразделения,
	|	ЕСТЬNULL(ТаблицаОтветственныеЛица.РуководительНаименование, """")     КАК Руководитель,
	|	ЕСТЬNULL(ТаблицаОтветственныеЛица.РуководительДолжность, """")        КАК ДолжностьРуководителя,
	|	ЕСТЬNULL(ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование, """") КАК ГлавныйБухгалтер,
	|	ЕСТЬNULL(ДанныеПоставщика.КПППоставщика, """")                        КАК КПППоставщика,
	|	ДанныеДокумента.Контрагент.КПП                                        КАК КПППокупателя,
	|	ДанныеДокумента.Валюта                                                КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное                             КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код                                            КАК ВалютаКод,
	|	ДанныеДокумента.Основание                                             КАК Основание,
	|	ДанныеДокумента.ДокументОснование                                     КАК ДокументОснование,
	|	ДанныеДокумента.ОснованиеДата                                         КАК ОснованиеДата,
	|	ДанныеДокумента.ОснованиеНомер                                        КАК ОснованиеНомер,
	|	ЛОЖЬ                                                                  КАК ПечатьНеТребуется,
	|	ЛОЖЬ                                                                  КАК РеализацияЧерезКомиссионера,
	|	ДанныеДокумента.НалогообложениеНДС                                    КАК НалогообложениеНДС,
	|	ЕСТЬNULL(НоменклатураДокументов.ЕстьПрослеживаемыеТовары, ЛОЖЬ)       КАК ЕстьПрослеживаемыеТовары
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|		ПО ДанныеДокумента.Ссылка = ДанныеПоставщика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураДокументов КАК НоменклатураДокументов
	|		ПО ДанныеДокумента.Ссылка = НоменклатураДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                        КАК Ссылка,
	|	ТаблицаДокумента.ВариантПредставленияНабораВПечатныхФормах     КАК ВариантПредставленияНабораВПечатныхФормах,
	|	ТаблицаДокумента.ВариантРасчетаЦеныНабора                      КАК ВариантРасчетаЦеныНабора,
	|	ТаблицаДокумента.ЭтоНабор                                      КАК ЭтоНабор,
	|	ТаблицаДокумента.ЭтоКомплектующие                              КАК ЭтоКомплектующие,
	|	ТаблицаДокумента.Номенклатура                                  КАК Номенклатура,
	|	ТаблицаДокумента.Номенклатура.НаименованиеПолное               КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ 
	|			ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ                                                          КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ
	|			&ТекстЗапросаЕдиницаИзмерения1
	|	КОНЕЦ                                                          КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|	КОНЕЦ                                                          КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ
	|			&ТекстЗапросаКодЕдиницыИзмерения1
	|	КОНЕЦ                                                          КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.Характеристика                                КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное             КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.Упаковка                                      КАК Упаковка,
	|	ТаблицаДокумента.СтавкаНДС                                     КАК СтавкаНДС,
	|	ТаблицаДокумента.НомерГТД                                      КАК НомерГТДСсылка,
	|	ЕСТЬNULL(ТаблицаДокумента.НомерГТД.ТипНомераГТД, НЕОПРЕДЕЛЕНО) КАК ТипНомераГТД,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НомерГТД.РегистрационныйНомер = """"
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ
	|			ТаблицаДокумента.НомерГТД.РегистрационныйНомер
	|	КОНЕЦ                                                          КАК НомерГТД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения                  КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код              КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Количество
	|		ИНАЧЕ
	|			ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ                                                          КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0)
	|		ИНАЧЕ
	|			ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0)
	|	КОНЕЦ                                                          КАК КоличествоДо,
	|	ТаблицаДокумента.КоличествоПоРНПТ                              КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)         КАК КоличествоПоРНПТДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КоличествоПоРНПТ >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|			ТОГДА ТаблицаДокумента.КоличествоПоРНПТ - ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК КоличествоПрослежУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КоличествоПоРНПТ < ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|			ТОГДА -(ТаблицаДокумента.КоличествоПоРНПТ - ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК КоличествоПрослежУменьшение,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА
	|				ВЫБОР 
	|					КОГДА ТаблицаДокумента.Количество <> 0
	|						ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество
	|					ИНАЧЕ 
	|						0
	|				КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР 
	|				КОГДА ТаблицаДокумента.КоличествоУпаковок <> 0
	|					ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок
	|				ИНАЧЕ
	|					0
	|			КОНЕЦ
	|	КОНЕЦ                                                          КАК Цена,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0) <> 0
	|						ТОГДА ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.Количество
	|					ИНАЧЕ
	|						0
	|				КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0) <> 0
	|					ТОГДА ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.КоличествоУпаковок
	|				ИНАЧЕ
	|					0
	|			КОНЕЦ
	|	КОНЕЦ                                                          КАК ЦенаДо,
	|	ТаблицаДокумента.СуммаБезНДС                                   КАК СуммаБезНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)              КАК СуммаБезНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК РазницаБезНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК РазницаБезНДСУменьшение,
	|	ТаблицаДокумента.СуммаНДС                                      КАК СуммаНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)                 КАК СуммаНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК РазницаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК РазницаНДСУменьшение,
	|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС       КАК СуммаСНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|		+ ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)           КАК СуммаСНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС >=
	|				ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС -
	|				(ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК РазницаСНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС <
	|				ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС -
	|				(ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК РазницаСНДСУменьшение,
	|	ТаблицаДокумента.НомерСтроки                                   КАК НомерСтроки,
	|	ЛОЖЬ                                                           КАК ЭтоВозвратнаяТара,
	|	1                                                              КАК Порядок,
	|	ТаблицаДокумента.КодТНВЭД                                      КАК КодТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения                     КАК ЕдиницаИзмеренияТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Представление       КАК ЕдиницаИзмеренияТНВЭДНаименование,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Код                 КАК ЕдиницаИзмеренияТНВЭДКод,
	|	ТаблицаДокумента.Количество                                    КАК КоличествоБазовыхЕдиниц,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0)               КАК КоличествоБазовыхЕдиницДо
	|ИЗ
	|	ОтгрузкаТоваровКлиентуТаблицаТоваров КАК ТаблицаДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ ОтгрузкаТоваровКлиентуТаблицаТоваров КАК ТаблицаТоваровПредыдущие
	|		ПО ТаблицаТоваровПредыдущие.ДоКорректировки
	|		И ТаблицаДокумента.Ссылка = ТаблицаТоваровПредыдущие.Ссылка
	|		И ТаблицаДокумента.Номенклатура = ТаблицаТоваровПредыдущие.Номенклатура
	|		И ТаблицаДокумента.Характеристика = ТаблицаТоваровПредыдущие.Характеристика
	|		И ТаблицаДокумента.Упаковка = ТаблицаТоваровПредыдущие.Упаковка
	|		И ТаблицаДокумента.НомерГТД = ТаблицаТоваровПредыдущие.НомерГТД
	|		И ТаблицаДокумента.СтавкаНДС = ТаблицаТоваровПредыдущие.СтавкаНДС
	|		И ТаблицаДокумента.Содержание = ТаблицаТоваровПредыдущие.Содержание
	|ГДЕ
	|	НЕ ТаблицаДокумента.ДоКорректировки
	|	И ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка     КАК Ссылка,
	|	ТаблицаШтрихкодов.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровКлиенту.ШтрихкодыУпаковок КАК ТаблицаШтрихкодов
	|		ПО ТаблицаДанныхДокументов.Ссылка = ТаблицаШтрихкодов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДанныхДокументов.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|		И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредыдущиеДокументы.Основной         КАК Ссылка,
	|	ТаблицаШтрихкодовДо.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	ПредыдущиеДокументы КАК ПредыдущиеДокументы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровКлиенту.ШтрихкодыУпаковок КАК ТаблицаШтрихкодовДо
	|		ПО ПредыдущиеДокументы.Предыдущий = ТаблицаШтрихкодовДо.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаШтрихкодовДо.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|		И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПредыдущиеДокументы.Основной         КАК Ссылка,
	|	ТаблицаШтрихкодовДо.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	ПредыдущиеДокументы КАК ПредыдущиеДокументы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровКлиенту.ШтрихкодыУпаковок КАК ТаблицаШтрихкодовДо
	|		ПО ПредыдущиеДокументы.Предыдущий = ТаблицаШтрихкодовДо.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО (ТаблицаШтрихкодовДо.Ссылка = ДокументыПрямогоОбмена.ДокументОснование)
	|			И (НЕ ДокументыПрямогоОбмена.ПометкаУдаления)
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка                           КАК Ссылка,
	|	ТаблицаДокумента.НомерГТД                         КАК НомерГТД,
	|	Комплектующие.НомерРНПТ                           КАК НомерРНПТ,
	|	ВЫБОР
	|		КОГДА Комплектующие.НомерРНПТ.РегистрационныйНомер = """"
	|			ТОГДА Комплектующие.НомерРНПТ
	|		ИНАЧЕ
	|			Комплектующие.НомерРНПТ.РегистрационныйНомер
	|	КОНЕЦ                                             КАК НомерТовара,
	|	НомераГТД.СуммаПоРНПТ                             КАК ОбщаяСуммаПоКомплекту,
	|	Комплектующие.СуммаПоРНПТ                         КАК СуммаПоРНПТ,
	|	Комплектующие.КоличествоПоРНПТ                    КАК Количество,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД               КАК ЕдиницаИзмерения,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Код           КАК ЕдиницаИзмеренияКод,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Представление КАК ЕдиницаИзмеренияНаименование
	|ИЗ
	|	ОтгрузкаТоваровКлиентуТаблицаТоваров КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД.ПрослеживаемыеКомплектующие КАК Комплектующие
	|		ПО ТаблицаДокумента.НомерГТД = Комплектующие.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|		ПО ТаблицаДокумента.НомерГТД = НомераГТД.Ссылка";
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаТоваровПредыдущие.Упаковка",
			"ТаблицаТоваровПредыдущие.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваровПредыдущие.Упаковка",
			"ТаблицаТоваровПредыдущие.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваровПредыдущие.Упаковка",
			"ТаблицаТоваровПредыдущие.Номенклатура"));
	
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	Запрос.УстановитьПараметр("ПустаяУпаковкаНоменклатуры", Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	
	ВидыМаркируемойПродукции = ИнтеграцияИСМП.ИспользуемыеВидыМаркируемойПродукцииИСМП();
	ИнтеграцияИСМПУТ.УстановитьУсловиеПоВидуМаркируемойПродукции(
		Запрос.Текст,
		ВидыМаркируемойПродукции,
		"ТаблицаДокумента.Номенклатура");

	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоИсходнымДанным = МассивРезультатов[0];
	РезультатПоШапке          = МассивРезультатов[1];
	РезультатПоТабличнойЧасти = МассивРезультатов[2];
	
	Если ПараметрыПечати.Свойство("ЗаполнениеЭлектронногоДокумента") Тогда
		
		ТаблицаТоварыДляЭДО = ИнтеграцияИСМПУТ.ТаблицаТоварыДляЭДО(РезультатПоТабличнойЧасти.Выгрузить(),
			"КоличествоБазовыхЕдиницДо");
		
		Если ТаблицаТоварыДляЭДО.Количество() Тогда
			ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования(ТаблицаТоварыДляЭДО[0].Ссылка);
		КонецЕсли;
		
		МаркировкаДо = МассивРезультатов[4].Выгрузить();
		МаркировкаДо = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(
			МаркировкаДо,
			ТаблицаТоварыДляЭДО,
			ПараметрыСканирования);
		
		Если МаркировкаДо.Количество() = 0 Тогда // Коды переданы через прямой обмен с ИС МП
			Маркировка = МаркировкаДо.СкопироватьКолонки();
		Иначе
			
			ТаблицаТоварыДляЭДО = ИнтеграцияИСМПУТ.ТаблицаТоварыДляЭДО(РезультатПоТабличнойЧасти.Выгрузить(),
				"КоличествоБазовыхЕдиниц");
			
			Маркировка = МассивРезультатов[3].Выгрузить();
			Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(
				Маркировка,
				ТаблицаТоварыДляЭДО,
				ПараметрыСканирования);
			
		КонецЕсли;
		
	Иначе
		
		МаркировкаДо = МассивРезультатов[4].Выгрузить();
		МаркировкаДо = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(МаркировкаДо);
		
		Если МаркировкаДо.Количество() = 0 Тогда // Коды переданы через прямой обмен с ИС МП
			Маркировка = МаркировкаДо.СкопироватьКолонки();
		Иначе
			
			Маркировка = МассивРезультатов[3].Выгрузить();
			Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(Маркировка);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПрослеживаемыеКомплектующие = УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных(
		МассивРезультатов[5]);

	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке",            РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти",   РезультатПоТабличнойЧасти);
	СтруктураДанныхДляПечати.Вставить("РезультатПоИсходнымДанным",   РезультатПоИсходнымДанным);
	СтруктураДанныхДляПечати.Вставить("Маркировка",                  Маркировка);
	СтруктураДанныхДляПечати.Вставить("МаркировкаДо",                МаркировкаДо);
	СтруктураДанныхДляПечати.Вставить("ПрослеживаемыеКомплектующие", ПрослеживаемыеКомплектующие);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Возвращает данные для формирования печатной формы МХ - 3.
//
// Параметры:
//	ПараметрыПечати - Структура -дополнительные настройки печати.
//	МассивОбъектов - Массив из ДокументСсылка.ОтгрузкаТоваровКлиенту - коллекция значений ссылок на документы,
//	по которым необходимо получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемых для печати, содержащая следующие следующие свойства:
//		* РезультатПоШапке - РезультатЗапроса - данные шапки документа.
//		* РезультатПоСкладам - РезультатЗапроса - данные о складе ответственного хранения.
//		* РезультатПоТабличнойЧасти - РезультатЗапроса - данные табличной части документа.
//		* РезультатПоОшибкам - РезультатЗапроса - данные об ошибках, возникающих при печати документа.
//
Функция ПолучитьДанныеДляПечатнойФормыМХ3(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка                                КАК Ссылка,
	|	ДанныеДокумента.Номер                                 КАК Номер,
	|	ДанныеДокумента.Дата                                  КАК Дата,
	|	ДанныеДокумента.Дата                                  КАК ДатаДокумента,
	|	ДанныеДокумента.Организация                           КАК Организация,
	|	ДанныеДокумента.Организация.Префикс                   КАК Префикс,
	|	ТоварыДокумента.Склад                                 КАК Склад,
	|	ЕСТЬNULL(Склады.СкладОтветственногоХранения, ЛОЖЬ)    КАК СкладОтветственногоХранения,
	|	ДанныеДокумента.Организация = Склады.Поклажедержатель КАК ОрганизацияПоклажедержатель,
	|	Склады.ИсточникИнформацииОЦенахДляПечати              КАК ИсточникИнформацииОЦенахДляПечати,
	|	Склады.УчетныйВидЦены                                 КАК ВидЦены,
	|	Склады.УчетныйВидЦены.ВалютаЦены                      КАК ВалютаЦены,
	|	РасчетСебестоимостиТоваровОрганизации.Ссылка.ПредварительныйРасчет КАК ПредварительныйРасчет,
	|	ДанныеДокумента.Дата                                  КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ТоварыДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровКлиенту КАК ДанныеДокумента
	|		ПО ТоварыДокумента.Ссылка = ДанныеДокумента.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		ПО РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ)
	|		И РасчетСебестоимостиТоваровОрганизации.Ссылка.Проведен
	|		И ДанныеДокумента.Организация = РасчетСебестоимостиТоваровОрганизации.Организация
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ТоварыДокумента.Склад = Склады.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И ДанныеДокумента.Ссылка В(&МассивДокументов)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                            КАК Ссылка,
	|	ДанныеДокумента.Номер                             КАК Номер,
	|	ДанныеДокумента.Дата                              КАК Дата,
	|	ДанныеДокумента.ДатаДокумента                     КАК ДатаДокумента,
	|	ДанныеДокумента.Организация                       КАК Организация,
	|	ДанныеДокумента.Префикс                           КАК Префикс,
	|	ДанныеДокумента.Склад                             КАК Склад,
	|	ДанныеДокумента.ИсточникИнформацииОЦенахДляПечати КАК ИсточникИнформацииОЦенахДляПечати,
	|	ДанныеДокумента.ВидЦены                           КАК ВидЦены,
	|	ДанныеДокумента.ВалютаЦены                        КАК ВалютаЦены,
	|	ДанныеДокумента.ПредварительныйРасчет             КАК ПредварительныйРасчет,
	|	ДанныеДокумента.ДатаПолученияСебестоимости        КАК ДатаПолученияСебестоимости
	|ПОМЕСТИТЬ ВтШапка
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.СкладОтветственногоХранения
	|	И НЕ ДанныеДокумента.ОрганизацияПоклажедержатель
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	Товары.Ссылка                              КАК Ссылка,
	|	Товары.Склад                               КАК Склад,
	|	Товары.Упаковка                            КАК Упаковка,
	|	Товары.НомерСтроки                         КАК НомерСтроки,
	|	Товары.Серия                               КАК Серия,
	|	Товары.Номенклатура                        КАК Номенклатура,
	|	Товары.Характеристика                      КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения1          КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ВидУпаковки,
	|	Товары.КоличествоУпаковок                  КАК КоличествоУпаковок,
	|	Товары.Количество                          КАК Количество,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ)     КАК ДатаПолученияЦены,
	|	Шапка.ВидЦены                              КАК ВидЦены,
	|	Шапка.ВалютаЦены                           КАК ВалютаЦены
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО Товары.Склад = Шапка.Склад
	|		И Товары.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоВидуЦен)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка                              КАК Ссылка,
	|	Товары.Склад                               КАК Склад,
	|	Товары.Упаковка                            КАК Упаковка,
	|	Товары.НомерСтроки                         КАК НомерСтроки,
	|	Товары.Серия                               КАК Серия,
	|	Товары.Номенклатура                        КАК Номенклатура,
	|	Товары.Характеристика                      КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения1          КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения1 КАК ВидУпаковки,
	|	Товары.КоличествоУпаковок                  КАК КоличествоУпаковок,
	|	Товары.Количество                          КАК Количество,
	|	КОНЕЦПЕРИОДА(Товары.Ссылка.Дата, ДЕНЬ)     КАК ДатаПолученияЦены,
	|	Шапка.ВидЦены                              КАК ВидЦены,
	|	Шапка.ВалютаЦены                           КАК ВалютаЦены
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК Товары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО Товары.Склад = Шапка.Склад
	|		И Товары.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	Товары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И Шапка.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|	И Шапка.ПредварительныйРасчет ЕСТЬ NULL
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка                                    КАК Ссылка,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры                КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ВидЗапасов                                КАК ВидЗапасов,
	|	Шапка.Организация                                     КАК Организация,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения  КАК Склад,
	|	ВидыЗапасов.Упаковка                                  КАК Упаковка,
	|	ВидыЗапасов.НомерСтроки                               КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2            КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения2                     КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения2            КАК ВидУпаковки,
	|	ВидыЗапасов.КоличествоУпаковок                        КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество                                КАК Количество,
	|	КОНЕЦПЕРИОДА(ВидыЗапасов.Ссылка.Дата, ДЕНЬ)           КАК ДатаПолученияЦены,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены КАК ВидЦены,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.УчетныйВидЦены.ВалютаЦены КАК ВалютаЦены,
	|	Шапка.ПредварительныйРасчет                           КАК ПредварительныйРасчет
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.ВидыЗапасов КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК Шапка
	|		ПО ВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения = Шапка.Склад
	|		И ВидыЗапасов.Ссылка = Шапка.Ссылка
	|ГДЕ
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ИсточникИнформацииОЦенахДляПечати = ЗНАЧЕНИЕ(Перечисление.ИсточникиИнформацииОЦенахДляПечати.ПоСебестоимости)
	|;
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "Товары.Упаковка", "Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "Товары.Упаковка", "Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "ВидыЗапасов.Упаковка", "ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "ВидыЗапасов.Упаковка", "ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура"));
	
	СкладыСервер.ДополнитьТекстЗапросаДляПечатныхФормМХ1Х3(Запрос);
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	РезультатПоШапке          = МассивРезультатов[МассивРезультатов.ВГраница() - 3];
	РезультатПоСкладам        = МассивРезультатов[МассивРезультатов.ВГраница() - 2];
	РезультатПоТабличнойЧасти = МассивРезультатов[МассивРезультатов.ВГраница() - 1];
	РезультатПоОшибкам        = МассивРезультатов[МассивРезультатов.ВГраница()];
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке",          РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоСкладам",        РезультатПоСкладам);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти", РезультатПоТабличнойЧасти);
	СтруктураДанныхДляПечати.Вставить("РезультатПоОшибкам",        РезультатПоОшибкам);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

//-- Локализация

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	
	//++ Локализация
	
	#Область ТекстВключениеИсключениеНДСВСтоимость   // (Дт 45.01 :: Кт 19.03)
	ТекстЗапроса = "
	|ВЫБРАТЬ //// НДС на себестоимость реализованного товара (Дт 45.01 :: Кт 19.03) @НДСПредъявленный
	|
	|	Партии.Ссылка КАК Ссылка,
	|	Партии.Период КАК Период,
	|	Партии.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСРегл
	|	ИНАЧЕ
	|		-Партии.НДСРегл
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА Партии.ВключениеНДСВСтоимость ТОГДА
	|		Партии.НДСУпр
	|	ИНАЧЕ
	|		-Партии.НДСУпр
	|	КОНЕЦ КАК СуммаУУ,
	|
	|	ВЫБОР
	|		КОГДА Партии.КорРазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПередачаВПереработку)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НоменклатураПереданная)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	Партии.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	Партии.Склад КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Партии.Подразделение КАК ПодразделениеДт,
	|	Партии.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Партии.ГруппаФинансовогоУчета КАК СубконтоДт1,
	|	Партии.Контрагент КАК СубконтоДт2,
	|	Партии.КорНоменклатура КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаКт,
	|	Партии.ВидДеятельностиНДС                                              КАК АналитикаУчетаКт,
	|	Партии.ВидЦенности                                                     КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЕСТЬNULL(ВводСобственныхСредств.НаправлениеДеятельности, Партии.НаправлениеДеятельности) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	Партии.Контрагент КАК СубконтоКт1,
	|	Партии.ДокументПоступления КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""НДС на себестоимость реализованного товара"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Партии КАК Партии
	|	ПО
	|		ДокументыКОтражению.Ссылка = Партии.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТВводСобственныхСредств КАК ВводСобственныхСредств
	|	ПО Партии.Ссылка = ВводСобственныхСредств.Ссылка
	|		И Партии.ДокументПоступления = ВводСобственныхСредств.СчетФактура
	|		И Партии.НаправлениеДеятельности = ВводСобственныхСредств.ИсходноеНаправлениеДеятельности
	|ГДЕ
	|	Партии.ВключениеНДСВСтоимость ИЛИ Партии.ИсключениеНДСИзСтоимости
	|";
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти
	ТекстыОтражения.Добавить(ТекстОтгрузкаБезПереходаПраваСобственности()); // Отгрузка без перехода права собственности (Дт 45.01 :: Кт 20, 41.01)
	ТекстыОтражения.Добавить(ТекстПродажаКомиссионногоТовара()); // Продажа комиссионного товара (Дт 002, 003, 004.01, 004.02 :: Кт 002, 003, 004.01) 
	ТекстыОтражения.Добавить(ПроизводствоБезЗаказаЛокализация.ТекстВыпускДавальческойПродукцииБаланс()); // Выпуск давальческой продукции по балансу (Дт 20, 23 :: Кт 20, 23)
	
	//-- Локализация
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	
	
	//-- Локализация
	
	Возврат "";
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

//++ Локализация

// Правила получения значений реквизитов ТТН
// 
// Возвращаемое значение:
//  Структура -  см. Документы.ТранспортнаяНакладная.ПараметрыФормированияТранспортныхНакладных 
//
Функция ПараметрыФормированияТранспортныхНакладных() Экспорт
	
	ПараметрыФормированияТранспортныхНакладных =
		Документы.ТранспортнаяНакладная.ПараметрыФормированияТранспортныхНакладных();
		
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.Грузоотправитель = 
	"	ВЫБОР
	|		КОГДА ОснованиеТранспортнойНакладной.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ОснованиеТранспортнойНакладной.Организация
	|		ИНАЧЕ ОснованиеТранспортнойНакладной.Грузоотправитель
	|	КОНЕЦ";
	
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.Грузополучатель = 
	"		ВЫБОР
	|			КОГДА ОснованиеТранспортнойНакладной.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ОснованиеТранспортнойНакладной.Контрагент
	|			ИНАЧЕ ОснованиеТранспортнойНакладной.Грузополучатель
	|		КОНЕЦ";
	
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.ЗаказчикПеревозки                 = "ОснованиеТранспортнойНакладной.Контрагент";
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.БанковскийСчетЗаказчикаПеревозки  = "ОснованиеТранспортнойНакладной.БанковскийСчетКонтрагента";
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.Плательщик                        = "ОснованиеТранспортнойНакладной.Контрагент";
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.БанковскийСчетПлательщика         = "ОснованиеТранспортнойНакладной.БанковскийСчетКонтрагента";
	
	ПараметрыФормированияТранспортныхНакладных.ЕстьЗаказы = Истина;
	ПараметрыФормированияТранспортныхНакладных.ТекстУсловияИспользоватьРасширенныеВозможностиЗаказа = 
		"ЕСТЬNULL(ДокументТовары.ЗаказКлиента.ЭтоЗаказКакСчет, &ИспользоватьРасширенныеВозможностиЗаказа)";
	ПараметрыФормированияТранспортныхНакладных.ИмяПоляЗаказВТЧТовары = "ЗаказКлиента";
	ПараметрыФормированияТранспортныхНакладных.ИмяПоляНакладнаяПоЗаказу = "ОтгрузкаПоЗаказам";
	
	ПараметрыФормированияТранспортныхНакладных.ИспользоватьРасширенныеВозможностиЗаказа = Истина;
	
	Возврат ПараметрыФормированияТранспортныхНакладных;
	
КонецФункции

// Правила получения значений реквизитов ЭТН
// 
// Возвращаемое значение:
//  см. ОбменСГИСЭПДПереопределяемый.ПараметрыФормированияЭлектронныхТранспортныхНакладных
//
Функция ПараметрыФормированияЭлектронныхТранспортныхНакладных() Экспорт
	
	ПараметрыФормирования =
		ОбменСГИСЭПДПереопределяемый.ПараметрыФормированияЭлектронныхТранспортныхНакладных();

	ПараметрыФормирования.Реквизиты.СсылкаТитулГрузоотправителяГрузополучатель = 
	"		ВЫБОР
	|			КОГДА ОснованиеТранспортнойНакладной.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ОснованиеТранспортнойНакладной.Контрагент
	|			ИНАЧЕ ОснованиеТранспортнойНакладной.Грузополучатель
	|		КОНЕЦ";
	
	ПараметрыФормирования.ЕстьЗаказы = Истина;
	ПараметрыФормирования.ТекстУсловияИспользоватьРасширенныеВозможностиЗаказа = 
		"ЕСТЬNULL(ДокументТовары.ЗаказКлиента.ЭтоЗаказКакСчет, &ИспользоватьРасширенныеВозможностиЗаказа)";
	ПараметрыФормирования.ИмяПоляЗаказВТЧТовары = "ЗаказКлиента";
	ПараметрыФормирования.ИмяПоляНакладнаяПоЗаказу = "ОтгрузкаПоЗаказам";
	ПараметрыФормирования.ИмяПоляСуммы = "Сумма";
	
	ПараметрыФормирования.ИспользоватьРасширенныеВозможностиЗаказа = Истина;

	Возврат	ПараметрыФормирования;
	
КонецФункции

// Правила получения значений реквизитов ЭЗН
// 
// Возвращаемое значение:
//  см. ОбменСГИСЭПДПереопределяемый.ПараметрыФормированияЭлектронныхЗаказНарядов
//
Функция ПараметрыФормированияЭлектронныхЗаказНарядов() Экспорт
	
	ПараметрыФормирования =
		ОбменСГИСЭПДПереопределяемый.ПараметрыФормированияЭлектронныхЗаказНарядов();
		
	ПараметрыФормирования.ЕстьЗаказы = Истина;
	ПараметрыФормирования.ИмяПоляЗаказВТЧТовары = "ЗаказКлиента";
	ПараметрыФормирования.ИмяПоляНакладнаяПоЗаказу = "ОтгрузкаПоЗаказам";
	
	Возврат ПараметрыФормирования;
	
КонецФункции

//-- Локализация

#Область МаркируемаяПродукция

#Область ПодборМаркируемойПродукцииИзОрдеров

// Заполнить штрихкоды упаковок.
// 
// Параметры:
//  РаспоряженияДляОрдеров - Массив Из ДокументСсылка - Распоряжения для ордеров
//  Товары - ТаблицаЗначений - Товары
//  ШтрихкодыУпаковок - ТаблицаЗначений - Штрихкоды упаковок
Процедура ЗаполнитьШтрихкодыУпаковок(РаспоряженияДляОрдеров, Товары, ШтрихкодыУпаковок) Экспорт

//++ Локализация

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", РаспоряженияДляОрдеров);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтгрузкаТоваровКлиентуТовары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ
	|	ПотребителиШтрихкодовУпаковок
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ОтгрузкаТоваровКлиентуТовары
	|ГДЕ
	|	ОтгрузкаТоваровКлиентуТовары.ЗаказКлиента В (&Ссылка)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтгрузкаТоваровКлиентуШтрихкодыУпаковок.ШтрихкодУпаковки
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.ШтрихкодыУпаковок КАК ОтгрузкаТоваровКлиентуШтрихкодыУпаковок
	|ГДЕ
	|	ОтгрузкаТоваровКлиентуШтрихкодыУпаковок.Ссылка В (ВЫБРАТЬ Ссылка ИЗ ПотребителиШтрихкодовУпаковок)";
	ИспользованныеШтрихкодыУпаковок = Запрос.Выполнить().Выгрузить();
	ИнтеграцияИСУТ.ЗаполнитьШтрихкодыУпаковок(РаспоряженияДляОрдеров, Товары, ШтрихкодыУпаковок, ИспользованныеШтрихкодыУпаковок);

//-- Локализация

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Прочее

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаОперацииСПрослеживаемымиТоварамиРасширенный(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
	
	//-- НЕ УТ
	
	ТекстЗапросаТаблицаПрослеживаемыеТоварыОтгруженныеВЕАЭС(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);

	//-- Локализация
	
КонецПроцедуры

//++ Локализация

//++ НЕ УТ

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОперацииСПрослеживаемымиТоварамиРасширенный(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)

	ИмяРегистра = "ОперацииСПрослеживаемымиТоварамиРасширенный";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТоварыОрганизаций", ТекстыЗапроса) Тогда
		Документы.ОтгрузкаТоваровКлиенту.ТекстЗапросаВтТоварыОрганизаций(Запрос, ТекстыЗапроса);
	КонецЕсли;

	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтЕстьДвиженияПоРасширенномуРегиструПрослеживаемости", ТекстыЗапроса) Тогда
		УчетПрослеживаемыхТоваровЛокализация.ТекстЗапросаВтЕстьДвиженияПоРасширенномуРегиструПрослеживаемости(Запрос, ТекстыЗапроса);
	КонецЕсли;

	ПрослеживаемостьУП.УстановитьПараметрыЗапросаТипыСобственныхТоваров(Запрос);
	ПрослеживаемостьУП.УстановитьПараметрыЗапросаКодОперацииРаботыРеализация(Запрос);
	Запрос.УстановитьПараметр("ПрименяетсяОсвобождениеОтУплатыНДС", ПрослеживаемостьУП.ПрименяетсяОсвобождениеОтУплатыНДС(Запрос.Параметры.Организация,Запрос.Параметры.Дата));
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаДокумента.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)) КАК НеОблагаетсяНДС
	|ПОМЕСТИТЬ ВТДанныеДокументаПоНДС
	|ИЗ
	|	ВтТаблицаВидыЗапасов КАК ТаблицаДокумента
	|;
	|
	|///////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	втТоварыОрганизаций.Период КАК Период,
	|	втТоварыОрганизаций.Организация КАК Организация,
	|	втТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|	втТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	втТоварыОрганизаций.НомерГТД КАК РНПТ,
	|	СУММА(втТоварыОрганизаций.КоличествоПоРНПТ) КАК КоличествоПоРНПТ,
	|	СУММА(втТоварыОрганизаций.СтоимостьПоРНПТ) КАК СтоимостьПоРНПТ,
	|	ВЫБОР
	|		КОГДА втТоварыОрганизаций.ВидЗапасов.ТипЗапасов В (&ТипыСобственныхТоваров)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.Реализация)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
	|	КОНЕЦ КАК КодОперации,
	|	&Контрагент КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ТипыДокументов.Накладная) КАК ТипДокументаВПрослеживаемости,
	|	ВЫБОР
	|		КОГДА НЕ втТоварыОрганизаций.ВидЗапасов.ТипЗапасов В (&ТипыСобственныхТоваров) И НЕ &ПрименяетсяОсвобождениеОтУплатыНДС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ЖурналСФ)
	|		КОГДА &НалогообложениеНДС В (&ТипыНалогообложенияНДСНеоблагаемые) 
	|			ИЛИ &НалогообложениеОрганизации В (&ТипыНалогообложенияНДСНеоблагаемые) 
	|			ИЛИ ВТДанныеДокументаПоНДС.НеОблагаетсяНДС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПродаж)
	|	КОНЕЦ КАК ОтражениеВОтчетности,
	|	втТоварыОрганизаций.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ИЗ
	|	втТоварыОрганизаций КАК втТоварыОрганизаций,
	|	ВТДанныеДокументаПоНДС КАК ВТДанныеДокументаПоНДС
	|ГДЕ
	|	втТоварыОрганизаций.НомерГТДТипНомераГТД В (ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТ), 
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНомеровГТД.НомерРНПТКомплекта))
	|	И втТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|СГРУППИРОВАТЬ ПО
	|	втТоварыОрганизаций.Период,
	|	втТоварыОрганизаций.Организация,
	|	втТоварыОрганизаций.ВидЗапасов,
	|	втТоварыОрганизаций.АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА НЕ втТоварыОрганизаций.ВидЗапасов.ТипЗапасов В (&ТипыСобственныхТоваров) И НЕ &ПрименяетсяОсвобождениеОтУплатыНДС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ЖурналСФ)
	|		КОГДА &НалогообложениеНДС В (&ТипыНалогообложенияНДСНеоблагаемые) 
	|			ИЛИ &НалогообложениеОрганизации В (&ТипыНалогообложенияНДСНеоблагаемые) 
	|			ИЛИ ВТДанныеДокументаПоНДС.НеОблагаетсяНДС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.ОтчетОбОперациях)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ПорядокОтраженияВОтчетностиПоПрослеживаемости.КнигаПродаж)
	|	КОНЕЦ,
	|	втТоварыОрганизаций.ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА втТоварыОрганизаций.ВидЗапасов.ТипЗапасов В (&ТипыСобственныхТоваров)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.Реализация)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.КодыОперацийПрослеживаемости.ПустаяСсылка)
	|	КОНЕЦ,
	|	втТоварыОрганизаций.НомерГТД
	|
	|ИМЕЮЩИЕ
	|	СУММА(втТоварыОрганизаций.КоличествоПоРНПТ) <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- НЕ УТ

Функция ТекстЗапросаТаблицаПрослеживаемыеТоварыОтгруженныеВЕАЭС(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ПрослеживаемыеТоварыОтгруженныеВЕАЭС";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 

	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасов", ТекстыЗапроса) Тогда
		Документы.ОтгрузкаТоваровКлиенту.ТекстЗапросаВтТаблицаВидыЗапасовДекоратор(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Контрагент,
	|	&Ссылка КАК СопроводительныйДокумент,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.НомерГТД КАК РНПТ,
	|	ТаблицаВидыЗапасов.НомерСтроки КАК ПорядковыйНомер,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ КАК КоличествоПрослеживаемости,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл КАК Сумма
	|ИЗ
	|	ВтТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	&ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|	И &КонтрагентУчастникЕАЭС
	|	И &КонтрагентНеРезидент
	|	И НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) >= &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|	И ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И ТаблицаВидыЗапасов.ПрослеживаемыйТовар";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- Локализация

#КонецОбласти

//++ Локализация

//++ НЕ УТ

#Область ФрагментыПроводокРеглУчета

Функция ТекстОтгрузкаБезПереходаПраваСобственности()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ //// Отгрузка без перехода права собственности (Дт 45.01 :: Кт 20, 41.01)
	|	Строки.Ссылка КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Стоимости.СуммаБалансовая, Строки.СуммаБалансовая) КАК Сумма,
	|	ЕСТЬNULL(Стоимости.СуммаБалансоваяУУ, Строки.СуммаБалансоваяУУ) КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РеализацияБезПереходаПраваСобственности) КАК ВидСчетаДт,
	|	Строки.КорГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.КорПодразделениеАналитики КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Строки.КорКонтрагент КАК СубконтоДт1,
	|	Строки.КорНоменклатура КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	ЕСТЬNULL(Стоимости.СуммаНУ, 0) КАК СуммаНУДт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, 0) КАК СуммаПРДт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, 0) КАК СуммаВРДт,
	|
	|	ВЫБОР
	|		КОГДА Строки.ПроизводственныеЗатраты
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	ВЫБОР
	|		КОГДА Строки.ПроизводственныеЗатраты
	|		ТОГДА Строки.ГруппаПродукции
	|		ИНАЧЕ Строки.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК АналитикаУчетаКт,
	|	ВЫБОР
	|		КОГДА Строки.ПроизводственныеЗатраты
	|		ТОГДА Строки.ПодразделениеАналитики
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.ПодразделениеАналитики КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	ВЫБОР
	|		КОГДА Строки.ПроизводственныеЗатраты
	|		ТОГДА Строки.ГруппаПродукции
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ КАК СубконтоКт2,
	|	Строки.ТипЗатрат КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	ЕСТЬNULL(Стоимости.СуммаНУ, 0) КАК СуммаНУКт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, 0) КАК СуммаПРКт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, 0) КАК СуммаВРКт,
	|	""Отгрузка без перехода права собственности"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		ДокументыКОтражению.Ссылка = Строки.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.ГруппаПродукции = Стоимости.ГруппаПродукции
	|		И Строки.КорГруппаПродукции = Стоимости.КорГруппаПродукции
	|		И Строки.НаправлениеДеятельности = Стоимости.НаправлениеДеятельности
	|		И Строки.РазделУчета = Стоимости.РазделУчета
	|		И Строки.ВидДвижения = Стоимости.ВидДвижения
	|		И Строки.ИдентификаторСтроки = Стоимости.ИдентификаторСтроки
	|		И Строки.ИдентификаторФинЗаписи = Стоимости.ИдентификаторФинЗаписи
	|		И Строки.НастройкаХозяйственнойОперации = Стоимости.НастройкаХозяйственнойОперации
	|ГДЕ
	|	Строки.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|							ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты))";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстПродажаКомиссионногоТовара()
	
	ТекстЗапроса = ПроизводствоБезЗаказаЛокализация.ТекстПередачаТоваровЗаБалансом(Ложь, Ложь);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Содержание", "ВЫБОР
	|		КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|			И Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтгрузкаТоваровКлиенту)
	|		ТОГДА ""Продажа комиссионного товара""
	|		ИНАЧЕ ""Передача товаров, принятых на хранение""
	|	КОНЕЦ");
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

//-- НЕ УТ

//-- Локализация

// Параметры:
//	ДокументСсылка - ДокументСсылка - 
//	ПараметрыСканирования - Структура, Неопределено - 
// Возвращаемое значение:
//	ТаблицаЗначений:
//	*СтрокаВыбрана - Булево
//	*Номенклатура - СправочникСсылка.Номенклатура
//	*Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//	*Серия - СправочникСсылка.СерииНоменклатуры
Функция ПолучитьШтрихкодыУпаковокОтгрузки(ДокументСсылка, ПараметрыСканирования = Неопределено) Экспорт
	
	ШтрихкодыУпаковокОтгрузки = Новый ТаблицаЗначений;
	
	ШтрихкодыУпаковокОтгрузки.Колонки.Добавить("СтрокаВыбрана", Новый ОписаниеТипов("Булево"));
	ШтрихкодыУпаковокОтгрузки.Колонки.Добавить("Номенклатура",
		Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ШтрихкодыУпаковокОтгрузки.Колонки.Добавить("Характеристика",
		Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ШтрихкодыУпаковокОтгрузки.Колонки.Добавить("Серия",
		Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	
	//++ Локализация
	
	ШтрихкодыУпаковокОтгрузки.Колонки.Добавить("ШтрихкодУпаковки",
		Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	
	ШтрихкодыУпаковокПоОтгрузке = ШтрихкодированиеИСМП.ВложенныеШтрихкодыУпаковокПоДокументу(
		ДокументСсылка, ПараметрыСканирования);
		
	Если Не ШтрихкодыУпаковокПоОтгрузке.ЕстьОшибки Тогда
		
		Для Каждого МаркированныйТовар Из ШтрихкодыУпаковокПоОтгрузке.ВложенныеШтрихкоды.МаркированныеТовары Цикл
			НоваяСтрока = ШтрихкодыУпаковокОтгрузки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, МаркированныйТовар);
		КонецЦикла;
		
	КонецЕсли; 
	
	//-- Локализация
	
	Возврат ШтрихкодыУпаковокОтгрузки;
	
КонецФункции

//++ Локализация

Процедура УстановитьПараметр(ОбластьМакета, ИмяПараметра, ЗначениеПараметра)
	
	ОбластьМакета.Параметры.Заполнить(Новый Структура(ИмяПараметра, ЗначениеПараметра));
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуРеестрНомеровГТД(МассивОбъектов, ОбъектыПечати)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаВидыЗапасов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВтТаблицаДокументов
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка В(&МассивОбъектов)
	|	И ТаблицаВидыЗапасов.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|;
	|
	|///////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                     КАК Ссылка,
	|	ДанныеДокумента.Номер                      КАК Номер,
	|	ДанныеДокумента.Дата                       КАК Дата,
	|	ДанныеДокумента.ИсправляемыйДокумент.Номер КАК НомерИсправляемогоДокумента,
	|	ДанныеДокумента.ИсправляемыйДокумент.Дата  КАК ДатаИсправляемогоДокумента,
	|	ДанныеДокумента.Организация                КАК Организация,
	|	ДанныеДокумента.Организация.Префикс        КАК Префикс,
	|	ДанныеДокумента.Контрагент                 КАК Контрагент,
	|	ДанныеДокумента.Менеджер.ФизическоеЛицо    КАК МенеджерФизическоеЛицо
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаДокументов КАК ТаблицаДокументов
	|		ПО ДанныеДокумента.Ссылка = ТаблицаДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|///////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.Ссылка                       КАК Ссылка,
	|	АналитикаУчетаНоменклатуры.Номенклатура         КАК Номенклатура,
	|	АналитикаУчетаНоменклатуры.Номенклатура.Код     КАК Код,
	|	АналитикаУчетаНоменклатуры.Номенклатура.Артикул КАК Артикул,
	|	АналитикаУчетаНоменклатуры.Номенклатура.НаименованиеПолное КАК НаименованиеНоменклатуры,
	|	АналитикаУчетаНоменклатуры.Характеристика.НаименованиеПолное КАК Характеристика,
	|	АналитикаУчетаНоменклатуры.Серия                КАК Серия,
	|	ПРЕДСТАВЛЕНИЕ(АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения) КАК ПредставлениеБазовойЕдиницыИзмерения,
	|	СУММА(ТаблицаВидыЗапасов.Количество)            КАК Количество,
	|	ТаблицаВидыЗапасов.НомерГТД                     КАК НомерГТД,
	|	ТаблицаВидыЗапасов.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ЛОЖЬ                                            КАК ЭтоВозвратнаяТара
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаДокументов КАК ТаблицаДокументов
	|		ПО ТаблицаВидыЗапасов.Ссылка = ТаблицаДокументов.Ссылка
	|ГДЕ
	|	ТаблицаВидыЗапасов.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаВидыЗапасов.Ссылка,
	|	ТаблицаВидыЗапасов.НомерСтроки,
	|	АналитикаУчетаНоменклатуры.Номенклатура,
	|	АналитикаУчетаНоменклатуры.Характеристика,
	|	АналитикаУчетаНоменклатуры.Серия,
	|	ТаблицаВидыЗапасов.НомерГТД
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	ТаблицаВидыЗапасов.НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|
	|///////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК ДанныеДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаДокументов КАК ТаблицаДокументов
	|		ПО ДанныеДокумента.Ссылка = ТаблицаДокументов.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка В(&МассивОбъектов)
	|	И ТаблицаДокументов.Ссылка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов = Запрос.ВыполнитьПакет(); 
	УстановитьПривилегированныйРежим(Ложь);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОтгрузкаТоваровКлиенту_РеестрНомеровГТД";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ОтгрузкаТоваровКлиенту.ПФ_MXL_РеестрНомеровГТД");
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов();
	ИмяКолонкиКодов = КолонкаКодов.ИмяКолонки;
	ПредставлениеКолонкиКодов = КолонкаКодов.ПредставлениеКолонки;
	ВыводитьКоды = ЗначениеЗаполнено(ИмяКолонкиКодов);
	
	ДанныеПечати              = МассивРезультатов[1].Выбрать();
	ВыборкаПоДокументам       = МассивРезультатов[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВыборкаПоДокументамБезГТД = МассивРезультатов[3].Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Область = Макет.ПолучитьОбласть("Шапка");
		
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, Область,
			ДанныеПечати.Ссылка);
			
		ДатаПолученияСведений = ?(
			ЗначениеЗаполнено(ДанныеПечати.ДатаИсправляемогоДокумента),
			ДанныеПечати.ДатаИсправляемогоДокумента,
			ДанныеПечати.Дата);
		
		ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(ДанныеПечати,
			НСтр("ru = 'Реестр номеров ГТД к накладной';
				|en = 'CCD number registry to invoice'"));
		
		ПредставлениеОрганизации = ФормированиеПечатныхФорм.ОписаниеОрганизации(
			ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДатаПолученияСведений),
			"ПолноеНаименование");
		
		ПредставлениеПартнера    = ФормированиеПечатныхФорм.ОписаниеОрганизации(
			ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Контрагент, ДатаПолученияСведений),
			"ПолноеНаименование");
		
		ДанныеШапки = Новый Структура;
		ДанныеШапки.Вставить("ТекстЗаголовка",           ТекстЗаголовка);
		ДанныеШапки.Вставить("Организация",              ДанныеПечати.Организация);
		ДанныеШапки.Вставить("ПредставлениеОрганизации", ПредставлениеОрганизации);
		ДанныеШапки.Вставить("Контрагент",               ДанныеПечати.Контрагент);
		ДанныеШапки.Вставить("ПредставлениеПартнера",    ПредставлениеПартнера);
		
		Область.Параметры.Заполнить(ДанныеШапки);
		ТабличныйДокумент.Вывести(Область);
		
		Область = Макет.ПолучитьОбласть("ШапкаТаблицы|НачалоСтроки");
		ТабличныйДокумент.Вывести(Область);
		
		Если ВыводитьКоды Тогда
			Область = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаКодов");
			
			УстановитьПараметр(Область, "ИмяКолонкиКодов", ПредставлениеКолонкиКодов);
			ТабличныйДокумент.Присоединить(Область);
		КонецЕсли;
		
		Область = Макет.ПолучитьОбласть("ШапкаТаблицы|КолонкаТоваров");
		ТабличныйДокумент.Присоединить(Область);
		
		НомерСтроки = 1;
		
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
		ВыборкаПоДокументам.НайтиСледующий(СтруктураПоиска);
		
		ВыборкаПоТоварам = ВыборкаПоДокументам.Выбрать();
		
		Пока ВыборкаПоТоварам.Следующий() Цикл
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы|НачалоСтроки");
			
			УстановитьПараметр(Область, "НомерСтроки", НомерСтроки);
			ТабличныйДокумент.Вывести(Область);
			
			НомерСтроки = НомерСтроки + 1;
			
			Если ВыводитьКоды Тогда
				Область = Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаКодов");
				
				УстановитьПараметр(Область, "ЗначениеКода", ВыборкаПоТоварам[ИмяКолонкиКодов]);
				ТабличныйДокумент.Присоединить(Область);
			КонецЕсли;
			
			Область = Макет.ПолучитьОбласть("СтрокаТаблицы|КолонкаТоваров");
			Область.Параметры.Заполнить(ВыборкаПоТоварам);
			
			ДополнительныеПараметрыПолученияНаименованияДляПечати =
				НоменклатураКлиентСервер.ДополнительныеПараметрыПредставлениеНоменклатурыДляПечати();
			ДополнительныеПараметрыПолученияНаименованияДляПечати.ВозвратнаяТара = ВыборкаПоТоварам.ЭтоВозвратнаяТара;
			ДополнительныеПараметрыПолученияНаименованияДляПечати.КодОсновногоЯзыка =
				ОбщегоНазначения.КодОсновногоЯзыка();
			
			Товар = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				ВыборкаПоТоварам.НаименованиеНоменклатуры,
				ВыборкаПоТоварам.Характеристика,
				,
				,
				ДополнительныеПараметрыПолученияНаименованияДляПечати);
			
			УстановитьПараметр(Область, "Товар", Товар);
			ТабличныйДокумент.Присоединить(Область);
			
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть("ПодвалТаблицы|НачалоСтроки");
		ТабличныйДокумент.Вывести(Область);
		
		Если ВыводитьКоды Тогда
			Область = Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаКодов");
			ТабличныйДокумент.Присоединить(Область);
		КонецЕсли;
		
		Область = Макет.ПолучитьОбласть("ПодвалТаблицы|КолонкаТоваров");
		ТабличныйДокумент.Присоединить(Область);
		
		Область = Макет.ПолучитьОбласть("Подписи");
		
		ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Всего наименований %1';
																						|en = 'Total items %1'"),
			Строка(НомерСтроки - 1));
		
		ДанныеПодписей = Новый Структура;
		ДанныеПодписей.Вставить("ИтоговаяСтрока", ИтоговаяСтрока);
		
		Если ЗначениеЗаполнено(ДанныеПечати.МенеджерФизическоеЛицо) Тогда
			ДанныеПодписей.Вставить("Менеджер",
				ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ДанныеПечати.МенеджерФизическоеЛицо, ДатаПолученияСведений));
		КонецЕсли;
		
		Область.Параметры.Заполнить(ДанныеПодписей);
		ТабличныйДокумент.Вывести(Область);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	// Выведем сообщения о документах, для которых не требуется печатать реестр номеров ГТД.
	Пока ВыборкаПоДокументамБезГТД.Следующий() Цикл
		ТекстСообщения = НСтр("ru = 'В документе %1 отсутствуют товары с номерами ГТД. Печать реестра номеров ГТД не требуется.';
								|en = 'Goods with CCD numbers are missing in document %1. Printing of CCD number registry is not required.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,
			Строка(ВыборкаПоДокументамБезГТД.Ссылка));
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ВыборкаПоДокументамБезГТД.Ссылка);
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура СформироватьВременнуюТаблицуНоменклатураДокументов(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ТаблицаТоваров.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьПрослеживаемыеТовары
	|ПОМЕСТИТЬ
	|	НоменклатураДокументов
	|ИЗ
	|	ОтгрузкаТоваровКлиентуТаблицаТоваров КАК ТаблицаТоваров
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	Запрос.Выполнить();
	
КонецПроцедуры

//-- Локализация

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ФормаДокумента

//++ НЕ УТ
Процедура КонтрагентПриИзменении(Форма) Экспорт

	//++ Локализация
	ЗаполнитьСтрануРеализации(Форма);
	//-- Локализация
	
КонецПроцедуры

Процедура ПартнерПриИзменении(Форма) Экспорт

	//++ Локализация
	ЗаполнитьСтрануРеализации(Форма);
	//-- Локализация
	
КонецПроцедуры
//-- НЕ УТ

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Локализация

//++ НЕ УТ
Процедура ЗаполнитьСтрануРеализации(Форма)

	Объект = Форма.Объект;
	Объект.СтранаРеализации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Форма.Объект.Контрагент, "СтранаРегистрации");
	
КонецПроцедуры
//-- НЕ УТ

//-- Локализация

#КонецОбласти
