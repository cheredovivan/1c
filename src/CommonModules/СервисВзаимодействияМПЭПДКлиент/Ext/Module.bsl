
#Область СлужебныйПрограммныйИнтерфейс

#Область АсинхронныеСценарии

Процедура НачатьАсинхронныйСценарий(ФункцияУправления) Экспорт
	
	ОповещениеУправления = Новый ОписаниеОповещения(
							ФункцияУправления.ИмяПроцедуры, 
							ФункцияУправления.Модуль,
							ФункцияУправления.ДополнительныеПараметры,
							"ПродолжитьАсинхронныйСценарийОшибка",
							СервисВзаимодействияМПЭПДКлиент);
							
	ФункцияУправления.ДополнительныеПараметры.Вставить("ОповещениеУправления", ОповещениеУправления);
	ФункцияУправления.ДополнительныеПараметры.Вставить("ИдентификаторСценарий", Новый УникальныйИдентификатор());

	ПродолжитьАсинхронныйСценарий(Неопределено, ФункцияУправления.ДополнительныеПараметры);
	
КонецПроцедуры

// Возвращает признак необходимости обработать ошибку.
// 
// Параметры:
//  СостояниеОшибки - Строка, ИнформацияОбОшибке, Неопределено - Состояние ошибки.
// 
// Возвращаемое значение:
//  Булево - Признак необходимости обработать ошибку.
Функция ОбработатьОшибку(СостояниеОшибки) Экспорт
	
	Результат = Ложь;
	Если ТипЗнч(СостояниеОшибки) = Тип("Строка") Тогда
		Результат = Истина;
	ИначеЕсли ЕстьОшибкаАсинхронногоСценария(СостояниеОшибки) Тогда
		Результат = Истина;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

// Возвращает описание оповещения продолжения ассинхронного сценария.
// 
// Параметры:
//  ДополнительныеПараметры - Структура - Дополнительные параметры.
// 
// Возвращаемое значение:
//  ОписаниеОповещения - Результат ассинхронный сценарий
Функция РезультатАсинхронныйСценарий(ДополнительныеПараметры) Экспорт
	
	ОписаниеФункции = Новый ОписаниеОповещения(
							"ПродолжитьАсинхронныйСценарий", 
							СервисВзаимодействияМПЭПДКлиент,
							ДополнительныеПараметры,
							"ПродолжитьАсинхронныйСценарийОшибка",
							СервисВзаимодействияМПЭПДКлиент);
	Возврат ОписаниеФункции;
	
КонецФункции

// Возвращает описание оповещения продолжения ассинхронного сценария без параметров.
// 
// Параметры:
//  ДополнительныеПараметры - Структура - Дополнительные параметры.
// 
// Возвращаемое значение:
//  ОписаниеОповещения - Результат ассинхронный сценарий без параметров
Функция РезультатАсинхронныйСценарийБезПараметров(ДополнительныеПараметры) Экспорт
	
	ОписаниеФункции = Новый ОписаниеОповещения(
							"ПродолжитьАсинхронныйСценарийБезПараметров", 
							СервисВзаимодействияМПЭПДКлиент,
							ДополнительныеПараметры,
							"ПродолжитьАсинхронныйСценарийОшибка",
							СервисВзаимодействияМПЭПДКлиент);
	Возврат ОписаниеФункции;
	
КонецФункции

Процедура ПродолжитьАсинхронныйСценарий(РезультатВызова = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеУправления, РезультатВызова);
	
КонецПроцедуры

Процедура ПродолжитьАсинхронныйСценарийБезПараметров(ДополнительныеПараметры = Неопределено) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеУправления, Неопределено);
	
КонецПроцедуры

Процедура ПродолжитьАсинхронныйСценарийОшибка(ДанныеОшибки, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ПродолжитьАсинхронныйСценарий(ДанныеОшибки, ДополнительныеПараметры)
	
КонецПроцедуры

// Признак ошибки ассинхронного сценария.
// 
// Параметры:
//  РезультатВызова - Строка, ИнформацияОбОшибке, Неопределено - Результат вызова.
// 
// Возвращаемое значение:
//  Булево - Есть ошибка ассинхронного сценария.
Функция ЕстьОшибкаАсинхронногоСценария(РезультатВызова) Экспорт
	
	Результат = ТипЗнч(РезультатВызова) = Тип("ИнформацияОбОшибке");
	Возврат Результат;
	
КонецФункции

Процедура НовоеФоновоеЗадание(ИмяФункции, ПараметрыЗадания, ПараметрыЦикла, УникальныйИдентификатор) Экспорт
	
	ДлительнаяОперация = СервисВзаимодействияМПЭПДВызовСервера.ОжидатьФоновоеЗадание(ИмяФункции, ПараметрыЗадания, УникальныйИдентификатор);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	Если ТипЗнч(ПараметрыЦикла) = Тип("Структура") Тогда
		Оповещение = РезультатАсинхронныйСценарий(ПараметрыЦикла);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	Иначе
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ПараметрыЦикла, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

// Структура подключения организации.
// 
// Возвращаемое значение:
//  Структура - Новая структура подключения организации:
// * ИдентификаторЭДО - Строка
// * Организация - ОпределяемыйТип.Организация
Функция НоваяСтруктураПодключенияОрганизации() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("ИдентификаторЭДО");
	Структура.Вставить("Организация");
	
	//@skip-check constructor-function-return-section
	Возврат Структура;
	
КонецФункции

Процедура ПодключениеОрганизации(СтруктураПодключенияОрганизации, УникальныйИдентификаторФормы = Неопределено,
		ОповещениеОЗавершении = Неопределено) Экспорт
	
	ИдентификаторЭДО = СтруктураПодключенияОрганизации.ИдентификаторЭДО;
	
	// Серверный вызов для получения данных выполняется только здесь
	СтруктураДанныхДляПодключения = 
		НеобходимыеДанныеССервераДляРегистрацииУчетнойЗаписи(ИдентификаторЭДО);
	
	Если СтруктураДанныхДляПодключения.НужноВвестиДанныеИнтернетПоддержки
		И Не СтруктураДанныхДляПодключения.ДоступенВводДанныхИнтернетПоддержки Тогда
		                                        
		ТекстПредупреждения = НСтр("ru = 'Для работы с мобильными устройствами необходимо подключиться к сервисам Интернет-поддержки пользователей.';
									|en = 'To operate with mobile devices, connect to the Online user support services.'") 
				+ НСтр("ru = ' Недостаточно прав для подключения Интернет-поддержки пользователей.';
						|en = ' You have insufficient rights to connect to Online support.'")
				+ Символы.ПС + Символы.ПС + НСтр("ru = 'Обратитесь к администратору.';
												|en = 'Please contact the administrator.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Результат = Ложь;
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
		Возврат;
	КонецЕсли;
	
	ИмяПервогоЭтапа = "НачалоПодключения";
	КонтекстСценария = ПодготовитьКонтекстСценария(ИмяПервогоЭтапа, ОповещениеОЗавершении, УникальныйИдентификаторФормы);
	
	КонтекстСценария.Вставить("ИдентификаторЭДО", СтруктураПодключенияОрганизации.ИдентификаторЭДО);
	КонтекстСценария.Вставить("Организация", СтруктураПодключенияОрганизации.Организация);
	КонтекстСценария.Вставить("НужноВвестиДанныеИнтернетПоддержки", СтруктураДанныхДляПодключения.НужноВвестиДанныеИнтернетПоддержки);
	
	Оповещение = Новый ОписаниеОповещения("СценарийПодключенияОрганизации", ЭтотОбъект, КонтекстСценария);
	НачатьАсинхронныйСценарий(Оповещение);
	
КонецПроцедуры

Процедура СценарийПодключенияОрганизации(РезультатПредыдущегоШага, КонтекстСценария) Экспорт
	
	Если КонтекстСценария.Этап = "НачалоПодключения" Тогда
		Если КонтекстСценария.НужноВвестиДанныеИнтернетПоддержки Тогда
			КонтекстСценария.Этап = "ВводДанныхИнтернетПоддержки";
			Оповещение = РезультатАсинхронныйСценарийБезПараметров(КонтекстСценария);
			ТекстПредупреждения = НСтр("ru = 'Для работы с мобильными устройствами необходимо подключиться к сервисам Интернет-поддержки пользователей.';
										|en = 'To operate with mobile devices, connect to the Online user support services.'");
			ПоказатьПредупреждение(Оповещение, ТекстПредупреждения);
			Возврат;
		Иначе
			КонтекстСценария.Этап = "ПолучениеМаркера";
		КонецЕсли;
	КонецЕсли;
	
	Если КонтекстСценария.Этап = "ВводДанныхИнтернетПоддержки" Тогда
		КонтекстСценария.Этап = "ПослеВведенияДанныхИнтернетПоддержки";
		Оповещение = РезультатАсинхронныйСценарий(КонтекстСценария);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	Если КонтекстСценария.Этап = "ПослеВведенияДанныхИнтернетПоддержки" Тогда
		// Данные интернет-поддержки не были введены. Сценарий прерывается.
		Если РезультатПредыдущегоШага = Неопределено Тогда
			ТекстПредупреждения = НСтр("ru = 'Без подключения к сервисам Интернет-поддержки пользователей работа с мобильными устройствами невозможна.';
										|en = 'Cannot operate with mobile devices without connecting to the Online user support services.'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
			Результат = Ложь;
			ВыполнитьОбработкуОповещенияОЗавершенииСценария(КонтекстСценария, Результат);
			Возврат;
		КонецЕсли;
		
		КонтекстСценария.Этап = "ПолучениеМаркера";
	КонецЕсли;
	
	Если КонтекстСценария.Этап = "ПолучениеМаркера" Тогда
		КонтекстСценария.Этап = "ПослеПолученияМаркера";
		МассивИдентификаторов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КонтекстСценария.ИдентификаторЭДО);
		КонтекстДиагностики = ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики();
		Оповещение = РезультатАсинхронныйСценарий(КонтекстСценария);
		СинхронизацияЭДОКлиент.АвторизоватьсяВСервисеЭлектронногоДокументооборота(Оповещение,
				КонтекстДиагностики, МассивИдентификаторов);
		Возврат;
	КонецЕсли;
	
	Если КонтекстСценария.Этап = "ПослеПолученияМаркера" Тогда
		КлючиСинхронизации = РезультатПредыдущегоШага.КлючиСинхронизации;
		ДанныеМаркера = КлючиСинхронизации[КонтекстСценария.ИдентификаторЭДО];
		
		Если ДанныеМаркера = Неопределено Тогда
			ТекстПредупреждения = НСтр("ru = 'Не удалось авторизоваться на сервере 1С-ЭДО.';
										|en = 'Cannot authorize on the 1C:EDI server.'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
			Результат = Ложь;
			ВыполнитьОбработкуОповещенияОЗавершенииСценария(КонтекстСценария, Результат);
			Возврат;
		КонецЕсли;
		
		КонтекстСценария.Этап = "РезультатРегистрацииОрганизации";
		
		Маркер = ПолучитьСтрокуИзДвоичныхДанных(ДанныеМаркера.МаркерРасшифрованный);
		УникальныйИдентификаторФормы = КонтекстСценария.УникальныйИдентификаторФормы;
		КонтекстСценария.Вставить("Маркер", Маркер);
		
		ПараметрыЗадания = МассивИзЗначений(КонтекстСценария.ИдентификаторЭДО,
				КонтекстСценария.Организация, Маркер);
		НовоеФоновоеЗадание("СервисВзаимодействияМПЭПД.СервисРегистрацияОрганизации",
				ПараметрыЗадания, КонтекстСценария, УникальныйИдентификаторФормы);
		Возврат;
	КонецЕсли;
	
	Если КонтекстСценария.Этап = "РезультатРегистрацииОрганизации" Тогда
		УспешностьРегистрации = ПолучитьИзВременногоХранилища(РезультатПредыдущегоШага.АдресРезультата);
		
		Если Не УспешностьРегистрации Тогда
			ТекстПредупреждения = НСтр("ru = 'Не удалось успешно зарегистрироваться на сервере взаимодействия с мобильными устройствами.';
										|en = 'Cannot successfully register on the mobile device collaboration server.'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
			Результат = Ложь;
			ВыполнитьОбработкуОповещенияОЗавершенииСценария(КонтекстСценария, Результат);
		Иначе
			Результат = Истина;
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("Маркер", КонтекстСценария.Маркер);
			ВыполнитьОбработкуОповещенияОЗавершенииСценария(КонтекстСценария, Результат, ДополнительныеПараметры);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВизуальноеОформлениеМПНаФормахДокументов

Процедура СписокВыборНаФорме(Форма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) Экспорт
	
	Если Поле = Форма.Элементы.МобильноеПриложение Тогда
		СтандартнаяОбработка = Ложь;
		
		ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
		Если ДанныеСтроки.Организация <> ДанныеСтроки.Перевозчик
			И ТипЗнч(ДанныеСтроки.Ссылка) <> Тип("ДокументСсылка.ЭлектронныйПутевойЛист") Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Мобильное приложение доступно только перевозчику';
											|en = 'The mobile application is available only to the carrier'"));
		Иначе
			ПараметрыДействийМП = ПараметрыДействийДляМобильногоПриложения(ДанныеСтроки.Ссылка,
				ДанныеСтроки.ИдентификаторЗаписиМП);
			Если ПараметрыДействийМП = Неопределено Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Документ не поддерживает обмен с мобильным приложением';
												|en = 'The document does not support exchange with the mobile application'"));
				Возврат;
			КонецЕсли;
			ПараметрыДействийМП.Вставить("Форма", Форма);
			ОбработатьВыборДействияНаФорме(Новый Структура("Значение", "Выбрать"), ПараметрыДействийМП);	
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработатьВыборДействияНаФорме(Результат, ПараметрыДействийМП) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ПараметрыДействийМП.Форма;
	ПараметрыДействийМП.Удалить("Форма");

	ОткрытьФорму("Обработка.ВзаимодействиеМПЭПД.Форма.ВыборДействияПоДокументу", 
		ПараметрыДействийМП, Форма, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
КонецПроцедуры

// Возвращает параметры действий для мобильного приложения
//
// Параметры:
//  Документ				 - ДокументСсылка - 
//  ИдентификаторЗаписиМП	 - Строка - 
//  ИндексРабот				 - Число - Индекс работы выполняемой пользователем МП см. СервисВзаимодействияМПЭПДКлиентСервер.ИндексРаботыВодитель().
// 
// Возвращаемое значение:
//  Структура - Параметры действия мобильного приложения:
// * ИдентификаторЭДО - Строка
// * Организация - ОпределяемыйТип.Организация
// * Документ - ОпределяемыйТип.ДокументыЭПД 
// * Титул - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
// * МассивСтруктурКлючевхПолейПользователей - Массив Из Структура:
// ** ИмяРеквизита - Строка
// ** ЗначениеРеквизита - Произвольный
// * Роль - Число - Роль пользователя МП.
// * ИдентификаторЗаписиМП - Строка
// * ЗаголовокПользователяМП - Строка
// * ДоступенОтказ - Булево - Доступность формирования УОУ.
// * ИндексРабот - Число
//
//@skip-check constructor-function-return-section
Функция ПараметрыДействийДляМобильногоПриложения(Документ, Знач ИдентификаторЗаписиМП, ИндексРабот = Неопределено) Экспорт
	
	СоответствиеТитуловИШагов = ОбменСГИСЭПДКлиентСервер.СоответствиеТитуловИШагов(Документ);
	СоответствиеТитуловИШагов = СервисВзаимодействияМПЭПДКлиентСервер.ПеревернутьСоответствие(СоответствиеТитуловИШагов);
	
	// Титул в котором содержатся данные по которым можно определить пользователя МП
	ТитулСПользователямиМП = Неопределено;
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
		ИмяРеквизитаИдентификаторЭДО = "ИдентификаторПеревозчика";
		РеквизитыДокумента = ОбменСГИСЭПДВызовСервера.ЗначенияРеквизитовОбъекта(Документ,
			ИмяРеквизитаИдентификаторЭДО + ", Организация, ТекущийТитул, ТекущийШаг");
	
		ТекущийТитул = РеквизитыДокумента.ТекущийТитул;
		
		Если Не ЗначениеЗаполнено(ТекущийТитул) Тогда
			ТекущийТитул = СоответствиеТитуловИШагов.Получить(РеквизитыДокумента.ТекущийШаг);
		КонецЕсли;		
		
		ЗаголовокПользователяМП = "Водитель";
		ИмяТаблицыПользователейМП = "ТитулГрузоотправителяВодители";
		ИмяТаблицыТелефоныПользователейМП = "ТитулГрузоотправителяВодительНомераТелефонов";
		ТитулСПользователямиМП = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул1");
		ИндексАтомарнойРоли = СервисВзаимодействияМПЭПДКлиентСервер.ИндексАтомарнойРолиВодителя();
		
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист") Тогда
		РеквизитыДокумента = ОбменСГИСЭПДВызовСервера.ЗначенияРеквизитовОбъекта(Документ, 
			"ИдентификаторОформителя, ИдентификаторМедорганизации, ИдентификаторТехконтроль, ИдентификаторПоказанияОдометра, Организация, ТекущийТитул, ТекущийШаг");
		
		ТекущийТитул = РеквизитыДокумента.ТекущийТитул;
		ИндексАтомарнойРоли = Неопределено;
		
		Если Не ЗначениеЗаполнено(ТекущийТитул) Тогда
			ТекущийТитул = СоответствиеТитуловИШагов.Получить(РеквизитыДокумента.ТекущийШаг);
		КонецЕсли;		
		
		Если Не ЗначениеЗаполнено(ТекущийТитул) Тогда
			ИндексАтомарнойРоли = СервисВзаимодействияМПЭПДКлиентСервер.ИндексАтомарнойРолиВодителя();
			ИмяРеквизитаИдентификаторЭДО = "ИдентификаторОформителя";
			ИмяТаблицыПользователейМП = "ТитулОформлениеВодители";
			ТитулСПользователямиМП = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул1");
			
		ИначеЕсли ИндексРабот = СервисВзаимодействияМПЭПДКлиентСервер.ИндексРаботыВодитель()
			Или (ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул1")
				И ИндексРабот = Неопределено) Тогда
			ИндексАтомарнойРоли = СервисВзаимодействияМПЭПДКлиентСервер.ИндексАтомарнойРолиВодителя();
			ИмяРеквизитаИдентификаторЭДО = "ИдентификаторОформителя";
			ИмяТаблицыПользователейМП = "ТитулОформлениеВодители";
			ТитулСПользователямиМП = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул1");
			
		ИначеЕсли ИндексРабот = СервисВзаимодействияМПЭПДКлиентСервер.ИндексРаботыМедработникДоРейса()
			Или (ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2")
				И ИндексРабот = Неопределено) Тогда
			ИндексАтомарнойРоли = СервисВзаимодействияМПЭПДКлиентСервер.ИндексАтомарнойРолиМедработника();
			ИмяРеквизитаИдентификаторЭДО = "ИдентификаторМедорганизации";
			
		ИначеЕсли ИндексРабот = СервисВзаимодействияМПЭПДКлиентСервер.ИндексРаботыМедработникПослеРейса()
			Или (ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6")
				И ИндексРабот = Неопределено) Тогда
			ИндексАтомарнойРоли = СервисВзаимодействияМПЭПДКлиентСервер.ИндексАтомарнойРолиМедработника();
			ИмяРеквизитаИдентификаторЭДО = "ИдентификаторМедорганизации";
			
		ИначеЕсли ИндексРабот = СервисВзаимодействияМПЭПДКлиентСервер.ИндексРаботыМеханик()
			Или (ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3")
				И ИндексРабот = Неопределено) Тогда
			ИндексАтомарнойРоли = СервисВзаимодействияМПЭПДКлиентСервер.ИндексАтомарнойРолиМеханика();
			ИмяРеквизитаИдентификаторЭДО = "ИдентификаторТехконтроль";
			
		ИначеЕсли ИндексРабот = СервисВзаимодействияМПЭПДКлиентСервер.ИндексРаботыПоказанияОдометраДоРейса()
			Или (ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4")
				И ИндексРабот = Неопределено) Тогда
			ИндексАтомарнойРоли = СервисВзаимодействияМПЭПДКлиентСервер.ИндексАтомарнойРолиОтветственногоЗаОдометр();
			ИмяРеквизитаИдентификаторЭДО = "ИдентификаторПоказанияОдометра";
			
		ИначеЕсли ИндексРабот = СервисВзаимодействияМПЭПДКлиентСервер.ИндексРаботыПоказанияОдометраПослеРейса()
			Или (ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5")
				И ИндексРабот = Неопределено) Тогда
			ИндексАтомарнойРоли = СервисВзаимодействияМПЭПДКлиентСервер.ИндексАтомарнойРолиОтветственногоЗаОдометр();
			ИмяРеквизитаИдентификаторЭДО = "ИдентификаторПоказанияОдометра";
			
		КонецЕсли;
		
		Если ИндексАтомарнойРоли <> Неопределено Тогда
			АтомарныеРоли = СервисВзаимодействияМПЭПДКлиентСервер.АтомарныеРолиМП();
			ЗаголовокПользователяМП = АтомарныеРоли[ИндексАтомарнойРоли];
		КонецЕсли;
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Документ не поддерживает обмен с мобильным приложением';
										|en = 'The document does not support exchange with the mobile application'"));
		Возврат Неопределено;
	КонецЕсли;
	
	Если ИмяРеквизитаИдентификаторЭДО = Неопределено Тогда
		ИдентификаторЭДО = Неопределено;
	Иначе
		ИдентификаторЭДО = РеквизитыДокумента[ИмяРеквизитаИдентификаторЭДО];
	КонецЕсли;
	
	МассивСтруктурКлючевхПолейПользователей = Неопределено;
	Если ТитулСПользователямиМП <> Неопределено Тогда
		СтруктураРеквизитовТитула = ОбменСГИСЭПДВызовСервера.ДанныеРеквизитовЭПД(Документ, ТитулСПользователямиМП);
		
		Если СтруктураРеквизитовТитула.Свойство(ИмяТаблицыПользователейМП, МассивСтруктурКлючевхПолейПользователей) Тогда
			Если ЗначениеЗаполнено(ИмяТаблицыТелефоныПользователейМП) Тогда
				ТелефоныПользователейМП = СтруктураРеквизитовТитула[ИмяТаблицыТелефоныПользователейМП];
				Для Каждого СтрокаТелефон Из ТелефоныПользователейМП Цикл
					Для Каждого СтрокаПользователь Из МассивСтруктурКлючевхПолейПользователей Цикл
						Если СтрокаПользователь.ИдентификаторСтроки = СтрокаТелефон.ИдентификаторСтрокиРодителя
						И СтрокаПользователь.Свойство("НомерТелефона") = Ложь Тогда
							СтрокаПользователь.Вставить("НомерТелефона", СтрокаТелефон.Телефон);	
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если МассивСтруктурКлючевхПолейПользователей = Неопределено Тогда
		МассивСтруктурКлючевхПолейПользователей = Новый Массив;
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
		ДоступенОтказ = ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул2");
	КонецЕсли;
	
	Роль = Pow(2, ИндексАтомарнойРоли);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИдентификаторЭДО", ИдентификаторЭДО);
	ПараметрыФормы.Вставить("Организация", РеквизитыДокумента.Организация);
	ПараметрыФормы.Вставить("Документ", Документ);
	ПараметрыФормы.Вставить("Титул", ТекущийТитул);
	ПараметрыФормы.Вставить("МассивСтруктурКлючевхПолейПользователей", МассивСтруктурКлючевхПолейПользователей);
	ПараметрыФормы.Вставить("Роль", Роль);
	ПараметрыФормы.Вставить("ИдентификаторЗаписиМП", ИдентификаторЗаписиМП);
	ПараметрыФормы.Вставить("ЗаголовокПользователяМП", ЗаголовокПользователяМП);
	ПараметрыФормы.Вставить("ДоступенОтказ", ДоступенОтказ);
	ПараметрыФормы.Вставить("ИндексРабот", ИндексРабот);
	
	Возврат ПараметрыФормы;
	
КонецФункции

#КонецОбласти

#Область ЗапускДополнительнойФункциональности

Процедура ОткрытьФормуСпискаМП(ВладелецФормы, Организация = Неопределено) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Организация", Организация);
	
	ОткрытьФорму("Обработка.ВзаимодействиеМПЭПД.Форма.СостояниеПодключенияМП", ПараметрыФормы, ВладелецФормы);
	
КонецПроцедуры

Процедура ОткрытьФормуПользователяМобильногоУстройства(ПараметрыФормы, ВладелецФормы = Неопределено, ОповещениеЗавершения = Неопределено) Экспорт
		
	Если ПараметрыФормы = Неопределено Тогда
		ПараметрыФормы = Новый Структура;
	КонецЕсли;
		
	Идентификатор = Неопределено;
	Если Не ПараметрыФормы.Свойство("Идентификатор", Идентификатор) Тогда
		Идентификатор = СокрЛП(Новый УникальныйИдентификатор());
		ПараметрыФормы.Вставить("Идентификатор", Идентификатор);
	КонецЕсли;	
	Уникальность = Идентификатор;
	
	ОткрытьФорму("Обработка.ВзаимодействиеМПЭПД.Форма.ПодключениеПользователя", ПараметрыФормы,
		ВладелецФормы, Уникальность, , , ОповещениеЗавершения, РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

Процедура ОткрытьФормуСуществующегоПользователяМобильногоУстройства(ПараметрыФормы, ВладелецФормы = Неопределено, ОповещениеЗавершения = Неопределено) Экспорт
		
	Идентификатор = Неопределено;
	ПараметрыФормы.Свойство("Идентификатор", Идентификатор);
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		ТекстПредупреждения = НСтр("ru = 'Устройство не выбрано';
									|en = 'Device is not selected'");
		ПоказатьПредупреждение(ОповещениеЗавершения, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
		
	ОткрытьФормуПользователяМобильногоУстройства(ПараметрыФормы, ВладелецФормы, ОповещениеЗавершения);
	
КонецПроцедуры

Процедура ОткрытьФормуПодборУчетныхЗаписейЭДО(ВладелецФормы, ОповещениеЗавершения, Организация = Неопределено) Экспорт

	ПараметрыФормы = Новый Структура("Организация", Организация);
	ОткрытьФорму("Обработка.ВзаимодействиеМПЭПД.Форма.ВыборУчетныхЗаписейЭДО",
		ПараметрыФормы, ВладелецФормы, , , , ОповещениеЗавершения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ОткрытьФормуВыбораМП(ВладелецФормы, ПараметрыФормы, ОповещениеЗавершения = Неопределено) Экспорт
	
	ОткрытьФорму("Обработка.ВзаимодействиеМПЭПД.Форма.ВыборПользователя", ПараметрыФормы, ВладелецФормы, , , , ОповещениеЗавершения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Открывает форму настроект МП для форм документов.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма документа, из которой вызываются настройки.
//
Процедура ОткрытьФормуНастроекМПДляФорм(Форма) Экспорт
	
	НеобходимаЗаписьДокумента = НеобходимаЗаписьДокументаПередОткрытиемФормыНастроекМП(Форма);
	
	Если НеобходимаЗаписьДокумента Тогда
		ТекстВопроса = НСтр("ru = 'Перед выбором настроек необходимо записать документ. Записать?';
							|en = 'Before selecting the settings, save the document. Save?'");
		ЗаголовокВопроса = НСтр("ru = 'Необходимо записать документ';
								|en = 'You need to save the document'");
		
		ДополнительныеПараметрыОткрытия = Новый Структура;
		ДополнительныеПараметрыОткрытия.Вставить("Форма", Форма);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОткрытьФормуНастроекМПДляФормПослеВопросаСлужебная",
			ЭтотОбъект, ДополнительныеПараметрыОткрытия);
		
		ПоказатьВопрос(ОповещениеОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена, ,
			КодВозвратаДиалога.Отмена, ЗаголовокВопроса);
	Иначе
		ОткрытьФормуНастроекМПДляФормПослеСохраненияСлужебная(Форма);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормуНастроекМПДляФормПослеВопросаСлужебная(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Форма = ДополнительныеПараметры.Форма;
		Форма.Записать();
		
		НеобходимаЗаписьДокумента = НеобходимаЗаписьДокументаПередОткрытиемФормыНастроекМП(Форма);
		Если НеобходимаЗаписьДокумента Тогда
			Возврат;
		КонецЕсли;
		
		ОткрытьФормуНастроекМПДляФормПослеСохраненияСлужебная(Форма);
	КонецЕсли;
	
КонецПроцедуры

Процедура ФормаНастроекМПДляФормЗакрытиеСлужебная(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Истина Тогда
		Форма = ДополнительныеПараметры.Форма;
		НастройкиОтображенияИнформацииПоМП = СервисВзаимодействияМПЭПДКлиентСервер.НастройкиОтображенияИнформацииПоМП(
			Форма, Форма.Объект.Ссылка, Форма.Объект.РольУчастника);
		СервисВзаимодействияМПЭПДКлиентСервер.УстановитьОформлениеМобильныхПриложений(Форма,
			НастройкиОтображенияИнформацииПоМП);
	КонецЕсли;

КонецПроцедуры

Процедура УдалитьЗаписьПодключенныхМПЭПД(ИдентификаторЭДО, Идентификатор) Экспорт
	
	СервисВзаимодействияМПЭПДВызовСервера.УдалитьЗаписьПодключенныхМПЭПД(ИдентификаторЭДО, Идентификатор);
	
КонецПроцедуры

Процедура ОткрытьФормуВыбораВидаПодписи(ЭлементФормыВидПодписи, ВидПодписи, Уникальность) Экспорт
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидПодписи", ВидПодписи);
	
	ОткрытьФорму("Перечисление.ВидыПодписиМПЭПД.Форма.ФормаВыбора", ПараметрыОткрытия, ЭлементФормыВидПодписи
		, Уникальность,	, , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

Процедура ОткрытьФормуВыбораСхемыОбмена(ЭлементФормыВидПодписи, ПрямойОбмен, Знач СтруктураДанныхДоверенности, Уникальность) Экспорт
	
	ПараметрыОткрытия = СтруктураДанныхДоверенности;
	ПараметрыОткрытия.Вставить("ПрямойОбмен", ПрямойОбмен);
	
	ОткрытьФорму("Обработка.ВзаимодействиеМПЭПД.Форма.ВыборСхемыОбмена", ПараметрыОткрытия, ЭлементФормыВидПодписи
		, Уникальность,	, , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

#КонецОбласти

#Область ПредопределенныеЗначения

// Имя оповещения об обновлении списка мобильных устройств.
// 
// Возвращаемое значение:
//  Строка - Имя оповещения об обновлении списка мобильных устройств
Функция ИмяОповещенияОбОбновленииСпискаМобильныхУстройств() Экспорт
	
	Возврат "ВзаимодействиеМПЭПД.ОповещениеОбОбновленииСпискаМобильныхУстройств1СЭПД";
	
КонецФункции

// Имя оповещения об изменении устройства.
// 
// Возвращаемое значение:
//  Строка - Имя оповещения об изменении устройства
Функция ИмяОповещенияОбИзмененииУстройства() Экспорт
	
	Возврат "ВзаимодействиеМПЭПД.ОповещениеОбИзмененииУстройства";
	
КонецФункции

// Имя оповещения о завершении регистрации.
// 
// Возвращаемое значение:
//  Строка - Имя оповещения о завершении регистрации
Функция ИмяОповещенияОЗавершенииРегистрации() Экспорт
	
	Возврат "ВзаимодействиеМПЭПД.ОповещениеОЗавершенииРегистрации";
	
КонецФункции

// Имя оповещения об изменении профиля пользователя МП.
// 
// Возвращаемое значение:
//  Строка - Имя оповещения об изменении профиля пользователя МП
Функция ИмяОповещенияОбИзмененииПрофиляПользователяМП() Экспорт
	
	Возврат "ВзаимодействиеМПЭПД.ОповещениеОбИзмененииПрофиляПользователяМП";
	
КонецФункции

// Ключевые поля контроля повторений регистра подключенных МПЭПД.
// 
// Возвращаемое значение:
//  Структура - Ключевые поля контроля повторений регистра подключенных МПЭПД:
// * СНИЛС - Строка
// * ИНН - Строка
//
//@skip-check constructor-function-return-section
Функция КлючевыеПоляКонтроляПовторенийРегистраПодключенныхМПЭПД() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("СНИЛС");
	Структура.Вставить("ИНН");
	
	Возврат Структура;
	
КонецФункции

// Структура полей регистра подключенных МПЭПД.
// 
// Возвращаемое значение:
//  Структура - Структура полей регистра подключенных МПЭПД:
// * Найден - Булево
// * Идентификатор - Строка
// * ИдентификаторЭДО - Строка
// * ИдентификаторМП - Строка
// * ПрямойОбмен - Булево
// * ИНН - Строка
// * СНИЛС - Строка 
// * ФизическоеЛицо - ОпределяемыйТип.ФизическоеЛицо
// * Фамилия- Строка 
// * Имя - Строка
// * Отчество - Строка
// * Состояние - ПеречислениеСсылка.СостояниеМПЭПД
// * ВидПодписи - ПеречислениеСсылка.ВидыПодписиМПЭПД
// * ВидыДокументов - Число
// * ЕстьБумажнаяДоверенность - Булево 
// * БумажнаяДоверенностьНомер - Строка
// * БумажнаяДоверенностьДата - Дата
// * МЧД - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
// * КодРегистрации - Строка 
// * ДатаВремяПолученияКодаРегистрации - Дата 
// * АдресФото - Строка
// * Наименование - Строка
// * Роль - Число
// * Телефоны - Строка
// * СерияВУ - Строка
// * НомерВУ - Строка
// * ДатаВыдачиВУ - Дата
// * ЛичныеДокументыДляОтправкиВМП - ХранилищеЗначения
//
//@skip-check constructor-function-return-section 
Функция СтруктураПолейРегистраПодключенныхМПЭПД() Экспорт
	
	Структура = Новый Структура();
	Структура.Вставить("Найден");
	
	Структура.Вставить("Идентификатор");
	Структура.Вставить("ИдентификаторЭДО");
	Структура.Вставить("ИдентификаторМП");
	Структура.Вставить("ПрямойОбмен");
	Структура.Вставить("ИНН");
	Структура.Вставить("СНИЛС");
	Структура.Вставить("ФизическоеЛицо");
	Структура.Вставить("Фамилия");
	Структура.Вставить("Имя");
	Структура.Вставить("Отчество");
	Структура.Вставить("Состояние");
	
	Структура.Вставить("ВидПодписи");
	Структура.Вставить("ВидыДокументов");
	
	Структура.Вставить("ЕстьБумажнаяДоверенность");
	Структура.Вставить("БумажнаяДоверенностьНомер");
	Структура.Вставить("БумажнаяДоверенностьДата");
	Структура.Вставить("МЧД");
	
	Структура.Вставить("КодРегистрации");
	Структура.Вставить("ДатаВремяПолученияКодаРегистрации");
	
	Структура.Вставить("АдресФото"); // Фото в регистре
	Структура.Вставить("Наименование");
	Структура.Вставить("Роль");
	Структура.Вставить("Телефоны");
	
	Структура.Вставить("СерияВУ");
	Структура.Вставить("НомерВУ");
	Структура.Вставить("ДатаВыдачиВУ");
	
	Структура.Вставить("ЛичныеДокументыДляОтправкиВМП");
	
	Возврат Структура;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Создает массив и помещает в него переданные значения.
//
// Параметры:
//  Значение1 - Произвольный - любое значение.
//  Значение2 - Произвольный
//  Значение3 - Произвольный
//  Значение4 - Произвольный
//
// Возвращаемое значение:
//  Массив из Произвольный
//  
// Пример:
//   Результат = ОбщегоНазначенияКлиентСервер.МассивЗначений(1, 2, 3);
//
//@skip-check doc-comment-collection-item-type
//@skip-check doc-comment-description-ends-on-dot
Функция МассивИзЗначений(Знач Значение1, Знач Значение2 = Неопределено, Знач Значение3 = Неопределено, 
	Знач Значение4 = Неопределено) Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить(Значение1);
	Если Значение2 <> Неопределено Тогда
		Результат.Добавить(Значение2);
	КонецЕсли;
	Если Значение3 <> Неопределено Тогда
		Результат.Добавить(Значение3);
	КонецЕсли;
	Если Значение4 <> Неопределено Тогда
		Результат.Добавить(Значение4);
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

Процедура ОткрытьФормуНастроекМПДляФормПослеСохраненияСлужебная(Форма)
	
	ДополнительныеПараметрыОткрытия = Новый Структура;
	ДополнительныеПараметрыОткрытия.Вставить("Форма", Форма);
	ОповещениеЗавершения = Новый ОписаниеОповещения("ФормаНастроекМПДляФормЗакрытиеСлужебная",
		СервисВзаимодействияМПЭПДКлиент, ДополнительныеПараметрыОткрытия);
		
	РольУчастника = Форма.Объект.РольУчастника;
	Ссылка = Форма.Объект.Ссылка;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист") Тогда
		// Оформитель
		Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 1) Тогда
			Организация = Форма.Объект.СсылкаТитулОформлениеОформитель;
			ИдентификаторЭДО = Форма.Объект.ИдентификаторОформителя;
			
		// Медосмотр
		ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 2) Тогда
			Организация = Форма.Объект.СсылкаТитулОформлениеМедорганизация;
			ИдентификаторЭДО = Форма.Объект.ИдентификаторМедорганизации;
		
		// Техконтроль
		ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 4) Тогда
			Организация = Форма.Объект.СсылкаТитулОформлениеТехконтроль;
			ИдентификаторЭДО = Форма.Объект.ИдентификаторТехконтроль;
		
		// Показания одометра
		ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 8) Тогда
			Организация = Форма.Объект.СсылкаТитулОформлениеПоказанияОдометра;
			ИдентификаторЭДО = Форма.Объект.ИдентификаторПоказанияОдометра;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
		// Перевозчик
		Если РольУчастника = 1 Тогда
			Организация = Форма.Объект.СсылкаТитулГрузоотправителяПеревозчик;
			ИдентификаторЭДО = Форма.Объект.ИдентификаторПеревозчика;
		КонецЕсли;
		
	Иначе
		Возврат;
	КонецЕсли;
		
	ПараметрыФормы = Новый Структура("ИдентификаторЭДО", ИдентификаторЭДО);
	ПараметрыФормы = Новый Структура("Организация", Организация);
	ОткрытьФорму("Обработка.ВзаимодействиеМПЭПД.Форма.НастройкиМПНаФормахДокументов",
		ПараметрыФормы, Форма, , , , ОповещениеЗавершения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Необходимые данные с сервера для регистрации учетной записи.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
// 
// Возвращаемое значение:
//  Структура - Необходимые данные для регистрации учетной записи:
// * УчетнаяЗаписьПодключена - Булево
// * НужноВвестиДанныеИнтернетПоддержки - Булево
//
//@skip-check constructor-function-return-section
Функция НеобходимыеДанныеССервераДляРегистрацииУчетнойЗаписи(ИдентификаторЭДО)
	
	// см. СервисВзаимодействияМПЭПД.НоваяСтруктураНеобходимыхДанныхДляРегистрацииУчетнойЗаписи
	Структура = СервисВзаимодействияМПЭПДВызовСервера.НеобходимыеДанныеДляРегистрацииУчетнойЗаписи(ИдентификаторЭДО);
	
	Если Не Структура.УчетнаяЗаписьПодключена
		И Структура.НужноВвестиДанныеИнтернетПоддержки Тогда
		
		ДоступенВводДанныхИнтернетПоддержки =
			ИнтернетПоддержкаПользователейКлиент.ДоступноПодключениеИнтернетПоддержки();
		Структура.Вставить("ДоступенВводДанныхИнтернетПоддержки", ДоступенВводДанныхИнтернетПоддержки);
	Иначе
		Структура.Вставить("ДоступенВводДанныхИнтернетПоддержки", Истина);
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции

Функция ПодготовитьКонтекстСценария(ИмяПервогоЭтапа, ОповещениеОЗавершении, УникальныйИдентификаторФормы)
	
	КонтекстСценария = Новый Структура;
	КонтекстСценария.Вставить("Этап", ИмяПервогоЭтапа);
	КонтекстСценария.Вставить("ОповещениеОЗавершении", ОповещениеОЗавершении);
	КонтекстСценария.Вставить("УникальныйИдентификаторФормы", УникальныйИдентификаторФормы);
	
	Возврат КонтекстСценария;
	
КонецФункции

Функция НовыйРезультатЗавершенияСценария()
	
	Структура = Новый Структура;
	Структура.Вставить("Успешность");
	Структура.Вставить("ДополнительныеПараметры");
	
	Возврат Структура;
	
КонецФункции

Процедура ВыполнитьОбработкуОповещенияОЗавершенииСценария(КонтекстСценария, Успешность, ДополнительныеПараметры = Неопределено)
	
	Если КонтекстСценария.ОповещениеОЗавершении <> Неопределено Тогда
		СтруктураРезультата = НовыйРезультатЗавершенияСценария(); 
		СтруктураРезультата.Успешность = Успешность;
		СтруктураРезультата.ДополнительныеПараметры = ДополнительныеПараметры;
		ВыполнитьОбработкуОповещения(КонтекстСценария.ОповещениеОЗавершении, СтруктураРезультата);
	КонецЕсли;
	
КонецПроцедуры

Функция НеобходимаЗаписьДокументаПередОткрытиемФормыНастроекМП(Форма)
	
	РольУчастника = Форма.Объект.РольУчастника;
	Ссылка = Форма.Объект.Ссылка;
	
	НеобходимаЗаписьДокумента = Ложь;	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист") Тогда
		// Оформитель
		Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 1) Тогда
			Организация = Форма.Объект.СсылкаТитулОформлениеОформитель;
			ИдентификаторЭДО = Форма.Объект.ИдентификаторОформителя;
			
		// Медосмотр
		ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 2) Тогда
			Организация = Форма.Объект.СсылкаТитулОформлениеМедорганизация;
			ИдентификаторЭДО = Форма.Объект.ИдентификаторМедорганизации;
		
		// Техконтроль
		ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 4) Тогда
			Организация = Форма.Объект.СсылкаТитулОформлениеТехконтроль;
			ИдентификаторЭДО = Форма.Объект.ИдентификаторТехконтроль;
		
		// Показания одометра
		ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 8) Тогда
			Организация = Форма.Объект.СсылкаТитулОформлениеПоказанияОдометра;
			ИдентификаторЭДО = Форма.Объект.ИдентификаторПоказанияОдометра;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
		// Перевозчик
		Если РольУчастника = 1 Тогда
			Организация = Форма.Объект.СсылкаТитулГрузоотправителяПеревозчик;
			ИдентификаторЭДО = Форма.Объект.ИдентификаторПеревозчика;
		КонецЕсли;
		
	Иначе
		Возврат НеобходимаЗаписьДокумента;
	КонецЕсли;
	
	НеобходимаЗаписьДокумента = Не ЗначениеЗаполнено(Организация) И Не ЗначениеЗаполнено(ИдентификаторЭДО);
	Возврат НеобходимаЗаписьДокумента;
	
КонецФункции

#КонецОбласти
