///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

#Область ТокенРутокен
// Обрабатывает результат чтения сертификатов с токена
//  Параметры:
//   Результат - Соответствие из КлючИЗначение
//   	* Ключ -  Строка - Отпечаток base64 в строке;
//   	* Значение - ДвоичныеДанные - Данные сертификата
//   ПараметрыОперации - Структура - Структура с ключами:
//    * Форма- ФормаКлиентскогоПриложения, Неопределено - Форма-Источник
//    * ОписаниеДанных - Структура, Неопределено - Структура с описанием данных
//    	см.ЭлектроннаяПодписьКлиент.Подписать():
//     ** НаборДанных - Структура:
//     *** Данные - Структура
//    * ПараметрыКомпоненты - Структура - Структура из:
//     ** ОбъектКомпоненты - ОбъектВнешнейКомпоненты, Неопределено - Объект компоненты
//     ** СвойстваСертификатов - Структура, Неопределено - Свойства сертификатов
//     ** СвойстваВыбранногоСертификата - Структура, Неопределено - Свойства выбранного сертификата
//     ** СоответствиеОтпечатков - Соответствие, Неопределено - Соответствия отпечатков
//     ** СодержимоеКонтейнераПрочитано - Булево
//     ** ПодписаниеЧерезФайловоеХранилище - Булево
//     ** ОписаниеОповещенияПослеОперации - ОписаниеОповещения, Неопределено - Оповещение
//     ** ИдентификаторТекущейОперации - Строка
//     ** ТекущаяОперация - ПеречислениеСсылка.ОперацииРутокенМобильногоЭДО, Неопределено - Текущая операция
//
//    * Операция - ПеречислениеСсылка.ОперацииРутокенМобильногоЭДО
//    * ОбработкаОшибки - ОписаниеОповещения:
//
Асинх Процедура ОбработатьРезультатЧтенияСертификатов(Результат, ПараметрыОперации) Экспорт

	Форма = ПараметрыОперации.Форма;
	Если Результат = Неопределено Тогда
		Ошибка = НовыйОшибкаРутокен();
		Ошибка.Форма = Форма;
		Ошибка.Описание = НСтр("ru = 'Прочитать содержимое контейнера не удалось';
								|en = 'Cannot read the contents of the container'");

		ВыполнитьОбработкуОповещения(ПараметрыОперации.ОбработкаОшибки, Ошибка);
		Возврат;
	КонецЕсли;
	
	ПараметрыКомпоненты = ПараметрыОперации.ПараметрыКомпоненты;
	ОписаниеДанных = ПараметрыОперации.ОписаниеДанных;
	
	СоответствиеОтпечатков = Новый Соответствие;
	
	Элементы = Форма.Элементы;
	ОтпечаткиВКонтейнере = Новый Массив;
	Для Каждого КлючИЗначение Из Результат Цикл
		//@skip-check invocation-parameter-type-intersect
		СертификатКриптографии = Новый СертификатКриптографии(Base64Значение(КлючИЗначение.Значение));
		ОтпечатокBase64 = Base64Строка(СертификатКриптографии.Отпечаток);
		//@skip-check typed-value-adding-to-untyped-collection
		ОтпечаткиВКонтейнере.Добавить(ОтпечатокBase64);
		
		СоответствиеОтпечатков.Вставить(ОтпечатокBase64, КлючИЗначение.Ключ);
	КонецЦикла;
	
	ПараметрыКомпоненты.СоответствиеОтпечатков = СоответствиеОтпечатков; 
	
	НаборДанных = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеДанных, "НаборДанных");
	
	ОтборОрганизаций = Новый Массив;
	Для Каждого Данные Из НаборДанных Цикл
		//@skip-check property-return-type
		Если ОтборОрганизаций.Найти(Данные.Организация) = Неопределено Тогда
			//@skip-check typed-value-adding-to-untyped-collection
			ОтборОрганизаций.Добавить(Данные.Организация);
		КонецЕсли;
	КонецЦикла;
	
	СвойстваСертификатовВКонтейнере = ИнтеграцияБСПБЭДВызовСервера.СвойстваСертификатовОрганизацийПоОтпечаткам(
		ОтпечаткиВКонтейнере,
		ОтборОрганизаций);
		
	ПараметрыКомпоненты.СвойстваСертификатов = СвойстваСертификатовВКонтейнере;
	
	ВКонтейнереОдинСертификат = СвойстваСертификатовВКонтейнере.Количество() = 1;
	
	Для Каждого КлючИЗначение Из СвойстваСертификатовВКонтейнере Цикл
		
		//@skip-check property-return-type
		//@skip-check dynamic-access-method-not-found
		Элементы.СертификатРутокен.СписокВыбора.Добавить(КлючИЗначение.Ключ);
		
		Если ВКонтейнереОдинСертификат Тогда
			//@skip-check property-return-type
			Форма.Сертификат = КлючИЗначение.Ключ;
			ЗаполнитьСвойстваПоСертификату(Форма, ПараметрыКомпоненты);
			//@skip-check invocation-parameter-type-intersect
			//@skip-check property-return-type
			ЗаполнитьПоДаннымБиометрии(Форма);
		КонецЕсли;

	КонецЦикла;

	ПараметрыКомпоненты.СодержимоеКонтейнераПрочитано = Истина;

КонецПроцедуры

//  Обрабатывает результат операции "Подписание"
//  Параметры:
//   Результат - Соответствие из КлючИЗначение
//   	* Ключ -  Строка - Отпечаток base64 в строке;
//   	* Значение - ДвоичныеДанные - Данные сертификата
//   ПараметрыОперации - Структура - Структура с ключами:
//    * Форма- ФормаКлиентскогоПриложения, Неопределено - Форма-Источник
//    * ОписаниеДанных - Структура, Неопределено - Структура с описанием данных
//    * ПараметрыКомпоненты - Структура - Структура из:
//     ** ОбъектКомпоненты - ОбъектВнешнейКомпоненты, Неопределено - Объект компоненты
//     ** СвойстваСертификатов - Структура, Неопределено - Свойства сертификатов
//     ** СвойстваВыбранногоСертификата - Структура, Неопределено - Свойства выбранного сертификата
//     ** СоответствиеОтпечатков - Соответствие, Неопределено - Соответствия отпечатков
//     ** СодержимоеКонтейнераПрочитано - Булево
//     ** ПодписаниеЧерезФайловоеХранилище - Булево
//     ** ОписаниеОповещенияПослеОперации - ОписаниеОповещения, Неопределено - Оповещение
//     ** ИдентификаторТекущейОперации - Строка
//     ** ТекущаяОперация - ПеречислениеСсылка.ОперацииРутокенМобильногоЭДО, Неопределено - Текущая операция
//
//    * Операция - ПеречислениеСсылка.ОперацииРутокенМобильногоЭДО
//    * ОбработкаОшибки - ОписаниеОповещения
//
Асинх Процедура ОбработатьРезультатПодписанияДанных(Результат, ПараметрыОперации) Экспорт

	Форма = ПараметрыОперации.Форма;
	Ошибка = НовыйОшибкаРутокен();
	Ошибка.Форма = Форма;
	
	Если Результат = Неопределено Тогда

		Ошибка.Описание = НСтр("ru = 'Не удалось подписать данные набора';
								|en = 'Cannot sign the set data'");
		ВыполнитьОбработкуОповещения(ПараметрыОперации.ОбработкаОшибки, Ошибка);
		Возврат;

	КонецЕсли;
	
	ПараметрыКомпоненты = ПараметрыОперации.ПараметрыКомпоненты;
	ОписаниеДанных = ПараметрыОперации.ОписаниеДанных;

	НаборДанных = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеДанных, "НаборДанных");

	Если НаборДанных = Неопределено Тогда
		
		Ошибка.Описание = НСтр("ru = 'Нет данных для подписания';
								|en = 'There is no data for signing'");
		ВыполнитьОбработкуОповещения(ПараметрыОперации.ОбработкаОшибки, Ошибка);
		Возврат;
	КонецЕсли;
	
	КоличествоДанныхВНаборе = НаборДанных.Количество();
	
	ИндексТекущейСтроки = 0;
	
	Элементы = Форма.Элементы;
	
	Для Каждого Данные Из НаборДанных Цикл
			
		Если Данные.ИдентификаторОперацииПодписания <> ПараметрыКомпоненты.ИдентификаторТекущейОперации Тогда
			ИндексТекущейСтроки = ИндексТекущейСтроки + 1;
			Продолжить;
		КонецЕсли;
		
		Если ПараметрыКомпоненты.ПодписаниеЧерезФайловоеХранилище Тогда
			ДвоичныеДанныеПодписи = Новый ДвоичныеДанные(Результат);
		Иначе
			Если ТипЗнч(Результат) = Тип("Строка") Тогда
				ДвоичныеДанныеПодписи = Base64Значение(Результат);
			КонецЕсли;
		КонецЕсли;
		
		ЗаполнитьСвойстваПодписиУДанныхНабора(Данные,
			ПараметрыКомпоненты.СвойстваВыбранногоСертификата,
			ДвоичныеДанныеПодписи);
		
		ПредставлениеДанных = Данные.Представление.Представление;
		
		Если Форма.ПакетнаяОтправка Тогда
			ПредставлениеДанных = СтрШаблон("(%1/%2) %3",
			ИндексТекущейСтроки + 1,
			КоличествоДанныхВНаборе,
			ПредставлениеДанных);
		КонецЕсли;
		
		Элементы.ДекорацияТекстовоеСостояниеОперации.Заголовок = СтрШаблон("%1%2%3%4",
			Символы.ПС,
			ПредставлениеДанных,
			Символы.ПС,
			НСтр("ru = 'ПОДПИСАН';
				|en = 'SIGNED'"));
			
		Прервать;
			
	КонецЦикла;
	
	Если ПараметрыКомпоненты.ПодписаниеЧерезФайловоеХранилище Тогда
		
		ИмяКаталогаВременныхФайловУстройства = КаталогВременныхФайловУстройства();
		КаталогВременныхФайловУстройства = Новый Файл(ИмяКаталогаВременныхФайловУстройства);
		Если КаталогВременныхФайловУстройства.Существует() Тогда
			УдалитьФайлыАсинх(ИмяКаталогаВременныхФайловУстройства);
		КонецЕсли;
		
	КонецЕсли;

	ЕстьДанныеДляПодписания = Ложь;
	
	Для Каждого Данные Из НаборДанных Цикл
		СвойстваПодписи = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Данные, "СвойстваПодписи");
		Если СвойстваПодписи = Неопределено Тогда
			ЕстьДанныеДляПодписания = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьДанныеДляПодписания Тогда
		Элементы.Подписать.Доступность = Истина;
		Элементы.СтраницыПодписанияСертификатом.ТекущаяСтраница = Элементы.СтраницаВыборСертификата;
	Иначе
	
		Если ПараметрыКомпоненты.ОписаниеОповещенияПослеОперации <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ПараметрыКомпоненты.ОписаниеОповещенияПослеОперации, Новый Структура());
		КонецЕсли;

		Если Форма.Открыта() Тогда
			Форма.Закрыть()
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Обрабатывает результат взаимодействия с компонентой
// Параметры:
// Результат - Структура - Структура ошибки:
// 	* КодОшибки - Строка - контролируемый код ошибки
//  * Описание - Строка - описание ошибки
//  * Форма - ФормаКлиентскогоПриложения - Форма
//	ДополнительныеПараметры - Структура, Неопределено - дополнительные параметры ошибки
//
Асинх Процедура ОбработатьОшибкуОперацииРутокен(Результат, ДополнительныеПараметры) Экспорт

	ТекстСообщения = "";
	
	КодОшибки = Результат.КодОшибки;
	Форма = Результат.Форма;
	Если ЗначениеЗаполнено(КодОшибки) Тогда
		Если КодОшибки = "99" Тогда
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Закончилось время ожидания взаимодействия с устройством';
					|en = 'The timeout for interaction with the device expired'"));
		КонецЕсли;
	КонецЕсли;
	
	Если Форма <> Неопределено Тогда
		Элементы = Форма.Элементы;
		Если Элементы.Найти("СтраницыПодписанияРутокен") <> Неопределено Тогда
			
			//@skip-check property-return-type
			Если Элементы.СтраницыПодписанияРутокен.ТекущаяСтраница <> Элементы.СтраницаВыборСертификатаРутокен Тогда
				Элементы.СтраницыПодписанияРутокен.ТекущаяСтраница = Элементы.СтраницаВыборСертификатаРутокен;
			КонецЕсли;
			
			Если Не Элементы.Подписать.Доступность Тогда
				Элементы.Подписать.Доступность = Истина;
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;
	
	ОписаниеОшибки = Результат.Описание;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
			ТекстСообщения = СтрШаблон("%1%2%3", ТекстСообщения, Символы.ПС, ОписаниеОшибки);
		Иначе
			ТекстСообщения = ОписаниеОшибки;
		КонецЕсли;
	
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

//  Заполняет свойства формы и свойства параметров компоненты
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//  ПараметрыКомпоненты -Структура - Структура с ключами:
//   * ОбъектКомпоненты - ОбъектВнешнейКомпоненты, Неопределено - Объект компоненты
//   * СвойстваСертификатов - Структура, Неопределено - Свойства сертификатов
//   * СвойстваВыбранногоСертификата - Структура, Неопределено - Свойства выбранного сертификата
//   * СоответствиеОтпечатков - Соответствие из КлючИЗначение:
//    ** Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//    ** Значение - Структура:
//     *** Ссылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//     *** Отозван - Булево
//     *** Отпечаток - Строка
//     *** ДействителенДо - Дата - дата окончания действия сертификата
//     *** КемВыдан - Строка - удостоверяющий центр, который выдал сертификат
//     *** Наименование - Строка - пользовательское представление сертификата
//     *** Программа - Строка - Программа, требуемая для подписания и расшифровки документов
//     *** ДанныеСертификата - ДвоичныеДанные - данные файла сертификата
//     *** ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - в ключ помещается 
//        ссылка на переданный сертификат.
//     *** ПарольПолучен - Булево - содержит Ложь
//     *** ПарольПользователя - Неопределено
//     *** Комментарий - Строка - содержит пустую строку
//     *** СрокДействияВДнях - Число - количество дней, оставшееся до истечения срока действия сертификата
//     *** Организация - ОпределяемыйТип.Организация
//     *** Фамилия - Строка - фамилия владельца сертификата
//     *** Имя - Строка - имя владельца сертификата
//     *** Отчество - Строка - отчество владельца сертификата
//     *** Должность - Строка - должность владельца сертификата
//	   *** Пользователь - ОпределяемыйТип.Пользователь
//     *** КомуВыдан - Строка
//     *** Фирма - Строка
//   * СодержимоеКонтейнераПрочитано - Булево
//   * ПодписаниеЧерезФайловоеХранилище - Булево
//   * ОписаниеОповещенияПослеОперации - ОписаниеОповещения, Неопределено - Оповещение
//   * ИдентификаторТекущейОперации - Строка
//   * ТекущаяОперация - ПеречислениеСсылка.ОперацииРутокенМобильногоЭДО, Неопределено - Текущая операция
//
Процедура ЗаполнитьСвойстваПоСертификату(Форма, ПараметрыКомпоненты) Экспорт
	
	Если ПараметрыКомпоненты.СвойстваСертификатов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваВыбранногоСертификата = ПараметрыКомпоненты.СвойстваСертификатов.Получить(Форма.Сертификат);
	Если СвойстваВыбранногоСертификата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма.СертификатАдрес = ПоместитьВоВременноеХранилище(СвойстваВыбранногоСертификата.ДанныеСертификата,
		Форма.УникальныйИдентификатор);
		
	Форма.СертификатДействителенДо = СвойстваВыбранногоСертификата.ДействителенДо;
	Форма.СертификатОтозван = СвойстваВыбранногоСертификата.Отозван;
	Форма.СертификатОтпечаток = СвойстваВыбранногоСертификата.Отпечаток;
	
	ПараметрыКомпоненты.СвойстваВыбранногоСертификата = СвойстваВыбранногоСертификата;	
	
	Форма.Элементы.ГруппаОтозванРутокен.Видимость = Форма.СертификатОтозван;
	
КонецПроцедуры

// Возвращает строку - имя каталога временных файлов на устройстве
// Возвращаемое значение:
//  Строка - Имя каталога
//
Функция КаталогВременныхФайловУстройства() Экспорт

	ИмяКаталогаДокументов = КаталогДокументов();

	РазделительПути = ПолучитьРазделительПути();

	Если Не СтрЗаканчиваетсяНа(ИмяКаталогаДокументов, РазделительПути) Тогда
		ИмяКаталогаДокументов = ИмяКаталогаДокументов + РазделительПути;
	КонецЕсли;

	Возврат СтрШаблон("%1%2%3", ИмяКаталогаДокументов, "temp", РазделительПути);

КонецФункции

#КонецОбласти

#Область Биометрия

// Выполняет подключение отключение биометрии для
//  Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма 
//   ИмяЗащищаемогоРеквизита - Строка - Имя реквизита
//   ИмяРеквизитаПодключитьБиометрию  - Строка - Имя флажка с настройкой подключения биометрии
//
Асинх Процедура ЗаполнитьПоДаннымБиометрии(Форма,
	ИмяЗащищаемогоРеквизита = "Пароль",
	ИмяРеквизитаПодключитьБиометрию = "ПодключитьБиометрию") Экспорт
	
	ПодключитьБиометрию = Форма[ИмяРеквизитаПодключитьБиометрию];
	
	Если Не ПодключитьБиометрию Тогда
		Возврат;
	КонецЕсли;
	
	Ключ = СтрШаблон("%1_%2", Форма.ИмяФормы, ИмяЗащищаемогоРеквизита);

#Если МобильныйКлиент Тогда
	
	Если Не БезопасноеХранилище.ПоддерживаетсяЗащитаДоступа(
		СпособЗащитыДоступаБезопасногоХранилища.ТребуетсяДополнительнаяПроверкаПользователя) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Биометрическая защита не поддерживается';
														|en = 'Biometric protection is not supported'"));
		Форма[ИмяРеквизитаПодключитьБиометрию] = Ложь;
		Возврат;
	КонецЕсли;
	
	Если Не БезопасноеХранилище.СодержитКлюч(Ключ) Тогда
		Форма[ИмяРеквизитаПодключитьБиометрию] = Ложь;
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Форма[ИмяЗащищаемогоРеквизита]) Тогда
		ПарольВБезопасномХранилище = Ждать БезопасноеХранилище.ПолучитьДанныеАсинх(
			Ключ,
			НСтр("ru = 'Для заполнения пароля необходимо прочитать биометрические данные';
				|en = 'To fill in the password, read the biometric data'"));
			
		Если ПарольВБезопасномХранилище <> Неопределено Тогда
			Форма[ИмяЗащищаемогоРеквизита] = ПарольВБезопасномХранилище;
		КонецЕсли;
	КонецЕсли;

	Форма.Элементы[ИмяЗащищаемогоРеквизита].ТолькоПросмотр = ПодключитьБиометрию;
	
	Если ПодключитьБиометрию Тогда
		Форма.Элементы[ИмяЗащищаемогоРеквизита].РежимПароля = Истина;
		Форма.Элементы[ИмяЗащищаемогоРеквизита].КартинкаКнопкиВыбора = БиблиотекаКартинок.ВводимыеСимволыВидны;
	КонецЕсли;

#КонецЕсли

КонецПроцедуры

// Выполняет подключение отключение биометрии для
//  Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма 
//   ИмяЭлементаФормы - Строка - Имя элемента формы
//   ИмяЗащищаемогоРеквизита - Строка - Имя реквизита
//   ИмяРеквизитаПодключитьБиометрию  - Строка - Имя флажка с настройкой подключения биометрии
//
Асинх Процедура ПодключитьОтключитьБиометрию(Форма,
	ИмяЭлементаФормы = "Пароль",
	ИмяЗащищаемогоРеквизита = "Пароль",
	ИмяРеквизитаПодключитьБиометрию = "ПодключитьБиометрию") Экспорт

Ключ = СтрШаблон("%1_%2", Форма.ИмяФормы, ИмяЗащищаемогоРеквизита);

#Если МобильныйКлиент Тогда
	
	ПодключитьБиометрию = Форма[ИмяРеквизитаПодключитьБиометрию];
	
	Если ПодключитьБиометрию = Истина Тогда
		
		Если Не БезопасноеХранилище.ПоддерживаетсяЗащитаДоступа(
			СпособЗащитыДоступаБезопасногоХранилища.ТребуетсяДополнительнаяПроверкаПользователя) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Биометрическая защита не поддерживается';
															|en = 'Biometric protection is not supported'"));
			Форма[ИмяРеквизитаПодключитьБиометрию] = Ложь;
			Возврат;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Форма[ИмяЗащищаемогоРеквизита]) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Пароль не заполнен';
															|en = 'The password is not filled in'"));
			Форма[ИмяРеквизитаПодключитьБиометрию] = Ложь;
			Возврат;
		КонецЕсли;

		ДанныеПомещены = Ждать БезопасноеХранилище.ПоместитьДанныеАсинх(Ключ,
			Форма[ИмяЗащищаемогоРеквизита],
			СпособЗащитыДоступаБезопасногоХранилища.ТребуетсяДополнительнаяПроверкаПользователя,
			СпособДополнительнойПроверкиПользователя.БиометрическаяИлиВводПароля);

		Форма.Элементы[ИмяЭлементаФормы].РежимПароля = Истина;
		Форма.Элементы[ИмяЭлементаФормы].КартинкаКнопкиВыбора = БиблиотекаКартинок.ВводимыеСимволыВидны;

		Если ДанныеПомещены = Истина Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Биометрия подключена';
												|en = 'Biometrics is enabled'"));
		КонецЕсли;
		
	Иначе

		Если БезопасноеХранилище.СодержитКлюч(Ключ) Тогда
			ДанныеУдалены = Ждать БезопасноеХранилище.УдалитьДанныеАсинх(Ключ);
			Если ДанныеУдалены = Истина Тогда
				ПоказатьОповещениеПользователя(НСтр("ru = 'Биометрия отключена';
													|en = 'Biometrics is disabled'"));
			КонецЕсли;
		КонецЕсли;

	КонецЕсли;
	
	Форма.Элементы[ИмяЗащищаемогоРеквизита].ТолькоПросмотр = Форма[ИмяРеквизитаПодключитьБиометрию];

#КонецЕсли

КонецПроцедуры

// Конструктор для инициализации переменной "ПараметрыКомпоненты"
//  Возвращаемое значение:
//   Структура - Структура с ключами:
//    * ОбъектКомпоненты - ОбъектВнешнейКомпоненты, Неопределено - Объект компоненты
//    * СвойстваСертификатов - Структура, Неопределено - Свойства сертификатов
//    * СвойстваВыбранногоСертификата - Структура, Неопределено - Свойства выбранного сертификата
//    * СоответствиеОтпечатков - Соответствие из КлючИЗначение:
//     ** Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//     ** Значение - Структура:
//      *** Ссылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// 	    *** Отозван - Булево
//      *** Отпечаток - Строка
//      *** ДействителенДо - Дата - дата окончания действия сертификата
//      *** КемВыдан - Строка - удостоверяющий центр, который выдал сертификат
//      *** Наименование - Строка - пользовательское представление сертификата
//      *** Программа - Строка - Программа, требуемая для подписания и расшифровки документов
//      *** ДанныеСертификата - ДвоичныеДанные - данные файла сертификата
//      *** ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - в ключ помещается 
//         ссылка на переданный сертификат.
//      *** ПарольПолучен - Булево - содержит Ложь
//      *** ПарольПользователя - Неопределено
//      *** Комментарий - Строка - содержит пустую строку
//      *** СрокДействияВДнях - Число - количество дней, оставшееся до истечения срока действия сертификата
//      *** Организация - ОпределяемыйТип.Организация
//      *** Фамилия - Строка - фамилия владельца сертификата
//      *** Имя - Строка - имя владельца сертификата
//      *** Отчество - Строка - отчество владельца сертификата
//      *** Должность - Строка - должность владельца сертификата
//	    *** Пользователь - ОпределяемыйТип.Пользователь
//      *** КомуВыдан - Строка
//      *** Фирма - Строка
//    * СодержимоеКонтейнераПрочитано - Булево
//    * ПодписаниеЧерезФайловоеХранилище - Булево
//    * ОписаниеОповещенияПослеОперации - ОписаниеОповещения, Неопределено - Оповещение
//    * ИдентификаторТекущейОперации - Строка
//    * ТекущаяОперация - ПеречислениеСсылка.ОперацииРутокенМобильногоЭДО, Неопределено - Текущая операция
//
Функция НовыйПараметрыКомпонентыРутокен() Экспорт

	ПараметрыКомпоненты = Новый Структура();
	ПараметрыКомпоненты.Вставить("ОбъектКомпоненты", Неопределено);
	ПараметрыКомпоненты.Вставить("СвойстваСертификатов", Неопределено);
	ПараметрыКомпоненты.Вставить("СвойстваВыбранногоСертификата", Неопределено);
	ПараметрыКомпоненты.Вставить("СоответствиеОтпечатков", Неопределено);
	ПараметрыКомпоненты.Вставить("СодержимоеКонтейнераПрочитано", Ложь);
	ПараметрыКомпоненты.Вставить("ПодписаниеЧерезФайловоеХранилище", Ложь);
	ПараметрыКомпоненты.Вставить("ОписаниеОповещенияПослеОперации", Неопределено);
	ПараметрыКомпоненты.Вставить("ИдентификаторТекущейОперации", "");
	ПараметрыКомпоненты.Вставить("ТекущаяОперация", Неопределено);
	
	//@skip-check constructor-function-return-section
	Возврат ПараметрыКомпоненты;

КонецФункции

// Конструктор для инициализации параметров текущей операции
//  Возвращаемое значение:
//   Структура - Структура с ключами:
//    * Форма- ФормаКлиентскогоПриложения, Неопределено - Форма-Источник
//    * ОписаниеДанных - Структура, Неопределено - Структура с описанием данных
//    * ПараметрыКомпоненты - Структура - Параметры компоненты (см. НовыйПараметрыКомпонентыРутокен)
//    * Операция - ПеречислениеСсылка.ОперацииРутокенМобильногоЭДО
//    * ОбработкаОшибки - ОписаниеОповещения
//@skip-check function-return-types
Функция НовыйПараметрыОперацииРутокен() Экспорт

	ПараметрыОперации = Новый Структура();
	ПараметрыОперации.Вставить("Операция",
		ПредопределенноеЗначение("Перечисление.ОперацииРутокенМобильногоЭДО.ЧтениеСертификатов"));
	ПараметрыОперации.Вставить("Форма", Неопределено);
	ПараметрыОперации.Вставить("ОписаниеДанных", Неопределено);
	ПараметрыОперации.Вставить("ПараметрыКомпоненты", Новый Структура());
	
	ОбработкаОшибки = Новый ОписаниеОповещения("ОбработатьОшибкуОперацииРутокен",
		МобильныйЭДОСлужебныйКлиент);
	ПараметрыОперации.Вставить("ОбработкаОшибки", ОбработкаОшибки);

	Возврат ПараметрыОперации;

КонецФункции

// Конструктор для инициализации результата обработки операции Рутокен
//  Возвращаемое значение:
//   Структура - Структура с ключами:
//    * Форма - ФормаКлиентскогоПриложения, Неопределено - Форма
//    * КодОшибки - Строка
//    * Описание - Строка
//
Функция НовыйОшибкаРутокен() Экспорт
	
	Ошибка = Новый Структура();
	Ошибка.Вставить("Форма", Неопределено);
	Ошибка.Вставить("Описание", "");
	Ошибка.Вставить("КодОшибки", "");

	//@skip-check constructor-function-return-section
	Возврат Ошибка;
	
КонецФункции


#КонецОбласти

// Возвращает представление данных в форме.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения
//
Функция ПолноеПредставлениеДанных(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	ЭлементПредставлениеДанных = Элементы.ПредставлениеДанных; // ПолеФормы
	Если Элементы.ПредставлениеДанных.ПоложениеЗаголовка <> ПоложениеЗаголовкаЭлементаФормы.Нет
	   И ЗначениеЗаполнено(ЭлементПредставлениеДанных.Заголовок) Тогда
	
		Возврат ЭлементПредставлениеДанных.Заголовок + ": " + Форма.ПредставлениеДанных;
	Иначе
		Возврат Форма.ПредставлениеДанных;
	КонецЕсли;
	
КонецФункции

// Открывает представление данных по гиперссылке в форме.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Элемент - ЭлементыФормы
//  СтандартнаяОбработка - Булево
//  ТекущийСписокПредставлений - СписокЗначений
//
Процедура ПредставлениеДанныхНажатие(Форма, Элемент, СтандартнаяОбработка, ТекущийСписокПредставлений) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Если ТекущийСписокПредставлений.Количество() > 1 Тогда
		СписокПредставленийДанных = Новый Массив;
		Для Каждого ЭлементСписка Из ТекущийСписокПредставлений Цикл
			СписокПредставленийДанных.Добавить(ЭлементСписка.Представление);
		КонецЦикла;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СписокПредставленийДанных", СписокПредставленийДанных);
		ПараметрыФормы.Вставить("ПредставлениеДанных", Форма.ПредставлениеДанных);
		НоваяФорма = ОткрытьФорму("Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования.Форма.ПросмотрДанных",
			ПараметрыФормы, Элемент);
		Если НоваяФорма = Неопределено Тогда
			Возврат;
		КонецЕсли;
		НоваяФорма.УстановитьСписокПредставлений(ТекущийСписокПредставлений, Неопределено);
	Иначе
		Значение = ТекущийСписокПредставлений[0].Значение;
		Если ТипЗнч(Значение) = Тип("ОписаниеОповещения") Тогда
			ВыполнитьОбработкуОповещения(Значение);
		Иначе
			ПоказатьЗначение(, Значение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Только для внутреннего использования.
Процедура ОткрытьНовуюФорму(ВидФормы, КлиентскиеПараметры, СерверныеПараметры, ОбработкаЗавершения) Экспорт
	
	ОписаниеДанных = КлиентскиеПараметры.ОписаниеДанных;
	
	СерверныеПараметры.Вставить("БезПодтверждения", Ложь);
	
	Если ОписаниеДанных.Свойство("БезПодтверждения")
		И ОписаниеДанных.БезПодтверждения
		И (СерверныеПараметры.Свойство("ОтборСертификатов")
				И ТипЗнч(СерверныеПараметры.ОтборСертификатов) = Тип("Массив")
				И СерверныеПараметры.ОтборСертификатов.Количество() = 1 
			Или СерверныеПараметры.Свойство("Подписант") И ЗначениеЗаполнено(СерверныеПараметры.Подписант)) Тогда
		СерверныеПараметры.Вставить("БезПодтверждения", Истина);
	КонецЕсли;
	
	Если СерверныеПараметры.Свойство("НаборСертификатов")
	   И ОписаниеДанных.Свойство("БезПодтверждения")
	   И ОписаниеДанных.БезПодтверждения Тогда
		
		СерверныеПараметры.Вставить("БезПодтверждения", Истина);
	КонецЕсли;
	
	НастроитьПредставлениеДанных(КлиентскиеПараметры, СерверныеПараметры);
	
	Контекст = Новый Структура;
	Контекст.Вставить("ВидФормы",            ВидФормы);
	Контекст.Вставить("КлиентскиеПараметры", КлиентскиеПараметры);
	Контекст.Вставить("СерверныеПараметры",  СерверныеПараметры);
	Контекст.Вставить("ОбработкаЗавершения", ОбработкаЗавершения);
	
	ОткрытьНовуюФормуЗавершение(Новый Массив, Контекст);

КонецПроцедуры

// Продолжение процедуры ОткрытьНовуюФорму.
Процедура ОткрытьНовуюФормуЗавершение(ОтпечаткиСертификатовНаКлиенте, Контекст)
	
	Контекст.СерверныеПараметры.Вставить("ОтпечаткиСертификатовНаКлиенте",
		ОтпечаткиСертификатовНаКлиенте);
	
	ФормаПередачаПараметров().ОткрытьНовуюФорму(
		Контекст.ВидФормы,
		Контекст.СерверныеПараметры,
		Контекст.КлиентскиеПараметры,
		Контекст.ОбработкаЗавершения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Для процедуры ОткрытьНовуюФорму
Процедура НастроитьПредставлениеДанных(КлиентскиеПараметры, СерверныеПараметры)
	
	ОписаниеДанных = КлиентскиеПараметры.ОписаниеДанных;
	
	Если ОписаниеДанных.Свойство("СписокПредставлений") Тогда
		СписокПредставлений = ОписаниеДанных.СписокПредставлений;
	Иначе
		СписокПредставлений = Новый Массив;
		
		Если ОписаниеДанных.Свойство("Данные")
		 Или ОписаниеДанных.Свойство("Объект") Тогда
			
			ЗаполнитьСписокПредставлений(СписокПредставлений, ОписаниеДанных);
		Иначе
			Для каждого ЭлементДанных Из ОписаниеДанных.НаборДанных Цикл
				ЗаполнитьСписокПредставлений(СписокПредставлений, ЭлементДанных);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ТекущийСписокПредставлений = Новый СписокЗначений;
	
	Для каждого ЭлементСписка Из СписокПредставлений Цикл
		Если ТипЗнч(ЭлементСписка) = Тип("Строка") Тогда
			Представление = ЭлементСписка;
			Значение = Неопределено;
		ИначеЕсли ТипЗнч(ЭлементСписка) = Тип("Структура") Тогда
			Представление = ЭлементСписка.Представление;
			Значение = ЭлементСписка.Значение;
		Иначе // Ссылка
			Представление = Строка(ЭлементСписка.Значение);
			Значение = ЭлементСписка.Значение;
		КонецЕсли;
		
		ТекущийСписокПредставлений.Добавить(Значение, Представление);
	КонецЦикла;
	
	Если ТекущийСписокПредставлений.Количество() > 1 Тогда
		СерверныеПараметры.Вставить("ПредставлениеДанныхОткрывается", Истина);
		СерверныеПараметры.Вставить("ПредставлениеДанных", СтрЗаменить(
			ОписаниеДанных.ПредставлениеНабора, "%1", ОписаниеДанных.НаборДанных.Количество()));
	Иначе
		СерверныеПараметры.Вставить("ПредставлениеДанныхОткрывается",
			ТипЗнч(ТекущийСписокПредставлений[0].Значение) = Тип("ОписаниеОповещения")
			Или ЗначениеЗаполнено(ТекущийСписокПредставлений[0].Значение));
		
		СерверныеПараметры.Вставить("ПредставлениеДанных",
			ТекущийСписокПредставлений[0].Представление);
	КонецЕсли;
	
	КлиентскиеПараметры.Вставить("ТекущийСписокПредставлений", ТекущийСписокПредставлений);
	
КонецПроцедуры

// Для процедуры НастроитьПредставлениеДанных.
Процедура ЗаполнитьСписокПредставлений(СписокПредставлений, ЭлементДанных)
	
	ЭлементСписка = Новый Структура("Значение, Представление", Неопределено, "");
	СписокПредставлений.Добавить(ЭлементСписка);
	
	Если ЭлементДанных.Свойство("Представление")
	   И ТипЗнч(ЭлементДанных.Представление) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭлементСписка, ЭлементДанных.Представление);
		Возврат;
	КонецЕсли;
	
	Если ЭлементДанных.Свойство("Представление")
		И ТипЗнч(ЭлементДанных.Представление) <> Тип("Строка") Тогда
	
		ЭлементСписка.Значение = ЭлементДанных.Представление;
		
	ИначеЕсли ЭлементДанных.Свойство("Объект")
		И ТипЗнч(ЭлементДанных.Объект) <> Тип("ОписаниеОповещения") Тогда
		
		ЭлементСписка.Значение = ЭлементДанных.Объект;
	КонецЕсли;
	
	Если ЭлементДанных.Свойство("Представление") Тогда
		ЭлементСписка.Представление = ЭлементДанных.Представление;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет свойства подписи у подготовленного набора данных:
// Параметры:
//  ДанныеНабора - Структура:
//  * Данные            - ДвоичныеДанные - данные для подписания.
//                          - Строка - адрес временного хранилища, содержащего двоичные данные.
//  * Объект              - ЛюбаяСсылка - (необязательный) - ссылка на объект , к которому нужно добавить подпись.
//                          Если не указан, то подпись не требуется добавлять.
//  ПараметрыСертификата - Структура - Параметры выбранного сертификата:
//   *КомуВыдан - Строка,
//   *Отпечаток - Строка,
//   *ДанныеСертификата - ДвоичныеДанные,
//   *Ссылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  ДвоичныеДанныеПодписи - ДвоичныеДанные - Полученные двоичные данные подписи
//
Процедура ЗаполнитьСвойстваПодписиУДанныхНабора(ДанныеНабора, ПараметрыСертификата, ДвоичныеДанныеПодписи)

		СвойстваПодписи = КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи();
		СвойстваПодписи.Подпись = ДвоичныеДанныеПодписи;
		СвойстваПодписи.УстановившийПодпись = ПользователиКлиент.ТекущийПользователь();
		СвойстваПодписи.ДатаПодписи = ДатаПодписания(ДвоичныеДанныеПодписи);
		
		СвойстваПодписи.ПодписьВерна = Истина;
		СвойстваПодписи.ДатаПроверкиПодписи = СвойстваПодписи.ДатаПодписи;
		
		СвойстваПодписи.КомуВыданСертификат = ПараметрыСертификата.КомуВыдан;
		СвойстваПодписи.Отпечаток = ПараметрыСертификата.Отпечаток;
		СвойстваПодписи.Сертификат = ПараметрыСертификата.ДанныеСертификата;

		ДанныеНабора.Вставить("СвойстваПодписи", СвойстваПодписи);
		ДанныеНабора.Вставить("ВыбранныйСертификат", ПараметрыСертификата.Ссылка);
		
КонецПроцедуры

// Возвращает дату, извлеченную из двоичных данных подписи, или Неопределено.
//
// Параметры:
//  Подпись - ДвоичныеДанные - данные подписи из которых нужно извлечь дату.
//  ПривестиКЧасовомуПоясуСеанса - Булево - привести универсальное время к времени сеанса.
//
// Возвращаемое значение:
//  Дата - успешно извлеченная дата подписи.
//  Неопределено - не удалось извлечь дату из данных подписи.
//
Функция ДатаПодписания(Подпись, ПривестиКЧасовомуПоясуСеанса = Истина)
	
	ДатаПодписания = ЭлектроннаяПодписьСлужебныйКлиентСервер.ДатаПодписанияУниверсальная(Подпись);
	
	Если ПривестиКЧасовомуПоясуСеанса Тогда
		ДатаПодписания = ДатаПодписания + (ОбщегоНазначенияКлиент.ДатаСеанса()
			- ОбщегоНазначенияКлиент.ДатаУниверсальная());
	КонецЕсли;
	
	Возврат ДатаПодписания;
	
КонецФункции

// Для процедур УстановитьПарольСертификата, ОткрытьНовуюФорму, ВыборСертификатаДляПодписанияИлиРасшифровки,
// ПроверитьСертификатСправочника.
//
Функция ФормаПередачаПараметров()
	
	ИмяПараметра = "СтандартныеПодсистемы.ПараметрыЭлектроннойПодписиИШифрования";
	Если ПараметрыПриложения[ИмяПараметра] = Неопределено Тогда
		ПараметрыПриложения.Вставить(ИмяПараметра, Новый Соответствие);
	КонецЕсли;
	
	Форма = ОткрытьФорму("Обработка.ПодписаниеДанныхВМобильномЭДО.Форма.ПередачаПараметров");
	ПараметрыПриложения["СтандартныеПодсистемы.ПараметрыЭлектроннойПодписиИШифрования"].Вставить("ФормаПередачаПараметров", Форма);
	
	Возврат Форма;
	
КонецФункции
#КонецОбласти