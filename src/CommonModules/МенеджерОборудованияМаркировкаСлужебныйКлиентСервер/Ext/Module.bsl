#Область СлужебныйПрограммныйИнтерфейс

#Область ФормированиеДанныхККТ

#Если Не ВебКлиент Тогда

// Сформировать XML для запроса КМ.
// 
// Параметры:
//  ПараметрыОперации - Структура - Параметры операции.  
// 
// Возвращаемое значение:
//   Строка
//
Функция СформироватьXMLДляЗапросаКМ(ПараметрыОперации, РевизияИнтерфейса = 0) Экспорт
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.УстановитьСтроку("UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьXML.ЗаписатьНачалоЭлемента("RequestKM");
	
	ИдентификаторЗапроса = ПараметрыОперации.ИдентификаторЗапроса;
	Если ПустаяСтрока(ИдентификаторЗапроса) Тогда
		ИдентификаторЗапроса = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьАтрибут("GUID", XMLСтрока(ИдентификаторЗапроса));
	ЗаписьXML.ЗаписатьАтрибут("WaitForResult", ?(ПараметрыОперации.ОжидатьПолучениеОтветаОИСМ, "True", "False"));      
	ЗаписьXML.ЗаписатьАтрибут("NotSendToServer", ?(НЕ ПараметрыОперации.ОтправлятьНаСерверОИСМ, "True", "False"));      
	
	ЗаписьXML.ЗаписатьАтрибут("MarkingCode", XMLСтрока(ПараметрыОперации.КонтрольнаяМарка));
	ПланируемыйСтатусТовара = КодПланируемыйСтатусМаркируемогоТовара(ПараметрыОперации.ПланируемыйСтатусТовара);
	ЗаписьXML.ЗаписатьАтрибут("PlannedStatus", XMLСтрока(ПланируемыйСтатусТовара));
	
	ДробноеКоличествоУказано = Ложь;
	ДробноеКоличество = ПараметрыОперации.ДробноеКоличество;
	Если Не ПустаяСтрока(ДробноеКоличество.Числитель) И Не ПустаяСтрока(ДробноеКоличество.Знаменатель) Тогда
		Если Число(ДробноеКоличество.Числитель) > 0 И Число(ДробноеКоличество.Знаменатель) > 0 Тогда 
			ДробноеКоличествоУказано = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если РевизияИнтерфейса >= 4001 Тогда
		// Приказ ФНС России от 12.04.2023 N ЕД-7-20/239@ 
		Если ПараметрыОперации.Количество <> 1 Тогда
			ЗаписьXML.ЗаписатьАтрибут("Quantity", XMLСтрока(ПараметрыОперации.Количество)); // тег 1023 
		КонецЕсли;
		Если ПланируемыйСтатусТовара = 2 Или ПланируемыйСтатусТовара = 4 Тогда    
			Если ДробноеКоличествоУказано Тогда 
				МераКоличестваПредметаРасчета = 0 ;
				ЗаписьXML.ЗаписатьАтрибут("MeasureOfQuantity", XMLСтрока(МераКоличестваПредметаРасчета));
				ЗаписьXML.ЗаписатьНачалоЭлемента("FractionalQuantity");
				ЗаписьXML.ЗаписатьАтрибут("Numerator", XMLСтрока(ДробноеКоличество.Числитель));
				ЗаписьXML.ЗаписатьАтрибут("Denominator", XMLСтрока(ДробноеКоличество.Знаменатель));
				ЗаписьXML.ЗаписатьКонецЭлемента();  
			КонецЕсли	
		КонецЕсли; 
		Если Не ПустаяСтрока(ПараметрыОперации.КодЕдиницыИзмерения) Тогда
			МераКоличестваПредметаРасчета = МераКоличестваПредметаРасчетаПоКодуЕдиницыИзмерения(ПараметрыОперации.КодЕдиницыИзмерения)
		Иначе
			МераКоличестваПредметаРасчета = ПараметрыОперации.МераКоличестваПредметаРасчета; 
		КонецЕсли;              
		Если Не ПустаяСтрока(МераКоличестваПредметаРасчета) И НЕ (МераКоличестваПредметаРасчета = 0) Тогда  // тег 2108
			МераКоличестваПредметаРасчета = КодМерыКоличестваПредметаРасчетаККТ(МераКоличестваПредметаРасчета);
			ЗаписьXML.ЗаписатьАтрибут("MeasureOfQuantity", XMLСтрока(МераКоличестваПредметаРасчета));
		КонецЕсли;    
	Иначе
		// Реквизиты "количество предмета расчета" (тег 1023) и "мера количества предмета расчета" (тег 2108) включаются в состав запроса о коде маркировки
		// в случае, если реквизит "планируемый статус товара" (тег 2003) принимает значения "2" или "4".
		Если ПланируемыйСтатусТовара = 2 Или ПланируемыйСтатусТовара = 4 Тогда    
			ЗаписьXML.ЗаписатьАтрибут("Quantity", XMLСтрока(ПараметрыОперации.Количество)); // тег 1023
			Если ДробноеКоличествоУказано Тогда 
				МераКоличестваПредметаРасчета = 0 ;
				ЗаписьXML.ЗаписатьАтрибут("MeasureOfQuantity", XMLСтрока(МераКоличестваПредметаРасчета));
				ЗаписьXML.ЗаписатьНачалоЭлемента("FractionalQuantity");
				ЗаписьXML.ЗаписатьАтрибут("Numerator", XMLСтрока(ДробноеКоличество.Числитель));
				ЗаписьXML.ЗаписатьАтрибут("Denominator", XMLСтрока(ДробноеКоличество.Знаменатель));
				ЗаписьXML.ЗаписатьКонецЭлемента();  
			Иначе	
				Если Не ПустаяСтрока(ПараметрыОперации.КодЕдиницыИзмерения) Тогда
					МераКоличестваПредметаРасчета = МераКоличестваПредметаРасчетаПоКодуЕдиницыИзмерения(ПараметрыОперации.КодЕдиницыИзмерения)
				Иначе
					МераКоличестваПредметаРасчета = ПараметрыОперации.МераКоличестваПредметаРасчета; 
				КонецЕсли;              
				Если Не ПустаяСтрока(МераКоличестваПредметаРасчета) Тогда  // тег 2108
					МераКоличестваПредметаРасчета = КодМерыКоличестваПредметаРасчетаККТ(МераКоличестваПредметаРасчета);
					ЗаписьXML.ЗаписатьАтрибут("MeasureOfQuantity", XMLСтрока(МераКоличестваПредметаРасчета));
				КонецЕсли;    
			КонецЕсли	
		КонецЕсли;
	КонецЕсли;

	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	Возврат ЗаписьXML.Закрыть();
	
КонецФункции  

// Получить результаты запроса КМ из XML пакета.
// 
// Параметры:
//  ДанныеXML - Строка - XML.
// 
// Возвращаемое значение:
//  Структура - Получить результаты запроса КМИз XMLПакета:
//   * КодМаркировкиПроверен - Булево -
//   * РезультатПроверки - Булево -
Функция РезультатыЗапросаКМИзXMLПакета(ДанныеXML) Экспорт
	
	Параметры = Новый Структура();
	Параметры.Вставить("КодМаркировкиПроверен", Ложь);
	Параметры.Вставить("РезультатПроверки", Ложь);
	
	Если Не ПустаяСтрока(ДанныеXML) Тогда
		ЧтениеXML = Новый ЧтениеXML; 
		ЧтениеXML.УстановитьСтроку(ДанныеXML);
		ЧтениеXML.ПерейтиКСодержимому();
		Если ЧтениеXML.Имя = "RequestKMResult" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Параметры.КодМаркировкиПроверен = ВРег(ЧтениеXML.ЗначениеАтрибута("Checking")) = "TRUE";
			Параметры.РезультатПроверки = ВРег(ЧтениеXML.ЗначениеАтрибута("CheckingResult")) = "TRUE";
		КонецЕсли;
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Получить результаты запроса ОИСМ КМ из XML пакета.
// 
// Параметры:
//  ДанныеXML - Строка - XML.
//  Параметры - Неопределено - Параметры
// 
// Возвращаемое значение:
//  Неопределено, Структура - Получить результаты ОИСМКМИз XMLПакета:
//   * ИдентификаторЗапроса - УникальныйИдентификатор.
//   * СтатусРезультата - ПеречислениеСсылка.СтатусРезультатаЗапросаКМ.
//   * РезультатПроверкиОИСМ - Булево -
//   * КодРезультатаПроверкиОИСМ - Число -
//   * РезультатПроверкиОИСМПредставление - Строка -
//   * РезультатПроверкиСведенийОТоваре - Булево
//   * РезультатПроверкиСведенийОТовареПФ - Булево
//   * КодОбработкиЗапроса - Число
//   * СтатусОбработкиЗапроса - ПеречислениеСсылка.СтатусОбработкиЗапросаКМ.
//   * СтатусТовара - Неопределено -
Функция РезультатыОИСМКМИзXMLПакета(ДанныеXML, Параметры = Неопределено) Экспорт
	
	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура();
	КонецЕсли;
	Параметры.Вставить("ИдентификаторЗапроса");
	Параметры.Вставить("СтатусРезультата");
	Параметры.Вставить("РезультатПроверкиОИСМ", Ложь);
	Параметры.Вставить("КодРезультатаПроверкиОИСМ", 0);
	Параметры.Вставить("РезультатПроверкиОИСМПредставление", "00000000");

	Параметры.Вставить("РезультатПроверкиСведенийОТоваре");
	Параметры.Вставить("РезультатПроверкиСведенийОТовареПФ");
	Параметры.Вставить("КодОбработкиЗапроса");
	Параметры.Вставить("СтатусОбработкиЗапроса");

	Параметры.Вставить("СтатусТовара", Неопределено);
	
	Если Не ПустаяСтрока(ДанныеXML) Тогда
		ЧтениеXML = Новый ЧтениеXML; 
		ЧтениеXML.УстановитьСтроку(ДанныеXML);
		ЧтениеXML.ПерейтиКСодержимому();
		Если ЧтениеXML.Имя = "ProcessingKMResult" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Параметры.РезультатПроверкиОИСМ = ВРег(ЧтениеXML.ЗначениеАтрибута("Result")) = "TRUE";
			Параметры.КодРезультатаПроверкиОИСМ = ЧтениеXML.ЗначениеАтрибута("ResultCode");
			Если ЧтениеXML.ЗначениеАтрибута("ResultCode") <> Неопределено Тогда
				Параметры.КодРезультатаПроверкиОИСМ = Число(ЧтениеXML.ЗначениеАтрибута("ResultCode"));
				Параметры.РезультатПроверкиОИСМПредставление = МенеджерОборудованияКлиентСервер.ПреобразоватьЧислоВБинарнуюСтроку(Параметры.КодРезультатаПроверкиОИСМ, 8);
			КонецЕсли;
			Если ЧтениеXML.ЗначениеАтрибута("StatusInfo") <> Неопределено Тогда
				СтатусТовара = Число(ЧтениеXML.ЗначениеАтрибута("StatusInfo"));
				Параметры.СтатусТовара = ОтветОИСМОСтатусеТовараПоКоду(СтатусТовара);
			КонецЕсли;
			Параметры.ИдентификаторЗапроса = ЧтениеXML.ЗначениеАтрибута("GUID");
			КодОбработкиЗапроса = ЧтениеXML.ЗначениеАтрибута("HandleCode");
			Если Не ПустаяСтрока(КодОбработкиЗапроса) Тогда
				Параметры.КодОбработкиЗапроса = Число(КодОбработкиЗапроса);
				Параметры.СтатусОбработкиЗапроса = СтатусОбработкиЗапросаКМПоКоду(Параметры.КодОбработкиЗапроса);
		КонецЕсли;
	КонецЕсли;
	КонецЕсли;
	
	Параметры.РезультатПроверкиСведенийОТоваре = МенеджерОборудованияМаркировкаКлиентСервер.
		РезультатПроверкиСведенийОТоваре(Параметры.РезультатПроверкиОИСМПредставление);
	Параметры.РезультатПроверкиСведенийОТовареПФ = МенеджерОборудованияМаркировкаКлиентСервер.
		РезультатПроверкиСведенийОТовареПФ(Параметры.РезультатПроверкиОИСМПредставление);
	
	Возврат Параметры;
	
КонецФункции

#КонецЕсли

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ФормированиеДанныхККТ

Функция КодПланируемыйСтатусМаркируемогоТовара(ПланируемыйСтатусМаркируемогоТовара)
	
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Возврат МенеджерОборудованияМаркировка.КодПланируемыйСтатусМаркируемогоТовара(ПланируемыйСтатусМаркируемогоТовара);
#Иначе
	Возврат МенеджерОборудованияМаркировкаКлиент.КодПланируемыйСтатусМаркируемогоТовара(ПланируемыйСтатусМаркируемогоТовара);
#КонецЕсли
	
КонецФункции

// Возвращает код меры количества предмета расчета ККТ.
// 
// Параметры:
//  МераКоличестваПредметаРасчетаККТ - ПеречислениеСсылка.МераКоличестваПредметаРасчетаККТ - Мера количества предмета расчета ККТ
// 
// Возвращаемое значение:
//  Произвольный, Число - Код меры количества предмета расчета ККТ     
Функция КодМерыКоличестваПредметаРасчетаККТ(МераКоличестваПредметаРасчетаККТ) Экспорт
	
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Возврат ОборудованиеЧекопечатающиеУстройства.
		КодМерыКоличестваПредметаРасчетаККТ(МераКоличестваПредметаРасчетаККТ);
#Иначе
	Возврат ОборудованиеЧекопечатающиеУстройстваКлиент.
		КодМерыКоличестваПредметаРасчетаККТ(МераКоличестваПредметаРасчетаККТ);
#КонецЕсли
	
КонецФункции

// Возвращает меру количества предмета расчета по коду единицы измерения.
// 
// Параметры:
//  КодЕдиницыИзмерения - Число - Код единицы измерения.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.МераКоличестваПредметаРасчетаККТ, Произвольный - Мера количества предмета расчета по коду единицы измерения
Функция МераКоличестваПредметаРасчетаПоКодуЕдиницыИзмерения(КодЕдиницыИзмерения) Экспорт
	
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Возврат ОборудованиеЧекопечатающиеУстройства.
		МераКоличестваПредметаРасчетаПоКодуЕдиницыИзмерения(КодЕдиницыИзмерения);
#Иначе
	Возврат ОборудованиеЧекопечатающиеУстройстваКлиент.
		МераКоличестваПредметаРасчетаПоКодуЕдиницыИзмерения(КодЕдиницыИзмерения);
#КонецЕсли
	
КонецФункции

// Возвращает ответ ОИСМ о статусе товара по коду.
// 
// Параметры:
//  ОтветОИСМОСтатусеТовара - Число - Ответ от ИСМО.  
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ОтветОИСМОСтатусеТовара - Ответ ОИСМ.
//
Функция ОтветОИСМОСтатусеТовараПоКоду(СтатусТовара)
	
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Возврат МенеджерОборудованияМаркировка.
		ОтветОИСМОСтатусеТовараПоКоду(СтатусТовара);
#Иначе
	Возврат МенеджерОборудованияМаркировкаКлиент.
		ОтветОИСМОСтатусеТовараПоКоду(СтатусТовара);
#КонецЕсли
	
КонецФункции

// Возвращает статус обработки запроса КМ по коду.
//
Функция СтатусОбработкиЗапросаКМПоКоду(КодОбработкиЗапроса) 
	
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Возврат МенеджерОборудованияМаркировка.
		СтатусОбработкиЗапросаКМПоКоду(КодОбработкиЗапроса);
#Иначе
	Возврат МенеджерОборудованияМаркировкаКлиент.
		СтатусОбработкиЗапросаКМПоКоду(КодОбработкиЗапроса);
#КонецЕсли
	
	
КонецФункции



#КонецОбласти

#КонецОбласти
