
#Область СлужебныйПрограммныйИнтерфейс

#Область ПредопределенныеДанные

// Имя формы состояния подключенных МП.
// 
// Возвращаемое значение:
//  Строка - Имя формы состояния подключенных МП
Функция ИмяФормыСостоянияПодключенныхМП() Экспорт
	
	Возврат "Обработка.ВзаимодействиеМПЭПД.Форма.СостояниеПодключенияМП";
	
КонецФункции

// Имя формы выбор действия по документу.
// 
// Возвращаемое значение:
//  Строка - Имя формы выбор действия по документу
Функция ИмяФормыВыборДействияПоДокументу() Экспорт
	
	Возврат "Обработка.ВзаимодействиеМПЭПД.Форма.ВыборДействияПоДокументу";
	
КонецФункции

// Имя формы подписанные данные МП.
// 
// Возвращаемое значение:
//  Строка - Имя формы подписанные данные МП
Функция ИмяФормыПодписанныеДанныеМП() Экспорт
	
	Возврат "ОбщаяФорма.ПодписанныеДанныеМПЭПД";
	
КонецФункции

// Вид взаимодействия СМП.
// 
// Параметры:
//  ИмяВида - Строка - Имя вида.
// 
// Возвращаемое значение:
//  СсылкаНаОбъектИнформационнойБазы - Вид взаимодействия СМП
Функция ВидВзаимодействияСМП(ИмяВида) Экспорт
	
	Возврат ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД." + ИмяВида);
	
КонецФункции

// Представление пустого значения данных МП.
// 
// Возвращаемое значение:
//  Строка - Представление пустого значения данных МП
Функция ПредставлениеПустогоЗначенияДанныхМП() Экспорт
	
	Возврат "<не заполнено>";
	
КонецФункции

// Картинка пустая фотография.
// 
// Возвращаемое значение:
//  Картинка - Картинка пустая фотография.
Функция КартинкаПустаяФотография() Экспорт
	
	Возврат БиблиотекаКартинок.ПустаяФотографияЭПД;
	
КонецФункции

// Картинка мобильное устройство ЭПД.
// 
// Возвращаемое значение:
//  Картинка - Картинка мобильное устройство ЭПД.
Функция КартинкаМобильноеУстройствоЭПД() Экспорт
	
	Возврат БиблиотекаКартинок.МобильноеУстройствоЭПД;
	
КонецФункции

// Картинка открыть в окне ЭПД.
// 
// Возвращаемое значение:
//  Картинка - Картинка открыть в окне ЭПД.
Функция КартинкаОткрытьВОкнеЭПД() Экспорт
	
	Возврат БиблиотекаКартинок.ОткрытьВОкнеЭПД;
	
КонецФункции

// Картинка изменить МПЭПД.
// 
// Возвращаемое значение:
//  Картинка - Картинка изменить МПЭПД.
Функция КартинкаИзменитьМПЭПД() Экспорт
	
	Возврат БиблиотекаКартинок.ИзменитьМПЭПД;
	
КонецФункции

// Картинка привязать МПЭПД.
// 
// Возвращаемое значение:
//  Картинка - Картинка привязать МПЭПД.
Функция КартинкаПривязатьМПЭПД() Экспорт
	
	Возврат БиблиотекаКартинок.ПривязатьМПЭПД;
	
КонецФункции

// Картинка телефонная трубка ЭПД.
// 
// Возвращаемое значение:
//  Картинка - Картинка телефонная трубка ЭПД.
Функция КартинкаТелефоннаяТрубкаЭПД() Экспорт
	
	Возврат БиблиотекаКартинок.ТелефоннаяТрубкаЭПД;
	
КонецФункции

// Картинка коллекция события мобильного приложения ЭПД.
// 
// Возвращаемое значение:
//  Картинка - Картинка коллекция события мобильного приложения ЭПД.
Функция КартинкаКоллекцияСобытияМобильногоПриложенияЭПД() Экспорт
	
	Возврат БиблиотекаКартинок.КоллекцияСобытияМобильногоПриложенияЭПД;
	
КонецФункции

#КонецОбласти

#Область РолиИВидыТипыДокументов

// Возвращает массив ролей МП.
// 
// Возвращаемое значение:
//  Массив из Строка - Роли МП.
Функция АтомарныеРолиМП() Экспорт
	
	Результат = Новый Массив;
	
	//@skip-check typed-value-adding-to-untyped-collection
	Результат.Добавить(НСтр("ru = 'Оформитель';
							|en = 'Issuer'"));
	//@skip-check typed-value-adding-to-untyped-collection
	Результат.Добавить(НСтр("ru = 'Медработник';
							|en = 'Healthcare worker'"));
	//@skip-check typed-value-adding-to-untyped-collection
	Результат.Добавить(НСтр("ru = 'Механик';
							|en = 'Mechanic'"));
	//@skip-check typed-value-adding-to-untyped-collection
	Результат.Добавить(НСтр("ru = 'Ответственный за показания одометра';
							|en = 'Person responsible for odometer readings'"));
	//@skip-check typed-value-adding-to-untyped-collection
	Результат.Добавить(НСтр("ru = 'Водитель';
							|en = 'Driver'"));
	
	Возврат Результат;
	
КонецФункции

// Возвращает индекс атомарной роли для водителя.
// 
// Возвращаемое значение:
//  Число - Индекс атомарной роли. 
//
Функция ИндексАтомарнойРолиВодителя() Экспорт
	
	Возврат 4;
	
КонецФункции

// Возвращает индекс атомарной роли для медработника.
// 
// Возвращаемое значение:
//  Число - Индекс атомарной роли. 
//
Функция ИндексАтомарнойРолиМедработника() Экспорт
	
	Возврат 1;
	
КонецФункции

// Возвращает индекс атомарной роли для механика.
// 
// Возвращаемое значение:
//  Число - Индекс атомарной роли. 
//
Функция ИндексАтомарнойРолиМеханика() Экспорт
	
	Возврат 2;
	
КонецФункции

// Возвращает индекс атомарной роли для ответственного за одометр.
// 
// Возвращаемое значение:
//  Число - Индекс атомарной роли. 
//
Функция ИндексАтомарнойРолиОтветственногоЗаОдометр() Экспорт
	
	Возврат 3;
	
КонецФункции

// Возвращает массив видов документов МП.
// 
// Возвращаемое значение:
//  Массив из Строка - Атомарные виды документов МП
Функция АтомарныеВидыДокументовМП() Экспорт
	
	Результат = Новый Массив;
	
	//@skip-check typed-value-adding-to-untyped-collection
	Результат.Добавить(НСтр("ru = 'Транспортная накладная';
							|en = 'Waybill'"));
	//@skip-check typed-value-adding-to-untyped-collection
	Результат.Добавить(НСтр("ru = 'Путевой лист';
							|en = 'Waybill'"));
	
	Возврат Результат;
	
КонецФункции

// Возвращает представления видов документов в МП.
// 
// Параметры:
//  Все - Булево - Все
//  Отбор - Неопределено - Отбор
// 
// Возвращаемое значение:
//  Соответствие из Число - Представления видов документов МП
Функция ПредставленияВидовДокументовМП(Все = Истина, Отбор = Неопределено) Экспорт
	
	Результат = Новый Соответствие;
	
	АтомарныеВидыДокументов = АтомарныеВидыДокументовМП();
	ВсегоДокументов = АтомарныеВидыДокументов.Количество();
	
	ВидыДокументовПоддерживаемыеМП = ВидыДокументовПоддерживаемыеМП();
	
	Если ВидыДокументовПоддерживаемыеМП.Количество() = 1 Тогда
		ИндексДокумента = ВидыДокументовПоддерживаемыеМП[0];
		Результат.Вставить(Pow(2, ИндексДокумента), АтомарныеВидыДокументов[ИндексДокумента]);	
		Возврат Результат;
	КонецЕсли;
	
	Если Все = Истина Тогда
		Для СчетчикОбщий = 1 По Pow(2, ВсегоДокументов) - 1 Цикл
			СчетчикАтомарный = 0;	
			ПредставлениеМассив = Новый Массив;
			СоответствуетОтбору = Истина;
			Для Каждого ВидДокумента Из АтомарныеВидыДокументов Цикл
				Если ВидыДокументовПоддерживаемыеМП.Найти(СчетчикАтомарный) <> Неопределено Тогда				
					ВидЧисловой = Pow(2, СчетчикАтомарный);
					Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(СчетчикОбщий, ВидЧисловой, ВсегоДокументов) Тогда
						//@skip-check typed-value-adding-to-untyped-collection
						ПредставлениеМассив.Добавить(ВидДокумента);
						Если Отбор <> Неопределено И СчетчикАтомарный <> Отбор Тогда
							СоответствуетОтбору = Ложь;	
						КонецЕсли;	
					КонецЕсли;	
				КонецЕсли;
				СчетчикАтомарный = СчетчикАтомарный + 1;
			КонецЦикла;	
			Если СоответствуетОтбору = Истина И ПредставлениеМассив.Количество() > 0 Тогда
				Результат.Вставить(СчетчикОбщий, СтрСоединить(ПредставлениеМассив, ", ")); 
			КонецЕсли;
		КонецЦикла;
	Иначе
		СчетчикАтомарный = 0;
		Для Каждого ВидДокумента Из АтомарныеВидыДокументов Цикл		
			Если ВидыДокументовПоддерживаемыеМП.Найти(СчетчикАтомарный) <> Неопределено
				И (Отбор = Неопределено Или СчетчикАтомарный = Отбор) Тогда
				ВидЧисловой = Pow(2, СчетчикАтомарный);
				Результат.Вставить(ВидЧисловой, ВидДокумента);
			КонецЕсли;
			СчетчикАтомарный = СчетчикАтомарный + 1;
		КонецЦикла;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает обязательные роли МП.
// 
// Возвращаемое значение:
//  Массив  из Число - Обязательные роли МП
Функция ОбязательныеРолиМП() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить(0);
	
	Возврат Результат;
	
КонецФункции

// Представления ролей МП.
// 
// Параметры:
//  Все - Булево - Все
//  Отбор - Неопределено, Структура - Отбор
//  ВидыДокументов - Неопределено, Число - Виды документов
// 
// Возвращаемое значение:
//  Соответствие из Число - Представления ролей МП
Функция ПредставленияРолейМП(Все = Истина, Отбор = Неопределено, ВидыДокументов = Неопределено) Экспорт
	
	Результат = Новый Соответствие;
	
	АтомарныеРоли = АтомарныеРолиМП();	
	ВсегоРолей = АтомарныеРоли.Количество();
	
	РолиПоддерживаемыеМП = РолиПоддерживаемыеМП(, , ВидыДокументов);
	Если РолиПоддерживаемыеМП.Количество() = 1 Тогда
		ИндексРоли = РолиПоддерживаемыеМП[0];
		Результат.Вставить(Pow(2, ИндексРоли), АтомарныеРоли[ИндексРоли]);	
		Возврат Результат;
	КонецЕсли;
	
	Если Все = Истина Тогда
		Для СчетчикОбщий = 1 По Pow(2, ВсегоРолей) - 1 Цикл
			СчетчикАтомарный = 0;	
			ПредставлениеМассив = Новый Массив;
			СоответствуетОтбору = Ложь;
			Для Каждого Роль Из АтомарныеРоли Цикл
				Если РолиПоддерживаемыеМП.Найти(СчетчикАтомарный) <> Неопределено Тогда				
					РольЧисловая = Pow(2, СчетчикАтомарный);
					Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(СчетчикОбщий, РольЧисловая, ВсегоРолей) Тогда
						//@skip-check typed-value-adding-to-untyped-collection
						ПредставлениеМассив.Добавить(Роль);
						Если Отбор = Неопределено Или СчетчикАтомарный = Отбор Тогда
							СоответствуетОтбору = Истина;	
						КонецЕсли;	
					КонецЕсли;	
				КонецЕсли;
				СчетчикАтомарный = СчетчикАтомарный + 1;
			КонецЦикла;	
			Если СоответствуетОтбору = Истина И ПредставлениеМассив.Количество() > 0 Тогда
				Результат.Вставить(СчетчикОбщий, СтрСоединить(ПредставлениеМассив, ", "));
			КонецЕсли;
		КонецЦикла;
	Иначе
		СчетчикАтомарный = 0;
		Для Каждого Роль Из АтомарныеРоли Цикл		
			Если РолиПоддерживаемыеМП.Найти(СчетчикАтомарный) <> Неопределено
				И (Отбор = Неопределено Или СчетчикАтомарный = Отбор) Тогда
				РольЧисловая = Pow(2, СчетчикАтомарный);
				Результат.Вставить(РольЧисловая, Роль); 
			КонецЕсли;
			СчетчикАтомарный = СчетчикАтомарный + 1;
		КонецЦикла;	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает соответствие титулов роля МП.
// 
// Возвращаемое значение:
//  Массив из Структура - Соответствие титулов роля МП:
//	* Роль - Число
//	* Титул - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
Функция СоответствиеТитуловРолямМП() Экспорт
	
	Результат = Новый Массив;
	
	АтомарныеРолиМП = АтомарныеРолиМП();
	РольВодитель = Pow(2, АтомарныеРолиМП.Найти(НСтр("ru = 'Водитель';
													|en = 'Driver'")));
	РольМедработник = Pow(2, АтомарныеРолиМП.Найти(НСтр("ru = 'Медработник';
														|en = 'Healthcare worker'")));
	РольМеханик = Pow(2, АтомарныеРолиМП.Найти(НСтр("ru = 'Механик';
													|en = 'Mechanic'")));
	РольОдометр = Pow(2, АтомарныеРолиМП.Найти(НСтр("ru = 'Ответственный за показания одометра';
													|en = 'Person responsible for odometer readings'")));
	
	// ЭТрН
	Результат.Добавить(Новый Структура("Роль, Титул", РольВодитель, ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул2")));
	Результат.Добавить(Новый Структура("Роль, Титул", РольВодитель, ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул4")));
	
	// ЭПЛ
	Результат.Добавить(Новый Структура("Роль, Титул", РольВодитель, ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул1")));
	Результат.Добавить(Новый Структура("Роль, Титул", РольМедработник, ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2")));
	Результат.Добавить(Новый Структура("Роль, Титул", РольМеханик, ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3")));
	Результат.Добавить(Новый Структура("Роль, Титул", РольОдометр, ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4")));
	Результат.Добавить(Новый Структура("Роль, Титул", РольОдометр, ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5")));
	Результат.Добавить(Новый Структура("Роль, Титул", РольМедработник, ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6")));
	
	Возврат Результат;
	
КонецФункции

// Возвращает соответствие титулов типам МП.
// 
// Параметры:
//  ПоТитулам - Булево
// 
// Возвращаемое значение:
//  Соответствие из Строка - Соответствие титулов типам МП.
Функция СоответствиеТитуловТипамМП(ПоТитулам = Ложь) Экспорт
	
	Результат = Новый Соответствие;
	
	// ЭТрН
	Результат.Вставить("acceptance", ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул2"));
	Результат.Вставить("unloading", ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул4"));
	
	// ЭПЛ
	Результат.Вставить("ShippingList_medic_before", ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2"));
	Результат.Вставить("ShippingList_medic_before_rejection", ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_ОткМ"));
	Результат.Вставить("ShippingList_inspection", ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3"));
	Результат.Вставить("ShippingList_odometr_before", ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4"));
	Результат.Вставить("ShippingList_odometr_after", ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5"));
	Результат.Вставить("ShippingList_medic_after", ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6"));
	
	Если ПоТитулам = Истина Тогда
		РезультатПеревернуто = Новый Соответствие;
	
		Для Каждого СтрокаКлюча Из Результат Цикл
			РезультатПеревернуто.Вставить(СтрокаКлюча.Ключ, СтрокаКлюча.Значение);
			РезультатПеревернуто.Вставить(СтрокаКлюча.Значение, СтрокаКлюча.Ключ);
		КонецЦикла;
		
		Возврат РезультатПеревернуто;
	Иначе	
		Возврат Результат;
	КонецЕсли;
	
КонецФункции

// Это тип сообщения об отправке титула из МП.
// 
// Параметры:
//  ТипСообщенияМП - Строка - Тип сообщения от МП.
// 
// Возвращаемое значение:
//  Булево - Признак того, что переданный тип это тип сообщения об отправке титула из МП.
Функция ЭтоТипСообщенияОбОтправкеТитулаИзМП(ТипСообщенияМП) Экспорт
	
	Возврат СтрЗаканчиваетсяНа(ТипСообщенияМП, "titul");
	
КонецФункции

// Приводит тип сообщения от МП к типу сообщения, по которому можно получить титул.
// 
// Параметры:
//  ТипСообщенияМП - Строка - Тип сообщения от МП.
// 
// Возвращаемое значение:
//  Строка - Привести тип сообщения к виду получения титула БЭД
Функция ПривестиТипСообщенияКВидуПолученияТитулаБЭД(ТипСообщенияМП) Экспорт
	
	Если СтрЗаканчиваетсяНа(ТипСообщенияМП, "_titul") Тогда
		Возврат Лев(ТипСообщенияМП, СтрДлина(ТипСообщенияМП) - 6);
	Иначе
		Возврат ТипСообщенияМП;
	КонецЕсли;
	
КонецФункции

// Определяет и возвращает тип документа МП по ссылке на документ.
// 
// Параметры:
//  ДокументЭПД - ОпределяемыйТип.ДокументыЭПД
// 
// Возвращаемое значение:
//  Строка - Тип документа МП.
Функция ТипДокументаМП(ДокументЭПД) Экспорт
	
	Результат = "";
	
	Если ТипЗнч(ДокументЭПД) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная")
		Или ДокументЭПД = "ЭлектроннаяТранспортнаяНакладная"
		Или ДокументЭПД = ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.ЭТрН") Тогда
		Результат = "ETRN";
	ИначеЕсли ТипЗнч(ДокументЭПД) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист")
		Или ДокументЭПД = "ЭлектронныйПутевойЛист"
		Или ДокументЭПД = ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.ЭПЛ") Тогда
		Результат = "EPL"; 
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает индексы поддерживаемых МП ролей пользователя, по массиву, возвращаемому методом АтомарныеРолиМП.
// 
// Параметры:
//  Все - Булево - Если Ложь, тогда будут возвращены только атомарные роли.
//  Отбор - Число - Атомарная роль.
//  ВидыДокументов - Число, Неопределено - Виды документов.
// 
// Возвращаемое значение:
//  Массив из Число - Роли (числовые) поддерживаемые мобильным приложением.
Функция РолиПоддерживаемыеМП(Все = Ложь, Отбор = Неопределено, ВидыДокументов = Неопределено) Экспорт
	
	ИндексыАтомарныхРолей = Новый Массив;
	
	АтомарныеРолиМП = АтомарныеРолиМП();
	ИндексРолиВодитель = АтомарныеРолиМП.Найти(НСтр("ru = 'Водитель';
													|en = 'Driver'"));
	ИндексРолиМедработник = АтомарныеРолиМП.Найти(НСтр("ru = 'Медработник';
														|en = 'Healthcare worker'"));
	ИндексРолиМеханик = АтомарныеРолиМП.Найти(НСтр("ru = 'Механик';
													|en = 'Mechanic'"));
	ИндексРолиОдометр = АтомарныеРолиМП.Найти(НСтр("ru = 'Ответственный за показания одометра';
													|en = 'Person responsible for odometer readings'"));
	
	АтомарныеВидыДокументов = АтомарныеВидыДокументовМП();
	ВидПутевойЛист = Pow(2, АтомарныеВидыДокументов.Найти(НСтр("ru = 'Путевой лист';
																|en = 'Waybill'")));
	
	Если ДокументПоддерживаетсяМП(ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.ЭПЛ")) = Истина
	И (ВидыДокументов = Неопределено 
		Или ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(ВидыДокументов, ВидПутевойЛист, АтомарныеВидыДокументов.Количество())) Тогда
		//@skip-check typed-value-adding-to-untyped-collection
		ИндексыАтомарныхРолей.Добавить(ИндексРолиМедработник);
		//@skip-check typed-value-adding-to-untyped-collection
		ИндексыАтомарныхРолей.Добавить(ИндексРолиМеханик);
		//@skip-check typed-value-adding-to-untyped-collection
		ИндексыАтомарныхРолей.Добавить(ИндексРолиОдометр);
	КонецЕсли;
	//@skip-check typed-value-adding-to-untyped-collection
	ИндексыАтомарныхРолей.Добавить(ИндексРолиВодитель); 
	
	Если Все = Ложь Тогда
		Возврат ИндексыАтомарныхРолей;
	Иначе
		Результат = Новый Массив;
		АтомарныеРоли = АтомарныеРолиМП();	
		ВсегоРолей = АтомарныеРоли.Количество();
		Для СчетчикОбщий = 1 По Pow(2, ВсегоРолей) - 1 Цикл
			Для СчетчикАтомарный = 0 По ВсегоРолей Цикл
				Если ИндексыАтомарныхРолей.Найти(СчетчикАтомарный) <> Неопределено Тогда				
					РольЧисловая = Pow(2, СчетчикАтомарный);
					Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(СчетчикОбщий, РольЧисловая, ВсегоРолей) Тогда
						Если Отбор = Неопределено Или СчетчикАтомарный = Отбор Тогда
							Результат.Добавить(СчетчикОбщий);	
						КонецЕсли;	
					КонецЕсли;	
				КонецЕсли;
			КонецЦикла;	
		КонецЦикла;
		
		Возврат Результат;
		
	КонецЕсли;
	
КонецФункции

// Возвращает индексы поддерживаемых МП видов документов, по массиву, возвращаемому методом АтомарныеРолиМП
// 
// Параметры:
//  Все - Булево - если Ложь, тогда будут возвращены только атомарные виды документов
//  Отбор - Неопределено - атомарный вид документа
// 
// Возвращаемое значение:
//  Массив из Число - Виды документов поддерживаемые мобильным приложением
Функция ВидыДокументовПоддерживаемыеМП(Все = Ложь, Отбор = Неопределено) Экспорт
	            
	ИндексыАтомарныхВидов = Новый Массив;
	
	АтомарныеВидыДокументов = АтомарныеВидыДокументовМП();
	ИндексТранспортнаяНакладная = АтомарныеВидыДокументов.Найти(НСтр("ru = 'Транспортная накладная';
																	|en = 'Waybill'"));
	ИндексПутевойЛист = АтомарныеВидыДокументов.Найти(НСтр("ru = 'Путевой лист';
															|en = 'Waybill'"));
	
	//@skip-check typed-value-adding-to-untyped-collection
	ИндексыАтомарныхВидов.Добавить(ИндексТранспортнаяНакладная);
	Если ДокументПоддерживаетсяМП(ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.ЭПЛ")) = Истина Тогда
		//@skip-check typed-value-adding-to-untyped-collection
		ИндексыАтомарныхВидов.Добавить(ИндексПутевойЛист);
	КонецЕсли;
	
	Если Все = Ложь Тогда
		Возврат ИндексыАтомарныхВидов;
	Иначе
		Результат = Новый Массив;	
		ВсегоВидов = АтомарныеВидыДокументов.Количество();
		Для СчетчикОбщий = 1 По Pow(2, ВсегоВидов) - 1 Цикл
			Для СчетчикАтомарный = 0 По ВсегоВидов Цикл
				Если ИндексыАтомарныхВидов.Найти(СчетчикАтомарный) <> Неопределено Тогда				
					ВидЧисловой = Pow(2, СчетчикАтомарный);
					Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(СчетчикОбщий, ВидЧисловой, ВсегоВидов) Тогда
						Если Отбор = Неопределено Или СчетчикАтомарный = Отбор Тогда
							Результат.Добавить(СчетчикОбщий);	
						КонецЕсли;	
					КонецЕсли;	
				КонецЕсли;
			КонецЦикла;	
		КонецЦикла;
		
		Возврат Результат;
		
	КонецЕсли;
	
КонецФункции

// Возвращает роль МП с учетом видов документов.
// 
// Параметры:
//  Роль - Число - Роль.
//  ВидыДокументов - Число - Виды документов.
// 
// Возвращаемое значение:
//  Число - Роль с учетом видов документов.
Функция РольСУчетомВидовДокументов(Знач Роль, ВидыДокументов) Экспорт
	
	АтомарныеВидыДокументовМП = АтомарныеВидыДокументовМП();
	ВсегоДокументов = АтомарныеВидыДокументовМП.Количество();
	ВсегоРолей = АтомарныеРолиМП().Количество();
	РольДелимаяПоДокументам = РольДелимаяПоДокументам();
			
	ЕстьРольДелимаяПоДокументам = ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(Роль, РольДелимаяПоДокументам, ВсегоРолей);
	
	Если ЕстьРольДелимаяПоДокументам Тогда
		Результат = Роль - РольДелимаяПоДокументам;
		Для СчетчикАтомарный = 0 По АтомарныеВидыДокументовМП.Количество() - 1 Цикл
			ВидЧисловой = Pow(2, СчетчикАтомарный);
			Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(ВидыДокументов, ВидЧисловой, ВсегоДокументов) Тогда
				Результат = Результат + РольДелимаяПоДокументам * Pow(2, СчетчикАтомарный);	
			КонецЕсли;
		КонецЦикла;
	Иначе
		Результат = Роль;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Разделить роль и виды документов.
// 
// Параметры:
//  Роль -Число - Роль с включенными видами документов.
// 
// Возвращаемое значение:
//  Структура - Разделить роль и виды документов:
// * Роль - Число
// * ВидыДокументов - Число 
Функция РазделитьРольИВидыДокументов(Роль) Экспорт
	
	Результат = Новый Структура("Роль, ВидыДокументов", Роль, 0);
	
	АтомарныеРоли = АтомарныеРолиМП();	
	ВсегоРолей = АтомарныеРоли.Количество();
	
	АтомарныеВидыДокументовМП = АтомарныеВидыДокументовМП();
	ВсегоДокументов = АтомарныеВидыДокументовМП.Количество();
	РольДелимаяПоДокументам = РольДелимаяПоДокументам();
	
	ЕстьДелимаяРоль = Ложь;
	Для СчетчикАтомарный = 0 По АтомарныеВидыДокументовМП.Количество() - 1 Цикл
		ВидЧисловой = Pow(2, СчетчикАтомарный);
		Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(Роль, РольДелимаяПоДокументам * ВидЧисловой, ВсегоРолей + ВсегоДокументов - 1) Тогда
			Результат.Роль = Результат.Роль - РольДелимаяПоДокументам * ВидЧисловой;	
			Результат.ВидыДокументов = Результат.ВидыДокументов + ВидЧисловой;
			ЕстьДелимаяРоль = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьДелимаяРоль Тогда
		Результат.Роль = Результат.Роль + РольДелимаяПоДокументам;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру с информацией о реквизитах по ролях участников
//
// Параметры:
//  Форма	 - ФормаКлиентскогоПриложения
// 
// Возвращаемое значение:
//  Неопределено, Структура - Структура с информацией о реквизитах по ролях участников. Неопределено когда документ не записан:
//	* РольУчастника - Число
//	* РеквизитОрганизация - Строка - Имя реквизита с ссылкой на участника.
//	* РеквизитИдентификатор - Строка - Имя реквизита с идентификатором участника.
//
Функция ИнформацияОРеквизитахПоРолиУчастникаПодерживаемыхМП(Форма) Экспорт
	
	РольУчастника = Форма.Объект.РольУчастника;
	Ссылка = Форма.Объект.Ссылка;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист") Тогда
		// Оформитель
		Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 1) Тогда
			РеквизитОрганизация = "СсылкаТитулОформлениеОформитель";
			РеквизитИдентификатор = "ИдентификаторОформителя";
			
		// Медосмотр
		ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 2) Тогда
			РеквизитОрганизация = "СсылкаТитулОформлениеМедорганизация";
			РеквизитИдентификатор = "ИдентификаторМедорганизации";
			
		// Техконтроль
		ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 4) Тогда
			РеквизитОрганизация = "СсылкаТитулОформлениеТехконтроль";
			РеквизитИдентификатор = "ИдентификаторТехконтроль";
			
		// Показания одометра
		ИначеЕсли ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 8) Тогда
			РеквизитОрганизация = "СсылкаТитулОформлениеТехконтроль";
			РеквизитИдентификатор = "ИдентификаторТехконтроль";
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
		// Перевозчик
		Если РольУчастника = 1 Тогда
			РеквизитОрганизация = "СсылкаТитулГрузоотправителяПеревозчик";
			РеквизитИдентификатор = "ИдентификаторПеревозчика";
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Струкутра = Новый Структура;
	Струкутра.Вставить("РольУчастника", РольУчастника);
	Струкутра.Вставить("РеквизитОрганизация", РеквизитОрганизация);
	Струкутра.Вставить("РеквизитИдентификатор", РеквизитИдентификатор);
	
	//@skip-check constructor-function-return-section
	Возврат Струкутра;
	
КонецФункции

// Возвращает признак установки роли пользователя без учета документов по индексу.
// 
// Параметры:
//  Роль - Число - Роль.
//  Индекс - Неопределено, Число - Индекс.
// 
// Возвращаемое значение:
//  Булево - Признак установки роли пользователя без учета документов по индексу.
Функция РольПользователяБезУчетаДокументовПоИндексуУстановлена(Роль, Индекс) Экспорт
	
	Возврат ПроверитьБит(Роль, Индекс);
	
КонецФункции

#КонецОбласти

#Область КлючевыеПоля

// Структура ключевых полей для поиска МП.
// 
// Возвращаемое значение:
//  Структура - Структура ключевых полей для поиска МП:
// * Фамилия - Неопределено 
// * Имя - Неопределено
// * Отчество - Неопределено
// * СерияВУ - Неопределено
// * НомерВУ - Неопределено
// * ДатаВыдачиВУ - Неопределено
// * ИНН - Неопределено
// * СНИЛС - Неопределено
Функция СтруктураКлючевыхПолейДляПоискаМП() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("Фамилия");
	Структура.Вставить("Имя");
	Структура.Вставить("Отчество");
	Структура.Вставить("СерияВУ");
	Структура.Вставить("НомерВУ");
	Структура.Вставить("ДатаВыдачиВУ");
	Структура.Вставить("ИНН");
	Структура.Вставить("СНИЛС");
	
	//@skip-check constructor-function-return-section
	Возврат Структура;
	
КонецФункции

// Соответствие наименований ключевых полей для поиска МП.
// 
// Возвращаемое значение:
//  Соответствие из Строка - Соответствие наименований ключевых полей для поиска МП.
Функция СоответствиеНаименованийКлючевыхПолейДляПоискаМП() Экспорт
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить("Фамилия", "Фамилия");
	Соответствие.Вставить("Имя", "Имя");
	Соответствие.Вставить("Отчество", "Отчество");
	Соответствие.Вставить("СерияВУ", "Серия В/У");
	Соответствие.Вставить("НомерВУ", "Номер В/У");
	Соответствие.Вставить("ДатаВыдачиВУ", "Дата выдачи В/У");
	Соответствие.Вставить("ИНН", "ИНН");
	Соответствие.Вставить("СНИЛС", "СНИЛС");
	
	Возврат Соответствие;
	
КонецФункции

// Формирует и возвращает соответствие, в котором содержатся различия ключевых полей.
//
// Параметры:
//  КоллекцияПолейМП		 - Произвольный	 - Произвольная коллекция с полями соответсвующими регистру сведений "ПодключенныеМПЭПД".
//  СтруктураКлючевыхПолей	 - Структура	 - Структура с ключевыми полями для поиска МП.
// 
// Возвращаемое значение:
//  Соответствие из Строка - Соответствие где ключ - это имя поля, значение - это структура с ключами "ПоДаннымМП" и "КлючевыеПоля".
//
Функция СоответствиеРазличийКлючевыхПолей(КоллекцияПолейМП, СтруктураКлючевыхПолей) Экспорт
	                             
	Соответствие = Новый Соответствие;

	МассивИсключенныхИзПроверкиПолей = Новый Массив;
	МассивИсключенныхИзПроверкиПолей.Добавить("Фамилия");
	МассивИсключенныхИзПроверкиПолей.Добавить("Имя");
	МассивИсключенныхИзПроверкиПолей.Добавить("Отчество");
	
	Для Каждого КлючЗначение Из СтруктураКлючевыхПолей Цикл
		Поле = КлючЗначение.Ключ;
		
		Если МассивИсключенныхИзПроверкиПолей.Найти(Поле) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеКлючевогоПоля = КлючЗначение.Значение;
		ЗначениеПоляУстройства = КоллекцияПолейМП[Поле];
		
		Если ЗначениеЗаполнено(ЗначениеКлючевогоПоля)
			И ЗначениеЗаполнено(ЗначениеПоляУстройства)
			И ЗначениеКлючевогоПоля <> ЗначениеПоляУстройства Тогда
			
			Структура = Новый Структура("ПоДаннымМП, КлючевыеПоля", ЗначениеПоляУстройства, ЗначениеКлючевогоПоля);
			Соответствие.Вставить(Поле, Структура);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Соответствие;
	
КонецФункции

#КонецОбласти

#Область ВизуальноеОформлениеМПНаФормахДокументов

// Возвращает информацию, необходимую для отображения данных МП на форме титула.
//
// Параметры:
//	Форма			 - ФормаКлиентскогоПриложения                    -
//  Документ		 - ДокументСсылка 								 - 
//  РольУчастника	 - Число 										 - Роль участника из документа. Может равняться 0, тогда считается, что роль неопределена.
//  ОткрытыйТитул	 - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО - Необязательный. Заполняется если открывается новый титул.
//																	   Если не заполнен, открытый титул определяется из соответствия "ДанныеМП".
// 
// Возвращаемое значение:
//  Структура - Информация необходимая для отображения титула:
//	* ДокументДоступенВМП - Булево
//	* НаФормеТитулаОтражаетсяМП - Булево
//	* ЗаголовокПолей - Строка
//	
//@skip-check constructor-function-return-section
Функция НастройкиОтображенияИнформацииПоМП(Форма, Документ, РольУчастника, ОткрытыйТитул = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ДокументДоступенВМП", Ложь);
	Результат.Вставить("НаФормеТитулаОтражаетсяМП", Ложь);
	Результат.Вставить("ЗаголовокПолей", "");
	Результат.Вставить("НастройкиФормы");
	
	Если ОткрытыйТитул = Неопределено Тогда
		ТекущийТитул = Форма.ДанныеМП.Получить("ТекущийТитул");
	Иначе
		ТекущийТитул = ОткрытыйТитул;
	КонецЕсли;
	
	Если Форма.ДанныеМП = Неопределено Тогда
		Соответствие = Новый Соответствие;
	Иначе
		Соответствие = Новый Соответствие(Форма.ДанныеМП);
	КонецЕсли;
	Соответствие.Вставить("ТекущийТитул", ТекущийТитул);
	Форма.ДанныеМП = Новый ФиксированноеСоответствие(Соответствие);
	
	ДокументИРольУчастникаПоддерживаетсяМП = ДокументИРольУчастникаПоддерживаетсяМП(Документ, РольУчастника);
	Если Не ДокументИРольУчастникаПоддерживаетсяМП Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
		
		Результат.Вставить("ДокументДоступенВМП", Истина);
		Результат.Вставить("ЗаголовокПолей", "По данным водителя");	
		Результат.Вставить("НаФормеТитулаОтражаетсяМП", Истина);
		ДокументДляНастроекФормы = "ЭлектроннаяТранспортнаяНакладная";

	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист") Тогда
		
		Результат.Вставить("ДокументДоступенВМП", Истина);
		Результат.Вставить("НаФормеТитулаОтражаетсяМП", Истина);
		ДокументДляНастроекФормы = "ЭлектронныйПутевойЛист";
		
		Если ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2")
			Или ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6") Тогда
			Результат.Вставить("ЗаголовокПолей", "По данным медработника");
		ИначеЕсли ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3") Тогда
			Результат.Вставить("ЗаголовокПолей", "По данным контролера");
		ИначеЕсли ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4")
			Или ТекущийТитул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5") Тогда
			Результат.Вставить("ЗаголовокПолей", "По данным ответственного за показания одометра");
		Иначе
			Результат.Вставить("ЗаголовокПолей", "По данным водителя");
		КонецЕсли;
		
	КонецЕсли;
	
	НастройкиФорм = НастройкиФормМП(Форма);
	Если НастройкиФорм = Неопределено Тогда
		Результат.Вставить("НастройкиФормы", Неопределено);
	Иначе
		Результат.Вставить("НастройкиФормы", НастройкиФорм[ДокументДляНастроекФормы]);
	КонецЕсли;
	
	Возврат Результат;	
	
КонецФункции

// Возвращает текст запроса данных документа отправленного из МП.
// 
// Возвращаемое значение:
//  Строка - Текст запроса.
//  
//@skip-check ql-temp-table-index
//@skip-check doc-comment-description-ends-on-dot
Функция ТекстЗапросаДанныхДокументаПоВерсииМП() Экспорт
	
	Возврат 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиВзаимодействияМПЭПД.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	НастройкиВзаимодействияМПЭПД.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_УчетныеЗаписи
	|ИЗ
	|	РегистрСведений.НастройкиВзаимодействияМПЭПД КАК НастройкиВзаимодействияМПЭПД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КэшВзаимодействияМПЭПД.Идентификатор КАК Идентификатор,
	|	КэшВзаимодействияМПЭПД.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	КэшВзаимодействияМПЭПД.ВидСообщения КАК ВидСообщения,
	|	КэшВзаимодействияМПЭПД.Прочитано КАК Прочитано,
	|	КэшВзаимодействияМПЭПД.ДатаПолучения КАК ДатаПолучения,
	|	КэшВзаимодействияМПЭПД.Исходящее КАК Исходящее,
	|	КэшВзаимодействияМПЭПД.ИдентификаторМП КАК ИдентификаторМП,
	|	КэшВзаимодействияМПЭПД.Содержимое КАК Содержимое,
	|	КэшВзаимодействияМПЭПД.ДанныеСообщения КАК ДанныеСообщения,
	|	КэшВзаимодействияМПЭПД.Дата КАК Дата,
	|	КэшВзаимодействияМПЭПД.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	КэшВзаимодействияМПЭПД.ИдентификаторРодителя КАК ИдентификаторРодителя,
	|	КэшВзаимодействияМПЭПД.ДокументЭПД КАК ДокументЭПД,
	|	ВЫБОР
	|		КОГДА КэшВзаимодействияМПЭПД.ВидСообщения В (ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаВодителя), ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаВодителя))
	|			ТОГДА ""Водитель""
	|		КОГДА КэшВзаимодействияМПЭПД.ВидСообщения В (ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаДоРейса), ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМедработникаДоРейса))
	|			ТОГДА ""МедработникДоРейса""
	|		КОГДА КэшВзаимодействияМПЭПД.ВидСообщения В (ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаПослеРейса), ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМедработникаПослеРейса))
	|			ТОГДА ""МедработникПослеРейса""
	|		КОГДА КэшВзаимодействияМПЭПД.ВидСообщения В (ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрДоРейса), ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаОтветственногоЗаОдометрДоРейса))
	|			ТОГДА ""ОдометрДоРейса""
	|		КОГДА КэшВзаимодействияМПЭПД.ВидСообщения В (ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрПослеРейса), ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаОтветственногоЗаОдометрПослеРейса))
	|			ТОГДА ""ОдометрПослеРейса""
	|		КОГДА КэшВзаимодействияМПЭПД.ВидСообщения В (ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрДоРейса), ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаОтветственногоЗаОдометрДоРейса))
	|			ТОГДА ""Механик""
	|		ИНАЧЕ ""Сообщение""
	|	КОНЕЦ КАК ЛогическийВидСообщения
	|ПОМЕСТИТЬ ВТ_ВзаимодействияПоДокументу
	|ИЗ
	|	РегистрСведений.КэшВзаимодействияМПЭПД КАК КэшВзаимодействияМПЭПД
	|ГДЕ
	|	КэшВзаимодействияМПЭПД.ДокументЭПД = &Документ
	|	И КэшВзаимодействияМПЭПД.ВидСообщения В(&МассивТиповВзаимодействий)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВзаимодействияПодокументу.ЛогическийВидСообщения КАК ЛогическийВидСообщения,
	|	МАКСИМУМ(ВТ_ВзаимодействияПодокументу.Дата) КАК Дата
	|ПОМЕСТИТЬ ВТ_ПоследниеПривязкиОтвязкиПользователей
	|ИЗ
	|	ВТ_ВзаимодействияПоДокументу КАК ВТ_ВзаимодействияПодокументу
	|ГДЕ
	|	ВТ_ВзаимодействияПодокументу.ВидСообщения <> ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.Сообщение)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ВзаимодействияПодокументу.ЛогическийВидСообщения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВзаимодействияПодокументу.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_СобытияПривязкиОтвязкиПользователя
	|ИЗ
	|	ВТ_ВзаимодействияПоДокументу КАК ВТ_ВзаимодействияПодокументу
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПоследниеПривязкиОтвязкиПользователей КАК ВТ_ПоследниеПривязкиОтвязкиПользователей
	|		ПО ВТ_ВзаимодействияПодокументу.Дата = ВТ_ПоследниеПривязкиОтвязкиПользователей.Дата
	|			И ВТ_ВзаимодействияПодокументу.ЛогическийВидСообщения = ВТ_ПоследниеПривязкиОтвязкиПользователей.ЛогическийВидСообщения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВзаимодействияПодокументу.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТ_СобытияВходящихСообщений
	|ИЗ
	|	ВТ_ВзаимодействияПоДокументу КАК ВТ_ВзаимодействияПодокументу
	|ГДЕ
	|	ВТ_ВзаимодействияПодокументу.ВидСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.Сообщение)
	|	И ВТ_ВзаимодействияПодокументу.Исходящее = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ВзаимодействияПодокументу.ДанныеСообщения КАК ДанныеСообщения,
	|	ВТ_ВзаимодействияПодокументу.ДатаПолучения КАК ДатаПолучения,
	|	ВТ_ВзаимодействияПодокументу.Дата КАК Дата,
	|	ВТ_ВзаимодействияПодокументу.Содержимое КАК Содержимое,
	|	ВТ_ВзаимодействияПодокументу.ВидСообщения КАК ВидСообщения,
	|	ВТ_ВзаимодействияПодокументу.ИдентификаторМП КАК ИдентификаторМП,
	|	ВТ_ВзаимодействияПодокументу.Прочитано КАК Прочитано,
	|	ПодключенныеМПЭПД.Наименование КАК МобильноеПриложение,
	|	ПодключенныеМПЭПД.Телефоны КАК Телефоны,
	|	ПодключенныеМПЭПД.Идентификатор КАК Идентификатор,
	|	ПодключенныеМПЭПД.Фото КАК Фото,
	|	ПодключенныеМПЭПД.Роль КАК Роль,
	|	ПодключенныеМПЭПД.ВидыДокументов КАК ВидыДокументов,
	|	ПодключенныеМПЭПД.Удален КАК Удален,
	|	НЕ ВТ_СобытияПривязкиОтвязкиПользователя.Идентификатор ЕСТЬ NULL КАК ЭтоСобытиеПривязкиОтвязкиПользователя,
	|	НЕ ВТ_СобытияВходящихСообщений.Идентификатор ЕСТЬ NULL КАК ЭтоСообщение
	|ИЗ
	|	ВТ_ВзаимодействияПоДокументу КАК ВТ_ВзаимодействияПодокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СобытияПривязкиОтвязкиПользователя КАК ВТ_СобытияПривязкиОтвязкиПользователя
	|		ПО ВТ_ВзаимодействияПодокументу.Идентификатор = ВТ_СобытияПривязкиОтвязкиПользователя.Идентификатор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СобытияВходящихСообщений КАК ВТ_СобытияВходящихСообщений
	|		ПО ВТ_ВзаимодействияПодокументу.Идентификатор = ВТ_СобытияВходящихСообщений.Идентификатор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодключенныеМПЭПД КАК ПодключенныеМПЭПД
	|		ПО ВТ_ВзаимодействияПодокументу.ИдентификаторЭДО = ПодключенныеМПЭПД.ИдентификаторЭДО
	|			И ВТ_ВзаимодействияПодокументу.ИдентификаторМП = ПодключенныеМПЭПД.ИдентификаторМП
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УчетныеЗаписи КАК ВТ_УчетныеЗаписи
	|		ПО ВТ_ВзаимодействияПодокументу.ИдентификаторЭДО = ВТ_УчетныеЗаписи.ИдентификаторЭДО
	|ГДЕ
	|	(НЕ ВТ_СобытияПривязкиОтвязкиПользователя.Идентификатор ЕСТЬ NULL
	|			ИЛИ НЕ ВТ_СобытияВходящихСообщений.Идентификатор ЕСТЬ NULL)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтоСообщение УБЫВ,
	|	ВТ_ВзаимодействияПодокументу.Дата УБЫВ";
	
КонецФункции

// Возвращает массив типов взаимодействий: привязка, отвязка, сообщение.
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ТипыВзаимодействияМПЭПД - Массив типов взаимодействий. 
//
Функция МассивТиповВзаимодействийПривязокОтвязокИСообщений() Экспорт
	
	Массив = Новый Массив;
	
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.Сообщение"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаВодителя"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаДоРейса"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаПослеРейса"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрДоРейса"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрПослеРейса"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМеханика"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМедработникаДоРейса"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМедработникаПослеРейса"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаОтветственногоЗаОдометрДоРейса"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаОтветственногоЗаОдометрПослеРейса"));
	Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМеханика"));
	
	Возврат Массив;
	
КонецФункции

// Возвращает тип привязки/отвязки.
//
// Параметры:
//  Документ			 - ДокументСсылка	 								 			- ЭПД.
//  ЭтоДействиеПривязки	 - Булево	 										 			- Определяет какое действие выполняется - привязка или отвязка пользователя.
//  Роль				 - Неопределено, Число	 										- Необязательное. Служит для определения типа привязки. Игнорируется если заполнено ИндексРабот. см. АтомарныеРолиМП().
//  Титул				 - Неопределено, ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО	- Необязательное. Используется для определения привязки/отвязки для доп. ролей. Игнорируется если заполнено ИндексРабот.
//  ИндексРабот			 - Неопределено, Число	 										- Необязательное. Если заполнено, то ИндексАтомарнойРоли и Титул игнорируются. 
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыВзаимодействияМПЭПД, Неопределено - Тип взаимодействия привязки/отвязки пользователя.
//
Функция ТипПривязкиОтвязки(Документ, ЭтоДействиеПривязки, Роль = Неопределено, Титул = Неопределено, ИндексРабот = Неопределено) Экспорт
	
    ТипПривязкиОтвязки = Неопределено;
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
		// Для ЭТрН возможна только привязка/отвязка водителя
		Если ЭтоДействиеПривязки Тогда
			ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаВодителя");
		Иначе
			ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаВодителя");
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист") Тогда
		
		Если Роль = Pow(2, ИндексАтомарнойРолиВодителя())
			Или ИндексРабот = ИндексРаботыВодитель() Тогда
			
			Если ЭтоДействиеПривязки Тогда
				ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаВодителя");
			Иначе
				ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаВодителя");
			КонецЕсли;
			
		ИначеЕсли Роль = Pow(2, ИндексАтомарнойРолиМеханика())
			Или ИндексРабот = ИндексРаботыМеханик() Тогда
			
			Если ЭтоДействиеПривязки Тогда
				ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМеханика");
			Иначе
				ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМеханика");
			КонецЕсли;
			
		ИначеЕсли Роль = Pow(2, ИндексАтомарнойРолиМедработника())
			Или ИндексРабот = ИндексРаботыМедработникДоРейса() 
			Или ИндексРабот = ИндексРаботыМедработникПослеРейса() Тогда
			
			Если ЭтоДействиеПривязки Тогда
				Если Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2")
					Или ИндексРабот = ИндексРаботыМедработникДоРейса() Тогда
					ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаДоРейса");
				ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6")
					Или ИндексРабот = ИндексРаботыМедработникПослеРейса() Тогда
					ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаПослеРейса");
				КонецЕсли;
			Иначе
				Если Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2")
					Или ИндексРабот = ИндексРаботыМедработникДоРейса() Тогда
					ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМедработникаДоРейса");
				ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6")
					Или ИндексРабот = ИндексРаботыМедработникПослеРейса() Тогда
					ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМедработникаПослеРейса");
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли Роль = Pow(2, ИндексАтомарнойРолиОтветственногоЗаОдометр())
			Или ИндексРабот = ИндексРаботыПоказанияОдометраДоРейса() 
			Или ИндексРабот = ИндексРаботыПоказанияОдометраПослеРейса() Тогда
			
			Если ЭтоДействиеПривязки Тогда
				Если Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4")
					Или ИндексРабот = ИндексРаботыПоказанияОдометраДоРейса() Тогда
					ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрДоРейса");
				ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5")
					Или ИндексРабот = ИндексРаботыПоказанияОдометраПослеРейса() Тогда
					ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрПослеРейса");
				КонецЕсли;
			Иначе
				Если Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4")
					Или ИндексРабот = ИндексРаботыПоказанияОдометраДоРейса() Тогда
					ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаОтветственногоЗаОдометрДоРейса");
				ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5")
					Или ИндексРабот = ИндексРаботыПоказанияОдометраПослеРейса() Тогда
					ТипПривязкиОтвязки = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаОтветственногоЗаОдометрПослеРейса");
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТипПривязкиОтвязки;
	
КонецФункции

// Возвращает признак того, что переданный тип взаимодействия является привязкой документа.
//
// Параметры:
//  ТипВзаимодействияМПЭПД	 - ПеречислениеСсылка.ТипыВзаимодействияМПЭПД 
// 
// Возвращаемое значение:
//  Булево - 
//
Функция ЭтоПривязкаДокумента(ТипВзаимодействияМПЭПД) Экспорт

	Возврат ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаВодителя")
		Или ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаДоРейса")
		Или ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаПослеРейса")
		Или ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрДоРейса")
		Или ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрПослеРейса")
		Или ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМеханика");

КонецФункции

// Возвращает признак того, что переданный тип взаимодействия является сообщением (уведомлением).
//
// Параметры:
//  ТипВзаимодействияМПЭПД	 - ПеречислениеСсылка.ТипыВзаимодействияМПЭПД 
// 
// Возвращаемое значение:
//  Булево - Признак того, что переданный тип взаимодействия является сообщением (уведомлением).
//
Функция ЭтоТипВзаимодействияСообщение(ТипВзаимодействияМПЭПД) Экспорт

	Возврат ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.Сообщение");

КонецФункции

// Возвращает индекс работы по типу взаимодействия.
// 
// Параметры:
//  ТипВзаимодействияМПЭПД - ПеречислениеСсылка.ТипыВзаимодействияМПЭПД - Тип взаимодействия МПЭПД.
// 
// Возвращаемое значение:
//  Число - Индекс работы по типу взаимодействия
Функция ИндексРаботыПоТипуВзаимодействия(ТипВзаимодействияМПЭПД) Экспорт
	
	Если ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаВодителя") 
		Или ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаВодителя") Тогда
		Возврат ИндексРаботыВодитель();
		
	ИначеЕсли ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаДоРейса") 
		Или ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМедработникаДоРейса") Тогда
		Возврат ИндексРаботыМедработникДоРейса();
		
	ИначеЕсли ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаПослеРейса") 
		Или ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМедработникаПослеРейса") Тогда
		Возврат ИндексРаботыМедработникПослеРейса();
		
	ИначеЕсли ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрДоРейса") 
		Или ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаОтветственногоЗаОдометрДоРейса") Тогда
		Возврат ИндексРаботыПоказанияОдометраДоРейса();
		
	ИначеЕсли ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрПослеРейса") 
		Или ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаОтветственногоЗаОдометрПослеРейса") Тогда
		Возврат ИндексРаботыПоказанияОдометраПослеРейса();
		
	ИначеЕсли ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМеханика")
		Или ТипВзаимодействияМПЭПД = ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМеханика") Тогда
		Возврат ИндексРаботыМеханик();
		
	КонецЕсли;
	
КонецФункции

// Возвращает индекс работы по титулу.
// 
// Параметры:
//  Титул - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
// 
// Возвращаемое значение:
//  Число - Индекс работы по титулу
Функция ИндексРаботыПоТитулу(Титул) Экспорт
	
	Если Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул2") Тогда
		Возврат ИндексРаботыВодитель();
		
	ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул4")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_ДопТитул4") Тогда
		Возврат ИндексРаботыВодитель();
		
	ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2") Тогда
		Возврат ИндексРаботыМедработникДоРейса();
		
	ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3") Тогда
		Возврат ИндексРаботыМеханик();
		
	ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4") Тогда
		Возврат ИндексРаботыПоказанияОдометраДоРейса();
		
	ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5") Тогда
		Возврат ИндексРаботыПоказанияОдометраПослеРейса();
		
	ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6") Тогда
		Возврат ИндексРаботыМедработникПослеРейса();
		
	КонецЕсли;
	
КонецФункции

// Возвращает индекс работы водителя.
// 
// Возвращаемое значение:
//  Число - Индекс работы водитель.
Функция ИндексРаботыВодитель() Экспорт
	
	Возврат 1;
	
КонецФункции

// Возвращает индекс работы медработника до рейса.
// 
// Возвращаемое значение:
//  Число - Индекс работы медработник до рейса.
Функция ИндексРаботыМедработникДоРейса() Экспорт
	
	Возврат 2;
	
КонецФункции

// Возвращает индекс работы медработник апосле рейса.
// 
// Возвращаемое значение:
//  Число - Индекс работы медработник после рейса.
Функция ИндексРаботыМедработникПослеРейса() Экспорт
	
	Возврат 3;
	
КонецФункции

// Возвращает индекс работы внесения показаний одометра до рейса.
// 
// Возвращаемое значение:
//  Число - Индекс работы показания одометра до рейса.
Функция ИндексРаботыПоказанияОдометраДоРейса() Экспорт
	
	Возврат 4;
	
КонецФункции

// Возвращает индекс работы внесения показаний одометра после рейса.
// 
// Возвращаемое значение:
//  Число - Индекс работы показания одометра после рейса.
Функция ИндексРаботыПоказанияОдометраПослеРейса() Экспорт
	
	Возврат 5;
	
КонецФункции

// Возвращает индекс работы механика.
// 
// Возвращаемое значение:
//  Число - Индекс работы механик.
Функция ИндексРаботыМеханик() Экспорт
	
	Возврат 6;
	
КонецФункции

// Массив доступных индексов работ по роли участника.
// 
// Параметры:
//  Документ - ОпределяемыйТип.ДокументыЭПД
//  РольУчастника- Число
// 
// Возвращаемое значение:
//  Массив из Число - Массив доступных индексов работ по роли участника
Функция МассивДоступныхИндексовРаботПоРолиУчастника(Документ, РольУчастника) Экспорт
	
	МассивРолей = Новый Массив;
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
		// Перевозчик
		Если РольУчастника = 1 Тогда
			МассивРолей.Добавить(ИндексРаботыВодитель());
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист") Тогда
		// Оформитель
		Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 1) Тогда
			МассивРолей.Добавить(ИндексРаботыВодитель());
		КонецЕсли;
		
		// Медосмотр
		Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 2) Тогда
			МассивРолей.Добавить(ИндексРаботыМедработникДоРейса());
			МассивРолей.Добавить(ИндексРаботыМедработникПослеРейса());
		КонецЕсли;
		
		// Техконтроль
		Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 4) Тогда
			МассивРолей.Добавить(ИндексРаботыМеханик());
		КонецЕсли;
		
		// Показания одометра
		Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 8) Тогда
			МассивРолей.Добавить(ИндексРаботыПоказанияОдометраДоРейса());
			МассивРолей.Добавить(ИндексРаботыПоказанияОдометраПослеРейса());
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат МассивРолей;
	
КонецФункции

// Массив доступных видов взаимодействия по титулу.
// 
// Параметры:
//  Титул - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ТипыВзаимодействияМПЭПД - Массив доступных видов взаимодействия по титулу.
Функция МассивДоступныхВидовВзаимодействияПоТитулу(Титул) Экспорт

	Массив = Новый Массив;
	
	Если Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул1")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул2")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул4")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_ДопТитул4")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул3")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул4")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул5")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул6")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул7")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул8")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул9")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_ОткП")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_ОткТ3")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_ОткГО")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_ДопТитул3")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_ДопТитул4")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_ДопТитул7")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_ДопТитул8")
		Или Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_ДопТитул9") Тогда
		
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаВодителя"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаВодителя"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.Сообщение"));
		
	ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул1") Тогда
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаВодителя"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаВодителя"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.Сообщение"));
		
	ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2") Тогда
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаДоРейса"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМедработникаДоРейса"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.Сообщение"));
		
	ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3") Тогда
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМеханика"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМеханика"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.Сообщение"));
		
	ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4") Тогда
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрДоРейса"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаОтветственногоЗаОдометрДоРейса"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.Сообщение"));
		
	ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5") Тогда
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрПослеРейса"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаОтветственногоЗаОдометрПослеРейса"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.Сообщение"));
		
	ИначеЕсли Титул = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6") Тогда
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаПослеРейса"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.ОтвязкаМедработникаПослеРейса"));
		Массив.Добавить(ПредопределенноеЗначение("Перечисление.ТипыВзаимодействияМПЭПД.Сообщение"));
		
	КонецЕсли;
	
	Возврат Массив;

КонецФункции

// Возвращает признак того, что индекс работы доступен для роли участника.
// 
// Параметры:
//  Документ - ОпределяемыйТип.ДокументыЭПД
//  РольУчастника - Число
//  ИндексРаботы - Число
// 
// Возвращаемое значение:
//  Булево - Признак того, что индекс работы доступен для роли участника.
Функция ИндексРаботыДоступенДляРолиУчастника(Документ, РольУчастника, ИндексРаботы) Экспорт
	
	МассивИндексов = МассивДоступныхИндексовРаботПоРолиУчастника(Документ, РольУчастника);
	ИндексРаботыДоступен = МассивИндексов.Найти(ИндексРаботы) <> Неопределено;
	Возврат ИндексРаботыДоступен;
	
КонецФункции

// Возвращает наименование работ по индексу.
//
// Параметры:
//  ИндексРабот	 - Число
// 
// Возвращаемое значение:
//  Строка - Наименование работ по индексу.
//
Функция НаименованиеРаботПоИндексу(ИндексРабот) Экспорт
	
	Если ИндексРабот = ИндексРаботыВодитель() Тогда
		Возврат НСтр("ru = 'Водитель';
					|en = 'Driver'");
		
	ИначеЕсли ИндексРабот = ИндексРаботыМедработникДоРейса() Тогда
		Возврат НСтр("ru = 'Медосмотр перед рейсом';
					|en = 'Medical examination before the trip'");
		
	ИначеЕсли ИндексРабот = ИндексРаботыМедработникПослеРейса() Тогда
		Возврат НСтр("ru = 'Медосмотр после рейса';
					|en = 'Medical examination after the trip'");
		
	ИначеЕсли ИндексРабот = ИндексРаботыПоказанияОдометраДоРейса() Тогда
		Возврат НСтр("ru = 'Внесение показаний одометра до рейса';
					|en = 'Enter odometer readings before the trip'");
		
	ИначеЕсли ИндексРабот = ИндексРаботыПоказанияОдометраПослеРейса() Тогда
		Возврат НСтр("ru = 'Внесение показаний одометра после рейса';
					|en = 'Enter odometer readings after the trip'");
		
	ИначеЕсли ИндексРабот = ИндексРаботыМеханик() Тогда
		Возврат НСтр("ru = 'Техосмотр';
					|en = 'Technical inspection'");
		
	КонецЕсли;
	
КонецФункции

// Возвращает картинку работ по индексу.
//
// Параметры:
//  ИндексРабот	 - Число
//  МПВыбрано	 - Булево
// 
// Возвращаемое значение:
//  Картинка - Картинка, отражающая работы.
//
Функция КартинкаРаботПоИндексу(ИндексРабот, МПВыбрано) Экспорт
	
	Если ИндексРабот = ИндексРаботыВодитель() И МПВыбрано Тогда
		Возврат БиблиотекаКартинок.РаботыВодителяВыбраныМПЭПД;
		
	ИначеЕсли ИндексРабот = ИндексРаботыВодитель() И Не МПВыбрано Тогда
		Возврат БиблиотекаКартинок.РаботыВодителяМПЭПД;
		
	ИначеЕсли ИндексРабот = ИндексРаботыМедработникДоРейса() И МПВыбрано Тогда
		Возврат БиблиотекаКартинок.МедосмотрПередРейсомВыбранМПЭПД;
		
	ИначеЕсли ИндексРабот = ИндексРаботыМедработникДоРейса() И Не МПВыбрано Тогда
		Возврат БиблиотекаКартинок.МедосмотрПередРейсомМПЭПД;
		
	ИначеЕсли ИндексРабот = ИндексРаботыМедработникПослеРейса() И МПВыбрано Тогда
		Возврат БиблиотекаКартинок.МедосмотрПослеРейсаВыбранМПЭПД;
		
	ИначеЕсли ИндексРабот = ИндексРаботыМедработникПослеРейса() И Не МПВыбрано Тогда
		Возврат БиблиотекаКартинок.МедосмотрПослеРейсаМПЭПД;
		
	ИначеЕсли ИндексРабот = ИндексРаботыПоказанияОдометраДоРейса() И МПВыбрано Тогда
		Возврат БиблиотекаКартинок.ПоказанияОдометраПередРейсомВыбраныМПЭПД;
		
	ИначеЕсли ИндексРабот = ИндексРаботыПоказанияОдометраДоРейса() И Не МПВыбрано Тогда
		Возврат БиблиотекаКартинок.ПоказанияОдометраПередРейсомМПЭПД;
		
	ИначеЕсли ИндексРабот = ИндексРаботыПоказанияОдометраПослеРейса() И МПВыбрано Тогда
		Возврат БиблиотекаКартинок.ПоказанияОдометраПослеРейсаВыбраныМПЭПД;
		
	ИначеЕсли ИндексРабот = ИндексРаботыПоказанияОдометраПослеРейса() И Не МПВыбрано Тогда
		Возврат БиблиотекаКартинок.ПоказанияОдометраПослеРейсаМПЭПД;
		
	ИначеЕсли ИндексРабот = ИндексРаботыМеханик() И МПВыбрано Тогда
		Возврат БиблиотекаКартинок.РаботыПоТехосмотруВыбраныМПЭПД;
		
	ИначеЕсли ИндексРабот = ИндексРаботыМеханик() И Не МПВыбрано Тогда
		Возврат БиблиотекаКартинок.РаботыПоТехосмотруМПЭПД;
		
	КонецЕсли;
	
КонецФункции

// Возвращает массив индексов работ по документу.
//
// Параметры:
//  Документ - ДокументСсылка, Строка	 - Документ, по которому необходимо получить индексы.
// 
// Возвращаемое значение:
//  Массив из Число - Массив индексов.
//
Функция МассивИндексовРаботПоДокументу(Документ) Экспорт

	МассивИндексовРаботПоДокументу = Новый Массив;
	
	РольУчастника = Неопределено;
	РольУчастникаПоддерживаетсяМП = ДокументИРольУчастникаПоддерживаетсяМП(Документ, РольУчастника);
	
	Если Не РольУчастникаПоддерживаетсяМП Тогда
		Возврат МассивИндексовРаботПоДокументу;
	КонецЕсли;

	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная")
		Или (ТипЗнч(Документ) = Тип("Строка") И Документ = "ЭлектроннаяТранспортнаяНакладная") Тогда
		МассивИндексовРаботПоДокументу.Добавить(ИндексРаботыВодитель());
		
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист")
		Или (ТипЗнч(Документ) = Тип("Строка") И Документ = "ЭлектронныйПутевойЛист") Тогда
		МассивИндексовРаботПоДокументу.Добавить(ИндексРаботыВодитель());
		МассивИндексовРаботПоДокументу.Добавить(ИндексРаботыМедработникДоРейса());
		МассивИндексовРаботПоДокументу.Добавить(ИндексРаботыМедработникПослеРейса());
		МассивИндексовРаботПоДокументу.Добавить(ИндексРаботыПоказанияОдометраДоРейса());
		МассивИндексовРаботПоДокументу.Добавить(ИндексРаботыПоказанияОдометраПослеРейса());
		МассивИндексовРаботПоДокументу.Добавить(ИндексРаботыМеханик());
		
	КонецЕсли;
	
	Возврат МассивИндексовРаботПоДокументу;
	
КонецФункции

// Возвращает структуру данных, описывающую работу опредленного МП.
// 
// Возвращаемое значение:
//  Структура - Структура описывающая работу:
//	* ИдентификаторМП - Строка
//	* ИдентификаторЗаписиМП - Строка
//	* ИндексРабот - Число
//	* БылоПринятоСообщениеОтМП - Булево
//
Функция НоваяСтруктураДанныхПоРаботеМП() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("ИдентификаторМП", "");
	Структура.Вставить("ИдентификаторЗаписиМП", "");
	Структура.Вставить("ИндексРабот", 0);
	Структура.Вставить("ПолученТитулНевозможноИзменитьМП", Ложь);
	
	//@skip-check constructor-function-return-section
	Возврат Структура;
	
КонецФункции

Процедура УстановитьВидимостьПанелиМобильныхПриложений(Форма, Видимость) Экспорт
	
	//@skip-check bsl-legacy-check-dynamic-feature-access
	Форма.Элементы.ГруппаПанельМПОбщая.Видимость = Видимость;
	
КонецПроцедуры

Процедура УстановитьОформлениеМобильныхПриложений(Форма, НастройкиОтображенияИнформацииПоМП) Экспорт
	
	//@skip-check bsl-legacy-check-dynamic-feature-access
	Элементы = Форма.Элементы;
	Ссылка = Форма.Объект.Ссылка;
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		УстановитьВидимостьПанелиМобильныхПриложений(Форма, Ложь);
		Возврат;
	КонецЕсли;
	
	НаФормеТитулаОтражаетсяМП = НастройкиОтображенияИнформацииПоМП.НаФормеТитулаОтражаетсяМП;
	// Настройка для конкретного документа, полученная по ключу из структуры настроек форм для всех документов,
	// см. НастройкиФормПоУмолчанию().
	Если НастройкиОтображенияИнформацииПоМП.НастройкиФормы = Неопределено Тогда
		ИндексыРаботОтображаемыеМП = Новый Соответствие;
	Иначе
		ИндексыРаботОтображаемыеМП = НастройкиОтображенияИнформацииПоМП.НастройкиФормы.ИндексыРаботОтображаемыеМП;
	КонецЕсли;
	МассивИндексовРаботПоДокументу = МассивИндексовРаботПоДокументу(Ссылка);
	
	ЕстьОтображаемыеМП = Ложь;
	Для Каждого ИндексРабот Из МассивИндексовРаботПоДокументу Цикл
		
		НужноОтражатьРаботуДляМППоНастройкам = ИндексыРаботОтображаемыеМП.Получить(ИндексРабот) = Истина;
		РаботаПроводитсяВОрганизации = ИндексРаботыДоступенДляРолиУчастника(Ссылка,
			Форма.Объект.РольУчастника, ИндексРабот);
		РаботаМПОграниченаДляОтображенияПоРоли = РаботаМПОграниченаДляОтображенияПоРоли(Ссылка,
			Форма.Объект.РольУчастника, РаботаПроводитсяВОрганизации);
			
		НужноОтражатьРаботуДляМПНаФормеТитула = НаФормеТитулаОтражаетсяМП
												И НужноОтражатьРаботуДляМППоНастройкам
												И Не РаботаМПОграниченаДляОтображенияПоРоли;
		
		ИмяЭлемента = ИмяСИндексом(ИндексРабот, "ГруппаМобильноеПриложение");
		//@skip-check bsl-legacy-check-dynamic-feature-access
		Элементы[ИмяЭлемента].Видимость = НужноОтражатьРаботуДляМПНаФормеТитула;
		
		Если Не НужноОтражатьРаботуДляМПНаФормеТитула Тогда
			Продолжить;
		Иначе
			ЕстьОтображаемыеМП = Истина;
		КонецЕсли;
		
		УстановитьОформлениеЭлементовМобильногоПриложения(Форма, ИндексРабот, РаботаПроводитсяВОрганизации);
		
	КонецЦикла;
	
	УстановитьВидимостьПанелиМобильныхПриложений(Форма, ЕстьОтображаемыеМП);
	
КонецПроцедуры

// Устанавливает оформление элементов МП.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//  ИндексРабот - Число, Произвольный - Индекс работ
//  РаботаПроводитсяВОрганизации - Булево - Работа проводится в организации
//
//@skip-check bsl-legacy-check-dynamic-feature-access
//@skip-check doc-comment-description-ends-on-dot
Процедура УстановитьОформлениеЭлементовМобильногоПриложения(Форма, ИндексРабот, РаботаПроводитсяВОрганизации) Экспорт
	
	Элементы = Форма.Элементы;
	ДанныеМП = Форма.ДанныеМП;
	
	МПЗаполнено = ДанныеМП <> Неопределено И ДанныеМП.Получить(ИндексРабот) <> Неопределено 
		И ЗначениеЗаполнено(ДанныеМП.Получить(ИндексРабот).ИдентификаторМП);
		
	// Отображение картинки не выбранного МП
	ИмяЭлемента = ИмяСИндексом(ИндексРабот, "МПФото");
	Если Не МПЗаполнено Тогда
		МПВыбрано = Ложь;
		Если ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() Тогда
			КартинкаРаботПоИндексу = КартинкаРаботПоИндексу(ИндексРабот, МПВыбрано);
			АдресФото = СервисВзаимодействияМПЭПДВызовСервера.ПоместитьКартинкуВоВременноеХранилище(
				КартинкаРаботПоИндексу, Форма.УникальныйИдентификатор);
		Иначе
			ДвоичныеДанныеКартинки = КартинкаРаботПоИндексу(ИндексРабот, МПВыбрано).ПолучитьДвоичныеДанные();
			АдресФото = ПоместитьВоВременноеХранилище(ДвоичныеДанныеКартинки, Форма.УникальныйИдентификатор);
		КонецЕсли;
		
		Форма[ИмяЭлемента] = АдресФото;
	КонецЕсли;
	
	// Наименование МП
	ИмяЭлемента = ИмяСИндексом(ИндексРабот, "МПНаименование");
	ИмяЭлементаКнопка = ИмяСИндексом(ИндексРабот, "Подключаемый_ОткрытьМП");
	Если Не РаботаПроводитсяВОрганизации Тогда
		Если ИндексРабот = ИндексРаботыВодитель() Тогда
			ТекстУстройства = НСтр("ru = 'Перевозка выполняется сторонней организацией';
									|en = 'Transportation is performed by a third party'");
		Иначе
			ТекстУстройства = НСтр("ru = 'Проводится сотрудником сторонней организации';
									|en = 'Performed by a third party employee'");
		КонецЕсли;
		Форма.Элементы[ИмяЭлементаКнопка].Заголовок = ТекстУстройства;
		Форма.Элементы[ИмяЭлементаКнопка].Доступность = Ложь;
		Форма.Элементы[ИмяЭлементаКнопка].ЦветТекста = Новый Цвет;			
	ИначеЕсли Не МПЗаполнено Тогда
		Форма.Элементы[ИмяЭлементаКнопка].Заголовок = НСтр("ru = 'Устройство не привязано';
															|en = 'The device is not linked'");
		Форма.Элементы[ИмяЭлементаКнопка].Доступность = Ложь;
		Форма.Элементы[ИмяЭлементаКнопка].ЦветТекста = Новый Цвет;			
	Иначе
		СкоращенноеНаименованиеМП = Лев(Форма[ИмяЭлемента], 40);
		Если СкоращенноеНаименованиеМП = Форма[ИмяЭлемента] Тогда
			НаименованиеМП = СкоращенноеНаименованиеМП;
		Иначе
			НаименованиеМП = СтрШаблон("%1...", СкоращенноеНаименованиеМП);
		КонецЕсли;
		Форма.Элементы[ИмяЭлементаКнопка].Заголовок = НаименованиеМП;
		Форма.Элементы[ИмяЭлементаКнопка].Доступность = Истина;
		ЦветТекста = СервисВзаимодействияМПЭПДВызовСервера.ЦветТекстаНаименованияВыбранногоМПЭПД();
		Форма.Элементы[ИмяЭлементаКнопка].ЦветТекста = ЦветТекста;			
	КонецЕсли;
	
	// Кнопка выбор МП
	ИмяЭлемента = ИмяСИндексом(ИндексРабот, "КомандаВыбратьМП");
	ИмяЭлементаВставка = ИмяСИндексом(ИндексРабот, "ВставкаВместоКомандыВыбратьМП");
	Если Не РаботаПроводитсяВОрганизации Тогда
		Элементы[ИмяЭлемента].Видимость = Ложь;
		Элементы[ИмяЭлементаВставка].Видимость = Истина;
	Иначе
		Элементы[ИмяЭлемента].Видимость = Истина;
		Элементы[ИмяЭлементаВставка].Видимость = Ложь;
		Если МПЗаполнено Тогда
			Элементы[ИмяЭлемента].Картинка = КартинкаИзменитьМПЭПД();
		Иначе
			Элементы[ИмяЭлемента].Картинка = КартинкаПривязатьМПЭПД();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Определяет и возвращет данные МП, связанные с определнной командой.
//
// Параметры:
//  Форма					 - ФормаКлиентскогоПриложения - 
//  ЭлементКомандаВыбратьМП	 - ДекорацияФормы, КомандаФормы - 
// 
// Возвращаемое значение:
//  Структура - см. НоваяСтруктураДанныхПоРаботеМП():
//	* ИдентификаторМП - Строка
//	* ИдентификаторЗаписиМП - Строка
//	* ИндексРабот - Число
//	* БылоПринятоСообщениеОтМП - Булево
//
Функция ПолучитьДанныеМП(Форма, ЭлементКомандаВыбратьМП) Экспорт
	
	//@skip-check bsl-legacy-check-dynamic-feature-access
	Имя = ЭлементКомандаВыбратьМП.Имя;
	Если СтрНайти(Имя, "КомандаВыбратьМП") Тогда
		ИндексРабот = Число(СтрЗаменить(Имя, "КомандаВыбратьМП", ""));
	ИначеЕсли СтрНайти(Имя, "Подключаемый_ОткрытьМП") Тогда
		ИндексРабот = Число(СтрЗаменить(Имя, "Подключаемый_ОткрытьМП", ""));
	КонецЕсли;
	ДанныеМП = Форма.ДанныеМП;
	СведенияОМПЗаполнены = ДанныеМП <> Неопределено И ДанныеМП.Получить(ИндексРабот) <> Неопределено;
	Если Не СведенияОМПЗаполнены Тогда
		СтруктураДанныхПоРаботеМП = НоваяСтруктураДанныхПоРаботеМП();
		СтруктураДанныхПоРаботеМП.ИндексРабот = ИндексРабот;
	Иначе
		СтруктураДанныхПоРаботеМП = ДанныеМП.Получить(ИндексРабот);
	КонецЕсли;
	
	Возврат СтруктураДанныхПоРаботеМП;
	
КонецФункции

// Возвращает параметры отвязки МП на форме.
//
// Параметры:
//  ИндексРабот	 - Число - 
// 
// Возвращаемое значение:
//  Структура - Параметры отвязки:
//  * ИндексРабот - Число
//
Функция ПараметрыОтвязкиМПНаФорме(ИндексРабот) Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("ИндексРабот", ИндексРабот);
	
	Возврат Структура;
	
КонецФункции

// Отвязывает МП от документа визуально, на форме.
//
// Параметры:
//  Форма			 - ФормаКлиентскогоПриложения - 
//  ПараметрыОтвязки - Структура - см. ПараметрыОтвязкиМПНаФорме().
//
Процедура ОтвязатьМПНаФорме(Форма, ПараметрыОтвязки) Экспорт
	
	ИндексРабот = ПараметрыОтвязки.ИндексРабот;
	Соответствие = Новый Соответствие(Форма.ДанныеМП);
	Соответствие.Удалить(ИндексРабот);
	Форма.ДанныеМП = Новый ФиксированноеСоответствие(Соответствие);
	НастройкиОтображенияИнформацииПоМП = НастройкиОтображенияИнформацииПоМП(Форма,
		Форма.Объект.Ссылка, Форма.Объект.РольУчастника);
	УстановитьОформлениеМобильныхПриложений(Форма, НастройкиОтображенияИнформацииПоМП);
	
КонецПроцедуры

// Возвращает параметры привязки МП на форме.
//
// Параметры:
//  СтруктураДанныхПоРаботеМП	- Структура - см. НоваяСтруктураДанныхПоРаботеМП().
//  МПНаименование				- Строка	-
//  МПФото						- Строка	-
// 
// Возвращаемое значение:
//  Структура - Структура привязки МП:
//  * СтруктураДанныхПоРаботеМП - Структура - см. НоваяСтруктураДанныхПоРаботеМП():
//  ** ИдентификаторМП - Строка
//	** ИдентификаторЗаписиМП - Строка
//	** ИндексРабот - Число
//	** БылоПринятоСообщениеОтМП - Булево
//	* МПНаименование - Строка
//	* МПФото - Строка - Ссылка на фотографию.
//
Функция ПараметрыПривязкиМПНаФорме(СтруктураДанныхПоРаботеМП, МПНаименование, МПФото) Экспорт
	                            
	Структура = Новый Структура;
	Структура.Вставить("СтруктураДанныхПоРаботеМП", СтруктураДанныхПоРаботеМП);
	Структура.Вставить("МПНаименование", МПНаименование);
	Структура.Вставить("МПФото", МПФото);
	
	Возврат Структура;
	
КонецФункции

// Привязывает МП к документу визуально, на форме.
//
// Параметры:
//  Форма			 	- ФормаКлиентскогоПриложения - 
//  ПараметрыПривязки	- Структура	 - см. ПараметрыПривязкиМПНаФорме().
//
Процедура ПривязатьМПНаФорме(Форма, ПараметрыПривязки) Экспорт
	
	Соответствие = Новый Соответствие(Форма.ДанныеМП);
	Соответствие.Вставить(ПараметрыПривязки.СтруктураДанныхПоРаботеМП.ИндексРабот,
		ПараметрыПривязки.СтруктураДанныхПоРаботеМП);
	Форма.ДанныеМП = Новый ФиксированноеСоответствие(Соответствие);
	
	ИндексРабот = ПараметрыПривязки.СтруктураДанныхПоРаботеМП.ИндексРабот;
	МассивЭлементовОбновления = Новый Массив;
	
	ИмяРеквизита = ИмяСИндексом(ИндексРабот, "МПНаименование");
	Форма[ИмяРеквизита] = ПараметрыПривязки.МПНаименование;
	
	ИмяРеквизита = ИмяСИндексом(ИндексРабот, "МПФото");
	Форма[ИмяРеквизита] = ПараметрыПривязки.МПФото;
	//@skip-check bsl-legacy-check-dynamic-feature-access
	МассивЭлементовОбновления.Добавить(Форма.Элементы[ИмяРеквизита]);
	
	//@skip-check bsl-legacy-check-dynamic-feature-access
	Форма.ОбновитьОтображениеДанных(МассивЭлементовОбновления);
	
	РаботаПроводитсяВОрганизации = Истина;
	УстановитьОформлениеЭлементовМобильногоПриложения(Форма, ИндексРабот, РаботаПроводитсяВОрганизации);
	
КонецПроцедуры

// Возвращает настройки форм по умолчанию.
// 
// Возвращаемое значение:
//  Структура - Настройки форм:
// * ЭлектроннаяТранспортнаяНакладная - Структура - :
// ** ИндексыРаботОтображаемыеМП - Соответствие из Число - 
// * ЭлектронныйПутевойЛист - Структура - :
// ** ИндексыРаботОтображаемыеМП - Соответствие из Число - 
Функция НастройкиФормПоУмолчанию() Экспорт
	
	ИндексВодитель = ИндексРаботыВодитель();
	
	СтруктураНастроекЭТрН = Новый Структура;
	// У ЭТрН по умолчанию не отображаются работы
	СоответствиеИндексовРабот = Новый Соответствие;
	СоответствиеИндексовРабот.Вставить(ИндексВодитель, Ложь);
	СтруктураНастроекЭТрН.Вставить("ИндексыРаботОтображаемыеМП", СоответствиеИндексовРабот);
	
	СтруктураНастроекЭПЛ = Новый Структура;
	// У ЭПЛ по умолчанию не отображаются работы
	СоответствиеИндексовРабот = Новый Соответствие;
	СоответствиеИндексовРабот.Вставить(ИндексВодитель, Ложь);
	СтруктураНастроекЭПЛ.Вставить("ИндексыРаботОтображаемыеМП", СоответствиеИндексовРабот);
	
	СтруктураНастроекПоДокументам = Новый Структура;
	СтруктураНастроекПоДокументам.Вставить("ЭлектроннаяТранспортнаяНакладная", СтруктураНастроекЭТрН);
	СтруктураНастроекПоДокументам.Вставить("ЭлектронныйПутевойЛист", СтруктураНастроекЭПЛ);
	
	Возврат СтруктураНастроекПоДокументам;
	
КонецФункции

#КонецОбласти

#Область ВидПодписи

// Возвращает признак того, что вид подписи является УКЭП.
//
// Параметры:
//  ВидПодписи - ПеречислениеСсылка.ВидыПодписиМПЭПД
// 
// Возвращаемое значение:
//  Булево - Признак того, что вид подписи является УКЭП.
//
Функция ЭтоКЭП(ВидПодписи) Экспорт
	
	Возврат ВидПодписи = ПредопределенноеЗначение("Перечисление.ВидыПодписиМПЭПД.УКЭПГосключ")
		Или ВидПодписи = ПредопределенноеЗначение("Перечисление.ВидыПодписиМПЭПД.УКЭПРутокен");
	
КонецФункции

// Возвращает признак того, что переданный вид подписи доступен для роли пользователя МП и вида обмена.
// Если вид подписи не доступен, формирует ошибку.
// 
// Параметры:
//  ВидПодписи - ПеречислениеСсылка.ВидыПодписиМПЭПД - Вид подписи.
//  ПрямойОбмен - Булево - Прямой обмен.
//  РольПользователяМПБезУчетаДокументов - Число - Роль пользователя МП без учета документов.
// 
// Возвращаемое значение:
//  Структура - Данные о доступности вида подписи для роли и вида обмена:
// * ВидПодписиДоступен - Булево - 
// * ТекстОшибки - Строка - 
Функция ВидПодписиДоступенДляРолиИВидаОбмена(ВидПодписи, ПрямойОбмен, РольПользователяМПБезУчетаДокументов) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ВидПодписиДоступен", Истина);
	Результат.Вставить("ТекстОшибки", "");
	
	АтомарныеРолиМП = АтомарныеРолиМП();
	ИндексРолиМедработник = АтомарныеРолиМП.Найти(НСтр("ru = 'Медработник';
														|en = 'Healthcare worker'"));
	
	ЕстьРольМедработник = РольПользователяБезУчетаДокументовПоИндексуУстановлена(
		РольПользователяМПБезУчетаДокументов, ИндексРолиМедработник);
		
	Если ЕстьРольМедработник Тогда
		Если Не ЭтоКЭП(ВидПодписи) И ПрямойОбмен Тогда
			Результат.ВидПодписиДоступен = Ложь;
			Результат.ТекстОшибки = НСтр("ru = 'Для медработника, при отправке документов напрямую другим участникам, необходимо использовать УКЭП';
										|en = 'Healthcare workers need to use an advanced qualified electronic signature when sending documents directly to other participants'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ДокументыФизЛицПользователейМП

// Возвращает данные лицензии на медосмотр строкой JSON.
// 
// Параметры:
//  Серия - Строка - Серия.
//  Номер - Строка - Номер.
//  ДатаВыдачи - Дата - Дата выдачи.
//  СрокДействия - Дата - Срок действия.
// 
// Возвращаемое значение:
//  Строка - Данные лицензии на медосмотр строкой JSON.
Функция ДанныеЛицензииНаМедосмотрСтрокой(Серия, Номер, ДатаВыдачи, СрокДействия) Экспорт
	
	ВидУдостоверяющегоДокумента = ВидУдостоверяющегоДокументаЛицензияНаПроведениеМедосмотра();
	
	Структура = НоваяСтруктураДокументовПользователяМП();
	Структура.Серия = Серия;
	Структура.Номер = Номер;
	Структура.ДатаВыдачи = ДатаВыдачи;
	Структура.СрокДействия = СрокДействия;
	Структура.ВидУдостоверяющегоДокумента = ВидУдостоверяющегоДокумента;
	
	#Если ВебКлиент Тогда
		ПараметрыПреобразования = Неопределено;
		ФорматДатыISO = Истина;
		Возврат СервисВзаимодействияМПЭПДВызовСервера.СтруктураВJSON(Структура, ПараметрыПреобразования, ФорматДатыISO);
	#Иначе
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		НастройкиСериализации = Новый НастройкиСериализацииJSON;
		НастройкиСериализации.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
		ЗаписатьJSON(ЗаписьJSON, Структура, НастройкиСериализации);
		
		Возврат ЗаписьJSON.Закрыть();	
	#КонецЕсли
	
КонецФункции

// Возвращает срукутуру лицензии на медосмотр из строки.
// 
// Параметры:
//  ЛицензияНаМедосмотрСтрокой - Строка - Лицензия на медосмотр строкой JSON.
// 
// Возвращаемое значение:
//  Структура - см. НоваяСтруктураДокументовПользователяМП():
// * Вид - Строка
// * Серия - Строка
// * Номер - Строка
// * ДатаВыдачи - Дата
// * СрокДействия - Дата
//
//@skip-check constructor-function-return-section
Функция СрукутураЛицензииНаМедосмотрИзСтроки(ЛицензияНаМедосмотрСтрокой) Экспорт
	
	Если Не ЗначениеЗаполнено(ЛицензияНаМедосмотрСтрокой) Тогда
		Возврат НоваяСтруктураДокументовПользователяМП();
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		Возврат СервисВзаимодействияМПЭПДВызовСервера.СтруктураИзJson(ЛицензияНаМедосмотрСтрокой);
	#Иначе
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ЛицензияНаМедосмотрСтрокой);

		МассивСвойствДата = Новый Массив;
		МассивСвойствДата.Добавить("ДатаВыдачи");
		МассивСвойствДата.Добавить("СрокДействия");

		Структура = ПрочитатьJSON(ЧтениеJSON, Ложь, МассивСвойствДата, ФорматДатыJSON.ISO);

		Возврат Структура;
	#КонецЕсли
		
КонецФункции

// Возвращает признак того, что документ физического лица для отправки в МП заполнен.
// 
// Параметры:
//  СтруктураДокумента - Структура - см. НоваяСтруктураДокументовПользователяМП():
// * Вид - Строка
// * Серия - Строка
// * Номер - Строка
// * ДатаВыдачи - Дата
// * СрокДействия - Дата
// 
// Возвращаемое значение:
//  Булево - Признак того, что документ физического лица для отправки в МП заполнен.
Функция ДокументФизическогоЛицаДляОтправкиВМПЗаполнен(СтруктураДокумента) Экспорт
	
	Возврат ЗначениеЗаполнено(СтруктураДокумента.Серия)
			И ЗначениеЗаполнено(СтруктураДокумента.Номер)
			И ЗначениеЗаполнено(СтруктураДокумента.ДатаВыдачи)
			И ЗначениеЗаполнено(СтруктураДокумента.СрокДействия);
	
КонецФункции

#КонецОбласти

// Возвращает признак того, что сам документ и роль участника в этом документе поддерживается в МП.
//
// Параметры:
//  Документ 		- ДокументСсылка, Строка - Ссылка на документ или его имя строкой.
//  РольУчастника 	- Число, Неопределено 	 - Если Неопределено или 0 тогда роль учатника не учитывается. 
// 
// Возвращаемое значение:
//  Булево - 
//
Функция ДокументИРольУчастникаПоддерживаетсяМП(Документ, РольУчастника = Неопределено) Экспорт
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная")
		Или (ТипЗнч(Документ) = Тип("Строка") И Документ = "ЭлектроннаяТранспортнаяНакладная") Тогда
		Если РольУчастника = Неопределено Или РольУчастника = 0 Тогда
			Возврат Истина;
		КонецЕсли;
		МассивРазрешенныхРолейУчастникаВМП = Новый Массив;
		МассивРазрешенныхРолейУчастникаВМП.Добавить(1);
		ДокументИРольПоддерживается = МассивРазрешенныхРолейУчастникаВМП.Найти(РольУчастника) <> Неопределено;
		
	ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист")
		Или (ТипЗнч(Документ) = Тип("Строка") И Документ = "ЭлектронныйПутевойЛист") Тогда
		Если РольУчастника = Неопределено Или РольУчастника = 0 Тогда
			Возврат Истина;
		КонецЕсли;
		МассивРазрешенныхРолейУчастникаВМП = Новый Массив;
		МассивРазрешенныхРолейУчастникаВМП.Добавить(1);
		МассивРазрешенныхРолейУчастникаВМП.Добавить(2);
		МассивРазрешенныхРолейУчастникаВМП.Добавить(4);
		МассивРазрешенныхРолейУчастникаВМП.Добавить(8);
		
		Для Каждого РазрешеннаяРоль Из МассивРазрешенныхРолейУчастникаВМП Цикл
			ДокументИРольПоддерживается = ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, РазрешеннаяРоль);
			Если ДокументИРольПоддерживается Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ДокументИРольПоддерживается;
	
КонецФункции

// Возвращает признак того, что титул поддерживается мобильным приложением.
// 
// Параметры:
//  ПрефиксТитула - Строка - Префикс титула.
// 
// Возвращаемое значение:
//  Булево - Признак того, что титул поддерживается мобильным приложением.
Функция ТитулПоддерживаетсяМобильнымПриложением(ПрефиксТитула) Экспорт
	
	Возврат ПрефиксТитула = "ТитулПеревозчикаПриемка"
		Или ПрефиксТитула = "ТитулПеревозчикаВыдача"
		Или ПрефиксТитула = "ТитулВыпуск"
		Или ПрефиксТитула = "ТитулВыезд"
		Или ПрефиксТитула = "ТитулЗаезд"
		Или ПрефиксТитула = "ТитулМедосмотрПосле";
	
	КонецФункции
	
Процедура ДобавитьНеПустоеЗначениеВМассив(Массив, Значение) Экспорт

	Если ЗначениеЗаполнено(Значение) Тогда
		Массив.Добавить(Значение);
	КонецЕсли;

КонецПроцедуры

// Возвращает имя с добавленным индексом.
// 
// Параметры:
//  Индекс - Число, Произвольный - Индекс
//  ИмяЭлемента - Строка - Имя элемента
// 
// Возвращаемое значение:
//  Строка - Имя с индексом.
Функция ИмяСИндексом(Индекс, ИмяЭлемента) Экспорт
	
	Возврат СтрШаблон("%1%2", ИмяЭлемента, Индекс);
	
КонецФункции

// Возвращает соответствие в котором ключ и значение меняются местами.
// 
// Параметры:
//  ТекущееСоответствие - Соответствие из Произвольный - Текущее соответствие.
// 
// Возвращаемое значение:
//  Соответствие из Произвольный - Соответствие в котором ключ и значение меняются местами.
Функция ПеревернутьСоответствие(ТекущееСоответствие) Экспорт
	
	Результат = Новый Соответствие;
	
	Для Каждого СтрокаКлюча Из ТекущееСоответствие Цикл
		Результат.Вставить(СтрокаКлюча.Ключ, СтрокаКлюча.Значение);
		Результат.Вставить(СтрокаКлюча.Значение, СтрокаКлюча.Ключ);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РольДелимаяПоДокументам()
	
	АтомарныеРолиМП = АтомарныеРолиМП();
	РольВодитель = АтомарныеРолиМП.Найти(НСтр("ru = 'Водитель';
												|en = 'Driver'"));
	Возврат Pow(2, РольВодитель);
	
КонецФункции

Функция ДокументПоддерживаетсяМП(ДокументЭПД)
	
	Результат = Ложь;
	
	Если ТипЗнч(ДокументЭПД) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная")
		Или ДокументЭПД = ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.ЭТрН") Тогда
		Результат = Истина;
	ИначеЕсли ТипЗнч(ДокументЭПД) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист")
		Или ДокументЭПД = ПредопределенноеЗначение("Перечисление.ТипыДокументовЭДО.ЭПЛ") Тогда
		Результат = Истина; 
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает настройки форм МП. Если настройки получить невозможно (МП не отображается) возвращается Неопределено.
//
// Параметры:
//  Форма	 - ФормаКлиентскогоПриложения
// 
// Возвращаемое значение:
//  Неопределено, Структура - Структура с настройками форм см. НастройкиФормПоУмолчанию():
//  * ЭлектроннаяТранспортнаяНакладная - Структура:
//  ** ИндексыРаботОтображаемыеМП - Соответствие Из Число
//  * ЭлектронныйПутевойЛист - Структура:
//  ** ИндексыРаботОтображаемыеМП - Соответствие Из Число 
//
Функция НастройкиФормМП(Форма)
	              
	Объект = Форма.Объект;
	Ссылка = Объект.Ссылка;
	
	СтруктураИнформацииОРеквизитах = ИнформацияОРеквизитахПоРолиУчастникаПодерживаемыхМП(Форма);
	Если СтруктураИнформацииОРеквизитах = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Отправитель = Объект[СтруктураИнформацииОРеквизитах.РеквизитОрганизация];
	ИдентификаторЭДО = Объект[СтруктураИнформацииОРеквизитах.РеквизитИдентификатор];
	ВидДокумента = Объект.ВидДокумента;
	
	МассивПолучателей = Новый Массив;
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист") Тогда
		Если Форма.ВариантМедосмотр = 1 И ЗначениеЗаполнено(Объект.СсылкаТитулОформлениеМедорганизация) Тогда
			МассивПолучателей.Добавить(Объект.СсылкаТитулОформлениеМедорганизация);
		КонецЕсли;
		Если Форма.ВариантТехконтроль = 1 И ЗначениеЗаполнено(Объект.СсылкаТитулОформлениеТехконтроль) Тогда
			МассивПолучателей.Добавить(Объект.СсылкаТитулОформлениеТехконтроль);
		КонецЕсли;
		Если Форма.ВариантПоказанияОдометра = 1 И ЗначениеЗаполнено(Объект.СсылкаТитулОформлениеПоказанияОдометра) Тогда
			МассивПолучателей.Добавить(Объект.СсылкаТитулОформлениеПоказанияОдометра);
		КонецЕсли;
		МассивПолучателей.Добавить(Объект.СсылкаТитулОформлениеОформитель);
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
		МассивПолучателей.Добавить(Объект.СсылкаТитулГрузоотправителяГрузоотправитель);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Отправитель)
		И Не ЗначениеЗаполнено(ИдентификаторЭДО)
		И МассивПолучателей.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	#Если Сервер Тогда
		НастройкиФорм = СервисВзаимодействияМПЭПД.ПолучитьНастройкиФормДляОформленияМП(Отправитель,
			ИдентификаторЭДО, МассивПолучателей, ВидДокумента);
	#Иначе
		НастройкиФорм = СервисВзаимодействияМПЭПДВызовСервера.ПолучитьНастройкиФормДляОформленияМП(Отправитель,
			ИдентификаторЭДО, МассивПолучателей, ВидДокумента);
	#КонецЕсли
	
	Возврат НастройкиФорм;
	
КонецФункции

Функция НоваяСтруктураДокументовПользователяМП()
	
	Структура = Новый Структура;
	Структура.Вставить("Вид");
	Структура.Вставить("Серия");
	Структура.Вставить("Номер");
	Структура.Вставить("ДатаВыдачи");
	Структура.Вставить("СрокДействия");
	Структура.Вставить("ВидУдостоверяющегоДокумента");
	
	Возврат Структура;
	
КонецФункции

Функция ВидУдостоверяющегоДокументаЛицензияНаПроведениеМедосмотра()

	Возврат "ЛицензияНаПроведениеМедосмотра";	
	
КонецФункции

#Область ВизуальноеОформлениеМПНаФормахДокументов

Функция РаботаМПОграниченаДляОтображенияПоРоли(Документ, РольУчастника, РаботаПроводитсяВОрганизации)
	
	// Не оформитель в документе "ЭлектронныйПутевойЛист"
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист")
		И Не ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(РольУчастника, 1) Тогда
		Возврат Не РаботаПроводитсяВОрганизации;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецОбласти
