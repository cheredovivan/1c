////////////////////////////////////////////////////////////////////////////////
//
// Серверные процедуры и функции регламентированных отчетов общего назначения 
// с кешируемым результатом на время сеанса.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция СостояниеТребованийДляПанели(Организация = Неопределено) Экспорт
	
	Подтвердить = ТребованияФНС.ТребующиеСкорогоПодтверждения(Организация).Количество();
	БезОтвета   = ТребованияФНС.ТребующиеСкорогоОтвета(Организация).Количество();
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Подтвердить", Подтвердить);
	ДополнительныеПараметры.Вставить("БезОтвета",   БезОтвета);
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

Функция ВидыТребований(Организация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ЖурналОтправокВКонтролирующиеОрганы.Ссылка.ВидДокумента КАК ВидДокумента
		|ИЗ
		|	РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
		|ГДЕ
		|	НЕ ЖурналОтправокВКонтролирующиеОрганы.ПометкаУдаления
		|	И ЖурналОтправокВКонтролирующиеОрганы.СтраницаЖурнала = ЗНАЧЕНИЕ(Перечисление.СтраницыЖурналаОтчетность.Входящие)
		|	И НЕ ЖурналОтправокВКонтролирующиеОрганы.Скрыт
		|	И ЖурналОтправокВКонтролирующиеОрганы.Ссылка ССЫЛКА Справочник.ДокументыРеализацииПолномочийНалоговыхОрганов
		|	И ЖурналОтправокВКонтролирующиеОрганы.Ссылка.ВидДокумента <> НЕОПРЕДЕЛЕНО";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ТребованияФНС.ДобавитьОтборВЗапросЖурнала(Запрос, "Организация", Организация);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выгрузка = РезультатЗапроса.Выгрузить();
	
	Выгрузка.Сортировать("ВидДокумента");
	
	ВидыДокументов = Выгрузка.ВыгрузитьКолонку("ВидДокумента");
	
	Возврат ВидыДокументов;
	
КонецФункции

Функция ПроизводственныйКалендарь() Экспорт

	Календарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
	Возврат Календарь;

КонецФункции

Функция ЕстьТребующиеПодтверждения(Организация = Неопределено) Экспорт
	
	Требования = ТребованияФНС.ТребующиеПодтверждения(Организация);
	Возврат Требования.Количество() > 0
	
КонецФункции

Функция ЕстьТребующиеСкорогоПодтверждения(Организация = Неопределено) Экспорт
	
	Требования = ТребованияФНС.ТребующиеСкорогоПодтверждения(Организация);
	Возврат Требования.Количество() > 0
	
КонецФункции

Процедура ВосстановитьСвойстваФайлов(ФайлыПослеОбработки, ФайлыДоОбработки) Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	
	Для каждого ОписаниеФайла Из ФайлыПослеОбработки Цикл
		
		Если ОбработкаФайловPDFКлиентСервер.ЭтоPDFФайл(ОписаниеФайла.Имя) Тогда
			
			Для каждого ФайлДоОбработки Из ФайлыДоОбработки Цикл
				
				ДвДанныеДо = ПолучитьИзВременногоХранилища(ФайлДоОбработки.Адрес);
				ДвДанныеПосле = ПолучитьИзВременногоХранилища(ОписаниеФайла.Адрес);
				
				ИмяДо = КонтекстЭДОСервер.ИмяФайлаБезРасширения(ФайлДоОбработки.Имя);
				ИмяПосле = КонтекстЭДОСервер.ИмяФайлаБезРасширения(ОписаниеФайла.Имя);
				
				Если ДвДанныеДо = ДвДанныеПосле И ИмяДо = ИмяПосле Тогда
					ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ОписаниеФайла, ФайлДоОбработки, Истина);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция РазмерПоАдресу(Адрес) Экспорт
	
	ДвДанные = ПолучитьИзВременногоХранилища(Адрес);
	Возврат ДвДанные.Размер();
	
КонецФункции

Функция СвойствоОтчета(Отчет, Имя) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Отчет) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
	
		УстановитьПривилегированныйРежим(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	*
			|ИЗ
			|	РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
			|ГДЕ
			|	ЖурналОтчетовСтатусы.Ссылка = &Отчет";
		
		Запрос.УстановитьПараметр("Отчет", Отчет);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			Возврат ВыборкаДетальныеЗаписи[Имя];
		КонецЕсли;
		
	Исключение
		// Не требует обработки. Может не быть отчетом и выпасть в исключение
		Возврат "";
	КонецПопытки;
	
КонецФункции

Функция ЭтоСообщениеОбИсчисленииИмущественныхНалогов(Требование) Экспорт

	Виды = Перечисления.ВидыНалоговыхДокументов;
	Вид  = Требование.ВидДокумента;

	ЭтоСообщениеОбИсчисленииИмущественныхНалогов = 
		(Вид = Виды.СообщениеОбИсчисленнойСуммеТранспортногоНалога
		Или Вид = Виды.СообщениеОбИсчисленнойСуммеТранспортногоИмущественногоЗемельногоНалогов
		Или Вид = Виды.СообщениеОбИсчисленнойСуммеЗемельногоНалога);

	Возврат ЭтоСообщениеОбИсчисленииИмущественныхНалогов;
	
КонецФункции

Функция ДатаОтменыПодтверждений() Экспорт
	
	Возврат Дата(2025, 2, 5);

КонецФункции

Функция ЭДООбязателенПоСсылке(Ссылка) Экспорт
	
	Данные = ТребованияФНС.ДанныеРегистраИзвлеченныеДанныеТребований(Ссылка);
	
	Результат = 
		ПолученоПозже5февраля2025(Ссылка)
		И ОтветилиНаВопросПроЭДО(Ссылка)
		И Данные.ЭДООбязателен;
	
	Возврат Результат;

КонецФункции

Функция ЭДОНеОбязателен(Ссылка) Экспорт
	
	Данные = ТребованияФНС.ДанныеРегистраИзвлеченныеДанныеТребований(Ссылка);
	
	Результат = 
		ПолученоПозже5февраля2025(Ссылка) 
		И НЕ Данные.ЭДООбязателен
		И Данные.ОтветилиНаВопросПроЭДО;
	
	Возврат Результат;

КонецФункции

Функция ОтветилиНаВопросПроЭДО(Ссылка) Экспорт
	
	Если ЭтоДокументРеализацииПолномочий(Ссылка) Тогда
		Данные = ТребованияФНС.ДанныеРегистраИзвлеченныеДанныеТребований(Ссылка);
		Возврат Данные.ОтветилиНаВопросПроЭДО;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Функция ОбязательностьЭДОНеОпределена(Источник) Экспорт
	
	Ссылка = ТребованиеУведомлениеПоСсылке(Источник);
	
	НеОпределена = ПолученоПозже5февраля2025(Ссылка) 
		И НЕ ОтветилиНаВопросПроЭДО(Ссылка) 
		И НЕ ЭДООбязателенПоСсылке(Ссылка);
		
	Возврат НеОпределена;
	
КонецФункции

Функция ПолученоПозже5февраля2025(Ссылка) Экспорт
	
	Возврат ПолученоПозжеДаты(Ссылка, ДатаОтменыПодтверждений());

КонецФункции

Функция ПолученоПозжеДаты(Ссылка, Дата) Экспорт
	
	ПолученоПозже = Ложь;
	
	ЭтоДокументРеализацииПолномочий = 
		ТипЗнч(Ссылка) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов")
		ИЛИ ТипЗнч(Ссылка) = Тип("СправочникОбъект.ДокументыРеализацииПолномочийНалоговыхОрганов");
		
	Если ЭтоДокументРеализацииПолномочий Тогда
			
		ПолученоПозже = (Ссылка.ДатаОтправки >= Дата
			ИЛИ ТребованияФНС.ДатаОтправкиТребования(Ссылка) >= Дата
			ИЛИ Ссылка.ДатаДокумента >= Дата);
			
	ИначеЕсли ЭтоОпись(Ссылка) Тогда
			
		ПолученоПозже = Ссылка.ДатаОтправки >= Дата;
		
	КонецЕсли;
	
	Возврат ПолученоПозже;

КонецФункции

Функция ЭтоОпись(Ссылка) Экспорт

	Возврат
		ТипЗнч(Ссылка) = Тип("СправочникСсылка.ОписиВходящихДокументовИзНалоговыхОрганов")
		ИЛИ ТипЗнч(Ссылка) = Тип("СправочникОбъект.ОписиВходящихДокументовИзНалоговыхОрганов");

КонецФункции

Функция СкорректированныеДнейДоОтвета(Требование, Знач ДнейДоОтвета) Экспорт
	
	// Может быть, если не распознали дату из pdf-файла
	Если НЕ ЗначениеЗаполнено(ДнейДоОтвета) Тогда
		Возврат ДнейДоОтвета;
	КонецЕсли;
	
	// Федеральный закон от 08.08.2024 N 259-ФЗ:
	// 8) пункт 4 статьи 31 дополнить абзацем следующего содержания:
	// "В случае направления налоговым органом налогоплательщику, указанному в абзаце первом пункта 5.1 
	//статьи 23 настоящего Кодекса, документа в электронной форме по телекоммуникационным каналам связи через 
	//оператора электронного документооборота датой его получения считается _шестой_ день со дня направления 
	//такого документа, указанного в подтверждении даты отправки электронного документа. Подтверждение даты 
	//отправки электронного документа направляется оператором электронного документооборота в налоговый орган 
	//не позднее дня, следующего за днем направления документа налогоплательщику.";
	
	Если ЭДООбязателенПоСсылке(Требование) 
		ИЛИ ЭДООбязательноПоОрганизации(Требование.Организация) = Истина 
		И ТребованияФНСВызовСервера.ПолученоПозже5февраля2025(Требование) Тогда
		
		ДнейДоОтвета = ДнейДоОтвета + 6;
		
	КонецЕсли;
	
	Возврат ДнейДоОтвета;
	
КонецФункции

Функция ТребованияВОписи(Опись, ВидДокумента = Неопределено) Экспорт
	
	МассивТребований = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ 
		|	ОписиВходящихДокументовИзНалоговыхОргановВходящиеДокументы.СсылкаНаОбъект
		|ИЗ
		|	Справочник.ОписиВходящихДокументовИзНалоговыхОрганов.ВходящиеДокументы КАК ОписиВходящихДокументовИзНалоговыхОргановВходящиеДокументы
		|ГДЕ
		|	ОписиВходящихДокументовИзНалоговыхОргановВходящиеДокументы.Ссылка = &Опись
		|	И ОписиВходящихДокументовИзНалоговыхОргановВходящиеДокументы.СсылкаНаОбъект.ВидДокумента в (&ВидДокументаТребование)";

	Запрос.УстановитьПараметр("Опись", Опись);
	
	ВидыДокументов = Новый Массив;
	Если ВидДокумента = Неопределено Тогда
		ВидыДокументов.Добавить(Перечисления.ВидыНалоговыхДокументов.ТребованиеОПредставленииДокументов);
		ВидыДокументов.Добавить(Перечисления.ВидыНалоговыхДокументов.ТребованиеОПредставленииПоясненийКДекларацииНДС);
	Иначе
		ВидыДокументов.Добавить(ВидДокумента);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ВидДокументаТребование", ВидыДокументов);

	Результат = Запрос.Выполнить();

	Выборка = Результат.Выбрать();

	Пока Выборка.Следующий() Цикл
		МассивТребований.Добавить(Выборка.СсылкаНаОбъект);
	КонецЦикла;
	
	Возврат МассивТребований;
	
КонецФункции

Функция УведомленияИзОписи(Опись) Экспорт
	
	Ссылки = ТребованияУведомленияИзОписи(Опись);
	Требования = ТребованияВОписи(Опись);
	
	Уведомления = ОбщегоНазначенияКлиентСервер.РазностьМассивов(Ссылки, Требования);
	Возврат Уведомления;
	
КонецФункции

Функция ТребованияУведомленияИзОписи(Опись) Экспорт

	Ссылки = Опись.ВходящиеДокументы.ВыгрузитьКолонку("СсылкаНаОбъект");
	
	Возврат Ссылки;

КонецФункции

Функция ТребованияУведомленияПоСсылке(Ссылка) Экспорт
	
	Если ЭтоОпись(Ссылка) Тогда
		Возврат ТребованияУведомленияИзОписи(Ссылка);
	Иначе
		Возврат ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка);
	КонецЕсли;
	
КонецФункции

Функция ТребованиеУведомлениеПоСсылке(Ссылка) Экспорт
	
	Если ЭтоОпись(Ссылка) Тогда
		Возврат ТребованияУведомленияИзОписи(Ссылка)[0];
	Иначе
		Возврат Ссылка;
	КонецЕсли;
	
КонецФункции

Функция УведомлениеИзОписи(Источник) Экспорт
	
	Если ТипЗнч(Источник) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") Тогда 
		
		Если ЭтоТребованиеФНС(Источник) Тогда
			Возврат Неопределено;
		Иначе
			Возврат Источник;
		КонецЕсли;
		
	Иначе
	
		Массив = УведомленияИзОписи(Источник);
		Если Массив.Количество() > 0 Тогда
			Возврат Массив[0];
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

Функция ТребованияПоСсылке(Источник) Экспорт
	
	Если ТипЗнч(Источник) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") Тогда
		Ссылки = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Источник);
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникСсылка.ЦиклыОбмена") Тогда
		Ссылки = ТребованияФНСВызовСервера.ТребованияВОписи(Источник.Предмет);
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникСсылка.ОписиВходящихДокументовИзНалоговыхОрганов") Тогда
		Ссылки = ТребованияФНСВызовСервера.ТребованияВОписи(Источник);
	КонецЕсли;
	
	Требования = Новый Массив;
	Для каждого Ссылка Из Ссылки Цикл
		Если ЭтоТребованиеФНС(Ссылка) Тогда 
			
			Требования.Добавить(Ссылка);
			
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Требования;
	
КонецФункции

Функция ТребованиеПоСсылке(Источник) Экспорт
	
	Массив = ТребованияПоСсылке(Источник);
	Если Массив.Количество() > 0 Тогда
		Возврат Массив[0];
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ЭтоТребованиеФНС(ДокументСсылка) Экспорт
	
	Возврат ДокументСсылка.ВидДокумента = Перечисления.ВидыНалоговыхДокументов.ТребованиеОПредставленииДокументов
		ИЛИ ДокументСсылка.ВидДокумента = Перечисления.ВидыНалоговыхДокументов.ТребованиеОПредставленииПоясненийКДекларацииНДС;
	
КонецФункции
	
Функция ЭтоДокументРеализацииПолномочий(Ссылка) Экспорт

	Возврат
		ТипЗнч(Ссылка) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов")
		ИЛИ ТипЗнч(Ссылка) = Тип("СправочникОбъект.ДокументыРеализацииПолномочийНалоговыхОрганов");

КонецФункции
	
Функция ЭтоКрупнейший(Организация) Экспорт

	Возврат ДокументооборотСКО.ЭтоКрупнейший(Организация);

КонецФункции

Функция ПолученоПозже5февраля2025ПоТС(ТранспортноеСообщение) Экспорт
	
	Если ЗначениеЗаполнено(ТранспортноеСообщение) Тогда
		
		Возврат ПолученоПозже5февраля2025ПоЦиклуОбмена(ТранспортноеСообщение.ЦиклОбмена);
		
	Иначе
	
		Возврат Ложь;
		
	КонецЕсли;

КонецФункции

Функция ПолученоПозже5февраля2025ПоЦиклуОбмена(ЦиклОбмена) Экспорт
	
	Если ЗначениеЗаполнено(ЦиклОбмена)
		И ЗначениеЗаполнено(ЦиклОбмена.Предмет) Тогда
		
		Опись = ЦиклОбмена.Предмет;
		Возврат ПолученоПозже5февраля2025(Опись);
		
	Иначе
	
		Возврат Ложь;
		
	КонецЕсли;

КонецФункции

Функция ЭДООбязательноПоТС(ТранспортноеСообщение) Экспорт
	
	Если ЗначениеЗаполнено(ТранспортноеСообщение) Тогда
		
		Возврат ЭДООбязательноПоЦиклуОбмена(ТранспортноеСообщение.ЦиклОбмена);
		
	Иначе
	
		Возврат Ложь;
		
	КонецЕсли;

КонецФункции

Функция ЭДООбязательноПоЦиклуОбмена(ЦиклОбмена) Экспорт
	
	Если ЗначениеЗаполнено(ЦиклОбмена)
		И ЗначениеЗаполнено(ЦиклОбмена.Организация) Тогда
		
		Организация = ЦиклОбмена.Организация;
		Возврат ЭДООбязательноПоОрганизации(Организация);
		
	Иначе
	
		Возврат Ложь;
		
	КонецЕсли;

КонецФункции

Функция ЭДООбязательноПоОрганизации(Организация) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОбязательностьЭДООрганизаций.ЭДООбязателен КАК ЭДООбязателен
		|ИЗ
		|	РегистрСведений.ОбязательностьЭДООрганизаций КАК ОбязательностьЭДООрганизаций
		|ГДЕ
		|	ОбязательностьЭДООрганизаций.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ЭДООбязателен;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Процедура СохранитьРезультатВыбораЭДОДляСсылкиИОрганизации(ВходящийКонтекст) Экспорт
	
	// Запомнить, орг, ссылка
	
	Ссылка = ВходящийКонтекст.Ссылка;
	Массив = ТребованияУведомленияПоСсылке(Ссылка);
	
	СохранитьРезультатВыбораЭДОДляСсылок(Массив, ВходящийКонтекст.Обязателен);
	
	Если ВходящийКонтекст.ЗапомнитьДляОрганизации Тогда
		СохранитьРезультатВыбораЭДОПоОрганизации(ВходящийКонтекст.Организация, ВходящийКонтекст.Обязателен);
	КонецЕсли;
	
КонецПроцедуры

Функция СохранитьРезультатВыбораЭДОПоОрганизации(Организация, Обязателен) Экспорт
	
	Возврат ТребованияФНС.СохранитьРезультатВыбораЭДОПоОрганизации(Организация, Обязателен);
	
КонецФункции

Функция ОписьСодержитТребованияПоВложению(ТранспортноеСообщение) Экспорт
	
	// извлекаем вложения расшифрованного входящего сообщения
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ВложенияДокумент = КонтекстЭДОСервер.ПолучитьВложенияТранспортногоСообщения(ТранспортноеСообщение, Истина, Перечисления.ТипыСодержимогоТранспортногоКонтейнера.Документ);
	
	Если ВложенияДокумент.Количество() = 0 Тогда
		ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(
			НСтр("ru = 'Не удалось разобрать опись входящих документов: некорректная структура входящего транспортного пакета.';
				|en = 'Не удалось разобрать опись входящих документов: некорректная структура входящего транспортного пакета.'"));		
		Возврат Неопределено;
	КонецЕсли;
	
	ВложениеДокумент = ВложенияДокумент[0];
	
	// считываем документ
	ВремФайлДокумент = ПолучитьИмяВременногоФайла();
	ВложениеДокумент.Данные.Получить().Записать(ВремФайлДокумент);
	
	СвойстваОписиВходящихДокументов = КонтекстЭДОСервер.ПолучитьСвойстваОписиВходящихДокументов(ТранспортноеСообщение, ВремФайлДокумент);
	
	ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ВремФайлДокумент);
	
	ТаблицаДокументов = СвойстваОписиВходящихДокументов.ТаблицаДокументов;
	
	Если ТаблицаДокументов.Количество() = 0 Тогда
		ДлительнаяОтправкаКлиентСервер.ВывестиОшибку(
			НСтр("ru = 'Не удалось разобрать опись входящих документов: не удалось разобрать файл документа из состава входящего пакета.';
				|en = 'Не удалось разобрать опись входящих документов: не удалось разобрать файл документа из состава входящего пакета.'"));
		Возврат Неопределено;
	КонецЕсли;
	
	Для каждого СтрокаДокумент Из ТаблицаДокументов Цикл
		ВидНалоговогоДокументаПоКНД = ТребованияФНС.ПолучитьВидНалоговогоДокументаПоКНД(СтрокаДокумент.РеквизитыДокумента.ВидДокумента);
		
		Если ВидНалоговогоДокументаПоКНД = Перечисления.ВидыНалоговыхДокументов.ТребованиеОПредставленииДокументов
		ИЛИ ВидНалоговогоДокументаПоКНД = Перечисления.ВидыНалоговыхДокументов.ТребованиеОПредставленииПоясненийКДекларацииНДС Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура СохранитьРезультатВыбораЭДОДляСсылок(Массив, Обязателен) Экспорт
	
	ТребованияФНС.СохранитьРезультатВыбораЭДОДляСсылок(Массив, Обязателен);
	
КонецПроцедуры

Процедура СохранитьРезультатВыбораЭДОДляСсылки(Ссылка, Обязателен) Экспорт
	
	Массив = ТребованияУведомленияПоСсылке(Ссылка);
	СохранитьРезультатВыбораЭДОДляСсылок(Массив, Обязателен);
	
КонецПроцедуры

Функция СохранитьОбязательностьЭДОДляНеподтвержденныхТребованийУведомлений(Организация, Обязателен) Экспорт
	
	Возврат ТребованияФНС.СохранитьОбязательностьЭДОДляНеподтвержденныхТребованийУведомлений(Организация, Обязателен);
	
КонецФункции

#КонецОбласти