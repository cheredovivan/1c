// @strict-types

#Область ПрограммныйИнтерфейс

// Определяет настройки учетной записи торговой площадки Яндекс Маркет.
//
// Параметры:
//   УчетнаяЗапись      - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису; 
//   СлужебныеПараметры - Булево - признак необходимости возврата в результате дополнительных параметров.
// 
// Возвращаемое значение:
//   см. ИнтеграцияСЯндексМаркетСервер.ДанныеУчетнойЗаписиЯндексМаркет
//
Функция ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись, СлужебныеПараметры = Истина) Экспорт

	Возврат ИнтеграцияСЯндексМаркетСервер.ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись, СлужебныеПараметры);

КонецФункции

// Определяет результат выполнения фонового задания.
// Если результат является структурой, дополнительно добавляет свойство КодОшибки (если оно отсутствует).
//
// Параметры:
//   АдресРезультата - Строка - адрес временного хранилища, в которое помещен результат работы фоновой процедуры.
// 
// Возвращаемое значение:
//   Произвольный - результат выполнения фонового задания.
//
Функция ПолучитьРезультатВыполненияФоновогоЗадания(АдресРезультата) Экспорт
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(Результат) = Тип("Структура") 
			И Не Результат.Свойство("КодОшибки") Тогда
		Результат.Вставить("КодОшибки", "");
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Подготавливает данные о доступных хозяйственных операциях.
//
// Параметры:
//   ПостфиксЗаголовкаФормы - Строка - значение, однозначно идентифицирующее владельца формы использующей данные о 
//                              доступных хозяйственных операциях.
// 
// Возвращаемое значение:
//   Структура - используемые хозяйственные операции:
//     * ПоставкиРеализация  - Соответствие из КлючИЗначение - описание хозяйственных операций раздела "Поставки" схемы реализации с отложенным переходом права собственности:
//       ** Ключ  - ПеречислениеСсылка.ХозяйственныеОперации - используемая хозяйственная операция;
//       ** Значение - Соответствие из КлючИЗначение - описание документов:
//          *** Ключ     - Строка - полное имя документа, для которого используется текущая хозяйственная операция;
//          *** Значение - Строка - заголовок формы при выборе текущей хозяйственной операции и вида документа.
//     * ПоставкиКомиссия  - Соответствие из КлючИЗначение - описание хозяйственных операций раздела "Поставки" комиссионной схемы:
//       ** Ключ  - ПеречислениеСсылка.ХозяйственныеОперации - используемая хозяйственная операция;
//       ** Значение - Соответствие из КлючИЗначение - описание документов:
//          *** Ключ     - Строка - полное имя документа, для которого используется текущая хозяйственная операция;
//          *** Значение - Строка - заголовок формы при выборе текущей хозяйственной операции и вида документа.
//     * ПродажиРеализация - Соответствие из КлючИЗначение - описание хозяйственных операций раздела "Продажи" схемы реализации с отложенным переходом права собственности:
//       ** Ключ  - ПеречислениеСсылка.ХозяйственныеОперации - используемая хозяйственная операция;
//       ** Значение - Соответствие из КлючИЗначение - описание документов:
//          *** Ключ     - Строка - полное имя документа, для которого используется текущая хозяйственная операция;
//          *** Значение - Строка - заголовок формы при выборе текущей хозяйственной операции и вида документа.
//     * ПродажиКомиссия   - Соответствие из КлючИЗначение - описание хозяйственных операций раздела "Продажи" комиссионной схемы:
//       ** Ключ  - ПеречислениеСсылка.ХозяйственныеОперации - используемая хозяйственная операция;
//       ** Значение - Соответствие из КлючИЗначение - описание документов:
//          *** Ключ     - Строка - полное имя документа, для которого используется текущая хозяйственная операция;
//          *** Значение - Строка - заголовок формы при выборе текущей хозяйственной операции и вида документа.
//
Функция ИспользуемыеХозяйственныеОперации(ПостфиксЗаголовкаФормы = "все") Экспорт
	
	ИспользуемыеХозОперации = Новый Структура;
	
	// Поставки. Реализация с отложенным переходом права собственности
	ХозОперации = Новый Соответствие;
	ШаблонЗаголовка = НСтр("ru = 'Документы поставок <%1>';
							|en = 'Shipment documents <%1>'");
	
		// Хозяйственная операция Оприходование товаров
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.ОприходованиеИзлишковТоваров.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(накладные)';
											|en = '(invoices)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.ОприходованиеТоваров, 
			ДокументыХозОперации);
		
		// Хозяйственная операция ПеремещениеТоваров
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.АктОРасхожденияхПослеПеремещения.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(расхождения)';
											|en = '(discrepancies)'"), 
				ПостфиксЗаголовкаФормы));
		ДокументыХозОперации.Вставить(Метаданные.Документы.ПеремещениеТоваров.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(расхождения)';
											|en = '(discrepancies)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.ПеремещениеТоваров,
			ДокументыХозОперации);
		
		// Хозяйственная операция ПересортицаТоваров
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.ПересортицаТоваров.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(расхождения)';
											|en = '(discrepancies)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.ПересортицаТоваров, 
			ДокументыХозОперации);
		
		// Хозяйственная операция ПорчаТоваров
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.ПорчаТоваров.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(расхождения)';
											|en = '(discrepancies)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.ПорчаТоваров, 
			ДокументыХозОперации);
		
		// Хозяйственная операция РазборкаТоваров
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.СборкаТоваров.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(расхождения)';
											|en = '(discrepancies)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.РазборкаТоваров, 
			ДокументыХозОперации);
		
		// Хозяйственная операция СборкаТоваров
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.СборкаТоваров.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(расхождения)';
											|en = '(discrepancies)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.СборкаТоваров, 
			ДокументыХозОперации);
		
		// Хозяйственная операция СписаниеТоваров
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.СписаниеНедостачТоваров.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(расхождения)';
											|en = '(discrepancies)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.СписаниеТоваров, 
			ДокументыХозОперации);
		
	ИспользуемыеХозОперации.Вставить("ПоставкиРеализация", ХозОперации);
	
	// Поставки. Через комиссионера (агента)
	ХозОперации = Новый Соответствие;
	ШаблонЗаголовка = НСтр("ru = 'Документы поставок <%1>';
							|en = 'Shipment documents <%1>'");
	
		// Хозяйственная операция ВозвратОтКомиссионера
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.АктОРасхожденияхПослеПриемки.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(расхождения)';
											|en = '(discrepancies)'"), 
				ПостфиксЗаголовкаФормы));
		ДокументыХозОперации.Вставить(Метаданные.Документы.ПоступлениеТоваровОтХранителя.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(возвраты)';
											|en = '(returns)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.ВозвратОтКомиссионера, 
			ДокументыХозОперации);
		
		// Хозяйственная операция ПередачаНаКомиссию
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.АктОРасхожденияхПослеОтгрузки.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(расхождения)';
											|en = '(discrepancies)'"), 
				ПостфиксЗаголовкаФормы));
		ДокументыХозОперации.Вставить(Метаданные.Документы.ПередачаТоваровХранителю.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(передачи на комиссию)';
											|en = '(consignment sales)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию, 
			ДокументыХозОперации);
		
		// Хозяйственная операция СписаниеНедостачЗаСчетКомитента
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.СписаниеОтгруженныхТоваров.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			 ШаблонЗаголовка + " " + НСтр("ru = '(списания у комиссионера)';
											|en = '(write-off at consignee)'"), ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.СписаниеНедостачЗаСчетКомитента, 
			ДокументыХозОперации);
		
	ИспользуемыеХозОперации.Вставить("ПоставкиКомиссия", ХозОперации);
	
	// Продажи. Реализация с отложенным переходом права собственности
	ХозОперации = Новый Соответствие;
	ШаблонЗаголовка = НСтр("ru = 'Документы продаж <%1>';
							|en = 'Sales documents <%1>'");
	
		// Хозяйственная операция ВозвратТоваровОтКлиента
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.АктОРасхожденияхПослеПриемки.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(расхождения)';
											|en = '(discrepancies)'"), 
				ПостфиксЗаголовкаФормы));
		ДокументыХозОперации.Вставить(Метаданные.Документы.ВозвратТоваровОтКлиента.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(возвраты)';
											|en = '(returns)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.ВозвратТоваровОтКлиента, 
			ДокументыХозОперации);
		
		// Хозяйственная операция ВозвратНедопоставленногоТовара
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.КорректировкаРеализации.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(корректировки)';
											|en = '(adjusting entries)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.ВозвратНедопоставленногоТовара, 
			ДокументыХозОперации);
		
		// Хозяйственная операция ИсправлениеОшибок
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.КорректировкаРеализации.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(корректировки)';
											|en = '(adjusting entries)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.ИсправлениеОшибок, 
			ДокументыХозОперации);
		
		// Хозяйственная операция КорректировкаПоСогласованиюСторон
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.КорректировкаРеализации.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(корректировки)';
											|en = '(adjusting entries)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон, 
			ДокументыХозОперации);
		
		// Хозяйственная операция РеализацияКлиенту
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.АктОРасхожденияхПослеОтгрузки.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(расхождения)';
											|en = '(discrepancies)'"), 
				ПостфиксЗаголовкаФормы));
		ДокументыХозОперации.Вставить(Метаданные.Документы.КорректировкаРеализации.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(корректировки)';
											|en = '(adjusting entries)'"), 
				ПостфиксЗаголовкаФормы));
		ДокументыХозОперации.Вставить(Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(накладные)';
											|en = '(invoices)'"), 
				ПостфиксЗаголовкаФормы));
		ДокументыХозОперации.Вставить(Метаданные.Документы.СчетФактураВыданный.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(накладные)';
											|en = '(invoices)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.РеализацияКлиенту, 
			ДокументыХозОперации);
		
		// Хозяйственная операция РеализацияБезПереходаПраваСобственности
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.АктОРасхожденияхПослеОтгрузки.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(расхождения)';
											|en = '(discrepancies)'"), 
				ПостфиксЗаголовкаФормы));
		ДокументыХозОперации.Вставить(Метаданные.Документы.КорректировкаРеализации.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(корректировки)';
											|en = '(adjusting entries)'"), 
				ПостфиксЗаголовкаФормы));
		ДокументыХозОперации.Вставить(Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(накладные)';
											|en = '(invoices)'"), 
				ПостфиксЗаголовкаФормы));
		ДокументыХозОперации.Вставить(Метаданные.Документы.СчетФактураВыданный.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(накладные)';
											|en = '(invoices)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности, 
			ДокументыХозОперации);
		
		// Хозяйственная операция РеализацияПерепоставленногоТовара
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.КорректировкаРеализации.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(корректировки)';
											|en = '(adjusting entries)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.РеализацияПерепоставленногоТовара, 
			ДокументыХозОперации);
		
	ИспользуемыеХозОперации.Вставить("ПродажиРеализация", ХозОперации);
	
	// Продажи. Через комиссионера (агента)
	ХозОперации = Новый Соответствие;
	ШаблонЗаголовка = НСтр("ru = 'Документы продаж <%1>';
							|en = 'Sales documents <%1>'");
	
		// Хозяйственная операция ВозвратТоваровЧерезКомиссионера
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.ВозвратТоваровОтКлиента.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(возвраты)';
											|en = '(returns)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.ВозвратТоваровЧерезКомиссионера, 
			ДокументыХозОперации);
		
		// Хозяйственная операция РеализацияЧерезКомиссионера
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.КорректировкаРеализации.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(корректировки)';
											|en = '(adjusting entries)'"), 
				ПостфиксЗаголовкаФормы));
		ДокументыХозОперации.Вставить(Метаданные.Документы.ОтчетКомиссионера.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(отчеты комиссионера)';
											|en = '(consignment issue notifications)'"), 
				ПостфиксЗаголовкаФормы));
		ДокументыХозОперации.Вставить(Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(накладные)';
											|en = '(invoices)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.РеализацияЧерезКомиссионера, 
			ДокументыХозОперации);
		
		// Хозяйственная операция РеализацияЧерезКомиссионераБезПереходаПраваСобственности
		ДокументыХозОперации = Новый Соответствие;
		ДокументыХозОперации.Вставить(Метаданные.Документы.КорректировкаРеализации.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(корректировки)';
											|en = '(adjusting entries)'"), 
				ПостфиксЗаголовкаФормы));
		ДокументыХозОперации.Вставить(Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя(),
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонЗаголовка + " " + НСтр("ru = '(накладные)';
											|en = '(invoices)'"), 
				ПостфиксЗаголовкаФормы));
		ХозОперации.Вставить(Перечисления.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности, 
			ДокументыХозОперации);
		
	ИспользуемыеХозОперации.Вставить("ПродажиКомиссия", ХозОперации);
	
	Возврат ИспользуемыеХозОперации;
	
КонецФункции

// Получает причину отмены по умолчанию.
//
// Возвращаемое значение:
//   Строка, СправочникСсылка.ПричиныОтменыЗаказовКлиентов - причина отмены заказа по умолчанию.
//
Функция ПолучитьПричинуОтменыЗаказовПоУмолчанию() Экспорт 
	
	ПричинаОтменыПоУмолчанию = ИнтеграцияСЯндексМаркетСервер.ПолучитьПричинуОтменыПоУмолчанию();
	
	Возврат  ПричинаОтменыПоУмолчанию;

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Получает все отправления заказов по их номеру. Если нет ни одного отправления, вернется исходный массив с
// номерами заказов.
// 
// Параметры:
//  НомераЗаказов - Массив из Строка - номера заказов, отправления которых нужно получить.
// 
// Возвращаемое значение:
//  Массив из Строка
//
Функция ВсеОтправленияЗаказов(НомераЗаказов) Экспорт
	
	Результат = Новый Массив; // Массив из Строка
	
	Выборка = ВыборкаДанныхНомеровОтправленийЗаказов(НомераЗаказов);
	
	Если Выборка = Неопределено Тогда
		Возврат НомераЗаказов;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.НомерОтправления);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  НомераЗаказов - Массив из Строка
// 
// Возвращаемое значение:
//  - Неопределено
//  - ВыборкаИзРезультатаЗапроса:
//     * НомерОтправления - Строка
//
Функция ВыборкаДанныхНомеровОтправленийЗаказов(НомераЗаказов)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыТорговыхПлощадок.НомерОтправления КАК НомерОтправления
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.НомерЗаказа В(&НомераЗаказов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерОтправления";
	
	Запрос.УстановитьПараметр("НомераЗаказов", НомераЗаказов);
	РезультатЗапроса = Запрос.Выполнить();
	Если Результатзапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат РезультатЗапроса.Выбрать();
	
КонецФункции

#КонецОбласти

