#Область СлужебныеПроцедурыИФункции

Процедура ЗакрытиеФормыНастроекРегистрацииЭДО(Знач ОперацияЭДО, Знач ОбработкаПродолжения) Экспорт
	
	Настройки = Неопределено;
	Если ОперацияЭДО <> Неопределено Тогда
		Настройки = ИнтеграцияБРОЭДОСлужебныйВызовСервера.ОперацияЭДОВСтроку(ОперацияЭДО);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОбработкаПродолжения, Настройки);
	
КонецПроцедуры


#Область ОткрытьФормуОтправкиДанныхОператоруЭДО

// Параметры:
//  Доверенность - СправочникСсылка.МЧД003,
//               - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//               - Неопределено
//  ДополнительныеПараметры - Структура:
//   * СсылкаНаСертификат - См. ОткрытьФормуОтправкиДанныхОператоруЭДО_ПослеПоискаСертификата.СсылкаНаСертификат
//   * Контекст - См. ОткрытьФормуОтправкиДанныхОператоруЭДО_ПослеПоискаСертификата.Контекст
Процедура ПродолжитьОткрыватьФормуПослеВыбораДоверенности(Доверенность, ДополнительныеПараметры) Экспорт
	
	Если Доверенность = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Контекст.Операция.Параметры.Доверенность = Доверенность;
	
	ОткрытьФормуОтправкиДанныхОператоруЭДО_ПослеПоискаСертификата(
		ДополнительныеПараметры.СсылкаНаСертификат, ДополнительныеПараметры.Контекст);
	
КонецПроцедуры

// Открыть форму отправки данных оператору ЭДО после поиска сертификата.
// 
// Параметры:
//  СсылкаНаСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  Контекст - Структура:
//   * Операция - См. ИнтеграцияБРОЭДОСлужебный.ОперацияЭДОИзСтроки
//   * Настройки - Строка - настройки регистрации.
//      См. ОбменСКонтрагентами.ИнициализироватьНастройкиПодключенияЭДО
//      См. ОбменСКонтрагентами.ИнициализироватьНастройкиПереизданияСертификатаЭДО
//   * ВыполняемоеОповещение - ОписаниеОповещения
Асинх Процедура ОткрытьФормуОтправкиДанныхОператоруЭДО_ПослеПоискаСертификата(СсылкаНаСертификат, Контекст) Экспорт
	
	Если Не ЗначениеЗаполнено(СсылкаНаСертификат) Тогда
		Результат = Новый Структура("Выполнено, Настройки", Ложь, Контекст.Настройки);
		ВыполнитьОбработкуОповещения(Контекст.ВыполняемоеОповещение, Результат);
		Возврат;
	КонецЕсли;
	
	Если Контекст.Операция.Действие = УчетныеЗаписиЭДОКлиентСервер.ДействиеОбновлениеСертификата() Тогда
		
		ТребуетсяЗапроситьДоверенность = ИнтеграцияБРОЭДОСлужебныйВызовСервера.ТребуетсяЗапроситьДоверенность(
			Контекст.Операция.Параметры.Организация, СсылкаНаСертификат);
		
		Если ТребуетсяЗапроситьДоверенность
				И Не ЗначениеЗаполнено(Контекст.Операция.Параметры.Доверенность) Тогда
			
			Ответ = Ждать ВопросАсинх(НСтр("ru = 'Для добавления данного сертификата в учетную запись требуется доверенность.
			|Перейти к выбору доверенности?';
			|en = 'An authorization letter is required to add this certificate to the account.
			|Proceed to selecting the  authorization letter?'"), РежимДиалогаВопрос.ДаНет);
			
			Если Ответ = КодВозвратаДиалога.Нет Тогда
				Возврат;
			КонецЕсли;
			
			Параметры = Новый Структура("СсылкаНаСертификат, Контекст", СсылкаНаСертификат, Контекст);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПродолжитьОткрыватьФормуПослеВыбораДоверенности", ЭтотОбъект, Параметры);
			МашиночитаемыеДоверенностиКлиент.ВыбратьМЧД(ОписаниеОповещения, Новый Структура("СертификатКриптографии", СсылкаНаСертификат));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Операция = Контекст.Операция;
	
	ОперацияПодключенияЭДО = СинхронизацияЭДОКлиентСервер.НоваяОперацияПодключенияЭДО();
	ОперацияОбновленияСертификата = СинхронизацияЭДОКлиентСервер.НоваяОперацияОбновленияСертификата();
	
	Если Операция.Действие = ОперацияПодключенияЭДО.Действие Тогда
		ОтборУчетнойЗаписи = Новый Структура;
		ОтборУчетнойЗаписи.Вставить("НазначениеУчетнойЗаписи", Операция.Параметры.НазначениеУчетнойЗаписи);
		ОтборУчетнойЗаписи.Вставить("СпособОбменаЭД", Операция.Параметры.СпособОбменаЭД);
		ОтборУчетнойЗаписи.Вставить("КодНалоговогоОргана", Операция.Параметры.КодНалоговогоОргана);
		ОтборУчетнойЗаписи.Вставить("Организация", Операция.Параметры.Организация);
		ОтборУчетнойЗаписи.Вставить("Сертификат", СсылкаНаСертификат);
		УчетнаяЗаписьСуществует = ИнтеграцияБРОЭДОСлужебныйВызовСервера.УчетнаяЗаписьСуществует(ОтборУчетнойЗаписи);
		Если УчетнаяЗаписьСуществует Тогда
			Результат = Новый Структура("Выполнено, Настройки", Истина, Неопределено);
			ВыполнитьОбработкуОповещения(Контекст.ВыполняемоеОповещение, Результат);
			Возврат;
		КонецЕсли;
		Операция.Параметры.Сертификат = СсылкаНаСертификат;
		УчетнаяЗаписьСуществует = ИнтеграцияБРОЭДОСлужебныйВызовСервера.УчетнаяЗаписьСуществует(Операция.Параметры);
		Если УчетнаяЗаписьСуществует Тогда
			Результат = Новый Структура("Выполнено, Настройки", Истина, Неопределено);
			ВыполнитьОбработкуОповещения(Контекст.ВыполняемоеОповещение, Результат);
			Возврат;
		КонецЕсли;
	ИначеЕсли Операция.Действие = ОперацияОбновленияСертификата.Действие Тогда
		Операция.Параметры.НовыйСертификат = СсылкаНаСертификат;
	КонецЕсли;
	
	ОбработкаЗакрытия = Новый ОписаниеОповещения("ОткрытьФормуОтправкиДанныхОператоруЭДО_ПослеВыполненияОперации",
		ЭтотОбъект, Контекст.ВыполняемоеОповещение);
	
	СинхронизацияЭДОКлиент.НачатьВыполнениеОперацииЭДО(Операция, ОбработкаЗакрытия);
	
КонецПроцедуры

Процедура ОткрытьФормуОтправкиДанныхОператоруЭДО_ПослеВыполненияОперации(Знач РезультатОперации, Знач ОбработкаПродолжения) Экспорт
	
	Результат = Новый Структура("Выполнено, Настройки", Истина, Неопределено);
	
	Если Не РезультатОперации.Выполнено Тогда
		Результат.Выполнено = Ложь;
		Результат.Настройки = ИнтеграцияБРОЭДОСлужебныйВызовСервера.ОперацияЭДОВСтроку(РезультатОперации.ОперацияЭДО);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОбработкаПродолжения, Результат);
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти