#Область ПрограммныйИнтерфейс

// Обработчик нажатии кнопки панели кнопок сообщения системы взаимодействия.
// 
// Параметры:
//  Сообщение - СообщениеСистемыВзаимодействия - обрабатываемое сообщение.
//  Кнопка - КнопкаПанелиКнопокСообщенияСистемыВзаимодействия - нажатая кнопка.
Процедура ПриНажатииКнопкиПанелиКнопокСообщенияСистемыВзаимодействия(Сообщение, Кнопка) Экспорт
	
	Если Кнопка.ИмяДействия = "ПерейтиКФормеСпискаСОтбором" Тогда
		МассивСсылок = Новый Массив;
		Для Каждого ЗначениеОтбора Из Кнопка.Данные.СоответствиеСсылок Цикл
			Для Каждого ЗначениеСоответствия Из ЗначениеОтбора.Значение Цикл
				МассивСсылок.Добавить(ЗначениеСоответствия);
			КонецЦикла;
		КонецЦикла;
		СтруктураОтбор = Новый Структура("Ссылка", МассивСсылок);
		ПараметрыОткрытия = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму(Кнопка.Данные.ИмяФормы, ПараметрыОткрытия);
	ИначеЕсли Кнопка.ИмяДействия = "ПерейтиКФормеПереходПоСсылкам" Тогда
		ПараметрыОткрытия = Новый Структура("СоответствиеСсылок", Кнопка.Данные.СоответствиеСсылок);
		ОткрытьФорму(Кнопка.Данные.ИмяФормы, ПараметрыОткрытия);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриНачалеРаботыСистемы(Параметры) Экспорт
	
	ОбработчикФормированияКоманд = Новый ОписаниеОповещения("ПриФормированииКомандСистемыВзаимодействий", ЭтотОбъект);
	СистемаВзаимодействия.ПодключитьОбработчикФормированияКоманд(ОбработчикФормированияКоманд);
	
КонецПроцедуры

Процедура НастроитьОповещения(ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.ИдентификаторКоманды = "НастроитьВсеОповещения" Тогда
		ПараметрыОткрытия = Новый Структура("ВидОповещения", ДополнительныеПараметры.ВидОповещения);
		ОткрытьФорму("РегистрСведений.ОтключенныеНастройкиОповещенийПользователей.Форма.ПерсональныеНастройки", ПараметрыОткрытия);
	ИначеЕсли ДополнительныеПараметры.ИдентификаторКоманды = "ОтключитьВсеОповещения" Тогда
		ОповещенияПользователейОСобытияхВызовСервера.УстановитьРежимОтображенияСообщений(
			ДополнительныеПараметры.ИдентификаторОбсуждения,
			Истина);
		ПоказатьОповещение(НСтр("ru = 'Отключены все оповещения по объекту.';
								|en = 'All notifications on the object are disabled.'"));
	ИначеЕсли ДополнительныеПараметры.ИдентификаторКоманды = "ПодписатьсяНаОповещенияТолькоМне" Тогда
		ОповещенияПользователейОСобытияхВызовСервера.УстановитьРежимОтображенияСообщений(
			ДополнительныеПараметры.ИдентификаторОбсуждения,
			Ложь,
			Ложь);
		ПоказатьОповещение(НСтр("ru = 'Установлена подписка на все оповещения по объекту, адресованные мне.';
								|en = 'You are subscribed to all notifications on the object addressed to you.'"));
	ИначеЕсли ДополнительныеПараметры.ИдентификаторКоманды = "ПодписатьсяНаВсеОповещения" Тогда
		ОповещенияПользователейОСобытияхВызовСервера.УстановитьРежимОтображенияСообщений(
			ДополнительныеПараметры.ИдентификаторОбсуждения,
			Ложь,
			Истина);
		ПоказатьОповещение(НСтр("ru = 'Установлена подписка на все оповещения по объекту.';
								|en = 'You are subscribed to all notifications on the object.'"));
	ИначеЕсли ДополнительныеПараметры.ИдентификаторКоманды = "ОтключитьТекущееОповещение" Тогда
		ОповещенияПользователейОСобытияхВызовСервера.ОтключитьОповещениеПоВидуОповещения(
			ДополнительныеПараметры.ВидОповещения);
		ПоказатьОповещение(НСтр("ru = 'Отключены оповещения данного вида.';
								|en = 'Notifications of this kind are disabled.'"));
	ИначеЕсли ДополнительныеПараметры.ИдентификаторКоманды = "ПодписатьсяНаТекущееОповещение" Тогда
		ОповещенияПользователейОСобытияхВызовСервера.ПодписатьсяНаОповещениеПоВидуОповещения(
			ДополнительныеПараметры.ВидОповещения);
		ПоказатьОповещение(НСтр("ru = 'Установлена подписка на оповещения данного вида.';
								|en = 'You are subscribed to notifications of this kind.'"));
	КонецЕсли;
	
КонецПроцедуры

// Обработчик, который будет вызван при нажатии левой или правой кнопки мыши на гиперссылке в тексте сообщения, 
// на вложение, гиперссылке действия или нажатие правой кнопки мыши на сообщение. 
// В обработчик передается список команд по умолчанию. В обработчике можно сформировать свой состав команд.
//
// Параметры:
//	ПараметрыКоманд - ПараметрыФормированияКомандСистемыВзаимодействия - описывает параметры вызова обработчика.
//	Команды - Массив из ОписаниеКомандыСистемыВзаимодействия - задает список команд для меню. При вызове список заполнен командами меню по умолчанию.
//	КомандаПоУмолчанию - ОписаниеКомандыСистемыВзаимодействия, Неопределено - определяет команду по умолчанию по значению из списка команд.
//	ДополнительныеПараметры - Структура, Неопределено - значение, которое было указано при создании объекта ОписаниеОповещения.
Процедура ПриФормированииКомандСистемыВзаимодействий(ПараметрыКоманд, Команды, КомандаПоУмолчанию, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ПараметрыКоманд.Сообщение) <> Тип("СообщениеСистемыВзаимодействия") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСообщения = ПараметрыКоманд.Сообщение.Данные;
	Если ТипЗнч(ДанныеСообщения) <> Тип("Структура")
		Или ТипЗнч(ДанныеСообщения) = Тип("Структура") И НЕ ДанныеСообщения.Свойство("ВидОповещения") 
		Или ПользователиКлиент.ЭтоСеансВнешнегоПользователя() Тогда
			Возврат;
	КонецЕсли;
	
	БезКонтекстноеОбсуждение = ПараметрыКоманд.Обсуждение.КонтекстОбсуждения = Неопределено;
	
	КомандыОповещений = Новый ОписаниеКомандыСистемыВзаимодействия(Новый Массив, НСтр("ru = 'Настроить';
																						|en = 'Set up'"));
	КомандыОповещений.Картинка = БиблиотекаКартинок.Настройка;
	
	ДобавитьКоманду(КомандыОповещений.Команда, ПараметрыКоманд, "ОтключитьТекущееОповещение", НСтр("ru = 'Отписаться от текущего оповещения';
																									|en = 'Unsubscribe from the current notification'"));
	Если БезКонтекстноеОбсуждение Тогда
		ДобавитьКоманду(КомандыОповещений.Команда, ПараметрыКоманд, "ОтключитьВсеОповещения", НСтр("ru = 'Отписаться от всех';
																									|en = 'Unsubscribe from all'"));
	Иначе
		ДобавитьКоманду(КомандыОповещений.Команда, ПараметрыКоманд, "ОтключитьВсеОповещения", НСтр("ru = 'Отписаться от всех (по объекту)';
																									|en = 'Unsubscribe from all (by object)'"));
	КонецЕсли;
	ДобавитьРазделитель(КомандыОповещений.Команда);
	ДобавитьКоманду(КомандыОповещений.Команда, ПараметрыКоманд, "ПодписатьсяНаТекущееОповещение", НСтр("ru = 'Подписаться на текущее оповещение';
																										|en = 'Subscribe to the current notification'"));
	ДобавитьКоманду(КомандыОповещений.Команда, ПараметрыКоманд, "ПодписатьсяНаОповещенияТолькоМне", НСтр("ru = 'Подписаться на оповещения, адресованные мне';
																										|en = 'Subscribe to notifications addressed to me'"));
	Если БезКонтекстноеОбсуждение Тогда
		ДобавитьКоманду(КомандыОповещений.Команда, ПараметрыКоманд, "ПодписатьсяНаВсеОповещения", НСтр("ru = 'Подписаться на все';
																										|en = 'Subscribe to all'"));
	Иначе
		ДобавитьКоманду(КомандыОповещений.Команда, ПараметрыКоманд, "ПодписатьсяНаВсеОповещения", НСтр("ru = 'Подписаться на все (по объекту)';
																										|en = 'Subscribe to all (by object)'"));
	КонецЕсли;
	ДобавитьРазделитель(КомандыОповещений.Команда);
	ДобавитьКоманду(КомандыОповещений.Команда, ПараметрыКоманд, "НастроитьВсеОповещения", НСтр("ru = 'Все оповещения...';
																								|en = 'All notifications...'"));
	
	Команды.Добавить(КомандыОповещений);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
//	КомандыПодменю - Массив из ОписаниеКомандыСистемыВзаимодействия - Команды подменю.
//	ПараметрыСообщения - ПараметрыФормированияКомандСистемыВзаимодействия - Параметры сообщения.
//	ИдентификаторКоманды - Строка - Идентификатор команды.
//	Представление - Строка - Представление.
Процедура ДобавитьКоманду(КомандыПодменю, ПараметрыСообщения, ИдентификаторКоманды, Представление)
	
	ПараметрыКоманды = Новый Структура;
	ПараметрыКоманды.Вставить("ТекстСообщения", ПараметрыСообщения.Сообщение.Текст);
	ПараметрыКоманды.Вставить("ИдентификаторСообщения", ПараметрыСообщения.Сообщение.Идентификатор);
	ПараметрыКоманды.Вставить("ИдентификаторКоманды", ИдентификаторКоманды);
	ПараметрыКоманды.Вставить("ИдентификаторОбсуждения", ПараметрыСообщения.Обсуждение.Идентификатор);
	ПараметрыКоманды.Вставить("ВидОповещения", ПараметрыСообщения.Сообщение.Данные.ВидОповещения);
	
	КомандаНастроитьОповещения = Новый ОписаниеКомандыСистемыВзаимодействия(
		Новый ОписаниеОповещения("НастроитьОповещения", ЭтотОбъект, ПараметрыКоманды), Представление);
	КомандыПодменю.Добавить(КомандаНастроитьОповещения);
	
КонецПроцедуры

// Параметры:
//	КомандыПодменю - Массив из ОписаниеКомандыСистемыВзаимодействия - Команды подменю.
Процедура ДобавитьРазделитель(КомандыПодменю)
	Разделитель = Новый ОписаниеКомандыСистемыВзаимодействия(Неопределено);
	КомандыПодменю.Добавить(Разделитель);
КонецПроцедуры

Процедура ПоказатьОповещение(ТекстОповещения)
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Настройки оповещений:';
			|en = 'Notification settings:'"),,
		ТекстОповещения,
		БиблиотекаКартинок.Информация32);
КонецПроцедуры

#КонецОбласти
