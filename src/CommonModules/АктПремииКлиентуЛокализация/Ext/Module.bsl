//@strict-types

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив из Строка - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	
	//++ НЕ УТ
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	МеханизмыДокумента.Добавить("УчетУСНПСН");
	//-- НЕ УТ
	
	//-- Локализация
	
КонецПроцедуры

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений из Строка - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//  ДокументОбъект - Неопределено, ДокументОбъект.АктПремииКлиенту - 
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект = Неопределено) Экспорт
	
	//++ Локализация
	
	//++ НЕ УТ
	
	ТекстЗапросаТаблицаПорядокОтраженияПрочихОпераций(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект);
	
	//-- НЕ УТ
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив из Строка  - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт

КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив из ДокументСсылка.АктПремииКлиенту    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ

#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив; // Массив из Строка
	
	//++ Локализация
	
#Область ТекстЗадолженностьРасчетыСКлиентомРасходы
	ТекстЗапроса =
	"ВЫБРАТЬ // Начисление/Списание задолженности покупателя на расходы по ретро-бонусам (Дт <91.2> :: Кт <62>)
	|	Расчеты.Ссылка КАК Ссылка,
	|	Расчеты.Период КАК Период,
	|	Расчеты.ОрганизацияАналитики КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	Расчеты.СуммаРегл КАК Сумма,
	|	Расчеты.СуммаУпр КАК СуммаУУ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	РегистрПрочиеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	РегистрПрочиеРасходы.Подразделение КАК МестоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	РегистрПрочиеРасходы.Подразделение КАК ПодразделениеДт,
	|	РегистрПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	РегистрПрочиеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	РегистрПрочиеРасходы.АналитикаРасходов КАК СубконтоДт2,
	|	РегистрПрочиеРасходы.ТипЗатрат КАК СубконтоДт3,
	|	Расчеты.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА НЕ РегистрПрочиеРасходы.ПринятиеКНалоговомуУчету
	|			ТОГДА 0
	|		ИНАЧЕ Расчеты.СуммаРегл
	|	КОНЕЦ КАК СуммаНУДт,
	|	ВЫБОР
	|		КОГДА РегистрПрочиеРасходы.ПринятиеКНалоговомуУчету
	|			ТОГДА 0
	|		ИНАЧЕ Расчеты.СуммаРегл
	|	КОНЕЦ КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	Расчеты.ВидСчета КАК ВидСчетаКт,
	|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	Расчеты.Валюта КАК ВалютаКт,
	|	Расчеты.Подразделение КАК ПодразделениеКт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	Расчеты.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	Расчеты.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	&Содержание КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасчетыСКлиентамиНоваяАрхитектура КАК Расчеты
	|		ПО ДокументыКОтражению.Ссылка = Расчеты.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасчетыСКлиентамиПоДокументам КАК НовыеРасчеты
	|		ПО (Расчеты.Ссылка = НовыеРасчеты.Ссылка)
	|			И (Расчеты.ОбъектРасчетов = НовыеРасчеты.ОбъектРасчетов)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеПрочиеРасходы КАК РегистрПрочиеРасходы
	|		ПО (Расчеты.Ссылка = РегистрПрочиеРасходы.Регистратор)
	|			И (РегистрПрочиеРасходы.ИдентификаторФинЗаписи = Расчеты.ИдентификаторФинЗаписи)";
	
	Содержание = НСтр("ru = '""Ретро-бонусы клиента за счет расходов""';
						|en = '""Customer rebates due to expenses""'");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Содержание", Содержание);
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти

#Область ТекстЗадолженностьРасчетыСКлиентомПрочиеАктивыПассивы
	ТекстЗапроса =
	"ВЫБРАТЬ // Начисление/Списание задолженности покупателя на активы/пассивы по ретро-бонусам (Дт <Прочие счета> :: Кт <62>)
	|	Расчеты.Ссылка КАК Ссылка,
	|	Расчеты.Период КАК Период,
	|	Расчеты.ОрганизацияАналитики КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	Расчеты.СуммаРегл КАК Сумма,
	|	Расчеты.СуммаУпр КАК СуммаУУ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	ПрочиеАктивыПассивы.Статья КАК АналитикаУчетаДт,
	|	ПрочиеАктивыПассивы.Подразделение КАК МестоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ПрочиеАктивыПассивы.Подразделение КАК ПодразделениеДт,
	|	ПрочиеАктивыПассивы.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	ПрочиеАктивыПассивы.СчетУчета КАК СчетДт,
	|	ПрочиеАктивыПассивы.Субконто1 КАК СубконтоДт1,
	|	ПрочиеАктивыПассивы.Субконто2 КАК СубконтоДт2,
	|	ПрочиеАктивыПассивы.Субконто3 КАК СубконтоДт3,
	|	Расчеты.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Расчеты.СуммаРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	Расчеты.ВидСчета КАК ВидСчетаКт,
	|	Расчеты.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	Расчеты.Валюта КАК ВалютаКт,
	|	Расчеты.Подразделение КАК ПодразделениеКт,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	Расчеты.Контрагент КАК СубконтоКт1,
	|	Расчеты.Договор КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	&Содержание КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасчетыСКлиентамиНоваяАрхитектура КАК Расчеты
	|		ПО ДокументыКОтражению.Ссылка = Расчеты.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасчетыСКлиентамиПоДокументам КАК НовыеРасчеты
	|		ПО (Расчеты.Ссылка = НовыеРасчеты.Ссылка)
	|			И (Расчеты.ОбъектРасчетов = НовыеРасчеты.ОбъектРасчетов)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеПрочиеАктивыПассивы КАК ПрочиеАктивыПассивы
	|		ПО (Расчеты.Ссылка = ПрочиеАктивыПассивы.Регистратор)
	|			И (ПрочиеАктивыПассивы.ИдентификаторФинЗаписи = Расчеты.ИдентификаторФинЗаписи)";
	
	Содержание = НСтр("ru = '""Ретро-бонусы клиента за счет прочих активов""';
						|en = '""Customer rebates due to other assets""'");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Содержание", Содержание);
	ТекстыОтражения.Добавить(ТекстЗапроса);
#КонецОбласти
	
	//-- Локализация
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстыЗапроса = Новый Массив; // Массив из Строка
	
	//++ Локализация
	
	#Область РасчетыСКлиентами
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Расчеты.Регистратор КАК Ссылка,
	|	Расчеты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ОбъектыРасчетов.Контрагент КАК Контрагент,
	|	ОбъектыРасчетов.Договор КАК Договор,
	|	ОбъектыРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ОбъектыРасчетов.Объект КАК ДокументОбъектаРасчетов,
	|	ОбъектыРасчетов.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ОбъектыРасчетов.Подразделение КАК Подразделение,
	|	Расчеты.Валюта,
	|	Расчеты.СтатьяДвиженияДенежныхСредств,
	|	СУММА(ВЫБОР КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Расчеты.Регистратор <> ОбъектыРасчетов.Объект
	|		ТОГДА -1 КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА 1
	|		ИНАЧЕ 0 КОНЕЦ * Расчеты.Сумма) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Расчеты.Регистратор <> ОбъектыРасчетов.Объект
	|		ТОГДА -1 КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА 1
	|		ИНАЧЕ 0 КОНЕЦ * Расчеты.СуммаУпр) КАК СуммаУпр,
	|	СУММА(ВЫБОР КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Расчеты.Регистратор <> ОбъектыРасчетов.Объект
	|		ТОГДА -1 КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА 1
	|		ИНАЧЕ 0 КОНЕЦ * Расчеты.СуммаРегл) КАК СуммаРегл
	|ПОМЕСТИТЬ РасчетыСКлиентами
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|	ПО
	|		ДокументыКОтражению.Ссылка = Расчеты.Регистратор
	|		И НЕ Расчеты.Сторно
	|		И НЕ Расчеты.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РезервированиеАвансаКлиента),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносПлатежаМеждуФилиалами),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносЗадолженностиМеждуФилиалами))
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|	ПО (Расчеты.ОбъектРасчетов = ОбъектыРасчетов.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.Регистратор,
	|	Расчеты.АналитикаУчетаПоПартнерам.Организация,
	|	ОбъектыРасчетов.Контрагент,
	|	ОбъектыРасчетов.Договор,
	|	ОбъектыРасчетов.НаправлениеДеятельности,
	|	Расчеты.ОбъектРасчетов,
	|	ОбъектыРасчетов.Объект,
	|	ОбъектыРасчетов.ГруппаФинансовогоУчета,
	|	ОбъектыРасчетов.Подразделение,
	|	Расчеты.Валюта,
	|	Расчеты.СтатьяДвиженияДенежныхСредств
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область РасчетыСКлиентамиПоДокументам
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Расчеты.Ссылка КАК Ссылка,
	|	Расчеты.Контрагент КАК Контрагент,
	|	Расчеты.Договор КАК Договор,
	|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	Расчеты.Подразделение КАК Подразделение,
	|	Расчеты.Валюта КАК Валюта,
	|	ВЫБОР КОГДА СУММА(Расчеты.Предоплата) < 0 ТОГДА -СУММА(Расчеты.Предоплата) ИНАЧЕ СУММА(Расчеты.Предоплата) КОНЕЦ КАК Предоплата,
	|	ВЫБОР КОГДА СУММА(Расчеты.ПредоплатаУпр) < 0 ТОГДА -СУММА(Расчеты.ПредоплатаУпр) ИНАЧЕ СУММА(Расчеты.ПредоплатаУпр) КОНЕЦ КАК ПредоплатаУпр,
	|	ВЫБОР КОГДА СУММА(Расчеты.ПредоплатаРегл) < 0 ТОГДА -СУММА(Расчеты.ПредоплатаРегл) ИНАЧЕ СУММА(Расчеты.ПредоплатаРегл) КОНЕЦ КАК ПредоплатаРегл,
	|	ВЫБОР КОГДА СУММА(Расчеты.Долг) < 0 ТОГДА -СУММА(Расчеты.Долг) ИНАЧЕ СУММА(Расчеты.Долг) КОНЕЦ КАК Долг,
	|	ВЫБОР КОГДА СУММА(Расчеты.ДолгУпр) < 0 ТОГДА -СУММА(Расчеты.ДолгУпр) ИНАЧЕ СУММА(Расчеты.ДолгУпр) КОНЕЦ КАК ДолгУпр,
	|	ВЫБОР КОГДА СУММА(Расчеты.ДолгРегл) < 0 ТОГДА -СУММА(Расчеты.ДолгРегл) ИНАЧЕ СУММА(Расчеты.ДолгРегл) КОНЕЦ КАК ДолгРегл,
	|	ВЫБОР КОГДА СУММА(Расчеты.ЗалогЗаТару) < 0 ТОГДА -СУММА(Расчеты.ЗалогЗаТару) ИНАЧЕ СУММА(Расчеты.ЗалогЗаТару) КОНЕЦ КАК ЗалогЗаТару,
	|	ВЫБОР КОГДА СУММА(Расчеты.ЗалогЗаТаруРегл) < 0 ТОГДА -СУММА(Расчеты.ЗалогЗаТаруРегл) ИНАЧЕ СУММА(Расчеты.ЗалогЗаТаруРегл) КОНЕЦ КАК ЗалогЗаТаруРегл
	|ПОМЕСТИТЬ РасчетыСКлиентамиПоДокументам
	|ИЗ
	|	РасчетыСКлиентамиНоваяАрхитектура КАК Расчеты
	|	
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.Ссылка,
	|	Расчеты.Контрагент,
	|	Расчеты.Договор,
	|	Расчеты.НаправлениеДеятельности,
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.ГруппаФинансовогоУчета,
	|	Расчеты.Подразделение,
	|	Расчеты.Валюта,
	|	Расчеты.ИдентификаторФинЗаписи = """",
	|	ВЫБОР КОГДА Расчеты.ИдентификаторФинЗаписи = """" ТОГДА Расчеты.Долг <> 0 ИНАЧЕ ЛОЖЬ КОНЕЦ
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ВТДанныеПрочиеРасходы
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.СтатьяРасходов КАК СтатьяРасходов,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	ДанныеРегистра.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеРегистра.АналитикаРасходов КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.ВариантРаспределенияРасходовРегл <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|		КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|		ТОГДА ОбъектыСтроительства.СпособСтроительства
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|	КОНЕЦ КАК ТипЗатрат,
	|	СтатьиРасходов.ПринятиеКНалоговомуУчету КАК ПринятиеКНалоговомуУчету,
	|	ДанныеРегистра.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ДанныеРегистра.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|	СУММА(ДанныеРегистра.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ДанныеРегистра.СуммаУпр) КАК СуммаУпр,
	|	СУММА(ДанныеРегистра.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТДанныеПрочиеРасходы
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК ДанныеРегистра
	|	ПО ДокументыКОтражению.Ссылка = ДанныеРегистра.Регистратор
	|		И НЕ ДанныеРегистра.Сторно
	|		И ДанныеРегистра.Активность
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО ДанныеРегистра.СтатьяРасходов = СтатьиРасходов.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО ОбъектыСтроительства.Ссылка = ДанныеРегистра.АналитикаРасходов
	|		И СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Регистратор,
	|	ДанныеРегистра.Период,
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.СтатьяРасходов,
	|	ДанныеРегистра.Подразделение,
	|	ДанныеРегистра.НаправлениеДеятельности,
	|	ДанныеРегистра.АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА СтатьиРасходов.ВариантРаспределенияРасходовРегл <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|		КОГДА ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|		ТОГДА ОбъектыСтроительства.СпособСтроительства
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|	КОНЕЦ,
	|	СтатьиРасходов.ПринятиеКНалоговомуУчету,
	|	ДанныеРегистра.ИдентификаторФинЗаписи,
	|	ДанныеРегистра.НастройкаХозяйственнойОперации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область ВТДанныеПрочиеАктивыПассивы
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Статья КАК Статья,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	ДанныеРегистра.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(НастройкиСчетовУчета.СчетУчета, НЕОПРЕДЕЛЕНО) КАК СчетУчета,
	|	ЕСТЬNULL(НастройкиСчетовУчета.Субконто1, НЕОПРЕДЕЛЕНО) КАК Субконто1,
	|	ЕСТЬNULL(НастройкиСчетовУчета.Субконто2, НЕОПРЕДЕЛЕНО) КАК Субконто2,
	|	ЕСТЬNULL(НастройкиСчетовУчета.Субконто3, НЕОПРЕДЕЛЕНО) КАК Субконто3,
	|	ДанныеРегистра.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ДанныеРегистра.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|	СУММА(ДанныеРегистра.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ДанныеРегистра.СуммаУпр) КАК СуммаУпр,
	|	СУММА(ДанныеРегистра.СуммаСНДС) КАК СуммаСНДС,
	|	МИНИМУМ(ДанныеПоПрочимДоходамРасходам.Регистратор ЕСТЬ NULL) КАК ТолькоАктивыПассивы
	|ПОМЕСТИТЬ ВТДанныеПрочиеАктивыПассивы
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДвиженияПоПрочимАктивамПассивам КАК ДанныеРегистра
	|	ПО ДокументыКОтражению.Ссылка = ДанныеРегистра.Регистратор
	|		И НЕ ДанныеРегистра.Сторно
	|		И ДанныеРегистра.Активность
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ ПЕРВЫЕ 1 Т.Регистратор ИЗ ВТДанныеПрочиеРасходы КАК Т) КАК ДанныеПоПрочимДоходамРасходам
	|	ПО ДокументыКОтражению.Ссылка = ДанныеПоПрочимДоходамРасходам.Регистратор
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиСчетовУчетаПрочихОпераций КАК НастройкиСчетовУчета
	|	ПО ДанныеРегистра.НастройкаСчетовУчета = НастройкиСчетовУчета.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Регистратор,
	|	ДанныеРегистра.Период,
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Статья,
	|	ДанныеРегистра.Подразделение,
	|	ДанныеРегистра.НаправлениеДеятельности,
	|	ЕСТЬNULL(НастройкиСчетовУчета.СчетУчета, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(НастройкиСчетовУчета.Субконто1, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(НастройкиСчетовУчета.Субконто2, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(НастройкиСчетовУчета.Субконто3, НЕОПРЕДЕЛЕНО),
	|	ДанныеРегистра.ИдентификаторФинЗаписи,
	|	ДанныеРегистра.НастройкаХозяйственнойОперации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	//-- Локализация
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

//-- НЕ УТ

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

//++ Локализация
//
//++ НЕ УТ

#Область ПроводкиРегУчета

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры, ДокументОбъект)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если ДокументОбъект.Статус <> Перечисления.СтатусыАктовПремийКлиентам.Получен Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		Документы.АктПремииКлиенту.ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период                      КАК Период,
	|	&Организация                 КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|";
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ"
		+ ДоходыИРасходыСервер.ДополнитьТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Ложь);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПорядокОтраженияПрочихОпераций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокОтраженияПрочихОпераций";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	&ДатаПризнанияВУчете КАК Дата,
	|	&Организация КАК Организация,
	|	&Ссылка КАК Документ,
	|	"""" КАК ИдентификаторСтроки
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК Статья
	|ГДЕ
	|	Статья.Ссылка = &СтатьяРасходовАктивов
	|	И Статья.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

//-- НЕ УТ

//-- Локализация

#КонецОбласти

#КонецОбласти