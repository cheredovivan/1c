
#Область ПрограммныйИнтерфейс

// См. ИнтеграцияShareПереопределяемый.КонтактныеЛицаСНомеромТелефона.
Процедура КонтактныеЛицаСНомеромТелефона(Знач Контрагент, КонтактнаяИнформация) Экспорт
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат;
	КонецЕсли;

	КонтактноеЛицоКонтрагента = ПартнерыИКонтрагенты.ПолучитьКонтактноеЛицоПартнераКонтрагентаПоУмолчанию(Контрагент);
	КонтактноеЛицоИКонтрагент = Новый Массив;
	КонтактноеЛицоИКонтрагент.Добавить(Контрагент);
	КонтактноеЛицоИКонтрагент.Добавить(КонтактноеЛицоКонтрагента);
	Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "Партнер");
	Если ЗначениеЗаполнено(Партнер) Тогда
		КонтактноеЛицоИКонтрагент.Добавить(Партнер);
	КонецЕсли;	
	
	ВидыКИ = Новый Массив;
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонПартнера);
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонКонтактногоЛица);
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.МобильныйТелефонКонтактногоЛица);

	ОтборКонтактнойИнформации = УправлениеКонтактнойИнформацией.ОтборКонтактнойИнформации();
	ОтборКонтактнойИнформации.ВидыКонтактнойИнформации = ВидыКИ;
	ОтборКонтактнойИнформации.Дата = ТекущаяДатаСеанса();
	
	КонтактнаяИнформацияДляСервиса = УправлениеКонтактнойИнформацией.КонтактнаяИнформация(КонтактноеЛицоИКонтрагент, ОтборКонтактнойИнформации);
	
	Для Каждого НайденныйКонтакт Из КонтактнаяИнформацияДляСервиса Цикл
		
		НомерТелефонаБезКодов = УправлениеКонтактнойИнформацией.НомерТелефонаКонтактнойИнформации(НайденныйКонтакт.Значение);
		
		Контакт = ИнтеграцияShareКлиентСервер.НовыйКонтактнаяИнформация();
		Контакт.ВладелецКонтакта      = НайденныйКонтакт.Объект;
		Контакт.НомерТелефонаБезКодов = НомерТелефонаБезКодов;
		
		КонтактнаяИнформация.Добавить(Контакт);
	КонецЦикла;
	
КонецПроцедуры

// См. ИнтеграцияShareПереопределяемый.ПриОпределенииСоставаКомандShare.
Процедура ПриОпределенииСоставаКомандShare(СоставКоманд) Экспорт

	СоставКоманд.Исходящие.Добавить(Метаданные.Документы.ЗаказКлиента.ПолноеИмя());
	СоставКоманд.Исходящие.Добавить(Метаданные.Документы.ЗаказПоставщику.ПолноеИмя());
	СоставКоманд.Исходящие.Добавить(Метаданные.Документы.СчетНаОплатуКлиенту.ПолноеИмя());

	СоставКоманд.Входящие.Добавить(Метаданные.Документы.ЗаказКлиента.ПолноеИмя());
	СоставКоманд.Входящие.Добавить(Метаданные.Документы.ЗаказПоставщику.ПолноеИмя());
	СоставКоманд.Входящие.Добавить(Метаданные.Документы.ЗаявкаНаРасходованиеДенежныхСредств.ПолноеИмя());

КонецПроцедуры

// См. ИнтеграцияShareПереопределяемый.ПриОпределенииГиперссылкиНаИнструкциюПоРаботеССервисом.
Процедура ПриОпределенииГиперссылкиНаИнструкциюПоРаботеССервисом(НавигационнаяСсылка) Экспорт
	
КонецПроцедуры

// См. ИнтеграцияShareПереопределяемый.ПриОпределенииФайловДокументаДляВыбораКПубликации.
Процедура ПриОпределенииФайловДокументаДляВыбораКПубликации(ДанныеФайлов, Знач СсылкаНаДокумент, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// См. ИнтеграцияShareПереопределяемый.ПриОпределенииДанныхПубликуемогоДокумента.
Процедура ПриОпределенииДанныхПубликуемогоДокумента(ДанныеТранспортногоКонтейнера, Знач СсылкаНаДокумент) Экспорт
	
	//Передача параметра валюты документа в макет
	СсылкаНаВалюту = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаДокумент, "Валюта");
	ВалютаДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаВалюту.Валюта, "Код, Наименование");
	
	ПолныйПуть = "КарточкаПередаваемыхДанных.Описание.Валюта.Код";
	Результат = ДанныеТранспортногоКонтейнера.Строки.Найти(ПолныйПуть, "ПолныйПуть", Истина);
	Если Результат <> Неопределено Тогда
		Результат.Значение = ВалютаДокумента.Код;
	КонецЕсли;
	
	ПолныйПуть = "КарточкаПередаваемыхДанных.Описание.Валюта.Наименование";
	Результат = ДанныеТранспортногоКонтейнера.Строки.Найти(ПолныйПуть, "ПолныйПуть", Истина);
	Если Результат <> Неопределено Тогда
		Результат.Значение = ВалютаДокумента.Наименование;
	КонецЕсли;
	
КонецПроцедуры

// См. ИнтеграцияShareПереопределяемый.ПриОпределенииВизуализацииПубликуемогоДокумента.
Процедура ПриОпределенииВизуализацииПубликуемогоДокумента(ОписаниеПредставленияДокумента,
	Знач ПараметрыПубликуемогоДокументаДляВизуализации, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// См. ИнтеграцияShareПереопределяемый.ЗаполнитьНастройкиФормированияДокумента.
Процедура ЗаполнитьНастройкиФормированияДокумента(СсылкаНаДокумент, НастройкиФормирования) Экспорт
	
	ПоддерживаемыеФорматыЭДО = ОбменСКонтрагентами.ФорматыДокументов();
	ТипыДокументов           = ОбменСКонтрагентами.ТипыДокументов();
	НаправленияДокументов    = ОбменСКонтрагентами.НаправленияДокументов();
	
	ТипИсточника = ТипЗнч(СсылкаНаДокумент);

	Если ТипИсточника = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		
		Реквизиты = "Организация, Контрагент, Договор";
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаДокумент, Реквизиты);
		
		НастройкиФормирования.ТипДокумента       = ТипыДокументов.ЗаказТовара;
		НастройкиФормирования.Формат             = ПоддерживаемыеФорматыЭДО.CML208;
		НастройкиФормирования.Направление        = НаправленияДокументов.Исходящий;
		НастройкиФормирования.Организация        = ЗначенияРеквизитов.Организация;
		НастройкиФормирования.Контрагент         = ЗначенияРеквизитов.Контрагент;
		НастройкиФормирования.ДоговорКонтрагента = ЗначенияРеквизитов.Договор;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		
		Реквизиты = "Организация, Контрагент, Договор";
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаДокумент, Реквизиты);
		
		НастройкиФормирования.ТипДокумента       = ТипыДокументов.ОтветНаЗаказ;
		НастройкиФормирования.Формат             = ПоддерживаемыеФорматыЭДО.CML208;
		НастройкиФормирования.Направление        = НаправленияДокументов.Исходящий;
		НастройкиФормирования.Организация        = ЗначенияРеквизитов.Организация;
		НастройкиФормирования.Контрагент         = ЗначенияРеквизитов.Контрагент;
		НастройкиФормирования.ДоговорКонтрагента = ЗначенияРеквизитов.Договор;
		
	ИначеЕсли ТипИсточника = Тип("ДокументСсылка.СчетНаОплатуКлиенту") Тогда
		
		Реквизиты = "Организация, Контрагент, Договор";
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаДокумент, Реквизиты);
		
		НастройкиФормирования.ТипДокумента       = ТипыДокументов.СчетНаОплату;
		НастройкиФормирования.Формат             = ПоддерживаемыеФорматыЭДО.CML208;
		НастройкиФормирования.Направление        = НаправленияДокументов.Исходящий;
		НастройкиФормирования.Организация        = ЗначенияРеквизитов.Организация;
		НастройкиФормирования.Контрагент         = ЗначенияРеквизитов.Контрагент;
		НастройкиФормирования.ДоговорКонтрагента = ЗначенияРеквизитов.Договор;

	КонецЕсли;

КонецПроцедуры

// См. ИнтеграцияShareПереопределяемый.ПриОпределенииНастроекЗагрузкиДокумента.
Процедура ПриОпределенииНастроекЗагрузкиДокумента(НастройкиЗагрузки, Знач ОписаниеЭлектронногоДокумента) Экспорт

	Если Не ЗначениеЗаполнено(ОписаниеЭлектронногоДокумента.СведенияОЗагруженномДокументе.ОбъектУчета)
							И ОписаниеЭлектронногоДокумента.ТипДокумента = Перечисления.ТипыДокументовЭДО.СчетНаОплату Тогда
							
		ПараметрыОтраженияВУчете = НастройкиЗагрузки.ПараметрыОтраженияВУчете;
		ПараметрыОтраженияВУчете.Очистить();
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаРасходованиеДенежныхСредств") Тогда
			НовыйПараметрОтраженияВУчете = ПараметрыОтраженияВУчете.Добавить();
			
			ПредставлениеДокумента   = СтрШаблон(НСтр("ru = '%1 от %2';
														|en = '%1 dated %2'",
				ОбщегоНазначения.КодОсновногоЯзыка()), ОписаниеЭлектронногоДокумента.ПредставлениеДокументаОтправителя,
				ОписаниеЭлектронногоДокумента.НаименованиеОтправителя);
			
			НовыйПараметрОтраженияВУчете.СпособОтраженияВУчете    = "ЗаявкаНаРасходованиеДенежныхСредств";
			НовыйПараметрОтраженияВУчете.ПредставлениеДокумента   = ПредставлениеДокумента;
			НовыйПараметрОтраженияВУчете.КартинкаРезультатаПоиска = БиблиотекаКартинок.ЗагрузитьShare;
			НовыйПараметрОтраженияВУчете.ПредставлениеВПодменюЗагрузить = СтрШаблон(НСтр("ru = 'Как %1';
																						|en = 'As %1'",
				ОбщегоНазначения.КодОсновногоЯзыка()), НСтр("ru = 'Заявка на расходование денежных средств';
															|en = 'Payment request'"));
			НовыйПараметрОтраженияВУчете.ПредставлениеРезультатаПоиска = СтрШаблон(НСтр("ru = 'Загрузить как %1';
																						|en = 'Import as %1'",
				ОбщегоНазначения.КодОсновногоЯзыка()), НСтр("ru = 'Заявка на расходование денежных средств';
															|en = 'Payment request'"));
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыПоставщикам") Тогда
			НовыйПараметрОтраженияВУчете = ПараметрыОтраженияВУчете.Добавить();
			
			ПредставлениеДокумента   = СтрШаблон(НСтр("ru = '%1 от %2';
														|en = '%1 dated %2'",
				ОбщегоНазначения.КодОсновногоЯзыка()), ОписаниеЭлектронногоДокумента.ПредставлениеДокументаОтправителя,
				ОписаниеЭлектронногоДокумента.НаименованиеОтправителя);
			
			НовыйПараметрОтраженияВУчете.СпособОтраженияВУчете    = "ЗаказПоставщику";
			НовыйПараметрОтраженияВУчете.ПредставлениеДокумента   = ПредставлениеДокумента;
			НовыйПараметрОтраженияВУчете.КартинкаРезультатаПоиска = БиблиотекаКартинок.ЗагрузитьShare;
			НовыйПараметрОтраженияВУчете.ПредставлениеВПодменюЗагрузить = СтрШаблон(НСтр("ru = 'Как %1';
																						|en = 'As %1'",
				ОбщегоНазначения.КодОсновногоЯзыка()), НСтр("ru = 'Заказ поставщику';
															|en = 'Purchase order'"));
			НовыйПараметрОтраженияВУчете.ПредставлениеРезультатаПоиска = СтрШаблон(НСтр("ru = 'Загрузить как %1';
																						|en = 'Import as %1'",
				ОбщегоНазначения.КодОсновногоЯзыка()), НСтр("ru = 'Заказ поставщику';
															|en = 'Purchase order'"));
		КонецЕсли;

		НовыйПараметрОтраженияВУчете = ПараметрыОтраженияВУчете.Добавить();
			
		ПредставлениеДокумента   = СтрШаблон(НСтр("ru = '%1 от %2';
													|en = '%1 dated %2'",
			ОбщегоНазначения.КодОсновногоЯзыка()), ОписаниеЭлектронногоДокумента.ПредставлениеДокументаОтправителя,
			ОписаниеЭлектронногоДокумента.НаименованиеОтправителя);
			
		НовыйПараметрОтраженияВУчете.СпособОтраженияВУчете    = "СписаниеБезналичныхДенежныхСредств";
		НовыйПараметрОтраженияВУчете.ПредставлениеДокумента   = ПредставлениеДокумента;
		НовыйПараметрОтраженияВУчете.КартинкаРезультатаПоиска = БиблиотекаКартинок.ЗагрузитьShare;
		НовыйПараметрОтраженияВУчете.ПредставлениеВПодменюЗагрузить = СтрШаблон(НСтр("ru = 'Как %1';
																					|en = 'As %1'",
			ОбщегоНазначения.КодОсновногоЯзыка()), НСтр("ru = 'Списание безналичных денежных средств';
														|en = 'Outgoing payment — Bank account'"));
		НовыйПараметрОтраженияВУчете.ПредставлениеРезультатаПоиска = СтрШаблон(НСтр("ru = 'Загрузить как %1';
																					|en = 'Import as %1'",
			ОбщегоНазначения.КодОсновногоЯзыка()), НСтр("ru = 'Списание безналичных денежных средств';
														|en = 'Outgoing payment — Bank account'"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
