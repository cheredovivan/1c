//@strict-types

#Область ПрограммныйИнтерфейс

// Возвращает ссылку на новую группу отбора в переданном отборе схемы компоновки данных.
//
// Параметры:
//  Отбор - ОтборКомпоновкиДанных - отбор схемы компоновки, в который необходимо добавить новый элемент
//  ТипГруппы - ТипГруппыЭлементовОтбораКомпоновкиДанных, Неопределено - тип создаваемой группы (по умолчанию - ИЛИ) 
//  
// Возвращаемое значение:
//  ОтборКомпоновкиДанных - ссылка на новый элемент отбора схемы компоновки данных.
//
Функция НоваяГруппаОтборов(Отбор, ТипГруппы = Неопределено) Экспорт
	
	НоваяГруппа = ФинансоваяОтчетностьСервер.НовыйОтбор(
		Отбор, Неопределено,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных")); // ОтборКомпоновкиДанных
	
	Если ТипГруппы = Неопределено Тогда
		НоваяГруппа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Иначе
		НоваяГруппа.ТипГруппы = ТипГруппы;
	КонецЕсли; 
	
	Возврат НоваяГруппа;
	
КонецФункции

// Возвращает последний согласованный и проведенный документ условий ретро-бонусов по корневому документу цепочки 
// 
// Параметры:
//  Документ - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика - Документ, по которому требуется получить актуальный (действующий) исправительный документ условий ретро-бонусов
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//	* Ссылка - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика -
// 
Функция АктуальныйДокументУсловийРетроБонусов(Документ) Экспорт
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РеестрДокументов.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|ГДЕ
	|	РеестрДокументов.ИсправляемыйДокумент = &ИсправляемыйДокумент
	|	И РеестрДокументов.Ссылка <> &ИсправительныйДокумент
	|	И НЕ РеестрДокументов.ДополнительнаяЗапись
	|	И РеестрДокументов.Проведен
	|	И РеестрДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДокументовРетроБонусов.Согласован)
	|
	|УПОРЯДОЧИТЬ ПО
	|	РеестрДокументов.ДатаДокументаИБ УБЫВ";
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ИсправляемыйДокумент", Документ.ИсправляемыйДокумент);
	Запрос.УстановитьПараметр("ИсправительныйДокумент", Документ.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

// Возвращает последний проведенный документ условий ретро-бонусов в указанном статусе по корневому документу цепочки.
// Если статус не указан, будет возвращен последний проведенный согласованный документ.
// Если нет исправительного документа - будет возвращен сам документ. 
// 
// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика - Ссылка на документ, по которому требуется получить актуальный (действующий) документ условий ретро-бонусов
//  Статус - ПеречислениеСсылка.СтатусыДокументовРетроБонусов, Неопределено - Статус последнего документа цепочки
// 
// Возвращаемое значение:
//  ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика -
// 
Функция АктуальныйДокументУсловийРетроБонусовПоСсылке(Документ, Статус = Неопределено) Экспорт
	
	Если Статус = Неопределено Тогда
		СтатусДокумента = Перечисления.СтатусыДокументовРетроБонусов.Согласован;
	Иначе
		СтатусДокумента = Статус;
	КонецЕсли;
	
	МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Документ);
	ТекстЗапроса = МенеджерДокумента.ТекстЗапросаАктуальногоДокументаУсловий();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ИсправляемыйДокумент", Документ);
	Запрос.УстановитьПараметр("СтатусДокумента", СтатусДокумента);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		АктуальныйДокумент = МенеджерДокумента.ПустаяСсылка();
		
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		АктуальныйДокумент = Выборка.Ссылка;
		
	КонецЕсли;
	
	Возврат АктуальныйДокумент;
	
КонецФункции

// см. ОтчетыУТПереопределяемый.ДополнитьСоответствияРегистраторовОтчетаОДвижениях
//
Процедура ДополнитьСоответствияРегистраторовОтчетаОДвижениях(Документ, СоответствиеРегистров) Экспорт
	
	Регистры = Метаданные.РегистрыСведений;
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.УсловияРетроБонусовКлиентов") Тогда
		
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыКлиентовИНН, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыКлиентовКонтрагенты, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыКлиентовСегментыПартнеров, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыКлиентовСегментыТоваров, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыКлиентовТовары, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыКлиентовСвойстваНоменклатуры, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыКлиентовУсловия, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыКлиентовДоговорыСоглашения, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыШкалыРасчета, "РегистраторДвижения");
		
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.УсловияРетроБонусовПоставщика") Тогда
		
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыПоставщиковИНН, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыПоставщиковСегментыТоваров, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыПоставщиковТовары, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыПоставщиковСвойстваНоменклатуры, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыПоставщиковУсловия, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыПоставщиковКонтрагенты, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыПоставщиковПоставщики, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыПоставщиковСклады, "РегистраторДвижения");
		СоответствиеРегистров.Вставить(Регистры.РетроБонусыШкалыРасчета, "РегистраторДвижения");
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура проверяет дубли строк в табличной части
//
// Параметры:
//  Объект - ДокументОбъект, ФормаКлиентскогоПриложения - проверяемый ДокументОбъект или форма
//  ИмяТЧ - Строка - имя проверяемой табличной части
//  КлючевыеРеквизитыТЧ - Массив из Строка -
//	                    - Строка - массив имен реквизитов, по которым определяется уникальность строки
//	                    - Структура - Ключ это наименование реквизита, значение - представление реквизита
//  Отказ - Булево - отказ продолжения операции.
//  ПредставлениеТЧ - Строка - если не указано, то представление будет получено из метаданных.
//  УказыватьНомераСтрок - Булево - определяет необходимость вывода номера строки в сообщении об ошибке.
//  ОтборСтрок - Структура - если не указано, то выбираются все строки из табличной части, иначе - согласно отбору:
//  	* Ключ - Строка
//  	* Значение - Произвольный
//
Процедура ПроверитьНаличиеДублейСтрокТЧ(Объект, ИмяТЧ, КлючевыеРеквизитыТЧ, Отказ, ПредставлениеТЧ = "", УказыватьНомераСтрок = Истина, ОтборСтрок = Неопределено) Экспорт
	
	ТипЗначенияКлючевыеРеквизитыТЧ = ТипЗнч(КлючевыеРеквизитыТЧ);
	ПредставлениеРеквизитаИзСинонима = Истина;
	
	Если ТипЗначенияКлючевыеРеквизитыТЧ = Тип("Строка") Тогда
		КлючевыеРеквизиты = СтрРазделить(КлючевыеРеквизитыТЧ, ",");
	ИначеЕсли ТипЗначенияКлючевыеРеквизитыТЧ = Тип("Структура") Тогда
		
		ПредставлениеРеквизитаИзСинонима = Ложь;
		КлючевыеРеквизиты = Новый Массив; // Массив из Строка
		Для Каждого ИмяИПредставлениеРеквизита Из КлючевыеРеквизитыТЧ Цикл
			
			КлючевыеРеквизиты.Добавить(ИмяИПредставлениеРеквизита.Ключ);
			
		КонецЦикла;
		
	Иначе
		КлючевыеРеквизиты = КлючевыеРеквизитыТЧ;
	КонецЕсли;
	
	КлючДанных = ОбщегоНазначенияУТ.КлючДанныхДляСообщенияПользователю(Объект);
	
	ЭтоФорма = Ложь;
	Если ТипЗнч(Объект) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		ЭтоФорма = Истина;
		
	Иначе
		
		МетаданныеОбъекта = Объект.Метаданные();
		Если ПустаяСтрока(ПредставлениеТЧ) Тогда
			ПредставлениеТЧ = МетаданныеОбъекта.ТабличныеЧасти[ИмяТЧ].Синоним;
		КонецЕсли;
		
	КонецЕсли;
	
	МассивСтрокПоляВыборки = Новый Массив; // Массив из Строка
	МассивСтрокПоляСоединения = Новый Массив; // Массив из Строка
	МассивСтрокПоляВыгрузки = Новый Массив; // Массив из Строка
	МассивСтрокСообщенияОДублях = Новый Массив; // Массив из Строка
	
	Для Каждого СтрМас Из КлючевыеРеквизиты Цикл
		
		МассивСтрокПоляВыборки.Добавить(СтрШаблон("ТаблицаПроверки.%1", СтрМас));
		МассивСтрокПоляСоединения.Добавить(СтрШаблон("ТаблицаПроверки.%1 = ДублирующиесяСтроки.%1", СтрМас));
		МассивСтрокПоляВыгрузки.Добавить(СтрМас);
		
		Если ПредставлениеРеквизитаИзСинонима Тогда
			ПредставлениеРеквизита = ?(ЭтоФорма, "", МетаданныеОбъекта.ТабличныеЧасти[ИмяТЧ].Реквизиты[СтрМас].Синоним);
		Иначе
			ПредставлениеРеквизита = ?(ЭтоФорма, "", КлючевыеРеквизитыТЧ[СтрМас]);
		КонецЕсли;
		МассивСтрокСообщенияОДублях.Добавить(СтрШаблон("""%1""", ПредставлениеРеквизита));
		
	КонецЦикла;	
	
	ТекстПоляВыборки = СтрСоединить(МассивСтрокПоляВыборки, "," + Символы.ПС);
	ТекстПоляСоединения = СтрСоединить(МассивСтрокПоляСоединения, Символы.ПС + "И" + " ");
	ТекстПоляВыгрузки = СтрСоединить(МассивСтрокПоляВыгрузки, ",");
	ТекстДляСообщенияОДублях =  СтрСоединить(МассивСтрокСообщенияОДублях, ",");
	
	ТекстЗапроса =
	"ВЫБРАТЬ 
	|	ТаблицаПроверки.НомерСтроки,
	|	&ПоляВыборки
	|ПОМЕСТИТЬ ТаблицаПроверки
	|ИЗ
	|	&ТаблицаПроверки КАК ТаблицаПроверки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаПроверки.НомерСтроки) КАК НомерСтроки,
	|	КОЛИЧЕСТВО(1) КАК КоличествоДублей,
	|	&ПоляВыборки
	|ПОМЕСТИТЬ ДублирующиесяСтроки
	|ИЗ
	|	ТаблицаПроверки КАК ТаблицаПроверки
	|
	|СГРУППИРОВАТЬ ПО 
	|	&ПоляВыборки
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(1) > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПроверки.НомерСтроки,
	|	ДублирующиесяСтроки.НомерСтроки КАК ПерваяСтрока,
	|	&ПоляВыборки
	|ИЗ
	|	ТаблицаПроверки КАК ТаблицаПроверки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДублирующиесяСтроки КАК ДублирующиесяСтроки
	|		ПО ТаблицаПроверки.НомерСтроки <> ДублирующиесяСтроки.НомерСтроки 
	|			И &ПоляСоединения";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляВыборки", ТекстПоляВыборки);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляСоединения", ?(ТекстПоляСоединения = "", "ИСТИНА", ТекстПоляСоединения));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	ТабличнаяЧасть = Объект[ИмяТЧ]; // ТабличнаяЧасть
	Запрос.УстановитьПараметр("ТаблицаПроверки", ТабличнаяЧасть.Выгрузить(ОтборСтрок, "НомерСтроки," + ТекстПоляВыгрузки));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если ЭтоФорма Тогда
		Если УказыватьНомераСтрок Тогда
			Если КлючевыеРеквизиты.Количество() = 1 Тогда
				ШаблонСообщения = НСтр("ru = 'В строке %НомерСтроки% списка ""%ПредставлениеТЧ%"" по сравнению со строкой %ПерваяСтрока% повторяется значение ""%ПовторяемоеЗначение%"" в ключевом поле.';
										|en = 'In line %НомерСтроки% of the %ПредставлениеТЧ% list, compared to line %ПерваяСтрока% the %ПовторяемоеЗначение% value is repeated in the key field.'");
			Иначе
				ШаблонСообщения = НСтр("ru = 'В строке %НомерСтроки% списка ""%ПредставлениеТЧ%"" по сравнению со строкой %ПерваяСтрока% повторяется сочетание значений ""%ПовторяемоеЗначение%"" в ключевых полях.';
										|en = 'In line %НомерСтроки% of the %ПредставлениеТЧ% list, compared to line %ПерваяСтрока% the %ПовторяемоеЗначение% value combination is repeated in key fields.'");
			КонецЕсли;
		Иначе
			Если КлючевыеРеквизиты.Количество() = 1 Тогда
				ШаблонСообщения = НСтр("ru = 'В списке ""%ПредставлениеТЧ%"" повторяется значение ""%ПовторяемоеЗначение%"" в ключевом поле.';
										|en = 'In the %ПредставлениеТЧ% list, the %ПовторяемоеЗначение% value is repeated in the key field.'");
			Иначе
				ШаблонСообщения = НСтр("ru = 'В списке ""%ПредставлениеТЧ%"" повторяется сочетание значений ""%ПовторяемоеЗначение%"" в ключевых полях.';
										|en = 'In the %ПредставлениеТЧ% list, the %ПовторяемоеЗначение% value combination is repeated in key fields.'");
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если УказыватьНомераСтрок Тогда
			Если КлючевыеРеквизиты.Количество() = 1 Тогда
				ШаблонСообщения = НСтр("ru = 'В строке %НомерСтроки% списка ""%ПредставлениеТЧ%"" по сравнению со строкой %ПерваяСтрока% повторяется значение ""%ПовторяемоеЗначение%"" в поле %НазванияПолей%.';
										|en = 'In line %НомерСтроки% of the ""%ПредставлениеТЧ%"" list, the ""%ПовторяемоеЗначение%"" value is repeated in field %НазванияПолей% in comparison with line %ПерваяСтрока%.'");
			Иначе
				ШаблонСообщения = НСтр("ru = 'В строке %НомерСтроки% списка ""%ПредставлениеТЧ%"" по сравнению со строкой %ПерваяСтрока% повторяется сочетание значений ""%ПовторяемоеЗначение%"" в полях %НазванияПолей%.';
										|en = 'In line %НомерСтроки% of the ""%ПредставлениеТЧ%"" list compared to line %ПерваяСтрока% the ""%ПовторяемоеЗначение%"" value combination is repeated in the %НазванияПолей% fields.'");
			КонецЕсли;
		Иначе
			Если КлючевыеРеквизиты.Количество() = 1 Тогда
				ШаблонСообщения = НСтр("ru = 'В списке ""%ПредставлениеТЧ%"" повторяется значение ""%ПовторяемоеЗначение%"" в поле %НазванияПолей%.';
										|en = 'In the ""%ПредставлениеТЧ%"" list, the ""%ПовторяемоеЗначение%"" value is repeated in field %НазванияПолей%.'");
			Иначе
				ШаблонСообщения = НСтр("ru = 'В списке ""%ПредставлениеТЧ%"" повторяется сочетание значений ""%ПовторяемоеЗначение%"" в полях %НазванияПолей%.';
										|en = 'In the ""%ПредставлениеТЧ%"" list, the ""%ПовторяемоеЗначение%"" value combination is repeated in fields %НазванияПолей%.'");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%ПредставлениеТЧ%", ПредставлениеТЧ);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Выборка.НомерСтроки);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПерваяСтрока%", Выборка.ПерваяСтрока);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НазванияПолей%", ТекстДляСообщенияОДублях);
		
		ПовторяемоеЗначение = "";
		
		Для Каждого СтрМас Из КлючевыеРеквизиты Цикл
			ПредставлениеЗначения = СокрЛП(Строка(Выборка[СтрМас]));
			Если ЗначениеЗаполнено(ПредставлениеЗначения) Тогда
				ПовторяемоеЗначение = ПовторяемоеЗначение + Выборка[СтрМас] + "/";
			КонецЕсли;
		КонецЦикла;
		
		ПовторяемоеЗначение = Лев(ПовторяемоеЗначение, СтрДлина(ПовторяемоеЗначение) - 1);
		
		ТекстСообщения =  СтрЗаменить(ТекстСообщения, "%ПовторяемоеЗначение%", ПовторяемоеЗначение);
		
		Если УказыватьНомераСтрок Тогда
			
			НомерСтроки = Выборка.НомерСтроки; // Число
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, НомерСтроки, "НомерСтроки");
			
		Иначе
			
			Поле = ИмяТЧ;
			
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, КлючДанных, Поле, "Объект", Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Проверка корректности указания начала и окончания периода для заданной периодичности
//
// Параметры:
//  ПериодичностьУсловий - ПеречислениеСсылка.ПериодичностиРетроБонусов
//  ДатаНачало - Дата - дата начала
//  ДатаОкончание - Дата - дата окончание
//  ПроверкаПериодаНачисления - Булево
// 
// Возвращаемое значение:
//  Структура - Проверить периоды по периодичности:
// * ЕстьНарушениеНачала - Булево - 
// * ТекстОшибкиНачала - Строка - 
// * ТребуемаяДатаНачала - Дата - 
// * ЕстьНарушениеОкончания - Булево - 
// * ТекстОшибкиОкончания - Строка - 
// * ТребуемаяДатаОкончания - Дата - 
//
Функция ПроверитьПериодыПоПериодичности(ПериодичностьУсловий, Знач ДатаНачало, Знач ДатаОкончание, ПроверкаПериодаНачисления = Ложь) Экспорт
	
	ПустаяДата = Дата(1, 1, 1);
	
	ЕстьНарушениеНачала = Ложь;
	ЕстьНарушениеОкончания = Ложь;
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("ЕстьНарушениеНачала", ЕстьНарушениеНачала);
	РезультатПроверки.Вставить("ТекстОшибкиНачала", "");
	РезультатПроверки.Вставить("ТребуемаяДатаНачала", ПустаяДата);
	РезультатПроверки.Вставить("ЕстьНарушениеОкончания", ЕстьНарушениеОкончания);
	РезультатПроверки.Вставить("ТекстОшибкиОкончания", "");
	РезультатПроверки.Вставить("ТребуемаяДатаОкончания", ПустаяДата);
	
	НачалоЗаполнено = (НачалоДня(ДатаНачало) <> ПустаяДата);
	ОкончаниеЗаполнено = (НачалоДня(ДатаОкончание) <> ПустаяДата);
	
	Если ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.Неделя Тогда
		
		ТребуемаяДатаНачала = НачалоНедели(ДатаНачало);
		ТребуемаяДатаОкончания = НачалоДня(КонецНедели(ДатаОкончание));
		
		Если НачалоЗаполнено
		   И ТребуемаяДатаНачала <> НачалоДня(ДатаНачало) Тогда
			
			ЕстьНарушениеНачала = Истина;
			ШаблонСообщенияНачалоДействия = НСтр("ru = 'Начало действия должно быть началом недели (%1)';
												|en = 'The effective date must be the beginning of the week (%1)'");
			ШаблонСообщенияНачалоПериода = НСтр("ru = 'Начало периода должно быть началом недели (%1)';
												|en = 'The period start must be the beginning of the week (%1)'");
			
		КонецЕсли;
		
		Если ОкончаниеЗаполнено
		   И ТребуемаяДатаОкончания <> НачалоДня(ДатаОкончание) Тогда
			
			ЕстьНарушениеОкончания = Истина;
			ШаблонСообщенияОкончанияДействия = НСтр("ru = 'Окончание действия должно быть концом недели (%1)';
													|en = 'The expiration date must be the end of the week (%1)'");
			ШаблонСообщенияОкончанияПериода = НСтр("ru = 'Окончание периода должно быть концом недели (%1)';
													|en = 'The period end must be the end of the week (%1)'");
			
		КонецЕсли;
		
	ИначеЕсли ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.Месяц Тогда
		
		ТребуемаяДатаНачала = НачалоМесяца(ДатаНачало);
		ТребуемаяДатаОкончания = НачалоДня(КонецМесяца(ДатаОкончание));
		
		Если НачалоЗаполнено
		   И ТребуемаяДатаНачала <> НачалоДня(ДатаНачало) Тогда
			
			ЕстьНарушениеНачала = Истина;
			ШаблонСообщенияНачалоДействия = НСтр("ru = 'Начало действия должно быть началом месяца (%1)';
												|en = 'The effective date must be the beginning of the month (%1)'");
			ШаблонСообщенияНачалоПериода = НСтр("ru = 'Начало периода должно быть началом месяца (%1)';
												|en = 'The period start must be the beginning of the month (%1)'");
			
		КонецЕсли;
		
		Если ОкончаниеЗаполнено
		   И  ТребуемаяДатаОкончания <> НачалоДня(ДатаОкончание) Тогда
			
			ЕстьНарушениеОкончания = Истина;
			ШаблонСообщенияОкончанияДействия = НСтр("ru = 'Окончание действия должно быть концом месяца (%1)';
													|en = 'The expiration date must be the end of the month (%1)'");
			ШаблонСообщенияОкончанияПериода = НСтр("ru = 'Окончание периода должно быть концом месяца (%1)';
													|en = 'The period end must be the end of the month (%1)'");
			
		КонецЕсли;
		
	ИначеЕсли ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.Квартал Тогда
		
		ТребуемаяДатаНачала = НачалоКвартала(ДатаНачало);
		ТребуемаяДатаОкончания = НачалоДня(КонецКвартала(ДатаОкончание));
		
		Если НачалоЗаполнено
		   И ТребуемаяДатаНачала <> НачалоДня(ДатаНачало) Тогда
			
			ЕстьНарушениеНачала = Истина;
			ШаблонСообщенияНачалоДействия = НСтр("ru = 'Начало действия должно быть началом квартала (%1)';
												|en = 'The effective date must be the beginning of the quarter (%1)'");
			ШаблонСообщенияНачалоПериода = НСтр("ru = 'Начало периода должно быть началом квартала (%1)';
												|en = 'The period start must be the beginning of the quarter (%1)'");
			
		КонецЕсли;
		
		Если ОкончаниеЗаполнено
		   И  ТребуемаяДатаОкончания <> НачалоДня(ДатаОкончание) Тогда
			
			ЕстьНарушениеОкончания = Истина;
			ШаблонСообщенияОкончанияДействия = НСтр("ru = 'Окончание действия должно быть концом квартала (%1)';
													|en = 'The expiration date must be the end of the quarter (%1)'");
			ШаблонСообщенияОкончанияПериода = НСтр("ru = 'Окончание периода должно быть концом квартала (%1)';
													|en = 'The period end must be the end of the quarter (%1)'");
			
		КонецЕсли;
		
	КонецЕсли;
	
	РезультатПроверки.ТребуемаяДатаНачала = ТребуемаяДатаНачала;
	РезультатПроверки.ТребуемаяДатаОкончания = ТребуемаяДатаОкончания;
	
	РезультатПроверки.ЕстьНарушениеНачала = ЕстьНарушениеНачала;
	Если ЕстьНарушениеНачала Тогда
		
		Если ПроверкаПериодаНачисления Тогда
			ШаблонСообщенияНачала = ШаблонСообщенияНачалоПериода;
		Иначе
			ШаблонСообщенияНачала = ШаблонСообщенияНачалоДействия;
		КонецЕсли;
		
		РезультатПроверки.ТекстОшибкиНачала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщенияНачала,
			Формат(ТребуемаяДатаНачала, "ДЛФ=D;"));
		
	КонецЕсли;
	
	РезультатПроверки.ЕстьНарушениеОкончания = ЕстьНарушениеОкончания;
	Если ЕстьНарушениеОкончания Тогда
		
		Если ПроверкаПериодаНачисления Тогда
			ШаблонСообщенияОкончания = ШаблонСообщенияОкончанияПериода;
		Иначе
			ШаблонСообщенияОкончания = ШаблонСообщенияОкончанияДействия;
		КонецЕсли;
		
		РезультатПроверки.ТекстОшибкиОкончания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщенияОкончания,
			Формат(ТребуемаяДатаОкончания, "ДЛФ=D;"));
		
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

#Область Прочее

// Выполняет установку параметров сеанса. Вызывается из модуля сеанса.
//
// Параметры:
//   ИмяПараметра  - Строка - имя параметра сеанса.
//   УстановленныеПараметры - Массив из Строка - массив всех установленных параметров сеанса.
//
Процедура УстановитьПараметрыСеанса(ИмяПараметра, УстановленныеПараметры) Экспорт
	
	ПараметрыСеанса.ПроводитьБезКонтроляОстатковРетроБонусовКлиентов = Ложь;
	
	Если ИмяПараметра = "ПроводитьБезКонтроляОстатковРетроБонусовКлиентов" Тогда
		
		ПараметрыСеанса.ПроводитьБезКонтроляОстатковРетроБонусовКлиентов = Ложь;
		УстановленныеПараметры.Добавить(ИмяПараметра);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Смотри также ОбновлениеИнформационнойБазыПереопределяемый.ПриНачальномЗаполненииЭлементов
//
// Параметры:
//  КодыЯзыков - см. ОбновлениеИнформационнойБазыПереопределяемый.ПриНачальномЗаполненииЭлементов.КодыЯзыков
//  Элементы   - см. ОбновлениеИнформационнойБазыПереопределяемый.ПриНачальномЗаполненииЭлементов.Элементы
//  ТабличныеЧасти - см. ОбновлениеИнформационнойБазыПереопределяемый.ПриНачальномЗаполненииЭлементов.ТабличныеЧасти
//
Процедура ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти) Экспорт
	
	#Область РетроБонусыКлиентов
	
	Набор = Элементы.Добавить();
	Набор.ИмяПредопределенногоНабора = "Справочник_ВидыРетроБонусовКлиентов";
	Набор.Ссылка = Новый УникальныйИдентификатор("a5dc0822-c1f3-46f1-aa4f-966144930cbd");
	Набор.Используется = ПолучитьФункциональнуюОпцию("ИспользоватьРетроБонусыКлиентов");
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Набор, "Наименование",
		"ru = 'Виды ретро-бонусов клиентов';
		|en = 'Customer rebate kinds'", КодыЯзыков); // @НСтр-2
	
	Набор = Элементы.Добавить();
	Набор.ИмяПредопределенногоНабора = "Документ_УсловияРетроБонусовКлиентов";
	Набор.Ссылка = Новый УникальныйИдентификатор("8a733795-0f00-41e7-8e2e-8f908562db9e");
	Набор.Используется = ПолучитьФункциональнуюОпцию("ИспользоватьРетроБонусыКлиентов");
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Набор, "Наименование",
		"ru = 'Условия ретро-бонусов клиентов';
		|en = 'Conditions of customer rebates'", КодыЯзыков); // @НСтр-2
	
	Набор = Элементы.Добавить();
	Набор.ИмяПредопределенногоНабора = "Документ_СписаниеРетроБонусовКлиента";
	Набор.Ссылка = Новый УникальныйИдентификатор("b770ef38-80ab-4ae0-bc77-f07eed4a2468");
	Набор.Используется = ПолучитьФункциональнуюОпцию("ИспользоватьРетроБонусыКлиентов");
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Набор, "Наименование",
		"ru = 'Списания ретро-бонусов клиентов';
		|en = 'Write off customer rebates'", КодыЯзыков); // @НСтр-2
	
	Набор = Элементы.Добавить();
	Набор.ИмяПредопределенногоНабора = "Документ_НачислениеРетроБонусовКлиента";
	Набор.Ссылка = Новый УникальныйИдентификатор("ec2e9239-8e45-421e-9444-6d8695e7fd63");
	Набор.Используется = ПолучитьФункциональнуюОпцию("ИспользоватьРетроБонусыКлиентов");
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Набор, "Наименование",
		"ru = 'Начисления ретро-бонусов клиентов';
		|en = 'Accrual of customer rebates'", КодыЯзыков); // @НСтр-2
	
	#КонецОбласти
	
	#Область РетроБонусыПоставщиков
	
	Набор = Элементы.Добавить();
	Набор.ИмяПредопределенногоНабора = "Справочник_ВидыРетроБонусовПоставщиков";
	Набор.Ссылка = Новый УникальныйИдентификатор("10bd040b-2e2c-42a1-8aeb-286679ff65c1");
	Набор.Используется  = ПолучитьФункциональнуюОпцию("ИспользоватьРетроБонусыПоставщиков");
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Набор, "Наименование",
		"ru = 'Виды ретро-бонусов поставщиков';
		|en = 'Vendor rebate kinds'", КодыЯзыков); // @НСтр-2
	
	Набор = Элементы.Добавить();
	Набор.ИмяПредопределенногоНабора = "Документ_УсловияРетроБонусовПоставщика";
	Набор.Ссылка = Новый УникальныйИдентификатор("8b5044da-952b-45a9-a41e-acc51eeff380");
	Набор.Используется  = ПолучитьФункциональнуюОпцию("ИспользоватьРетроБонусыПоставщиков");
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Набор, "Наименование",
		"ru = 'Условия ретро-бонусов поставщика';
		|en = 'Conditions of vendor rebates'", КодыЯзыков); // @НСтр-2
	
	Набор = Элементы.Добавить();
	Набор.ИмяПредопределенногоНабора = "Документ_НачислениеРетроБонусовПоставщика";
	Набор.Ссылка = Новый УникальныйИдентификатор("5e2d68e4-3440-4cd3-badc-7d3fa36d40ca");
	Набор.Используется  = ПолучитьФункциональнуюОпцию("ИспользоватьРетроБонусыПоставщиков");
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Набор, "Наименование",
		"ru = 'Начисление ретро-бонусов поставщика';
		|en = 'Accrual of vendor rebates'", КодыЯзыков); // @НСтр-2
	
	Набор = Элементы.Добавить();
	Набор.ИмяПредопределенногоНабора = "Документ_СписаниеРетроБонусовПоставщика";
	Набор.Ссылка = Новый УникальныйИдентификатор("9a178e3f-35c3-4a06-b20c-e840c03805aa");
	Набор.Используется = ПолучитьФункциональнуюОпцию("ИспользоватьРетроБонусыПоставщиков");
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Набор, "Наименование",
		"ru = 'Списания ретро-бонусов поставщиков';
		|en = 'Write off vendor rebates'", КодыЯзыков); // @НСтр-2
	
	#КонецОбласти
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.Справочники.АктПремииКлиентуПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Справочники.АктПремииПоставщикаПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Справочники.УсловияРетроБонусовКлиентовПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Справочники.УсловияРетроБонусовПоставщикаПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Справочники.НачислениеРетроБонусовКлиентаПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Справочники.НачислениеРетроБонусовПоставщикаПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Справочники.СписаниеРетроБонусовКлиентаПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Справочники.СписаниеРетроБонусовПоставщикаПрисоединенныеФайлы, Истина);
	
	Списки.Вставить(Метаданные.Документы.УсловияРетроБонусовКлиентов, Истина);
	Списки.Вставить(Метаданные.Документы.УсловияРетроБонусовПоставщика, Истина);
	Списки.Вставить(Метаданные.Документы.НачислениеРетроБонусовКлиента, Истина);
	Списки.Вставить(Метаданные.Документы.НачислениеРетроБонусовПоставщика, Истина);
	Списки.Вставить(Метаданные.Документы.СписаниеРетроБонусовКлиента, Истина);
	Списки.Вставить(Метаданные.Документы.СписаниеРетроБонусовПоставщика, Истина);
	Списки.Вставить(Метаданные.Документы.АктПремииКлиенту, Истина);
	Списки.Вставить(Метаданные.Документы.АктПремииПоставщика, Истина);
	
	Списки.Вставить(Метаданные.РегистрыСведений.ЗавершенныеУсловияРетроБонусовПоставщиков, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.ЗавершенныеУсловияРетроБонусовПоставщиковИстория, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыКлиентовИНН, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыПоставщиковИНН, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыКлиентовКонтрагенты, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыПоставщиковКонтрагенты, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыПоставщиковПоставщики, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыКлиентовСегментыПартнеров, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыКлиентовСегментыТоваров, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыПоставщиковСегментыТоваров, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыПоставщиковСклады, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыКлиентовТовары, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыПоставщиковТовары, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыКлиентовСвойстваНоменклатуры, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыПоставщиковСвойстваНоменклатуры, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыКлиентовУсловия, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыПоставщиковУсловия, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыКлиентовПериодыНачислений, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыКлиентовДоговорыСоглашения, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.РетроБонусыШкалыРасчета, Истина);
	
	Списки.Вставить(Метаданные.РегистрыНакопления.РетроБонусыКлиентов, Истина);
	Списки.Вставить(Метаданные.РегистрыНакопления.РетроБонусыПоставщиков, Истина);
	
КонецПроцедуры

// См. ВариантыОтчетовПереопределяемый.ОпределитьОбъектыСКомандамиОтчетов
Процедура ОпределитьОбъектыСКомандамиОтчетов(Объекты) Экспорт
	
	Объекты.Добавить(Метаданные.Документы.УсловияРетроБонусовКлиентов);
	Объекты.Добавить(Метаданные.Документы.НачислениеРетроБонусовКлиента);
	Объекты.Добавить(Метаданные.Документы.АктПремииКлиенту);
	Объекты.Добавить(Метаданные.Документы.АктПремииПоставщика);
	Объекты.Добавить(Метаданные.Документы.УсловияРетроБонусовПоставщика);
	Объекты.Добавить(Метаданные.Документы.НачислениеРетроБонусовПоставщика);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область Проведение

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - См. ПроведениеДокументов.СвойстваДокумента
//
// Возвращаемое значение:
//  См. ПроведениеДокументов.ПараметрыУчетногоМеханизма
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	ТипДокумента = ТипЗнч(Документ);
	Если ТипДокумента = Тип("ДокументОбъект.УсловияРетроБонусовКлиентов")
	 ИЛИ ТипДокумента = Тип("ДокументОбъект.УсловияРетроБонусовПоставщика") Тогда
		
		Параметры.ЕстьПроизводныеДвижения = Истина;
		
	КонецЕсли;
	
	Если ТипДокумента = Тип("ДокументОбъект.УсловияРетроБонусовКлиентов")
	 ИЛИ ТипДокумента = Тип("ДокументОбъект.НачислениеРетроБонусовКлиента")
	 ИЛИ ТипДокумента = Тип("ДокументОбъект.СписаниеРетроБонусовКлиента")
	 ИЛИ ТипДокумента = Тип("ДокументОбъект.АктПремииКлиенту") Тогда
		
		// Проведение
		Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			
			Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.РетроБонусыКлиентов);
			
		КонецЕсли;
		
		// Контроль
		Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
			
			Параметры.КонтрольныеРегистрыИзменений.Добавить(Метаданные.РегистрыНакопления.РетроБонусыКлиентов);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипДокумента  = Тип("ДокументОбъект.НачислениеРетроБонусовКлиента") Тогда
		
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыСведений.РетроБонусыКлиентовПериодыНачислений);
		
	КонецЕсли;
	
	Если ТипДокумента = Тип("ДокументОбъект.УсловияРетроБонусовПоставщика")
	 ИЛИ ТипДокумента = Тип("ДокументОбъект.НачислениеРетроБонусовПоставщика")
	 ИЛИ ТипДокумента = Тип("ДокументОбъект.СписаниеРетроБонусовПоставщика")
	 ИЛИ ТипДокумента = Тип("ДокументОбъект.АктПремииПоставщика") Тогда
		
		// Проведение
		Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			
			Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.РетроБонусыПоставщиков);
			
		КонецЕсли;
		
		// Контроль
		Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
			
			Параметры.КонтрольныеРегистрыИзменений.Добавить(Метаданные.РегистрыНакопления.РетроБонусыПоставщиков);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Возвращает тексты запросов для сторнирования движений при исправлении документов
// 
// Параметры:
// 	МетаданныеДокумента - ОбъектМетаданныхДокумент - Метаданные документа, который проводится.
// 
// Возвращаемое значение:
// 	Соответствие Из КлючИЗначение - Содержит тексты запросов сторнирования, где:
// 		* Ключ - Строка - Полное имя регистра.
// 		* Значение - Строка - Текст запроса.
//
Функция ТекстыЗапросовСторнирования(МетаданныеДокумента) Экспорт
	
	ТекстыЗапросов = Новый Соответствие();
	
	Возврат ТекстыЗапросов;
	
КонецФункции

// Возникает перед выполнением записи регистров документа.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц,
//								используемый для хранения таблиц контроля изменений регистров.
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура ПередЗаписьюДвиженийДокумента(Документ, МенеджерВременныхТаблиц, Отказ) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Возникает после выполнения записи регистров документа
//
// Параметры:
//  Документ - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика - записываемый документ
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц,
//								используемый для хранения таблиц контроля изменений регистров.
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура ПослеЗаписиДвиженийДокумента(Документ, МенеджерВременныхТаблиц, Отказ) Экспорт
	
	СвойстваДокумента = ПроведениеДокументов.СвойстваДокумента(Документ);
	
	ИменаРегистров = Новый Массив; // Массив из Строка
	
	ТипДокумента = ТипЗнч(Документ);
	Если ТипДокумента = Тип("ДокументОбъект.УсловияРетроБонусовКлиентов") Тогда
		
		ИменаРегистров.Добавить("РетроБонусыКлиентовУсловия");
		ИменаРегистров.Добавить("РетроБонусыКлиентовКонтрагенты");
		ИменаРегистров.Добавить("РетроБонусыКлиентовИНН");
		ИменаРегистров.Добавить("РетроБонусыКлиентовДоговорыСоглашения");
		ИменаРегистров.Добавить("РетроБонусыКлиентовСегментыПартнеров");
		ИменаРегистров.Добавить("РетроБонусыКлиентовТовары");
		ИменаРегистров.Добавить("РетроБонусыКлиентовСвойстваНоменклатуры");
		ИменаРегистров.Добавить("РетроБонусыКлиентовСегментыТоваров");
		ИменаРегистров.Добавить("РетроБонусыШкалыРасчета");
		
	ИначеЕсли ТипДокумента = Тип("ДокументОбъект.УсловияРетроБонусовПоставщика") Тогда
		
		ИменаРегистров.Добавить("РетроБонусыПоставщиковУсловия");
		ИменаРегистров.Добавить("РетроБонусыПоставщиковИНН");
		ИменаРегистров.Добавить("РетроБонусыПоставщиковТовары");
		ИменаРегистров.Добавить("РетроБонусыПоставщиковСвойстваНоменклатуры");
		ИменаРегистров.Добавить("РетроБонусыПоставщиковСегментыТоваров");
		ИменаРегистров.Добавить("РетроБонусыПоставщиковКонтрагенты");
		ИменаРегистров.Добавить("РетроБонусыПоставщиковПоставщики");
		ИменаРегистров.Добавить("РетроБонусыПоставщиковСклады");
		ИменаРегистров.Добавить("РетроБонусыШкалыРасчета");
		
	Иначе
		
		ШаблонИсключения = НСтр("ru = 'В метод %1 передан неизвестный тип документа %2';
								|en = 'An unknown document type %2 was passed to method %1'");
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонИсключения,
			"РетроБонусыСервер.ПослеЗаписиДвиженийДокумента",
			Строка(ТипДокумента));
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	ИсходныйДокумент = ?(Документ.ИсправляемыйДокумент.Пустая(), Документ.Ссылка, Документ.ИсправляемыйДокумент);
	
	Если СвойстваДокумента.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		
		Если Документ.ИсправляемыйДокумент.Пустая() Тогда
			
			ОчиститьРегистрыАктуальныхДанных(Документ.Ссылка, ИменаРегистров);
			
		Иначе
			
			Выборка = АктуальныйДокументУсловийРетроБонусов(Документ);
			Если Выборка.Следующий() Тогда
				
				АктуальныйДокумент = Выборка.Ссылка; // ДокументСсылка.УсловияРетроБонусовПокупателей
				СформироватьРегистрыАктуальныхДанных(АктуальныйДокумент, ИсходныйДокумент, ИменаРегистров);
				
			Иначе
				
				// Остался только сам исходный документ
				СформироватьРегистрыАктуальныхДанных(ИсходныйДокумент, ИсходныйДокумент, ИменаРегистров);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Если Документ.Статус = Перечисления.СтатусыДокументовРетроБонусов.Согласован
		 ИЛИ Документ.ИсправляемыйДокумент.Пустая() Тогда
			
			СформироватьРегистрыАктуальныхДанных(Документ.Ссылка, ИсходныйДокумент, ИменаРегистров);
			
		Иначе
			
			Выборка = АктуальныйДокументУсловийРетроБонусов(Документ);
			
			Если Выборка.Следующий() Тогда
				
				АктуальныйДокумент = Выборка.Ссылка; // ДокументСсылка.УсловияРетроБонусовПокупателей
				СформироватьРегистрыАктуальныхДанных(АктуальныйДокумент, ИсходныйДокумент, ИменаРегистров);
				
			Иначе
				
				// Остался только сам исходный документ
				СформироватьРегистрыАктуальныхДанных(ИсходныйДокумент, ИсходныйДокумент, ИменаРегистров);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирования движений по подчиненным регистрам управления ассортиментом.
//
// Параметры:
//	ТаблицыДляДвижений - Структура - таблицы данных документа
//	Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//	Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "РетроБонусыКлиентов");
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "РетроБонусыКлиентовПериодыНачислений");
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "РетроБонусыПоставщиков");
	
КонецПроцедуры

// Дополняет текст запроса механизма проверки даты запрета по таблице изменений.
// 
// Параметры:
// 	Запрос - Запрос - используется для установки параметров запроса.
// 
// Возвращаемое значение:
//	Соответствие Из КлючИЗначение:
//		* Ключ - Строка - Имя временной таблицы изменений регистра.
//		* Значение - Строка - Текст запроса.
//
Функция ТекстыЗапросовКонтрольДатыЗапретаПоТаблицеИзменений(Запрос) Экспорт
	
	СоответствиеТекстовЗапросов = Новый Соответствие();
	Возврат СоответствиеТекстовЗапросов;
	
КонецФункции

// Формирует тексты запросов для контроля изменений записанных движений регистров.
//
// Параметры:
//  Запрос - Запрос - запрос, хранящий параметры используемые в списке запросов
//  ТекстыЗапроса - СписокЗначений из Строка - список текстов запросов и их имен.
//  Документ - ДокументОбъект - записываемый документ.
//
Процедура ИнициализироватьДанныеКонтроляИзменений(Запрос, ТекстыЗапроса, Документ) Экспорт
	
	ИнициализацияКонтроляИзмененийРетроБонусыКлиентов(Запрос, ТекстыЗапроса, Документ);
	
КонецПроцедуры

// Выводит сообщения пользователю при наличии ошибок контроля изменений записанных движений регистров.
//
// Параметры:
//  РезультатыКонтроля - Структура - таблицы с результатами контроля изменений:
//   * ОшибкиСписанияСуммБонусов - ТаблицаЗначений:
//     ** Организация - СправочникСсылка.Организации
//     ** Контрагент - СправочникСсылка.Контрагенты
//     ** Партнер - СправочникСсылка.Партнеры
//     ** НачалоПериода - Дата
//     ** ОкончаниеПериода - Дата
//     ** ДокументУсловий - ДокументСсылка.УсловияРетроБонусовКлиентов
//     ** Валюта - СправочникСсылка.Валюты
//     ** СуммаБонусОстаток - ОпределяемыйТип.ДенежнаяСуммаЛюбогоЗнака
//     ** КонтрольНаДатуДокумента - Булево
//  Документ - ДокументОбъект - записываемый документ
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура СообщитьОРезультатахКонтроляИзменений(РезультатыКонтроля, Документ, Отказ) Экспорт
	
	КонтрольИзмененийРетроБонусыКлиентов(РезультатыКонтроля, Документ, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииПоставляемыхПрофилейГруппДоступа
//
Процедура ДополнитьПоставляемыеПрофилиГруппДоступа(ОписанияПрофилей, ПараметрыОбновления) Экспорт
	
	ДобавитьПрофильОтветственныйВидыРетроБонусовКлиентов(ОписанияПрофилей, ПараметрыОбновления);
	ДобавитьПрофильМенеджерРетроБонусовКлиентов(ОписанияПрофилей, ПараметрыОбновления);
	ДобавитьПрофильУчастникСогласованияУсловийРетроБонусовКлиентов(ОписанияПрофилей, ПараметрыОбновления);
	ДобавитьПрофильОтветственныйАктыПремийКлиентамДополнительный(ОписанияПрофилей, ПараметрыОбновления);
	
	ДобавитьПрофильОтветственныйВидыРетроБонусовПоставщиков(ОписанияПрофилей, ПараметрыОбновления);
	ДобавитьПрофильМенеджерРетроБонусовПоставщиков(ОписанияПрофилей, ПараметрыОбновления);
	ДобавитьПрофильУчастникСогласованияУсловийРетроБонусовПоставщиков(ОписанияПрофилей, ПараметрыОбновления);
	ДобавитьПрофильОтветственныйАктыПремийПоставщикаДополнительный(ОписанияПрофилей, ПараметрыОбновления);
	
	ДобавитьПрофильПравоОтключенияКонтроляОстатковРетроБонусовДополнительный(ОписанияПрофилей, ПараметрыОбновления);
	
КонецПроцедуры

#КонецОбласти

#Область ПакетнаяОбработкаСтрок

// см. ПакетнаяОбработкаТабличнойЧастиСервер.ПриДобавленииОбработчиковСтрокКоллекции
Процедура ПриДобавленииОбработчиковСтрокКоллекции(ОбработчикиСтрок) Экспорт
	
	ОбработчикиСтрок.Добавить(
		"ЗаполнитьПризнакСоглашенияВСтрокеТЧ",
		"РетроБонусыСервер.ОбработкаСтрокЗаполнитьПризнакСоглашенияВСтрокеТЧ");
	
	ОбработчикиСтрок.Добавить(
		"ЗаполнитьПризнакУказанияВалютыРетроБонусовПоставщиков",
		"РетроБонусыСервер.ОбработкаСтрокЗаполнитьСвойстваРетроБонусовПоставщиков");
	
	ОбработчикиСтрок.Добавить(
		"ЗаполнитьВалютуРетроБонусовПоставщиков",
		"РетроБонусыСервер.ОбработкаСтрокЗаполнитьСвойстваРетроБонусовПоставщиков");
	
КонецПроцедуры

// Параметры:
//	ТекущаяСтрока - Структура:
//		* Соглашение - СправочникСсылка.СоглашенияСКлиентами
//		* ЭтоТиповоеСоглашение - Булево
//	СтруктураДействий - См. ПакетнаяОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ.СтруктураДействий
//	КешированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения
//
Процедура ОбработкаСтрокЗаполнитьПризнакСоглашенияВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КешированныеЗначения = Неопределено) Экспорт
	
	Перем ИмяПоляСоглашение; // Строка
	
	СтруктураДействий.Свойство("ЗаполнитьПризнакСоглашенияВСтрокеТЧ", ИмяПоляСоглашение);
	
	ДанныеДляОбработки = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки(
		"ПризнакиСоглашений", КешированныеЗначения); 
	
	Если ДанныеДляОбработки <> Неопределено Тогда
		
		ПолученныеДанные = ДанныеДляОбработки[0].ЭтоТиповоеСоглашение; // Булево
		ТекущаяСтрока.ЭтоТиповоеСоглашение = ПолученныеДанные;
		
		ЗначениеСоглашения = ТекущаяСтрока[ИмяПоляСоглашение]; // СправочникСсылка.СоглашенияСКлиентами
		КешированныеЗначения.ПризнакиСоглашений[ЗначениеСоглашения] = ТекущаяСтрока.ЭтоТиповоеСоглашение;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//	ТекущаяСтрока - Структура:
//		* ДокументУсловий - ДокументСсылка.УсловияРетроБонусовПоставщика
//		* ВалютаОпределяетсяУсловием - Булево
//		* ВалютаБонуса - СправочникСсылка.Валюты
//	СтруктураДействий - См. ПакетнаяОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ.СтруктураДействий
//	КешированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения
//
Процедура ОбработкаСтрокЗаполнитьСвойстваРетроБонусовПоставщиков(ТекущаяСтрока, СтруктураДействий, КешированныеЗначения = Неопределено) Экспорт
	
	ДанныеДляОбработки = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки(
		"СвойстваРетроБонусовПоставщиков", КешированныеЗначения); 
	
	Если ДанныеДляОбработки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ДействиеЗаполнения Из СтруктураДействий Цикл
		
		ДействиеТребуется = ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
			ДействиеЗаполнения.Ключ,
			СтруктураДействий,
			КешированныеЗначения); 
		
		Если НЕ ДействиеТребуется  Тогда 
			СтруктураДействий.Удалить(ДействиеЗаполнения.Ключ);
		КонецЕсли;
		
	КонецЦикла;
	
	ДопИнформация = ПакетнаяОбработкаТабличнойЧастиСервер.ОписаниеДополнительнойИнформации(СтруктураДействий);
	
	ЗаполнитьЗначенияСвойств(
		ТекущаяСтрока,
		ДанныеДляОбработки[0],
		ДопИнформация.РеквизитыЗаполнения);
	
КонецПроцедуры

// Параметры:
//  Действия - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ.СтруктураДействий
//  ОписаниеЗапроса - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОписаниеЗапроса
//  КешированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения
//
Процедура ДополнитьТекстыЗапросовИсточниковДанных(Действия, ОписаниеЗапроса, КешированныеЗначения) Экспорт
	
	ДополнитьТекстЗапросаПризнакСоглашенияВСтрокеТЧ(Действия, ОписаниеЗапроса, КешированныеЗначения);
	ДополнитьТекстЗапросаСвойствамиРетроБонусовПоставщиков(Действия, ОписаниеЗапроса, КешированныеЗначения);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСФормами

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма списка:
//   * Список - ДинамическийСписок
//
Процедура УстановитьОформлениеСтатуса(Форма) Экспорт
	
	СписокУсловноеОформление = Форма.Список.УсловноеОформление;
	
	#Область НаСогласовании
	Элемент = СписокУсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Выделение шрифтом статуса ""На согласовании""';
								|en = 'Highlight the ""On approval"" status with font'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Статус");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Статус");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДокументовРетроБонусов.НаСогласовании;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ЖирныйШрифт);
	#КонецОбласти
	
	#Область Согласован
	Элемент = СписокУсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Выделение цветом статуса ""Согласован""';
								|en = 'Highlight the ""Approved"" status'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Статус");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Статус");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДокументовРетроБонусов.Согласован;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.РезультатУспехЦвет);
	#КонецОбласти
	
	#Область НеСогласован
	Элемент = СписокУсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Выделение цветом статуса ""Не согласован""';
								|en = 'Highlight the ""Not approved"" status'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Статус");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Статус");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыДокументовРетроБонусов.НеСогласован;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.РезультатПроблемаЦвет);
	#КонецОбласти
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма списка:
//   * Список - ДинамическийСписок
//
Процедура УстановитьОформлениеМультивалютныхДокументов(Форма) Экспорт
	
	СписокУсловноеОформление = Форма.Список.УсловноеОформление;
	
	#Область Валюта
	Элемент = СписокУсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Скрытие валюты мультивалютных документов';
								|en = 'Hiding the currency of multicurrency documents'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Валюта");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("МультивалютныйДокумент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", "<..>");
	Элемент.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение", ГоризонтальноеПоложение.Центр);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	#КонецОбласти
	
	#Область СуммаДокумента
	Элемент = СписокУсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Текст мультивалютных документов';
								|en = 'Text of multicurrency documents'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("СуммаДокумента");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("МультивалютныйДокумент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", "<..>");
	Элемент.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение", ГоризонтальноеПоложение.Центр);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	#КонецОбласти
	
КонецПроцедуры

// Установить заголовки декораций фиксации сегментов.
// 
// Параметры:
//  Форма - См. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента
//  	  - См. Документ.УсловияРетроБонусовПоставщика.Форма.ФормаДокумента
//
Процедура УстановитьЗаголовкиДекорацийСГиперссылками(Форма) Экспорт
	
	Если Форма.ЕстьНачисления Тогда
		
		ТекстОтменитьСогласование = НСтр("ru = 'Созданы начисления, отмена согласования запрещена';
										|en = 'Accruals are created. Cannot cancel the approval'");
		
	Иначе
		
		ШаблонОтменитьСогласование = НСтр("ru = '<a href=""%1"">Отменить согласование</a>';
											|en = '<a href=""%1"">Cancel approval</a>'");
		ТекстОтменитьСогласование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОтменитьСогласование, "ОтменитьСогласование");
		
	КонецЕсли;
	
	Если Форма.ЕстьКорректировкаНаСогласовании Тогда
		
		ТекстЗафиксировать = НСтр("ru = 'Введена корректировка в статусе ""На согласовании"", фиксация состава документа запрещена';
									|en = 'Adjustment is entered in the ""Pending approval"" status. Cannot record the document'");
		
	Иначе
		
		ШаблонЗафиксировать = НСтр("ru = 'Состав документа не зафиксирован. <a href=""%1"">Зафиксировать</a>.';
									|en = 'The document content is not recorded. <a href=""%1"">Record</a>.'");
		ТекстЗафиксировать = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонЗафиксировать, "Зафиксировать");
		
	КонецЕсли;
	
	Если Форма.ЕстьНачисления Тогда
		
		ТекстОтменить = НСтр("ru = 'Созданы начисления, отмена фиксации состава документа запрещена';
							|en = 'Accruals are created. Cannot cancel the document recording'");
		
	Иначе
		
		ШаблонОтменитьФиксацию = НСтр("ru = 'Состав документа зафиксирован. <a href=""%1"">Отменить фиксацию</a>.';
										|en = 'The document content is recorded. <a href=""%1"">Cancel recording</a>.'");
		ТекстОтменить = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОтменитьФиксацию, "ОтменитьФиксацию");
		
	КонецЕсли;
	
	Форма.Элементы.ДекорацияЗафиксироватьСостав.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ТекстЗафиксировать);
	Форма.Элементы.ДекорацияОтменитьФиксациюСостава.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ТекстОтменить);
	Форма.Элементы.ДекорацияОтменитьСогласование.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ТекстОтменитьСогласование);
	
КонецПроцедуры

// Параметры:
//  Форма - См. Справочник.ВидыРетроБонусовКлиентов.Форма.ФормаСписка
//  	  - См. Справочник.ВидыРетроБонусовКлиентов.Форма.ФормаВыбора
//  	  - См. Справочник.ВидыРетроБонусовПоставщиков.Форма.ФормаСписка
//  	  - См. Справочник.ВидыРетроБонусовПоставщиков.Форма.ФормаВыбора
//	ИменаПолей - Массив из Строка - 
//
Процедура УстановитьУсловноеОформлениеСпискаВидаБезРасчета(Форма, ИменаПолей) Экспорт
	
	Элемент = Форма.УсловноеОформление.Элементы.Добавить();
	
	Для Каждого ИмяПоля Из ИменаПолей Цикл
		
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
		
	КонецЦикла;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.БезРасчета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

// Параметры:
//  Форма - См. Справочник.ВидыРетроБонусовКлиентов.Форма.ФормаСписка
//  	  - См. Справочник.ВидыРетроБонусовКлиентов.Форма.ФормаВыбора
//  	  - См. Справочник.ВидыРетроБонусовПоставщиков.Форма.ФормаСписка
//  	  - См. Справочник.ВидыРетроБонусовПоставщиков.Форма.ФормаВыбора
//	ИменаПолей - Массив из Строка - 
//
Процедура УстановитьУсловноеОформлениеСпискаВидаПоказательТоваровНеИспользуется(Форма, ИменаПолей) Экспорт
	
	Элемент = Форма.УсловноеОформление.Элементы.Добавить();
	
	Для Каждого ИмяПоля Из ИменаПолей Цикл
		
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
		
	КонецЦикла;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ПоказательТоваров");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.НеИспользуется;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

// Параметры:
//  УсловноеОформление - УсловноеОформлениеКомпоновкиДанных
//
Процедура УстановитьОформлениеТаблицыШкалаРасчета(УсловноеОформление) Экспорт
	
	УстановитьОформлениеШкалаРасчетаДиапазонОт(УсловноеОформление);
	УстановитьОформлениеШкалаРасчетаБонусПроцент(УсловноеОформление);
	
КонецПроцедуры

// Параметры:
//  УсловноеОформление - УсловноеОформлениеКомпоновкиДанных
//  ИмяТаблицы - Строка
//
Процедура УстановитьОформлениеТаблицыПолеКоличествоПлан(УсловноеОформление, ИмяТаблицы) Экспорт
	
	// Особенность: поле может быть отдельно заблокировано по исходной строке в методе УстановитьОформлениеИсходныхСтрокТовары()
	
	ИмяПоля = ИмяТаблицы + "КоличествоПлан";
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПоказательТоваров");
	ОтборЭлемента.ПравоеЗначение = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВидШкалыРасчета");
	ОтборЭлемента.ПравоеЗначение = 1; // По диапазонам
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

// Параметры:
//  УсловноеОформление - УсловноеОформлениеКомпоновкиДанных
//  ИмяТаблицы - Строка
//
Процедура УстановитьОформлениеТаблицыПолеПроцент(УсловноеОформление, ИмяТаблицы) Экспорт
	
	// Особенность: поле может быть отдельно заблокировано по исходной строке в методе формы объекта УстановитьОформлениеИсходныхСтрокТовары()
	
	ИмяПоля = ИмяТаблицы + "Процент";
	ПутьКПолю = "Объект." + ИмяТаблицы + ".Процент";
	
	#Область ОбязательностьЗаполнения
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПоказательТоваров");
	ОтборЭлемента.ПравоеЗначение = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВидШкалыРасчета");
	ОтборЭлемента.ПравоеЗначение = 1; // По диапазонам
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	#КонецОбласти
	
	#Область ПустоеЗначение
		Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПоказательТоваров");
	ОтборЭлемента.ПравоеЗначение = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВидШкалыРасчета");
	ОтборЭлемента.ПравоеЗначение = 1; // По диапазонам
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолю);
	ОтборЭлемента.ПравоеЗначение = 0;
	
	СтрокаНоль = НСтр("ru = '<нет>';
						|en = '<no>'");
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", СтрокаНоль);
	#КонецОбласти
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Содержит данную форму
//  ЕстьТипБонуса - Булево - Признак наличия типа бонуса в объекте формы
//
Процедура УстановитьОформлениеТаблицыТоварыХарактеристика(Форма, ЕстьТипБонуса = Ложь) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(Форма);
	УсловноеОформление = Форма.УсловноеОформление;
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТоварыХарактеристика");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.Характеристика");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.ХарактеристикиИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Если ЕстьТипБонуса Тогда
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ТипБонуса");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыРетроБонусовПоставщиков.Остатки;
		
	КонецЕсли;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстПредопределенногоЗначения);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Все>';
																|en = '<All>'"));
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура - Новые параметры оформления исправления:
// * ПоляИсходнаяСтрока - Массив из Строка - 
// * ПоляДоИзменений - Массив из Строка -
//
Функция НовыеПараметрыОформленияИсходныхСтрок() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ПоляИсходнаяСтрока", Новый Массив);
	Результат.Вставить("ПоляДоИзменений", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  УсловноеОформление - УсловноеОформлениеКомпоновкиДанных -
//  ТаблицаФормы - ТаблицаФормы - Таблица формы
//  Параметры - см. РетроБонусыСервер.НовыеПараметрыОформленияИсходныхСтрок
// 
Процедура ОформитьПоляПоИсходнымСтрокам(УсловноеОформление, ТаблицаФормы, Параметры) Экспорт
	
	ПутьКТЧ = ТаблицаФормы.ПутьКДанным;
	ПутьКИсходнаяСтрока = ПутьКТЧ + ".ИсходнаяСтрока";
	
	#Область ДанныеИсходнойСтроки
	ПоляИсходнаяСтрока = Параметры.ПоляИсходнаяСтрока;
	Если ПоляИсходнаяСтрока.Количество() > 0 Тогда
		
		Элемент = УсловноеОформление.Элементы.Добавить();
		
		Для Каждого ИмяПоля Из ПоляИсходнаяСтрока Цикл
			
			ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
			ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
			
		КонецЦикла;
		
		Для Каждого ИмяПоля Из Параметры.ПоляДоИзменений Цикл
			
			ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
			ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
			
		КонецЦикла;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКИсходнаяСтрока);
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ЖирныйШрифт);
		
	КонецЕсли;
	#КонецОбласти
	
	#Область ПоляДоИзменений
	Если Параметры.ПоляДоИзменений.Количество() > 0 Тогда
		
		Элемент = УсловноеОформление.Элементы.Добавить();
		
		Для Каждого ИмяПоля Из Параметры.ПоляДоИзменений Цикл
			
			ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
			ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
			
		КонецЦикла;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКИсходнаяСтрока);
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", "-");
		Элемент.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение", ГоризонтальноеПоложение.Центр);
		
	КонецЕсли;
	#КонецОбласти
	
	ОформлениеФлагаОтмены(УсловноеОформление, ТаблицаФормы);
	
КонецПроцедуры

// Параметры:
//  Элементы - ВсеЭлементыФормы
//  УсловноеОформление - УсловноеОформлениеКомпоновкиДанных -
//
Процедура УстановитьОформлениеИсходныхСтрокТаблицыСвойстваНоменклатуры(Элементы, УсловноеОформление) Экспорт
	
	ПараметрыОформления = НовыеПараметрыОформленияИсходныхСтрок();
	ИмяТаблицы = "СвойстваНоменклатуры";
	
	ПолеЗначениеСвойства = ИмяТаблицы + "ЗначениеСвойства";
	ПолеКоличествоПлан = ИмяТаблицы + "КоличествоПлан";
	ПолеБазоваяЦена = ИмяТаблицы + "БазоваяЦена";
	ПолеПроцент = ИмяТаблицы + "Процент";
	
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(ПолеЗначениеСвойства);
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(ПолеКоличествоПлан);
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(ПолеБазоваяЦена);
	ПараметрыОформления.ПоляИсходнаяСтрока.Добавить(ПолеПроцент);
	
	ПолеНачалоДействияДоИзменений = ИмяТаблицы + "НачалоДействияДоИзменений";
	ПолеОкончаниеДействияДоИзменений = ИмяТаблицы + "ОкончаниеДействияДоИзменений";
	
	ПараметрыОформления.ПоляДоИзменений.Добавить(ПолеНачалоДействияДоИзменений);
	ПараметрыОформления.ПоляДоИзменений.Добавить(ПолеОкончаниеДействияДоИзменений);
	
	ОформитьПоляПоИсходнымСтрокам(
		УсловноеОформление,
		Элементы[ИмяТаблицы],
		ПараметрыОформления);
	
КонецПроцедуры

// Параметры:
//  ИмяТаблицы - Строка -
//	Настройки - ТаблицаЗначений - таблица с колонками:
//		* Поля - Массив из Строка - поля, для которых определяются настройки отображения
//		* Условие - ОтборКомпоновкиДанных - условия применения настройки
//		* Свойства - Структура - имена и значения свойств 
//
Процедура НастройкаПоляСвойстваНоменклатурыКоличествоПлан(Настройки) Экспорт
	
	ИмяПоля = "СвойстваНоменклатуры.КоличествоПлан";
	СоставыСписков = Перечисления.СоставыСписковРетроБонусов;
	ПоказателиТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	Элемент = Настройки.Добавить();
	
	Элемент.Поля.Добавить(ИмяПоля);
	
	ГруппаИЛИ = НоваяГруппаОтборов(Элемент.Условие);
	ФинансоваяОтчетностьСервер.НовыйОтбор(ГруппаИЛИ, "ОтборТоваров", СоставыСписков.КромеВыбранных);
	
	ГруппаИ = НоваяГруппаОтборов(ГруппаИЛИ, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ФинансоваяОтчетностьСервер.НовыйОтбор(ГруппаИ, "ОтборТоваров", СоставыСписков.Выбранные);
	
	Показатели = Новый СписокЗначений; // СписокЗначений Из ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам
	Показатели.Добавить(ПоказателиТоваров.НеИспользуется);
	Показатели.Добавить(ПоказателиТоваров.Сумма);
	
	ФинансоваяОтчетностьСервер.НовыйОтбор(
		ГруппаИ, "Дополнительно.ПоказательТоваров", Показатели,, ВидСравненияКомпоновкиДанных.ВСписке);
	
	Элемент.Свойства.Вставить("Видимость", Ложь);
	
КонецПроцедуры

// Параметры:
//  ИмяТаблицы - Строка -
//	Настройки - ТаблицаЗначений - таблица с колонками:
//		* Поля - Массив из Строка - поля, для которых определяются настройки отображения
//		* Условие - ОтборКомпоновкиДанных - условия применения настройки
//		* Свойства - Структура - имена и значения свойств 
//
Процедура НастройкаПоляДекорацияСвойстваЕстьНачисления(Настройки) Экспорт
	
	СоставыСписков = Перечисления.СоставыСписковРетроБонусов;
	ПоказателиПродаж = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	СоставТоваров = Перечисления.СоставыТоваровРетроБонусов;
	
	Элемент = Настройки.Добавить();
	
	Элемент.Поля.Добавить("ДекорацияСвойстваЕстьНачисления");
	
	ГруппаИЛИ = НоваяГруппаОтборов(Элемент.Условие);
	ФинансоваяОтчетностьСервер.НовыйОтбор(ГруппаИЛИ, "ОтборТоваров", СоставыСписков.КромеВыбранных);
	
	ИсклПоказатели = Новый СписокЗначений; // СписокЗначений Из ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам
	ИсклПоказатели.Добавить(ПоказателиПродаж.КоличествоСовокупно);
	ИсклПоказатели.Добавить(ПоказателиПродаж.ПакетноеПредложение);
	ФинансоваяОтчетностьСервер.НовыйОтбор(
		ГруппаИЛИ, "Дополнительно.ПоказательТоваров", ИсклПоказатели,, ВидСравненияКомпоновкиДанных.ВСписке);
	ФинансоваяОтчетностьСервер.НовыйОтбор(Элемент.Условие, "Дополнительно.ЕстьНачисления", Истина);
	ФинансоваяОтчетностьСервер.НовыйОтбор(
		Элемент.Условие,
		"Дополнительно.СоставТоваров",
		СоставТоваров.СвойствоНоменклатуры);
	
	Элемент.Свойства.Вставить("Видимость", Истина);
	
КонецПроцедуры

// Параметры:
//  ИмяТаблицы - Строка -
//	Настройки - ТаблицаЗначений - таблица с колонками:
//		* Поля - Массив из Строка - поля, для которых определяются настройки отображения
//		* Условие - ОтборКомпоновкиДанных - условия применения настройки
//		* Свойства - Структура - имена и значения свойств 
//
Процедура НастройкаПоляСвойствоНоменклатурыПредставление(Настройки) Экспорт
	
	СоставТоваров = Перечисления.СоставыТоваровРетроБонусов;
	
	#Область Видимость
	Элемент = Настройки.Добавить();
	
	Элемент.Поля.Добавить("СвойствоНоменклатурыПредставление");
	
	ГруппаИЛИ = НоваяГруппаОтборов(Элемент.Условие);
	ФинансоваяОтчетностьСервер.НовыйОтбор(
		ГруппаИЛИ,
		"ВидРетроБонуса",
		Справочники.ВидыРетроБонусовКлиентов.ПустаяСсылка());
	ФинансоваяОтчетностьСервер.НовыйОтбор(ГруппаИЛИ, "БезРасчета", Истина);
	ФинансоваяОтчетностьСервер.НовыйОтбор(
		ГруппаИЛИ, 
		"Дополнительно.СоставТоваров",
		СоставТоваров.СвойствоНоменклатуры,
		,
		ВидСравненияКомпоновкиДанных.НеРавно);
	
	Элемент.Свойства.Вставить("Видимость", Ложь);
	#КонецОбласти
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Заголовок документа.
// 
// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика, ДокументСсылка.НачислениеРетроБонусовКлиента, ДокументСсылка.НачислениеРетроБонусовПоставщика, ДокументСсылка.СписаниеРетроБонусовКлиента, ДокументСсылка.СписаниеРетроБонусовПоставщика - Обрабатываемый документ
//  НомерДокумента - Строка - Номер документа
//  ДатаДокумента - Дата - Дата документа
//  Исправление - Булево, Неопределено - Признак исправительного документа
// 
// Возвращаемое значение:
//  Строка - представление документа
//
Функция ЗаголовокДокумента(Документ, НомерДокумента, ДатаДокумента, Исправление = Неопределено) Экспорт
	
	Если Исправление = Null Тогда
		Возврат "";
	КонецЕсли;
	
	// При расширение типов необходимо дописать условие ниже
	ДопустимыеТипы = Новый Массив; // Массив из Тип
	ДопустимыеТипы.Добавить(Тип("ДокументСсылка.УсловияРетроБонусовКлиентов"));
	ДопустимыеТипы.Добавить(Тип("ДокументСсылка.НачислениеРетроБонусовКлиента"));
	ДопустимыеТипы.Добавить(Тип("ДокументСсылка.СписаниеРетроБонусовКлиента"));
	ДопустимыеТипы.Добавить(Тип("ДокументСсылка.УсловияРетроБонусовПоставщика"));
	ДопустимыеТипы.Добавить(Тип("ДокументСсылка.НачислениеРетроБонусовПоставщика"));
	ДопустимыеТипы.Добавить(Тип("ДокументСсылка.СписаниеРетроБонусовПоставщика"));
	
	ТипДокумента = ТипЗнч(Документ);
	ИмяМетода = "РетроБонусыСервер.ЗаголовокДокумента";
	ШаблонОшибкиТипов = НСтр("ru = 'Передан необрабатываемый тип %1';
							|en = 'Unprocessable type %1 passed'");
	ТекстОшибкиТипов = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонОшибкиТипов, Строка(ТипДокумента));
	
	ОбщегоНазначенияКлиентСервер.Проверить(
		ДопустимыеТипы.Найти(ТипДокумента) <> Неопределено,
		ТекстОшибкиТипов,
		ИмяМетода);
	
	// Выполняется явно для ускорения обработки в обработчике представления, не используется получение менеджера по ссылке
	Если ТипДокумента = Тип("ДокументСсылка.УсловияРетроБонусовКлиентов") Тогда
		ШаблонЗаголовкаДокумента = Документы.УсловияРетроБонусовКлиентов.ШаблонЗаголовкаДокумента(Исправление);
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.НачислениеРетроБонусовКлиента") Тогда
		ШаблонЗаголовкаДокумента = Документы.НачислениеРетроБонусовКлиента.ШаблонЗаголовкаДокумента();
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.СписаниеРетроБонусовКлиента") Тогда
		ШаблонЗаголовкаДокумента = Документы.СписаниеРетроБонусовКлиента.ШаблонЗаголовкаДокумента();
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.УсловияРетроБонусовПоставщика") Тогда
		ШаблонЗаголовкаДокумента = Документы.УсловияРетроБонусовПоставщика.ШаблонЗаголовкаДокумента(Исправление);
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.НачислениеРетроБонусовПоставщика") Тогда
		ШаблонЗаголовкаДокумента = Документы.НачислениеРетроБонусовПоставщика.ШаблонЗаголовкаДокумента();
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.СписаниеРетроБонусовПоставщика") Тогда
		ШаблонЗаголовкаДокумента = Документы.СписаниеРетроБонусовПоставщика.ШаблонЗаголовкаДокумента();
	Иначе
		
		ШаблонИсключения = НСтр("ru = 'Для получения шаблона заголовка документа в методе %1 не определен метод получения для типа %2.';
								|en = 'To obtain the document header template, %1 method does not define a receiving method for the %2 type.'");
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонИсключения,
			ИмяМетода,
			Строка(ТипДокумента));
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	ОкончаниеЗаголовка = "";
	
	Если ЗначениеЗаполнено(Документ) Тогда
		
		ШаблонОкончаниеЗаголовка = НСтр("ru = '%1 от %2';
										|en = '%1 from %2'");
		Если ТипДокумента =  Тип("ДокументСсылка.УсловияРетроБонусовКлиентов")
		 ИЛИ ТипДокумента =  Тип("ДокументСсылка.УсловияРетроБонусовПоставщика") Тогда
			
			ОкончаниеЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОкончаниеЗаголовка, НомерДокумента, Формат(ДатаДокумента, "ДЛФ=D;"));
			
		Иначе
			
			ОкончаниеЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОкончаниеЗаголовка, НомерДокумента, Строка(ДатаДокумента));
			
		КонецЕсли;
		
	Иначе
		
		ОкончаниеЗаголовка = НСтр("ru = '(создание)';
									|en = '(create)'");
		
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонЗаголовкаДокумента, ОкончаниеЗаголовка);
	
КонецФункции

// Определяет максимальную дату окончания периода зарегистрированных начислений по условию ретро-бонусов клиентов
// 
// Параметры:
//  ДокументУсловий - ДокументСсылка.УсловияРетроБонусовКлиентов -
// 
// Возвращаемое значение:
//  Дата - максимальная дата окончания периода зарегистрированных начислений:
//
Функция ГраницаНачисленийКлиентов(ДокументУсловий) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Дата(1, 1, 1);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументУсловий", ДокументУсловий);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РетроБонусыКлиентов.ДокументУсловий,
	|	МАКСИМУМ(РетроБонусыКлиентов.ОкончаниеПериода) КАК ОкончаниеПериодаНачислений
	|ИЗ
	|	РегистрНакопления.РетроБонусыКлиентов КАК РетроБонусыКлиентов
	|ГДЕ
	|	РетроБонусыКлиентов.ДокументУсловий = &ДокументУсловий
	|	И РетроБонусыКлиентов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И РетроБонусыКлиентов.Регистратор <> &ДокументУсловий
	|СГРУППИРОВАТЬ ПО
	|	РетроБонусыКлиентов.ДокументУсловий";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат = Выборка.ОкончаниеПериодаНачислений; // Дата
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Определяет максимальную дату периода зарегистрированных начислений по условию ретро-бонусов поставщиков
// 
// Параметры:
//  ДокументУсловий - ДокументСсылка.УсловияРетроБонусовПоставщика -
// 
// Возвращаемое значение:
//  Дата - максимальная дата окончания периода зарегистрированных начислений
//
Функция ГраницаНачисленийПоставщиков(ДокументУсловий) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Дата(1, 1, 1);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументУсловий", ДокументУсловий);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РетроБонусыПоставщиков.ДокументУсловий КАК ДокументУсловий,
	|	МАКСИМУМ(ВЫБОР
	|		КОГДА РетроБонусыПоставщиков.ОкончаниеПериода = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДАТАВРЕМЯ(3999, 12, 31)
	|		ИНАЧЕ РетроБонусыПоставщиков.ОкончаниеПериода
	|	КОНЕЦ) КАК ОкончаниеПериодаНачислений
	|ИЗ
	|	РегистрНакопления.РетроБонусыПоставщиков КАК РетроБонусыПоставщиков
	|ГДЕ
	|	РетроБонусыПоставщиков.ДокументУсловий = &ДокументУсловий
	|	И РетроБонусыПоставщиков.ВидДвиженияРегистра = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияНакопления.Приход)
	|	И РетроБонусыПоставщиков.Регистратор <> &ДокументУсловий
	|СГРУППИРОВАТЬ ПО
	|	РетроБонусыПоставщиков.ДокументУсловий";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат = Выборка.ОкончаниеПериодаНачислений; // Дата
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Фиксирует актуальный состав документа условий для дальнейшего расчета
// 
// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов
//
Процедура ЗафиксироватьСоставУРБКлиентов(Документ) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РетроБонусыКлиентовСегментыТоваров");
		ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РетроБонусыКлиентовТовары");
		ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РетроБонусыКлиентовСегментыПартнеров");
		ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РетроБонусыКлиентовКонтрагенты");
		ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РетроБонусыКлиентовУсловия");
		ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		ВведенаКорректировкаНаСогласовании = ЕстьКорректировкаНаСогласовании(Документ);
		Если ВведенаКорректировкаНаСогласовании Тогда
			
			ТекстИсключения = НСтр("ru = 'По документу условий введена корректировка в статусе ""На согласовании""';
									|en = 'For the condition document, an adjustment is registered in the ""On approval"" status.'");
			ВызватьИсключение ТекстИсключения;
			
		КонецЕсли;
		
		ЗафиксироватьСегменты = Ложь;
		
		УстановитьПривилегированныйРежим(Истина);
		
		СоставТоваров = Неопределено;
		
		АктуальныеДанныеДокумента = Документы.УсловияРетроБонусовКлиентов.АктуальныеДанные(Документ);
		Если АктуальныеДанныеДокумента.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры Тогда
			
			СоставТоваров = СоставСегментовТоваров(Документ);
			ЗафиксироватьСегменты = Истина;
			
		ИначеЕсли АктуальныеДанныеДокумента.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры Тогда
			
			СоставТоваров = СоставТоваровПоСвойству(Документ);
			ЗафиксироватьСегменты = Истина;
			
		КонецЕсли;
		
		Если СоставТоваров <> Неопределено
		   И СоставТоваров.Количество() > 0 Тогда
			
			НаборЗаписейТовары = РегистрыСведений.РетроБонусыКлиентовТовары.СоздатьНаборЗаписей();
			НаборЗаписейТовары.Отбор.ДокументУсловий.Установить(Документ);
			НаборЗаписейТовары.Загрузить(СоставТоваров);
			НаборЗаписейТовары.Записать();
			
		КонецЕсли;
		
		Если АктуальныеДанныеДокумента.СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.СегментыПартнеров Тогда
			
			СоставПартнеров = СоставСегментовПартнеров(Документ);
			Если СоставПартнеров.Количество() > 0 Тогда
				
				НаборЗаписейКонтрагенты = РегистрыСведений.РетроБонусыКлиентовКонтрагенты.СоздатьНаборЗаписей();
				НаборЗаписейКонтрагенты.Отбор.ДокументУсловий.Установить(Документ);
				НаборЗаписейКонтрагенты.Загрузить(СоставПартнеров);
				НаборЗаписейКонтрагенты.Записать();
				
			КонецЕсли;
			
			ЗафиксироватьСегменты = Истина;
			
		КонецЕсли;
		
		Если ЗафиксироватьСегменты Тогда
			
			НаборЗаписейУсловия = РегистрыСведений.РетроБонусыКлиентовУсловия.СоздатьНаборЗаписей();
			НаборЗаписейУсловия.Отбор.ДокументУсловий.Установить(Документ);
			НаборЗаписейУсловия.Прочитать();
			Для Каждого ЗаписьУсловия Из НаборЗаписейУсловия Цикл
				
				ЗаписьУсловия.ФиксацияВыполнена = Истина;
				
			КонецЦикла;
			НаборЗаписейУсловия.Записать();
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Фиксирует актуальный состав документа условий для дальнейшего расчета
// 
// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовПоставщика
//
Процедура ЗафиксироватьСоставУРБПоставщиков(Документ) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РетроБонусыПоставщиковСегментыТоваров");
		ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РетроБонусыПоставщиковТовары");
		ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РетроБонусыПоставщиковУсловия");
		ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		ВведенаКорректировкаНаСогласовании = ЕстьКорректировкаНаСогласовании(Документ);
		Если ВведенаКорректировкаНаСогласовании Тогда
			
			ТекстИсключения = НСтр("ru = 'По документу условий введена корректировка в статусе ""На согласовании"".';
									|en = 'For the condition document, an adjustment is registered in the ""On approval"" status.'");
			ВызватьИсключение ТекстИсключения;
			
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		
		ЗафиксироватьСегменты = Ложь;
		СоставТоваров = Неопределено;
		
		АктуальныеДанныеДокумента = Документы.УсловияРетроБонусовПоставщика.АктуальныеДанные(Документ);
		Если АктуальныеДанныеДокумента.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры Тогда
			
			СоставТоваров = СоставСегментовТоваров(Документ);
			ЗафиксироватьСегменты = Истина;
			
		ИначеЕсли АктуальныеДанныеДокумента.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры Тогда
			
			СоставТоваров = СоставТоваровПоСвойству(Документ);
			ЗафиксироватьСегменты = Истина;
			
		КонецЕсли;
		
		Если СоставТоваров <> Неопределено
		   И СоставТоваров.Количество() > 0 Тогда
			
			НаборЗаписейТовары = РегистрыСведений.РетроБонусыПоставщиковТовары.СоздатьНаборЗаписей();
			НаборЗаписейТовары.Отбор.ДокументУсловий.Установить(Документ);
			НаборЗаписейТовары.Загрузить(СоставТоваров);
			НаборЗаписейТовары.Записать();
			
		КонецЕсли;
		
		Если ЗафиксироватьСегменты Тогда
			
			НаборЗаписейУсловия = РегистрыСведений.РетроБонусыПоставщиковУсловия.СоздатьНаборЗаписей();
			НаборЗаписейУсловия.Отбор.ДокументУсловий.Установить(Документ);
			НаборЗаписейУсловия.Прочитать();
			Для Каждого ЗаписьУсловия Из НаборЗаписейУсловия Цикл
				
				ЗаписьУсловия.ФиксацияВыполнена = Истина;
				
			КонецЦикла;
			НаборЗаписейУсловия.Записать();
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Отменяет фиксацию состава документа условий
// 
// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов
//
Процедура ОтменитьФиксациюСоставаУРБКлиентов(Документ) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РетроБонусыКлиентовУсловия.СоставУчастников КАК СоставУчастников,
	|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.Вид.СвойствоНоменклатуры, """") КАК СвойствоНоменклатуры,
	|	РетроБонусыКлиентовУсловия.СоставТоваров КАК СоставТоваров
	|ИЗ
	|	РегистрСведений.РетроБонусыКлиентовУсловия КАК РетроБонусыКлиентовУсловия
	|ГДЕ
	|	РетроБонусыКлиентовУсловия.ДокументУсловий = &ДокументУсловий
	|	И РетроБонусыКлиентовУсловия.ФиксацияВыполнена";
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РетроБонусыКлиентовТовары");
		ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РетроБонусыКлиентовКонтрагенты");
		ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РетроБонусыКлиентовУсловия");
		ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("ДокументУсловий", Документ);
		РезультатЗапроса = Запрос.Выполнить();
		СегментыЗафиксированы = НЕ РезультатЗапроса.Пустой();
		
		Если СегментыЗафиксированы Тогда
			
			УстановитьПривилегированныйРежим(Истина);
			
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			
			Если Выборка.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры
			 ИЛИ НЕ ПустаяСтрока(Выборка.СвойствоНоменклатуры) Тогда
				
				НаборЗаписейТовары = РегистрыСведений.РетроБонусыКлиентовТовары.СоздатьНаборЗаписей();
				НаборЗаписейТовары.Отбор.ДокументУсловий.Установить(Документ);
				НаборЗаписейТовары.Очистить();
				НаборЗаписейТовары.Записать();
				
			КонецЕсли;
			
			Если Выборка.СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.СегментыПартнеров Тогда
				
				НаборЗаписейКонтрагенты = РегистрыСведений.РетроБонусыКлиентовКонтрагенты.СоздатьНаборЗаписей();
				НаборЗаписейКонтрагенты.Отбор.ДокументУсловий.Установить(Документ);
				НаборЗаписейКонтрагенты.Очистить();
				НаборЗаписейКонтрагенты.Записать();
				
			КонецЕсли;
			
			НаборЗаписейУсловия = РегистрыСведений.РетроБонусыКлиентовУсловия.СоздатьНаборЗаписей();
			НаборЗаписейУсловия.Отбор.ДокументУсловий.Установить(Документ);
			НаборЗаписейУсловия.Прочитать();
			Для Каждого ЗаписьУсловия Из НаборЗаписейУсловия Цикл
				
				ЗаписьУсловия.ФиксацияВыполнена = Ложь;
				
			КонецЦикла;
			НаборЗаписейУсловия.Записать();
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Отменяет фиксацию состава документа условий
// 
// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовПоставщика
//
Процедура ОтменитьФиксациюСоставаУРБПоставщиков(Документ) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	УсловияРетроБонусов.СоставТоваров КАК СоставТоваров,
	|	ЕСТЬNULL(УсловияРетроБонусов.Вид.СвойствоНоменклатуры, """") КАК СвойствоНоменклатуры
	|ИЗ
	|	РегистрСведений.РетроБонусыПоставщиковУсловия КАК УсловияРетроБонусов
	|ГДЕ
	|	УсловияРетроБонусов.ДокументУсловий = &ДокументУсловий
	|	И УсловияРетроБонусов.ФиксацияВыполнена";
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РетроБонусыПоставщиковТовары");
		ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РетроБонусыПоставщиковУсловия");
		ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", Документ);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("ДокументУсловий", Документ);
		РезультатЗапроса = Запрос.Выполнить();
		СегментыЗафиксированы = НЕ РезультатЗапроса.Пустой();
		
		Если СегментыЗафиксированы Тогда
			
			УстановитьПривилегированныйРежим(Истина);
			
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			
			Если Выборка.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры
			 ИЛИ НЕ ПустаяСтрока(Выборка.СвойствоНоменклатуры) Тогда
				
				НаборЗаписейТовары = РегистрыСведений.РетроБонусыПоставщиковТовары.СоздатьНаборЗаписей();
				НаборЗаписейТовары.Отбор.ДокументУсловий.Установить(Документ);
				НаборЗаписейТовары.Очистить();
				НаборЗаписейТовары.Записать();
				
			КонецЕсли;
			
			НаборЗаписейУсловия = РегистрыСведений.РетроБонусыПоставщиковУсловия.СоздатьНаборЗаписей();
			НаборЗаписейУсловия.Отбор.ДокументУсловий.Установить(Документ);
			НаборЗаписейУсловия.Прочитать();
			Для Каждого ЗаписьУсловия Из НаборЗаписейУсловия Цикл
				
				ЗаписьУсловия.ФиксацияВыполнена = Ложь;
				
			КонецЦикла;
			НаборЗаписейУсловия.Записать();
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Возвращает таблицу товаров, входящих в сегменты документа, соответствующую структуре РС РетроБонусыКлиентовТовары
//
// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов -
//           - ДокументСсылка.УсловияРетроБонусовПоставщика -
// 
// Возвращаемое значение:
//  см. НовыйСоставСегментовТоваров
//
Функция СоставСегментовТоваров(Документ) Экспорт
	
	СоставСегментовТоваров = НовыйСоставСегментовТоваров(Документ);
	
	АктуальныйДокумент = АктуальныйДокументУсловийРетроБонусовПоСсылке(Документ);
	Если НЕ АктуальныйДокумент.Пустая() Тогда
		
		НоменклатураСегментов = ЗаполнитьДанныеДляФиксацииСегментовТоваров(Документ);
		Если НоменклатураСегментов.Количество() > 0 Тогда
			
			МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Документ);
			ДанныеДокумента = МенеджерДокумента.АктуальныеДанные(Документ);
			ОтсортироватьТаблицуСегментовНоменклатурыПоПредставлению(НоменклатураСегментов);
			Счетчик = 1;
			
			Для Каждого ДанныеНоменклатуры Из НоменклатураСегментов Цикл
				
				НоваяСтрока = СоставСегментовТоваров.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДокумента);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеНоменклатуры);
				НоваяСтрока.ДокументУсловий = Документ;
				НоваяСтрока.РегистраторДвижения = АктуальныйДокумент;
				НоваяСтрока.НомерСтрокиДокумента = Счетчик;
				
				Счетчик = Счетчик + 1;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СоставСегментовТоваров;
	
КонецФункции

// Возвращает таблицу партнеров, входящих в сегменты документа, соответствующую структуре РС РетроБонусыКлиентовКонтрагенты
//
// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
//  * НачалоДействия - Дата
//  * ОкончаниеДействия - Дата
//  * ДокументУсловий - ДокументСсылка.УсловияРетроБонусовКлиентов
//  * Контрагент - СправочникСсылка.Контрагенты
//  * Партнер - СправочникСсылка.Партнеры
//  * СуммаПлан - ОпределяемыйТип.ДенежнаяСуммаНеотрицательная
//  * РегистраторДвижения - ДокументСсылка.УсловияРетроБонусовКлиентов
//
Функция СоставСегментовПартнеров(Документ) Экспорт
	
	СоставСегментовПартнеров = РегистрыСведений.РетроБонусыКлиентовКонтрагенты.СоздатьНаборЗаписей().Выгрузить();
	
	АктуальныйДокумент = АктуальныйДокументУсловийРетроБонусовПоСсылке(Документ);
	Если НЕ АктуальныйДокумент.Пустая() Тогда
		
		ПартнерыСегментов = ЗаполнитьДанныеДляФиксацииСегментовПартнеров(Документ);
		
		Если ПартнерыСегментов.Количество() > 0 Тогда
			
			ДанныеДокумента = Документы.УсловияРетроБонусовКлиентов.АктуальныеДанные(Документ);
			ОтсортироватьТаблицуСегментовПартнеровПоПредставлению(ПартнерыСегментов);
			Счетчик = 1;
			
			Для Каждого ДанныеПартнера Из ПартнерыСегментов Цикл
				
				НоваяСтрока = СоставСегментовПартнеров.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеДокумента);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеПартнера);
				НоваяСтрока.ДокументУсловий = Документ;
				НоваяСтрока.РегистраторДвижения = АктуальныйДокумент;
				НоваяСтрока.НомерСтрокиДокумента = Счетчик;
				
				Счетчик = Счетчик + 1;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СоставСегментовПартнеров;
	
КонецФункции

// Определяет имя табличной части и представление для условий ретро-бонусов клиентов по переданным параметрам
// 
// Параметры:
//  Состав - ПеречислениеСсылка.СоставыУчастниковРетроБонусов, ПеречислениеСсылка.СоставыТоваровРетроБонусов - 
//  ОтборУчастников - ПеречислениеСсылка.СоставыСписковРетроБонусов - Отбор участников
// 
// Возвращаемое значение:
//  Структура - Имя представление таблицы по реквизитам:
// * ИмяТаблицы - Строка - 
// * ПредставлениеТаблицы - Строка - 
//
Функция ИмяПредставлениеТаблицыПоРеквизитамУсловийКлиентов(Состав, ОтборУчастников) Экспорт
	
	ИмяПредставлениеТаблицы = Новый Структура;
	ИмяПредставлениеТаблицы.Вставить("ИмяТаблицы", "");
	ИмяПредставлениеТаблицы.Вставить("ПредставлениеТаблицы", "");
	
	Если ТипЗнч(Состав) = Тип("ПеречислениеСсылка.СоставыУчастниковРетроБонусов") Тогда
		
		Если Состав = Перечисления.СоставыУчастниковРетроБонусов.Контрагенты Тогда
			
			ИмяПредставлениеТаблицы.ИмяТаблицы = "Контрагенты";
			Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Выбранные контрагенты';
																	|en = 'Selected counterparties'");
			Иначе
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Кроме контрагентов';
																	|en = 'Except for counterparties'");
			КонецЕсли;
			
		ИначеЕсли Состав = Перечисления.СоставыУчастниковРетроБонусов.Клиенты Тогда
			
			ИмяПредставлениеТаблицы.ИмяТаблицы = "Контрагенты";
			Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Выбранные клиенты';
																	|en = 'Selected customers'");
			Иначе
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Кроме клиентов';
																	|en = 'Except for customers'");
			КонецЕсли;
			
		ИначеЕсли Состав = Перечисления.СоставыУчастниковРетроБонусов.ИНН Тогда
			
			ИмяПредставлениеТаблицы.ИмяТаблицы = "ИННКонтрагентов";
			Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Выбранные ИНН';
																	|en = 'Selected TIN'");
			Иначе
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Кроме ИНН';
																	|en = 'Except for TIN'");
			КонецЕсли;
			
		ИначеЕсли Состав = Перечисления.СоставыУчастниковРетроБонусов.Договоры Тогда
			
			ИмяПредставлениеТаблицы.ИмяТаблицы = "ДоговорыСоглашения";
			ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Выбранные договоры';
																|en = 'Selected contracts'");
			
		ИначеЕсли Состав = Перечисления.СоставыУчастниковРетроБонусов.Соглашения Тогда
			
			ИмяПредставлениеТаблицы.ИмяТаблицы = "ДоговорыСоглашения";
			ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Выбранные соглашения';
																|en = 'Selected terms of sales'");
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Состав) = Тип("ПеречислениеСсылка.СоставыТоваровРетроБонусов") Тогда
		
		Если Состав = Перечисления.СоставыТоваровРетроБонусов.Номенклатура Тогда
			
			ИмяПредставлениеТаблицы.ИмяТаблицы = "Товары";
			Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Выбранные товары';
																	|en = 'Selected goods'");
			Иначе
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Кроме товаров';
																	|en = 'Except for goods'");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИмяПредставлениеТаблицы;
	
КонецФункции

// Определяет имя табличной части и представление для условий ретро-бонусов поставщиков по переданным параметрам
// 
// Параметры:
//  Состав - ПеречислениеСсылка.СоставыУчастниковРетроБонусов, ПеречислениеСсылка.СоставыТоваровРетроБонусов - 
//  ОтборУчастников - ПеречислениеСсылка.СоставыСписковРетроБонусов - Отбор участников
// 
// Возвращаемое значение:
//  Структура - Имя представление таблицы по реквизитам:
// * ИмяТаблицы - Строка - 
// * ПредставлениеТаблицы - Строка - 
//
Функция ИмяПредставлениеТаблицыПоРеквизитамУсловийПоставщиков(Состав, ОтборУчастников) Экспорт
	
	ИмяПредставлениеТаблицы = Новый Структура;
	ИмяПредставлениеТаблицы.Вставить("ИмяТаблицы", "");
	ИмяПредставлениеТаблицы.Вставить("ПредставлениеТаблицы", "");
	
	Если ТипЗнч(Состав) = Тип("ПеречислениеСсылка.СоставыУчастниковРетроБонусов") Тогда
		
		Если Состав = Перечисления.СоставыУчастниковРетроБонусов.Контрагенты Тогда
			
			ИмяПредставлениеТаблицы.ИмяТаблицы = "КонтрагентыКлиентов";
			Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Клиенты: выбранные контрагенты';
																	|en = 'Customers: selected counterparties'");
			Иначе
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Клиенты: кроме контрагентов';
																	|en = 'Customers: except for counterparties'");
			КонецЕсли;
			
		ИначеЕсли Состав = Перечисления.СоставыУчастниковРетроБонусов.ИНН Тогда
			
			ИмяПредставлениеТаблицы.ИмяТаблицы = "ИННКонтрагентовКлиентов";
			Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Клиенты: выбранные ИНН';
																	|en = 'Customers: selected TIN'");
			Иначе
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Клиенты: кроме ИНН';
																	|en = 'Customers: except TIN'");
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Состав) = Тип("ПеречислениеСсылка.СоставыТоваровРетроБонусов") Тогда
		
		Если Состав = Перечисления.СоставыТоваровРетроБонусов.Номенклатура Тогда
			
			ИмяПредставлениеТаблицы.ИмяТаблицы = "Товары";
			Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Выбранные товары';
																	|en = 'Selected goods'");
			Иначе
				ИмяПредставлениеТаблицы.ПредставлениеТаблицы = НСтр("ru = 'Кроме товаров';
																	|en = 'Except for goods'");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИмяПредставлениеТаблицы;
	
КонецФункции

// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ТипыНоменклатуры
Функция ПоддерживаемыеТипыНоменклатуры() Экспорт
	
	ПоддерживаемыеТипыНоменклатуры = Новый Массив; // Массив из ПеречислениеСсылка.ТипыНоменклатуры
	ПоддерживаемыеТипыНоменклатуры.Добавить(Перечисления.ТипыНоменклатуры.Товар);
	ПоддерживаемыеТипыНоменклатуры.Добавить(Перечисления.ТипыНоменклатуры.Услуга);
	ПоддерживаемыеТипыНоменклатуры.Добавить(Перечисления.ТипыНоменклатуры.Работа);
	
	Возврат ПоддерживаемыеТипыНоменклатуры;
	
КонецФункции

// см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов
Процедура НастроитьВариантыОтчетов(Настройки) Экспорт
	
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.ВедомостьПоРетроБонусамКлиентов);
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.РасчетРетроБонусовКлиентов);
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.ДействующиеРетроБонусыКлиента);
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.ДействующиеРетроБонусыПоСоглашению);
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.ДействующиеРетроБонусыПоДоговору);
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.СоставРетроБонусовКлиентов);
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.СоставРетроБонусовПоставщиков);
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.ВедомостьПоРетроБонусамПоставщиков);
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.РасчетРетроБонусовПоставщиков);
	ВариантыОтчетов.НастроитьОтчетВМодулеМенеджера(Настройки, Метаданные.Отчеты.ДействующиеРетроБонусыПоставщиков);
	
КонецПроцедуры

// Формирует представление периода расчета для отчета.
// 
// Параметры:
//  НачалоПериодаРасчета - Дата
//  Периодичность - ПеречислениеСсылка.ПериодичностиРетроБонусов - Периодичность условий ретро-бонусов
//  ПериодОтчета - СтандартныйПериод
// 
// Возвращаемое значение:
//  Строка - Строковое представление периода расчета
//
Функция ПредставлениеПериодаРасчета(НачалоПериодаРасчета, Периодичность, ПериодОтчета) Экспорт
	
	НачалоПериодаРасчетаПредставление = Формат(НачалоПериодаРасчета, "ДЛФ=D");
	ШаблонПредставленияПериода = "%1 - %2";
	
	Периодичности = Перечисления.ПериодичностиРетроБонусов;
	Если Периодичность = Периодичности.День Тогда
		
		ПредставлениеПериода = НачалоПериодаРасчетаПредставление;
		
	ИначеЕсли Периодичность = Периодичности.Неделя Тогда
		
		КонецПериода = КонецНедели(НачалоПериодаРасчета);
		КонецПериодаПредставление = Формат(КонецПериода, "ДЛФ=D");
		ПредставлениеПериода = СтрШаблон(
			ШаблонПредставленияПериода, НачалоПериодаРасчетаПредставление, КонецПериодаПредставление);
		
	ИначеЕсли Периодичность = Периодичности.Месяц Тогда
		
		КонецПериода = КонецМесяца(НачалоПериодаРасчета);
		ПредставлениеПериода = ПредставлениеПериода(НачалоПериодаРасчета, КонецПериода);
		
	ИначеЕсли Периодичность = Периодичности.Квартал Тогда
		
		КонецПериода = КонецКвартала(НачалоПериодаРасчета);
		ПредставлениеПериода = ПредставлениеПериода(НачалоПериодаРасчета, КонецПериода);
		
	Иначе
		
		НачалоПериодаОтчетаПредставление = Формат(ПериодОтчета.ДатаНачала, "ДЛФ=D");
		КонецПериодаОтчетаПредставление = Формат(ПериодОтчета.ДатаОкончания, "ДЛФ=D");
		ПредставлениеПериода = СтрШаблон(
			ШаблонПредставленияПериода, НачалоПериодаОтчетаПредставление, КонецПериодаОтчетаПредставление);
		
	КонецЕсли;
	
	Возврат ПредставлениеПериода;
	
КонецФункции

// Возвращает признак наличия корректировочных документов в статусе "На согласовании" для документа условий ретро-бонусов
// 
// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика -
//
// Возвращаемое значение:
//  Булево
//
Функция ЕстьКорректировкаНаСогласовании(Документ) Экспорт
	
	КорректировкаНаСогласовании = АктуальныйДокументУсловийРетроБонусовПоСсылке(
		Документ,
		Перечисления.СтатусыДокументовРетроБонусов.НаСогласовании);
	
	Результат = (НЕ КорректировкаНаСогласовании.Пустая()
				  И КорректировкаНаСогласовании <> Документ);
	
	Возврат Результат;
	
КонецФункции

// Имя события контроля остатков
// 
// Возвращаемое значение:
//  Строка
//
Функция ИмяСобытияВыключенКонтрольОстатков() Экспорт
	
	КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	
	Возврат НСтр("ru = 'Контроль остатков ретро-бонусов';
				|en = 'Rebate balance check'", КодОсновногоЯзыка);
	
КонецФункции

// Проверить периоды таблицы.
// 
// Параметры:
//  ОбъектПроверки - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика -
//  ИмяТаблицы - Строка - Имя проверяемой табличной части
//  ПредставлениеТаблицы - Строка - Представление проверяемой табличной части
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПериодыТаблицы(ОбъектПроверки, Знач ИмяТаблицы, Знач ПредставлениеТаблицы, Отказ) Экспорт
	
	ПустаяДата = Дата(1, 1, 1);
	
	Если ОбъектПроверки.НачалоДействия = ПустаяДата
	   И ОбъектПроверки.ОкончаниеДействия = ПустаяДата Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонСообщения = НСтр("ru = 'В строке %1 списка %2 указана %3, не соответствующая периоду действия ретро-бонуса';
							|en = 'In line %1 of the %2 list, %3, which mismatches the rebate period, is specified'");
	ШаблонСообщенияОшибкаПериода = НСтр("ru = 'В строке %1 списка %2 дата начала больше даты окончания';
										|en = 'Start date is later than the end date in line %1 of the %2 list'");
	СодержитОтменено = Неопределено;
	
	ТабличнаяЧастьОбъекта = ОбъектПроверки[ИмяТаблицы]; // ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Контрагенты, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.ДоговорыСоглашения, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Поставщики  
	Для Каждого СтрокаТЧ Из ТабличнаяЧастьОбъекта Цикл
		
		#Область ОкончаниеБольшеНачала
		Если СтрокаТЧ.НачалоДействия <> ПустаяДата
		   И СтрокаТЧ.ОкончаниеДействия <> ПустаяДата
		   И СтрокаТЧ.НачалоДействия > СтрокаТЧ.ОкончаниеДействия Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщенияОшибкаПериода, Строка(СтрокаТЧ.НомерСтроки), ПредставлениеТаблицы);
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТаблицы, СтрокаТЧ.НомерСтроки, "НачалоДействия");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, Поле,, Отказ);
			
		КонецЕсли;
		#КонецОбласти
		
		Если СодержитОтменено = Неопределено Тогда
			СодержитОтменено =  ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТЧ, "Отменено");
		КонецЕсли;
		
		ВыполнятьПроверку = Истина;
		Если СодержитОтменено
		   И СтрокаТЧ.Отменено Тогда
			ВыполнятьПроверку = Ложь;
		КонецЕсли;
		
		#Область НачалоВПериодеДействия
		Если ВыполнятьПроверку
		   И СтрокаТЧ.НачалоДействия <> ПустаяДата
		   И НЕ (СтрокаТЧ.НачалоДействия >= ОбъектПроверки.НачалоДействия
				 И СтрокаТЧ.НачалоДействия <= ОбъектПроверки.ОкончаниеДействия) Тогда
			
			ПредставлениеПоля = НСтр("ru = 'дата начала';
									|en = 'start date'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения,
				Формат(СтрокаТЧ.НомерСтроки, "ЧГ=;"),
				ПредставлениеТаблицы,
				ПредставлениеПоля);
				
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТаблицы, СтрокаТЧ.НомерСтроки, "НачалоДействия");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, Поле,, Отказ);
			
		КонецЕсли;
		#КонецОбласти
		
		#Область ОкончаниеВПериодеДействия
		Если ВыполнятьПроверку
		   И СтрокаТЧ.ОкончаниеДействия <> ПустаяДата
		   И НЕ (СтрокаТЧ.ОкончаниеДействия >= ОбъектПроверки.НачалоДействия
				 И СтрокаТЧ.ОкончаниеДействия <= ОбъектПроверки.ОкончаниеДействия) Тогда
			
			ПредставлениеПоля = НСтр("ru = 'дата окончания';
									|en = 'end date'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения,
				Формат(СтрокаТЧ.НомерСтроки, "ЧГ=;"),
				ПредставлениеТаблицы,
				ПредставлениеПоля);
				
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТаблицы, СтрокаТЧ.НомерСтроки, "ОкончаниеДействия");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, Поле,, Отказ);
			
		КонецЕсли;
		#КонецОбласти
		
		#Область Периодичность
		РезультатПроверки = ПроверитьПериодыПоПериодичности(
			ОбъектПроверки.ПериодичностьУсловий,
			СтрокаТЧ.НачалоДействия, 
			СтрокаТЧ.ОкончаниеДействия);
		
		Если РезультатПроверки.ЕстьНарушениеНачала Тогда
			
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Колонка",
				"Корректность",
				НСтр("ru = 'Начало действия';
					|en = 'Valid from'"),
				Формат(СтрокаТЧ.НомерСтроки, "ЧГ=;"),
				ПредставлениеТаблицы,
				РезультатПроверки.ТекстОшибкиНачала);
			
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТаблицы, СтрокаТЧ.НомерСтроки, "НачалоДействия");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ОбъектПроверки, ПутьКПолю,, Отказ);
			
		КонецЕсли;
		
		Если РезультатПроверки.ЕстьНарушениеОкончания Тогда
			
			ТекстОшибки = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Колонка",
				"Корректность",
				НСтр("ru = 'Окончание действия';
					|en = 'Valid to'"),
				Формат(СтрокаТЧ.НомерСтроки, "ЧГ=;"),
				ПредставлениеТаблицы,
				РезультатПроверки.ТекстОшибкиОкончания);
			
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТаблицы, СтрокаТЧ.НомерСтроки, "ОкончаниеДействия");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ОбъектПроверки, ПутьКПолю,, Отказ);
			
		КонецЕсли;
		#КонецОбласти
		
	КонецЦикла;
	
КонецПроцедуры

// Проверить наличие действующих строк в табличной части
// 
// Параметры:
//  ОбъектПроверки - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика -
//  ИмяТаблицы - Строка - Имя проверяемой табличной части
//  ПредставлениеТаблицы - Строка - Представление проверяемой табличной части
//  Отказ - Булево - Отказ
//
Процедура ПроверитьНаличиеДействующихСтрок(ОбъектПроверки, Знач ИмяТаблицы, ПредставлениеТаблицы, Отказ) Экспорт
	
	Если НЕ ОбъектПроверки.Исправление Тогда
		Возврат;
	КонецЕсли;
	
	ТабличнаяЧасть = ОбъектПроверки[ИмяТаблицы]; // ТабличнаяЧасть
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Отменено", Истина);
	КоличествоСтрокВсего = ТабличнаяЧасть.Количество();
	КоличествоСтрокОтменено = ТабличнаяЧасть.НайтиСтроки(СтруктураОтбора).Количество();
	
	ВсеСтрокиТЧОтменены = (КоличествоСтрокВсего = КоличествоСтрокОтменено);
	
	Если ВсеСтрокиТЧОтменены Тогда
		
		ТекстПричины = НСтр("ru = 'Все строки отменены, должна быть хотя бы одна действующая строка';
							|en = 'All lines are canceled. There must be at least one valid line'");
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Список",
			"Корректность",
			,
			,
			ПредставлениеТаблицы,
			ТекстПричины);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, ИмяТаблицы,, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ОбъектИнициализации - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика -
//  ИмяТаблицы - Строка - Имя проверяемой табличной части
//  ИменаОчищаемыхПолей - Неопределено, Массив из Строка - имена полей табличной части, которые дополнительно требуется очистить
//
Процедура ИнициализироватьКопируемыйСписок(ОбъектИнициализации, Знач ИмяТаблицы, ИменаОчищаемыхПолей = Неопределено) Экспорт
	
	СтрокиДляУдаления = Новый Массив; // Массив из ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Контрагенты, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ДоговорыСоглашения, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.СегментыПартнеров, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.СегментыТоваров, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Товары, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.КонтрагентыКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.ИННКонтрагентовКлиентов
	
	ТабличнаяЧасть = ОбъектИнициализации[ИмяТаблицы]; // ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Контрагенты, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.ДоговорыСоглашения, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СегментыПартнеров, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СегментыТоваров, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.КонтрагентыКлиентов, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.ИННКонтрагентовКлиентов
	
	Для Каждого СтрокаТЧ Из ТабличнаяЧасть Цикл
		
		Если СтрокаТЧ.Отменено Тогда
			
			СтрокиДляУдаления.Добавить(СтрокаТЧ);
			
		Иначе
			
			СтрокаТЧ.ИсходнаяСтрока = Ложь;
			
			Если ИменаОчищаемыхПолей <> Неопределено Тогда
				
				Для Каждого ИмяПоля Из ИменаОчищаемыхПолей Цикл
					СтрокаТЧ[ИмяПоля] = Неопределено;
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Строка Из СтрокиДляУдаления Цикл
		ТабличнаяЧасть.Удалить(Строка);
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ИсправляемыйДокумент - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика -
//  СторнируемыйДокумент - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика -
//  ИдентификаторЦепочки - Строка -
//  РежимЗаписи - РежимЗаписиДокумента
//
Процедура ПроверитьЦепочкуДокументов(Знач ИсправляемыйДокумент, Знач СторнируемыйДокумент, Знач ИдентификаторЦепочки,  Знач РежимЗаписи) Экспорт
	
	Если ИсправляемыйДокумент.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыИсправляемого = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ИсправляемыйДокумент, "ИдентификаторЦепочки");
	
	Если РеквизитыИсправляемого.ИдентификаторЦепочки <> ИдентификаторЦепочки
	   И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ТекстОшибки = НСтр("ru = 'Исправляемый документ был пересогласован, текущая корректировка запрещена к использованию.
						   |Создайте новый документ корректировки.';
						   |en = 'The document to correct was reapproved. The current adjustment cannot be used.
						   |Create a new adjustment document.'");
		ВызватьИсключение ТекстОшибки
		
	КонецЕсли;
	
	РеквизитыСторнируемого = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		СторнируемыйДокумент, "Статус");
		
	Если РеквизитыСторнируемого.Статус <> Перечисления.СтатусыДокументовРетроБонусов.Согласован Тогда
		
		ТекстОшибки = НСтр("ru = 'Предыдущее исправление документ не согласовано, проведение документа запрещено.';
							|en = 'The previous correction of the document is not approved. Cannot post the document.'");
		ВызватьИсключение ТекстОшибки
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  Объект - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика -
//  ИмяТаблицы - Строка
//  ПредставлениеТаблицы - Строка
//  Отказ - Булево
//
Процедура ПроверитьЗаполнениеИНН(Объект, ИмяТаблицы, ПредставлениеТаблицы, Отказ) Экспорт
	
	ТабличнаяЧасть = Объект[ИмяТаблицы]; // ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.ИННКонтрагентовКлиентов
	Для Каждого СтрокаИНН Из ТабличнаяЧасть Цикл
		
		ДлинаИНН = СтрДлина(СокрЛП(СтрокаИНН.ИНН));
		ПутьКСтроке = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТаблицы, СтрокаИНН.НомерСтроки, "ИНН");
		
		Если ПустаяСтрока(СтрокаИНН.ИНН) Тогда
			
			// Проверка платформы будет вызвана
			Продолжить;
			
		ИначеЕсли ДлинаИНН <> 10
				И ДлинаИНН <> 12 Тогда
			
			РасшифровкаОшибки = НСтр("ru = 'ИНН должен состоять из 10 или 12 цифр';
									|en = 'TIN must contain 10 or 12 digits'");
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Колонка",
				"Корректность",
				"ИНН",
				Формат(СтрокаИНН.НомерСтроки, "ЧГ=;"),
				ПредставлениеТаблицы,
				РасшифровкаОшибки);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, ПутьКСтроке,, Отказ);
			
		Иначе
			
			ИННКорректный = Истина;
			ТекстПроверки = "";
			
			//++Локализация
			ИННКорректный = РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(
				СтрокаИНН.ИНН, (ДлинаИНН = 10), ТекстПроверки);
			//--Локализация
			
			Если НЕ ИННКорректный Тогда
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Колонка",
				"Корректность",
				"ИНН",
				Формат(СтрокаИНН.НомерСтроки, "ЧГ=;"),
				ПредставлениеТаблицы,
				ТекстПроверки);
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, ПутьКСтроке,, Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ШкалыРасчета - ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.ШкалыРасчета
//               - ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.ШкалыРасчета
//  ВидШкалыРасчета - Число
//  РеквизитыВида - Структура:
//  	* ПоказательТоваров - ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам
//  Отказ - Булево -
//
Процедура ПроверитьШкалыРасчета(ШкалыРасчета, ВидШкалыРасчета, РеквизитыВида, Отказ) Экспорт
	
	Если РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
	 ИЛИ ВидШкалыРасчета = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидШкалыРасчета = 1 Тогда // Прогрессивная шкала
		
		Для Каждого СтрокаШкалы Из ШкалыРасчета Цикл
			
			Если СтрокаШкалы.БонусПроцент <> 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"КОЛОНКА",
				,
				НСтр("ru = 'Бонус, %';
					|en = 'Bonus, %'"),
				Формат(СтрокаШкалы.НомерСтроки, "ЧГ=;"),
				НСтр("ru = 'Шкалы расчета';
					|en = 'Calculation scales'"));
			
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				"ШкалыРасчета", СтрокаШкалы.НомерСтроки, "БонусПроцент");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ВидШкалыРасчета = 2 Тогда // По диапазонам
		
		Если ШкалыРасчета.Количество() = 1
		   И ШкалыРасчета[0].ДиапазонОт = 0 Тогда
			
			ТекстСообщения = НСтр("ru = 'Требуется указание не менее двух диапазонов шкалы расчета';
									|en = 'At least two calculation scale ranges must be specified'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ШкалыРасчета", "Объект", Отказ);
			
		ИначеЕсли ШкалыРасчета.Количество() > 0 Тогда
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("БонусПроцент", 0);
			
			НайденныеСтроки = ШкалыРасчета.НайтиСтроки(ПараметрыОтбора);
			Если ШкалыРасчета.Количество() = НайденныеСтроки.Количество() Тогда
				
				ТекстСообщения = НСтр("ru = 'Процент бонуса должен быть указан не менее чем для одного из диапазонов';
										|en = 'The bonus percentage must be specified for at least one of the ranges'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ШкалыРасчета", "Объект", Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ШкалыРасчета - ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.ШкалыРасчета
//               - ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.ШкалыРасчета
//  ВидШкалыРасчета - Число
//  РеквизитыВида - Структура:
//  	* ПоказательТоваров - ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам
//
Процедура ОчиститьШкалыРасчета(ШкалыРасчета, ВидШкалыРасчета, РеквизитыВида) Экспорт
	
	Если РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
	 ИЛИ ВидШкалыРасчета = 0 Тогда
		
		ШкалыРасчета.Очистить();
	
	ИначеЕсли ВидШкалыРасчета = 1 Тогда // Прогрессивная шкала
		
		Для Каждого СтрокаШкалы Из ШкалыРасчета Цикл
			
			СтрокаШкалы.ДиапазонПо = 0;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  Форма - см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента
//        - см. Документ.УсловияРетроБонусовПоставщика.Форма.ФормаДокумента
//  ИмяТаблицы - Строка - имя обрабатываемой таблицы
//
Процедура ИнициализироватьПрогрессивнуюШкалу(Форма, ИмяТаблицы) Экспорт
	
	Товары = Форма.Объект[ИмяТаблицы]; // ДанныеФормыКоллекция
	ИмяПоляКоличество = "КоличествоПлан";
	ИмяПоляКоличествоГраница = "КоличествоПланГраницаДиапазона";
	
	СтрокиКУдалению = Новый Массив; // Массив из ДанныеФормыЭлементКоллекции
	
	Для Каждого СтрокаТоваров Из Товары Цикл
		
		Если СтрокаТоваров[ИмяПоляКоличество] = 0 Тогда
			
			СтрокиКУдалению.Добавить(СтрокаТоваров);
			
		Иначе
			
			СтрокаТоваров[ИмяПоляКоличествоГраница] = 0;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначенияУТ.УдалитьСтрокиТаблицы(Товары, СтрокиКУдалению);
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура - Параметры проверки периодов товаров по ключам:
// * ПоказательТоваров - ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам - 
// * ПериодичностьУсловий - ПеречислениеСсылка.ПериодичностиРетроБонусов - 
// * НачалоДействия - Дата - 
// * ОкончаниеДействия - Дата - 
// * ПредставлениеТаблицы - Строка -
// * ВидШкалыРасчета - Число -
// 
Функция ПараметрыПроверкиПериодовПоКлючам() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ПоказательТоваров", Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.ПустаяСсылка());
	Параметры.Вставить("ПериодичностьУсловий", Перечисления.ПериодичностиРетроБонусов.ПустаяСсылка());
	Параметры.Вставить("НачалоДействия", Дата(1, 1 ,1 ));
	Параметры.Вставить("ОкончаниеДействия", Дата(1, 1 ,1 ));
	Параметры.Вставить("ПредставлениеТаблицы", "");
	Параметры.Вставить("ВидШкалыРасчета", 0);
	
	Возврат Параметры;
	
КонецФункции

// Параметры:
//  ТаблицаДанных - ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Товары
//                - ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Товары
//  ПараметрыПроверки - см. ПараметрыПроверкиПериодовПоКлючам
//  Отказ - Булево -
//
Процедура ПроверитьПериодыТоваровПоКлючам(ТаблицаДанных, ПараметрыПроверки, Отказ) Экспорт
	
	ИмяТаблицы = "Товары";
	Данные = ДанныеПериодовТоваровПоКлючам(ТаблицаДанных, ПараметрыПроверки);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры") Тогда
		
		ШаблонСообщенияПериоды = НСтр("ru = 'В списке ""%1"" пересекаются периоды действия по ключевым полям ""Номенклатура, Характеристика"" (%2)';
										|en = 'In the ""%1"" list, validity periods for the ""Item, Variant"" key fields (%2) overlap'");
		ШаблонСообщенияПериодыВесьПериод = НСтр("ru = 'В списке ""%1"" указаны разные периоды действия по ключевым полям ""Номенклатура, Характеристика"" (%2). Для периодичности условий ""Весь период"" наличие разных периодов не допускается.';
												|en = 'In the ""%1"" list, different validity periods are specified for the ""Item, Variant"" key fields (%2). Different periods are not allowed for the ""Whole period"" condition frequency.'");
		ШаблонСообщенияДублиПланаПроцента = НСтр("ru = 'В списке ""%1"" дублируются показатели ""Кол-во, план и/или Бонус, %"" по ключевым полям ""Номенклатура, Характеристика"" (%2)';
												|en = 'In the ""%1"" list, the ""Quantity, plan and/or Rebate, %"" indicators for the ""Item, Variant"" key fields (%2) are duplicated'");
		ШаблонСообщенияДублиДиапазонов = НСтр("ru = 'В списке ""%1"" дублируются показатели ""Кол-во, план"" (%2) по ключевым полям ""Номенклатура, Характеристика"" (%3)';
												|en = 'In the ""%1"" list, the ""Quantity, plan"" (%2) indicators for the ""Item, Variant"" (%3) key fields are duplicated'");
		
	Иначе
		
		ШаблонСообщенияПериоды = НСтр("ru = 'В списке ""%1"" пересекаются периоды действия по ключевому полю ""Номенклатура"" (%2)';
										|en = 'In the ""%1"" list, validity periods for the ""Item"" key field (%2) overlap'");
		ШаблонСообщенияПериодыВесьПериод = НСтр("ru = 'В списке ""%1"" указаны разные периоды действия по ключевому полю ""Номенклатура"" (%2). Для периодичности условий ""Весь период"" наличие разных периодов не допускается.';
												|en = 'In the ""%1"" list, different validity periods are specified for the ""Item"" key field (%2). Different periods are not allowed for the ""Whole period"" condition frequency.'");
		ШаблонСообщенияДублиПланаПроцента = НСтр("ru = 'В списке ""%1"" дублируются показатели ""Кол-во, план и/или Бонус, %"" по ключевому полю ""Номенклатура"" (%2)';
												|en = 'In the ""%1"" list, the ""Quantity, plan and/or Rebate, %"" indicators for the ""Item"" key field (%2) are duplicated'");
		ШаблонСообщенияДублиДиапазонов = НСтр("ru = 'В списке ""%1"" дублируются показатели ""Кол-во, план"" (%2) по ключевому полю ""Номенклатура"" (%3)';
												|en = 'In the ""%1"" list, the ""Quantity, plan"" (%2) indicators for the ""Item"" key field (%3) are duplicated'");
		
	КонецЕсли;
	
	Пока Данные.ПересеченияПериодов.Следующий() Цикл
		
		Если ПараметрыПроверки.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество
		   И ПараметрыПроверки.ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.ВесьПериод Тогда
			ШаблонПериоды = ШаблонСообщенияПериодыВесьПериод;
		Иначе
			ШаблонПериоды = ШаблонСообщенияПериоды;
		КонецЕсли;
		
		ПредставлениеНоменклатуры = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
			Данные.ПересеченияПериодов.Номенклатура,
			Данные.ПересеченияПериодов.Характеристика);
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонПериоды,
			ПараметрыПроверки.ПредставлениеТаблицы,
			ПредставлениеНоменклатуры);
			
		ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, Данные.ПересеченияПериодов.ПерваяСтрока, "НомерСтроки");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ПутьКПолю, "Объект", Отказ);
		
	КонецЦикла;
	
	Пока Данные.ДублиПланаПроцента.Следующий() Цикл
		
		ПредставлениеНоменклатуры = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
			Данные.ДублиПланаПроцента.Номенклатура,
			Данные.ДублиПланаПроцента.Характеристика);
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщенияДублиПланаПроцента,
			ПараметрыПроверки.ПредставлениеТаблицы,
			ПредставлениеНоменклатуры);
			
		ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, Данные.ДублиПланаПроцента.ПерваяСтрока, "НомерСтроки");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ПутьКПолю, "Объект", Отказ);
		
	КонецЦикла;
	
	Пока Данные.ДублиПоДиапазонам.Следующий() Цикл
		
		ПредставлениеНоменклатуры = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
			Данные.ДублиПоДиапазонам.Номенклатура,
			Данные.ДублиПоДиапазонам.Характеристика);
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщенияДублиДиапазонов,
			ПараметрыПроверки.ПредставлениеТаблицы,
			Формат(Данные.ДублиПоДиапазонам.КоличествоПлан, "ЧДЦ=3"),
			ПредставлениеНоменклатуры);
			
		ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, Данные.ДублиПоДиапазонам.ПерваяСтрока, "КоличествоПлан");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ПутьКПолю, "Объект", Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ТаблицаДанных - ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры
//                - ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры
//  ПараметрыПроверки - см. ПараметрыПроверкиПериодовПоКлючам
//  Отказ - Булево -
//
Процедура ПроверитьПериодыСвойствНоменклатурыПоКлючам(ТаблицаДанных, ПараметрыПроверки, Отказ) Экспорт
	
	ИмяТаблицы = "СвойстваНоменклатуры";
	Данные = ДанныеПериодовСвойствНоменклатурыПоКлючам(ТаблицаДанных, ПараметрыПроверки);
	
	ШаблонСообщенияПериоды = НСтр("ru = 'В списке ""%1"" пересекаются периоды действия по значению свойства ""%2""';
									|en = 'In the ""%1"" list, validity periods for the ""%2"" property value overlap'");
	ШаблонСообщенияПериодыВесьПериод = НСтр("ru = 'В списке ""%1"" указаны разные периоды действия по значению свойства ""%2"". Для периодичности условий ""Весь период"" наличие разных периодов не допускается.';
											|en = 'In the ""%1"" list, different validity periods are specified for the ""%2"" property value. Different periods are not allowed for the ""Whole period"" condition frequency.'");
	ШаблонСообщенияДублиПланаПроцента = НСтр("ru = 'В списке ""%1"" дублируются показатели ""Кол-во, план и/или Бонус, %"" по значению свойства ""%2""';
											|en = 'In the ""%1"" list, the ""Quantity, plan and/or Rebate, %"" indicators for the ""%2"" property value are duplicated'");
	ШаблонСообщенияДублиДиапазонов = НСтр("ru = 'В списке ""%1"" дублируются показатели ""Кол-во, план"" (%2) по значению свойства ""%3""';
											|en = 'In the ""%1"" list, the ""Quantity, plan"" indicators (%2) for the ""%3"" property value are duplicated'");
	
	Пока Данные.ПересеченияПериодов.Следующий() Цикл
		
		Если ПараметрыПроверки.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество
		   И ПараметрыПроверки.ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.ВесьПериод Тогда
			ШаблонПериоды = ШаблонСообщенияПериодыВесьПериод;
		Иначе
			ШаблонПериоды = ШаблонСообщенияПериоды;
		КонецЕсли;
		
		ПредставлениеПоля = СокрЛП(Данные.ПересеченияПериодов.ЗначениеСвойства);
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонПериоды,
			ПараметрыПроверки.ПредставлениеТаблицы,
			ПредставлениеПоля);
			
		ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, Данные.ПересеченияПериодов.ПерваяСтрока, "НомерСтроки");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ПутьКПолю, "Объект", Отказ);
		
	КонецЦикла;
	
	Пока Данные.ДублиПланаПроцента.Следующий() Цикл
		
		ПредставлениеПоля = СокрЛП(Данные.ДублиПланаПроцента.ЗначениеСвойства);
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщенияДублиПланаПроцента,
			ПараметрыПроверки.ПредставлениеТаблицы,
			ПредставлениеПоля);
			
		ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, Данные.ДублиПланаПроцента.ПерваяСтрока, "НомерСтроки");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ПутьКПолю, "Объект", Отказ);
		
	КонецЦикла;
	
	Пока Данные.ДублиПоДиапазонам.Следующий() Цикл
		
		ПредставлениеПоля = СокрЛП(Данные.ДублиПоДиапазонам.ЗначениеСвойства);
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщенияДублиДиапазонов,
			ПараметрыПроверки.ПредставлениеТаблицы,
			Формат(Данные.ДублиПоДиапазонам.КоличествоПлан, "ЧДЦ=3"),
			ПредставлениеПоля);
			
		ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, Данные.ДублиПоДиапазонам.ПерваяСтрока, "КоличествоПлан");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ПутьКПолю, "Объект", Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает текст запроса для формирования временной таблицы с товарами, отобранными по значениям указанного свойства.
// 
// Параметры:
//  РасчетБонусовКлиентов - Булево -
//  ИмяСвойства - Строка -
//  СвойствоНоменклатурыСсылка - ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения 
//  ПоДаннымДокумента - Булево
// 
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ТекстЗапросаТоварыПоСвойству(РасчетБонусовКлиентов, ИмяСвойства, СвойствоНоменклатурыСсылка, ПоДаннымДокумента = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СвойствоНоменклатурыСсылка) Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаИсточник.ДокументУсловий КАК ДокументУсловий,
		|	СправочникНоменклатура.Ссылка КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
		|	ТаблицаИсточник.ЗначениеСвойства КАК ЗначениеСвойства,
		|	ТаблицаИсточник.НачалоДействия КАК НачалоДействия,
		|	ТаблицаИсточник.ОкончаниеДействия КАК ОкончаниеДействия,
		|	ТаблицаИсточник.БазоваяЦена КАК БазоваяЦена,
		|	ТаблицаИсточник.БонусПроцент КАК БонусПроцент,
		|	ТаблицаИсточник.КоличествоПлан КАК КоличествоПлан,
		|	ТаблицаИсточник.КоличествоПланГраницаДиапазона КАК КоличествоПланГраницаДиапазона
		|ПОМЕСТИТЬ ВТ_ТоварыПоСвойству
		|ИЗ
		|	&ТаблицаИсточник КАК ТаблицаИсточник
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО ТаблицаИсточник.ЗначениеСвойства = &ВыражениеПолеРеквизита
		|ГДЕ
		|	ТаблицаИсточник.ДокументУсловий = &ИсходныйДокумент
		|	И &УсловиеВывода";
		
		ВыражениеПолеРеквизита = "СправочникНоменклатура." + ИмяСвойства;
		
		Если ПоДаннымДокумента Тогда
			
			Если РасчетБонусовКлиентов Тогда
				ТаблицаИсточник = "Документ.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры";
			Иначе
				ТаблицаИсточник = "Документ.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры";
			КонецЕсли;
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеВывода", "НЕ ТаблицаИсточник.Отменено");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаИсточник.ДокументУсловий", "ТаблицаИсточник.Ссылка");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаИсточник.БонусПроцент", "ТаблицаИсточник.Процент");
			
		Иначе
			
			Если РасчетБонусовКлиентов Тогда
				ТаблицаИсточник = "РегистрСведений.РетроБонусыКлиентовСвойстваНоменклатуры";
			Иначе
				ТаблицаИсточник = "РегистрСведений.РетроБонусыПоставщиковСвойстваНоменклатуры";
			КонецЕсли;
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеВывода", "ИСТИНА");
			
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеПолеРеквизита", ВыражениеПолеРеквизита);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточник", ТаблицаИсточник);
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаИсточник.ДокументУсловий КАК ДокументУсловий,
		|	НоменклатураДополнительныеРеквизиты.Ссылка КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
		|	ТаблицаИсточник.ЗначениеСвойства КАК ЗначениеСвойства,
		|	ТаблицаИсточник.НачалоДействия КАК НачалоДействия,
		|	ТаблицаИсточник.ОкончаниеДействия КАК ОкончаниеДействия,
		|	ТаблицаИсточник.БазоваяЦена КАК БазоваяЦена,
		|	ТаблицаИсточник.БонусПроцент КАК БонусПроцент,
		|	ТаблицаИсточник.КоличествоПлан КАК КоличествоПлан,
		|	ТаблицаИсточник.КоличествоПланГраницаДиапазона КАК КоличествоПланГраницаДиапазона
		|ПОМЕСТИТЬ ВТ_ТоварыПоСвойству
		|ИЗ
		|	&ТаблицаИсточник КАК ТаблицаИсточник
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
		|		ПО &СвойствоНоменклатуры = НоменклатураДополнительныеРеквизиты.Свойство
		|			И ТаблицаИсточник.ЗначениеСвойства = НоменклатураДополнительныеРеквизиты.Значение
		|ГДЕ
		|	ТаблицаИсточник.ДокументУсловий = &ИсходныйДокумент
		|	И &УсловиеВывода";
		
		Если ПоДаннымДокумента Тогда
			
			Если РасчетБонусовКлиентов Тогда
				ТаблицаИсточник = "Документ.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры";
			Иначе
				ТаблицаИсточник = "Документ.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры";
			КонецЕсли;
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеВывода", "НЕ ТаблицаИсточник.Отменено");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаИсточник.ДокументУсловий", "ТаблицаИсточник.Ссылка");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаИсточник.БонусПроцент", "ТаблицаИсточник.Процент");
			
		Иначе
			
			Если РасчетБонусовКлиентов Тогда
				ТаблицаИсточник = "РегистрСведений.РетроБонусыКлиентовСвойстваНоменклатуры";
			Иначе
				ТаблицаИсточник = "РегистрСведений.РетроБонусыПоставщиковСвойстваНоменклатуры";
			КонецЕсли;
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеВывода", "ИСТИНА");
			
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточник", ТаблицаИсточник);
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Параметры:
//  ОбъектПроверки - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика -
//  ПредставлениеТаблицы - Строка - Представление проверяемой табличной части
//  РеквизитыВида - Структура:
//  	* ПоказательТоваров - ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам
//  Отказ - Булево - Отказ -
//
Процедура ПроверитьУказаниеПустыхЗначенийХарактеристик(ОбъектПроверки, ПредставлениеТаблицы, РеквизитыВида, Отказ) Экспорт
	
	ОтборТоваров = ОбъектПроверки.ОтборТоваров;
	ПериодичностьУсловий = ОбъектПроверки.ПериодичностьУсловий;
	ПредставлениеКлючевогоПоля = НСтр("ru = 'Номенклатура';
										|en = 'Items'");
	ПредставлениеВспомогательногоПоля = НСтр("ru = 'Характеристика';
											|en = 'Variant'");
	
	Выборка = ВыборкаПустыхИЗаполненныхХарактеристик(ОбъектПроверки, РеквизитыВида);
	
	Если ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.КромеВыбранных Тогда
		
		ШаблонСообщения = НСтр("ru = 'В списке ""%1"" по ключевому полю ""%2"" (%3) есть строки с заполненным и незаполненным значением в поле ""%4""';
								|en = 'In the ""%1"" list for the ""%2"" (%3) key field, there are lines with a filled and unfilled value in the ""%4"" field'");
		
	ИначеЕсли ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.ВесьПериод
	   И РеквизитыВида.ПоказательТоваров =  Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		
		ШаблонСообщения = НСтр("ru = 'В списке ""%1"" по ключевому полю ""%2"" (%3) есть строки с заполненным и незаполненным значением в поле ""%4"". Для периодичности условий ""%5"" наличие строк по одной номенклатуре с заполненной и не заполненной характеристиками не допускается.';
								|en = 'In the ""%1"" list for the ""%2"" (%3) key field, there are strings with a filled and unfilled value in the ""%4"" field. For frequency of the ""%5"" condition, the presence of lines for one item with filled and unfilled characteristics is not allowed.'");
		
	Иначе
		
		ШаблонСообщения = НСтр("ru = 'В списке ""%1"" по ключевому полю ""%2"" (%3) в пересекающихся периодах есть строки с заполненным и незаполненным значением в поле ""%4""';
								|en = 'In the ""%1"" list for the ""%2"" (%3) key field in overlapping periods, there are lines with filled and unfilled values in the ""%4"" field'");
		
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			"Товары", Выборка.НомерСтроки, "НомерСтроки");
			
		Если ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.ВесьПериод
		   И РеквизитыВида.ПоказательТоваров =  Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения,
				ПредставлениеТаблицы,
				ПредставлениеКлючевогоПоля,
				Строка(Выборка.Номенклатура),
				ПредставлениеВспомогательногоПоля,
				Строка(ПериодичностьУсловий));
			
		Иначе
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения,
				ПредставлениеТаблицы,
				ПредставлениеКлючевогоПоля,
				Строка(Выборка.Номенклатура),
				ПредставлениеВспомогательногоПоля);
			
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ПутьКПолю, "Объект", Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  Форма - см. Справочник.ВидыРетроБонусовПоставщиков.Форма.ФормаЭлемента
//        - см. Справочник.ВидыРетроБонусовКлиентов.Форма.ФормаЭлемента
//
Процедура ЗаполнитьСписокОперандовНоменклатуры(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	СвойстваНоменклатуры = Форма.СвойстваНоменклатуры;
	ДеревоОперандовНоменклатуры = ПолучитьДеревоОперандовНоменклатуры();
	
	ИспользоватьДополнительныеРеквизитыИСведения =
		ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения");
	ТипыЭлементов = РаботаСФормулами.ТипыЭлементовДереваОперандов();
	ТипыСвойствНоменклатуры = Метаданные.ОпределяемыеТипы.ТипыСвойствНоменклатурыРетроБонусов.Тип;
	МетаданныеОбъекта = Метаданные.Справочники.Номенклатура;
	СтандартныеРеквизитыОбъекта = МетаданныеОбъекта.СтандартныеРеквизиты;
	РеквизитыОбъекта = МетаданныеОбъекта.Реквизиты;
	ОбщиеРеквизиты = Метаданные.ОбщиеРеквизиты;
	
	Для Каждого ТекущаяСтрокаДерева Из ДеревоОперандовНоменклатуры.Строки Цикл
		
		ПропуститьСтроку =
			(ТекущаяСтрокаДерева.ПометкаУдаления
			ИЛИ НЕ РазрешенныйТипЗначенияДляСвойстваНоменклатуры(ТекущаяСтрокаДерева.ТипЗначения, ТипыСвойствНоменклатуры)
			ИЛИ ТекущаяСтрокаДерева.ТипЭлементаДерева = ТипыЭлементов.ДополнительноеСведение
			ИЛИ (НЕ ИспользоватьДополнительныеРеквизитыИСведения
				И ТекущаяСтрокаДерева.ТипЭлементаДерева = ТипыЭлементов.ДополнительныйРеквизит));
		
		ИдентификаторРеквизита = ТекущаяСтрокаДерева.Идентификатор;
		ЭтоДопРеквизитСвойство = (СтрНайти(ИдентификаторРеквизита, "{") > 0);
		
		Если НЕ ЭтоДопРеквизитСвойство
		   И НЕ ПропуститьСтроку
		   И НЕ ОбщегоНазначения.ЭтоСтандартныйРеквизит(СтандартныеРеквизитыОбъекта, ИдентификаторРеквизита) Тогда
			
			ОбъектМетаданных = РеквизитыОбъекта.Найти(ИдентификаторРеквизита);
			Если ОбъектМетаданных = Неопределено Тогда
				
				ОбъектМетаданных = ОбщиеРеквизиты.Найти(ИдентификаторРеквизита);
				Если ОбъектМетаданных <> Неопределено Тогда
					ПропуститьСтроку = Истина;
				КонецЕсли;
				
			Иначе
				
				ПропуститьСтроку = НЕ ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ОбъектМетаданных);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПропуститьСтроку Тогда
			Продолжить;
		КонецЕсли;
		
		Представление = ТекущаяСтрокаДерева.Представление;
		Элементы.СвойствоНоменклатуры.СписокВыбора.Добавить(ИдентификаторРеквизита, Представление);
		
		Если НЕ ЭтоДопРеквизитСвойство Тогда
			СвойстваНоменклатуры.Добавить(ИдентификаторРеквизита, Представление);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ИдентификаторРеквизита - Строка
// Возвращаемое значение:
// см. ДанныеРеквизитаНоменклатуры
//
Функция ДанныеПроверкиСвойстваНоменклатуры(Знач ИдентификаторРеквизита) Экспорт
	
	Результат = ДанныеРеквизитаНоменклатуры();
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторРеквизита) Тогда
		Возврат Результат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЭтоДопРеквизитСвойство = (СтрНайти(ИдентификаторРеквизита, "{") > 0);
	
	Если ЭтоДопРеквизитСвойство Тогда
		
		РезультатПроверки = ЯвляетсяДополнительнымРеквизитомНоменклатуры(ИдентификаторРеквизита);
		ЗаполнитьЗначенияСвойств(Результат, РезультатПроверки);
		
	ИначеЕсли ЯвляетсяРеквизитомНоменклатуры(ИдентификаторРеквизита) Тогда
		
		Результат.ЯвляетсяРеквизитом = Истина;
		МетаданныеОбъекта = Метаданные.Справочники.Номенклатура;
		
		Если ОбщегоНазначения.ЭтоСтандартныйРеквизит(МетаданныеОбъекта.СтандартныеРеквизиты, ИдентификаторРеквизита) Тогда
			
			Результат.ТипЗначения = МетаданныеОбъекта.СтандартныеРеквизиты[ИдентификаторРеквизита].Тип;
			Результат.Заголовок = МетаданныеОбъекта.СтандартныеРеквизиты[ИдентификаторРеквизита].Представление();
			
		Иначе
			
			Результат.ТипЗначения = МетаданныеОбъекта.Реквизиты[ИдентификаторРеквизита].Тип;
			Результат.Заголовок = МетаданныеОбъекта.Реквизиты[ИдентификаторРеквизита].Представление();
			
			ОбъектМетаданных = МетаданныеОбъекта.Реквизиты[ИдентификаторРеквизита];
			Результат.ДоступенПоФункциональнымОпциям =
				ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ОбъектМетаданных);
			
		КонецЕсли;
		
		Результат.РазрешенныйТипЗначения = РазрешенныйТипЗначенияДляСвойстваНоменклатуры(Результат.ТипЗначения);
		Результат.РазрешеноИспользовать = (Результат.РазрешенныйТипЗначения
											И Результат.ДоступенПоФункциональнымОпциям);
	Иначе
		
		Результат.РеквизитНеНайден = Истина;
		Результат.Заголовок = ИдентификаторРеквизита;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ИдентификаторРеквизита - Строка
//  СоставТоваров - ПеречислениеСсылка.СоставыТоваровРетроБонусов
//  Отказ - Булево
//
Процедура КонтрольСвойстваНоменклатурыВидаРетроБонуса(Знач ИдентификаторРеквизита, Знач СоставТоваров, Отказ) Экспорт
	
	Если СоставТоваров <> Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры
	   И ПустаяСтрока(ИдентификаторРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	РезультатПроверки = ДанныеПроверкиСвойстваНоменклатуры(ИдентификаторРеквизита);
	
	Если НЕ РезультатПроверки.РазрешеноИспользовать Тогда
		
		ОписаниеПричиныЗапрета = ОписаниеПричиныЗапретаСвойстваНоменклатуры(РезультатПроверки);
		ТекстСообщения = ОписаниеПричиныЗапрета.ТекстСообщения;
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "СвойствоНоменклатуры", "Объект", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика -
//  РеквизитыУсловий -ВыборкаИзРезультатаЗапроса:
//   * СоставТоваров - ПеречислениеСсылка.СоставыТоваровРетроБонусов
//   * СвойствоНоменклатуры - Строка
//  Отказ - Булево
// 
Процедура ПроверитьВидРетроБонусаДокументаУсловий(Документ, РеквизитыУсловий, Отказ = Ложь) Экспорт
	
	СоставТоваров = РеквизитыУсловий.СоставТоваров;
	ИдентификаторРеквизита = РеквизитыУсловий.СвойствоНоменклатуры;
	
	Если СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры Тогда
		
		РезультатПроверки = ДанныеПроверкиСвойстваНоменклатуры(ИдентификаторРеквизита);
		Если НЕ РезультатПроверки.РазрешеноИспользовать Тогда
			
			ИмяПоля = "ДокументУсловий";
			СообщитьОбОшибкеСвойстваНоменклатуры(РезультатПроверки, ИмяПоля, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  РезультатПроверки - см. ДанныеРеквизитаНоменклатуры
//  ИмяПоля - Строка
//  Отказ - Булево
// 
Процедура СообщитьОбОшибкеСвойстваНоменклатуры(Знач РезультатПроверки, Знач ИмяПоля, Отказ = Ложь) Экспорт
	
	ОписаниеПричиныЗапрета = ОписаниеПричиныЗапретаСвойстваНоменклатуры(РезультатПроверки);
	ТекстСообщенияНачало = ОписаниеПричиныЗапрета.ТекстСообщения;
	ТекстСообщенияКонец = НСтр("ru = 'Проверьте настройки вида ретро-бонусов.';
								|en = 'Check the rebates kind settings.'");
	ТекстСообщения = ТекстСообщенияНачало + " " + ТекстСообщенияКонец;
	
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, ИмяПоля, "Объект", Отказ);
	
КонецПроцедуры

// Параметры:
//  РезультатПроверки - см. ДанныеРеквизитаНоменклатуры
//  ВидРетроБонусаИспользуется - Булево
// 
// Возвращаемое значение:
//  Структура:
//   * КраткоеОписание - Строка
//   * ПодробноеОписание - Строка
//   * ТекстСообщения - Строка
//
Функция ОписаниеПричиныЗапретаСвойстваНоменклатуры(Знач РезультатПроверки, Знач ВидРетроБонусаИспользуется = Ложь) Экспорт
	
	ПричинаЗапрета = Новый Структура;
	ПричинаЗапрета.Вставить("КраткоеОписание", "");
	ПричинаЗапрета.Вставить("ПодробноеОписание", "");
	ПричинаЗапрета.Вставить("ТекстСообщения", "");
	
	Если РезультатПроверки.РазрешеноИспользовать Тогда
		
		Возврат ПричинаЗапрета;
		
	КонецЕсли;
	
	Если РезультатПроверки.ДополнительныйРеквизит Тогда
		
		ПричинаЗапретаДляДополнительногоРеквизита(ПричинаЗапрета, РезультатПроверки, ВидРетроБонусаИспользуется);
		
	Иначе
		
		ПричинаЗапретаДляРеквизита(ПричинаЗапрета, РезультатПроверки, ВидРетроБонусаИспользуется);
		
	КонецЕсли;
	
	Возврат ПричинаЗапрета;
	
КонецФункции

// Параметры:
//  ТипЗначения - ОписаниеТипов
//  ТипыСвойствНоменклатуры - ОписаниеТипов
// Возвращаемое значение:
// Булево
//
Функция РазрешенныйТипЗначенияДляСвойстваНоменклатуры(ТипЗначения, ТипыСвойствНоменклатуры = Неопределено) Экспорт
	
	Результат = Ложь;
	
	Если ТипыСвойствНоменклатуры = Неопределено Тогда
		ТипыСвойствНоменклатуры = Метаданные.ОпределяемыеТипы.ТипыСвойствНоменклатурыРетроБонусов.Тип;
	КонецЕсли;
	
	МассивТипов = ТипЗначения.Типы();
	Для Каждого ТекущийТипЗначения Из МассивТипов Цикл
		
		Если ТипыСвойствНоменклатуры.СодержитТип(ТекущийТипЗначения) Тогда
			
			Результат = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ТипЗначения - ОписаниеТипов
// Возвращаемое значение:
// ОписаниеТипов
//
Функция РазрешенныеТипыЗначенийСвойстваНоменклатуры(ТипЗначения) Экспорт
	
	МассивПереданныхТипов = ТипЗначения.Типы();
	МассивТипов = Новый Массив; // Массив из Тип
	ОписаниеТипов = Метаданные.ОпределяемыеТипы.ТипыСвойствНоменклатурыРетроБонусов.Тип;
	
	Для Каждого ТекущийТипЗначения Из МассивПереданныхТипов Цикл
		
		Если ОписаниеТипов.СодержитТип(ТекущийТипЗначения) Тогда
			
			МассивТипов.Добавить(ТекущийТипЗначения);
			
		КонецЕсли;
		
	КонецЦикла;
	
	КвалификаторыЧисла = ТипЗначения.КвалификаторыЧисла;
	КвалификаторыСтроки = ТипЗначения.КвалификаторыСтроки;
	КвалификаторыДаты = ТипЗначения.КвалификаторыДаты;
	КвалификаторыДвоичныхДанных = ТипЗначения.КвалификаторыДвоичныхДанных;
	
	Возврат Новый ОписаниеТипов(
		МассивТипов,
		,
		,
		КвалификаторыЧисла,
		КвалификаторыСтроки,
		КвалификаторыДаты,
		КвалификаторыДвоичныхДанных);
	
КонецФункции

#КонецОбласти

#Область ОбщиеМетодыПоДиапазонам

// Обновляет поле в ТЧ по внесенному изменению по всем строкам, относящимся к диапазоном
//
// Параметры:
//  Таблица - ТабличнаяЧасть, ДанныеФормыКоллекция -
//  ПараметрыОтбора - Структура
//  Значение - Произвольный -
//  ИмяПоля - Строка -
//  КромеИсходных - Булево - 
//
Процедура ОбновитьПолеТаблицыПоОтбору(Таблица, ПараметрыОтбора, Значение, ИмяПоля, КромеИсходных = Ложь) Экспорт
	
	ИмяПоляИсходнаяСтрока = "ИсходнаяСтрока";
	
	СтрокиКИзменению = Таблица.НайтиСтроки(ПараметрыОтбора);
	Для Каждого СтрокаТаблицы Из СтрокиКИзменению Цикл
		
		Если КромеИсходных
		   И СтрокаТаблицы[ИмяПоляИсходнаяСтрока] Тогда
			Продолжить;
		КонецЕсли;
		СтрокаТаблицы[ИмяПоля] = Значение;
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ОбрабатываемаяТаблица - ДанныеФормыКоллекция, ТаблицаЗначений -
//  ТаблицаКоррекции - ТаблицаЗначений - Таблица с данными к коррекции
//  ПараметрыОтбора - Структура - Структура для отбора данных обрабатываемой таблицы
// 
Процедура ОбновитьОбозначениеДиапазонаСтрок(ОбрабатываемаяТаблица, ТаблицаКоррекции, ПараметрыОтбора) Экспорт
	
	ОбозначениеИнтервалаДо = НСтр("ru = '≤ %1';
									|en = '≤ %1'");
	ОбозначениеИнтервалаОтДо = НСтр("ru = '> %1 и ≤ %2';
									|en = '> %1 and ≤ %2'");
	ОбозначениеИнтервалаСвыше = НСтр("ru = '> %1';
									|en = '> %1'");
		
	Для каждого СтрокаКоррекции Из ТаблицаКоррекции Цикл
		
		ЗаполнитьЗначенияСвойств(ПараметрыОтбора, СтрокаКоррекции);
		Если ТипЗнч(ОбрабатываемаяТаблица) = Тип("ТаблицаЗначений") Тогда
			
			ВременнаяТаблица = ОбрабатываемаяТаблица.Скопировать(ПараметрыОтбора);
			
		Иначе
			
			НайденныеСтроки = ОбрабатываемаяТаблица.НайтиСтроки(ПараметрыОтбора);
			ВременнаяТаблица = ОбрабатываемаяТаблица.Выгрузить(НайденныеСтроки);
			
		КонецЕсли;
		
		ВременнаяТаблица.Сортировать("КоличествоПлан");
		КоличествоИнтервалов = ВременнаяТаблица.Количество();
		
		Если КоличествоИнтервалов = 0 Тогда
			
			Продолжить;
			
		ИначеЕсли ВременнаяТаблица[0].КоличествоПлан <> 0 Тогда
			
			ОбозначениеДиапазона = НСтр("ru = 'Нет диапазона со значением 0';
										|en = 'There is no range with the 0 value'");
			Для Каждого СтрокаТаблицыТоваров Из ВременнаяТаблица Цикл
				
				СтрокаЗаполнения = ОбрабатываемаяТаблица[СтрокаТаблицыТоваров.НомерСтроки - 1];
				СтрокаЗаполнения.ОбозначениеДиапазона = ОбозначениеДиапазона;
				
			КонецЦикла;
			
		ИначеЕсли КоличествоИнтервалов = 1 Тогда
			
			ПервыйИнтервал = ВременнаяТаблица[0];
			
			ГраницаИнтервала = РетроБонусыКлиентСервер.ФорматГраницыИнтервала(ПервыйИнтервал.КоличествоПлан);
			ОбозначениеДиапазона = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ОбозначениеИнтервалаСвыше,
				ГраницаИнтервала);
			
			СтрокаЗаполнения = ОбрабатываемаяТаблица[ПервыйИнтервал.НомерСтроки - 1];
			СтрокаЗаполнения.ОбозначениеДиапазона = ОбозначениеДиапазона;
			СтрокаЗаполнения.КоличествоПланГраницаДиапазона = 0;
			
		ИначеЕсли КоличествоИнтервалов > 1 Тогда
			
			ПервыйИнтервал = ВременнаяТаблица[0];
			ВторойИнтервал = ВременнаяТаблица[1];
			
			ГраницаИнтервала = РетроБонусыКлиентСервер.ФорматГраницыИнтервала(ВторойИнтервал.КоличествоПлан);
			ОбозначениеДиапазона = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ОбозначениеИнтервалаДо,
				ГраницаИнтервала);
			
			СтрокаЗаполнения = ОбрабатываемаяТаблица[ПервыйИнтервал.НомерСтроки - 1];
			СтрокаЗаполнения.ОбозначениеДиапазона = ОбозначениеДиапазона;
			СтрокаЗаполнения.КоличествоПланГраницаДиапазона = ВторойИнтервал.КоличествоПлан;
			
			Для ТекИндекс = 1 По КоличествоИнтервалов - 2 Цикл
				
				ЭтотИнтервал = ВременнаяТаблица[ТекИндекс];
				СледующийИнтервал = ВременнаяТаблица[ТекИндекс + 1];
				
				ГраницаИнтервалаОт = РетроБонусыКлиентСервер.ФорматГраницыИнтервала(ЭтотИнтервал.КоличествоПлан);
				ГраницаИнтервалаДо = РетроБонусыКлиентСервер.ФорматГраницыИнтервала(СледующийИнтервал.КоличествоПлан);
				ОбозначениеДиапазона = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ОбозначениеИнтервалаОтДо,
					ГраницаИнтервалаОт,
					ГраницаИнтервалаДо);
				
				СтрокаЗаполнения = ОбрабатываемаяТаблица[ЭтотИнтервал.НомерСтроки - 1];
				СтрокаЗаполнения.ОбозначениеДиапазона = ОбозначениеДиапазона;
				СтрокаЗаполнения.КоличествоПланГраницаДиапазона = СледующийИнтервал.КоличествоПлан;
				
			КонецЦикла;
			
			ПоследнийИнтервал = ВременнаяТаблица[КоличествоИнтервалов - 1];
			
			ГраницаИнтервала = РетроБонусыКлиентСервер.ФорматГраницыИнтервала(ПоследнийИнтервал.КоличествоПлан);
			ОбозначениеДиапазона = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ОбозначениеИнтервалаСвыше,
				ГраницаИнтервала);
			
			СтрокаЗаполнения = ОбрабатываемаяТаблица[ПоследнийИнтервал.НомерСтроки - 1];
			СтрокаЗаполнения.ОбозначениеДиапазона = ОбозначениеДиапазона;
			СтрокаЗаполнения.КоличествоПланГраницаДиапазона = 0;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ИмяТаблицы - Строка -
//	Настройки - ТаблицаЗначений - таблица с колонками:
//		* Поля - Массив из Строка - поля, для которых определяются настройки отображения
//		* Условие - ОтборКомпоновкиДанных - условия применения настройки
//		* Свойства - Структура - имена и значения свойств 
//
Процедура НастройкаПоляОбозначениеДиапазона(ИмяТаблицы, Настройки) Экспорт
	
	ИмяПоля = ИмяТаблицы + "ОбозначениеДиапазона";
	ПоказателиТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	Элемент = Настройки.Добавить();
	
	Элемент.Поля.Добавить(ИмяПоля);
	
	ФинансоваяОтчетностьСервер.НовыйОтбор(
		Элемент.Условие, "Дополнительно.ПоказательТоваров", ПоказателиТоваров.Количество);
	ФинансоваяОтчетностьСервер.НовыйОтбор(Элемент.Условие, "ВидШкалыРасчета", 1); // По диапазонам
	
	Элемент.Свойства.Вставить("Видимость", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ТоварыПоДиапазонам

// Параметры:
//  Форма - см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента
//        - см. Документ.УсловияРетроБонусовПоставщика.Форма.ФормаДокумента
//
Процедура ИнициализироватьДиапазоныТоваров(Форма) Экспорт
	
	Если Форма.СоставТоваров <> Перечисления.СоставыТоваровРетроБонусов.Номенклатура Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	Товары = Форма.Объект.Товары;
	
	ИменаКолонок = "НомерСтроки, Номенклатура, Характеристика, ХарактеристикиИспользуются, КоличествоПлан, Отменено";
	ОбрабатываемаяТаблицаТоваров = Товары.Выгрузить(, ИменаКолонок);
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц();
	Выборка = ВыборкаТоваровБезНачальногоДиапазона(Товары, МенеджерВТ, Истина);
	Пока Выборка.Следующий() Цикл
		
		СтрокаНачальногоДиапазона = Товары.Вставить(Выборка.НомерСтроки - 1);
		СтрокаНачальногоДиапазона.Номенклатура = Выборка.Номенклатура;
		СтрокаНачальногоДиапазона.Характеристика = Выборка.Характеристика;
		СтрокаНачальногоДиапазона.ХарактеристикиИспользуются = Выборка.ХарактеристикиИспользуются;
		
	КонецЦикла;
	
	ИменаПолей = "Номенклатура, Характеристика";
	ОбрабатываемаяТаблицаТоваров.Свернуть(ИменаПолей);
	
	ОбновитьДиапазоныСтрокТоваровПоТаблице(Объект, ОбрабатываемаяТаблицаТоваров);
	
КонецПроцедуры

// Параметры:
//  Объект - см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента.Объект
//         - см. Документ.УсловияРетроБонусовПоставщика.Форма.ФормаДокумента.Объект
//  ПоказательТоваров - ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам
//
Процедура ОбновитьДиапазоныСтрокВсехТоваров(Объект, ПоказательТоваров) Экспорт
	
	Если НЕ РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров) Тогда
		Возврат;
	КонецЕсли;
	
	ИменаПолей = "Номенклатура, Характеристика, ХарактеристикиИспользуются";
	ТаблицаКОбработке = Объект.Товары.Выгрузить(, ИменаПолей);
	ТаблицаКОбработке.Свернуть(ИменаПолей);
	ОбновитьДиапазоныСтрокТоваровПоТаблице(Объект, ТаблицаКОбработке);
	
КонецПроцедуры

// Параметры:
//  Объект - см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента.Объект
//         - см. Документ.УсловияРетроБонусовПоставщика.Форма.ФормаДокумента.Объект
//	Номенклатура - СправочникСсылка.Номенклатура
//	Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//
Процедура ОбновитьДиапазоныСтрокТоваровПоТовару(Объект, Номенклатура, Характеристика) Экспорт
	
	ТаблицаКОбработке = НоваяТаблицаТоваровПоДиапазонамКОбработке();
	
	СтрокаТоваров = ТаблицаКОбработке.Добавить();
	СтрокаТоваров.Номенклатура = Номенклатура;
	СтрокаТоваров.Характеристика = Характеристика;
	
	ОбновитьДиапазоныСтрокТоваровПоТаблице(Объект, ТаблицаКОбработке);
	
КонецПроцедуры

// Параметры:
//  Объект - см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента.Объект
//         - см. Документ.УсловияРетроБонусовПоставщика.Форма.ФормаДокумента.Объект
//	ОбрабатываемаяТаблицаТоваров - ТаблицаЗначений:
//		* Номенклатура - СправочникСсылка.Номенклатура
//		* Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//		* ХарактеристикиИспользуются - Булево
//
Процедура ОбновитьДиапазоныСтрокТоваровПоТаблице(Объект, ОбрабатываемаяТаблицаТоваров) Экспорт
	
	Товары = Объект.Товары;
	ОбновитьОбозначениеДиапазонаСтрокТоваров(Товары, ОбрабатываемаяТаблицаТоваров);
	
КонецПроцедуры

// Параметры:
//  Товары - ДанныеФормыКоллекция, ТаблицаЗначений - Товары
//  ОбрабатываемаяТаблицаТоваров - ТаблицаЗначений - Обрабатываемая таблица товаров:
// * Номенклатура - СправочникСсылка.Номенклатура
// * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
// * ХарактеристикиИспользуются - Булево
// 
Процедура ОбновитьОбозначениеДиапазонаСтрокТоваров(Товары, ОбрабатываемаяТаблицаТоваров) Экспорт
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Номенклатура", Справочники.Номенклатура.ПустаяСсылка());
	ПараметрыОтбора.Вставить("Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	ПараметрыОтбора.Вставить("Отменено", Ложь);
	
	ОбновитьОбозначениеДиапазонаСтрок(Товары, ОбрабатываемаяТаблицаТоваров, ПараметрыОтбора);
	
КонецПроцедуры

// Параметры:
//  ОбъектПроверки - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика -
//  ПредставлениеТаблицы - Строка - Представление проверяемой табличной части
//  МенеджерВт - МенеджерВременныхТаблиц - 
//  Отказ - Булево - Отказ
//
Процедура ПроверитьТоварыНаличиеНачальногоДиапазона(ОбъектПроверки, ПредставлениеТаблицы, МенеджерВт, Отказ) Экспорт
	
	ИмяТаблицы = "Товары";
	ШаблонОшибки = НСтр("ru = 'Для %1 не указан начальный диапазон (от количества 0)';
						|en = 'For %1, the initial range (from 0 quantity) is not specified'");
	ТабличнаяЧасть = ОбъектПроверки[ИмяТаблицы]; // ДанныеФормыКоллекция, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Товары
	
	Выборка = ВыборкаТоваровБезНачальногоДиапазона(ТабличнаяЧасть, МенеджерВт);
	
	Пока Выборка.Следующий() Цикл
		
		ПредставлениеНоменклатуры= НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
			Выборка.Номенклатура,
			Выборка.Характеристика);
		
		ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, Выборка.НомерСтроки, "КоличествоПлан");
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			ПредставлениеНоменклатуры);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, ПутьКПолю,, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ОбъектПроверки - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика -
//  ПредставлениеТаблицы - Строка - Представление проверяемой табличной части
//  МенеджерВт - МенеджерВременныхТаблиц -
//  Отказ - Булево - Отказ
//
Процедура ПроверитьТоварыИдентичностьДанныхДиапазонов(ОбъектПроверки, ПредставлениеТаблицы, МенеджерВт, Отказ) Экспорт
	
	ИмяТаблицы = "Товары";
	ШаблонОшибкиНачалоПериода = НСтр("ru = 'Для %1 указаны разные периоды начало действия';
									|en = 'For %1, different validity start periods are specified'");
	ШаблонОшибкиОкончаниеПериода = НСтр("ru = 'Для %1 указаны разные периоды окончания действия';
										|en = 'For %1, different validity end periods are specified'");
	ШаблонОшибкиБазоваяЦена = НСтр("ru = 'Для %1 указаны разные базовые цены в действующих строках';
									|en = 'For %1, different base prices are specified in the active lines'");
	ТабличнаяЧасть = ОбъектПроверки[ИмяТаблицы]; // ТабличнаяЧасть
	
	//@skip-check invocation-parameter-type-intersect
	// составной тип
	Выборка = ВыборкаТоваровДиапазоновСРазнымиДанными(ТабличнаяЧасть, МенеджерВт);
	
	Пока Выборка.Следующий() Цикл
		
		ПредставлениеНоменклатуры= НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
			Выборка.Номенклатура,
			Выборка.Характеристика);
		
		Если Выборка.КоличествоНачалоДействия > 1 Тогда
			
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТаблицы, Выборка.НомерСтроки, "НачалоДействия");
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибкиНачалоПериода,
				ПредставлениеНоменклатуры);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, ПутьКПолю,, Отказ);
			
		КонецЕсли;
		
		Если Выборка.КоличествоОкончаниеДействия > 1 Тогда
			
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТаблицы, Выборка.НомерСтроки, "ОкончаниеДействия");
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибкиОкончаниеПериода,
				ПредставлениеНоменклатуры);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, ПутьКПолю,, Отказ);
			
		КонецЕсли;
		
		Если Выборка.КоличествоБазоваяЦена > 1 Тогда
			
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТаблицы, Выборка.НомерСтроки, "БазоваяЦена");
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибкиБазоваяЦена,
				ПредставлениеНоменклатуры);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, ПутьКПолю,, Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ОбъектПроверки - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика -
//  ПредставлениеТаблицы - Строка - Представление проверяемой табличной части
//  МенеджерВт - МенеджерВременныхТаблиц - 
//  Отказ - Булево - Отказ
//
Процедура ПроверитьТоварыНаличиеПроцентовБонуса(ОбъектПроверки, ПредставлениеТаблицы, МенеджерВт, Отказ) Экспорт
	
	ИмяТаблицы = "Товары";
	ШаблонОшибки = НСтр("ru = 'Для %1 не указано ни одного процента бонуса в строках';
						|en = 'For %1, no bonus percentage is specified in the lines'");
	ТабличнаяЧасть = ОбъектПроверки[ИмяТаблицы]; // ТабличнаяЧасть
	
	//@skip-check invocation-parameter-type-intersect
	// составной тип
	Выборка = ВыборкаТоваровБезПроцентовБонуса(ТабличнаяЧасть, МенеджерВт);
	
	Пока Выборка.Следующий() Цикл
		
		ПредставлениеНоменклатуры= НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
			Выборка.Номенклатура,
			Выборка.Характеристика);
		
		ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, Выборка.НомерСтроки, "Процент");
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			ПредставлениеНоменклатуры);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, ПутьКПолю,, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  Объект - см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента.Объект
//         - см. Документ.УсловияРетроБонусовПоставщика.Форма.ФормаДокумента.Объект
//  ТекущаяСтрокаИдентификатор - Число - Текущая строка идентификатор
//
Процедура ЗаполнитьЕдиныеДанныеДиапазонаСтрокиТовара(Объект, ТекущаяСтрокаИдентификатор, ТолькоПериоды = Ложь) Экспорт
	
	Если Объект.Товары.Количество() < 2 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТоваров = Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	ДанныеЗаполнены = НЕ СтрокаТоваров.Номенклатура.Пустая();
	
	Если ДанныеЗаполнены Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Номенклатура", СтрокаТоваров.Номенклатура);
		ПараметрыОтбора.Вставить("Характеристика", СтрокаТоваров.Характеристика);
		ПараметрыОтбора.Вставить("ХарактеристикиИспользуются", СтрокаТоваров.ХарактеристикиИспользуются);
		
		НайденныеСтроки = Объект.Товары.НайтиСтроки(ПараметрыОтбора);
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			
			Если НайденнаяСтрока.ПолучитьИдентификатор() = ТекущаяСтрокаИдентификатор Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаТоваров.НачалоДействия = НайденнаяСтрока.НачалоДействия;
			СтрокаТоваров.ОкончаниеДействия = НайденнаяСтрока.ОкончаниеДействия;
			Если НЕ ТолькоПериоды Тогда
				СтрокаТоваров.БазоваяЦена = НайденнаяСтрока.БазоваяЦена;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		СтрокаТоваров.НачалоДействия = Дата(1, 1, 1);
		СтрокаТоваров.ОкончаниеДействия = Дата(1, 1, 1);
		Если НЕ ТолькоПериоды Тогда
			СтрокаТоваров.БазоваяЦена = 0;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СвойстваПоДиапазонам

// Параметры:
//  Форма - см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента
//        - см. Документ.УсловияРетроБонусовПоставщика.Форма.ФормаДокумента
//
Процедура ИнициализироватьДиапазоныСвойств(Форма) Экспорт
	
	Если Форма.СоставТоваров <> Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	СвойстваНоменклатуры = Форма.Объект.СвойстваНоменклатуры;
	
	ИменаКолонок = "НомерСтроки, ЗначениеСвойства, КоличествоПлан, Отменено";
	ОбрабатываемаяТаблица = СвойстваНоменклатуры.Выгрузить(, ИменаКолонок);
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц();
	Выборка = ВыборкаСвойствБезНачальногоДиапазона(СвойстваНоменклатуры, МенеджерВТ, Истина);
	Пока Выборка.Следующий() Цикл
		
		СтрокаНачальногоДиапазона = СвойстваНоменклатуры.Вставить(Выборка.НомерСтроки - 1);
		СтрокаНачальногоДиапазона.ЗначениеСвойства = Выборка.ЗначениеСвойства;
		
	КонецЦикла;
	
	ИменаПолей = "ЗначениеСвойства";
	ОбрабатываемаяТаблица.Свернуть(ИменаПолей);
	
	ОбновитьДиапазоныСтрокСвойствПоТаблице(Объект, ОбрабатываемаяТаблица);
	
КонецПроцедуры

// Параметры:
//  Объект - см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента.Объект
//         - см. Документ.УсловияРетроБонусовПоставщика.Форма.ФормаДокумента.Объект
//  ПоказательТоваров - ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам
//
Процедура ОбновитьДиапазоныСтрокВсехСвойств(Объект, ПоказательТоваров) Экспорт
	
	Если НЕ РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(Объект.ВидШкалыРасчета, ПоказательТоваров) Тогда
		Возврат;
	КонецЕсли;
	
	ИменаПолей = "ЗначениеСвойства";
	ТаблицаКОбработке = Объект.СвойстваНоменклатуры.Выгрузить(, ИменаПолей);
	ТаблицаКОбработке.Свернуть(ИменаПолей);
	ОбновитьДиапазоныСтрокСвойствПоТаблице(Объект, ТаблицаКОбработке);
	
КонецПроцедуры

// Параметры:
//  Объект - см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента.Объект
//         - см. Документ.УсловияРетроБонусовПоставщика.Форма.ФормаДокумента.Объект
//	ЗначениеСвойства - ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
//
Процедура ОбновитьДиапазоныСтрокСвойствПоСвойству(Объект, ЗначениеСвойства) Экспорт
	
	ТаблицаКОбработке = НоваяТаблицаСвойствПоДиапазонамКОбработке();
	
	СтрокаСвойства = ТаблицаКОбработке.Добавить();
	СтрокаСвойства.ЗначениеСвойства = ЗначениеСвойства;
	
	ОбновитьДиапазоныСтрокСвойствПоТаблице(Объект, ТаблицаКОбработке);
	
КонецПроцедуры

// Параметры:
//  Объект - см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента.Объект
//         - см. Документ.УсловияРетроБонусовПоставщика.Форма.ФормаДокумента.Объект
//	ОбрабатываемаяТаблица - ТаблицаЗначений:
//		* ЗначениеСвойства - ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
//
Процедура ОбновитьДиапазоныСтрокСвойствПоТаблице(Объект, ОбрабатываемаяТаблица) Экспорт
	
	Товары = Объект.СвойстваНоменклатуры;
	ОбозначениеДиапазонаСтрокСвойств(Товары, ОбрабатываемаяТаблица);
	
КонецПроцедуры

// Параметры:
//  СвойстваНоменклатуры - ДанныеФормыКоллекция, ТаблицаЗначений - Свойства
//  ОбрабатываемаяТаблица - ТаблицаЗначений - Обрабатываемая таблица свойств:
// * ЗначениеСвойства - ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
// 
Процедура ОбозначениеДиапазонаСтрокСвойств(СвойстваНоменклатуры, ОбрабатываемаяТаблица) Экспорт
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ЗначениеСвойства", Неопределено);
	ПараметрыОтбора.Вставить("Отменено", Ложь);
	
	ОбновитьОбозначениеДиапазонаСтрок(СвойстваНоменклатуры, ОбрабатываемаяТаблица, ПараметрыОтбора);
	
КонецПроцедуры

// Параметры:
//  ОбъектПроверки - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика -
//  ПредставлениеТаблицы - Строка - Представление проверяемой табличной части
//  МенеджерВт - МенеджерВременныхТаблиц - 
//  Отказ - Булево - Отказ
//
Процедура ПроверитьСвойстваНоменклатурыНаличиеНачальногоДиапазона(ОбъектПроверки, ПредставлениеТаблицы, МенеджерВт, Отказ) Экспорт
	
	ИмяТаблицы = "СвойстваНоменклатуры";
	ШаблонОшибки = НСтр("ru = 'Для %1 не указан начальный диапазон (от количества 0)';
						|en = 'For %1, the initial range (from 0 quantity) is not specified'");
	ТабличнаяЧасть = ОбъектПроверки[ИмяТаблицы]; // ДанныеФормыКоллекция, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры
	
	Выборка = ВыборкаСвойствБезНачальногоДиапазона(ТабличнаяЧасть, МенеджерВт);
	
	Пока Выборка.Следующий() Цикл
		
		ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, Выборка.НомерСтроки, "КоличествоПлан");
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			СокрЛП(Выборка.ЗначениеСвойства));
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, ПутьКПолю,, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ОбъектПроверки - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика -
//  ПредставлениеТаблицы - Строка - Представление проверяемой табличной части
//  МенеджерВт - МенеджерВременныхТаблиц -
//  Отказ - Булево - Отказ
//
Процедура ПроверитьСвойстваНоменклатурыИдентичностьДанныхДиапазонов(ОбъектПроверки, ПредставлениеТаблицы, МенеджерВт, Отказ) Экспорт
	
	ИмяТаблицы = "СвойстваНоменклатуры";
	ШаблонОшибкиНачалоПериода = НСтр("ru = 'Для %1 указаны разные периоды начало действия';
									|en = 'For %1, different validity start periods are specified'");
	ШаблонОшибкиОкончаниеПериода = НСтр("ru = 'Для %1 указаны разные периоды окончания действия';
										|en = 'For %1, different validity end periods are specified'");
	ШаблонОшибкиБазоваяЦена = НСтр("ru = 'Для %1 указаны разные базовые цены в действующих строках';
									|en = 'For %1, different base prices are specified in active lines'");
	ТабличнаяЧасть = ОбъектПроверки[ИмяТаблицы]; // ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры
	
	Выборка = ВыборкаСвойствНоменклатурыДиапазоновСРазнымиДанными(ТабличнаяЧасть, МенеджерВт);
	
	Пока Выборка.Следующий() Цикл
		
		ПредставлениеПоля = СокрЛП(Выборка.ЗначениеСвойства);
		
		Если Выборка.КоличествоНачалоДействия > 1 Тогда
			
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТаблицы, Выборка.НомерСтроки, "НачалоДействия");
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибкиНачалоПериода,
				ПредставлениеПоля);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, ПутьКПолю,, Отказ);
			
		КонецЕсли;
		
		Если Выборка.КоличествоОкончаниеДействия > 1 Тогда
			
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТаблицы, Выборка.НомерСтроки, "ОкончаниеДействия");
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибкиОкончаниеПериода,
				ПредставлениеПоля);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, ПутьКПолю,, Отказ);
			
		КонецЕсли;
		
		Если Выборка.КоличествоБазоваяЦена > 1 Тогда
			
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТаблицы, Выборка.НомерСтроки, "БазоваяЦена");
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибкиБазоваяЦена,
				ПредставлениеПоля);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, ПутьКПолю,, Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ОбъектПроверки - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика -
//  ПредставлениеТаблицы - Строка - Представление проверяемой табличной части
//  МенеджерВт - МенеджерВременныхТаблиц - 
//  Отказ - Булево - Отказ
//
Процедура ПроверитьСвойстваНоменклатурыНаличиеПроцентовБонуса(ОбъектПроверки, ПредставлениеТаблицы, МенеджерВт, Отказ) Экспорт
	
	ИмяТаблицы = "Товары";
	ШаблонОшибки = НСтр("ru = 'Для %1 не указано ни одного процента бонуса в строках';
						|en = 'For %1, no rebate percentage is specified in the lines'");
	ТабличнаяЧасть = ОбъектПроверки[ИмяТаблицы]; // ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры
	
	Выборка = ВыборкаСвойствНоменклатурыБезПроцентовБонуса(ТабличнаяЧасть, МенеджерВт);
	
	Пока Выборка.Следующий() Цикл
		
		ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, Выборка.НомерСтроки, "Процент");
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			СокрЛП(Выборка.ЗначениеСвойства));
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОбъектПроверки, ПутьКПолю,, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  Объект - см. Документ.УсловияРетроБонусовКлиентов.Форма.ФормаДокумента.Объект
//         - см. Документ.УсловияРетроБонусовПоставщика.Форма.ФормаДокумента.Объект
//  ТекущаяСтрокаИдентификатор - Число - Текущая строка идентификатор
//  ТолькоПериоды - Булево -
//
Процедура ЗаполнитьЕдиныеДанныеДиапазонаСтрокиСвойства(Объект, ТекущаяСтрокаИдентификатор, ТолькоПериоды = Ложь) Экспорт
	
	Если Объект.СвойстваНоменклатуры.Количество() < 2 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаСвойства = Объект.СвойстваНоменклатуры.НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	
	ДанныеЗаполнены = ЗначениеЗаполнено(СтрокаСвойства.ЗначениеСвойства);
	
	Если ДанныеЗаполнены Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ЗначениеСвойства", СтрокаСвойства.ЗначениеСвойства);
		
		НайденныеСтроки = Объект.СвойстваНоменклатуры.НайтиСтроки(ПараметрыОтбора);
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			
			Если НайденнаяСтрока.ПолучитьИдентификатор() = ТекущаяСтрокаИдентификатор Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаСвойства.НачалоДействия = НайденнаяСтрока.НачалоДействия;
			СтрокаСвойства.ОкончаниеДействия = НайденнаяСтрока.ОкончаниеДействия;
			Если НЕ ТолькоПериоды Тогда
				СтрокаСвойства.БазоваяЦена = НайденнаяСтрока.БазоваяЦена;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		СтрокаСвойства.НачалоДействия = Дата(1, 1, 1);
		СтрокаСвойства.ОкончаниеДействия = Дата(1, 1, 1);
		Если НЕ ТолькоПериоды Тогда
			СтрокаСвойства.БазоваяЦена = 0;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаПериодовСтрок

// Проверить изменения периодов таблицы.
// 
// Параметры:
//  НачалоДействия - Дата
//  ОкончаниеДействия - Дата
//  МаксимальнаяДата - Дата
//  ИмяТаблицы - Строка
//  ПредставлениеТаблицы - Строка
//  СтрокаТаблицы - ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.КонтрагентыКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.ИННКонтрагентовКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Поставщики, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Контрагенты, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ДоговорыСоглашения, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры - Текущая строка таблицы
//  Отказ - Булево
//
Процедура ПроверитьИзмененияПериодовТаблицы(Знач НачалоДействия, Знач ОкончаниеДействия, Знач МаксимальнаяДата, Знач ИмяТаблицы, Знач ПредставлениеТаблицы, СтрокаТаблицы, Отказ) Экспорт
	
	ПустаяДата = Дата(1, 1, 1);
	
	Если СтрокаТаблицы.НачалоДействия = ПустаяДата Тогда
		ТекущаяДатаНачала = НачалоДействия;
	Иначе
		ТекущаяДатаНачала = СтрокаТаблицы.НачалоДействия;
	КонецЕсли;
	
	Если СтрокаТаблицы.ОкончаниеДействия = ПустаяДата Тогда
		ТекущаяДатаОкончания = ОкончаниеДействия;
	Иначе
		ТекущаяДатаОкончания = СтрокаТаблицы.ОкончаниеДействия;
	КонецЕсли;
	
	ПроверитьДопустимостьНачалаПериодаСтроки(
		МаксимальнаяДата,
		ИмяТаблицы,
		ПредставлениеТаблицы,
		ТекущаяДатаНачала,
		СтрокаТаблицы,
		Отказ);
	
	ПроверитьИзменениеНачалоПериодаИсходнойСтроки(
		МаксимальнаяДата,
		ИмяТаблицы,
		ПредставлениеТаблицы,
		ТекущаяДатаНачала,
		СтрокаТаблицы,
		Отказ);
	
	ПроверитьДопустимостьНачалаПериодаИсходнойСтроки(
		МаксимальнаяДата,
		ИмяТаблицы,
		ПредставлениеТаблицы,
		ТекущаяДатаНачала,
		СтрокаТаблицы,
		Отказ);
	
	ПроверитьДопустимостьОкончанияПериодаСтроки(
		МаксимальнаяДата,
		ИмяТаблицы,
		ПредставлениеТаблицы,
		ТекущаяДатаОкончания,
		СтрокаТаблицы,
		Отказ);
	
	ПроверитьИзменениеОкончанияПериодаИсходнойСтроки(
		МаксимальнаяДата,
		ИмяТаблицы,
		ПредставлениеТаблицы,
		ТекущаяДатаОкончания,
		СтрокаТаблицы,
		Отказ);
	
	ПроверитьДопустимостьИзменениеОкончанияПериодаИсходнойСтроки(
		МаксимальнаяДата,
		ИмяТаблицы,
		ПредставлениеТаблицы,
		ТекущаяДатаОкончания,
		СтрокаТаблицы,
		Отказ);
	
КонецПроцедуры

// Проверка наличия изменений, в таблицах без периода действия запрещенных к изменению
// 
// Параметры:
//  Объект - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика -
//  СоставСписка - ПеречислениеСсылка.СоставыСписковРетроБонусов
//  ИмяТаблицы - Строка
//  ПредставлениеТаблицы - Строка - Представление проверяемой табличной части
//  Отказ - Булево
//
Процедура ПроверитьИзмененияУсловийВТаблицеБезПериодовДействия(Объект, СоставСписка, ИмяТаблицы, ПредставлениеТаблицы, Отказ) Экспорт
	
	Если СоставСписка = Перечисления.СоставыСписковРетроБонусов.Все Тогда
		Возврат;
	КонецЕсли;
	
	Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" запрещены изменения, так как в период действия условия были введены начисления';
					|en = 'Changes are disabled in line %1 of the ""%2"" list as accruals were entered during the condition validity period'");
	
	ТаблицаДанных = Объект[ИмяТаблицы]; // ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Контрагенты, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.КонтрагентыКлиентов, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.ИННКонтрагентовКлиентов, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Поставщики
	
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		
		БылиКорректировки = Ложь;
			
		Если НЕ СтрокаДанных.ИсходнаяСтрока
		 ИЛИ СтрокаДанных.Отменено Тогда
			
			БылиКорректировки = Истина;
			
		КонецЕсли;
		
		Если БылиКорректировки Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Шаблон,
				Строка(СтрокаДанных.НомерСтроки),
				ПредставлениеТаблицы);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				ИмяТаблицы, СтрокаДанных.НомерСтроки, "НомерСтроки");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура - Новые параметры проверки условий в таблице:
// * Объект - ДокументОбъект.УсловияРетроБонусовКлиентов, ДокументОбъект.УсловияРетроБонусовПоставщика -
// * ИмяТаблицы - Строка - 
// * ПредставлениеТаблицы - Строка - 
// * НачалоДействия - Дата - 
// * ОкончаниеДействия - Дата - 
// * МаксимальнаяДата - Дата - 
// * ПроверятьСуммуПлана - Булево - 
// * СуммаПлана - Число - используется при ПроверятьСуммуПлана
// * СоставСписка - ПеречислениеСсылка.СоставыСписковРетроБонусов - 
// * ВыполнятьПроверкуПериодовВТаблицах - Булево -
// 
Функция НовыеПараметрыПроверкиУсловийВТаблице() Экспорт
	
	ПустаяДата = Дата(1, 1, 1);
	
	Результат = Новый Структура;
	
	Результат.Вставить("Объект", Неопределено);
	Результат.Вставить("ИмяТаблицы", "");
	Результат.Вставить("ПредставлениеТаблицы", "");
	Результат.Вставить("НачалоДействия", ПустаяДата);
	Результат.Вставить("ОкончаниеДействия", ПустаяДата);
	Результат.Вставить("МаксимальнаяДата", ПустаяДата);
	Результат.Вставить("ПроверятьСуммуПлана", Ложь);
	Результат.Вставить("СуммаПлана", 0);
	Результат.Вставить("СоставСписка", Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка());
	Результат.Вставить("ВыполнятьПроверкуПериодовВТаблицах", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Проверка корректности внесенных изменения по отношению к периоду начислений
// 
// Параметры:
//  ПараметрыПроверки - см. НовыеПараметрыПроверкиУсловийВТаблице
//  Отказ - Булево
//
Процедура ПроверитьИзмененияУсловийВТаблице(ПараметрыПроверки, Отказ)Экспорт
	
	ИмяТаблицы = ПараметрыПроверки.ИмяТаблицы;
	ПредставлениеТаблицы = ПараметрыПроверки.ПредставлениеТаблицы;
	НачалоДействия = ПараметрыПроверки.НачалоДействия;
	ОкончаниеДействия = ПараметрыПроверки.ОкончаниеДействия;
	СоставСписка = ПараметрыПроверки.СоставСписка;
	
	Если СоставСписка = Перечисления.СоставыСписковРетроБонусов.Все
	 ИЛИ СоставСписка = Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка()
	 ИЛИ (НЕ ПараметрыПроверки.ВыполнятьПроверкуПериодовВТаблицах
	      И СоставСписка = Перечисления.СоставыСписковРетроБонусов.Выбранные) Тогда
		Возврат;
	КонецЕсли;
	
	ПустаяДата = Дата(1, 1, 1);
	
	ТабличнаяЧастьОбъекта = ПараметрыПроверки.Объект[ИмяТаблицы]; // ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Контрагенты, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.КонтрагентыКлиентов, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.ИННКонтрагентовКлиентов, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Поставщики 
	Для Каждого ТекущаяСтрокаТаблицы Из ТабличнаяЧастьОбъекта Цикл
		
		Если СоставСписка = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
			
			ПроверитьИзмененияПериодовТаблицы(
				НачалоДействия,
				ОкончаниеДействия,
				ПараметрыПроверки.МаксимальнаяДата,
				ИмяТаблицы,
				ПредставлениеТаблицы,
				ТекущаяСтрокаТаблицы,
				Отказ);
			
			Если ТекущаяСтрокаТаблицы.Отменено
			   И ТекущаяСтрокаТаблицы.НачалоДействияДоИзменений <= ПараметрыПроверки.МаксимальнаяДата Тогда
				
				Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" отмена невозможна. В период действия условия были зарегистрированы начисления';
								|en = 'Cannot cancel in line %1 of the ""%2"" list. Accruals were registered during the condition validity period'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Шаблон,
					Строка(ТекущаяСтрокаТаблицы.НомерСтроки),
					ПредставлениеТаблицы);
				
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					ИмяТаблицы, ТекущаяСтрокаТаблицы.НомерСтроки, "НомерСтроки");
					
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
				
			КонецЕсли;
			
			Если ПараметрыПроверки.ПроверятьСуммуПлана Тогда
				
				Если ТекущаяСтрокаТаблицы.СуммаПлан = 0 Тогда
					СуммаПланТекущая = ПараметрыПроверки.СуммаПлана;
				Иначе
					СуммаПланТекущая = ТекущаяСтрокаТаблицы.СуммаПлан;
				КонецЕсли;
				
				Если ТекущаяСтрокаТаблицы.ИсходнаяСтрока
				   И СуммаПланТекущая <> ТекущаяСтрокаТаблицы.СуммаПланДоИзменений
				   И ТекущаяСтрокаТаблицы.НачалоДействияДоИзменений <= ПараметрыПроверки.МаксимальнаяДата Тогда
					
					Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" сумму плана менять запрещено. В период действия условия были зарегистрированы начисления';
									|en = 'Cannot change the plan amount in line %1 of the ""%2"" list. Accruals were registered during the condition validity period'");
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						Шаблон,
						Строка(ТекущаяСтрокаТаблицы.НомерСтроки),
						ПредставлениеТаблицы);
					
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
						ИмяТаблицы, ТекущаяСтрокаТаблицы.НомерСтроки, "СуммаПлан");
						
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			БылиКорректировки = Ложь;
			
			Если НЕ ТекущаяСтрокаТаблицы.ИсходнаяСтрока
			 ИЛИ ТекущаяСтрокаТаблицы.Отменено
			 ИЛИ ТекущаяСтрокаТаблицы.ОкончаниеДействия <> ПустаяДата
			 ИЛИ ТекущаяСтрокаТаблицы.НачалоДействия <> ПустаяДата Тогда
				
				БылиКорректировки = Истина;
				
			КонецЕсли;
			
			Если ПараметрыПроверки.ПроверятьСуммуПлана
			   И ТекущаяСтрокаТаблицы.СуммаПлан <> 0
			   И ТекущаяСтрокаТаблицы.СуммаПлан <> ТекущаяСтрокаТаблицы.СуммаПланДоИзменений Тогда
				
				БылиКорректировки = Истина;
				
			КонецЕсли;
			
			Если БылиКорректировки Тогда
				
				Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" запрещены изменения, так как в период действия условия были введены начисления';
								|en = 'Changes are disabled in line %1 of the ""%2"" list as accruals were entered during the condition validity period'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Шаблон,
					Строка(ТекущаяСтрокаТаблицы.НомерСтроки),
					ПредставлениеТаблицы);
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					ИмяТаблицы, ТекущаяСтрокаТаблицы.НомерСтроки, "НомерСтроки");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ОчиститьРегистрыАктуальныхДанных(ДокументСсылка, ИменаРегистров)
	
	Для Каждого ИмяРегистра Из ИменаРегистров Цикл
		
		НаборЗаписей = РегистрыСведений[ИмяРегистра].СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ДокументУсловий.Установить(ДокументСсылка);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьРегистрыАктуальныхДанных(ДокументРегистратор, ДокументОснование, ИменаРегистров)
	
	СтрокаРегистров = СтрСоединить(ИменаРегистров, ",");
	
	ТаблицыДляДвижений = ПроведениеДокументов.ДанныеДокументаДляПроведения(ДокументРегистратор, СтрокаРегистров);
	Для Каждого ИмяРегистра Из ИменаРегистров Цикл
		
		ИмяТаблицы = "Таблица" + ИмяРегистра;
		
		НаборЗаписей = РегистрыСведений[ИмяРегистра].СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ДокументУсловий.Установить(ДокументОснование);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();
		
		// Можем не готовить таблицу, если по настройкам она не используется, но очистить записи требуется
		Если ТаблицыДляДвижений.Свойство(ИмяТаблицы) Тогда
			
			ТаблицаДанных = ТаблицыДляДвижений[ИмяТаблицы]; // ТаблицаЗначений
			НаборЗаписей.Загрузить(ТаблицаДанных);
			
		КонецЕсли;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// см. ИнициализироватьДанныеКонтроляИзменений
Процедура ИнициализацияКонтроляИзмененийРетроБонусыКлиентов(Запрос, ТекстыЗапроса, Документ) Экспорт
	
	ЕстьИзмененияДвиженийКОформлению = ПроведениеДокументов.ЕстьЗаписиВТаблице(
		Документ, "ДвиженияРетроБонусыКлиентовИзменение");
	
	Если НЕ ЕстьИзмененияДвиженийКОформлению Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДатаКонтроляПосле", Новый Граница(КонецДня(Документ.Дата), ВидГраницы.Включая));
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИСТИНА КАК КонтрольНаДатуДокумента,
	|	РетроБонусыКлиентовОстатки.Организация КАК Организация,
	|	РетроБонусыКлиентовОстатки.Контрагент КАК Контрагент,
	|	РетроБонусыКлиентовОстатки.Партнер КАК Партнер,
	|	РетроБонусыКлиентовОстатки.НачалоПериода КАК НачалоПериода,
	|	РетроБонусыКлиентовОстатки.ОкончаниеПериода КАК ОкончаниеПериода,
	|	РетроБонусыКлиентовОстатки.ДокументУсловий КАК ДокументУсловий,
	|	РетроБонусыКлиентовОстатки.Валюта КАК Валюта,
	|	РетроБонусыКлиентовОстатки.СуммаБонусОстаток - РетроБонусыКлиентовОстатки.КАктированиюОстаток - РетроБонусыКлиентовОстатки.КПодписаниюОстаток - РетроБонусыКлиентовОстатки.КСписаниюОстаток КАК СуммаБонусОстаток
	|ИЗ
	|	РегистрНакопления.РетроБонусыКлиентов.Остатки(&ДатаКонтроляПосле, (Организация, Контрагент, Партнер, НачалоПериода,	ОкончаниеПериода, ДокументУсловий, Валюта) В
	|		(ВЫБРАТЬ
	|			Таблица.Организация,
	|			Таблица.Контрагент,
	|			Таблица.Партнер,
	|			Таблица.НачалоПериода,
	|			Таблица.ОкончаниеПериода,
	|			Таблица.ДокументУсловий,
	|			Таблица.Валюта
	|		ИЗ
	|			ДвиженияРетроБонусыКлиентовИзменение КАК Таблица)) КАК РетроБонусыКлиентовОстатки
	|ГДЕ
	|	РетроБонусыКлиентовОстатки.СуммаБонусОстаток - РетроБонусыКлиентовОстатки.КАктированиюОстаток - РетроБонусыКлиентовОстатки.КПодписаниюОстаток - РетроБонусыКлиентовОстатки.КСписаниюОстаток < 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ЛОЖЬ КАК КонтрольНаДатуДокумента,
	|	РетроБонусыКлиентовОстатки.Организация КАК Организация,
	|	РетроБонусыКлиентовОстатки.Контрагент КАК Контрагент,
	|	РетроБонусыКлиентовОстатки.Партнер КАК Партнер,
	|	РетроБонусыКлиентовОстатки.НачалоПериода КАК НачалоПериода,
	|	РетроБонусыКлиентовОстатки.ОкончаниеПериода КАК ОкончаниеПериода,
	|	РетроБонусыКлиентовОстатки.ДокументУсловий КАК ДокументУсловий,
	|	РетроБонусыКлиентовОстатки.Валюта КАК Валюта,
	|	РетроБонусыКлиентовОстатки.СуммаБонусОстаток - РетроБонусыКлиентовОстатки.КАктированиюОстаток - РетроБонусыКлиентовОстатки.КПодписаниюОстаток - РетроБонусыКлиентовОстатки.КСписаниюОстаток КАК СуммаБонусОстаток
	|ИЗ
	|	РегистрНакопления.РетроБонусыКлиентов.Остатки(, (Организация, Контрагент, Партнер, НачалоПериода, ОкончаниеПериода,	ДокументУсловий, Валюта) В
	|		(ВЫБРАТЬ
	|			Таблица.Организация,
	|			Таблица.Контрагент,
	|			Таблица.Партнер,
	|			Таблица.НачалоПериода,
	|			Таблица.ОкончаниеПериода,
	|			Таблица.ДокументУсловий,
	|			Таблица.Валюта
	|		ИЗ
	|			ДвиженияРетроБонусыКлиентовИзменение КАК Таблица)) КАК РетроБонусыКлиентовОстатки
	|ГДЕ
	|	РетроБонусыКлиентовОстатки.СуммаБонусОстаток - РетроБонусыКлиентовОстатки.КАктированиюОстаток - РетроБонусыКлиентовОстатки.КПодписаниюОстаток - РетроБонусыКлиентовОстатки.КСписаниюОстаток < 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ОшибкиСписанияСуммБонусов");
	
КонецПроцедуры

// см. СообщитьОРезультатахКонтроляИзменений
Процедура КонтрольИзмененийРетроБонусыКлиентов(РезультатыКонтроля, Документ, Отказ)
	
	Если НЕ ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "ДвиженияРетроБонусыКлиентовИзменение") Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонСообщенияНаДатуДокумента = НСтр("ru = 'Недостаточно суммы бонуса за период с %1 по %2 по документу ""%3"", контрагенту ""%4"" и партнеру ""%5"" на дату документа.
										  |Превышение: %6 %7';
										  |en = 'The bonus amount is insufficient for the period from %1 to %2 for the ""%3"" document, the ""%4"" counterparty, and the ""%5"" partner as of the document date.
										  |Exceeded: %6%7'");
	
	ШаблонСообщенияОбщий = НСтр("ru = 'Недостаточно суммы бонуса за период с %1 по %2 по документу ""%3"", контрагенту ""%4"" и партнеру ""%5"".
								|Превышение: %6 %7';
								|en = 'The bonus amount is insufficient for the period from %1 to %2 for the ""%3"" document, the ""%4"" counterparty, and the ""%5"" partner.
								|Exceeded: %6%7'");
	
	Для каждого СтрокаОшибки Из РезультатыКонтроля.ОшибкиСписанияСуммБонусов Цикл
		
		КонтрольНаДатуДокумента = СтрокаОшибки.КонтрольНаДатуДокумента;
		СуммаПревышения = Формат(- СтрокаОшибки.СуммаБонусОстаток, "ЧДЦ=2;");
		НачалоПериода = Формат(СтрокаОшибки.НачалоПериода, "ДЛФ=D;");
		ОкончаниеПериода = Формат(СтрокаОшибки.ОкончаниеПериода, "ДЛФ=D;");
		ДокументУсловий = Строка(СтрокаОшибки.ДокументУсловий);
		Валюта = Строка(СтрокаОшибки.Валюта);
		Партнер = Строка(СтрокаОшибки.Партнер);
		Контрагент = Строка(СтрокаОшибки.Контрагент);
		
		Шаблон = ШаблонСообщенияОбщий;
		Если КонтрольНаДатуДокумента Тогда
			
			Шаблон = ШаблонСообщенияНаДатуДокумента;
			
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Шаблон,
			НачалоПериода,
			ОкончаниеПериода,
			ДокументУсловий,
			Контрагент,
			Партнер,
			СуммаПревышения,
			Валюта);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Документ,,, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеДоступом

// Параметры:
//  ОписаниеПрофиля - см. УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа
//
Процедура ДополнитьПрофильРолямиЧтенияВидовРетроБонусовКлиентов(ОписаниеПрофиля)
	
	ОписаниеПрофиля.Роли.Добавить("ПодсистемаCRMИМаркетинг");
	ОписаниеПрофиля.Роли.Добавить("РазделCRMИМаркетингРетроБонусыКлиентов");
	
	ОписаниеПрофиля.Роли.Добавить("ЧтениеВидовРетроБонусовКлиентов");
	
КонецПроцедуры

Процедура ДобавитьПрофильОтветственныйВидыРетроБонусовКлиентов(ОписанияПрофилей, ПараметрыОбновления)
	
	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя = "ОтветственныйВидыРетроБонусовКлиентов";
	ОписаниеПрофиля.Идентификатор = "01f05bc6-7524-42db-b87f-c303d0ac3349";
	ОписаниеПрофиля.Родитель = "АдминистрированиеИНСИ";
	ОписаниеПрофиля.Наименование =
		НСтр("ru = 'Ответственный за параметры ретро-бонусов клиентов (дополнительный)';
			|en = 'Person responsible for customer rebate parameters (additional)'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Описание =
		НСтр("ru = 'Профиль предоставляет права на изменение видов ретро-бонусов клиентов.
			 |Должен быть назначен в дополнение к любому другому профилю с правом входа в систему.';
			 |en = 'The profile grants the right to change customer rebate kinds.
			 |It must be assigned in addition to any other profile with authorization rights.'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	
	ДополнитьПрофильРолямиЧтенияВидовРетроБонусовКлиентов(ОписаниеПрофиля);
	
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеВидовРетроБонусовКлиентов");
	
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
КонецПроцедуры

Процедура ДобавитьПрофильМенеджерРетроБонусовКлиентов(ОписанияПрофилей, ПараметрыОбновления)
	
	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя = "МенеджерРетроБонусовКлиентов";
	ОписаниеПрофиля.Идентификатор = "44ec2720-e674-40fa-aec7-04fd4f3f6c6f";
	ОписаниеПрофиля.Родитель = "Продажи";
	ОписаниеПрофиля.Наименование =
		НСтр("ru = 'Менеджер ретро-бонусов клиентов (дополнительный)';
			|en = 'Manager of customer rebates (additional)'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Описание =
		НСтр("ru = 'Профиль предоставляет права ведения условий, начислений, списаний ретро-бонусов клиентов.
			 |Должен быть назначен в дополнение к профилю менеджера продаж или руководителя отдела продаж.';
			 |en = 'The profile grants the right to maintain conditions, accruals, and write-offs of customer rebates.
			 |It must be assigned in addition to the sales representative profile or the sales manager profile.'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	
	ДополнитьПрофильРолямиЧтенияВидовРетроБонусовКлиентов(ОписаниеПрофиля);
	
	ОписаниеПрофиля.Роли.Добавить("ЧтениеИнформацииПоПартнерам");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеБанковскихСчетовКонтрагентов");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеНормативноСправочнойИнформации");
	
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеУсловийРетроБонусовКлиентов");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеНачисленийРетроБонусовКлиентов");
	ОписаниеПрофиля.Роли.Добавить("ИспользованиеОбработкиГрупповоеНачислениеРетроБонусовКлиентов");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеСписанийРетроБонусовКлиентов");
	
	ОписаниеПрофиля.Роли.Добавить("ПросмотрОтчетаВедомостьПоРетроБонусамКлиентов");
	ОписаниеПрофиля.Роли.Добавить("ПросмотрОтчетаРасчетРетроБонусовКлиентов");
	
	ОписаниеПрофиля.ВидыДоступа.Добавить("Организации", "ВначалеВсеРазрешены");
	
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
КонецПроцедуры

Процедура ДобавитьПрофильУчастникСогласованияУсловийРетроБонусовКлиентов(ОписанияПрофилей, ПараметрыОбновления)
	
	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя = "УчастникСогласованияУсловийРетроБонусовКлиентов";
	ОписаниеПрофиля.Идентификатор = "a9d6dc14-4886-4d20-b30a-f8ae3c2956c4";
	ОписаниеПрофиля.Родитель = "Продажи";
	ОписаниеПрофиля.Наименование =
		НСтр("ru = 'Участник согласования условий ретро-бонусов клиентов (дополнительный)';
			|en = 'Employee approving conditions of customer rebates (additional)'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Описание =
		НСтр("ru = 'Профиль предоставляет право непосредственного согласования документ условий ретро-бонусов клиентов.
			 |Должен быть назначен в дополнение к профилю ""Менеджер ретро-бонусов клиентов (дополнительный)""';
			 |en = 'The profile grants the right to approve the customer rebate conditions document.
			 |It must be assigned in addition to the ""Manager of customer rebates (additional)"" profile'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	
	ОписаниеПрофиля.Роли.Добавить("ПравоСогласованияУсловийРетроБонусовКлиентов");
	
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
КонецПроцедуры

Процедура ДобавитьПрофильОтветственныйАктыПремийКлиентамДополнительный(ОписанияПрофилей, ПараметрыОбновления)
	
	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя = "ОтветственныйАктыПремийКлиентамДополнительный";
	ОписаниеПрофиля.Идентификатор = "852ffecd-ef3c-4be8-ab91-05c17c2645ba";
	ОписаниеПрофиля.Родитель = "Продажи";
	ОписаниеПрофиля.Наименование =
		НСтр("ru = 'Ответственный за акты премий клиентам (дополнительный)';
			|en = 'Person responsible for customer bonus certificates (additional)'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Описание =
		НСтр("ru = 'Профиль предоставляет право оформления актов премий клиентам.
			 |Должен быть назначен в дополнение к профилю ""Менеджер ретро-бонусов клиентов (дополнительный)""';
			 |en = 'The profile grants the right to issue customer bonus certificates.
			 |It must be assigned in addition to the ""Manager of customer rebates (additional)"" profile'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеАктПремииКлиенту");
	
	ОписаниеПрофиля.ВидыДоступа.Добавить("Организации", "ВначалеВсеРазрешены");
	
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
КонецПроцедуры

// Параметры:
//  ОписаниеПрофиля - см. УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа
//
Процедура ДополнитьПрофильРолямиЧтенияВидовРетроБонусовПоставщиков(ОписаниеПрофиля)
	
	ОписаниеПрофиля.Роли.Добавить("ПодсистемаЗакупки");
	ОписаниеПрофиля.Роли.Добавить("РазделЗакупкиРетроБонусыПоставщиков");
	
	ОписаниеПрофиля.Роли.Добавить("ЧтениеВидовРетроБонусовПоставщиков");
	
КонецПроцедуры

Процедура ДобавитьПрофильОтветственныйВидыРетроБонусовПоставщиков(ОписанияПрофилей, ПараметрыОбновления)
	
	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя = "ОтветственныйВидыРетроБонусовПоставщиков";
	ОписаниеПрофиля.Идентификатор = "830073fa-d91f-4abd-a4cf-16bcd4edf8fc";
	ОписаниеПрофиля.Родитель = "АдминистрированиеИНСИ";
	ОписаниеПрофиля.Наименование =
		НСтр("ru = 'Ответственный за параметры ретро-бонусов поставщиков (дополнительный)';
			|en = 'Person responsible for vendor rebate parameters (additional)'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Описание =
		НСтр("ru = 'Профиль предоставляет права на изменение видов ретро-бонусов поставщиков.
			 |Должен быть назначен в дополнение к любому другому профилю с правом входа в систему.';
			 |en = 'The profile grants the right to change vendor rebate kinds.
			 |It must be assigned in addition to any other profile with authorization rights.'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	
	ДополнитьПрофильРолямиЧтенияВидовРетроБонусовПоставщиков(ОписаниеПрофиля);
	
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеВидовРетроБонусовПоставщиков");
	
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
КонецПроцедуры

Процедура ДобавитьПрофильМенеджерРетроБонусовПоставщиков(ОписанияПрофилей, ПараметрыОбновления)
	
	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя = "МенеджерРетроБонусовПоставщиков";
	ОписаниеПрофиля.Идентификатор = "985bb178-f7df-4f9d-bf19-f61d5449a9c7";
	ОписаниеПрофиля.Родитель = "ЗакупкиИЗапасы";
	ОписаниеПрофиля.Наименование =
		НСтр("ru = 'Менеджер ретро-бонусов поставщиков (дополнительный)';
			|en = 'Manager of vendor rebates (additional)'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Описание =
		НСтр("ru = 'Профиль предоставляет права ведения условий, начислений, списаний ретро-бонусов поставщиков.
			 |Должен быть назначен в дополнение к профилю менеджера по закупкам.';
			 |en = 'The profile grants the right to maintain conditions, accruals, and write-offs of vendor rebates.
			 |It must be assigned in addition to the chief procurement officer profile.'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	
	ДополнитьПрофильРолямиЧтенияВидовРетроБонусовПоставщиков(ОписаниеПрофиля);
	
	ОписаниеПрофиля.Роли.Добавить("ЧтениеИнформацииПоПартнерам");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеБанковскихСчетовКонтрагентов");
	ОписаниеПрофиля.Роли.Добавить("ЧтениеНормативноСправочнойИнформации");
	
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеУсловийРетроБонусовПоставщиков");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеНачисленийРетроБонусовПоставщиков");
	ОписаниеПрофиля.Роли.Добавить("ПросмотрОтчетаВедомостьПоРетроБонусамПоставщиков");
	ОписаниеПрофиля.Роли.Добавить("ПросмотрОтчетаРасчетРетроБонусовПоставщиков");
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеСписанийРетроБонусовПоставщиков");
	
	ОписаниеПрофиля.ВидыДоступа.Добавить("Организации", "ВначалеВсеРазрешены");
	
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
КонецПроцедуры

Процедура ДобавитьПрофильУчастникСогласованияУсловийРетроБонусовПоставщиков(ОписанияПрофилей, ПараметрыОбновления)
	
	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя = "УчастникСогласованияУсловийРетроБонусовПоставщиков";
	ОписаниеПрофиля.Идентификатор = "f8442f04-da40-4257-8bc5-85cffbcf9b90";
	ОписаниеПрофиля.Родитель = "ЗакупкиИЗапасы";
	ОписаниеПрофиля.Наименование =
		НСтр("ru = 'Участник согласования условий ретро-бонусов поставщиков (дополнительный)';
			|en = 'Employee approving conditions of vendor rebates (additional)'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Описание =
		НСтр("ru = 'Профиль предоставляет право непосредственного согласования документ условий ретро-бонусов поставщиков.
			 |Должен быть назначен в дополнение к профилю ""Менеджер ретро-бонусов поставщиков (дополнительный)""';
			 |en = 'The profile grants the right to approve the vendor rebate conditions document.
			 |It must be assigned in addition to the ""Manager of vendor rebates (additional)"" profile'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	
	ОписаниеПрофиля.Роли.Добавить("ПравоСогласованияУсловийРетроБонусовПоставщиков");
	
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
КонецПроцедуры

Процедура ДобавитьПрофильОтветственныйАктыПремийПоставщикаДополнительный(ОписанияПрофилей, ПараметрыОбновления)
	
	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя = "ОтветственныйАктыПремийПоставщикаДополнительный";
	ОписаниеПрофиля.Идентификатор = "8dd4a055-22ac-484d-849c-0a300847a924";
	ОписаниеПрофиля.Родитель = "ЗакупкиИЗапасы";
	ОписаниеПрофиля.Наименование =
		НСтр("ru = 'Ответственный за акты премий поставщика (дополнительный)';
			|en = 'Person responsible for vendor bonus certificates (additional)'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Описание =
		НСтр("ru = 'Профиль предоставляет право оформления актов премий поставщика.
			 |Должен быть назначен в дополнение к профилю ""Менеджер ретро-бонусов поставщика (дополнительный)""';
			 |en = 'The profile grants the right to issue vendor bonus certificates.
			 |It must be assigned in addition to the ""Manager of vendor rebates (additional)"" profile'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	
	ОписаниеПрофиля.Роли.Добавить("ДобавлениеИзменениеАктПремииПоставщика");
	
	ОписаниеПрофиля.ВидыДоступа.Добавить("Организации", "ВначалеВсеРазрешены");
	
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
КонецПроцедуры

Процедура ДобавитьПрофильПравоОтключенияКонтроляОстатковРетроБонусовДополнительный(ОписанияПрофилей, ПараметрыОбновления)
	
	ОписаниеПрофиля = УправлениеДоступом.НовоеОписаниеПрофиляГруппДоступа();
	ОписаниеПрофиля.Имя = "ПравоОтключенияКонтроляОстатковРетроБонусовДополнительный";
	ОписаниеПрофиля.Идентификатор = "17126a31-a792-4be6-92b9-6a5451248c79";
	ОписаниеПрофиля.Родитель = "Продажи";
	ОписаниеПрофиля.Наименование =
		НСтр("ru = 'Право отключения контроля остатков ретро-бонусов клиентов (дополнительный)';
			|en = 'Right to disable customer rebate balance check (additional)'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	ОписаниеПрофиля.Описание =
		НСтр("ru = 'Профиль предоставляет право отключения контроля остатков ретро-бонусов клиентов.
			 |
			 |Назначается в дополнение к профилю ""Менеджер ретро-бонусов клиентов (дополнительный)"" или ""Ответственный за акты премий клиентам (дополнительный)""';
			 |en = 'The profile grants the right to disable customer rebate balance check.
			 |
			 |It is assigned in addition to the ""Manager of customer rebates (additional)"" or ""Person responsible for customer bonus certificates (additional)"" profile'",
			 ОбщегоНазначения.КодОсновногоЯзыка());
	
	ОписаниеПрофиля.Роли.Добавить("РазрешитьОтключениеКонтроляРетроБонусовКлиентовНаВремяСеанса");
	
	ОписанияПрофилей.Добавить(ОписаниеПрофиля);
	
КонецПроцедуры

#КонецОбласти

#Область ФиксацияСоставовДокументов

// Параметры:
//  ДокументУсловий - ДокументСсылка.УсловияРетроБонусовКлиентов
//  ПоДаннымДокумента - Булево
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
// * Номенклатура - СправочникСсылка.Номенклатура
// * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//
Функция ЗаполнитьДанныеДляФиксацииСегментовТоваров(ДокументУсловий, ПоДаннымДокумента = Ложь) Экспорт
	
	ТипыНоменклатуры = ПоддерживаемыеТипыНоменклатуры();
	
	МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДокументУсловий);
	ТекстЗапроса = МенеджерДокумента.ТекстЗапросаДляФиксацииСегментовНоменклатуры(ПоДаннымДокумента);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументУсловий", ДокументУсловий);
	Запрос.УстановитьПараметр("ПоддерживаемыеТипыНоменклатуры", ТипыНоменклатуры);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	РезультатСтатическиеСегменты = РезультатыЗапроса[1];
	РезультатДинамическиеСегменты = РезультатыЗапроса[2];
	
	НоменклатураСегментов = НоваяТаблицаНоменклатурыСегментов();
		
	Если НЕ РезультатСтатическиеСегменты.Пустой() Тогда
		
		Выборка = РезультатСтатическиеСегменты.Выбрать();
		ЗаполнитьДанныеДляФиксацииСтатическихСегментов(Выборка, НоменклатураСегментов);
		
	КонецЕсли;
	
	Если НЕ РезультатДинамическиеСегменты.Пустой() Тогда
		
		Выборка = РезультатДинамическиеСегменты.Выбрать();
		ЗаполнитьДанныеДляФиксацииДинамическихСегментовТоваров(
			Выборка,
			НоменклатураСегментов);
			
	КонецЕсли;
	
	Возврат НоменклатураСегментов;
	
КонецФункции

// Параметры:
//  ДокументУсловий - ДокументСсылка.УсловияРетроБонусовКлиентов
//  ПоДаннымДокумента - Булево
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
// * Партнер - СправочникСсылка.Партнеры
//
Функция ЗаполнитьДанныеДляФиксацииСегментовПартнеров(ДокументУсловий, ПоДаннымДокумента = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДляФиксацииСегментовПартнеров(ПоДаннымДокумента);
	Запрос.УстановитьПараметр("ДокументУсловий", ДокументУсловий);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	РезультатСтатическиеСегменты = РезультатыЗапроса[1];
	РезультатДинамическиеСегменты = РезультатыЗапроса[2];
	
	ПартнерыСегментов = НоваяТаблицаПартнеровСегментов();
	
	Если НЕ РезультатСтатическиеСегменты.Пустой() Тогда
		
		Выборка = РезультатСтатическиеСегменты.Выбрать();
		ЗаполнитьДанныеДляФиксацииСтатическихСегментов(Выборка, ПартнерыСегментов);
		
	КонецЕсли;
	
	Если НЕ РезультатДинамическиеСегменты.Пустой() Тогда
		
		Выборка = РезультатДинамическиеСегменты.Выбрать();
		ЗаполнитьДанныеДляФиксацииДинамическихСегментовПартнеров(Выборка, ПартнерыСегментов);
		
	КонецЕсли;
	
	Возврат ПартнерыСегментов;
	
КонецФункции

Функция ТекстЗапросаДляФиксацииСегментовПартнеров(ПоДаннымДокумента)
	
	МассивЗапросов = Новый Массив; // Массив из Строка
	
	Если ПоДаннымДокумента Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	УсловияРетроБонусовКлиентовСегментыПартнеров.Сегмент КАК Сегмент,
		|	УсловияРетроБонусовКлиентовСегментыПартнеров.Сегмент.СпособФормирования КАК СпособФормирования
		|ПОМЕСТИТЬ ВТ_Сегменты
		|ИЗ
		|	Документ.УсловияРетроБонусовКлиентов.СегментыПартнеров КАК УсловияРетроБонусовКлиентовСегментыПартнеров
		|ГДЕ
		|	УсловияРетроБонусовКлиентовСегментыПартнеров.Ссылка = &ДокументУсловий
		|	И НЕ УсловияРетроБонусовКлиентовСегментыПартнеров.Отменено";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СегментыПартнеров.Сегмент КАК Сегмент,
		|	СегментыПартнеров.Сегмент.СпособФормирования КАК СпособФормирования
		|ПОМЕСТИТЬ ВТ_Сегменты
		|ИЗ
		|	РегистрСведений.РетроБонусыКлиентовСегментыПартнеров КАК СегментыПартнеров
		|ГДЕ
		|	СегментыПартнеров.ДокументУсловий = &ДокументУсловий
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сегмент";
		
	КонецЕсли;
	
	МассивЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПартнерыСегмента.Партнер КАК Партнер
	|ИЗ
	|	ВТ_Сегменты КАК ВТ_Сегменты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПартнерыСегмента КАК ПартнерыСегмента
	|		ПО ВТ_Сегменты.Сегмент = ПартнерыСегмента.Сегмент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Сегменты.Сегмент КАК Сегмент
	|ИЗ
	|	ВТ_Сегменты КАК ВТ_Сегменты
	|ГДЕ
	|	ВТ_Сегменты.СпособФормирования = ЗНАЧЕНИЕ(Перечисление.СпособыФормированияСегментов.ФормироватьДинамически)";
	
	МассивЗапросов.Добавить(ТекстЗапроса);
	ТекстЗапроса = СтрСоединить(МассивЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса:
//  ТаблицаДанныхСегментов - ТаблицаЗначений
//
Процедура ЗаполнитьДанныеДляФиксацииСтатическихСегментов(Выборка, ТаблицаДанныхСегментов)
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеСегмента = ТаблицаДанныхСегментов.Добавить();
		ЗаполнитьЗначенияСвойств(ДанныеСегмента, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса:
//  * Сегмент - СправочникСсылка.СегментыНоменклатуры
//  ТаблицаДанныхСегментов - см. НоваяТаблицаНоменклатурыСегментов
//
Процедура ЗаполнитьДанныеДляФиксацииДинамическихСегментовТоваров(Выборка, ТаблицаДанныхСегментов)
	
	НоменклатураДинамическихСегментов = НоваяТаблицаНоменклатурыСегментов();
	
	Пока Выборка.Следующий() Цикл
		
		ТаблицаСегмента = ТаблицаДинамическогоСегментаНоменклатуры(Выборка.Сегмент);
		КолонкаХарактеристика = ТаблицаСегмента.Колонки.Найти("ХарактеристикаЭлемента");
		
		Для Каждого СтрокаТаблицыСегмента Из ТаблицаСегмента Цикл
			
			ДанныеСегмента = НоменклатураДинамическихСегментов.Добавить();
			ДанныеСегмента.Номенклатура = СтрокаТаблицыСегмента.ЭлементСписка;
			
			Если КолонкаХарактеристика <> Неопределено Тогда
				
				ДанныеСегмента.Характеристика = СтрокаТаблицыСегмента.ХарактеристикаЭлемента;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(НоменклатураСегментов.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	НоменклатураСегментов.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ВТ_НоменклатураСегментов
	|ИЗ
	|	&НоменклатураСегментов КАК НоменклатураСегментов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НоменклатураСегментов.Номенклатура КАК Номенклатура,
	|	ВТ_НоменклатураСегментов.Характеристика КАК Характеристика
	|ИЗ
	|	ВТ_НоменклатураСегментов КАК ВТ_НоменклатураСегментов
	|ГДЕ
	|	ВТ_НоменклатураСегментов.Номенклатура.ТипНоменклатуры В (&ПоддерживаемыеТипыНоменклатуры)";
	
	ТипыНоменклатуры = ПоддерживаемыеТипыНоменклатуры();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("НоменклатураСегментов", НоменклатураДинамическихСегментов);
	Запрос.УстановитьПараметр("ПоддерживаемыеТипыНоменклатуры", ТипыНоменклатуры);
	
	РезультатСоставСегментов = Запрос.Выполнить();
	Если НЕ РезультатСоставСегментов.Пустой() Тогда
		
		Выборка = РезультатСоставСегментов.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ДанныеСегмента = ТаблицаДанныхСегментов.Добавить();
			ЗаполнитьЗначенияСвойств(ДанныеСегмента, Выборка);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ТаблицаДанныхСегментов.Свернуть("Номенклатура, Характеристика");
	
КонецПроцедуры

// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса:
//  * Сегмент - СправочникСсылка.СегментыПартнеров
//  ТаблицаДанныхСегментов - см. НоваяТаблицаПартнеровСегментов
//
Процедура ЗаполнитьДанныеДляФиксацииДинамическихСегментовПартнеров(Выборка, ТаблицаДанныхСегментов)
	
	Пока Выборка.Следующий() Цикл
		
		ТаблицаСегмента = ТаблицаДинамическогоСегментаПартнеров(Выборка.Сегмент);
		Для Каждого СтрокаТаблицыСегмента Из ТаблицаСегмента Цикл
			
			ДанныеСегмента = ТаблицаДанныхСегментов.Добавить();
			ДанныеСегмента.Партнер = СтрокаТаблицыСегмента.ЭлементСписка;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаДанныхСегментов.Свернуть("Партнер");
	
КонецПроцедуры

// Возвращаемое значение:
//  ТаблицаЗначений - Новая таблица номенклатуры сегментов:
//  * Номенклатура - СправочникСсылка.Номенклатура
//  * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
Функция НоваяТаблицаНоменклатурыСегментов()
	
	ОписаниеТиповНоменклатура = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ОписаниеТиповХарактеристика = Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры");
	
	НоменклатураСегментов = Новый ТаблицаЗначений();
	НоменклатураСегментов.Колонки.Добавить("Номенклатура", ОписаниеТиповНоменклатура);
	НоменклатураСегментов.Колонки.Добавить("Характеристика", ОписаниеТиповХарактеристика);
	
	Возврат НоменклатураСегментов;
	
КонецФункции

// Возвращаемое значение:
//  ТаблицаЗначений - Новая таблица номенклатуры сегментов:
//  * Партнер - СправочникСсылка.Партнеры
Функция НоваяТаблицаПартнеровСегментов()
	
	ОписаниеТиповПартнер = Новый ОписаниеТипов("СправочникСсылка.Партнеры");
	
	ПартнерыСегментов = Новый ТаблицаЗначений();
	ПартнерыСегментов.Колонки.Добавить("Партнер", ОписаниеТиповПартнер);
	
	Возврат ПартнерыСегментов;
	
КонецФункции

// Параметры:
//  Сегмент - СправочникСсылка.СегментыНоменклатуры
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
//  * ЭлементСписка - СправочникСсылка.Номенклатура
//  * ХарактеристикаЭлемента - СправочникСсылка.ХарактеристикиНоменклатуры
Функция ТаблицаДинамическогоСегментаНоменклатуры(Сегмент)
	
	ТаблицаСегмента = СегментыСервер.СписокЭлементовСКД(Сегмент);
	Возврат ТаблицаСегмента;
	
КонецФункции

// Параметры:
//  Сегмент - СправочникСсылка.СегментыПартнеров
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
//  * ЭлементСписка - СправочникСсылка.Партнеры
Функция ТаблицаДинамическогоСегментаПартнеров(Сегмент)
	
	ТаблицаСегмента = СегментыСервер.СписокЭлементовСКД(Сегмент);
	Возврат ТаблицаСегмента;
	
КонецФункции

// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов , ДокументСсылка.УсловияРетроБонусовПоставщика -
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
//  * НачалоДействия - Дата
//  * ОкончаниеДействия - Дата
//  * ДокументУсловий - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика -
//  * Номенклатура - СправочникСсылка.Номенклатура
//  * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//  * КоличествоПлан - Число
//  * БонусПроцент - Число
//  * БазоваяЦена - Число
//  * РегистраторДвижения - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика -
//  * НомерСтрокиДокумента - Число -
//
Функция НовыйСоставСегментовТоваров(Документ)
	
	ТипДокумента = ТипЗнч(Документ);
	МассивТиповДокумента = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипДокумента);
	
	ОписаниеТипаДата = ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата);
	ОписаниеТипаЧисло15_3 = ОбщегоНазначения.ОписаниеТипаЧисло(15, 3, ДопустимыйЗнак.Неотрицательный);
	ОписаниеТипаЧисло4_2 = ОбщегоНазначения.ОписаниеТипаЧисло(4, 2, ДопустимыйЗнак.Неотрицательный);
	ОписаниеТипаЧисло5_0 = ОбщегоНазначения.ОписаниеТипаЧисло(5, 0, ДопустимыйЗнак.Неотрицательный);
	ОписаниеТипаДокументУсловий = Новый ОписаниеТипов(МассивТиповДокумента);
	ОписаниеТипаНоменклатура = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ОписаниеТипаХарактеристика = Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры");
	
	ТаблицаСоставаСегментов = Новый ТаблицаЗначений();
	ТаблицаСоставаСегментов.Колонки.Добавить("НачалоДействия", ОписаниеТипаДата);
	ТаблицаСоставаСегментов.Колонки.Добавить("ОкончаниеДействия", ОписаниеТипаДата);
	ТаблицаСоставаСегментов.Колонки.Добавить("ДокументУсловий", ОписаниеТипаДокументУсловий);
	ТаблицаСоставаСегментов.Колонки.Добавить("Номенклатура", ОписаниеТипаНоменклатура);
	ТаблицаСоставаСегментов.Колонки.Добавить("Характеристика", ОписаниеТипаХарактеристика);
	ТаблицаСоставаСегментов.Колонки.Добавить("КоличествоПлан", ОписаниеТипаЧисло15_3);
	ТаблицаСоставаСегментов.Колонки.Добавить("БонусПроцент", ОписаниеТипаЧисло4_2);
	ТаблицаСоставаСегментов.Колонки.Добавить("БазоваяЦена", Метаданные.ОпределяемыеТипы.Цена.Тип);
	ТаблицаСоставаСегментов.Колонки.Добавить("РегистраторДвижения", ОписаниеТипаДокументУсловий);
	ТаблицаСоставаСегментов.Колонки.Добавить("НомерСтрокиДокумента", ОписаниеТипаЧисло5_0);
	
	Возврат ТаблицаСоставаСегментов;
	
КонецФункции

// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов , ДокументСсылка.УсловияРетроБонусовПоставщика -
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
//  * НачалоДействия - Дата
//  * ОкончаниеДействия - Дата
//  * ДокументУсловий - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика -
//  * Номенклатура - СправочникСсылка.Номенклатура
//  * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//  * КоличествоПлан - Число
//  * БонусПроцент - Число
//  * БазоваяЦена - Число
//  * КоличествоПланГраницаДиапазона - Число
//  * РегистраторДвижения - ДокументСсылка.УсловияРетроБонусовКлиентов, ДокументСсылка.УсловияРетроБонусовПоставщика -
//  * НомерСтрокиДокумента - Число -
//  * ЗначениеСвойства - ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
//
Функция НовыйСоставСвойствТоваров(Документ)
	
	ТипДокумента = ТипЗнч(Документ);
	МассивТиповДокумента = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипДокумента);
	
	ОписаниеТипаДата = ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата);
	ОписаниеТипаЧисло15_3 = ОбщегоНазначения.ОписаниеТипаЧисло(15, 3, ДопустимыйЗнак.Неотрицательный);
	ОписаниеТипаЧисло4_2 = ОбщегоНазначения.ОписаниеТипаЧисло(4, 2, ДопустимыйЗнак.Неотрицательный);
	ОписаниеТипаЧисло5_0 = ОбщегоНазначения.ОписаниеТипаЧисло(5, 0, ДопустимыйЗнак.Неотрицательный);
	ОписаниеТипаДокументУсловий = Новый ОписаниеТипов(МассивТиповДокумента);
	ОписаниеТипаНоменклатура = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ОписаниеТипаХарактеристика = Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры");
	ОписаниеТипаСвойство = Метаданные.ОпределяемыеТипы.ТипыСвойствНоменклатурыРетроБонусов.Тип;
	
	ТаблицаСоставаСвойств = Новый ТаблицаЗначений();
	ТаблицаСоставаСвойств.Колонки.Добавить("НачалоДействия", ОписаниеТипаДата);
	ТаблицаСоставаСвойств.Колонки.Добавить("ОкончаниеДействия", ОписаниеТипаДата);
	ТаблицаСоставаСвойств.Колонки.Добавить("ДокументУсловий", ОписаниеТипаДокументУсловий);
	ТаблицаСоставаСвойств.Колонки.Добавить("Номенклатура", ОписаниеТипаНоменклатура);
	ТаблицаСоставаСвойств.Колонки.Добавить("Характеристика", ОписаниеТипаХарактеристика);
	ТаблицаСоставаСвойств.Колонки.Добавить("КоличествоПлан", ОписаниеТипаЧисло15_3);
	ТаблицаСоставаСвойств.Колонки.Добавить("БонусПроцент", ОписаниеТипаЧисло4_2);
	ТаблицаСоставаСвойств.Колонки.Добавить("БазоваяЦена", Метаданные.ОпределяемыеТипы.Цена.Тип);
	ТаблицаСоставаСвойств.Колонки.Добавить("КоличествоПланГраницаДиапазона", ОписаниеТипаЧисло15_3);
	ТаблицаСоставаСвойств.Колонки.Добавить("РегистраторДвижения", ОписаниеТипаДокументУсловий);
	ТаблицаСоставаСвойств.Колонки.Добавить("НомерСтрокиДокумента", ОписаниеТипаЧисло5_0);
	ТаблицаСоставаСвойств.Колонки.Добавить("ЗначениеСвойства", ОписаниеТипаСвойство);
	
	Возврат ТаблицаСоставаСвойств;
	
КонецФункции

// Параметры:
//  ПартнерыСегментов - см. НоваяТаблицаПартнеровСегментов
Процедура ОтсортироватьТаблицуСегментовПартнеровПоПредставлению(ПартнерыСегментов)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ПартнерыСегментов.Партнер КАК Справочник.Партнеры) КАК Партнер
	|ПОМЕСТИТЬ ВТ_ПартнерыСегментов
	|ИЗ
	|	&ПартнерыСегментов КАК ПартнерыСегментов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПартнерыСегментов.Партнер КАК Партнер
	|ИЗ
	|	ВТ_ПартнерыСегментов КАК ВТ_ПартнерыСегментов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_ПартнерыСегментов.Партнер
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ПартнерыСегментов", ПартнерыСегментов);
	
	ПартнерыСегментов = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

// Параметры:
Процедура ОтсортироватьТаблицуСегментовНоменклатурыПоПредставлению(НоменклатураСегментов)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(НоменклатураСегментов.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(НоменклатураСегментов.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика
	|ПОМЕСТИТЬ ВТ_НоменклатураСегментов
	|ИЗ
	|	&НоменклатураСегментов КАК НоменклатураСегментов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НоменклатураСегментов.Номенклатура КАК Номенклатура,
	|	ВТ_НоменклатураСегментов.Характеристика КАК Характеристика
	|ИЗ
	|	ВТ_НоменклатураСегментов КАК ВТ_НоменклатураСегментов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_НоменклатураСегментов.Номенклатура,
	|	ВТ_НоменклатураСегментов.Характеристика
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("НоменклатураСегментов", НоменклатураСегментов);
	
	НоменклатураСегментов = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

// Возвращает таблицу товаров, подобранных по свойству номенклатуры, соответствующую структуре РС РетроБонусыКлиентовТовары
//
// Параметры:
//  Документ - ДокументСсылка.УсловияРетроБонусовКлиентов -
//           - ДокументСсылка.УсловияРетроБонусовПоставщика -
// 
// Возвращаемое значение:
//  см. НовыйСоставСвойствТоваров
//
Функция СоставТоваровПоСвойству(Документ)
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.УсловияРетроБонусовКлиентов") Тогда
		
		ПараметрыУсловийРетроБонусов = РетроБонусыРасчет.ПараметрыУсловийРетроБонусовКлиентов(Документ);
		РасчетБонусовКлиентов = Истина;
		
	Иначе
		
		ПараметрыУсловийРетроБонусов = РетроБонусыРасчет.ПараметрыУсловийРетроБонусовПоставщиков(Документ);
		РасчетБонусовКлиентов = Ложь;
		
	КонецЕсли;
	
	ТекстТоварыПоСвойству = ТекстЗапросаТоварыПоСвойству(
		РасчетБонусовКлиентов,
		ПараметрыУсловийРетроБонусов.СвойствоНоменклатуры,
		ПараметрыУсловийРетроБонусов.СвойствоНоменклатурыСсылка);
	
	ТекстСоставТоваров =
	"ВЫБРАТЬ
	|	ТоварыПоСвойству.НачалоДействия КАК НачалоДействия,
	|	ТоварыПоСвойству.ОкончаниеДействия КАК ОкончаниеДействия,
	|	ТоварыПоСвойству.ДокументУсловий КАК ДокументУсловий,
	|	ТоварыПоСвойству.Номенклатура КАК Номенклатура,
	|	ТоварыПоСвойству.Характеристика КАК Характеристика,
	|	ТоварыПоСвойству.КоличествоПлан КАК КоличествоПлан,
	|	ТоварыПоСвойству.БонусПроцент КАК БонусПроцент,
	|	ТоварыПоСвойству.БазоваяЦена КАК БазоваяЦена,
	|	ТоварыПоСвойству.КоличествоПланГраницаДиапазона КАК КоличествоПланГраницаДиапазона,
	|	ТоварыПоСвойству.ЗначениеСвойства КАК ЗначениеСвойства
	|ИЗ
	|	ВТ_ТоварыПоСвойству КАК ТоварыПоСвойству
	|ГДЕ
	|	ТоварыПоСвойству.Номенклатура.ТипНоменклатуры В (&ПоддерживаемыеТипыНоменклатуры)
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	ТекстРазделитель = ОбщегоНазначения.РазделительПакетаЗапросов();
	
	ФрагментыЗапроса = Новый Массив; // Массив из Строка
	ФрагментыЗапроса.Добавить(ТекстТоварыПоСвойству);
	ФрагментыЗапроса.Добавить(ТекстСоставТоваров);
	
	ТекстЗапроса = СтрСоединить(ФрагментыЗапроса, ТекстРазделитель);
	
	ТипыНоменклатуры = ПоддерживаемыеТипыНоменклатуры();
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ИсходныйДокумент", Документ);
	Запрос.УстановитьПараметр("СвойствоНоменклатуры", ПараметрыУсловийРетроБонусов.СвойствоНоменклатурыСсылка);
	Запрос.УстановитьПараметр("ПоддерживаемыеТипыНоменклатуры", ТипыНоменклатуры);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТоварыПоСвойствам = НовыйСоставСвойствТоваров(Документ);
	
	АктуальныйДокумент = АктуальныйДокументУсловийРетроБонусовПоСсылке(Документ);
	Если НЕ АктуальныйДокумент.Пустая()
	   И НЕ РезультатЗапроса.Пустой() Тогда
		
		Счетчик = 1;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = ТоварыПоСвойствам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.РегистраторДвижения = АктуальныйДокумент;
			НоваяСтрока.НомерСтрокиДокумента = Счетчик;
			
			Счетчик = Счетчик + 1;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТоварыПоСвойствам;
	
КонецФункции

#КонецОбласти

#Область ПакетнаяОбработкаСтрок

Процедура ПакетнаяОбработкаЗаполнитьПризнакУказанияВалютыРетроБонусовПоставщиков(СтруктураДействий, КешированныеЗначения, ПоляВыборки)
	
	Перем ПараметрыДействия; // Структура
	
	ДействиеТребуется = ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
		"ЗаполнитьПризнакУказанияВалютыРетроБонусовПоставщиков",
		СтруктураДействий,
		КешированныеЗначения,
		ПараметрыДействия);
	
	Если ДействиеТребуется Тогда
		
		ПолеВыборкиШаблон =
		"ВЫБОР
		|	КОГДА ЕСТЬNULL(ИсточникДанных.%1, ЗНАЧЕНИЕ(Документ.УсловияРетроБонусовПоставщика.ПустаяСсылка)) = ЗНАЧЕНИЕ(Документ.УсловияРетроБонусовПоставщика.ПустаяСсылка)
		|		ТОГДА ЛОЖЬ
		|	ИНАЧЕ
		|		ИсточникДанных.%1.Валюта <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
		|КОНЕЦ КАК %2";
		
		Для Каждого Поле Из ПараметрыДействия Цикл // КлючИЗначение
			ПолеВыборки = СтрШаблон(ПолеВыборкиШаблон, Поле.Ключ, Поле.Значение);
		КонецЦикла;
		
		ПоляВыборки.Добавить(ПолеВыборки);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПакетнаяОбработкаЗаполнитьВалютуРетроБонусовПоставщиков(СтруктураДействий, КешированныеЗначения, ПоляВыборки)
	
	Перем ПараметрыДействия; // Структура
	
	ДействиеТребуется = ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
		"ЗаполнитьВалютуРетроБонусовПоставщиков",
		СтруктураДействий,
		КешированныеЗначения,
		ПараметрыДействия);
	
	Если ДействиеТребуется Тогда
		
		ПолеВыборкиШаблон =
		"ВЫБОР
		|	КОГДА ЕСТЬNULL(ИсточникДанных.%1, ЗНАЧЕНИЕ(Документ.УсловияРетроБонусовПоставщика.ПустаяСсылка)) = ЗНАЧЕНИЕ(Документ.УсловияРетроБонусовПоставщика.ПустаяСсылка)
		|		ТОГДА ЛОЖЬ
		|	ИНАЧЕ
		|		ИсточникДанных.%1.Валюта
		|КОНЕЦ КАК %2";
		
		Для Каждого Поле Из ПараметрыДействия Цикл // КлючИЗначение
			ПолеВыборки = СтрШаблон(ПолеВыборкиШаблон, Поле.Ключ, Поле.Значение);
		КонецЦикла;
		
		ПоляВыборки.Добавить(ПолеВыборки);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПакетнаяОбработкаЗаполнитьТипРетроБонусовПоставщиков(СтруктураДействий, КешированныеЗначения, ПоляВыборки)
	
	Перем ПараметрыДействия; // Структура
	
	ДействиеТребуется = ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
		"ЗаполнитьТипРетроБонусовПоставщиков",
		СтруктураДействий,
		КешированныеЗначения,
		ПараметрыДействия);
	
	Если ДействиеТребуется Тогда
		
		ПолеВыборкиШаблон =
		"ВЫБОР
		|	КОГДА ЕСТЬNULL(ИсточникДанных.%1, ЗНАЧЕНИЕ(Документ.УсловияРетроБонусовПоставщика.ПустаяСсылка)) = ЗНАЧЕНИЕ(Документ.УсловияРетроБонусовПоставщика.ПустаяСсылка)
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРетроБонусовПоставщиков.ПустаяСсылка)
		|	ИНАЧЕ
		|		ИсточникДанных.%1.ТипБонуса
		|КОНЕЦ КАК %2";
		
		Для Каждого Поле Из ПараметрыДействия Цикл // КлючИЗначение
			ПолеВыборки = СтрШаблон(ПолеВыборкиШаблон, Поле.Ключ, Поле.Значение);
		КонецЦикла;
		
		ПоляВыборки.Добавить(ПолеВыборки);
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  СтруктураДействий - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ.СтруктураДействий
//  ОписаниеЗапроса - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОписаниеЗапроса
//  КешированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения
//
Процедура ДополнитьТекстЗапросаПризнакСоглашенияВСтрокеТЧ(СтруктураДействий, ОписаниеЗапроса, КешированныеЗначения)
	
	Перем ИмяПоляСоглашение; // Строка
	
	ДействиеТребуется = ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
		"ЗаполнитьПризнакСоглашенияВСтрокеТЧ",
		СтруктураДействий,
		КешированныеЗначения,
		ИмяПоляСоглашение);
	
	Если ДействиеТребуется Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИсточникДанных.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
		|	СоглашенияСКлиентами.Типовое КАК ЭтоТиповоеСоглашение
		|ИЗ
		|	ВтИсточникДанных КАК ИсточникДанных
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
		|		ПО &ПолеСоглашение = СоглашенияСКлиентами.Ссылка";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСоглашение", "ИсточникДанных." + ИмяПоляСоглашение);
		
		ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "ПризнакиСоглашений");
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  СтруктураДействий - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ.СтруктураДействий
//  ОписаниеЗапроса - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОписаниеЗапроса
//  КешированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения
//
Процедура ДополнитьТекстЗапросаСвойствамиРетроБонусовПоставщиков(СтруктураДействий, ОписаниеЗапроса, КешированныеЗначения)
	
	ПоляВыборки = Новый Массив; // Массив из Строка
	
	ПакетнаяОбработкаЗаполнитьПризнакУказанияВалютыРетроБонусовПоставщиков(
		СтруктураДействий, КешированныеЗначения, ПоляВыборки);
	
	ПакетнаяОбработкаЗаполнитьВалютуРетроБонусовПоставщиков(
		СтруктураДействий, КешированныеЗначения, ПоляВыборки);
	
	ПакетнаяОбработкаЗаполнитьТипРетроБонусовПоставщиков(
		СтруктураДействий, КешированныеЗначения, ПоляВыборки);
	
	Если ПоляВыборки.Количество() > 0 Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИсточникДанных.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
		|	&ПоляВыборки
		|ИЗ
		|	ВтИсточникДанных КАК ИсточникДанных";
		
		СоединительПолейВыборки = Символы.ПС + ",";
		СтрокаПолейВыборки = СтрСоединить(ПоляВыборки, СоединительПолейВыборки);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляВыборки", СтрокаПолейВыборки);
		
		ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "СвойстваРетроБонусовПоставщиков");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УсловноеОформлениеШкалаРасчета

Процедура УстановитьОформлениеШкалаРасчетаБонусПроцент(УсловноеОформление)
	
	#Область ПрогрессивнаяШкала
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ШкалыРасчетаБонусПроцент");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВидШкалыРасчета");
	ОтборЭлемента.ПравоеЗначение = 1; // Прогрессивная шкала
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ШкалыРасчета.БонусПроцент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	#КонецОбласти
	
	#Область ПоДиапазонам
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ШкалыРасчетаБонусПроцент");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВидШкалыРасчета");
	ОтборЭлемента.ПравоеЗначение = 2; // По диапазонам
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ШкалыРасчета.БонусПроцент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	СтрокаНоль = НСтр("ru = '<нет>';
						|en = '<no>'");
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", СтрокаНоль);
	#КонецОбласти
	
КонецПроцедуры

Процедура УстановитьОформлениеШкалаРасчетаДиапазонОт(УсловноеОформление)
	
	// Запрет изменения границы 0 для диапазонов (начальное значение первого диапазона)
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ШкалыРасчетаДиапазонОт");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВидШкалыРасчета");
	ОтборЭлемента.ПравоеЗначение = 2; // По диапазонам
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ШкалыРасчета.ДиапазонОт");
	ОтборЭлемента.ПравоеЗначение = 0;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ШкалыРасчета.НомерСтроки");
	ОтборЭлемента.ПравоеЗначение = 1;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаПериодовСтрок

// Проверка даты начала действия ретро-бонуса в добавленной строке по отношению к максимальной дате окончания периода зарегистрированного начисления.
//  
// Параметры:
//  МаксимальнаяДата - Дата - Максимальная дата окончания зарегистрированного начисления
//  ИмяТаблицы - Строка - Имя таблицы
//  ПредставлениеТаблицы - Строка - Представление таблицы
//  ТекущаяДатаНачала - Дата - Дата начала в добавленной строке
//  ТекущаяСтрокаТаблицы - ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.КонтрагентыКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.ИННКонтрагентовКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Поставщики, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Контрагенты, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ДоговорыСоглашения, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры - Текущая строка таблицы
//  Отказ - Булево - Отказ
//
Процедура ПроверитьДопустимостьНачалаПериодаСтроки(Знач МаксимальнаяДата, Знач ИмяТаблицы, Знач ПредставлениеТаблицы, Знач ТекущаяДатаНачала, ТекущаяСтрокаТаблицы, Отказ)
	
	Если НЕ ТекущаяСтрокаТаблицы.ИсходнаяСтрока
	   И ТекущаяДатаНачала <= МаксимальнаяДата Тогда
		
		Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" дата начала не может быть меньше или равна %3 - даты окончания периода зарегистрированного начисления';
						|en = 'In line %1 of the ""%2"" list, the start date cannot be less than or equal to %3 - the end date of the registered accrual period'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Шаблон,
			Строка(ТекущаяСтрокаТаблицы.НомерСтроки),
			ПредставлениеТаблицы,
			Формат(МаксимальнаяДата, "ДЛФ=D;"));
		
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, ТекущаяСтрокаТаблицы.НомерСтроки, "НачалоДействия");
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

// Проверка наличия изменения даты начала действия ретро-бонуса в исходной строке при зарегистрированном начислении, когда дата начала действия ретро-бонуса до изменения была меньше или равна максимальной даты окончания периода зарегистрированного начисления.
// 
// Параметры:
//  МаксимальнаяДата - Дата - Максимальная дата окончания периода зарегистрированного начисления
//  ИмяТаблицы - Строка - Имя таблицы
//  ПредставлениеТаблицы - Строка - Представление таблицы
//  ТекущаяДатаНачала - Дата - Дата начала в исходной строке
//  ТекущаяСтрокаТаблицы - ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.КонтрагентыКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.ИННКонтрагентовКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Товары, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Поставщики, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Контрагенты, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ДоговорыСоглашения - Текущая строка таблицы
//  Отказ - Булево - Отказ
//
Процедура ПроверитьИзменениеНачалоПериодаИсходнойСтроки(Знач МаксимальнаяДата, Знач ИмяТаблицы, Знач ПредставлениеТаблицы, Знач ТекущаяДатаНачала, ТекущаяСтрокаТаблицы, Отказ)
	
	Если ТекущаяСтрокаТаблицы.ИсходнаяСтрока
	   И НЕ ТекущаяСтрокаТаблицы.Отменено
	   И ТекущаяСтрокаТаблицы.НачалоДействияДоИзменений <> Дата(1, 1, 1)
	   И ТекущаяСтрокаТаблицы.НачалоДействияДоИзменений <= МаксимальнаяДата
	   И ТекущаяДатаНачала <> ТекущаяСтрокаТаблицы.НачалоДействияДоИзменений Тогда
		
		Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" дата начала не может быть изменена, так как в период действия условия были зарегистрированы начисления';
						|en = 'In line %1 of the ""%2"" list, the start date cannot be changed as accruals were registered during the condition validity period'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Шаблон,
			Строка(ТекущаяСтрокаТаблицы.НомерСтроки),
			ПредставлениеТаблицы);
		
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, ТекущаяСтрокаТаблицы.НомерСтроки, "НачалоДействия");
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

// Проверка даты начала действия ретро-бонуса в исходной строке по отношению к максимальной дате окончания периода зарегистрированного начисления, когда дата начала действия ретро-бонуса до изменения была больше максимальной даты окончания периода зарегистрированного начисления.
// 
// Параметры:
//  МаксимальнаяДата - Дата - Максимальная дата окончания периода зарегистрированного начисления
//  ИмяТаблицы - Строка - Имя таблицы
//  ПредставлениеТаблицы - Строка - Представление таблицы
//  ТекущаяДатаНачала - Дата - Дата начала в исходной строке
//  ТекущаяСтрокаТаблицы - ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.КонтрагентыКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.ИННКонтрагентовКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Товары, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Поставщики, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Контрагенты, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ДоговорыСоглашения - Текущая строка таблицы
//  Отказ - Булево - Отказ
//
Процедура ПроверитьДопустимостьНачалаПериодаИсходнойСтроки(Знач МаксимальнаяДата, Знач ИмяТаблицы, Знач ПредставлениеТаблицы, Знач ТекущаяДатаНачала, ТекущаяСтрокаТаблицы, Отказ)
	
	Если ТекущаяСтрокаТаблицы.ИсходнаяСтрока
	   И НЕ ТекущаяСтрокаТаблицы.Отменено
	   И ТекущаяСтрокаТаблицы.НачалоДействияДоИзменений > МаксимальнаяДата
	   И ТекущаяДатаНачала <= МаксимальнаяДата Тогда
		
		Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" дата начала не может меньше или равной %3 - даты окончания периода зарегистрированного начисления';
						|en = 'In line %1 of the ""%2"" list, the start date cannot be less than or equal to %3 - the end date of the registered accrual period'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Шаблон,
			Строка(ТекущаяСтрокаТаблицы.НомерСтроки),
			ПредставлениеТаблицы,
			Формат(МаксимальнаяДата, "ДЛФ=D;"));
		
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, ТекущаяСтрокаТаблицы.НомерСтроки, "НачалоДействия");
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

// Проверка даты окончания действия ретро-бонуса в добавленной строке по отношению к максимальной дате окончания периода зарегистрированного начисления.
// 
// Параметры:
//  МаксимальнаяДата - Дата - Максимальная дата окончания зарегистрированного начисления
//  ИмяТаблицы - Строка - Имя таблицы
//  ПредставлениеТаблицы - Строка - Представление таблицы
//  ТекущаяДатаОкончания - Дата - Дата окончания в добавленной строке
//  ТекущаяСтрокаТаблицы - ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.КонтрагентыКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.ИННКонтрагентовКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Товары, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Поставщики, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Контрагенты, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ДоговорыСоглашения - Текущая строка таблицы
//  Отказ - Булево - Отказ
//
Процедура ПроверитьДопустимостьОкончанияПериодаСтроки(Знач МаксимальнаяДата, Знач ИмяТаблицы, Знач ПредставлениеТаблицы, Знач ТекущаяДатаОкончания, ТекущаяСтрокаТаблицы, Отказ)
	
	Если НЕ ТекущаяСтрокаТаблицы.ИсходнаяСтрока
	   И ТекущаяДатаОкончания <= МаксимальнаяДата Тогда
		
		Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" дата окончания не может быть меньше или равна %3 - даты окончания периода зарегистрированного начисления';
						|en = 'In line %1 of the ""%2"" list, the end date cannot be less than or equal to %3 - the end date of the registered accrual period'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Шаблон,
			Строка(ТекущаяСтрокаТаблицы.НомерСтроки),
			ПредставлениеТаблицы,
			Формат(МаксимальнаяДата, "ДЛФ=D;"));
		
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, ТекущаяСтрокаТаблицы.НомерСтроки, "ОкончаниеДействия");
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

// Проверка наличия изменения даты окончания действия ретро-бонуса в исходной строке при зарегистрированном начислении, когда дата окончания действия ретро-бонуса до изменения была меньше максимальной даты окончания периода зарегистрированного начисления.
// 
// Параметры:
//  МаксимальнаяДата - Дата - Максимальная дата окончания периода зарегистрированного начисления
//  ИмяТаблицы - Строка - Имя таблицы
//  ПредставлениеТаблицы - Строка - Представление таблицы
//  ТекущаяДатаОкончания - Дата - Дата окончания в исходной строке
//  ТекущаяСтрокаТаблицы - ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.КонтрагентыКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.ИННКонтрагентовКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Товары, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Поставщики, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Контрагенты, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ДоговорыСоглашения - Текущая строка таблицы
//  Отказ - Булево - Отказ
//
Процедура ПроверитьИзменениеОкончанияПериодаИсходнойСтроки(Знач МаксимальнаяДата, Знач ИмяТаблицы, Знач ПредставлениеТаблицы, Знач ТекущаяДатаОкончания, ТекущаяСтрокаТаблицы, Отказ)
	
	Если ТекущаяСтрокаТаблицы.ИсходнаяСтрока
	   И НЕ ТекущаяСтрокаТаблицы.Отменено
	   И ТекущаяСтрокаТаблицы.ОкончаниеДействияДоИзменений <> Дата(1, 1, 1)
	   И ТекущаяСтрокаТаблицы.ОкончаниеДействияДоИзменений < МаксимальнаяДата
	   И ТекущаяДатаОкончания <> ТекущаяСтрокаТаблицы.ОкончаниеДействияДоИзменений Тогда
		
		Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" дата окончания не может быть изменена, так как в период действия условия были зарегистрированы начисления';
						|en = 'In line %1 of the ""%2"" list, the end date cannot be changed as accruals were registered during the condition validity period'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Шаблон,
			Строка(ТекущаяСтрокаТаблицы.НомерСтроки),
			ПредставлениеТаблицы);
		
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, ТекущаяСтрокаТаблицы.НомерСтроки, "ОкончаниеДействия");
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

// Проверка даты окончания действия ретро-бонуса в исходной строке по отношению к максимальной дате окончания периода зарегистрированного начисления, когда дата окончания действия ретро-бонуса до изменения была больше или ровна максимальной даты окончания периода зарегистрированного начисления.
// 
// Параметры:
//  МаксимальнаяДата - Дата - Максимальная дата окончания периода зарегистрированного начисления
//  ИмяТаблицы - Строка - Имя таблицы
//  ПредставлениеТаблицы - Строка - Представление таблицы
//  ТекущаяДатаОкончания - Дата - Дата окончания в исходной строке
//  ТекущаяСтрокаТаблицы - ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.КонтрагентыКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.ИННКонтрагентовКлиентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Товары, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Поставщики, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Контрагенты, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ИННКонтрагентов, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовКлиентов.ДоговорыСоглашения - Текущая строка таблицы
//  Отказ - Булево - Отказ
//
Процедура ПроверитьДопустимостьИзменениеОкончанияПериодаИсходнойСтроки(Знач МаксимальнаяДата, Знач ИмяТаблицы, Знач ПредставлениеТаблицы, Знач ТекущаяДатаОкончания, ТекущаяСтрокаТаблицы, Отказ)
	
	Если ТекущаяСтрокаТаблицы.ИсходнаяСтрока
	   И НЕ ТекущаяСтрокаТаблицы.Отменено
	   И ТекущаяСтрокаТаблицы.ОкончаниеДействияДоИзменений >= МаксимальнаяДата
	   И ТекущаяДатаОкончания < МаксимальнаяДата Тогда
		
		Шаблон = НСтр("ru = 'В строке %1 списка ""%2"" дата окончания не может быть меньше %3 - даты окончания периода зарегистрированного начисления';
						|en = 'In line %1 of the ""%2"" list, the end date cannot be less than %3 - the end date of the registered accrual period'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Шаблон,
			Строка(ТекущаяСтрокаТаблицы.НомерСтроки),
			ПредставлениеТаблицы,
			Формат(МаксимальнаяДата, "ДЛФ=D;"));
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ИмяТаблицы, ТекущаяСтрокаТаблицы.НомерСтроки, "ОкончаниеДействия");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Параметры:
//  ТаблицаДанных - ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Товары
//                - ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Товары
//  ПараметрыПроверки - см. ПараметрыПроверкиПериодовПоКлючам
// 
// Возвращаемое значение:
//  Структура - Данные периодов товаров по ключам:
// * ПересеченияПериодов - ВыборкаИзРезультатаЗапроса:
//	** ПерваяСтрока - Число -
// 	** Номенклатура - СправочникСсылка.Номенклатура -
// 	** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры -
// * ДублиПланаПроцента - ВыборкаИзРезультатаЗапроса:
//	** ПерваяСтрока - Число -
// 	** Номенклатура - СправочникСсылка.Номенклатура -
// 	** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры -
// * ДублиПоДиапазонам - ВыборкаИзРезультатаЗапроса:
//	** ПерваяСтрока - Число -
// 	** Номенклатура - СправочникСсылка.Номенклатура -
// 	** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры -
// 	** КоличествоПлан - Число -
// 
Функция ДанныеПериодовТоваровПоКлючам(ТаблицаДанных, ПараметрыПроверки);
	
	ЭтоПрогрессивнаяШкала = Ложь;
	ЭтоШкалаПоДиапазонам = Ложь;
	ЭтоРасчетПоШкалам = Ложь;
	
	Если ПараметрыПроверки.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		
		ЭтоПрогрессивнаяШкала = (ПараметрыПроверки.ВидШкалыРасчета = 0);
		ЭтоШкалаПоДиапазонам = (ПараметрыПроверки.ВидШкалыРасчета = 1);
		ЭтоРасчетПоШкалам =
			(ЭтоПрогрессивнаяШкала
			 ИЛИ ЭтоШкалаПоДиапазонам);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДанных.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДанных.Номенклатура КАК Номенклатура,
	|	ТаблицаДанных.Характеристика КАК Характеристика,
	|	ТаблицаДанных.КоличествоПлан КАК КоличествоПлан,
	|	ТаблицаДанных.Процент КАК Процент,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.НачалоДействия = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &НачалоДействия
	|		ИНАЧЕ ТаблицаДанных.НачалоДействия
	|	КОНЕЦ КАК НачалоДействия,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.ОкончаниеДействия = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &ОкончаниеДействия
	|		ИНАЧЕ ТаблицаДанных.ОкончаниеДействия
	|	КОНЕЦ КАК ОкончаниеДействия
	|ПОМЕСТИТЬ ТаблицаДанных
	|ИЗ
	|	&ПодготовленныеДанные КАК ТаблицаДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанных.Номенклатура КАК Номенклатура,
	|	ТаблицаДанных.Характеристика КАК Характеристика,
	|	МИНИМУМ(ТаблицаДанных.НомерСтроки) КАК ПерваяСтрока
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаДанных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанных КАК ТаблицаДанныхЗеркало
	|		ПО ТаблицаДанных.НомерСтроки <> ТаблицаДанныхЗеркало.НомерСтроки
	|		И ТаблицаДанных.Номенклатура = ТаблицаДанныхЗеркало.Номенклатура
	|		И ТаблицаДанных.Характеристика = ТаблицаДанныхЗеркало.Характеристика
	|		И (ТаблицаДанных.НачалоДействия МЕЖДУ ТаблицаДанныхЗеркало.НачалоДействия И ТаблицаДанныхЗеркало.ОкончаниеДействия
	|		ИЛИ ТаблицаДанных.ОкончаниеДействия
	|			МЕЖДУ ТаблицаДанныхЗеркало.НачалоДействия И ТаблицаДанныхЗеркало.ОкончаниеДействия)
	|ГДЕ
	|	НЕ &ЭтоРасчетПоШкалам
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.Номенклатура,
	|	ТаблицаДанных.Характеристика
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика,
	|	МИНИМУМ(ТаблицаТоваров.НомерСтроки)
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанных КАК ТаблицаТоваровЗеркало
	|		ПО ТаблицаТоваров.НомерСтроки <> ТаблицаТоваровЗеркало.НомерСтроки
	|		И ТаблицаТоваров.Номенклатура = ТаблицаТоваровЗеркало.Номенклатура
	|		И ТаблицаТоваров.Характеристика = ТаблицаТоваровЗеркало.Характеристика
	|		И (ТаблицаТоваров.НачалоДействия
	|			МЕЖДУ ТаблицаТоваровЗеркало.НачалоДействия И ТаблицаТоваровЗеркало.ОкончаниеДействия
	|		ИЛИ ТаблицаТоваров.ОкончаниеДействия
	|			МЕЖДУ ТаблицаТоваровЗеркало.НачалоДействия И ТаблицаТоваровЗеркало.ОкончаниеДействия
	|		ИЛИ &ВесьПериод)
	|		И (ТаблицаТоваров.НачалоДействия <> ТаблицаТоваровЗеркало.НачалоДействия
	|		ИЛИ ТаблицаТоваров.ОкончаниеДействия <> ТаблицаТоваровЗеркало.ОкончаниеДействия)
	|ГДЕ
	|	&ЭтоПрогрессивнаяШкала
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Номенклатура,
	|	ТаблицаТоваров.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДанных.Номенклатура КАК Номенклатура,
	|	ТаблицаДанных.Характеристика КАК Характеристика,
	|	МИНИМУМ(ТаблицаДанных.НомерСтроки) КАК ПерваяСтрока
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаДанных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанных КАК ТаблицаДанныхЗеркало
	|		ПО ТаблицаДанных.НомерСтроки <> ТаблицаДанныхЗеркало.НомерСтроки
	|		И ТаблицаДанных.Номенклатура = ТаблицаДанныхЗеркало.Номенклатура
	|		И ТаблицаДанных.Характеристика = ТаблицаДанныхЗеркало.Характеристика
	|		И (ТаблицаДанных.НачалоДействия МЕЖДУ ТаблицаДанныхЗеркало.НачалоДействия И ТаблицаДанныхЗеркало.ОкончаниеДействия
	|		ИЛИ ТаблицаДанных.ОкончаниеДействия
	|			МЕЖДУ ТаблицаДанныхЗеркало.НачалоДействия И ТаблицаДанныхЗеркало.ОкончаниеДействия
	|		ИЛИ &ВесьПериод)
	|		И (ТаблицаДанных.КоличествоПлан = ТаблицаДанныхЗеркало.КоличествоПлан
	|		ИЛИ ТаблицаДанных.Процент = ТаблицаДанныхЗеркало.Процент)
	|ГДЕ
	|	&ЭтоПрогрессивнаяШкала
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.Номенклатура,
	|	ТаблицаДанных.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДанных.Номенклатура КАК Номенклатура,
	|	ТаблицаДанных.Характеристика КАК Характеристика,
	|	ТаблицаДанных.КоличествоПлан КАК КоличествоПлан,
	|	МИНИМУМ(ТаблицаДанных.НомерСтроки) КАК ПерваяСтрока
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаДанных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанных КАК ТаблицаДанныхЗеркало
	|		ПО ТаблицаДанных.НомерСтроки <> ТаблицаДанныхЗеркало.НомерСтроки
	|		И ТаблицаДанных.Номенклатура = ТаблицаДанныхЗеркало.Номенклатура
	|		И ТаблицаДанных.Характеристика = ТаблицаДанныхЗеркало.Характеристика
	|		И ТаблицаДанных.КоличествоПлан = ТаблицаДанныхЗеркало.КоличествоПлан
	|ГДЕ
	|	&ЭтоШкалаПоДиапазонам
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.Номенклатура,
	|	ТаблицаДанных.Характеристика,
	|	ТаблицаДанных.КоличествоПлан";
	
	Запрос.Текст = ТекстЗапроса;
	
	ПараметрыОтбора = Новый Структура("Отменено", Ложь);
	КолонкиВыгрузки = "НомерСтроки, Номенклатура, Характеристика, КоличествоПлан, Процент, НачалоДействия, ОкончаниеДействия";
	ПодготовленныеДанные= ТаблицаДанных.Выгрузить(ПараметрыОтбора, КолонкиВыгрузки);
	Запрос.УстановитьПараметр("ПодготовленныеДанные", ПодготовленныеДанные);
	
	Запрос.УстановитьПараметр("НачалоДействия", ПараметрыПроверки.НачалоДействия);
	Запрос.УстановитьПараметр("ОкончаниеДействия", ПараметрыПроверки.ОкончаниеДействия);
	
	Запрос.УстановитьПараметр("ЭтоПрогрессивнаяШкала", ЭтоПрогрессивнаяШкала);
	Запрос.УстановитьПараметр("ЭтоШкалаПоДиапазонам", ЭтоШкалаПоДиапазонам);
	Запрос.УстановитьПараметр("ЭтоРасчетПоШкалам", ЭтоРасчетПоШкалам);
	
	ВесьПериод = (ЭтоПрогрессивнаяШкала
				  И ПараметрыПроверки.ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.ВесьПериод);
	Запрос.УстановитьПараметр("ВесьПериод", ВесьПериод);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет(); // РезультатЗапроса
	ВсегоРезультатов = РезультатЗапроса.ВГраница();
	
	ВыборкаПересеченийПериодов = РезультатЗапроса[ВсегоРезультатов - 2].Выбрать();
	ВыборкаДублейПланаПроцента = РезультатЗапроса[ВсегоРезультатов - 1].Выбрать();
	ВыборкаДублейШкалыДиапазонов = РезультатЗапроса[ВсегоРезультатов].Выбрать();
	
	Результат = Новый Структура;
	Результат.Вставить("ПересеченияПериодов", ВыборкаПересеченийПериодов);
	Результат.Вставить("ДублиПланаПроцента", ВыборкаДублейПланаПроцента);
	Результат.Вставить("ДублиПоДиапазонам", ВыборкаДублейШкалыДиапазонов);
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ТаблицаДанных - ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры
//                - ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры
//  ПараметрыПроверки - см. ПараметрыПроверкиПериодовПоКлючам
// 
// Возвращаемое значение:
//  Структура - Данные периодов товаров по ключам:
// * ПересеченияПериодов - ВыборкаИзРезультатаЗапроса:
//	** ПерваяСтрока - Число -
// 	** ЗначениеСвойства - ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
// * ДублиПланаПроцента - ВыборкаИзРезультатаЗапроса:
//	** ПерваяСтрока - Число -
// 	** ЗначениеСвойства - ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
// * ДублиПоДиапазонам - ВыборкаИзРезультатаЗапроса:
//	** ПерваяСтрока - Число -
// 	** ЗначениеСвойства - ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
// 	** КоличествоПлан - Число -
// 
Функция ДанныеПериодовСвойствНоменклатурыПоКлючам(ТаблицаДанных, ПараметрыПроверки);
	
	ЭтоПрогрессивнаяШкала = Ложь;
	ЭтоШкалаПоДиапазонам = Ложь;
	ЭтоРасчетПоШкалам = Ложь;
	
	Если ПараметрыПроверки.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		
		ЭтоПрогрессивнаяШкала = (ПараметрыПроверки.ВидШкалыРасчета = 0);
		ЭтоШкалаПоДиапазонам = (ПараметрыПроверки.ВидШкалыРасчета = 1);
		ЭтоРасчетПоШкалам =
			(ЭтоПрогрессивнаяШкала
			 ИЛИ ЭтоШкалаПоДиапазонам);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДанных.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДанных.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ТаблицаДанных.КоличествоПлан КАК КоличествоПлан,
	|	ТаблицаДанных.Процент КАК Процент,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.НачалоДействия = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &НачалоДействия
	|		ИНАЧЕ ТаблицаДанных.НачалоДействия
	|	КОНЕЦ КАК НачалоДействия,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.ОкончаниеДействия = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &ОкончаниеДействия
	|		ИНАЧЕ ТаблицаДанных.ОкончаниеДействия
	|	КОНЕЦ КАК ОкончаниеДействия
	|ПОМЕСТИТЬ ТаблицаДанных
	|ИЗ
	|	&ПодготовленныеДанные КАК ТаблицаДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанных.ЗначениеСвойства КАК ЗначениеСвойства,
	|	МИНИМУМ(ТаблицаДанных.НомерСтроки) КАК ПерваяСтрока
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаДанных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанных КАК ТаблицаДанныхЗеркало
	|		ПО ТаблицаДанных.НомерСтроки <> ТаблицаДанныхЗеркало.НомерСтроки
	|		И ТаблицаДанных.ЗначениеСвойства = ТаблицаДанныхЗеркало.ЗначениеСвойства
	|		И (ТаблицаДанных.НачалоДействия МЕЖДУ ТаблицаДанныхЗеркало.НачалоДействия И ТаблицаДанныхЗеркало.ОкончаниеДействия
	|		ИЛИ ТаблицаДанных.ОкончаниеДействия
	|			МЕЖДУ ТаблицаДанныхЗеркало.НачалоДействия И ТаблицаДанныхЗеркало.ОкончаниеДействия)
	|ГДЕ
	|	НЕ &ЭтоРасчетПоШкалам
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.ЗначениеСвойства
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТоваров.ЗначениеСвойства,
	|	МИНИМУМ(ТаблицаТоваров.НомерСтроки)
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанных КАК ТаблицаТоваровЗеркало
	|		ПО ТаблицаТоваров.НомерСтроки <> ТаблицаТоваровЗеркало.НомерСтроки
	|		И ТаблицаТоваров.ЗначениеСвойства = ТаблицаТоваровЗеркало.ЗначениеСвойства
	|		И (ТаблицаТоваров.НачалоДействия
	|			МЕЖДУ ТаблицаТоваровЗеркало.НачалоДействия И ТаблицаТоваровЗеркало.ОкончаниеДействия
	|		ИЛИ ТаблицаТоваров.ОкончаниеДействия
	|			МЕЖДУ ТаблицаТоваровЗеркало.НачалоДействия И ТаблицаТоваровЗеркало.ОкончаниеДействия
	|		ИЛИ &ВесьПериод)
	|		И (ТаблицаТоваров.НачалоДействия <> ТаблицаТоваровЗеркало.НачалоДействия
	|		ИЛИ ТаблицаТоваров.ОкончаниеДействия <> ТаблицаТоваровЗеркало.ОкончаниеДействия)
	|ГДЕ
	|	&ЭтоПрогрессивнаяШкала
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.ЗначениеСвойства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДанных.ЗначениеСвойства КАК ЗначениеСвойства,
	|	МИНИМУМ(ТаблицаДанных.НомерСтроки) КАК ПерваяСтрока
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаДанных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанных КАК ТаблицаДанныхЗеркало
	|		ПО ТаблицаДанных.НомерСтроки <> ТаблицаДанныхЗеркало.НомерСтроки
	|		И ТаблицаДанных.ЗначениеСвойства = ТаблицаДанныхЗеркало.ЗначениеСвойства
	|		И (ТаблицаДанных.НачалоДействия МЕЖДУ ТаблицаДанныхЗеркало.НачалоДействия И ТаблицаДанныхЗеркало.ОкончаниеДействия
	|		ИЛИ ТаблицаДанных.ОкончаниеДействия
	|			МЕЖДУ ТаблицаДанныхЗеркало.НачалоДействия И ТаблицаДанныхЗеркало.ОкончаниеДействия
	|		ИЛИ &ВесьПериод)
	|		И (ТаблицаДанных.КоличествоПлан = ТаблицаДанныхЗеркало.КоличествоПлан
	|		ИЛИ ТаблицаДанных.Процент = ТаблицаДанныхЗеркало.Процент)
	|ГДЕ
	|	&ЭтоПрогрессивнаяШкала
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.ЗначениеСвойства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДанных.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ТаблицаДанных.КоличествоПлан КАК КоличествоПлан,
	|	МИНИМУМ(ТаблицаДанных.НомерСтроки) КАК ПерваяСтрока
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаДанных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанных КАК ТаблицаДанныхЗеркало
	|		ПО ТаблицаДанных.НомерСтроки <> ТаблицаДанныхЗеркало.НомерСтроки
	|		И ТаблицаДанных.ЗначениеСвойства = ТаблицаДанныхЗеркало.ЗначениеСвойства
	|		И ТаблицаДанных.КоличествоПлан = ТаблицаДанныхЗеркало.КоличествоПлан
	|ГДЕ
	|	&ЭтоШкалаПоДиапазонам
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.ЗначениеСвойства,
	|	ТаблицаДанных.КоличествоПлан";
	
	Запрос.Текст = ТекстЗапроса;
	
	ПараметрыОтбора = Новый Структура("Отменено", Ложь);
	КолонкиВыгрузки = "НомерСтроки, ЗначениеСвойства, КоличествоПлан, Процент, НачалоДействия, ОкончаниеДействия";
	ПодготовленныеДанные = ТаблицаДанных.Выгрузить(ПараметрыОтбора, КолонкиВыгрузки);
	Запрос.УстановитьПараметр("ПодготовленныеДанные", ПодготовленныеДанные);
	
	Запрос.УстановитьПараметр("НачалоДействия", ПараметрыПроверки.НачалоДействия);
	Запрос.УстановитьПараметр("ОкончаниеДействия", ПараметрыПроверки.ОкончаниеДействия);
	
	Запрос.УстановитьПараметр("ЭтоПрогрессивнаяШкала", ЭтоПрогрессивнаяШкала);
	Запрос.УстановитьПараметр("ЭтоШкалаПоДиапазонам", ЭтоШкалаПоДиапазонам);
	Запрос.УстановитьПараметр("ЭтоРасчетПоШкалам", ЭтоРасчетПоШкалам);
	
	ВесьПериод = (ЭтоПрогрессивнаяШкала
				  И ПараметрыПроверки.ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.ВесьПериод);
	Запрос.УстановитьПараметр("ВесьПериод", ВесьПериод);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет(); // РезультатЗапроса
	ВсегоРезультатов = РезультатЗапроса.ВГраница();
	
	ВыборкаПересеченийПериодов = РезультатЗапроса[ВсегоРезультатов - 2].Выбрать();
	ВыборкаДублейПланаПроцента = РезультатЗапроса[ВсегоРезультатов - 1].Выбрать();
	ВыборкаДублейШкалыДиапазонов = РезультатЗапроса[ВсегоРезультатов].Выбрать();
	
	Результат = Новый Структура;
	Результат.Вставить("ПересеченияПериодов", ВыборкаПересеченийПериодов);
	Результат.Вставить("ДублиПланаПроцента", ВыборкаДублейПланаПроцента);
	Результат.Вставить("ДублиПоДиапазонам", ВыборкаДублейШкалыДиапазонов);
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ОбрабатываемаяТаблицаТоваров - ДанныеФормыКоллекция, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Товары - Обрабатываемая таблица товаров
//  МенеджерВТ - МенеджерВременныхТаблиц
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//  * НомерСтроки - Число
//  * Номенклатура - СправочникСсылка.Номенклатура
//  * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//  * ХарактеристикиИспользуются - Булево
//
Функция ВыборкаТоваровБезПроцентовБонуса(ОбрабатываемаяТаблицаТоваров, МенеджерВТ)
	
	СоздатьВременнуюТаблицуТоваровДляПроверкиДиапазонов(ОбрабатываемаяТаблицаТоваров, МенеджерВТ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МИНИМУМ(ВтТовары.НомерСтроки) КАК НомерСтроки,
	|	ВтТовары.Номенклатура КАК Номенклатура,
	|	ВтТовары.Характеристика КАК Характеристика
	|ИЗ
	|	ВтТовары КАК ВтТовары
	|ГДЕ
	|НЕ ВтТовары.Отменено
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтТовары.Характеристика,
	|	ВтТовары.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ВтТовары.Процент = 0
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

// Параметры:
//  ОбрабатываемаяТаблица - ДанныеФормыКоллекция, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры - Обрабатываемая таблица
//  МенеджерВТ - МенеджерВременныхТаблиц
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//  * НомерСтроки - Число
//  * ЗначениеСвойства - ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
//
Функция ВыборкаСвойствНоменклатурыБезПроцентовБонуса(ОбрабатываемаяТаблица, МенеджерВТ)
	
	СоздатьВременнуюТаблицуСвойствДляПроверкиДиапазонов(ОбрабатываемаяТаблица, МенеджерВТ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МИНИМУМ(ВтСвойстваНоменклатуры.НомерСтроки) КАК НомерСтроки,
	|	ВтСвойстваНоменклатуры.ЗначениеСвойства КАК ЗначениеСвойства
	|ИЗ
	|	ВтСвойстваНоменклатуры КАК ВтСвойстваНоменклатуры
	|ГДЕ
	|НЕ ВтСвойстваНоменклатуры.Отменено
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтСвойстваНоменклатуры.ЗначениеСвойства
	|
	|ИМЕЮЩИЕ
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ВтСвойстваНоменклатуры.Процент = 0
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

// Параметры:
//  ОбъектПроверки - см. ПроверитьУказаниеПустыхЗначенийХарактеристик
//   РеквизитыВида - Структура:
//  	* ПоказательТоваров - ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//	* НомерСтроки - Число -
//	* Номенклатура - СправочникСсылка.Номенклатура
//
Функция ВыборкаПустыхИЗаполненныхХарактеристик(ОбъектПроверки, РеквизитыВида)
	
	ОтборТоваров = ОбъектПроверки.ОтборТоваров;
	ПериодичностьУсловий = ОбъектПроверки.ПериодичностьУсловий;
	НачалоДействия = ОбъектПроверки.НачалоДействия;
	ОкончаниеДействия = ОбъектПроверки.ОкончаниеДействия;
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Отменено", Ложь);
	
	Запрос = Новый Запрос;
	
	Если ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.КромеВыбранных
	 ИЛИ (ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.ВесьПериод
	      И РеквизитыВида.ПоказательТоваров =  Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество) Тогда
		
		ТекстЗапроса = ТекстЗапросаДляПустыхИЗаполненныхХарактеристик();
		Колонки = "НомерСтроки, Номенклатура, Характеристика";
		
	Иначе
		
		ТекстЗапроса = ТекстЗапросаДляПустыхИЗаполненныхХарактеристикСУчетомПериода();
		Колонки = "НомерСтроки, Номенклатура, Характеристика, НачалоДействия, ОкончаниеДействия";
		Запрос.УстановитьПараметр("НачалоДействия", НачалоДействия);
		Запрос.УстановитьПараметр("ОкончаниеДействия", ОкончаниеДействия);
		
	КонецЕсли;
	
	ТаблицаПроверки = ОбъектПроверки.Товары.Выгрузить(ОтборСтрок, Колонки);
	Запрос.УстановитьПараметр("ТаблицаПроверки", ТаблицаПроверки);
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

Функция ТекстЗапросаДляПустыхИЗаполненныхХарактеристикСУчетомПериода()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаПроверки.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПроверки.Номенклатура КАК Номенклатура,
	|	ТаблицаПроверки.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаПроверки.НачалоДействия = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &НачалоДействия
	|		ИНАЧЕ ТаблицаПроверки.НачалоДействия
	|	КОНЕЦ КАК НачалоДействия,
	|	ВЫБОР
	|		КОГДА ТаблицаПроверки.ОкончаниеДействия = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &ОкончаниеДействия
	|		ИНАЧЕ ТаблицаПроверки.ОкончаниеДействия
	|	КОНЕЦ КАК ОкончаниеДействия
	|ПОМЕСТИТЬ ТаблицаПроверки
	|ИЗ
	|	&ТаблицаПроверки КАК ТаблицаПроверки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПроверки.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПроверки.Номенклатура КАК Номенклатура,
	|	ТаблицаПроверки.НачалоДействия КАК НачалоДействия,
	|	ТаблицаПроверки.ОкончаниеДействия КАК ОкончаниеДействия
	|ПОМЕСТИТЬ ВТ_СтрокиБезХарактеристики
	|ИЗ
	|	ТаблицаПроверки КАК ТаблицаПроверки
	|ГДЕ
	|	ТаблицаПроверки.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_СтрокиБезХарактеристики.НомерСтроки КАК НомерСтроки,
	|	ВТ_СтрокиБезХарактеристики.Номенклатура КАК Номенклатура
	|ИЗ
	|	ВТ_СтрокиБезХарактеристики КАК ВТ_СтрокиБезХарактеристики
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПроверки КАК ТаблицаПроверки
	|		ПО ВТ_СтрокиБезХарактеристики.Номенклатура = ТаблицаПроверки.Номенклатура
	|			И ВТ_СтрокиБезХарактеристики.НачалоДействия <= ТаблицаПроверки.ОкончаниеДействия
	|			И ВТ_СтрокиБезХарактеристики.ОкончаниеДействия >= ТаблицаПроверки.НачалоДействия
	|			И (ТаблицаПроверки.Характеристика <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДляПустыхИЗаполненныхХарактеристик()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаПроверки.НомерСтроки КАК НомерСтроки,
	|	ТаблицаПроверки.Номенклатура КАК Номенклатура,
	|	ТаблицаПроверки.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ТаблицаПроверки
	|ИЗ
	|	&ТаблицаПроверки КАК ТаблицаПроверки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаПроверки.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаПроверки.Номенклатура КАК Номенклатура
	|ИЗ
	|	ТаблицаПроверки КАК ТаблицаПроверки
	|СГРУППИРОВАТЬ ПО
	|	Номенклатура,
	|	ТаблицаПроверки.Номенклатура
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
	|		КОГДА Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ) > 1";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращаемое значение:
//  СтрокаДереваЗначений, ДанныеФормыЭлементДерева - см. РаботаСФормулами.НоваяСтрокаДереваОперанда
//
Функция ПолучитьДеревоОперандовНоменклатуры()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДеревоОперандов = РаботаСФормулами.ПолучитьПустоеДеревоОперандов();
	
	КорневаяСтрокаДерева = РаботаСФормулами.НоваяСтрокаДереваОперанда(ДеревоОперандов);
	КорневаяСтрокаДерева.Идентификатор = "Номенклатура";
	КорневаяСтрокаДерева.Представление = НСтр("ru = 'Номенклатура';
												|en = 'Item'");
	КорневаяСтрокаДерева.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	КорневаяСтрокаДерева.РазрешаетсяВыборОперанда = Ложь;
	КорневаяСтрокаДерева.ВключаетсяВИдентификатор = Ложь;
	КорневаяСтрокаДерева.РазворачиватьДоРеквизитов = Истина;
	
	// Формирование второго слоя операндов. Переопределяется состав доп. реквизитов (из набора свойств).
	// 1. Описание дополнительных ограничений на слой реквизитов и дополнительных реквизитов.
	ОграниченияРазвертки = РаботаСФормулами.ОграниченияРазверткиОперандов();
	ОграниченияРазвертки.РекурсивноРазворачиватьОперандыСхемыКомпоновки = Ложь;
	РаботаСФормулами.РазвернутьСтрокуОперанда(КорневаяСтрокаДерева, ОграниченияРазвертки);
	
	Возврат КорневаяСтрокаДерева;
	
КонецФункции

Функция ЯвляетсяРеквизитомНоменклатуры(Знач ИдентификаторРеквизита)
	
	Результат = Истина;
	МетаданныеОбъекта = Метаданные.Справочники.Номенклатура;
	
	Если Не ОбщегоНазначения.ЭтоСтандартныйРеквизит(МетаданныеОбъекта.СтандартныеРеквизиты, ИдентификаторРеквизита) Тогда
		
		Результат = ОбщегоНазначения.ЕстьРеквизитОбъекта(ИдентификаторРеквизита, МетаданныеОбъекта);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЯвляетсяДополнительнымРеквизитомНоменклатуры(Знач ИдентификаторРеквизита)
	
	Результат = ДанныеРеквизитаНоменклатуры();
	ИдентификаторДопРеквизитаСвойства = РаботаСФормулами.СвойстваХарактеристики(ИдентификаторРеквизита).Идентификатор;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения") Тогда
		
		Результат.ДополнительныйРеквизит = Истина;
		Результат.Заголовок = ИдентификаторДопРеквизитаСвойства;
		Возврат Результат;
		
	КонецЕсли;
	
	Результат.ДоступенПоФункциональнымОпциям = Истина;
	ОбщийНабор = УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Номенклатура_Общие");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДополнительныеРеквизитыИСведения.Ссылка КАК Свойство,
	|	ДополнительныеРеквизитыИСведения.ТипЗначения КАК ТипЗначения,
	|	ДополнительныеРеквизитыИСведения.ПометкаУдаления КАК ПометкаУдаления
	|ПОМЕСТИТЬ ВТ_ДополнительныеРеквизиты
	|ИЗ
	|	ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|ГДЕ
	|	&ВыражениеИдентификаторДляФормул = &ИдентификаторыДопРеквизита
	|	И НЕ ДополнительныеРеквизитыИСведения.ЭтоДополнительноеСведение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыНоменклатуры.НаборСвойств КАК НаборСвойств
	|ПОМЕСТИТЬ ВТ_НаборыСвойствНоменклатуры
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|ГДЕ
	|	НЕ ВидыНоменклатуры.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ОбщийНабор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДополнительныеРеквизиты.Свойство КАК Свойство,
	|	ВТ_ДополнительныеРеквизиты.ТипЗначения КАК ТипЗначения,
	|	НаборыДополнительныеРеквизиты.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_НаборыДополнительныхРеквизитов
	|ИЗ
	|	ВТ_ДополнительныеРеквизиты КАК ВТ_ДополнительныеРеквизиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныеРеквизиты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений КАК НаборыДополнительныхРеквизитовИСведений
	|			ПО НаборыДополнительныеРеквизиты.Ссылка = НаборыДополнительныхРеквизитовИСведений.Ссылка
	|				И (НаборыДополнительныхРеквизитовИСведений.Используется)
	|		ПО ВТ_ДополнительныеРеквизиты.Свойство = НаборыДополнительныеРеквизиты.Свойство
	|			И (НЕ НаборыДополнительныеРеквизиты.ПометкаУдаления)
	|ГДЕ
	|	НЕ НаборыДополнительныхРеквизитовИСведений.ЭтоГруппа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТ_НаборыДополнительныхРеквизитов.Свойство КАК Свойство,
	|	ВТ_НаборыДополнительныхРеквизитов.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ВТ_НаборыДополнительныхРеквизитов КАК ВТ_НаборыДополнительныхРеквизитов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_НаборыСвойствНоменклатуры КАК ВТ_НаборыСвойствНоменклатуры
	|		ПО ВТ_НаборыДополнительныхРеквизитов.Ссылка = ВТ_НаборыСвойствНоменклатуры.НаборСвойств
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТ_НаборыДополнительныхРеквизитов.Ссылка КАК Ссылка,
	|	ВТ_НаборыДополнительныхРеквизитов.Свойство КАК Свойство,
	|	ВТ_НаборыДополнительныхРеквизитов.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ВТ_НаборыДополнительныхРеквизитов КАК ВТ_НаборыДополнительныхРеквизитов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НаборыСвойствНоменклатуры КАК ВТ_НаборыСвойствНоменклатуры
	|		ПО ВТ_НаборыДополнительныхРеквизитов.Ссылка = ВТ_НаборыСвойствНоменклатуры.НаборСвойств
	|ГДЕ
	|	ВТ_НаборыСвойствНоменклатуры.НаборСвойств ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДополнительныеРеквизиты.Свойство КАК Свойство,
	|	ВТ_ДополнительныеРеквизиты.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ВТ_ДополнительныеРеквизиты КАК ВТ_ДополнительныеРеквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныеРеквизиты
	|		ПО ВТ_ДополнительныеРеквизиты.Свойство = НаборыДополнительныеРеквизиты.Свойство
	|ГДЕ
	|	НаборыДополнительныеРеквизиты.Свойство ЕСТЬ NULL
	|	И НЕ ВТ_ДополнительныеРеквизиты.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДополнительныеРеквизиты.Свойство КАК Свойство,
	|	ВТ_ДополнительныеРеквизиты.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ВТ_ДополнительныеРеквизиты КАК ВТ_ДополнительныеРеквизиты
	|ГДЕ
	|	ВТ_ДополнительныеРеквизиты.ПометкаУдаления";
	
	ВыражениеИдентификаторДляФормул = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"ДополнительныеРеквизитыИСведения.%1",
		РаботаСФормулами.ПолеИдентификатораДополнительныхРеквизитовИСведений());
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеИдентификаторДляФормул", ВыражениеИдентификаторДляФормул);
	
	Запрос.УстановитьПараметр("ОбщийНабор", ОбщийНабор);
	Запрос.УстановитьПараметр("ИдентификаторыДопРеквизита", ИдентификаторДопРеквизитаСвойства);
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = РезультатЗапроса.ВГраница();
	РеквизитПринадлежитНаборуНоменклатуры = РезультатЗапроса[КоличествоРезультатов - 3];
	РеквизитПринадлежитДругомуНабору = РезультатЗапроса[КоличествоРезультатов - 2];
	РеквизитНеПринадлежитНабору = РезультатЗапроса[КоличествоРезультатов - 1];
	РеквизитПомеченНаУдаление = РезультатЗапроса[КоличествоРезультатов];
	
	ДанныеРеквизита = Новый Структура;
	ДанныеРеквизита.Вставить("ЯвляетсяРеквизитом", Ложь);
	ДанныеРеквизита.Вставить("ПринадлежитДругомуНабору", Ложь);
	ДанныеРеквизита.Вставить("НеПринадлежитНабору", Ложь);
	ДанныеРеквизита.Вставить("ПометкаУдаления", Ложь);
	ДанныеРеквизита.Вставить("РеквизитНеНайден", Ложь);
	
	Если НЕ РеквизитПринадлежитНаборуНоменклатуры.Пустой() Тогда
		
		ДанныеРеквизита.ЯвляетсяРеквизитом = Истина;
		Выборка = РеквизитПринадлежитНаборуНоменклатуры.Выбрать();
		ЗаполнитьДанныеДополнительногоРеквизитаНоменклатуры(Результат, ДанныеРеквизита, Выборка);
		
	ИначеЕсли НЕ РеквизитПринадлежитДругомуНабору.Пустой() Тогда
		
		ДанныеРеквизита.ПринадлежитДругомуНабору = Истина;
		Выборка = РеквизитПринадлежитДругомуНабору.Выбрать();
		ЗаполнитьДанныеДополнительногоРеквизитаНоменклатуры(Результат, ДанныеРеквизита, Выборка);
		
	ИначеЕсли НЕ РеквизитНеПринадлежитНабору.Пустой() Тогда
		
		ДанныеРеквизита.НеПринадлежитНабору = Истина;
		Выборка = РеквизитНеПринадлежитНабору.Выбрать();
		ЗаполнитьДанныеДополнительногоРеквизитаНоменклатуры(Результат, ДанныеРеквизита, Выборка);
		
	ИначеЕсли НЕ РеквизитПомеченНаУдаление.Пустой() Тогда
		
		ДанныеРеквизита.ПометкаУдаления = Истина;
		Выборка = РеквизитПомеченНаУдаление.Выбрать();
		ЗаполнитьДанныеДополнительногоРеквизитаНоменклатуры(Результат, ДанныеРеквизита, Выборка);
		
	Иначе
		
		ДанныеРеквизита.РеквизитНеНайден = Истина;
		ЗаполнитьДанныеДополнительногоРеквизитаНоменклатуры(Результат, ДанныеРеквизита);
		Результат.Заголовок = ИдентификаторДопРеквизитаСвойства;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Заполнить данные реквизита номенклатуры.
// 
// Параметры:
//  Результат - см. ДанныеРеквизитаНоменклатуры
//  ДанныеРеквизита - Структура:
//   * ЯвляетсяРеквизитом - Булево
//   * ПринадлежитДругомуНабору - Булево
//   * НеПринадлежитНабору - Булево
//   * ПометкаУдаления - Булево
//   * РеквизитНеНайден - Булево
//  Выборка - ВыборкаИзРезультатаЗапроса:
//   * ТипЗначения - ОписаниеТипов
//   * Свойство - ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения
//
Процедура ЗаполнитьДанныеДополнительногоРеквизитаНоменклатуры(Результат, ДанныеРеквизита, Выборка = Неопределено)
	
	Результат.ДополнительныйРеквизит = Истина;
	ЗаполнитьЗначенияСвойств(Результат, ДанныеРеквизита);
	РазрешенныйТипЗначения = Ложь;
	
	Если Выборка <> Неопределено Тогда
		
		Выборка.Следующий();
		ТипЗначения = Выборка.ТипЗначения; // ОписаниеТипов
		Свойство = Выборка.Свойство; // ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения
		Результат.Свойство = Свойство;
		Результат.ТипЗначения = ТипЗначения;
		Результат.Заголовок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Свойство, "Заголовок",, ТекущийЯзык().КодЯзыка);
		РазрешенныйТипЗначения = РазрешенныйТипЗначенияДляСвойстваНоменклатуры(ТипЗначения);
		
	КонецЕсли;
	
	Результат.РазрешенныйТипЗначения = РазрешенныйТипЗначения;
	Результат.РазрешеноИспользовать = (Результат.ЯвляетсяРеквизитом
										И НЕ Результат.ПометкаУдаления
										И НЕ Результат.РеквизитНеНайден
										И РазрешенныйТипЗначения);
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура - Данные реквизита номенклатуры:
// * ЯвляетсяРеквизитом - Булево - 
// * Свойство - ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения - 
// * ТипЗначения - ОписаниеТипов - 
// * Заголовок - Строка - 
// * ДоступенПоФункциональнымОпциям - Булево - 
// * ДополнительныйРеквизит - Булево - 
// * ПометкаУдаления - Булево - 
// * РеквизитНеНайден - Булево
// * РазрешенныйТипЗначения - Булево
// * РазрешеноИспользовать - Булево
// * ПринадлежитДругомуНабору - Булево
// * НеПринадлежитНабору - Булево
// 
Функция ДанныеРеквизитаНоменклатуры()
	
	ДанныеРеквизита = Новый Структура;
	ДанныеРеквизита.Вставить("ЯвляетсяРеквизитом", Ложь);
	ДанныеРеквизита.Вставить("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка());
	ДанныеРеквизита.Вставить("ТипЗначения", Новый ОписаниеТипов());
	ДанныеРеквизита.Вставить("Заголовок", "");
	ДанныеРеквизита.Вставить("ДоступенПоФункциональнымОпциям", Ложь);
	ДанныеРеквизита.Вставить("ДополнительныйРеквизит", Ложь);
	ДанныеРеквизита.Вставить("ПометкаУдаления", Ложь);
	ДанныеРеквизита.Вставить("РеквизитНеНайден", Ложь);
	ДанныеРеквизита.Вставить("РазрешенныйТипЗначения", Ложь);
	ДанныеРеквизита.Вставить("РазрешеноИспользовать", Ложь);
	ДанныеРеквизита.Вставить("ПринадлежитДругомуНабору", Ложь);
	ДанныеРеквизита.Вставить("НеПринадлежитНабору", Ложь);
	
	Возврат ДанныеРеквизита;
	
КонецФункции

// Параметры:
//  ПричинаЗапрета - Структура:
//   * КраткоеОписание - Строка
//   * ПодробноеОписание - Строка
//   * ТекстСообщения - Строка
//  РезультатПроверки - см. ДанныеРеквизитаНоменклатуры
//  ВидРетроБонусаИспользуется - Булево
// 
Процедура ПричинаЗапретаДляРеквизита(ПричинаЗапрета, Знач РезультатПроверки, Знач ВидРетроБонусаИспользуется)
	
	КраткоеОписание = "";
	ПодробноеОписание = "";
	ТекстСообщения = "";
	
	Если РезультатПроверки.РеквизитНеНайден Тогда
		
		Если ВидРетроБонусаИспользуется Тогда
			
			ПодробноеОписание = Нстр("ru = 'Реквизит был удален или переименован. Ранее введенные документы являются некорректными. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство с тем же типом значения, что и у предыдущего. Обратитесь к администратору системы.';
									|en = 'The attribute is deleted or renamed. Previously entered documents are incorrect. Cannot use the rebate kind in documents. Select another property with the same value type as the previous one. Contact the system administrator.'");
			
		Иначе
			
			ПодробноеОписание = Нстр("ru = 'Реквизит был удален или переименован. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство.';
									|en = 'The attribute is deleted or renamed. Cannot use the rebate kind in documents. Select a different property.'");
			
		КонецЕсли;
		
		ШаблонКраткоеОписание = НСтр("ru = 'Свойство не найдено (<a href=""%1"">подробнее</a>)';
									|en = 'The property is not found (<a href=""%1"">details</a>)'");
		ШаблонСообщения = НСтр("ru = 'Свойство номенклатуры ""%1"" не найдено (было удалено или переименовано).';
								|en = 'The ""%1"" item property is not found (it was deleted or renamed).'");
		
	ИначеЕсли НЕ РезультатПроверки.ДоступенПоФункциональнымОпциям
	   И РезультатПроверки.ЯвляетсяРеквизитом Тогда
		
		Если ВидРетроБонусаИспользуется Тогда
			
			ПодробноеОписание = Нстр("ru = 'Отключена функциональная опция, разрешающая использование свойства. Ранее введенные документы являются некорректными. Вид ретро-бонуса не может использоваться в документах. Необходимо включить использование свойства в настройках системы или выбрать другое свойство с тем же типом значения, что и у предыдущего. Обратитесь к администратору системы.';
									|en = 'The functional option that allows the use of the property is disabled. Previously entered documents are invalid. You cannot use the rebate kind in documents. Enable the use of the property in the system settings, or select another property with the same value type as the previous one. Contact your system administrator.'");
			
		Иначе
			
			ПодробноеОписание = Нстр("ru = 'Отключена функциональная опция, разрешающая использование свойства. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство.';
									|en = 'The functional option that allows the use of the property is disabled. Previously entered documents are invalid. You cannot use the rebate kind in documents. Select another property.'");
			
		КонецЕсли;
		
		ШаблонКраткоеОписание = НСтр("ru = 'Свойство недоступно (<a href=""%1"">подробнее</a>)';
									|en = 'The property is unavailable (<a href=""%1"">details</a>)'");
		ШаблонСообщения = НСтр("ru = 'Отключена функциональная опция, разрешающая использование свойства ""%1"".';
								|en = 'The functional option that allows using the ""%1"" property is disabled.'");
		
	ИначеЕсли НЕ РезультатПроверки.РазрешенныйТипЗначения
	   И РезультатПроверки.ЯвляетсяРеквизитом Тогда
		
		Если ВидРетроБонусаИспользуется Тогда
			
			ПодробноеОписание = Нстр("ru = 'Реквизит имеет запрещенный тип. Ранее введенные документы являются некорректными. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство с допустимым типом. Обратитесь к администратору системы.';
									|en = 'The attribute has a prohibited type. Previously entered documents are incorrect. Cannot use the rebate kind in documents. Select another property with a valid type. Contact the system administrator.'");
			
		Иначе
			
			ПодробноеОписание = Нстр("ru = 'Реквизит имеет запрещенный тип. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство с допустимым типом.';
									|en = 'The attribute has a prohibited type. Cannot use the rebate kind in documents. Select another property with a valid type.'");
			
		КонецЕсли;
		
		ШаблонКраткоеОписание = НСтр("ru = 'Свойство имеет недопустимый тип (<a href=""%1"">подробнее</a>)';
									|en = 'The property has an invalid type (<a href=""%1"">details</a>)'");
		ШаблонСообщения = НСтр("ru = 'Свойство номенклатуры ""%1"" имеет недопустимый тип и не может использоваться в настройках видов ретро-бонусов.';
								|en = 'The ""%1"" item property has an invalid type and cannot be used in rebate kind settings.'");
		
	Иначе
		
		Если ВидРетроБонусаИспользуется Тогда
			
			ПодробноеОписание = Нстр("ru = 'Реквизит не найден. Ранее введенные документы являются некорректными. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство с тем же типом значения, что и у предыдущего. Обратитесь к администратору системы.';
									|en = 'The attribute is not found. Previously entered documents are incorrect. Cannot use the rebate kind in documents. Select another property with the same value type as the previous one. Contact the system administrator.'");
			
		Иначе
			
			ПодробноеОписание = Нстр("ru = 'Реквизит не найден. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство.';
									|en = 'The attribute is not found. Cannot use the rebate kind in documents. Select a different property.'");
			
		КонецЕсли;
		
		ШаблонКраткоеОписание = НСтр("ru = 'Свойство не найдено (<a href=""%1"">подробнее</a>)';
									|en = 'The property is not found (<a href=""%1"">details</a>)'");
		ШаблонСообщения = НСтр("ru = 'Свойство номенклатуры ""%1"" не найдено (было удалено или переименовано).';
								|en = 'The ""%1"" item property is not found (it was deleted or renamed).'");
		
	КонецЕсли;
	
	ПоказатьПодробноеОписание = "ПоказатьПодробноеОписание";
	КраткоеОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонКраткоеОписание,
			ПоказатьПодробноеОписание);
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения,
			РезультатПроверки.Заголовок);
	
	ПричинаЗапрета.КраткоеОписание = КраткоеОписание;
	ПричинаЗапрета.ПодробноеОписание = ПодробноеОписание;
	ПричинаЗапрета.ТекстСообщения = ТекстСообщения;
	
КонецПроцедуры

// Параметры:
//  ПричинаЗапрета - Структура:
//   * КраткоеОписание - Строка
//   * ПодробноеОписание - Строка
//   * ТекстСообщения - Строка
//  РезультатПроверки - см. ДанныеРеквизитаНоменклатуры
//  ВидРетроБонусаИспользуется - Булево
// 
Процедура ПричинаЗапретаДляДополнительногоРеквизита(ПричинаЗапрета, Знач РезультатПроверки, Знач ВидРетроБонусаИспользуется)
	
	КраткоеОписание = "";
	ПодробноеОписание = "";
	ТекстСообщения = "";
	
	Если НЕ РезультатПроверки.ДоступенПоФункциональнымОпциям Тогда
			
		Если ВидРетроБонусаИспользуется Тогда
			
			ПодробноеОписание = Нстр("ru = 'Выбран дополнительный реквизит (свойство), при этом отключено использование свойств. Ранее введенные документы являются некорректными. Вид ретро-бонуса не может использоваться в документах. Необходимо включить использование свойств в настройках системы или выбрать другое свойство с тем же типом значения, что и у предыдущего. Обратитесь к администратору системы.';
									|en = 'The additional attribute (property) is selected, while properties are disabled. Previously entered documents are incorrect. Cannot use the rebate kind in documents. Enable properties in the system settings or select another property with the same value type as the previous one. Contact the system administrator.'");
			
		Иначе
			
			ПодробноеОписание = Нстр("ru = 'Выбран дополнительный реквизит (свойство), при этом отключено использование свойств. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство.';
									|en = 'The additional attribute (property) is selected, while properties are disabled. Cannot use the rebate kind in documents. Select a different property.'");
			
		КонецЕсли;
		
		ШаблонКраткоеОписание = НСтр("ru = 'Отключено использование свойств (<a href=""%1"">подробнее</a>)';
									|en = 'Properties are disabled (<a href=""%1"">details</a>)'");
		ШаблонСообщения = НСтр("ru = 'Выбран дополнительный реквизит ""%1"" при этом отключено использование свойств.';
								|en = 'The ""%1"" additional attribute is selected, while properties are disabled.'");
		
	ИначеЕсли РезультатПроверки.НеПринадлежитНабору
	  ИЛИ РезультатПроверки.ПринадлежитДругомуНабору Тогда
		
		Если ВидРетроБонусаИспользуется Тогда
			
			ПодробноеОписание = Нстр("ru = 'Дополнительный реквизит (свойство) больше не используется. В случае удаления реквизита документы по данному виду станут некорректными. Вид ретро-бонуса не может использоваться в документах. Рекомендуем восстановить действие дополнительного реквизита или выбрать другое свойство в виде ретро-бонуса. Обратитесь к администратору системы.';
									|en = 'The additional attribute (property) is no longer used. If you delete the attribute, the documents for this kind will become incorrect. Cannot use the rebate kind in documents. We recommend restoring the additional attribute or choosing another property in the rebate kind. Contact the system administrator.'");
			
		Иначе
			
			ПодробноеОписание = Нстр("ru = 'Дополнительный реквизит (свойство) больше не используется. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство.';
									|en = 'The additional attribute (property) is no longer used. Cannot use the rebate kind in documents. Select a different property.'");
			
		КонецЕсли;
		
		ШаблонКраткоеОписание = НСтр("ru = 'Свойство не используется (<a href=""%1"">подробнее</a>)';
									|en = 'The property is not used (<a href=""%1"">details</a>)'");
		ШаблонСообщения = НСтр("ru = 'Дополнительный реквизит ""%1"" больше не используется.';
								|en = 'The ""%1"" additional attribute is no longer used.'");
		
	ИначеЕсли РезультатПроверки.ПометкаУдаления Тогда
		
		Если ВидРетроБонусаИспользуется Тогда
			
			ПодробноеОписание = Нстр("ru = 'Дополнительный реквизит (свойство) помечен на удаление. В случае удаления реквизита документы по данному виду станут некорректными. Вид ретро-бонуса не может использоваться в документах. Рекомендуем восстановить действие дополнительного реквизита или выбрать другое свойство в виде ретро-бонуса. Обратитесь к администратору системы.';
									|en = 'The additional attribute (property) is marked for deletion. If you delete the attribute, the documents for this kind will become incorrect. Cannot use the rebate kind in documents. We recommend restoring the additional attribute or choosing another property in the rebate kind. Contact the system administrator.'");
			
		Иначе
			
			ПодробноеОписание = Нстр("ru = 'Дополнительный реквизит (свойство) помечен на удаление. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство.';
									|en = 'The additional attribute (property) is marked for deletion. Cannot use the rebate kind in documents. Select a different property.'");
			
		КонецЕсли;
		
		ШаблонКраткоеОписание = НСтр("ru = 'Свойство помечено на удаление (<a href=""%1"">подробнее</a>)';
									|en = 'The property is marked for deletion (<a href=""%1"">details</a>)'");
		ШаблонСообщения = НСтр("ru = 'Дополнительный реквизит ""%1"" помечен на удаление.';
								|en = 'The ""%1"" additional attribute is marked for deletion.'");
		
	ИначеЕсли РезультатПроверки.РеквизитНеНайден Тогда
		
		Если ВидРетроБонусаИспользуется Тогда
			
			ПодробноеОписание = Нстр("ru = 'Дополнительный реквизит (свойство) был удален или у него был изменен идентификатор. Ранее введенные документы являются некорректными. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство с тем же типом значения, что и у предыдущего. Обратитесь к администратору системы.';
									|en = 'Additional attribute (property) is deleted, or its ID is changed. Previously entered documents are incorrect. Cannot use the rebate kind in documents. Select another property with the same value type as the previous one. Contact the system administrator.'");
			
		Иначе
			
			ПодробноеОписание = Нстр("ru = 'Дополнительный реквизит (свойство) был удален или у него был изменен идентификатор. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство.';
									|en = 'Additional attribute (property) is deleted, or its ID is changed. Cannot use the rebate kind in documents. Select a different property.'");
			
		КонецЕсли;
		
		ШаблонКраткоеОписание = НСтр("ru = 'Свойство не найдено (<a href=""%1"">подробнее</a>)';
									|en = 'The property is not found (<a href=""%1"">details</a>)'");
		ШаблонСообщения = НСтр("ru = 'Дополнительный реквизит не найден по идентификатору ""%1"" (реквизит был удален или у него изменен идентификатор).';
								|en = 'The additional attribute is not found by ID ""%1"" (the attribute is deleted, or its ID is changed).'");
		
	ИначеЕсли НЕ РезультатПроверки.РазрешенныйТипЗначения
	   И РезультатПроверки.ЯвляетсяРеквизитом Тогда
		
		Если ВидРетроБонусаИспользуется Тогда
			
			ПодробноеОписание = Нстр("ru = 'Дополнительный реквизит (свойство) имеет запрещенный тип. Ранее введенные документы являются некорректными. Вид ретро-бонуса не может использоваться в документах. Необходимо изменить тип свойства или выбрать другое свойство с допустимым типом. Обратитесь к администратору системы.';
									|en = 'Additional attribute (property) has a prohibited type. Previously entered documents are incorrect. Cannot use the rebate kind in documents. Change the property type or select another property with a valid type. Contact the system administrator.'");
			
		Иначе
			
			ПодробноеОписание = Нстр("ru = 'Дополнительный реквизит (свойство) имеет запрещенный тип. Вид ретро-бонуса не может использоваться в документах. Необходимо изменить тип свойства или выбрать другое свойство с допустимым типом.';
									|en = 'Additional attribute (property) has a prohibited type. Cannot use the rebate kind in documents. Change the property type or select another property with a valid type.'");
			
		КонецЕсли;
		
		ШаблонКраткоеОписание = НСтр("ru = 'Свойство имеет недопустимый тип (<a href=""%1"">подробнее</a>)';
									|en = 'The property has an invalid type (<a href=""%1"">details</a>)'");
		ШаблонСообщения = НСтр("ru = 'Дополнительный реквизит ""%1""  имеет недопустимый тип и не может использоваться в настройках видов ретро-бонусов.';
								|en = 'The ""%1"" additional attribute has an invalid type and cannot be used in rebate kind settings.'");
		
	Иначе
		
		Если ВидРетроБонусаИспользуется Тогда
			
			ПодробноеОписание = Нстр("ru = 'Дополнительный реквизит (свойство) не найден. Ранее введенные документы являются некорректными. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство с тем же типом значения, что и у предыдущего. Обратитесь к администратору системы.';
									|en = 'Additional attribute (property) is not found. Previously entered documents are incorrect. Cannot use the rebate kind in documents. Select another property with the same value type as the previous one. Contact the system administrator.'");
			
		Иначе
			
			ПодробноеОписание = Нстр("ru = 'Дополнительный реквизит (свойство) не найден. Вид ретро-бонуса не может использоваться в документах. Необходимо выбрать другое свойство.';
									|en = 'Additional attribute (property) is not found. Cannot use the rebate kind in documents. Select a different property.'");
			
		КонецЕсли;
		
		ШаблонКраткоеОписание = НСтр("ru = 'Свойство не найдено (<a href=""%1"">подробнее</a>)';
									|en = 'The property is not found (<a href=""%1"">details</a>)'");
		ШаблонСообщения = НСтр("ru = 'Дополнительный реквизит не найден по идентификатору ""%1"" (реквизит был удален или у него изменен идентификатор).';
								|en = 'The additional attribute is not found by ID ""%1"" (the attribute is deleted, or its ID is changed).'");
		
	КонецЕсли;
	
	ПоказатьПодробноеОписание = "ПоказатьПодробноеОписание";
	КраткоеОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонКраткоеОписание,
		ПоказатьПодробноеОписание);
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения,
			РезультатПроверки.Заголовок);
	
	ПричинаЗапрета.КраткоеОписание = КраткоеОписание;
	ПричинаЗапрета.ПодробноеОписание = ПодробноеОписание;
	ПричинаЗапрета.ТекстСообщения = ТекстСообщения;
	
КонецПроцедуры

// Блокировка флага отмены в исправлении документа
//
// Параметры:
//  УсловноеОформление - УсловноеОформлениеКомпоновкиДанных -
//  ТаблицаФормы - ТаблицаФормы - Таблица формы
// 
Процедура ОформлениеФлагаОтмены(УсловноеОформление, ТаблицаФормы)
	
	ПутьКТЧ = ТаблицаФормы.ПутьКДанным;
	ИмяТЧ = ТаблицаФормы.Имя;
	ИмяОтменено = ИмяТЧ + "Отменено";
	ПутьКИсходнаяСтрока = ПутьКТЧ + ".ИсходнаяСтрока";
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяОтменено);
	
	ГруппаИЛИ = НоваяГруппаОтборов(Элемент.Отбор);
	
	ОтборЭлемента = ГруппаИЛИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКИсходнаяСтрока);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

#Область ТоварыПоДиапазонам

// Возвращаемое значение:
// 	ТаблицаЗначений:
// 		* Номенклатура - СправочникСсылка.Номенклатура
// 		* Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
// 		* ХарактеристикиИспользуются - Булево
//  
Функция НоваяТаблицаТоваровПоДиапазонамКОбработке()
	
	ТипНоменклатура = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ТипХарактеристикиНоменклатуры = Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры");
	ТипБулево = Новый ОписаниеТипов("Булево");
	
	ТаблицаКОбработке = Новый ТаблицаЗначений;
	ТаблицаКОбработке.Колонки.Добавить("Номенклатура", ТипНоменклатура);
	ТаблицаКОбработке.Колонки.Добавить("Характеристика", ТипХарактеристикиНоменклатуры);
	ТаблицаКОбработке.Колонки.Добавить("ХарактеристикиИспользуются", ТипБулево);
	
	Возврат ТаблицаКОбработке;
	
КонецФункции

// Параметры:
//  ОбрабатываемаяТаблицаТоваров - ДанныеФормыКоллекция, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Товары - Обрабатываемая таблица
//  МенеджерВТ - МенеджерВременныхТаблиц
// 
Процедура СоздатьВременнуюТаблицуТоваровДляПроверкиДиапазонов(ОбрабатываемаяТаблицаТоваров, МенеджерВТ)
	
	Если МенеджерВТ.Таблицы.Найти("ВтТовары") <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеТаблицы = ОбрабатываемаяТаблицаТоваров.Выгрузить();
	
	ЗапросВтТовары = Новый Запрос;
	ЗапросВтТовары.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Если ДанныеТаблицы.Колонки.Найти("ХарактеристикиИспользуются") <> Неопределено Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеТоваров.НомерСтроки КАК НомерСтроки,
		|	ДанныеТоваров.Номенклатура КАК Номенклатура,
		|	ДанныеТоваров.Характеристика КАК Характеристика,
		|	ДанныеТоваров.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
		|	ДанныеТоваров.НачалоДействия КАК НачалоДействия,
		|	ДанныеТоваров.ОкончаниеДействия КАК ОкончаниеДействия,
		|	ДанныеТоваров.КоличествоПлан КАК КоличествоПлан,
		|	ДанныеТоваров.БазоваяЦена КАК БазоваяЦена,
		|	ДанныеТоваров.Процент КАК Процент,
		|	ДанныеТоваров.Отменено КАК Отменено
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	&ОбрабатываемаяТаблицаТоваров КАК ДанныеТоваров";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеТоваров.НомерСтроки КАК НомерСтроки,
		|	ДанныеТоваров.Номенклатура КАК Номенклатура,
		|	ДанныеТоваров.Характеристика КАК Характеристика,
		|	ДанныеТоваров.НачалоДействия КАК НачалоДействия,
		|	ДанныеТоваров.ОкончаниеДействия КАК ОкончаниеДействия,
		|	ДанныеТоваров.КоличествоПлан КАК КоличествоПлан,
		|	ДанныеТоваров.БазоваяЦена КАК БазоваяЦена,
		|	ДанныеТоваров.Процент КАК Процент,	
		|	ДанныеТоваров.Отменено КАК Отменено
		|ПОМЕСТИТЬ ВтТоварыПромежуточная
		|ИЗ
		|	&ОбрабатываемаяТаблицаТоваров КАК ДанныеТоваров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтТоварыПромежуточная.НомерСтроки КАК НомерСтроки,
		|	ВтТоварыПромежуточная.Номенклатура КАК Номенклатура,
		|	ВтТоварыПромежуточная.Характеристика КАК Характеристика,
		|	ВтТоварыПромежуточная.НачалоДействия КАК НачалоДействия,
		|	ВтТоварыПромежуточная.ОкончаниеДействия КАК ОкончаниеДействия,
		|	ВЫБОР
		|		КОГДА ВЫРАЗИТЬ(ВтТоварыПромежуточная.Номенклатура КАК Справочник.Номенклатура).ИспользованиеХарактеристик В (ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры), ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры), ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры))
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ХарактеристикиИспользуются,
		|	ВтТоварыПромежуточная.КоличествоПлан КАК КоличествоПлан,
		|	ВтТоварыПромежуточная.БазоваяЦена КАК БазоваяЦена,
		|	ВтТоварыПромежуточная.Процент КАК Процент,
		|	ВтТоварыПромежуточная.Отменено КАК Отменено
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	ВтТоварыПромежуточная КАК ВтТоварыПромежуточная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтТоварыПромежуточная";
		
	КонецЕсли;
	
	ЗапросВтТовары.Текст = ТекстЗапроса;
	ЗапросВтТовары.УстановитьПараметр("ОбрабатываемаяТаблицаТоваров", ДанныеТаблицы);
	ЗапросВтТовары.Выполнить();
	
КонецПроцедуры

// Параметры:
//  ОбрабатываемаяТаблицаТоваров - ДанныеФормыКоллекция, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Товары - Обрабатываемая таблица товаров
//  МенеджерВТ - МенеджерВременныхТаблиц
//  УпорядочитьПоУбыванию - Булево
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//  * НомерСтроки - Число
//  * Номенклатура - СправочникСсылка.Номенклатура
//  * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//  * ХарактеристикиИспользуются - Булево
//
Функция ВыборкаТоваровБезНачальногоДиапазона(ОбрабатываемаяТаблицаТоваров, МенеджерВТ, УпорядочитьПоУбыванию = Ложь)
	
	СоздатьВременнуюТаблицуТоваровДляПроверкиДиапазонов(ОбрабатываемаяТаблицаТоваров, МенеджерВТ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МИНИМУМ(ВтТовары.НомерСтроки) КАК НомерСтроки,
	|	ВтТовары.Номенклатура КАК Номенклатура,
	|	ВтТовары.Характеристика КАК Характеристика,
	|	ВтТовары.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются
	|ИЗ
	|	ВтТовары КАК ВтТовары
	|ГДЕ
	|НЕ ВтТовары.Отменено
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтТовары.Характеристика,
	|	ВтТовары.Номенклатура,
	|	ВтТовары.ХарактеристикиИспользуются
	|
	|ИМЕЮЩИЕ
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ВтТовары.КоличествоПлан <> 0
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки УБЫВ";
	
	Если НЕ УпорядочитьПоУбыванию Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "УБЫВ", "ВОЗР"); //@Query-part-1, @Query-part-2
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

// Параметры:
//  ОбрабатываемаяТаблицаТоваров - ДанныеФормыКоллекция, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.Товары, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.Товары - Обрабатываемая таблица товаров
//  МенеджерВТ - МенеджерВременныхТаблиц
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//  * НомерСтроки - Число
//  * Номенклатура - СправочникСсылка.Номенклатура
//  * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//  * ХарактеристикиИспользуются - Булево
//  * КоличествоНачалоДействия - Число
//  * КоличествоОкончаниеДействия - Число
//  * КоличествоБазоваяЦена - Число
//
Функция ВыборкаТоваровДиапазоновСРазнымиДанными(ОбрабатываемаяТаблицаТоваров, МенеджерВТ)
	
	СоздатьВременнуюТаблицуТоваровДляПроверкиДиапазонов(ОбрабатываемаяТаблицаТоваров, МенеджерВТ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МИНИМУМ(ВтТовары.НомерСтроки) КАК НомерСтроки,
	|	ВтТовары.Номенклатура КАК Номенклатура,
	|	ВтТовары.Характеристика КАК Характеристика,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтТовары.НачалоДействия) КАК КоличествоНачалоДействия,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтТовары.ОкончаниеДействия) КАК КоличествоОкончаниеДействия,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтТовары.БазоваяЦена) КАК КоличествоБазоваяЦена
	|ИЗ
	|	ВтТовары КАК ВтТовары
	|ГДЕ
	|НЕ ВтТовары.Отменено
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтТовары.Характеристика,
	|	ВтТовары.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтТовары.НачалоДействия) > 1
	|		ИЛИ КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтТовары.ОкончаниеДействия) > 1
	|		ИЛИ КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтТовары.БазоваяЦена) > 1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

#КонецОбласти

#Область СвойстваПоДиапазонам

// Возвращаемое значение:
// 	ТаблицаЗначений:
// 		* ЗначениеСвойства - ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
//  
Функция НоваяТаблицаСвойствПоДиапазонамКОбработке()
		
	ТаблицаКОбработке = Новый ТаблицаЗначений;
	ТаблицаКОбработке.Колонки.Добавить(
		"ЗначениеСвойства",
		Метаданные.ОпределяемыеТипы.ТипыСвойствНоменклатурыРетроБонусов.Тип);
	
	Возврат ТаблицаКОбработке;
	
КонецФункции

// Параметры:
//  ОбрабатываемаяТаблица - ДанныеФормыКоллекция, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры - Обрабатываемая таблица
//  МенеджерВТ - МенеджерВременныхТаблиц
// 
Процедура СоздатьВременнуюТаблицуСвойствДляПроверкиДиапазонов(ОбрабатываемаяТаблица, МенеджерВТ)
	
	Если МенеджерВТ.Таблицы.Найти("ВтСвойстваНоменклатуры") <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеТаблицы = ОбрабатываемаяТаблица.Выгрузить();
	
	ЗапросВтСвойстваНоменклатуры = Новый Запрос;
	ЗапросВтСвойстваНоменклатуры.МенеджерВременныхТаблиц = МенеджерВТ;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеСвойств.НомерСтроки КАК НомерСтроки,
	|	ДанныеСвойств.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ДанныеСвойств.НачалоДействия КАК НачалоДействия,
	|	ДанныеСвойств.ОкончаниеДействия КАК ОкончаниеДействия,
	|	ДанныеСвойств.КоличествоПлан КАК КоличествоПлан,
	|	ДанныеСвойств.БазоваяЦена КАК БазоваяЦена,
	|	ДанныеСвойств.Процент КАК Процент,
	|	ДанныеСвойств.Отменено КАК Отменено
	|ПОМЕСТИТЬ ВтСвойстваНоменклатуры
	|ИЗ
	|	&ОбрабатываемаяТаблица КАК ДанныеСвойств";
	
	ЗапросВтСвойстваНоменклатуры.Текст = ТекстЗапроса;
	ЗапросВтСвойстваНоменклатуры.УстановитьПараметр("ОбрабатываемаяТаблица", ДанныеТаблицы);
	ЗапросВтСвойстваНоменклатуры.Выполнить();
	
КонецПроцедуры

// Параметры:
//  ОбрабатываемаяТаблица - ДанныеФормыКоллекция, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры - Обрабатываемая таблица
//  МенеджерВТ - МенеджерВременныхТаблиц
//  УпорядочитьПоУбыванию - Булево
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//  * НомерСтроки - Число
//  * ЗначениеСвойства- ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
//
Функция ВыборкаСвойствБезНачальногоДиапазона(ОбрабатываемаяТаблица, МенеджерВТ, УпорядочитьПоУбыванию = Ложь)
	
	СоздатьВременнуюТаблицуСвойствДляПроверкиДиапазонов(ОбрабатываемаяТаблица, МенеджерВТ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МИНИМУМ(ВтСвойстваНоменклатуры.НомерСтроки) КАК НомерСтроки,
	|	ВтСвойстваНоменклатуры.ЗначениеСвойства КАК ЗначениеСвойства
	|ИЗ
	|	ВтСвойстваНоменклатуры КАК ВтСвойстваНоменклатуры
	|ГДЕ
	|НЕ ВтСвойстваНоменклатуры.Отменено
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтСвойстваНоменклатуры.ЗначениеСвойства
	|
	|ИМЕЮЩИЕ
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ВтСвойстваНоменклатуры.КоличествоПлан <> 0
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки УБЫВ";
	
	Если НЕ УпорядочитьПоУбыванию Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "УБЫВ", "ВОЗР"); //@Query-part-1, @Query-part-2
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

// Параметры:
//  ОбрабатываемаяТаблица - ДанныеФормыКоллекция, ДокументТабличнаяЧасть.УсловияРетроБонусовКлиентов.СвойстваНоменклатуры, ДокументТабличнаяЧасть.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры - Обрабатываемая таблица
//  МенеджерВТ - МенеджерВременныхТаблиц
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//  * НомерСтроки - Число
//  * ЗначениеСвойства - ОпределяемыйТип.ТипыСвойствНоменклатурыРетроБонусов
//  * ХарактеристикиИспользуются - Булево
//  * КоличествоНачалоДействия - Число
//  * КоличествоОкончаниеДействия - Число
//  * КоличествоБазоваяЦена - Число
//
Функция ВыборкаСвойствНоменклатурыДиапазоновСРазнымиДанными(ОбрабатываемаяТаблица, МенеджерВТ)
	
	СоздатьВременнуюТаблицуСвойствДляПроверкиДиапазонов(ОбрабатываемаяТаблица, МенеджерВТ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МИНИМУМ(ВтСвойстваНоменклатуры.НомерСтроки) КАК НомерСтроки,
	|	ВтСвойстваНоменклатуры.ЗначениеСвойства КАК ЗначениеСвойства,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтСвойстваНоменклатуры.НачалоДействия) КАК КоличествоНачалоДействия,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтСвойстваНоменклатуры.ОкончаниеДействия) КАК КоличествоОкончаниеДействия,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтСвойстваНоменклатуры.БазоваяЦена) КАК КоличествоБазоваяЦена
	|ИЗ
	|	ВтСвойстваНоменклатуры КАК ВтСвойстваНоменклатуры
	|ГДЕ
	|НЕ ВтСвойстваНоменклатуры.Отменено
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтСвойстваНоменклатуры.ЗначениеСвойства
	|
	|ИМЕЮЩИЕ
	|	(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтСвойстваНоменклатуры.НачалоДействия) > 1
	|		ИЛИ КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтСвойстваНоменклатуры.ОкончаниеДействия) > 1
	|		ИЛИ КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтСвойстваНоменклатуры.БазоваяЦена) > 1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти