#Область ПрограммныйИнтерфейс

// Определяет логотип по виду торговой площадки.
//
// Параметры:
//   ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//   Постфикс        - Строка - постфикс картинки логотипа
//
// Возвращаемое значение:
//   Картинка - логотип.
//
Функция ЛоготипТорговойПлощадки(ВидМаркетплейса, Знач Постфикс = "") Экспорт

	Если ЭтоLamoda(ВидМаркетплейса) Тогда
		Логотип = БиблиотекаКартинок["ЛоготипLamoda" + Постфикс]; // Картинка
	ИначеЕсли ЭтоOzon(ВидМаркетплейса) Тогда
		Если Не ЗначениеЗаполнено(Постфикс) Тогда
			Постфикс = "2";
		КонецЕсли;
		
		Логотип = БиблиотекаКартинок["ЛоготипOzon" + Постфикс]; // Картинка
	ИначеЕсли ЭтоWildberries(ВидМаркетплейса) Тогда
		Логотип = БиблиотекаКартинок["ЛоготипWildberries" + Постфикс]; // Картинка
	ИначеЕсли ЭтоЯндексМаркет(ВидМаркетплейса) Тогда
		Логотип = БиблиотекаКартинок["ЛоготипЯндексМаркет" + Постфикс]; // Картинка
	Иначе
		Логотип = БиблиотекаКартинок.ПустаяКартинка;
	КонецЕсли;
	
	Возврат Логотип;

КонецФункции

// Возвращает краткое представление торговой площадки.
//
// Параметры:
//   ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//
// Возвращаемое значение:
//   Строка - краткое представление вида торговой площадки.
//
Функция ПредставлениеВидаТорговойПлощадки(ВидМаркетплейса) Экспорт

	Представление = "";
	
	Если ЭтоLamoda(ВидМаркетплейса) Тогда
		Представление = НСтр("ru = 'Lamoda';
							|en = 'Lamoda'");
	ИначеЕсли ЭтоOzon(ВидМаркетплейса) Тогда
		Представление = НСтр("ru = 'Ozon';
							|en = 'Ozon'");
	ИначеЕсли ЭтоWildberries(ВидМаркетплейса) Тогда
		Представление = НСтр("ru = 'Wildberries';
							|en = 'Wildberries'");
	ИначеЕсли ЭтоЯндексМаркет(ВидМаркетплейса) Тогда
		Представление = НСтр("ru = 'Яндекс Маркет';
							|en = 'Yandex.Market'");
	КонецЕсли;
	
	Возврат Представление;

КонецФункции

// Возвращает данные по отправлениям заказа.
//
// Параметры:
//   Заказ                    - ДокументСсылка.ЗаказКлиента - проверяемый заказ.
//   РезультатПроверкиЗаказов - Соответствие из ДокументСсылка - где: ключ - заказ, значение - структура из ответа.
//
// Возвращаемое значение:
//   Произвольный,
//   см. НовыйРезультатПроверкиЗаказа.
//
Функция РезультатПроверкиЗаказа(Заказ, РезультатПроверкиЗаказов) Экспорт

	РезультатПроверкиЗаказа = РезультатПроверкиЗаказов[Заказ]; // Структура, Неопределено
	Если РезультатПроверкиЗаказа = Неопределено Тогда
		РезультатПроверкиЗаказа = НовыйРезультатПроверкиЗаказа();
		
		РезультатПроверкиЗаказов.Вставить(Заказ, РезультатПроверкиЗаказа);
	КонецЕсли;
	
	Возврат РезультатПроверкиЗаказа;

КонецФункции

// Возвращает представления идентификаторов торговой площадки.
//
// Параметры:
//   ВидТорговойПлощадки - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//
// Возвращаемое значение:
//   Соответствие из Строка - имена идентификаторов и их представления.
//
Функция ПредставленияИдентификаторов(ВидТорговойПлощадки) Экспорт
	
	ПредставленияИдентификаторов = Новый Соответствие;
	
	Если ВидТорговойПлощадки = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		ПредставленияИдентификаторов.Вставить("ИдентификаторПубликации", НСтр("ru = 'Идентификатор 1С';
																				|en = '1C ID'"));
		ПредставленияИдентификаторов.Вставить("ИдентификаторОбъектаМаркетплейса", НСтр("ru = 'Идентификатор Ozon';
																						|en = 'Ozon ID'"));
		ПредставленияИдентификаторов.Вставить("ИдентификаторSKU", НСтр("ru = 'SKU';
																		|en = 'SKU'"));
		
		ПредставленияИдентификаторов.Вставить("ИдентификаторыПубликации", НСтр("ru = 'Идентификаторы 1С';
																				|en = '1C IDs'"));
		ПредставленияИдентификаторов.Вставить("ИдентификаторыМаркетплейса", НСтр("ru = 'Идентификаторы Ozon';
																				|en = 'Ozon IDs'"));
		ПредставленияИдентификаторов.Вставить("ИдентификаторыSKU", НСтр("ru = 'SKU';
																		|en = 'SKU'"));
	КонецЕсли;
	
	Возврат ПредставленияИдентификаторов;
	
КонецФункции

// Сформировать наименование регламентного задания.
// 
// Параметры:
//  Наименование - Строка - Наименование
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов, Строка - Учетная запись или ее представление
// 
// Возвращаемое значение:
//  Строка - Сформировать наименование регламентного задания
Функция СформироватьНаименованиеРегламентногоЗадания(Знач Наименование, Знач УчетнаяЗапись) Экспорт
	Возврат Сред(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 (%2)", Наименование, УчетнаяЗапись), 1, 128);
КонецФункции

// Проверяет по значению принадлежность к маркетплейсу Ozon.
// 
// Параметры:
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид маркетплейса
// 
// Возвращаемое значение:
//  Булево - Результат проверки
Функция ЭтоOzon(ВидМаркетплейса) Экспорт
	Возврат ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon");
КонецФункции

// Проверяет по значению принадлежность к маркетплейсу Яндекс маркету.
// 
// Параметры:
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид маркетплейса
// 
// Возвращаемое значение:
//  Булево - Результат проверки
Функция ЭтоЯндексМаркет(ВидМаркетплейса) Экспорт
	Возврат ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет");
КонецФункции

// Проверяет по значению принадлежность к маркетплейсу Wildberries.
// 
// Параметры:
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид маркетплейса
// 
// Возвращаемое значение:
//  Булево - Результат проверки
Функция ЭтоWildberries(ВидМаркетплейса) Экспорт
	Возврат ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсWildberries");
КонецФункции

// Проверяет по значению принадлежность к маркетплейсу Lamoda.
// 
// Параметры:
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид маркетплейса
// 
// Возвращаемое значение:
//  Булево - Результат проверки
Функция ЭтоLamoda(ВидМаркетплейса) Экспорт
	Возврат ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсLamoda");
КонецФункции

// Проверяет переданное имя на удовлетворение требованиям, предъявляемым к именованию переменных встроенного языка.
//
// Параметры:
//  Имя - Строка - проверяемое имя свойства.
//
// Возвращаемое значение:
//  Булево
//
Функция ИмяСоответствуетТребованиямИменованияСвойств(Имя) Экспорт
	Буквы = "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯABCDEFGHIJKLMNOPQRSTUVWXYZ"; // @Non-NLS, АПК:163 В данных буква допустима
	Цифры = "1234567890"; // @Non-NLS
	
	Если Имя = "" Или СтрНайти(Буквы + "_", ВРег(Лев(Имя, 1))) = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат СтрРазделить(ВРег(Имя), Буквы + Цифры + "_", Ложь).Количество() = 0;
КонецФункции

#Область ИмпортТоваровИзСервиса

// Возвращает префикс импорта товарного каталога.
//
// Возвращаемое значение:
//   Строка - префикс.
//
Функция ПрефиксИмпортаТоварногоКаталога() Экспорт
	Возврат "ИмпортТоварногоКаталога";
КонецФункции

// Определяет имя регламентного задания, используемого для импорта товарного каталога.
//
// Параметры:
//   ВидТорговойПлощадки - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//
// Возвращаемое значение:
//   Строка - имя регламентного задания, запускаемого при импорте товарного каталога.
//
Функция ИмяРегламентногоЗаданияИмпортаТоварногоКаталога(ВидТорговойПлощадки) Экспорт
	
	Результат = "";
	
	Если ВидТорговойПлощадки = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		Результат = "ОбновлениеТоварногоКаталогаOzon";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает ключ задания импорта товарного каталога.
//
// Параметры:
//   ИдентификаторЗадания - Строка - уникальный идентификатор учетной записи торговой площадки или формы.
//
// Возвращаемое значение:
//   Строка - ключ задания импорта товарного каталога.
//
Функция КлючЗаданияИмпортаТоварногоКаталога(ИдентификаторЗадания = "") Экспорт
	Возврат СтрШаблон("%1_%2", КлючИмпортаТоварногоКаталога(), ИдентификаторЗадания);
КонецФункции

// Возвращает ключ задания ручного импорта товарного каталога.
//
// Параметры:
//   КлючЗадания          - см. КлючЗаданияИмпортаТоварногоКаталога
//   ИдентификаторЗадания - Строка - уникальный идентификатор учетной записи торговой площадки или формы.
//                                   Используется, если не заполнен параметр КлючЗадания.
//
// Возвращаемое значение:
//   Строка - ключ задания ручного импорта товарного каталога.
//
Функция КлючЗаданияРучногоИмпортаТоварногоКаталога(КлючЗадания = Неопределено, ИдентификаторЗадания = Неопределено) Экспорт
	
	КлючРучногоИмпорта = "ЗапускВручную";
	
	Если Не ЗначениеЗаполнено(КлючЗадания) Тогда
		Результат = СтрШаблон("%1_%2", КлючЗаданияИмпортаТоварногоКаталога(ИдентификаторЗадания), КлючРучногоИмпорта);
	Иначе
		Результат = СтрШаблон("%1_%2", КлючЗадания, КлючРучногоИмпорта);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает имя события начала выполнения импорта товарного каталога.
//
// Возвращаемое значение:
//   Строка - имя события.
//
Функция ИмяСобытияНачалаИмпортаТоварногоКаталога() Экспорт
	Возврат СтрШаблон("%1_Начало", КлючИмпортаТоварногоКаталога());
КонецФункции

// Возвращает имя события завершения выполнения импорта товарного каталога.
//
// Возвращаемое значение:
//   Строка - имя события.
//
Функция ИмяСобытияЗавершенияИмпортаТоварногоКаталога() Экспорт
	Возврат СтрШаблон("%1_Конец", КлючИмпортаТоварногоКаталога());
КонецФункции

// Возвращает ключ объекта сохраняемых системных настроек для данных запуска импорта товарного каталога.
//
// Возвращаемое значение:
//   Строка - ключ объекта настроек.
//
Функция КлючОбъектаДанныхЗапускаИмпортаТоварногоКаталога() Экспорт
	Возврат СтрШаблон("%1_ДанныеЗапуска", КлючИмпортаТоварногоКаталога());
КонецФункции

// Возвращает список параметров для получения товаров из сервиса.
//
// Параметры:
//   ВидТорговойПлощадки - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//
// Возвращаемое значение:
//   Структура - параметры для получения товаров из сервиса:
//     * УникальныйИдентификатор - УникальныйИдентификатор - идентификатор формы.
//     * КлючЗадания             - Строка - ключ фонового задания.
//     * АдресРезультата         - Неопределено, Строка - адрес помещения результата фонового задания.
//     * ВыводитьОкноОжидания    - Булево - признак интерактивного ожидания завершения.
//     * ИмяМетода               - Строка - имя вызываемого метода получения товаров из сервиса.
//     * РегламентноеЗадание     - см. ИмяРегламентногоЗаданияИмпортаТоварногоКаталога
//     * ПараметрыМетода         - см. ПараметрыЗапросаПолученияТоваровИзСервиса
//     * НастройкиПорцииМетода   - Структура - настройки получения обрабатываемой порции:
//         ** ИмяМетодаПолученияПорций - Строка - имя метода получения обрабатываемой порции. Если не задано,
//                                                основной метод обрабатывает все данные в одном вызове.
//         ** РазмерПорции             - Число - размер обрабатываемой порции.
//
Функция ПараметрыПолученияТоваровИзСервиса(ВидТорговойПлощадки) Экспорт

	Если ВидТорговойПлощадки = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		ИмяМетодаПолученияПорций = "ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьПорциюИдентификаторовДляИмпорта";
		ИмяМетодаОбработкиПорций = "ИнтеграцияСМаркетплейсомOzonСервер.ИмпортироватьПорциюТоварногоКаталога";
		РазмерПорции             = 500;
	Иначе
		ИмяМетодаПолученияПорций = "";
		ИмяМетодаОбработкиПорций = "";
		РазмерПорции             = 0;
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("УникальныйИдентификатор", Новый УникальныйИдентификатор);
	Параметры.Вставить("КлючЗадания",             КлючИмпортаТоварногоКаталога());
	Параметры.Вставить("АдресРезультата",         Неопределено);
	Параметры.Вставить("ВыводитьОкноОжидания",    Истина);
	
	Параметры.Вставить("ИмяМетода",               ИмяМетодаОбработкиПорций);
	Параметры.Вставить("РегламентноеЗадание",     ИмяРегламентногоЗаданияИмпортаТоварногоКаталога(ВидТорговойПлощадки));
	
	// Параметры выполнения вызываемого метода.
	Параметры.Вставить("ПараметрыМетода",         ПараметрыЗапросаПолученияТоваровИзСервиса(ВидТорговойПлощадки));
	
	// Настройки получения порции вызываемого метода.
	Параметры.Вставить("НастройкиПорцииМетода",   Новый Структура);
	Параметры.НастройкиПорцииМетода.Вставить("ИмяМетодаПолученияПорций", ИмяМетодаПолученияПорций);
	Параметры.НастройкиПорцииМетода.Вставить("РазмерПорции",             РазмерПорции);
	
	Возврат Параметры;

КонецФункции

// Возвращает список параметров для запроса получения товаров из сервиса.
//
// Параметры:
//   ВидТорговойПлощадки - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//
// Возвращаемое значение:
//   Структура - параметры для получения товаров из сервиса:
//     * УчетнаяЗапись            - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//     * ВидТорговойПлощадки      - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//     * Логотип                  - Картинка - логотип торговой площадки.
//     * ИмяСобытияОповещения     - Строка - имя события. Может быть использовано
//                                    для идентификации сообщений принимающими их формами.
//     * Партнер                  - СправочникСсылка.Партнеры - партнер, представляющий торговую площадку.
//     * ЭтоЗапускВручную         - Булево - признак ручного запуска получения товаров из сервиса.
//     * ВыводитьСообщения        - Булево - признак вывода сообщений пользователю по процессу.
//     * ИдентификаторНазначения  - Неопределено, УникальныйИдентификатор - идентификатор формы для вывода сообщений.
//     * Идентификаторы           - см. НовыйОтборПоИдентификаторам
//     * ПолученныеИдентификаторы - см. НовыйОтборПоИдентификаторам
//     * НастройкиИмпорта         - Структура:
//        ** ЗаписыватьНовые           - Булево - Признак того, что будут созданы новые записи по неархивным товарам.
//        ** ЗаписыватьНовыеАрхивные   - Булево - Признак того, что будут созданы новые записи по архивным товарам.
//        ** ОбновлятьНесопоставленные - Булево - Признак того, что нужно обновить существующие несопоставленные записи
//                                                в любых статусах.
//        ** ОбновлятьСопоставленные   - Булево - Признак того, что нужно обновить существующие сопоставленные записи
//                                                в определенных статусах.
//        ** ОбновлятьВЛюбомСтатусе    - Булево - Признак того, что нужно обновить существующие сопоставленные записи
//                                                в любых статусах.
//
Функция ПараметрыЗапросаПолученияТоваровИзСервиса(Знач ВидТорговойПлощадки = Неопределено) Экспорт

	Если ВидТорговойПлощадки = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		Логотип = ЛоготипТорговойПлощадки(ВидТорговойПлощадки, "2");
		ИмяСобытияОповещения = "ИнтеграцияСМаркетплейсомOzonСервер.ОбновитьДанные";
	ИначеЕсли ВидТорговойПлощадки = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет") Тогда
		Логотип = ЛоготипТорговойПлощадки(ВидТорговойПлощадки, "2");
		ИмяСобытияОповещения = "";
	Иначе
		ВидТорговойПлощадки = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ПустаяСсылка");
		Логотип = БиблиотекаКартинок.ПустаяКартинка;
		ИмяСобытияОповещения = "";
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("УчетнаяЗапись",            ПредопределенноеЗначение("Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка"));
	Параметры.Вставить("ВидТорговойПлощадки",      ВидТорговойПлощадки);
	Параметры.Вставить("Логотип",                  Логотип);
	Параметры.Вставить("ИмяСобытияОповещения",     ИмяСобытияОповещения);
	Параметры.Вставить("Партнер",                  ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка"));
	
	Параметры.Вставить("ЭтоЗапускВручную",         Ложь);
	Параметры.Вставить("ВыводитьСообщения",        Ложь);
	Параметры.Вставить("ИдентификаторНазначения",  Неопределено);
	
	Параметры.Вставить("Идентификаторы",           НовыйОтборПоИдентификаторам());
	Параметры.Вставить("ПолученныеИдентификаторы", НовыйОтборПоИдентификаторам());
	
	НастройкиИмпорта = Новый Структура;
	НастройкиИмпорта.Вставить("ЗаписыватьНовые",           Ложь);
	НастройкиИмпорта.Вставить("ЗаписыватьНовыеАрхивные",   Ложь);
	НастройкиИмпорта.Вставить("ОбновлятьНесопоставленные", Ложь);
	НастройкиИмпорта.Вставить("ОбновлятьСопоставленные",   Ложь);
	НастройкиИмпорта.Вставить("ОбновлятьВЛюбомСтатусе",    Ложь);
	
	Параметры.Вставить("НастройкиИмпорта", НастройкиИмпорта);
	
	Возврат Параметры;

КонецФункции

// Возвращает список идентификаторов отбора для использования в методах сервиса.
//
// Возвращаемое значение:
//   Структура - идентификаторы для отбора в методах сервиса:
//     * ИдентификаторыПубликации   - Массив Из Строка - список идентификаторов публикации.
//     * ИдентификаторыМаркетплейса - Массив Из Строка - список идентификаторов объектов маркетплейса.
//     * ИдентификаторыSKU          - Массив Из Строка - список идентификаторов SKU.
//
Функция НовыйОтборПоИдентификаторам() Экспорт

	Отбор = Новый Структура;
	Отбор.Вставить("ИдентификаторыПубликации",   Новый Массив);
	Отбор.Вставить("ИдентификаторыМаркетплейса", Новый Массив);
	Отбор.Вставить("ИдентификаторыSKU",          Новый Массив);
	
	Возврат Отбор;

КонецФункции

// Определяет, есть ли переданные идентификаторы в параметрах.
// 
// Параметры:
//  Идентификаторы - см. НовыйОтборПоИдентификаторам
// 
// Возвращаемое значение:
//  Булево
//
Функция ЕстьПереданныеИдентификаторы(Идентификаторы) Экспорт
	
	Результат = 
		Идентификаторы.ИдентификаторыПубликации.Количество() > 0
			Или Идентификаторы.ИдентификаторыМаркетплейса.Количество() > 0
			Или Идентификаторы.ИдентификаторыSKU.Количество() > 0;
	
	Возврат Результат;
	
КонецФункции

// Возвращает ключ уведомлений, отправляемых с сервера на клиент.
//
// Возвращаемое значение:
//   Строка - ключ уведомлений.
//
Функция КлючСерверныхУведомлений() Экспорт
	Возврат "ИнтеграцияСМаркетплейсами";
КонецФункции

// Конструктор данных серверного уведомления.
// 
// Возвращаемое значение:
//  Структура:
//   * ИмяСобытияОповещения - Строка - имя события для оповещения форм.
//   * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
// 
Функция НовыеДанныеСерверногоУведомления() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("ИмяСобытияОповещения", "");
	Результат.Вставить(
		"УчетнаяЗапись", ПредопределенноеЗначение("Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка"));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Определяет способ отражения продажи как Продажи через комиссионера (агента).
//
// Возвращаемое значение:
//   Строка
//
Функция СпособОтраженияКомиссия() Экспорт
	Возврат "Комиссия";
КонецФункции

// Определяет способ отражения продажи как Реализация с отложенным переходом права собственности (по факту доставки).
//
// Возвращаемое значение:
//   Строка
//
Функция СпособОтраженияРеализацияВПути() Экспорт
	Возврат "РеализацияВПути";
КонецФункции

// Определяет способ отражения продажи как Продажи не используются.
//
// Возвращаемое значение:
//   Строка
//
Функция СпособОтраженияПродажиНеИспользуются() Экспорт
	Возврат "НеИспользуются";
КонецФункции

// Проверяет, является ли указанный способ отражения продажами через комиссионера (агента).
//
// Параметры:
//   СпособОтражения - Строка - проверяемый способ отражения продаж.
//
// Возвращаемое значение:
//   Булево
//
Функция ЭтоСпособОтраженияКомиссия(СпособОтражения) Экспорт
	Результат = (СпособОтражения = СпособОтраженияКомиссия());
	Возврат Результат;
КонецФункции

// Проверяет, является ли указанный способ отражения реализацией с отложенным переходом права собственности.
//
// Параметры:
//   СпособОтражения - Строка - проверяемый способ отражения продаж.
//
// Возвращаемое значение:
//   Булево
//
Функция ЭтоСпособОтраженияРеализацияВПути(СпособОтражения) Экспорт
	Результат = (СпособОтражения = СпособОтраженияРеализацияВПути());
	Возврат Результат;
КонецФункции

// Проверяет, используется ли отражение продаж в информационной базе.
//
// Параметры:
//   СпособОтражения - Строка - проверяемый способ отражения продаж.
//
// Возвращаемое значение:
//   Булево
//
Функция ЭтоСпособОтраженияПродажиНеИспользуются(СпособОтражения) Экспорт
	Результат = (СпособОтражения = СпособОтраженияПродажиНеИспользуются());
	Возврат Результат;
КонецФункции

// Определяет, будет ли использован позаказный отчет о реализации по указанным способам отражения продаж.
//
// Параметры:
//  СпособОтраженияПродажFBO - Строка - способ отражения продаж через склады торговой площадки (FBO).
//  СпособОтраженияПродажFBS - Строка - способ отражения продаж с собственных складов (FBS).
//  ПродажиРазделяютсяПоДоговорам - Булево - признак разделения продаж по разным договорам.
//
// Возвращаемое значение:
//  Булево
//
Функция ИспользуетсяПозаказныйОтчетОРеализации(СпособОтраженияПродажFBO, СпособОтраженияПродажFBS,
			ПродажиРазделяютсяПоДоговорам) Экспорт
	
	Результат = Ложь;
	
	КомиссияFBO = ЭтоСпособОтраженияКомиссия(СпособОтраженияПродажFBO);
	КомиссияFBS = ЭтоСпособОтраженияКомиссия(СпособОтраженияПродажFBS);
	РеализацияFBS = ЭтоСпособОтраженияРеализацияВПути(СпособОтраженияПродажFBS);
	
	Если РеализацияFBS
			Или КомиссияFBO И КомиссияFBS И ПродажиРазделяютсяПоДоговорам Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Возвращает список параметров для изменения владельца товарного каталога.
//
// Возвращаемое значение:
//   Структура - параметры для изменения владельца товарного каталога:
//     * УчетнаяЗапись                  - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//     * ВидТорговойПлощадки            - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//     * Логотип                        - Картинка - логотип торговой площадки.
//     * ТекущийВладелец                - СправочникСсылка.Партнеры - текущий владелец каталога.
//     * НовыйВладелец                  - СправочникСсылка.Партнеры - новый владелец каталога.
//     * ОбрабатыватьЗаписиБезВладельца - Булево - признак обработки записей с незаполненной номенклатурой партнера.
//     * УникальныйИдентификатор        - УникальныйИдентификатор - идентификатор формы.
//     * ВыводитьОкноОжидания           - Булево - признак интерактивного ожидания завершения.
//     * ИмяСобытияОповещения           - Строка - имя события. Может быть использовано
//                                         для идентификации сообщений принимающими их формами.
//
Функция ПараметрыИзмененияВладельцаТоварногоКаталога() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("УчетнаяЗапись",                  ПредопределенноеЗначение("Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка"));
	Параметры.Вставить("ВидТорговойПлощадки",            ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ПустаяСсылка"));
	Параметры.Вставить("Логотип",                        БиблиотекаКартинок.ПустаяКартинка);
	Параметры.Вставить("ТекущийВладелец",                ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка"));
	Параметры.Вставить("НовыйВладелец",                  ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка"));
	Параметры.Вставить("ОбрабатыватьЗаписиБезВладельца", Ложь);
	Параметры.Вставить("УникальныйИдентификатор",        Новый УникальныйИдентификатор);
	Параметры.Вставить("ВыводитьОкноОжидания",           Ложь);
	Параметры.Вставить("ИмяСобытияОповещения",           "");
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Словарь

// Получение имени вида цены, которое используется для выгрузки цены "Цена продавца без скидок" на маркетплейс
// 
// Возвращаемое значение:
//  Строка - Имя вида цены
Функция ИмяВидЦеныДоСкидок() Экспорт
	Возврат "ВидЦеныДоСкидок";
КонецФункции

// Получение имени вида цены, которое используется для выгрузки цены "Цена со скидкой продавца" на маркетплейс
// 
// Возвращаемое значение:
//  Строка - Имя вид цены
Функция ИмяВидЦеныСоСкидкойПродавца() Экспорт
	Возврат "ВидЦеныСоСкидкойПродавца";
КонецФункции

// Получение имени вида цены, которое используется для выгрузки цены "Цена участника WB-клуба" на маркетплейс
// 
// Возвращаемое значение:
//  Строка - Имя вида цены
Функция ИмяВидЦеныКлубная() Экспорт
	Возврат "ВидЦеныКлубная";
КонецФункции

#КонецОбласти

#Область СопоставлениеДанных

// Возвращает доступные варианты результатов сопоставления загружаемой строки.
//
// Возвращаемое значение:
//   Структура - доступные варианты результатов сопоставления с клюами:
//     * СтрокаСопоставлена   - Строка - в значении представление с загружаемой строкой.
//     * СтрокаНеСопоставлена - Строка - в значении представление с загружаемой строкой.
//     * Неоднозначность      - Строка - в значении представление с загружаемой строкой.
//
Функция ВариантыРезультатовСопоставления() Экспорт

	ВариантыРезультатовСопоставления = Новый Структура;
	ВариантыРезультатовСопоставления.Вставить("СтрокаСопоставлена",   "СтрокаСопоставлена");
	ВариантыРезультатовСопоставления.Вставить("СтрокаНеСопоставлена", "СтрокаНеСопоставлена");
	ВариантыРезультатовСопоставления.Вставить("Неоднозначность",      "Неоднозначность");

	Возврат ВариантыРезультатовСопоставления;

КонецФункции

// Устанавливает результат сопоставления в строке.
// В заполняемой строке в колонке РезультатСопоставленияСтроки содержится ограниченный список значений
// (см. ВариантыРезультатовСопоставления).
//
// Параметры:
//   ТекущиеДанные                    - СтрокаТаблицыЗначений, ДанныеФормыЭлементКоллекции - строка заполнения
//                                           результата сопоставления.
//   КолонкиОбязательныеПриПроверке   - СписокЗначений Из Строка - список обязательных колонок.
//   КолонкиДополнительныеПриПроверке - СписокЗначений Из Строка - список проверяемых дополнительных колонок.
//
Процедура ЗаполнитьРезультатСопоставленияСтроки(ТекущиеДанные, КолонкиОбязательныеПриПроверке,
			КолонкиДополнительныеПриПроверке) Экспорт

	ВариантыРезультатовСопоставления = ВариантыРезультатовСопоставления();
	Ошибки = Новый Массив;

	Для Каждого Колонка Из КолонкиОбязательныеПриПроверке Цикл
		Если Не ЗначениеЗаполнено(ТекущиеДанные[Колонка.Значение]) Тогда
			Ошибки.Добавить(Колонка.Значение);
		КонецЕсли;
	КонецЦикла;

	Для Каждого Колонка Из КолонкиДополнительныеПриПроверке Цикл
		Если Не ЗначениеЗаполнено(ТекущиеДанные[Колонка.Значение]) Тогда
			Ошибки.Добавить(Колонка.Значение);
		КонецЕсли;
	КонецЦикла;
	
	ОписаниеОшибки = СтрСоединить(Ошибки, "; ");
	ЗаполненыВсеОбязательныеКолонки = ПустаяСтрока(ОписаниеОшибки);

	Если ТекущиеДанные.РезультатСопоставленияСтроки = ВариантыРезультатовСопоставления.Неоднозначность Тогда
		ТекущиеДанные.СтрокаОбработана = ЗаполненыВсеОбязательныеКолонки;
		Возврат;
	КонецЕсли;

	ТекущиеДанные.РезультатСопоставленияСтроки = ?(ЗаполненыВсеОбязательныеКолонки,
		ВариантыРезультатовСопоставления.СтрокаСопоставлена,
		ВариантыРезультатовСопоставления.СтрокаНеСопоставлена);
	ТекущиеДанные.ОписаниеОшибки               = ОписаниеОшибки;
	ТекущиеДанные.СтрокаОбработана             = ЗаполненыВсеОбязательныеКолонки;

КонецПроцедуры

// Возвращаемое значение:
//  Строка - Способ обработки сопоставления номенклатуры
//
Функция СпособОбработкиСопоставленияНоменклатурыПоискВариантов() Экспорт
	Возврат "ПоискВариантовСопоставленияНоменклатуры";
КонецФункции

// Возвращает состав полей, требуемых для создания номенклатуры по данным контрагента.
//
// Параметры:
//   ИсточникДанных - Произвольный - источник данных для определения категории сервиса.
//
// Возвращаемое значение:
//  Структура - Ключ структуры - имя реквизита, значение - значение реквизита:
//   * ВидНоменклатуры - СправочникСсылка.ВидыНоменклатуры - вид номенклатуры.
//   * Родитель - СправочникСсылка.Номенклатура - группа номенклатуры.
//   * ТоварнаяКатегория - СправочникСсылка.ТоварныеКатегории - товарная категория.
//   * ИспользованиеХарактеристик - ПеречислениеСсылка.ВариантыИспользованияХарактеристикНоменклатуры -
//                                    вариант использования характеристик.
//   * ВладелецХарактеристик - СправочникСсылка.ВидыНоменклатуры - владелец характеристик.
//   * ИспользоватьУпаковки - Булево - признак использования характеристик.
//   * НаборУпаковок - СправочникСсылка.НаборыУпаковок - набор упаковок.
//
Функция РеквизитыНоменклатурыДляСозданияПоДаннымКонтрагента(ИсточникДанных = Неопределено) Экспорт

	Результат = Новый Структура;
	
	Результат.Вставить("ВидНоменклатуры",   ПредопределенноеЗначение("Справочник.ВидыНоменклатуры.ПустаяСсылка"));
	Результат.Вставить("Родитель",          ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
	Результат.Вставить("ТоварнаяКатегория", ПредопределенноеЗначение("Справочник.ТоварныеКатегории.ПустаяСсылка"));
	
	Результат.Вставить("ИспользованиеХарактеристик",
		ПредопределенноеЗначение("Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ПустаяСсылка"));
	Результат.Вставить("ВладелецХарактеристик",
		ПредопределенноеЗначение("Справочник.ВидыНоменклатуры.ПустаяСсылка"));
	
	Результат.Вставить("ИспользоватьУпаковки", Ложь);
	Результат.Вставить("НаборУпаковок",        ПредопределенноеЗначение("Справочник.НаборыУпаковок.ПустаяСсылка"));
	
	Если ИсточникДанных <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Результат, ИсточникДанных);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Возвращает состав полей, требуемых для определения категории сервиса.
//
// Параметры:
//   УчетнаяЗапись           - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//   ЗаполняемыйОбъект       - ФормаКлиентскогоПриложения, СправочникОбъект.Номенклатура - заполняемый объект.
//   НоменклатураКонтрагента - СправочникСсылка.НоменклатураКонтрагентов - номенклатура партнера.
//   ОбновитьДополнительныеРеквизитыФормыНаКлиенте - Булево - признак обновления дополнительных реквизитов формы,
//                                                            если Ложь, тогда обновляются данные таблицы объекта.
//
Процедура ЗаполнитьРеквизитыНоменклатурыПоКатегорииСервиса(УчетнаяЗапись, ЗаполняемыйОбъект, НоменклатураКонтрагента,
			ОбновитьДополнительныеРеквизитыФормыНаКлиенте = Ложь) Экспорт

	ЭтоФормаНоменклатуры = (ТипЗнч(ЗаполняемыйОбъект) = Тип("ФормаКлиентскогоПриложения"));
	
	Если ЭтоФормаНоменклатуры Тогда
		НоменклатураОбъект = ЗаполняемыйОбъект.Объект; // ДанныеФормыСтруктура
	Иначе
		НоменклатураОбъект = ЗаполняемыйОбъект; // // ДанныеФормыСтруктура, СправочникОбъект.Номенклатура
	КонецЕсли;
	
	РеквизитыНоменклатуры = РеквизитыНоменклатурыДляСозданияПоДаннымКонтрагента(НоменклатураОбъект);
	
	ОписаниеДанных =
		ИнтеграцияСМаркетплейсамиВызовСервера.ПолучитьДанныеНоменклатурыПоКатегорииСервиса(
			УчетнаяЗапись,
			НоменклатураКонтрагента,
			РеквизитыНоменклатуры);
	
	Если ЗначениеЗаполнено(ОписаниеДанных) Тогда
		ЗаполнитьЗначенияСвойств(НоменклатураОбъект, ОписаниеДанных.Реквизиты);
		
		Если ЗначениеЗаполнено(ОписаниеДанных.ДополнительныеРеквизиты) Тогда
			Если ЭтоФормаНоменклатуры
					И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЗаполняемыйОбъект, "Свойства_ОписаниеДополнительныхРеквизитов") Тогда
				ОписаниеДополнительныхРеквизитов = ЗаполняемыйОбъект.Свойства_ОписаниеДополнительныхРеквизитов; // ТаблицаЗначений, ДанныеФормыКоллекция
				
				Для Каждого СвойствоКлючЗначение Из ОписаниеДанных.ДополнительныеРеквизиты Цикл // КлючИЗначение
					Если Не ЗначениеЗаполнено(СвойствоКлючЗначение.Значение) Тогда
						Продолжить;
					КонецЕсли;
					
					Отбор = Новый Структура("Свойство", СвойствоКлючЗначение.Ключ);
					СтрокиРеквизита = ОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
					Если СтрокиРеквизита.Количество() = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					ЗаполняемыйОбъект[СтрокиРеквизита[0].ИмяРеквизитаЗначение] = СвойствоКлючЗначение.Значение;
				КонецЦикла;
			Иначе
				Если ОбновитьДополнительныеРеквизитыФормыНаКлиенте Тогда
					Для Каждого СвойствоКлючЗначение Из ОписаниеДанных.ДополнительныеРеквизиты Цикл // КлючИЗначение
						Если Не ЗначениеЗаполнено(СвойствоКлючЗначение.Значение) Тогда
							Продолжить;
						КонецЕсли;
						
						ДополнительныеРеквизиты = НоменклатураОбъект["ДополнительныеРеквизиты"]; // ДанныеФормыКоллекция, СправочникТабличнаяЧасть.Номенклатура.ДополнительныеРеквизиты
						
						Отбор = Новый Структура("Свойство", СвойствоКлючЗначение.Ключ);
						СтрокиРеквизита = ДополнительныеРеквизиты.НайтиСтроки(Отбор);
						Если СтрокиРеквизита.Количество() = 0 Тогда
							СтрокаРеквизита = ДополнительныеРеквизиты.Добавить();
						Иначе
							СтрокаРеквизита = СтрокиРеквизита[0];
						КонецЕсли;
						
						СтрокаРеквизита["Свойство"] = СвойствоКлючЗначение.Ключ;
						СтрокаРеквизита["Значение"] = СвойствоКлючЗначение.Значение;
						
						Если ТипЗнч(СвойствоКлючЗначение.Значение) = Тип("Строка")
							И СтрДлина(СвойствоКлючЗначение.Значение) > 1024 Тогда
							СтрокаРеквизита["ТекстоваяСтрока"] = СвойствоКлючЗначение.Значение;
						КонецЕсли;
					КонецЦикла;
				Иначе
					ИнтеграцияСМаркетплейсамиВызовСервера.УстановитьСвойстваДополнительныеОбъекта(
						НоменклатураОбъект,
						ОписаниеДанных.ДополнительныеРеквизиты);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область РаботаСЗаказамиИОтправлениями

// Возвращает представление навигационной ссылки документа отгрузки.
//
// Параметры:
//   Документ    - Неопределено, ДокументСсылка - ссылка на документ.
//   Статус      - ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - статус документа торговой площадки.
//
// Возвращаемое значение:
//   Строка - представление навигационной ссылки документа отгрузки.
//
Функция ШаблонНавигационнойСсылкиДокументаОтгрузки(Знач Документ, Знач Статус) Экспорт

	ЭтоРеализация = (ТипЗнч(Документ) = Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	ЭтоПередача   = (ТипЗнч(Документ) = Тип("ДокументСсылка.ПередачаТоваровХранителю"));
	
	Если Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки")
			Или Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке") Тогда
		Если ЭтоРеализация Тогда
			Шаблон = НСтр("ru = 'Отгружается реализацией № %1 от %2';
							|en = 'Being shipped by customer invoice No. %1 dated %2'");
		ИначеЕсли ЭтоПередача Тогда
			Шаблон = НСтр("ru = 'Отгружается передачей товаров № %1 от %2';
							|en = 'Being shipped by transfer of goods No. %1 dated %2'");
		Иначе
			Шаблон = "";
		КонецЕсли;
	Иначе
		Если ЭтоРеализация Тогда
			Шаблон = НСтр("ru = 'Отгружен реализацией № %1 от %2';
							|en = 'Shipped by customer invoice No. %1 dated %2'");
		ИначеЕсли ЭтоПередача Тогда
			Шаблон = НСтр("ru = 'Отгружается передачей товаров № %1 от %2';
							|en = 'Being shipped by transfer of goods No. %1 dated %2'");
		Иначе
			Шаблон = "";
		КонецЕсли;
	КонецЕсли;
	
	Возврат Шаблон;

КонецФункции

// Конструктор результата проверки заказа.
// 
// Возвращаемое значение:
//   Структура - сведения по отправлениям заказа:
//     * МаксимальныйИдентификаторЭкземпляра - Число - количество отправлений по заказу.
//     * УчетнаяЗапись                       - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись.
//     * ВидМаркетплейса                     - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//     * ОтправленияВСборке                  - Массив из Строка - собираемые отправления, 
//                                               по которым есть экземпляры.
//     * ОтправленияКПодтверждениюСборки     - Массив из Строка - собираемые отправления,
//                                               которые готовы к подтверждению сборки.
//     * ОтправленияНеВСборке                - Массив из Строка - отправления, 
//                                               по которым не требуется сборка.
//     * ОтправленияБезТоваров               - Массив из Строка - собираемые отправления,
//                                               по которым нет экземпляров.
//     * ОтправленияСНезаполненнымиДанными   - Массив из Строка - собираемые отправления,
//                                               по которым есть экземпляры с незаполненными данными.
//     * ОтправленияИзмененные               - Массив из Строка - собираемые отправления,
//                                               по которым нужно перезаполнить документы отгрузки.
//     * ОтправленияБезДокументовОтгрузки    - Массив из Строка - собираемые отправления,
//                                               для которых нужно создать документы отгрузки.
//     * ОтправленияОтмененные               - Массив из Строка - отмененные отправления.
//     * ОтправленияВДоставке                - Массив из Строка - отправления в доставке.
//     * ОтправленияВКонечномСтатусе         - Массив из Строка - отправления в конечном статусе.
//     * ЭкземплярыБезДокументовОтгрузки     - Массив из Строка - список идентификаторов экземпляров без документов отгрузки.
//     * ИзмененныеДокументыОтгрузки         - Соответствие из ДокументСсылка - список измененных документов отгрузки,
//                                               где: ключ - ссылка на документ отгрузки, значение - номер отправления.
//     * ДанныеПоНомерамОтправлений          - Соответствие из Строка - список номеров отправлений и их данных,
//                                               где: ключ - номер отправления, значение - ключи данных для записи заказа.
//     * ДетализацияПроблемПоОтправлениям    - Соответствие из Строка - список номеров отправлений с описанием 
//                                               выявленных по ним проблемам.
//
Функция НовыйРезультатПроверкиЗаказа()

	РезультатПроверкиЗаказа = Новый Структура;
	РезультатПроверкиЗаказа.Вставить("МаксимальныйИдентификаторЭкземпляра", 0);
	РезультатПроверкиЗаказа.Вставить("ХозяйственнаяОперация",               ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПустаяСсылка"));
	РезультатПроверкиЗаказа.Вставить("УчетнаяЗапись",                       ПредопределенноеЗначение("Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка"));
	РезультатПроверкиЗаказа.Вставить("ВидМаркетплейса",                     ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ПустаяСсылка"));
	РезультатПроверкиЗаказа.Вставить("ОтправленияВСборке",                  Новый Массив);
	РезультатПроверкиЗаказа.Вставить("ОтправленияКПодтверждениюСборки",     Новый Массив);
	РезультатПроверкиЗаказа.Вставить("ОтправленияНеВСборке",                Новый Массив);
	РезультатПроверкиЗаказа.Вставить("ОтправленияБезТоваров",               Новый Массив);
	РезультатПроверкиЗаказа.Вставить("ОтправленияСНезаполненнымиДанными",   Новый Массив);
	РезультатПроверкиЗаказа.Вставить("ОтправленияИзмененные",               Новый Массив);
	РезультатПроверкиЗаказа.Вставить("ОтправленияБезДокументовОтгрузки",    Новый Массив);
	РезультатПроверкиЗаказа.Вставить("ОтправленияОтмененные",               Новый Массив);
	РезультатПроверкиЗаказа.Вставить("ОтправленияВДоставке",                Новый Массив);
	РезультатПроверкиЗаказа.Вставить("ОтправленияВКонечномСтатусе",         Новый Массив);
	РезультатПроверкиЗаказа.Вставить("ЭкземплярыБезДокументовОтгрузки",     Новый Массив);
	РезультатПроверкиЗаказа.Вставить("ИзмененныеДокументыОтгрузки",         Новый Соответствие);
	РезультатПроверкиЗаказа.Вставить("ДанныеПоНомерамОтправлений",          Новый Соответствие);
	РезультатПроверкиЗаказа.Вставить("ДетализацияПроблемПоОтправлениям",    Новый Соответствие);
	
	Возврат РезультатПроверкиЗаказа;

КонецФункции

// Конструктор данных отправления для обработки.
// Доступные варианты обработки отправлений см. в командах регистра сведений ЗаказыТорговыхПлощадок.
// 
// Возвращаемое значение:
//  Структура:
//   * Заказ - ДокументСсылка.ЗаказКлиента
//   * ДокументЗаказа - ДокументСсылка.ЗаказКлиента
//   * НомерЗаказа - Строка
//   * НомерОтправления - Строка
//   * СтатусОтправления - ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок
//
Функция НовыеДанныеОтправленияДляОбработки() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("Заказ", ПредопределенноеЗначение("Документ.ЗаказКлиента.ПустаяСсылка"));
	Результат.Вставить("ДокументЗаказа", ПредопределенноеЗначение("Документ.ЗаказКлиента.ПустаяСсылка"));
	Результат.Вставить("НомерЗаказа", "");
	Результат.Вставить("НомерОтправления", "");
	Результат.Вставить("СтатусОтправления", ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.ПустаяСсылка"));
	
	Возврат Результат;
	
КонецФункции

// Формирует массив с данными для вывода сообщений пользователю. Используется в случаях, когда данных для вывода может
// быть много, в таком случае массив будет ограничен по количеству элементов, а для тех данных, которые не будут
// выведены, будет выдано соответствующее сообщение.
// Например, есть 10 аналогичных сообщений пользователю. В таком случае достаточно вывести 4 сообщения, а вместо вывода
// остальных сообщений можно вывести сообщение с текстом "и еще 6 аналогичных сообщений".
//
// Параметры:
//  Массив - Массив из Строка - массив данных, который будет выведен в сообщениях пользователю.
//  ШаблонСообщения - Строка - шаблон строки сообщения пользователю о тех данных, которые не выведены в сообщении.
//                    См. описание параметра Шаблон для СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка.
//  КоличествоЭлементов - Число - максимальное количество элементов, больше которого нужно ограничить массив.
//
// Возвращаемое значение:
//  Массив из Строка
//
Функция ЭлементыМассиваДляВыводаСообщений(Знач Массив, ШаблонСообщения, КоличествоЭлементов = 5) Экспорт
	
	Если Массив.Количество() <= КоличествоЭлементов Тогда
		Возврат Массив;
	КонецЕсли;
	
	Результат = Новый Массив; // Массив из Строка
	Для Индекс = 0 По КоличествоЭлементов - 2 Цикл
		Результат.Добавить(Массив[Индекс]);
	КонецЦикла;
	
	ПредставлениеЧислаНевыведенныхДокументов = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		ШаблонСообщения,
		Массив.Количество() - КоличествоЭлементов);
		
	Результат.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'и еще %1';
			|en = 'and also %1'"),
		ПредставлениеЧислаНевыведенныхДокументов));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ИмпортТоваровИзСервисаСлужебный

Функция КлючИмпортаТоварногоКаталога()
	Возврат "МП_ИмпортТоварногоКаталога";
КонецФункции

#КонецОбласти

#КонецОбласти

