///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ИнтернетЭквайринг".
// ОбщийМодуль.ИнтернетЭквайрингКлиентСервер.
// 
// Клиент серверные процедуры работы с интернет-эквайрингом:
//  - конструкторы параметров различных методов;
//  - проверка совпадения значений двух переменных;
//  - формирование описания дополнительных параметров аутентификации по данным ответа сервиса;
//  - проверка заполнения реквизитов форм, созданных программно.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область КонструкторыПараметровМетодов

// Формирует пустое описание участника Интернет-эквайринга
// 
// Возвращаемое значение:
//  Структура - описание участника:
//    * Идентификатор - Строка - внутренний идентификатор банка в платежной системе.
//    * Представление - Строка - наименование банка для вывода на экран.
//    * БИК - Строка - БИК банка для поиска.
//    * СпособАутентификации - Строка - вариант аутентификации в системе банка.
//    * СсылкаСправка - Строка - Ссылка на страницу банка с описанием процесса получения данных для аутентификации.
//    * СсылкаСоглашение - Строка - Ссылка на страницу с условием пользовательского соглашения.
//    * ПоляАутентификации - Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации
//
Функция НовыйПараметрыУчастника() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("Идентификатор",        "");
	Результат.Вставить("Представление",        "");
	Результат.Вставить("БИК",                  "");
	Результат.Вставить("СпособАутентификации", "");
	Результат.Вставить("СсылкаСправка",        "");
	Результат.Вставить("СсылкаСоглашение",     "");
	Результат.Вставить("ПоляАутентификации",   Новый Массив);
	
	Возврат Результат;
	
КонецФункции

// Формирует пустое описание поля аутентификации в системах Интернет-эквайринга.
// 
// Возвращаемое значение:
//  Структура - параметры поля аутентификации:
//    * Идентификатор - Строка - идентификатор поля.
//    * Представление - Строка - представление поля.
//    * ДлинаСтроки - Число - длина поля.
//    * РежимПароля - Булево - если Истина, то вводимый в поле текст будет скрыт.
//    * Значение - Строка - значение поля.
//    * ПредставлениеЗначения - Строка - представление значения для дополнительных полей аутентификации.
//    * ЭтоИдентификаторМерчанта - Булево - истина, если параметр является идентификатором мерчанта.
//    * ТребуемыйПараметр - Булево - только для дополнительных параметров настройки аутентификации. Истина, если
//        требуется заполнить значение параметра.
//    * ДоступныеЗначения - Соответствие Из КлючИЗначение - только для дополнительных параметров настройки
//        аутентификации. Список доступных для выбора значений:
//        ** Ключ - Строка - значение параметра;
//        ** Значение - Строка - представление параметра.
//
Функция НовыйПараметрыПоляАутентификации() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("Идентификатор",            "");
	Результат.Вставить("Представление",            "");
	Результат.Вставить("ДлинаСтроки",              0);
	Результат.Вставить("РежимПароля",              Ложь);
	Результат.Вставить("Значение",                 "");
	Результат.Вставить("ПредставлениеЗначения",    "");
	Результат.Вставить("ЭтоИдентификаторМерчанта", Ложь);
	Результат.Вставить("ТребуемыйПараметр",        Ложь);
	Результат.Вставить("ДоступныеЗначения",        Новый Соответствие);
	
	Возврат Результат;
	
КонецФункции

// Возвращает описание реквизитов и элементов формы, созданных программно.
// 
// Возвращаемое значение:
//  Структура - описание всех динамически созданных объектов:
//    * НастройкиАутентификации - Массив Из см. НовыйОписаниеДинамическогоРеквизита - описание реквизитов и элементов
//        формы для настройки аутентификации.
//    * ПрикладныеНастройки - Массив Из см. НовыйОписаниеДинамическогоРеквизита - описание реквизитов и элементов формы
//        для прикладных настроек.
//    * ДополнительныеПараметры - Массив Из см. НовыйПараметрыПоляАутентификации - описание реквизитов и
//        элементов формы в которые выводятся дополнительные параметры.
//    * ТребуемыеНастройки - Массив Из см. НовыйОписаниеДинамическогоРеквизита - описание реквизитов и элементов формы
//        для заполнения дополнительных настроек параметров получения токена.
//    * РеквизитПоИмениЭлемента - Соответствие Из КлючИЗначение - связь элемента и реквизита формы:
//        ** Ключ - Строка - имя элемента формы.
//        ** Значение - Строка - имя реквизита, с которым связан элемент.
//    * ИдентификаторПоИмениЭлемента - Соответствие Из КлючИЗначение - связь элемента и идентификатора настроек:
//        ** Ключ - Строка - имя элемента формы.
//        ** Значение - Строка - идентификатор настройки, с которым связан элемент.
//
Функция НовыйДинамическиеРеквизитыПодключения() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("НастройкиАутентификации",         Новый Массив);
	Результат.Вставить("ПрикладныеНастройки",             Новый Массив);
	Результат.Вставить("ДополнительныеПараметры",         Новый Массив);
	Результат.Вставить("ТребуемыеНастройки",              Новый Массив);
	Результат.Вставить("РеквизитПоИмениЭлемента",         Новый Соответствие);
	Результат.Вставить("ИдентификаторПоИмениЭлемента",    Новый Соответствие);
	
	Возврат Результат;
	
КонецФункции

// Информация о динамически созданном реквизите и элементе формы
// 
// Возвращаемое значение:
//  Структура - описание:
//    * Идентификатор - Строка - идентификатор параметра аутентификации или прикладного параметра, к которому относятся
//        созданный реквизит и элемент.
//    * Представление - Строка - представление поля для вывода сообщений.
//    * ИмяРеквизита - Строка - имя созданного реквизита.
//    * ИмяЭлемента - Строка - имя созданного элемента.
//    * ПроверкаЗаполнения - Булево - если Истина, то реквизит должен быть обязательно заполнен. Значение по умолчанию
//        Истина.
//    * Тип - ОписаниеТипов - тип вводимых данных.
//    * ДлинаСтроки - Число - длина строки для реквизита типа Строка.
//    * ПараметрыВыбора - ФиксированныйМассив Из ПараметрВыбора - настройка параметров выбора
//                      - Неопределено - если параметры выбора не заданы.
//    * СвязиПараметровВыбора - ФиксированныйМассив Из СвязьПараметраВыбора - связи параметров выбора поля.
//                            - Неопределено - если связь параметров выбора не задана.
//    * РежимПароля - Булево - признак использования режима пароля.
//    * ЭтоИдентификаторМерчанта - Булево - истина, если параметр является идентификатором мерчанта.
//    * ЭтоНастройкаПодключения - Булево - признак того, что динамическое поле содержит настройку подключения.
//
Функция НовыйОписаниеДинамическогоРеквизита() Экспорт

	Результат = Новый Структура;
	
	Результат.Вставить("Идентификатор",            "");
	Результат.Вставить("Представление",            "");
	Результат.Вставить("ИмяРеквизита",             "");
	Результат.Вставить("ИмяЭлемента",              "");
	Результат.Вставить("ПроверкаЗаполнения",       Истина);
	Результат.Вставить("Тип",                      Новый ОписаниеТипов(Неопределено));
	Результат.Вставить("ДлинаСтроки",              0);
	Результат.Вставить("ПараметрыВыбора",          Неопределено);
	Результат.Вставить("СвязиПараметровВыбора",    Неопределено);
	Результат.Вставить("РежимПароля",              Ложь);
	Результат.Вставить("ЭтоИдентификаторМерчанта", Ложь);
	Результат.Вставить("ЭтоНастройкаПодключения",  Ложь);

	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыИФункции

// Сравнивает данные сложной структуры с учетом вложенности.
//
// Параметры:
//  Данные1 - Структура
//          - ФиксированнаяСтруктура
//          - Соответствие Из КлючИЗначение
//          - ФиксированноеСоответствие Из КлючИЗначение
//          - Массив Из Произвольный
//          - ФиксированныйМассив Из Произвольный
//          - Строка
//          - Число
//          - Булево - сравниваемые данные.
//  Данные2 - Произвольный - те же типы, что и для параметра Данные1.
//  Пропустить - Строка - имена параметров, сравнение которых нужно пропустить. Для структур и соответствий.
//
// Возвращаемое значение:
//  Булево - Истина, если совпадают.
//
Функция ДанныеСовпадают(Данные1, Данные2, Пропустить = "") Экспорт
	
	Исключаемые = СтрРазделить(Пропустить, ", ", Ложь);
	
	Если ТипЗнч(Данные1) <> ТипЗнч(Данные2) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(Данные1) = Тип("Структура")
		Или ТипЗнч(Данные1) = Тип("ФиксированнаяСтруктура") Тогда
		
		Если Данные1.Количество() <> Данные2.Количество() Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Для каждого КлючИЗначение Из Данные1 Цикл
			Если Исключаемые.Найти(КлючИЗначение.Ключ) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			СтароеЗначение = Неопределено;
			
			Если Не Данные2.Свойство(КлючИЗначение.Ключ, СтароеЗначение) // АПК:1415 Сравнение произвольных структур.
				Или Не ДанныеСовпадают(КлючИЗначение.Значение, СтароеЗначение, Пропустить) Тогда
			
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Возврат Истина;
		
	ИначеЕсли ТипЗнч(Данные1) = Тип("Соответствие")
		Или ТипЗнч(Данные1) = Тип("ФиксированноеСоответствие") Тогда
		
		Если Данные1.Количество() <> Данные2.Количество() Тогда
			Возврат Ложь;
		КонецЕсли;
		
		КлючиНовогоСоответствия = Новый Соответствие;
		
		Для каждого КлючИЗначение Из Данные1 Цикл
			Если Исключаемые.Найти(КлючИЗначение.Ключ) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			КлючиНовогоСоответствия.Вставить(КлючИЗначение.Ключ, Истина);
			СтароеЗначение = Данные2.Получить(КлючИЗначение.Ключ);
			
			Если Не ДанныеСовпадают(КлючИЗначение.Значение, СтароеЗначение, Пропустить) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Для каждого КлючИЗначение Из Данные2 Цикл
			Если КлючиНовогоСоответствия[КлючИЗначение.Ключ] = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Возврат Истина;
		
	ИначеЕсли ТипЗнч(Данные1) = Тип("Массив")
		Или ТипЗнч(Данные1) = Тип("ФиксированныйМассив") Тогда
		
		Если Данные1.Количество() <> Данные2.Количество() Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Индекс = Данные1.Количество() - 1;
		Пока Индекс >= 0 Цикл
			Если Не ДанныеСовпадают(Данные1.Получить(Индекс), Данные2.Получить(Индекс), Пропустить) Тогда
				Возврат Ложь;
			КонецЕсли;
			Индекс = Индекс - 1;
		КонецЦикла;
		
		Возврат Истина;

	КонецЕсли;
	
	Возврат Данные1 = Данные2;
	
КонецФункции

// Выполняет проверку заполнения всех полей аутентификации, которые были программно созданы на форме.
//   Если переданы ошибочные параметры, то считается, что поля не заполнены и в поле ВсеЗаполнены возвращается значение
//   Ложь. При этом, поле НезаполненныеПоля будет содержать пустой массив, а в поле СообщениеОбОшибке будет указана
//   причина ошибки.
//   Если все параметры переданы правильно, то поле СообщениеОбОшибке будет пустое.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма на которой необходимо проверить введенные данные. На формы должен быть
//    реквизит ДинамическиеРеквизиты.
//  ТипПолей - Строка - тип полей, которые необходимо проверить. Одно из следующих значений:
//    -- "НастройкиАутентификации" - поля для ввода параметров подключения с сервисам банка.
//    -- "ПрикладныеНастройки" - поля для ввода прикладных настроек.
// 
// Возвращаемое значение:
//  Структура - результат проверки заполнения полей:
//    * ВсеЗаполнены - Булево - Истина, если все поля заполнены, Ложь в противном случае
//    * СообщениеОбОшибке - Строка - Сообщение об ошибке в процессе проверки.
//    * НезаполненныеПоля - Массив Из Структура - Описание не заполненных полей:
//        ** Представление - Строка - представление поля.
//        ** ИмяРеквизита - Строка - Имя реквизита, связанного с полем
//
Функция ДинамическиеРеквизитыЗаполнены(Форма, ТипПолей) Экспорт

	Результат = Новый Структура;
	
	Результат.Вставить("ВсеЗаполнены",      Ложь);
	Результат.Вставить("СообщениеОбОшибке", "");
	Результат.Вставить("НезаполненныеПоля", Новый Массив);
	
	Эталон = НовыйДинамическиеРеквизитыПодключения();
	
	Если ТипЗнч(Форма.ДинамическиеРеквизиты) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Эталон, Форма.ДинамическиеРеквизиты);
	Иначе
		Результат.СообщениеОбОшибке = НСтр("ru = 'Ошибка при проверке динамических реквизитов:
		|Тип реквизита формы ДинамическиеРеквизиты отличается от ожидаемого';
		|en = 'Error checking dynamic attributes:
		|The ДинамическиеРеквизиты from attribute type differs from the expected one'");		
		Возврат Результат;
	КонецЕсли;
	
	Если ТипПолей = "НастройкиАутентификации" Тогда
		ИмяНастройки = "НастройкиАутентификации";
		ПредставлениеНастройки = НСтр("ru = 'Настройки подключения';
										|en = 'Connection settings'");
	ИначеЕсли ТипПолей = "ПрикладныеНастройки" Тогда
		ИмяНастройки = "ПрикладныеНастройки";
		ПредставлениеНастройки =  НСтр("ru = 'Настройки оплат';
										|en = 'Payment settings'");
	Иначе
		Результат.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка при проверке динамических реквизитов:
				|тип %1 не найден';
				|en = 'Error checking dynamic attributes:
				|the %1 type is not found'"),
			ИмяНастройки);
		Возврат Результат;
	КонецЕсли;
	
	Если Эталон[ИмяНастройки].Количество() = 0 Тогда
		Результат.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка при проверке заполнения полей ""%1"": поля отсутствуют.';
				|en = 'Error when checking the ""%1"" fields: the fields are missing.'"),
			ПредставлениеНастройки);
		Возврат Результат;
	КонецЕсли;
	
	Отказ = Ложь;
	Для Каждого ОписаниеРеквизита Из Эталон[ИмяНастройки] Цикл
		
		ЭталонПоля = НовыйОписаниеДинамическогоРеквизита();
		ЗаполнитьЗначенияСвойств(ЭталонПоля, ОписаниеРеквизита);
		
		Если Не ЭталонПоля.ПроверкаЗаполнения
			Или ЭталонПоля.ЭтоНастройкаПодключения Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Форма[ЭталонПоля.ИмяРеквизита]) Тогда
			Результат.НезаполненныеПоля.Добавить(
				Новый Структура(
					"Представление, ИмяРеквизита",
					ЭталонПоля.Представление,
					ЭталонПоля.ИмяРеквизита));
			Отказ = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Результат.ВсеЗаполнены = Не Отказ;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти
