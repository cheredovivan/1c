
#Область СлужебныйПрограммныйИнтерфейс

// Открывает форму редактирования адреса
// 
// Параметры:
// 	ПредставлениеАдреса - Строка - представление адреса.
// 	ЗначениеПолейАдреса - Строка - строка JSON или XML контактной информации, соответствующая XDTO-пакету КонтактнаяИнформация.
// 	Оповещение - ОписаниеОповещения - обработчик оповещения, который будет вызван после закрытия формы.
//  ДанныеКонтактнойИнформации - Неопределено,
//								- Структура:
//								* Значение - см. УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению
//								* ПараметрыВида - см. УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации
//
Процедура НачатьРедактированиеАдреса(ПредставлениеАдреса, ЗначениеПолейАдреса = "",
	Оповещение = Неопределено, ДанныеКонтактнойИнформации = Неопределено) Экспорт
	
	Если ДанныеКонтактнойИнформации <> Неопределено Тогда
		ПараметрыВида = ДанныеКонтактнойИнформации.ПараметрыВида;
	Иначе
		
		ТипАдрес = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес");
		
		ДанныеКонтактнойИнформации = ИнтеграцияБСПБЭДСлужебныйВызовСервера.ДанныеКонтактнойИнформацииПоПредставлению(
			ПредставлениеАдреса, ТипАдрес);
		
		ПараметрыВида = ДанныеКонтактнойИнформации.ПараметрыВида;
		ПараметрыВида.РедактированиеТолькоВДиалоге = Истина;
		ПараметрыВида.ОбязательноеЗаполнение = Истина;
		ПараметрыВида.Наименование = НСтр("ru = 'Юридический адрес организации';
											|en = 'Company legal address'");
		ПараметрыВида.НастройкиПроверки.ПроверятьКорректность = Ложь;
		
	КонецЕсли;
	
	Если ЗначениеПолейАдреса = "" Тогда
		ЗначениеПолейАдреса = ДанныеКонтактнойИнформации.Значение;
	КонецЕсли;
	
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		ПараметрыВида, ЗначениеПолейАдреса, ПредставлениеАдреса,, ТипАдрес);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, , Оповещение);
	
КонецПроцедуры

// Открывает форму редактирования телефона
// 
// Параметры:
// 	ПредставлениеТелефона - Строка - представление адреса.
// 	ЗначениеПолейТелефона - Строка - строка JSON или XML контактной информации, соответствующая XDTO-пакету КонтактнаяИнформация.
// 	Оповещение - ОписаниеОповещения - обработчик оповещения, который будет вызван после закрытия формы.
//  ДанныеКонтактнойИнформации - Неопределено,
//								- Структура:
//								* Значение - см. УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению
//								* ПараметрыВида - см. УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации
//
Процедура НачатьРедактированиеТелефона(ПредставлениеТелефона, ЗначениеПолейТелефона = "",
	Оповещение = Неопределено, ДанныеКонтактнойИнформации = Неопределено) Экспорт
	
	Если ДанныеКонтактнойИнформации <> Неопределено Тогда
		ПараметрыВида = ДанныеКонтактнойИнформации.ПараметрыВида;
	Иначе
		
		ТипТелефон = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Телефон");
		
		ДанныеКонтактнойИнформации = ИнтеграцияБСПБЭДСлужебныйВызовСервера.ДанныеКонтактнойИнформацииПоПредставлению(
			ПредставлениеТелефона, ТипТелефон);
		
		ПараметрыВида = ДанныеКонтактнойИнформации.ПараметрыВида;
		ПараметрыВида.РедактированиеТолькоВДиалоге = Истина;
		ПараметрыВида.ОбязательноеЗаполнение = Истина;
		ПараметрыВида.Наименование = НСтр("ru = 'Телефон';
											|en = 'Phone number'");
		
	КонецЕсли;
	
	Если ЗначениеПолейТелефона = "" Тогда
		ЗначениеПолейТелефона = ДанныеКонтактнойИнформации.Значение;
	КонецЕсли;
	
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		ПараметрыВида, ЗначениеПолейТелефона, ПредставлениеТелефона,, ТипТелефон);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, , Оповещение);
	
КонецПроцедуры

// Открывает переданный сертификат на просмотр.
// 
// Параметры:
// 	АдресДанныхСертификата - адрес во временном хранилища, в котором лежит сертификат.
Процедура ПоказатьСертификат(АдресДанныхСертификата) Экспорт
	
	СтруктураСертификата = ИнтеграцияБСПБЭДСлужебныйВызовСервера.СвойстваСертификата(АдресДанныхСертификата);
	
	Если СтруктураСертификата <> Неопределено Тогда
		ПараметрыФормы = Новый Структура("СтруктураСертификата, Отпечаток, АдресСертификата",
			СтруктураСертификата, СтруктураСертификата.Отпечаток, АдресДанныхСертификата);
		ОткрытьФорму("ОбщаяФорма.Сертификат", ПараметрыФормы);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Сертификат не найден';
														|en = 'Certificate is not found'"));
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ПодписанныйОбъект - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы 
//  ПорядковыйНомерПодписи - Число
//  ИдентификаторФормы - УникальныйИдентификатор
Процедура ПоказатьСертификатПодписиПодписанногоОбъекта(ПодписанныйОбъект, ПорядковыйНомерПодписи,
	ИдентификаторФормы) Экспорт
	
	ДанныеСертификата = ИнтеграцияБСПБЭДСлужебныйВызовСервера.СвойстваСертификатаПодписиПодписанногоОбъекта(
		ПодписанныйОбъект, ПорядковыйНомерПодписи, ИдентификаторФормы);
	
	Если ЗначениеЗаполнено(ДанныеСертификата.СвойстваСертификата) Тогда
		ПараметрыФормы = Новый Структура("СтруктураСертификата, Отпечаток, АдресСертификата",
			ДанныеСертификата.СвойстваСертификата, 
			ДанныеСертификата.СвойстваСертификата.Отпечаток,
			ДанныеСертификата.АдресДанныхСертификата);
		ОткрытьФорму("ОбщаяФорма.Сертификат", ПараметрыФормы);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Сертификат не найден';
														|en = 'Certificate is not found'"));
	КонецЕсли;

КонецПроцедуры

// Возвращает имя события, по которому БСП оповещает о записи сертификата.
// 
// Возвращаемое значение:
// 	Строка - имя события.
Функция ИмяСобытияЗаписьСертификата() Экспорт
	
	Возврат "Запись_СертификатыКлючейЭлектроннойПодписиИШифрования";
	
КонецФункции

Процедура ПодключитьОбсуждения(ОписаниеЗавершения) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОписаниеЗавершения", ОписаниеЗавершения);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодключенияОбсуждений", ИнтеграцияБСПБЭДСлужебныйКлиент, ДополнительныеПараметры);
	
	МодульОбсужденияСлужебныйКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбсужденияСлужебныйКлиент"); // АПК:271 Не проверять наличие модуля
	МодульОбсужденияСлужебныйКлиент.ПоказатьПодключение(ОписаниеОповещения);
	
КонецПроцедуры

// Конструктор параметров для метода см. ОткрытьФормуВыбораФайловИзОбщегоХранилища
// 
// Возвращаемое значение:
// 	Структура - имя события:
//	* ЗакрыватьПриВыборе - Булево - признак закрытия формы после выбора файла.
//	* МножественныйВыбор - Булево - признак доступности множественного выбора файла.
//	
Функция НовыеПараметрыВыбораФайловИзОбщегоХранилища() экспорт

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Ложь);
	
	Возврат ПараметрыФормы;
	
КонецФункции

// Открывает форму выбора файлов из общего хранилища.
// 
// Параметры:
// 	Параметры - См. НовыеПараметрыВыбораФайловИзОбщегоХранилища
// 	Оповещение - обработчик оповещения, который будет вызван после выбора файлов. В качестве результата может быть передано:
// 	  * Неопределено - пользователь отказался от выбора
// 	  * СправочникСсылка.Файлы - ссылка на выбранный файл
// 	  * Массив - массив из выбранных файлов в случае множественного выбора.
//
Процедура ОткрытьФормуВыбораФайловИзОбщегоХранилища(Параметры, Оповещение) Экспорт
	
	ТекстПредупреждения = "";
	Если НЕ ИнтеграцияБСПБЭДСлужебныйВызовСервера.ЕстьДоступКХранилищуФайлов(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение( ,ТекстПредупреждения);
		Возврат;
	КонецЕсли;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Параметры.ЗакрыватьПриВыборе);
	ПараметрыФормы.Вставить("МножественныйВыбор", Параметры.МножественныйВыбор);
		
	ОткрытьФорму("Справочник.Файлы.ФормаВыбора", ПараметрыФормы,,,,, Оповещение);
	
КонецПроцедуры

#КонецОбласти
