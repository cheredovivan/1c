
#Область ПрограммныйИнтерфейс

// Документы к сверке.
// 
// Параметры:
//  ПараметрыДокументовСверки - Структура из КлючИЗначение - Параметры документов сверки.
//  *ДатаНачала - Дата
//  *ДатаОкончания - Дата
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Документы к сверке.
//
Функция ДокументыКСверке(ПараметрыДокументовСверки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КомпоновщикОтбор = Новый КомпоновщикНастроекКомпоновкиДанных;
	Документы.СверкаВзаиморасчетов2_5_11.ИнициализироватьКомпоновщикНастроек(КомпоновщикОтбор);
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "ТолькоНеОтправленныеНаСверку", ПараметрыДокументовСверки.ТолькоНеОтправленныеНаСверку);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "НачалоПериода", ПараметрыДокументовСверки.ДатаНачала);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "КонецПериода", КонецДня(ПараметрыДокументовСверки.ДатаОкончания));
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "Период", ПараметрыДокументовСверки.ДатаНачала);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "ПолучатьДокументыСверки", Ложь);
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "РежимСверкиПоДоговорам", Истина);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "РазбиватьПоТипамРасчетов", Ложь);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "РазбиватьПоПартнерам", Ложь);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "РазбиватьПоДоговорам", Ложь);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "СверкаВВалюте", Ложь);
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "Организации", ПараметрыДокументовСверки.МассивОрганизаций);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикОтбор.Настройки, "Контрагенты", ПараметрыДокументовСверки.МассивКонтрагентов);
	
	СоединениеСервиса = 
		"	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
		|		ПО ДанныеСверки.РасчетныйДокумент = РеестрДокументов.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности КАК ВзаимозачетЗадолженности
		|		ПО ДанныеСверки.РасчетныйДокумент = ВзаимозачетЗадолженности.Ссылка
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК РеестрСверки
		|	ПО ДанныеСверки.РасчетныйДокумент = РеестрСверки.Документ
		|		И (&ТолькоНеОтправленныеНаСверку)";
	
	ОтборСервиса = 
		"	И НЕ ЕСТЬNULL(РеестрДокументов.СторноИсправление, ЛОЖЬ)
		|	И НЕ ЕСТЬNULL(ВзаимозачетЗадолженности.ВидОперации, НЕОПРЕДЕЛЕНО) В (
		|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ОплатаПодарочнымСертификатом),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ОтражениеОплатыЧерезКомиссионера),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ОтражениеВозвратаОплатыЧерезКомиссионера),
		|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.Бартер))
		|	И ВЫБОР
		|		КОГДА &ТолькоНеОтправленныеНаСверку
		|			ТОГДА РеестрСверки.Документ ЕСТЬ NULL
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ
		|	И " + ТекстЗапросаОтбораПоДокументамПодлежащимСверке("%1 ДанныеСверки.РасчетныйДокумент ССЫЛКА Документ.%2");
	
	ФрагментыЗапросаСервиса = Новый Структура();
	ФрагментыЗапросаСервиса.Вставить("Соединение", СоединениеСервиса);
	ФрагментыЗапросаСервиса.Вставить("Отбор", ОтборСервиса);
	
	ТаблицаРезультатаСКД = Документы.СверкаВзаиморасчетов2_5_11.ПолучитьВзаиморасчеты(КомпоновщикОтбор, ФрагментыЗапросаСервиса);// ТаблицаЗначений - 
	ТаблицаРезультатаСКД.Свернуть("Организация,Контрагент,РасчетныйДокумент,НомерДокумента,ДатаДокумента,НомерДокументаИБ,ДатаДокументаИБ","ОборотДт,ОборотКт");
	Индекс = 0;
	Пока Индекс < ТаблицаРезультатаСКД.Количество() Цикл
		Запись = ТаблицаРезультатаСКД[Индекс];
		Если ТипЗнч(Запись.РасчетныйДокумент) = Тип("ДокументСсылка.ВзаимозачетЗадолженности")
			И (Запись.ОборотДт - Запись.ОборотКт) = 0 Тогда
			ТаблицаРезультатаСКД.Удалить(Индекс);
			Продолжить;
		КонецЕсли;
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Возврат ТаблицаРезультатаСКД;
	
КонецФункции

// Подготовить отчет о расхождениях.
// 
// Параметры:
//  ПараметрыОтчета - Структура
// 
// Возвращаемое значение:
//  Структура - Подготовить отчет о расхождениях:
//   * Выполнено - Булево - 
//   * Результат - ТабличныйДокумент - 
//   * ДанныеРасшифровки - ДанныеРасшифровкиКомпоновкиДанных- 
//   * КонтрольноеСоотношениеИтоговВыполняется - Булево - 
//   * КраткоеПредставлениеОшибки - Строка - 
//   * ПодробноеПредставлениеОшибки - Строка - 
//
Функция ПодготовитьОтчетОРасхождениях(ПараметрыОтчета) Экспорт
	
	Перем ДанныеРасшифровкиОбъект;
	
	Результат = Новый ТабличныйДокумент;
	
	РезультатФормированияОтчета = Новый Структура;
	РезультатФормированияОтчета.Вставить("Выполнено",         Истина);
	РезультатФормированияОтчета.Вставить("Результат",         Результат);
	РезультатФормированияОтчета.Вставить("ДанныеРасшифровки", ПараметрыОтчета.ДанныеРасшифровки);
	РезультатФормированияОтчета.Вставить("КонтрольноеСоотношениеИтоговВыполняется", Истина);
	РезультатФормированияОтчета.Вставить("КраткоеПредставлениеОшибки",   "");
	РезультатФормированияОтчета.Вставить("ПодробноеПредставлениеОшибки", "");
	
	МенеджерОтчета = Отчеты[ПараметрыОтчета.ИдентификаторОтчета];
	
	ПараметрыИсполненияОтчета = ПолучитьПараметрыИсполненияОтчета(ПараметрыОтчета, МенеджерОтчета);
	
	Если ПараметрыИсполненияОтчета.ИнициализацияВПривилегированномРежиме Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыОтчета.СхемаКомпоновкиДанных) = Тип("Строка") Тогда
		Если ЭтоАдресВременногоХранилища(ПараметрыОтчета.СхемаКомпоновкиДанных) Тогда
			СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(ПараметрыОтчета.СхемаКомпоновкиДанных);
		КонецЕсли;
	Иначе
		СхемаКомпоновкиДанных = ПараметрыОтчета.СхемаКомпоновкиДанных;
	КонецЕсли;
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.ЗагрузитьНастройки(ПараметрыОтчета.НастройкиКомпоновкиДанных);
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	
	Если ПараметрыИсполненияОтчета.ИспользоватьПередКомпоновкойМакета Тогда
		МенеджерОтчета.ПередКомпоновкойМакета(ПараметрыОтчета, СхемаКомпоновкиДанных, КомпоновщикНастроек);
	КонецЕсли;
	
	КомпоновщикНастроек.Восстановить();
	
	НастройкиДляКомпоновкиМакета = КомпоновщикНастроек.ПолучитьНастройки();
	
	// Сгенерируем макет компоновки данных при помощи компоновщика макета
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;

	Попытка
		
		// В качестве схемы компоновки будет выступать схема самого отчета.
		// В качестве настроек отчета - текущие настройки отчета.
		// Данные расшифровки будем помещать в ДанныеРасшифровки.
		Если ПараметрыИсполненияОтчета.ИспользоватьДанныеРасшифровки Тогда 
			МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиДляКомпоновкиМакета, ДанныеРасшифровкиОбъект);
		Иначе
			МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиДляКомпоновкиМакета);
		КонецЕсли;
		
		// Вызываем событие отчета
		Если ПараметрыИсполненияОтчета.ИспользоватьПослеКомпоновкиМакета Тогда
			МенеджерОтчета.ПослеКомпоновкиМакета(ПараметрыОтчета, МакетКомпоновки);
		КонецЕсли;

		Если ПараметрыИсполненияОтчета.ИспользоватьВнешниеНаборыДанных Тогда
			ВнешниеНаборыДанных = МенеджерОтчета.ПолучитьВнешниеНаборыДанных(ПараметрыОтчета, МакетКомпоновки);
		КонецЕсли;
	
		// Создадим и инициализируем процессор компоновки
		ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
		Если ВнешниеНаборыДанных = Неопределено Тогда
			ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,, ДанныеРасшифровкиОбъект, Истина);
		Иначе
			ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровкиОбъект, Истина);
		КонецЕсли;	
		
		// Создадим и инициализируем процессор вывода результата
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
		ПроцессорВывода.УстановитьДокумент(Результат);
		
		// Перед началом вывода проверим, нужен ли привилегированный режим
		ПредыдущееЗначениеПривилегированногоРежима = Неопределено;
		Если Не ПривилегированныйРежим() И ПараметрыИсполненияОтчета.ИспользоватьПривилегированныйРежим Тогда
			ПредыдущееЗначениеПривилегированногоРежима = Ложь;
			УстановитьПривилегированныйРежим(Истина);
		ИначеЕсли ПривилегированныйРежим() И Не ПараметрыИсполненияОтчета.ИспользоватьПривилегированныйРежим Тогда 
			ПредыдущееЗначениеПривилегированногоРежима = Истина;
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
	
		// Обозначим начало вывода
		ПроцессорВывода.НачатьВывод();
		
		Если ПараметрыИсполненияОтчета.ИспользоватьПередВыводомЭлементаРезультата Тогда
			// Основной цикл вывода отчета
			ЭлементРезультата = ПроцессорКомпоновки.Следующий();
			Пока ЭлементРезультата <> Неопределено Цикл
				
				ПропуститьЭлемент = Ложь;
				МенеджерОтчета.ПередВыводомЭлементаРезультата(
					ПараметрыОтчета,
					МакетКомпоновки,
					ДанныеРасшифровкиОбъект,
					ЭлементРезультата,
					ПропуститьЭлемент);
				
				Если Не ПропуститьЭлемент Тогда // Элемент получен, выведем его при помощи процессора вывода.
					ПроцессорВывода.ВывестиЭлемент(ЭлементРезультата);
				КонецЕсли;	

				// Получим следующий элемент результата компоновки.
				ЭлементРезультата = ПроцессорКомпоновки.Следующий();
				
			КонецЦикла;
			
			// Завершение вывода отчета
			ПроцессорВывода.ЗакончитьВывод();

		Иначе

			ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);

		КонецЕсли;
		
		// Проверим, нет ли очевидных расхождений в общих итогах, вызванных повреждением информационной базы.
		Если ПараметрыИсполненияОтчета.ПроверкаИтогов Тогда
			МенеджерОтчета.ПроверитьКорректностьИтогов(ПараметрыОтчета, Результат);
		КонецЕсли;
		
		// После окончания процессором вывода отчета, поместим расшифровку во временное хранилище.
		ДанныеДляРасшифровки = Новый Структура("Объект, ДанныеРасшифровки", ПараметрыОтчета, ДанныеРасшифровкиОбъект);
		РезультатФормированияОтчета.ДанныеРасшифровки = ПоместитьВоВременноеХранилище(
			ДанныеДляРасшифровки, РезультатФормированияОтчета.ДанныеРасшифровки);
		
		// Возвращаем предыдущее значение привилегированного режима, если он изменялся.
		Если ПредыдущееЗначениеПривилегированногоРежима <> Неопределено Тогда
			УстановитьПривилегированныйРежим(ПредыдущееЗначениеПривилегированногоРежима);
		КонецЕсли;
		
	Исключение

		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
			
		// Записываем в журнал регистрации
		Событие = НСтр("ru = 'Ошибка формирования отчета';
						|en = 'Reporting error'", ОбщегоНазначения.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(
			Событие,
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Отчеты[ПараметрыОтчета.ИдентификаторОтчета],
			, // Данных нет
			ПодробноеПредставлениеОшибки);
		
		// Информируем пользователя файловой версии
		Пока ИнформацияОбОшибке.Причина <> Неопределено Цикл
			ИнформацияОбОшибке = ИнформацияОбОшибке.Причина;
		КонецЦикла;
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Отчет не сформирован! %1';
										|en = 'The report is not generated. %1'"), ИнформацияОбОшибке.Описание);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
		РезультатФормированияОтчета.Выполнено                    = Ложь;
		РезультатФормированияОтчета.КраткоеПредставлениеОшибки   = ТекстСообщения;
		РезультатФормированияОтчета.ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки;
		
		Возврат РезультатФормированияОтчета;

	КонецПопытки;

	МенеджерОтчета.ПослеВыводаРезультата(ПараметрыОтчета, Результат);
	
	Возврат РезультатФормированияОтчета;
	
КонецФункции

// См. СервисСверкиРасчетов.ДанныеОРелевантныхКонтрагентах()
//
// Возвращаемое значение:
//  Структура из КлючИЗначение
//
Функция ДанныеОРелевантныхКонтрагентах() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНачала", СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов());
	Запрос.Текст = ТекстЗапросаРелевантныеКонтрагенты();
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	РелевантныеКонтрагенты = Результат.Выгрузить();

	// Если к сервису подключены менее трех релевантных контрагентов, то считаем, что рассказывать об этом рано.
	МинимальноеКоличествоКонтрагентовДляПоказаБаннера = 3;
	Если РелевантныеКонтрагенты.Количество() < МинимальноеКоличествоКонтрагентовДляПоказаБаннера Тогда
		Возврат Неопределено;
	КонецЕсли;

	КэшДанныхОРелевантныхКонтрагентах = Новый Структура();
	КэшДанныхОРелевантныхКонтрагентах.Вставить("РелевантныеКонтрагенты", РелевантныеКонтрагенты);
	КэшДанныхОРелевантныхКонтрагентах.Вставить("КоличествоПодключенныхКонтрагентов", РелевантныеКонтрагенты.Количество());
	
	Возврат КэшДанныхОРелевантныхКонтрагентах;
	
КонецФункции

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.РегистрыСведений.СервисСверкиРасчетовДанныеСервиса, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.СервисСверкиРасчетовОбнаруженныеРасхождения, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.СервисСверкиРасчетовРеестрДокументов, Истина);
	Списки.Вставить(Метаданные.РегистрыСведений.СервисСверкиРасчетовУстраненныеРасхождения, Истина);
	
КонецПроцедуры

// Текст запроса наличие незарегистрированных документов.
// 
// Возвращаемое значение:
//  Строка - Текст запроса наличие незарегистрированных документов
Функция ТекстЗапросаНаличиеНезарегистрированныхДокументов() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОрганизацииБизнесСеть.Организация КАК Организация
	|ПОМЕСТИТЬ МассивОрганизаций
	|ИЗ
	|	РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Контрагент
	|ПОМЕСТИТЬ МассивКонтрагентов
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	НЕ Контрагенты.ПометкаУдаления
	|	И Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|	И Контрагенты.ИНН <> """"
	|	И ВЫБОР
	|		КОГДА Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА Контрагенты.КПП <> """"
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.Ссылка КАК Организации
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	НЕ Организации.ПометкаУдаления
	|	И Организации.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|	И Организации.ИНН <> """"
	|	И ВЫБОР
	|		КОГДА Организации.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА Организации.КПП <> """"
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Аналитика.КлючАналитики КАК КлючАналитики,
	|	Аналитика.Организация КАК Организация,
	|	Аналитика.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Аналитика.Партнер КАК Партнер,
	|	ВЫБОР КОГДА Аналитика.Договор = НЕОПРЕДЕЛЕНО
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		ИНАЧЕ Аналитика.Договор
	|	КОНЕЦ КАК ДоговорПартнера,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ТипДоговора, ЕСТЬNULL(ДоговорыОрганизаций.ТипДоговора, НЕОПРЕДЕЛЕНО)) КАК ТипДоговора,
	|	Аналитика.Контрагент КАК Контрагент
	|	
	|ПОМЕСТИТЬ АналитикаПоПартнерам
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО Аналитика.Договор = ДоговорыКонтрагентов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыМеждуОрганизациями КАК ДоговорыОрганизаций
	|		ПО Аналитика.Договор = ДоговорыОрганизаций.Ссылка
	|ГДЕ
	|	Аналитика.Организация В (ВЫБРАТЬ Организация ИЗ МассивОрганизаций)
	|	И Аналитика.Контрагент В (ВЫБРАТЬ Контрагент ИЗ МассивКонтрагентов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Аналитика.КлючАналитики
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Контрагент КАК Контрагент,
	|	ТИПЗНАЧЕНИЯ(ВложенныйЗапрос.Регистратор) = ТИП(Документ.ВзаимозачетЗадолженности) КАК ЭтоВзаимозачет,
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|	СУММА(ВложенныйЗапрос.Долг) КАК Долг,
	|	СУММА(ВложенныйЗапрос.ДолгРегл) КАК ДолгРегл,
	|	СУММА(ВложенныйЗапрос.Предоплата) КАК Предоплата,
	|	СУММА(ВложенныйЗапрос.ПредоплатаРегл) ПредоплатаРегл,
	|	10 КАК ПорядокТипаПлатежа
	|ПОМЕСТИТЬ Обороты
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыСКлиентами.Период КАК Период,
	|		Аналитика.ТипДоговора КАК ТипДоговора,
	|		Аналитика.Организация КАК Организация,
	|		Аналитика.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		Аналитика.Контрагент КАК Контрагент,
	|		РасчетыСКлиентами.ДокументРегистратор КАК Регистратор,
	|		РасчетыСКлиентами.Валюта КАК Валюта,
	|		
	|		ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСКлиентами.Долг
	|			ИНАЧЕ -РасчетыСКлиентами.Долг
	|		КОНЕЦ КАК Долг,
	|		ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСКлиентами.ДолгРегл
	|			ИНАЧЕ -РасчетыСКлиентами.ДолгРегл
	|		КОНЕЦ КАК ДолгРегл,
	|		ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСКлиентами.Предоплата
	|			ИНАЧЕ -РасчетыСКлиентами.Предоплата
	|		КОНЕЦ КАК Предоплата,
	|		ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСКлиентами.ПредоплатаРегл
	|			ИНАЧЕ -РасчетыСКлиентами.ПредоплатаРегл
	|		КОНЕЦ КАК ПредоплатаРегл
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаПоПартнерам КАК Аналитика
	|			ПО РасчетыСКлиентами.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК Реестр
	|			ПО РасчетыСКлиентами.ДокументРегистратор = Реестр.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности КАК Взаимозачет
	|			ПО РасчетыСКлиентами.ДокументРегистратор = Взаимозачет.Ссылка
	|	ГДЕ
	|		РасчетыСКлиентами.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		И НЕ ЕСТЬNULL(Реестр.СторноИсправление, ЛОЖЬ)
	|		И ТИПЗНАЧЕНИЯ(РасчетыСКлиентами.Регистратор) <> ТИП(Документ.РасчетКурсовыхРазниц)
	|		И НЕ ЕСТЬNULL(Взаимозачет.ВидОперации, НЕОПРЕДЕЛЕНО) В (
	|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ОплатаПодарочнымСертификатом),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ОтражениеОплатыЧерезКомиссионера),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ОтражениеВозвратаОплатыЧерезКомиссионера),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.Бартер)
	|		)
	|	) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Контрагент,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.Валюта
	|	
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Долг) <> 0 ИЛИ СУММА(ВложенныйЗапрос.ДолгРегл) <> 0 
	|	ИЛИ СУММА(ВложенныйЗапрос.Предоплата) <> 0 ИЛИ СУММА(ВложенныйЗапрос.ПредоплатаРегл) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Контрагент КАК Контрагент,
	|	ТИПЗНАЧЕНИЯ(ВложенныйЗапрос.Регистратор) = ТИП(Документ.ВзаимозачетЗадолженности) КАК ЭтоВзаимозачет,
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|	СУММА(ВложенныйЗапрос.Долг) КАК Долг,
	|	СУММА(ВложенныйЗапрос.ДолгРегл) КАК ДолгРегл,
	|	СУММА(ВложенныйЗапрос.Предоплата) КАК Предоплата,
	|	СУММА(ВложенныйЗапрос.ПредоплатаРегл) КАК ПредоплатаРегл,
	|	20 КАК ПорядокТипаПлатежа
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыСПоставщиками.Период КАК Период,
	|		Аналитика.ТипДоговора КАК ТипДоговора,
	|		Аналитика.Организация КАК Организация,
	|		Аналитика.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		Аналитика.Контрагент КАК Контрагент,
	|		РасчетыСПоставщиками.ДокументРегистратор КАК Регистратор,
	|		РасчетыСПоставщиками.Валюта КАК Валюта,
	|		
	|		ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСПоставщиками.Долг
	|			ИНАЧЕ -РасчетыСПоставщиками.Долг
	|		КОНЕЦ КАК Долг,
	|		ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСПоставщиками.ДолгРегл
	|			ИНАЧЕ -РасчетыСПоставщиками.ДолгРегл
	|		КОНЕЦ КАК ДолгРегл,
	|		ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСПоставщиками.Предоплата
	|			ИНАЧЕ -РасчетыСПоставщиками.Предоплата
	|		КОНЕЦ КАК Предоплата,
	|		ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСПоставщиками.ПредоплатаРегл
	|			ИНАЧЕ -РасчетыСПоставщиками.ПредоплатаРегл
	|		КОНЕЦ КАК ПредоплатаРегл
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщиками
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаПоПартнерам КАК Аналитика
	|			ПО РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК Реестр
	|			ПО РасчетыСПоставщиками.ДокументРегистратор = Реестр.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВзаимозачетЗадолженности КАК Взаимозачет
	|			ПО РасчетыСПоставщиками.ДокументРегистратор = Взаимозачет.Ссылка
	|	ГДЕ
	|		РасчетыСПоставщиками.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		И НЕ ЕСТЬNULL(Реестр.СторноИсправление, ЛОЖЬ)
	|		И ТИПЗНАЧЕНИЯ(РасчетыСПоставщиками.Регистратор) <> ТИП(Документ.РасчетКурсовыхРазниц)
	|		И НЕ ЕСТЬNULL(Взаимозачет.ВидОперации, НЕОПРЕДЕЛЕНО) В (
	|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ОплатаПодарочнымСертификатом),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ОтражениеОплатыЧерезКомиссионера),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.ОтражениеВозвратаОплатыЧерезКомиссионера),
	|			ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВзаимозачетаЗадолженности.Бартер)
	|		)
	|	) КАК ВложенныйЗапрос
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Контрагент,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.Валюта
	|	
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Долг) <> 0 ИЛИ СУММА(ВложенныйЗапрос.ДолгРегл) <> 0 
	|	ИЛИ СУММА(ВложенныйЗапрос.Предоплата) <> 0 ИЛИ СУММА(ВложенныйЗапрос.ПредоплатаРегл) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.ТипДоговора КАК ТипДоговора,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Контрагент КАК Контрагент,
	|	ТИПЗНАЧЕНИЯ(ВложенныйЗапрос.Регистратор) = ТИП(Документ.ВзаимозачетЗадолженности) КАК ЭтоВзаимозачет,
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|	
	|	СУММА(ВложенныйЗапрос.Дебет) КАК Дебет,
	|	СУММА(ВложенныйЗапрос.ДебетРегл) КАК ДебетРегл,
	|	СУММА(ВложенныйЗапрос.Кредит) КАК Кредит,
	|	СУММА(ВложенныйЗапрос.КредитРегл) КАК КредитРегл,
	|	
	|	ВложенныйЗапрос.ПорядокТипаПлатежа КАК ПорядокТипаПлатежа
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыОбороты.Период КАК Период,
	|		ВЫБОР 
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКредитором)
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|					ИЛИ РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСДебитором)
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Аренда)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСАрендодателем)
	|		КОНЕЦ КАК ТипРасчетов,
	|		РасчетыОбороты.Договор.ТипДоговора КАК ТипДоговора,
	|		Аналитика.Организация КАК Организация,
	|		Аналитика.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		Аналитика.Контрагент КАК Контрагент,
	|		РасчетыОбороты.Регистратор КАК Регистратор,
	|		РасчетыОбороты.Валюта КАК Валюта,
	|	
	|		РасчетыОбороты.СуммаПриход КАК Дебет,
	|		РасчетыОбороты.СуммаРеглПриход КАК ДебетРегл,
	|		РасчетыОбороты.СуммаРасход КАК Кредит,
	|		РасчетыОбороты.СуммаРеглРасход КАК КредитРегл,
	|	
	|		ВЫБОР 
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|				ТОГДА 31
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|					ИЛИ РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|				ТОГДА 32
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Аренда)
	|				ТОГДА 33
	|		КОНЕЦ КАК ПорядокТипаПлатежа
	|	ИЗ
	|		РегистрНакопления.РасчетыПоФинансовымИнструментам.Обороты(
	|			&ДатаНачала, &ДатаОкончания, Регистратор,
	|			ТипСуммы В (&ТипыСуммАренды) ИЛИ ТИПЗНАЧЕНИЯ(Договор) = ТИП(Справочник.ДоговорыКредитовИДепозитов)
	|		) КАК РасчетыОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаПоПартнерам КАК Аналитика
	|			ПО РасчетыОбороты.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК Реестр
	|			ПО РасчетыОбороты.Регистратор = Реестр.Ссылка
	|	ГДЕ
	|		РасчетыОбороты.СуммаОборот <> 0
	|		И НЕ ЕСТЬNULL(Реестр.СторноИсправление, ЛОЖЬ)
	|		И ТИПЗНАЧЕНИЯ(РасчетыОбороты.Регистратор) <> ТИП(Документ.РасчетКурсовыхРазниц)) КАК ВложенныйЗапрос
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.ТипДоговора,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Контрагент,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.Валюта,
	|	ВложенныйЗапрос.ПорядокТипаПлатежа
	|	
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Дебет) <> 0 ИЛИ СУММА(ВложенныйЗапрос.ДебетРегл) <> 0
	|	ИЛИ СУММА(ВложенныйЗапрос.Кредит) <> 0 ИЛИ СУММА(ВложенныйЗапрос.КредитРегл) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Обороты.ТипРасчетов КАК ТипРасчетов,
	|	Обороты.Регистратор КАК Документ,
	|	Обороты.ЭтоВзаимозачет КАК ЭтоВзаимозачет,
	|	Обороты.Долг КАК Долг,
	|	Обороты.Предоплата КАК Предоплата
	|ИЗ
	|	Обороты КАК Обороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|		ПО Обороты.Регистратор = СервисСверкиРасчетовРеестрДокументов.Документ
	|ГДЕ
	|	(Обороты.ЭтоВзаимозачет И Обороты.Долг - Обороты.Предоплата <> 0
	|		ИЛИ НЕ Обороты.ЭтоВзаимозачет)
	|	И СервисСверкиРасчетовРеестрДокументов.Документ ЕСТЬ NULL";
	
	ТекстЗапроса = ТекстЗапроса + "
		|	И " + ТекстЗапросаОтбораПоДокументамПодлежащимСверке();
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Стороны документа по ссылке.
// 
// Параметры:
//  ДокументСсылка  - ДокументСсылка
// 
// Возвращаемое значение:
//  Структура - Стороны документа по ссылке:
// * Организация - СправочникСсылка.Организации
// * Контрагент - СправочникСсылка.Контрагенты
//
Функция СтороныДокумента(ДокументСсылка) Экспорт
	
	ПолноеИмяОбъектаМетаданных = ДокументСсылка.Метаданные().ПолноеИмя();
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	*
	|ИЗ
	|	&ПолноеИмяМетаданных КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &Ссылка";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолноеИмяМетаданных", ПолноеИмяОбъектаМетаданных);
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	РеквизитыДокумента = Запрос.Выполнить().Выбрать();
	РеквизитыДокумента.Следующий();
	
	Результат = Новый Структура("Организация, Контрагент");
	Результат.Организация = РеквизитыДокумента.Организация;
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
			ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
		Результат.Контрагент = РеквизитыДокумента.ОрганизацияПолучатель;
	//++ НЕ УТКА
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетДавальцуМеждуОрганизациями") Тогда
		Результат.Контрагент = РеквизитыДокумента.Давалец;
	//-- НЕ УТКА
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")
			ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациямиОСписании") Тогда
		Результат.Контрагент = РеквизитыДокумента.Комиссионер;
	
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств")
			И (РеквизитыДокумента.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
				ИЛИ РеквизитыДокумента.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации) Тогда
		Результат.Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыДокумента.БанковскийСчетОтправитель,"Владелец");
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств")
			И (РеквизитыДокумента.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию
				ИЛИ РеквизитыДокумента.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию) Тогда
		Результат.Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыДокумента.БанковскийСчетПолучатель,"Владелец");
		
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВзаимозачетЗадолженности") Тогда
		Результат.Контрагент = РеквизитыДокумента.КонтрагентДебитор;
		Результат.Вставить("КонтрагентДебитор", РеквизитыДокумента.КонтрагентДебитор);
		Результат.Вставить("КонтрагентКредитор", РеквизитыДокумента.КонтрагентКредитор);
		Результат.Вставить("ОрганизацияКредитор", РеквизитыДокумента.ОрганизацияКредитор);
		
	Иначе
		Результат.Контрагент = РеквизитыДокумента.Контрагент;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Регистрирует документ задолженности по всем организациям и контрагентам документа
// 
// Параметры:
//  Ссылка - ДокументСсылка.ВзаимозачетЗадолженности
//  Реквизиты - ДокументОбъект, Структура Из КлючИЗначение -  
//  Отказ - Булево
Процедура ЗарегистрироватьВзаимозачет(Ссылка, Реквизиты, Отказ) Экспорт
	
	СтороныДокумента = Новый Структура("Организация,Контрагент");
	СтороныДокумента.Организация = Реквизиты.Организация;
	СтороныДокумента.Контрагент = Реквизиты.КонтрагентДебитор;
	Если ЗначениеЗаполнено(Реквизиты.КонтрагентДебитор) Тогда
		Отказ = Истина;
		СервисСверкиРасчетов.ЗарегистрироватьДокументКОтправке(
			СтороныДокумента.Организация, СтороныДокумента.Контрагент, Ссылка);
	КонецЕсли;
	Если ЗначениеЗаполнено(Реквизиты.КонтрагентКредитор)
		И ЗначениеЗаполнено(Реквизиты.КонтрагентДебитор)
		И Реквизиты.КонтрагентДебитор <> Реквизиты.КонтрагентКредитор Тогда
		Отказ = Истина;
		СтороныДокумента.Контрагент = Реквизиты.КонтрагентКредитор;
		СервисСверкиРасчетов.ЗарегистрироватьДокументКОтправке(
			СтороныДокумента.Организация, СтороныДокумента.Контрагент, Ссылка);
	КонецЕсли;
	Если ЗначениеЗаполнено(Реквизиты.ОрганизацияКредитор) Тогда
		Отказ = Истина;
		СтороныДокумента.Контрагент = Реквизиты.ОрганизацияКредитор;
		СервисСверкиРасчетов.ЗарегистрироватьДокументКОтправке(
			СтороныДокумента.Организация, СтороныДокумента.Контрагент, Ссылка);
		СервисСверкиРасчетов.ЗарегистрироватьДокументКОтправке(
			СтороныДокумента.Контрагент, СтороныДокумента.Организация, Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПараметрыИсполненияОтчета(ПараметрыОтчета, МенеджерОтчета)
	Перем ПараметрыИсполненияОтчета;
	
	Попытка
		ПараметрыИсполненияОтчета = МенеджерОтчета.ПолучитьПараметрыИсполненияОтчета();
	Исключение
		// Запись в журнал регистрации не требуется
	КонецПопытки;
	Если Не ЗначениеЗаполнено(ПараметрыИсполненияОтчета) Тогда
		ПараметрыИсполненияОтчета = Новый Структура;
	КонецЕсли;
	
	ПараметрыПоУмолчанию = Новый Структура;
	ПараметрыПоУмолчанию.Вставить("ПриВыводеЗаголовка",                  Ложь);
	ПараметрыПоУмолчанию.Вставить("ПередКомпоновкойМакета",              Ложь);
	ПараметрыПоУмолчанию.Вставить("ДанныеРасшифровки",                   Истина);
	ПараметрыПоУмолчанию.Вставить("ПослеКомпоновкиМакета",               Ложь);
	ПараметрыПоУмолчанию.Вставить("ВнешниеНаборыДанных",                 Ложь);
	ПараметрыПоУмолчанию.Вставить("ПриВыводеПодвала",                    Ложь);
	ПараметрыПоУмолчанию.Вставить("ПередВыводомЭлементаРезультата",      Ложь);
	ПараметрыПоУмолчанию.Вставить("ПослеВыводаРезультата",               Ложь);
	ПараметрыПоУмолчанию.Вставить("ПривилегированныйРежим",              Истина);
	ПараметрыПоУмолчанию.Вставить("ФиксированныйМакетЗаголовкаИПодвала", Ложь);

	Для каждого Параметр Из ПараметрыПоУмолчанию Цикл
		
		Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				ПараметрыИсполненияОтчета, "Использовать" + Параметр.Ключ, Неопределено) = Неопределено Тогда
			ПараметрыИсполненияОтчета.Вставить("Использовать" + Параметр.Ключ, Параметр.Значение);
		КонецЕсли;
	
	КонецЦикла;
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ПараметрыИсполненияОтчета, "ИнициализацияВПривилегированномРежиме", Неопределено) = Неопределено Тогда
		ПараметрыИсполненияОтчета.Вставить("ИнициализацияВПривилегированномРежиме", Ложь);
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ПараметрыИсполненияОтчета, "ПроверкаИтогов", Неопределено) = Неопределено Тогда
		ПараметрыИсполненияОтчета.Вставить("ПроверкаИтогов", Ложь);
	КонецЕсли;
	
	Если ПараметрыИсполненияОтчета.Свойство("ИспользоватьДанныеРасшифровки")
		И ПараметрыОтчета.Свойство("ИспользоватьДанныеРасшифровки") Тогда
		ПараметрыИсполненияОтчета.ИспользоватьДанныеРасшифровки = ПараметрыОтчета.ИспользоватьДанныеРасшифровки;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
			ПараметрыИсполненияОтчета, "МинимальнаяШиринаЗаголовка", Неопределено) = Неопределено Тогда
		ПараметрыИсполненияОтчета.Вставить("МинимальнаяШиринаЗаголовка", 0);
	КонецЕсли;
	
	Возврат ПараметрыИсполненияОтчета;
	
КонецФункции

Функция ТекстЗапросаРелевантныеКонтрагенты()
	
	Возврат
	"ВЫБРАТЬ
	|	КонтрагентыБизнесСеть.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ МассивКонтрагентов
	|ИЗ
	|	РегистрСведений.КонтрагентыБизнесСеть КАК КонтрагентыБизнесСеть
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Аналитика.Контрагент КАК Контрагент,
	|	РасчетыСКлиентами.ДолгРегл + РасчетыСКлиентами.ПредоплатаРегл КАК Сумма,
	|	РасчетыСКлиентами.ДокументРегистратор КАК Регистратор
	|ПОМЕСТИТЬ Взаиморасчеты
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентами
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|		ПО РасчетыСКлиентами.АналитикаУчетаПоПартнерам = Аналитика.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ МассивКонтрагентов КАК МассивКонтрагентов
	|		ПО Аналитика.Контрагент = МассивКонтрагентов.Контрагент
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО Аналитика.Контрагент = Контрагенты.Ссылка
	|ГДЕ
	|	РасчетыСКлиентами.Период >= &ДатаНачала
	|	И Контрагенты.ИНН <> """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Аналитика.Контрагент КАК Контрагент,
	|	РасчетыСПоставщиками.ДолгРегл + РасчетыСПоставщиками.ПредоплатаРегл КАК Сумма,
	|	РасчетыСПоставщиками.ДокументРегистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщиками
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|		ПО РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = Аналитика.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ МассивКонтрагентов КАК МассивКонтрагентов
	|		ПО Аналитика.Контрагент = МассивКонтрагентов.Контрагент
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО Аналитика.Контрагент = Контрагенты.Ссылка
	|ГДЕ
	|	РасчетыСПоставщиками.Период >= &ДатаНачала
	|	И Контрагенты.ИНН <> """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Аналитика.Контрагент КАК Контрагент,
	|	Расчеты.СуммаРегл КАК Сумма,
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам КАК Расчеты
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
	|		ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитика.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ МассивКонтрагентов КАК МассивКонтрагентов
	|		ПО Аналитика.Контрагент = МассивКонтрагентов.Контрагент
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО Аналитика.Контрагент = Контрагенты.Ссылка
	|ГДЕ
	|	Расчеты.Период >= &ДатаНачала
	|	И Контрагенты.ИНН <> """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Взаиморасчеты.Контрагент КАК Контрагент,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Взаиморасчеты.Регистратор) КАК КоличествоДокументов,
	|	СУММА(Взаиморасчеты.Сумма) КАК Оборот
	|ИЗ
	|	Взаиморасчеты КАК Взаиморасчеты
	|
	|СГРУППИРОВАТЬ ПО
	|	Взаиморасчеты.Контрагент
	|
	|УПОРЯДОЧИТЬ ПО
	|	СУММА(Взаиморасчеты.Сумма) УБЫВ";
	
КонецФункции

Функция ТекстЗапросаОтбораПоДокументамПодлежащимСверке(ШаблонУсловияНаТипДокумента = "")
	
	Если ШаблонУсловияНаТипДокумента = "" Тогда
		ШаблонУсловияНаТипДокумента = "%1 Обороты.Регистратор ССЫЛКА Документ.%2";
	КонецЕсли;
	УсловиеНаТипДокумента = "";
	ЭтоПерваяСтрока = Истина;
	ТипыДокументовПодлежащихСверке = Метаданные.ОпределяемыеТипы.СервисСверкиРасчетовДокументыПодлежащиеСверке.Тип.Типы();
	СтрокиОтбора = Новый Массив;
	Для Каждого ТипДокумента Из ТипыДокументовПодлежащихСверке Цикл
		ДокументВМетаданных = Метаданные.НайтиПоТипу(ТипДокумента);
		Если ДокументВМетаданных = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ЭтоПерваяСтрока Тогда
			УсловиеНаТипДокумента = СтрШаблон(ШаблонУсловияНаТипДокумента, "", ДокументВМетаданных.Имя);
			ЭтоПерваяСтрока = Ложь;
			СтрокиОтбора.Добавить(УсловиеНаТипДокумента);
		Иначе
			УсловиеНаТипДокумента = СтрШаблон(ШаблонУсловияНаТипДокумента, "		ИЛИ ", ДокументВМетаданных.Имя);
			СтрокиОтбора.Добавить(УсловиеНаТипДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрШаблон("(%1)", СтрСоединить(СтрокиОтбора, Символы.ПС));
	
КонецФункции

#КонецОбласти