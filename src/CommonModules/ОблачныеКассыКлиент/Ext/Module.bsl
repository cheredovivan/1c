///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ОблачныеКассы".
// ОбщийМодуль.ОблачныеКассыКлиент.
//
// Клиентские процедуры настройки подключения к Облачным кассам:
//  - открытие форм настройки параметров подключения;
//  - переход в журнал регистрации для просмотра лога.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Открывает форму настройки подключения к Облачной кассе.
//
// Параметры:
//  Касса - ОпределяемыйТип.ОблачнаяКасса - ссылка на элемент справочника для которого настроена интеграция с Облачной
//    кассой.
//  ОповещениеОЗакрытии - Неопределено - оповещение закрытия формы настроек не требуется.
//                      - ОписаниеОповещения - оповещение закрытия формы настроек:
//    * Результат - Неопределено - форма настроек подключения закрыта без сохранения.
//                - Булево - настройка подключения сохранена.
//    * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//
Процедура НастройкаПодключения(Касса, ОповещениеОЗакрытии = Неопределено) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ОблачнаяКасса", Касса);
	
	ОткрытьФорму(
		"Справочник.НастройкиПодключенияКОблачнымКассам.Форма.ФормаЭлемента",
		ПараметрыФормы,
		Касса,
		,
		,
		,
		ОповещениеОЗакрытии);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Определяет поведение системы при выборе настройки подключения к облачной кассе.
//
// Параметры:
//  Владелец - ФормаКлиентскогоПриложения - форма владелец.
//  Оповещение - ОписаниеОповещения, Неопределено - оповещение, которое
//    необходимо вызвать после завершения выбора облачной кассы. В случае успешного
//    завершения в результате оповещения будет возвращено Истина.
//  Организация - ОпределяемыйТип.Организация - организация, по которой должен быть выполнен подбор
//    настроек подключения к облачным кассам.
//
Процедура НастройкаПодключенияОрганизации(Владелец, Оповещение, Организация) Экспорт
	
	НастройкиОблачныхКасс = ОблачныеКассыВызовСервера.НастройкиОблачныхКассОрганизации(Организация);
	
	Если НастройкиОблачныхКасс.Количество() = 0 Тогда
		СоздатьНастройкуПоОрганизации(Владелец, Оповещение, Организация);
	Иначе
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
		ДополнительныеПараметры.Вставить("Владелец", Владелец);
		ДополнительныеПараметры.Вставить("Организация", Организация);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("НастройкиОблачныхКасс", НастройкиОблачныхКасс);
		
		ОткрытьФорму(
			"Справочник.НастройкиПодключенияКОблачнымКассам.Форма.ФормаВыбораНастройкиПодключения",
			ПараметрыФормы,
			Владелец,
			,
			,
			,
			Новый ОписаниеОповещения(
				"ПослеВыбораНастройкиПодключенияКОблачнойКассе",
				ЭтотОбъект,
				ДополнительныеПараметры));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПослеВыбораНастройкиПодключенияКОблачнойКассе(
		РезультатВыбора,
		ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(РезультатВыбора.НастройкаПодключения) Тогда
			ВыполнитьОбработкуОповещения(
				ДополнительныеПараметры.Оповещение,
				РезультатВыбора.НастройкаПодключения);
		ИначеЕсли ЗначениеЗаполнено(РезультатВыбора.ОблачнаяКасса) Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЭтоРежимСозданияНастройки", Истина);
			ПараметрыФормы.Вставить("ОблачнаяКасса", РезультатВыбора.ОблачнаяКасса);
			
			ОткрытьФорму(
				"Справочник.НастройкиПодключенияКОблачнымКассам.Форма.ФормаЭлемента",
				ПараметрыФормы,
				ДополнительныеПараметры.Владелец,
				,
				,
				,
				ДополнительныеПараметры.Оповещение);
		Иначе
			СоздатьНастройкуПоОрганизации(
				ДополнительныеПараметры.Владелец,
				ДополнительныеПараметры.Оповещение,
				ДополнительныеПараметры.Организация);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьНастройкуПоОрганизации(Владелец, Оповещение, Организация)
	
	ПараметрыСозданияКассы = Новый Структура;
	ПараметрыСозданияКассы.Вставить("Организация", Организация);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЭтоРежимСозданияНастройки", Истина);
	ПараметрыФормы.Вставить("ПараметрыСозданияКассы", ПараметрыСозданияКассы);
	
	ОткрытьФорму(
		"Справочник.НастройкиПодключенияКОблачнымКассам.Форма.ФормаЭлемента",
		ПараметрыФормы,
		Владелец,
		,
		,
		,
		Оповещение);
	
КонецПроцедуры

#КонецОбласти
