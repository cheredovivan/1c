///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

// @strict-types

#Область ПрограммныйИнтерфейс

#Область Подписание

// Подписывает данные, возвращает подпись и добавляет подпись в объект, если указано.
//
// Общий подход к обработке значений свойств с типом ОписаниеОповещения в параметре ОписаниеДанных.
//  При выполнении обработки оповещения в нее передается структура параметров, в которой всегда есть
//  свойство "Оповещение" типа ОписаниеОповещения, обработку которого нужно выполнить для продолжения.
//  Кроме того, в структуре всегда есть свойство ОписаниеДанных, полученное при вызове процедуры.
//  При вызове оповещения в качестве значения должна передаваться структура. Если в процессе асинхронного
//  выполнения возникает ошибка, тогда в эту структуру нужно вставить свойство ОписаниеОшибки типа Строка.
// 
// Параметры:
//  ОписаниеДанных - Структура:
//    * Операция             - Строка - заголовок формы подписания данных, например Подписание файла.
//    * ЗаголовокДанных      - Строка - заголовок элемента или набора данных, например Файл.
//    * СообщитьОЗавершении  - Булево - (необязательный) - если Ложь, то не будет показано оповещение о успешном
//                           завершении операции для представления данных, указанного рядом с заголовком.
//    * ПоказатьКомментарий  - Булево - (необязательный) - разрешает ввод комментария в форме
//                           подписания данных. Если не указан, значит Ложь.
//    * ОтборСертификатов    - Массив - (необязательный) - содержит ссылки на элементы справочника.
//                           СертификатыЭлектроннойПодписиИШифрования, которые могут быть выбраны
//                           пользователем. Отбор блокирует возможность выбора других сертификатов
//                           из личного хранилища.
//                           - Структура:
//                             * Организация - ОпределяемыйТип.Организация - содержит ссылку на организацию,
//                                 по которой будет установлен отбор в списке сертификатов пользователя 
//                                 и в списке подписантов.
//    * Подписант            - Структура - (необязательный) - параметры подписанта мобильного сервиса,
//                           см. ПараметрыПодписанта
//                           будет добавлен в справочник подписантов в случае удачной отправки.
//    * БезПодтверждения     - Булево - (необязательный) - пропустить подтверждение пользователя, если
//                           в свойстве ОтборСертификатов только один сертификат и:
//                           а) либо для сертификата указано "Вводить пароль в программе электронной подписи";
//                           б) либо пользователь запомнил пароль к сертификату на время сеанса;
//                           в) либо пароль установлен ранее методом УстановитьПарольСертификата.
//                           Если в процессе подписания возникла ошибка, тогда будет открыта форма
//                           с возможностью указать пароль. Параметр ПоказатьКомментарий игнорируется.
//    * ПередВыполнением     - ОписаниеОповещения - (необязательный) - описание обработчика дополнительной
//                           подготовки данных после выбора сертификата, которым будут подписаны данные.
//                           В этом обработчике можно заполнить параметр Данные, если он зависит
//                           от сертификата/подписанта, который в момент вызова уже вставлен в ОписаниеДанных
//                           как ВыбранныйСертификат/ВыбранныйПодписант (смотри ниже).
//                           Также можно переопределить параметр ВыбраннаяДоверенность в ОписаниеДанных.
//                           Следует учесть общий подход (смотри выше).
//    * ВыполнятьНаСервере   - Неопределено
//                           - Булево - (необязательный) - когда не указан или Неопределено,
//                           тогда выполнение будет определено автоматически: если есть сервер, то сначала
//                           на сервере, потом (при неудаче) на клиенте, потом сообщение о двух ошибках.
//                           Когда Истина: если разрешено выполнение на сервере, тогда выполнение
//                           только на сервере, при неудаче одно сообщение об ошибке на сервере.
//                           Когда Ложь: выполнение только на клиенте, как будто нет сервера.
//    * ПараметрыДополнительныхДействий - Произвольный - (необязательный) - если указан, то передается
//                           на сервер в процедуру ПередНачаломОперации общего модуля
//                           ЭлектроннаяПодписьПереопределяемый как ВходныеПараметры.
//    * КонтекстОперации     - Неопределено - (необязательный) - если указан, тогда в свойство будет
//                           установлено определенное значение произвольного типа, которое позволяет
//                           выполнить действие с тем же сертификатом повторно (у пользователя 
//                           не запрашивается ни пароль, ни подтверждение действия).
//                           В случае ошибки после вызова процедуры, повторное действие, выполненное
//                           с полученным контекстом, может быть также ошибочно.
//                           - Произвольный - (необязательный) - если определено, то действие будет выполнено
//                           с тем же сертификатом без запроса пароля или подтверждения.
//                           Параметр БезПодтверждения считается равным Истина.
//                           Параметры Операция, ЗаголовокДанных, ПоказатьКомментарий, ОтборСертификатов
//                           и ВыполнятьНаСервере игнорируются, их значения остаются такими же, как при первом вызове.
//                           Параметр ПараметрыДополнительныхДействий игнорируется.
//                           Процедура ПередНачаломОперации не вызывается.
//                           Если передать контекст, возвращенный процедурой Расшифровать, тогда
//                           пароль, введенный для сертификата, может быть использован, как если бы
//                           пароль был сохранен на время сеанса. В остальном контекст игнорируется.
//    * ПрекратитьВыполнение - Произвольный - если свойство существует и в процессе асинхронного выполнения возникает
//                           ошибка, выполнение прекращается без отображения формы операции или с закрытием этой формы,
//                           если она была открыта.
//
//    Вариант 1.
//    * Данные              - ДвоичныеДанные - данные для подписания.
//                          - Строка - адрес временного хранилища, содержащего двоичные данные.
//                          Следующие варианты недоступны для отправки в сервис мобильной подписи:
//                          - ОписаниеОповещения - обработчик получения данных, который возвращает
//                          их в свойстве Данные (смотри выше общий подход). В момент вызова
//                          в ОписаниеДанных уже вставлен параметр ВыбранныйСертификат (смотри ниже).
//                          - Структура:
//                             * КонвертXML       - см. ЭлектроннаяПодписьКлиент.КонвертXML
//                             * ПараметрыXMLDSig - см. ЭлектроннаяПодписьКлиент.ПараметрыXMLDSig
//                          - Структура:
//                             * ПараметрыCMS - см. ЭлектроннаяПодписьКлиент.ПараметрыCMS
//                             * Данные  - Строка - произвольная строка для подписания,
//                                       - ДвоичныеДанные - двоичные данные для подписания.
//    * Объект              - ЛюбаяСсылка - (необязательный) - ссылка на объект , к которому нужно добавить подпись.
//                          Если не указан, то подпись не требуется добавлять.
//                          - ОписаниеОповещения - (необязательный) - обработчик добавления подписи в
//                          регистр сведений ЭлектронныеПодписи. Следует учесть общий подход (смотри выше).
//                          В момент вызова в ОписаниеДанных уже вставлен параметр СвойстваПодписи.
//                          В случае параметра НаборДанных, в ОписаниеДанных вставляется
//                          свойство ТекущийЭлементНабораДанных, содержащее параметр СвойстваПодписи.
//    * ВерсияОбъекта       - Строка - (необязательный) - версия данных объекта для проверки и
//                          блокировки объекта перед добавлением подписи.
//    * Представление       - ЛюбаяСсылка - (необязательный), если параметр не указан,
//                                  тогда представление вычисляется по значению свойства Объект.
//                          - Строка
//                          - Структура:
//                             ** Значение      - ЛюбаяСсылка
//                                              - ОписаниеОповещения - для открытия.
//                             ** Представление - Строка - представление значения.
//                             ** ИмяФайла      - Строка - (необязательный) имя файла, отображается в мобильном приложении,
//                              заполнять, если оно отличается от представления.
//                              Если ПараметрыПодписи.РазрешитьОтправкуНаПодписание = Истина, представление или имя файла 
//                              обязательно и должно содержать имя файла длиной не более 250 символов. Символы,
//                              не соответствующие регулярному выражению [^\.\(\)\w,\s-] будут заменены на "_", символ №
//                              на N.
//    Вариант 2.
//    * НаборДанных         - Массив - структуры со свойствами, описанными в Варианте 1.
//    * ПредставлениеНабора - Строка - представления нескольких элементов набора данных, например, "Файлы (%1)".
//                          В это представление в параметр %1 заполняется количество элементов.
//                          По гиперссылке можно открыть список.
//                          Если в наборе данных 1 элемент, тогда используется значение
//                          в свойстве Представление свойства НаборДанных, если не указано, тогда
//                          представление вычисляется по значению свойства Объект элемента набора данных.
//    * СписокПредставлений - СписокЗначений
//                          - Массив - (необязательный) - произвольный список элементов
//                          или массив со значениями, как у свойства Представление, которые
//                          сможет открыть пользователь. Если не указан, то заполняется из
//                          свойств Представление или Объект в свойстве НаборДанных.
//                          Если ПараметрыПодписи.РазрешитьОтправкуНаПодписание = Истина, представление значения или имя
//                          файла обязательно и должно содержать имя файла длиной не более 250 символов и без дублей
//                          в наборе данных. Символы, не соответствующие регулярному выражению [^\.\(\)\w,\s-] будут
//                          заменены на "_", символ № на N.
//
//  Форма - ФормаКлиентскогоПриложения - форма, из которой нужно получить уникальный идентификатор,
//                                который будет использоваться при блокировке объекта.
//        - УникальныйИдентификатор - уникальный идентификатор, который будет
//                                использоваться при блокировке объекта.
//        - Неопределено     - использовать стандартную форму.
//
//  ОбработкаРезультата - ОписаниеОповещения -
//     Требуется для нестандартной обработки результата, например, если не указан параметр Объект и/или Форма.
//     В результат передается входной параметр ОписаниеДанных, который в успешном случае дополняется свойствами:
//     # Успех - Булево - Истина, если все прошло успешно. Если Успех = Ложь, то частичное завершение
//               определяется по наличию свойства СвойстваПодписи. Если есть, то шаг выполнен.
//     # Отказ - Булево - Истина, если пользователь отменил операцию интерактивно.
//     # ПользовательНажалКнопкуПодписать - Булево - если Истина, то пользователь хотя бы раз
//               нажал на кнопку Подписать. Используется для сценариев, когда для продолжения бизнес-процесса
//               достаточно простой подписи (намерения установить подпись), а установка квалифицированной подписи
//               является дополнением, которое можно реализовать позднее, если возникли технические проблемы.
//     # ПодписаниеВЭтомПриложении - Булево - Истина, если выбран сертификат, Ложь, если выбрана отправка на подписание
//               в сервис мобильной подписи.
//     # ВыбранныйСертификат - Структура - содержит свойства сертификата:
//         ## Ссылка    - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - ссылка на сертификат.
//         ## Отпечаток - Строка - отпечаток сертификата в формате строки Base64.
//         ## Данные    - Строка - адрес временного хранилища, содержащего двоичные данные сертификата.
//     # ВыбранныйПодписант - см. ПараметрыПодписанта - если выбрана отправка на подписание в сервис мобильной подписи.
//     # ВыбраннаяДоверенность - ЛюбаяСсылка
//     # СвойстваПодписи = Строка - адрес временного хранилища, содержащего описанную ниже структуру,
//               если подписание произошло на сервере.
//                       = Структура:
//                        * Подпись - ДвоичныеДанные
//                        * ТипПодписи  - ПеречислениеСсылка.ТипыПодписиКриптографии
//                        * СрокДействияПоследнейМеткиВремени - Дата, Неопределено - заполняется только с помощью
//                                                                                   менеджера криптографии.
//                        * ДатаПодписиИзМетки - Дата, Неопределено - самый ранний штамп времени
//                        * НеподтвержденнаяДатаПодписи - Дата - неподтвержденная дата подписи.
//                        * Сертификат  - ДвоичныеДанные - сертификат для проверки подписи.
//                        * Отпечаток           - Строка - отпечаток сертификата в формате строки Base64.
//                        * КомуВыданСертификат - Строка - представление субъекта, полученное из двоичных данных сертификата. 
//         При передаче параметра НаборДанных, свойство нужно проверять в нем.
//                       
//  ПараметрыПодписи - см. НовыйТипПодписи
//
Процедура Подписать(ОписаниеДанных, Форма = Неопределено, ОбработкаРезультата = Неопределено, ПараметрыПодписи = Неопределено) Экспорт
	
	КлиентскиеПараметры = Новый Структура;
	КлиентскиеПараметры.Вставить("ОписаниеДанных", ОписаниеДанных);
	КлиентскиеПараметры.Вставить("Форма", Форма);
	КлиентскиеПараметры.Вставить("ОбработкаРезультата", ОбработкаРезультата);
	
	ОбработкаЗавершения = Новый ОписаниеОповещения("СтандартноеЗавершение",
		ЭлектроннаяПодписьСлужебныйКлиент, КлиентскиеПараметры);
	
	Если ОписаниеДанных.Свойство("КонтекстОперации")
	   И ТипЗнч(ОписаниеДанных.КонтекстОперации) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		ЭлектроннаяПодписьСлужебныйКлиент.ПродлитьХранениеКонтекстаОперации(ОписаниеДанных);
		НачалоИмениФормы = "Обработка.ПодписаниеДанныхВМобильномЭДО.Форма.";
		
		Если ОписаниеДанных.КонтекстОперации.ИмяФормы = НачалоИмениФормы + "ПодписаниеДанных" Тогда
			ОписаниеДанных.КонтекстОперации.ВыполнитьПодписание(КлиентскиеПараметры, ОбработкаЗавершения);
			Возврат;
		КонецЕсли;
		Если ОписаниеДанных.КонтекстОперации.ИмяФормы = НачалоИмениФормы + "РасшифровкаДанных" Тогда
			КлиентскиеПараметры.Вставить("УказанКонтекстДругойОперации");
		КонецЕсли;
	КонецЕсли;
	
	СерверныеПараметры = Новый Структура;
	СерверныеПараметры.Вставить("Операция",            НСтр("ru = 'Подписание данных';
															|en = 'Data signing'"));
	СерверныеПараметры.Вставить("ЗаголовокДанных",     НСтр("ru = 'Данные';
															|en = 'Data'"));
	СерверныеПараметры.Вставить("ПоказатьКомментарий", Ложь);
	СерверныеПараметры.Вставить("ОтборСертификатов");
	СерверныеПараметры.Вставить("Подписант");
	СерверныеПараметры.Вставить("ВыполнятьНаСервере");
	СерверныеПараметры.Вставить("ПараметрыДополнительныхДействий");
	СерверныеПараметры.Вставить("ОповеститьОбОкончанииСрокаДействия", Истина);
	ЗаполнитьЗначенияСвойств(СерверныеПараметры, ОписаниеДанных);
	
	СерверныеПараметры.Вставить("ТипПодписи", ПараметрыПодписи);
	
	МобильныйЭДОСлужебныйКлиент.ОткрытьНовуюФорму("ПодписаниеДанных",
		КлиентскиеПараметры, СерверныеПараметры, ОбработкаЗавершения);
	
КонецПроцедуры

#КонецОбласти

#Область Расшифровка

// Расшифровывает данные, возвращает их и помещает в объект, если указано.
// 
// Общий подход к обработке значений свойств с типом ОписаниеОповещения в параметре ОписаниеДанных.
//  При выполнении обработки оповещения в нее передается структура параметров, в которой всегда есть
//  свойство "Оповещение" типа ОписаниеОповещения, обработку которого нужно выполнить для продолжения.
//  Кроме того, в структуре всегда есть свойство ОписаниеДанных, полученное при вызове процедуры.
//  При вызове оповещения в качестве значения должна передаваться структура. Если в процессе асинхронного
//  выполнения возникает ошибка, тогда в эту структуру нужно вставить свойство ОписаниеОшибки типа Строка.
// 
// Параметры:
//  ОписаниеДанных - Структура:
//    * Операция             - Строка - заголовок формы расшифровки данных, например, Расшифровка файла.
//    * ЗаголовокДанных      - Строка - заголовок элемента или набора данных, например Файл.
//    * СообщитьОЗавершении  - Булево - (необязательный) - если Ложь, то не будет показано оповещение о успешном
//                           завершении операции для представления данных, указанного рядом с заголовком.
//    * ОтборСертификатов    - Массив - (необязательный) - содержит ссылки на элементы справочника.
//                           СертификатыЭлектроннойПодписиИШифрования, которые могут быть выбраны
//                           пользователем. Отбор блокирует возможность выбора других сертификатов
//                           из личного хранилища.
//    * БезПодтверждения     - Булево - (необязательный) - пропустить подтверждение пользователя, если
//                           в свойстве ОтборСертификатов только один сертификат и:
//                           а) либо для сертификата указано "Вводить пароль в программе электронной подписи";
//                           б) либо пользователь запомнил пароль к сертификату на время сеанса;
//                           в) либо пароль установлен ранее методом УстановитьПарольСертификата.
//                           Если в процессе расшифровки возникла ошибка, тогда будет открыта форма
//                           с возможностью указать пароль.
//    * ЭтоАутентификация    - Булево - (необязательный) - если Истина, то вместо кнопки Расшифровать
//                           будет показана кнопка ОК. А также скорректированы некоторые надписи.
//                           Кроме того, параметр СообщитьОЗавершении устанавливается в Ложь.
//    * ПередВыполнением     - ОписаниеОповещения - (необязательный) - описание обработчика дополнительной
//                           подготовки данных после выбора сертификата, которым будут расшифрованы данные.
//                           В этом обработчике можно заполнить параметр Данные, если необходимо.
//                           В момент вызова в ОписаниеДанных уже вставлен выбранный сертификат,
//                           как ВыбранныйСертификат (смотри ниже). Следует учесть общий подход (смотри выше).
//    * ВыполнятьНаСервере   - Неопределено
//                           - Булево - (необязательный) - когда не указан или Неопределено,
//                           тогда выполнение будет определено автоматически: если есть сервер, то сначала
//                           на сервере, потом (при неудаче) на клиенте, потом сообщение о двух ошибках.
//                           Когда Истина: если разрешено выполнение на сервере, тогда выполнение
//                           только на сервере, при неудаче одно сообщение об ошибке на сервере.
//                           Когда Ложь: выполнение только на клиенте, как будто нет сервера.
//    * ПараметрыДополнительныхДействий - Произвольный - (необязательный) - если указан, то передается
//                           на сервер в процедуру ПередНачаломОперации общего модуля.
//                           ЭлектроннаяПодписьПереопределяемый, как ВходныеПараметры.
//    * КонтекстОперации     - Неопределено - (необязательный) - если указан, тогда в свойство будет
//                           установлено определенное значение произвольного типа, которое позволяет
//                           выполнить действие с тем же сертификатом повторно (у пользователя 
//                           не запрашивается ни пароль, ни подтверждение действия).
//                           В случае ошибки после вызова процедуры, повторное действие, выполненное
//                           с полученным контекстом, может быть также ошибочно.
//                           - Произвольный - (необязательный) - если определено, то действие будет выполнено
//                           с тем же сертификатом без запроса пароля или подтверждения.
//                           Параметр БезПодтверждения считается равным Истина.
//                           Параметры Операция, ЗаголовокДанных, ОтборСертификатов, ЭтоАутентификация
//                           и ВыполнятьНаСервере игнорируются, их значения остаются такими же, как при первом вызове.
//                           Параметр ПараметрыДополнительныхДействий игнорируется.
//                           Процедура ПередНачаломОперации не вызывается.
//                           Если передать контекст, возвращенный процедурой Подписать, тогда
//                           пароль, введенный для сертификата, может быть использован, как если бы
//                           пароль был сохранен на время сеанса. В остальном контекст игнорируется.
//    * ПрекратитьВыполнение - Произвольный - если свойство существует и в процессе асинхронного выполнения возникает
//                           ошибка, выполнение прекращается без отображения формы операции или с закрытием этой формы,
//                           если она была открыта.
// 
//    Вариант 1.
//    * Данные                - ДвоичныеДанные - данные для расшифровки.
//                            - Строка - адрес временного хранилища, содержащего двоичные данные.
//                            - ОписаниеОповещения - обработчик получения данных, который возвращает
//                            их в свойстве Данные (смотри выше общий подход). В момент вызова
//                            в ОписаниеДанных уже вставлен параметр ВыбранныйСертификат (смотри ниже).
//    * РазмещениеРезультата  - Неопределено - (необязательный) - описывает, куда поместить расшифрованные данные.
//                            Если не указан или Неопределено, тогда через параметр ОбработкаРезультата.
//                            - ОписаниеОповещения - обработчик сохранения расшифрованных данных.
//                            Следует учесть общий подход (смотри выше).
//                            В момент вызова в ОписаниеДанных уже вставлен параметр РасшифрованныеДанные.
//                            В случае параметра НаборДанных, в ОписаниеДанных вставляется
//                            свойство ТекущийЭлементНабораДанных, содержащее параметр РасшифрованныеДанные.
//    * Объект                - ЛюбаяСсылка - (необязательный) - ссылка на объект, который необходимо расшифровать,
//                            а также очистить записи из регистра сведений СертификатыШифрования
//                            после успешного завершения расшифровки.
//                            Если не указан, то сертификаты не требуется получать из объекта и очищать.
//                            - Строка - адрес временного хранилища, содержащий
//                              ## Массив из Структура - массив сертификатов шифрования
//                                 ### Отпечаток     - Строка - отпечаток сертификата в формате строки Base64.
//                                 ### Представление - Строка - сохраненное представление субъекта,
//                                                     полученное из двоичных данных сертификата.
//                                 ### Сертификат    - ДвоичныеДанные - содержит выгрузку сертификата,
//                                                     который использовался для шифрования.
//    * Представление       - ЛюбаяСсылка - (необязательный), если параметр не указан,
//                                  тогда представление вычисляется по значению свойства Объект.
//                          - Строка
//                          - Структура:
//                             ** Значение      - ЛюбаяСсылка
//                                              - ОписаниеОповещения - для открытия.
//                             ** Представление - Строка - представление значения.
// 
//    Вариант 2.
//    * НаборДанных           - Массив - структуры со свойствами, описанными в Варианте 1.
//    * ПредставлениеНабора   - Строка - представления нескольких элементов набора данных, например, "Файлы (%1)".
//                            В это представление в параметр %1 заполняется количество элементов.
//                            По гиперссылке можно открыть список.
//                            Если в наборе данных 1 элемент, тогда используется значение
//                            в свойстве Представление свойства НаборДанных, если не указано, тогда
//                            представление вычисляется по значению свойства Объект элемента набора данных.
//    * СписокПредставлений   - СписокЗначений
//                            - Массив - (необязательный) - произвольный список элементов
//                            или массив со значениями, как у свойства Представление, которые
//                            сможет открыть пользователь. Если не указан, то заполняется из
//                            свойств Представление или Объект в свойстве НаборДанных.
//    * СертификатыШифрования - Массив - (необязательный) значения, как у параметра Объект. Используется
//                            для извлечения списков сертификатов шифрования для элементов, указанных
//                            в параметре СписокПредставлений (порядок должен соответствовать).
//                            Когда указан, параметр Объект не используется.
//
//  Форма - ФормаКлиентскогоПриложения - форма, из которой нужно получить уникальный идентификатор, который будет
//        использоваться при помещении расшифрованных данных во временное хранилище.
//        - УникальныйИдентификатор - уникальный идентификатор, который будет использоваться
//        при помещении расшифрованных данных во временное хранилище.
//        - Неопределено - использовать стандартную форму.
//
//  ОбработкаРезультата - ОписаниеОповещения -
//     Требуется для нестандартной обработки результата, если не указан параметр Форма и/или РазмещениеРезультата.
//     В результат передается входной параметр ОписаниеДанных, который в успешном случае, дополняется свойствами:
//     # Успех - Булево - Истина, если все прошло успешно. Если Успех = Ложь, то частичное завершение
//               определяется по наличию свойства РасшифрованныеДанные. Если есть, то шаг выполнен.
//     # Отказ - Булево - Истина, если пользователь отменил операцию интерактивно.
//     # ВыбранныйСертификат - Структура - содержит свойства сертификата:
//         ## Ссылка    - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - ссылка на сертификат.
//         ## Отпечаток - Строка - отпечаток сертификата в формате строки Base64.
//         ## Данные    - Строка - адрес временного хранилища, содержащего двоичные данные сертификата.
//     # РасшифрованныеДанные = ДвоичныеДанные - результат расшифровки.
//                              При передаче параметра НаборДанных, свойство нужно проверять в нем.
//                            = Строка - адрес временного хранилища, содержащего результат расшифровки.
//
Процедура Расшифровать(ОписаниеДанных, Форма = Неопределено, ОбработкаРезультата = Неопределено) Экспорт
	
	КлиентскиеПараметры = Новый Структура;
	КлиентскиеПараметры.Вставить("ОписаниеДанных", ОписаниеДанных);
	КлиентскиеПараметры.Вставить("Форма", Форма);
	КлиентскиеПараметры.Вставить("ОбработкаРезультата", ОбработкаРезультата);
	
	ОбработкаЗавершения = Новый ОписаниеОповещения("СтандартноеЗавершение",
		ЭлектроннаяПодписьСлужебныйКлиент, КлиентскиеПараметры);
	
	Если ОписаниеДанных.Свойство("КонтекстОперации")
	   И ТипЗнч(ОписаниеДанных.КонтекстОперации) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		ЭлектроннаяПодписьСлужебныйКлиент.ПродлитьХранениеКонтекстаОперации(ОписаниеДанных);
		НачалоИмениФормы = "Обработка.ПодписаниеДанныхВМобильномЭДО.Форма.";
		
		Если ОписаниеДанных.КонтекстОперации.ИмяФормы = НачалоИмениФормы + "РасшифровкаДанных" Тогда
			ОписаниеДанных.КонтекстОперации.ВыполнитьРасшифровку(КлиентскиеПараметры, ОбработкаЗавершения);
			Возврат;
		КонецЕсли;
		Если ОписаниеДанных.КонтекстОперации.ИмяФормы = НачалоИмениФормы + "ПодписаниеДанных" Тогда
			КлиентскиеПараметры.Вставить("УказанКонтекстДругойОперации");
		КонецЕсли;
	КонецЕсли;
	
	СерверныеПараметры = Новый Структура;
	СерверныеПараметры.Вставить("Операция",            НСтр("ru = 'Расшифровка данных';
															|en = 'Data decryption'"));
	СерверныеПараметры.Вставить("ЗаголовокДанных",     НСтр("ru = 'Данные';
															|en = 'Data'"));
	СерверныеПараметры.Вставить("ОтборСертификатов");
	СерверныеПараметры.Вставить("СертификатыШифрования");
	СерверныеПараметры.Вставить("ЭтоАутентификация");
	СерверныеПараметры.Вставить("ВыполнятьНаСервере");
	СерверныеПараметры.Вставить("ПараметрыДополнительныхДействий");
	СерверныеПараметры.Вставить("РазрешитьЗапоминатьПароль");
	ЗаполнитьЗначенияСвойств(СерверныеПараметры, ОписаниеДанных);
	
	Если ОписаниеДанных.Свойство("Данные") Тогда
		Если ТипЗнч(СерверныеПараметры.СертификатыШифрования) <> Тип("Массив")
		   И ОписаниеДанных.Свойство("Объект") Тогда
			
			СерверныеПараметры.Вставить("СертификатыШифрования", ОписаниеДанных.Объект);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(СерверныеПараметры.СертификатыШифрования) <> Тип("Массив") Тогда
		
		СерверныеПараметры.Вставить("СертификатыШифрования", Новый Массив);
		Для каждого ЭлементДанных Из ОписаниеДанных.НаборДанных Цикл
			Если ЭлементДанных.Свойство("Объект") Тогда
				СерверныеПараметры.СертификатыШифрования.Добавить(ЭлементДанных.Объект);
			Иначе
				СерверныеПараметры.СертификатыШифрования.Добавить(Неопределено);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	МобильныйЭДОСлужебныйКлиент.ОткрытьНовуюФорму("РасшифровкаДанных",
		КлиентскиеПараметры,
		СерверныеПараметры,
		ОбработкаЗавершения);
	
КонецПроцедуры

#КонецОбласти

#Область ТокенРутокен

// Запускает в обработку операцию взаимодействия с Рутокеном
// Параметры:
//  ПараметрыОперации - Структура - Структура с ключами:
//   * Форма- ФормаКлиентскогоПриложения, Неопределено - Форма-Источник
//   * ОписаниеДанных - Структура, Неопределено - Структура с описанием данных
//   * ПараметрыКомпоненты - Структура - Параметры компоненты:
//    ** ОбъектКомпоненты - ОбъектВнешнейКомпоненты, Неопределено - Объект компоненты
//    ** СвойстваСертификатов - Структура, Неопределено - Свойства сертификатов
//    ** СвойстваВыбранногоСертификата - Структура, Неопределено - Свойства выбранного сертификата
//    ** СоответствиеОтпечатков - Соответствие из КлючИЗначение:
//     *** Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//     *** Значение - Структура:
//      **** Ссылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//      **** Отозван - Булево
//      **** Отпечаток - Строка
//      **** ДействителенДо - Дата - дата окончания действия сертификата
//      **** КемВыдан - Строка - удостоверяющий центр, который выдал сертификат
//      **** Наименование - Строка - пользовательское представление сертификата
//      **** Программа - Строка - Программа, требуемая для подписания и расшифровки документов
//      **** ДанныеСертификата - ДвоичныеДанные - данные файла сертификата
//      **** ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - в ключ помещается 
//           ссылка на переданный сертификат.
//      **** ПарольПолучен - Булево - содержит Ложь
//      **** ПарольПользователя - Неопределено
//      **** Комментарий - Строка - содержит пустую строку
//      **** СрокДействияВДнях - Число - количество дней, оставшееся до истечения срока действия сертификата
//      **** Организация - ОпределяемыйТип.Организация
//      **** Фамилия - Строка - фамилия владельца сертификата
//      **** Имя - Строка - имя владельца сертификата
//      **** Отчество - Строка - отчество владельца сертификата
//      **** Должность - Строка - должность владельца сертификата
//	    **** Пользователь - ОпределяемыйТип.Пользователь
//      **** КомуВыдан - Строка
//      **** Фирма - Строка
//    ** СодержимоеКонтейнераПрочитано - Булево
//    ** ПодписаниеЧерезФайловоеХранилище - Булево
//    ** ОписаниеОповещенияПослеОперации - ОписаниеОповещения, Неопределено - Оповещение
//    ** ИдентификаторТекущейОперации - Строка
//    ** ТекущаяОперация - ПеречислениеСсылка.ОперацииРутокенМобильногоЭДО, Неопределено - Текущая операция
//   * Операция - ПеречислениеСсылка.ОперацииРутокенМобильногоЭДО
//   * ОбработкаОшибки - ОписаниеОповещения
// 
Асинх Процедура НачатьОбработкуОперацииРутокен(ПараметрыОперации) Экспорт

#Если МобильныйКлиент Тогда
	УстановитьЗапретЗасыпанияКомпьютера(Истина);
#КонецЕсли

	Операция = ПараметрыОперации.Операция;
	
	КомпонентаПодключена = Ждать ПодключитьКомпонентуРутокенАсинх(ПараметрыОперации);
	
	Ошибка = МобильныйЭДОСлужебныйКлиент.НовыйОшибкаРутокен();
	Ошибка.Форма = ПараметрыОперации.Форма;
	Если КомпонентаПодключена <> Истина Тогда
		
		ТекстОшибки = НСтр("ru = 'Не удалось подключить внешнюю компоненту ""Рутокен""';
							|en = 'Cannot attach the ""Rutoken"" add-in'");
		Ошибка.Описание = ТекстОшибки;
		ВыполнитьОбработкуОповещения(ПараметрыОперации.ОбработкаОшибки, Ошибка);
		Возврат;

	КонецЕсли;
	
	ИдентификаторОперации = "";
	ОтветКомпоненты = "";

	Форма = ПараметрыОперации.Форма;
	ПараметрыКомпоненты = ПараметрыОперации.ПараметрыКомпоненты;
	ПараметрыКомпоненты.ТекущаяОперация = Операция;

	Элементы = Форма.Элементы;
	ОбъектКомпоненты = ПараметрыКомпоненты.ОбъектКомпоненты;
	
	Если Операция = ПредопределенноеЗначение(
		"Перечисление.ОперацииРутокенМобильногоЭДО.ЧтениеСертификатов") Тогда

		//@skip-check dynamic-access-method-not-found
		//@skip-check statement-type-change
		ОтветКомпоненты = ОбъектКомпоненты.ПолучитьСертификаты();
		//@skip-check invocation-parameter-type-intersect
		ИдентификаторОперации = ИдентификаторТекущейОперации(ОтветКомпоненты, ПараметрыОперации);
		
	КонецЕсли;

	Если Операция = ПредопределенноеЗначение(
		"Перечисление.ОперацииРутокенМобильногоЭДО.Подписание") Тогда
		
		Элементы.ДекорацияТекстовоеСостояниеОперации.Заголовок = "";
		//@skip-check property-return-type
		Элементы.СтраницыПодписанияРутокен.ТекущаяСтраница = Элементы.СтраницаОбработкаОперацииРутокен;
	
		ЗаголовокОперации = НСтр("ru = 'ПОДПИСЫВАЕТСЯ:';
								|en = 'SIGNED:'");
		
		ОписаниеДанных = ПараметрыОперации.ОписаниеДанных;

		НаборДанных = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеДанных, "НаборДанных");
		
		ОтпечатокВКонтейнере = ПараметрыКомпоненты.СоответствиеОтпечатков.Получить(Форма.СертификатОтпечаток);
		
		ИндексПодписываемыхДанных = 0;
		
		КоличествоДанныхВНаборе = НаборДанных.Количество();

		Для Каждого Данные Из НаборДанных Цикл
			
			Если Данные.Свойство("СвойстваПодписи") Тогда
				ИндексПодписываемыхДанных = ИндексПодписываемыхДанных + 1;
				Продолжить;
			КонецЕсли;
			
			ДанныеДляПодписания = Данные.Данные;
			Если ТипЗнч(ДанныеДляПодписания) = Тип("Строка") Тогда
				ДанныеДляПодписания = ПолучитьИзВременногоХранилища(ДанныеДляПодписания); 
			КонецЕсли;

			//@skip-check dynamic-access-method-not-found
			ОтветКомпоненты = ОбъектКомпоненты.Подписать(Форма.Пароль,
				ОтпечатокВКонтейнере,
				?(ПараметрыКомпоненты.ПодписаниеЧерезФайловоеХранилище,
				ИмяВременногоФайлаСДанными(ДанныеДляПодписания), ДанныеДляПодписания));
			
			ИдентификаторОперации = ИдентификаторТекущейОперации(ОтветКомпоненты, ПараметрыОперации);
			
			Данные.Вставить("ИдентификаторОперацииПодписания", ИдентификаторОперации);
			
			//@skip-check property-return-type
			ПредставлениеДанных = Данные.Представление.Представление;
					
			Если Форма.ПакетнаяОтправка Тогда
				ПредставлениеДанных = СтрШаблон("(%1/%2) %3",
					ИндексПодписываемыхДанных + 1,
					КоличествоДанныхВНаборе,
					ПредставлениеДанных);
				КонецЕсли;
				
			Элементы.ДекорацияТекстовоеСостояниеОперации.Заголовок = СтрШаблон("%1%2%3",
				ЗаголовокОперации,
				Символы.ПС,
				ПредставлениеДанных);
			
			Прервать;

		КонецЦикла;

	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторОперации) Тогда
		ПараметрыКомпоненты.ИдентификаторТекущейОперации = ИдентификаторОперации;
	Иначе
		
		ТекстОшибки = НСтр("ru = 'Не найден идентификатор текущей операции. Повторите операцию';
							|en = 'The current operation ID is not found. Try again'");
		Ошибка.Описание = ТекстОшибки;
		ВыполнитьОбработкуОповещения(ПараметрыОперации.ОбработкаОшибки, Ошибка);
		Возврат;
	КонецЕсли;

КонецПроцедуры

// Завершает начатую операцию
// Параметры:
// Результат - Строка - Ответ компоненты в формате JSON
// ПараметрыОперации - Структура - см.МобильныйЭДОСлужебныйКлиент.НовыйПараметрыОперацииРутокен()
//
Асинх Процедура ЗавершитьОбработкуОперацииРутокен(Результат, ПараметрыОперации) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

#Если Не ВебКлиент Тогда
	
	Ошибка = МобильныйЭДОСлужебныйКлиент.НовыйОшибкаРутокен();
	Ошибка.Форма= ПараметрыОперации.Форма;

	// Все ассинхронные операции при штатной ситуации возвращают JSON - соответствие с полями
	//  ID      - Строка - идентификатор операции, возвращается при запуске операции
	//  isError - Булево - наличие ошибка
	//  Content - Строка, Соответствие (Структура) - результат операции, при ошибке может отсутствовать
	//  Error   - Строка - описание ошибки
	//  ErrorCode - Строка - код ошибки
	//
	ЧтениеОбъекта = Новый ЧтениеJSON;
	ЧтениеОбъекта.УстановитьСтроку(Результат);
	ЧтениеДанных = ПрочитатьJSON(ЧтениеОбъекта, Истина);

	Если ЧтениеДанных["isError"] Тогда
		Ошибка.КодОшибки = ЧтениеДанных["ErrorCode"];
	Иначе
		//@skip-check variable-value-type
		ИдентификаторОперации = ЧтениеДанных["ID"];

		ПараметрыКомпоненты = ПараметрыОперации.ПараметрыКомпоненты;

		Если ПараметрыКомпоненты.ИдентификаторТекущейОперации = ИдентификаторОперации Тогда
			
			ДанныеОперации = ЧтениеДанных["Content"];

			Если ДанныеОперации = Неопределено Тогда
				Ошибка.Описание = НСтр("ru = 'Устройство не смогло выполнить операцию.
					|Необходимо повторить';
					|en = 'The device failed to perform the operation.
					|Try again'");
			
			Иначе
			
				Операция = ПараметрыОперации.Операция;
	
				Если Операция = ПредопределенноеЗначение(
						"Перечисление.ОперацииРутокенМобильногоЭДО.ЧтениеСертификатов") Тогда
	
					ОповещениеОбработатьРезультат = Новый ОписаниеОповещения("ОбработатьРезультатЧтенияСертификатов",
						МобильныйЭДОСлужебныйКлиент, ПараметрыОперации);
	
				ИначеЕсли Операция = ПредопределенноеЗначение(
						"Перечисление.ОперацииРутокенМобильногоЭДО.Подписание") Тогда
	
					ОповещениеОбработатьРезультат = Новый ОписаниеОповещения("ОбработатьРезультатПодписанияДанных",
						МобильныйЭДОСлужебныйКлиент, ПараметрыОперации);
				Иначе
					Ошибка.Описание = НСтр("ru = 'Данная операция не поддерживается устройством';
											|en = 'This operation is not supported by the device'");
				КонецЕсли;
	
				Если Не ЗначениеЗаполнено(Ошибка.Описание) Тогда
					ВыполнитьОбработкуОповещения(ОповещениеОбработатьРезультат, ДанныеОперации);
				КонецЕсли;
			
			КонецЕсли;
		
		КонецЕсли;

	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ошибка.КодОшибки) Или ЗначениеЗаполнено(Ошибка.Описание) Тогда
		ВыполнитьОбработкуОповещения(ПараметрыОперации.ОбработкаОшибки, Ошибка);
	КонецЕсли;

#КонецЕсли

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТокенРутокен

Асинх Функция ПодключитьКомпонентуРутокенАсинх(ПараметрыОперации)
	
	ПараметрыКомпоненты = ПараметрыОперации.ПараметрыКомпоненты;
	
	ОбъектКомпоненты = ПараметрыКомпоненты.ОбъектКомпоненты;

	Если ОбъектКомпоненты = Неопределено Тогда
		Ждать УстановитьВнешнююКомпонентуАсинх("ОбщийМакет.КомпонентаРутокенМобильногоЭДО");
	КонецЕсли;

	КомпонентаПодключена = Ждать ПодключитьВнешнююКомпонентуАсинх("ОбщийМакет.КомпонентаРутокенМобильногоЭДО",
		"BIT",
		ТипВнешнейКомпоненты.Native);

	Если КомпонентаПодключена Тогда
		ПараметрыКомпоненты.ОбъектКомпоненты = Новый ("AddIn.BIT.EDMSRutoken");
		Возврат Истина;
	Иначе
		ТекстКомпонентаНеПодключена = НСтр(
			"ru = 'Не удалось подключить внешнюю компоненту ""Рутокен""';
			|en = 'Cannot attach the ""Rutoken"" add-in'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстКомпонентаНеПодключена);
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Возвращает идентификатор начатой операции
// Параметры:
//  ОтветКомпоненты - Строка - Ответ компоненты в формате JSON
//  ПараметрыОперации - Структура - Структура с ключами:
//   * Форма- ФормаКлиентскогоПриложения, Неопределено - Форма-Источник
//   * ОписаниеДанных - Структура, Неопределено - Структура с описанием данных
//   * ПараметрыКомпоненты - Структура - Параметры компоненты:
//    ** ОбъектКомпоненты - ОбъектВнешнейКомпоненты, Неопределено - Объект компоненты
//    ** СвойстваСертификатов - Структура, Неопределено - Свойства сертификатов
//    ** СвойстваВыбранногоСертификата - Структура, Неопределено - Свойства выбранного сертификата
//    ** СоответствиеОтпечатков - Соответствие из КлючИЗначение:
//     *** Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//     *** Значение - Структура:
//      **** Ссылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//      **** Отозван - Булево
//      **** Отпечаток - Строка
//      **** ДействителенДо - Дата - дата окончания действия сертификата
//      **** КемВыдан - Строка - удостоверяющий центр, который выдал сертификат
//      **** Наименование - Строка - пользовательское представление сертификата
//      **** Программа - Строка - Программа, требуемая для подписания и расшифровки документов
//      **** ДанныеСертификата - ДвоичныеДанные - данные файла сертификата
//      **** ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - в ключ помещается 
//           ссылка на переданный сертификат.
//      **** ПарольПолучен - Булево - содержит Ложь
//      **** ПарольПользователя - Неопределено
//      **** Комментарий - Строка - содержит пустую строку
//      **** СрокДействияВДнях - Число - количество дней, оставшееся до истечения срока действия сертификата
//      **** Организация - ОпределяемыйТип.Организация
//      **** Фамилия - Строка - фамилия владельца сертификата
//      **** Имя - Строка - имя владельца сертификата
//      **** Отчество - Строка - отчество владельца сертификата
//      **** Должность - Строка - должность владельца сертификата
//	    **** Пользователь - ОпределяемыйТип.Пользователь
//      **** КомуВыдан - Строка
//      **** Фирма - Строка
//    ** СодержимоеКонтейнераПрочитано - Булево
//    ** ПодписаниеЧерезФайловоеХранилище - Булево
//    ** ОписаниеОповещенияПослеОперации - ОписаниеОповещения, Неопределено - Оповещение
//    ** ИдентификаторТекущейОперации - Строка
//    ** ТекущаяОперация - ПеречислениеСсылка.ОперацииРутокенМобильногоЭДО, Неопределено - Текущая операция
//   * Операция - ПеречислениеСсылка.ОперацииРутокенМобильногоЭДО
//   * ОбработкаОшибки - ОписаниеОповещения
// Возвращаемое значение:
//  Строка - идентификатор операции
// 
Функция ИдентификаторТекущейОперации(ОтветКомпоненты, ПараметрыОперации)
	
ИдентификаторОперации = "";

#Если Не ВебКлиент Тогда

	ТекстСообщения = "";
	КодОшибки = "";

	Попытка
		//@skip-check bsl-legacy-check-type-environments
		ЧтениеОбъекта = Новый ЧтениеJSON;
		//@skip-check bsl-legacy-check-dynamic-feature-access
		ЧтениеОбъекта.УстановитьСтроку(ОтветКомпоненты);
		//@skip-check undefined-function
		ЧтениеДанных = ПрочитатьJSON(ЧтениеОбъекта);
		
		//@skip-check property-return-type
		Если ЧтениеДанных.isError Тогда
			//@skip-check property-return-type
			КодОшибки = ЧтениеДанных.ErrorCode;
			ТекстСообщения = ЧтениеДанных.Error;
		Иначе
			ИдентификаторОперации = ЧтениеДанных.Content;
		КонецЕсли;

	Исключение
		ТекстСообщения = СтрШаблон(НСтр("ru = '%1 Данные:%2';
										|en = '%1 Data:%2'"), ОписаниеОшибки(), ОтветКомпоненты);
		ИдентификаторОперации = "";
	КонецПопытки;

	Если ЗначениеЗаполнено(КодОшибки) Или ЗначениеЗаполнено(ТекстСообщения) Тогда
		
		ТекстОшибки = НСтр("ru = 'Не удалось получить идентификатор текущей операции';
							|en = 'Cannot get the current operation ID'");
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
			ТекстОшибки = ТекстСообщения
		КонецЕсли;

		Ошибка = МобильныйЭДОСлужебныйКлиент.НовыйОшибкаРутокен();
		Ошибка.Форма = ПараметрыОперации.Форма;
		Ошибка.КодОшибки = КодОшибки;
		Ошибка.Описание = ТекстОшибки;
		
		ВыполнитьОбработкуОповещения(ПараметрыОперации.ОбработкаОшибки, Ошибка);
		Возврат "";
	КонецЕсли;

#КонецЕсли
	
	Возврат ИдентификаторОперации;

КонецФункции

//Конвертирует данные для отправки на подпись в необходимый формат
// Параметры:
//  ДанныеОперации - ДвоичныеДанные - Двоичные данные для отправки на подпись
// Возвращаемое значение:
//  Строка - Имя временного файла с данными для подписания
//
Функция ИмяВременногоФайлаСДанными(ДанныеОперации)

	ИмяВременногоФайла = "";
	
	Если ДанныеОперации <> Неопределено Тогда
		КаталогВременныхФайлов = МобильныйЭДОСлужебныйКлиент.КаталогВременныхФайловУстройства();
		ИмяВременногоФайла = СтрШаблон("%1%2", КаталогВременныхФайлов, "temp.tmp");
		ДанныеОперации.Записать(ИмяВременногоФайла);
	КонецЕсли;

	Возврат ИмяВременногоФайла;

КонецФункции

#КонецОбласти

#КонецОбласти
