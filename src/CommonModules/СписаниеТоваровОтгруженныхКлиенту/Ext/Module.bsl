//////////////////////////////////////////////////////////////////////////
// Процедуры и функции подсистемы "Продажи"
// связанные с хозяйственной операцией "Списание товаров, отгруженных клиенту".
//
//////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область УнифицированныеМетодыПоХозяйственнымОперациям

#Область Проведение

// Возвращает параметры заполнения видов запасов для документа "Списание отгруженных товаров".
//
// Параметры:
//	Объект - ДокументОбъект.СписаниеОтгруженныхТоваров - документ, для которого выполняется заполнение запасов.
//
// Возвращаемое значение:
//	см. ЗапасыСервер.ПараметрыЗаполненияВидовЗапасов.
//
Функция ПараметрыЗаполненияВидовЗапасов(Объект) Экспорт
	
	ПараметрыЗаполнения = ЗапасыСервер.ПараметрыЗаполненияВидовЗапасов();
	ПараметрыЗаполнения.ПодбиратьВТЧТоварыПринятыеНаОтветственноеХранение.Вставить(Перечисления.ТипыЗапасов.ТоварНаХраненииСПравомПродажи, "ПоНастройкамДоговора");
	ПараметрыЗаполнения.СторнируемыйДокумент = Объект.СторнируемыйДокумент;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

#КонецОбласти

#Область ХозяйственныеОперации

// Возвращает хозяйственную операцию договора.
//
// Возвращаемое значение:
//	ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция договора.
//
Функция ХозяйственнаяОперацияДоговора() Экспорт
	
	Возврат Перечисления.ХозяйственныеОперации.РеализацияКлиенту;
	
КонецФункции

// Возвращает коллекцию имен реквизитов объекта, доступных по операции.
//
// Возвращаемое значение:
//	Массив Из Строка - коллекция имен реквизитов объекта, доступных по операции.
//
Функция РеквизитыОперации() Экспорт
	
	РеквизитыОперации = Новый Массив;
	РеквизитыОперации.Добавить("Распоряжение");
	РеквизитыОперации.Добавить("Валюта");
	РеквизитыОперации.Добавить("СуммаДокумента");
	РеквизитыОперации.Добавить("УменьшитьОтгрузкуПоРаспоряжению");
	РеквизитыОперации.Добавить("Склад");
	РеквизитыОперации.Добавить("ВидЦены");
	РеквизитыОперации.Добавить("ИсточникИнформацииОЦенахДляПечати");
	
	РеквизитыОперации.Добавить("Товары.Цена");
	РеквизитыОперации.Добавить("Товары.Сумма");
	РеквизитыОперации.Добавить("Товары.Склад");
	
	Возврат РеквизитыОперации;
	
КонецФункции

#КонецОбласти

#Область ФункциональныеОпции

// Возвращает признак применяемости соглашений с клиентами.
//
// Возвращаемое значение:
//	Булево - признак применяемости соглашений с клиентами.
//
Функция СоглашенияСКлиентамиПрименимы() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Возвращает признак использования соглашений с клиентами.
//
// Возвращаемое значение:
//	Булево - признак использование соглашений с клиентами.
//
Функция ИспользоватьСоглашенияСКлиентами() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");

КонецФункции

#КонецОбласти

#Область Форма

// Получает договор по умолчанию.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - данные документа.
//
// Возвращаемое значение:
//	СправочникСсылка.ДоговорыКонтрагентов - значение договора документа по умолчанию.
//
Функция ПолучитьДоговорПоУмолчанию(Объект) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоходовПоНаправлениямДеятельности") Тогда
		Договор = ПродажиСервер.ПолучитьДоговорПоУмолчанию(
					Объект,
					ХозяйственнаяОперацияДоговора(),
					Неопределено,
					Объект.НаправлениеДеятельности);
	Иначе
		Договор = ПродажиСервер.ПолучитьДоговорПоУмолчанию(
					Объект,
					ХозяйственнаяОперацияДоговора());
	КонецЕсли;
	
	Возврат Договор;
	
КонецФункции

// Устанавливает доступность элемента договор.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма клиентского приложения.
//	Объект - ДанныеФормыСтруктура - данные документа.
//	Договор - СправочникСсылка.ДоговорыКонтрагентов, Неопределено - значение договора документа.
//
Процедура УстановитьДоступностьДоговора(Форма, Объект, Договор = Неопределено) Экспорт
	
	Элементы = Форма.Элементы;
	
	ПродажиСервер.УстановитьДоступностьДоговора(
		Объект, Элементы.Договор.Доступность, Элементы.Договор.Видимость, Договор);
	
КонецПроцедуры

// Настраивает форму.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - форма клиентского приложения.
//	Номер - Строка - номер документа.
//	Дата  - Дата - дата документа.
//
Процедура НастроитьФорму(Форма, Номер, Дата) Экспорт
	
	Элементы  = Форма.Элементы;
	Параметры = Форма.Параметры;
	
	Форма.АвтоЗаголовок = Ложь;
	Форма.Заголовок     = ЗаголовокФормыДокумента(Не ЗначениеЗаполнено(Параметры.Ключ), Номер, Дата);
	
	Элементы.Партнер.Заголовок = НСтр("ru = 'Клиент';
										|en = 'Client'");
	
	Элементы.Соглашение.Видимость = ИспользоватьСоглашенияСКлиентами();
	Элементы.ИсточникИнформацииОЦенахДляПечати.Видимость = Истина;
	Элементы.ВалютаДокумента.Видимость = Истина;
	Элементы.УменьшитьОтгрузкуПоРаспоряжению.Видимость = Форма.ОтгрузкаБезПереходаПраваСобственности25;
	Элементы.Склад.Видимость = Форма.ОтгрузкаБезПереходаПраваСобственности25;
	Элементы.Склад.АвтоОтметкаНезаполненного = Форма.ОтгрузкаБезПереходаПраваСобственности25;
	
	Элементы.ТоварыЗаполнитьЦеныПоДоговору.Видимость          = Истина;
	Элементы.СтраницыРаспоряжение.Видимость                   = Форма.ОтгрузкаБезПереходаПраваСобственности25;
	Элементы.ТоварыПодобратьТоварыПоЗаказам.Видимость         = Форма.ОтгрузкаБезПереходаПраваСобственности25;
	Элементы.ТоварыОтвязатьОтЗаказа.Видимость                 = Форма.ОтгрузкаБезПереходаПраваСобственности25;
	Элементы.ТоварыЗаполнитьСкладВВыделенныхСтроках.Видимость = Форма.ОтгрузкаБезПереходаПраваСобственности25;
	Элементы.ТоварыНазначение.Видимость                       = Не Форма.ОтгрузкаБезПереходаПраваСобственности25;
	
	ТекстЗаголовка = НСтр("ru = 'Подобрать отгруженные товары';
							|en = 'Pick shipped goods'");
	
	КомандаФормы = Форма.Команды.Найти("ПодобратьПереданныеТовары");
	КомандаФормы.Заголовок = ТекстЗаголовка;
	КомандаФормы.Подсказка = ТекстЗаголовка;
	
КонецПроцедуры

// Настраивает параметры выбора и связи параметров выбора реквизитов формы.
//
// Параметры:
//	Форма  - ФормаКлиентскогоПриложения - клиентского приложения.
//	Объект - ДанныеФормыСтруктура - данные документа.
//
Процедура НастроитьПараметрыВыбораЭлементов(Форма, Объект) Экспорт
	
	Элементы = Форма.Элементы;
	
	// Партнер
	МассивПараметровВыбора = Новый Массив;
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Клиент", Истина));
	
	Элементы.Партнер.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	
	// Контрагент
	МассивПараметровВыбора = Новый Массив;
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("ЗаголовокПоПартнеру", НСтр("ru = 'По клиенту';
																					|en = 'By customer'")));
	
	Элементы.Контрагент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	
	//Договор
	МассивПараметровВыбора = Новый Массив;
	МассивПараметровВыбора.Добавить(
		Новый ПараметрВыбора("Отбор.Статус", Перечисления.СтатусыДоговоровКонтрагентов.Действует));
	МассивПараметровВыбора.Добавить(
		Новый ПараметрВыбора("Отбор.ХозяйственнаяОперация", ХозяйственнаяОперацияДоговора()));
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления", Ложь));
	
	МассивСвязейПараметровВыбора = Новый Массив;
	МассивСвязейПараметровВыбора.Добавить(
		Новый СвязьПараметраВыбора("Отбор.Контрагент", "Объект.Контрагент", РежимИзмененияСвязанногоЗначения.Очищать));
	МассивСвязейПараметровВыбора.Добавить(
		Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация", РежимИзмененияСвязанногоЗначения.Очищать));	
	МассивСвязейПараметровВыбора.Добавить(
		Новый СвязьПараметраВыбора("Партнер", "Объект.Партнер", РежимИзмененияСвязанногоЗначения.Очищать));
	МассивСвязейПараметровВыбора.Добавить(
		Новый СвязьПараметраВыбора("Соглашение", "Объект.Соглашение", РежимИзмененияСвязанногоЗначения.НеИзменять));
	МассивСвязейПараметровВыбора.Добавить(
		Новый СвязьПараметраВыбора("ВалютаВзаиморасчетов", "Объект.Валюта", РежимИзмененияСвязанногоЗначения.Очищать));
	
	Элементы.Договор.ПараметрыВыбора       = Новый ФиксированныйМассив(МассивПараметровВыбора);
	Элементы.Договор.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивСвязейПараметровВыбора);
	
	//Соглашение
	МассивПараметровВыбора = Новый Массив;
	МассивПараметровВыбора.Добавить(
		Новый ПараметрВыбора("Отбор.КомиссионныеПродажи25", Ложь));
	
	МассивСвязейПараметровВыбора = Новый Массив;
	МассивСвязейПараметровВыбора.Добавить(
		Новый СвязьПараметраВыбора("Отбор.Дата", "Объект.Дата", РежимИзмененияСвязанногоЗначения.НеИзменять));
	МассивСвязейПараметровВыбора.Добавить(
		Новый СвязьПараметраВыбора("Отбор.Партнер", "Объект.Партнер", РежимИзмененияСвязанногоЗначения.Очищать));
	
	Элементы.Соглашение.ПараметрыВыбора       = Новый ФиксированныйМассив(МассивПараметровВыбора);
	Элементы.Соглашение.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивСвязейПараметровВыбора);
	
	// Направление деятельности
	МассивПараметровВыбора = Новый Массив;
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.УчетДоходов", Истина));
	
	Элементы.НаправлениеДеятельности.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	
	// Номенклатура
	МассивПараметровВыбора = Новый Массив;
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.ТипНоменклатуры", Перечисления.ТипыНоменклатуры.Товар));
	Элементы.ТоварыНоменклатура.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
	
КонецПроцедуры

// Возвращает заголовок формы документа передача товаров хранителю.
//
// Параметры:
//	НовыйДокумент - Булево - признак создания нового документа.
//	Номер - Строка - номер документа.
//	Дата - Дата - дата документа
//
// Возвращаемое значение:
//	Строка - заголовок формы документа.
//
Функция ЗаголовокФормыДокумента(НовыйДокумент, Номер, Дата) Экспорт
	
	Если НовыйДокумент Тогда
		Заголовок = СтрШаблон(НСтр("ru = '%1 (создание)';
									|en = '%1 (Create)'"), ПредставлениеОбъекта());
	Иначе
		Заголовок = СтрШаблон(НСтр("ru = '%1 %2 от %3';
									|en = '%1 %2 dated %3'"), ПредставлениеОбъекта(), Номер, Дата);
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции

// Возвращает представление документа.
//
// Возвращаемое значение:
//	Строка - представление документа.
//
Функция ПредставлениеОбъекта() Экспорт
	
	Возврат НСтр("ru = 'Списание товаров, отгруженных клиенту';
				|en = 'Write-off of goods shipped to customer'");
	
КонецФункции

#КонецОбласти

#Область ПечатныеФормы

// Возвращает синоним документа используемый в печатной форме "Акт о списании товаров у хранителя".
//
// Возвращаемое значение:
//	Строка - синоним документа в печатной форме.
//
Функция СинонимДокументаДляПечатнойФормыАктОСписанииТоваров() Экспорт
	
	Возврат НСтр("ru = 'Акт о списании товаров у клиента';
				|en = 'Stock shortage recognition at customer'");
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти
