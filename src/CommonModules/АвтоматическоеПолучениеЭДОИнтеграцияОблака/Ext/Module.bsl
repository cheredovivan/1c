// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

#Область Использование

// Параметры:
//  ИдентификаторыЭДО - Массив из Строка
//  РасшифрованныеМаркеры - См. УчетныеЗаписиЭДОИнтеграцияОблака.МаркерыАвторизацииИзРасшифрованныхМаркеров.РасшифрованныеМаркеры
// 
// Возвращаемое значение:
//  См. АвтоматическоеПолучениеЭДО.НовыйРезультатВключенияПоУчетнымЗаписямЭДО
Функция ВключитьПоУчетнымЗаписямЭДО(ИдентификаторыЭДО, РасшифрованныеМаркеры) Экспорт
	
	Результат = АвтоматическоеПолучениеЭДО.НовыйРезультатВключенияПоУчетнымЗаписямЭДО();

	МаркерыАвторизации = УчетныеЗаписиЭДОИнтеграцияОблака.МаркерыАвторизацииИзРасшифрованныхМаркеров(РасшифрованныеМаркеры);
	Если Не ЗначениеЗаполнено(МаркерыАвторизации) Тогда
		ДобавитьОшибкуВключенияПоУчетнымЗаписямЭДО(Результат.КонтекстДиагностики, НСтр(
			"ru = 'Не указан маркер для авторизации в сервисе ЭДО';
			|en = 'EDI service authorization marker is not specified'"));
		Возврат Результат;
	КонецЕсли;
	
	ИдентификаторыПоУчетнымЗаписям = РегистрыСведений.НастройкиОблачногоЭДО.СгруппироватьПоУчетнымЗаписям(
		ИдентификаторыЭДО);
	
	ТекущийПользовательДляЗапроса = Пользователи.ТекущийПользователь();
	Для Каждого КлючЗначение Из ИдентификаторыПоУчетнымЗаписям Цикл
		УчетнаяЗаписьОблачногоЭДО = КлючЗначение.Ключ;
		ИдентификаторыЭДОУчетнойЗаписи = КлючЗначение.Значение;
		
		ПараметрыВыполнения = ИнтеграцияОблачногоЭДОПовтИсп.ПараметрыВыполненияМетодаСервиса(
			УчетнаяЗаписьОблачногоЭДО, 
			ТекущийПользовательДляЗапроса);
		
		МаркерыАвторизацииУчетнойЗаписи = МаркерыАвторизацииУчетныхЗаписейЭДО(
			МаркерыАвторизации,
			ИдентификаторыЭДОУчетнойЗаписи);
		
		ОписаниеМетода = ИнтеграцияОблачногоЭДО.ОписаниеМетодаСервиса(
			"ВключитьИспользованиеКлючаДоступаКСервисуЭДО",
			ИдентификаторыЭДОУчетнойЗаписи,
			МаркерыАвторизацииУчетнойЗаписи);
		
		РезультатВключения = ИнтеграцияОблачногоЭДО.ВыполнитьМетодСервиса(ПараметрыВыполнения, 
			ОписаниеМетода, Результат.КонтекстДиагностики); // См. ПрограммныйИнтерфейсОблачногоЭДОВерсия2.ВключитьИспользованиеКлючаДоступаКСервисуЭДО
			
		Если РезультатВключения <> Неопределено Тогда
			
			СостоянияВключенияИдентификаторовЭДО = РезультатВключения.СостоянияВключенияИдентификаторовЭДО;
			Для Каждого СостояниеВключения Из СостоянияВключенияИдентификаторовЭДО Цикл
				СостояниеПолучения = РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.НовоеСостояниеПолучения();
				ЗаполнитьЗначенияСвойств(СостояниеПолучения, СостояниеВключения);
				Результат.СостояниеПоИдентификаторамЭДО.Вставить(СостояниеВключения.ИдентификаторЭДО, СостояниеПолучения);
			КонецЦикла;
			
			Для Каждого ОшибкаВключения Из РезультатВключения.Ошибки Цикл
				ДобавитьОшибкуВключенияПоУчетнымЗаписямЭДО(
					Результат.КонтекстДиагностики,
					ОшибкаВключения,
					УчетнаяЗаписьОблачногоЭДО);
			КонецЦикла;
		
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Получение

// Параметры:
//  ПакетМетодов - См. ИнтеграцияОблачногоЭДО.НовыйПакетМетодовСервиса
//  ПараметрыОперации - См. ЭлектронныеДокументыЭДОИнтеграцияОблака.НовыеПараметрыОперацииПоДействиямЭДО
Процедура ДобавитьОписаниеМетодаПолученияЧерезИнтервал(ПакетМетодов, ПараметрыОперации) Экспорт
	
	// См. ПрограммныйИнтерфейсОблачногоЭДОВерсия3.ПолучитьЭДОЧерезИнтервал
	ОписаниеМетода = ИнтеграцияОблачногоЭДО.ОписаниеМетодаСервиса("ПолучитьЭДОЧерезИнтервал",
		ПараметрыОперации.МаркерыАвторизации);
	ИдентификаторМетода = ИдентификаторМетодаПолученияЧерезИнтервал();
	ИнтеграцияОблачногоЭДО.ДобавитьОписаниеМетодаВПакет(ПакетМетодов, ОписаниеМетода, ИдентификаторМетода);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Использование

// Параметры:
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  ТекстОшибки - Строка
//  УчетнаяЗаписьОблачногоЭДО - Неопределено,СправочникСсылка.УчетныеЗаписиОблачногоЭДО
Процедура ДобавитьОшибкуВключенияПоУчетнымЗаписямЭДО(КонтекстДиагностики, ТекстОшибки,
	УчетнаяЗаписьОблачногоЭДО = Неопределено)
	
	ИнтеграцияОблачногоЭДО.ДобавитьНеизвестнуюОшибку(КонтекстДиагностики,
		ВидОперацииВключениеИспользованияКлючаДоступаКСервисуЭДО(), ТекстОшибки);
	
КонецПроцедуры

// Параметры:
//  МаркерыАвторизации - См. УчетныеЗаписиЭДОИнтеграцияОблака.МаркерыАвторизацииИзРасшифрованныхМаркеров
//  ИдентификаторыЭДОУчетнойЗаписи - Массив Из Строка
// 
// Возвращаемое значение:
//  См. УчетныеЗаписиЭДОИнтеграцияОблака.МаркерыАвторизацииИзРасшифрованныхМаркеров
Функция МаркерыАвторизацииУчетныхЗаписейЭДО(МаркерыАвторизации, ИдентификаторыЭДОУчетнойЗаписи)
	Результат = Новый Массив;// См. УчетныеЗаписиЭДОИнтеграцияОблака.МаркерыАвторизацииИзРасшифрованныхМаркеров

	Для Каждого МаркерАвторизации Из МаркерыАвторизации Цикл
		Если ИдентификаторыЭДОУчетнойЗаписи.Найти(МаркерАвторизации.ИдентификаторУчетнойЗаписиЭДО) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(МаркерАвторизации);
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Строка
Функция ВидОперацииВключениеИспользованияКлючаДоступаКСервисуЭДО()
	
	Возврат НСтр("ru = 'Включение использования ключа доступа к сервису ЭДО';
				|en = 'Enable the access key for the EDI service'");
	
КонецФункции

#КонецОбласти

#Область Получение

// Возвращаемое значение:
//  Строка
Функция ИдентификаторМетодаПолученияЧерезИнтервал()
	Возврат "ПолучитьЧерезИнтервал";
КонецФункции

#КонецОбласти

#КонецОбласти
