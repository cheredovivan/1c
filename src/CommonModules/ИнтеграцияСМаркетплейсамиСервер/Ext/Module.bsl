///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает имя события журнала регистрации при работе с торговой площадкой.
//
// Параметры:
//   Источник - Неопределено - для получения общего имени события;
//            - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки;
//            - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//            - Строка - источник события электронного взаимодействия.
//
// Возвращаемое значение:
//   Строка - Наименование события для записей в журнале регистрации.
//
Функция СобытиеЖурналаРегистрации(Знач Источник = Неопределено) Экспорт
	
	ИмяСобытия = НСтр("ru = 'Интеграция с маркетплейсами';
						|en = 'Integration with marketplaces'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	Если ТипЗнч(Источник) = Тип("СправочникСсылка.УчетныеЗаписиМаркетплейсов")
		Или ТипЗнч(Источник) = Тип("ПеречислениеСсылка.ВидыМаркетплейсов") Тогда
		
		Если ТипЗнч(Источник) = Тип("СправочникСсылка.УчетныеЗаписиМаркетплейсов") Тогда
			Источник = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник, "ВидМаркетплейса");
		КонецЕсли;
		
		Если Источник = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
			ИмяСобытия = ИнтеграцияСЯндексМаркетСервер.СобытиеЖурналаРегистрации();
		ИначеЕсли Источник = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
			ИмяСобытия = ИнтеграцияСМаркетплейсомOzonСервер.СобытиеЖурналаРегистрации();
		ИначеЕсли ИнтеграцияСМаркетплейсамиСИ.ЭтоМаркетплейсСИ(Источник) Тогда
			 ИмяСобытия = ИнтеграцияСМаркетплейсамиСИ.СобытиеЖурналаРегистрации(Источник);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("Строка") Тогда
		
		Если Источник = "СверкаДокументаСТорговойПлощадкой" Тогда
			ИмяСобытия = НСтр("ru = 'Сверка документа с торговой площадкой.';
								|en = 'Reconcile document with trading platform.'");
		Иначе
			ИмяСобытия = Источник;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИмяСобытия;
	
КонецФункции

// Возвращает признак использования интеграции хотя бы с одним маркетплейсом.
//
// Возвращаемое значение:
//   Булево - признак использования интеграции.
//
Функция ИспользуетсяИнтеграцияСМаркетплейсами() Экспорт

	ИспользуютсяИнтеграцияСOzon = ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСOzon");
	ЕстьУчетныеЗаписиOzon = Ложь;
	Если ИспользуютсяИнтеграцияСOzon Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	ИСТИНА
			|ИЗ
			|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
			|ГДЕ
			|	УчетныеЗаписиМаркетплейсов.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.МаркетплейсOzon)
			|	И НЕ УчетныеЗаписиМаркетплейсов.ПометкаУдаления";
		УстановитьПривилегированныйРежим(Истина);
		Результат = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		ЕстьУчетныеЗаписиOzon = Не Результат.Пустой();
	КонецЕсли;

	ИспользуютсяИнтеграцияСЯндексМаркет = ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет");
	ЕстьУчетныеЗаписиЯндексМаркет = Ложь;
	Если ИспользуютсяИнтеграцияСЯндексМаркет Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	ИСТИНА
			|ИЗ
			|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
			|ГДЕ
			|	УчетныеЗаписиМаркетплейсов.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет)
			|	И НЕ УчетныеЗаписиМаркетплейсов.ПометкаУдаления";
		УстановитьПривилегированныйРежим(Истина);
		Результат = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		ЕстьУчетныеЗаписиЯндексМаркет = Не Результат.Пустой();
	КонецЕсли;

	ИспользуютсяИнтеграцииСМаркетплейсами = ИспользуютсяИнтеграцияСOzon И ЕстьУчетныеЗаписиOzon
			Или ИспользуютсяИнтеграцияСЯндексМаркет И ЕстьУчетныеЗаписиЯндексМаркет;

	Возврат ИспользуютсяИнтеграцииСМаркетплейсами;

КонецФункции

// Создает элементы подсистемы "Интеграция с Ozon" в форме присоединенного файла.
// 
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - источник события.
//
Процедура ПриСозданииФормыЭлементаПрисоединенныхФайлов(Форма) Экспорт

	Если Не ИспользуетсяИнтеграцияСМаркетплейсами()
			Или ТипЗнч(Форма.Объект["Ссылка"]) <> Тип("СправочникСсылка.НоменклатураПрисоединенныеФайлы") Тогда
		Возврат;
	КонецЕсли;

	ЭлементПубликуетсяНаМП = Форма.Элементы.Вставить("ПубликуетсяВСервисах", Тип("ПолеФормы"), Форма.Элементы.ГруппаКоманды, Форма.Элементы.ФормаОткрытьФайлДляПросмотра);
	ЭлементПубликуетсяНаМП.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементПубликуетсяНаМП.ВидФлажка = ВидФлажка.Выключатель;
	ЭлементПубликуетсяНаМП.ПутьКДанным = "Объект.ПубликуетсяВСервисах";
	ЭлементПубликуетсяНаМП.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;

КонецПроцедуры

// Создает элементы подсистемы "Интеграция с Ozon" в форме списка присоединенных файлов.
// 
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - источник события.
//
Процедура ПриСозданииФормыСпискаПрисоединенныхФайлов(Форма) Экспорт

	Если Не ИспользуетсяИнтеграцияСМаркетплейсами() Или ТипЗнч(Форма.ВладелецФайла) <> Тип("СправочникСсылка.Номенклатура") Тогда
		Возврат;
	КонецЕсли;

	ПсевдонимПоля = "ПубликуетсяВСервисах";
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(Форма.Список.ТекстЗапроса);

	ЗапросПакета = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1];
	ОператорВыбрать = ЗапросПакета.Операторы[0];
	ТекстПоля = ОператорВыбрать.Источники[0].Источник.Псевдоним + "." + ПсевдонимПоля;

	ПолеПубликуетсяВСервисах = ОператорВыбрать.ВыбираемыеПоля.Добавить(ТекстПоля);
	ВыражениеПубликуетсяНаМП = ОператорВыбрать.ВыраженияОтбораКомпоновкиДанных.Добавить(ПолеПубликуетсяВСервисах);
	ВыражениеПубликуетсяНаМП.Псевдоним = ПсевдонимПоля;

	КолонкиСхемыЗапроса = ЗапросПакета.Колонки.Найти(ПолеПубликуетсяВСервисах);
	КолонкиСхемыЗапроса.Псевдоним = ПсевдонимПоля;
	ЗапросПакета.ПоляВыбораКомпоновкиДанных.Добавить(КолонкиСхемыЗапроса);

	Форма.Список.ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();

	ЭлементПубликуетсяНаМП = Форма.Элементы.Добавить(ПсевдонимПоля, Тип("ПолеФормы"), Форма.Элементы.Список);
	ЭлементПубликуетсяНаМП.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементПубликуетсяНаМП.ПутьКДанным = "Список.ПубликуетсяВСервисах";

КонецПроцедуры

// Создает элементы подсистемы "Интеграция с Ozon" в форме сертификата номенклатуры.
// 
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - источник события.
//
Процедура ПриСозданииФормыЭлементаСертификатаНоменклатуры(Форма) Экспорт

	Если Не ИспользуетсяИнтеграцияСМаркетплейсами() Тогда
		Возврат;
	КонецЕсли;

	ЭлементПубликуетсяНаМП = Форма.Элементы.Вставить("ПубликуетсяВСервисах", Тип("ПолеФормы"), Форма.Элементы.ГруппаИзображение, Форма.Элементы.ГруппаКоманднаяПанельИГиперссылка);
	ЭлементПубликуетсяНаМП.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементПубликуетсяНаМП.ВидФлажка = ВидФлажка.Выключатель;
	ЭлементПубликуетсяНаМП.ПутьКДанным = "Объект.ПубликуетсяВСервисах";
	ЭлементПубликуетсяНаМП.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;

КонецПроцедуры

// При создании формы списка контекст сертификатов номенклатуры.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - источник события.
//
Процедура ПриСозданииФормыСпискаКонтекстСертификатовНоменклатуры(Форма) Экспорт

	Если Не ИспользуетсяИнтеграцияСМаркетплейсами() Тогда
		Возврат;
	КонецЕсли;

	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(Форма.СертификатыНоменклатуры.ТекстЗапроса);
	СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля.Добавить("СертификатыНоменклатуры.ПубликуетсяВСервисах");
	Форма.СертификатыНоменклатуры.ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	НовыйЭлемент = Форма.Элементы.Добавить("СертификатыНоменклатурыПубликуетсяВСервисах", Тип("ПолеФормы"), Форма.Элементы.СертификатыНоменклатуры);
	НовыйЭлемент.ПутьКДанным = "СертификатыНоменклатуры.ПубликуетсяВСервисах";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;

КонецПроцедуры

// Описывает структуру ошибки.
//
// Возвращаемое значение:
//   Структура - описание ошибки:
//     * КодОшибки      - Строка - код ошибки;
//     * ОписаниеОшибки - Строка - текстовое описание ошибки;
//     * Детализация    - Массив Из Строка - детализированная информация по ошибке;
//                      - Неопределено - детализация не используется (по умолчанию).
//
Функция НоваяОшибка() Экспорт

	Ошибка = Новый Структура;
	Ошибка.Вставить("КодОшибки",      "");
	Ошибка.Вставить("ОписаниеОшибки", "");
	Ошибка.Вставить("Детализация",    Неопределено);

	Возврат Ошибка;

КонецФункции

// Дополняет состав ошибки.
//
// Параметры:
//   Приемник - см. НоваяОшибка.
//   Источник - см. НоваяОшибка.
//
Процедура ДополнитьОшибку(Приемник, Источник) Экспорт

	Если ПустаяСтрока(Источник.КодОшибки) Тогда
		Возврат;
	ИначеЕсли ПустаяСтрока(Приемник.КодОшибки) Тогда
		Приемник = Источник;
	Иначе
		Если Не ЗначениеЗаполнено(Приемник.Детализация) Тогда
			ОписаниеОшибки = Новый Массив;
			Если Не ПустаяСтрока(Приемник.ОписаниеОшибки) Тогда
				ОписаниеОшибки.Добавить(Приемник.ОписаниеОшибки);
			КонецЕсли;
			Если Не ПустаяСтрока(Источник.ОписаниеОшибки) Тогда
				ОписаниеОшибки.Добавить(Источник.ОписаниеОшибки);
			КонецЕсли;
			
			Приемник.ОписаниеОшибки = ?(ОписаниеОшибки.Количество() > 0, СтрСоединить(ОписаниеОшибки, Символы.ПС), "");;
			Приемник.Детализация = Источник.Детализация;
		Иначе
			Если ТипЗнч(Приемник.Детализация) = Тип("Массив") Тогда
				Детализация = Приемник.Детализация;
			Иначе
				Детализация = Новый Массив;
				Детализация.Добавить(Приемник.Детализация);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Источник.Детализация) Тогда
				СоставОшибкиИсточника = ?(ПустаяСтрока(Источник.ОписаниеОшибки), "", Источник.ОписаниеОшибки + ":" + Символы.ПС)
					+ СтрСоединить(Источник.Детализация, Символы.ПС + "   ");
			Иначе
				СоставОшибкиИсточника = Источник.ОписаниеОшибки;
			КонецЕсли;
			
			Детализация.Добавить(СоставОшибкиИсточника);
			
			Приемник.Детализация = Детализация;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Конструктор ошибок.
//
// Возвращаемое значение:
//   Структура - список доступных кодов ошибок:
//     * ОшибкаПрочие - Строка - прочая ошибка.
//     * ОшибкаНеУказанаУчетнаяЗапись - Строка - ошибка заполнения учетной записи.
//     * ОшибкаОбновлениеДанныхЗапрещено - Строка - ошибка обновления данных из-за запрета.
//     * ОшибкаЗаполненияДанных - Строка - ошибка заполнения данных в 1С.
//     * ОшибкаПубликацииТоваров - Строка - ошибка публикации товаров.
//     * ОшибкаЗагрузкиСертификата - Строка - Ошибка загрузки сертификата.
//     * ОшибкаВыборкиТоваров - Строка - ошибка выборки товаров.
//     * ОшибкаПереносаТоваров - Строка - ошибка переноса товаров в архив.
//     * ОшибкаВозвратаТоваров - Строка - ошибка возврата товаров из архива.
//     * ОшибкаУдаленияТоваров - Строка - ошибка удаления товаров.
//     * ОшибкаОтсутствуетИнформацияОЛимитах - Строка - ошибка отсутствия информации о лимитах.
//     * ОшибкаКатегорияУстарела - Строка - ошибка из-за устаревшей категории.
//
Функция КодыОшибок() Экспорт

	КодыОшибок = Новый Структура;
	КодыОшибок.Вставить("ОшибкаПрочие",                        "Ошибка_прочие");
	КодыОшибок.Вставить("ОшибкаНеУказанаУчетнаяЗапись",        "Ошибка_не_указана_учетная_запись");
	КодыОшибок.Вставить("ОшибкаОбновлениеДанныхЗапрещено",     "Ошибка_обновление_данных_торговой_площадки_запрещено");
	КодыОшибок.Вставить("ОшибкаЗаполненияДанных",              "Ошибка_заполнения_данных_в_1С");
	КодыОшибок.Вставить("ОшибкаПубликацииТоваров",             "Ошибка_публикации");
	КодыОшибок.Вставить("ОшибкаЗагрузкиСертификата",           "Ошибка_загрузки_сертификата");
	КодыОшибок.Вставить("ОшибкаВыборкиТоваров",                "Ошибка_выборки_товаров");
	КодыОшибок.Вставить("ОшибкаПереносаТоваров",               "Ошибка_переноса_в_архив");
	КодыОшибок.Вставить("ОшибкаВозвратаТоваров",               "Ошибка_возврата_из_архива");
	КодыОшибок.Вставить("ОшибкаУдаленияТоваров",               "Ошибка_удаления_товаров");
	КодыОшибок.Вставить("ОшибкаОтсутствуетИнформацияОЛимитах", "Ошибка_отсутствует_информация_о_лимитах");
	КодыОшибок.Вставить("ОшибкаКатегорияУстарела",             "Ошибка_категория_устарела");
	
	Возврат КодыОшибок;

КонецФункции

// Дополняет дату смещением часового пояса.
//
// Параметры:
//   ДополняемаяДата       - Дата - дополняемая дата.
//   СмещениеЧасовогоПояса - Число - количество секунд.
//   ОбновлятьТекущуюДату  - Булево - признак обновления текущей даты.
//
// Возвращаемое значение:
//   Дата - дата со смещением часового пояса.
//
Функция ДатаСоСмещениемЧасовогоПояса(Знач ДополняемаяДата, СмещениеЧасовогоПояса = 0,
			ОбновлятьТекущуюДату = Ложь) Экспорт

	Если ОбновлятьТекущуюДату Тогда
		ТекущаяДата = ТекущаяДатаСеанса();
		ДополняемаяДата = ?(ДополняемаяДата = НачалоДня(ТекущаяДата),
							ТекущаяДата,
							КонецДня(ДополняемаяДата));
	КонецЕсли;
	
	Возврат ДополняемаяДата + СмещениеЧасовогоПояса;

КонецФункции

// Добавляет команду для открытия формы печати этикеток торговой площадки.
//
// Параметры:
//  КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
// Возвращаемое значение:
//  - Неопределено - если не используется интеграция с торговыми площадками.
//  - СтрокаТаблицыЗначений из см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Функция ДобавитьКомандуПечатиЭтикеток(КомандыПечати) Экспорт
	
	ИспользоватьИнтеграциюСOzon = ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСOzon");
	ИспользоватьИнтеграциюСЯндексМаркет = ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет");
	
	Если Не ИспользоватьИнтеграциюСOzon И Не ИспользоватьИнтеграциюСЯндексМаркет Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КомандаПечати                                = КомандыПечати.Добавить();
	КомандаПечати.Обработчик                     = "ИнтеграцияСМаркетплейсамиКлиент.ПечатьЭтикеток";
	КомандаПечати.МенеджерПечати                 = "Справочник.УчетныеЗаписиМаркетплейсов";
	КомандаПечати.Идентификатор                  = "ЭтикеткиТорговойПлощадки";
	КомандаПечати.Представление                  = НСтр("ru = 'Этикетки торговой площадки';
														|en = 'Trading platform labels'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	Возврат КомандаПечати;
	
КонецФункции

#Область РаботаСВнешнимиРесурсами

// Возвращает общие параметры подключения к REST API Яндекс.OAuth.
// 
// Возвращаемое значение:
//   Структура - дополненная структура функции ПараметрыСоединения:
//     * ШаблонОшибки       - Строка - шаблон ошибки.
//     * ШаблонОшибкиСКодом - Строка - шаблон ошибки.
//     * ТипОбъектОшибка    - ТипЗначенияXDTO, ТипОбъектаXDTO, Неопределено - тип объекта.
//
Функция ПараметрыРаботыССервисомАвторизации() Экспорт

	ПараметрыРаботы = Новый Структура;
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыРаботы, ПараметрыСоединения());
	ПараметрыРаботы.Сервер = "oauth.yandex.ru";
	ПараметрыРаботы.Вставить("ШаблонОшибки", НСтр("ru = 'При выполнении метода API %1 возникла ошибка: %2';
													|en = 'An error occurred when executing the %1 API method: %2'"));
	ПараметрыРаботы.Вставить("ШаблонОшибкиСКодом", НСтр("ru = 'При выполнении метода API %1 возникла ошибка: (%2) %3';
														|en = 'An error occurred when executing the %1 API method: (%2) %3'"));
	ПараметрыРаботы.Вставить("ТипОбъектОшибка", ФабрикаXDTO.Тип("oauth.yandex.ru", "error"));

	Возврат ПараметрыРаботы;

КонецФункции

// Возвращает общие параметры подключения к REST API Яндекс.Диск.
// 
// Возвращаемое значение:
//   Структура - дополненная структура функции ПараметрыСоединения:
//     * ШаблонОшибки       - Строка - шаблон ошибки.
//     * ШаблонОшибкиСКодом - Строка - шаблон ошибки.
//     * ТипОбъектОшибка    - ТипЗначенияXDTO, ТипОбъектаXDTO, Неопределено - тип объекта.
//
Функция ПараметрыРаботыСЯндексДиском() Экспорт

	ПараметрыРаботы = Новый Структура;
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыРаботы, ПараметрыСоединения());
	ПараметрыРаботы.Сервер = "cloud-api.yandex.net";
	ПараметрыРаботы.Порт = 443;
	ПараметрыРаботы.Вставить("ШаблонОшибки", НСтр("ru = 'При выполнении метода API %1 возникла ошибка: %2';
													|en = 'An error occurred when executing the %1 API method: %2'"));
	ПараметрыРаботы.Вставить("ШаблонОшибкиСКодом", НСтр("ru = 'При выполнении метода API %1 возникла ошибка: (%2) %3';
														|en = 'An error occurred when executing the %1 API method: (%2) %3'"));
	ПараметрыРаботы.Вставить("ТипОбъектОшибка", ФабрикаXDTO.Тип("cloud-api.yandex.net/v1/disk", "error"));

	Возврат ПараметрыРаботы;

КонецФункции

// Возвращает общие параметры приложения для использования в механизмах сервиса.
// 
// Возвращаемое значение:
//   см. ПараметрыАвторизацииПриложения.
//
Функция ПараметрыПриложенияЯндексДиск() Экспорт

	ПараметрыПриложенияЯндексДиск = ПараметрыАвторизацииПриложения();
	ПараметрыПриложенияЯндексДиск.ИдентификаторПриложения = "ЯндексДиск";
	ПараметрыПриложенияЯндексДиск.client_id = "17d0ffccaae840dba48edd4e246f6f27";
	ПараметрыПриложенияЯндексДиск.client_secret = "80ebf05fe140474d9b7adbdf6ade7e4e";

	Возврат ПараметрыПриложенияЯндексДиск;

КонецФункции

// Формирует HTTPЗапрос по переданным данным.
// 
// Параметры:
//   Адрес                   - Строка - путь к ресурсу на сервере.
//   ТелоЗапроса             - Строка, ДвоичныеДанные - источник для установки тела запроса.
//                               Применимо для методов HTTP: OPTIONS, PATCH, POST и PUT.
//   ДополнительныеЗаголовки - Соответствие Из КлючИЗначение - заголовки запроса. Могут перезаписывать обязательные 
//                               заголовки функции. Названия заголовков регистронезависимые.
// 
// Возвращаемое значение:
//   HTTPЗапрос - подготовленный для отправки запрос HTTP.
//
Функция ЗапросHTTP(Адрес, ТелоЗапроса = "", ДополнительныеЗаголовки = Неопределено) Экспорт

	Заголовки = Новый Соответствие;
	Заголовки.Вставить("charset", "UTF-8");
	Заголовки.Вставить("accept", "application/json");
	Заголовки.Вставить("content-type", "application/json");

	Если ДополнительныеЗаголовки <> Неопределено Тогда
		Для Каждого Заголовок Из ДополнительныеЗаголовки Цикл
			Заголовки.Вставить(НРег(Заголовок.Ключ), Заголовок.Значение);
		КонецЦикла;
	КонецЕсли;

	Запрос = Новый HTTPЗапрос(Адрес, Заголовки);

	Если Не ПустаяСтрока(ТелоЗапроса) Тогда
		Если ТипЗнч(ТелоЗапроса) = Тип("ДвоичныеДанные") Тогда
			Запрос.УстановитьТелоИзДвоичныхДанных(ТелоЗапроса);
		Иначе
			Запрос.УстановитьТелоИзСтроки(ТелоЗапроса);
		КонецЕсли;
	КонецЕсли;

	Возврат Запрос;

КонецФункции

// Записывает список ключей и значений по адресу в защищенное хранилище.
// 
// Параметры:
//   УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - используется для формирования адреса в 
//                                 хранилище.
//   ПараметрыАвторизацииПриложения - см. ПараметрыАвторизацииПриложения.
//   ДанныеПриложения          - Структура, Соответствие Из Произвольный - источник пар ключ/значение для записи в 
//                                 защищенное хранилище.
//
Процедура ЗаписатьДанныеПриложенияВХранилище(УчетнаяЗаписьМаркетплейса, ПараметрыАвторизацииПриложения, ДанныеПриложения) Экспорт

	АдресВХранилище = Строка(УчетнаяЗаписьМаркетплейса.УникальныйИдентификатор()) + "/" + ПараметрыАвторизацииПриложения.ИдентификаторПриложения;
	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого ЭлементДанных Из ДанныеПриложения Цикл
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(АдресВХранилище, ЭлементДанных.Значение, ЭлементДанных.Ключ);
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

// Получает данные приложения из хранилища.
//
// Параметры:
//   УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - используется для формирования адреса в 
//                                 хранилище.
//   ПараметрыАвторизацииПриложения - см. ПараметрыАвторизацииПриложения.
//   Ключи                     - Строка - имена ключей, разделенные запятыми.
// 
// Возвращаемое значение:
//   Неопределено - если данные отсутствуют,
//   см. ПараметрыАвторизацииПриложения - данные из безопасного хранилища. Если указан один ключ, то возвращается его 
//     значение, иначе структура.
//
Функция ПолучитьДанныеПриложенияИзХранилища(УчетнаяЗаписьМаркетплейса, ПараметрыАвторизацииПриложения, Ключи) Экспорт

	АдресВХранилище = Строка(УчетнаяЗаписьМаркетплейса.УникальныйИдентификатор()) + "/" + ПараметрыАвторизацииПриложения.ИдентификаторПриложения;
	УстановитьПривилегированныйРежим(Истина);
	Возврат ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(АдресВХранилище, Ключи);

КонецФункции

#Область РаботаСЯндексАвторизацией

// Формирует адрес для интерактивного запроса кода подтверждения.
// 
// Параметры:
//   КодПриложения - Строка - код приложения Яндекс, для которого запрашивается доступ.
// 
// Возвращаемое значение:
//   Строка - адрес для перехода на страницу запроса кода подтверждения.
//
Функция АдресЗапросаКодаПодтверждения(КодПриложения) Экспорт

	ТипОбъекта = ФабрикаXDTO.Тип("oauth.yandex.ru", "authorizeRequest");
	ОбъектXDTO = ФабрикаXDTO.Создать(ТипОбъекта);
	ОбъектXDTO.client_id = КодПриложения;

	ПараметрыСоединения = ПараметрыРаботыССервисомАвторизации();

	Метод = "authorize";

	СтрокаСоединения = ПротоколПоУмолчанию(ПараметрыСоединения.Порт) + ПараметрыСоединения.Сервер + "/" + Метод + ПараметрыЗапроса(ОбъектXDTO, ТипОбъекта);

	Возврат СтрокаСоединения;

КонецФункции

// Запрашивает токены авторизации приложения по коду подтверждения от пользователя.
// 
// Параметры:
//   Приложение       - Структура - описание приложения Яндекс. 
//                        Содержит ключи: client_id, client_secret.
//   КодПодтверждения - Строка - код подтверждения, указанный пользователем.
// 
// Возвращаемое значение:
//   см. ПараметрыКлючаАвторизацииПриложения.
//
Функция ЗапроситьТокеныАвторизацииПоКоду(Приложение, КодПодтверждения) Экспорт

	ПараметрыРаботы = ПараметрыРаботыССервисомАвторизации();

	ТипОбъекта = ФабрикаXDTO.Тип("oauth.yandex.ru", "tokenRequest");
	ЗапросОбъект = ФабрикаXDTO.Создать(ТипОбъекта);
	ЗапросОбъект.code = КодПодтверждения;
	ЗапросОбъект.client_id = Приложение.client_id;
	ЗапросОбъект.client_secret = Приложение.client_secret;

	ТелоЗапроса = ПараметрыЗапроса(ЗапросОбъект, ТипОбъекта, Ложь);
	Метод = "token";

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	Заголовки.Вставить("Content-Length", СтрДлина(ТелоЗапроса));

	Запрос = ЗапросHTTP(Метод, ТелоЗапроса, Заголовки);
	РезультатОтправки = ВыполнитьЗапросHTTP(ПараметрыРаботы, Запрос);

	ПараметрыОтвета = ПараметрыКлючаАвторизацииПриложения();

	Если РезультатОтправки.Отказ Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатОтправки.ТекстОшибки);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат ПараметрыОтвета;
	КонецЕсли;

	Если РезультатОтправки.КодСостояния = 200 Тогда
		РезультатПриведения = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ФабрикаXDTO.Тип("oauth.yandex.ru", "tokenResponse"));
		Если РезультатПриведения.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатПриведения.ТекстОшибки);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Иначе
			ЗаполнитьЗначенияСвойств(ПараметрыОтвета, РезультатПриведения.Объект);
			ПараметрыОтвета.Отказ = Ложь;
		КонецЕсли;
	Иначе
		РезультатПриведенияКОшибке = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ПараметрыРаботы.ТипОбъектОшибка);
		Если РезультатПриведенияКОшибке.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, РезультатОтправки.КодСостояния, РезультатОтправки.ТекстОтвета);
		Иначе
			ОтветОбъект = РезультатПриведенияКОшибке.Объект;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, ОтветОбъект.error, ОтветОбъект.error_description);
		КонецЕсли;
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'При авторизации с данным кодом подтверждения возникла ошибка.
			|Подробности см. в журнале регистрации.';
			|en = 'An error occurred when authorizing with this confirmation code.
			|For more information, see the event log.'"));
	КонецЕсли;

	Возврат ПараметрыОтвета;

КонецФункции

// Запрашивает токены авторизации приложения по токену обновления.
// 
// Параметры:
//   Приложение      - Структура - описание приложения Яндекс. 
//                       Содержит ключи: client_id, client_secret.
//   ТокенОбновления - Строка - токен обновления, полученный при предыдущем запросе.
// 
// Возвращаемое значение:
//   см. ПараметрыКлючаАвторизацииПриложения.
//
Функция ОбновитьТокеныАвторизации(Приложение, ТокенОбновления) Экспорт

	ПараметрыРаботы = ПараметрыРаботыССервисомАвторизации();

	ТипОбъекта = ФабрикаXDTO.Тип("oauth.yandex.ru", "refreshRequest");
	ЗапросОбъект = ФабрикаXDTO.Создать(ТипОбъекта);
	ЗапросОбъект.grant_type = "refresh_token";
	ЗапросОбъект.refresh_token = ТокенОбновления;
	ЗапросОбъект.client_id = Приложение.client_id;
	ЗапросОбъект.client_secret = Приложение.client_secret;

	ТелоЗапроса = ПараметрыЗапроса(ЗапросОбъект, ТипОбъекта, Ложь);
	Метод = "token";

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	Заголовки.Вставить("Content-Length", СтрДлина(ТелоЗапроса));

	Запрос = ЗапросHTTP(Метод, ТелоЗапроса, Заголовки);
	РезультатОтправки = ВыполнитьЗапросHTTP(ПараметрыРаботы, Запрос);

	ПараметрыОтвета = ПараметрыКлючаАвторизацииПриложения();
	
	Если РезультатОтправки.Отказ Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатОтправки.ТекстОшибки);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат ПараметрыОтвета;
	КонецЕсли;

	Если РезультатОтправки.КодСостояния = 200 Тогда
		РезультатПриведения = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ФабрикаXDTO.Тип("oauth.yandex.ru", "tokenResponse"));
		Если РезультатПриведения.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатПриведения.ТекстОшибки);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Иначе
			ЗаполнитьЗначенияСвойств(ПараметрыОтвета, РезультатПриведения.Объект);
			ПараметрыОтвета.Отказ = Ложь;
		КонецЕсли;
	Иначе
		РезультатПриведенияКОшибке = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ПараметрыРаботы.ТипОбъектОшибка);
		Если РезультатПриведенияКОшибке.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, РезультатОтправки.КодСостояния, РезультатОтправки.ТекстОтвета);
		Иначе
			ОтветОбъект = РезультатПриведенияКОшибке.Объект;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, ОтветОбъект.error, ОтветОбъект.error_description);
		КонецЕсли;
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'При авторизации с данным кодом подтверждения возникла ошибка.
			|Подробности см. в журнале регистрации.';
			|en = 'An error occurred when authorizing with this confirmation code.
			|For more information, see the event log.'"));
	КонецЕсли;

	Возврат ПараметрыОтвета;

КонецФункции

// Добавляет данные авторизации в безопасное хранилище по ключу обновления. 
//
// Параметры:
//   УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - используется для формирования адреса в 
//                                 хранилище.
//
Процедура ОбновитьКлючиДоступаПоУчетнойЗаписи(УчетнаяЗаписьМаркетплейса) Экспорт  
	
	ПараметрыАвторизацииПриложения = ПараметрыПриложенияЯндексДиск();
	ТокенОбновления = ПолучитьДанныеПриложенияИзХранилища(УчетнаяЗаписьМаркетплейса, ПараметрыАвторизацииПриложения, "refresh_token");
	
	НовыеКлючи = ОбновитьТокеныАвторизации(ПараметрыАвторизацииПриложения, ТокенОбновления);
	
	Если НЕ НовыеКлючи.Отказ Тогда  
		Если НовыеКлючи.Свойство("access_token") 
			И НовыеКлючи.Свойство("expires_in") 
			И НовыеКлючи.Свойство("refresh_token") Тогда
			
			Если ОбщегоНазначенияУТКлиентСервер.ЭтаСтрокаЯвляетсяЦелымНеотрицательнымЧислом(НовыеКлючи.expires_in) Тогда
				ДанныеПриложения = Новый Структура();
				ДанныеПриложения.Вставить("access_token", НовыеКлючи.access_token);
				ДанныеПриложения.Вставить("expires_in", ТекущаяДатаСеанса() + НовыеКлючи.expires_in);
				ДанныеПриложения.Вставить("refresh_token", НовыеКлючи.refresh_token);
				ЗаписатьДанныеПриложенияВХранилище(УчетнаяЗаписьМаркетплейса, ПараметрыАвторизацииПриложения, ДанныеПриложения);
			Иначе
				ТекстОписанияСобытия = НСтр("ru = 'Ошибка сохранения данных в безопасном хранилище';
											|en = 'An error occurred when saving the data to the secure storage'",
					ОбщегоНазначения.КодОсновногоЯзыка());
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Некорректное значение свойства expires_in при получении ключа авторизации: %1';
						|en = 'Incorrect value of the expires_in property when receiving an authorization key: %1'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
					НовыеКлючи.expires_in);
				
				ЗаписьЖурналаРегистрации(ТекстОписанияСобытия, 
					УровеньЖурналаРегистрации.Ошибка,,, 
					ТекстОшибки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область РаботаСЯндексДиском

// Получает из хранилища токен доступа, выданный приложению для работы с сервисом Яндекс.Диск.
// 
// Параметры:
//   УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - используется для формирования адреса в 
//                                 хранилище.
// 
// Возвращаемое значение:
//   Строка - токен доступа приложения.
//
Функция ПолучитьТокенДоступаЯндексДиск(УчетнаяЗаписьМаркетплейса) Экспорт

	ПараметрыАвторизацииПриложения = ПараметрыПриложенияЯндексДиск();
	ТокенДоступа = ПолучитьДанныеПриложенияИзХранилища(УчетнаяЗаписьМаркетплейса, ПараметрыАвторизацииПриложения, "access_token");

	Возврат ТокенДоступа;

КонецФункции  

// Получает из хранилища дату окончания срока действия доступа, выданному приложению для работы с сервисом Яндекс.Диск.
// 
// Параметры:
//   УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - используется для формирования адреса в 
//                                 хранилище.
// 
// Возвращаемое значение:
//   Дата - дата окончания срока действия доступа.
//
Функция ПолучитьДатуДействияДоступаЯндексДиск(УчетнаяЗаписьМаркетплейса) Экспорт

	ПараметрыАвторизацииПриложения = ПараметрыПриложенияЯндексДиск();
	ДатаДействия = ПолучитьДанныеПриложенияИзХранилища(УчетнаяЗаписьМаркетплейса, ПараметрыАвторизацииПриложения, "expires_in");
	
	Если НЕ ЗначениеЗаполнено(ДатаДействия) Тогда 
		ДатаДействия = Дата('00010101');
	КонецЕсли;
	
	Возврат ДатаДействия;

КонецФункции

// Получает метаинформацию о файле или каталоге с помощью запроса GET к методу v1/disk/resources.
// 
// Параметры:
//   ТокенДоступа          - Строка - access_token приложения.
//   ПрисоединенныйФайл    - СправочникСсылка.НоменклатураПрисоединенныеФайлы - файл, по которому запрашивается информация 
//                           от сервиса.
//   СписокПолей           - Строка - имена реквизитов, разделенные запятой. Подробности см. в описании метода API. 
//   ПовторятьПриОшибке404 - Число  - количество повторений при получении временной ошибки (код 404).
// 
// Возвращаемое значение:
//   Структура - содержит значения запрошенных из СписокПолей и ключи:
//     * Отказ - Булево - признак наличия ошибки.
//     * КодСостояния - Строка - код состояния из сервиса.
//
Функция ПолучитьИнформациюОРесурсе(ТокенДоступа, ПрисоединенныйФайл, СписокПолей = "name", ПовторятьПриОшибке404 = 0) Экспорт

	ИнформацияОРесурсе = Новый Структура;
	ИнформацияОРесурсе.Вставить("Отказ", Истина);
	ИнформацияОРесурсе.Вставить("КодСостояния", "");
	
	Если Не ПустаяСтрока(СписокПолей) Тогда
		ДополнительнаяИнформацияОРесурсе = Новый Структура(СписокПолей);
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ИнформацияОРесурсе, ДополнительнаяИнформацияОРесурсе);
	КонецЕсли;
	
	ПараметрыРаботы = ПараметрыРаботыСЯндексДиском();
	ПутьКФайлуНаЯДиске = ПутьКРесурсу(ПрисоединенныйФайл);

	ТипОбъекта = ФабрикаXDTO.Тип("cloud-api.yandex.net/v1/disk", "metainfoRequest");
	ЗапросОбъект = ФабрикаXDTO.Создать(ТипОбъекта);
	ЗапросОбъект.path = ПутьКФайлуНаЯДиске;
	ЗапросОбъект.fields = СписокПолей;

	Метод = "v1/disk/resources";

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Accept", "application/json");
	Заголовки.Вставить("Authorization", "OAuth " + ТокенДоступа);

	Запрос = ЗапросHTTP(Метод + ПараметрыЗапроса(ЗапросОбъект, ТипОбъекта),, Заголовки);
	РезультатОтправки = ВыполнитьЗапросHTTP(ПараметрыРаботы, Запрос, "GET");

	ИнформацияОРесурсе.КодСостояния = РезультатОтправки.КодСостояния;

	Если РезультатОтправки.Отказ Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатОтправки.ТекстОшибки);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат ИнформацияОРесурсе;
	КонецЕсли;

	Если РезультатОтправки.КодСостояния = 200 Тогда
		РезультатПриведения = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ФабрикаXDTO.Тип("cloud-api.yandex.net/v1/disk", "metainfoResponse"));
		Если РезультатПриведения.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатПриведения.ТекстОшибки);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Иначе
			ОтветОбъект = РезультатПриведения.Объект;
			ЗаполнитьЗначенияСвойств(ИнформацияОРесурсе, ОтветОбъект);
			ИнформацияОРесурсе.Отказ = Ложь;
		КонецЕсли; 
	ИначеЕсли РезультатОтправки.КодСостояния = 404 Тогда  
		ПовторятьПриОшибке404 = ПовторятьПриОшибке404 + 1;
		ОбщегоНазначенияБТС.Пауза(1);
		Если ПовторятьПриОшибке404 < 3 Тогда   
			ПолучитьИнформациюОРесурсе(ТокенДоступа, ПрисоединенныйФайл, СписокПолей, ПовторятьПриОшибке404);
		КонецЕсли;
	Иначе
		РезультатПриведенияКОшибке = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ПараметрыРаботы.ТипОбъектОшибка);
		Если РезультатПриведенияКОшибке.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, РезультатОтправки.КодСостояния, РезультатОтправки.ТекстОтвета);
		Иначе
			ОтветОбъект = РезультатПриведенияКОшибке.Объект;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, ОтветОбъект.error, ОтветОбъект.description);
		КонецЕсли;
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;

	Возврат ИнформацияОРесурсе;

КонецФункции

// Загружает файл на сервис Яндекс.Диск в папку приложения и публикует его.
// 
// Параметры:
//   ТокенДоступа          - Строка - access_token приложения.
//   ПрисоединенныйФайл    - СправочникСсылка.НоменклатураПрисоединенныеФайлы - файл, который загружается на сервис.
//   ОбновитьФайлНаСервисе - Булево - признак обновления.
// 
// Возвращаемое значение:
//   Структура - содержит путь к файлу на сервисе, публичную ссылку и признак отказа в выполнении операции:
//     * ПутьКФайлуНаЯДиске - Строка - путь к файлу.
//     * ПубличнаяСсылка    - Строка - публичная ссылка на файл.
//     * Отказ              - Булево - признак наличия ошибок при загрузке файла.
//
Функция ЗагрузитьФайлНаСервис(ТокенДоступа, ПрисоединенныйФайл, ОбновитьФайлНаСервисе = Истина) Экспорт

	ПараметрыРаботы = ПараметрыРаботыСЯндексДиском();
	РезультатПубликации = Новый Структура("ПутьКФайлуНаЯДиске, ПубличнаяСсылка, Отказ", "", "", Истина);
	ПутьКФайлуНаЯДиске = ПутьКРесурсу(ПрисоединенныйФайл);
	РезультатПубликации.ПутьКФайлуНаЯДиске = ПутьКФайлуНаЯДиске;

	ИнформацияОРесурсе = ПолучитьИнформациюОРесурсе(ТокенДоступа, ПрисоединенныйФайл);
	Если ИнформацияОРесурсе.КодСостояния = 200 И Не ОбновитьФайлНаСервисе Тогда
		РезультатПубликации.ПубличнаяСсылка = ИнформацияОРесурсе.public_url;
		РезультатПубликации.Отказ = Ложь;
		Возврат РезультатПубликации;
	КонецЕсли;

	ТипОбъекта = ФабрикаXDTO.Тип("cloud-api.yandex.net/v1/disk", "uploadGetRequest");
	ЗапросОбъект = ФабрикаXDTO.Создать(ТипОбъекта);
	ЗапросОбъект.path = ПутьКФайлуНаЯДиске;

	Метод = "v1/disk/resources/upload";

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Accept", "application/json");
	Заголовки.Вставить("Authorization", "OAuth " + ТокенДоступа);

	Запрос = ЗапросHTTP(Метод + ПараметрыЗапроса(ЗапросОбъект, ТипОбъекта),, Заголовки);
	РезультатОтправки = ВыполнитьЗапросHTTP(ПараметрыРаботы, Запрос, "GET");

	Если РезультатОтправки.Отказ Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатОтправки.ТекстОшибки);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат РезультатПубликации;
	КонецЕсли;

	Если РезультатОтправки.КодСостояния = 200 Тогда
		РезультатПриведения = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ФабрикаXDTO.Тип("cloud-api.yandex.net/v1/disk", "uploadGetResponse"));
		Если РезультатПриведения.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатПриведения.ТекстОшибки);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			Возврат РезультатПубликации;
		Иначе
			ОтветОбъект = РезультатПриведения.Объект;
		КонецЕсли;
	Иначе
		РезультатПриведенияКОшибке = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ПараметрыРаботы.ТипОбъектОшибка);
		Если РезультатПриведенияКОшибке.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, РезультатОтправки.КодСостояния, РезультатОтправки.ТекстОтвета);
		Иначе
			ОтветОбъект = РезультатПриведенияКОшибке.Объект;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, ОтветОбъект.error, ОтветОбъект.description);
		КонецЕсли;
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат РезультатПубликации;
	КонецЕсли;

	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(ОтветОбъект.href);

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Content-Type", "image/jpeg");

	ПараметрыСоединения = ПараметрыСоединения();
	ПараметрыСоединения.Сервер = СтруктураURI.Хост;
	ПараметрыСоединения.Порт = СтруктураURI.Порт;
	ТелоЗапроса = РаботаСФайлами.ДвоичныеДанныеФайла(ПрисоединенныйФайл, Ложь);
	Если ТелоЗапроса = Неопределено Тогда
		Возврат РезультатПубликации;
	КонецЕсли;

	Метод = СтруктураURI.ПутьНаСервере;

	Запрос = ЗапросHTTP(Метод, ТелоЗапроса, Заголовки);
	РезультатОтправки = ВыполнитьЗапросHTTP(ПараметрыСоединения, Запрос, "PUT");

	Если РезультатОтправки.Отказ Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатОтправки.ТекстОшибки);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат РезультатПубликации;
	КонецЕсли;

	Если Не ЗапросУспешноОбработан(РезультатОтправки.КодСостояния) Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатОтправки.КодСостояния, РезультатОтправки.ТекстОшибки);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат РезультатПубликации;
	КонецЕсли;

	Если Не ЗапросУспешноОбработан(РезультатОтправки.КодСостояния) Тогда
		РезультатПриведенияКОшибке = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ПараметрыРаботы.ТипОбъектОшибка);
		Если РезультатПриведенияКОшибке.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, РезультатОтправки.КодСостояния, РезультатОтправки.ТекстОтвета);
		Иначе
			ОтветОбъект = РезультатПриведенияКОшибке.Объект;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, ОтветОбъект.error, ОтветОбъект.description);
		КонецЕсли;
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат РезультатПубликации;
	КонецЕсли;

	ТипОбъекта = ФабрикаXDTO.Тип("cloud-api.yandex.net/v1/disk", "publishRequest");
	ЗапросОбъект = ФабрикаXDTO.Создать(ТипОбъекта);
	ЗапросОбъект.path = ПутьКФайлуНаЯДиске;
	ЗапросОбъект.fields = "public_key";

	Метод = "v1/disk/resources/publish";

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Accept", "application/json");
	Заголовки.Вставить("Authorization", "OAuth " + ТокенДоступа);

	Запрос = ЗапросHTTP(Метод + ПараметрыЗапроса(ЗапросОбъект, ТипОбъекта),, Заголовки);
	РезультатОтправки = ВыполнитьЗапросHTTP(ПараметрыРаботы, Запрос, "PUT");

	Если РезультатОтправки.Отказ Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатОтправки.ТекстОшибки);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат РезультатПубликации;
	КонецЕсли;

	Если РезультатОтправки.КодСостояния = 200 Тогда
		РезультатПриведения = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ФабрикаXDTO.Тип("cloud-api.yandex.net/v1/disk", "publishResponse"));
		Если РезультатПриведения.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатПриведения.ТекстОшибки);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			Возврат РезультатПубликации;
		Иначе
			ОтветОбъект = РезультатПриведения.Объект;
		КонецЕсли;
	Иначе
		РезультатПриведенияКОшибке = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ПараметрыРаботы.ТипОбъектОшибка);
		Если РезультатПриведенияКОшибке.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, РезультатОтправки.КодСостояния, РезультатОтправки.ТекстОтвета);
		Иначе
			ОтветОбъект = РезультатПриведенияКОшибке.Объект;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, ОтветОбъект.error, ОтветОбъект.description);
		КонецЕсли;
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат РезультатПубликации;
	КонецЕсли;

	ИнформацияОРесурсе = ПолучитьИнформациюОРесурсе(ТокенДоступа, ПрисоединенныйФайл, "public_url");

	Если Не ИнформацияОРесурсе.Отказ Тогда
		РезультатПубликации.ПубличнаяСсылка = ИнформацияОРесурсе.public_url;
		РезультатПубликации.Отказ = Ложь;
	КонецЕсли;
	Возврат РезультатПубликации;

КонецФункции

// Удаляет ресурс с сервиса Яндекс.Диск из папки приложения.
// 
// Параметры:
//   ТокенДоступа       - Строка - access_token приложения.
//   ПутьКФайлуНаЯДиске - Строка - путь к файлу на сервисе, возвращается функцией ЗагрузитьФайлНаСервис().
// 
// Возвращаемое значение:
//   Булево - признак успешного завершения операции удаления файла.
//
Функция УдалитьФайлССервиса(ТокенДоступа, ПутьКФайлуНаЯДиске) Экспорт

	ПараметрыРаботы = ПараметрыРаботыСЯндексДиском();

	ТипОбъекта = ФабрикаXDTO.Тип("cloud-api.yandex.net/v1/disk", "deleteRequest");
	ЗапросОбъект = ФабрикаXDTO.Создать(ТипОбъекта);
	ЗапросОбъект.path = ПутьКФайлуНаЯДиске;

	Метод = "v1/disk/resources";

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Accept", "application/json");
	Заголовки.Вставить("Authorization", "OAuth " + ТокенДоступа);

	Запрос = ЗапросHTTP(Метод + ПараметрыЗапроса(ЗапросОбъект, ТипОбъекта),, Заголовки);
	РезультатОтправки = ВыполнитьЗапросHTTP(ПараметрыРаботы, Запрос, "DELETE");

	Если РезультатОтправки.Отказ Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатОтправки.ТекстОшибки);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;

	Если Не ЗапросУспешноОбработан(РезультатОтправки.КодСостояния) Тогда
		РезультатПриведенияКОшибке = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ПараметрыРаботы.ТипОбъектОшибка);
		Если РезультатПриведенияКОшибке.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, РезультатОтправки.КодСостояния, РезультатОтправки.ТекстОтвета);
		Иначе
			ОтветОбъект = РезультатПриведенияКОшибке.Объект;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, ОтветОбъект.error, ОтветОбъект.description);
		КонецЕсли;
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;

КонецФункции

// Проверяет, что переданная ссылка является публичной для сервиса Яндекс.Диск.
// 
// Параметры:
//   URL - Строка - url ссылки в формате: [Протокол://]<Сервер>/<Ключ>.
// 
// Возвращаемое значение:
//   Булево - является ли ссылка публичной.
//
Функция ЭтоПубличнаяСсылкаЯндексДиска(URL) Экспорт
	
	Возврат (СтрНайти(НРег(URL), "yadi.") > 0);
	
КонецФункции

// Генерирует url для скачивания файла по публичной ссылке сервиса Яндекс.Диск.
// 
// Параметры:
//   ТокенДоступа    - Строка - access_token приложения.
//   ПубличнаяСсылка - Строка - url файла в формате: [Протокол://]<Сервер>/<Ключ>.
// 
// Возвращаемое значение:
//   Строка - содержит путь к файлу на сервисе.
//
Функция ПолучитьФайлПоПубличнойСсылкеИзСервиса(ТокенДоступа, ПубличнаяСсылка) Экспорт
	
	ПараметрыРаботы = ПараметрыРаботыСЯндексДиском();
	
	ТипОбъекта        = ФабрикаXDTO.Тип("cloud-api.yandex.net/v1/disk", "downloadGetPublic");
	ЗапросОбъект      = ФабрикаXDTO.Создать(ТипОбъекта);
	ЗапросОбъект.public_key = ПубличнаяСсылка;

	Метод = "v1/disk/public/resources/download";

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Accept",        "application/json");
	Заголовки.Вставить("Content-Type",  "application/json");
	Заголовки.Вставить("Authorization", "OAuth " + ТокенДоступа);

	Запрос = ЗапросHTTP(Метод + ПараметрыЗапроса(ЗапросОбъект, ТипОбъекта),, Заголовки);
	РезультатОтправки = ВыполнитьЗапросHTTP(ПараметрыРаботы, Запрос, "GET");

	Если РезультатОтправки.Отказ Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатОтправки.ТекстОшибки);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат ПубличнаяСсылка;
	КонецЕсли;

	Если РезультатОтправки.КодСостояния = 200 Тогда
		РезультатПриведения = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ФабрикаXDTO.Тип("cloud-api.yandex.net/v1/disk", "publishResponse"));
		Если РезультатПриведения.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибки, Метод, РезультатПриведения.ТекстОшибки);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			Возврат ПубличнаяСсылка;
		Иначе
			ОтветОбъект = РезультатПриведения.Объект;
		КонецЕсли;
	Иначе
		РезультатПриведенияКОшибке = ПривестиКТипуОбъекта(РезультатОтправки.ТекстОтвета, ПараметрыРаботы.ТипОбъектОшибка);
		Если РезультатПриведенияКОшибке.Отказ Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, РезультатОтправки.КодСостояния, РезультатОтправки.ТекстОтвета);
		Иначе
			ОтветОбъект = РезультатПриведенияКОшибке.Объект;
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПараметрыРаботы.ШаблонОшибкиСКодом, Метод, ОтветОбъект.error, ОтветОбъект.description);
		КонецЕсли;
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат ПубличнаяСсылка;
	КонецЕсли;
	
	Возврат ОтветОбъект.href;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область РегламентныеЗадания

// Создание / изменение регламентного задания.
// 
// Параметры:
//  ПараметрыЗадания - Структура - ключевые параметры задания. Свойства:
//   * Метаданные - ОбъектМетаданныхРегламентноеЗадание - Регламентное задание
//   * Ключ - Строка - Ключ
//   * Использование - Булево - Использование задания
//  Наименование - Строка - Используется только, если Создавать = Истина
//  Создавать - Булево - Создать регламентное задание, если такое не найдено и Использование = Истина
Процедура СоздатьИзменитьРегламентноеЗадание(ПараметрыЗадания, Наименование = "", Создавать = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыПоиска = Новый Структура("Метаданные, Ключ", Неопределено, "");
	ЗаполнитьЗначенияСвойств(ПараметрыПоиска, ПараметрыЗадания);
	НайденныеРегламентныеЗадания = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыПоиска);
	КоличествоНайденныхЗаданий = НайденныеРегламентныеЗадания.Количество();
	
	Если КоличествоНайденныхЗаданий = 0 И ПараметрыЗадания.Использование И Создавать Тогда
		
		Если Не ОбщегоНазначения.РазделениеВключено() Тогда
			Если ПустаяСтрока(Наименование) Тогда
				ПараметрыЗадания.Вставить("Наименование",
					ИнтеграцияСМаркетплейсамиКлиентСервер.СформироватьНаименованиеРегламентногоЗадания(
						ПараметрыЗадания.Метаданные.Синоним, ПараметрыЗадания.Метаданные.ИмяМетода));
			Иначе
				ПараметрыЗадания.Вставить("Наименование", Наименование);
			КонецЕсли;
		КонецЕсли;
		
		РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
		
	ИначеЕсли НайденныеРегламентныеЗадания.Количество() > 0 Тогда
		
		Для Каждого НайденноеРегламентноеЗадания Из НайденныеРегламентныеЗадания Цикл
			Если НайденноеРегламентноеЗадания.Использование <> ПараметрыЗадания.Использование Тогда
				РегламентныеЗаданияСервер.ИзменитьЗадание(НайденноеРегламентноеЗадания.УникальныйИдентификатор, ПараметрыЗадания);
			КонецЕсли;
			Если ПараметрыЗадания.Использование Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Выполняет загрузку информации о продажах товаров через торговую площадку. Используется регламентным заданием.
//
// Параметры:
//   УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//
Процедура ЗагрузитьИнформациюОПродажахТоваровЧерезТорговуюПлощадку(УчетнаяЗаписьМаркетплейса) Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ЗагрузкаИнформацииОПродажахТоваровЧерезТорговуюПлощадку);

	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗаписьМаркетплейса, "ВидМаркетплейса");
	
	Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		ИнтеграцияСМаркетплейсомOzonСервер.ЗагрузитьИнформациюОПродажахТоваровРегламентнымЗаданием(УчетнаяЗаписьМаркетплейса);
		
	ИначеЕсли ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет") Тогда
		ИнтеграцияСЯндексМаркетСервер.ЗагрузитьИнформациюОПродажахТоваровРегламентнымЗаданием(УчетнаяЗаписьМаркетплейса);
	КонецЕсли;

КонецПроцедуры

// Выполняет обновление информации о продажах товаров через торговую площадку. Используется регламентным заданием.
//
// Параметры:
//   УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//
Процедура ОбновитьИнформациюОПродажахТоваровЧерезТорговыеПлощадки(УчетнаяЗаписьМаркетплейса) Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ОбновлениеИнформацииОПродажахТоваровЧерезСкладыOzon);

	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗаписьМаркетплейса, "ВидМаркетплейса");
	
	Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		ИнтеграцияСМаркетплейсомOzonСервер.ОбновитьИнформациюОПродажахТоваровРегламентнымЗаданием(УчетнаяЗаписьМаркетплейса);
	КонецЕсли;

КонецПроцедуры

// Выполняет загрузку необработанных заказов (отправлений) с торговой площадки.
// Используется регламентным заданием.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//
Процедура ЗагрузитьНеобработанныеЗаказыСТорговойПлощадки(УчетнаяЗапись) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ЗагрузкаНеобработанныхЗаказовСТорговойПлощадки);
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	Если Не МожноВыполнитьРегламентноеЗадание(УчетнаяЗапись, ВидМаркетплейса) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяСобытия = СобытиеЖурналаРегистрации(ВидМаркетплейса);
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытия,
		УровеньЖурналаРегистрации.Информация,,,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Начата регламентная загрузка необработанных заказов с торговой площадки <%1>.';
				|en = 'Scheduled import of unprocessed orders from the <%1> marketplace has started.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УчетнаяЗапись));
	
	УстановитьПривилегированныйРежим(Истина);
	
	РегистрыСведений.ЗаказыТорговыхПлощадок.ЗагрузитьЗаказыТорговойПлощадки(УчетнаяЗапись);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытия,
		УровеньЖурналаРегистрации.Информация,,,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Завершена регламентная загрузка необработанных заказов с торговой площадки <%1>.';
				|en = 'Scheduled import of unprocessed orders from the <%1> marketplace is completed.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УчетнаяЗапись));
	
КонецПроцедуры

// Обновляет статусы заказов (отправлений) торговой площадки.
// Используется регламентным заданием.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//
Процедура ОбновитьСтатусыЗаказовТорговойПлощадки(УчетнаяЗапись) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ОбновлениеСтатусовЗаказовТорговойПлощадки);
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	Если Не МожноВыполнитьРегламентноеЗадание(УчетнаяЗапись, ВидМаркетплейса) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяСобытия = СобытиеЖурналаРегистрации(ВидМаркетплейса);
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытия,
		УровеньЖурналаРегистрации.Информация,,,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Начато регламентное обновление статусов заказов торговой площадки <%1>.';
				|en = 'Scheduled update of <%1> marketplace order statuses is started.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УчетнаяЗапись));
	
	УстановитьПривилегированныйРежим(Истина);
	
	Ошибка = РегистрыСведений.ЗаказыТорговыхПлощадок.ОбновитьСтатусыЗаказовТорговойПлощадки(УчетнаяЗапись);
	
	Если ПустаяСтрока(Ошибка.КодОшибки) Тогда
		ТекстЗаписи = НСтр("ru = 'Завершено регламентное обновление статусов заказов торговой площадки <%1>.';
							|en = 'Scheduled update of <%1> marketplace order statuses is completed.'",
			ОбщегоНазначения.КодОсновногоЯзыка());
	Иначе
		ТекстЗаписи = НСтр("ru = 'Регламентное обновление статусов заказов торговой площадки <%1> завершено с ошибками.';
							|en = 'Scheduled update of <%1> marketplace order statuses is completed with errors.'",
			ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытия,
		УровеньЖурналаРегистрации.Информация,,,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстЗаписи,
			УчетнаяЗапись));
	
КонецПроцедуры

// Получает этикетки для сформированных отправлений с торговой площадки. 
// Используется регламентным заданием.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//
Процедура ПолучитьЭтикеткиДляЗаказовТорговойПлощадки(УчетнаяЗапись) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ПолучениеЭтикетокДляЗаказовТорговойПлощадки);
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьЭтикеткиОтправленийРегламентнымЗаданием(УчетнаяЗапись);
	КонецЕсли;
	
КонецПроцедуры

// Выгружает остатки товаров на маркетплейсы.
// 
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись
//   ДополнительныеПараметры - Неопределено, Структура - см. ДополнительныеПараметрыРегламентногоЗадания
Процедура ВыгрузитьОстаткиНаМаркетплейсы(УчетнаяЗапись, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ВыгрузкаОстатковНаМаркетплейсы);
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ИнтеграцияСМаркетплейсомOzonСервер.ВыгрузитьОстаткиТоваровРегламентнымЗаданием(УчетнаяЗапись);
		
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		Результат = ИнтеграцияСЯндексМаркетСервер.ВыгрузитьОстаткиТоваровРегламентнымЗаданием(УчетнаяЗапись, ДополнительныеПараметры);
		Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
			И ЗначениеЗаполнено(ДополнительныеПараметры.АдресРезультата) Тогда
			
			ПоместитьВоВременноеХранилище(Результат, ДополнительныеПараметры.АдресРезультата);
		КонецЕсли;
		
	ИначеЕсли ИнтеграцияСМаркетплейсамиСИ.ЭтоМаркетплейсСИ(ВидМаркетплейса) Тогда
		Если ДополнительныеПараметры = Неопределено Тогда
			ДополнительныеПараметры = Новый Структура;
		КонецЕсли;
		ДополнительныеПараметры.Вставить("ВидМаркетплейса", ВидМаркетплейса);
		
		ИнтеграцияСМаркетплейсамиСИ.ВыгрузитьОстаткиТоваровРегламентнымЗаданием(УчетнаяЗапись, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

// Выгружает цены на маркетплейсы.
// 
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись
//   ДополнительныеПараметры - Неопределено, Структура - Возможные ключи дополнительных параметров:
//    * ПоРасписанию - Булево - признак автоматического или ручного запуска регламентного задания.
//    * АдресРезультата - Строка - Адрес временного хранилища, в которое необходимо положить результат выполнения функции.
//    * ТаблицаТоваров        - ТаблицаЗначений - список товаров:
//      ** УчетнаяЗапись        - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису;
//      ** Номенклатура         - СправочникСсылка.Номенклатура - номенклатура;
//      ** Характеристика       - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика;
//      ** Упаковка             - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка.
Процедура ВыгрузитьЦеныНаМаркетплейсы(УчетнаяЗапись, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ВыгрузкаЦенНаМаркетплейсы);
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ИнтеграцияСМаркетплейсомOzonСервер.ВыгрузитьЦеныТоваровРегламентнымЗаданием(УчетнаяЗапись);
		
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		Результат = ИнтеграцияСЯндексМаркетСервер.ВыгрузитьЦеныТоваровРегламентнымЗаданием(УчетнаяЗапись, ДополнительныеПараметры);
		Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
			И ЗначениеЗаполнено(ДополнительныеПараметры.АдресРезультата) Тогда
			
			ПоместитьВоВременноеХранилище(Результат, ДополнительныеПараметры.АдресРезультата);
		КонецЕсли;
		
	ИначеЕсли ИнтеграцияСМаркетплейсамиСИ.ЭтоМаркетплейсСИ(ВидМаркетплейса) Тогда
		Если ДополнительныеПараметры = Неопределено Тогда
			ДополнительныеПараметры = Новый Структура;
		КонецЕсли;
		ДополнительныеПараметры.Вставить("ВидМаркетплейса", ВидМаркетплейса);
		ИнтеграцияСМаркетплейсамиСИ.ВыгрузитьЦеныТоваровРегламентнымЗаданием(УчетнаяЗапись, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

// Измененяет экземпляр регламентного задания.
// 
// Параметры:
//  Параметры - См. ПараметрыИзмененияЭкземпляраРегламентногоЗадания
//  ДополнительныеПараметры - Неопределено, Структура - Возможные ключи дополнительных параметров:
//    * ПоРасписанию - Булево - Признак автоматического или ручного запуска регламентного задания.
Процедура ИзменитьЭкземплярРегламентногоЗадания(Параметры, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ТипЗнч(Параметры.УчетнаяЗапись) <> Тип("СправочникСсылка.УчетныеЗаписиМаркетплейсов") Тогда
		Возврат;
	КонецЕсли;
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.УчетнаяЗапись, "ВидМаркетплейса");
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		
		ПараметрыПоискаИсточника = Новый Структура;
		ПараметрыПоискаИсточника.Вставить("Метаданные", Параметры.МетаданныеДо);
		ПараметрыПоискаИсточника.Вставить("Ключ", Параметры.Ключ);
		
		ПараметрыПоискаПриемника = Новый Структура;
		ПараметрыПоискаПриемника.Вставить("Метаданные", Параметры.МетаданныеПосле);
		ПараметрыПоискаПриемника.Вставить("Ключ", Параметры.Ключ);
		
		НайденныеЗаданияИсточника = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыПоискаИсточника);
		НайденныеЗаданияПриемника = РегламентныеЗаданияСервер.НайтиЗадания(ПараметрыПоискаПриемника);
		
		Если НайденныеЗаданияИсточника.Количество() > 0 И НайденныеЗаданияПриемника.Количество() = 0 Тогда
			
			Задание = НайденныеЗаданияИсточника[0];
			
			ПараметрыЗадания = Новый Структура;
			ПараметрыЗадания.Вставить("ИмяПользователя", Задание.ИмяПользователя);
			ПараметрыЗадания.Вставить("ИнтервалПовтораПриАварийномЗавершении", Задание.ИнтервалПовтораПриАварийномЗавершении);
			ПараметрыЗадания.Вставить("Использование", Задание.Использование);
			ПараметрыЗадания.Вставить("Ключ", Задание.Ключ);
			ПараметрыЗадания.Вставить("КоличествоПовторовПриАварийномЗавершении", Задание.КоличествоПовторовПриАварийномЗавершении);
			ПараметрыЗадания.Вставить("Метаданные", Параметры.МетаданныеПосле);
			ПараметрыЗадания.Вставить("Наименование", Задание.Наименование);
			ПараметрыЗадания.Вставить("ПоследнееЗадание", Задание.ПоследнееЗадание);
			ПараметрыЗадания.Вставить("Предопределенное", Задание.Предопределенное);
			ПараметрыЗадания.Вставить("РазделениеДанных", Задание.РазделениеДанных);
			ПараметрыЗадания.Вставить("Расписание", Задание.Расписание);
			ПараметрыЗадания.Вставить("Параметры", Новый Массив);
			ПараметрыЗадания.Параметры.Добавить(Параметры.УчетнаяЗапись);
			ПараметрыЗадания.Параметры.Добавить(ДополнительныеПараметры);
			
			НачатьТранзакцию();
			Попытка
				РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
				РегламентныеЗаданияСервер.УдалитьЗадание(Задание.УникальныйИдентификатор);
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(Параметры.УчетнаяЗапись),,,, ТекстОшибки);
			КонецПопытки;
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры изменения экземпляра регламентного задания.
// 
// Возвращаемое значение:
//  Структура - Параметры изменения экземпляра регламентного задания:
// * МетаданныеДо - ОбъектМетаданных, Неопределено - Метаданные существующего экземпляра регламентного задания,
// * МетаданныеПосле - ОбъектМетаданных, Неопределено - Метаданные нового экземпляра регламентного задания,
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись маркетплейса,
// * Ключ - Строка - Ключ регламентного задания.
Функция ПараметрыИзмененияЭкземпляраРегламентногоЗадания() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("МетаданныеДо", Неопределено);
	Параметры.Вставить("МетаданныеПосле", Неопределено);
	Параметры.Вставить("УчетнаяЗапись", Справочники.УчетныеЗаписиМаркетплейсов.ПустаяСсылка());
	Параметры.Вставить("Ключ", "");
	
	Возврат Параметры;
	
КонецФункции

// Дополнительные параметры регламентного задания.
// 
// Возвращаемое значение:
//  Структура - Дополнительные параметры регламентного задания:
// * ПоРасписанию - Булево - 
// * АдресРезультата - Строка - 
// * ТаблицаТоваров - Неопределено -
Функция ДополнительныеПараметрыРегламентногоЗадания() Экспорт
	Параметры = Новый Структура;
	
	Параметры.Вставить("ПоРасписанию", Ложь);
	Параметры.Вставить("АдресРезультата", "");
	Параметры.Вставить("ТаблицаТоваров", Неопределено);
	
	Возврат Параметры;
КонецФункции

#КонецОбласти

#Область РаботаСФормами

// Процедура, вызываемая для дополнения формы при вызове обработчика события формы.
//
// Параметры:
//  Форма                   - ФормаКлиентскогоПриложения - форма, из обработчика события которой происходит вызов процедуры.
//  Отказ                   - Булево - признак отказа от создания формы.
//  СтандартнаяОбработка    - Булево - признак выполнения стандартной (системной) обработки события
//  ДополнительныеПараметры - Структура - дополнительные параметры.
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка, ДополнительныеПараметры = Неопределено) Экспорт

	Если Не ИспользуетсяИнтеграцияСМаркетплейсами() Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементыОтслеживаемыеПриИзменении = Новый Массив;
	
	Если Форма.ИмяФормы = "Справочник.Номенклатура.Форма.ФормаЭлемента" Тогда
		ЭлементыФормы = Форма.Элементы;
		
		ЭлементыОтслеживаемыеПриИзменении.Добавить(ЭлементыФормы["Родитель"]);
		ЭлементыОтслеживаемыеПриИзменении.Добавить(ЭлементыФормы["ТоварнаяКатегория"]);
		
		Если Не ЗначениеЗаполнено(Форма.Объект["Ссылка"]) Тогда
			ВходящиеПараметры = Новый Структура;
			ВходящиеПараметры.Вставить("УчетнаяЗапись",   Справочники.УчетныеЗаписиМаркетплейсов.ПустаяСсылка());
			ВходящиеПараметры.Вставить("ВидНоменклатуры", Справочники.ВидыНоменклатуры.ПустаяСсылка());
			ВходящиеПараметры.Вставить("СопоставлениеНоменклатурыКонтрагентов", Неопределено);
			
			ЗаполнитьЗначенияСвойств(ВходящиеПараметры, Форма.Параметры);
			ЗаполнитьЗначенияСвойств(ВходящиеПараметры, Форма.ПараметрыСоздания);
			
			Если ЗначениеЗаполнено(ВходящиеПараметры.УчетнаяЗапись) Тогда
				Если ЗначениеЗаполнено(ВходящиеПараметры.СопоставлениеНоменклатурыКонтрагентов) Тогда
					НоменклатураКонтрагента = ВходящиеПараметры.СопоставлениеНоменклатурыКонтрагентов.НоменклатураКонтрагента;
				Иначе
					НоменклатураКонтрагента = Справочники.НоменклатураКонтрагентов.ПустаяСсылка();
				КонецЕсли;
				
				ИнтеграцияСМаркетплейсамиКлиентСервер.ЗаполнитьРеквизитыНоменклатурыПоКатегорииСервиса(
					ВходящиеПараметры.УчетнаяЗапись,
					Форма,
					НоменклатураКонтрагента);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Подключаемые процедуры должны присутствовать в форме.
	
	// При изменении
	Для Каждого Элемент Из ЭлементыОтслеживаемыеПриИзменении Цикл
		Если Элемент <> Неопределено Тогда
			Элемент.УстановитьДействие("ПриИзменении", "Подключаемый_ПриИзмененииЭлемента");
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Определяет следующие свойства регламентных заданий:
//  - зависимость от функциональных опций;
//  - возможность выполнения в различных режимах работы программы;
//  - прочие параметры.
//
// Параметры:
//   Настройки - см. РегламентныеЗаданияПереопределяемый.ПриОпределенииНастроекРегламентныхЗаданий.Настройки.
//
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	
	// Яндекс Маркет
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.УдалитьВыгрузкаОстатковТоваровЯндексМаркет;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСЯндексМаркет;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ВыгрузкаОстатковНаМаркетплейсы;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСЯндексМаркет;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.УдалитьВыгрузкаУстановленныхЦенЯндексМаркет;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСЯндексМаркет;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ВыгрузкаЦенНаМаркетплейсы;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСЯндексМаркет;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ЗагрузкаРекомендованныхЦенЯндексМаркет;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСЯндексМаркет;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ОтправкаНаМодерациюСвязейТоваровЯндексМаркет;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСЯндексМаркет;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ПолучениеРекомендацийПоСклейкеТовараЯндексМаркет;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСЯндексМаркет;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ПолучениеСтатусовМодерацииТоваровЯндексМаркет;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСЯндексМаркет;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	// Ozon
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ВыгрузкаОстатковНаМаркетплейсы;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСOzon;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ВыгрузкаЦенНаМаркетплейсы;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСOzon;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ЗагрузкаОстатковСМаркетплейсаOzon;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСOzon;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ЗагрузкаЦенСМаркетплейсаOzon;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСOzon;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ОбновлениеИнформацииОПродажахТоваровЧерезСкладыOzon;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСOzon;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ОбновлениеТоварногоКаталогаOzon;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСOzon;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	// Общие
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ЗагрузкаИнформацииОПродажахТоваровЧерезТорговуюПлощадку;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСOzon;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ЗагрузкаНеобработанныхЗаказовСТорговойПлощадки;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСOzon;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ОбновлениеСтатусовЗаказовТорговойПлощадки;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСOzon;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	// Wildberries
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ВыгрузкаОстатковНаМаркетплейсы;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСWildberries;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.СинхронизацияССервисомИнтеграции;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСWildberries;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ВыгрузкаЦенНаМаркетплейсы;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСWildberries;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	// Lamoda
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ВыгрузкаОстатковНаМаркетплейсы;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСLamoda;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.СинхронизацияССервисомИнтеграции;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСLamoda;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
	Настройка                                         = Настройки.Добавить();
	Настройка.РегламентноеЗадание                     = Метаданные.РегламентныеЗадания.ВыгрузкаЦенНаМаркетплейсы;
	Настройка.ФункциональнаяОпция                     = Метаданные.ФункциональныеОпции.ИспользоватьИнтеграциюСLamoda;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Истина;
	Настройка.ДоступноВМоделиСервиса                  = Истина;
	Настройка.РаботаетСВнешнимиРесурсами              = Истина;
	Настройка.Параметризуется                         = Истина;
	
КонецПроцедуры

// см. ОбщегоНазначенияПереопределяемый.ПриДобавленииСерверныхОповещений
Процедура ПриДобавленииСерверныхОповещений(Оповещения) Экспорт
	
	Оповещение                    = СерверныеОповещения.НовоеСерверноеОповещение("ИнтеграцияСЯндексМаркетСервер.ОбновитьДанные");
	Оповещение.ИмяМодуляПолучения = "ИнтеграцияСМаркетплейсамиКлиент";
	Оповещение.ПериодПроверки     = 60;
	Оповещения.Вставить(Оповещение.Имя, Оповещение);
	
	Оповещение                    = СерверныеОповещения.НовоеСерверноеОповещение("ИнтеграцияСМаркетплейсомOzonСервер.ОбновитьДанные");
	Оповещение.ИмяМодуляПолучения = "ИнтеграцияСМаркетплейсамиКлиент";
	Оповещение.ПериодПроверки     = 60;
	Оповещения.Вставить(Оповещение.Имя, Оповещение);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ЭлектронноеВзаимодействие

// Размещает команды ЭДО на форме.
//
// Параметры:
//   ПараметрыРазмещенияКоманд - см. ПодключаемыеКомандыЭДО.ПараметрыРазместитьНаФормеКомандыЭДО.
//
Процедура РазместитьНаФормеДополнительныеКомандыЭДО(ПараметрыРазмещенияКоманд) Экспорт
	
	Форма = ПараметрыРазмещенияКоманд.Форма;
	АдресКомандЭДОВоВременномХранилище = "АдресКомандЭДОВоВременномХранилище";
	
	КомандаФормы = Форма.Команды.Найти(АдресКомандЭДОВоВременномХранилище);
	Если КомандаФормы <> Неопределено Тогда
		Если ТипЗнч(Форма) = Тип("ФормаКлиентскогоПриложения") Тогда
			ИмяФормы = Форма.ИмяФормы;
		Иначе
			ИмяФормы = Форма;
		КонецЕсли;
		
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяФормы);
		Если ОбъектМетаданных <> Неопределено 
				И Не Метаданные.ОбщиеФормы.Содержит(ОбъектМетаданных) Тогда
			ОбъектМетаданных = ОбъектМетаданных.Родитель();
		КонецЕсли;
		
		СоставКоманд = Новый Структура;
		СоставКоманд.Вставить("Исходящие", Новый Массив);
		СоставКоманд.Вставить("Входящие",  Новый Массив);
		
		НаправлениеЭД = ПараметрыРазмещенияКоманд.Направление;
		КомандыЭДО    = ПолучитьИзВременногоХранилища(КомандаФормы.Действие);
		
		НовыеКомандыЭДО = КомандыЭДО.Скопировать();
		НовыеКомандыЭДО.Очистить();
		ПриОпределенииСпискаКомандЭДО(СоставКоманд, ОбъектМетаданных.ПолноеИмя(), НаправлениеЭД, НовыеКомандыЭДО);
		
		// Размещение команд на форме
		МестоРазмещенияКомандПоУмолчанию = ПараметрыРазмещенияКоманд.МестоРазмещенияКоманд;
		
		ТаблицаКоманд = НовыеКомандыЭДО.Скопировать(, "МестоРазмещения");
		ТаблицаКоманд.Свернуть("МестоРазмещения");
		МестаРазмещения = ТаблицаКоманд.ВыгрузитьКолонку("МестоРазмещения");
	
		Для Каждого МестоРазмещения Из МестаРазмещения Цикл
			НайденныеКоманды = НовыеКомандыЭДО.НайтиСтроки(
				Новый Структура("МестоРазмещения, СкрытаФункциональнымиОпциями, Отключена", МестоРазмещения, Ложь, Ложь));
			ЭлементФормыДляРазмещения = Форма.Элементы.Найти(МестоРазмещения); // Массив из СтрокаТаблицыЗначений
			Если ЭлементФормыДляРазмещения = Неопределено Тогда
				ЭлементФормыДляРазмещения = МестоРазмещенияКомандПоУмолчанию;
			КонецЕсли;

			Если НайденныеКоманды.Количество() > 0 Тогда
				ДобавитьКомандыЭДО(Форма, НайденныеКоманды, ЭлементФормыДляРазмещения,
					ПараметрыРазмещенияКоманд.ИсточникКомандЭДО);
			КонецЕсли;
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(НовыеКомандыЭДО, КомандыЭДО);
		КомандаФормы.Действие = ПоместитьВоВременноеХранилище(КомандыЭДО, Форма.УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

// Дополняет список команд ЭДО командами других подсистем.
//
// Параметры:
//   СоставКоманд  - Структура - состав команд ЭДО:
//     * Исходящие    - Массив из Строка - объекты для команд работы с исходящими документами.
//     * Входящие     - Массив из Строка - объекты для команд работы со входящими документами.
//     * БезПодписи   - Массив из Строка - объекты для команд выполнения ЭДО без подписи.
//     * Внутренние   - Массив из Строка - объекты для команд внутреннего ЭДО.
//     * Интеркампани - Массив из Строка - объекты для команд обмена между организациями в рамках одной базы.
//     * Контрагенты  - Массив из Строка - объекты для команд работы с контрагентами.
//     * Организации  - Массив из Строка - объекты для команд работы с организациями.
//     * Договоры     - Массив из Строка - объекты для команд работы с договорами.
//   ПолноеИмя     - Строка - имя объекта, например "Документ.РеализацияТоваровУслуг".
//   НаправлениеЭД - ПеречислениеСсылка.НаправленияЭДО - параметр отбора входящих или исходящих документов.
//   КомандыЭДО    - ТаблицаЗначений - подготавливаемый список команд ЭДО.
//
Процедура ПриОпределенииСпискаКомандЭДО(Знач СоставКоманд, Знач ПолноеИмя, Знач НаправлениеЭД, КомандыЭДО) Экспорт
	
	ИспользоватьОбменЭД  = ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭД");
	ЕстьПравоОбработкиЭД = ОбменСКонтрагентами.ЕстьПравоОбработкиЭД();
	ЕстьПравоОбменаЭД    = ОбменСКонтрагентами.ЕстьПравоВыполненияОбмена();
	
	СоставКоманд = Новый Структура;
	СоставКоманд.Вставить("Исходящие", Новый Массив);
	СоставКоманд.Вставить("Входящие",  Новый Массив);

	ПриОпределенииСоставаКомандЭДО(СоставКоманд);
	
	Если СоставКоманд.Входящие.Найти(ПолноеИмя) <> Неопределено 
			И НаправлениеЭД <> Перечисления.НаправленияЭДО.Исходящий Тогда 
		Идентификатор = "СверитьДокументСТорговойПлощадкой";
		
		Если ИспользоватьОбменЭД
				И ЕстьПравоОбменаЭД
				И КомандыЭДО.Найти(Идентификатор, "Идентификатор") = Неопределено Тогда
			КомандаЭДО = КомандыЭДО.Добавить();
			КомандаЭДО.Обработчик                  = "ИнтеграцияСМаркетплейсамиКлиент.СверитьДокументСТорговойПлощадкой";
			КомандаЭДО.СписокФорм                  = "";
			КомандаЭДО.Идентификатор               = Идентификатор;
			КомандаЭДО.Представление               = НСтр("ru = 'Сверить документ с получателем';
															|en = 'Reconcile the document with the recipient'");
			КомандаЭДО.Порядок                     = 100;
			КомандаЭДО.Картинка                    = БиблиотекаКартинок.ЗагрузкаИзВнешнегоИсточника;
			КомандаЭДО.РежимИспользованияПараметра = РежимИспользованияПараметраКоманды.Одиночный;
			КомандаЭДО.МестоРазмещения             = "КомандыЭДОСервис";
			КомандаЭДО.ТолькоВоВсехДействиях       = Ложь;
			КомандаЭДО.Недоступна                  = Не ЕстьПравоОбработкиЭД;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Определяет параметры прикладного электронного документа по данным файла.
//
// Параметры:
//   ДвоичныеДанные      - ДвоичныеДанные - данные файла прикладного электронного документа.
//   РасширениеФайла     - Строка - расширение файла без точки.
//   Параметры           - см. НовыйПрикладнойТипЭлектронногоДокумента.
//   ВидТорговойПлощадки - Неопределено, ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//   ТекстОшибки         - Строка - представление ошибки.
//
Процедура ПриОпределенииПараметровФайлаПрикладногоЭлектронногоДокумента(Знач ДвоичныеДанные, Знач РасширениеФайла, 
			Параметры, ВидТорговойПлощадки = Неопределено, ТекстОшибки = "") Экспорт
	
	РасширениеФайла = НРег(СтрЗаменить(РасширениеФайла, ".", ""));
	
	Если НРег(РасширениеФайла) <> "xls" 
			И НРег(РасширениеФайла) <> "xlsx" 
			И НРег(РасширениеФайла) <> "ods" Тогда
		ТекстОшибки = НСтр("ru = 'Некорректный формат файла. Выберите файл с расширением ""xml"" или ""xls"".';
							|en = 'Incorrect file format. Select a file with ""xml"" or ""xls"" extension.'");
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = ПрочитатьТабличныйДокумент(ДвоичныеДанные, РасширениеФайла, ТекстОшибки);
	Если ДанныеФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ЕстьМаркировка",      Ложь);
	Параметры.Вставить("ВидТорговойПлощадки", Неопределено);
	Параметры.Вставить("Представление",       Новый ТабличныйДокумент);
	Параметры.Вставить("ДанныеРазбора",       Новый ТаблицаЗначений);
	
	Если ДанныеФайла.Области.Количество() > 1 Тогда
		ТабличныйДокумент = Новый ТабличныйДокумент;
		
		Для Каждого Лист Из ДанныеФайла.Области Цикл
			Область = ТабличныйДокумент.Область(ТабличныйДокумент.ВысотаТаблицы + 1 
				+ ?(ТабличныйДокумент.ВысотаТаблицы > 0, 1, 0), 1);
			Область.Текст                 = СтрЗаменить(СтрЗаменить(Лист.Имя, "__", ", "), "_", " ");
			Область.Шрифт                 = ШрифтыСтиля.ВажнаяНадписьШрифт;
			Область.ВертикальноеПоложение = ВертикальноеПоложение.Верх;
			Область.ВысотаСтроки          = 15;
			
			ТабличныйДокумент.Вывести(ДанныеФайла.ПолучитьОбласть(Лист.Имя));
		КонецЦикла;
		
		Параметры.Вставить("Представление", ТабличныйДокумент);
		
	Иначе
		Параметры.Вставить("Представление", ДанныеФайла);
	КонецЕсли;
	
	// Акт приемки товаров
	Если Не ЗначениеЗаполнено(ВидТорговойПлощадки)
			Или ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		Обработки.УправлениеПродажамиНаOzon.ПрочитатьАктСверки(ДанныеФайла, Параметры);
		Если ЗначениеЗаполнено(Параметры.ВидТорговойПлощадки) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидТорговойПлощадки)
			Или ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		Обработки.УправлениеПродажамиНаЯндексМаркет.ПрочитатьАктСверки(ДанныеФайла, Параметры);
		Если ЗначениеЗаполнено(Параметры.ВидТорговойПлощадки) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидТорговойПлощадки)
			Или ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсWildberries Тогда
		Обработки.УправлениеПродажамиНаМаркетплейсах.ПрочитатьАктСверки(ДанныеФайла, Параметры);
		Если ЗначениеЗаполнено(Параметры.ВидТорговойПлощадки) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Параметры.ТипЭлектронногоДокумента = Справочники.ИдентификаторыОбъектовМетаданных.ПустаяСсылка();
	ТекстОшибки = НСтр("ru = 'Содержимое выбранного файла не соответствует требуемому формату.';
						|en = 'The content of the selected file does not match the required format.'");
	
КонецПроцедуры

// Формирование представления прикладного электронного документа по данным файла.
//
// Параметры:
//   ТабличныйДокумент   - ТабличныйДокумент - визуализация прикладного электронного документа.
//   ДвоичныеДанные      - ДвоичныеДанные - данные файла прикладного электронного документа.
//   ПараметрыЗаполнения - Структура из КлючИЗначение - дополнительные параметры для формирования табличного документа.
//   ТекстОшибки         - Строка - представление ошибки.
//
Процедура ПриЗаполненииТабличногоДокументаИзФайлаПрикладногоЭлектронногоДокумента(ТабличныйДокумент, Знач ДвоичныеДанные, 
			ПараметрыЗаполнения, ТекстОшибки = "") Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ДанныеФайла       = ПрочитатьТабличныйДокумент(ДвоичныеДанные,, ТекстОшибки);
	
	Если ДанныеФайла <> Неопределено Тогда
		Если ДанныеФайла.Области.Количество() > 1 Тогда
			Для Каждого Лист Из ДанныеФайла.Области Цикл
				Область = ТабличныйДокумент.Область(ТабличныйДокумент.ВысотаТаблицы + 1 
					+ ?(ТабличныйДокумент.ВысотаТаблицы > 0, 1, 0), 1);
				Область.Текст                 = СтрЗаменить(СтрЗаменить(Лист.Имя, "__", ", "), "_", " ");
				Область.Шрифт                 = ШрифтыСтиля.ВажнаяНадписьШрифт;
				Область.ВертикальноеПоложение = ВертикальноеПоложение.Верх;
				Область.ВысотаСтроки          = 15;
				
				ТабличныйДокумент.Вывести(ДанныеФайла.ПолучитьОбласть(Лист.Имя));
			КонецЦикла;
			
		Иначе
			ТабличныйДокумент = ДанныеФайла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Выполняется при отражении в учете электронного документа, содержащего информацию для сопоставления номенклатуры.
// Позволяет определить необходимость сопоставления номенклатуры для указанного способа обработки электронных документов,
// а также отказаться от отражения в учете, если сопоставление не выполнено.
//
// Параметры:
//   Документ - Структура - данные электронного документа (только чтение):
//     * СпособОбработки - Строка    - способ отражения в учете электронного документа.
//                       - Структура - способ отражения в учете УПД (расширенный):
//       ** ПервичныйДокумент - Строка - способ отражения в учете первичного документа.
//       ** СчетФактура       - Строка - способ отражения в учете счет-фактуры.
//  Настройки - Структура - настройки сопоставления номенклатуры (для изменения):
//   * ОтражатьТолькоСопоставленные - Булево - признак разрешения отражать в учете только документы с сопоставленной 
//                                      номенклатурой.
//
Процедура ПриОтраженииВУчетеЭлектронногоДокументаСНоменклатурой(Знач Документ, Настройки) Экспорт
	
	СпособОбработки = ?(ТипЗнч(Документ.СпособОбработки) = Тип("Структура"),
		Документ.СпособОбработки.ПервичныйДокумент,
		Документ.СпособОбработки);
		
	Если Строка(СпособОбработки) = "СверкаДокументаСТорговойПлощадкой" Тогда
		Настройки.ОтражатьТолькоСопоставленные = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Получает описание сообщения по двоичным данным.
//
// Параметры:
//   ОписаниеФайла - см. РаботаСФайламиБЭД.НовоеОписаниеФайла.
//   ТекстОшибки   - Строка - представление ошибки.
//
// Возвращаемое значение:
//   - Неопределено - 
//   - Структура - дополненная структура ФорматыЭДО.ПрочитатьСодержаниеДокумента:
//       * ИдентификаторДокумента       - Строка
//       * ТипДокумента                 - ПеречислениеСсылка.ТипыДокументовЭДО
//       * ПрикладнойТипДокумента       - Неопределено
//       * ТипРегламента                - ПеречислениеСсылка.ТипыРегламентовЭДО
//       * НомерДокумента               - Строка
//       * ДатаДокумента                - Дата
//       * СуммаДокумента               - Число
//       * ВалютаДокумента              - Строка 
//       * СтавкаНДСДокумента           - Строка
//       * СуммаНДСДокумента            - Число
//       * ЕстьМаркировка               - Булево
//       * Отправитель                  - см. ФорматыЭДО.НовоеОписаниеУчастникаЭДО
//       * Получатель                   - см. ФорматыЭДО.НовоеОписаниеУчастникаЭДО
//       * Формат                       - Строка
//       * ОтражениеВУчете              - Неопределено
//       * СвойстваДокументовПоФорматам - см. ФорматыЭДО.НовоеОписаниеСвойствДокументовПоФорматам
//       * ДанныеРазбора                - Неопределено, Строка - адрес временного хранилища. 
//
Функция СодержаниеЭлектронногоДокумента(Знач ОписаниеФайла, ТекстОшибки = "") Экспорт
	
	Файл            = Новый Файл(ОписаниеФайла.ИмяФайла);
	РасширениеФайла = НРег(СтрЗаменить(Файл.Расширение, ".", ""));
	
	Если РасширениеФайла = "xls" 
			Или РасширениеФайла = "xlsx" 
			Или РасширениеФайла = "ods" Тогда
		Параметры = НовыйПрикладнойТипЭлектронногоДокумента();
		ПриОпределенииПараметровФайлаПрикладногоЭлектронногоДокумента(ОписаниеФайла.ДвоичныеДанные, 
			РасширениеФайла, 
			Параметры,
			,
			ТекстОшибки);
		
		Если Параметры.ТипЭлектронногоДокумента = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Результат = ЭлектронныеДокументыЭДО.НовоеСодержаниеСообщения();
		Результат.Вставить("ЕстьМаркировка",      Параметры.ЕстьМаркировка);
		Результат.Вставить("ВидТорговойПлощадки", Параметры.ВидТорговойПлощадки);
		Результат.Вставить("Представление",       Параметры.Представление);
		Результат.Вставить("ДанныеРазбора",       ПоместитьВоВременноеХранилище(Параметры.ДанныеРазбора, 
													Новый УникальныйИдентификатор));
		
		Результат.ТипДокумента           = Перечисления.ТипыДокументовЭДО.Прикладной;
		Результат.ПрикладнойТипДокумента = Параметры.ТипЭлектронногоДокумента;
		Результат.ТипРегламента          = Перечисления.ТипыРегламентовЭДО.Неформализованный;
		Результат.Формат                 = ОбменСКонтрагентами.ФорматыДокументов().Прикладной;
		Результат.НомерДокумента         = Параметры.НомерДокументаОтправителя;
		Результат.ДатаДокумента          = Параметры.ДатаДокументаОтправителя;
		Результат.СуммаДокумента         = Параметры.СуммаДокумента;
		Результат.Отправитель            = Параметры.Отправитель;
		Результат.Получатель             = Параметры.Получатель;
		
	Иначе
		Результат = ЭлектронныеДокументыЭДО.СодержаниеСообщения(ОписаниеФайла);
		
		Если ЗначениеЗаполнено(Результат) Тогда
			Результат.Вставить("ДанныеРазбора", Неопределено);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Определяет предполагаемый вид торговой площадки.
//
// Параметры:
//   ДанныеЭлектронногоДокумента - см. ОтражениеВУчетеЭДО.НовыеДанныеЭлектронногоДокументаДляОтраженияВУчете.
//   ТекстОшибки                 - Строка - представление ошибки.
// 
// Возвращаемое значение:
//   ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//
Функция ОпределитьВидТорговойПлощадкиПоЭлектронномуДокументу(ДанныеЭлектронногоДокумента, ТекстОшибки = "") Экспорт
	
	Попытка
		ДанныеРазбора = ПолучитьДанныеЭлектронногоДокумента(ДанныеЭлектронногоДокумента, ТекстОшибки);
		
		Если ЗначениеЗаполнено(ДанныеРазбора) Тогда
			СтрокиТребующиеСопоставления             = СтрокиТребующиеСопоставления(ДанныеРазбора.НовыйЭД, Истина, ТекстОшибки);
			ТоварыПодчиненыОднойКорневойСтрокеДерева = ТоварыПодчиненыОднойКорневойСтрокеДерева(СтрокиТребующиеСопоставления);
			
			Если ЗначениеЗаполнено(СтрокиТребующиеСопоставления) 
					И ТоварыПодчиненыОднойКорневойСтрокеДерева Тогда
				Константа_Идентификатор = ИнтеграцияСМаркетплейсомOzonКлиентСервер.КонстантыСервиса()["УПД_ИдентификаторОбъекта"];	//@NON-NLS-1
				Константа_Штрихкод      = ИнтеграцияСЯндексМаркетКлиентСервер.КонстантыСервиса()["УПД_Штрихкод"];	//@NON-NLS-1
						
				Для Каждого КлючИЗначение Из СтрокиТребующиеСопоставления Цикл
					ПутьКДопДанным       = КлючИЗначение.Ключ.ПолныйПуть + ".ДопДанныеПодписанные";
					ДопДанныеПодписанные = КлючИЗначение.Ключ.Строки.Найти(ПутьКДопДанным, "ПолныйПуть", Истина);
					
					Если ЗначениеЗаполнено(ДопДанныеПодписанные.Значение) Тогда
						ПутьКДопДанным = ПутьКДопДанным + ".НомерСтроки";
						Для Каждого СтрокаДереваЗначений Из ДопДанныеПодписанные.Строки Цикл
							Имя = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаДереваЗначений, 
								ПутьКДопДанным + ".Идентификатор",);
							Значение = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаДереваЗначений, 
								ПутьКДопДанным + ".Значение");
							
							Если ЗначениеЗаполнено(Имя)
									И ЗначениеЗаполнено(Значение) Тогда
								Если ВРег(Имя) = Константа_Идентификатор Тогда
									Возврат Перечисления.ВидыМаркетплейсов.МаркетплейсOzon;
								ИначеЕсли ВРег(Имя) = Константа_Штрихкод Тогда
									Возврат Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет;
								КонецЕсли;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
					
					Прервать;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	Исключение
		ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Возврат Перечисления.ВидыМаркетплейсов.ПустаяСсылка();
	
КонецФункции

// Отражает данные электронного документа в учете.
//
// Параметры:
//   ПервичныйДокумент           - ДокументСсылка.ПередачаТоваровХранителю - первичный документ.
//   ДанныеЭлектронногоДокумента - см. ОтражениеВУчетеЭДО.НовыеДанныеЭлектронногоДокументаДляОтраженияВУчете.
//   СпособОбработки             - см. ОтражениеВУчетеЭДО.СпособОбработкиДокумента.
//   СопоставленнаяНоменклатура  - Соответствие из КлючИЗначение - сведения о сопоставленной номенклатуре:
//     * Ключ     - Строка - идентификатор номенклатуры контрагента.
//     * Значение - Структура - данные сопоставления:
//       ** НоменклатураИБ          - см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы.
//       ** НоменклатураКонтрагента - см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента.
//
// Возвращаемое значение:
//   - Неопределено,
//   - Структура - результат анализа расхождений:
//       * ТоварыИзФайла                - ТаблицаЗначений из см. Документ.ПриобретениеТоваровУслуг.Товары.
//       * ШтрихкодыУпаковокИзФайла     - см. ШтрихкодированиеИС.ШтрихкодыУпаковок.
//       * ТоварыИзДокумента            - ТаблицаЗначений из см. Документ.ПередачаТоваровХранителю.Товары.
//       * ШтрихкодыУпаковокИзДокумента - см. ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ.
//       * ТоварыРасхождения            - см. НоваяТаблицаТоварыРасхождения.
//       * ШтрихкодыУпаковокРасхождения - см. НоваяТаблицаШтрихкодыУпаковокРасхождения.
//       * ДанныеРазбора                - Неопределено,
//                                      - ТаблицаЗначений,
//                                      - ТабличныйДокумент,
//                                      - ДвоичныеДанные,
//                                      - Произвольный,
//                                      - Структура:
//         ** ДеревоРазбора               - см. ДеревоЭлектронногоДокументаБЭД.ИнициализироватьДеревоРазбора.
//         ** НовыйЭД                     - см. ДеревоЭлектронногоДокументаБЭД.НайтиСоздатьТипОбъектаВДеревеРазбора.
//         ** Содержание                  - Неопределено.
//
//@skip-check function-return-types
Функция ОтразитьДанныеОРасхожденияхЭлектронногоДокумента(Знач ПервичныйДокумент, ДанныеЭлектронногоДокумента, 
			Знач СпособОбработки = "", Знач СопоставленнаяНоменклатура = Неопределено) Экспорт
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ИнтеграцияСМаркетплейсамиСервер.ОтразитьДанныеОРасхожденияхЭлектронногоДокумента");
	
	ЭтоПрикладнойТипЭлектронногоДокумента = 
		ИнтеграцияЭДО.ПрикладныеТипыЭлектронныхДокументов().Найти(ДанныеЭлектронногоДокумента.ТипДокумента) <> Неопределено
			Или ДанныеЭлектронногоДокумента.ТипДокумента = Перечисления.ТипыДокументовЭДО.Прикладной;
	
	Если Не ЭтоПрикладнойТипЭлектронногоДокумента Тогда
		ПоддерживаемыеФорматы = ОбменСКонтрагентами.ФорматыДокументов();
	КонецЕсли;
		
	ОбъектыУчета  = Новый Массив;
	ДанныеРазбора = Неопределено;
	ТекстОшибки   = "";
	
	ШтрихкодыУпаковок = ЭлектронноеВзаимодействиеИСМП.НоваяТаблицаШтрихкодыУпаковок(Истина);
	ШтрихкодыУпаковок.Колонки.Добавить("ВидУпаковки", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыУпаковокИС"));
	
	// Чтение электронного документа
	Если ТипЗнч(ДанныеЭлектронногоДокумента.ДополнительныеДанные) = Тип("Структура")
			И ДанныеЭлектронногоДокумента.ДополнительныеДанные.Свойство("СпособОбработки")
			И ДанныеЭлектронногоДокумента.ДополнительныеДанные.СпособОбработки = "СверкаДокументаСТорговойПлощадкой" Тогда
		УчетнаяЗапись = ДанныеЭлектронногоДокумента.ДополнительныеДанные.УчетнаяЗапись;	// СправочникСсылка.УчетныеЗаписиМаркетплейсов
		ВидТорговойПлощадки = ДанныеЭлектронногоДокумента.ДополнительныеДанные.ВидТорговойПлощадки;
		Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
			ВидТорговойПлощадки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
		КонецЕсли;
		
		Если ЭтоПрикладнойТипЭлектронногоДокумента Тогда
			ИнформацияОТоварах = ИнформацияОТоварахИзТабличногоДокумента(ДанныеЭлектронногоДокумента, ТекстОшибки);
			ДанныеРазбора      = ДанныеЭлектронногоДокумента.ДополнительныеДанные.ДанныеРазбора;
			
			Если ИнформацияОТоварах = Неопределено Тогда
				Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Ошибка анализа расхождений по данным файла %1';
							|en = 'Error analyzing discrepancies in the %1 file'"), 
						ДанныеЭлектронногоДокумента.ДанныеОсновногоФайла.ИмяФайла);
				КонецЕсли;
					
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(ДанныеЭлектронногоДокумента.ДополнительныеДанные.СпособОбработки), 
					УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
					
				ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, 0);
				Возврат Неопределено;
				
			Иначе
				УстановитьПривилегированныйРежим(Истина);
				ПриобретениеТоваровУслуг = Документы.ПриобретениеТоваровУслуг.СоздатьДокумент();
				УстановитьПривилегированныйРежим(Ложь);
				
				Для Каждого ЭлементКоллекции Из ИнформацияОТоварах Цикл
					СтрокаТабличнойЧасти = ПриобретениеТоваровУслуг.Товары.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ЭлементКоллекции);
					
					ЭлементСопоставления = Неопределено;
					Если ЗначениеЗаполнено(СопоставленнаяНоменклатура)
							И ЗначениеЗаполнено(ЭлементКоллекции.ИД) Тогда
						ЭлементСопоставления = СопоставленнаяНоменклатура.Получить("Идентификатор_" + ЭлементКоллекции.ИД);
						Если ЗначениеЗаполнено(ЭлементСопоставления) Тогда
							ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ЭлементСопоставления.НоменклатураИБ);
							
							СтрокаТаблицыЗначений = ДанныеРазбора[ИнформацияОТоварах.Индекс(ЭлементКоллекции)];
							ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗначений, ЭлементСопоставления.НоменклатураИБ);
							СтрокаТаблицыЗначений.ВариантУказанияНоменклатуры = 
								ЭлементСопоставления.НоменклатураКонтрагента.ВариантУказанияНоменклатуры;
						КонецЕсли;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ЭлементКоллекции.КонтрольныеИдентификационныеЗнаки) Тогда
						СтрокаТабличнойЧасти = ПриобретениеТоваровУслуг.ШтрихкодыУпаковок.Добавить();
						СтрокаТабличнойЧасти.ЗначениеШтрихкода = ЭлементКоллекции.КонтрольныеИдентификационныеЗнаки;
						
						СтрокаТаблицыЗначений = ШтрихкодыУпаковок.Добавить();
						СтрокаТаблицыЗначений.ЗначениеШтрихкода = ЭлементКоллекции.КонтрольныеИдентификационныеЗнаки;
						Если ЗначениеЗаполнено(ЭлементСопоставления) Тогда
							ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗначений, ЭлементСопоставления.НоменклатураИБ);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
				СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(
					ПриобретениеТоваровУслуг);
				
				СтруктураДействий = Новый Структура;
				СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
				СтруктураДействий.Вставить("ПересчитатьСумму",     "КоличествоУпаковок");
				СтруктураДействий.Вставить("ПересчитатьСуммуНДС",  СтруктураПересчетаСуммы);
				СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
				СтруктураДействий.Вставить("ОчиститьСуммуВзаиморасчетов");
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
				
				КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
				ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьТЧ(ПриобретениеТоваровУслуг.Товары, 
					СтруктураДействий, 
					КэшированныеЗначения);
					
				ОбъектыУчета.Добавить(ПриобретениеТоваровУслуг);
			КонецЕсли;
			
		ИначеЕсли ЗначениеЗаполнено(СопоставленнаяНоменклатура)
					И ЗначениеЗаполнено(ВидТорговойПлощадки) Тогда
			ДанныеРазбора = ПолучитьДанныеЭлектронногоДокумента(ДанныеЭлектронногоДокумента, ТекстОшибки);
			
			Если Не ЗначениеЗаполнено(ДанныеРазбора) Тогда
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(ДанныеЭлектронногоДокумента.ДополнительныеДанные.СпособОбработки), 
					УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
					
				ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, 0);
				Возврат Неопределено;
			КонецЕсли;
			
			СтрокиТребующиеСопоставления = СтрокиТребующиеСопоставления(ДанныеРазбора.НовыйЭД, Ложь, ТекстОшибки);
			Если СтрокиТребующиеСопоставления = Неопределено Тогда
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(ДанныеЭлектронногоДокумента.ДополнительныеДанные.СпособОбработки), 
					УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
					
				ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, 0);
				Возврат Неопределено;
			КонецЕсли;
			
			Для Каждого КлючИЗначение Из СтрокиТребующиеСопоставления Цикл
				Идентификатор   = "";
				КодНоменклатуры = "";
				Штрихкод        = "";
				
				ОпределитьКлючевыеРеквизитыСопоставления(ВидТорговойПлощадки, КлючИЗначение.Ключ, 
					Идентификатор, КодНоменклатуры, Штрихкод);
				
				ЭлементСопоставления = Неопределено;
				
				Если ЗначениеЗаполнено(Идентификатор) Тогда
					КлючИЗначение.Значение.Вставить("Идентификатор", Идентификатор);
					
					ПутьКСопоставлению = КлючИЗначение.Ключ.ПолныйПуть + ".Сопоставление";
					ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(КлючИЗначение.Ключ, 
						ПутьКСопоставлению + ".Идентификатор", Идентификатор);
					
					Если Не ЗначениеЗаполнено(ЭлементСопоставления) Тогда
						ЭлементСопоставления = СопоставленнаяНоменклатура.Получить("Идентификатор_" + Идентификатор);
					КонецЕсли;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(КодНоменклатуры) Тогда
					КлючИЗначение.Значение.Вставить("КодНоменклатуры", КодНоменклатуры);
					
					Если ДанныеЭлектронногоДокумента.Формат = ПоддерживаемыеФорматы.ФНС.УПД.ИнформацияПродавца
							Или ДанныеЭлектронногоДокумента.Формат = ПоддерживаемыеФорматы.ФНС.УПД2019.ИнформацияПродавца Тогда
						ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(КлючИЗначение.Ключ, 
							КлючИЗначение.Ключ.ПолныйПуть + ".ТоварКод", КодНоменклатуры);
					ИначеЕсли ДанныеЭлектронногоДокумента.Формат = ПоддерживаемыеФорматы.ФНС.УПД_5_02.ИнформацияПродавца Тогда
						ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(КлючИЗначение.Ключ, 
							КлючИЗначение.Ключ.ПолныйПуть + ".Код", КодНоменклатуры);
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(ЭлементСопоставления) Тогда
						ЭлементСопоставления = СопоставленнаяНоменклатура.Получить("КодНоменклатуры_" + КодНоменклатуры);
					КонецЕсли;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Штрихкод)
						И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Штрихкод) Тогда
					КлючИЗначение.Значение.Вставить("Штрихкод", Штрихкод);
					
					ПутьКСопоставлению = КлючИЗначение.Ключ.ПолныйПуть + ".Сопоставление";
					ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(КлючИЗначение.Ключ, 
						ПутьКСопоставлению + ".Штрихкод", Штрихкод);
					
					Если Не ЗначениеЗаполнено(ЭлементСопоставления) Тогда
						ЭлементСопоставления = СопоставленнаяНоменклатура.Получить("Штрихкод_" + Штрихкод);
					КонецЕсли;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ЭлементСопоставления) Тогда
					ПутьКСопоставлению = КлючИЗначение.Ключ.ПолныйПуть + ".Сопоставление";
					
					Если Не ЭлектронноеВзаимодействие.СуществуетРеквизитВДереве(КлючИЗначение.Ключ, 
							ПутьКСопоставлению + ".ВариантУказанияНоменклатурыИБ") Тогда
						ДеревоЭлектронногоДокументаБЭД.ДобавитьЗначениеВДерево(
							КлючИЗначение.Ключ.Строки.Найти(ПутьКСопоставлению, "ПолныйПуть", Истина), 
							ПутьКСопоставлению + ".ВариантУказанияНоменклатурыИБ", 
							ЭлементСопоставления.НоменклатураКонтрагента.ВариантУказанияНоменклатуры);
					Иначе
						ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(КлючИЗначение.Ключ, 
							ПутьКСопоставлению + ".ВариантУказанияНоменклатурыИБ", 
							ЭлементСопоставления.НоменклатураКонтрагента.ВариантУказанияНоменклатуры);
					КонецЕсли;
					КлючИЗначение.Значение.Вставить("ВариантУказанияНоменклатурыИБ", 
						ЭлементСопоставления.НоменклатураКонтрагента.ВариантУказанияНоменклатуры);
					
					ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(КлючИЗначение.Ключ, 
						ПутьКСопоставлению + ".НоменклатураИБ", ЭлементСопоставления.НоменклатураИБ.Номенклатура);
					КлючИЗначение.Значение.Вставить("НоменклатураИБ", 
						ЭлементСопоставления.НоменклатураИБ.Номенклатура);
					
					ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(КлючИЗначение.Ключ, 
						ПутьКСопоставлению + ".ХарактеристикаИБ", ЭлементСопоставления.НоменклатураИБ.Характеристика);
					КлючИЗначение.Значение.Вставить("ХарактеристикаИБ", 
						ЭлементСопоставления.НоменклатураИБ.Характеристика);
					
					ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(КлючИЗначение.Ключ, 
						ПутьКСопоставлению + ".УпаковкаИБ", ЭлементСопоставления.НоменклатураИБ.Упаковка);
					КлючИЗначение.Значение.Вставить("УпаковкаИБ", 
						ЭлементСопоставления.НоменклатураИБ.Упаковка);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ОбъектыУчета) Тогда
		ОтражениеВУчетеЭДО.ОтразитьДанныеЭлектронногоДокумента(ДанныеЭлектронногоДокумента, 
			СпособОбработки, 
			ОбъектыУчета, 
			ДанныеРазбора);
	КонецЕсли;
	
	// Соответствие штрихкодов упаковок
	Если Не ЭтоПрикладнойТипЭлектронногоДокумента Тогда
		Если ДанныеЭлектронногоДокумента.Формат = ПоддерживаемыеФорматы.ФНС.УПД.ИнформацияПродавца
				Или ДанныеЭлектронногоДокумента.Формат = ПоддерживаемыеФорматы.ФНС.УПД2019.ИнформацияПродавца Тогда
			СведенияОТоварах = ДанныеРазбора.НовыйЭД.ЗначениеРеквизита.Строки.Найти("СведенияОТоварах", 
				"ПолныйПуть", Истина);
			
			ПутьКСопоставлению         = "СведенияОТоварах.НомерСтроки.Сопоставление";
			ПутьСведенияОМаркировке    = "СведенияОТоварах.НомерСтроки.СведенияОМаркировке";
			ПутьКонтрольныеЗнаки       = ПутьСведенияОМаркировке + ".КонтрольныеИдентификационныеЗнаки";
			ПутьТранспортныеУпаковки   = ПутьСведенияОМаркировке + ".ТранспортныеУпаковки";
			ПутьИндивидуальныеУпаковки = ПутьСведенияОМаркировке + ".ИндивидуальныеУпаковки";
			
			Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
				Штрихкоды = ЭлектронноеВзаимодействиеИСМП.НоваяТаблицаШтрихкодыУпаковок(Истина);
				Штрихкоды.Колонки.Добавить("ВидУпаковки", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыУпаковокИС"));
				
				Номенклатура = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
					ПутьКСопоставлению + ".НоменклатураИБ");
				Характеристика = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
					ПутьКСопоставлению + ".ХарактеристикаИБ");
				
				ДанныеМаркировки = СведенияОТоваре.Строки.Найти(ПутьСведенияОМаркировке, "ПолныйПуть", Истина);
				
				Если ДанныеМаркировки <> Неопределено И ЗначениеЗаполнено(ДанныеМаркировки.Значение) Тогда
					КонтрольныеИдентификационныеЗнаки = ДанныеМаркировки.Строки.Найти(ПутьКонтрольныеЗнаки, "ПолныйПуть", Истина);
					Если КонтрольныеИдентификационныеЗнаки <> Неопределено Тогда
						Для Каждого СтрокаДереваЗначений Из КонтрольныеИдентификационныеЗнаки.Строки Цикл
							ЗначениеШтрихкода = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаДереваЗначений, 
								ПутьКонтрольныеЗнаки + ".НомерСтроки.Код");
							ДобавитьШтрихкодУпаковкиИзСтрокиДерева(Штрихкоды, ЗначениеШтрихкода, 
								Номенклатура, Характеристика, Перечисления.ВидыУпаковокИС.Потребительская);
						КонецЦикла;
					КонецЕсли;
					
					ТранспортныеУпаковки = ДанныеМаркировки.Строки.Найти(ПутьТранспортныеУпаковки, "ПолныйПуть", Истина);
					Если ТранспортныеУпаковки <> Неопределено Тогда
						Для Каждого СтрокаДереваЗначений Из ТранспортныеУпаковки.Строки Цикл
							ЗначениеШтрихкода = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаДереваЗначений,
								ПутьТранспортныеУпаковки + ".НомерСтроки.Код");
							ДобавитьШтрихкодУпаковкиИзСтрокиДерева(Штрихкоды, ЗначениеШтрихкода, 
								Номенклатура, Характеристика, Перечисления.ВидыУпаковокИС.Логистическая);
						КонецЦикла;
					КонецЕсли;
					
					ИндивидуальныеУпаковки = ДанныеМаркировки.Строки.Найти(ПутьИндивидуальныеУпаковки, "ПолныйПуть", Истина);
					Если ИндивидуальныеУпаковки <> Неопределено Тогда
						Для Каждого СтрокаДереваЗначений Из ИндивидуальныеУпаковки.Строки Цикл
							ЗначениеШтрихкода = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаДереваЗначений,
								ПутьИндивидуальныеУпаковки + ".НомерСтроки.Код");
							ДобавитьШтрихкодУпаковкиИзСтрокиДерева(Штрихкоды, ЗначениеШтрихкода, 
								Номенклатура, Характеристика, Перечисления.ВидыУпаковокИС.Групповая);
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Штрихкоды, ШтрихкодыУпаковок);
			КонецЦикла;
			
		ИначеЕсли ДанныеЭлектронногоДокумента.Формат = ПоддерживаемыеФорматы.ФНС.УПД_5_02.ИнформацияПродавца Тогда
			СведенияОТоварах = ДанныеРазбора.НовыйЭД.ЗначениеРеквизита.Строки.Найти("ТаблицаСчетаФактуры.СведенияОбОтгруженныхПозициях", 
				"ПолныйПуть", Истина);
			
			ПутьКСопоставлению         = "ТаблицаСчетаФактуры.СведенияОбОтгруженныхПозициях.НомерСтроки.Сопоставление";
			ПутьСведенияОМаркировке    = "ТаблицаСчетаФактуры.СведенияОбОтгруженныхПозициях.НомерСтроки.СведенияОМаркировке";
			ПутьКонтрольныеЗнаки       = ПутьСведенияОМаркировке + ".НомерСтроки.Идентификатор.КонтрольныеЗнаки";
			ПутьТранспортнаяУпаковка   = ПутьСведенияОМаркировке + ".НомерСтроки.ИдентификаторУпаковки";
			ПутьИндивидуальныеУпаковки = ПутьСведенияОМаркировке + ".НомерСтроки.Идентификатор.ИдентификаторыУпаковок";
			ПутьКоличествоОСУ          = ПутьСведенияОМаркировке + ".НомерСтроки.Количество";
			ПутьГТИН                   = "ТаблицаСчетаФактуры.СведенияОбОтгруженныхПозициях.НомерСтроки.ГТИН";
			
			Для Каждого СведенияОТоваре Из СведенияОТоварах.Строки Цикл
				Штрихкоды = ЭлектронноеВзаимодействиеИСМП.НоваяТаблицаШтрихкодыУпаковок(Истина);
				Штрихкоды.Колонки.Добавить("ВидУпаковки", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыУпаковокИС"));
				
				Номенклатура = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
					ПутьКСопоставлению + ".НоменклатураИБ");
				Характеристика = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СведенияОТоваре,
					ПутьКСопоставлению + ".ХарактеристикаИБ");
				
				ДанныеМаркировки = СведенияОТоваре.Строки.Найти(ПутьСведенияОМаркировке, "ПолныйПуть", Истина);
				ДанныеГТИН       = СведенияОТоваре.Строки.Найти(ПутьГТИН, "ПолныйПуть", Истина);
				
				Если ДанныеМаркировки <> Неопределено И ЗначениеЗаполнено(ДанныеМаркировки.Значение) Тогда
					Для Каждого СтрокаМаркировки Из ДанныеМаркировки.Строки Цикл
						КонтрольныеИдентификационныеЗнаки = СтрокаМаркировки.Строки.Найти(ПутьКонтрольныеЗнаки, "ПолныйПуть", Истина);
						Если КонтрольныеИдентификационныеЗнаки <> Неопределено Тогда
							Для Каждого СтрокаДереваЗначений Из КонтрольныеИдентификационныеЗнаки.Строки Цикл
								ЗначениеШтрихкода = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаДереваЗначений, 
									ПутьКонтрольныеЗнаки + ".НомерСтроки.КонтрольныйЗнак");
								ДобавитьШтрихкодУпаковкиИзСтрокиДерева(Штрихкоды, ЗначениеШтрихкода, 
									Номенклатура, Характеристика, Перечисления.ВидыУпаковокИС.Потребительская);
							КонецЦикла;
						КонецЕсли;
						
						ТранспортнаяУпаковка = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаМаркировки,
							ПутьТранспортнаяУпаковка, Ложь);
						Если ТранспортнаяУпаковка <> Неопределено Тогда
							ДобавитьШтрихкодУпаковкиИзСтрокиДерева(Штрихкоды, ТранспортнаяУпаковка, 
								Номенклатура, Характеристика, Перечисления.ВидыУпаковокИС.Логистическая);
						КонецЕсли;
						
						ИндивидуальныеУпаковки = СтрокаМаркировки.Строки.Найти(ПутьИндивидуальныеУпаковки, "ПолныйПуть", Истина);
						Если ИндивидуальныеУпаковки <> Неопределено Тогда
							Для Каждого СтрокаДереваЗначений Из ИндивидуальныеУпаковки.Строки Цикл
								ЗначениеШтрихкода = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаДереваЗначений,
									ПутьИндивидуальныеУпаковки + ".НомерСтроки.ИдентификаторУпаковки");
								ДобавитьШтрихкодУпаковкиИзСтрокиДерева(Штрихкоды, ЗначениеШтрихкода, 
									Номенклатура, Характеристика, Перечисления.ВидыУпаковокИС.Групповая);
							КонецЦикла;
						КонецЕсли; 
						
						КоличествоПоОСУ = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаМаркировки,
							ПутьКоличествоОСУ, Ложь);
						Если Не КоличествоПоОСУ = Неопределено 
								И ЗначениеЗаполнено(КоличествоПоОСУ) 
								И Не ДанныеГТИН = Неопределено Тогда
							ЗначениеШтрихкода = ЭлектронноеВзаимодействиеИСМП.СформироватьКодОСУПоКоличествуИGTIN(ДанныеГТИН, КоличествоПоОСУ);
							ДобавитьШтрихкодУпаковкиИзСтрокиДерева(Штрихкоды, ЗначениеШтрихкода, 
								Номенклатура, Характеристика, Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Штрихкоды, ШтрихкодыУпаковок);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ОбъектыУчета) Тогда
		ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, 0);
		Возврат Неопределено;
	КонецЕсли;
	
	ОбъектУчета   = ОбъектыУчета[0];	// ДокументОбъект.ПередачаТоваровХранителю
	ТоварыИзФайла = ОбъектУчета.Товары.Выгрузить();
	
	ШтрихкодыУпаковокИзФайла = ШтрихкодированиеИС.ДанныеШтрихкодовУпаковок(ШтрихкодыУпаковок.ВыгрузитьКолонку("ЗначениеШтрихкода"),, Истина);
	ШтрихкодыУпаковокИзФайла.Колонки.Штрихкод.Имя = "ЗначениеШтрихкода";
	ШтрихкодыУпаковокИзФайла.Колонки.ШтрихкодУпаковки.Имя = "Ссылка";
	ШтрихкодыУпаковокИзФайла.Колонки.Добавить("Обработан",   Новый ОписаниеТипов("Булево"));
	ШтрихкодыУпаковокИзФайла.Колонки.Добавить("ВидУпаковки", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыУпаковокИС"));
	
	Для Каждого СтрокаТаблицыЗначений Из ШтрихкодыУпаковок Цикл
		Запись = ШтрихкодыУпаковокИзФайла.Найти(СтрокаТаблицыЗначений.ЗначениеШтрихкода);
		Если Запись = Неопределено Тогда
			Запись = ШтрихкодыУпаковокИзФайла.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицыЗначений);
			Запись.Количество = 1;
		Иначе
			ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицыЗначений, "Номенклатура, Характеристика, ВидУпаковки");
		КонецЕсли;
	КонецЦикла;
	
	ТоварыИзДокумента            = Новый ТаблицаЗначений;
	ШтрихкодыУпаковокИзДокумента = Новый ТаблицаЗначений;
	ПолучитьТабличныеЧастиИзДокумента(ПервичныйДокумент, ТоварыИзДокумента, ШтрихкодыУпаковокИзДокумента);
	
	ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(Замер, 
		ТоварыИзФайла.Количество() / 100,
		"ЧтениеЭлектронногоДокумента");
	
	// Анализ расхождений
	Расхождения = АнализРасхожденийДокументаИТорговойПлощадки(ТоварыИзФайла, 
		ШтрихкодыУпаковокИзФайла, 
		ТоварыИзДокумента, 
		ШтрихкодыУпаковокИзДокумента);
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(ПервичныйДокумент, Расхождения.ТоварыРасхождения);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, 
		ТоварыИзФайла.Количество() / 100);
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеРазбора",                ДанныеРазбора);
	Результат.Вставить("ТоварыИзФайла",                ТоварыИзФайла);
	Результат.Вставить("ШтрихкодыУпаковокИзФайла",     ШтрихкодыУпаковокИзФайла);
	Результат.Вставить("ТоварыИзДокумента",            ТоварыИзДокумента);
	Результат.Вставить("ШтрихкодыУпаковокИзДокумента", ШтрихкодыУпаковокИзДокумента);
	Результат.Вставить("ТоварыРасхождения",            Расхождения.ТоварыРасхождения);
	Результат.Вставить("ШтрихкодыУпаковокРасхождения", Расхождения.ШтрихкодыУпаковокРасхождения);
	
	Возврат Результат;
	
КонецФункции

// Получает преобразованные табличные части первичного документа для анализа расхождений.
//
// Параметры:
//   ПервичныйДокумент            - ДокументСсылка.ПередачаТоваровХранителю - первичный документ.
//   ТоварыИзДокумента            - ТаблицаЗначений - см. Документ.ПередачаТоваровХранителю.Товары.
//   ШтрихкодыУпаковокИзДокумента - см. ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ.
//
Процедура ПолучитьТабличныеЧастиИзДокумента(Знач ПервичныйДокумент, ТоварыИзДокумента, ШтрихкодыУпаковокИзДокумента) Экспорт
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПервичныйДокумент, "Товары, ШтрихкодыУпаковок");
	ТоварыИзДокумента  = РеквизитыДокумента.Товары.Выгрузить();
	
	ШтрихкодыУпаковок = РеквизитыДокумента.ШтрихкодыУпаковок.Выгрузить();
	ШтрихкодыУпаковок.Колонки.ШтрихкодУпаковки.Имя = "Штрихкод";
	
	ТаблицаТоварыДляЭДО = ИнтеграцияИСМПУТ.ТаблицаТоварыДляЭДО(ТоварыИзДокумента, "Количество");
	Если ТаблицаТоварыДляЭДО.Количество() Тогда
		ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования(ПервичныйДокумент);
	КонецЕсли;
	ШтрихкодыУпаковокИзДокумента = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(ШтрихкодыУпаковок,
		ТаблицаТоварыДляЭДО,
		ПараметрыСканирования);
	ШтрихкодыУпаковокИзДокумента.Колонки.Удалить("Ссылка");
	ШтрихкодыУпаковокИзДокумента.Колонки.Штрихкод.Имя = "Ссылка";
	
КонецПроцедуры

// Заполняет служебные реквизиты табличной части расхождений по товарам.
//
// Параметры:
//   ПервичныйДокумент - ДокументСсылка.ПередачаТоваровХранителю - первичный документ.
//   ТоварыРасхождения - ТаблицаЗначений - с дополнениями для Документ.ПередачаТоваровХранителю.Товары.
//
Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(Знач ПервичныйДокумент, ТоварыРасхождения) Экспорт
	
	СтруктураДействий = Новый Структура;
	
	Если ТипЗнч(ПервичныйДокумент) = Тип("ДокументСсылка.ПередачаТоваровХранителю") Тогда
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПервичныйДокумент, "Склад, ХозяйственнаяОперация");
		Обработчик         = Документы.ПередачаТоваровХранителю.ОбработчикДействий(РеквизитыДокумента.ХозяйственнаяОперация);
		СкладГруппа        = Обработчик.ЭтоГруппаСкладовИСкладыИспользуютсяВТЧДокументовПродажи(РеквизитыДокумента.Склад);
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ПервичныйДокумент, 
			Документы.ПередачаТоваровХранителю);
		
		ПроверитьСериюРассчитатьСтатус = Новый Структура;
		ПроверитьСериюРассчитатьСтатус.Вставить("Склад",                  РеквизитыДокумента.Склад);
		ПроверитьСериюРассчитатьСтатус.Вставить("ПараметрыУказанияСерий", ПараметрыУказанияСерий);
		
		ПроверитьЗаполнитьСклад = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруЗаполненияСкладаВСтрокеТЧ(
			ПервичныйДокумент,
			СкладГруппа);
		
		СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус", ПроверитьСериюРассчитатьСтатус);
		СтруктураДействий.Вставить("ПроверитьЗаполнитьСклад",        ПроверитьЗаполнитьСклад);
		
	Иначе
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ПервичныйДокумент,
			Документы.АктОРасхожденияхПослеОтгрузки);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ПервичныйДокумент, ПараметрыУказанияСерий);
	КонецЕсли;
	
	СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", 
		Новый Структура("Номенклатура", "ТипНоменклатуры"));
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", 
		Новый Структура("Номенклатура", "Артикул"));
	СтруктураДействий.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются", 
		Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
	
	КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьТЧ(ТоварыРасхождения, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

// Определяет расхождения между двумя документами:
//   - по таблице товаров для Документ.ПередачаТоваровХранителю.Товары;
//   - по таблице штрихкодов для ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ.
//
// Параметры:
//   ТоварыИзФайла                - ТаблицаЗначений из см. Документ.ПриобретениеТоваровУслуг.Товары.
//   ШтрихкодыУпаковокИзФайла     - см. ШтрихкодированиеИС.ШтрихкодыУпаковок.
//   ТоварыИзДокумента            - ТаблицаЗначений из см. Документ.ПередачаТоваровХранителю.Товары.
//   ШтрихкодыУпаковокИзДокумента - см. ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ.
//
// Возвращаемое значение:
//   Структура - данные по расхождениям:
//     * ТоварыРасхождения            - см. НоваяТаблицаТоварыРасхождения.
//     * ШтрихкодыУпаковокРасхождения - см. НоваяТаблицаШтрихкодыУпаковокРасхождения.
//
Функция АнализРасхожденийДокументаИТорговойПлощадки(Знач ТоварыИзФайла, Знач ШтрихкодыУпаковокИзФайла, 
			Знач ТоварыИзДокумента, Знач ШтрихкодыУпаковокИзДокумента) Экспорт
			
	ШтрихкодыУпаковокИзФайла.Индексы.Добавить("ЗначениеШтрихкода");
	ШтрихкодыУпаковокИзДокумента.Индексы.Добавить("ЗначениеШтрихкода");
	
	// Расхождения по номенклатуре
	ТоварыРасхождения = НоваяТаблицаТоварыРасхождения(ТоварыИзДокумента);
	
	ЕстьКолонкаСумма = ЗначениеЗаполнено(ТоварыИзФайла.Итог("Сумма"));
	
	ВариантыРаспределения = Новый Массив;
	Если ЕстьКолонкаСумма Тогда
		ВариантыРаспределения.Добавить("ПоУпаковкеИСумме");
	КонецЕсли;
	ВариантыРаспределения.Добавить("ПоУпаковке");
	ВариантыРаспределения.Добавить("ПоНоменклатуре");
	
	Для Каждого СтрокаТаблицыЗначений Из ТоварыИзФайла Цикл // СтрокаТаблицыЗначений
		Количество         = СтрокаТаблицыЗначений.Количество;
		КоличествоУпаковок = СтрокаТаблицыЗначений.КоличествоУпаковок;
		Коэффициент        = ?(Количество = 0, 1, Количество / КоличествоУпаковок);
		
		Отбор = Новый Структура;
		Отбор.Вставить("Номенклатура",   СтрокаТаблицыЗначений.Номенклатура);
		Отбор.Вставить("Характеристика", СтрокаТаблицыЗначений.Характеристика);
		
		НайденныеСтроки = ТоварыРасхождения.НайтиСтроки(Отбор);
		Для Каждого ЭлементКоллекции Из НайденныеСтроки Цикл
			Если Количество <= 0 Тогда
				Прервать;
			КонецЕсли;
			
			КоэффициентПоДокументу = ?(ЭлементКоллекции.КоличествоПоДокументу = 0, 1, 
				ЭлементКоллекции.КоличествоПоДокументу / ЭлементКоллекции.КоличествоУпаковокПоДокументу);
			
			// Распределим по указанному варианту с учетом количества в документе
			Для Каждого Вариант Из ВариантыРаспределения Цикл
				Если Количество <= 0 Тогда
					Прервать;
				КонецЕсли;
				
				Если Вариант = "ПоУпаковкеИСумме"
							И СтрокаТаблицыЗначений.Упаковка = ЭлементКоллекции.Упаковка
							И СтрокаТаблицыЗначений.Цена = ЭлементКоллекции.Цена
						Или Вариант = "ПоУпаковке"
							И СтрокаТаблицыЗначений.Упаковка = ЭлементКоллекции.Упаковка
						Или Вариант = "ПоНоменклатуре" Тогда
					КоличествоКРаспределению = Мин(Количество, 
						ЭлементКоллекции.КоличествоПоДокументу - ЭлементКоллекции.Количество);
					
					Если КоличествоКРаспределению > 0 Тогда
						Количество               = Количество - КоличествоКРаспределению;
						КоличествоУпаковок       = КоличествоУпаковок - Окр(КоличествоКРаспределению 
							/ ?(Коэффициент = 0, 1, Коэффициент), 3, РежимОкругления.Окр15как20);
							
						ЭлементКоллекции.Количество         = ЭлементКоллекции.Количество + КоличествоКРаспределению;
						ЭлементКоллекции.КоличествоУпаковок = Окр(ЭлементКоллекции.Количество 
							/ ?(КоэффициентПоДокументу = 0, 1, КоэффициентПоДокументу), 3, РежимОкругления.Окр15как20);
						ЭлементКоллекции.Сумма              = ЭлементКоллекции.КоличествоУпаковок * ЭлементКоллекции.Цена;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			// Распределим остаток по первому варианту как излишек
			Если Количество > 0 Тогда
				Вариант = ВариантыРаспределения[0];
				
				Если Вариант = "ПоУпаковкеИСумме"
							И СтрокаТаблицыЗначений.Упаковка = ЭлементКоллекции.Упаковка
							И СтрокаТаблицыЗначений.Цена = ЭлементКоллекции.Цена
						Или Вариант = "ПоУпаковке"
							И СтрокаТаблицыЗначений.Упаковка = ЭлементКоллекции.Упаковка Тогда
					КоличествоКРаспределению = Количество;
					
					Если КоличествоКРаспределению > 0 Тогда
						Количество               = Количество - КоличествоКРаспределению;
						КоличествоУпаковок       = КоличествоУпаковок - Окр(КоличествоКРаспределению 
							/ ?(Коэффициент = 0, 1, Коэффициент), 3, РежимОкругления.Окр15как20);
							
						ЭлементКоллекции.Количество         = ЭлементКоллекции.Количество + КоличествоКРаспределению;
						ЭлементКоллекции.КоличествоУпаковок = Окр(ЭлементКоллекции.Количество 
							/ ?(КоэффициентПоДокументу = 0, 1, КоэффициентПоДокументу), 3, РежимОкругления.Окр15как20);
						ЭлементКоллекции.Сумма              = ЭлементКоллекции.КоличествоУпаковок * ЭлементКоллекции.Цена;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		// Остаток - как новая строка
		Если Количество > 0 Тогда
			Если НайденныеСтроки.Количество() > 0 Тогда
				Запись = ТоварыРасхождения.Вставить(
					ТоварыРасхождения.Индекс(НайденныеСтроки[НайденныеСтроки.ВГраница()]) + 1);
			Иначе
				Запись = ТоварыРасхождения.Добавить();
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицыЗначений, "Номенклатура, Характеристика, Упаковка, Цена");
			
			Запись.Количество                    = Количество;
			Запись.КоличествоУпаковок            = КоличествоУпаковок;
			Запись.Сумма                         = Запись.КоличествоУпаковок * Запись.Цена;
			Запись.КоличествоПоДокументу         = 0;
			Запись.КоличествоУпаковокПоДокументу = 0;
			Запись.СуммаПоДокументу              = 0;
		КонецЕсли;
	КонецЦикла;
	
	МассивНоменклатуры    = ТоварыРасхождения.ВыгрузитьКолонку("Номенклатура");	// Массив из СправочникСсылка.Номенклатура
	РеквизитыНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивНоменклатуры, "ЕдиницаИзмерения");
		
	// Расхождения по кодам маркировки
	ШтрихкодыУпаковокРасхождения = НоваяТаблицаШтрихкодыУпаковокРасхождения(ШтрихкодыУпаковокИзДокумента);
	
	ЕстьШтрихкодыУпаковокИзФайла = ЗначениеЗаполнено(ШтрихкодыУпаковокИзФайла);
	Если Не ЕстьШтрихкодыУпаковокИзФайла Тогда
		ШтрихкодыУпаковокРасхождения.ЗаполнитьЗначения("КодовМаркировкиНетВФайле",            "Статус");
		ШтрихкодыУпаковокРасхождения.ЗаполнитьЗначения(НСтр("ru = '(в файле нет сведений)';
															|en = '(there are no information records in the file)'"), "ТекстСообщения");
		
		Для Каждого СтрокаТаблицыЗначений Из ШтрихкодыУпаковокРасхождения Цикл
			СтрокаТаблицыЗначений.Количество                        = СтрокаТаблицыЗначений.КоличествоПоДокументу;
			СтрокаТаблицыЗначений.КоличествоПотребительскихУпаковок = СтрокаТаблицыЗначений.КоличествоПотребительскихУпаковокПоДокументу;
		КонецЦикла;
		
	Иначе
		ШтрихкодыУпаковокРасхождения.ЗаполнитьЗначения("НедостачаКодовМаркировки", "Статус");
		
		Для Каждого СтрокаТаблицыЗначений Из ШтрихкодыУпаковокИзФайла Цикл
			Количество                        = СтрокаТаблицыЗначений.Количество;
			КоличествоПотребительскихУпаковок = ?(Не ЗначениеЗаполнено(СтрокаТаблицыЗначений.КоличествоПотребительскихУпаковок),
				1, СтрокаТаблицыЗначений.КоличествоПотребительскихУпаковок);
			Коэффициент                       = ?(Количество = 0, 1, КоличествоПотребительскихУпаковок / Количество);
			
			Отбор = Новый Структура;
			Отбор.Вставить("ЗначениеШтрихкода", СтрокаТаблицыЗначений.ЗначениеШтрихкода);
			
			НайденныеСтроки = ШтрихкодыУпаковокРасхождения.НайтиСтроки(Отбор);
			Для Каждого ЭлементКоллекции Из НайденныеСтроки Цикл
				Если Количество <= 0 Тогда
					Прервать;
				КонецЕсли;
				
				КоличествоКРаспределению = Мин(Количество, 
					ЭлементКоллекции.КоличествоПоДокументу - ЭлементКоллекции.Количество);
				
				Если КоличествоКРаспределению > 0 Тогда
					Количество                        = Количество - КоличествоКРаспределению;
					КоличествоПотребительскихУпаковок = КоличествоПотребительскихУпаковок - Окр(КоличествоКРаспределению 
						/ ?(Коэффициент = 0, 1, Коэффициент), 3, РежимОкругления.Окр15как20);
						
					КоэффициентПоДокументу                             = ?(ЭлементКоллекции.КоличествоПоДокументу = 0, 
						1, ЭлементКоллекции.КоличествоПотребительскихУпаковокПоДокументу / ЭлементКоллекции.КоличествоПоДокументу);
					ЭлементКоллекции.Количество                        = ЭлементКоллекции.Количество + КоличествоКРаспределению;
					ЭлементКоллекции.КоличествоПотребительскихУпаковок = Окр(ЭлементКоллекции.Количество 
						/ ?(КоэффициентПоДокументу = 0, 1, КоэффициентПоДокументу), 3, РежимОкругления.Окр15как20);
						
					Если СтрокаТаблицыЗначений.Номенклатура <> ЭлементКоллекции.Номенклатура
							Или СтрокаТаблицыЗначений.Характеристика <> ЭлементКоллекции.Характеристика Тогда
						ЭлементКоллекции.Статус         = "ПересортицаКодовМаркировки";
						ЭлементКоллекции.ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = '(в файле: %1%2)';
								|en = '(in the file: %1%2)'"), 
							СтрокаТаблицыЗначений.Номенклатура, 
							?(ЗначениеЗаполнено(СтрокаТаблицыЗначений.Характеристика), 
								", " + СтрокаТаблицыЗначений.Характеристика, ""));
					Иначе
						ЭлементКоллекции.Статус = "КодыМаркировкиСовпадают";
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			// Остаток - как новая строка
			Если Количество > 0 Тогда
				Запись = ШтрихкодыУпаковокРасхождения.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицыЗначений);
				
				Запись.Статус                                        = ?(ЗначениеЗаполнено(СтрокаТаблицыЗначений.Ссылка), 
					"ИзлишекКодовМаркировки", "КодыМаркировкиНеНайдены");
				Запись.Количество                                    = Количество;
				Запись.КоличествоПотребительскихУпаковок             = КоличествоПотребительскихУпаковок;
				Запись.КоличествоПоДокументу                         = 0;
				Запись.КоличествоПотребительскихУпаковокПоДокументу  = 0;
				Запись.КоличествоВПотребительскойУпаковкеПоДокументу = 0;
				Запись.КоличествоРаспределено                        = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ВариантыРаспределения = Новый Массив;
	ВариантыРаспределения.Добавить("ССерией");
	ВариантыРаспределения.Добавить("БезСерии");
	
	Для Каждого СтрокаТаблицыЗначений Из ТоварыРасхождения Цикл
		Если СтрокаТаблицыЗначений.СведенияОМаркировке = Неопределено Тогда
			СтрокаТаблицыЗначений.СведенияОМаркировке     = Новый Соответствие;
			СтрокаТаблицыЗначений.РасхожденийПоМаркировке = ?(ЕстьШтрихкодыУпаковокИзФайла, 0, -1);
		КонецЕсли;
		
		Количество            = СтрокаТаблицыЗначений.Количество;
		КоличествоПоДокументу = СтрокаТаблицыЗначений.КоличествоПоДокументу;
		
		Отбор = Новый Структура;
		Отбор.Вставить("Номенклатура",   СтрокаТаблицыЗначений.Номенклатура);
		Отбор.Вставить("Характеристика", СтрокаТаблицыЗначений.Характеристика);
		
		НайденныеСтроки = ШтрихкодыУпаковокРасхождения.НайтиСтроки(Отбор);
		Для Каждого ЭлементКоллекции Из НайденныеСтроки Цикл
			Если Количество <= 0 Тогда
				Прервать;
			КонецЕсли;
			
			Для Каждого Вариант Из ВариантыРаспределения Цикл
				Если Количество <= 0 Тогда
					Прервать;
				КонецЕсли;
				
				Если Вариант = "ССерией"
							И СтрокаТаблицыЗначений.Серия = ЭлементКоллекции.Серия
						Или Вариант = "БезСерии" Тогда
					КоличествоКРаспределению = Мин(Количество, ЭлементКоллекции.Количество 
						- ЭлементКоллекции.КоличествоРаспределено);
					
					Если КоличествоКРаспределению > 0 Тогда
						Количество                              = Количество - КоличествоКРаспределению;
						ЭлементКоллекции.КоличествоРаспределено = ЭлементКоллекции.КоличествоРаспределено 
							+ КоличествоКРаспределению;
						
						ЭлементКоллекции.Упаковка = ?(ЗначениеЗаполнено(СтрокаТаблицыЗначений.Упаковка),
							СтрокаТаблицыЗначений.Упаковка, 
							РеквизитыНоменклатуры.Получить(СтрокаТаблицыЗначений.Номенклатура));
						
						СведенияОМаркировке = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ЭлементКоллекции);
						СтрокаТаблицыЗначений.СведенияОМаркировке.Вставить(ЭлементКоллекции.ЗначениеШтрихкода, 
							СведенияОМаркировке);
						
						Если ЕстьШтрихкодыУпаковокИзФайла
								И ЭлементКоллекции.Статус <> "КодыМаркировкиСовпадают" Тогда
							СтрокаТаблицыЗначений.РасхожденийПоМаркировке = СтрокаТаблицыЗначений.РасхожденийПоМаркировке 
								+ КоличествоКРаспределению;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		НайденныеСтроки = ШтрихкодыУпаковокРасхождения.НайтиСтроки(Отбор);
		Для Каждого ЭлементКоллекции Из НайденныеСтроки Цикл
			Если КоличествоПоДокументу <= 0 Тогда
				Прервать;
			КонецЕсли;
			
			Для Каждого Вариант Из ВариантыРаспределения Цикл
				Если КоличествоПоДокументу <= 0 Тогда
					Прервать;
				КонецЕсли;
				
				Если Вариант = "ССерией"
							И СтрокаТаблицыЗначений.Серия = ЭлементКоллекции.Серия
						Или Вариант = "БезСерии" Тогда
					КоличествоКРаспределению = Мин(КоличествоПоДокументу, ЭлементКоллекции.КоличествоПоДокументу 
						- ЭлементКоллекции.КоличествоРаспределеноПоДокументу);
					
					Если КоличествоКРаспределению > 0 Тогда
						КоличествоПоДокументу                              = КоличествоПоДокументу - КоличествоКРаспределению;
						ЭлементКоллекции.КоличествоРаспределеноПоДокументу = ЭлементКоллекции.КоличествоРаспределеноПоДокументу 
							+ КоличествоКРаспределению;
							
						ЭлементКоллекции.Упаковка = ?(ЗначениеЗаполнено(СтрокаТаблицыЗначений.Упаковка),
							СтрокаТаблицыЗначений.Упаковка, 
							РеквизитыНоменклатуры.Получить(СтрокаТаблицыЗначений.Номенклатура));
						
						Если СтрокаТаблицыЗначений.СведенияОМаркировке.Получить(ЭлементКоллекции.ЗначениеШтрихкода) = Неопределено Тогда
							СведенияОМаркировке = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ЭлементКоллекции);
							СтрокаТаблицыЗначений.СведенияОМаркировке.Вставить(ЭлементКоллекции.ЗначениеШтрихкода, 
								СведенияОМаркировке);
							
							Если ЕстьШтрихкодыУпаковокИзФайла
									И ЭлементКоллекции.Статус <> "КодыМаркировкиСовпадают" Тогда
								СтрокаТаблицыЗначений.РасхожденийПоМаркировке = СтрокаТаблицыЗначений.РасхожденийПоМаркировке 
									+ КоличествоКРаспределению;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	// Излишки штрихкодов из документа
	Для Каждого СтрокаТаблицыЗначений Из ШтрихкодыУпаковокРасхождения Цикл
		Если СтрокаТаблицыЗначений.КоличествоРаспределеноПоДокументу < СтрокаТаблицыЗначений.КоличествоПоДокументу Тогда
			Отбор = Новый Структура;
			Отбор.Вставить("Номенклатура",   СтрокаТаблицыЗначений.Номенклатура);
			Отбор.Вставить("Характеристика", СтрокаТаблицыЗначений.Характеристика);
			
			СтрокаНайдена   = Ложь;
			НайденныеСтроки = ТоварыРасхождения.НайтиСтроки(Отбор);
			
			Для Каждого ЭлементКоллекции Из НайденныеСтроки Цикл
				Для Каждого Вариант Из ВариантыРаспределения Цикл
					Если Вариант = "ССерией"
								И СтрокаТаблицыЗначений.Серия = ЭлементКоллекции.Серия
							Или Вариант = "БезСерии" Тогда
						СведенияОМаркировке = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаТаблицыЗначений);
						СведенияОМаркировке.Вставить("Статус", "ЛишнийКодМаркировки");
						ЭлементКоллекции.СведенияОМаркировке.Вставить(СтрокаТаблицыЗначений.ЗначениеШтрихкода, 
							СведенияОМаркировке);
						
						Если ЕстьШтрихкодыУпаковокИзФайла Тогда
							ЭлементКоллекции.РасхожденийПоМаркировке = ЭлементКоллекции.РасхожденийПоМаркировке 
								+ СтрокаТаблицыЗначений.КоличествоПоДокументу 
								- СтрокаТаблицыЗначений.КоличествоРаспределеноПоДокументу;
						КонецЕсли;
						СтрокаТаблицыЗначений.КоличествоРаспределеноПоДокументу = СтрокаТаблицыЗначений.КоличествоПоДокументу;
						
						СтрокаТаблицыЗначений.Упаковка = ?(ЗначениеЗаполнено(ЭлементКоллекции.Упаковка),
							ЭлементКоллекции.Упаковка, РеквизитыНоменклатуры.Получить(ЭлементКоллекции.Номенклатура));
						
						СтрокаНайдена = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если СтрокаНайдена Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТаблицыЗначений Из ТоварыРасхождения Цикл
		СтрокаТаблицыЗначений.ИндексНабора                  = ?(ЗначениеЗаполнено(СтрокаТаблицыЗначений.НоменклатураНабора), 1, 0);
		СтрокаТаблицыЗначений.КоличествоУпаковокРасхождения = СтрокаТаблицыЗначений.КоличествоУпаковок 
																- СтрокаТаблицыЗначений.КоличествоУпаковокПоДокументу;
		СтрокаТаблицыЗначений.СуммаРасхождения              = СтрокаТаблицыЗначений.Сумма 
																- СтрокаТаблицыЗначений.СуммаПоДокументу;
		СтрокаТаблицыЗначений.ЕстьРасхождения               = ЗначениеЗаполнено(СтрокаТаблицыЗначений.КоличествоУпаковокРасхождения)
																Или ЗначениеЗаполнено(СтрокаТаблицыЗначений.СуммаРасхождения);
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицыЗначений.СведенияОМаркировке) Тогда
			СтрокаТаблицыЗначений.Маркировка = "";
		ИначеЕсли Не ЕстьШтрихкодыУпаковокИзФайла Тогда
			СтрокаТаблицыЗначений.Маркировка = НСтр("ru = 'в файле нет сведений';
													|en = 'There are no information records in the file'");
		ИначеЕсли ЗначениеЗаполнено(СтрокаТаблицыЗначений.РасхожденийПоМаркировке) Тогда
			СтрокаТаблицыЗначений.ЕстьРасхождения = Истина;
			СтрокаТаблицыЗначений.Маркировка = Формат(СтрокаТаблицыЗначений.РасхожденийПоМаркировке, "ЧДЦ=0; ЧН=0; ЧГ=0") + " "
				+ ОбщегоНазначенияУТКлиентСервер.СклонениеСлова(
					СтрокаТаблицыЗначений.РасхожденийПоМаркировке,
					НСтр("ru = 'расхождение, расхождения, расхождений, м';
						|en = 'Discrepancy, discrepancies, discrepancies, м'"));
		Иначе
			СтрокаТаблицыЗначений.Маркировка = НСтр("ru = 'нет расхождений';
													|en = 'No discrepancies'");
		КонецЕсли;
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ТоварыРасхождения",            ТоварыРасхождения);
	Результат.Вставить("ШтрихкодыУпаковокРасхождения", ШтрихкодыУпаковокРасхождения);
	
	Возврат Результат;
	
КонецФункции

// Возвращает список констант для анализа расхождений.
//
// Возвращаемое значение:
//   Соответствие из КлючИЗначение - имена и значения констант:
//     * Ключ     - Строка - статус.
//     * Значение - Строка - имя картинки в библиотеке.
//
Функция КонстантыРасхождений() Экспорт
	
	Результат = Новый Соответствие;
	
	Результат.Вставить("КодыМаркировкиСовпадают",    "ВыполненоУспешно");
	Результат.Вставить("ЛишнийКодМаркировки",        "Штрихкод");
	Результат.Вставить("КодовМаркировкиНетВФайле",   "Предупреждение");
	Результат.Вставить("ПересортицаКодовМаркировки", "Предупреждение");
	Результат.Вставить("ИзлишекКодовМаркировки",     "КрасныйПлюс");
	Результат.Вставить("НедостачаКодовМаркировки",   "ЗначокМинус");
	Результат.Вставить("КодыМаркировкиНеНайдены",    "ПредупреждениеКрасноеБЭД16");
	
	Возврат Результат;
	
КонецФункции

// Формирует представлений расхождений в виде форматированной строки.
//
// Параметры:
//   СведенияОМаркировке - Соответствие из КлючИЗначение - сведения о маркируемой продукции:
//     * Ключ     - Строка - код маркировки.
//     * Значение - Структура из КлючИЗначение - с дополнениями для ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ.
//   
// Возвращаемое значение:
//   ФорматированнаяСтрока.
//
Функция СведенияОМаркировкеФорматированнойСтрокой(Знач СведенияОМаркировке) Экспорт
	
	Если Не ЗначениеЗаполнено(СведенияОМаркировке) Тогда
		Возврат "";
	КонецЕсли;
	
	КонстантыРасхождений = КонстантыРасхождений();
	Результат = "";
	
	Для Каждого КлючИЗначение Из СведенияОМаркировке Цикл
		Результат = Результат + "<img src=""" + КонстантыРасхождений[КлючИЗначение.Значение.Статус] + """>";
		Результат = Результат + Символы.НПП + КлючИЗначение.Значение.ЗначениеШтрихкода;
		Если Макс(КлючИЗначение.Значение.Количество, КлючИЗначение.Значение.КоличествоПоДокументу) > 1 Тогда
			Результат = Результат + "<span style=""color: ЦветНеАктивнойСтроки"">"
				+ Символы.НПП + "-" + Символы.НПП 
				+ Макс(КлючИЗначение.Значение.Количество, КлючИЗначение.Значение.КоличествоПоДокументу) 
				+ Символы.НПП + СтрЗаменить(КлючИЗначение.Значение.Упаковка, " ", Символы.НПП)
				+ "</span>";
		КонецЕсли;
		Если ЗначениеЗаполнено(КлючИЗначение.Значение.ТекстСообщения) Тогда
			Результат = Результат + "<span style=""color: ЦветНеАктивнойСтроки"">"
				+ Символы.НПП + СтрЗаменить(КлючИЗначение.Значение.ТекстСообщения, " ", Символы.НПП)
				+ "</span>";
		КонецЕсли;
		Результат = Результат + Символы.ПС;
	КонецЦикла;
	
	Возврат СтроковыеФункции.ФорматированнаяСтрока(Результат);
	
КонецФункции

// Конструктор таблицы данных контрагента для создания номенклатуры, характеристики и упаковки.
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
//   * НоменклатураНаименование - Строка - 
//   * ХарактеристикаНаименование - Строка - 
//   * УпаковкаНаименование - Строка - 
//   * Номенклатура - СправочникСсылка.Номенклатура - 
//   * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - 
//   * Упаковка - СправочникСсылка.УпаковкиЕдиницыИзмерения - 
//   * ХарактеристикаНайдена - Булево - 
//   * УпаковкаНайдена - Булево - 
//   * НоменклатураКонтрагента - см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента
//
Функция НоваяТаблицаДанныхКонтрагентаДляСозданияНоменклатуры() Экспорт
	
	Результат = Новый ТаблицаЗначений;
	
	Результат.Колонки.Добавить("НоменклатураНаименование",   ОбщегоНазначения.ОписаниеТипаСтрока(100));
	Результат.Колонки.Добавить("ХарактеристикаНаименование", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	Результат.Колонки.Добавить("УпаковкаНаименование",       ОбщегоНазначения.ОписаниеТипаСтрока(50));
	
	Результат.Колонки.Добавить("Номенклатура",   Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Результат.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Результат.Колонки.Добавить("Упаковка",       Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
	
	Результат.Колонки.Добавить("ХарактеристикаНайдена", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("УпаковкаНайдена",       Новый ОписаниеТипов("Булево"));
	
	Результат.Колонки.Добавить("НоменклатураКонтрагента"); // Произвольный
	
	Возврат Результат;
	
КонецФункции

// Возвращает состав полей, требуемых для создания номенклатуры по данным контрагента.
//
// Параметры:
//   ИсточникДанных - Произвольный - источник данных для определения категории сервиса.
//
// Возвращаемое значение:
//  Структура - см. ИнтеграцияСМаркетплейсамиКлиентСервер.РеквизитыНоменклатурыДляСозданияПоДаннымКонтрагента и дополнительно:
//   * ДополнительныеРеквизиты - см. НовыеДополнительныеРеквизитыДляСозданияПоДаннымКонтрагента.
//
Функция НовыеРеквизитыНоменклатурыДляСозданияПоДаннымКонтрагента(ИсточникДанных = Неопределено) Экспорт
	
	Результат = Новый Структура;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(
		Результат,
		ИнтеграцияСМаркетплейсамиКлиентСервер.РеквизитыНоменклатурыДляСозданияПоДаннымКонтрагента(ИсточникДанных));
	
	Результат.Вставить("ДополнительныеРеквизиты", НовыеДополнительныеРеквизитыДляСозданияПоДаннымКонтрагента());
	
	Возврат Результат;
	
КонецФункции

// Формирует данные для создания элементов справочников Номенклатура, Характеристики номенклатуры,
// Упаковки и единицы измерения по переданным данным номенклатуры контрагентов.
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - 
//  ДанныеКонтрагента - см. НоваяТаблицаДанныхКонтрагентаДляСозданияНоменклатуры
//  РеквизитыНоменклатуры - см. НовыеРеквизитыНоменклатурыДляСозданияПоДаннымКонтрагента
// 
// Возвращаемое значение:
//  см. НовыеДанныеДляСозданияНоменклатурыПоДаннымКонтрагента
//
Функция ДанныеДляСозданияНоменклатурыПоДаннымКонтрагента(УчетнаяЗапись, ДанныеКонтрагента, РеквизитыНоменклатуры) Экспорт
	
	Результат = НовыеДанныеДляСозданияНоменклатурыПоДаннымКонтрагента();
	
	УстановитьСсылкиНаНоменклатуруВДанныхКонтрагента(ДанныеКонтрагента);
	ВыполнитьПоискХарактеристикВДанныхКонтрагента(ДанныеКонтрагента, РеквизитыНоменклатуры);
	ВыполнитьПоискУпаковокВДанныхКонтрагента(ДанныеКонтрагента, РеквизитыНоменклатуры);
	
	Для Каждого Строка Из ДанныеКонтрагента Цикл
		
		ДобавитьОписаниеДанныхСоздаваемойНоменклатуры(
			Результат.СоздаваемаяНоменклатура, РеквизитыНоменклатуры, Строка);
		
		ДобавитьОписаниеДанныхСоздаваемойХарактеристики(
			Результат.СоздаваемыеХарактеристики, РеквизитыНоменклатуры, Строка);
		
		ДобавитьОписаниеДанныхСоздаваемойУпаковки(
			Результат.СоздаваемыеУпаковки, РеквизитыНоменклатуры, Строка);
		
	КонецЦикла;
	
	РеквизитыУчетнойЗаписи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		УчетнаяЗапись, "ИсточникКатегории, ВидМаркетплейса");
		
	Категория1С = Категория1СДляСозданияНоменклатурыПоДаннымКонтрагента(
		РеквизитыУчетнойЗаписи.ИсточникКатегории, РеквизитыНоменклатуры);
	
	Если РеквизитыУчетнойЗаписи.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		
		ИнтеграцияСМаркетплейсомOzonСервер.ПриЗаполненииДанныхДляСозданияНоменклатурыПоДаннымКонтрагента(
			УчетнаяЗапись, Категория1С, ДанныеКонтрагента, Результат);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Создает элементы справочников Номенклатура, Характеристики номенклатуры, Упаковки и единицы измерения по переданным
// данным номенклатуры контрагентов.
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - 
//  ДанныеКонтрагента - см. НоваяТаблицаДанныхКонтрагентаДляСозданияНоменклатуры
//  РеквизитыНоменклатуры - см. НовыеРеквизитыНоменклатурыДляСозданияПоДаннымКонтрагента
// 
// Возвращаемое значение:
//  Массив из см. НовыйРезультатСозданияНовойНоменклатуры
//
Функция СоздатьНоменклатуруПоДаннымКонтрагента(УчетнаяЗапись, ДанныеКонтрагента, РеквизитыНоменклатуры) Экспорт
	
	Результат = Новый Массив; // Массив из см. НовыйРезультатСозданияНовойНоменклатуры
	
	ДанныеДляСоздания = ДанныеДляСозданияНоменклатурыПоДаннымКонтрагента(
		УчетнаяЗапись, ДанныеКонтрагента, РеквизитыНоменклатуры);
		
	ПараметрыПрогресса = ПараметрыПрогресса();
	
	ПараметрыПрогресса.ШагИтерацииВПроцентах = 50;
	
	УстановитьПроцентПрогресса(1, 1, 100, ПараметрыПрогресса);
	
	ПараметрыПрогресса.ПрогрессВыполнения = 45;
	
	ПроцентВыполнения = 0;
	ИндексТекущегоОбъекта = 1;
	
	ВсегоСтрок =
		ДанныеДляСоздания.СоздаваемаяНоменклатура.Количество()
		+ ДанныеДляСоздания.СоздаваемыеХарактеристики.Количество()
		+ ДанныеДляСоздания.СоздаваемыеУпаковки.Количество();
	
	ЗначенияКонстантУпаковок = ЗначенияКонстантДляСозданияУпаковок();
	
	НачатьТранзакцию();
	
	Попытка
		
		Для Каждого КлючЗначение Из ДанныеДляСоздания.СоздаваемаяНоменклатура Цикл
			
			ОписаниеДанных = КлючЗначение.Значение;
			
			СоздатьНовыйЭлементСправочникаНоменклатура(ОписаниеДанных);
			
			УстановитьПроцентПрогресса(ВсегоСтрок, ИндексТекущегоОбъекта, ПроцентВыполнения, ПараметрыПрогресса);
			ИндексТекущегоОбъекта = ИндексТекущегоОбъекта + 1;
			
		КонецЦикла;
		
		Для Каждого КлючЗначение Из ДанныеДляСоздания.СоздаваемыеХарактеристики Цикл
			
			ОписаниеДанных = КлючЗначение.Значение;
			
			СоздатьНовыйЭлементСправочникаХарактеристикиНоменклатуры(РеквизитыНоменклатуры, ОписаниеДанных);
			
			УстановитьПроцентПрогресса(ВсегоСтрок, ИндексТекущегоОбъекта, ПроцентВыполнения, ПараметрыПрогресса);
			ИндексТекущегоОбъекта = ИндексТекущегоОбъекта + 1;
			
		КонецЦикла;
		
		Для Каждого КлючЗначение Из ДанныеДляСоздания.СоздаваемыеУпаковки Цикл
			
			ОписаниеДанных = КлючЗначение.Значение;
			
			СоздатьНовыйЭлементСправочникаУпаковкиЕдиницыИзмерения(
				РеквизитыНоменклатуры, ОписаниеДанных, ЗначенияКонстантУпаковок);
			
			УстановитьПроцентПрогресса(ВсегоСтрок, ИндексТекущегоОбъекта, ПроцентВыполнения, ПараметрыПрогресса);
			ИндексТекущегоОбъекта = ИндексТекущегоОбъекта + 1;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , УчетнаяЗапись, ТекстОшибки);
		
	КонецПопытки;
	
	Для Каждого Строка Из ДанныеКонтрагента Цикл
		
		НоменклатураИБ = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы(
			Строка.Номенклатура, Строка.Характеристика, Строка.Упаковка);
		
		СозданныйЭлемент = НовыйРезультатСозданияНовойНоменклатуры();
		
		СозданныйЭлемент.НоменклатураКонтрагента = Строка.НоменклатураКонтрагента;
		СозданныйЭлемент.НоменклатураИБ          = НоменклатураИБ;
		
		Результат.Добавить(СозданныйЭлемент);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СопоставлениеНоменклатуры

// См. ЭлектронноеВзаимодействиеПереопределяемый.ПриСозданииФормыПодсистемы.
Процедура ПриСозданииФормыСопоставленияНоменклатуры(Контекст, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Контекст.Назначение = "СопоставлениеНоменклатуры"
			И ТипЗнч(Контекст.Форма.ДополнительныеПараметрыПоиска) = Тип("Структура")
			И Контекст.Форма.ДополнительныеПараметрыПоиска.Свойство("СпособОбработки")
			И Контекст.Форма.ДополнительныеПараметрыПоиска.СпособОбработки = "СверкаДокументаСТорговойПлощадкой" Тогда
		Префикс = Контекст.Форма.ДополнительныеПараметрыПоиска.СпособОбработки + "_";
		
		КомандаНазад = Контекст.Форма.Команды.Добавить(Префикс + "Назад");
		КомандаНазад.Действие = "Подключаемый_КомандаДействие";
		КомандаНазад = Контекст.Форма.Команды.Добавить(Префикс + "Далее");
		КомандаНазад.Действие = "Подключаемый_КомандаДействие";
		КомандаЗакрыть = Контекст.Форма.Команды.Добавить(Префикс + "Закрыть");
		КомандаЗакрыть.Действие = "Подключаемый_КомандаДействие";
		КомандаПоказатьПодсказку = Контекст.Форма.Команды.Добавить(Префикс + "ПоказатьПодсказку");
		КомандаПоказатьПодсказку.Действие  = "Подключаемый_КомандаДействие";
		КомандаПоказатьПодсказку.Подсказка = НСтр("ru = 'Показать подсказку';
													|en = 'Show tooltip'");
		КомандаСкрытьПодсказку = Контекст.Форма.Команды.Добавить(Префикс + "СкрытьПодсказку");
		КомандаСкрытьПодсказку.Действие  = "Подключаемый_КомандаДействие";
		КомандаСкрытьПодсказку.Подсказка = НСтр("ru = 'Скрыть подсказку';
												|en = 'Hide tooltip'");
		
		Контекст.Форма.ОтображатьЗаголовок      = Ложь;
		Контекст.Форма.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
		
		ГруппаИнформация = Контекст.Форма.Элементы.Вставить(Префикс + "ГруппаИнформация", Тип("ГруппаФормы"), 
			Контекст.Форма.Элементы.ГруппаСопоставление, Контекст.Форма.Элементы.ГруппаОшибкаВыполненияПоиска);
		ГруппаИнформация.Вид                      = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаИнформация.Видимость                = Ложь;
		ГруппаИнформация.ОтображатьЗаголовок      = Ложь;
		ГруппаИнформация.РастягиватьПоГоризонтали = Истина;
		ГруппаИнформация.ЦветФона                 = ЦветаСтиля.ЦветФонаПодсказки;
			ДекорацияИнформация = Контекст.Форма.Элементы.Добавить(Префикс + "ДекорацияИнформация", Тип("ДекорацияФормы"), 
				ГруппаИнформация);
			ДекорацияИнформация.АвтоМаксимальнаяШирина   = Ложь;
			ДекорацияИнформация.Заголовок                = Новый ФорматированнаяСтрока(
				НСтр("ru = '1. Проверьте и при необходимости измените подобранный вариант сопоставления номенклатуры из файла с данными информационной базы.
					|2. Переход к следующему шагу помощника возможен после сопоставления всех позиций номенклатуры.
					|3. Запись позиций, которые были найдены по данным публикации товаров торговых площадок (выделены шрифтом), не производится. Изменения действуют только при работе с текущим помощником.
					|4. При нажатии кнопки ""Назад"" выполненные изменения аннулируются. Повторный поиск вариантов сопоставления будет выполнен без учета выполненных изменений.
					|5. Нажмите кнопку ""Далее"", чтобы перейти к следующему шагу помощника ""Обработка расхождений"".';
					|en = '1. Check and, if necessary, change the selected mapping option of items from the file with the infobase data.
					|2. You can proceed to the next step of the wizard after mapping all the items.
					|3. Items that were found based on the publication data of marketplace goods (in bold) are not recorded. Changes are valid only when using the current wizard.
					|4. When you click Back, the changes are canceled. A repeated search for mapping options will be performed without taking into account the changes made.
					|5. Click Next to proceed to the next ""Handling discrepancies"" wizard step.'"));
			ДекорацияИнформация.РастягиватьПоГоризонтали = Истина;
			ДекорацияИнформация.ЦветТекста               = ЦветаСтиля.ПоясняющийТекст;
			
			КнопкаСкрытьПодсказку = Контекст.Форма.Элементы.Добавить(Префикс + "СкрытьПодсказку", Тип("КнопкаФормы"), 
				ГруппаИнформация);
			КнопкаСкрытьПодсказку.Вид                            = ВидКнопкиФормы.Гиперссылка;
			КнопкаСкрытьПодсказку.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
			КнопкаСкрытьПодсказку.ИмяКоманды                     = Префикс + "СкрытьПодсказку";
			КнопкаСкрытьПодсказку.Картинка                       = БиблиотекаКартинок.ПолеВводаОчистить;
			КнопкаСкрытьПодсказку.Отображение                    = ОтображениеКнопки.Картинка;
			
		Контекст.Форма.Элементы.ВладелецОтбор.ТолькоПросмотр = Истина;
		Контекст.Форма.Элементы.ВладелецОтбор.КнопкаОчистки  = Ложь;
		
		ГруппаОтбор = Контекст.Форма.Элементы.ГруппаОтбор;
		ГруппаОтбор.РастягиватьПоГоризонтали = Истина;
			ДекорацияНадпись = Контекст.Форма.Элементы.Вставить(Префикс + "ДекорацияРазделитель1", Тип("ДекорацияФормы"), 
				ГруппаОтбор, Контекст.Форма.Элементы.СопоставленоОтбор);
			ДекорацияНадпись.РастягиватьПоГоризонтали = Ложь;
			
			КомандаСоздатьНоменклатуру = Контекст.Форма.Элементы.КомандаСоздатьНоменклатуру;
			Контекст.Форма.Элементы.Переместить(КомандаСоздатьНоменклатуру, ГруппаОтбор);
			
			ДекорацияНадпись = Контекст.Форма.Элементы.Вставить(Префикс + "ДекорацияРазделитель2", Тип("ДекорацияФормы"), 
				ГруппаОтбор, КомандаСоздатьНоменклатуру);
			ДекорацияНадпись.РастягиватьПоГоризонтали = Ложь;
			
			КнопкаПоказатьПодсказку = Контекст.Форма.Элементы.Добавить(Префикс + "ПоказатьПодсказку", Тип("КнопкаФормы"), 
				ГруппаОтбор);
			КнопкаПоказатьПодсказку.Вид                            = ВидКнопкиФормы.Гиперссылка;
			КнопкаПоказатьПодсказку.ГоризонтальноеПоложениеВГруппе = ГоризонтальноеПоложениеЭлемента.Право;
			КнопкаПоказатьПодсказку.ИмяКоманды                     = Префикс + "ПоказатьПодсказку";
			КнопкаПоказатьПодсказку.Картинка                       = БиблиотекаКартинок.Справка;
			КнопкаПоказатьПодсказку.Отображение                    = ОтображениеКнопки.Картинка;
		
		ГруппаКоманднаяПанель = Контекст.Форма.Элементы.Добавить(Префикс + "ГруппаКоманднаяПанель", Тип("ГруппаФормы"), 
			Контекст.Форма);
		ГруппаКоманднаяПанель.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ГруппаКоманднаяПанельЛево = Контекст.Форма.Элементы.Добавить(Префикс + "ГруппаКоманднаяПанельЛево", 
				Тип("ГруппаФормы"), ГруппаКоманднаяПанель);
			ГруппаКоманднаяПанельЛево.Вид = ВидГруппыФормы.КоманднаяПанель;    
			ГруппаКоманднаяПанельЛево.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
				КнопкаНазад = Контекст.Форма.Элементы.Добавить(Префикс + "КнопкаНазад", Тип("КнопкаФормы"), 
					ГруппаКоманднаяПанельЛево);
				КнопкаНазад.Заголовок = НСтр("ru = '< Назад';
											|en = '< Back'");
				КнопкаНазад.ИмяКоманды = Префикс + "Назад";
			ГруппаКоманднаяПанельПраво = Контекст.Форма.Элементы.Добавить(Префикс + "ГруппаКоманднаяПанельПраво", 
				Тип("ГруппаФормы"), ГруппаКоманднаяПанель);
			ГруппаКоманднаяПанельПраво.Вид = ВидГруппыФормы.КоманднаяПанель;    
			ГруппаКоманднаяПанельПраво.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
				КнопкаДалее = Контекст.Форма.Элементы.Добавить(Префикс + "КнопкаДалее", Тип("КнопкаФормы"), 
					ГруппаКоманднаяПанельПраво);
				КнопкаДалее.Заголовок = НСтр("ru = 'Далее >';
											|en = 'Next >'");
				КнопкаДалее.ИмяКоманды = Префикс + "Далее";
				КнопкаДалее.КнопкаПоУмолчанию = Истина;
				КнопкаЗакрыть = Контекст.Форма.Элементы.Добавить(Префикс + "КнопкаЗакрыть", Тип("КнопкаФормы"), 
					ГруппаКоманднаяПанельПраво);
				КнопкаЗакрыть.Заголовок = НСтр("ru = 'Закрыть';
												|en = 'Close'");
				КнопкаЗакрыть.ИмяКоманды = Префикс + "Закрыть";
				
		УсловноеОформление = Контекст.Форма.УсловноеОформление;
		ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();

		ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("СопоставлениеНоменклатура");
		ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("СопоставлениеХарактеристика");
		ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("СопоставлениеУпаковка");
		
		ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объект.Сопоставление.ВариантУказанияНоменклатуры");
		ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.ВариантУказанияНоменклатураОрганизации();

		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ВажнаяНадписьШрифт);
	КонецЕсли;
	
КонецПроцедуры

// Позволяет провести поиск вариантов по дополнительным натуральным ключам.
//
// Параметры:
//   ТаблицаНоменклатурыКонтрагентов - ТаблицаЗначений из см. СопоставлениеНоменклатурыКонтрагентовСлужебный.ПреобразоватьНаборНоменклатурыКонтрагентовВТаблицу.
//   ДополнительныеПараметры         - см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НовыеПараметрыСопоставленияНоменклатуры.
// 
Процедура ПодобратьВариантыСопоставленияНоменклатуры(ТаблицаНоменклатурыКонтрагентов, 
			Знач ДополнительныеПараметры) Экспорт
	
	НайтиВариантыСопоставленияНоменклатуры(ТаблицаНоменклатурыКонтрагентов, 
		ДополнительныеПараметры.ДополнительныеПараметрыПоиска, 
		Истина);
	
КонецПроцедуры

// Формирует список номенклатуры для сопоставления.
//
// Параметры:
//   ДанныеЭлектронногоДокумента - см. ОтражениеВУчетеЭДО.НовыеДанныеЭлектронногоДокументаДляОтраженияВУчете.
// 
// Возвращаемое значение:
//   Структура - данные для сопоставления:
//     * НаборНоменклатурыКонтрагентов - Массив из см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента.
//     * ДанныеРазбора                 - Неопределено,
//                                     - ТабличныйДокумент,
//                                     - ДвоичныеДанные,
//                                     - Произвольный,
//                                     - Структура - данные разбора:
//       ** ДеревоРазбора               - см. ДеревоЭлектронногоДокументаБЭД.ИнициализироватьДеревоРазбора.
//       ** НовыйЭД                     - см. ДеревоЭлектронногоДокументаБЭД.НайтиСоздатьТипОбъектаВДеревеРазбора.
//       ** Содержание                  - Неопределено.
//                                     - см. ПолучитьДанныеЭлектронногоДокумента,
//
Функция НоменклатураЭлектронногоДокументаДляСопоставления(ДанныеЭлектронногоДокумента) Экспорт
	
	Если ДанныеЭлектронногоДокумента.ТипДокумента = Перечисления.ТипыДокументовЭДО.Прикладной Тогда
		ИнформацияОТоварах = ИнформацияОТоварахИзТабличногоДокумента(ДанныеЭлектронногоДокумента);
		ДанныеРазбора      = ДанныеЭлектронногоДокумента.ДополнительныеДанные.ДанныеРазбора;
		
	Иначе
		УчетнаяЗапись = ДанныеЭлектронногоДокумента.ДополнительныеДанные.УчетнаяЗапись;	// СправочникСсылка.УчетныеЗаписиМаркетплейсов
		ВидТорговойПлощадки = ДанныеЭлектронногоДокумента.ДополнительныеДанные.ВидТорговойПлощадки;
		Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
			ВидТорговойПлощадки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
		КонецЕсли;
		
		ПараметрыПолучения = ЭлектронныеДокументыЭДО.НовыеПараметрыПолученияДанныхДокумента();
		ПараметрыПолучения.ОсновнойФайл = ДанныеЭлектронногоДокумента.ДанныеОсновногоФайла;
		
		ИнформацияОТоварах = ЭлектронныеДокументыЭДО.ДанныеНоменклатурыДокумента(ПараметрыПолучения);
		
		// Изменение параметров сопоставления
		Если ЗначениеЗаполнено(ВидТорговойПлощадки) Тогда
			Отказ = Истина;
			ДанныеРазбора = ПолучитьДанныеЭлектронногоДокумента(ДанныеЭлектронногоДокумента);
			
			Если ЗначениеЗаполнено(ДанныеРазбора) Тогда
				СтрокиТребующиеСопоставления             = СтрокиТребующиеСопоставления(ДанныеРазбора.НовыйЭД, Истина);
				ТоварыПодчиненыОднойКорневойСтрокеДерева = ТоварыПодчиненыОднойКорневойСтрокеДерева(СтрокиТребующиеСопоставления);
				
				Если ЗначениеЗаполнено(СтрокиТребующиеСопоставления) 
						И ТоварыПодчиненыОднойКорневойСтрокеДерева Тогда
					Для Каждого КлючИЗначение Из СтрокиТребующиеСопоставления Цикл
						Идентификатор         = "";
						КодНоменклатуры       = "";
						Штрихкод              = "";
						СтрокаТаблицыЗначений = Неопределено;
						
						ОпределитьКлючевыеРеквизитыСопоставления(ВидТорговойПлощадки, КлючИЗначение.Ключ, 
							Идентификатор, КодНоменклатуры, Штрихкод);
						
						Если ЗначениеЗаполнено(Идентификатор)
								Или ЗначениеЗаполнено(КодНоменклатуры)
								Или ЗначениеЗаполнено(Штрихкод) Тогда
							СтрокаТаблицыЗначений = ИнформацияОТоварах[КлючИЗначение.Ключ.Значение - 1];
							
							Если ЗначениеЗаполнено(Идентификатор) Тогда
								СтрокаТаблицыЗначений.ИД = Идентификатор;
								СтрокаТаблицыЗначений.Сопоставление.Вставить("Идентификатор", Идентификатор);
							КонецЕсли;
							
							Если ЗначениеЗаполнено(КодНоменклатуры) Тогда
								СтрокаТаблицыЗначений.КодТовара = КодНоменклатуры;
								СтрокаТаблицыЗначений.Сопоставление.Вставить("КодНоменклатуры", КодНоменклатуры);
							КонецЕсли;
							
							Если ЗначениеЗаполнено(Штрихкод)
									И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Штрихкод) Тогда
								СтрокаТаблицыЗначений.Штрихкод = Штрихкод;
								СтрокаТаблицыЗначений.Сопоставление.Вставить("ШтрихкодКомбинации", Штрихкод);
							КонецЕсли;
						КонецЕсли;
						
						ПутьКСопоставлению = КлючИЗначение.Ключ.ПолныйПуть + ".Сопоставление";
						ВариантУказанияНоменклатурыИБ = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(КлючИЗначение.Ключ, 
							ПутьКСопоставлению + ".ВариантУказанияНоменклатурыИБ", Ложь);
						НоменклатураИБ = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(КлючИЗначение.Ключ, 
							ПутьКСопоставлению + ".НоменклатураИБ", Ложь);
						ХарактеристикаИБ = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(КлючИЗначение.Ключ, 
							ПутьКСопоставлению + ".ХарактеристикаИБ", Ложь);
						УпаковкаИБ = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(КлючИЗначение.Ключ, 
							ПутьКСопоставлению + ".УпаковкаИБ", Ложь);
						
						Если ЗначениеЗаполнено(СтрокаТаблицыЗначений)
								И ЗначениеЗаполнено(НоменклатураИБ) Тогда
							СтрокаТаблицыЗначений.Сопоставление.Вставить("ВариантУказанияНоменклатурыИБ", ВариантУказанияНоменклатурыИБ);
							СтрокаТаблицыЗначений.Сопоставление.Вставить("НоменклатураИБ",                НоменклатураИБ);
							СтрокаТаблицыЗначений.Сопоставление.Вставить("ХарактеристикаИБ",              ХарактеристикаИБ);
							СтрокаТаблицыЗначений.Сопоставление.Вставить("УпаковкаИБ",                    УпаковкаИБ);
						КонецЕсли;
					КонецЦикла;
					
					Отказ = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			Если Отказ Тогда
				Для Каждого СтрокаТаблицыЗначений Из ИнформацияОТоварах Цикл
					Если Не ЗначениеЗаполнено(СтрокаТаблицыЗначений.ИД) 
							И ЗначениеЗаполнено(СтрокаТаблицыЗначений.КодТовара) Тогда
						СтрокаТаблицыЗначений.ИД = СтрокаТаблицыЗначений.КодТовара;
						СтрокаТаблицыЗначений.Сопоставление.Вставить("Идентификатор", СтрокаТаблицыЗначений.КодТовара);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Владелец = СопоставлениеНоменклатурыКонтрагентовСлужебный.ВладелецНоменклатурыКонтрагента(
		ДанныеЭлектронногоДокумента.Отправитель);
	
	НаборНоменклатурыКонтрагентов   = НаборНоменклатурыКонтрагентовПоИнформацииОТоварах(ИнформацияОТоварах, Владелец);
	ТаблицаНоменклатурыКонтрагентов = НоваяТаблицаНоменклатурыКонтрагентов(НаборНоменклатурыКонтрагентов);
	
	НайтиВариантыСопоставленияНоменклатуры(ТаблицаНоменклатурыКонтрагентов, 
		ДанныеЭлектронногоДокумента.ДополнительныеДанные, 
		Ложь);
		
	ТаблицаНоменклатурыКонтрагентов.Колонки.НоменклатураИБ.Имя   = "Номенклатура";
	ТаблицаНоменклатурыКонтрагентов.Колонки.Характеристика.Имя   = "НаименованиеХарактеристики";
	ТаблицаНоменклатурыКонтрагентов.Колонки.ХарактеристикаИБ.Имя = "Характеристика";
	ТаблицаНоменклатурыКонтрагентов.Колонки.УпаковкаИБ.Имя       = "Упаковка";
	
	НаборНоменклатурыКонтрагентов = ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаНоменклатурыКонтрагентов);
	
	Результат = Новый Структура;
	Результат.Вставить("НаборНоменклатурыКонтрагентов", НаборНоменклатурыКонтрагентов);
	Результат.Вставить("ДанныеРазбора",                 ДанныеРазбора);
	
	Возврат Результат;
	
КонецФункции

// Возвращает настройки, по которым выполняется поиск номенклатуры партнера и сопоставление ее полей.
//
// Параметры:
//   ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//
// Возвращаемое значение:
//   Соответствие из Строка - настройки, по которым выполняется поиск номенклатуры партнера и сопоставление ее полей:
//     * ПоляСопоставления - Соответствие Из Строка - сопоставление полей поиска:
//         ** Ключ     - Строка - поле источника данных.
//         ** Значение - Строка - поле номенклатуры партнера.
//     * ПоляПоиска        - Соответствие из Строка - сопоставление полей заполнения номенклатуры партнера:
//         ** Ключ     - Строка - поле источника данных.
//         ** Значение - Строка - поле номенклатуры партнера.
//
Функция НастройкиСопоставленияНоменклатуры(ВидМаркетплейса) Экспорт

	Настройки = Новый Структура;
	Настройки.Вставить("ПоляПоиска",        Новый Соответствие);
	Настройки.Вставить("ПоляСопоставления", Новый Соответствие);
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		Настройки.ПоляСопоставления.Вставить("ПредставлениеОбъектаМаркетплейса", "ПредставлениеТовара");
		Настройки.ПоляСопоставления.Вставить("ИдентификаторПубликации",          "Идентификатор");
		Настройки.ПоляСопоставления.Вставить("ИдентификаторПубликации",          "ИдентификаторКакАртикул");
		Настройки.ПоляСопоставления.Вставить("ИдентификаторОбъектаМаркетплейса", "ИдентификаторТовара");
		
		СтатусНедействителен = "Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовМаркетплейсаOzon.ВАрхиве),
								|			ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовМаркетплейсаOzon.НеИспользуется))";
		Настройки.ПоляСопоставления.Вставить(СтатусНедействителен, "Недействителен");
		
		Настройки.ПоляПоиска.Вставить("ИдентификаторПубликации",          "Идентификатор");
		Настройки.ПоляПоиска.Вставить("ИдентификаторОбъектаМаркетплейса", "ИдентификаторНоменклатуры");
	КонецЕсли;
	
	Возврат Настройки;

КонецФункции

// Подготавливает шаблон текста запроса поиска номенклатуры партнера по переданным настройкам.
//
// Параметры:
//   НастройкиСопоставленияНоменклатуры - см. НастройкиСопоставленияНоменклатуры.
//   ДополнительноеУсловие - Строка - дополнительное условие соединений для поиска.
//                                    Например: "И ИсточникДанных.НоменклатураПартнера = НЕОПРЕДЕЛЕНО".
//
// Возвращаемое значение:
//   Структура - настройки текста запроса:
//     * ПоляНоменклатурыПартнера - Строка - описание полей для заполнения номенклатуры партнера.
//     * РезультатПоиска          - Строка - строка заполнения значения поля по результатам применения соединений для поиска.
//     * СоединенияПоиска         - Строка - строка соединений для поиска.
//
Функция НастройкиЗапросаПоискаНоменклатуры(НастройкиСопоставленияНоменклатуры, ДополнительноеУсловие = "") Экспорт

	Настройки = Новый Структура;
	Настройки.Вставить("ПоляНоменклатурыПартнера", "");
	Настройки.Вставить("РезультатПоиска",          "");
	Настройки.Вставить("СоединенияПоиска",         "");
	
	Если НастройкиСопоставленияНоменклатуры.ПоляСопоставления.Количество() = 0 Тогда
		Настройки.ПоляНоменклатурыПартнера = "НЕОПРЕДЕЛЕНО";
	Иначе
		ШаблонПоля = "ИсточникДанных.%1" + " " + "КАК" + " " + "%2";
		
		ОписаниеПолей = Новый Массив;
		Для Каждого Поле Из НастройкиСопоставленияНоменклатуры.ПоляСопоставления Цикл
			ОписаниеПолей.Добавить(СтрШаблон(ШаблонПоля, Поле.Ключ, Поле.Значение));
		КонецЦикла;
		
		Настройки.ПоляНоменклатурыПартнера = СтрСоединить(ОписаниеПолей, "," + Символы.ПС + "	");
	КонецЕсли;
	
	Если НастройкиСопоставленияНоменклатуры.ПоляПоиска.Количество() > 0 Тогда
		ПолеНоменклатурыПартнера = "НЕОПРЕДЕЛЕНО";
		
		ИндексПоиска = 0;
		ШаблонСоединения = "
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК СправочникНоменклатураКонтрагентов%1
		|		ПО (СправочникНоменклатураКонтрагентов%1.Владелец = &ВладелецКаталога)
		|			И ИсточникДанных.%2 = СправочникНоменклатураКонтрагентов%1.%3";
		
		Если Не ПустаяСтрока(ДополнительноеУсловие) Тогда
			ШаблонСоединения = ШаблонСоединения + " " + ДополнительноеУсловие;
		КонецЕсли;
		
		СоединенияПоиска = Новый Массив;
		Для Каждого Поле Из НастройкиСопоставленияНоменклатуры.ПоляПоиска Цикл
			ИндексПоиска = ИндексПоиска + 1;
			
			ШаблонПоля = СтрШаблон("ЕСТЬNULL(СправочникНоменклатураКонтрагентов%1.Ссылка, НЕОПРЕДЕЛЕНО)", ИндексПоиска);
			
			ПолеНоменклатурыПартнера = СтрЗаменить(ПолеНоменклатурыПартнера, "НЕОПРЕДЕЛЕНО", ШаблонПоля);
			СоединенияПоиска.Добавить(СтрШаблон(ШаблонСоединения, ИндексПоиска, Поле.Ключ, Поле.Значение));
		КонецЦикла;
		
		Если СоединенияПоиска.Количество() > 0 Тогда
			Настройки.РезультатПоиска = ПолеНоменклатурыПартнера;
			Настройки.СоединенияПоиска = СтрСоединить(СоединенияПоиска, Символы.ПС);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Настройки;

КонецФункции

// Параметры:
//   УчетнаяЗапись                 - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//   НаборНоменклатурыКонтрагентов - Массив из см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента
//
// Возвращаемое значение:
//  СправочникСсылка.ВидыНоменклатуры - вид номенклатуры.
//
Функция ВидНовойНоменклатуры(УчетнаяЗапись, НаборНоменклатурыКонтрагентов) Экспорт

	ВидНоменклатуры = Справочники.ВидыНоменклатуры.ПустаяСсылка();
	
	ИдентификаторыТоваров = Новый Массив;
	Для Каждого НоменклатураКонтрагента Из НаборНоменклатурыКонтрагентов Цикл
		ИдентификаторыТоваров.Добавить(НоменклатураКонтрагента.Артикул);
	КонецЦикла;
	
	РеквизитыУчетнойЗаписи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		УчетнаяЗапись,
		"ВидМаркетплейса, ИсточникКатегории");
	
	ТекстЗапроса = ТекстЗапросаПоискаВидаНоменклатуры(
		РеквизитыУчетнойЗаписи.ВидМаркетплейса,
		РеквизитыУчетнойЗаписи.ИсточникКатегории);
	
	Если Не ПустаяСтрока(ТекстЗапроса) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("УчетнаяЗапись",         УчетнаяЗапись);
		Запрос.УстановитьПараметр("ИдентификаторыТоваров", ИдентификаторыТоваров);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.ИсточникЭтоГруппа Тогда
				ВидНоменклатуры = ВидНоменклатурыПодчиненныхЭлементов(
					Выборка.Источник,
					РеквизитыУчетнойЗаписи.ИсточникКатегории);
			Иначе
				ВидНоменклатуры = Выборка.ВидНоменклатуры;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВидНоменклатуры;

КонецФункции

#КонецОбласти

// Получение двоичных данных по адресу URL, например изображения.
//
// Параметры:
//   ВнешнийАдрес - Строка - адрес URL картинки.
// 
// Возвращаемое значение:
//   См. ПолучениеФайловИзИнтернета.СкачатьФайлВоВременноеХранилище.
//
Функция ДанныеФайлаПоВнешнейСсылке(Знач ВнешнийАдрес) Экспорт
	
	Ответ = ПолучениеФайловИзИнтернета.СкачатьФайлВоВременноеХранилище(ВнешнийАдрес, Неопределено, Истина);
	Возврат Ответ;
	
КонецФункции

// Проверяет является ли указанное значение файлом картинки.
// 
// Параметры:
//   ПроверяемоеЗначение - ДвоичныеДанные, Строка - данные картинки или адрес во временном хранилище.
// 
// Возвращаемое значение:
//   Булево - это файл картинки.
//
Функция ЭтоФайлКартинки(ПроверяемоеЗначение) Экспорт
	
	ЭтоФайлКартинки = Истина;
	ДвоичныеДанныеИзображения = ПроверяемоеЗначение;
	
	Если ЭтоАдресВременногоХранилища(ПроверяемоеЗначение) Тогда
		ДвоичныеДанныеИзображения = ПолучитьИзВременногоХранилища(ПроверяемоеЗначение);
	КонецЕсли;
	
	МеханизмКартинка = Новый Картинка(ДвоичныеДанныеИзображения);
	Если МеханизмКартинка.Формат() = Неопределено 
			Или МеханизмКартинка.Формат() = ФорматКартинки.НеизвестныйФормат Тогда
		ЭтоФайлКартинки = Ложь;
	КонецЕсли;
	
	Возврат ЭтоФайлКартинки;
	
КонецФункции

// Возвращает возможные расширения картинок.
//
// Возвращаемое значение:
//   Строка - список расширений картинок: "JPG JPEG JP2 JPG2 PNG BMP TIFF".
//
Функция РасширенияКартинок() Экспорт

	Возврат "JPG JPEG JP2 JPG2 PNG BMP TIFF";

КонецФункции

// Добавляет реквизит формы УчетнаяЗаписьМаркетплейса и заполняет его значением из параметров формы.
// Вызывается из форм обработок ПодборТоваровПоОтбору и ПодборТоваровВДокументПродажи.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - изменяемая форма.
//
Процедура ДобавитьЗаполнитьУчетнуюЗапись(Форма) Экспорт

	ИспользоватьИнтеграциюСOzon         = ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСOzon");
	ИспользоватьИнтеграциюСЯндексМаркет = ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет");

	Если Не ИспользоватьИнтеграциюСOzon 
			И Не ИспользоватьИнтеграциюСЯндексМаркет Тогда
		Возврат;
	КонецЕсли;

	РеквизитУчетнаяЗапись = Новый РеквизитФормы("УчетнаяЗаписьМаркетплейса", Новый ОписаниеТипов("СправочникСсылка.УчетныеЗаписиМаркетплейсов"));
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(РеквизитУчетнаяЗапись);
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);

	Форма.Параметры.Свойство("УчетнаяЗаписьМаркетплейса", Форма.УчетнаяЗаписьМаркетплейса);

КонецПроцедуры

// Заполняет параметр учетной записи в компоновщике настроек.
//
// Параметры:
//   СтруктураНастроек - Структура - настройки:
//     * КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - настройки КД, в которых содержатся необходимые 
//                               параметры.
//   Компоновщик       - КомпоновщикНастроекКомпоновкиДанных - настройки КД, для которых требуется установить параметры.
//
Процедура ЗаполнитьУчетнуюЗапись(СтруктураНастроек, Компоновщик) Экспорт

	Если СтруктураНастроек.КомпоновщикНастроек <> Неопределено Тогда
		ПараметрУчетнаяЗапись = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(СтруктураНастроек.КомпоновщикНастроек.Настройки, "УчетнаяЗаписьМаркетплейса");
		Если ПараметрУчетнаяЗапись <> Неопределено 
				И ЗначениеЗаполнено(ПараметрУчетнаяЗапись.Значение) Тогда
			КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
				Компоновщик.Настройки, "УчетнаяЗаписьМаркетплейса",
				ПараметрУчетнаяЗапись.Значение);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Дополняет схему компоновки данных для маркетплейсов. 
// Выполняет инъекцию в текст запроса СКД для ценообразования. Вызывается из обработки ПодборТоваровПоОтбору.
// 
// Параметры:
//   СтруктураНастроек     - Структура - данные, структура настроек;
//   СхемаКомпоновкиДанных - СхемаКомпоновкиДанных - модифицируемая схема.
//
Процедура ДополнитьСКДДляМаркетплейсов(СтруктураНастроек, СхемаКомпоновкиДанных) Экспорт

	ИспользоватьИнтеграциюСOzon         = ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСOzon");
	ИспользоватьИнтеграциюСЯндексМаркет = ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет");
	ИспользуетсяЦенообразование25       = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25();

	Если Не ИспользоватьИнтеграциюСOzon 
			И Не ИспользоватьИнтеграциюСЯндексМаркет Тогда
		Возврат;
	КонецЕсли;
	
	//@skip-check bsl-legacy-check-string-literal
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктураНастроек.УчетнаяЗаписьМаркетплейса, "ВидМаркетплейса");

	РабочийНаборДанных = СхемаКомпоновкиДанных.НаборыДанных[0];

	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.РежимКомпоновкиДанных = Истина;
	СхемаЗапроса.УстановитьТекстЗапроса(РабочийНаборДанных.Запрос);

	ЗапросПакета = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1];
	ОператорВыбрать = ЗапросПакета.Операторы[0];
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ИмяТаблицы = "РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon";
		УсловиеСоединения =
			"(СтатусыПубликации.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса)
			|И (СтатусыПубликации.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар))
			|И (СтатусыПубликации.Номенклатура = ИсходныеДанныеПоследнийЗапрос.Номенклатура)
			|И (СтатусыПубликации.Характеристика = ИсходныеДанныеПоследнийЗапрос.Характеристика)
			|И &УсловиеПоУпаковке
			|И (СтатусыПубликации.ОбъектПубликации = НЕОПРЕДЕЛЕНО)";
		
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		ИмяТаблицы = "РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет";
		УсловиеСоединения =
			"(СтатусыПубликации.УчетнаяЗапись= &УчетнаяЗаписьМаркетплейса)
			|И (СтатусыПубликации.Номенклатура = ИсходныеДанныеПоследнийЗапрос.Номенклатура)
			|И (СтатусыПубликации.Характеристика = ИсходныеДанныеПоследнийЗапрос.Характеристика)
			|И &УсловиеПоУпаковке";
		
	Иначе
		Возврат;
	КонецЕсли;
	
	ТаблицаСтатусыПубликации = ЗапросПакета.ДоступныеТаблицы.Найти(ИмяТаблицы);
	Если ОператорВыбрать.Источники.НайтиПоИмени(ИмяТаблицы) <> Неопределено Тогда
		Возврат;	// Инъекция уже выполнена.
	КонецЕсли;
	
	ИсточникСтатусыПубликации = ОператорВыбрать.Источники.Добавить(ТаблицаСтатусыПубликации, "СтатусыПубликации");
	ИсточникСтатусыПубликации.Соединения.Очистить();

	Если ИспользуетсяЦенообразование25 Тогда
    	УсловиеСоединения = СтрЗаменить(УсловиеСоединения, "&УсловиеПоУпаковке", "(ВЫБОР
			|		КОГДА СтатусыПубликации.Упаковка = СтатусыПубликации.Номенклатура.ЕдиницаИзмерения
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|		ИНАЧЕ СтатусыПубликации.Упаковка
			|	КОНЕЦ = ИсходныеДанныеПоследнийЗапрос.Упаковка)");
	Иначе
		УсловиеСоединения = СтрЗаменить(УсловиеСоединения, "&УсловиеПоУпаковке", "ИСТИНА");
	КонецЕсли;

	Если ОператорВыбрать.Источники[0].Соединения.Добавить(ИсточникСтатусыПубликации, УсловиеСоединения) Тогда
		ПоследнееСоединение = ОператорВыбрать.Источники[0].Соединения[ОператорВыбрать.Источники[0].Соединения.Количество() - 1];
		ПоследнееСоединение.ОбязательноеСоединение = Ложь;
		ПоследнееСоединение.НачалоГруппыНеобязательныхСоединений = Истина;

		ТекстПоля = "НЕ СтатусыПубликации.Номенклатура ЕСТЬ NULL";
		ПсевдонимПоля = "ЯвляетсяТоваромМаркетплейса";
		ПолеПубликуетсяНаМП = ОператорВыбрать.ВыбираемыеПоля.Добавить(ТекстПоля);
		ВыражениеПубликуетсяНаМП = ОператорВыбрать.ВыраженияОтбораКомпоновкиДанных.Добавить(ПолеПубликуетсяНаМП);
		ВыражениеПубликуетсяНаМП.Псевдоним = ПсевдонимПоля;

		КолонкиСхемыЗапроса = ЗапросПакета.Колонки.Найти(ПолеПубликуетсяНаМП);
		КолонкиСхемыЗапроса.Псевдоним = ПсевдонимПоля;
		ЗапросПакета.ПоляВыбораКомпоновкиДанных.Добавить(КолонкиСхемыЗапроса);

		РабочийНаборДанных.Запрос = СхемаЗапроса.ПолучитьТекстЗапроса();

		ПолеНабора             = РабочийНаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
		ПолеНабора.Поле        = ПсевдонимПоля;
		ПолеНабора.ПутьКДанным = ПсевдонимПоля;

		ПараметрУчетнаяЗапись = СхемаКомпоновкиДанных.Параметры.Добавить();
		ПараметрУчетнаяЗапись.Имя = "УчетнаяЗаписьМаркетплейса";
		ПараметрУчетнаяЗапись.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.УчетныеЗаписиМаркетплейсов");
		ПараметрУчетнаяЗапись.ОграничениеИспользования = Истина;
		ПараметрУчетнаяЗапись.ВключатьВДоступныеПоля = Ложь;

		ПараметрыОтбора = Новый Структура("ВПользовательскиеНастройки", Истина);
		КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(СхемаКомпоновкиДанных.НастройкиПоУмолчанию, ПсевдонимПоля, Истина, ВидСравненияКомпоновкиДанных.Равно, Ложь, ПараметрыОтбора);
	КонецЕсли;

КонецПроцедуры

// Дополняет текст запроса для маркетплейсов.
// Выполняет инъекцию в текст запроса для подбора товаров. Вызывается из обработки ПодборТоваровВДокументПродажи.
//
// Параметры:
//   ШаблонТекстЗапроса        - Строка - текст запроса для динамического списка.
//   ТипСписка                 - Строка - может принимать значения: "СписокНоменклатура", "СписокХарактеристики", 
//                                 "СписокНоменклатураПартнера".
//   УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//
Процедура ДополнитьТекстЗапросаДляМаркетплейсов(ШаблонТекстЗапроса, ТипСписка, УчетнаяЗаписьМаркетплейса) Экспорт

	ИспользоватьИнтеграциюСOzon         = ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСOzon");
	ИспользоватьИнтеграциюСЯндексМаркет = ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет");

	Если Не ИспользоватьИнтеграциюСOzon 
			И Не ИспользоватьИнтеграциюСЯндексМаркет Тогда
		Возврат;
	КонецЕсли;
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗаписьМаркетплейса, "ВидМаркетплейса");

	Если ТипСписка <> "СписокНоменклатура" И ТипСписка <> "СписокХарактеристики" Тогда
		Возврат;
	КонецЕсли;

	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.РежимКомпоновкиДанных = Истина;
	СхемаЗапроса.УстановитьТекстЗапроса(ШаблонТекстЗапроса);

	ЗапросПакета = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1];
	ОператорВыбрать = ЗапросПакета.Операторы[0];

	КолонкаНоменклатура = ЗапросПакета.Колонки.Найти("Номенклатура");
	ЗначениеПоляНоменклатура = Строка(КолонкаНоменклатура.Поля[0]);
	КолонкаХарактеристика = ЗапросПакета.Колонки.Найти("Характеристика");
	Если КолонкаХарактеристика = Неопределено Тогда
		ЗначениеПоляХарактеристика = "ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)";
	Иначе
		ЗначениеПоляХарактеристика = Строка(КолонкаХарактеристика.Поля[0]);
	КонецЕсли;
	КолонкаУпаковка = ЗапросПакета.Колонки.Найти("Упаковка");
	ЗначениеПоляУпаковка = Строка(КолонкаУпаковка.Поля[0]);

	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ИмяТаблицы = "РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon";
		УсловиеСоединения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"(СтатусыПубликации.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса)
			|И (СтатусыПубликации.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар))
			|И (СтатусыПубликации.Номенклатура = %1)
			|И (СтатусыПубликации.Характеристика = %2)
			|И (СтатусыПубликации.Упаковка = %3)
			|И (СтатусыПубликации.ОбъектПубликации = НЕОПРЕДЕЛЕНО)",
			ЗначениеПоляНоменклатура, ЗначениеПоляХарактеристика, ЗначениеПоляУпаковка);
		
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		ИмяТаблицы = "РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет";
		УсловиеСоединения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"(СтатусыПубликации.УчетнаяЗапись = &УчетнаяЗаписьМаркетплейса)
			|И (СтатусыПубликации.Номенклатура = %1)
			|И (СтатусыПубликации.Характеристика = %2)
			|И (ВЫБОР
			|		КОГДА СтатусыПубликации.Упаковка = СтатусыПубликации.Номенклатура.ЕдиницаИзмерения
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|		ИНАЧЕ СтатусыПубликации.Упаковка
			|	КОНЕЦ = %3)",
			ЗначениеПоляНоменклатура, ЗначениеПоляХарактеристика, ЗначениеПоляУпаковка);
		
	Иначе
		Возврат;
	КонецЕсли;
	
	ТаблицаСтатусыПубликации = ЗапросПакета.ДоступныеТаблицы.Найти(ИмяТаблицы);
	Если ОператорВыбрать.Источники.НайтиПоИмени(ИмяТаблицы) <> Неопределено Тогда
		Возврат;	// Инъекция уже выполнена.
	КонецЕсли;
	ИсточникСтатусыПубликации = ОператорВыбрать.Источники.Добавить(ТаблицаСтатусыПубликации);
	ИсточникСтатусыПубликации.Источник.Псевдоним = "СтатусыПубликации";
	ИсточникСтатусыПубликации.Соединения.Очистить();

	ОператорВыбрать.Источники[0].Соединения.Добавить(ИсточникСтатусыПубликации, УсловиеСоединения);

	ТекстПоля = "НЕ СтатусыПубликации.Номенклатура ЕСТЬ NULL";
	ПсевдонимПоля = "ПубликуетсяНаМаркетплейсе";
	ПолеПубликуетсяНаМП = ОператорВыбрать.ВыбираемыеПоля.Добавить(ТекстПоля);
	ВыражениеПубликуетсяНаМП = ОператорВыбрать.ВыраженияОтбораКомпоновкиДанных.Добавить(ПолеПубликуетсяНаМП);
	ВыражениеПубликуетсяНаМП.Псевдоним = ПсевдонимПоля;

	КолонкиСхемыЗапроса = ЗапросПакета.Колонки.Найти(ПолеПубликуетсяНаМП);
	КолонкиСхемыЗапроса.Псевдоним = ПсевдонимПоля;

	ЗапросПакета.ПоляВыбораКомпоновкиДанных.Добавить(КолонкиСхемыЗапроса);

	ШаблонТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();

КонецПроцедуры

// Устанавливает условное оформление динамических списков.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма клиентского приложения, для которой устанавливается условное оформление.
//
Процедура УстановитьУсловноеОформлениеДинамическихСписков(Форма) Экспорт

	ИспользоватьИнтеграциюСOzon         = ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСOzon");
	ИспользоватьИнтеграциюСЯндексМаркет = ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСЯндексМаркет");

	Если Не ИспользоватьИнтеграциюСOzon 
			И Не ИспользоватьИнтеграциюСЯндексМаркет Тогда
		Возврат;
	КонецЕсли;

	Форма.СписокНоменклатура.УстановитьОбязательноеИспользование("ПубликуетсяНаМаркетплейсе", Истина);

	Элемент = Форма.УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.СписокСтандартныйПоискНоменклатура.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.СписокРасширенныйПоискНоменклатура.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатура.ПубликуетсяНаМаркетплейсе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекста);

	Форма.СписокХарактеристики.УстановитьОбязательноеИспользование("ПубликуетсяНаМаркетплейсе", Истина);

	Элемент = Форма.УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.СписокСтандартныйПоискХарактеристики.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Форма.Элементы.СписокРасширенныйПоискХарактеристики.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокХарактеристики.ПубликуетсяНаМаркетплейсе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекста);

КонецПроцедуры

// Подготавливает состав результата записи загружаемых данных.
// 
// Возвращаемое значение:
//   Структура - результат записи:
//     * ОписаниеОшибки  - Строка - Описание ошибки.
//     * СсылкаНаОбъект  - Неопределено, ЛюбаяСсылка - ссылка на созданный при загрузке объект.
//     * Ссылки          - Массив из ЛюбаяСсылка - все записанные ссылки для заполнения параметра отбора динамического
//                           списка формы Справочники.УчетныеЗаписиМаркетплейсов.Формы.ВыгрузкаЗагрузкаДанных.
//     * СостоянияСсылок - Соответствие из ДокументСсылка - описание ошибки.
//     * Служебное       - Произвольный, Неопределено - любые дополнительные данные.
//
Функция РезультатЗаписиЗагружаемыхДанных() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ОписаниеОшибки",  "");
	Результат.Вставить("СсылкаНаОбъект",  Неопределено);
	Результат.Вставить("Ссылки",          Новый Массив);
	Результат.Вставить("СостоянияСсылок", Новый Соответствие);
	Результат.Вставить("Служебное",       Неопределено);
	
	Возврат Результат;
	
КонецФункции

// Подготавливает пустую таблицу для объекта метаданных.
//
// Параметры:
//   ИмяОбъектаМетаданных      - Строка - имя объекта метаданных, например:"РегистрСведений.ЗаказыТорговыхПлощадок".
//   ПривилегированныйРежим    - Булево - признак чтения таблицы под полными правами.
//   ИсключаемыеТипыДляКолонок - Неопределено, Соответствие из Строка - имя колонки и исключаемый ее тип.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица с составом полей переданного объекта метаданных:
//     * ИмяКолонкиТаблицы - Произвольный - колонка как стандартный реквизит, реквизит, измерение, ресурс.
//
Функция ПустаяТаблицаОбъектаМетаданных(ИмяОбъектаМетаданных, ПривилегированныйРежим = Ложь,
			ИсключаемыеТипыДляКолонок = Неопределено) Экспорт

	ТекстЗапроса = СтрЗаменить(
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	*
		|ИЗ
		|	&ОбъектМетаданных КАК ОбъектМетаданных",
		"&ОбъектМетаданных",
		ИмяОбъектаМетаданных);
	
	Если ПривилегированныйРежим Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	ТаблицаАнализа = Запрос.Выполнить().Выгрузить();
	
	Если ИсключаемыеТипыДляКолонок = Неопределено Тогда
		ИсключаемыеТипыДляКолонок = Новый Соответствие;
	КонецЕсли;
	
	ДобавляемыеКолонки = Новый Структура;
	Для Каждого Колонка Из ТаблицаАнализа.Колонки Цикл
		ИмяКолонки = Колонка.Имя;
		Типы = Колонка.ТипЗначения.Типы();
		
		ИсключаемыеТипы = Новый Массив;
		ИсключаемыеТипы.Добавить(Тип("Null"));
		
		Значение = ИсключаемыеТипыДляКолонок[ИмяКолонки];
		Если Значение = Тип("Массив") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
				ИсключаемыеТипы, Значение);
		ИначеЕсли Значение = Тип("Строка") Тогда
			ИсключаемыеТипы.Добавить(Значение);
		КонецЕсли;
		
		Для Каждого ИсключаемыйТип Из ИсключаемыеТипы Цикл
			ИндексЭлемента = Типы.Найти(ИсключаемыйТип);
			Если ИндексЭлемента <> Неопределено Тогда
				Типы.Удалить(ИндексЭлемента);
			КонецЕсли;
		КонецЦикла;
		
		Если Типы.Количество() = 0 Тогда
			Типы.Добавить(Тип("Неопределено"));
		КонецЕсли;
		
		ДобавляемыеКолонки.Вставить(ИмяКолонки, Новый ОписаниеТипов(Типы));
	КонецЦикла;
	
	ПустаяТаблица = Новый ТаблицаЗначений;
	Для Каждого Колонка Из ДобавляемыеКолонки Цикл
		ПустаяТаблица.Колонки.Добавить(Колонка.Ключ, Колонка.Значение);
	КонецЦикла;
	
	Возврат ПустаяТаблица;

КонецФункции

// Формирует текст запроса для выбора всех полей объекта метаданных.
//
// Параметры:
//   ИмяОбъектаМетаданных - Строка - имя объекта метаданных, например:"РегистрСведений.ЗаказыТорговыхПлощадок".
//   ПсевдонимТаблицы     - Строка - имя таблицы в запросе, из которой выполняется выборка данных.
//   ИсключаемыеПоля      - Неопределено, Массив из Строка - исключаемые из запроса поля. Для исключения полей
//                          табличных частей необходимо их указать в виде "ИмяТабличнойЧасти.ИмяИсключаемогоПоля".
//   ПрефиксПолей         - Строка - префикс для полей, выбираемых в запросе.
//
// Возвращаемое значение:
//   Строка - текст запроса для выбора полей объекта.
//
Функция ТекстЗапросаПолейОбъектаМетаданных(ИмяОбъектаМетаданных, ПсевдонимТаблицы, Знач ИсключаемыеПоля = Неопределено,
			ПрефиксПолей = "") Экспорт
	
	Если ИсключаемыеПоля = Неопределено Тогда
		ИсключаемыеПоля = Новый Массив;
	КонецЕсли;
	
	МетаданныеРегистра = ОбщегоНазначения.ОбъектМетаданныхПоПолномуИмени(ИмяОбъектаМетаданных);
	
	Кандидаты = Новый Структура;
	Кандидаты.Вставить("Измерения", Неопределено);
	Кандидаты.Вставить("Ресурсы", Неопределено);
	Кандидаты.Вставить("СтандартныеРеквизиты", Неопределено);
	Кандидаты.Вставить("Реквизиты", Неопределено);
	Кандидаты.Вставить("ТабличныеЧасти", Неопределено);
	
	ЗаполнитьЗначенияСвойств(Кандидаты, МетаданныеРегистра);
	
	ПустыеЗначенияПолей = Новый Соответствие;
	ДобавляемыеПоля = Новый Массив; // Массив из Строка
	
	ДобавитьПоляКоллекцииМетаданных(
		Кандидаты.Измерения, ДобавляемыеПоля, ИсключаемыеПоля, ПустыеЗначенияПолей);
	ДобавитьПоляКоллекцииМетаданных(
		Кандидаты.Ресурсы, ДобавляемыеПоля, ИсключаемыеПоля, ПустыеЗначенияПолей);
	ДобавитьПоляКоллекцииМетаданных(
		Кандидаты.СтандартныеРеквизиты, ДобавляемыеПоля, ИсключаемыеПоля, ПустыеЗначенияПолей);
	ДобавитьПоляКоллекцииМетаданных(
		Кандидаты.Реквизиты, ДобавляемыеПоля, ИсключаемыеПоля, ПустыеЗначенияПолей);
	
	ПоляРегистра = Новый Массив; // Массив из Строка
	
	Для Каждого ИмяПоля Из ДобавляемыеПоля Цикл
		
		ПустоеЗначение = ПустыеЗначенияПолей.Получить(ИмяПоля);
		ТекстПоля = СтрШаблон("ЕСТЬNULL(%1.%2, %3) КАК %4%2", ПсевдонимТаблицы, ИмяПоля, ПустоеЗначение, ПрефиксПолей);
		ПоляРегистра.Добавить(ТекстПоля);
		
	КонецЦикла;
	
	Если Кандидаты.ТабличныеЧасти <> Неопределено Тогда
		
		Для Каждого ТабличнаяЧасть Из Кандидаты.ТабличныеЧасти Цикл // ОбъектМетаданныхТабличнаяЧасть
			
			ИмяТабличнойЧасти = ТабличнаяЧасть.Имя;
			
			ДобавляемыеПоляТабличнойЧасти = Новый Массив;
			
			ДобавитьПоляКоллекцииМетаданных(
				ТабличнаяЧасть.СтандартныеРеквизиты,
				ДобавляемыеПоляТабличнойЧасти,
				ИсключаемыеПоля, ,
				ИмяТабличнойЧасти);
				
			ДобавитьПоляКоллекцииМетаданных(
				ТабличнаяЧасть.Реквизиты,
				ДобавляемыеПоляТабличнойЧасти,
				ИсключаемыеПоля, ,
				ИмяТабличнойЧасти);
			
			ПоляТабличнойЧасти = Новый Массив; // Массив из Строка
			
			Для Каждого ИмяПоля Из ДобавляемыеПоляТабличнойЧасти Цикл
				ПоляТабличнойЧасти.Добавить(СтрШаблон("	%1 КАК %1", ИмяПоля));
			КонецЦикла;
			
			ТекстЗапросаПоляТабличнойЧасти = СтрСоединить(ПоляТабличнойЧасти, "," + Символы.ПС);
			
			ТекстПоляВыборкиТабличнойЧасти = СтрШаблон(
				"%1.%2.(
				|	Ссылка КАК Ссылка,
				|%3
				|) КАК %4%2",
				ПсевдонимТаблицы,
				ИмяТабличнойЧасти,
				ТекстЗапросаПоляТабличнойЧасти,
				ПрефиксПолей);
			
			ПоляРегистра.Добавить(ТекстПоляВыборкиТабличнойЧасти);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Результат = СтрСоединить(ПоляРегистра, "," + Символы.ПС);
	
	Возврат Результат;
	
КонецФункции

// Отправляет уведомление с сервера на клиент через МенеджерУведомленийКлиента с ключом серверных уведомлений подсистемы.
//
// Параметры:
//  ИмяСобытияОповещения - Строка - оповещение для уточнения события, которое будет передано на клиент.
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//
Процедура ОтправитьСерверноеУведомлениеНаКлиент(ИмяСобытияОповещения, УчетнаяЗапись) Экспорт
	
	Если ПустаяСтрока(ИмяСобытияОповещения) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеУведомления = ИнтеграцияСМаркетплейсамиКлиентСервер.НовыеДанныеСерверногоУведомления();
	
	ДанныеУведомления.ИмяСобытияОповещения = ИмяСобытияОповещения;
	ДанныеУведомления.УчетнаяЗапись        = УчетнаяЗапись;
	
	УведомленияКлиента.ОтправитьУведомление(
		ИнтеграцияСМаркетплейсамиКлиентСервер.КлючСерверныхУведомлений(), ДанныеУведомления);
	
КонецПроцедуры

// Конструктор данных о причине отмены заказа.
// 
// Возвращаемое значение:
//  Структура:
//   * Идентификатор - Строка - идентификатор причины отмены заказа.
//   * Наименование - Строка - наименование причины отмены заказа.
// 
функция НовыеДанныеПричиныОтменыЗаказа() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Идентификатор", "");
	Результат.Вставить("Наименование", "");
	Возврат Результат;
	
КонецФункции

// Ищет причину отмены заказа клиента по данным регистра соответствия объектов, а затем, если не найдена, по наименованию. Если и по наименованию
// не найдена, то создает новую.
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//  ДанныеПричины - см. НовыеДанныеПричиныОтменыЗаказа
//  ИмяСобытияЖурналаРегистрации - Строка - имя события для записи ошибок в журнал регистрации.
//  КэшПричинОтмены - Соответствие из КлючИЗначение:
//   * Ключ - Строка - идентификатор причины отмены.
//   * Значение - СправочникСсылка.ПричиныОтменыЗаказовКлиентов
//
// Возвращаемое значение:
//  СправочникСсылка.ПричиныОтменыЗаказовКлиентов
//
Функция ПричинаОтменыЗаказаКлиента(УчетнаяЗапись, ДанныеПричины, ИмяСобытияЖурналаРегистрации = Неопределено, КэшПричинОтмены = Неопределено) Экспорт
	
	Если ИмяСобытияЖурналаРегистрации = Неопределено Тогда
		ИмяСобытияЖурналаРегистрации = СобытиеЖурналаРегистрации(УчетнаяЗапись);
	КонецЕсли;
	Если КэшПричинОтмены = Неопределено Тогда
		КэшПричинОтмены = Новый Соответствие;
	КонецЕсли;
	
	// Из кэша
	Результат = КэшПричинОтмены.Получить(ДанныеПричины.Идентификатор);
	Если Результат <> Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	// Из регистра соответствия
	Результат = РегистрыСведений.СоответствияОбъектовМаркетплейсов.ПолучитьОбъектСоответствия(
		УчетнаяЗапись,
		Перечисления.ВидыОбъектовМаркетплейсов.ПричинаОтмены,
		ДанныеПричины.Идентификатор,
		Тип("СправочникСсылка.ПричиныОтменыЗаказовКлиентов")); // СправочникСсылка.ПричиныОтменыЗаказовКлиентов
	
	Если Результат <> Неопределено Тогда
		ДобавитьПричинуОтменыВКэш(УчетнаяЗапись, Результат, ДанныеПричины, КэшПричинОтмены, Ложь);
		Возврат Результат;
	КонецЕсли;
	
	// По наименованию
	Выборка = ВыборкаПричиныОтменыЗаказаКлиентаПоНаименованию(ДанныеПричины.Наименование);
	Если Выборка <> Неопределено Тогда
		Результат = Выборка.Ссылка;
		ДобавитьПричинуОтменыВКэш(УчетнаяЗапись, Результат, ДанныеПричины, КэшПричинОтмены, Истина);
		Возврат Результат;
	КонецЕсли;
	
	// Новый элемент
	НачатьТранзакцию();
	Попытка
		ПричинаОтменыОбъект = Справочники.ПричиныОтменыЗаказовКлиентов.СоздатьЭлемент();
		ПричинаОтменыОбъект.Заполнить(Неопределено);
		ПричинаОтменыОбъект.Наименование = ДанныеПричины.Наименование;
		ПричинаОтменыОбъект.Записать();
		
		Результат = ПричинаОтменыОбъект.Ссылка;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При записи причины отмены ""%1"" возникла ошибка: %2';
				|en = 'An error occurred when saving the ""%1"" cancellation reason: %2'"),
			ДанныеПричины.Наименование,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(
			ИмяСобытияЖурналаРегистрации,
			УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстОшибки);
	КонецПопытки;
	
	ДобавитьПричинуОтменыВКэш(УчетнаяЗапись, Результат, ДанныеПричины, КэшПричинОтмены, ЗначениеЗаполнено(Результат));
	Возврат Результат;
	
КонецФункции

#Область ДлительныеОперации

// Сообщает о прогрессе выполнения длительной операции.
//
// Параметры:
//   ВсегоОбрабатываемыхОбъектов - Число - общее количество обрабатываемых за итерацию объектов.
//   ИндексТекущегоОбъекта       - Число - порядковый номер текущего объекта в итерации.
//   ВыполненоПроцентовИтерации  - Число - процент, установленный в предыдущем обращении в данную процедуру.
//   ПараметрыПрогресса          - Структура - характеристики прогресса, см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыПрогресса.
//
Процедура УстановитьПроцентПрогресса(Знач ВсегоОбрабатываемыхОбъектов, ИндексТекущегоОбъекта,
			ВыполненоПроцентовИтерации, ПараметрыПрогресса) Экспорт

	ВсегоОбрабатываемыхОбъектов = ?(ВсегоОбрабатываемыхОбъектов = 0, 1, ВсегоОбрабатываемыхОбъектов);

	ТекущийПроцентВыполнения = Окр(ИндексТекущегоОбъекта / ВсегоОбрабатываемыхОбъектов, 1);
	Если ВыполненоПроцентовИтерации = ТекущийПроцентВыполнения Тогда
		Возврат;
	КонецЕсли;

	ВыполненоПроцентовИтерации = ТекущийПроцентВыполнения;

	Процент = ИндексТекущегоОбъекта * (ПараметрыПрогресса.ШагИтерацииВПроцентах / ВсегоОбрабатываемыхОбъектов)
		+ ПараметрыПрогресса.ПрогрессВыполнения;
	МодульДлительныеОперации = ОбщегоНазначения.ОбщийМодуль("ДлительныеОперации");
	МодульДлительныеОперации.СообщитьПрогресс(Процент);

КонецПроцедуры

// Конструктор параметров прогресса.
//
// Возвращаемое значение:
//   Структура:
//     * ШагИтерацииВПроцентах - Число - максимальный размер итерации в процентах.
//     * ПрогрессВыполнения    - Число - текущий прогресс процесса до перехода к новой итерации.
//
Функция ПараметрыПрогресса() Экспорт

	ПараметрыПрогресса = Новый Структура;
	ПараметрыПрогресса.Вставить("ШагИтерацииВПроцентах", 100);
	ПараметрыПрогресса.Вставить("ПрогрессВыполнения", 0);
	
	Возврат ПараметрыПрогресса;

КонецФункции

#КонецОбласти

#Область Документы

// Определяет набор используемых функциональных опций.
//
// Параметры:
//   Соглашение - СправочникСсылка.СоглашенияСКлиентами - соглашение с клиентом;
//              - Неопределено - соглашения не используются.
//
// Возвращаемое значение:
//   Структура - используемые функциональные опции:
//     * ИспользоватьХарактеристикиНоменклатуры             - Булево - используются ли характеристики номенклатуры.
//     * ИспользоватьУпаковкиНоменклатуры                   - Булево - используются ли упаковки номенклатуры.
//     * ИспользоватьПартнеровИКонтрагентов                 - Булево - разделять ли партнеров и контрагентов.
//     * ИспользоватьСоглашенияСКлиентами                   - Булево - используется ли соглашения с клиентами.
//     * ИспользуютсяДоговорыКонтрагентов                   - Булево - используется ли договоры контрагентов.
//     * ИспользоватьНаправленияДеятельности                - Булево - используются ли направления деятельности.
//     * ИспользоватьНесколькоСкладов                       - Булево - используется ли несколько складов.
//     * ИспользоватьСкладыВТабличнойЧастиДокументовПродажи - Булево - разрешено ли выбирать склады в табличных частях документов продажи.
//     * ИспользоватьОрдернуюСхемуПриОтгрузке               - Булево - используется ли ордерная схема при отгрузке.
//     * ИспользоватьУчетНДС                                - Булево - используется ли учет НДС.
//     * ИспользуетсяЦенообразование25                      - Булево - используется ли ценообразование версии 2.5.
//     * КомиссионерВедетУчетПоРНПТ                         - Булево - ведет ли комиссионер учет по РНПТ.
//     * ИспользоватьЗаказыКлиентов                         - Булево - используются ли заказы клиентов. 
//     * ИспользоватьРасширенныеВозможностиЗаказаКлиента    - Булево - используются ли расширенные возможности заказов клиентов.  
//     * ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента       - Булево - используется ли построчная отгрузка в заказах клиентов.
//     * ИспользоватьОтгрузкуБезПереходаПраваСобственности  - Булево - используется ли реализация с отложенным переходом права собственности.
//     * ИспользоватьКорректировкиРеализаций                - Булево - используются ли корректировки реализаций.
//     * ИспользоватьКомиссиюПриПродажах                    - Булево - используются ли продажи через комиссионера.
//
Функция ПолучитьФункциональныеОпции(Соглашение = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьХарактеристикиНоменклатуры", ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры"));
	Результат.Вставить("ИспользоватьУпаковкиНоменклатуры",       ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры"));
	Результат.Вставить("ИспользоватьПартнеровИКонтрагентов",     ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов"));
	Результат.Вставить("ИспользоватьСоглашенияСКлиентами",       ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами"));
	Результат.Вставить("ИспользуютсяДоговорыКонтрагентов",       ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами"));
	Результат.Вставить("ИспользоватьНаправленияДеятельности",    ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоходовПоНаправлениямДеятельности"));
	Результат.Вставить("ИспользоватьНесколькоСкладов",           ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов"));
	Результат.Вставить("ИспользоватьОрдернуюСхемуПриОтгрузке",   ПолучитьФункциональнуюОпцию("ИспользоватьОрдернуюСхемуПриОтгрузке"));
	Результат.Вставить("ИспользоватьУчетНДС",                    ПолучитьФункциональнуюОпцию("ИспользоватьУчетНДС"));
	Результат.Вставить("ИспользуетсяЦенообразование25",          ПолучитьФункциональнуюОпцию("ИспользуетсяЦенообразование25"));
	
	Если Результат.ИспользоватьСоглашенияСКлиентами Тогда
		Результат.Вставить("ИспользуютсяДоговорыКонтрагентов",
			Результат.ИспользуютсяДоговорыКонтрагентов
				И ЗначениеЗаполнено(Соглашение)
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Соглашение, "ИспользуютсяДоговорыКонтрагентов"));
	КонецЕсли;
	
	Результат.Вставить("КомиссионерВедетУчетПоРНПТ",
		(Результат.ИспользоватьСоглашенияСКлиентами
			И Справочники.СоглашенияСКлиентами.КомиссионерВедетУчетПоРНПТ(Соглашение))
				Или УчетПрослеживаемыхТоваровЛокализация.ИспользоватьУчетПрослеживаемыхИмпортныхТоваров(ТекущаяДатаСеанса()));
				
	Результат.Вставить("ИспользоватьЗаказыКлиентов",                         ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыКлиентов"));
	Результат.Вставить("ИспользоватьРасширенныеВозможностиЗаказаКлиента",    ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"));
	Результат.Вставить("ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента",       ПолучитьФункциональнуюОпцию("ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента"));
	Результат.Вставить("ИспользоватьОтгрузкуБезПереходаПраваСобственности",
		ПолучитьФункциональнуюОпцию("ИспользоватьОтгрузкуБезПереходаПраваСобственности2_0"));
	Результат.Вставить("ИспользоватьКорректировкиРеализаций",                ПолучитьФункциональнуюОпцию("ИспользоватьКорректировкиРеализаций"));
	Результат.Вставить("ИспользоватьКомиссиюПриПродажах",                    ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах"));
	Результат.Вставить("ИспользоватьСкладыВТабличнойЧастиДокументовПродажи", ПолучитьФункциональнуюОпцию("ИспользоватьСкладыВТабличнойЧастиДокументовПродажи"));
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

// Включает отгрузку без перехода права собственности версии 2.0,
// в случае, если она не была включена.
//
Процедура ВключитьОтгрузкуБезПереходаПраваСобственности2_0() Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	Если НЕ Константы.ИспользоватьОтгрузкуБезПереходаПраваСобственности2_0.Получить() Тогда
			Константы.ИспользоватьОтгрузкуБезПереходаПраваСобственности2_0.Установить(Истина);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Выполняет блокировку документа от изменения другими режимами или пользователями.
//
// Параметры:
//   ДокументОбъект - ДокументОбъект - документ к блокировке.
//   ИмяСобытия     - Строка - имя события журнала регистрации. 
//
// Возвращаемое значение:
//   Строка - описание ошибки при выполнении блокировки документа.
//
Функция ЗаблокироватьДокумент(ДокументОбъект, ИмяСобытия) Экспорт
	
	ОписаниеОшибки = "";
	
	Попытка
		ДокументОбъект.Заблокировать();
		
	Исключение
		ШаблонОшибки = НСтр("ru = 'Не удалось заблокировать ""%1"" по причине: %2. Подробнее см. в журнале регистрации.';
							|en = 'Cannot lock ""%1"" due to: %2. For more information, see the Event log.'");
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			ДокументОбъект.Ссылка,
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка,
			ДокументОбъект.Метаданные().ПредставлениеОбъекта,
			ДокументОбъект.Ссылка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат ОписаниеОшибки;
	
КонецФункции

// Выполняет запись и (или) проведение документа.
//
// Параметры:
//   ДокументОбъект    - ДокументОбъект - документ к записи и (или) проведению.
//   Провести          - Булево - признак проведения документа. 
//   ИмяСобытия        - Строка - имя события журнала регистрации.
//   ЗаписатьПриОшибке - Булево - признак записи при неудачной попытке проведения документа.
//   ПроверитьЗаполнение - Булево - признак проверки заполнения.
//
// Возвращаемое значение:
//   См. НовыйРезультатЗаписиДокументов.
//
Функция ЗаписатьПровестиДокумент(ДокументОбъект, Провести, ИмяСобытия,
			ЗаписатьПриОшибке = Истина, ПроверитьЗаполнение = Истина) Экспорт
	
	Результат = НовыйРезультатЗаписиДокументов();
	
	ОписаниеОшибкиЗаписи    = "";
	ПодробноеОписаниеОшибки = "";
	
	Если ПроверитьЗаполнение Тогда
		ПродолжитьПроведение = ДокументОбъект.ПроверитьЗаполнение();
	Иначе
		ПродолжитьПроведение = Истина;
	КонецЕсли;
	
	ЗаписатьПриОшибке = ЗаписатьПриОшибке Или ДокументОбъект.ОбменДанными.Загрузка;
	
	Если Провести И ПродолжитьПроведение И Не ДокументОбъект.ОбменДанными.Загрузка Тогда
		// Проведение документа
		Попытка
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			УстановитьПривилегированныйРежим(Ложь);
			Результат.ЗаписьВыполнена = Истина;
			
		Исключение
			ОписаниеОшибкиЗаписи    = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ПодробноеОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КонецПопытки;
	КонецЕсли;
	
	Если Не Результат.ЗаписьВыполнена И ЗаписатьПриОшибке Тогда
		// Запись при неудачной попытке проведения
		Попытка
			Если Не ДокументОбъект.ЭтоНовый()
					И Не ДокументОбъект.ОбменДанными.Загрузка
					И ДокументОбъект.Проведен Тогда
				РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения;
			Иначе
				РежимЗаписи = РежимЗаписиДокумента.Запись;
			КонецЕсли;
			
			УстановитьПривилегированныйРежим(Истина);
			ДокументОбъект.Записать(РежимЗаписи);
			УстановитьПривилегированныйРежим(Ложь);
			Результат.ЗаписьВыполнена = Истина;
			
		Исключение
			ОписаниеОшибкиЗаписи    = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ПодробноеОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		КонецПопытки;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОписаниеОшибкиЗаписи) Тогда
		Результат.ОписаниеОшибки = ОписаниеОшибкиЗаписи;
		
		ЗаписьЖурналаРегистрации(ИмяСобытия,
			УровеньЖурналаРегистрации.Ошибка,
			ДокументОбъект.Метаданные(),
			ДокументОбъект.Ссылка,
			ПодробноеОписаниеОшибки);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Создает документы заказов клиента.
//
// Параметры:
//   Результат          - См. РезультатЗаписиЗагружаемыхДанных.
//   ТаблицаЗаказов     - ТаблицаЗначений - подготовленная таблица данных для загрузки.
//   Параметры          - Структура - параметры заполнения документов:
//     * ВидТорговойПлощадки          - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки;
//     * УчетнаяЗапись                - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки;
//     * ВалютаУчета                  - СправочникСсылка.Валюты, Неопределено - валюта учета учетной записи;
//     * Организация                  - СправочникСсылка.Организации - организация в шапке;
//     * Партнер                      - СправочникСсылка.Партнеры - партнер в шапке;
//     * Контрагент                   - СправочникСсылка.Контрагенты - контрагент в шапке;
//     * Соглашение                   - СправочникСсылка.СоглашенияСКлиентами - соглашение в шапке;
//     * Договор                      - СправочникСсылка.ДоговорыКонтрагентов - договор в шапке;
//     * ДанныеПокупателейПоЗаказам   - Соответствие Из Строка - данные о покупателе для каждого переданного заказа:
//         ** Ключ - Строка - идентификатор заказа;
//         ** Значение - см. НовыеДанныеПокупателяЗаказа.
//     * ХозяйственнаяОперация        - ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция в шапке;
//     * ПричинаОтменыПоУмолчанию     - СправочникСсылка.ПричиныОтменыЗаказовКлиентов - причина отмены по умолчанию;
//     * КодОшибки                    - Строка - код ошибки для результат выполнения;
//     * СобытиеЖурналаРегистрации    - Строка - наименование события для записей в журнале регистрации;
//     * ЗагружатьНезаполненныеСтроки - Булево - добавлять ли строки с несопоставленными показателями;
//     * ПерезаписатьДокументы        - Булево - перезаписывать ли существующие документы;
//     * ОчиститьТабличныеЧасти       - Булево - очищать табличные части документов перед обработкой;
//     * ОбработатьВариантОбеспечения - Булево - заполнять вариант обеспечения с возможным переносом строк;
//     * ПараметрыОбработки           - Структура Из КлючИЗначение, Неопределено - параметры обработки табличных частей документов;
//     * ТекстЗапроса                 - Строка - текст запроса для обработки заказов;
//     * ЗаписатьНеобработанныеЗаказы - Булево - записать необработанные заказы торговой площадки в специализированный регистр сведений.
//   ПараметрыПрогресса - См. ПараметрыПрогресса.
//
// Возвращаемое значение:
//   См. НоваяОшибка.
//
Функция ЗаписатьЗаказыКлиентов(Результат, ТаблицаЗаказов, Параметры, ПараметрыПрогресса = Неопределено) Экспорт
	
	Ошибка = НоваяОшибка();
	
	Партнер    = Параметры.Партнер;
	Контрагент = Параметры.Контрагент;
	Соглашение = Параметры.Соглашение;
	Договор    = Параметры.Договор;
	
	Если Не ЗначениеЗаполнено(Партнер)
			И Параметры.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию Тогда
		Партнер    = Справочники.Партнеры.РозничныйПокупатель;
		Контрагент = Справочники.Контрагенты.РозничныйПокупатель;
		Соглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
		Договор    = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
	ЗначенияФункциональныхОпций = ПолучитьФункциональныеОпции(Соглашение);
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ИнтеграцияСМаркетплейсамиСервер.ЗаписатьЗаказыКлиентов");
	
	ТорговаяПлощадка               = Параметры.ВидТорговойПлощадки;
	УчетнаяЗаписьПредставление       =
		Справочники.УчетныеЗаписиМаркетплейсов.ПолноеПредставлениеУчетнойЗаписи(
			Параметры.ВидТорговойПлощадки,
			Параметры.УчетнаяЗапись);
	ВалютаРегламентированногоУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Параметры.Организация);
	ДоступныеСостоянияДокументов   = ДоступныеСостоянияДокументов();
	СозданныеДокументы             = Новый Соответствие;
	НастройкиСкладов               = Новый Соответствие;
	ОкруглятьКоличество            = Истина;
	ЗаполнитьОбособленно           = ?(ЗначениеЗаполнено(Соглашение),
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Соглашение, "ОбеспечиватьЗаказыОбособленно"),
		Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	*
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок";
	ЗаказыТорговыхПлощадок = Запрос.Выполнить().Выгрузить();
	ЗаказыТорговыхПлощадок.Колонки.Добавить("СведенияПоТоварам");
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Дата",                  ТекущаяДатаСеанса());
	ДанныеЗаполнения.Вставить("Организация",           Параметры.Организация);
	ДанныеЗаполнения.Вставить("Валюта",                ?(ЗначениеЗаполнено(Параметры.ВалютаУчета), 
															Параметры.ВалютаУчета, 
															ВалютаРегламентированногоУчета));
	ДанныеЗаполнения.Вставить("ХозяйственнаяОперация", Параметры.ХозяйственнаяОперация);
	ДанныеЗаполнения.Вставить("Партнер",               Партнер);
	ДанныеЗаполнения.Вставить("Контрагент",            Контрагент);
	ДанныеЗаполнения.Вставить("Соглашение",            Соглашение);
	ДанныеЗаполнения.Вставить("Договор",               Договор);
	
	Если Параметры.ПараметрыОбработки = Неопределено Тогда
		Параметры.ПараметрыОбработки = Новый Структура;
		Параметры.ПараметрыОбработки.Вставить("СтруктураДействий",            Неопределено);
		Параметры.ПараметрыОбработки.Вставить("ПересчитатьКоличествоЕдиниц",  Ложь);
		Параметры.ПараметрыОбработки.Вставить("ЗаполнитьВариантОбеспечения",  Ложь);
		Параметры.ПараметрыОбработки.Вставить("ЗаполнитьЦены",                Ложь);
		Параметры.ПараметрыОбработки.Вставить("ОчиститьАвтоматическуюСкидку", Истина);
		Параметры.ПараметрыОбработки.Вставить("ПересчитатьЦенаВключаетНДС",   Истина);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = Параметры.ТекстЗапроса;
	
	ТаблицаСкладов = ПолучитьСопоставленныеСклады(Параметры.УчетнаяЗапись, Ложь, Истина); 
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",                    Параметры.УчетнаяЗапись);
	Запрос.УстановитьПараметр("ТаблицаЗаказов",                   ТаблицаЗаказов);
	Запрос.УстановитьПараметр("ТаблицаСкладов",                   ТаблицаСкладов);
	Запрос.УстановитьПараметр("ИспользоватьУпаковкиНоменклатуры", ЗначенияФункциональныхОпций.ИспользоватьУпаковкиНоменклатуры);
	Запрос.УстановитьПараметр("ВладелецКаталога",                 Параметры.ВладелецКаталога);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ТаблицаОстатков = РезультатЗапроса[РезультатЗапроса.ВГраница()].Выгрузить();
	ТаблицаОстатков.Индексы.Добавить("Номенклатура, Характеристика, Склад");
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
		Замер, 
		ТаблицаЗаказов.Количество() / 100);
		
	ВыборкаНомерДокумента = РезультатЗапроса[РезультатЗапроса.ВГраница() - 1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВсегоСтрок            = ВыборкаНомерДокумента.Количество();
	НомерТекущейСтроки    = 1;
	ПроцентВыполнения     = 0;
	
	Если ВсегоСтрок = 0 Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Данные для загрузки заказов с торговой площадки <%1> отсутствуют.';
				|en = 'Data for importing orders from the <%1> marketplace is missing.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Параметры.УчетнаяЗапись);
		
		Ошибка.КодОшибки      = Параметры.КодОшибки;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	СтатусыКонечные = Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовКонечные();
	КэшПричинОтмены = Новый Соответствие;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пока ВыборкаНомерДокумента.Следующий() Цикл
		Если ПараметрыПрогресса <> Неопределено Тогда
			УстановитьПроцентПрогресса(
				ВсегоСтрок,
				НомерТекущейСтроки,
				ПроцентВыполнения,
				ПараметрыПрогресса);
		КонецЕсли;
		
		ЗаказКлиентаОбъект      = Неопределено;
		ЗаказКлиентаСсылка      = Неопределено;
		ЗаказКлиентаСостояние   = ДоступныеСостоянияДокументов.СостояниеНеОбновлен;
		НовыйЗаказ              = Неопределено;
		ИдентификаторЭкземпляра = 1;
		
		ВыборкаДанных = ВыборкаНомерДокумента.Выбрать();
		Пока ВыборкаДанных.Следующий() Цикл
			Если ЗаказКлиентаОбъект = Неопределено Тогда
				ЗаказКлиентаСсылка = ВыборкаДанных.ЗаказКлиентаСсылка;
				
				Если Не ЗначениеЗаполнено(ЗаказКлиентаСсылка) Тогда
					ЗаказКлиентаСсылка = СозданныеДокументы.Получить(ВыборкаНомерДокумента.НомерДокумента);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ЗаказКлиентаСсылка) Тогда
					// Существующий документ
					Если Не Параметры.ПерезаписатьДокументы
							И Не ВыборкаДанных.ЗаказКлиентаПометкаУдаления Тогда
						Прервать;
					КонецЕсли;
					
					ЗаказКлиентаОбъект    = ЗаказКлиентаСсылка.ПолучитьОбъект();
					ЗаказКлиентаСостояние = ДоступныеСостоянияДокументов.СостояниеНеОбновлен;
					
					ТекстОшибки = ЗаблокироватьДокумент(ЗаказКлиентаОбъект, Параметры.СобытиеЖурналаРегистрации);
					
					Если Не ПустаяСтрока(ТекстОшибки) Тогда
						Если Ошибка.Детализация = Неопределено Тогда
							Ошибка.Детализация = Новый Массив;
						КонецЕсли;
						Ошибка.КодОшибки = Параметры.КодОшибки;
						Ошибка.Детализация.Добавить(ТекстОшибки);
						
						ЗаказКлиентаОбъект = Неопределено;
						
						Прервать;
					КонецЕсли;
					
				Иначе
					// Новый документ
					ЗаказКлиентаОбъект    = Документы.ЗаказКлиента.СоздатьДокумент();
					ЗаказКлиентаСостояние = ДоступныеСостоянияДокументов.СостояниеНовый;
				КонецЕсли;
				
				НовыйЗаказ = ЗаказыТорговыхПлощадок.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйЗаказ, ВыборкаДанных);
				НовыйЗаказ.УчетнаяЗапись                        = Параметры.УчетнаяЗапись;
				НовыйЗаказ.Заказ                                = Неопределено;
				НовыйЗаказ.ДокументОтгрузки                     = ВыборкаДанных.ДокументОтгрузкиСсылка;
				НовыйЗаказ.Статус                               = Перечисления.СтатусыЗаказовТорговыхПлощадок.ПолучитьПоПредставлению(
																	ВыборкаДанных.СтатусОтправления, 
																	ВыборкаДанных.ПодстатусОтправления,
																	ТорговаяПлощадка);
				НовыйЗаказ.СрокОтгрузки                         = ВыборкаДанных.ДатаОтгрузки;
				НовыйЗаказ.СрокДоставки                         = ВыборкаДанных.ДатаДоставки;
				НовыйЗаказ.ИдентификаторЗаказа                  = ВыборкаДанных.ИдентификаторЗаказа;
				НовыйЗаказ.НомерЗаказа                          = ВыборкаДанных.НомерЗаказа;
				НовыйЗаказ.НомерОтправления                     = ВыборкаДанных.НомерОтправления;
				НовыйЗаказ.ИдентификаторСкладаТорговойПлощадки  = ВыборкаДанных.ИдентификаторСклада;
				НовыйЗаказ.ДатаУстановкиСтатуса                 = ТекущаяДатаСеанса();
				НовыйЗаказ.ДатаСозданияЗаказа                   = ВыборкаДанных.ДатаОтправления;
				НовыйЗаказ.НомерРодительскогоОтправления        = ВыборкаДанных.НомерРодительскогоОтправления;
				НовыйЗаказ.НаименованиеСкладаТорговойПлощадки   = ВыборкаДанных.НаименованиеСклада;
				НовыйЗаказ.ТребуетсяЗаполнениеСведенийПоТоварам = ВыборкаНомерДокумента.ТребуетсяГТД
																	Или ВыборкаНомерДокумента.ТребуетсяРНПТ
																	Или ВыборкаНомерДокумента.ТребуетсяСтранаПроисхождения
																	Или ВыборкаНомерДокумента.ТребуетсяМаркировка
																	Или ВыборкаНомерДокумента.ТребуетсяУИНЮвелирногоИзделия;
				НовыйЗаказ.НомерЗаданияПолученияЭтикеток        = ВыборкаДанных.НомерЗаданияПолученияЭтикеток;
				НовыйЗаказ.ИдентификаторОтгрузки                = ВыборкаДанных.ИдентификаторОтгрузки;
				НовыйЗаказ.ДополнительныеСведения               = ВыборкаДанных.ДополнительныеСведения;
				НовыйЗаказ.СведенияПоТоварам               = Новый Массив;
				
				ВариантОбеспеченияСтрок           = ВариантОбеспеченияСтрокПоСтатусуЗаказа(НовыйЗаказ.Статус);
				ВариантОбеспеченияСтрокБезОстатка = ВариантОбеспеченияСтрокБезОстаткаПоСтатусуЗаказа(НовыйЗаказ.Статус);
				
				АдресДоставки = Новый Массив;
				АдресДоставки.Добавить(ВыборкаДанных.РегионДоставки);
				АдресДоставки.Добавить(ВыборкаДанных.ГородДоставки);
				АдресДоставки.Добавить(ВыборкаДанных.АдресДоставки);

				Если Параметры.ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
					ДанныеЗаполнения.Вставить("НомерПоДаннымКлиента", ВыборкаДанных.НомерДокумента);  
				Иначе	
					ДанныеЗаполнения.Вставить("НомерПоДаннымКлиента", ВыборкаДанных.НомерЗаказа);
				КонецЕсли; 

				ДанныеЗаполнения.Вставить("НомерПоДаннымКлиента", "");

				ДанныеЗаполнения.Вставить("ДатаПоДаннымКлиента",  ВыборкаДанных.ДатаОтправления);
				ДанныеЗаполнения.Вставить("Склад",                ВыборкаДанных.Склад);
				ДанныеЗаполнения.Вставить("АдресДоставки",        СтрСоединить(АдресДоставки, ", "));
				
				Если ВыборкаДанных.ЕстьНесколькоОтправленийПоЗаказу Тогда
					ДанныеЗаполнения.НомерПоДаннымКлиента = ВыборкаДанных.НомерОтправления;
					Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = '%1. Отправление %2 из заказа %3';
							|en = '%1. The %2 shipment from the %3 order'"),
						УчетнаяЗаписьПредставление,
						ВыборкаДанных.НомерОтправления,
						ВыборкаДанных.НомерЗаказа);
				Иначе
					ДанныеЗаполнения.НомерПоДаннымКлиента = ВыборкаДанных.НомерЗаказа;
					Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = '%1. Заказ %2';
							|en = '%1. The %2 order'"),
						УчетнаяЗаписьПредставление,
						ВыборкаДанных.НомерЗаказа);
				КонецЕсли;
				
				Если Не ПустаяСтрока(ВыборкаДанных.КомментарийКЗаказу) Тогда
					Комментарий = Комментарий + Символы.ПС + ВыборкаДанных.КомментарийКЗаказу;
				КонецЕсли;
				ДанныеЗаполнения.Вставить("Комментарий", Комментарий);
				
				Если ТорговаяПлощадка = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет
						И ТаблицаСкладов.Количество() > 0 Тогда
					ИдентификаторСклада = ТаблицаСкладов[0].ИдентификаторСклада;
				Иначе 
					ИдентификаторСклада = ВыборкаДанных.ИдентификаторСклада;	
				КонецЕсли;
				
				НастройкиСклада = НастройкиСкладов.Получить(ИдентификаторСклада);
				Если НастройкиСклада = Неопределено Тогда
					СтрокаСклада = ТаблицаСкладов.Найти(ИдентификаторСклада, "ИдентификаторСклада");
					Если СтрокаСклада <> Неопределено Тогда
						НастройкиСклада = СтрокаСклада.НастройкиСклада;
						НастройкиСкладов.Вставить(ИдентификаторСклада, НастройкиСклада);
					Иначе
						НастройкиСклада = НовыйНастройкиСкладаFBS();
					КонецЕсли;
				КонецЕсли;
				ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДанныеЗаполнения, НастройкиСклада, Истина);
				
				Если Параметры.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию
						И ЗначениеЗаполнено(Параметры.ДанныеПокупателейПоЗаказам) Тогда
					ДанныеПокупателяПоЗаказу =
						Параметры.ДанныеПокупателейПоЗаказам[ВыборкаДанных.ИдентификаторЗаказа];
					
					Если ДанныеПокупателяПоЗаказу <> Неопределено Тогда
						Если ЗначениеЗаполнено(ДанныеПокупателяПоЗаказу.Партнер) Тогда
							ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, ДанныеПокупателяПоЗаказу);
						ИначеЕсли Не ПустаяСтрока(ДанныеПокупателяПоЗаказу.НаименованиеКонтрагента) Тогда
							ДопИнформация = Новый Массив;
							ДопИнформация.Добавить(ЗаказКлиентаОбъект.ДополнительнаяИнформация);
							ДопИнформация.Добавить(СтрШаблон(НСтр("ru = 'Сведения о покупателе: %1.';
																	|en = 'Information on the customer: %1.'"),
								ДанныеПокупателяПоЗаказу.НаименованиеКонтрагента));
							
							ЗаказКлиентаОбъект.ДополнительнаяИнформация = СокрЛП(СтрСоединить(ДопИнформация, " "));
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;

				Если ЗаказКлиентаЗаполнить(ЗаказКлиентаОбъект, ДанныеЗаполнения, 
						ЗначенияФункциональныхОпций, Параметры) Тогда

					Если ЗаказКлиентаСостояние <> ДоступныеСостоянияДокументов.СостояниеНовый Тогда
						ЗаказКлиентаСостояние = ДоступныеСостоянияДокументов.СостояниеОбновлен;
					КонецЕсли;
				Иначе
					ЗаполнитьЗначенияСвойств(ЗаказКлиентаОбъект, ДанныеЗаполнения);
					ЗаказКлиентаСостояние = ДоступныеСостоянияДокументов.СостояниеОшибкиЗаполнения;
				КонецЕсли;
				
				ЗаказКлиентаОбъект.Склад = ДанныеЗаполнения.Склад;
				
				Если НовыйЗаказ.Статус <> Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки
						И НовыйЗаказ.Статус <> Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке Тогда
					Если ЗначенияФункциональныхОпций.ИспользоватьРасширенныеВозможностиЗаказаКлиента
							И Не ЗначенияФункциональныхОпций.ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента Тогда
						ЗаказКлиентаОбъект.Статус = Перечисления.СтатусыЗаказовКлиентов.НеСогласован;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если Не Параметры.ЗагружатьНезаполненныеСтроки
					И Не ЗначениеЗаполнено(ВыборкаДанных.Номенклатура) Тогда
				Продолжить;
			КонецЕсли;
			
			СкладГруппа = ?(ЗначениеЗаполнено(ЗаказКлиентаОбъект.Склад)
									И ЗначенияФункциональныхОпций.ИспользоватьСкладыВТабличнойЧастиДокументовПродажи,
								Справочники.Склады.ЭтоГруппа(ЗаказКлиентаОбъект.Склад),
								Ложь);
				
			// Заполнение строки табличной части
			СтрокаТабличнойЧасти = ЗаказКлиентаОбъект.Товары.Добавить();
			СтрокаТабличнойЧасти.Номенклатура = ВыборкаДанных.Номенклатура;
			
			Если ЗначенияФункциональныхОпций.ИспользоватьХарактеристикиНоменклатуры Тогда
				СтрокаТабличнойЧасти.Характеристика = ВыборкаДанных.Характеристика;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ВыборкаДанных.Упаковка)
					И ВыборкаДанных.Упаковка <> ВыборкаДанных.ЕдиницаИзмерения Тогда
				Если ЗначенияФункциональныхОпций.ИспользоватьУпаковкиНоменклатуры Тогда
					СтрокаТабличнойЧасти.Упаковка = ВыборкаДанных.Упаковка;
					КоличествоУпаковок            = ВыборкаДанных.Количество;
					Количество                    = Окр(КоличествоУпаковок * ВыборкаДанных.УпаковкаКоэффициент, 
														3, РежимОкругления.Окр15как20);
				Иначе
					СтрокаТабличнойЧасти.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
					КоличествоУпаковок            = Окр(ВыборкаДанных.Количество * ВыборкаДанных.УпаковкаКоэффициент, 
														3, РежимОкругления.Окр15как20);
					Количество                    = КоличествоУпаковок;
				КонецЕсли;
			Иначе
				СтрокаТабличнойЧасти.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
				КоличествоУпаковок            = ВыборкаДанных.Количество;
				Количество                    = КоличествоУпаковок;
			КонецЕсли;
			
			СтрокаТабличнойЧасти.НоменклатураПартнера = ВыборкаДанных.НоменклатураПартнера;
			Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.НоменклатураПартнера)
					И ЗначениеЗаполнено(Параметры.ВладелецКаталога) Тогда
				ДанныеНоменклатуры = НовыеДанныеНоменклатурыПартнераДляОбновления();
				ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры, ВыборкаДанных);
				
				//@skip-check query-in-loop
				РезультатСоздания = СоздатьОбновитьНоменклатуруПартнера(Параметры.ВладелецКаталога, ДанныеНоменклатуры);
				СтрокаТабличнойЧасти.НоменклатураПартнера = РезультатСоздания.НоменклатураПартнера;
			КонецЕсли;
			
			СтрокаТабличнойЧасти.КоличествоУпаковок  = КоличествоУпаковок;
			СтрокаТабличнойЧасти.Количество          = Количество;
			СтрокаТабличнойЧасти.Цена                = ВыборкаДанных.Цена;
			СтрокаТабличнойЧасти.Сумма               = ВыборкаДанных.Сумма;
			СтрокаТабличнойЧасти.СуммаСНДС           = ВыборкаДанных.Сумма;
			СтрокаТабличнойЧасти.Обособленно         = ЗаполнитьОбособленно;
			СтрокаТабличнойЧасти.ВариантОбеспечения  = ВариантОбеспеченияСтрок;
			СтрокаТабличнойЧасти.Склад               = ?(Не СкладГруппа, ВыборкаДанных.Склад, Справочники.Склады.ПустаяСсылка());
			СтрокаТабличнойЧасти.КодСтроки           = СтрокаТабличнойЧасти.НомерСтроки;
			СтрокаТабличнойЧасти.ИдентификаторСтроки = "";
			СтрокаТабличнойЧасти.ДатаОтгрузки        = ЗаказКлиентаОбъект.ДатаОтгрузки;
			СтрокаТабличнойЧасти.Отменено            = ЗначениеЗаполнено(ВыборкаДанных.ИдентификаторПричиныОтмены);
			СтрокаТабличнойЧасти.ПричинаОтмены       = ВыборкаДанных.ПричинаОтмены;
			
			Если ЗаказКлиентаОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту
					Или ЗаказКлиентаОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности
					Или Не ЗначениеЗаполнено(ЗаказКлиентаОбъект.ХозяйственнаяОперация) Тогда
				СтрокаТабличнойЧасти.СуммаРучнойСкидки = ВыборкаДанных.СуммаСкидки;
			КонецЕсли;
			
			// Обеспечение
			Если ЗаказКлиентаОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию
					И ЗначенияФункциональныхОпций.ИспользоватьРасширенныеВозможностиЗаказаКлиента
					И Не ЗначенияФункциональныхОпций.ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента
					И НовыйЗаказ.Статус <> Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен Тогда
				СтрокаТабличнойЧасти.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Отгрузить;
			КонецЕсли;
			
			НоваяСтрока = Неопределено;
			
			Если Параметры.ОбработатьВариантОбеспечения Тогда
				Если НовыйЗаказ.Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен Тогда
					СтрокаТабличнойЧасти.ВариантОбеспечения = ВариантОбеспеченияСтрок;
					СтрокаТабличнойЧасти.Отменено           = Истина;
					Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ПричинаОтмены) Тогда
						ДанныеПричиныОтмены = ИнтеграцияСМаркетплейсамиСервер.НовыеДанныеПричиныОтменыЗаказа();
						ДанныеПричиныОтмены.Идентификатор = ВыборкаДанных.ИдентификаторПричиныОтмены;
						ДанныеПричиныОтмены.Наименование = ВыборкаДанных.НаименованиеПричиныОтмены;
						//@skip-check query-in-loop - для каждой причины результат кэшируется
						СтрокаТабличнойЧасти.ПричинаОтмены = ПричинаОтменыЗаказаКлиента(
							Параметры.УчетнаяЗапись, ДанныеПричиныОтмены, Параметры.СобытиеЖурналаРегистрации, КэшПричинОтмены);
					КонецЕсли;
				Иначе
					Отбор = Новый Структура;
					Отбор.Вставить("Номенклатура",   СтрокаТабличнойЧасти.Номенклатура);
					Отбор.Вставить("Характеристика", СтрокаТабличнойЧасти.Характеристика);
					Если СкладГруппа Тогда
						Отбор.Вставить("ГруппаСкладов", ЗаказКлиентаОбъект.Склад);
					Иначе
						Отбор.Вставить("Склад", СтрокаТабличнойЧасти.Склад);
					КонецЕсли;
					
					НайденныеСтроки       = ТаблицаОстатков.НайтиСтроки(Отбор);
					ОстатокКРаспределению = СтрокаТабличнойЧасти.Количество;
					
					Для Каждого ЭлементКоллекции Из НайденныеСтроки Цикл
						Если ОстатокКРаспределению <= 0 Тогда
							Прервать;
						ИначеЕсли ЭлементКоллекции.Остаток <= 0 Тогда
							Продолжить;
						КонецЕсли;
						
						Если СтрокаТабличнойЧасти.Количество <= ЭлементКоллекции.Остаток Тогда
							ДляРаспределенияКоличество = СтрокаТабличнойЧасти.Количество;
							СтрокаТабличнойЧасти.Склад = ЭлементКоллекции.Склад;
							
						Иначе
							ДляРаспределенияКоличество = ЭлементКоллекции.Остаток;
							
							Количество          = ДляРаспределенияКоличество;
							КоличествоУпаковок  = Окр(Количество 
														/ ?(ВыборкаДанных.УпаковкаКоэффициент = 0, 1, ВыборкаДанных.УпаковкаКоэффициент), 
													3, РежимОкругления.Окр15как20);
							Если ОкруглятьКоличество Тогда
								КоличествоУпаковок         = Цел(КоличествоУпаковок);
								Количество                 = Окр(КоличествоУпаковок * ВыборкаДанных.УпаковкаКоэффициент, 
																3, РежимОкругления.Окр15как20);
								ДляРаспределенияКоличество = Количество;
							КонецЕсли;
							
							Если Количество > 0 Тогда
								НоваяСтрока = ЗаказКлиентаОбъект.Товары.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
								
								НоваяСтрока.Склад               = ЭлементКоллекции.Склад;
								НоваяСтрока.Количество          = Количество;
								НоваяСтрока.КоличествоУпаковок  = КоличествоУпаковок;
								НоваяСтрока.КодСтроки           = НоваяСтрока.НомерСтроки;
								НоваяСтрока.ИдентификаторСтроки = "";
								
								СтрокаТабличнойЧасти.Количество         = СтрокаТабличнойЧасти.Количество - ДляРаспределенияКоличество;
								СтрокаТабличнойЧасти.КоличествоУпаковок = Окр(СтрокаТабличнойЧасти.Количество 
																				/ ?(ВыборкаДанных.УпаковкаКоэффициент = 0, 1, ВыборкаДанных.УпаковкаКоэффициент), 
																			3, РежимОкругления.Окр15как20);
								Если ОкруглятьКоличество Тогда
									СтрокаТабличнойЧасти.КоличествоУпаковок = Цел(СтрокаТабличнойЧасти.КоличествоУпаковок);
									СтрокаТабличнойЧасти.Количество         = Окр(СтрокаТабличнойЧасти.КоличествоУпаковок * ВыборкаДанных.УпаковкаКоэффициент, 
																					3, РежимОкругления.Окр15как20);
								КонецЕсли;
								
								ЗаполнитьДополнительныеСведенияПоТоварам(НовыйЗаказ, НоваяСтрока, ВыборкаДанных, ИдентификаторЭкземпляра);
							КонецЕсли;
						КонецЕсли;
						
						ЭлементКоллекции.Остаток = ЭлементКоллекции.Остаток - ДляРаспределенияКоличество;
						ОстатокКРаспределению    = ОстатокКРаспределению - ДляРаспределенияКоличество;
					КонецЦикла;
					
					Если ОстатокКРаспределению > 0 Тогда
						Если Не ЗначенияФункциональныхОпций.ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента Тогда
							СтрокаТабличнойЧасти.Отменено      = Истина;
							СтрокаТабличнойЧасти.ПричинаОтмены = Параметры.ПричинаОтменыПоУмолчанию;
						Иначе
							СтрокаТабличнойЧасти.ВариантОбеспечения = ВариантОбеспеченияСтрокБезОстатка;
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
			Если СтатусыКонечные.Найти(НовыйЗаказ.Статус) = Неопределено Тогда
				ЗаполнитьДополнительныеСведенияПоТоварам(НовыйЗаказ, СтрокаТабличнойЧасти, ВыборкаДанных, ИдентификаторЭкземпляра);
			КонецЕсли;
		КонецЦикла;
		
		Если ЗаказКлиентаОбъект <> Неопределено Тогда
			Если ЗаказКлиентаОбъект.Товары.Количество() > 0 Тогда
				// Постобработка табличной части
				Если Не ЗаказКлиентаОбработатьТЧ(ЗаказКлиентаОбъект, Параметры.ПараметрыОбработки, 
							Параметры.СобытиеЖурналаРегистрации) Тогда
					ЗаказКлиентаСостояние = ДоступныеСостоянияДокументов.СостояниеОшибкиЗаполнения;
				КонецЕсли;
				
				ЗаказКлиентаОбъект.МаксимальныйКодСтроки = ЗаказКлиентаОбъект.Товары.Количество();
				
				// Запись и проведение документа
				РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(ЗаказКлиентаОбъект, 
					Истина, Параметры.СобытиеЖурналаРегистрации);
				ЗаказКлиентаСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
					ЗаказКлиентаСостояние, 
					ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
				
				Если ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
					ОтборОтмененныхСтрок = Новый Структура("Отменено", Истина);
					Если ЗаказКлиентаОбъект.Товары.НайтиСтроки(ОтборОтмененныхСтрок).Количество() = ЗаказКлиентаОбъект.Товары.Количество() Тогда
						ЗаказЗакрыт = РегистрыСведений.ЗаказыТорговыхПлощадок.ЗакрытьЗаказ(
							ЗаказКлиентаОбъект,
							Параметры.СобытиеЖурналаРегистрации);
						
						Если Не ЗаказЗакрыт Тогда
							Если Ошибка.Детализация = Неопределено Тогда
								Ошибка.Детализация = Новый Массив;
							КонецЕсли;
							
							Ошибка.Детализация.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Не удалось закрыть ""%1"".';
									|en = 'Cannot close ""%1"".'"),
								ЗаказКлиентаОбъект.Ссылка));
						КонецЕсли;
					КонецЕсли;
				Иначе
					Если Ошибка.Детализация = Неопределено Тогда
						Ошибка.Детализация = Новый Массив;
					КонецЕсли;
					Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
				КонецЕсли;
				
				Если ЗаказКлиентаСостояние <> ДоступныеСостоянияДокументов.СостояниеНовый Тогда
					ЗаказКлиентаОбъект.Разблокировать();
				КонецЕсли;
			
			ИначеЕсли Не ЗаказКлиентаОбъект.ЭтоНовый() 
						И Параметры.ПерезаписатьДокументы Тогда
				// Запись и пометка на удаление документа
				ЗаказКлиентаОбъект.ПометкаУдаления = Истина;
				
				РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(ЗаказКлиентаОбъект, 
					Ложь, Параметры.СобытиеЖурналаРегистрации);
				ЗаказКлиентаСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
					ЗаказКлиентаСостояние, 
					ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
					
				ЗаказКлиентаОбъект.Разблокировать();
				
				Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
					Если Ошибка.Детализация = Неопределено Тогда
						Ошибка.Детализация = Новый Массив;
					КонецЕсли;
					Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
				КонецЕсли;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ЗаказКлиентаСсылка) Тогда
				ЗаказКлиентаСсылка = ЗаказКлиентаОбъект.Ссылка;
			КонецЕсли;
			
			СозданныеДокументы.Вставить(ВыборкаНомерДокумента.НомерДокумента, ЗаказКлиентаСсылка);
			НовыйЗаказ.Заказ = ЗаказКлиентаСсылка;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЗаказКлиентаСсылка) 
				И Результат.Ссылки.Найти(ЗаказКлиентаСсылка) = Неопределено Тогда
			Результат.Ссылки.Добавить(ЗаказКлиентаСсылка);
			Результат.СостоянияСсылок.Вставить(ЗаказКлиентаСсылка, ЗаказКлиентаСостояние);
		КонецЕсли;
		
		НомерТекущейСтроки = НомерТекущейСтроки + 1;
	КонецЦикла;
	
	// Новые необработанные заказы торговой площадки
	Если Параметры.ЗаписатьНеобработанныеЗаказы
			И ЗаказыТорговыхПлощадок.Количество() > 0 Тогда
		НачатьТранзакцию();
		
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок");
			ЭлементБлокировкиДанных.ИсточникДанных = ЗаказыТорговыхПлощадок;
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Заказ", "Заказ");
			БлокировкаДанных.Заблокировать();
			
			Для Каждого СтрокаТаблицыЗначений Из ЗаказыТорговыхПлощадок Цикл
				Если ЗначениеЗаполнено(СтрокаТаблицыЗначений.Заказ) Тогда
					Запись = РегистрыСведений.ЗаказыТорговыхПлощадок.СоздатьМенеджерЗаписи();
					ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицыЗначений);
					Запись.ТребуетсяПолучениеЭтикеток = (Запись.Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке);
					Запись.Записать(Параметры.ПерезаписатьДокументы);
					
					НаборЗаписей = РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Заказ.Установить(СтрокаТаблицыЗначений.Заказ);
					Для Каждого ЭлементКоллекции Из СтрокаТаблицыЗначений.СведенияПоТоварам Цикл
						Запись = НаборЗаписей.Добавить();
						ЗаполнитьЗначенияСвойств(Запись, ЭлементКоллекции);
						Запись.Заказ = СтрокаТаблицыЗначений.Заказ;
					КонецЦикла;
					НаборЗаписей.Записать(Истина);
				КонецЕсли;
			КонецЦикла;
			
			Если Параметры.ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
				// Получить идентификаторы экземпляров     			
				НомераОтправлений = ЗаказыТорговыхПлощадок.ВыгрузитьКолонку("НомерОтправления");
				ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьЭкземплярыТоваровИзОтправлений(Параметры.УчетнаяЗапись, НомераОтправлений, Ложь, Истина);
			КонецЕсли;
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			Ошибка.КодОшибки      = Параметры.КодОшибки;
			Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При записи заказов с торговой площадки <%1> возникли ошибки. Подробности см. в журнале регистрации.';
					|en = 'Errors occurred while saving orders from the <%1> marketplace. For more information, see the event log.'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				Параметры.УчетнаяЗапись);
			
			Если Ошибка.Детализация = Неопределено Тогда
				Ошибка.Детализация = Новый Массив;
			КонецЕсли;
			Ошибка.Детализация.Добавить(ТекстОшибки);
				
			ЗаписьЖурналаРегистрации(Параметры.СобытиеЖурналаРегистрации,
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстОшибки);
		КонецПопытки;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Ошибка;
	
КонецФункции

// Создает документы продажи с отложенным переходом права собственности.
//
// Параметры:
//   Результат          - См. РезультатЗаписиЗагружаемыхДанных.
//   ТаблицаДанных      - ТаблицаЗначений - подготовленная таблица сопоставленных данных для загрузки.
//   Параметры          - Структура - параметры заполнения документов:
//     * ВидТорговойПлощадки             - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки;
//     * УчетнаяЗапись                   - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки;
//     * ВалютаУчета                     - СправочникСсылка.Валюты, Неопределено - валюта учета учетной записи;
//     * Организация                     - СправочникСсылка.Организации - организация в шапке;
//     * Партнер                         - СправочникСсылка.Партнеры - партнер в шапке;
//     * Контрагент                      - СправочникСсылка.Контрагенты - контрагент в шапке;
//     * Соглашение                      - СправочникСсылка.СоглашенияСКлиентами - соглашение в шапке;
//     * Договор                         - СправочникСсылка.ДоговорыКонтрагентов - договор в шапке;
//     * СкладВШапке                     - СправочникСсылка.Склады, Неопределено, Строка - склад в шапке;
//     * ПерезаписатьСклад               - Булево - перезаписывать склад в существующих документах реализации;
//     * КодОшибки                       - Строка - код ошибки для результат выполнения;
//     * СобытиеЖурналаРегистрации       - Строка - наименование события для записей в журнале регистрации;
//     * ЗагружатьНезаполненныеСтроки    - Булево - добавлять ли строки с несопоставленными показателями;
//     * ПерезаписатьДокументы           - Булево - перезаписывать ли существующие документы;
//     * ОчиститьТабличныеЧасти          - Булево - очищать табличные части документов перед обработкой;
//     * ПараметрыОбработкиРеализация    - Структура Из КлючИЗначение, Неопределено - параметры обработки табличных частей документов реализации;
//     * ПараметрыОбработкиКорректировка - Структура Из КлючИЗначение, Неопределено - параметры обработки табличных частей документов корректировки;
//     * ТекстЗапроса                    - Строка - текст запроса для отражения продаж с отложенным переходом права собственности.
//   ПараметрыПрогресса - См. ПараметрыПрогресса.
//
// Возвращаемое значение:
//   См. НоваяОшибка.
//
Функция ЗаписатьПродажиСОтложеннымПереходомПраваСобственности(Результат, ТаблицаДанных, Параметры, ПараметрыПрогресса = Неопределено) Экспорт
	
	Ошибка = НоваяОшибка();
	
	Если Не ЗначениеЗаполнено(Параметры.УчетнаяЗапись) Тогда
		ТекстОшибки = НСтр("ru = 'Не указана учетная запись торговой площадки.';
							|en = 'Marketplace personal account not specified.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		Ошибка.КодОшибки      = Параметры.КодОшибки;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	Партнер    = Параметры.Партнер;
	Контрагент = Параметры.Контрагент;
	Соглашение = Параметры.Соглашение;
	Договор    = Параметры.Договор;
	
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		Партнер    = Справочники.Партнеры.РозничныйПокупатель;
		Контрагент = Справочники.Контрагенты.РозничныйПокупатель;
		Соглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
		Договор    = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
	ЗначенияФункциональныхОпций = ПолучитьФункциональныеОпции(Соглашение);
	
	Если Не ЗначенияФункциональныхОпций.ИспользоватьОтгрузкуБезПереходаПраваСобственности Тогда
		ТекстОшибки = НСтр("ru = 'Для отражения данных о реализованных товарах по схеме ""Реализация с отложенным переходом права собственности"" необходимо включить возможность оформления отгрузки с отложенным переходом права собственности в настройках раздела ""НСИ и администрирование"" - ""Продажи"" - ""Оптовые продажи"".';
							|en = 'To record data on the items sold by the ""Sale with deferred transfer of title to goods"" scheme, enable shipments with deferred transfer of title to goods in Master data and settings - Sales - Wholesale.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
			
		Ошибка.КодОшибки      = Параметры.КодОшибки;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ИнтеграцияСМаркетплейсамиСервер.ЗаписатьПродажиСОтложеннымПереходомПраваСобственности");
	
	УчетнаяЗаписьПредставление       =
		Справочники.УчетныеЗаписиМаркетплейсов.ПолноеПредставлениеУчетнойЗаписи(
			Параметры.ВидТорговойПлощадки,
			Параметры.УчетнаяЗапись);
	ВалютаРегламентированногоУчета   = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Параметры.Организация);
	ДоступныеСостоянияДокументов     = ДоступныеСостоянияДокументов();
	ВариантыРезультатовСопоставления = ИнтеграцияСМаркетплейсамиКлиентСервер.ВариантыРезультатовСопоставления();
	СозданныеРеализации              = Новый Соответствие;
	СозданныеКорректировки           = Новый Соответствие;
	
	РеквизитыШапкиРеализация = Новый Структура;
	РеквизитыШапкиРеализация.Вставить("Организация",           Параметры.Организация);
	РеквизитыШапкиРеализация.Вставить("Партнер",               Партнер);
	РеквизитыШапкиРеализация.Вставить("Контрагент",            Контрагент);
	РеквизитыШапкиРеализация.Вставить("Соглашение",            ?(ЗначенияФункциональныхОпций.ИспользоватьСоглашенияСКлиентами, 
																	Соглашение, 
																	Справочники.СоглашенияСКлиентами.ПустаяСсылка()));
	РеквизитыШапкиРеализация.Вставить("Договор",               ?(ЗначенияФункциональныхОпций.ИспользуютсяДоговорыКонтрагентов, 
																	Договор, 
																	Справочники.ДоговорыКонтрагентов.ПустаяСсылка()));
	РеквизитыШапкиРеализация.Вставить("Валюта",                ?(ЗначениеЗаполнено(Параметры.ВалютаУчета), 
																	Параметры.ВалютаУчета, 
																	ВалютаРегламентированногоУчета));
	РеквизитыШапкиРеализация.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности);
	
	ДанныеЗаполненияРеализация = Новый Структура;
	ДанныеЗаполненияРеализация.Вставить("ВариантОформленияПродажи", Перечисления.ВариантыОформленияПродажи.РеализацияТоваровУслуг);
	ДанныеЗаполненияРеализация.Вставить("РеквизитыШапки",           РеквизитыШапкиРеализация);
	
	Если ТипЗнч(Параметры.СкладВШапке) = Тип("СправочникСсылка.Склады") Тогда
		ДанныеЗаполненияРеализация.РеквизитыШапки.Вставить("Склад", Параметры.СкладВШапке);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполненияРеализация.РеквизитыШапки.Соглашение) Тогда
		ДанныеЗаполненияРеализация.Вставить("Партнер", ДанныеЗаполненияРеализация.РеквизитыШапки.Партнер);
	КонецЕсли;
	
	Если Параметры.ПараметрыОбработкиРеализация = Неопределено Тогда
		Параметры.ПараметрыОбработкиРеализация = Новый Структура;
		Параметры.ПараметрыОбработкиРеализация.Вставить("СтруктураДействий",            Неопределено);
		Параметры.ПараметрыОбработкиРеализация.Вставить("ПересчитатьКоличествоЕдиниц",  Ложь);
		Параметры.ПараметрыОбработкиРеализация.Вставить("ЗаполнитьЦены",                Ложь);
		Параметры.ПараметрыОбработкиРеализация.Вставить("ОчиститьАвтоматическуюСкидку", Истина);
		Параметры.ПараметрыОбработкиРеализация.Вставить("ПересчитатьЦенаВключаетНДС",   Истина);
	КонецЕсли;
	
	ДанныеЗаполненияКорректировка = Новый Структура;
	ДанныеЗаполненияКорректировка.Вставить("Реализация");
	ДанныеЗаполненияКорректировка.Вставить("Организация",           Параметры.Организация);
	ДанныеЗаполненияКорректировка.Вставить("Валюта",                ?(ЗначениеЗаполнено(Параметры.ВалютаУчета), 
																		Параметры.ВалютаУчета, 
																		ВалютаРегламентированногоУчета));
	ДанныеЗаполненияКорректировка.Вставить("ПараметрыОформления",   Новый Структура("ВводитьНаОснованииНепроведенного", Истина));
	ДанныеЗаполненияКорректировка.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон);
	
	Если Параметры.ПараметрыОбработкиКорректировка = Неопределено Тогда
		Параметры.ПараметрыОбработкиКорректировка = Новый Структура;
		Параметры.ПараметрыОбработкиКорректировка.Вставить("СтруктураДействий",          Неопределено);
		Параметры.ПараметрыОбработкиКорректировка.Вставить("ЗаполнитьВариантОтражения",  Истина);
		Параметры.ПараметрыОбработкиКорректировка.Вставить("ПересчитатьЦенаВключаетНДС", Истина);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = Параметры.ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ТаблицаДанных",             ТаблицаДанных);
	Запрос.УстановитьПараметр("КоличествоСтрок",           ТаблицаДанных.Количество());
	Запрос.УстановитьПараметр("Организация",               Параметры.Организация);
	Запрос.УстановитьПараметр("Склад",                     Параметры.СкладВШапке);
	Запрос.УстановитьПараметр("УчетнаяЗапись",             Параметры.УчетнаяЗапись);
	Запрос.УстановитьПараметр("КомментарийНевыкупТоваров", КомментарийНевыкупТоваров(УчетнаяЗаписьПредставление));
	Запрос.УстановитьПараметр("КомментарийВозвратТоваров", КомментарийВозвратТоваров(УчетнаяЗаписьПредставление));
	Запрос.УстановитьПараметр("КомментарийКорректировка",  КомментарийКорректировкаСтоимости(УчетнаяЗаписьПредставление));
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
		Замер, 
		ТаблицаДанных.Количество() / 100);
	
	ЕстьПокупательВВыборке        = (РезультатЗапроса.Колонки.Найти("Контрагент") <> Неопределено)
										И (РезультатЗапроса.Колонки.Найти("Партнер") <> Неопределено);
	ЕстьСкладВВыборке             = (РезультатЗапроса.Колонки.Найти("Склад") <> Неопределено);
	ЕстьЗаказВВыборке             = (РезультатЗапроса.Колонки.Найти("ЗаказКлиентаСсылка") <> Неопределено);
	ЕстьРеализацияДругогоПериода  = (РезультатЗапроса.Колонки.Найти("РеализацияДругогоПериода") <> Неопределено);
	ВыполнитьКорректировкуПоСумме = Параметры.ВыполнитьКорректировкуПоСумме И (РезультатЗапроса.Колонки.Найти("КорректировкаРеализацииПоСумме") <> Неопределено);
	
	ВыборкаНомерОтправления = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВсегоСтрок              = ВыборкаНомерОтправления.Количество();
	НомерТекущейСтроки      = 1;
	ПроцентВыполнения       = 0;
	НеПроведенныеПоЗаказам  = Ложь;
	
	Если ВсегоСтрок = 0 Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Данные для загрузки продаж с отложенным переходом правом собственности с торговой площадки <%1> отсутствуют.';
				|en = 'Data for importing sales with deferred transfer of title to goods from the <%1> marketplace is missing.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Параметры.УчетнаяЗапись);
		
		Ошибка.КодОшибки      = Параметры.КодОшибки;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	Пока ВыборкаНомерОтправления.Следующий() Цикл
		Если ПараметрыПрогресса <> Неопределено Тогда
			УстановитьПроцентПрогресса(
				ВсегоСтрок,
				НомерТекущейСтроки,
				ПроцентВыполнения,
				ПараметрыПрогресса);
		КонецЕсли;
		
		ВыборкаДатаДокумента = ВыборкаНомерОтправления.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДатаДокумента.Следующий() Цикл
			
			ВыборкаДатаДоставки = ВыборкаДатаДокумента.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДатаДоставки.Следующий() Цикл
				РеализацияТоваровУслугОбъект     = Неопределено;
				РеализацияТоваровУслугСсылка     = Неопределено;
				РеализацияТоваровУслугСостояние  = ДоступныеСостоянияДокументов.СостояниеНеОбновлен;
				РеализацияТоваровУслугСумма      = ВыборкаДатаДоставки.РеализацияТоваровУслугСумма;
				ДатаПереходаПраваСобственности   = ВыборкаДатаДоставки.ДатаДоставки;
				
				// Товары, переданные в доставку
				ТолькоТоварыПереданные = Неопределено;
				
				Если ЕстьЗаказВВыборке Тогда
					ЗаказКлиентаСсылка = ВыборкаДатаДоставки.ЗаказКлиентаСсылка;
				Иначе
					ЗаказКлиентаСсылка = Неопределено;
				КонецЕсли;
				
				ВыборкаДанных = ВыборкаДатаДоставки.Выбрать();
				Пока ВыборкаДанных.Следующий() Цикл
					Если ТолькоТоварыПереданные = Неопределено Тогда
						ТолькоТоварыПереданные = (ВыборкаДанных.ТипОперации = 1);
					ИначеЕсли ТолькоТоварыПереданные И ВыборкаДанных.ТипОперации <> 1 Тогда
						Прервать;
					КонецЕсли;
					
					Если РеализацияТоваровУслугОбъект = Неопределено Тогда
						РеализацияТоваровУслугСсылка = ВыборкаДанных.РеализацияТоваровУслугСсылка;
						
						Если Не ЗначениеЗаполнено(РеализацияТоваровУслугСсылка) Тогда
							РеализацияТоваровУслугСсылка = СозданныеРеализации.Получить(ВыборкаНомерОтправления.НомерОтправления);
						КонецЕсли;
						
						Если ЗначениеЗаполнено(РеализацияТоваровУслугСсылка) Тогда
							// Существующий документ
							Если ЗначениеЗаполнено(ЗаказКлиентаСсылка) Тогда
								Прервать;
							КонецЕсли;
							
							Если ВыборкаДанных.ТипОперации <> 1 Тогда
								Если ЗначениеЗаполнено(РеализацияТоваровУслугСсылка) 
										Или Результат.Ссылки.Найти(РеализацияТоваровУслугСсылка) <> Неопределено Тогда
									Прервать;
								КонецЕсли;
							ИначеЕсли Не Параметры.ПерезаписатьДокументы Тогда
								Прервать;
							КонецЕсли;
							
							РеализацияТоваровУслугОбъект    = РеализацияТоваровУслугСсылка.ПолучитьОбъект();
							РеализацияТоваровУслугСостояние = ДоступныеСостоянияДокументов.СостояниеНеОбновлен;
							
							ТекстОшибки = ЗаблокироватьДокумент(РеализацияТоваровУслугОбъект, Параметры.СобытиеЖурналаРегистрации);
							
							Если Не ПустаяСтрока(ТекстОшибки) Тогда
								Если Ошибка.Детализация = Неопределено Тогда
									Ошибка.Детализация = Новый Массив;
								КонецЕсли;
								Ошибка.Детализация.Добавить(ТекстОшибки);
								
								Прервать;
							КонецЕсли;
							
						Иначе
							// Новый документ
							РеализацияТоваровУслугОбъект    = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
							РеализацияТоваровУслугСостояние = ДоступныеСостоянияДокументов.СостояниеНовый;
						КонецЕсли;
						
						Если ЗначениеЗаполнено(ВыборкаДанных.НомерСчетаФактуры) Тогда
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Отражение продаж юридическому лицу по заказу (отправлению) №%1 от %2 необходимо скорректировать вручную.';
									|en = 'Manually adjust the record of sales to the legal entity for order (shipment) No. %1 dated %2.'"),
								ВыборкаДанных.НомерОтправления,
								Формат(ВыборкаДанных.ДатаОтправления, "ДЛФ=D;"));
								
							Если Ошибка.Детализация = Неопределено Тогда
								Ошибка.Детализация = Новый Массив;
							КонецЕсли;
							Ошибка.Детализация.Добавить(ТекстОшибки);
						КонецЕсли;
						
						ДанныеЗаполненияРеализация.Вставить("Дата",                           КонецДня(ВыборкаДанных.ДатаПередачиВДоставку));
						ДанныеЗаполненияРеализация.Вставить("Основание",                      СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
																									НСтр("ru = '%1. Отчет о реализации товаров';
																										|en = '%1. Goods sales report'"),
																									УчетнаяЗаписьПредставление));
						ДанныеЗаполненияРеализация.Вставить("ОснованиеНомер",                 ВыборкаДанных.НомерОтправления);
						ДанныеЗаполненияРеализация.Вставить("ОснованиеДата",                  ВыборкаДанных.ДатаОтправления);
						ДанныеЗаполненияРеализация.Вставить("Статус",                         ?(ДатаПереходаПраваСобственности = Дата(1, 1, 1), 
																									Перечисления.СтатусыРеализацийТоваровУслуг.ВПути, 
																									Перечисления.СтатусыРеализацийТоваровУслуг.Отгружено));
						ДанныеЗаполненияРеализация.Вставить("ДатаПереходаПраваСобственности", ДатаПереходаПраваСобственности);
						ДанныеЗаполненияРеализация.Вставить("Комментарий",                    СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
																									НСтр("ru = '%1. Отчет о реализации товаров по заказу (отправлению) №%2 от %3';
																										|en = '%1. Goods sales report for order (shipment) No. %2 dated %3'"),
																									УчетнаяЗаписьПредставление,
																									ВыборкаДанных.НомерОтправления,
																									Формат(ВыборкаДанных.ДатаОтправления, "ДЛФ=D;")));
						
						Если ЕстьПокупательВВыборке Тогда
							Если ЗначениеЗаполнено(ВыборкаДанных.Контрагент) Тогда
								ДанныеЗаполненияРеализация.РеквизитыШапки.Вставить("Партнер",    ВыборкаДанных.Партнер);
								ДанныеЗаполненияРеализация.РеквизитыШапки.Вставить("Контрагент", ВыборкаДанных.Контрагент);
								ДанныеЗаполненияРеализация.РеквизитыШапки.Вставить("Соглашение", Справочники.СоглашенияСКлиентами.ПустаяСсылка());
								ДанныеЗаполненияРеализация.РеквизитыШапки.Вставить("Договор",    Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
								
							Иначе
								ДанныеЗаполненияРеализация.РеквизитыШапки.Вставить("Партнер",    Партнер);
								ДанныеЗаполненияРеализация.РеквизитыШапки.Вставить("Контрагент", Контрагент);
								ДанныеЗаполненияРеализация.РеквизитыШапки.Вставить("Соглашение", ?(ЗначенияФункциональныхОпций.ИспользоватьСоглашенияСКлиентами, 
																										Соглашение, 
																										Справочники.СоглашенияСКлиентами.ПустаяСсылка()));
								ДанныеЗаполненияРеализация.РеквизитыШапки.Вставить("Договор",    ?(ЗначенияФункциональныхОпций.ИспользуютсяДоговорыКонтрагентов, 
																										Договор, 
																										Справочники.ДоговорыКонтрагентов.ПустаяСсылка()));
							КонецЕсли;
								
							Если Не ЗначениеЗаполнено(ДанныеЗаполненияРеализация.РеквизитыШапки.Соглашение) Тогда
								ДанныеЗаполненияРеализация.Вставить("Партнер", ДанныеЗаполненияРеализация.РеквизитыШапки.Партнер);
							ИначеЕсли ДанныеЗаполненияРеализация.Свойство("Партнер") Тогда
								ДанныеЗаполненияРеализация.Удалить("Партнер");
							КонецЕсли;
						КонецЕсли;
							
						Если РеализацияТоваровУслугСостояние = ДоступныеСостоянияДокументов.СостояниеНовый
								Или Параметры.ПерезаписатьСклад Тогда
							Если ЗначениеЗаполнено(ЗаказКлиентаСсылка) Тогда
								ДанныеЗаполненияРеализация.Вставить("РеализацияПоЗаказам", ЗначениеЗаполнено(ЗаказКлиентаСсылка));
								ДанныеЗаполненияРеализация.Вставить("ДокументОснование",   ЗаказКлиентаСсылка);
								
								Склад = Справочники.Склады.ПустаяСсылка();
								
							Иначе
								ДанныеЗаполненияРеализация.Вставить("РеализацияПоЗаказам", Ложь);
								ДанныеЗаполненияРеализация.Вставить("ДокументОснование",   Неопределено);
								
								Склад = Справочники.Склады.ПустаяСсылка();
								
								Если ТипЗнч(Параметры.СкладВШапке) = Тип("Строка") Тогда
									Склад       = ВыборкаДанных[Параметры.СкладВШапке];
									СкладГруппа = ?(ЗначениеЗаполнено(Склад)
															И ЗначенияФункциональныхОпций.ИспользоватьСкладыВТабличнойЧастиДокументовПродажи,
														Справочники.Склады.ЭтоГруппа(Склад),
														Ложь);
									
									Если Не СкладГруппа Тогда
										Склад = Справочники.Склады.ПустаяСсылка();
									КонецЕсли;
								КонецЕсли;
								
								Если Не ЗначениеЗаполнено(Склад)
										И ЕстьСкладВВыборке Тогда
									Склад = ВыборкаДанных.Склад;
								КонецЕсли;
							КонецЕсли;
							
							Если ЗначениеЗаполнено(Склад) Тогда
								ДанныеЗаполненияРеализация.РеквизитыШапки.Вставить("Склад", Склад);
							КонецЕсли;
						КонецЕсли;
							
						Если РеализацияТоваровУслугЗаполнить(РеализацияТоваровУслугОбъект, ДанныеЗаполненияРеализация, 
								ЗначенияФункциональныхОпций, Параметры.СобытиеЖурналаРегистрации) Тогда
							Если РеализацияТоваровУслугСостояние <> ДоступныеСостоянияДокументов.СостояниеНовый Тогда
								РеализацияТоваровУслугСостояние = ДоступныеСостоянияДокументов.СостояниеОбновлен;
							КонецЕсли;
						Иначе
							ЗаполнитьЗначенияСвойств(РеализацияТоваровУслугОбъект, ДанныеЗаполненияРеализация);
							ЗаполнитьЗначенияСвойств(РеализацияТоваровУслугОбъект, ДанныеЗаполненияРеализация.РеквизитыШапки);
							РеализацияТоваровУслугСостояние = ДоступныеСостоянияДокументов.СостояниеОшибкиЗаполнения;
						КонецЕсли;
						
						Если (РеализацияТоваровУслугСостояние = ДоступныеСостоянияДокументов.СостояниеНовый
									Или Параметры.ПерезаписатьСклад)
								И ДанныеЗаполненияРеализация.РеквизитыШапки.Свойство("Склад") Тогда
							РеализацияТоваровУслугОбъект.Склад = ДанныеЗаполненияРеализация.РеквизитыШапки.Склад;
						КонецЕсли;
							
						Если Не ЗначениеЗаполнено(ЗаказКлиентаСсылка)
								И Параметры.ОчиститьТабличныеЧасти Тогда
							РеализацияТоваровУслугОбъект.Товары.Очистить();
						КонецЕсли;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ЗаказКлиентаСсылка) Тогда
						Если РеализацияТоваровУслугОбъект.Товары.Количество() > 0 Тогда
							Прервать;
						Иначе
							ЗаказКлиентаСсылка = Неопределено;
						КонецЕсли;
					КонецЕсли;
					
					Если Не Параметры.ЗагружатьНезаполненныеСтроки
							И ВыборкаДанных.РезультатСопоставленияСтроки <> ВариантыРезультатовСопоставления.СтрокаСопоставлена Тогда
						Продолжить;
					КонецЕсли;
					
					// Заполнение строки табличной части
					СтрокаТабличнойЧасти = РеализацияТоваровУслугОбъект.Товары.Добавить();
					СтрокаТабличнойЧасти.Номенклатура = ВыборкаДанных.Номенклатура;
					
					Если ВыборкаДанных.ХарактеристикиИспользуются Тогда
						СтрокаТабличнойЧасти.Характеристика = ВыборкаДанных.Характеристика;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ВыборкаДанных.Упаковка)
							И ВыборкаДанных.Упаковка <> ВыборкаДанных.НоменклатураЕдиницаИзмерения Тогда
						Если ЗначенияФункциональныхОпций.ИспользоватьУпаковкиНоменклатуры Тогда
							СтрокаТабличнойЧасти.Упаковка = ВыборкаДанных.Упаковка;
							КоличествоУпаковок            = ВыборкаДанных.Количество;
							Количество                    = Окр(КоличествоУпаковок * ВыборкаДанных.УпаковкаКоэффициент, 
																3, РежимОкругления.Окр15как20);
						Иначе
							СтрокаТабличнойЧасти.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
							КоличествоУпаковок            = Окр(ВыборкаДанных.Количество * ВыборкаДанных.УпаковкаКоэффициент, 
																3, РежимОкругления.Окр15как20);
							Количество                    = КоличествоУпаковок;
						КонецЕсли;
					Иначе
						СтрокаТабличнойЧасти.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
						КоличествоУпаковок            = ВыборкаДанных.Количество;
						Количество                    = КоличествоУпаковок;
					КонецЕсли;
					
					СтрокаТабличнойЧасти.КоличествоУпаковок = КоличествоУпаковок;
					СтрокаТабличнойЧасти.Количество         = Количество;
					СтрокаТабличнойЧасти.Цена               = ВыборкаДанных.Цена;
					СтрокаТабличнойЧасти.Сумма              = ВыборкаДанных.Сумма;
					СтрокаТабличнойЧасти.СуммаСНДС          = ВыборкаДанных.Сумма;
					СтрокаТабличнойЧасти.СуммаРучнойСкидки  = ВыборкаДанных.СуммаСкидки;
					
					Если ЕстьСкладВВыборке Тогда
						СтрокаТабличнойЧасти.Склад = ВыборкаДанных.Склад;
					Иначе
						СтрокаТабличнойЧасти.Склад = РеализацияТоваровУслугОбъект.Склад;
					КонецЕсли;
					
					Если ЕстьЗаказВВыборке Тогда
						СтрокаТабличнойЧасти.ЗаказКлиента = ВыборкаДанных.ЗаказКлиентаСсылка;
					КонецЕсли;
				КонецЦикла;
				
				Если РеализацияТоваровУслугОбъект <> Неопределено Тогда
					Если РеализацияТоваровУслугОбъект.Товары.Количество() > 0 Тогда
						// Постобработка табличной части
						Если Не ЗначениеЗаполнено(ЗаказКлиентаСсылка)
								И Не РеализацияТоваровУслугОбработатьТЧ(РеализацияТоваровУслугОбъект, 
										Параметры.ПараметрыОбработкиРеализация, 
										Параметры.СобытиеЖурналаРегистрации) Тогда
							РеализацияТоваровУслугСостояние = ДоступныеСостоянияДокументов.СостояниеОшибкиЗаполнения;
						КонецЕсли;
						
						// Запись и проведение документа
						НеПроводитьДокумент    = ЕстьЗаказВВыборке И ЗначениеЗаполнено(ВыборкаДатаДоставки.ЗаказКлиентаСсылка);
						НеПроведенныеПоЗаказам = НеПроведенныеПоЗаказам Или НеПроводитьДокумент;
						
						РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(РеализацияТоваровУслугОбъект, 
							Не НеПроводитьДокумент, Параметры.СобытиеЖурналаРегистрации);
						РеализацияТоваровУслугСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
							РеализацияТоваровУслугСостояние, 
							ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
							
						Если РеализацияТоваровУслугСостояние <> ДоступныеСостоянияДокументов.СостояниеНовый Тогда
							РеализацияТоваровУслугОбъект.Разблокировать();
						КонецЕсли;
						
						Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
							Если Ошибка.Детализация = Неопределено Тогда
								Ошибка.Детализация = Новый Массив;
							КонецЕсли;
							Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
						КонецЕсли;
					
					ИначеЕсли Не РеализацияТоваровУслугОбъект.ЭтоНовый() Тогда
						// Запись и пометка на удаление документа
						РеализацияТоваровУслугОбъект.ПометкаУдаления = Истина;
						
						РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(РеализацияТоваровУслугОбъект, 
							Ложь, Параметры.СобытиеЖурналаРегистрации);
						РеализацияТоваровУслугСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
							РеализацияТоваровУслугСостояние, 
							ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
							
						РеализацияТоваровУслугОбъект.Разблокировать();
						
						Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
							Если Ошибка.Детализация = Неопределено Тогда
								Ошибка.Детализация = Новый Массив;
							КонецЕсли;
							Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
						КонецЕсли;
					КонецЕсли;
					
					РеализацияТоваровУслугСумма = РеализацияТоваровУслугОбъект.СуммаДокумента;
					
					Если Не ЗначениеЗаполнено(РеализацияТоваровУслугСсылка) Тогда
						РеализацияТоваровУслугСсылка = РеализацияТоваровУслугОбъект.Ссылка;
					КонецЕсли;
					
					СозданныеРеализации.Вставить(ВыборкаНомерОтправления.НомерОтправления, РеализацияТоваровУслугСсылка);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(РеализацияТоваровУслугСсылка) 
						И Результат.Ссылки.Найти(РеализацияТоваровУслугСсылка) = Неопределено Тогда
					Результат.Ссылки.Добавить(РеализацияТоваровУслугСсылка);
					Результат.СостоянияСсылок.Вставить(РеализацияТоваровУслугСсылка, РеализацияТоваровУслугСостояние);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(РеализацияТоваровУслугСсылка) Тогда
					// Доставленные товары
					Если ВыполнитьКорректировкуПоСумме
							И ВыборкаДатаДоставки.Сумма <> РеализацияТоваровУслугСумма
							И ДатаПереходаПраваСобственности = Дата(1, 1, 1) Тогда
						ДатаПереходаПраваСобственности = КонецДня(ВыборкаДанных.ДатаПередачиВДоставку);
					КонецЕсли;
					
					ВыборкаДанных.Сбросить();
					Пока ВыборкаДанных.Следующий() Цикл
						Если ВыборкаДанных.ТипОперации <> 1 
								Или Параметры.ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
							Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеализацияТоваровУслугСсылка, "ДатаПереходаПраваСобственности") <> ДатаПереходаПраваСобственности Тогда
								РеализацияТоваровУслугОбъект = РеализацияТоваровУслугСсылка.ПолучитьОбъект();
								
								ТекстОшибки = ЗаблокироватьДокумент(РеализацияТоваровУслугОбъект, Параметры.СобытиеЖурналаРегистрации);
								
								Если Не ПустаяСтрока(ТекстОшибки) Тогда
									Если Ошибка.Детализация = Неопределено Тогда
										Ошибка.Детализация = Новый Массив;
									КонецЕсли;
									Ошибка.Детализация.Добавить(ТекстОшибки);
									
									Если Результат.Ссылки.Найти(РеализацияТоваровУслугСсылка) = Неопределено Тогда
										Результат.Ссылки.Добавить(РеализацияТоваровУслугСсылка);
										Результат.СостоянияСсылок.Вставить(РеализацияТоваровУслугСсылка, 
											ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
									КонецЕсли;
									
								Иначе
									РеализацияТоваровУслугОбъект.Статус                         = Перечисления.СтатусыРеализацийТоваровУслуг.Отгружено;
									РеализацияТоваровУслугОбъект.ДатаПереходаПраваСобственности = ДатаПереходаПраваСобственности;
									
									РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(РеализацияТоваровУслугОбъект, 
										Истина, Параметры.СобытиеЖурналаРегистрации);
									РеализацияТоваровУслугСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
										ДоступныеСостоянияДокументов.СостояниеОбновлен, 
										РеализацияТоваровУслугСостояние);
										
									Если Результат.Ссылки.Найти(РеализацияТоваровУслугОбъект.Ссылка) = Неопределено Тогда
										Результат.Ссылки.Добавить(РеализацияТоваровУслугОбъект.Ссылка);
									КонецЕсли;
									Результат.СостоянияСсылок.Вставить(РеализацияТоваровУслугСсылка, РеализацияТоваровУслугСостояние);
										
									РеализацияТоваровУслугОбъект.Разблокировать();
									
									Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
										Если Ошибка.Детализация = Неопределено Тогда
											Ошибка.Детализация = Новый Массив;
										КонецЕсли;
										Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
									КонецЕсли;
								КонецЕсли;
							КонецЕсли;
							
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
					// Корректировка по стоимости
					Если ВыполнитьКорректировкуПоСумме 
							И СозданныеКорректировки.Получить(ВыборкаНомерОтправления.НомерОтправления) = Неопределено Тогда
						КорректировкаРеализацииОбъект    = Неопределено;
						КорректировкаРеализацииСсылка    = Неопределено;
						КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеНеОбновлен;
						
						Если ВыборкаДатаДоставки.Сумма <> РеализацияТоваровУслугСумма Тогда
							//@skip-check bsl-legacy-check-string-literal
							Если ЗначениеЗаполнено(ВыборкаНомерОтправления.КорректировкаРеализацииСсылка)
									И Параметры.ПерезаписатьДокументы
									И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаНомерОтправления.КорректировкаРеализацииСсылка, "Проведен") Тогда
								КорректировкаРеализацииСсылка    = ВыборкаНомерОтправления.КорректировкаРеализацииСсылка;
								КорректировкаРеализацииОбъект    = КорректировкаРеализацииСсылка.ПолучитьОбъект();
								КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеОбновлен;
								
								ТекстОшибки = ЗаблокироватьДокумент(КорректировкаРеализацииОбъект, Параметры.СобытиеЖурналаРегистрации);
								
								Если Не ПустаяСтрока(ТекстОшибки) Тогда
									Если Ошибка.Детализация = Неопределено Тогда
										Ошибка.Детализация = Новый Массив;
									КонецЕсли;
									Ошибка.Детализация.Добавить(ТекстОшибки);
									
								Иначе
									РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(КорректировкаРеализацииОбъект, 
										Ложь, Параметры.СобытиеЖурналаРегистрации);
									КорректировкаРеализацииСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
										КорректировкаРеализацииСостояние, 
										ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
										
									КорректировкаРеализацииОбъект.Разблокировать();
									
									Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
										Если Ошибка.Детализация = Неопределено Тогда
											Ошибка.Детализация = Новый Массив;
										КонецЕсли;
										Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
									КонецЕсли;
								КонецЕсли;
								
								Если ЗначениеЗаполнено(КорректировкаРеализацииСсылка) 
										И Результат.Ссылки.Найти(КорректировкаРеализацииСсылка) = Неопределено Тогда
									Результат.Ссылки.Добавить(КорректировкаРеализацииСсылка);
									Результат.СостоянияСсылок.Вставить(КорректировкаРеализацииСсылка, КорректировкаРеализацииСостояние);
								КонецЕсли;
								
								КорректировкаРеализацииОбъект    = Неопределено;
								КорректировкаРеализацииСсылка    = Неопределено;
								КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеНеОбновлен;
							КонецЕсли;
							
							// Товары, переданные в доставку
							ТолькоТоварыПереданные = Неопределено;
							
							ВыборкаДанных.Сбросить();
							Пока ВыборкаДанных.Следующий() Цикл
								Если ТолькоТоварыПереданные = Неопределено Тогда
									ТолькоТоварыПереданные = (ВыборкаДанных.ТипОперации = 1);
								ИначеЕсли ТолькоТоварыПереданные И ВыборкаДанных.ТипОперации <> 1 Тогда
									Прервать;
								КонецЕсли;
								
								Если Не ЗначенияФункциональныхОпций.ИспользоватьКорректировкиРеализаций Тогда
									ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru = 'Корректировка стоимости по заказу (отправлению) №%1 от %2 не оформлен.';
											|en = 'Cost adjustment for order (shipment) No. %1 dated %2 is not registered.'"),
										ВыборкаДанных.НомерОтправления,
										Формат(ВыборкаДанных.ДатаОтправления, "ДЛФ=D;"));
										
									Если Ошибка.Детализация = Неопределено Тогда
										Ошибка.Детализация = Новый Массив;
									КонецЕсли;
									Ошибка.Детализация.Добавить(ТекстОшибки + " " + НСтр("ru = 'Подробности см. в журнале регистрации.';
																						|en = 'See the event log for details.'"));
									
									ТекстОшибки = ТекстОшибки + Символы.ПС
										+ НСтр("ru = 'Для отражения данных о корректировке стоимости товаров по схеме ""Реализация с отложенным переходом права собственности"" необходимо включить опцию корректировок реализаций в настройках раздела ""НСИ и администрирование"" - ""Продажи"" - ""Оптовые продажи"".';
												|en = 'To display data about goods cost adjustment by the ""Sale with deferred transfer of title to goods"" scheme, enable sales adjustments in Master data and administration - Sales - Wholesale.'");
										
									ЗаписьЖурналаРегистрации(Параметры.СобытиеЖурналаРегистрации, 
										УровеньЖурналаРегистрации.Предупреждение,
										,
										,
										ТекстОшибки);
									Прервать;
								КонецЕсли;
								
								Если КорректировкаРеализацииОбъект = Неопределено Тогда
									КорректировкаРеализацииСсылка = ВыборкаДанных.КорректировкаРеализацииПоСумме;
									
									Если ЗначениеЗаполнено(КорректировкаРеализацииСсылка) Тогда
										// Существующий документ
										КорректировкаРеализацииОбъект    = КорректировкаРеализацииСсылка.ПолучитьОбъект();
										КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеНеОбновлен;
										
										ТекстОшибки = ЗаблокироватьДокумент(КорректировкаРеализацииОбъект, Параметры.СобытиеЖурналаРегистрации);
										
										Если Не ПустаяСтрока(ТекстОшибки) Тогда
											Если Ошибка.Детализация = Неопределено Тогда
												Ошибка.Детализация = Новый Массив;
											КонецЕсли;
											Ошибка.Детализация.Добавить(ТекстОшибки);
											
											Прервать;
										КонецЕсли;
										
									Иначе
										// Новый документ
										КорректировкаРеализацииОбъект    = Документы.КорректировкаРеализации.СоздатьДокумент();
										КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеНовый;
									КонецЕсли;
									
									ДанныеЗаполненияКорректировка.Вставить("Дата",              КонецДня(ДатаПереходаПраваСобственности));
									ДанныеЗаполненияКорректировка.Вставить("ДокументОснование", РеализацияТоваровУслугСсылка);
									ДанныеЗаполненияКорректировка.Вставить("Основание",         СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
																									НСтр("ru = '%1. Отчет о реализации товаров';
																										|en = '%1. Goods sales report'"),
																									УчетнаяЗаписьПредставление));
									ДанныеЗаполненияКорректировка.Вставить("ОснованиеНомер",    ВыборкаДанных.НомерОтправления);
									ДанныеЗаполненияКорректировка.Вставить("ОснованиеДата",     ВыборкаДанных.ДатаОтправления);
									ДанныеЗаполненияКорректировка.Вставить("Комментарий",       КомментарийКорректировкаСтоимости(
																									УчетнаяЗаписьПредставление,
																									Формат(ВыборкаДанных.ДатаОтправления, "ДЛФ=D;"),
																									ВыборкаДанных.НомерОтправления));
									
									Если КорректировкаРеализацииЗаполнить(КорректировкаРеализацииОбъект, ДанныеЗаполненияКорректировка, 
											ЗначенияФункциональныхОпций, Параметры.СобытиеЖурналаРегистрации) Тогда
										Если КорректировкаРеализацииСостояние <> ДоступныеСостоянияДокументов.СостояниеНовый Тогда
											КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеОбновлен;
										КонецЕсли;
									Иначе
										ЗаполнитьЗначенияСвойств(КорректировкаРеализацииОбъект, ДанныеЗаполненияКорректировка);
										Документы.КорректировкаРеализации.ЗаполнитьТоварыПоИсходнымДанным(
											КорректировкаРеализацииОбъект.ДокументОснование,
											КорректировкаРеализацииОбъект.Товары,
											КорректировкаРеализацииОбъект.Расхождения,
											КорректировкаРеализацииОбъект.Ссылка);
											
										КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеОшибкиЗаполнения;
									КонецЕсли;
									
									Если Не ЗначениеЗаполнено(КорректировкаРеализацииОбъект.Основание) Тогда
										КорректировкаРеализацииОбъект.Основание = ДанныеЗаполненияКорректировка.Основание;
									КонецЕсли;
									КорректировкаРеализацииОбъект.ОснованиеНомер = ДанныеЗаполненияКорректировка.ОснованиеНомер;
									КорректировкаРеализацииОбъект.ОснованиеДата  = ДанныеЗаполненияКорректировка.ОснованиеДата;
									
									КорректировкаРеализацииОбъект.Товары.Очистить();
								КонецЕсли;
								
								Если Не Параметры.ЗагружатьНезаполненныеСтроки
										И ВыборкаДанных.РезультатСопоставленияСтроки <> ВариантыРезультатовСопоставления.СтрокаСопоставлена Тогда
									Продолжить;
								КонецЕсли;
								
								// Заполнение строки табличной части
								СтрокаТабличнойЧасти = КорректировкаРеализацииОбъект.Товары.Добавить();
								СтрокаТабличнойЧасти.Номенклатура = ВыборкаДанных.Номенклатура;
								
								Если ВыборкаДанных.ХарактеристикиИспользуются Тогда
									СтрокаТабличнойЧасти.Характеристика = ВыборкаДанных.Характеристика;
								КонецЕсли;
								
								Если ЗначениеЗаполнено(ВыборкаДанных.Упаковка)
										И ВыборкаДанных.Упаковка <> ВыборкаДанных.НоменклатураЕдиницаИзмерения Тогда
									Если ЗначенияФункциональныхОпций.ИспользоватьУпаковкиНоменклатуры Тогда
										СтрокаТабличнойЧасти.Упаковка = ВыборкаДанных.Упаковка;
										КоличествоУпаковок            = ВыборкаДанных.Количество;
										Количество                    = Окр(КоличествоУпаковок * ВыборкаДанных.УпаковкаКоэффициент, 
																			3, РежимОкругления.Окр15как20);
									Иначе
										СтрокаТабличнойЧасти.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
										КоличествоУпаковок            = Окр(ВыборкаДанных.Количество * ВыборкаДанных.УпаковкаКоэффициент, 
																			3, РежимОкругления.Окр15как20);
										Количество                    = КоличествоУпаковок;
									КонецЕсли;
								Иначе
									СтрокаТабличнойЧасти.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
									КоличествоУпаковок            = ВыборкаДанных.Количество;
									Количество                    = КоличествоУпаковок;
								КонецЕсли;
								
								СтрокаТабличнойЧасти.КоличествоУпаковок = КоличествоУпаковок;
								СтрокаТабличнойЧасти.Количество         = Количество;
								СтрокаТабличнойЧасти.Цена               = ВыборкаДанных.Цена;
								СтрокаТабличнойЧасти.Сумма              = ВыборкаДанных.Сумма;
								СтрокаТабличнойЧасти.СуммаСНДС          = ВыборкаДанных.Сумма;
								СтрокаТабличнойЧасти.ВариантОтражения   = Перечисления.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализацию;
								
								Если ЕстьСкладВВыборке Тогда
									СтрокаТабличнойЧасти.Склад = ВыборкаДанных.Склад;
								Иначе
									СтрокаТабличнойЧасти.Склад = КорректировкаРеализацииОбъект.Склад;
								КонецЕсли;
								
								Если ЕстьЗаказВВыборке Тогда
									СтрокаТабличнойЧасти.ЗаказКлиента = ВыборкаДанных.ЗаказКлиентаСсылка;
								КонецЕсли;
							КонецЦикла;
							
							Если КорректировкаРеализацииОбъект <> Неопределено Тогда
								// Постобработка табличной части
								СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(КорректировкаРеализацииОбъект);
								Если Параметры.ПараметрыОбработкиКорректировка.ПересчитатьЦенаВключаетНДС Тогда
									СтруктураПересчетаСуммы.Вставить("ЦенаВключаетНДС", Истина);
								КонецЕсли;
								
								СтруктураДействий = Новый Структура;
								СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС",   ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(
																						КорректировкаРеализацииОбъект));
								СтруктураДействий.Вставить("ПересчитатьСумму",     "КоличествоУпаковок");
								СтруктураДействий.Вставить("ПересчитатьСуммуНДС",  СтруктураПересчетаСуммы);
								СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
								
								Параметры.ПараметрыОбработкиКорректировка.Вставить("СтруктураДействий", СтруктураДействий);
								
								Если Не КорректировкаРеализацииОбработатьТЧ(КорректировкаРеализацииОбъект, Параметры.ПараметрыОбработкиКорректировка, 
											Параметры.СобытиеЖурналаРегистрации) Тогда
									КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеОшибкиЗаполнения;
								КонецЕсли;
								
								Если КорректировкаРеализацииОбъект.Расхождения.Количество() > 0 Тогда
									// Запись и проведение документа
									РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(КорректировкаРеализацииОбъект, 
										Истина, Параметры.СобытиеЖурналаРегистрации);
									КорректировкаРеализацииСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
										КорректировкаРеализацииСостояние, 
										ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
										
									Если КорректировкаРеализацииСостояние <> ДоступныеСостоянияДокументов.СостояниеНовый Тогда
										КорректировкаРеализацииОбъект.Разблокировать();
									КонецЕсли;
									
									Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
										Если Ошибка.Детализация = Неопределено Тогда
											Ошибка.Детализация = Новый Массив;
										КонецЕсли;
										Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
									КонецЕсли;
								
								ИначеЕсли Не КорректировкаРеализацииОбъект.ЭтоНовый() Тогда
									// Запись и пометка на удаление документа
									КорректировкаРеализацииОбъект.ПометкаУдаления = Истина;
									
									РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(КорректировкаРеализацииОбъект, 
										Ложь, Параметры.СобытиеЖурналаРегистрации);
									КорректировкаРеализацииСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
										КорректировкаРеализацииСостояние, 
										ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
										
									КорректировкаРеализацииОбъект.Разблокировать();
									
									Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
										Если Ошибка.Детализация = Неопределено Тогда
											Ошибка.Детализация = Новый Массив;
										КонецЕсли;
										Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
									КонецЕсли;
								КонецЕсли;
								
								Если Не ЗначениеЗаполнено(КорректировкаРеализацииСсылка) Тогда
									КорректировкаРеализацииСсылка = КорректировкаРеализацииОбъект.Ссылка;
								КонецЕсли;
								
								СозданныеКорректировки.Вставить(ВыборкаНомерОтправления.НомерОтправления, КорректировкаРеализацииСсылка);
							КонецЕсли;
							
							Если ЗначениеЗаполнено(КорректировкаРеализацииСсылка) 
									И Результат.Ссылки.Найти(КорректировкаРеализацииСсылка) = Неопределено Тогда
								Результат.Ссылки.Добавить(КорректировкаРеализацииСсылка);
								Результат.СостоянияСсылок.Вставить(КорректировкаРеализацииСсылка, КорректировкаРеализацииСостояние);
							КонецЕсли;
						КонецЕсли;
						
						//@skip-check bsl-legacy-check-string-literal
						Если КорректировкаРеализацииОбъект = Неопределено
								И ЗначениеЗаполнено(ВыборкаДатаДоставки.КорректировкаРеализацииПоСумме)
								И Параметры.ПерезаписатьДокументы
								И Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаДатаДоставки.КорректировкаРеализацииПоСумме, "ПометкаУдаления") Тогда
							КорректировкаРеализацииСсылка    = ВыборкаДатаДоставки.КорректировкаРеализацииПоСумме;
							КорректировкаРеализацииОбъект    = КорректировкаРеализацииСсылка.ПолучитьОбъект();
							КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеОбновлен;
							
							ТекстОшибки = ЗаблокироватьДокумент(КорректировкаРеализацииОбъект, Параметры.СобытиеЖурналаРегистрации);
							
							Если Не ПустаяСтрока(ТекстОшибки) Тогда
								Если Ошибка.Детализация = Неопределено Тогда
									Ошибка.Детализация = Новый Массив;
								КонецЕсли;
								Ошибка.Детализация.Добавить(ТекстОшибки);
								
							Иначе
								КорректировкаРеализацииОбъект.ПометкаУдаления = Истина;
								
								РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(КорректировкаРеализацииОбъект, 
									Ложь, Параметры.СобытиеЖурналаРегистрации);
								КорректировкаРеализацииСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
									КорректировкаРеализацииСостояние, 
									ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
									
								КорректировкаРеализацииОбъект.Разблокировать();
								
								Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
									Если Ошибка.Детализация = Неопределено Тогда
										Ошибка.Детализация = Новый Массив;
									КонецЕсли;
									Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
						
						Если ЗначениеЗаполнено(КорректировкаРеализацииСсылка) 
								И Результат.Ссылки.Найти(КорректировкаРеализацииСсылка) = Неопределено Тогда
							Результат.Ссылки.Добавить(КорректировкаРеализацииСсылка);
							Результат.СостоянияСсылок.Вставить(КорректировкаРеализацииСсылка, КорректировкаРеализацииСостояние);
						КонецЕсли;
					КонецЕсли;
					
					// Невыкупы и возврат
					КорректировкаРеализацииОбъект    = Неопределено;
					КорректировкаРеализацииСсылка    = Неопределено;
					КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеНеОбновлен;
					
					ВыборкаДанных.Сбросить();
					Пока ВыборкаДанных.Следующий() Цикл
						Если ВыборкаДанных.ТипОперации = 3
								Или ВыборкаДанных.ТипОперации = 4 Тогда
							Если ЕстьРеализацияДругогоПериода
									И ВыборкаДанных.РеализацияДругогоПериода Тогда
								Если Результат.Служебное.Найти(РеализацияТоваровУслугСсылка) = Неопределено Тогда
									Результат.Служебное.Добавить(РеализацияТоваровУслугСсылка);
								КонецЕсли;
							КонецЕсли;
							
							Если Не ЗначенияФункциональныхОпций.ИспользоватьКорректировкиРеализаций Тогда
								ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									?(ВыборкаДанных.ТипОперации = 3, 
										НСтр("ru = 'Невыкуп товаров по заказу (отправлению) №%1 от %2 не оформлен.';
											|en = 'The goods non-purchase for order (shipment) No. %1 dated %2 is not registered.'"), 
										НСтр("ru = 'Возврат товаров по заказу (отправлению) №%1 от %2 не оформлен.';
											|en = 'The return of goods for order (shipment) No. %1 dated %2 is not registered.'")),
									ВыборкаДанных.НомерОтправления,
									Формат(ВыборкаДанных.ДатаОтправления, "ДЛФ=D;"));
									
								Если Ошибка.Детализация = Неопределено Тогда
									Ошибка.Детализация = Новый Массив;
								КонецЕсли;
								Ошибка.Детализация.Добавить(ТекстОшибки + " " + НСтр("ru = 'Подробности см. в журнале регистрации.';
																					|en = 'See the event log for details.'"));
								
								ТекстОшибки = ТекстОшибки + Символы.ПС
									+ НСтр("ru = 'Для отражения данных о невыкупах и возвратах товаров по схеме ""Реализация с отложенным переходом права собственности"" необходимо включить опцию корректировок реализаций в настройках раздела ""НСИ и администрирование"" - ""Продажи"" - ""Оптовые продажи"".';
											|en = 'To record data on goods non-purchase and return by the ""Sale with deferred transfer of title to goods"" scheme, enable sales adjustments in Master data and settings - Sales - Wholesale.'");
									
								ЗаписьЖурналаРегистрации(Параметры.СобытиеЖурналаРегистрации, 
									УровеньЖурналаРегистрации.Предупреждение,
									,
									,
									ТекстОшибки);
								Прервать;
							КонецЕсли;
							
							Если КорректировкаРеализацииОбъект = Неопределено Тогда
								КорректировкаРеализацииСсылка = ВыборкаДанных.КорректировкаРеализацииСсылка;
								
								Если ЗначениеЗаполнено(КорректировкаРеализацииСсылка) Тогда
									// Существующий документ
									Если Не Параметры.ПерезаписатьДокументы Тогда
										Прервать;
									КонецЕсли;
									
									КорректировкаРеализацииОбъект    = КорректировкаРеализацииСсылка.ПолучитьОбъект();
									КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеНеОбновлен;
									
									ТекстОшибки = ЗаблокироватьДокумент(КорректировкаРеализацииОбъект, Параметры.СобытиеЖурналаРегистрации);
									
									Если Не ПустаяСтрока(ТекстОшибки) Тогда
										Если Ошибка.Детализация = Неопределено Тогда
											Ошибка.Детализация = Новый Массив;
										КонецЕсли;
										Ошибка.Детализация.Добавить(ТекстОшибки);
										
										Прервать;
									КонецЕсли;
									
								Иначе
									// Новый документ
									КорректировкаРеализацииОбъект    = Документы.КорректировкаРеализации.СоздатьДокумент();
									КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеНовый;
								КонецЕсли;
								
								ДанныеЗаполненияКорректировка.Вставить("Дата",              КонецДня(Макс(ДатаПереходаПраваСобственности, ВыборкаДанных.ДатаНевыкупаВозврата)));
								ДанныеЗаполненияКорректировка.Вставить("ДокументОснование", РеализацияТоваровУслугСсылка);
								ДанныеЗаполненияКорректировка.Вставить("Основание",         СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
																								НСтр("ru = '%1. Отчет о реализации товаров';
																									|en = '%1. Goods sales report'"),
																								УчетнаяЗаписьПредставление));
								ДанныеЗаполненияКорректировка.Вставить("ОснованиеНомер",    ВыборкаДанных.НомерОтправления);
								ДанныеЗаполненияКорректировка.Вставить("ОснованиеДата",     ВыборкаДанных.ДатаОтправления);
								
								Если ВыборкаДанных.ТипОперации = 3 Тогда
									Комментарий = КомментарийНевыкупТоваров(
										УчетнаяЗаписьПредставление,
										Формат(ВыборкаДанных.ДатаОтправления, "ДЛФ=D;"),
										ВыборкаДанных.НомерОтправления);
								Иначе
									Комментарий = КомментарийВозвратТоваров(
										УчетнаяЗаписьПредставление,
										Формат(ВыборкаДанных.ДатаОтправления, "ДЛФ=D;"),
										ВыборкаДанных.НомерОтправления);
								КонецЕсли;
								ДанныеЗаполненияКорректировка.Вставить("Комментарий", Комментарий);
								
								Если КорректировкаРеализацииЗаполнить(КорректировкаРеализацииОбъект, ДанныеЗаполненияКорректировка, 
										ЗначенияФункциональныхОпций, Параметры.СобытиеЖурналаРегистрации) Тогда
									Если КорректировкаРеализацииСостояние <> ДоступныеСостоянияДокументов.СостояниеНовый Тогда
										КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеОбновлен;
									КонецЕсли;
								Иначе
									ЗаполнитьЗначенияСвойств(КорректировкаРеализацииОбъект, ДанныеЗаполненияКорректировка);
									Документы.КорректировкаРеализации.ЗаполнитьТоварыПоИсходнымДанным(
										КорректировкаРеализацииОбъект.ДокументОснование,
										КорректировкаРеализацииОбъект.Товары,
										КорректировкаРеализацииОбъект.Расхождения,
										КорректировкаРеализацииОбъект.Ссылка);
										
									КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеОшибкиЗаполнения;
								КонецЕсли;
								
								Если Не ЗначениеЗаполнено(КорректировкаРеализацииОбъект.Основание) Тогда
									КорректировкаРеализацииОбъект.Основание = ДанныеЗаполненияКорректировка.Основание;
								КонецЕсли;
								КорректировкаРеализацииОбъект.ОснованиеНомер = ДанныеЗаполненияКорректировка.ОснованиеНомер;
								КорректировкаРеализацииОбъект.ОснованиеДата  = ДанныеЗаполненияКорректировка.ОснованиеДата;
							КонецЕсли;
							
							Если Не Параметры.ЗагружатьНезаполненныеСтроки
									И ВыборкаДанных.РезультатСопоставленияСтроки <> ВариантыРезультатовСопоставления.СтрокаСопоставлена Тогда
								Продолжить;
							КонецЕсли;
							
							// Заполнение строки табличной части
							Номенклатура = ВыборкаДанных.Номенклатура;
							
							Если ВыборкаДанных.ХарактеристикиИспользуются Тогда
								Характеристика = ВыборкаДанных.Характеристика;
							Иначе
								Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
							КонецЕсли;
							
							Если ЗначениеЗаполнено(ВыборкаДанных.Упаковка)
									И ВыборкаДанных.Упаковка <> ВыборкаДанных.НоменклатураЕдиницаИзмерения Тогда
								Если ЗначенияФункциональныхОпций.ИспользоватьУпаковкиНоменклатуры Тогда
									Упаковка           = ВыборкаДанных.Упаковка;
									КоличествоУпаковок = ВыборкаДанных.Количество;
									Количество         = Окр(КоличествоУпаковок * ВыборкаДанных.УпаковкаКоэффициент, 
															3, РежимОкругления.Окр15как20);
								Иначе
									Упаковка           = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
									КоличествоУпаковок = Окр(ВыборкаДанных.Количество * ВыборкаДанных.УпаковкаКоэффициент, 
															3, РежимОкругления.Окр15как20);
									Количество         = КоличествоУпаковок;
								КонецЕсли;
							Иначе
								Упаковка           = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
								КоличествоУпаковок = ВыборкаДанных.Количество;
								Количество         = КоличествоУпаковок;
							КонецЕсли;
							
							Отбор = Новый Структура;
							Отбор.Вставить("Номенклатура",   Номенклатура);
							Отбор.Вставить("Характеристика", Характеристика);
							Отбор.Вставить("Упаковка",       Упаковка);
							
							НайденныеСтроки = КорректировкаРеализацииОбъект.Товары.НайтиСтроки(Отбор);
							Найдено         = Ложь;
							
							Для Каждого СтрокаТабличнойЧасти Из НайденныеСтроки Цикл
								Если Количество > 0
										И СтрокаТабличнойЧасти.Количество > 0 
										И Окр(ВыборкаДанных.Сумма / Количество, 2, РежимОкругления.Окр15как20) = Окр(СтрокаТабличнойЧасти.СуммаСНДС / СтрокаТабличнойЧасти.Количество, 2, РежимОкругления.Окр15как20) Тогда
									Найдено                                 = Истина;
									ДляСписанияКоличество                   = Мин(Количество, СтрокаТабличнойЧасти.Количество);
									Количество                              = Количество - ДляСписанияКоличество;
									СтрокаТабличнойЧасти.Количество         = СтрокаТабличнойЧасти.Количество - ДляСписанияКоличество;
									ДляСписанияКоличествоУпаковок           = Мин(КоличествоУпаковок, СтрокаТабличнойЧасти.КоличествоУпаковок);
									КоличествоУпаковок                      = КоличествоУпаковок - ДляСписанияКоличествоУпаковок;
									СтрокаТабличнойЧасти.КоличествоУпаковок = СтрокаТабличнойЧасти.КоличествоУпаковок - ДляСписанияКоличествоУпаковок;
									
									Если СтрокаТабличнойЧасти.Количество = 0 Тогда
										КорректировкаРеализацииОбъект.Товары.Удалить(СтрокаТабличнойЧасти);
									КонецЕсли;
								КонецЕсли;
							КонецЦикла;
							
							Если Не Найдено Тогда
								Продолжить;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
					
					//@skip-check bsl-legacy-check-string-literal
					Если КорректировкаРеализацииОбъект <> Неопределено Тогда
						// Постобработка табличной части
						Параметры.ПараметрыОбработкиКорректировка.Вставить("СтруктураДействий", Неопределено);
						
						Если Не КорректировкаРеализацииОбработатьТЧ(КорректировкаРеализацииОбъект, Параметры.ПараметрыОбработкиКорректировка, 
									Параметры.СобытиеЖурналаРегистрации) Тогда
							КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеОшибкиЗаполнения;
						КонецЕсли;
						
						Если КорректировкаРеализацииОбъект.Расхождения.Количество() > 0 Тогда
							// Запись и проведение документа
							РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(КорректировкаРеализацииОбъект, 
								Истина, Параметры.СобытиеЖурналаРегистрации);
							КорректировкаРеализацииСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
								КорректировкаРеализацииСостояние, 
								ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
								
							Если КорректировкаРеализацииСостояние <> ДоступныеСостоянияДокументов.СостояниеНовый Тогда
								КорректировкаРеализацииОбъект.Разблокировать();
							КонецЕсли;
							
							Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
								Если Ошибка.Детализация = Неопределено Тогда
									Ошибка.Детализация = Новый Массив;
								КонецЕсли;
								Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
							КонецЕсли;
						
						ИначеЕсли Не КорректировкаРеализацииОбъект.ЭтоНовый() Тогда
							// Запись и пометка на удаление документа
							КорректировкаРеализацииОбъект.ПометкаУдаления = Истина;
							
							РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(КорректировкаРеализацииОбъект, 
								Ложь, Параметры.СобытиеЖурналаРегистрации);
							КорректировкаРеализацииСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
								КорректировкаРеализацииСостояние, 
								ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
								
							КорректировкаРеализацииОбъект.Разблокировать();
							
							Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
								Если Ошибка.Детализация = Неопределено Тогда
									Ошибка.Детализация = Новый Массив;
								КонецЕсли;
								Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
							КонецЕсли;
						КонецЕсли;
						
						Если Не ЗначениеЗаполнено(КорректировкаРеализацииСсылка) Тогда
							КорректировкаРеализацииСсылка = КорректировкаРеализацииОбъект.Ссылка;
						КонецЕсли;
						
					ИначеЕсли ЗначениеЗаполнено(ВыборкаДатаДоставки.КорректировкаРеализацииСсылка)
								И Параметры.ПерезаписатьДокументы
								И Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаДатаДоставки.КорректировкаРеализацииСсылка, "ПометкаУдаления") Тогда
						КорректировкаРеализацииСсылка    = ВыборкаДатаДоставки.КорректировкаРеализацииСсылка;
						КорректировкаРеализацииОбъект    = КорректировкаРеализацииСсылка.ПолучитьОбъект();
						КорректировкаРеализацииСостояние = ДоступныеСостоянияДокументов.СостояниеОбновлен;
						
						ТекстОшибки = ЗаблокироватьДокумент(КорректировкаРеализацииОбъект, Параметры.СобытиеЖурналаРегистрации);
						
						Если Не ПустаяСтрока(ТекстОшибки) Тогда
							Если Ошибка.Детализация = Неопределено Тогда
								Ошибка.Детализация = Новый Массив;
							КонецЕсли;
							Ошибка.Детализация.Добавить(ТекстОшибки);
							
						Иначе
							КорректировкаРеализацииОбъект.ПометкаУдаления = Истина;
							
							РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(КорректировкаРеализацииОбъект, 
								Ложь, Параметры.СобытиеЖурналаРегистрации);
							КорректировкаРеализацииСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
								КорректировкаРеализацииСостояние, 
								ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
								
							КорректировкаРеализацииОбъект.Разблокировать();
							
							Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
								Если Ошибка.Детализация = Неопределено Тогда
									Ошибка.Детализация = Новый Массив;
								КонецЕсли;
								Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
								
							Иначе
								КорректировкаРеализацииСсылка = КорректировкаРеализацииОбъект.Ссылка;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(КорректировкаРеализацииСсылка) 
							И Результат.Ссылки.Найти(КорректировкаРеализацииСсылка) = Неопределено Тогда
						Результат.Ссылки.Добавить(КорректировкаРеализацииСсылка);
						Результат.СостоянияСсылок.Вставить(КорректировкаРеализацииСсылка, КорректировкаРеализацииСостояние);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		НомерТекущейСтроки = НомерТекущейСтроки + 1;
	КонецЦикла;
	
	Если НеПроведенныеПоЗаказам Тогда
		ТекстОшибки = НСтр("ru = 'Документы реализации, созданные при загрузке отчета по оформленным ранее заказам, необходимо проверить по составу и стоимости, а затем провести их.';
							|en = 'Check the items and prices in the sales documents created when importing the report on earlier created orders and then post the documents.'");
			
		Если Ошибка.Детализация = Неопределено Тогда
			Ошибка.Детализация = Новый Массив;
		КонецЕсли;
		Ошибка.Детализация.Добавить(ТекстОшибки);
	КонецЕсли;
	
	Возврат Ошибка;
	
КонецФункции

// Создает документы взаимозачета по продажам с отложенным переходом права собственности.
//
// Параметры:
//   Результат          - Структура - см. РезультатЗаписиЗагружаемыхДанных.
//   ТаблицаДанных      - ТаблицаЗначений - подготовленная таблица сопоставленных данных для загрузки.
//   Параметры          - Структура - параметры заполнения документов:
//     * ВидТорговойПлощадки       - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки;
//     * УчетнаяЗапись             - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки;
//     * ВалютаУчета               - СправочникСсылка.Валюты, Неопределено - валюта учета учетной записи;
//     * Организация               - СправочникСсылка.Организации - организация взаимозачета;
//     * Партнер                   - СправочникСсылка.Партнеры - партнер взаимозачета;
//     * Контрагент                - СправочникСсылка.Контрагенты - контрагент взаимозачета;
//     * Соглашение                - СправочникСсылка.СоглашенияСКлиентами - соглашение взаимозачета;
//     * Договор                   - СправочникСсылка.ДоговорыКонтрагентов - договор взаимозачета;
//     * КодОшибки                 - Строка - код ошибки для результат выполнения;
//     * СобытиеЖурналаРегистрации - Строка - наименование события для записей в журнале регистрации;
//     * ПерезаписатьДокументы     - Булево - перезаписывать ли существующие документы;
//     * ОчиститьТабличныеЧасти    - Булево - очищать табличные части документов перед обработкой;
//     * ТекстЗапроса              - Строка - текст запроса для отражения взаимозачета по продажам;
//     * ИсключаемыеОбъекты        - Массив Из ДокументСсылка.РеализацияТоваровУслуг - исключаемые из обработки документы.
//   ПараметрыПрогресса - См. ПараметрыПрогресса.
//
// Возвращаемое значение:
//   См. НоваяОшибка.
//
Функция ЗаписатьВзаимозачетыЗадолженности(Результат, Параметры, ПараметрыПрогресса = Неопределено) Экспорт
	
	Ошибка = НоваяОшибка();
	
	Если Не ЗначениеЗаполнено(Параметры.УчетнаяЗапись) Тогда
		ТекстОшибки = НСтр("ru = 'Не указана учетная запись торговой площадки.';
							|en = 'Marketplace personal account not specified.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		Ошибка.КодОшибки      = Параметры.КодОшибки;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	Если Результат.Ссылки.Количество() = 0 Тогда
		Возврат Ошибка;
	КонецЕсли;
	
	ЗначенияФункциональныхОпций = ПолучитьФункциональныеОпции(Параметры.Соглашение);
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ИнтеграцияСМаркетплейсамиСервер.ЗаписатьВзаимозачетыЗадолженности");
	
	УчетнаяЗаписьПредставление     =
		Справочники.УчетныеЗаписиМаркетплейсов.ПолноеПредставлениеУчетнойЗаписи(
			Параметры.ВидТорговойПлощадки,
			Параметры.УчетнаяЗапись);
	ВалютаРегламентированногоУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Параметры.Организация);
	ДоступныеСостоянияДокументов   = ДоступныеСостоянияДокументов();
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ВидОперации",  Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПереносДолгаКлиентаМеждуКонтрагентами);
	ДанныеЗаполнения.Вставить("Организация",  Параметры.Организация);
	ДанныеЗаполнения.Вставить("ТипДебитора",  Перечисления.ТипыУчастниковВзаимозачета.Клиент);
	ДанныеЗаполнения.Вставить("ТипКредитора", Перечисления.ТипыУчастниковВзаимозачета.Клиент);
	ДанныеЗаполнения.Вставить("Валюта",       ?(ЗначениеЗаполнено(Параметры.ВалютаУчета), 
													Параметры.ВалютаУчета, 
													ВалютаРегламентированногоУчета));
	
	ПараметрыОбъекта = Новый Структура;
	ПараметрыОбъекта.Вставить("ТипРасчетов",        Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
	ПараметрыОбъекта.Вставить("Организация",        Параметры.Организация);
	ПараметрыОбъекта.Вставить("Партнер",            Параметры.Партнер);
	ПараметрыОбъекта.Вставить("Контрагент",         Параметры.Контрагент);
	ПараметрыОбъекта.Вставить("Договор",            Параметры.Договор);
	ПараметрыОбъекта.Вставить("Объект",             Параметры.Договор);
	ПараметрыОбъекта.Вставить("ТипОбъектаРасчетов", Перечисления.ТипыОбъектовРасчетов.Договор);
	
	ОбъектРасчетов = ОпределитьОбъектРасчетов(ПараметрыОбъекта);

	Запрос = Новый Запрос;
	Запрос.Текст = Параметры.ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ЗагруженныеОбъекты",                Результат.Ссылки);
	Запрос.УстановитьПараметр("ИсключаемыеОбъекты",                Параметры.ИсключаемыеОбъекты);
	Запрос.УстановитьПараметр("Организация",                       Параметры.Организация);
	Запрос.УстановитьПараметр("ВидОперации",                       Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПереносДолгаКлиентаМеждуКонтрагентами);
	Запрос.УстановитьПараметр("Контрагент",                        Параметры.Контрагент);
	Запрос.УстановитьПараметр("ТипРасчетов",                       Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
	Запрос.УстановитьПараметр("КомментарийНевыкупТоваров",         КомментарийНевыкупТоваров(УчетнаяЗаписьПредставление));
	Запрос.УстановитьПараметр("КомментарийВозвратТоваров",         КомментарийВозвратТоваров(УчетнаяЗаписьПредставление));
	Запрос.УстановитьПараметр("КомментарийКорректировка",          КомментарийКорректировкаСтоимости(УчетнаяЗаписьПредставление));
	Запрос.УстановитьПараметр("КомментарийОформлениеВзаимозачета", КомментарийОформлениеВзаимозачета(УчетнаяЗаписьПредставление));
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
		Замер, 
		Результат.Ссылки.Количество() / 10);
		
	ВыборкаДатаДокумента = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВсегоСтрок           = ВыборкаДатаДокумента.Количество();
	НомерТекущейСтроки   = 1;
	ПроцентВыполнения    = 0;
	
	Пока ВыборкаДатаДокумента.Следующий() Цикл
		Если ПараметрыПрогресса <> Неопределено Тогда
			УстановитьПроцентПрогресса(
				ВсегоСтрок,
				НомерТекущейСтроки,
				ПроцентВыполнения,
				ПараметрыПрогресса);
		КонецЕсли;
		
		ВыборкаКонтрагент = ВыборкаДатаДокумента.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКонтрагент.Следующий() Цикл
			
			ВыборкаЭтоВозврат = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаЭтоВозврат.Следующий() Цикл
				ВзаимозачетЗадолженностиОбъект    = Неопределено;
				ВзаимозачетЗадолженностиСсылка    = Неопределено;
				ВзаимозачетЗадолженностиСостояние = ДоступныеСостоянияДокументов.СостояниеНеОбновлен;
				
				ВыборкаДанных = ВыборкаЭтоВозврат.Выбрать();
				Пока ВыборкаДанных.Следующий() Цикл
					Если ВзаимозачетЗадолженностиОбъект = Неопределено Тогда
						ВзаимозачетЗадолженностиСсылка = ВыборкаДанных.ВзаимозачетЗадолженностиСсылка;
						
						Если ЗначениеЗаполнено(ВзаимозачетЗадолженностиСсылка) Тогда
							// Существующий документ
							Если Не Параметры.ПерезаписатьДокументы Тогда
								Прервать;
							КонецЕсли;
							
							ВзаимозачетЗадолженностиОбъект    = ВзаимозачетЗадолженностиСсылка.ПолучитьОбъект();
							ВзаимозачетЗадолженностиСостояние = ДоступныеСостоянияДокументов.СостояниеНеОбновлен;
							
							ТекстОшибки = ЗаблокироватьДокумент(ВзаимозачетЗадолженностиОбъект, Параметры.СобытиеЖурналаРегистрации);
							
							Если Не ПустаяСтрока(ТекстОшибки) Тогда
								Если Ошибка.Детализация = Неопределено Тогда
									Ошибка.Детализация = Новый Массив;
								КонецЕсли;
								Ошибка.Детализация.Добавить(ТекстОшибки);
								
								Прервать;
							КонецЕсли;
							
						Иначе
							// Новый документ
							ВзаимозачетЗадолженностиОбъект    = Документы.ВзаимозачетЗадолженности.СоздатьДокумент();
							ВзаимозачетЗадолженностиСостояние = ДоступныеСостоянияДокументов.СостояниеНовый;
						КонецЕсли;
						
						ДанныеЗаполнения.Вставить("Дата",               КонецДня(ВыборкаДанных.ДатаДокумента));
						ДанныеЗаполнения.Вставить("КонтрагентДебитор",  ?(ВыборкаДанных.ЭтоВозврат, Параметры.Контрагент, ВыборкаДанных.Контрагент));
						ДанныеЗаполнения.Вставить("КонтрагентКредитор", ?(ВыборкаДанных.ЭтоВозврат, ВыборкаДанных.Контрагент, Параметры.Контрагент));
						ДанныеЗаполнения.Вставить("Комментарий",        КомментарийОформлениеВзаимозачета(
																			УчетнаяЗаписьПредставление,
																			ВыборкаДанных.ЭтоВозврат));
						
						Если ВзаимозачетЗадолженностиЗаполнить(ВзаимозачетЗадолженностиОбъект, ДанныеЗаполнения, 
								ЗначенияФункциональныхОпций, Параметры.СобытиеЖурналаРегистрации) Тогда
							Если ВзаимозачетЗадолженностиСостояние <> ДоступныеСостоянияДокументов.СостояниеНовый Тогда
								ВзаимозачетЗадолженностиСостояние = ДоступныеСостоянияДокументов.СостояниеОбновлен;
							КонецЕсли;
						Иначе
							ЗаполнитьЗначенияСвойств(ВзаимозачетЗадолженностиОбъект, ДанныеЗаполнения);
							ВзаимозачетЗадолженностиСостояние = ДоступныеСостоянияДокументов.СостояниеОшибкиЗаполнения;
						КонецЕсли;
						
						ВзаимозачетЗадолженностиОбъект.ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ПереносДолгаКлиентаМеждуКонтрагентами;
						
						Если Параметры.ОчиститьТабличныеЧасти Тогда
							ВзаимозачетЗадолженностиОбъект.ДебиторскаяЗадолженность.Очистить();
							ВзаимозачетЗадолженностиОбъект.КредиторскаяЗадолженность.Очистить();
						КонецЕсли;
					КонецЕсли;
					
					// Заполнение строки табличной части 
					Если ВыборкаДанных.ЭтоВозврат Тогда
						СтрокаТабличнойЧасти = ВзаимозачетЗадолженностиОбъект.КредиторскаяЗадолженность.Добавить();
					Иначе
						СтрокаТабличнойЧасти = ВзаимозачетЗадолженностиОбъект.ДебиторскаяЗадолженность.Добавить();
					КонецЕсли;
					
					ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ВыборкаДанных);
					
					Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ВалютаВзаиморасчетов) Тогда
						СтрокаТабличнойЧасти.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета;
					КонецЕсли;
				КонецЦикла;
				
				Если ВзаимозачетЗадолженностиОбъект <> Неопределено Тогда
					Если ВзаимозачетЗадолженностиОбъект.ДебиторскаяЗадолженность.Количество() > 0 
							Или ВзаимозачетЗадолженностиОбъект.КредиторскаяЗадолженность.Количество() > 0 Тогда
						Если ВзаимозачетЗадолженностиОбъект.КредиторскаяЗадолженность.Количество() > 0 Тогда
							СтрокаТабличнойЧасти = ВзаимозачетЗадолженностиОбъект.ДебиторскаяЗадолженность.Добавить();
							Сумма                = ВзаимозачетЗадолженностиОбъект.КредиторскаяЗадолженность.Итог("Сумма");
						Иначе
							СтрокаТабличнойЧасти = ВзаимозачетЗадолженностиОбъект.КредиторскаяЗадолженность.Добавить();
							Сумма                = ВзаимозачетЗадолженностиОбъект.ДебиторскаяЗадолженность.Итог("Сумма");
						КонецЕсли;
						
						СтрокаТабличнойЧасти.ТипРасчетов         = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
						СтрокаТабличнойЧасти.Организация         = Параметры.Организация;
						СтрокаТабличнойЧасти.Партнер             = Параметры.Партнер;
						СтрокаТабличнойЧасти.Сумма               = Сумма;
						СтрокаТабличнойЧасти.СуммаВзаиморасчетов = Сумма;
						СтрокаТабличнойЧасти.ОбъектРасчетов      = ОбъектРасчетов;
						
						Если ЗначениеЗаполнено(ОбъектРасчетов) Тогда
							СтрокаТабличнойЧасти.ВалютаВзаиморасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектРасчетов, "ВалютаВзаиморасчетов");
						КонецЕсли;
						Если Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ВалютаВзаиморасчетов) Тогда
							СтрокаТабличнойЧасти.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета;
						КонецЕсли;
						
						// Постобработка табличной части
						Если Не ВзаимозачетЗадолженностиОбработатьТЧ(ВзаимозачетЗадолженностиОбъект) Тогда
							ВзаимозачетЗадолженностиСостояние = ДоступныеСостоянияДокументов.СостояниеОшибкиЗаполнения;
						КонецЕсли;
						
						// Запись и проведение документа
						РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(ВзаимозачетЗадолженностиОбъект, 
							Истина, Параметры.СобытиеЖурналаРегистрации);
						ВзаимозачетЗадолженностиСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
							ВзаимозачетЗадолженностиСостояние, 
							ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
							
						Если ВзаимозачетЗадолженностиСостояние <> ДоступныеСостоянияДокументов.СостояниеНовый Тогда
							ВзаимозачетЗадолженностиОбъект.Разблокировать();
						КонецЕсли;
						
						Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
							Если Ошибка.Детализация = Неопределено Тогда
								Ошибка.Детализация = Новый Массив;
							КонецЕсли;
							Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
						КонецЕсли;
					
					ИначеЕсли Не ВзаимозачетЗадолженностиОбъект.ЭтоНовый()
								И Параметры.ПерезаписатьДокументы Тогда
						// Запись и пометка на удаление документа
						ВзаимозачетЗадолженностиОбъект.ПометкаУдаления = Истина;
						
						РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(ВзаимозачетЗадолженностиОбъект, 
							Ложь, Параметры.СобытиеЖурналаРегистрации);
						ВзаимозачетЗадолженностиСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
							ВзаимозачетЗадолженностиСостояние, 
							ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
							
						ВзаимозачетЗадолженностиОбъект.Разблокировать();
						
						Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
							Если Ошибка.Детализация = Неопределено Тогда
								Ошибка.Детализация = Новый Массив;
							КонецЕсли;
							Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
						КонецЕсли;
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(ВзаимозачетЗадолженностиСсылка) Тогда
						ВзаимозачетЗадолженностиСсылка = ВзаимозачетЗадолженностиОбъект.Ссылка;
					КонецЕсли;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ВзаимозачетЗадолженностиСсылка) 
						И Результат.Ссылки.Найти(ВзаимозачетЗадолженностиСсылка) = Неопределено Тогда
					Результат.Ссылки.Добавить(ВзаимозачетЗадолженностиСсылка);
					Результат.СостоянияСсылок.Вставить(ВзаимозачетЗадолженностиСсылка, ВзаимозачетЗадолженностиСостояние);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		НомерТекущейСтроки = НомерТекущейСтроки + 1;
	КонецЦикла;
	
	Возврат Ошибка;
	
КонецФункции

// Создает документы продажи через комиссионера (агента).
//
// Параметры:
//   Результат          - См. РезультатЗаписиЗагружаемыхДанных.
//   ТаблицаДанных      - ТаблицаЗначений - подготовленная таблица сопоставленных данных для загрузки.
//   Параметры          - Структура - параметры заполнения документов:
//     * ВидТорговойПлощадки            - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки;
//     * УчетнаяЗапись                  - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки;
//     * ВалютаУчета                    - СправочникСсылка.Валюты, Неопределено - валюта учета учетной записи;
//     * Организация                    - СправочникСсылка.Организации - организация в шапке;
//     * Партнер                        - СправочникСсылка.Партнеры - партнер в шапке;
//     * Контрагент                     - СправочникСсылка.Контрагенты - контрагент в шапке;
//     * Соглашение                     - СправочникСсылка.СоглашенияСКлиентами - соглашение в шапке;
//     * Договор                        - СправочникСсылка.ДоговорыКонтрагентов, Неопределено - договор в шапке;
//     * НомерВходящегоДокумента        - Строка - номер входящего документа;
//     * ДатаВходящегоДокумента         - Дата - дата входящего документа;
//     * НачалоПериода                  - Дата, Неопределено - начало периода в загружаемых данных;
//     * КонецПериода                   - Дата, Неопределено - окончание периода в загружаемых данных;
//     * УслугаПродажиЧерезКомиссионера - СправочникСсылка.Номенклатура, Неопределено - услуга по комиссионной продаже товаров;
//     * КодОшибки                      - Строка - код ошибки для результат выполнения;
//     * СобытиеЖурналаРегистрации      - Строка - наименование события для записей в журнале регистрации;
//     * ЗагружатьНезаполненныеСтроки   - Булево - добавлять ли строки с несопоставленными показателями;
//     * ПерезаписатьДокументы          - Булево - перезаписывать ли существующие документы;
//     * ОчиститьТабличныеЧасти         - Булево - очищать табличные части документов перед обработкой;
//     * ПараметрыОбработки             - Структура Из КлючИЗначение, Неопределено - параметры обработки табличных частей документов;
//     * ТекстЗапроса                   - Строка - текст запроса для отражения продаж через комиссионера.
//   ПараметрыПрогресса - См. ПараметрыПрогресса.
//
// Возвращаемое значение:
//   См. НоваяОшибка.
//
Функция ЗаписатьПродажиЧерезКомиссионера(Результат, ТаблицаДанных, Параметры, ПараметрыПрогресса = Неопределено) Экспорт
	
	Ошибка = НоваяОшибка();
	
	Если Не ЗначениеЗаполнено(Параметры.УчетнаяЗапись) Тогда
		ТекстОшибки = НСтр("ru = 'Не указана учетная запись торговой площадки.';
							|en = 'Marketplace personal account not specified.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		Ошибка.КодОшибки      = Параметры.КодОшибки;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	ЗначенияФункциональныхОпций = ПолучитьФункциональныеОпции(Параметры.Соглашение);
	
	Если Не ЗначенияФункциональныхОпций.ИспользоватьКомиссиюПриПродажах Тогда
		ТекстОшибки = НСтр("ru = 'Для отражения данных о реализованных товарах по схеме ""Продажи через комиссионера (агента)"" необходимо включить опцию ведения комиссионных продаж в настройках раздела ""НСИ и администрирование"" - ""Продажи"" - ""Оптовые продажи"".';
							|en = 'To record data on items sold by the ""Consignment sales"" scheme, enable the consignment sales option in Master data and settings - Sales - Wholesale.'", 
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		Ошибка.КодОшибки      = Параметры.КодОшибки;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ИнтеграцияСМаркетплейсамиСервер.ЗаписатьПродажиЧерезКомиссионера");
	
	УчетнаяЗаписьПредставление       =
		Справочники.УчетныеЗаписиМаркетплейсов.ПолноеПредставлениеУчетнойЗаписи(
			Параметры.ВидТорговойПлощадки,
			Параметры.УчетнаяЗапись);
	ВалютаРегламентированногоУчета   = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Параметры.Организация);
	ДоступныеСостоянияДокументов     = ДоступныеСостоянияДокументов();
	ВариантыРезультатовСопоставления = ИнтеграцияСМаркетплейсамиКлиентСервер.ВариантыРезультатовСопоставления();
	
	РеквизитыШапки = Новый Структура;
	РеквизитыШапки.Вставить("Организация",           Параметры.Организация);
	РеквизитыШапки.Вставить("Партнер",               Параметры.Партнер);
	РеквизитыШапки.Вставить("Контрагент",            Параметры.Контрагент);
	РеквизитыШапки.Вставить("Соглашение",            ?(ЗначенияФункциональныхОпций.ИспользоватьСоглашенияСКлиентами, 
															Параметры.Соглашение, 
															Справочники.СоглашенияСКлиентами.ПустаяСсылка()));
	РеквизитыШапки.Вставить("Договор",               ?(ЗначенияФункциональныхОпций.ИспользуютсяДоговорыКонтрагентов, 
															Параметры.Договор, 
															Справочники.ДоговорыКонтрагентов.ПустаяСсылка()));
	РеквизитыШапки.Вставить("Валюта",                ?(ЗначениеЗаполнено(Параметры.ВалютаУчета), 
															Параметры.ВалютаУчета, 
															ВалютаРегламентированногоУчета));
	РеквизитыШапки.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.РеализацияЧерезКомиссионера);
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ЗаполнятьПоДаннымКОформлению", Истина);
	ДанныеЗаполнения.Вставить("РеквизитыШапки",               РеквизитыШапки);
	ДанныеЗаполнения.Вставить("НомерВходящегоДокумента",      Параметры.НомерВходящегоДокумента);
	ДанныеЗаполнения.Вставить("ДатаВходящегоДокумента",       Параметры.ДатаВходящегоДокумента);
	
	Если ТаблицаДанных.Колонки.Найти("СуммаВознаграждения") = Неопределено Тогда
		ДанныеЗаполнения.Вставить("СпособРасчетаВознаграждения", Перечисления.СпособыРасчетаКомиссионногоВознаграждения.НеРассчитывается);
	Иначе
		ДанныеЗаполнения.Вставить("СпособРасчетаВознаграждения", Перечисления.СпособыРасчетаКомиссионногоВознаграждения.ПроизвольнаяСумма);
		ДанныеЗаполнения.Вставить("Услуга",                      Параметры.УслугаПродажиЧерезКомиссионера);
	КонецЕсли;
	
	Если ЗначенияФункциональныхОпций.ИспользоватьСоглашенияСКлиентами Тогда
		ДанныеЗаполнения.Вставить("ЗаполнятьПоСоглашению", Истина);
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ДанныеЗаполнения, РеквизитыШапки, Истина);
	КонецЕсли;
	
	Если Параметры.ПараметрыОбработки = Неопределено Тогда
		Параметры.ПараметрыОбработки = Новый Структура;
		Параметры.ПараметрыОбработки.Вставить("СтруктураДействий",           Неопределено);
		Параметры.ПараметрыОбработки.Вставить("ПересчитатьКоличествоЕдиниц", Ложь);
		Параметры.ПараметрыОбработки.Вставить("ЗаполнитьЦены",               Истина);
		Параметры.ПараметрыОбработки.Вставить("ПересчитатьЦенаВключаетНДС",  Истина);
		Параметры.ПараметрыОбработки.Вставить("КомиссионерВедетУчетПоРНПТ",  ЗначенияФункциональныхОпций.КомиссионерВедетУчетПоРНПТ);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = Параметры.ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ТаблицаДанных",           ТаблицаДанных);
	Запрос.УстановитьПараметр("КоличествоСтрок",         ТаблицаДанных.Количество());
	Запрос.УстановитьПараметр("Организация",             Параметры.Организация);
	Запрос.УстановитьПараметр("Партнер",                 Параметры.Партнер);
	Запрос.УстановитьПараметр("Контрагент",              Параметры.Контрагент);
	Запрос.УстановитьПараметр("Соглашение",              РеквизитыШапки.Соглашение);
	Запрос.УстановитьПараметр("Договор",                 РеквизитыШапки.Договор);
	Запрос.УстановитьПараметр("НачалоПериода",           Параметры.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",            Параметры.КонецПериода);
	Запрос.УстановитьПараметр("НомерВходящегоДокумента", Параметры.НомерВходящегоДокумента);
	Запрос.УстановитьПараметр("ДатаВходящегоДокумента",  Параметры.ДатаВходящегоДокумента);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
		Замер, 
		ТаблицаДанных.Количество() / 100);
	
	ВыборкаДатаДокумента = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВсегоСтрок           = ВыборкаДатаДокумента.Количество();
	НомерТекущейСтроки   = 1;
	ПроцентВыполнения    = 0;
	
	Если ВсегоСтрок = 0 Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Данные для загрузки продаж через комиссионера (агента) с торговой площадки <%1> отсутствуют.';
				|en = 'Data for importing sales via the consignee from the <%1> marketplace is missing.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Параметры.УчетнаяЗапись);
		
		Ошибка.КодОшибки      = Параметры.КодОшибки;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	Пока ВыборкаДатаДокумента.Следующий() Цикл
		Если ПараметрыПрогресса <> Неопределено Тогда
			УстановитьПроцентПрогресса(
				ВсегоСтрок,
				НомерТекущейСтроки,
				ПроцентВыполнения,
				ПараметрыПрогресса);
		КонецЕсли;
		
		ВыборкаНачалоПериода = ВыборкаДатаДокумента.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНачалоПериода.Следующий() Цикл
			Если Параметры.ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
				СуммаПродажиКРаспределению = ВыборкаНачалоПериода.СуммаПродажи;
				БазоваяСуммаВознаграждения = Окр(ВыборкаНачалоПериода.БазоваяКомиссия * 0.01, 2);
				ВознаграждениеПослеСкидок  = ВыборкаНачалоПериода.БазоваяКомиссия - ВыборкаНачалоПериода.БаллыЗаСкидки;
				
				Если ВознаграждениеПослеСкидок < БазоваяСуммаВознаграждения Тогда
					СуммаВознагражденияКРаспределению = БазоваяСуммаВознаграждения;
				Иначе
					СуммаВознагражденияКРаспределению = ВознаграждениеПослеСкидок;
				КонецЕсли;
			КонецЕсли;
			
			ВыборкаДоговор = ВыборкаНачалоПериода.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДоговор.Следующий() Цикл
				ОтчетКомиссионераОбъект    = Неопределено;
				ОтчетКомиссионераСсылка    = Неопределено;
				ОтчетКомиссионераСостояние = ДоступныеСостоянияДокументов.СостояниеНеОбновлен;
				ЕстьСчетаФактуры           = Ложь;
				
				ВыборкаДанных = ВыборкаДоговор.Выбрать();
				Пока ВыборкаДанных.Следующий() Цикл
					Если ОтчетКомиссионераОбъект = Неопределено Тогда
						ОтчетКомиссионераСсылка = ВыборкаДанных.ОтчетКомиссионераСсылка;
						
						Если ЗначениеЗаполнено(ОтчетКомиссионераСсылка) Тогда
							// Существующий документ
							Если Не Параметры.ПерезаписатьДокументы Тогда
								Прервать;
							КонецЕсли;
							
							ОтчетКомиссионераОбъект    = ОтчетКомиссионераСсылка.ПолучитьОбъект();
							ОтчетКомиссионераСостояние = ДоступныеСостоянияДокументов.СостояниеНеОбновлен;
							
							ТекстОшибки = ЗаблокироватьДокумент(ОтчетКомиссионераОбъект, Параметры.СобытиеЖурналаРегистрации);
							
							Если Не ПустаяСтрока(ТекстОшибки) Тогда
								Если Ошибка.Детализация = Неопределено Тогда
									Ошибка.Детализация = Новый Массив;
								КонецЕсли;
								Ошибка.Детализация.Добавить(ТекстОшибки);
								
								Прервать;
							КонецЕсли;
							
						Иначе
							// Новый документ
							ОтчетКомиссионераОбъект    = Документы.ОтчетКомиссионера.СоздатьДокумент();
							ОтчетКомиссионераСостояние = ДоступныеСостоянияДокументов.СостояниеНовый;
						КонецЕсли;
						
						ДанныеЗаполнения.Вставить("Дата",          КонецДня(ВыборкаДанных.ДатаДокумента));
						ДанныеЗаполнения.Вставить("НачалоПериода", ВыборкаДанных.НачалоПериода);
						ДанныеЗаполнения.Вставить("КонецПериода",  ВыборкаДанных.КонецПериода);
						ДанныеЗаполнения.Вставить("Комментарий",   СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
																		НСтр("ru = '%1. Отчет о реализации товаров %2за период с %3 по %4';
																			|en = '%1. Goods sales report %2 for the period from %3 to %4'"),
																		УчетнаяЗаписьПредставление,
																		?(ЗначениеЗаполнено(Параметры.НомерВходящегоДокумента), "№" + Параметры.НомерВходящегоДокумента + " ", ""),
																		Формат(ВыборкаДанных.НачалоПериода, "ДЛФ=D;"),
																		Формат(ВыборкаДанных.КонецПериода, "ДЛФ=D;")));
						Если Параметры.Договор = Неопределено Тогда
							ДанныеЗаполнения.РеквизитыШапки.Вставить("Договор", ВыборкаДанных.Договор);
						КонецЕсли;
						
						Если ОтчетКомиссионераЗаполнить(ОтчетКомиссионераОбъект, ДанныеЗаполнения, 
								ЗначенияФункциональныхОпций, Параметры.СобытиеЖурналаРегистрации) Тогда
							Если ОтчетКомиссионераСостояние <> ДоступныеСостоянияДокументов.СостояниеНовый Тогда
								ОтчетКомиссионераСостояние = ДоступныеСостоянияДокументов.СостояниеОбновлен;
							КонецЕсли;
						Иначе
							ЗаполнитьЗначенияСвойств(ОтчетКомиссионераОбъект, ДанныеЗаполнения);
							ЗаполнитьЗначенияСвойств(ОтчетКомиссионераОбъект, ДанныеЗаполнения.РеквизитыШапки);
							ОтчетКомиссионераСостояние = ДоступныеСостоянияДокументов.СостояниеОшибкиЗаполнения;
						КонецЕсли;
						
						Если Параметры.ОчиститьТабличныеЧасти Тогда
							ОтчетКомиссионераОбъект.Товары.Очистить();
						КонецЕсли;
					КонецЕсли;
					
					Если Не Параметры.ЗагружатьНезаполненныеСтроки
							И ВыборкаДанных.РезультатСопоставленияСтроки <> ВариантыРезультатовСопоставления.СтрокаСопоставлена Тогда
						Продолжить;
					КонецЕсли;
					
					// Заполнение строки табличной части
					СтрокаТабличнойЧасти = ОтчетКомиссионераОбъект.Товары.Добавить();
					СтрокаТабличнойЧасти.Номенклатура = ВыборкаДанных.Номенклатура;
					
					Если ВыборкаДанных.ХарактеристикиИспользуются Тогда
						СтрокаТабличнойЧасти.Характеристика = ВыборкаДанных.Характеристика;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ВыборкаДанных.Упаковка)
							И ВыборкаДанных.Упаковка <> ВыборкаДанных.НоменклатураЕдиницаИзмерения Тогда
						Если ЗначенияФункциональныхОпций.ИспользоватьУпаковкиНоменклатуры Тогда
							СтрокаТабличнойЧасти.Упаковка = ВыборкаДанных.Упаковка;
							КоличествоУпаковок            = ВыборкаДанных.Количество;
							Количество                    = Окр(КоличествоУпаковок * ВыборкаДанных.УпаковкаКоэффициент, 
																3, РежимОкругления.Окр15как20);
						Иначе
							СтрокаТабличнойЧасти.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
							КоличествоУпаковок            = Окр(ВыборкаДанных.Количество * ВыборкаДанных.УпаковкаКоэффициент, 
																3, РежимОкругления.Окр15как20);
							Количество                    = КоличествоУпаковок;
						КонецЕсли;
					Иначе
						СтрокаТабличнойЧасти.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
						КоличествоУпаковок            = ВыборкаДанных.Количество;
						Количество                    = КоличествоУпаковок;
					КонецЕсли;
					
					СтрокаТабличнойЧасти.Цена                          = ВыборкаДанных.Цена;
					СтрокаТабличнойЧасти.ЦенаПродажи                   = ВыборкаДанных.ЦенаПродажи;
					СтрокаТабличнойЧасти.Количество                    = Количество;
					СтрокаТабличнойЧасти.КоличествоУпаковок            = КоличествоУпаковок;
					СтрокаТабличнойЧасти.КоличествоУпаковокФакт        = КоличествоУпаковок;
					СтрокаТабличнойЧасти.КоличествоУпаковокУчет        = КоличествоУпаковок;
					СтрокаТабличнойЧасти.Сумма                         = ВыборкаДанных.Сумма;
					СтрокаТабличнойЧасти.СуммаСНДС                     = ВыборкаДанных.Сумма;
					СтрокаТабличнойЧасти.СуммаПродажи                  = ВыборкаДанных.СуммаПродажи;
					СтрокаТабличнойЧасти.СуммаВознаграждения           = ВыборкаДанных.СуммаВознаграждения;
					
					СтрокаТабличнойЧасти.Покупатель                    = ВыборкаДанных.Контрагент;
					СтрокаТабличнойЧасти.НомерСчетаФактурыКомиссионера = ВыборкаДанных.НомерСчетаФактуры;
					СтрокаТабличнойЧасти.ДатаСчетаФактурыКомиссионера  = ВыборкаДанных.ДатаСчетаФактуры;
					СтрокаТабличнойЧасти.НомерДокументаОтгрузки        = ВыборкаДанных.НомерОтправления;
					СтрокаТабличнойЧасти.ДатаДокументаОтгрузки         = ВыборкаДанных.ДатаОтправления;
					
					Если Не ЗначенияФункциональныхОпций.КомиссионерВедетУчетПоРНПТ Тогда
						СтрокаТабличнойЧасти.НомерГТД = Неопределено;
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ВыборкаДанных.НомерСчетаФактуры) Тогда
						ЕстьСчетаФактуры = Истина;
					КонецЕсли;
				КонецЦикла;
				
				Если ОтчетКомиссионераОбъект <> Неопределено Тогда
					Если ОтчетКомиссионераОбъект.Товары.Количество() > 0 Тогда
						// Постобработка табличной части
						Если Не ОтчетКомиссионераОбработатьТЧ(ОтчетКомиссионераОбъект, Параметры.ПараметрыОбработки,
									Ошибка, Параметры.СобытиеЖурналаРегистрации) Тогда
							ОтчетКомиссионераСостояние = ДоступныеСостоянияДокументов.СостояниеОшибкиЗаполнения;
						КонецЕсли;
						
						// Расчет вознаграждения
						Если Параметры.ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
							Если ОтчетКомиссионераОбъект.НачалоПериода >= ИнтеграцияСМаркетплейсомOzonСервер.ДатаНовойВерсииОтчетаОРеализации() Тогда   
								СуммаВознаграждения = ?(СуммаВознагражденияКРаспределению = 0, 
									0, 
									Окр(ОтчетКомиссионераОбъект.Товары.Итог("СуммаПродажи") * СуммаВознагражденияКРаспределению / СуммаПродажиКРаспределению, 2));
									
								Если ОтчетКомиссионераОбъект.СуммаВознаграждения <> СуммаВознаграждения Тогда
									ОтчетКомиссионераОбъект.СуммаВознаграждения = СуммаВознаграждения;
									КомиссионнаяТорговляСервер.РассчитатьСуммуВознаграждения(ОтчетКомиссионераОбъект, "СуммаПродажи");
								КонецЕсли;
								
								СуммаПродажиКРаспределению        = СуммаПродажиКРаспределению - ОтчетКомиссионераОбъект.Товары.Итог("СуммаПродажи");
								СуммаВознагражденияКРаспределению = СуммаВознагражденияКРаспределению - ОтчетКомиссионераОбъект.СуммаВознаграждения;
								
								Если СуммаПродажиКРаспределению = 0
										И СуммаВознагражденияКРаспределению <> 0 Тогда
									ОтчетКомиссионераОбъект.Товары[0].СуммаВознаграждения = ОтчетКомиссионераОбъект.Товары[0].СуммаВознаграждения 
										+ СуммаВознагражденияКРаспределению;
								КонецЕсли;
							КонецЕсли;
							
							ПроцентНДС = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(ОтчетКомиссионераОбъект.СтавкаНДСВознаграждения);
							
							ОтчетКомиссионераОбъект.СуммаВознаграждения    = ОтчетКомиссионераОбъект.Товары.Итог("СуммаВознаграждения");
							ОтчетКомиссионераОбъект.СуммаНДСВознаграждения = Окр(ОтчетКомиссионераОбъект.СуммаВознаграждения
								* ПроцентНДС / (100 + ПроцентНДС), 2, РежимОкругления.Окр15как20);
						КонецЕсли;
						
						// Запись и проведение документа
						РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(ОтчетКомиссионераОбъект, 
							Истина, Параметры.СобытиеЖурналаРегистрации);
						ОтчетКомиссионераСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
							ОтчетКомиссионераСостояние, 
							ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
							
						Если ОтчетКомиссионераСостояние <> ДоступныеСостоянияДокументов.СостояниеНовый Тогда
							ОтчетКомиссионераОбъект.Разблокировать();
						КонецЕсли;
						
						Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
							Если Ошибка.Детализация = Неопределено Тогда
								Ошибка.Детализация = Новый Массив;
							КонецЕсли;
							Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
						КонецЕсли;
						
						Если ЕстьСчетаФактуры Тогда
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Отражение продаж юридическим лицам в документе №%1 от %2 необходимо скорректировать вручную.';
									|en = 'Manually adjust the record of sales to legal entities in document No. %1 dated %2.'"),
								ОтчетКомиссионераОбъект.Номер,
								Формат(ОтчетКомиссионераОбъект.Дата, "ДЛФ=D;"));
								
							Если Ошибка.Детализация = Неопределено Тогда
								Ошибка.Детализация = Новый Массив;
							КонецЕсли;
							Ошибка.Детализация.Добавить(ТекстОшибки);
						КонецЕсли;
						
					ИначеЕсли Не ОтчетКомиссионераОбъект.ЭтоНовый() Тогда
						// Запись и пометка на удаление документа
						ОтчетКомиссионераОбъект.ПометкаУдаления = Истина;
						
						РезультатЗаписиДокумента = ЗаписатьПровестиДокумент(ОтчетКомиссионераОбъект, 
							Ложь, Параметры.СобытиеЖурналаРегистрации);
						ОтчетКомиссионераСостояние = ?(РезультатЗаписиДокумента.ЗаписьВыполнена, 
							ОтчетКомиссионераСостояние, 
							ДоступныеСостоянияДокументов.СостояниеНеОбновлен);
							
						ОтчетКомиссионераОбъект.Разблокировать();
						
						Если Не ПустаяСтрока(РезультатЗаписиДокумента.ОписаниеОшибки) Тогда
							Если Ошибка.Детализация = Неопределено Тогда
								Ошибка.Детализация = Новый Массив;
							КонецЕсли;
							Ошибка.Детализация.Добавить(РезультатЗаписиДокумента.ОписаниеОшибки);
						КонецЕсли;
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(ОтчетКомиссионераСсылка) Тогда
						ОтчетКомиссионераСсылка = ОтчетКомиссионераОбъект.Ссылка;
					КонецЕсли;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ОтчетКомиссионераСсылка) 
						И Результат.Ссылки.Найти(ОтчетКомиссионераСсылка) = Неопределено Тогда
					Результат.Ссылки.Добавить(ОтчетКомиссионераСсылка);
					Результат.СостоянияСсылок.Вставить(ОтчетКомиссионераСсылка, ОтчетКомиссионераСостояние);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		НомерТекущейСтроки = НомерТекущейСтроки + 1;
	КонецЦикла;
	
	Возврат Ошибка;
	
КонецФункции

// Формирует комментарий для документа Корректировка реализации, содержащей невыкуп товаров.
// Если дата и номер не переданы, то комментарий формируется в виде шаблона, для поиска с помощью оператора "ПОДОБНО" в запросе.
// 
// Параметры:
//  УчетнаяЗаписьПредставление - см. Справочники.УчетныеЗаписиМаркетплейсов.ПолноеПредставлениеУчетнойЗаписи
//  Дата - Неопределено, Строка - дата документа, приведенная к нужному формату для установки в комментарии.
//  Номер - Неопределено, Строка - номер документа.
// 
// Возвращаемое значение:
//  Строка
//
Функция КомментарийНевыкупТоваров(УчетнаяЗаписьПредставление, Дата = Неопределено, Номер = Неопределено) Экспорт
	
	Если Дата = Неопределено И Номер = Неопределено Тогда
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Невыкуп товаров';
				|en = '%1. Goods non-purchase'"),
			УчетнаяЗаписьПредставление) + "%";
	Иначе
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Невыкуп товаров по заказу №%2 от %3';
				|en = '%1. Goods non-purchase for order No. %2 dated %3'"), 
			УчетнаяЗаписьПредставление,
			Номер,
			Дата);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует комментарий для документа Корректировка реализации, содержащей возврат товаров.
// Если дата и номер не переданы, то комментарий формируется в виде шаблона, для поиска с помощью оператора "ПОДОБНО" в запросе.
// 
// Параметры:
//  УчетнаяЗаписьПредставление - см. Справочники.УчетныеЗаписиМаркетплейсов.ПолноеПредставлениеУчетнойЗаписи
//  Дата - Неопределено, Строка - дата документа, приведенная к нужному формату для установки в комментарии.
//  Номер - Неопределено, Строка - номер документа.
// 
// Возвращаемое значение:
//  Строка
//
Функция КомментарийВозвратТоваров(УчетнаяЗаписьПредставление, Дата = Неопределено, Номер = Неопределено) Экспорт
	
	Если Дата = Неопределено И Номер = Неопределено Тогда
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Возврат товаров';
				|en = '%1. Return of goods'"),
			УчетнаяЗаписьПредставление) + "%";
	Иначе
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Возврат товаров по заказу №%2 от %3';
				|en = '%1. Return of goods for order No. %2 dated %3'"), 
			УчетнаяЗаписьПредставление,
			Номер,
			Дата);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует комментарий для документа Корректировка реализации, содержащей корректировку стоимости товаров.
// Если дата и номер не переданы, то комментарий формируется в виде шаблона, для поиска с помощью оператора "ПОДОБНО" в запросе.
// 
// Параметры:
//  УчетнаяЗаписьПредставление - см. Справочники.УчетныеЗаписиМаркетплейсов.ПолноеПредставлениеУчетнойЗаписи
//  Дата - Неопределено, Строка - дата документа, приведенная к нужному формату для установки в комментарии.
//  Номер - Неопределено, Строка - номер документа.
// 
// Возвращаемое значение:
//  Строка
//
Функция КомментарийКорректировкаСтоимости(УчетнаяЗаписьПредставление, Дата = Неопределено, Номер = Неопределено) Экспорт
	
	Если Дата = Неопределено И Номер = Неопределено Тогда
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Корректировка стоимости';
				|en = '%1. Cost adjustment'"),
			УчетнаяЗаписьПредставление) + "%";
	Иначе
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Корректировка стоимости по заказу №%2 от %3';
				|en = '%1. Cost adjustment for order No. %2 dated %3'"), 
			УчетнаяЗаписьПредставление,
			Номер,
			Дата);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует комментарий для документа Взаимозачет задолженности.
// Если дата и номер не переданы, то комментарий формируется в виде шаблона, для поиска с помощью оператора "ПОДОБНО" в запросе.
// 
// Параметры:
//  УчетнаяЗаписьПредставление - см. Справочники.УчетныеЗаписиМаркетплейсов.ПолноеПредставлениеУчетнойЗаписи
//  ЭтоВозврат - Неопределено, Булево - признак необходимости формирования комментария по взаимозачету по возврату или невыкупу товаров.
// 
// Возвращаемое значение:
//  Строка
//
Функция КомментарийОформлениеВзаимозачета(УчетнаяЗаписьПредставление, ЭтоВозврат = Неопределено) Экспорт
	
	Если ЭтоВозврат = Неопределено Тогда
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Оформление взаимозачета';
				|en = '%1. Register AR/AP offset'"),
			УчетнаяЗаписьПредставление) + "%";
		
	ИначеЕсли ЭтоВозврат Тогда
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Оформление взаимозачета (невыкуп, возврат товаров)';
				|en = '%1. Register AR/AP offset (non-purchase, return of goods)'"),
			УчетнаяЗаписьПредставление);
	Иначе
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Оформление взаимозачета (доставленные товары)';
				|en = '%1. Register AR/AP offset (delivered goods)'"),
			УчетнаяЗаписьПредставление);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПартнерыИКонтрагенты

// Конструктор для заполнения данных контрагента.
//
// Возвращаемое значение:
//   Структура - данные для заполнения контрагента:
//     * Наименование - Строка - наименование контрагента;
//     * НаименованиеПолное - Строка - полное наименование контрагента;
//     * ИНН - Строка - ИНН контрагента;
//     * КПП - Строка - КПП контрагента;
//     * ЮрФизЛицоКонтрагента - ПеречислениеСсылка.ЮрФизЛицо - статус контрагента;
//     * ЮрФизЛицоПартнера - ПеречислениеСсылка.КомпанияЧастноеЛицо - статус контрагента;
//     * ЮридическийАдрес - Строка - юридический адрес;
//     * ФактическийАдрес - Строка - фактический адрес;
//     * ГруппаДоступа - СправочникСсылка.ГруппыДоступаПартнеров - группа доступа;
//     * Партнер - СправочникСсылка.Партнеры - партнер;
//     * ДополнительнаяИнформация - Строка - комментарий;
//
Функция ДанныеЗаполненияКонтрагента() Экспорт

	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Наименование", "");
	ДанныеЗаполнения.Вставить("НаименованиеПолное", "");
	ДанныеЗаполнения.Вставить("ИНН", "");
	ДанныеЗаполнения.Вставить("КПП", "");
	ДанныеЗаполнения.Вставить("ЮрФизЛицоКонтрагента", Перечисления.ЮрФизЛицо.ЮрЛицо);
	ДанныеЗаполнения.Вставить("ЮрФизЛицоПартнера", Перечисления.КомпанияЧастноеЛицо.Компания);
	ДанныеЗаполнения.Вставить("ЮридическийАдрес", "");
	ДанныеЗаполнения.Вставить("ФактическийАдрес", "");
	ДанныеЗаполнения.Вставить("ГруппаДоступа", Справочники.ГруппыДоступаПартнеров.ПустаяСсылка());
	ДанныеЗаполнения.Вставить("Партнер", Справочники.Партнеры.ПустаяСсылка());
	ДанныеЗаполнения.Вставить("ДополнительнаяИнформация", "");
	
	Возврат ДанныеЗаполнения;

КонецФункции

// Конструктор таблицы для заполнения данных покупателей, полученных из сервиса.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица для заполнения данных покупателей по заказам:
//     * ИдентификаторЗаказа - Строка - идентификатор заказа в сервисе.
//     * ТипКонтрагента - ПеречислениеСсылка.ЮридическоеФизическоеЛицо - тип покупателя в сервисе.
//     * ИдентификаторКонтрагента - Строка - идентификатор покупателя в сервисе.
//     * НаименованиеКонтрагента - Строка - наименование покупателя в сервисе.
//     * ФамилияКонтрагента - Строка - ИНН покупателя в сервисе.
//     * ИмяКонтрагента - Строка - ИНН покупателя в сервисе.
//     * ОтчествоКонтрагента - Строка - ИНН покупателя в сервисе.
//     * ТелефонКонтрагента - Строка - ИНН покупателя в сервисе.
//     * ИННКонтрагента - Строка - ИНН покупателя в сервисе.
//     * КППКонтрагента - Строка - КПП покупателя в сервисе.
//     * ЮридическийАдресКонтрагента - Строка - юридический адрес покупателя в сервисе.
//     * КонтрагентПроверен - Булево - покупатель проверен в сервисе.
//
Функция НоваяТаблицаДанныхПокупателейИзСервиса() Экспорт

	ТипБулево = Новый ОписаниеТипов("Булево");
	ТипСтрока200 = ОбщегоНазначения.ОписаниеТипаСтрока(200);
	МетаданныеРеквизитовКонтрагента = Метаданные.Справочники.Контрагенты.Реквизиты;
	
	ТаблицаДанныхПокупателей = Новый ТаблицаЗначений;
	ТаблицаДанныхПокупателей.Колонки.Добавить("ИдентификаторЗаказа",         ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаДанныхПокупателей.Колонки.Добавить("ТипКонтрагента",              МетаданныеРеквизитовКонтрагента["ЮридическоеФизическоеЛицо"].Тип);
	ТаблицаДанныхПокупателей.Колонки.Добавить("ИдентификаторКонтрагента",    ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаДанныхПокупателей.Колонки.Добавить("НаименованиеКонтрагента",     МетаданныеРеквизитовКонтрагента["НаименованиеПолное"].Тип);
	ТаблицаДанныхПокупателей.Колонки.Добавить("ФамилияКонтрагента",          ТипСтрока200);
	ТаблицаДанныхПокупателей.Колонки.Добавить("ИмяКонтрагента",              ТипСтрока200);
	ТаблицаДанныхПокупателей.Колонки.Добавить("ОтчествоКонтрагента",         ТипСтрока200);
	ТаблицаДанныхПокупателей.Колонки.Добавить("ТелефонКонтрагента",          ТипСтрока200);
	ТаблицаДанныхПокупателей.Колонки.Добавить("ИННКонтрагента",              МетаданныеРеквизитовКонтрагента["ИНН"].Тип);
	ТаблицаДанныхПокупателей.Колонки.Добавить("КППКонтрагента",              МетаданныеРеквизитовКонтрагента["КПП"].Тип);
	ТаблицаДанныхПокупателей.Колонки.Добавить("ЮридическийАдресКонтрагента", ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	ТаблицаДанныхПокупателей.Колонки.Добавить("КонтрагентПроверен",          ТипБулево);
	
	Возврат ТаблицаДанныхПокупателей;

КонецФункции

// Подбирает и при необходимости создает данные по покупателям заказов.
//
// Параметры:
//   УчетнаяЗапись              - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//   НастройкиУчетнойЗаписи     - см. Справочники.УчетныеЗаписиМаркетплейсов.НастройкиУчетнойЗаписи.
//   ДанныеПокупателейИзСервиса - Неопределено,
//                              - см. НоваяТаблицаДанныхПокупателейИзСервиса.
//
// Возвращаемое значение:
//   Соответствие из Строка - данные о покупателе для каждого переданного заказа:
//     * Ключ - Строка - идентификатор заказа;
//     * Значение - см. НовыеДанныеПокупателяЗаказа.
//
Функция ДанныеПокупателейПоЗаказам(УчетнаяЗапись, НастройкиУчетнойЗаписи, ДанныеПокупателейИзСервиса) Экспорт

	ДанныеПокупателейПоЗаказам = Новый Соответствие;
	
	Если Не ЗначениеЗаполнено(ДанныеПокупателейИзСервиса) Тогда
		Возврат ДанныеПокупателейПоЗаказам;
	КонецЕсли;
	
	ТекстыЗапроса = Новый Массив;
	
	ТекстЗапросаПодготовкиДанных =
		"ВЫБРАТЬ
		|	ДанныеПокупателей.ИдентификаторЗаказа КАК ИдентификаторЗаказа,
		|	ДанныеПокупателей.ТипКонтрагента КАК ТипКонтрагента,
		|	ДанныеПокупателей.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
		|	ДанныеПокупателей.НаименованиеКонтрагента КАК НаименованиеКонтрагента,
		|	ДанныеПокупателей.ИННКонтрагента КАК ИННКонтрагента,
		|	ДанныеПокупателей.КППКонтрагента КАК КППКонтрагента,
		|	ДанныеПокупателей.ЮридическийАдресКонтрагента КАК ЮридическийАдресКонтрагента,
		|	ДанныеПокупателей.КонтрагентПроверен КАК КонтрагентПроверен
		|ПОМЕСТИТЬ ВТ_ДанныеПокупателейИзСервиса
		|ИЗ
		|	&ДанныеПокупателейИзСервиса КАК ДанныеПокупателей";
	ТекстыЗапроса.Добавить(ТекстЗапросаПодготовкиДанных);
	
	ТекстЗапросаПоиска = ТекстЗапросаПоискаКонтрагентовИПартнеров("ВТ_ДанныеПокупателейИзСервиса");
	ТекстыЗапроса.Добавить(ТекстЗапросаПоиска);
	
	ТекстЗапросаИтоговый =
		"ВЫБРАТЬ
		|	ДанныеПокупателей.ИдентификаторЗаказа КАК ИдентификаторЗаказа,
		|	ДанныеПокупателей.ТипКонтрагента КАК ТипКонтрагента,
		|	ДанныеПокупателей.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
		|	ДанныеПокупателей.НаименованиеКонтрагента КАК НаименованиеКонтрагента,
		|	ДанныеПокупателей.ИННКонтрагента КАК ИННКонтрагента,
		|	ДанныеПокупателей.КППКонтрагента КАК КППКонтрагента,
		|	ДанныеПокупателей.ЮридическийАдресКонтрагента КАК ЮридическийАдресКонтрагента,
		|	ДанныеПокупателей.КонтрагентПроверен КАК КонтрагентПроверен,
		|	ЕСТЬNULL(НайденныеКонтрагентыИПартнеры.Контрагент, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Контрагент,
		|	ЕСТЬNULL(НайденныеКонтрагентыИПартнеры.Партнер, ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)) КАК Партнер
		|ИЗ
		|	ВТ_ДанныеПокупателейИзСервиса КАК ДанныеПокупателей
		|		ЛЕВОЕ СОЕДИНЕНИЕ НайденныеКонтрагентыИПартнеры КАК НайденныеКонтрагентыИПартнеры
		|		ПО (ДанныеПокупателей.ТипКонтрагента = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо))
		|			И ДанныеПокупателей.НаименованиеКонтрагента = НайденныеКонтрагентыИПартнеры.НаименованиеКонтрагента
		|			И ДанныеПокупателей.ИННКонтрагента = НайденныеКонтрагентыИПартнеры.ИННКонтрагента
		|			И ДанныеПокупателей.КППКонтрагента = НайденныеКонтрагентыИПартнеры.КППКонтрагента";
	
	ТекстыЗапроса.Добавить(ТекстЗапросаИтоговый);
	
	ГруппаДоступа = Справочники.ГруппыДоступаПартнеров.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.УстановитьПараметр("ДанныеПокупателейИзСервиса", ДанныеПокупателейИзСервиса);
	
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	Пока ВыборкаДанных.Следующий() Цикл
		ДанныеПокупателяЗаказа = НовыеДанныеПокупателяЗаказа();
		ЗаполнитьЗначенияСвойств(ДанныеПокупателяЗаказа, ВыборкаДанных);
		
		Если Не ЗначениеЗаполнено(ДанныеПокупателяЗаказа.Контрагент)
				И Не ПустаяСтрока(ВыборкаДанных.НаименованиеКонтрагента) Тогда
			ЗаполнитьДанныеПокупателя(
				ДанныеПокупателяЗаказа,
				ВыборкаДанных,
				УчетнаяЗапись,
				НастройкиУчетнойЗаписи.ВидМаркетплейса,
				ГруппаДоступа);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеПокупателяЗаказа.Контрагент)
				Или ЗначениеЗаполнено(ДанныеПокупателяЗаказа.Партнер) Тогда
			ДанныеПокупателейПоЗаказам.Вставить(ВыборкаДанных.ИдентификаторЗаказа, ДанныеПокупателяЗаказа);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДанныеПокупателейПоЗаказам;

КонецФункции

// Заполняет данные по партнеру и контрагенту.
//
// Параметры:
//   ПриемникДанных  - Произвольный - коллекция для заполнения, содержащая поля или свойства:
//                       Контрагент, Партнер, Соглашение, Договор.
//   ИсточникДанных  - Произвольный - коллекция или выборка из результата запроса.
//   УчетнаяЗапись   - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки;
//   ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//   ГруппаДоступа   - СправочникСсылка.ГруппыДоступаПартнеров - группа доступа.
//
Процедура ЗаполнитьДанныеПокупателя(ПриемникДанных, ИсточникДанных, УчетнаяЗапись,
			ВидМаркетплейса = Неопределено, ГруппаДоступа = Неопределено) Экспорт

	Если Не ЗначениеЗаполнено(ВидМаркетплейса)
			И ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	КонецЕсли;

	ДанныеЗаполнения = ДанныеЗаполненияКонтрагента();
	ДанныеЗаполнения.НаименованиеПолное = ИсточникДанных.НаименованиеКонтрагента;
	ДанныеЗаполнения.ИНН = ИсточникДанных.ИННКонтрагента;
	ДанныеЗаполнения.КПП = ИсточникДанных.КППКонтрагента;
	ДанныеЗаполнения.ЮридическийАдрес = ИсточникДанных.ЮридическийАдресКонтрагента;
	ДанныеЗаполнения.Партнер = ИсточникДанных.Партнер;
	ДанныеЗаполнения.ДополнительнаяИнформация =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Создан при обмене данными с ""%1"" (%2).';
				|en = 'Created during data exchange with ""%1"" (%2).'"),
			УчетнаяЗапись,
			ВидМаркетплейса);
	
	Если ЗначениеЗаполнено(ГруппаДоступа) Тогда // Ранее группа доступа найдена.
		ДанныеЗаполнения.ГруппаДоступа = ГруппаДоступа;
	КонецЕсли;
	
	ПриемникДанных.Контрагент = СоздатьКонтрагента(
		ДанныеЗаполнения,
		ВидМаркетплейса);
	
	ПриемникДанных.Партнер = ДанныеЗаполнения.Партнер;
	ГруппаДоступа = ДанныеЗаполнения.ГруппаДоступа;

КонецПроцедуры

// Создает нового контрагента.
//
// Параметры:
//   ДанныеЗаполнения - см. ДанныеЗаполненияКонтрагента.
//   ВидМаркетплейса  - Неопределено, ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//   ИспользоватьПривилегированныйРежим - Булево - признак использования привилегированного режима.
//
// Возвращаемое значение:
//   СправочникСсылка.Контрагенты - созданный контрагент или пустая ссылка.
//
Функция СоздатьКонтрагента(ДанныеЗаполнения, ВидМаркетплейса = Неопределено, ИспользоватьПривилегированныйРежим = Истина) Экспорт

	КонтрагентСсылка = Справочники.Контрагенты.ПустаяСсылка();
	
	Если ПустаяСтрока(ДанныеЗаполнения.Наименование)
				И ПустаяСтрока(ДанныеЗаполнения.НаименованиеПолное)
			Или Не ИспользоватьПривилегированныйРежим
				И Не ПравоДоступа("Добавление", Метаданные.Справочники.Контрагенты) Тогда
		Возврат КонтрагентСсылка;
	КонецЕсли;
	
	Если ИспользоватьПривилегированныйРежим Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	НовыйКонтрагент = Справочники.Контрагенты.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НовыйКонтрагент, ДанныеЗаполнения);
	
	Если ПустаяСтрока(НовыйКонтрагент.Наименование) Тогда
		НовыйКонтрагент.Наименование = НовыйКонтрагент.НаименованиеПолное;
	КонецЕсли;
	
	Если ПустаяСтрока(НовыйКонтрагент.НаименованиеПолное) Тогда
		НовыйКонтрагент.НаименованиеПолное = НовыйКонтрагент.Наименование;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НовыйКонтрагент.Партнер) Тогда
		НовыйКонтрагент.Партнер = СоздатьПартнера(ДанныеЗаполнения, ВидМаркетплейса, Ложь);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НовыйКонтрагент.Партнер) Тогда
		Возврат КонтрагентСсылка;
	КонецЕсли;
	
	НовыйКонтрагент.ЮрФизЛицо = ДанныеЗаполнения.ЮрФизЛицоКонтрагента;
	
	ЗаполнитьКонтактнуюИнформацию(НовыйКонтрагент, ДанныеЗаполнения);
	
	Попытка
		НовыйКонтрагент.Записать();
		КонтрагентСсылка = НовыйКонтрагент.Ссылка;
	Исключение
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось записать контрагента ""%1"" по причине: %2';
				|en = 'Cannot save the ""%1"" counterparty due to: %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
			НовыйКонтрагент.НаименованиеПолное,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(ВидМаркетплейса),
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			ТекстОшибки);
	КонецПопытки;
	
	Если ИспользоватьПривилегированныйРежим Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат КонтрагентСсылка;

КонецФункции

// Возвращает текст запроса поиска контрагентов по наименованию, ИНН, КПП.
//
// Параметры:
//   ИмяТаблицыДанныхДляПоиска - Строка - имя временной таблицы в запросе.
//
// Возвращаемое значение:
//   Строка - текст запроса.
//
Функция ТекстЗапросаПоискаКонтрагентовИПартнеров(ИмяТаблицыДанныхДляПоиска = "ЗагружаемыеДанные") Экспорт

	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ДанныеДляПоиска.НаименованиеКонтрагента КАК НаименованиеКонтрагента,
		|	ДанныеДляПоиска.ИННКонтрагента КАК ИННКонтрагента,
		|	ДанныеДляПоиска.КППКонтрагента КАК КППКонтрагента,
		|	Контрагенты.Ссылка КАК Контрагент,
		|	Контрагенты.Партнер КАК Партнер,
		|	1 КАК Приоритет
		|ПОМЕСТИТЬ ВыборкаКонтрагентов
		|ИЗ
		|	ИмяТаблицыДанныхДляПоиска КАК ДанныеДляПоиска
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ДанныеДляПоиска.ИННКонтрагента = Контрагенты.ИНН
		|			И ДанныеДляПоиска.КППКонтрагента = Контрагенты.КПП
		|			И (НЕ Контрагенты.ПометкаУдаления)
		|ГДЕ
		|	ДанныеДляПоиска.ИННКонтрагента <> """"
		|	И ДанныеДляПоиска.КППКонтрагента <> """"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДляПоиска.НаименованиеКонтрагента,
		|	ДанныеДляПоиска.ИННКонтрагента,
		|	ДанныеДляПоиска.КППКонтрагента,
		|	Контрагенты.Ссылка,
		|	Контрагенты.Партнер,
		|	2
		|ИЗ
		|	ИмяТаблицыДанныхДляПоиска КАК ДанныеДляПоиска
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ДанныеДляПоиска.ИННКонтрагента = Контрагенты.ИНН
		|			И (НЕ Контрагенты.ПометкаУдаления)
		|ГДЕ
		|	ДанныеДляПоиска.КППКонтрагента = """"
		|	И ДанныеДляПоиска.ИННКонтрагента <> """"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДляПоиска.НаименованиеКонтрагента,
		|	ДанныеДляПоиска.ИННКонтрагента,
		|	ДанныеДляПоиска.КППКонтрагента,
		|	Контрагенты.Ссылка,
		|	Контрагенты.Партнер,
		|	3
		|ИЗ
		|	ИмяТаблицыДанныхДляПоиска КАК ДанныеДляПоиска
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ДанныеДляПоиска.НаименованиеКонтрагента = Контрагенты.НаименованиеПолное
		|			И (НЕ Контрагенты.ПометкаУдаления)
		|ГДЕ
		|	ДанныеДляПоиска.ИННКонтрагента = """"
		|	И ДанныеДляПоиска.КППКонтрагента = """"
		|	И ДанныеДляПоиска.НаименованиеКонтрагента <> """"
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДляПоиска.НаименованиеКонтрагента,
		|	ДанныеДляПоиска.ИННКонтрагента,
		|	ДанныеДляПоиска.КППКонтрагента,
		|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
		|	Партнеры.Ссылка,
		|	4
		|ИЗ
		|	ИмяТаблицыДанныхДляПоиска КАК ДанныеДляПоиска
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
		|		ПО ДанныеДляПоиска.НаименованиеКонтрагента = Партнеры.НаименованиеПолное
		|			И (НЕ Партнеры.ПометкаУдаления)
		|ГДЕ
		|	ДанныеДляПоиска.НаименованиеКонтрагента <> """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыборкаКонтрагентов.НаименованиеКонтрагента КАК НаименованиеКонтрагента,
		|	ВыборкаКонтрагентов.ИННКонтрагента КАК ИННКонтрагента,
		|	ВыборкаКонтрагентов.КППКонтрагента КАК КППКонтрагента,
		|	МИНИМУМ(ВыборкаКонтрагентов.Приоритет) КАК Приоритет
		|ПОМЕСТИТЬ ВыборкаПриоритета
		|ИЗ
		|	ВыборкаКонтрагентов КАК ВыборкаКонтрагентов
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыборкаКонтрагентов.НаименованиеКонтрагента,
		|	ВыборкаКонтрагентов.ИННКонтрагента,
		|	ВыборкаКонтрагентов.КППКонтрагента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыборкаКонтрагентов.НаименованиеКонтрагента КАК НаименованиеКонтрагента,
		|	ВыборкаКонтрагентов.ИННКонтрагента КАК ИННКонтрагента,
		|	ВыборкаКонтрагентов.КППКонтрагента КАК КППКонтрагента,
		|	МАКСИМУМ(ВыборкаКонтрагентов.Контрагент) КАК Контрагент,
		|	МАКСИМУМ(ВыборкаКонтрагентов.Партнер) КАК Партнер
		|ПОМЕСТИТЬ НайденныеКонтрагентыИПартнеры
		|ИЗ
		|	ВыборкаКонтрагентов КАК ВыборкаКонтрагентов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыборкаПриоритета КАК ВыборкаПриоритета
		|		ПО ВыборкаКонтрагентов.НаименованиеКонтрагента = ВыборкаПриоритета.НаименованиеКонтрагента
		|			И ВыборкаКонтрагентов.ИННКонтрагента = ВыборкаПриоритета.ИННКонтрагента
		|			И ВыборкаКонтрагентов.КППКонтрагента = ВыборкаПриоритета.КППКонтрагента
		|			И ВыборкаКонтрагентов.Приоритет = ВыборкаПриоритета.Приоритет
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыборкаКонтрагентов.НаименованиеКонтрагента,
		|	ВыборкаКонтрагентов.ИННКонтрагента,
		|	ВыборкаКонтрагентов.КППКонтрагента
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НаименованиеКонтрагента,
		|	ИННКонтрагента,
		|	КППКонтрагента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВыборкаКонтрагентов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВыборкаПриоритета";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяТаблицыДанныхДляПоиска", ИмяТаблицыДанныхДляПоиска);
	
	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти

#Область НоменклатураКонтрагентов

// Выполняет перенос (копирование) элементов справочника "Номенклатура партнеров" между владельцами.
//
// Параметры:      
//   Параметры   - см. ИнтеграцияСМаркетплейсамиКлиентСервер.ПараметрыИзмененияВладельцаТоварногоКаталога.
//   Скопировать - Булево - Истина, если скопировать элементы.
//
// Возвращаемое значение:
//   См. НоваяОшибка.
//
Функция ПеренестиНоменклатуруПартнера(Параметры, Скопировать = Истина) Экспорт
	
	Ошибка = НоваяОшибка();
	
	Если Ошибка.Детализация = Неопределено Тогда
		Ошибка.Детализация = Новый Массив;
	КонецЕсли;
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ИнтеграцияСМаркетплейсамиСервер.ПеренестиНоменклатуруПартнера");
	
	Если Параметры.ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		ИмяРегистраСведений       = "СтатусыПубликацииТоваровЯндексМаркет";
		ИмяИзмеренияУчетнаяЗапись = "УчетнаяЗапись";
	ИначеЕсли Параметры.ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ИмяРегистраСведений       = "СтатусыПубликацииОбъектовМаркетплейсаOzon";
		ИмяИзмеренияУчетнаяЗапись = "УчетнаяЗаписьМаркетплейса";
	ИначеЕсли ИнтеграцияСМаркетплейсамиСИ.ЭтоМаркетплейсСИ(Параметры.ВидТорговойПлощадки) И Не Скопировать Тогда
		ИмяРегистраСведений       = "";
		ИмяИзмеренияУчетнаяЗапись = "";
	Иначе
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неизвестный вид торговой площадки %1.';
				|en = 'Unknown marketplace type: %1.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Строка(Параметры.ВидТорговойПлощадки));
		
		Ошибка.КодОшибки      = "НоменклатураКонтрагентов_Запись";
		Ошибка.ОписаниеОшибки = ТекстОшибки;
			
		Возврат Ошибка;
	КонецЕсли;
	
	ИмяРегистра = СтрШаблон("РегистрСведений.%1", ИмяРегистраСведений);
	
	ПараметрыОбработки = НовыеПараметрыОбработкиДанныхНоменклатурыПартнераДляПереноса();
	ПараметрыОбработки.ПрефиксИмпортируемыеТовары = "ИмпортируемыеТовары";
	ПараметрыОбработки.ПрефиксСтатусыПубликации = "СтатусыПубликации";
	ПараметрыОбработки.КопироватьЭлементы = Скопировать;
	
	Если Скопировать Тогда
		
		ПараметрыОбработки.ИмпортируемыеТоварыСтарые = РегистрыСведений.ИмпортируемыеТоварыТорговыхПлощадок.СоздатьНаборЗаписей();
		ПараметрыОбработки.ИмпортируемыеТоварыНовые = РегистрыСведений.ИмпортируемыеТоварыТорговыхПлощадок.СоздатьНаборЗаписей();
		
		Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяРегистра);
		
		ПараметрыОбработки.СтатусыПубликацииСтарые = Менеджер.СоздатьНаборЗаписей(); // РегистрСведенийНаборЗаписей.СтатусыПубликацииОбъектовМаркетплейсаOzon, РегистрСведенийНаборЗаписей.СтатусыПубликацииТоваровЯндексМаркет
		ПараметрыОбработки.СтатусыПубликацииНовые = Менеджер.СоздатьНаборЗаписей(); // РегистрСведенийНаборЗаписей.СтатусыПубликацииОбъектовМаркетплейсаOzon, РегистрСведенийНаборЗаписей.СтатусыПубликацииТоваровЯндексМаркет
		
	КонецЕсли;
	
	ПакетЗапросов = ДанныеНоменклатурыПартнераДляПереноса(
		ИмяРегистра,
		ИмяИзмеренияУчетнаяЗапись,
		Параметры,
		ПараметрыОбработки);
	
	РезультатЗапросаПоГруппам = ПакетЗапросов[0];
	РезультатЗапроса = ПакетЗапросов[1];
	
	Если РезультатЗапросаПоГруппам.Пустой()
			И РезультатЗапроса.Пустой()
			И Не Параметры.ОбрабатыватьЗаписиБезВладельца Тогда
		Возврат Ошибка;
	КонецЕсли;
	
	ПараметрыОбработки.ИменаКолонокИмпортируемыеТовары = ИменаКолонокРезультатаЗапросаПоПрефиксу(
		РезультатЗапроса, ПараметрыОбработки.ПрефиксИмпортируемыеТовары);
		
	ПараметрыОбработки.ИменаКолонокСтатусыПубликации = ИменаКолонокРезультатаЗапросаПоПрефиксу(
		РезультатЗапроса, ПараметрыОбработки.ПрефиксСтатусыПубликации);
	
	ЗаполняемыеСвойстваЭлемента = ИменаРеквизитовСправочникаНоменклатураКонтрагентов(Ложь);
	ЗаполняемыеСвойстваГруппы = ИменаРеквизитовСправочникаНоменклатураКонтрагентов(Истина);
	
	ИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	ИспользоватьУпаковкиНоменклатуры       = ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаДанных = Новый БлокировкаДанных;
		
		Если Скопировать Тогда
			
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ИмпортируемыеТоварыТорговыхПлощадок");
			ЭлементБлокировкиДанных.ИсточникДанных = РезультатЗапроса;
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных(
				"УчетнаяЗапись", ПараметрыОбработки.ПрефиксИмпортируемыеТовары + "УчетнаяЗапись");
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных(
				"НоменклатураПартнера", ПараметрыОбработки.ПрефиксИмпортируемыеТовары + "НоменклатураПартнера");
			
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить(ИмяРегистра);
			ЭлементБлокировкиДанных.ИсточникДанных = РезультатЗапроса;
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных(
				ИмяИзмеренияУчетнаяЗапись, ПараметрыОбработки.ПрефиксСтатусыПубликации + ИмяИзмеренияУчетнаяЗапись);
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных(
				"Номенклатура", ПараметрыОбработки.ПрефиксСтатусыПубликации + "Номенклатура");
			
			Если Параметры.ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
				ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных(
					"НоменклатураПартнера", ПараметрыОбработки.ПрефиксСтатусыПубликации + "НоменклатураПартнера");
			КонецЕсли;
			
			Если ИспользоватьХарактеристикиНоменклатуры Тогда
				ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных(
					"Характеристика", ПараметрыОбработки.ПрефиксСтатусыПубликации + "Характеристика");
			КонецЕсли;
			
			Если ИспользоватьУпаковкиНоменклатуры Тогда
				ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных(
					"Упаковка", ПараметрыОбработки.ПрефиксСтатусыПубликации + "Упаковка");
			КонецЕсли;
			
		Иначе
			
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.НоменклатураКонтрагентов");
			ЭлементБлокировкиДанных.ИсточникДанных = РезультатЗапросаПоГруппам;
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Ссылка", "ПредыдущаяСсылка");
			
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.НоменклатураКонтрагентов");
			ЭлементБлокировкиДанных.ИсточникДанных = РезультатЗапроса;
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Ссылка", "ПредыдущаяСсылка");
			
		КонецЕсли;
		
		БлокировкаДанных.Заблокировать();
		
		ПараметрыОбработки.РезультатЗапроса = РезультатЗапросаПоГруппам;
		ПараметрыОбработки.ЗаполняемыеСвойства = ЗаполняемыеСвойстваГруппы;
		ПараметрыОбработки.ЭтоОбработкаГрупп = Истина;
		
		ПеренестиНоменклатуруПартнераСлужебный(
			Параметры.ВидТорговойПлощадки, Параметры.НовыйВладелец, Ошибка, ПараметрыОбработки);
		
		ПараметрыОбработки.РезультатЗапроса = РезультатЗапроса;
		ПараметрыОбработки.ЗаполняемыеСвойства = ЗаполняемыеСвойстваЭлемента;
		ПараметрыОбработки.ЭтоОбработкаГрупп = Ложь;
		
		ПеренестиНоменклатуруПартнераСлужебный(
			Параметры.ВидТорговойПлощадки, Параметры.НовыйВладелец, Ошибка, ПараметрыОбработки);
		
		Если ПараметрыОбработки.ИмпортируемыеТоварыСтарые <> Неопределено
				И ПараметрыОбработки.ИмпортируемыеТоварыСтарые.Количество() > 0 Тогда
			ПараметрыОбработки.ИмпортируемыеТоварыСтарые.Записать(РежимЗамещения.Удаление);
		КонецЕсли;
		
		Если ПараметрыОбработки.ИмпортируемыеТоварыНовые <> Неопределено
				И ПараметрыОбработки.ИмпортируемыеТоварыНовые.Количество() > 0 Тогда
			ПараметрыОбработки.ИмпортируемыеТоварыНовые.Записать(РежимЗамещения.Слияние);
		КонецЕсли;
		
		Если ПараметрыОбработки.СтатусыПубликацииСтарые <> Неопределено
				И ПараметрыОбработки.СтатусыПубликацииСтарые.Количество() > 0 Тогда
			ПараметрыОбработки.СтатусыПубликацииСтарые.Записать(РежимЗамещения.Удаление);
		КонецЕсли;
		
		Если ПараметрыОбработки.СтатусыПубликацииНовые <> Неопределено
				И ПараметрыОбработки.СтатусыПубликацииНовые.Количество() > 0 Тогда
			ПараметрыОбработки.СтатусыПубликацииНовые.Записать(РежимЗамещения.Слияние);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		Если Скопировать Тогда
			ШаблонОшибки = НСтр("ru = 'При копировании позиций товарного каталога возникли ошибки: %1.';
								|en = 'Errors occurred when copying the goods directory items: %1.'",
				ОбщегоНазначения.КодОсновногоЯзыка());
		Иначе
			ШаблонОшибки = НСтр("ru = 'При переносе позиций товарного каталога возникли ошибки: %1.';
								|en = 'Errors occurred when transferring the goods directory items: %1.'",
				ОбщегоНазначения.КодОсновногоЯзыка());
		КонецЕсли;
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(Параметры.ВидТорговойПлощадки),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
		
		Ошибка.КодОшибки      = "НоменклатураКонтрагентов_Запись";
		Ошибка.ОписаниеОшибки = ТекстОшибки;
			
		Возврат Ошибка;
	КонецПопытки;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
		Замер, 
		ПараметрыОбработки.КоличествоОбработано / 100);
	
	Если Параметры.ОбрабатыватьЗаписиБезВладельца Тогда
		Если Параметры.ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
			ОшибкаЗаполненияНоменклатуры = РегистрыСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon.ЗаполнитьНоменклатуруПартнера(
				Параметры.УчетнаяЗапись);
			
			ДополнитьОшибку(Ошибка, ОшибкаЗаполненияНоменклатуры);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ошибка;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//   * НоменклатураПартнера - СправочникСсылка.НоменклатураКонтрагентов - 
//   * Артикул - Строка - 
//   * НаименованиеТовара - Строка - 
//   * ПредставлениеТовара - Строка - 
//   * ИдентификаторТовара - Строка - 
//   * ИдентификаторКакАртикул - Строка - 
//   * ИдентификаторМаркетплейса - Строка - 
//   * Штрихкод - Строка - 
//   * ШтрихкодыНоменклатуры - Строка - 
//   * Недействителен - Булево - 
//   * Номенклатура - СправочникСсылка.НоменклатураКонтрагентов - 
//   * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - 
//   * Упаковка - СправочникСсылка.УпаковкиЕдиницыИзмерения - 
//   * СтранаПроисхожденияКод - Строка - 
//   * КодТНВЭД - Строка - 
//   * СтавкаНДС - Строка - 
//   * НаименованиеУпаковки - Строка - 
//   * ЕдиницаИзмерения - Строка - 
//   * ЕдиницаИзмеренияКод - Строка - 
//   * КоэффициентУпаковки - Число - 
//
Функция НовыеДанныеНоменклатурыПартнераДляОбновления() Экспорт
	
	ДанныеНоменклатуры = Новый Структура;
	
	ДанныеНоменклатуры.Вставить("НоменклатураПартнера", Справочники.НоменклатураКонтрагентов.ПустаяСсылка());
	
	ДанныеНоменклатуры.Вставить("Артикул",             "");
	ДанныеНоменклатуры.Вставить("НаименованиеТовара",  "");
	ДанныеНоменклатуры.Вставить("ПредставлениеТовара", "");
	
	ДанныеНоменклатуры.Вставить("ИдентификаторТовара",       "");
	ДанныеНоменклатуры.Вставить("ИдентификаторКакАртикул",   "");
	ДанныеНоменклатуры.Вставить("ИдентификаторМаркетплейса", "");
	
	ДанныеНоменклатуры.Вставить("Штрихкод",              "");
	ДанныеНоменклатуры.Вставить("ШтрихкодыНоменклатуры", "");
	ДанныеНоменклатуры.Вставить("Недействителен",        Ложь);
	
	ДанныеНоменклатуры.Вставить("Номенклатура",   Справочники.НоменклатураКонтрагентов.ПустаяСсылка());
	ДанныеНоменклатуры.Вставить("Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	ДанныеНоменклатуры.Вставить("Упаковка",       Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	
	ДанныеНоменклатуры.Вставить("СтранаПроисхожденияКод", "");
	ДанныеНоменклатуры.Вставить("КодТНВЭД",               "");
	ДанныеНоменклатуры.Вставить("СтавкаНДС",              "");
	
	ДанныеНоменклатуры.Вставить("НаименованиеУпаковки", "");
	ДанныеНоменклатуры.Вставить("ЕдиницаИзмерения",     "");
	ДанныеНоменклатуры.Вставить("ЕдиницаИзмеренияКод",  "");
	ДанныеНоменклатуры.Вставить("КоэффициентУпаковки",  0);
	
	Возврат ДанныеНоменклатуры;
	
КонецФункции

// Синхронизирует данные по товару маркетплейса с номенклатурой партнера
// 
// Параметры:
//  Партнер - СправочникСсылка.Партнеры - 
//  ДанныеНоменклатуры - см. НовыеДанныеНоменклатурыПартнераДляОбновления
// 
// Возвращаемое значение:
//  см. НовыйРезультатСозданияОбновленияНоменклатурыПартнера
//
Функция СоздатьОбновитьНоменклатуруПартнера(Партнер, ДанныеНоменклатуры) Экспорт
	
	Результат = НовыйРезультатСозданияОбновленияНоменклатурыПартнера();
	
	Если Не ДанныеНоменклатурыПартнераЗаполнены(ДанныеНоменклатуры) Тогда
		Возврат Результат;
		
	ИначеЕсли Не ЕстьИзмененияРеквизитовНоменклатурыКонтрагента(ДанныеНоменклатуры) Тогда
		
		Результат.НоменклатураПартнера = ДанныеНоменклатуры.НоменклатураПартнера;
		Возврат Результат;
		
	КонецЕсли;
	
	ДанныеНоменклатурыКонтрагента = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента();
	
	ДанныеНоменклатурыКонтрагента.Вставить("НаименованиеНоменклатуры", "");
	ДанныеНоменклатурыКонтрагента.Вставить("НаименованиеПолное",       "");
	ДанныеНоменклатурыКонтрагента.Вставить("Недействителен",           Ложь);
	
	ДанныеНоменклатурыКонтрагента.Владелец = Партнер;
	
	ЗаполнитьНоменклатуруКонтрагентаПоДаннымНоменклатуры(ДанныеНоменклатурыКонтрагента, ДанныеНоменклатуры);
	
	ДанныеНоменклатурыКонтрагента.КоличествоУпаковок = 1;
	
	НоменклатураИБ = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы(
		ДанныеНоменклатуры.Номенклатура, ДанныеНоменклатуры.Характеристика, ДанныеНоменклатуры.Упаковка);
		
	ДополнительныеПараметры = СопоставлениеНоменклатурыКонтрагентов.НовыеДополнительныеПараметрыПриЗаписиНоменклатурыКонтрагентов();
	ДополнительныеПараметры.ТребуетсяПоискСсылки = Ложь;
	
	СопоставлениеНоменклатурыКонтрагентов.СоздатьОбновитьНоменклатуруКонтрагента(
		ДанныеНоменклатурыКонтрагента,
		НоменклатураИБ,
		Результат.Отказ,
		Результат.ТекстОшибки,
		ДополнительныеПараметры,
		Ложь);
	
	Если Результат.Отказ Тогда
		
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,
			ДанныеНоменклатурыКонтрагента.НоменклатураКонтрагента.Метаданные(),
			ДанныеНоменклатурыКонтрагента.НоменклатураКонтрагента,
			Результат.ТекстОшибки);
			
	Иначе
		Результат.НоменклатураПартнера = ДанныеНоменклатурыКонтрагента.НоменклатураКонтрагента;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Синхронизирует данные номенклатуры, характеристики и упаковки, указанных в номенклатуре партнера, с данными
// регистра статусов публикации. Номенклатура партнера считается достоверным источником данных.
//
// Параметры:
//  УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - 
//  МассивНоменклатурыПартнера - Массив из СправочникСсылка.НоменклатураКонтрагентов - 
//
// Возвращаемое значение:
//  см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка
//
Функция ОбновитьДанныеСтатусовПубликацииПоНоменклатуреПартнера(УчетнаяЗаписьМаркетплейса,
			МассивНоменклатурыПартнера = Неопределено) Экспорт
	
	Ошибка = НоваяОшибка();
	
	РеквизитыУчетнойЗаписи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		УчетнаяЗаписьМаркетплейса, "ВидМаркетплейса, ИсточникКатегории");
	
	ПараметрыОбновления = ПараметрыОбновленияДанныхСтатусовПубликацииПоНоменклатуреПартнера(
		РеквизитыУчетнойЗаписи.ВидМаркетплейса);
	
	РезультатЗапроса = ВыборкаДанныхДляОбновленияСтатусовПубликацииПоНоменклатуреПартнера(
		УчетнаяЗаписьМаркетплейса, ПараметрыОбновления, МассивНоменклатурыПартнера, Ложь, Истина);
	
	Если РезультатЗапроса = Неопределено Тогда
		Возврат Ошибка;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ДополнительныеДанные = ДополнительныеДанныеДляОбновленияСтатусовПубликацииПоНоменклатуреПартнера(
		РеквизитыУчетнойЗаписи.ВидМаркетплейса, РеквизитыУчетнойЗаписи.ИсточникКатегории, Выборка);
	
	ЕстьОшибки = Ложь;
	
	МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПараметрыОбновления.ПолноеИмяРегистра);
	
	Выборка.Сбросить();
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить(ПараметрыОбновления.ПолноеИмяРегистра);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура",         "Номенклатура");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика",       "Характеристика");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Упаковка",             "Упаковка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("НоменклатураПартнера", "НоменклатураПартнера");
		
		ЭлементБлокировки.УстановитьЗначение(
			ПараметрыОбновления.ИмяПоляУчетнаяЗапись, УчетнаяЗаписьМаркетплейса);
		
		Для Каждого КлючЗначение Из ПараметрыОбновления.ДополнительныеОтборы Цикл
			ЭлементБлокировки.УстановитьЗначение(КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЦикла;
		
		Блокировка.Заблокировать();
		
		СтарыеЗаписи = НовыйНаборЗаписейДляОбновленияСтатусовПубликацииСОтборами(
			МенеджерРегистра, УчетнаяЗаписьМаркетплейса, ПараметрыОбновления);
		
		НовыеЗаписи = НовыйНаборЗаписейДляОбновленияСтатусовПубликацииСОтборами(
			МенеджерРегистра, УчетнаяЗаписьМаркетплейса, ПараметрыОбновления,, "Новая");
		
		Пока Выборка.Следующий() Цикл
			// Запись для удаления.
			СтараяЗапись = СтарыеЗаписи.Добавить();
			ЗаполнитьЗначенияСвойств(СтараяЗапись, Выборка);
			
			// Запись для добавления.
			НоваяЗапись = НовыеЗаписи.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			
			НоваяЗапись.Номенклатура   = Выборка.НоменклатураНовая;
			НоваяЗапись.Характеристика = Выборка.ХарактеристикаНовая;
			НоваяЗапись.Упаковка       = Выборка.УпаковкаНовая;
		КонецЦикла;
			
		Если СтарыеЗаписи.Количество() > 0 Тогда
			//@skip-check bsl-legacy-check-expression-type
			СтарыеЗаписи.Записать(РежимЗамещения.Удаление);
		КонецЕсли;
		
		Если НовыеЗаписи.Количество() > 0 Тогда
			ДополнитьНовыеЗаписиСтатусовПубликацииДополнительнымиДанными(
				РеквизитыУчетнойЗаписи.ВидМаркетплейса, НовыеЗаписи, ДополнительныеДанные);
			
			//@skip-check bsl-legacy-check-expression-type
			НовыеЗаписи.Записать(РежимЗамещения.Слияние);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, , ,
			ТекстОшибки);
			
		ЕстьОшибки = Истина;
		
	КонецПопытки;
	
	Если ЕстьОшибки Тогда
		
		Ошибка.КодОшибки      = КодыОшибок().ОшибкаЗаполненияДанных;
		Ошибка.ОписаниеОшибки = НСтр("ru = 'При обновлении данных импортированных товаров возникли ошибки.
			|Подробности см. в журнале регистрации.';
			|en = 'Errors occurred when updating the imported goods data.
			|For more information, see the event log.'");
		
	КонецЕсли;
	
	Возврат Ошибка;
	
КонецФункции

#КонецОбласти

#Область Склады

// Конструктор определения настроек склада FBS.
//
// Параметры:
//   ИсключатьОтправленияИзДоставки - Булево - признак исключения отправлений из доставки.
//   ТолькоНастройкиСпособаДоставки - Булево - признак возврата только настроек способа доставки.
//
// Возвращаемое значение:
//   Структура - пустые настройки склада:
//     * Склад - СправочникСсылка.Склады - склад учетной системы.
//     * СпособДоставки - ПеречислениеСсылка.СпособыДоставки - способ доставки для склада.
//     * ПредставлениеСпособаДоставки - см. Перечисления.СпособыДоставки.ПредставлениеСпособаДоставки.
//     * ЗонаДоставки - СправочникСсылка.ЗоныДоставки - зона доставки.
//     * ПеревозчикПартнер - СправочникСсылка.Партнеры - партнер.
//     * Курьер - СправочникСсылка.ФизическиеЛица - курьер.
//     * АдресДоставкиПеревозчика - Строка - адрес перевозчика.
//     * АдресДоставкиПеревозчикаЗначение - Строка - адрес перевозчика служебный.
//     * АдресДоставкиПеревозчикаЗначенияПолей - Строка - адрес перевозчика служебный.
//     * ВремяДоставкиС - Дата - начальная граница времени доставки.
//     * ВремяДоставкиПо - Дата - конечная граница времени доставки.
//     * ДатаАктуальности - Дата - дата записи настроек.
//     * ИсключатьОтправленияИзДоставки - Булево - признак исключения отправлений из доставки до формирования отгрузки.
//     * ЗаполненыНастройкиСклада - Булево - признак заполненности настроек склада.
//
Функция НовыйНастройкиСкладаFBS(ИсключатьОтправленияИзДоставки = Истина,
			ТолькоНастройкиСпособаДоставки = Ложь) Экспорт

	НастройкиСклада = Новый Структура;
	
	Если Не ТолькоНастройкиСпособаДоставки Тогда
		НастройкиСклада.Вставить("Склад", Справочники.Склады.ПустаяСсылка());
	КонецЕсли;
	
	НастройкиСклада.Вставить("СпособДоставки", Перечисления.СпособыДоставки.Самовывоз);
	НастройкиСклада.Вставить("ПредставлениеСпособаДоставки", "");
	НастройкиСклада.Вставить("ЗонаДоставки", Справочники.ЗоныДоставки.ПустаяСсылка());
	НастройкиСклада.Вставить("ПеревозчикПартнер", Справочники.Партнеры.ПустаяСсылка());
	НастройкиСклада.Вставить("Курьер", Справочники.ФизическиеЛица.ПустаяСсылка());
	НастройкиСклада.Вставить("АдресДоставкиПеревозчика", "");
	НастройкиСклада.Вставить("АдресДоставкиПеревозчикаЗначение", "");
	НастройкиСклада.Вставить("АдресДоставкиПеревозчикаЗначенияПолей", "");
	НастройкиСклада.Вставить("ВремяДоставкиС", Дата(1,1,1));
	НастройкиСклада.Вставить("ВремяДоставкиПо", Дата(1,1,1));
	НастройкиСклада.Вставить("ДатаАктуальности", Дата(1,1,1));
	НастройкиСклада.Вставить("ИсключатьОтправленияИзДоставки", ИсключатьОтправленияИзДоставки);
	НастройкиСклада.Вставить("ЗаполненыНастройкиСклада", Ложь);
	
	Возврат НастройкиСклада;

КонецФункции

// Конструктор определения настроек метода доставки.
//
// Возвращаемое значение:
//   Структура - список атрибутов:
//     * ИдентификаторСклада         - Строка - идентификатор склада.
//     * ИдентификаторМетодаДоставки - Строка - идентификатор метода доставки склада.
//     * НаименованиеМетодаДоставки  - Строка - наименование метода доставки склада.
//     * АктивныйСклад               - Булево - признак активности склада.
//     * ВремяОтгрузки               - Дата - время отгрузки.
//     * ВремяОтгрузкиРекомендуемое  - Дата - время отгрузки рекомендуемое.
//     * ДоверительнаяПриемка        - Булево - признак доверительной приемки.
//
Функция НовыйМетодДоставкиСклада() Экспорт

	МетодДоставки = Новый Структура;
	МетодДоставки.Вставить("ИдентификаторСклада", "");
	МетодДоставки.Вставить("ИдентификаторМетодаДоставки", "");
	МетодДоставки.Вставить("НаименованиеМетодаДоставки", "");
	МетодДоставки.Вставить("АктивныйСклад", Ложь);
	МетодДоставки.Вставить("ВремяОтгрузки", Дата(1, 1, 1));
	МетодДоставки.Вставить("ВремяОтгрузкиРекомендуемое", Дата(1, 1, 1));
	МетодДоставки.Вставить("ДоверительнаяПриемка", Ложь);
	
	Возврат МетодДоставки;

КонецФункции

// Конструктор таблицы значений для списка складов.
//
// Параметры:
//   УчитыватьМетодДоставки - Булево - признак получения информации по методу доставки склада.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица с колонками:
//     * ИдентификаторОбъектаМаркетплейса - Строка - идентификатор склада.
//     * НаименованиеОбъектаМаркетплейса  - Строка - название склада.
//     * ЭтоRealFBS                       - Булево - признак работы склада по схеме rFBS.
//     * ДоверительнаяПриемка             - Булево - признак доверительной приемки при передаче в доставку.
//     * Статус                           - Строка - статус склада на площадке.
//     * Склад1С                          - СправочникСсылка.Склады - сопоставленный склад информационной базы.
//     * ЗаполненыНастройкиСклада         - Булево - признак наличия сохраненных настроек склада.
//     * ИдентификаторМетодаДоставки      - Строка - идентификатор метода доставки склада площадки.
//     * НаименованиеМетодаДоставки       - Строка - наименование метода доставки склада площадки.
//     * ВремяОтгрузки                    - Дата - время, до которого нужно отгрузить заказ по методу доставки склада площадки.
//
Функция НоваяТаблицаСкладов(УчитыватьМетодДоставки = Ложь) Экспорт

	ТаблицаСкладов = Новый ТаблицаЗначений;
	ТаблицаСкладов.Колонки.Добавить("ИдентификаторОбъектаМаркетплейса", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50)));
	ТаблицаСкладов.Колонки.Добавить("НаименованиеОбъектаМаркетплейса",  Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(250)));
	ТаблицаСкладов.Колонки.Добавить("ЭтоRealFBS",                       Новый ОписаниеТипов("Булево"));
	ТаблицаСкладов.Колонки.Добавить("ДоверительнаяПриемка",             Новый ОписаниеТипов("Булево"));
	ТаблицаСкладов.Колонки.Добавить("Статус",                           Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(25)));
	ТаблицаСкладов.Колонки.Добавить("АктивныйСклад",                    Новый ОписаниеТипов("Булево"));
	ТаблицаСкладов.Колонки.Добавить("Склад1С",                          Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаСкладов.Колонки.Добавить("ЗаполненыНастройкиСклада",         Новый ОписаниеТипов("Булево"));
	ТаблицаСкладов.Колонки.Добавить("НастройкиСклада",                  Новый ОписаниеТипов("Структура"));
	
	Если УчитыватьМетодДоставки Тогда
		ТаблицаСкладов.Колонки.Добавить("ИдентификаторМетодаДоставки",      Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50)));
		ТаблицаСкладов.Колонки.Добавить("НаименованиеМетодаДоставки",       Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(250)));
		ТаблицаСкладов.Колонки.Добавить("ВремяОтгрузки",                    Новый ОписаниеТипов("Дата",,,,, Новый КвалификаторыДаты(ЧастиДаты.Время)));
	КонецЕсли;
	
	Возврат ТаблицаСкладов;

КонецФункции

// Возврат данные о складах из сервиса.
//
// Параметры:
//   ВидМаркетплейса        - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//   УчетнаяЗапись          - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   ТолькоИзСервиса        - Булево - признак включения в ответ только данных из сервиса, иначе выполнится дополнение
//                               данными сопоставления из информационной базы.
//   УчитыватьМетодДоставки - Булево - признак включения в ответ сведений по активным методам доставки складов.
// 
// Возвращаемое значение:
//   Структура:
//     * ТаблицаСкладов - см. НоваяТаблицаСкладов.
//     * Ошибка         - см. НоваяОшибка.
//
Функция ПолучитьСкладыСервиса(ВидМаркетплейса, УчетнаяЗапись, ТолькоИзСервиса = Ложь, УчитыватьМетодДоставки = Ложь) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ТаблицаСкладов", НоваяТаблицаСкладов(УчитыватьМетодДоставки));
	Результат.Вставить("Ошибка",         НоваяОшибка());
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УчетныеЗаписиМаркетплейсов.Ссылка КАК УчетнаяЗапись
		|ИЗ
		|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
		|ГДЕ
		|	УчетныеЗаписиМаркетплейсов.ВидМаркетплейса = &ВидМаркетплейса
		|	И &Условие";
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ТекстЗамены = "УчетныеЗаписиМаркетплейсов.Ссылка = &УчетнаяЗапись";
		Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	Иначе
		ТекстЗамены = "НЕ УчетныеЗаписиМаркетплейсов.ПометкаУдаления";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", ТекстЗамены);
	Запрос.УстановитьПараметр("ВидМаркетплейса", ВидМаркетплейса);
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаУчетныхЗаписей = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	СписокПроблемныхУчетныхЗаписей = Новый Массив;
	
	Пока ВыборкаУчетныхЗаписей.Следующий() Цикл
		Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
			РезультатПолученияСкладов =
				ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьСкладыСервиса(ВыборкаУчетныхЗаписей.УчетнаяЗапись,, Истина);
		Иначе
			РезультатПолученияСкладов = Неопределено;
		КонецЕсли;
		
		Если РезультатПолученияСкладов <> Неопределено Тогда
			Если РезультатПолученияСкладов.ОшибкаПолученияСкладовИзСервиса Тогда
				СписокПроблемныхУчетныхЗаписей.Добавить(ВыборкаУчетныхЗаписей.УчетнаяЗапись);
			Иначе
				Результат.ТаблицаСкладов = РезультатПолученияСкладов.ТаблицаСкладов;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СписокПроблемныхУчетныхЗаписей) Тогда
		ТекстШаблона = НСтр("ru = 'Не удалось получить список складов для учетных записей: %1. Подробнее см. журнал регистрации.
			|Заполнение списка складов выполнено из сохраненных настроек.';
			|en = 'Cannot get a warehouse list for accounts: %1. For more information, see the event log.
			|The warehouse list is filled from the saved settings.'");
		
		Результат.Ошибка.КодОшибки = КодыОшибок().ОшибкаПрочие;
		Результат.Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстШаблона,
			СтрСоединить(СписокПроблемныхУчетныхЗаписей, ", "));
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Заполняет в форме данные о складах и методах доставки из сервиса.
//
// Параметры:
//   ВидМаркетплейса    - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//   ТаблицаСкладов     - см. НоваяТаблицаСкладов.
//   СписокВыбораСклада - СписокЗначений из Строка - список выбора для заполнения складами торговой площадки.
//   МетодыДоставки     - Неопределено - переменная для заполнения методов доставки складов торговой площадки.
//
Процедура ЗаполнитьСписокСкладовИМетодовДоставки(ВидМаркетплейса, ТаблицаСкладов,
			СписокВыбораСклада,
			МетодыДоставки) Экспорт

	СписокВыбораСклада.Очистить();
	МетодыДоставки = Новый Структура;
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ПолеПроверки         = "ЭтоRealFBS";
		ПредставлениеFBS     = "FBS";
		ПредставлениеRealFBS = "RealFBS";
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ТаблицаСкладов <> Неопределено Тогда
		Для Каждого СтрокаСклада Из ТаблицаСкладов Цикл
			ПредставлениеЗначения = ПредставлениеСклада(
				СтрокаСклада.НаименованиеОбъектаМаркетплейса,
				?(СтрокаСклада[ПолеПроверки], ПредставлениеRealFBS, ПредставлениеFBS),
				СтрокаСклада.Статус,
				СтрокаСклада.ДоверительнаяПриемка);
			
			СписокВыбораСклада.Добавить(СтрокаСклада.ИдентификаторОбъектаМаркетплейса, ПредставлениеЗначения);
			
			ДанныеАктивногоМетодаДоставки = НовыйМетодДоставкиСклада();
			ЗаполнитьЗначенияСвойств(ДанныеАктивногоМетодаДоставки, СтрокаСклада);
			ДанныеАктивногоМетодаДоставки.ИдентификаторСклада = СтрокаСклада.ИдентификаторОбъектаМаркетплейса;
			
			МетодыДоставки.Вставить("Склад_" + СтрокаСклада.ИдентификаторОбъектаМаркетплейса,
				ДанныеАктивногоМетодаДоставки);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

// Определяет сопоставленные склады из данных информационной базы.
//
// Параметры:
//   УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   ТолькоСсылки              - Булево - признак возврата только массива складов, иначе возвращается таблица.
//   ВыводитьПодчиненныеСклады - Булево - признак включения в ответ подчиненных складов.
// 
// Возвращаемое значение:
//   - Массив Из СправочникСсылка.Склады - список ссылок СправочникСсылка.Склады.
//   - ТаблицаЗначений - таблица с данными по складам:
//       * Группа                        - СправочникСсылка.Склады - группа складов 1С.
//       * Склад                         - СправочникСсылка.Склады - склад 1С.
//       * ИдентификаторСклада           - Строка - идентификатор склада.
//       * НаименованиеСклада            - Строка - наименование склада.
//       * ИспользуетсяДляСхемыРаботыFBO - Булево - признак схемы FBO.
//       * ИспользуетсяДляСхемыРаботыFBS - Булево - признак схемы FBS.
//       * ИспользуетсяДляСхемыРаботыDBS - Булево - признак схемы DBS.
//       * ЗаполненыНастройкиСклада      - Булево - признак заполненности настроек склада.
//       * НастройкиСклада               - см. НастройкиСклада.
//
Функция ПолучитьСопоставленныеСклады(УчетнаяЗаписьМаркетплейса, ТолькоСсылки = Истина,
			ВыводитьПодчиненныеСклады = Истина) Экспорт

	СопоставленныеСклады = Новый ТаблицаЗначений;
	СопоставленныеСклады.Колонки.Добавить("Группа",              		   Новый ОписаниеТипов("СправочникСсылка.Склады"));
	СопоставленныеСклады.Колонки.Добавить("Склад",               		   Новый ОписаниеТипов("СправочникСсылка.Склады"));
	СопоставленныеСклады.Колонки.Добавить("ИдентификаторСклада", 		   Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
	СопоставленныеСклады.Колонки.Добавить("НаименованиеСклада",  		   Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(250)));
	СопоставленныеСклады.Колонки.Добавить("ИспользуетсяДляСхемыРаботыFBO", Новый ОписаниеТипов("Булево"));
	СопоставленныеСклады.Колонки.Добавить("ИспользуетсяДляСхемыРаботыFBS", Новый ОписаниеТипов("Булево"));
	СопоставленныеСклады.Колонки.Добавить("ИспользуетсяДляСхемыРаботыDBS", Новый ОписаниеТипов("Булево"));
	СопоставленныеСклады.Колонки.Добавить("ЗаполненыНастройкиСклада",      Новый ОписаниеТипов("Булево"));
	СопоставленныеСклады.Колонки.Добавить("НастройкиСклада",               Новый ОписаниеТипов("Структура"));

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СоответствияОбъектов.Объект1С КАК Склад,
		|	СоответствияОбъектов.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторСклада,
		|	СоответствияОбъектов.НаименованиеОбъектаМаркетплейса КАК НаименованиеСклада,
		|	СоответствияОбъектов.ИспользуетсяДляСхемыРаботыFBO КАК ИспользуетсяДляСхемыРаботыFBO,
		|	СоответствияОбъектов.ИспользуетсяДляСхемыРаботыFBS КАК ИспользуетсяДляСхемыРаботыFBS,
		|	СоответствияОбъектов.ИспользуетсяДляСхемыРаботыDBS КАК ИспользуетсяДляСхемыРаботыDBS,
		|	Склады.ЭтоГруппа КАК ЭтоГруппаСкладов
		|ИЗ
		|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
		|		ПО СоответствияОбъектов.Объект1С = Склады.Ссылка
		|ГДЕ
		|	СоответствияОбъектов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
		|	И СоответствияОбъектов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Склад)"; 
	
	Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", УчетнаяЗаписьМаркетплейса);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Если ТолькоСсылки Тогда
			Возврат Новый Массив;
		Иначе
			Возврат СопоставленныеСклады;
		КонецЕсли;
	КонецЕсли;

	ИдентификаторыСкладов = Новый Массив;

	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ВыводитьПодчиненныеСклады И Выборка.ЭтоГруппаСкладов Тогда
			УстановитьПривилегированныйРежим(Истина);
			ПодчиненныеСклады = СкладыСервер.СписокПодчиненныхСкладов(Выборка.Склад);
			УстановитьПривилегированныйРежим(Ложь);
			
			Для Каждого Склад Из ПодчиненныеСклады Цикл
				НоваяСтрока = СопоставленныеСклады.Добавить();
				НоваяСтрока.Группа = Выборка.Склад;
				НоваяСтрока.Склад = Склад.Значение;
				НоваяСтрока.ИдентификаторСклада = Выборка.ИдентификаторСклада;
				НоваяСтрока.НаименованиеСклада = Выборка.НаименованиеСклада;
				НоваяСтрока.ИспользуетсяДляСхемыРаботыFBO = Выборка.ИспользуетсяДляСхемыРаботыFBO;
				НоваяСтрока.ИспользуетсяДляСхемыРаботыFBS = Выборка.ИспользуетсяДляСхемыРаботыFBS;
				НоваяСтрока.ИспользуетсяДляСхемыРаботыDBS = Выборка.ИспользуетсяДляСхемыРаботыDBS;
				
				ИдентификаторыСкладов.Добавить(Выборка.ИдентификаторСклада);
			КонецЦикла;
			
		Иначе
			НоваяСтрока = СопоставленныеСклады.Добавить();
			НоваяСтрока.Группа = Выборка.Склад;
			НоваяСтрока.Склад = Выборка.Склад;
			НоваяСтрока.ИдентификаторСклада = Выборка.ИдентификаторСклада;
			НоваяСтрока.НаименованиеСклада = Выборка.НаименованиеСклада;
			НоваяСтрока.ИспользуетсяДляСхемыРаботыFBO = Выборка.ИспользуетсяДляСхемыРаботыFBO;
			НоваяСтрока.ИспользуетсяДляСхемыРаботыFBS = Выборка.ИспользуетсяДляСхемыРаботыFBS;
			НоваяСтрока.ИспользуетсяДляСхемыРаботыDBS = Выборка.ИспользуетсяДляСхемыРаботыDBS;
			
			ИдентификаторыСкладов.Добавить(Выборка.ИдентификаторСклада);
		КонецЕсли;
	КонецЦикла;

	Если ТолькоСсылки Тогда
		СопоставленныеСклады.Свернуть("Склад");
		Возврат СопоставленныеСклады.ВыгрузитьКолонку("Склад");
	Иначе
		СопоставленныеСклады.Свернуть("Группа, Склад, ИдентификаторСклада, НаименованиеСклада, ИспользуетсяДляСхемыРаботыFBO, ИспользуетсяДляСхемыРаботыFBS, ИспользуетсяДляСхемыРаботыDBS, ЗаполненыНастройкиСклада, НастройкиСклада");

		НастройкиСкладов = НастройкиСкладов(УчетнаяЗаписьМаркетплейса, ИдентификаторыСкладов);
		Для Каждого НастройкиИдентификатораСклада Из НастройкиСкладов Цикл
			НастройкиСклада = НастройкиИдентификатораСклада.Значение;
			ЗаполненыНастройкиСклада = НастройкиСклада.ЗаполненыНастройкиСклада;
			НастройкиСклада.Удалить("ЗаполненыНастройкиСклада");
			
			Отбор = Новый Структура("ИдентификаторСклада", НастройкиИдентификатораСклада.Ключ);
			НайденныеСтроки = СопоставленныеСклады.НайтиСтроки(Отбор);
			
			Для Каждого ЭлементКоллекции Из НайденныеСтроки Цикл
				ЭлементКоллекции.ЗаполненыНастройкиСклада = ЗаполненыНастройкиСклада;
				ЭлементКоллекции.НастройкиСклада          = НастройкиСклада;
			КонецЦикла;
		КонецЦикла;

		Возврат СопоставленныеСклады;
	КонецЕсли;

КонецФункции

// Возвращает сохраненные настройки складов торговой площадки.
//
// Параметры:
//   УчетнаяЗапись         - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись.
//   ИдентификаторыСкладов - Массив из Строка - идентификаторы складов торговой площадки.
//
// Возвращаемое значение:
//   Соответствие из Строка - список складов со списком реквизитов с их значениями.
//
Функция НастройкиСкладов(УчетнаяЗапись, Знач ИдентификаторыСкладов) Экспорт

	НастройкиСкладов      = Новый Соответствие;
	ИменаРеквизитов       = РеквизитыНастроекСкладаFBS();
	ИдентификаторыСкладов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ИдентификаторыСкладов);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СоответствияОбъектовМаркетплейсов.ИдентификаторВладельцаОбъектаМаркетплейса КАК ИдентификаторСклада,
		|	СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса КАК ИмяРеквизита,
		|	СоответствияОбъектовМаркетплейсов.Объект1С КАК Значение,
		|	СоответствияОбъектовМаркетплейсов.ДополнительныеСведения КАК ЗначениеСтрока,
		|	СоответствияОбъектовМаркетплейсов.ДатаАктуальности КАК ДатаАктуальности
		|ИЗ
		|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
		|ГДЕ
		|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись
		|	И СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Склад)
		|	И СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса В(&ИменаРеквизитов)
		|	И СоответствияОбъектовМаркетплейсов.ИдентификаторВладельцаОбъектаМаркетплейса В(&ИдентификаторыСкладов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса,
		|	""Склад"",
		|	СоответствияОбъектовМаркетплейсов.Объект1С,
		|	СоответствияОбъектовМаркетплейсов.ДополнительныеСведения,
		|	СоответствияОбъектовМаркетплейсов.ДатаАктуальности
		|ИЗ
		|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
		|ГДЕ
		|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись
		|	И СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Склад)
		|	И СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса В(&ИдентификаторыСкладов)
		|ИТОГИ ПО
		|	ИдентификаторСклада";
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",         УчетнаяЗапись);
	Запрос.УстановитьПараметр("ИменаРеквизитов",       ИменаРеквизитов);
	Запрос.УстановитьПараметр("ИдентификаторыСкладов", ИдентификаторыСкладов);
	
	ВыборкаСкладов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСкладов.Следующий() Цикл
		НастройкиСклада = НовыйНастройкиСкладаFBS();
		
		ВыборкаНастроек = ВыборкаСкладов.Выбрать();
		Пока ВыборкаНастроек.Следующий() Цикл
			НастройкиСклада.ДатаАктуальности = Макс(НастройкиСклада.ДатаАктуальности, ВыборкаНастроек.ДатаАктуальности);
			
			НастройкиСклада[ВыборкаНастроек.ИмяРеквизита] =
				?(ВыборкаНастроек.Значение = "ДополнительныеСведения",
				  ВыборкаНастроек.ЗначениеСтрока,
				  ВыборкаНастроек.Значение);
			
			Если ЗначениеЗаполнено(ВыборкаНастроек.ИмяРеквизита) Тогда
				НастройкиСклада.ЗаполненыНастройкиСклада = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой") Тогда
			НастройкиСпособаДоставки = НовыйНастройкиСкладаFBS(, Истина);
			
			ЗаполнитьЗначенияСвойств(НастройкиСклада, НастройкиСпособаДоставки);
		КонецЕсли;
		
		НастройкиСклада.ПредставлениеСпособаДоставки =
			Перечисления.СпособыДоставки.ПредставлениеСпособаДоставки(НастройкиСклада.СпособДоставки);
		
		НастройкиСкладов.Вставить(ВыборкаСкладов.ИдентификаторСклада, НастройкиСклада);
		
		ИндексСклада = ИдентификаторыСкладов.Найти(ВыборкаСкладов.ИдентификаторСклада);
		Если ИндексСклада <> Неопределено Тогда
			ИдентификаторыСкладов.Удалить(ИндексСклада);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ИдентификаторСклада Из ИдентификаторыСкладов Цикл
		НастройкиСклада = НовыйНастройкиСкладаFBS();
		
		НастройкиСкладов.Вставить(ИдентификаторСклада, НастройкиСклада);
	КонецЦикла;
	
	Возврат НастройкиСкладов;

КонецФункции

// Возвращает сохраненные настройки склада торговой площадки.
//
// Параметры:
//   УчетнаяЗапись       - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись.
//   ИдентификаторСклада - Строка - идентификатор склада торговой площадки.
//
// Возвращаемое значение:
//   Произвольный - настройки склада.
//   см. НовыйНастройкиСкладаFBS.
//
Функция НастройкиСклада(УчетнаяЗапись, ИдентификаторСклада) Экспорт

	ИдентификаторыСкладов = Новый Массив;
	ИдентификаторыСкладов.Добавить(ИдентификаторСклада);
	
	НастройкиСкладов = НастройкиСкладов(УчетнаяЗапись, ИдентификаторыСкладов);
	
	НастройкиСклада = НастройкиСкладов[ИдентификаторСклада];
	
	Если НастройкиСклада = Неопределено Тогда
		НастройкиСклада = НовыйНастройкиСкладаFBS();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НастройкиСклада.СпособДоставки) Тогда
		НастройкиСклада.СпособДоставки = Перечисления.СпособыДоставки.Самовывоз;
		НастройкиСклада.ПредставлениеСпособаДоставки =
			Перечисления.СпособыДоставки.ПредставлениеСпособаДоставки(НастройкиСклада.СпособДоставки);
	КонецЕсли;
	
	Возврат НастройкиСклада;

КонецФункции

// Сохраняет сохраненные настройки склада торговой площадки и возвращает результат.
//
// Параметры:
//   УчетнаяЗапись       - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись.
//   ИдентификаторСклада - Строка - идентификатор склада торговой площадки.
//   ИсточникНастроек    - Форма  - форма, из которой инициирована запись.
//
// Возвращаемое значение:
//   Структура - результат выполнения:
//     * Выполнено      - Булево - признак успешности сохранения настроек;
//     * ОписаниеОшибки - Строка - описание ошибки.
//
Функция СохранитьНастройкиСклада(УчетнаяЗапись, ИдентификаторСклада, ИсточникНастроек) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("Выполнено", Ложь);
	Результат.Вставить("ОписаниеОшибки", "");

	ИменаРеквизитов = РеквизитыНастроекСкладаFBS();

	ВидОбъектаСклад = Перечисления.ВидыОбъектовМаркетплейсов.Склад;

	НачатьТранзакцию();
	Попытка
		УстановитьПривилегированныйРежим(Истина);
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СоответствияОбъектовМаркетплейсов");
		ЭлементБлокировкиДанных.УстановитьЗначение("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
		ЭлементБлокировкиДанных.УстановитьЗначение("ВидОбъектаМаркетплейса", ВидОбъектаСклад);
		ЭлементБлокировкиДанных.УстановитьЗначение("ИдентификаторВладельцаОбъектаМаркетплейса", ИдентификаторСклада);
		БлокировкаДанных.Заблокировать();

		НаборЗаписей = РегистрыСведений.СоответствияОбъектовМаркетплейсов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УчетнаяЗаписьМаркетплейса.Установить(УчетнаяЗапись);
		НаборЗаписей.Отбор.ВидОбъектаМаркетплейса.Установить(ВидОбъектаСклад);
		НаборЗаписей.Отбор.ИдентификаторВладельцаОбъектаМаркетплейса.Установить(ИдентификаторСклада);

		Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
			Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ИсточникНастроек, ИмяРеквизита) Тогда
				Продолжить;
			КонецЕсли;
			
			ЗначениеРеквизита = ИсточникНастроек[ИмяРеквизита];
			
			Если Не ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
				Продолжить;
			КонецЕсли;
			
			Запись = НаборЗаписей.Добавить();
			Запись.УчетнаяЗаписьМаркетплейса = УчетнаяЗапись;
			Запись.ВидОбъектаМаркетплейса = ВидОбъектаСклад;
			Запись.ИдентификаторОбъектаМаркетплейса = ИмяРеквизита;
			Запись.ИдентификаторВладельцаОбъектаМаркетплейса = ИдентификаторСклада;

			Если ТипЗнч(ЗначениеРеквизита) = Тип("Строка") И СтрДлина(ЗначениеРеквизита) > 150 Тогда
				Запись.Объект1С = "ДополнительныеСведения";
				Запись.ДополнительныеСведения = ЗначениеРеквизита;
			Иначе
				Запись.Объект1С = ЗначениеРеквизита;
			КонецЕсли;

			Запись.ДатаАктуальности = ТекущаяДатаСеанса();
		КонецЦикла;

		НаборЗаписей.Записать();

		УстановитьПривилегированныйРежим(Ложь);

		Результат.Выполнено = Истина;

		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();

		ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");

		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(ВидМаркетплейса),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);

		Результат.ОписаниеОшибки = ТекстОшибки;
	КонецПопытки;

	Возврат Результат;

КонецФункции

// Формирует представление склада торговой площадки.
//
// Параметры:
//   Наименование - Строка - наименование склада.
//   СхемаРаботы  - Строка - представление схемы работы.
//   Статус       - Строка - представление статуса склада.
//   ДоверительнаяПриемка - Булево - признак доверительной приемки;
//
// Возвращаемое значение:
//   Строка - представление склада.
//
Функция ПредставлениеСклада(Наименование, СхемаРаботы, Статус, ДоверительнаяПриемка = Ложь) Экспорт

	ПредставлениеСклада = Наименование
							+ " (" + СхемаРаботы + ")"
							+ ?(ДоверительнаяПриемка, ", " + Нстр("ru = 'С доверительной приемкой';
																	|en = 'With trust receipt'"), "")
							+ ", " + Статус;
	
	Возврат ПредставлениеСклада;

КонецФункции

Процедура НастройкиПрайсЛиста(Форма, ПараметрыДляНастройки) Экспорт
	
	// Установка отборов
	ОтборФормы = Форма.КомпоновщикНастроекОтбор.Настройки.Отбор; // ОтборКомпоновкиДанных
	ОтборФормы.Элементы.Очистить();
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ОтборФормы,
		"Номенклатура",
		ВидСравненияКомпоновкиДанных.ВСписке,
		ПараметрыДляНастройки.СписокНоменклатуры,
		Неопределено,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	
	УстановкаЦенСервер.НастроитьЗаголовокОтбора(Форма);
	УстановкаЦенСервер.ИнициализироватьВыбранныеЦены(Форма, ПараметрыДляНастройки.СписокВидовЦен);
	
	Для Каждого ВыбраннаяЦена Из Форма.ВыбранныеЦены Цикл // СтрокаТаблицыЗначений
	
		ВыбраннаяЦена.РеквизитДопУпорядочивания = 0;
		
		Если ВыбраннаяЦена.ЗапрещенныйВидЦены Тогда
			Продолжить;
		КонецЕсли;
		
		НайденныйВидЦен = ПараметрыДляНастройки.СписокВидовЦен.НайтиПоЗначению(ВыбраннаяЦена.Ссылка);
	
		Если НайденныйВидЦен <> Неопределено Тогда
			ВыбраннаяЦена.Выбрана = Истина;
			ВыбраннаяЦена.Редактировать = Истина;
			ВыбраннаяЦена.РеквизитДопУпорядочивания = ПараметрыДляНастройки.СписокВидовЦен.Индекс(НайденныйВидЦен) + 1;
		Иначе
			ВыбраннаяЦена.Выбрана = Ложь;
			ВыбраннаяЦена.Редактировать = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	УстановкаЦенКлиентСервер.ПроставитьФлагиВлияетЗависитНаКлиенте(Форма);
	
КонецПроцедуры

#КонецОбласти

#Область ИмпортТоваровИзСервиса

// Конструктор параметров для проверки необходимости запуска импорта товарного каталога.
//
// Возвращаемое значение:
//  Структура:
//    * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//    * КлючЗадания - см. ИнтеграцияСМаркетплейсамиКлиентСервер.КлючЗаданияИмпортаТоварногоКаталога
//
Функция НовыеПараметрыПроверкиНаличияДанныхИЗапускаИмпортаКаталога() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("УчетнаяЗапись", Справочники.УчетныеЗаписиМаркетплейсов.ПустаяСсылка());
	Результат.Вставить("КлючЗадания",   "");
	
	Возврат Результат;
	
КонецФункции

// Возвращает результат проверки необходимости импорта.
//
// Параметры:
//   Параметры - см. НовыеПараметрыПроверкиНаличияДанныхИЗапускаИмпортаКаталога
//
// Возвращаемое значение:
//   Структура - результат проверки:
//     * ИспользоватьНоменклатуруПартнеров - Булево - признак использования номенклатуры партнеров.
//     * Партнер                           - СправочникСсылка.Партнеры - партнер,
//                                             представляющий торговую площадку.
//     * ДанныеСуществуют                  - Булево - признак наличия данных в товарном каталоге.
//     * ТребуютСопоставления              - Булево - признак наличия требующих сопоставления данных
//                                             в товарном каталоге.
//     * РучнойИмпортРегламентнымЗаданием  - Булево - признак необходимости запуска ручного импорта
//                                             с помощью регламентного задания.
//     * ЗаданиеАктивно                    - Булево - признак активности фонового задания.
//     * ЗаданиеЗавершено                  - Дата   - дата завершения последнего фонового задания.
//
Функция ПроверитьНаличиеДанныхИЗапускИмпортаКаталога(Параметры) Экспорт

	РеквизитыУчетнойЗаписи =
		ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.УчетнаяЗапись, "ВидМаркетплейса, Партнер");
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользоватьНоменклатуруПартнеров", ПолучитьФункциональнуюОпцию("ИспользоватьНоменклатуруПартнеров"));
	Результат.Вставить("Партнер",                           РеквизитыУчетнойЗаписи.Партнер);
	Результат.Вставить("ДанныеСуществуют",                  Истина);
	Результат.Вставить("ТребуютСопоставления",              Ложь);
	Результат.Вставить("РучнойИмпортРегламентнымЗаданием",  Ложь);
	Результат.Вставить("ЗаданиеАктивно",                    Ложь);
	Результат.Вставить("ЗаданиеЗавершено",                  Дата(1, 1, 1));
	
	ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СтатусыПубликации.УчетнаяЗапись КАК УчетнаяЗапись
		|ИЗ
		|	&Регистр КАК СтатусыПубликации
		|ГДЕ
		|	СтатусыПубликации.УчетнаяЗапись = &УчетнаяЗапись
		|	И &Условие";
	
	ВидМаркетплейса = РеквизитыУчетнойЗаписи.ВидМаркетплейса;
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Регистр", "РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет");
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Регистр", "РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СтатусыПубликации.УчетнаяЗапись", "СтатусыПубликации.УчетнаяЗаписьМаркетплейса");
	Иначе
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("УчетнаяЗапись", Параметры.УчетнаяЗапись);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос.Текст = СтрЗаменить(ТекстЗапроса,
		"&Условие",
		"СтатусыПубликации.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)");
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Результат.ДанныеСуществуют = Ложь;
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(ТекстЗапроса,
		"&Условие",
		"СтатусыПубликации.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)");
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Результат.ТребуютСопоставления = Истина;
	КонецЕсли;
	
	ДанныеЗапуска = ДанныеЗапускаИмпортаТоварногоКаталога(Параметры.КлючЗадания);
	
	Результат.ЗаданиеЗавершено                  = ДанныеЗапуска.ДатаЗавершения;
	Результат.РучнойИмпортРегламентнымЗаданием = 
		Не РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована()
			И ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	ИмяРегламентногоЗадания = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяРегламентногоЗаданияИмпортаТоварногоКаталога(
		ВидМаркетплейса);
	
	МетаданныеЗадания = Неопределено;
	
	Если Не ПустаяСтрока(ИмяРегламентногоЗадания) Тогда
		МетаданныеЗадания = Метаданные.РегламентныеЗадания[ИмяРегламентногоЗадания];
	Иначе
		Результат.РучнойИмпортРегламентнымЗаданием = Ложь;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Ключ",      Параметры.КлючЗадания);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	Если Результат.РучнойИмпортРегламентнымЗаданием Тогда
		Отбор.Вставить("ИмяМетода", МетаданныеЗадания.ИмяМетода);
	КонецЕсли;
	
	Задания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Если Результат.РучнойИмпортРегламентнымЗаданием И Задания.Количество() = 0 Тогда
		
		Отбор.Ключ = ИнтеграцияСМаркетплейсамиКлиентСервер.КлючЗаданияРучногоИмпортаТоварногоКаталога(
			Параметры.КлючЗадания);
		
		Задания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
		
	КонецЕсли;
	
	Если Задания.Количество() > 0 Тогда
		Результат.ЗаданиеАктивно = Истина;
	КонецЕсли;
	
	Если Результат.РучнойИмпортРегламентнымЗаданием И Не Результат.ЗаданиеАктивно Тогда
		
		Результат.ЗаданиеАктивно = ЕстьВключенноеРегламентноеЗаданиеРучногоИмпортаТоварногоКаталога(
			МетаданныеЗадания, Отбор.Ключ, ДанныеЗапуска);
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;

КонецФункции

// Читает сохраненные данные о запуске импорта товаров из сервиса.
//
// Параметры:
//  КлючНастроек - Строка - ключ регламентного задания импорта товарного каталога.
//
// Возвращаемое значение:
//   Структура:
//    * ДатаСозданияРучногоИмпортаТоварногоКаталога - Дата - дата создания регламентного задания по ручному запуску
//                                                    импорта товарного каталога из сервиса.
//    * ДатаЗавершения - Дата - дата последнего успешного завершения импорта.
//
Функция ДанныеЗапускаИмпортаТоварногоКаталога(КлючНастроек) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Структура;
	
	Результат.Вставить("ДатаСозданияРучногоИмпортаТоварногоКаталога", Дата(1, 1, 1));
	Результат.Вставить("ДатаЗавершения",                              Дата(1, 1, 1));
	
	СохраненныеНастройки = Новый Структура;
	
	СохраненныеНастройки = ОбщегоНазначения.ХранилищеСистемныхНастроекЗагрузить(
		ИнтеграцияСМаркетплейсамиКлиентСервер.КлючОбъектаДанныхЗапускаИмпортаТоварногоКаталога(),
		КлючНастроек, 
		СохраненныеНастройки, ,
		"");
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ЗаполнитьЗначенияСвойств(Результат, СохраненныеНастройки);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Отправления

// Конструктор параметров замены номеров отправлений по заказу.
// 
// Возвращаемое значение:
//  Структура:
//   * ИмяРегистра - Строка - полное имя регистра сведений, в котором необходимо заменить номер отправления.
//                   Поддерживаются ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок и ЗаказыТорговыхПлощадок.
//   * ИмяСобытияЖурналаРегистрации - Строка - имя события, с которым будет произведена запись в журнал регистрации.
//   * Заказ - ДокументСсылка.ЗаказКлиента - заказ клиента, номера отправлений которого нужно изменить.
//   * ДанныеДляЗамены - Соответствие из КлючИЗначение:
//      ** Ключ - Строка - старый номер отправления.
//      ** Значение - см. НовыеДанныеЗаписиДляЗаменыНомераОтправленияПоЗаказу
//
Функция НовыеПараметрыЗаменыНомеровОтправленийПоЗаказу() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("ИмяРегистра", "");
	Результат.Вставить("ИмяСобытияЖурналаРегистрации", "");
	Результат.Вставить("Заказ", Документы.ЗаказКлиента.ПустаяСсылка());
	Результат.Вставить("ДанныеДляЗамены", Новый Соответствие);
	
	Возврат Результат;
	
КонецФункции

// Конструктор данных записи для замены номера отправления по заказу.
// 
// Возвращаемое значение:
//  Структура:
//   * НомерОтправления - Строка - новый номер отправления из сервиса.
//   * ИдентификаторОтправления - Строка - идентификатор отправления из сервиса.
//   * ИдентификаторОтгрузки - Строка - идентификатор отгрузки из сервиса.
//   * Статус - ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - новый статус заказа.
//   * ДатаУстановкиСтатуса - Дата - дата установки или изменения статуса заказа. Используется, если передан статус.
//   * ТребуетсяПолучениеЭтикеток - Булево - признак необходимости получения этикеток по номеру отправления.
//                                  Используется, если передан статус.
//
Функция НовыеДанныеЗаписиДляЗаменыНомераОтправленияПоЗаказу() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("НомерОтправления", "");
	Результат.Вставить("ИдентификаторОтправления", "");
	Результат.Вставить("ИдентификаторОтгрузки", "");
	Результат.Вставить("Статус", Перечисления.СтатусыЗаказовТорговыхПлощадок.ПустаяСсылка());
	Результат.Вставить("ДатаУстановкиСтатуса", Дата(1, 1, 1));
	Результат.Вставить("ТребуетсяПолучениеЭтикеток", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Заменяет номера отправлений для заказа после подтверждения сборки выделенных товаров.
// 
// Параметры:
//  ПараметрыЗамены - см. НовыеПараметрыЗаменыНомеровОтправленийПоЗаказу
// 
// Возвращаемое значение:
//  Строка - текст ошибки.
//
Функция ЗаменитьНомераОтправленийПоЗаказу(ПараметрыЗамены) Экспорт
	
	ТекстОшибки = "";
	
	НомераОтправлений = Новый Массив; // Массив из Строка
	Для Каждого КлючЗначение Из ПараметрыЗамены.ДанныеДляЗамены Цикл
		НомераОтправлений.Добавить(КлючЗначение.Ключ);
	КонецЦикла;
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(ПараметрыЗамены.ИмяРегистра);
		ЭлементБлокировки.УстановитьЗначение("Заказ", ПараметрыЗамены.Заказ);

		Блокировка.Заблокировать();
		
		Выборка = ВыборкаДанныхДляЗаменыНомеровОтправленийПоЗаказу(
			ПараметрыЗамены.ИмяРегистра, ПараметрыЗамены.Заказ, НомераОтправлений);
		
		МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПараметрыЗамены.ИмяРегистра);
		НаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();
		
		Пока Выборка.Следующий() Цикл
			
			ИзменятьНомерРодительскогоОтправления = Ложь;
			
			НовыеДанныеЗаписи = ПараметрыЗамены.ДанныеДляЗамены.Получить(Выборка.СтарыйНомерОтправления);
			НовыеДанныеЗаписиРодителя = ПараметрыЗамены.ДанныеДляЗамены.Получить(Выборка.НомерРодительскогоОтправления);
			
			Если НовыеДанныеЗаписи = Неопределено Или НовыеДанныеЗаписиРодителя <> Неопределено Тогда
				ИзменятьНомерРодительскогоОтправления = Истина;
			КонецЕсли;
			
			ДанныеЗаполнения = ДанныеЗаполненияПоНовымДаннымЗаписи(
				НовыеДанныеЗаписи,
				(Выборка.СтарыйНомерОтправления = Выборка.НомерРодительскогоОтправления));
			
			Если ИзменятьНомерРодительскогоОтправления Тогда
				ДанныеЗаполнения.Вставить("НомерРодительскогоОтправления", НовыеДанныеЗаписиРодителя.НомерОтправления);
			КонецЕсли;
			
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ДанныеЗаполнения);
			
		КонецЦикла;
		
		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей.Записать(РежимЗамещения.Обновление);
		КонецЕсли;
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось изменить номера отправлений для заказа <%1> по причине: %2.';
				|en = 'Cannot change the shipment numbers for the <%1> order. Reason: %2.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Строка(ПараметрыЗамены.Заказ),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		ЗаписьЖурналаРегистрации(
			ПараметрыЗамены.ИмяСобытияЖурналаРегистрации,
			УровеньЖурналаРегистрации.Ошибка,
			ПараметрыЗамены.Заказ.Метаданные(),
			ПараметрыЗамены.Заказ,
			ТекстОшибки);
	КонецПопытки;
	
	Возврат ТекстОшибки;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыгрузкаЗагрузкаДанныхСлужебныеПроцедурыИФункции

// Конструктор определения используемых методов. Заполняется в модуле менеджера объекта метаданных.
// Описание синтаксиса процедур / функций см. в ИспользуемыеМетодыМенеджераОбъектаМетаданных().
//
// Возвращаемое значение:
//   Структура - список параметров:
//     * ВыполнитьПроверкуИнформацииПоКолонкам                    - Булево - признак наличия функции.
//            Используется для проверки наличия различий в сохраненных пользователем настройках по колонкам
//            и базовых настройках, заданных на этапе разработки, с целью восстановления стандартных настроек
//            при наличии расхождений.
//              С параметрами:
//                ИнформацияПоКолонкам - см. ИнициализироватьТаблицуИнформацииПоКолонкам.
//                ИмяТабличнойЧасти    - Строка - имя загружаемой табличной части объекта метаданных,
//                                         например: "ДанныеПоставок".
//              С возвращаемым значением:
//                Булево - результат проверки.
//     * УстановитьПараметрыЗагрузкиИзФайлаВТабличнуюЧасть        - Булево - признак наличия процедуры.
//            Используется для заполнения основных настроек.
//              С параметрами:
//              Параметры - смотри ПараметрыЗагрузкиИзФайлаВТабличнуюЧасть.
//     * ДополнитьУсловноеОформлениеТаблицыСопоставленияДанных    - Булево - признак наличия процедуры.
//            Используется для добавления условного оформления в форму.
//              С параметрами:
//                Форма             - ФормаКлиентскогоПриложения - форма, для которой необходимо дополнить
//                                      условное оформление.
//                ИмяТабличнойЧасти - Строка - имя загружаемой табличной части объекта метаданных,
//                                      например: "ДанныеПоставок".
//     * НастроитьДинамическийСписокОтображенияРезультатаЗагрузки - Булево - признак наличия процедуры.
//            Используется изменения текста запроса динамического списка и заполнения его свойств и параметров.
//              С параметрами:
//                СвойстваДинамическогоСписка - Структура - см. ОбщегоНазначения.СтруктураСвойствДинамическогоСписка.
//                ИмяТабличнойЧасти - Строка - имя загружаемой табличной части объекта метаданных,
//                                      например: "ДанныеПоставок".
//     * ПолучитьДанныеИнтеграционнымиМетодами                    - Булево - признак наличия функции.
//            Используется для получения данных по API.
//              С параметрами:
//                Параметры                  - см. ПараметрыЗагрузкиИзФайлаВТабличнуюЧасть.
//              С возвращаемым значением:
//                ТаблицаЗначений - таблица результата сопоставления.
//     * СопоставитьЗагружаемыеДанные                             - Булево - признак наличия функции.
//            Используется для сопоставления данных, загружаемых в табличную часть объекта метаданных, с данными в ИБ.
//              С параметрами:
//                ТаблицаСопоставленияДанных - ТаблицаЗначений - динамически подготовленная таблица для заполнения
//                                               загруженных сопоставленных данных.
//                ДанныеИзФайла              - ТаблицаЗначений - динамически подготовленная таблица с загруженными данными.
//                Параметры                  - см. ПараметрыЗагрузкиИзФайлаВТабличнуюЧасть.
//              С возвращаемым значением:
//                ТаблицаЗначений - таблица результата сопоставления.
//     * ЗаписатьЗагружаемыеДанные                                - Булево - признак наличия процедуры.
//            Используется для записи загружаемых данных в учетную систему 1С.
//              С параметрами:
//                ТаблицаСопоставленияДанных - ТаблицаЗначений - динамически подготовленная таблица для заполнения
//                                               загруженных сопоставленных данных.
//                Параметры                  - Структура - параметры, переданные из формы-источника.
//                СсылкаДляОбновленияДанных  - ДокументСсылка, Неопределено - ссылка для заполнения загружаемыми данными.
//              С возвращаемым значением:
//                Структура - смотри РезультатЗаписиЗагружаемыхДанных.
//     * НаименованиеСохраняемогоФайла                            - Булево - признак наличия функции.
//            Используется для формирования наименования присоединенного файла в зависимости от типа ссылки.
//              С параметрами:
//                СсылкаНаОбъект   - ДокументСсылка - ссылка на объект, для которого нужно получить наименование файла.
//                ДляВыгрузкиВФайл - Булево - признак формирования наименования файла для сохранения
//                                     в файловую систему пользователя.
//              С возвращаемым значением:
//                Строка - наименование файла.
//     * ЗаполнитьШапкуМакета                                     - Булево - признак наличия процедуры.
//            Используется для заполнения шапки выгружаемого табличного документа.
//            См. СформироватьТабличныйДокументПоИмениМакетаСШаблоном.
//     * ЗаполнитьЗаголовокТаблицыМакета                          - Булево - признак наличия процедуры.
//            Используется для заполнения заголовка таблицы выгружаемого табличного документа.
//            См. СформироватьТабличныйДокументПоИмениМакетаСШаблоном.
//     * ПолучитьДанныеСтрокТаблицыМакета                         - Булево - признак наличия функции.
//            Используется для подготовки коллекции данных по строкам таблицы выгружаемого табличного документа.
//            См. СформироватьТабличныйДокументПоИмениМакетаСШаблоном.
//     * ВывестиТаблицуМакета                                     - Булево - признак наличия процедуры.
//            Используется для вывода таблицы макета выгружаемого табличного документа,
//            если подготовки данных таблицы и стандартного вывода не достаточно.
//            См. СформироватьТабличныйДокументПоИмениМакетаСШаблоном.
//     * ЗаполнитьПодвалМакета                                    - Булево - признак наличия процедуры.
//            Используется для заполнения подвала выгружаемого табличного документа.
//            См. СформироватьТабличныйДокументПоИмениМакетаСШаблоном.
//
Функция ИспользуемыеМетодыМенеджераОбъектаМетаданных() Экспорт

	ИспользуемыеМетоды = Новый Структура;

	// Основные методы.
	ИспользуемыеМетоды.Вставить("ВыполнитьПроверкуИнформацииПоКолонкам",                    Ложь);
	ИспользуемыеМетоды.Вставить("УстановитьПараметрыЗагрузкиИзФайлаВТабличнуюЧасть",        Ложь);
	ИспользуемыеМетоды.Вставить("ДополнитьУсловноеОформлениеТаблицыСопоставленияДанных",    Ложь);
	ИспользуемыеМетоды.Вставить("НастроитьДинамическийСписокОтображенияРезультатаЗагрузки", Ложь);
	ИспользуемыеМетоды.Вставить("ПолучитьДанныеИнтеграционнымиМетодами",                    Ложь);
	ИспользуемыеМетоды.Вставить("СопоставитьЗагружаемыеДанные",                             Ложь);
	ИспользуемыеМетоды.Вставить("ЗаписатьЗагружаемыеДанные",                                Ложь);

	// Выгрузка в файл - см. СформироватьТабличныйДокументПоИмениМакетаСШаблоном.
	ИспользуемыеМетоды.Вставить("НаименованиеСохраняемогоФайла",                            Ложь);
	ИспользуемыеМетоды.Вставить("ЗаполнитьШапкуМакета",                                     Ложь);
	ИспользуемыеМетоды.Вставить("ЗаполнитьЗаголовокТаблицыМакета",                          Ложь);
	ИспользуемыеМетоды.Вставить("ПолучитьДанныеСтрокТаблицыМакета",                         Ложь);
	ИспользуемыеМетоды.Вставить("ВывестиТаблицуМакета",                                     Ложь);
	ИспользуемыеМетоды.Вставить("ЗаполнитьПодвалМакета",                                    Ложь);

	Возврат ИспользуемыеМетоды;

КонецФункции

// Возвращает признак использования запрашиваемого метода.
//
// Параметры:
//   ИмяОбъектаМетаданных - Строка - полное имя объекта метаданных.
//   ИмяМетода            - Строка - имя проверяемого метода.
//
// Возвращаемое значение:
//   Булево.
//
Функция МетодКонфигурацииИспользуется(ИмяОбъектаМетаданных, ИмяМетода) Экспорт

	МетодИспользуется = Ложь;

	МенеджерОбъекта = МенеджерОбъекта(ИмяОбъектаМетаданных);
	Методы = МенеджерОбъекта.ОпределитьИспользуемыеМетоды();

	Попытка
		МетодИспользуется = Методы[ИмяМетода];
	Исключение
		МетодИспользуется = Ложь;
	КонецПопытки;

	Возврат МетодИспользуется;

КонецФункции

// Конструктор параметров шапки макета.
//
// Возвращаемое значение:
//   Структура:
//     * Параметры - Структура - данные по параметрам, где ключ - имя параметра, значение - значение параметра.
//     * Шаблоны   - Массив из см. ДанныеШаблона - данные по каждому шаблону в шапке макета.
//
//@skip-check function-return-types
Функция ПараметрыШапки() Экспорт

	ПараметрыШапки = Новый Структура;
	ПараметрыШапки.Вставить("Параметры", Новый Структура);
	ПараметрыШапки.Вставить("Шаблоны",   Новый Массив);

	Возврат ПараметрыШапки;

КонецФункции

// Конструктор заполняемых данных шаблона.
//
// Возвращаемое значение:
//   Структура:
//     * Шаблон               - Строка - содержимое ячейки шапки макета.
//     * КлючевыеСловаШаблона - Массив Из Строка - слова, заключенные в эталонном макете объекта метаданных в фигурные 
//                                скобки, например {отчет}. Состав этих слов должен полностью совпадать в загружаемом 
//                                макете с эталонным.
//     * ПараметрыШаблона     - Массив Из Строка - слова, заключенные в эталонном макете объекта метаданных в фигурные 
//                                скобки, например: [НачалоПериода]. Параметры определяются в загружаемом макете в 
//                                порядке расположения между ключевыми словами.
//     * ШиринаОбластиПоиска  - Число - количество ячеек по горизонтали.
//     * ВысотаОбластиПоиска  - Число - количество ячеек по вертикали.
//
Функция ДанныеШаблона()

	ДанныеШаблона = Новый Структура;
	ДанныеШаблона.Вставить("Шаблон",               "");
	ДанныеШаблона.Вставить("КлючевыеСловаШаблона", Новый Массив);
	ДанныеШаблона.Вставить("ПараметрыШаблона",     Новый Массив);
	ДанныеШаблона.Вставить("ШиринаОбластиПоиска",  0); // Сколько колонок нужно читать для шаблона.
	ДанныеШаблона.Вставить("ВысотаОбластиПоиска",  0); // Сколько строк нужно читать для шаблона.

	Возврат ДанныеШаблона;

КонецФункции

// Конструктор структуры параметров для формы Справочник.УчетныеЗаписиМаркетплейсов.Форма.ВыгрузкаЗагрузкаДанных.
//
// Возвращаемое значение:
//   Структура - список параметров:
//    * ИмяОбъектаМетаданных - Строка - имя объекта метаданных, из которого берутся данные для выгрузки/загрузки.
//    * ИмяТабличнойЧасти - Строка - имя таблицы-источника колонок из указанного объекта метаданных.
//    * ИмяМакетаСШаблоном - Строка - имя макета-источника данных, ожидаемых при загрузке.
//    * ОбязательныеКолонки - Массив из Строка - имена колонок, заполнение которых проверяется.
//          Строки с незаполненными значениями будут пропущены при записи в ИБ.
//    * ЗаполняемыеПриЗагрузкеКолонки - Соответствие Из Строка - ключом является имя колонки, 
//          значением синоним колонки.
//          Добавляются доступные для редактирования пользователем реквизиты из состава объекта метаданных,
//          а также служебные невидимые реквизиты (отсутствующие в реквизитах объекта метаданных),
//          необходимые на этапе сопоставления данных.
//    * ТипыДанныхКолонок - Соответствие из Строка - ключом является имя колонки, значением тип значения колонки.
//          Добавляются типы реквизитов для дальнейшей обработки данных.
//          Если для реквизита тип не добавлен, то по умолчанию используется тип Строка.
//    * ОсновныеКолонки - Массив из Строка - имена колонок, которые можно отфильтровать вариантом "Основные".
//    * ПустыеВИтогахКолонки - Массив из Строка - имена колонок для определения строки итогов.
//          Для указанных колонок в строке итогов всегда не заполнены значения.
//    * КомандаИзменитьПоведениеЭлементовФормы - Строка - имя команды изменения поведения элементов формы.
//          Например: КомандаИзменитьПоведениеЭлементовФормы =
//                         "Обработки.УправлениеПродажамиНаOzon.Подключаемый_ИзменитьПоведениеЭлементовФормы".
//    * ПодключаемыеОбработчикиСобытий - Структура из КлючИЗначение - в ключе указывается имя поля и события, 
//          а в значении путь к процедуре/функции, выполняемой на сервере.
//          Доступны события формы Справочник.УчетныеЗаписиМаркетплейсов.Форма.ВыгрузкаЗагрузкаДанных:
//                 Подключаемый_ТаблицаСопоставленияДанныхПолеПриИзменении - "ПриИзменении",
//                 Подключаемый_ТаблицаСопоставленияДанныхПолеНачалоВыбора - "ПараметрыНачалаВыбора",
//                 ОбработкаОповещения для имени события "ВыгрузкаЗагрузкаДанных_ОбработатьОповещениеНаСервере" -
//                        в источнике передается имя метода.
//          Например: ОбработчикиСобытий.Вставить("ИдентификаторПубликацииПриИзменении",
//                    "Обработки.УправлениеПродажамиНаOzon.Подключаемый_ПолеПриИзменении");
//                  ОбработчикиСобытий.Вставить("СкладПараметрыНачалаВыбора",
//                    "Обработки.УправлениеПродажамиНаOzon.Подключаемый_ПолеПараметрыНачалаВыбора");
//                  ОбработчикиСобытий.Вставить("ЗаполнитьСкладыВВыделенныхСтроках",
//                    "Обработки.УправлениеПродажамиНаOzon.Подключаемый_ЗаполнитьСкладыВВыделенныхСтроках"); // Оповещение.
//    * ПодключаемыеКомандыЗаполнения - Структура из КлючИЗначение - в ключе указывается имя подключаемой команды,
//          а в значении структура с ключами с ключами "Представление, Действие, Картинка".
//          В ключе Действие указывается путь к процедуре, выполняемой на клиенте. 
//          Если в ключе Действие ничего не задано, то используется команда формы Подключаемый_ВыполнитьКоманду.
//          Если картинка задана, то она будет отображена для команды.
//          Например: ПодключаемыеКомандыЗаполнения.Вставить("ЗаполнитьСклады",
//                    Новый Структура("Представление, Действие, Картинка", НСтр("ru = 'Заполнить склады'"), ""));.
//    * ДополнительныеПараметры - Структура из КлючИЗначение - произвольный состав дополнительных параметров.
//          Может использоваться в подключаемых алгоритмах.
//    * АдресХранилища - Строка - адрес хранилища, в которое будет помещен результат загрузки.
//    * ВысотаЗаголовкаТаблицыМакетаСШаблоном - Число - высота заголовка таблицы эталонного макета объекта конфигурации.
//          Заполняется программно, по умолчанию 1.
//    * ВысотаЗаголовкаТаблицыЗагружаемогоМакета - Число -высота заголовка загружаемого макета.
//          Заполняется программно, по умолчанию 1,
//          но может быть задана при заполнении параметров формы в алгоритмах подключения.
//          Например: Параметры.ВысотаЗаголовкаТаблицыЗагружаемогоМакета = 3;
//                  (с учетом строки с нумерацией колонок, которой нет в шапке шаблона).
//     * ЛистыШаблона                             - Неопределено - используется одностраничный шаблон.
//                                                - Структура - описание многостраничного шаблона:
//       ** ИмяЛиста                                 - Строка - имя листа.
//       ** ИмяМакетаСШаблоном                       - Строка - имя макета объекта метаданных.
//       ** Служебный                                - Произвольный - дополнительная служебная информация.
//       ** ПараметрыШапки                           - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыШапки.
//       ** ИнформацияПоКолонкам                     - см. ИнтеграцияСМаркетплейсамиСервер.ИнициализироватьТаблицуИнформацииПоКолонкам.
//       ** ВысотаЗаголовкаТаблицыЗагружаемогоМакета - Число - высота заголовка таблицы загружаемого макета.
//       ** ВысотаЗаголовкаТаблицыМакетаСШаблоном    - Число - высота заголовка таблицы эталонного макета объекта конфигурации.
//       ** ШаблонСДанными                           - ТабличныйДокумент - данные в виде табличного документа, которые нужно обработать.
//       ** ЗаполняемыйШаблон                        - Неопределено, ТабличныйДокумент - заполняемый табличный документ.
//
//@skip-check function-return-types
Функция ПараметрыЗагрузкиИзФайлаВТабличнуюЧасть() Экспорт

	ПараметрыПоУмолчанию = Новый Структура;
	ПараметрыПоУмолчанию.Вставить("ИмяОбъектаМетаданных",                     "");
	ПараметрыПоУмолчанию.Вставить("ИмяТабличнойЧасти",                        "");
	ПараметрыПоУмолчанию.Вставить("ИмяМакетаСШаблоном",                       "");
	ПараметрыПоУмолчанию.Вставить("ОбязательныеКолонки",                      Новый Массив);
	ПараметрыПоУмолчанию.Вставить("ПустыеВИтогахКолонки",                     Новый Массив);
	ПараметрыПоУмолчанию.Вставить("ЗаполняемыеПриЗагрузкеКолонки",            Новый Соответствие);
	ПараметрыПоУмолчанию.Вставить("ТипыДанныхКолонок",                        Новый Соответствие);
	ПараметрыПоУмолчанию.Вставить("ОсновныеКолонки",                          Новый Массив);
	ПараметрыПоУмолчанию.Вставить("КомандаИзменитьПоведениеЭлементовФормы",   "");
	ПараметрыПоУмолчанию.Вставить("ПодключаемыеОбработчикиСобытий",           Новый Структура);
	ПараметрыПоУмолчанию.Вставить("ПодключаемыеКомандыЗаполнения",            Новый Структура);
	ПараметрыПоУмолчанию.Вставить("ДополнительныеПараметры",                  Новый Структура);
	ПараметрыПоУмолчанию.Вставить("АдресХранилища",                           "");
	ПараметрыПоУмолчанию.Вставить("ВысотаЗаголовкаТаблицыМакетаСШаблоном",    1);
	ПараметрыПоУмолчанию.Вставить("ВысотаЗаголовкаТаблицыЗагружаемогоМакета", 1);
	ПараметрыПоУмолчанию.Вставить("ЛистыШаблона",                             Неопределено);

	Возврат ПараметрыПоУмолчанию;

КонецФункции

// Создать таблицу со списком неоднозначностей для которых в ИБ имеется несколько подходящих вариантов данных.
// 
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * НомерСтроки - Число  - номер строки, в которой была обнаружена неоднозначность;
//     * Колонка     - Строка - имя колонки, в которой была обнаружена неоднозначность.
//
Функция НовыйСписокНеоднозначностей() Экспорт

	СписокНеоднозначностей = Новый ТаблицаЗначений;
	СписокНеоднозначностей.Колонки.Добавить("НомерСтроки");
	СписокНеоднозначностей.Колонки.Добавить("Колонка");

	Возврат СписокНеоднозначностей;

КонецФункции

// Возвращает доступные состояния документов, отображаемые в списке загруженных документов.
//
// Возвращаемое значение:
//   Структура:
//     * СостояниеНовый            - Строка - документ новый.
//     * СостояниеОбновлен         - Строка - документ переоформлен.
//     * СостояниеНеОбновлен       - Строка - документ не обновлен.
//     * СостояниеОшибкиЗаполнения - Строка - ошибки при заполнении документа.
//
Функция ДоступныеСостоянияДокументов() Экспорт

	ДоступныеСостоянияДокументов = Новый Структура;
	ДоступныеСостоянияДокументов.Вставить("СостояниеНовый",            НСтр("ru = 'Создан новый';
																			|en = 'New is created'"));
	ДоступныеСостоянияДокументов.Вставить("СостояниеОбновлен",         НСтр("ru = 'Обновлен';
																			|en = 'Updated'"));
	ДоступныеСостоянияДокументов.Вставить("СостояниеНеОбновлен",       НСтр("ru = 'Не обновлен';
																			|en = 'Not updated'"));
	ДоступныеСостоянияДокументов.Вставить("СостояниеОшибкиЗаполнения", НСтр("ru = 'Ошибки заполнения';
																			|en = 'Ошибки заполнения'"));

	Возврат ДоступныеСостоянияДокументов;

КонецФункции

// Получает менеджер объекта метаданных.
//
// Параметры:
//   ПолноеИмяОбъектаМетаданных - Строка - полное имя объекта метаданных.
//
// Возвращаемое значение:
//   СправочникМенеджер, ДокументМенеджер, ОбработкаМенеджер, РегистрСведенийМенеджер - менеджер объекта.
//
Функция МенеджерОбъекта(ПолноеИмяОбъектаМетаданных) Экспорт

	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяОбъектаМетаданных);
	
	Если МенеджерОбъекта = Неопределено Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Загрузка данных из файла в табличную часть не поддерживается для объекта с типом: %1';
				|en = 'Cannot import data from a file to a table for object of the %1 type'"),
			ПолноеИмяОбъектаМетаданных);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

	Возврат МенеджерОбъекта;

КонецФункции

// Получает имя менеджера объекта метаданных.
//
// Параметры:
//   ПолноеИмяОбъектаМетаданных - Строка - полное имя объекта метаданных.
//
// Возвращаемое значение:
//    Строка
//
Функция ИмяМенеджераОбъекта(ПолноеИмяОбъектаМетаданных) Экспорт

	ОбъектМетаданных = ОбъектМетаданных(ПолноеИмяОбъектаМетаданных);
	ИмяБазовогоТипа  = ОбщегоНазначения.ИмяБазовогоТипаПоОбъектуМетаданных(ОбъектМетаданных);

	ЧастиИмени = СтрРазделить(ПолноеИмяОбъектаМетаданных, ".");
	ЧастиИмени[0] = ИмяБазовогоТипа;

	ИмяМенеджераОбъекта = СтрСоединить(ЧастиИмени, ".");

	Возврат ИмяМенеджераОбъекта;

КонецФункции

// Получает объект метаданных.
//
// Параметры:
//   ПолноеИмяОбъектаМетаданных - Строка - полное имя объекта метаданных.
//
// Возвращаемое значение:
//   ОбъектМетаданных
//
Функция ОбъектМетаданных(ПолноеИмяОбъектаМетаданных) Экспорт

	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъектаМетаданных);
	
	Если ОбъектМетаданных = Неопределено Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Загрузка данных из файла в табличную часть не поддерживается для объекта с типом: %1';
				|en = 'Cannot import data from a file to a table for object of the %1 type'"),
			ПолноеИмяОбъектаМетаданных);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

	Возврат ОбъектМетаданных;

КонецФункции

// Получает табличную часть объект метаданных.
//
// Параметры:
//   ИмяОбъектаМетаданных - Строка - полное имя объекта метаданных.
//   ИмяТабличнойЧасти    - Строка - имя табличной части.
//
// Возвращаемое значение:
//    ОбъектМетаданных - табличная часть объекта метаданных.
//
Функция ТабличнаяЧастьОбъектаМетаданных(ИмяОбъектаМетаданных, ИмяТабличнойЧасти) Экспорт

	ОбъектМетаданных = ОбъектМетаданных(ИмяОбъектаМетаданных);
	ТабличнаяЧасть   = ОбъектМетаданных.ТабличныеЧасти.Найти(ИмяТабличнойЧасти);

	Если ТабличнаяЧасть = Неопределено Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Загрузка данных из файла в табличную часть %1 не поддерживается для объекта с типом: %2';
				|en = 'Cannot import data from a file to the %1 table for object of the %2 type'"),
			ИмяТабличнойЧасти,
			ИмяОбъектаМетаданных);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

	Возврат ТабличнаяЧасть;

КонецФункции

// Формирует имя файла для выгрузки/загрузки.
//
// Параметры:
//   ИмяОбъектаМетаданных - Строка - имя объекта метаданных.
//   СсылкаНаОбъект       - ЛюбаяСсылка - ссылка объекта для формирования имени файла.
//   ДляВыгрузкиВФайл     - Булево - признак выгрузки/загрузки.
//
// Возвращаемое значение:
//   Строка - имя файла для выгрузки/загрузки.
//
Функция СформироватьИмяФайлаДляОбъектаМетаданных(Знач ИмяОбъектаМетаданных, СсылкаНаОбъект,
			ДляВыгрузкиВФайл = Ложь) Экспорт

	ИмяФайла = "";

	Если МетодКонфигурацииИспользуется(ИмяОбъектаМетаданных, "НаименованиеСохраняемогоФайла") Тогда
		МенеджерОбъекта = МенеджерОбъекта(ИмяОбъектаМетаданных);
		ИмяФайла = МенеджерОбъекта.НаименованиеСохраняемогоФайла(СсылкаНаОбъект, ДляВыгрузкиВФайл);
	КонецЕсли;

	Если ПустаяСтрока(ИмяФайла) Тогда
		Если СтрНайти(ИмяОбъектаМетаданных, ".ТабличныеЧасти.") Тогда
			ИмяОбъектаМетаданных = СтрЗаменить(ИмяОбъектаМетаданных, ".ТабличныеЧасти.", Символы.ПС);
			ЧастиИмениОбъектаМетаданных = СтрРазделить(ИмяОбъектаМетаданных, Символы.ПС);
			МетаданныеОбъекта = ТабличнаяЧастьОбъектаМетаданных(
					ЧастиИмениОбъектаМетаданных[0], ЧастиИмениОбъектаМетаданных[1]);
		Иначе
			МетаданныеОбъекта = ОбъектМетаданных(ИмяОбъектаМетаданных);
		КонецЕсли;

		Если МетаданныеОбъекта <> Неопределено Тогда
			ИмяФайла = СокрЛП(МетаданныеОбъекта.Синоним);
			Если СтрДлина(ИмяФайла) = 0 Тогда
				ИмяФайла = ИмяОбъектаМетаданных;
			КонецЕсли;
		Иначе
			ИмяФайла = ИмяОбъектаМетаданных;
		КонецЕсли;
	КонецЕсли;

	ИмяФайла = СтрЗаменить(ИмяФайла,":","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"*","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"\","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"/","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"&","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"<","");
	ИмяФайла = СтрЗаменить(ИмяФайла,">","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"|","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"""","");

	Возврат ИмяФайла;

КонецФункции

// Возвращает найденный по наименованию файл из присоединенных файлов.
//
// Параметры:
//   СсылкаНаОбъект    - ЛюбаяСсылка - ссылка на объект, для которого нужен поиск присоединенного файла, должен иметь 
//                         справочник присоединенных файлов.
//   НаименованиеФайла - Строка - наименование искомого файла.
//
// Возвращаемое значение:
//   Неопределено, СправочникСсылка - ссылка на присоединенный файл из справочника присоединенных файлов ссылки на объект.
//
Функция НайтиПрисоединенныйФайл(СсылкаНаОбъект, НаименованиеФайла) Экспорт

	ПрисоединенныйФайл = Неопределено;

	Файлы = Новый Массив;
	РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(СсылкаНаОбъект, Файлы);
	Для Каждого Файл Из Файлы Цикл
		Если Файл.Наименование = НаименованиеФайла И Не Файл.ПометкаУдаления Тогда
			ПрисоединенныйФайл = Файл;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПрисоединенныйФайл;

КонецФункции

// Записывает или обновляет присоединенный файл для объекта метаданных и возвращает ссылку на него.
//
// Параметры:
//   СсылкаНаОбъект                   - ОпределяемыйТип.ВладелецПрисоединенныхФайлов - ссылка объект, для которого есть 
//                                        справочник присоединенных файлов.
//   ПутьКВременномуФайлу             - Строка - путь к файлу на сервере, в котором содержится записываемая информация.
//   ПутьКВыбранномуФайлу             - Строка - путь к файлу, который был выбран пользователем.
//   НаименованиеПрисоединенногоФайла - Строка - наименование, под которым будет записан файл.
//
// Возвращаемое значение:
//   СправочникСсылка - ссылка на справочник присоединенных файлов для объекта метаданных.
//
Функция ЗаписатьПрисоединенныйФайл(СсылкаНаОбъект, ПутьКВременномуФайлу, ПутьКВыбранномуФайлу,
			НаименованиеПрисоединенногоФайла) Экспорт

	ПрисоединенныйФайл = Неопределено;

	Если Не ПустаяСтрока(ПутьКВременномуФайлу) Тогда
		Попытка
			ИмяФайла        = СтрЗаменить(ПутьКВременномуФайлу, "\", "/");
			ИмяФайла        = Сред(ИмяФайла, СтрНайти(ИмяФайла, "/", НаправлениеПоиска.СКонца) + 1);
			РасширениеФайла = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(
				ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИмяФайла));

			ДвоичныеДанные = Новый ДвоичныеДанные(ПутьКВременномуФайлу);
			АдресВременногоХранилищаФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
			ПрисоединенныйФайл = НайтиПрисоединенныйФайл(
				СсылкаНаОбъект, НаименованиеПрисоединенногоФайла);

			Если ЗначениеЗаполнено(ПрисоединенныйФайл) Тогда
				ИнформацияОФайле = Новый Структура;
				ИнформацияОФайле.Вставить("АдресФайлаВоВременномХранилище", АдресВременногоХранилищаФайла);
				ИнформацияОФайле.Вставить("АдресВременногоХранилищаТекста", Неопределено);
				ИнформацияОФайле.Вставить("РасширениеБезТочки", РасширениеФайла);

				РаботаСФайлами.ОбновитьФайл(ПрисоединенныйФайл, ИнформацияОФайле)
			Иначе
				ДополнительныеРеквизиты = Новый Структура;
				ДополнительныеРеквизиты.Вставить("ДатаСоздания", ТекущаяУниверсальнаяДата());

				ПараметрыФайла = РаботаСФайлами.ПараметрыДобавленияФайла(ДополнительныеРеквизиты);
				ПараметрыФайла.Автор                       = Пользователи.АвторизованныйПользователь();
				ПараметрыФайла.ВладелецФайлов              = СсылкаНаОбъект;
				ПараметрыФайла.ИмяБезРасширения            = НаименованиеПрисоединенногоФайла;
				ПараметрыФайла.РасширениеБезТочки          = РасширениеФайла;
				ПараметрыФайла.ГруппаФайлов                = Неопределено;
				ПараметрыФайла.ВремяИзмененияУниверсальное = ДополнительныеРеквизиты.ДатаСоздания;

				ПрисоединенныйФайл = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, 
					АдресВременногоХранилищаФайла,
					,
					ПутьКВыбранномуФайлу);
			КонецЕсли;
		Исключение
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации() + "."
					+ НСтр("ru = 'Запись загружаемого файла в присоединенные файлы';
							|en = 'Save the file to import to attachments'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				, 
				СсылкаНаОбъект, 
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЕсли;

	Возврат ПрисоединенныйФайл;

КонецФункции

// Записывает или обновляет присоединенный файл для объекта метаданных.
//
// Параметры:
//   ВладелецФайла       - ОпределяемыйТип.ВладелецПрисоединенныхФайлов - ссылка объект, для которого есть 
//                           справочник присоединенных файлов.
//   АдресДвоичныхДанных - Строка - адрес во временном хранилище.
//   НаименованиеФайла   - Строка - наименование, под которым будет записан файл.
//   РасширениеФайла     - Строка - расширение файла.
//
Процедура ЗаписатьДвоичныеДанныеВПрисоединенныйФайл(ВладелецФайла, АдресДвоичныхДанных, НаименованиеФайла, РасширениеФайла = "") Экспорт

	Если АдресДвоичныхДанных <> Неопределено Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресДвоичныхДанных); // ДвоичныеДанные
		
		//@skip-check missing-temporary-file-deletion
		ПутьКВременномуФайлу = ПолучитьИмяВременногоФайла(РасширениеФайла);
		
		ДвоичныеДанные.Записать(ПутьКВременномуФайлу);
		
		ЗаписатьПрисоединенныйФайл(
			ВладелецФайла,
			ПутьКВременномуФайлу,
			"",
			НаименованиеФайла);
		
		ФайловаяСистема.УдалитьВременныйФайл(ПутьКВременномуФайлу);
	КонецЕсли;

КонецПроцедуры

// Переобразует файл картинки в файл PDF.
//
// Параметры:
//   АдресДвоичныхДанных - Строка - адрес двоичных данных полученной печатной формы.
//   РасширениеФайла     - Строка - расширение файла.
//
Процедура ПреобразоватьВPDF(АдресДвоичныхДанных, РасширениеФайла = "") Экспорт

	Если ПустаяСтрока(АдресДвоичныхДанных)
			Или Не ЭтоАдресВременногоХранилища(АдресДвоичныхДанных) Тогда
		Возврат;
	КонецЕсли;
	
	РасширенияКартинок = РасширенияКартинок();
	ПреобразоватьВPDF  = СтрНайти(РасширенияКартинок, ВРег(РасширениеФайла)) <> Неопределено;
	
	Если ПреобразоватьВPDF Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресДвоичныхДанных); // ДвоичныеДанные
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.АвтоМасштаб = Истина;
		
		Попытка
			Картинка = ТабличныйДокумент.Рисунки.Добавить(ТипРисункаТабличногоДокумента.Картинка);
			Картинка.Расположить(ТабличныйДокумент.Область(1, 1));
			
			Картинка.Картинка = Новый Картинка(ДвоичныеДанные);
			Картинка.Ширина   = ТабличныйДокумент.ШиринаСтраницы - ТабличныйДокумент.ПолеСлева - ТабличныйДокумент.ПолеСправа;
			Картинка.Высота   = Картинка.Картинка.Высота() * Картинка.Ширина / Картинка.Картинка.Ширина();
			Картинка.Линия    = Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии, 0);
			
		Исключение
			ТабличныйДокумент.Область(1, 1).Текст = НСтр("ru = 'Поврежденный файл.';
														|en = 'Corrupted file.'");
		КонецПопытки;
		
		РасширениеФайла      = "pdf";
		//@skip-check missing-temporary-file-deletion
		ПутьКВременномуФайлу = ПолучитьИмяВременногоФайла(РасширениеФайла);
		
		Попытка
			ТабличныйДокумент.Записать(ПутьКВременномуФайлу, ТипФайлаТабличногоДокумента.PDF);
			
			ДвоичныеДанные      = Новый ДвоичныеДанные(ПутьКВременномуФайлу);
			АдресДвоичныхДанных = ПоместитьВоВременноеХранилище(ДвоичныеДанные, АдресДвоичныхДанных);
			
		Исключение
			АдресДвоичныхДанных = "";
		КонецПопытки;
		
		ФайловаяСистема.УдалитьВременныйФайл(ПутьКВременномуФайлу);
	КонецЕсли;

КонецПроцедуры

// Заполняет в параметрах выгрузки/загрузки данных информацию об именах колонок, их типах
// и обязательности заполнения.
//
// Параметры:
//  Параметры - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыЗагрузкиИзФайлаВТабличнуюЧасть.
//
Процедура ЗаполнитьИнформациюПоЗагружаемымРеквизитам(Параметры) Экспорт

	ОбъектМетаданных = ТабличнаяЧастьОбъектаМетаданных(
		Параметры.ИмяОбъектаМетаданных,
		Параметры.ИмяТабличнойЧасти);

	Если ОбъектМетаданных <> Неопределено Тогда
		Для Каждого РеквизитОбъектаМетаданных Из ОбъектМетаданных.Реквизиты Цикл
			Параметры.ЗаполняемыеПриЗагрузкеКолонки.Вставить(РеквизитОбъектаМетаданных.Имя,
				РеквизитОбъектаМетаданных.Синоним);
			Параметры.ТипыДанныхКолонок.Вставить(РеквизитОбъектаМетаданных.Имя, РеквизитОбъектаМетаданных.Тип);

			Если РеквизитОбъектаМетаданных.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку Тогда
				Параметры.ОбязательныеКолонки.Добавить(РеквизитОбъектаМетаданных.Имя);
				Параметры.ОсновныеКолонки.Добавить(РеквизитОбъектаМетаданных.Имя);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#Область ИнформацияПоШапкеМакета

Функция ОбластьЗаголовкаШаблонаТаблицы(Шаблон)

	Если Шаблон.Области.Найти("ЗаголовокТаблицы") <> Неопределено Тогда
		ОбластьЗаголовокТаблицы = Шаблон.ПолучитьОбласть("ЗаголовокТаблицы");
	Иначе
		ОбластьШапкиТаблицы = Шаблон;

		ВысотаШапки = 1;
		СчетчикПустыхСтрок = 0;

		Для НомерСтроки = 1 По ОбластьШапкиТаблицы.ВысотаТаблицы Цикл
			Для НомерКолонки = 1 По ОбластьШапкиТаблицы.ШиринаТаблицы Цикл
				Ячейка = ОбластьШапкиТаблицы.ПолучитьОбласть(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки).ТекущаяОбласть;
				Если ЗначениеЗаполнено(Ячейка.Текст) Тогда
					ВысотаШапки = НомерСтроки;
				КонецЕсли;
			КонецЦикла;

			Если СчетчикПустыхСтрок = 10 Тогда
				Прервать;
			ИначеЕсли ВысотаШапки <> НомерСтроки Тогда
				СчетчикПустыхСтрок = СчетчикПустыхСтрок + 1;
			КонецЕсли;
		КонецЦикла;
		
		ОбластьЗаголовокТаблицы = ОбластьШапкиТаблицы.ПолучитьОбласть(1, 1, ВысотаШапки, ОбластьШапкиТаблицы.ШиринаТаблицы);
	КонецЕсли;

	Возврат ОбластьЗаголовокТаблицы;

КонецФункции

Процедура ЗаполнитьПараметрыШапки(Шаблон, ПараметрыШапки)

	ПараметрыШапки.Вставить("Параметры", Новый Структура);
	ПараметрыШапки.Вставить("Шаблоны",   Новый Массив);

	Если Шаблон.Области.Найти("ДанныеШапки") = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Попытка
		ДанныеШапки = Шаблон.ПолучитьОбласть("ДанныеШапки");

		Для НомерСтроки = 1 По ДанныеШапки.ВысотаТаблицы Цикл
			Для НомерКолонки = 1 По ДанныеШапки.ШиринаТаблицы Цикл
				Ячейка = ДанныеШапки.ПолучитьОбласть(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки).ТекущаяОбласть;
				ТекстШаблона = Ячейка.Текст;

				Если ПустаяСтрока(ТекстШаблона) Тогда
					Продолжить;
				КонецЕсли;

				ТекстЯчейки = Ячейка.Текст;	// ТекстЯчейки = ТекстЯчейкиКРазбору(ТекстЯчейки);
				ТекстЯчейки = СтрЗаменить(ТекстЯчейки, "{", Символы.ПС + "{");
				ТекстЯчейки = СтрЗаменить(ТекстЯчейки, "}", "}" + Символы.ПС);
				ТекстЯчейки = СтрЗаменить(ТекстЯчейки, "[", Символы.ПС + "[");
				ТекстЯчейки = СтрЗаменить(ТекстЯчейки, "]", "]" + Символы.ПС);

				ШиринаОбластиПоиска = 1;	// ШиринаОбластиПоиска = Шаблон.ШиринаТаблицы - НомерКолонки + 1; // Сколько колонок нужно читать для шаблона.
				ВысотаОбластиПоиска = 1;

				// Определение размера области поиска параметров.
				РазмерОбластиПоиска = Ячейка.Примечание.Текст; // Ожидается в примечании текст по шаблону: "ШиринаОбластиПоиска = 7; ВысотаОбластиПоиска = 4".

				Если Не ПустаяСтрока(РазмерОбластиПоиска) Тогда
					ШиринаОбластиПоискаИзПримечания = 1;
					ВысотаОбластиПоискаИзПримечания = 1;

					РазмерОбластиПоиска = СтрЗаменить(РазмерОбластиПоиска, "=", "");
					РазмерОбластиПоиска = СтрРазделить(РазмерОбластиПоиска, Символы.ПС);
					Для Каждого ОбластьПоиска Из РазмерОбластиПоиска Цикл
						Если СтрНайти(ОбластьПоиска, "ШиринаОбластиПоиска") > 0 Тогда
							ШиринаОбластиПоискаИзПримечания = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СокрЛП(СтрЗаменить(ОбластьПоиска, "ШиринаОбластиПоиска", "")));
						КонецЕсли;
						Если СтрНайти(ОбластьПоиска, "ВысотаОбластиПоиска") > 0 Тогда
							ВысотаОбластиПоискаИзПримечания = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СокрЛП(СтрЗаменить(ОбластьПоиска, "ВысотаОбластиПоиска", "")));
						КонецЕсли;
					КонецЦикла;

					ШиринаОбластиПоиска = ШиринаОбластиПоискаИзПримечания;
					ВысотаОбластиПоиска = ВысотаОбластиПоискаИзПримечания;
				КонецЕсли;

				// Разбор шаблона.
				ПараметрыШаблона     = Новый Массив;
				КлючевыеСловаШаблона = Новый Массив;

				СоставЯчейки = СтрРазделить(ТекстЯчейки, Символы.ПС, Ложь);
				Для Каждого ЧастьСостава Из СоставЯчейки Цикл
					Если СтрНайти(ЧастьСостава, "{") > 0 Тогда
						КлючевоеСлово = СтрЗаменить(ЧастьСостава, "{", "");
						КлючевоеСлово = СтрЗаменить(КлючевоеСлово, "}", "");

						КлючевыеСловаШаблона.Добавить(КлючевоеСлово);
					КонецЕсли;
					Если СтрНайти(ЧастьСостава, "[") > 0 Тогда
						ИмяПараметра = СтрЗаменить(ЧастьСостава, "[", "");
						ИмяПараметра = СтрЗаменить(ИмяПараметра, "]", "");

						ПараметрыШаблона.Добавить(ИмяПараметра);
						ПараметрыШапки.Параметры.Вставить(ИмяПараметра, "");
					КонецЕсли;
				КонецЦикла;

				Если ПараметрыШаблона.Количество() > 0 Тогда
					ДанныеШаблона = ДанныеШаблона();
					ДанныеШаблона.Шаблон               = ТекстШаблона;
					ДанныеШаблона.КлючевыеСловаШаблона = КлючевыеСловаШаблона;
					ДанныеШаблона.ПараметрыШаблона     = ПараметрыШаблона;
					ДанныеШаблона.ШиринаОбластиПоиска  = ШиринаОбластиПоиска; // Сколько колонок нужно читать для шаблона.
					ДанныеШаблона.ВысотаОбластиПоиска  = ВысотаОбластиПоиска; // Сколько строк нужно читать для шаблона.

					ПараметрыШапки.Шаблоны.Добавить(ДанныеШаблона);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	Исключение
		Возврат;
	КонецПопытки;

КонецПроцедуры

// Заполняет параметры шапки чтением данных из присоединенного файла.
//
// Параметры:
//   ПрисоединенныйФайл          - СправочникСсылка - ссылка на присоединенный файл объекта метаданных.
//   ИнформацияПоКолонкамТаблица - см. ИнтеграцияСМаркетплейсамиСервер.ИнициализироватьТаблицуИнформацииПоКолонкам.
//   ПараметрыШапки              - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыШапки.
//
Процедура ЗаполнитьПараметрыШапкиИзПрисоединенногоФайла(ПрисоединенныйФайл, ИнформацияПоКолонкамТаблица,
			ПараметрыШапки) Экспорт

	Если Не ЗначениеЗаполнено(ПрисоединенныйФайл) Тогда
		Возврат;
	КонецЕсли;

	ДанныеФайла = РаботаСФайлами.ДанныеФайла(ПрисоединенныйФайл);

	//@skip-check missing-temporary-file-deletion
	ПутьКВременномуФайлу = ПолучитьИмяВременногоФайла(ДанныеФайла.Расширение);
	ДвоичныеДанные = ПолучитьИЗВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла); // ДвоичныеДанные
	ДвоичныеДанные.Записать(ПутьКВременномуФайлу);

	СодержимоеФайла = Новый ТабличныйДокумент;
	СодержимоеФайла.Прочитать(ПутьКВременномуФайлу, СпособЧтенияЗначенийТабличногоДокумента.Значение);
	ФайловаяСистема.УдалитьВременныйФайл(ПутьКВременномуФайлу);

	ТаблицаСДаннымиМакета = ПолучитьТаблицуСДаннымиИзМакета(СодержимоеФайла);

	ИнициализироватьКолонкиИШапку(ИнформацияПоКолонкамТаблица, ТаблицаСДаннымиМакета, ПараметрыШапки);

КонецПроцедуры

// Возвращает колонки-реквизиты зависимые от функциональных опций.
//
// Параметры:
//   ПолноеИмяОбъекта - Строка - полное наименование объекта.
//   
// Возвращаемое значение:
//   Соответствие Из КлючИЗначение:
//     * Ключ     - Строка - имя колонки;
//     * Значение - Булево - признак доступности.
//
Функция КолонкиЗависимыеОтФункциональныхОпций(ПолноеИмяОбъекта)

	ИнформацияОФункциональныхОпциях = Новый Соответствие;
	ИмяОбъектаССуффиксомРеквизит = ПолноеИмяОбъекта + ".Реквизит.";

	ФункциональныеОпции = ДоступностьОбъектовПоОпциям();
	Для Каждого ФункциональнаяОпция Из ФункциональныеОпции Цикл
		Если СтрНачинаетсяС(ФункциональнаяОпция.Ключ, ИмяОбъектаССуффиксомРеквизит) Тогда
			ИнформацияОФункциональныхОпциях.Вставить(Сред(ФункциональнаяОпция.Ключ, СтрДлина(ИмяОбъектаССуффиксомРеквизит) + 1), ФункциональнаяОпция.Значение);
		КонецЕсли;
	КонецЦикла;

	Возврат ИнформацияОФункциональныхОпциях;

КонецФункции

Функция ДоступностьОбъектовПоОпциям()

	Параметры = Новый Структура;
	ОбщегоНазначенияПереопределяемый.ПриОпределенииПараметровФункциональныхОпцийИнтерфейса(Параметры);

	ДоступностьОбъектов = Новый Соответствие;
	Для Каждого ФункциональнаяОпция Из Метаданные.ФункциональныеОпции Цикл
		Значение = -1;
		Для Каждого Элемент Из ФункциональнаяОпция.Состав Цикл
			Если Элемент.Объект = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если Значение = -1 Тогда
				Значение = ПолучитьФункциональнуюОпцию(ФункциональнаяОпция.Имя, Параметры);
			КонецЕсли;
			ПолноеИмя = Элемент.Объект.ПолноеИмя();
			Если Значение = Истина Тогда
				ДоступностьОбъектов.Вставить(ПолноеИмя, Истина);
			Иначе
				Если ДоступностьОбъектов[ПолноеИмя] = Неопределено Тогда
					ДоступностьОбъектов.Вставить(ПолноеИмя, Ложь);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Возврат Новый ФиксированноеСоответствие(ДоступностьОбъектов);

КонецФункции

#КонецОбласти

#Область ПодготовкаКЗагрузкеДанных

// Возвращает предопределенные стандартные колонки.
//
// Параметры:
//   ДобавитьОписаниеКолонок        - Булево - признак добавления описания колонки,т.е. типа и синонима.
//   ДобавитьСписокНеоднозначностей - Булево - признак добавления колонки описания неоднозначностей.
//
// Возвращаемое значение:
//   Массив из Строка    - список имен стандартных колонок;
//   Массив из Структура - список стандартных колонок с именем, типом и синонимом.
//
Функция СтандартныеКолонкиЗагрузки(ДобавитьОписаниеКолонок = Истина, ДобавитьСписокНеоднозначностей = Истина)
	
	СтандартныеКолонки = Новый Массив;
	
	Если ДобавитьОписаниеКолонок Тогда
		ОписаниеКолонки = "Имя, Тип, Синоним";
		
		ТипБулево    = Новый ОписаниеТипов("Булево");
		ТипСтрока    = Новый ОписаниеТипов("Строка");
		ТипЧисло     = Новый ОписаниеТипов("Число");
		ТипСоставной = Новый ОписаниеТипов("Булево, Строка, Число",, Новый КвалификаторыСтроки(1024));
		
		СтандартныеКолонки.Добавить(Новый Структура(ОписаниеКолонки, "НомерСтроки",
			ТипЧисло, НСтр("ru = '№';
							|en = 'No.'")));
		СтандартныеКолонки.Добавить(Новый Структура(ОписаниеКолонки, "РезультатСопоставленияСтроки",
			ТипСтрока, НСтр("ru = 'Результат';
							|en = 'Result'")));
		СтандартныеКолонки.Добавить(Новый Структура(ОписаниеКолонки, "СтрокаОбработана",
			ТипБулево, НСтр("ru = 'Обработана';
							|en = 'Processed'")));
		СтандартныеКолонки.Добавить(Новый Структура(ОписаниеКолонки, "ОписаниеОшибки",
			ТипСтрока, НСтр("ru = 'Причина';
							|en = 'Reason'")));
		СтандартныеКолонки.Добавить(Новый Структура(ОписаниеКолонки, "Служебный",
			ТипСоставной, НСтр("ru = 'Служебный';
								|en = 'Internal'")));
		
		Если ДобавитьСписокНеоднозначностей Тогда
			СтандартныеКолонки.Добавить(Новый Структура(ОписаниеКолонки, "СписокНеоднозначностей",
				Новый ОписаниеТипов("СписокЗначений"), НСтр("ru = 'Список неоднозначностей';
															|en = 'List of conflicts'")));
		КонецЕсли;
		
	Иначе
		СтандартныеКолонки.Добавить("НомерСтроки");
		СтандартныеКолонки.Добавить("РезультатСопоставленияСтроки");
		СтандартныеКолонки.Добавить("СтрокаОбработана");
		СтандартныеКолонки.Добавить("ОписаниеОшибки");
		СтандартныеКолонки.Добавить("Служебный");
		Если ДобавитьСписокНеоднозначностей Тогда
			СтандартныеКолонки.Добавить("СписокНеоднозначностей");
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтандартныеКолонки;
	
КонецФункции

// Добавляет стандартные колонки или реквизиты формы в зависимости от типа коллекции.
//
// Параметры:
//   Коллекция - ТаблицаЗначений         - дополняемая таблица.
//             - Массив из РеквизитФормы - дополняемый список реквизитов формы.
//   ДобавитьСписокНеоднозначностей - Булево - признак добавления колонки для заполнения списка неоднозначностей.
//   ИмяКоллекцииФормы - Строка - имя коллекции для добавления реквизитов формы.
//
Процедура ДобавитьСтандартныеКолонки(Коллекция, ДобавитьСписокНеоднозначностей = Истина,
			ИмяКоллекцииФормы = "") Экспорт

	ТипКоллекции = ТипЗнч(Коллекция);
	Если ТипКоллекции <> Тип("ТаблицаЗначений") И ТипКоллекции <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;

	СтандартныеКолонки = СтандартныеКолонкиЗагрузки(Истина, ДобавитьСписокНеоднозначностей);

	ДобавитьКакРеквизитФормы = Не ПустаяСтрока(ИмяКоллекцииФормы);

	Для Каждого Колонка Из СтандартныеКолонки Цикл
		Если ДобавитьКакРеквизитФормы Тогда
			Коллекция.Добавить(Новый РеквизитФормы(Колонка.Имя, Колонка.Тип,
				ИмяКоллекцииФормы, Колонка.Синоним));
		Иначе
			Коллекция.Колонки.Добавить(Колонка.Имя, Колонка.Тип, Колонка.Синоним);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

// Инициализируется таблица информации по колонкам.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма, в которой необходимо инициализировать таблицу информации по колонкам;
//         - Неопределено - при незаполненном значении параметра создается таблица значений.
//
// Возвращаемое значение:
//   ТаблицаЗначений - если параметр Форма не заполнен, иначе ничего не возвращается:
//     * ПредставлениеКолонки - Строка - представление колонки.
//     * Синоним - Строка - синоним колонки.
//     * ИмяКолонки - Строка - имя колонки.
//     * Группа - Строка - группа вывода колонки.
//     * Использование - Строка - описание использования колонки.
//     * Родитель - Строка - родитель колонки.
//     * Примечание - Строка - примечание к колонке.
//     * ТипКолонки - Тип - тип колонки.
//     * ОбязательнаДляЗаполнения - Булево - признак обязательного заполнения колонки.
//     * ВидимостьВМакете - Булево - признак видимости колонки в макете.
//     * ВидимостьПриСопоставлении - Булево - признак видимости колонки при сопоставлении.
//     * ВИтогахВсегдаПустая - Булево - признак пустой в итогах колонки.
//     * Ширина - Число - ширина колонки.
//     * ПозицияВМакете - Число - позиция колонки в макете.
//     * Позиция - Число - позиция колонки.
//
Функция ИнициализироватьТаблицуИнформацииПоКолонкам(Форма = Неопределено) Экспорт

	ТипСтрока       = Новый ОписаниеТипов("Строка");
	ТипСтрока150    = Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(100));
	ТипБулево       = Новый ОписаниеТипов("Булево");
	ТипСтрока       = Новый ОписаниеТипов("Строка");
	ТипЧислоЦелое   = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(10, 0));
	ТипЧислоДробное = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(10, 3));
	ТипПроизвольный = Новый ОписаниеТипов();

	Если Форма = Неопределено Тогда
		ТаблицаИнформацииПоКолонкам = Новый ТаблицаЗначений;

		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("ПредставлениеКолонки",      ТипСтрока150);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("Синоним",                   ТипСтрока150);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("ИмяКолонки",                ТипСтрока150);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("Группа",                    ТипСтрока150);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("Использование",             ТипСтрока150);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("Родитель",                  ТипСтрока);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("Примечание",                ТипСтрока);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("ТипКолонки",                ТипПроизвольный);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("ОбязательнаДляЗаполнения",  ТипБулево);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("ВидимостьВМакете",          ТипБулево);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("ВидимостьПриСопоставлении", ТипБулево);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("ВИтогахВсегдаПустая",       ТипБулево);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("Ширина",                    ТипЧислоДробное);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("ПозицияВМакете",            ТипЧислоЦелое);
		ТаблицаИнформацииПоКолонкам.Колонки.Добавить("Позиция",                   ТипЧислоЦелое);

		Возврат ТаблицаИнформацииПоКолонкам;

	Иначе // Заполнение таблицы формы.

		МассивРеквизитов = Новый Массив;

		Если СтрНайти(Форма.ИмяФормы, "РедактированиеБланка") = 0 Тогда
			МассивРеквизитов.Добавить(Новый РеквизитФормы("ПредставлениеКолонки", ТипСтрока150, "ИнформацияПоКолонкам"));
			МассивРеквизитов.Добавить(Новый РеквизитФормы("Синоним",              ТипСтрока150, "ИнформацияПоКолонкам"));
			МассивРеквизитов.Добавить(Новый РеквизитФормы("ВидимостьВМакете",     ТипБулево,    "ИнформацияПоКолонкам"));
			МассивРеквизитов.Добавить(Новый РеквизитФормы("Группа",               ТипСтрока150, "ИнформацияПоКолонкам"));
		КонецЕсли;

		МассивРеквизитов.Добавить(Новый РеквизитФормы("ИмяКолонки",                ТипСтрока150,    "ИнформацияПоКолонкам"));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("Использование",             ТипСтрока150,    "ИнформацияПоКолонкам"));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("Родитель",                  ТипСтрока,       "ИнформацияПоКолонкам"));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("Примечание",                ТипСтрока,       "ИнформацияПоКолонкам"));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("ТипКолонки",                ТипПроизвольный, "ИнформацияПоКолонкам"));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("ОбязательнаДляЗаполнения",  ТипБулево,       "ИнформацияПоКолонкам"));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("ВидимостьПриСопоставлении", ТипБулево,       "ИнформацияПоКолонкам"));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("ВИтогахВсегдаПустая",       ТипБулево,       "ИнформацияПоКолонкам"));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("Ширина",                    ТипЧислоДробное, "ИнформацияПоКолонкам"));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("ПозицияВМакете",            ТипЧислоЦелое,   "ИнформацияПоКолонкам"));
		МассивРеквизитов.Добавить(Новый РеквизитФормы("Позиция",                   ТипЧислоЦелое,   "ИнформацияПоКолонкам"));

		Форма.ИзменитьРеквизиты(МассивРеквизитов);

	КонецЕсли;

КонецФункции

// Определяет состав колонок для загрузки данных и параметров шапки, если она задана.
//
// Параметры:
//   ПараметрыЗагрузки    - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыЗагрузкиИзФайлаВТабличнуюЧасть.
//   ИнформацияПоКолонкам - см. ИнтеграцияСМаркетплейсамиСервер.ИнициализироватьТаблицуИнформацииПоКолонкам.
//   ПараметрыШапки       - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыШапки.
//
Процедура ОпределитьИнформациюПоКолонкамИПараметрамШапки(ПараметрыЗагрузки, ИнформацияПоКолонкам, ПараметрыШапки) Экспорт

	МенеджерОбъекта = МенеджерОбъекта(ПараметрыЗагрузки.ИмяОбъектаМетаданных);

	Макет = МенеджерОбъекта.ПолучитьМакет(ПараметрыЗагрузки.ИмяМакетаСШаблоном);
	Если Макет = Неопределено Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Загрузка данных из файла в табличную часть не поддерживается, т.к. отсутствует макет %1 у объекта с типом: %2';
				|en = 'Cannot import data from a file to a table since there is no %1 template for object of the %2 type'"),
			ПараметрыЗагрузки.ИмяМакетаСШаблоном,
			ПараметрыЗагрузки.ИмяОбъектаМетаданных);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

	Если ПараметрыШапки <> Неопределено Тогда
		ЗаполнитьПараметрыШапки(Макет, ПараметрыШапки);
	КонецЕсли;

	ОбластьЗаголовокТаблицы = ОбластьЗаголовкаШаблонаТаблицы(Макет);
	ПараметрыЗагрузки.Вставить("ВысотаЗаголовкаТаблицыМакетаСШаблоном",    ОбластьЗаголовокТаблицы.ВысотаТаблицы);
	ПараметрыЗагрузки.Вставить("ВысотаЗаголовкаТаблицыЗагружаемогоМакета", ОбластьЗаголовокТаблицы.ВысотаТаблицы);

	ЗаполнитьИнформациюПоЗагружаемымРеквизитам(ПараметрыЗагрузки);

	ОпределитьИнформациюПоКолонкамТабличнаяЧасть(ИнформацияПоКолонкам, Макет, ПараметрыЗагрузки);

	ТребуетсяПересчетПозиций = Ложь;

	СписокКолонокСФункциональнымиОпциями = КолонкиЗависимыеОтФункциональныхОпций(ПараметрыЗагрузки.ИмяОбъектаМетаданных + "." + ПараметрыЗагрузки.ИмяТабличнойЧасти);
	Для Каждого ФункциональнаяОпцияКолонкиВключена Из СписокКолонокСФункциональнымиОпциями Цикл
		СтрокаСИнформациейОКолонке = ИнформацияПоКолонкам.Найти(ФункциональнаяОпцияКолонкиВключена.Ключ, "ИмяКолонки");
		Если СтрокаСИнформациейОКолонке <> Неопределено Тогда
			Если НЕ ФункциональнаяОпцияКолонкиВключена.Значение Тогда
				ИнформацияПоКолонкам.Удалить(СтрокаСИнформациейОКолонке);
				ТребуетсяПересчетПозиций = Истина;
			КонецЕсли;
		Иначе
			Если ФункциональнаяОпцияКолонкиВключена.Значение Тогда
				ЗаполнитьИнформациюПоКолонкамНаОснованииШаблона(
					ИнформацияПоКолонкам,
					ПараметрыЗагрузки,
					ОбластьЗаголовокТаблицы,
					ФункциональнаяОпцияКолонкиВключена.Ключ);
				ТребуетсяПересчетПозиций = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Если ТребуетсяПересчетПозиций Тогда
		ИнформацияПоКолонкам.Сортировать("Позиция");
		Позиция = 1;
		ПозицияВМакете = 1;

		Для Каждого Колонка Из ИнформацияПоКолонкам Цикл
			Колонка.Позиция = Позиция;
			Позиция = Позиция + 1;

			Если Колонка.ВидимостьВМакете Тогда
				Колонка.ПозицияВМакете = ПозицияВМакете;
				ПозицияВМакете = ПозицияВМакете + 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура ОпределитьИнформациюПоКолонкамТабличнаяЧасть(Знач ИнформацияПоКолонкам, Знач Макет, ПараметрыЗагрузки)

	Если МетодКонфигурацииИспользуется(ПараметрыЗагрузки.ИмяОбъектаМетаданных, "УстановитьПараметрыЗагрузкиИзФайлаВТабличнуюЧасть") Тогда
		МенеджерОбъекта = МенеджерОбъекта(ПараметрыЗагрузки.ИмяОбъектаМетаданных);
		МенеджерОбъекта.УстановитьПараметрыЗагрузкиИзФайлаВТабличнуюЧасть(ПараметрыЗагрузки);
	КонецЕсли;

	ЗаголовокТаблицы = ОбластьЗаголовкаШаблонаТаблицы(Макет);
	ЗаполнитьИнформациюПоКолонкамНаОснованииШаблона(
		ИнформацияПоКолонкам,
		ПараметрыЗагрузки,
		ЗаголовокТаблицы,
		,
		ИнформацияПоКолонкам.Количество() <> 0);

КонецПроцедуры

// Заполняет таблицу о колонках в макете. Информация используется для построения таблицы сопоставления.
//
// Параметры:
//   ИнформацияПоКолонкам     - см. ИнтеграцияСМаркетплейсамиСервер.ИнициализироватьТаблицуИнформацииПоКолонкам.
//   ПараметрыЗагрузки        - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыЗагрузкиИзФайлаВТабличнуюЧасть.
//   ОбластьЗаголовокТаблицы  - ТабличныйДокумент - область заголовка макета.
//   ИменаДобавляемыхКолонок  - Строка - список добавляемых колонок через запятую. Если значение не заполонено, то 
//                                добавляются все.
//   ТолькоДополнитьКолонки   - Булево - признак добавления колонок только при их отсутствии.
//
Процедура ЗаполнитьИнформациюПоКолонкамНаОснованииШаблона(ИнформацияПоКолонкам, ПараметрыЗагрузки, ОбластьЗаголовокТаблицы,
			ИменаДобавляемыхКолонок = Неопределено, ТолькоДополнитьКолонки = Ложь)

	ВыборочноеДобавление = Ложь;
	Если ЗначениеЗаполнено(ИменаДобавляемыхКолонок) Тогда
		ВыборочноеДобавление = Истина;
		МассивДобавляемыхКолонок = СтрРазделить(ИменаДобавляемыхКолонок, ",", Ложь);
		Позиция = ИнформацияПоКолонкам.Количество() + 1;
	ИначеЕсли ТолькоДополнитьКолонки Тогда
		Позиция = ИнформацияПоКолонкам.Количество() + 1;
	Иначе
		ИнформацияПоКолонкам.Очистить();
		Позиция = 1;
	КонецЕсли;

	Если ПараметрыЗагрузки.Свойство("ТипыДанныхКолонок") Тогда
		СоответствиеТиповДанныхКолонок = ПараметрыЗагрузки.ТипыДанныхКолонок;
	Иначе
		СоответствиеТиповДанныхКолонок = Новый Соответствие;
	КонецЕсли;

	НомерКолонки = 0;
	Группы = Новый Соответствие;
	
	ВысотаШапки = ОбластьЗаголовокТаблицы.ВысотаТаблицы;
	Если ВысотаШапки >= 2 Тогда
		НомерКолонки = 1;
		Пока НомерКолонки <= ОбластьЗаголовокТаблицы.ШиринаТаблицы Цикл
			Область = ОбластьЗаголовокТаблицы.ПолучитьОбласть(1, НомерКолонки);
			Ячейка = ОбластьЗаголовокТаблицы.ПолучитьОбласть(1, НомерКолонки, 1, НомерКолонки).ТекущаяОбласть;
			Группа = Ячейка.Текст;
			Для Индекс = НомерКолонки По НомерКолонки + Область.ШиринаТаблицы -1 Цикл
				Группы.Вставить(Индекс, Группа);
			КонецЦикла;
			НомерКолонки = НомерКолонки + Область.ШиринаТаблицы;
		КонецЦикла;
	КонецЕсли;

	ОбъектМетаданных = ТабличнаяЧастьОбъектаМетаданных(ПараметрыЗагрузки.ИмяОбъектаМетаданных, ПараметрыЗагрузки.ИмяТабличнойЧасти);

	ДобавленныеКолонки = Новый Массив;

	Для НомерСтроки = 1 По ВысотаШапки Цикл
		Для НомерКолонки = 1 По ОбластьЗаголовокТаблицы.ШиринаТаблицы Цикл
			Ячейка = ОбластьЗаголовокТаблицы.ПолучитьОбласть(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки).ТекущаяОбласть;

			Если ПустаяСтрока(Ячейка.ПараметрРасшифровки) Тогда
				Продолжить;
			ИначеЕсли НЕ ПустаяСтрока(Ячейка.Параметр) Тогда
				Продолжить;
			КонецЕсли;

			Если Ячейка.Имя = "R1C1" Тогда
				ИмяРеквизита = ?(ЗначениеЗаполнено(Ячейка.ПараметрРасшифровки), Ячейка.ПараметрРасшифровки, Ячейка.Текст);
				ПредставлениеРеквизита = ?(ЗначениеЗаполнено(Ячейка.Текст), Ячейка.Текст, Ячейка.ПараметрРасшифровки);
				Родитель = ?(ЗначениеЗаполнено(Ячейка.ПараметрРасшифровки), Ячейка.ПараметрРасшифровки, Ячейка.Текст);
			Иначе
				ИмяРеквизита = Ячейка.Имя;
				ПредставлениеРеквизита = ?(ЗначениеЗаполнено(Ячейка.Текст), Ячейка.Текст, Ячейка.Имя);
				Родитель = ?(ЗначениеЗаполнено(Ячейка.ПараметрРасшифровки), Ячейка.ПараметрРасшифровки, Ячейка.Имя);
			КонецЕсли;

			Если ЗначениеЗаполнено(ИмяРеквизита) Тогда
				Попытка
					МетаданныеРеквизита = ОбъектМетаданных.Реквизиты.Найти(ИмяРеквизита);
				Исключение
					МетаданныеРеквизита = Неопределено;
				КонецПопытки;

				ТипДанныхКолонки = Новый ОписаниеТипов("Строка");
				Если МетаданныеРеквизита <> Неопределено Тогда
					ТипДанныхКолонки = МетаданныеРеквизита.Тип;
				КонецЕсли;
				Если СоответствиеТиповДанныхКолонок <> Неопределено Тогда
					ТипДанныхКолонкиПереопределенный = СоответствиеТиповДанныхКолонок.Получить(ИмяРеквизита);
					Если ТипДанныхКолонкиПереопределенный <> Неопределено Тогда
						ТипДанныхКолонки = ТипДанныхКолонкиПереопределенный;
					КонецЕсли;
				КонецЕсли;

				Если ВыборочноеДобавление И МассивДобавляемыхКолонок.Найти(ИмяРеквизита) = Неопределено Тогда
					Продолжить;
				КонецЕсли;

				ДобавленныеКолонки.Добавить(ИмяРеквизита);

				ОбязательнаДляЗаполнения = Ячейка.Шрифт.Полужирный = Истина
					Или Ячейка.Шрифт = ШрифтыСтиля.ВажнаяНадписьШрифт
					Или ПараметрыЗагрузки.ОбязательныеКолонки.Найти(ИмяРеквизита) <> Неопределено;
				ВИтогахВсегдаПустая      = ПараметрыЗагрузки.ПустыеВИтогахКолонки.Найти(ИмяРеквизита) <> Неопределено;

				ПримечаниеВШапкеКолонки = Ячейка.Примечание.Текст + ?(ОбязательнаДляЗаполнения,
					Символы.ПС + НСтр("ru = 'Обязательно для заполнения.';
										|en = 'Required.'"), "");

				ДобавитьКолонку = Не ТолькоДополнитьКолонки;
				Если ТолькоДополнитьКолонки Тогда
					СтрокаИнфоПроКолонки = ИнформацияПоКолонкам.Найти(ИмяРеквизита, "ИмяКолонки");
					ДобавитьКолонку = (СтрокаИнфоПроКолонки = Неопределено);
				КонецЕсли;

				Если ДобавитьКолонку Тогда
					СтрокаИнфоПроКолонки = ИнформацияПоКолонкам.Добавить();
					СтрокаИнфоПроКолонки.ИмяКолонки            = ИмяРеквизита;
					СтрокаИнфоПроКолонки.ПредставлениеКолонки  = ПредставлениеРеквизита;
					СтрокаИнфоПроКолонки.Позиция               = Позиция;
					СтрокаИнфоПроКолонки.ПозицияВМакете        = Позиция;
				КонецЕсли;

				СтрокаИнфоПроКолонки.ТипКолонки                = ТипДанныхКолонки;
				СтрокаИнфоПроКолонки.ОбязательнаДляЗаполнения  = ОбязательнаДляЗаполнения;
				СтрокаИнфоПроКолонки.Родитель                  = Родитель;
				СтрокаИнфоПроКолонки.ВидимостьВМакете          = (Ячейка.ШиринаКолонки > 0);
				СтрокаИнфоПроКолонки.ВидимостьПриСопоставлении = (Ячейка.ШиринаКолонки > 0);
				СтрокаИнфоПроКолонки.Примечание                = ПримечаниеВШапкеКолонки;
				СтрокаИнфоПроКолонки.Ширина                    = Ячейка.ШиринаКолонки;
				СтрокаИнфоПроКолонки.ВИтогахВсегдаПустая       = ВИтогахВсегдаПустая;

				Если МетаданныеРеквизита <> Неопределено И ОбщегоНазначения.ЭтоОбъектСсылочногоТипа(ОбъектМетаданных) Тогда
					Если МетаданныеРеквизита.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы Тогда
						СтрокаИнфоПроКолонки.Использование = "ДляГруппы";
					ИначеЕсли МетаданныеРеквизита.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппыИЭлемента Тогда
						СтрокаИнфоПроКолонки.Использование = "ДляГруппыИЭлемента";
					Иначе
						СтрокаИнфоПроКолонки.Использование = "ДляЭлемента";
					КонецЕсли;
				КонецЕсли;

				Если НомерКолонки > 0 Тогда
					ГруппаКолонки = Группы.Получить(НомерКолонки);
					СтрокаИнфоПроКолонки.Группа =
						?(СтрокаИнфоПроКолонки.ПредставлениеКолонки = ГруппаКолонки, "", ГруппаКолонки);
				КонецЕсли;

				Если ДобавитьКолонку Тогда
					Позиция = Позиция + 1;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Для Каждого Реквизит Из ПараметрыЗагрузки.ЗаполняемыеПриЗагрузкеКолонки Цикл
		ИмяРеквизита     = Реквизит.Ключ;
		СинонимРеквизита = Реквизит.Значение;

		Если ДобавленныеКолонки.Найти(ИмяРеквизита) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;

		ДобавленныеКолонки.Добавить(ИмяРеквизита);

		ТипДанныхКолонки = Новый ОписаниеТипов("Строка");
		Если СоответствиеТиповДанныхКолонок <> Неопределено Тогда
			ТипДанныхКолонкиПереопределенный = СоответствиеТиповДанныхКолонок.Получить(ИмяРеквизита);
			Если ТипДанныхКолонкиПереопределенный <> Неопределено Тогда
				ТипДанныхКолонки = ТипДанныхКолонкиПереопределенный;
			КонецЕсли;
		КонецЕсли;

		ОбязательнаДляЗаполнения = ПараметрыЗагрузки.ОбязательныеКолонки.Найти(ИмяРеквизита) <> Неопределено;
		ВИтогахВсегдаПустая      = ПараметрыЗагрузки.ПустыеВИтогахКолонки.Найти(ИмяРеквизита) <> Неопределено;

		ДобавитьКолонку = Не ТолькоДополнитьКолонки;
		Если ТолькоДополнитьКолонки Тогда
			СтрокаИнфоПроКолонки = ИнформацияПоКолонкам.Найти(ИмяРеквизита, "ИмяКолонки");
			ДобавитьКолонку = (СтрокаИнфоПроКолонки = Неопределено);
		КонецЕсли;

		Если ДобавитьКолонку Тогда
			СтрокаИнфоПроКолонки = ИнформацияПоКолонкам.Добавить();
			СтрокаИнфоПроКолонки.ИмяКолонки            = ИмяРеквизита;
			СтрокаИнфоПроКолонки.ПредставлениеКолонки  = СинонимРеквизита;
			СтрокаИнфоПроКолонки.Позиция               = Позиция;
			СтрокаИнфоПроКолонки.ПозицияВМакете        = -1;
		КонецЕсли;

		СтрокаИнфоПроКолонки.ТипКолонки                = ТипДанныхКолонки;
		СтрокаИнфоПроКолонки.ОбязательнаДляЗаполнения  = ОбязательнаДляЗаполнения;
		СтрокаИнфоПроКолонки.Родитель                  = ИмяРеквизита;
		СтрокаИнфоПроКолонки.ВидимостьВМакете          = Ложь;
		СтрокаИнфоПроКолонки.ВидимостьПриСопоставлении = Истина;
		СтрокаИнфоПроКолонки.Примечание                = НСтр("ru = 'Служебная колонка';
																|en = 'Internal column'");
		СтрокаИнфоПроКолонки.ВИтогахВсегдаПустая       = ВИтогахВсегдаПустая;

		Попытка
			МетаданныеРеквизита = ОбъектМетаданных.Реквизиты.Найти(ИмяРеквизита);
		Исключение
			МетаданныеРеквизита = Неопределено;
		КонецПопытки;

		Если МетаданныеРеквизита <> Неопределено И ОбщегоНазначения.ЭтоОбъектСсылочногоТипа(ОбъектМетаданных) Тогда
			Если МетаданныеРеквизита.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы Тогда
				СтрокаИнфоПроКолонки.Использование = "ДляГруппы";
			ИначеЕсли МетаданныеРеквизита.Использование = Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппыИЭлемента Тогда
				СтрокаИнфоПроКолонки.Использование = "ДляГруппыИЭлемента";
			Иначе
				СтрокаИнфоПроКолонки.Использование = "ДляЭлемента";
			КонецЕсли;
		КонецЕсли;

		Если Группы <> Неопределено И НомерКолонки > 0 Тогда
			СтрокаИнфоПроКолонки.Группа = Группы.Получить(НомерКолонки);
		КонецЕсли;

		Если ДобавитьКолонку Тогда
			Позиция = Позиция + 1;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция ШиринаКолонкиПоТипу(Тип)

	ШиринаКолонки = 20;

	Если Тип.СодержитТип(Тип("Булево")) Тогда 
		ШиринаКолонки = 3;
	ИначеЕсли Тип.СодержитТип(Тип("Число")) Тогда 
		ШиринаКолонки = Тип.КвалификаторыЧисла.Разрядность + 1;
	ИначеЕсли Тип.СодержитТип(Тип("Строка")) Тогда 
		Если Тип.КвалификаторыСтроки.Длина > 0 Тогда 
			ШиринаКолонки = ?(Тип.КвалификаторыСтроки.Длина > 20, 20, Тип.КвалификаторыСтроки.Длина);
		Иначе
			ШиринаКолонки = 20;
		КонецЕсли;
	ИначеЕсли Тип.СодержитТип(Тип("Дата")) Тогда 
		ШиринаКолонки = 12;
	ИначеЕсли Тип.СодержитТип(Тип("УникальныйИдентификатор")) Тогда 
		ШиринаКолонки = 20;
	Иначе
		Для Каждого ТипОбъекта Из Тип.Типы() Цикл
			МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипОбъекта);
			Если ОбщегоНазначения.ЭтоСправочник(МетаданныеОбъекта) Тогда
				Если Не МетаданныеОбъекта.Автонумерация И МетаданныеОбъекта.ДлинаКода > 0  Тогда
					ШиринаКолонки = МетаданныеОбъекта.ДлинаКода + 1;
				КонецЕсли;

				Если МетаданныеОбъекта.ДлинаНаименования > 0  Тогда
					Если МетаданныеОбъекта.ДлинаНаименования > ШиринаКолонки Тогда
						ШиринаКолонки = ?(МетаданныеОбъекта.ДлинаНаименования > 30, 30, МетаданныеОбъекта.ДлинаНаименования + 1);
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли ОбщегоНазначения.ЭтоПеречисление(МетаданныеОбъекта) Тогда
				ДлинаПредставления = СтрДлина(МетаданныеОбъекта.Представление());
				ШиринаКолонки      = ?( ДлинаПредставления > 30, 30, ДлинаПредставления + 1);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Возврат ШиринаКолонки;

КонецФункции

// Создает шапку бланка по информации о колонках.
//
// Параметры:
//   ИнформацияПоКолонкам                     - см. ИнтеграцияСМаркетплейсамиСервер.ИнициализироватьТаблицуИнформацииПоКолонкам.
//   ПараметрыШапки                           - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыШапки.
//   ВысотаЗаголовкаТаблицыЗагружаемогоМакета - Число - высота заголовка таблицы загружаемого макета.
//   
// Возвращаемое значение:
//   ТабличныйДокумент - шапка бланка.
//
Функция ШапкаБланкаДляЗаполненияПоИнформацииПоКолонкам(ИнформацияПоКолонкам, ПараметрыШапки,
			ВысотаЗаголовкаТаблицыЗагружаемогоМакета) Экспорт

	ТабличныйДокумент = Новый ТабличныйДокумент;
	Макет = Справочники.УчетныеЗаписиМаркетплейсов.ПолучитьМакет("ПростойШаблон");

	КолонкиИмеютГруппировку = КолонкиИмеютГруппировку(ИнформацияПоКолонкам);
	Если КолонкиИмеютГруппировку Тогда
		Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
		ОбластьЗаголовокСГруппой = Макет.ПолучитьОбласть("Шапка2Строки");
		ОбластьЗаголовокБезГруппы = Макет.ПолучитьОбласть("Шапка2СтрокиБезГруппы");
		НомерСтроки = 2;
	Иначе
		ОбластьЗаголовокБезГруппы = Макет.ПолучитьОбласть("Заголовок");
		НомерСтроки = 1;
	КонецЕсли;

	РазницаВШапке = ВысотаЗаголовкаТаблицыЗагружаемогоМакета - НомерСтроки;

	ИнформацияПоКолонкам.Сортировать("Позиция");

	Группа = Неопределено;
	ПозицияНачалоГруппы = 1;
	Сдвиг = 0;
	Для Позиция = 0 По ИнформацияПоКолонкам.Количество() -1 Цикл
		Колонка = ИнформацияПоКолонкам.Получить(Позиция);

		Если Колонка.ВидимостьВМакете Тогда
			Если Группа = Неопределено Тогда
				Группа = Колонка.Группа;
			КонецЕсли;

			Если КолонкиИмеютГруппировку Тогда
				Если ПустаяСтрока(Колонка.Группа) Тогда
					ОбластьЗаголовок = ОбластьЗаголовокБезГруппы;
					ОбластьИмяКолонки = ОбластьЗаголовок.Область(НомерСтроки - 1, 1, НомерСтроки + РазницаВШапке, 1);
					ОбластьИмяКолонки.Объединить();
				Иначе
					ОбластьЗаголовок = ОбластьЗаголовокСГруппой;
					ОбластьИмяКолонки = ОбластьЗаголовок.Область(НомерСтроки, 1, НомерСтроки + РазницаВШапке, 1);
					ОбластьИмяКолонки.Объединить();
				КонецЕсли;
			Иначе
				ОбластьЗаголовок = ОбластьЗаголовокБезГруппы;
				ОбластьИмяКолонки = ОбластьЗаголовок.Область(НомерСтроки, 1, НомерСтроки + РазницаВШапке, 1);
				ОбластьИмяКолонки.Объединить();
			КонецЕсли;

			ОбластьИмяКолонки.Имя = Колонка.ИмяКолонки;
			ОбластьИмяКолонки.Расшифровка = Колонка.Группа;
			ОбластьИмяКолонки.Примечание.Текст = Колонка.Примечание;
			Если Колонка.ОбязательнаДляЗаполнения Тогда
				ОбластьИмяКолонки.Шрифт = ШрифтыСтиля.ВажнаяНадписьШрифт;
			Иначе
				ОбластьИмяКолонки.Шрифт = Неопределено;
			КонецЕсли;

			ОбластьИмяКолонки.ШиринаКолонки = ?(Колонка.Ширина = 0, ШиринаКолонкиПоТипу(Колонка.ТипКолонки), Колонка.Ширина);
			ОбластьЗаголовок.Параметры.Заголовок = ?(ПустаяСтрока(Колонка.Синоним), Колонка.ПредставлениеКолонки, Колонка.Синоним);
			ТабличныйДокумент.Присоединить(ОбластьЗаголовок);

			Если КолонкиИмеютГруппировку И Колонка.Группа <> Группа Тогда
				Если Не ПустаяСтрока(Группа) Тогда
					Область = ТабличныйДокумент.Область(1, ПозицияНачалоГруппы, 1, Позиция - Сдвиг);
					Область.Текст = Группа;
					Область.Объединить();
					Область.Обвести(Линия, Линия, Линия, Линия);
				КонецЕсли;
				ПозицияНачалоГруппы = Позиция + 1 - Сдвиг;
				Группа = Колонка.Группа;
			КонецЕсли;
		Иначе
			Сдвиг = Сдвиг + 1;
		КонецЕсли;
	КонецЦикла;

	Если КолонкиИмеютГруппировку И Не ПустаяСтрока(Группа) Тогда
		Область = ТабличныйДокумент.Область(1, ПозицияНачалоГруппы, 1, Позиция - Сдвиг);
		Область.Текст = Группа;
		Область.Объединить();
		Область.Обвести(Линия, Линия, Линия, Линия);
	КонецЕсли;

	ШаблоныДанныхШапки   = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыШапки, "Шаблоны", Новый Массив);
	ПараметрыДанныхШапки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыШапки, "Параметры", Новый Структура);
	МассивСтрокШапки = Новый Массив;

	Если ШаблоныДанныхШапки.Количество() > 0 Тогда
		Для Каждого ДанныеШаблона Из ШаблоныДанныхШапки Цикл
			СтрокаШапки = ДанныеШаблона.Шаблон; // Шаблон состоит из ключевых слов в фигурных скобках {} и параметров в квадратных скобках [],
			//									например: "{Отчет} о {реализации} {№} [Параметр1] {от} [Параметр2]".

			Для Каждого ПараметрШаблона Из ПараметрыДанныхШапки Цикл
				СтрокаШапки = СтрЗаменить(СтрокаШапки,
					"[" + ПараметрШаблона.Ключ + "]",
					?(ЗначениеЗаполнено(ПараметрШаблона.Значение), Строка(ПараметрШаблона.Значение), "_____________"));
			КонецЦикла;
			
			СтрокаШапки = СтрЗаменить(СтрокаШапки, "{", "");
			СтрокаШапки = СтрЗаменить(СтрокаШапки, "}", "");

			МассивСтрокШапки.Добавить(СтрокаШапки);
		КонецЦикла;
	КонецЕсли;

	Если МассивСтрокШапки.Количество() > 0 Тогда
		ИтоговыйТабличныйДокумент = Новый ТабличныйДокумент;
		ОбластьЗаполнения = ИтоговыйТабличныйДокумент.ПолучитьОбласть(1, 1, 1, ТабличныйДокумент.ШиринаТаблицы);
		ОбластьЗаполнения.Область(1, 1, 1, ОбластьЗаполнения.ШиринаТаблицы).Объединить();

		Для Каждого СтрокаШапки Из МассивСтрокШапки Цикл
			ОбластьЗаполнения.ТекущаяОбласть.Текст = СтрокаШапки;
			ИтоговыйТабличныйДокумент.Вывести(ОбластьЗаполнения);
		КонецЦикла;

		ИтоговыйТабличныйДокумент.Вывести(ТабличныйДокумент);
	Иначе
		ИтоговыйТабличныйДокумент = ТабличныйДокумент
	КонецЕсли;

	Возврат ИтоговыйТабличныйДокумент;

КонецФункции

Функция КолонкиИмеютГруппировку(Знач ИнформацияПоКолонкам)

	ГруппыКолонок = Новый Соответствие;
	Для Каждого КолонкаТаблицы Из ИнформацияПоКолонкам Цикл
		ГруппыКолонок.Вставить(КолонкаТаблицы.Группа);
	КонецЦикла;
	Возврат ?(ГруппыКолонок.Количество() > 1, Истина, Ложь);

КонецФункции

// Подготавливает состав результата получения данных по API.
// 
// Возвращаемое значение:
//   Структура - список параметров:
//     * ИнформацияОбОшибке            - см. НоваяОшибка.
//     * АдресТаблицыЗагруженныхДанных - Неопределено, Строка - адрес хранилища с данными.
//     * ПараметрыШапки                - см. ПараметрыШапки.
//
Функция РезультатПолученияДанныхИнтеграционнымиМетодами() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ИнформацияОбОшибке", НоваяОшибка());
	Результат.Вставить("АдресТаблицыЗагруженныхДанных", Неопределено);
	Результат.Вставить("ПараметрыШапки", ПараметрыШапки());

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ЗагрузкаДанных

// Конструктор параметров загрузки табличного документа.
//
// Параметры:
//   УникальныйИдентификаторФормы - УникальныйИдентификатор - уникальный идентификатор формы, для которой нужны параметры.
//
// Возвращаемое значение:
//   Структура:
//     * ДанныеФайла                              - Неопределено, ДвоичныеДанные - данные файла, которые требуется загрузить.
//     * РасширениеФайла                          - Строка - расширение файла.
//     * ЗаполняемыйШаблон                        - Неопределено, ТабличныйДокумент - заполняемый табличный документ.
//     * ПараметрыШапки                           - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыШапки.
//     * ИнформацияПоКолонкам                     - см. ИнтеграцияСМаркетплейсамиСервер.ИнициализироватьТаблицуИнформацииПоКолонкам.
//     * ВысотаЗаголовкаТаблицыЗагружаемогоМакета - Число - высота заголовка таблицы загружаемого макета.
//     * ВысотаЗаголовкаТаблицыМакетаСШаблоном    - Число - высота заголовка таблицы эталонного макета объекта конфигурации.
//     * АдресХранилища                           - Строка - адрес хранилища, в которое будет помещен результат загрузки.
//     * ЛистыШаблона                             - Неопределено - используется одностраничный шаблон.
//                                                - Структура - описание многостраничного шаблона:
//       ** ИмяЛиста                                 - Строка - имя листа.
//       ** ИмяМакетаСШаблоном                       - Строка - имя макета объекта метаданных.
//       ** Служебный                                - Произвольный - дополнительная служебная информация.
//       ** ПараметрыШапки                           - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыШапки.
//       ** ИнформацияПоКолонкам                     - см. ИнтеграцияСМаркетплейсамиСервер.ИнициализироватьТаблицуИнформацииПоКолонкам.
//       ** ВысотаЗаголовкаТаблицыЗагружаемогоМакета - Число - высота заголовка таблицы загружаемого макета.
//       ** ВысотаЗаголовкаТаблицыМакетаСШаблоном    - Число - высота заголовка таблицы эталонного макета объекта конфигурации.
//       ** ШаблонСДанными                           - ТабличныйДокумент - данные в виде табличного документа, которые нужно обработать.
//       ** ЗаполняемыйШаблон                        - Неопределено, ТабличныйДокумент - заполняемый табличный документ.
//
Функция ПараметрыЗагрузкиТабличногоДокумента(УникальныйИдентификаторФормы) Экспорт

	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ДанныеФайла",                              Неопределено);
	ПараметрыЗагрузки.Вставить("РасширениеФайла",                          "");
	ПараметрыЗагрузки.Вставить("ЗаполняемыйШаблон",                        Неопределено);
	ПараметрыЗагрузки.Вставить("ПараметрыШапки",                           ПараметрыШапки());
	ПараметрыЗагрузки.Вставить("ИнформацияПоКолонкам",                     Новый ТаблицаЗначений);
	ПараметрыЗагрузки.Вставить("ВысотаЗаголовкаТаблицыЗагружаемогоМакета", 1);
	ПараметрыЗагрузки.Вставить("ВысотаЗаголовкаТаблицыМакетаСШаблоном",    1);
	ПараметрыЗагрузки.Вставить("АдресХранилища",                           ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификаторФормы));
	ПараметрыЗагрузки.Вставить("ЛистыШаблона",                             Неопределено);

	Возврат ПараметрыЗагрузки;

КонецФункции

// Загружает данные в таблицу значений.
//
// Параметры:
//   ПараметрыЗагрузки - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыЗагрузкиТабличногоДокумента.
//
// Возвращаемое значение:
//   См. ТабличныйДокументВТаблицу.
//
Функция ЗагрузитьФайлВТаблицу(ПараметрыЗагрузки) Экспорт

	//@skip-check missing-temporary-file-deletion
	ПутьКВременномуФайлу = ПолучитьИмяВременногоФайла(ПараметрыЗагрузки.РасширениеФайла);
	ДвоичныеДанные = ПараметрыЗагрузки.ДанныеФайла; // ДвоичныеДанные
	ДвоичныеДанные.Записать(ПутьКВременномуФайлу);

	ДанныеФайла = Новый ТабличныйДокумент;
	ДанныеФайла.Прочитать(ПутьКВременномуФайлу, СпособЧтенияЗначенийТабличногоДокумента.Значение);
	ФайловаяСистема.УдалитьВременныйФайл(ПутьКВременномуФайлу);
	
	Если ПараметрыЗагрузки.ЛистыШаблона <> Неопределено Тогда
		Для Каждого Область Из ДанныеФайла.Области Цикл
			ОбластьИмя = СтрЗаменить(Область.Имя, "ё", "е");
			
			Если ПараметрыЗагрузки.ЛистыШаблона.Свойство(ОбластьИмя) Тогда
				ШаблонСДанными = Новый ТабличныйДокумент;
				ШаблонСДанными.Вывести(ДанныеФайла.ПолучитьОбласть(Область.Имя));
				
				ПараметрыЗагрузки.ЛистыШаблона[ОбластьИмя].ШаблонСДанными = ШаблонСДанными;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Результат = ТабличныйДокументВТаблицу(ДанныеФайла, ПараметрыЗагрузки);

	Возврат Результат;

КонецФункции

// Создает таблицу значений по данным из шаблона и сохраняет ее во временное хранилище.
// 
// Параметры:
//   ШаблонСДанными        - ТабличныйДокумент - данные в виде табличного документа, которые нужно обработать.
//   ПараметрыЗагрузки     - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыЗагрузкиТабличногоДокумента.
//   КакТаблицаЗначений    - Булево - Истина, если требуется вернуть результат в виде таблицы значений.
//   ПроверитьОбязательные - Булево - признак удаления считанных строк, если они не содержат обязательные реквизиты.
//
// Возвращаемое значение:
//   - ТаблицаЗначений - если запрашивается результат в виде таблицы;
//   - Структура - если запрашивается результат в виде структуры см. ЛистТабличногоДокументаВТаблицу без ТаблицаЗагруженныхДанных и:
//       * АдресТаблицыЗагруженныхДанных - Строка - адрес временного хранилища с загруженными данными.
//       * ЛистыШаблона                  - Неопределено - используется одностраничный шаблон.
//                                       - Структура - описание многостраничного шаблона:
//         ** ИмяЛиста                                 - Строка - имя листа.
//         ** ИмяМакетаСШаблоном                       - Строка - имя макета объекта метаданных.
//         ** Служебный                                - Произвольный - дополнительная служебная информация.
//         ** ПараметрыШапки                           - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыШапки.
//         ** ИнформацияПоКолонкам                     - см. ИнтеграцияСМаркетплейсамиСервер.ИнициализироватьТаблицуИнформацииПоКолонкам.
//         ** ВысотаЗаголовкаТаблицыЗагружаемогоМакета - Число - высота заголовка таблицы загружаемого макета.
//         ** ВысотаЗаголовкаТаблицыМакетаСШаблоном    - Число - высота заголовка таблицы эталонного макета объекта конфигурации.
//         ** ШаблонСДанными                           - ТабличныйДокумент - данные в виде табличного документа, которые нужно обработать.
//         ** ЗаполняемыйШаблон                        - Неопределено, ТабличныйДокумент - заполняемый табличный документ.
//
//@skip-check function-return-types
Функция ТабличныйДокументВТаблицу(ШаблонСДанными, Знач ПараметрыЗагрузки, КакТаблицаЗначений = Ложь, ПроверитьОбязательные = Ложь) Экспорт

	ПараметрыПрогресса = ПараметрыПрогресса();
	ПараметрыПрогресса.ШагИтерацииВПроцентах = 30;

	Если ПараметрыЗагрузки.ЛистыШаблона = Неопределено Тогда
		Результат = ЛистТабличногоДокументаВТаблицу(ШаблонСДанными, ПараметрыЗагрузки, ПараметрыПрогресса, ПроверитьОбязательные);

	Иначе
		ШагИтерации = Цел(ПараметрыПрогресса.ШагИтерацииВПроцентах / ПараметрыЗагрузки.ЛистыШаблона.Количество());
		ПараметрыПрогресса.ШагИтерацииВПроцентах = ШагИтерации;
		
		ТаблицаЗагруженныхДанных = Новый ТаблицаЗначений;
		
		Для Каждого КлючИЗначение Из ПараметрыЗагрузки.ЛистыШаблона Цикл
			ПараметрыЗагрузки.ПараметрыШапки                           = ОбщегоНазначения.СкопироватьРекурсивно(КлючИЗначение.Значение.ПараметрыШапки);
			ПараметрыЗагрузки.ЗаполняемыйШаблон                        = КлючИЗначение.Значение.ЗаполняемыйШаблон;
			ПараметрыЗагрузки.ВысотаЗаголовкаТаблицыЗагружаемогоМакета = КлючИЗначение.Значение.ВысотаЗаголовкаТаблицыЗагружаемогоМакета;
			ПараметрыЗагрузки.ВысотаЗаголовкаТаблицыМакетаСШаблоном    = КлючИЗначение.Значение.ВысотаЗаголовкаТаблицыМакетаСШаблоном;
			
			Если ТипЗнч(КлючИЗначение.Значение.ИнформацияПоКолонкам) = Тип("ТаблицаЗначений") Тогда
				ПараметрыЗагрузки.ИнформацияПоКолонкам = КлючИЗначение.Значение.ИнформацияПоКолонкам.Скопировать();
			Иначе
				ПараметрыЗагрузки.ИнформацияПоКолонкам.Очистить();
				Для Каждого ЭлементКоллекции Из КлючИЗначение.Значение.ИнформацияПоКолонкам Цикл
					ЗаполнитьЗначенияСвойств(ПараметрыЗагрузки.ИнформацияПоКолонкам.Добавить(), ЭлементКоллекции);
				КонецЦикла;
			КонецЕсли;
			
			Результат = ЛистТабличногоДокументаВТаблицу(КлючИЗначение.Значение.ШаблонСДанными, ПараметрыЗагрузки, ПараметрыПрогресса, ПроверитьОбязательные);
			Результат.ТаблицаЗагруженныхДанных.ЗаполнитьЗначения(КлючИЗначение.Значение.Служебный, "Служебный");

			ПараметрыПрогресса.ПрогрессВыполнения = ПараметрыПрогресса.ПрогрессВыполнения + ШагИтерации;
			
			Для Каждого ЭлементКоллекции Из Результат.ТаблицаЗагруженныхДанных.Колонки Цикл
				Если ТаблицаЗагруженныхДанных.Колонки.Найти(ЭлементКоллекции.Имя) = Неопределено Тогда
					ТаблицаЗагруженныхДанных.Колонки.Добавить(ЭлементКоллекции.Имя, Новый ОписаниеТипов(ЭлементКоллекции.ТипЗначения), ЭлементКоллекции.Заголовок);
				КонецЕсли;
			КонецЦикла;
			
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Результат.ТаблицаЗагруженныхДанных, ТаблицаЗагруженныхДанных);
			
			КлючИЗначение.Значение.ПараметрыШапки = Результат.ПараметрыШапки;
			КлючИЗначение.Значение.ШаблонСДанными = Результат.ШаблонСДанными;
		КонецЦикла;
		
		Для Каждого СтрокаТаблицыЗначений Из ТаблицаЗагруженныхДанных Цикл
			СтрокаТаблицыЗначений.НомерСтроки = ТаблицаЗагруженныхДанных.Индекс(СтрокаТаблицыЗначений) + 1;
		КонецЦикла;
		
		Результат.ТаблицаЗагруженныхДанных = ТаблицаЗагруженныхДанных.Скопировать();
	КонецЕсли;
	
	Если КакТаблицаЗначений Тогда
		Возврат Результат.ТаблицаЗагруженныхДанных;
	КонецЕсли;
	
	АдресХранилища = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗагрузки, "АдресХранилища", "");
	Результат.Вставить("АдресТаблицыЗагруженныхДанных", ПоместитьВоВременноеХранилище(Результат.ТаблицаЗагруженныхДанных, АдресХранилища));
	Результат.Удалить("ТаблицаЗагруженныхДанных");
	Результат.Вставить("ЛистыШаблона", ПараметрыЗагрузки.ЛистыШаблона);
	
	Возврат Результат;

КонецФункции

// Создает таблицу значений по данным из шаблона и сохраняет ее во временное хранилище.
// 
// Параметры:
//   ШаблонСДанными        - ТабличныйДокумент - данные в виде табличного документа, которые нужно обработать.
//   ПараметрыЗагрузки     - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыЗагрузкиТабличногоДокумента. 
//   ПараметрыПрогресса    - Структура - характеристики прогресса, см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыПрогресса.
//   ПроверитьОбязательные - Булево - признак удаления считанных строк, если они не содержат обязательные реквизиты.
//
// Возвращаемое значение:
//   Структура Из КлючИЗначение:
//     * ШаблонБезОшибокЗаполнения - Булево - признак отсутствия ошибок.
//     * ТаблицаЗагруженныхДанных  - Строка - адрес временного хранилища с загруженными данными.
//     * ПараметрыШапки            - Структура Из КлючИЗначение - параметры шапки.
//     * ШаблонСДанными            - ТабличныйДокумент - загружаемый табличный документ.
//
Функция ЛистТабличногоДокументаВТаблицу(ШаблонСДанными, Знач ПараметрыЗагрузки, Знач ПараметрыПрогресса, ПроверитьОбязательные = Ложь)

	Результат = Новый Структура;
	Результат.Вставить("ШаблонБезОшибокЗаполнения", Истина);
	Результат.Вставить("ТаблицаЗагруженныхДанных",  Новый ТаблицаЗначений);
	Результат.Вставить("ПараметрыШапки",            Новый Структура);
	Результат.Вставить("ШаблонСДанными",            Новый ТабличныйДокумент);
	
	ТаблицаСДаннымиМакета = ПолучитьТаблицуСДаннымиИзМакета(ШаблонСДанными);

	ИнформацияПоКолонкам                     = ПараметрыЗагрузки.ИнформацияПоКолонкам;
	ПараметрыШапки                           = ПараметрыЗагрузки.ПараметрыШапки;
	ЗаполняемыйШаблон                        = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗагрузки, "ЗаполняемыйШаблон", Неопределено);
	ВысотаЗаголовкаТаблицыЗагружаемогоМакета = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗагрузки, "ВысотаЗаголовкаТаблицыЗагружаемогоМакета", 1);
	ВысотаЗаголовкаТаблицыМакетаСШаблоном    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗагрузки, "ВысотаЗаголовкаТаблицыМакетаСШаблоном", 1);

	ЗагружатьВТабличныйДокумент = (ЗаполняемыйШаблон <> Неопределено);

	ОписаниеТипаСтрока = Новый ОписаниеТипов("Строка");

	ТаблицаИнформацияПоКолонкам = ИнформацияПоКолонкам.Скопировать();

	СтрокаСДаннымиТаблицы = 0;
	ИнициализироватьКолонкиИШапку(ТаблицаИнформацияПоКолонкам, ТаблицаСДаннымиМакета, ПараметрыШапки, СтрокаСДаннымиТаблицы);

	Отбор = Новый Структура("ВИтогахВсегдаПустая", Истина);
	КоличествоПустыхКолонокДляОпределенияСтрокиИтогов = ТаблицаИнформацияПоКолонкам.НайтиСтроки(Отбор).Количество();

	Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
	АвтоЦвет = Новый Цвет();

	ТаблицаЗагруженныхДанных = Новый ТаблицаЗначений;

	Для Каждого Колонка Из ТаблицаИнформацияПоКолонкам Цикл
		Если Колонка.ТипКолонки = Неопределено Тогда
			ТипКолонки = ОписаниеТипаСтрока;
		Иначе
			ТипКолонки = Колонка.ТипКолонки;
		КонецЕсли;
		ТаблицаЗагруженныхДанных.Колонки.Добавить(Колонка.ИмяКолонки, ТипКолонки, Колонка.ПредставлениеКолонки);
	КонецЦикла;

	ДобавитьСтандартныеКолонки(ТаблицаЗагруженныхДанных);

	Если ЗагружатьВТабличныйДокумент Тогда
		Отбор = Новый Структура("ВидимостьВМакете", Истина);
		КолонкиМакета = ТаблицаИнформацияПоКолонкам.НайтиСтроки(Отбор);

		СтрокаНачалаВывода = ВысотаЗаголовкаТаблицыЗагружаемогоМакета + ПараметрыШапки.Шаблоны.Количество();
		КоличествоКолонокШаблона = КолонкиМакета.Количество();

		ОбластьВывода = ЗаполняемыйШаблон.ПолучитьОбласть(СтрокаНачалаВывода, 1,
			СтрокаНачалаВывода , КоличествоКолонокШаблона);

		Счетчик = 1;
		Для Каждого Колонка Из КолонкиМакета Цикл
			Если ЗначениеЗаполнено(Колонка.ПозицияВМакете) Тогда
				ОбластьЗаполнения = ОбластьВывода.Область(1, Счетчик, 1, Счетчик);
				ОбластьЗаполнения.Параметр = Колонка.ИмяКолонки;
				ОбластьЗаполнения.Заполнение = ТипЗаполненияОбластиТабличногоДокумента.Параметр;

				Счетчик = Счетчик + 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	СтрокаСДаннымиТаблицы = СтрокаСДаннымиТаблицы
		+ ?(ЗагружатьВТабличныйДокумент, ВысотаЗаголовкаТаблицыЗагружаемогоМакета, ВысотаЗаголовкаТаблицыМакетаСШаблоном) - 1;
	КорректировкаНомераСтроки = СтрокаСДаннымиТаблицы - 1;
	
	ВсегоСтрок = ТаблицаСДаннымиМакета.Количество() - СтрокаСДаннымиТаблицы;
	ПроцентВыполнения = 0;

	// Начало чтения со строки за шапкой.
	Для НомерСтроки = СтрокаСДаннымиТаблицы По ТаблицаСДаннымиМакета.Количество() Цикл
		УстановитьПроцентПрогресса(ВсегоСтрок, НомерСтроки - СтрокаСДаннымиТаблицы, ПроцентВыполнения, ПараметрыПрогресса);

		СтрокаТаблицы = ТаблицаСДаннымиМакета.Получить(НомерСтроки - 1);

		СтрокаТаблицыПустая     = Истина;
		КоличествоКолонокИтогов = 0;
		СтрокаБезОшибок         = Истина;
		ЗаполненыОбязательные   = Ложь;

		НоваяСтрока = ТаблицаЗагруженныхДанных.Добавить();
		НоваяСтрока.НомерСтроки = НомерСтроки - КорректировкаНомераСтроки;

		НомерКолонки = 0;
		Для Каждого Колонка Из ТаблицаИнформацияПоКолонкам Цикл
			Если Колонка.ПозицияВМакете = -1 Тогда
				Продолжить;
			КонецЕсли;

			НомерКолонки = НомерКолонки + 1;

			ЗначениеЯчейки = СокрЛП(СтрокаТаблицы["Колонка_" + Колонка.ПозицияВМакете]);
			ЗначениеЯчейки = ?(ЗначениеЯчейки = "0", "", ЗначениеЯчейки);

			Если СтрокаТаблицыПустая Тогда
				СтрокаТаблицыПустая = Не ЗначениеЗаполнено(ЗначениеЯчейки);
			КонецЕсли;

			КоличествоКолонокИтогов = КоличествоКолонокИтогов + ?(Колонка.ВИтогахВсегдаПустая И Не ЗначениеЗаполнено(ЗначениеЯчейки), 1, 0);

			ИмяКолонки = Колонка.ИмяКолонки;

			Если ЗначениеЗаполнено(ЗначениеЯчейки) Тогда
				НоваяСтрока[ИмяКолонки] = ПриведениеЗначенияКПростомуТипу(ЗначениеЯчейки, Колонка.ТипКолонки);
			КонецЕсли;

			Если Не ЗначениеЗаполнено(НоваяСтрока[ИмяКолонки]) И ЗначениеЗаполнено(ЗначениеЯчейки) Тогда
				НоваяСтрока[ИмяКолонки] = ЗначениеЯчейки(Колонка.ТипКолонки.Типы(), ЗначениеЯчейки);
			КонецЕсли;

			Если ЗагружатьВТабличныйДокумент Тогда
				Ячейка = ОбластьВывода.Область(1, НомерКолонки, 1, НомерКолонки);
				Если Колонка.ОбязательнаДляЗаполнения И Не ЗначениеЗаполнено(НоваяСтрока[ИмяКолонки]) Тогда
					Ячейка.ЦветФона  = ЦветаСтиля.ЦветФонаВнимание;
					Ячейка.ЦветРамки = WebЦвета.СеребристоСерый;
					Ячейка.Обвести(Линия,, Линия, Линия);
					СтрокаБезОшибок = Ложь;
				Иначе
					Ячейка.ЦветФона = АвтоЦвет;
				КонецЕсли;
				
			ИначеЕсли ПроверитьОбязательные Тогда
				Если Колонка.ОбязательнаДляЗаполнения И ЗначениеЗаполнено(НоваяСтрока[ИмяКолонки]) Тогда
					ЗаполненыОбязательные = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;

		Если СтрокаТаблицыПустая 
				Или ПроверитьОбязательные И Не ЗаполненыОбязательные Тогда
			ТаблицаЗагруженныхДанных.Удалить(НоваяСтрока);
			КорректировкаНомераСтроки = КорректировкаНомераСтроки + 1;
		ИначеЕсли КоличествоПустыхКолонокДляОпределенияСтрокиИтогов <> 0 И КоличествоКолонокИтогов = КоличествоПустыхКолонокДляОпределенияСтрокиИтогов Тогда
			ТаблицаЗагруженныхДанных.Удалить(НоваяСтрока);
			// После обнаружения строки итогов прерываем чтение.
			Прервать;
		Иначе
			Результат.ШаблонБезОшибокЗаполнения = СтрокаБезОшибок;

			Если ЗагружатьВТабличныйДокумент Тогда
				ОбластьВывода.Параметры.Заполнить(НоваяСтрока);
				ЗаполняемыйШаблон.Вывести(ОбластьВывода);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Результат.ТаблицаЗагруженныхДанных = ТаблицаЗагруженныхДанных.Скопировать();
	Результат.ПараметрыШапки           = ПараметрыШапки;

	ЗаполненныеШаблоны = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыШапки, "ЗаполненныеШаблоны", Новый Массив);

	Если ЗагружатьВТабличныйДокумент Тогда
		Если ЗаполненныеШаблоны.Количество() > 0 Тогда
			ТабличныйДокумент = Новый ТабличныйДокумент;

			// Вывести строки шапки в результирующий табличный документ.
			ОбластьЗаполнения = ТабличныйДокумент.ПолучитьОбласть(1, 1, 1, ЗаполняемыйШаблон.ШиринаТаблицы);
			ОбластьЗаполнения.Область(1, 1, 1, ЗаполняемыйШаблон.ШиринаТаблицы).Объединить();

			Для Каждого СтрокаШапки Из ЗаполненныеШаблоны Цикл
				ОбластьЗаполнения.ТекущаяОбласть.Текст = СтрокаШапки;
				ТабличныйДокумент.Вывести(ОбластьЗаполнения);
			КонецЦикла;

			// Вывести в результирующий табличный документ заполненный загружаемыми строками табличный документ.
			ТабличныйДокумент.Вывести(ЗаполняемыйШаблон);

			Результат.Вставить("ШаблонСДанными", ТабличныйДокумент);
		Иначе
			Результат.Вставить("ШаблонСДанными", ЗаполняемыйШаблон);
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Считывает данные табличного документа в таблицу значений.
//
// Параметры:
//   МакетСДанными             - ТабличныйДокумент - данные, прочитанные из файла.
//   МаксимальнаяШиринаТаблицы - Число - максимальное количество перебираемых строк при поиске колонок шапки.
//
// Возвращаемое значение:
//   ТаблицаЗначений, ДеревоЗначений - таблица с составом колонок из файла, колонки именуются с префиксом "Колонка_".
//
Функция ПолучитьТаблицуСДаннымиИзМакета(МакетСДанными, МаксимальнаяШиринаТаблицы = 100)

	ТабличныйДокумент = Новый ТабличныйДокумент;

	КоличествоКолонок = ?(МакетСДанными.ШиринаТаблицы = 0, 1, Мин(МакетСДанными.ШиринаТаблицы, МаксимальнаяШиринаТаблицы));
	Для НомерКолонки = 1 По КоличествоКолонок Цикл
		АдресЯчейки = "R1" + "C" + Формат(НомерКолонки, "ЧГ=0");
		Ячейка = ТабличныйДокумент.Область(АдресЯчейки);
		Ячейка.Текст = "Колонка_" + Формат(НомерКолонки, "ЧГ=0");
	КонецЦикла;

	ТабличныйДокумент.Вывести(МакетСДанными);

	Построитель = Новый ПостроительЗапроса;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабличныйДокумент.Область());
	Построитель.Выполнить();
	ТаблицаСДаннымиИзМакета = Построитель.Результат.Выгрузить();

	Возврат ТаблицаСДаннымиИзМакета;

КонецФункции

Функция ЗначениеЯчейки(Типы, ЗначениеЯчейки)

	ДанныеЯчейки = "";
	Для Каждого ТипДанных Из Типы Цикл
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипДанных);
		
		Если МетаданныеОбъекта = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Если ОбщегоНазначения.ЭтоСправочник(МетаданныеОбъекта) Тогда
			Если Не МетаданныеОбъекта.Автонумерация И МетаданныеОбъекта.ДлинаКода > 0 Тогда
				ДанныеЯчейки = Справочники[МетаданныеОбъекта.Имя].НайтиПоКоду(ЗначениеЯчейки, Истина);
			КонецЕсли;

			Если Не ЗначениеЗаполнено(ДанныеЯчейки) Тогда
				ДанныеЯчейки = Справочники[МетаданныеОбъекта.Имя].НайтиПоНаименованию(ЗначениеЯчейки, Истина);
			КонецЕсли;

			Если Не ЗначениеЗаполнено(ДанныеЯчейки) Тогда
				ДанныеЯчейки = Справочники[МетаданныеОбъекта.Имя].НайтиПоКоду(ЗначениеЯчейки, Истина);
			КонецЕсли;
		ИначеЕсли ОбщегоНазначения.ЭтоПеречисление(МетаданныеОбъекта) Тогда
			Для Каждого ЗначениеПеречисления Из МетаданныеОбъекта.ЗначенияПеречисления Цикл // ОбъектМетаданныхПеречисление
				Если Строка(ЗначениеПеречисления) = СокрЛП(ЗначениеЯчейки) Тогда
					ДанныеЯчейки = Перечисления[МетаданныеОбъекта.Имя][ЗначениеПеречисления.Имя];
					Прервать;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ОбщегоНазначения.ЭтоПланСчетов(МетаданныеОбъекта) Тогда
			ДанныеЯчейки = ПланыСчетов[МетаданныеОбъекта.Имя].НайтиПоКоду(ЗначениеЯчейки);
			Если ДанныеЯчейки.Пустая() Тогда
				ДанныеЯчейки = ПланыСчетов[МетаданныеОбъекта.Имя].НайтиПоНаименованию(ЗначениеЯчейки, Истина);
			КонецЕсли;
		ИначеЕсли ОбщегоНазначения.ЭтоПланВидовХарактеристик(МетаданныеОбъекта) Тогда
			Если НЕ МетаданныеОбъекта.Автонумерация И МетаданныеОбъекта.ДлинаКода > 0 Тогда
				ДанныеЯчейки = ПланыВидовХарактеристик[МетаданныеОбъекта.Имя].НайтиПоКоду(ЗначениеЯчейки);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(ДанныеЯчейки) Тогда
				ДанныеЯчейки = ПланыВидовХарактеристик[МетаданныеОбъекта.Имя].НайтиПоНаименованию(ЗначениеЯчейки, Истина);
			КонецЕсли;
		Иначе
			ДанныеЯчейки = СтрЗаменить(ЗначениеЯчейки, Символы.НПП, "");
		КонецЕсли;
		Если ЗначениеЗаполнено(ДанныеЯчейки) Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДанныеЯчейки;
	
КонецФункции

// Приводит строковое значение к требуемому простому типу.
//
// Параметры:
//   Значение - Строка - преобразуемое значение.
//   ОписаниеТипов - ОписаниеТипов из Дата, Булево, Строка, Число - ожидаемый тип значения.
//
// Возвращаемое значение:
//   Дата, Булево, Строка, Число - преобразованное значение.
//
Функция ПриведениеЗначенияКПростомуТипу(Значение, ОписаниеТипов) Экспорт

	Для Каждого Тип Из ОписаниеТипов.Типы() Цикл
		Если Тип = Тип("Дата") Тогда
			Возврат СтроковыеФункцииКлиентСервер.СтрокаВДату(Значение, ОписаниеТипов.КвалификаторыДаты.ЧастиДаты);

		ИначеЕсли Тип = Тип("Булево") Тогда
			ОписаниеТипаБулево = Новый ОписаниеТипов("Булево");
			Возврат ОписаниеТипаБулево.ПривестиЗначение(Значение);

		ИначеЕсли Тип = Тип("Строка") Тогда
			ОписаниеТипаБулево = Новый ОписаниеТипов("Строка");
			Возврат ОписаниеТипаБулево.ПривестиЗначение(СтрЗаменить(Значение, Символы.НПП, ""));

		ИначеЕсли Тип = Тип("Число") Тогда
			НецифровыеСимволы = СтрСоединить(СтрРазделить(Значение, "1234567890,."));
			Значение = СтрСоединить(СтрРазделить(Значение, НецифровыеСимволы));
			ОписаниеТипаЧисло = Новый ОписаниеТипов("Число");
			Возврат ОписаниеТипаЧисло.ПривестиЗначение(Значение);

		КонецЕсли;
	КонецЦикла;

	Возврат Значение;

КонецФункции

Функция ТекстЯчейкиКРазбору(Знач ТекстЯчейки)

	ЛишниеСимволы = "[]{}";
	ТекстЯчейки = СтрРазделить(ТекстЯчейки, ЛишниеСимволы, Ложь);
	ТекстЯчейки = СтрСоединить(ТекстЯчейки, " ");
	ТекстЯчейки = СтрРазделить(ТекстЯчейки, " ", Ложь);

	// Удалим лишние пробелы.
	Для Инд = 0 По ТекстЯчейки.Количество() - 1 Цикл
		ТекстЯчейки[Инд] = СокрЛП(ТекстЯчейки[Инд]);
	КонецЦикла;

	ТекстЯчейки = СтрСоединить(ТекстЯчейки, " ");

	Возврат ТекстЯчейки;

КонецФункции

// Заполняет параметры шапки и определяет колонки из загружаемых данных.
//
// Параметры:
//   ИнформацияПоКолонкам  - см. ИнтеграцияСМаркетплейсамиСервер.ИнициализироватьТаблицуИнформацииПоКолонкам.
//   ТаблицаСДаннымиМакета - ТаблицаЗначений - 
//   ПараметрыШапки        - Структура - 
//   СтрокаСДаннымиТаблицы - Число - номер строки, с которой начинается чтение данных строк таблицы.
//   ВысотаШапки           - Число - максимальное количество перебираемых строк при поиске колонок шапки.
//
Процедура ИнициализироватьКолонкиИШапку(ИнформацияПоКолонкам, ТаблицаСДаннымиМакета, ПараметрыШапки = Неопределено,
			СтрокаСДаннымиТаблицы = 0, ВысотаШапки = 100)

	Для Каждого Строка Из ИнформацияПоКолонкам Цикл
		Строка.ПозицияВМакете = -1;
	КонецЦикла;

	КолонокКПоиску = ИнформацияПоКолонкам.Количество();
	НомерСтроки = 1;

	// Чтение данных шапки.
	ШаблоныДанныхШапки   = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыШапки, "Шаблоны",   Новый Массив);
	ПараметрыДанныхШапки = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыШапки, "Параметры", Новый Структура);

	// Очистка значений параметров.
	Для Каждого ПараметрДанныхШапки Из ПараметрыДанныхШапки Цикл
		ПараметрыДанныхШапки[ПараметрДанныхШапки.Ключ] = "";
	КонецЦикла;

	МассивСтрокШапки    = Новый Массив; // Для формирования заголовка в ЗаполняемыйШаблон.
	ОбработанныеШаблоны = Новый Массив; // Шаблоны, исключаемые из обработки.
	ОбработанныеЯчейки  = Новый Массив; // Ячейки, исключаемые из обработки.

	НомерОбработаннойСтроки = 1;

	Если ШаблоныДанныхШапки.Количество() > 0 Тогда
		Для Каждого СтрокаТаблицы Из ТаблицаСДаннымиМакета Цикл
			Если НомерСтроки = ВысотаШапки Тогда
				Прервать;
			КонецЕсли;

			Если ШаблоныДанныхШапки.Количество() = ОбработанныеШаблоны.Количество() Тогда
				Прервать;
			КонецЕсли;

			Для НомерКолонки = 1 По ТаблицаСДаннымиМакета.Колонки.Количество() Цикл
				АдресЯчейки = Строка(НомерСтроки) + ";" + Строка(НомерКолонки);
				Если ОбработанныеЯчейки.Найти(АдресЯчейки) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;

				Колонка = ТаблицаСДаннымиМакета.Колонки.Получить(НомерКолонки - 1);

				ЗначениеЯчейки = СокрЛП(СтрокаТаблицы[Колонка.Имя]);

				Если ПустаяСтрока(ЗначениеЯчейки) Тогда
					Продолжить;
				КонецЕсли;

				ШаблонНайден = Ложь;

				Для Каждого ДанныеШаблона Из ШаблоныДанныхШапки Цикл
					Если ШаблонНайден Тогда
						Прервать;
					КонецЕсли;

					Шаблон = ДанныеШаблона.Шаблон;  // Шаблон состоит ключевых слов в фигурных скобках ({}), параметров в квадратных скобках ([])
													// и любых иных слов и символов, например:
													// "{Отчет} {реализации} {№} [Параметр1] {от} [Параметр2]".
													// Шаблон - это отдельная ячейка в области ДанныеШапки макета используемого объекта метаданных.

					Если ОбработанныеШаблоны.Найти(Шаблон) <> Неопределено Тогда
						Продолжить;
					КонецЕсли;

					КлючевыеСловаШаблона = ДанныеШаблона.КлючевыеСловаШаблона;
					ПараметрыШаблона     = ДанныеШаблона.ПараметрыШаблона;
					ШиринаОбластиПоиска  = ДанныеШаблона.ШиринаОбластиПоиска;
					ВысотаОбластиПоиска  = ДанныеШаблона.ВысотаОбластиПоиска;

					ВсегоСовпадений = КлючевыеСловаШаблона.Количество();

					Если ВсегоСовпадений = 0 Тогда // Нет ключевых слов поиска.
						Продолжить;
					КонецЕсли;

					ЗначениеОбласти = ЗначениеЯчейки;

					ПоследняяСтрокаОбласти = НомерСтроки;

					СодержимоеЯчеек = Новый Массив;
					СодержимоеЯчеек.Добавить(Новый Структура("Строка, Колонка, СодержимоеЯчейки",
						НомерСтроки, НомерКолонки, ЗначениеЯчейки));
					НачальнаяСтрокаОбласти  = НомерСтроки;
					НачальнаяКолонкаОбласти = НомерКолонки;
					КонечнаяСтрокаОбласти  = НомерСтроки;
					КонечнаяКолонкаОбласти = НомерКолонки;

					// Чтение данных из области поиска.
					// Область поиска задается в примечании ячейки области ДанныеШапки макета используемого объекта метаданных в формате:
					// "ШиринаОбластиПоиска = 8
					//  ВысотаОбластиПоиска = 4".
					Если ВысотаОбластиПоиска <> 1 Или ШиринаОбластиПоиска <> 1 Тогда
						НачальныйНомерСтроки   = НомерСтроки + ?(ШиринаОбластиПоиска > 1, 0, 1);
						НачальныйНомерКолонки  = НомерКолонки + ?(ШиринаОбластиПоиска > 1, 1, 0);
						ПоследняяСтрокаОбласти = Мин(НомерСтроки + ВысотаОбластиПоиска - 1, ТаблицаСДаннымиМакета.Количество());
						ВсегоКолонокОбласти    = Мин(НомерКолонки + ШиринаОбластиПоиска - 1, ТаблицаСДаннымиМакета.Колонки.Количество());

						Для НомерСтрокиОбласти = НачальныйНомерСтроки По ПоследняяСтрокаОбласти Цикл
							СтрокаОбласти = ТаблицаСДаннымиМакета.Получить(НомерСтрокиОбласти - 1);
							Для НомерКолонкиОбласти = НачальныйНомерКолонки По ВсегоКолонокОбласти Цикл
								ЗначениеЯчейкиОбласти = СокрЛП(СтрокаОбласти["Колонка_" + (НомерКолонкиОбласти)]);

								СодержимоеЯчеек.Добавить(Новый Структура("Строка, Колонка, СодержимоеЯчейки",
									НомерСтрокиОбласти, НомерКолонкиОбласти, ЗначениеЯчейкиОбласти));

								Если ПустаяСтрока(ЗначениеЯчейкиОбласти) Тогда
									Продолжить;
								КонецЕсли;

								ЗначениеОбласти = ЗначениеОбласти + Символы.ПС + ЗначениеЯчейкиОбласти;
							КонецЦикла;

							НачальныйНомерКолонки = НомерКолонки;
						КонецЦикла;

						НомерКолонки = ВсегоКолонокОбласти;
					КонецЕсли;

					// Удаление из шаблона незначимых частей для поиска параметров.
					ПодготовленныйШаблон = СтрЗаменить(Шаблон, "{", Символы.ПС + "{");
					ПодготовленныйШаблон = СтрЗаменить(ПодготовленныйШаблон, "{", Символы.ПС + "{");
					ПодготовленныйШаблон = СтрЗаменить(ПодготовленныйШаблон, "}", "}" + Символы.ПС);
					ПодготовленныйШаблон = СтрЗаменить(ПодготовленныйШаблон, "[", Символы.ПС + "[");
					ПодготовленныйШаблон = СтрЗаменить(ПодготовленныйШаблон, "]", "]" + Символы.ПС);
					ЗначимыеЧастиШаблона = СтрРазделить(ПодготовленныйШаблон, Символы.ПС, Ложь);

					Инд = ЗначимыеЧастиШаблона.Количество() - 1;
					Пока Инд >=0 Цикл
						ЧастьШаблона = ЗначимыеЧастиШаблона[Инд];
						Если СтрНайти(ЧастьШаблона, "{") = 0 И СтрНайти(ЧастьШаблона, "[") = 0 Тогда
							ЗначимыеЧастиШаблона.Удалить(Инд);
						КонецЕсли;
						Инд = Инд - 1;
					КонецЦикла;

					// Поиск ключевых слов.
					ЗначениеОбласти = ТекстЯчейкиКРазбору(ЗначениеОбласти);
					ШаблонОбласти   = "";

					НачальнаяПозиция = 1;
					Для Каждого КлючевоеСловоШаблона Из КлючевыеСловаШаблона Цикл
						Позиция = СтрНайти(ЗначениеОбласти, КлючевоеСловоШаблона);
						Если Позиция = 0 Тогда
							Продолжить;
						КонецЕсли;

						ДлинаСлова = СтрДлина(КлючевоеСловоШаблона);
						Замена = Сред(ЗначениеОбласти, Позиция, ДлинаСлова);

						// Анализ содержимого до ключевого слова, т.к. между ключевыми словами может содержаться параметр.
						СтрокаДоСловаЗамены = "";
						ДобавлятьСловоДоЗамены = Ложь;
						ИндЗначимойЧасти = ЗначимыеЧастиШаблона.Найти("{" + КлючевоеСловоШаблона + "}");
						Если ИндЗначимойЧасти <> Неопределено И ИндЗначимойЧасти > 0 Тогда
							ПредыдущаяЗначимаяЧасть = ЗначимыеЧастиШаблона[ИндЗначимойЧасти - 1];
							Если СтрНайти(ПредыдущаяЗначимаяЧасть, "[") > 0 Тогда
								СтрокаДоСловаЗамены = Сред(ЗначениеОбласти, НачальнаяПозиция, ?(НачальнаяПозиция = Позиция, 0, Позиция - НачальнаяПозиция));
								ДобавлятьСловоДоЗамены = Истина;
								Если ПустаяСтрока(СтрокаДоСловаЗамены) Тогда
									СтрокаДоСловаЗамены = " ";
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;

						// Заполнение шаблона области.
						ЗначениеОбласти = Лев(ЗначениеОбласти, Позиция - 1) + Сред(ЗначениеОбласти, Позиция + ДлинаСлова);
						ШаблонОбласти   = ШаблонОбласти + ?(ДобавлятьСловоДоЗамены, Символы.ПС + СокрЛП(СтрокаДоСловаЗамены), "")
							+ Символы.ПС +  "{" + Замена + "}";

						НачальнаяПозиция = Позиция;
						ВсегоСовпадений = ВсегоСовпадений - 1;
					КонецЦикла;

					ШаблонОбласти = ШаблонОбласти + Символы.ПС + СокрЛП(Сред(ЗначениеОбласти, НачальнаяПозиция));
					СоставЯчейки = СтрРазделить(ШаблонОбласти, Символы.ПС, Ложь);

					ШаблонНайден = Истина;
					НайденыПараметры = Ложь;
					СтрокаШапки = Шаблон;

					// Заполнение параметров.
					ИндексПараметра = 0;
					Для ИндексЧасти = 0 По СоставЯчейки.Количество() - 1 Цикл
						Если ИндексПараметра > ПараметрыШаблона.Количество() - 1 Тогда
							Прервать;
						КонецЕсли;

						Если СтрНайти(СоставЯчейки[ИндексЧасти], "{") = 0 Тогда
							Попытка
								ЗначениеПараметра = СоставЯчейки[ИндексЧасти];
								ПараметрыДанныхШапки[ПараметрыШаблона[ИндексПараметра]] = ЗначениеПараметра;

								СтрокаШапки = СтрЗаменить(СтрокаШапки, "[" + ПараметрыШаблона[ИндексПараметра] + "]", ЗначениеПараметра);

								НайденыПараметры = Истина;
								ИндексПараметра = ИндексПараметра + 1;
							Исключение
								Прервать;
							КонецПопытки;
						КонецЕсли;

						// Получим обработанные ячейки.
						Инд = 0;
						Для Каждого СодержимоеЯчейки Из СодержимоеЯчеек Цикл
							ИскомаяЧасть = СтрЗаменить(СоставЯчейки[ИндексЧасти], "{", "");
							ИскомаяЧасть = СтрЗаменить(ИскомаяЧасть, "}", "");
							Если СтрНайти(СодержимоеЯчейки.СодержимоеЯчейки, ИскомаяЧасть) > 0 Тогда
								КонечнаяСтрокаОбласти  = Макс(КонечнаяСтрокаОбласти, СодержимоеЯчейки.Строка);
								КонечнаяКолонкаОбласти = Макс(КонечнаяКолонкаОбласти, СодержимоеЯчейки.Колонка);
								Прервать;
							КонецЕсли;

							Инд = Инд + 1;
						КонецЦикла;
					КонецЦикла;

					// Заполнение строки шапки для вывода в ЗаполняемыйШаблон.
					Если НайденыПараметры Тогда
						СтрокаШапки = СтрЗаменить(СтрокаШапки, "{", "");
						СтрокаШапки = СтрЗаменить(СтрокаШапки, "}", "");
						МассивСтрокШапки.Добавить(СтрокаШапки);
						ОбработанныеШаблоны.Добавить(Шаблон);
					КонецЕсли;

					// Добавление обработанных ячеек области текущего шаблона для исключения из анализа других шаблонов.
					Для СтрокаЯчейки = НачальнаяСтрокаОбласти По КонечнаяСтрокаОбласти Цикл
						Для КолонкаЯчейки = НачальнаяКолонкаОбласти По КонечнаяКолонкаОбласти Цикл
							АдресЯчейки = Строка(СтрокаЯчейки) + ";" + Строка(КолонкаЯчейки);
							ОбработанныеЯчейки.Добавить(АдресЯчейки);
						КонецЦикла;
					КонецЦикла;

					НомерОбработаннойСтроки = Макс(НомерСтроки, КонечнаяСтрокаОбласти - 1);
				КонецЦикла;
			КонецЦикла;

			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
	КонецЕсли;

	Если МассивСтрокШапки.Количество() > 0 Тогда
		ПараметрыШапки.Вставить("ЗаполненныеШаблоны", МассивСтрокШапки);
	КонецЕсли;

	Если ОбработанныеШаблоны.Количество() = 0 Тогда
		НомерСтрокиВыборки = 1;
	Иначе
		НомерСтрокиВыборки = НомерОбработаннойСтроки + 1;
	КонецЕсли;

	// Чтение заголовка таблицы.
	Для НомерСтроки = НомерСтрокиВыборки По ТаблицаСДаннымиМакета.Количество() - 1 Цикл
		СтрокаТаблицы = ТаблицаСДаннымиМакета[НомерСтроки - 1];

		Если НомерСтроки = ВысотаШапки Тогда
			Прервать;
		КонецЕсли;

		Если КолонокКПоиску = 0 Тогда
			Прервать;
		КонецЕсли;

		// Поиск по точному совпадению значения ячейки табличного документа ожидаемому значению.
		Для Каждого Колонка Из ТаблицаСДаннымиМакета.Колонки Цикл
			ЗначениеЯчейки = СокрЛП(СтрокаТаблицы[Колонка.Имя]);
			ПозицияВМакете = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрЗаменить(Колонка.Имя, "Колонка_", ""));

			Если ЗначениеЗаполнено(ЗначениеЯчейки) Тогда
				// Возможно, шапка начинается с группы колонок.
				Отбор = Новый Структура("Группа, ПозицияВМакете", ЗначениеЯчейки, -1);
				НайденныеКолонки = ИнформацияПоКолонкам.НайтиСтроки(Отбор);
				Если НайденныеКолонки.Количество() > 0 Тогда
					Если СтрокаСДаннымиТаблицы = 0 Тогда
						СтрокаСДаннымиТаблицы = НомерСтроки;
					Иначе
						СтрокаСДаннымиТаблицы = Мин(СтрокаСДаннымиТаблицы, НомерСтроки);
					КонецЕсли;

					Продолжить;
				КонецЕсли;

				// Поиск колонки.
				Отбор = Новый Структура("Синоним, ПозицияВМакете, ВидимостьВМакете",
					ЗначениеЯчейки, -1, Истина);
				НайденныеКолонки = ИнформацияПоКолонкам.НайтиСтроки(Отбор);
				Если НайденныеКолонки.Количество() > 0 Тогда
					НайденныеКолонки[0].ПозицияВМакете = ПозицияВМакете;
					КолонокКПоиску = КолонокКПоиску - 1;

					Если СтрокаСДаннымиТаблицы = 0 Тогда
						СтрокаСДаннымиТаблицы = НомерСтроки;
					Иначе
						СтрокаСДаннымиТаблицы = Мин(СтрокаСДаннымиТаблицы, НомерСтроки);
					КонецЕсли;
				Иначе
					Отбор = Новый Структура("ПредставлениеКолонки, ПозицияВМакете, ВидимостьВМакете",
						ЗначениеЯчейки, -1, Истина);
					НайденныеКолонки = ИнформацияПоКолонкам.НайтиСтроки(Отбор);
					Если НайденныеКолонки.Количество() > 0 Тогда
						НайденныеКолонки[0].ПозицияВМакете = ПозицияВМакете;
						КолонокКПоиску = КолонокКПоиску - 1;

						Если СтрокаСДаннымиТаблицы = 0 Тогда
							СтрокаСДаннымиТаблицы = НомерСтроки;
						Иначе
							СтрокаСДаннымиТаблицы = Мин(СтрокаСДаннымиТаблицы, НомерСтроки);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;

		Если КолонокКПоиску > 0 Тогда
			// Поиск по частичному вхождению ожидаемого значения в значение ячейки табличного документа.
			Для Каждого Колонка Из ТаблицаСДаннымиМакета.Колонки Цикл
				ЗначениеЯчейки = СокрЛП(СтрокаТаблицы[Колонка.Имя]);
				ПозицияВМакете = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрЗаменить(Колонка.Имя, "Колонка_", ""));

				Отбор = Новый Структура("ПозицияВМакете", ПозицияВМакете);
				НайденныеКолонки = ИнформацияПоКолонкам.НайтиСтроки(Отбор);
				Если НайденныеКолонки.Количество() = 0 И ЗначениеЗаполнено(ЗначениеЯчейки) Тогда
					Отбор = Новый Структура("ПозицияВМакете", -1);
					НайденныеКолонки = ИнформацияПоКолонкам.НайтиСтроки(Отбор);

					ЗавершитьПоиск = Ложь;
					Для Каждого НайденнаяКолонка Из НайденныеКолонки Цикл
						Если Не ПустаяСтрока(НайденнаяКолонка.Синоним) Тогда
							СоставСинонима = СтрРазделить(НайденнаяКолонка.Синоним, "/", Ложь);
							Для Каждого ЧастьСинонима Из СоставСинонима Цикл
								ПозицияСинонима = СтрНайти(ЗначениеЯчейки, СокрЛП(ЧастьСинонима));
								Если ПозицияСинонима > 0 Тогда
									НайденнаяКолонка.ПозицияВМакете = ПозицияВМакете;
									КолонокКПоиску = КолонокКПоиску - 1;

									Если СтрокаСДаннымиТаблицы = 0 Тогда
										СтрокаСДаннымиТаблицы = НомерСтроки;
									Иначе
										СтрокаСДаннымиТаблицы = Мин(СтрокаСДаннымиТаблицы, НомерСтроки);
									КонецЕсли;

									ЗавершитьПоиск = Истина;
									Прервать;
								КонецЕсли;
							КонецЦикла;
							Если ЗавершитьПоиск Тогда
								Прервать;
							КонецЕсли;
						КонецЕсли;

						Если Не ПустаяСтрока(НайденнаяКолонка.ПредставлениеКолонки) Тогда
							СоставСинонима = СтрРазделить(НайденнаяКолонка.ПредставлениеКолонки, "/", Ложь);
							ЗавершитьПоиск = Ложь;
							Для Каждого ЧастьСинонима Из СоставСинонима Цикл
								ПозицияПредставленияКолонки = СтрНайти(ЗначениеЯчейки, СокрЛП(ЧастьСинонима));
								Если ПозицияПредставленияКолонки > 0 Тогда
									НайденнаяКолонка.ПозицияВМакете = ПозицияВМакете;
									КолонокКПоиску = КолонокКПоиску - 1;

									Если СтрокаСДаннымиТаблицы = 0 Тогда
										СтрокаСДаннымиТаблицы = НомерСтроки;
									Иначе
										СтрокаСДаннымиТаблицы = Мин(СтрокаСДаннымиТаблицы, НомерСтроки);
									КонецЕсли;

									ЗавершитьПоиск = Истина;
									Прервать;
								КонецЕсли;
							КонецЦикла;
							Если ЗавершитьПоиск Тогда
								Прервать;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

	СтрокаСДаннымиТаблицы = СтрокаСДаннымиТаблицы + 1;

КонецПроцедуры

// Конструктор параметров загрузки методами API.
//
// Возвращаемое значение:
//   Структура:
//     * ИмяОбъектаМетаданных          - Строка - имя объекта метаданных, из которого берутся данные для выгрузки/загрузки.
//     * ИмяТабличнойЧасти             - Строка - имя таблицы-источника колонок из указанного объекта метаданных.
//     * АдресТаблицыЗагруженныхДанных - Строка - адрес хранилища с результатом.
//     * ОбязательныеКолонки           - Массив из Строка - имена колонок, заполнение которых проверяется.
//     * ПараметрыШапки                - см. ПараметрыШапки.
//     * ИнформацияПоКолонкам          - см. ИнтеграцияСМаркетплейсамиСервер.ИнициализироватьТаблицуИнформацииПоКолонкам.
//     * УчетнаяЗапись                 - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//     * НачалоПериода                 - Дата - начало выборки данных.
//     * КонецПериода                  - Дата - окончание выборки данных.
//     * ЛистыШаблона                             - Неопределено - используется одностраничный шаблон.
//                                                - Структура - описание многостраничного шаблона:
//       ** ИмяЛиста                                 - Строка - имя листа.
//       ** ИмяМакетаСШаблоном                       - Строка - имя макета объекта метаданных.
//       ** Служебный                                - Произвольный - дополнительная служебная информация.
//       ** ПараметрыШапки                           - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыШапки.
//       ** ИнформацияПоКолонкам                     - см. ИнтеграцияСМаркетплейсамиСервер.ИнициализироватьТаблицуИнформацииПоКолонкам.
//       ** ВысотаЗаголовкаТаблицыЗагружаемогоМакета - Число - высота заголовка таблицы загружаемого макета.
//       ** ВысотаЗаголовкаТаблицыМакетаСШаблоном    - Число - высота заголовка таблицы эталонного макета объекта конфигурации.
//       ** ШаблонСДанными                           - ТабличныйДокумент - данные в виде табличного документа, которые нужно обработать.
//       ** ЗаполняемыйШаблон                        - Неопределено, ТабличныйДокумент - заполняемый табличный документ.
//
Функция ПараметрыЗагрузкиИнтеграционнымиМетодами() Экспорт

	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ИмяОбъектаМетаданных",          "");
	ПараметрыЗагрузки.Вставить("ИмяТабличнойЧасти",             "");
	ПараметрыЗагрузки.Вставить("АдресТаблицыЗагруженныхДанных", "");
	ПараметрыЗагрузки.Вставить("ОбязательныеКолонки",           Новый Массив);
	ПараметрыЗагрузки.Вставить("ПараметрыШапки",                ПараметрыШапки());
	ПараметрыЗагрузки.Вставить("ИнформацияПоКолонкам",          Новый ТаблицаЗначений);
	ПараметрыЗагрузки.Вставить("ЛистыШаблона",                  Неопределено);

	ПараметрыЗагрузки.Вставить("УчетнаяЗапись",                 Справочники.УчетныеЗаписиМаркетплейсов.ПустаяСсылка());
	ПараметрыЗагрузки.Вставить("НачалоПериода",                 Дата(1, 1, 1));
	ПараметрыЗагрузки.Вставить("КонецПериода",                  Дата(1, 1, 1));

	Возврат ПараметрыЗагрузки;

КонецФункции

#КонецОбласти

#Область ПодготовкаКВыгрузкеДанных

// Конструктор параметров выгрузки данных в макет.
//
// Возвращаемое значение:
//   Структура:
//     * УчетнаяЗапись           - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//     * ИмяОбъектаМетаданных    - Строка - полное имя объекта метаданных.
//     * ИмяМакетаСШаблоном      - Строка - имя макета объекта метаданных.
//     * СсылкаНаОбъект          - Неопределено, ЛюбаяСсылка - ссылка на объект, данные по которому выводятся в табличный 
//                                   документ.
//     * ИнформацияПоКолонкам    - см. ИнтеграцияСМаркетплейсамиСервер.ИнициализироватьТаблицуИнформацииПоКолонкам.
//     * ПараметрыШапки          - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыШапки.
//     * ДополнительныеПараметры - Структура - любые дополнительные параметры.
//
//@skip-check function-return-types
Функция ПараметрыВыгрузкиДанныхВМакет() Экспорт

	ПараметрыВыгрузкиДанныхВМакет = Новый Структура;
	ПараметрыВыгрузкиДанныхВМакет.Вставить("УчетнаяЗапись",           Справочники.УчетныеЗаписиМаркетплейсов.ПустаяСсылка());
	ПараметрыВыгрузкиДанныхВМакет.Вставить("ИмяОбъектаМетаданных",    "");
	ПараметрыВыгрузкиДанныхВМакет.Вставить("ИмяМакетаСШаблоном",      "");
	ПараметрыВыгрузкиДанныхВМакет.Вставить("СсылкаНаОбъект",          Неопределено);
	ПараметрыВыгрузкиДанныхВМакет.Вставить("ИнформацияПоКолонкам",    Новый ТаблицаЗначений);
	ПараметрыВыгрузкиДанныхВМакет.Вставить("ПараметрыШапки",          ПараметрыШапки());
	ПараметрыВыгрузкиДанныхВМакет.Вставить("ДополнительныеПараметры", Новый Структура);

	Возврат ПараметрыВыгрузкиДанныхВМакет;
	
КонецФункции

// Формирует табличный документ для переданной в параметра ссылки на объект.
//
// Параметры:
//   Параметры - см. ИнтеграцияСМаркетплейсамиСервер.ПараметрыВыгрузкиДанныхВМакет.
//
// Возвращаемое значение:
//   Структура:
//     * ШаблонСДанными - ТабличныйДокумент - сформированный табличный документ;
//     * ОписаниеОшибки - Строка - описание ошибки.
//
Функция СформироватьТабличныйДокументПоИмениМакетаСШаблоном(Параметры) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ШаблонСДанными", Новый ТабличныйДокумент);
	Результат.Вставить("ОписаниеОшибки", "");

	МенеджерОбъекта = МенеджерОбъекта(Параметры.ИмяОбъектаМетаданных); // МенеджерОбъектаМетаданных

	Макет = УправлениеПечатью.МакетПечатнойФормы(Параметры.ИмяОбъектаМетаданных + "." + Параметры.ИмяМакетаСШаблоном); // ТабличныйДокумент

	Если Макет = Неопределено Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Загрузка данных из файла в табличную часть не поддерживается, т.к. отсутствует макет %1 у объекта с типом: %2';
				|en = 'Cannot import data from a file to a table since there is no %1 template for object of the %2 type'"),
			Параметры.ИмяМакетаСШаблоном,
			Параметры.ИмяОбъектаМетаданных);
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;

	МассивОшибок = Новый Массив;

	ШаблонСДанными = Результат.ШаблонСДанными;

	// Заполнение шапки макета.

	Попытка
		ОбластьДанныеШапки = Макет.ПолучитьОбласть("ДанныеШапки");
	Исключение
		ОбластьДанныеШапки = Неопределено;
	КонецПопытки;

	// ОбластьДанныеШапки может быть переопределена и заполнена в модуле менеджера используемого объекта метаданных.
	Если МетодКонфигурацииИспользуется(Параметры.ИмяОбъектаМетаданных, "ЗаполнитьШапкуМакета") Тогда
		//@skip-check bsl-legacy-check-static-feature-access-for-unknown-left-part
		МенеджерОбъекта.ЗаполнитьШапкуМакета(ОбластьДанныеШапки, Параметры);
	КонецЕсли;

	Если ОбластьДанныеШапки <> Неопределено Тогда
		ШаблонСДанными.Вывести(ОбластьДанныеШапки);
	КонецЕсли;

	// Заполнение заголовка таблицы макета.

	Попытка
		ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	Исключение
		ОбластьЗаголовокТаблицы = Неопределено;
	КонецПопытки;

	// ОбластьЗаголовокТаблицы может быть переопределена и заполнена в модуле менеджера используемого объекта метаданных.
	Если МетодКонфигурацииИспользуется(Параметры.ИмяОбъектаМетаданных, "ЗаполнитьЗаголовокТаблицыМакета") Тогда
		//@skip-check bsl-legacy-check-static-feature-access-for-unknown-left-part
		МенеджерОбъекта.ЗаполнитьЗаголовокТаблицыМакета(ОбластьЗаголовокТаблицы, Параметры);
	КонецЕсли;

	Если ОбластьЗаголовокТаблицы <> Неопределено Тогда
		ШаблонСДанными.Вывести(ОбластьЗаголовокТаблицы);
	КонецЕсли;

	// Заполнение строк макета.

	Попытка
		ОбластьДанныеСтроки = Макет.ПолучитьОбласть("ДанныеСтроки");
	Исключение
		ОбластьДанныеСтроки = Неопределено;
	КонецПопытки;

	ДанныеСтрокМакета = Неопределено;
	Если ОбластьДанныеСтроки <> Неопределено Тогда
		// Данные строк таблицы макета должны быть получены в модуле менеджера используемого объекта метаданных.
		ОписаниеОшибки = "";

		Если МетодКонфигурацииИспользуется(Параметры.ИмяОбъектаМетаданных, "ПолучитьДанныеСтрокТаблицыМакета") Тогда
			//@skip-check bsl-legacy-check-static-feature-access-for-unknown-left-part
			ДанныеСтрокМакета = МенеджерОбъекта.ПолучитьДанныеСтрокТаблицыМакета(Параметры, ОписаниеОшибки);
		КонецЕсли;

		Если Не ПустаяСтрока(ОписаниеОшибки) Тогда
			МассивОшибок.Добавить(ОписаниеОшибки);
		КонецЕсли;
	КонецЕсли;

	Если ОбластьДанныеСтроки <> Неопределено И ДанныеСтрокМакета <> Неопределено Тогда
		Если ТипЗнч(ДанныеСтрокМакета) = Тип("ВыборкаИзРезультатаЗапроса") Тогда
			Пока ДанныеСтрокМакета.Следующий() Цикл
				ОбластьДанныеСтроки.Параметры.Заполнить(ДанныеСтрокМакета);
				ШаблонСДанными.Вывести(ОбластьДанныеСтроки);
			КонецЦикла;
		Иначе
			Для Каждого ДанныеСтроки Из ДанныеСтрокМакета Цикл
				ОбластьДанныеСтроки.Параметры.Заполнить(ДанныеСтроки);
				ШаблонСДанными.Вывести(ОбластьДанныеСтроки);
			КонецЦикла;
		КонецЕсли;
	Иначе
		// Таблица макета может быть заполнена в модуле менеджера используемого объекта метаданных.
		Если МетодКонфигурацииИспользуется(Параметры.ИмяОбъектаМетаданных, "ВывестиТаблицуМакета") Тогда
			//@skip-check bsl-legacy-check-static-feature-access-for-unknown-left-part
			МенеджерОбъекта.ВывестиТаблицуМакета(ШаблонСДанными, Параметры);
		КонецЕсли;
	КонецЕсли;

	// Заполнение подвала макета.

	Попытка
		ОбластьДанныеПодвала = Макет.ПолучитьОбласть("ДанныеПодвала");
	Исключение
		ОбластьДанныеПодвала = Неопределено;
	КонецПопытки;

	// ОбластьДанныеПодвала может быть переопределена и заполнена в модуле менеджера используемого объекта метаданных.
	Если МетодКонфигурацииИспользуется(Параметры.ИмяОбъектаМетаданных, "ЗаполнитьПодвалМакета") Тогда
		//@skip-check bsl-legacy-check-static-feature-access-for-unknown-left-part
		МенеджерОбъекта.ЗаполнитьПодвалМакета(ОбластьДанныеПодвала, Параметры);
	КонецЕсли;

	Если ОбластьДанныеПодвала <> Неопределено Тогда
		ШаблонСДанными.Вывести(ОбластьДанныеПодвала);
	КонецЕсли;

	Результат.ШаблонСДанными = ШаблонСДанными;
	Результат.ОписаниеОшибки = СтрСоединить(МассивОшибок, Символы.ПС);

	Возврат Результат;

КонецФункции

#КонецОбласти

#КонецОбласти

// Формирует строку параметров запроса (QUERY).
// 
// Параметры:
//   ОбъектXDTO          - ОбъектXDTO - объект, из реквизитов с формой "атрибут" которого формируется строка.
//   ТипОбъекта          - ТипОбъектаXDTO - тип объекта из операнда ОбъектXDTO.
//   ДобавитьРазделитель - Булево - добавить к строке предваряющий разделитель (знак вопроса).
// 
// Возвращаемое значение:
//   Строка - строковое представление параметров запроса для использования в URI.
//
Функция ПараметрыЗапроса(ОбъектXDTO, ТипОбъекта, ДобавитьРазделитель = Истина)

	СтрокиПараметров = Новый Массив;

	Для Каждого Свойство Из ТипОбъекта.Свойства Цикл
		Если Свойство.Форма = ФормаXML.Атрибут Тогда
			СтрокиПараметров.Добавить(Свойство.Имя + "=" + КодироватьСтроку(XMLСтрока(ОбъектXDTO[Свойство.Имя]), СпособКодированияСтроки.КодировкаURL));
		КонецЕсли;
	КонецЦикла;

	Если СтрокиПараметров.Количество() > 0 Тогда
		Если ДобавитьРазделитель Тогда
			Возврат "?" + СтрСоединить(СтрокиПараметров, "&");
		Иначе
			Возврат СтрСоединить(СтрокиПараметров, "&");
		КонецЕсли;
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция ПараметрыСоединения()

	Возврат Новый Структура("Сервер, Порт, Протокол", "", 443, "https");

КонецФункции

// Возвращает описание приложения Яндекс.
// 
// Возвращаемое значение:
//   Структура - описание приложения Яндекс:
//     * ИдентификаторПриложения - Строка - идентификатор приложения.
//     * client_id     - Строка - идентификатор клиента в сервисе.
//     * client_secret - Строка - ключ доступа в сервисе.
//
Функция ПараметрыАвторизацииПриложения()

	Возврат Новый Структура("ИдентификаторПриложения, client_id, client_secret", "", "", "");

КонецФункции

// Конструктор параметров ключа авторизации приложения.
//
// Возвращаемое значение:
//   Структура - результат обновления токенов доступа для приложения:
//     * Отказ         - Булево - признак отказа из-за ошибок.
//     * token_type    - Строка - тип токена.
//     * access_token  - Строка - токен.
//     * expires_in    - Дата - срок действия токена.
//     * refresh_token - Строка - токен обновления.
//
Функция ПараметрыКлючаАвторизацииПриложения()

	ПараметрыОтвета = Новый Структура;
	ПараметрыОтвета.Вставить("Отказ",         Истина);
	ПараметрыОтвета.Вставить("token_type",    "");
	ПараметрыОтвета.Вставить("access_token",  "");
	ПараметрыОтвета.Вставить("expires_in",    Дата(1,1,1));
	ПараметрыОтвета.Вставить("refresh_token", "");
	
	Возврат ПараметрыОтвета;

КонецФункции

// Анализирует код состояния запроса после обработки сервером на корректность (диапазон 200-299).
// Применяется в случаях возврата сервером множества значений кода состояния.
// 
// Параметры:
//   КодСостояния - Число - код состояния запроса HTTP.
// 
// Возвращаемое значение:
//   Булево - признак успешной обработки запроса сервером.
//
Функция ЗапросУспешноОбработан(КодСостояния)

	Возврат КодСостояния >= 200 И КодСостояния <= 299;

КонецФункции

// Проверяет строку на допустимость использования в качестве метода HTTP согласно разделу 4 RFC 7231.
// 
// Параметры:
//   Метод - Строка - переданный метод HTTP.
// 
// Возвращаемое значение:
//   Булево - признак допустимости метода HTTP.
//
Функция МетодHTTPДопустим(Метод)

	Методы = Новый Структура;
	Методы.Вставить("DELETE");
	Методы.Вставить("GET");
	Методы.Вставить("POST");
	Методы.Вставить("PUT");
	
	Возврат Методы.Свойство(ВРег(Метод)) <> Неопределено;

КонецФункции

Функция ПротоколПоУмолчанию(Порт)

	ПортыПоУмолчанию = Новый Соответствие;
	ПортыПоУмолчанию.Вставить(80, "http://");
	ПортыПоУмолчанию.Вставить(443, "https://");
	ПортыПоУмолчанию.Вставить(21, "ftp://");
	ПортыПоУмолчанию.Вставить(990, "ftps://");

	Протокол = ПортыПоУмолчанию.Получить(Порт);
	Если Протокол = Неопределено Тогда
		Возврат "";
	Иначе
		Возврат Протокол;
	КонецЕсли;

КонецФункции

// Формирует и отправляет запрос HTTP с анализом ответа сервера.
// 
// Параметры:
//   ПараметрыПодключения - Структура - параметры подключения к серверу, см. ПараметрыСоединения.
//   Запрос               - HTTPЗапрос - запрос для отправки.
//   МетодHTTP            - Строка - метод HTTP из допустимых по разделу 4 RFC 7231.
// 
// Возвращаемое значение:
//   Структура - информация об отправке и обработке запроса сервером. Содержит ключи: КодСостояния, ТекстОтвета, Отказ, 
//     ТекстОшибки.
//
Функция ВыполнитьЗапросHTTP(ПараметрыПодключения, Запрос, МетодHTTP = "POST")

	Прокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси(ПараметрыПодключения.Протокол);
	ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
	Соединение = Новый HTTPСоединение(ПараметрыПодключения.Сервер, ПараметрыПодключения.Порт,,, Прокси, 20, ЗащищенноеСоединение, Ложь);

	Ответ = Новый Структура();
	Ответ.Вставить("КодСостояния", 0);
	Ответ.Вставить("ТекстОтвета", "");
	Ответ.Вставить("Отказ", Ложь);
	Ответ.Вставить("ТекстОшибки", "");

	Если Не МетодHTTPДопустим(МетодHTTP) Тогда
		ШаблонОшибки = НСтр("ru = 'Указан недопустимый метод HTTP (%1)';
							|en = 'Invalid HTTP method is specified (%1)'");
		Ответ.Отказ = Истина;
		Ответ.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, МетодHTTP);
		Возврат Ответ;
	КонецЕсли;

	Попытка
		РезультатЗапроса = Соединение.ВызватьHTTPМетод(ВРег(МетодHTTP), Запрос);

		Ответ.КодСостояния = РезультатЗапроса.КодСостояния;
		Ответ.ТекстОтвета = РезультатЗапроса.ПолучитьТелоКакСтроку("UTF8");
	Исключение
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При отправке запроса на сервер %1 возникла ошибка: %2';
				|en = 'An error occurred when sending the request to the %1 server: %2'"),
			ПараметрыПодключения.Сервер,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	
		Ответ.Отказ = Истина;
		Ответ.ТекстОшибки = ТекстОшибки;
	КонецПопытки;

	Возврат Ответ;

КонецФункции   

// Выполняет попытку приведения строки в формате JSON к указанному типу объекта XDTO.
// 
// Параметры:
//   ТекстОбъекта - Строка - строковое описание объекта в формате JSON.
//   ТипОбъекта   - ТипОбъектаXDTO - тип объекта для приведения.
// 
// Возвращаемое значение:
//   Структура - результат выполнения приведения. Содержит ключи: Объект, Отказ, ТекстОшибки.
//
Функция ПривестиКТипуОбъекта(ТекстОбъекта, ТипОбъекта)

	РезультатПриведения = Новый Структура("Объект, Отказ, ТекстОшибки", Неопределено, Ложь, "");

	Попытка
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеJSON.УстановитьСтроку(ТекстОбъекта);
		РезультатПриведения.Объект = ФабрикаXDTO.ПрочитатьJSON(ЧтениеJSON, ТипОбъекта);
	Исключение
		РезультатПриведения.Отказ = Истина;
		РезультатПриведения.ТекстОшибки = ОписаниеОшибки();
	КонецПопытки;

	Возврат РезультатПриведения

КонецФункции

// Возвращает путь к ресурсу (папке или файлу) относительно папки приложения.
// 
// Параметры:
//   ПрисоединенныйФайл - СправочникСсылка.НоменклатураПрисоединенныеФайлы - файл, по которому запрашивается информация 
//                          от сервиса.
// 
// Возвращаемое значение:
//   Строка - путь к ресурсу в кодировке URL.
//
Функция ПутьКРесурсу(ПрисоединенныйФайл)

	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПрисоединенныйФайл, "ДатаМодификацииУниверсальная, Расширение");
	Возврат "app:/" + ПрисоединенныйФайл.УникальныйИдентификатор() + Формат(ЗначенияРеквизитов.ДатаМодификацииУниверсальная, "ДФ=ггггММддччммсс") + "." + ЗначенияРеквизитов.Расширение;

КонецФункции

#Область СкладыСлужебный

// Возвращает список имен реквизитов настроек склада FBS.
//
// Возвращаемое значение:
//   Массив из Строка - список имен реквизитов, если без типов данных.
//
Функция РеквизитыНастроекСкладаFBS()

	НастройкиСклада = НовыйНастройкиСкладаFBS();
	РазделительРеквизитов = ",";
	ИменаРеквизитовСтрокой = ОбщегоНазначенияКлиентСервер.КлючиСтруктурыВСтроку(НастройкиСклада, РазделительРеквизитов);
	ИменаРеквизитов = СтрРазделить(ИменаРеквизитовСтрокой, РазделительРеквизитов, Ложь);
	
	Возврат ИменаРеквизитов;

КонецФункции

#КонецОбласти

#Область ДокументыСлужебный

// Конструктор результата записи и (или) проведения документа.
//
// Возвращаемое значение:
//   Структура - результат записи и (или) проведения документа:
//     * ЗаписьВыполнена - Булево - результат записи;
//     * ОписаниеОшибки  - Строка - описание ошибки при выполнении процедуры записи.
//
Функция НовыйРезультатЗаписиДокументов()
	
	Результат = Новый Структура;
	Результат.Вставить("ЗаписьВыполнена", Ложь);
	Результат.Вставить("ОписаниеОшибки",  "");
	
	Возврат Результат;
	
КонецФункции

// Заполняет заказ клиента данными из другого объекта.
//
// Параметры:
//   ЗаказКлиентаОбъект          - ДокументОбъект.ЗаказКлиента - заказ клиента к заполнению.
//   ДанныеЗаполнения            - Структура Из КлючИЗначение - данные заполнения. 
//   ЗначенияФункциональныхОпций - См. ПолучитьФункциональныеОпции. 
//   Параметры                   - Структура - параметры заполнения документов.
//
// Возвращаемое значение:
//   Булево - результат заполнения документа.
//
Функция ЗаказКлиентаЗаполнить(ЗаказКлиентаОбъект, ДанныеЗаполнения, ЗначенияФункциональныхОпций, 
			Параметры)
	
	Если ЗначенияФункциональныхОпций = Неопределено Тогда
		ЗначенияФункциональныхОпций = ПолучитьФункциональныеОпции(ДанныеЗаполнения.Соглашение);
	КонецЕсли;
	
	ОчиститьРеквизитыЗаказаПередЗаполнением(ЗаказКлиентаОбъект, ДанныеЗаполнения, Параметры);
	
	Попытка
		ЗаполнитьЗначенияСвойств(ЗаказКлиентаОбъект, ДанныеЗаполнения);
		ЗаказКлиентаОбъект.Заполнить(ДанныеЗаполнения);
		
	Исключение
		ЗаписьЖурналаРегистрации(Параметры.СобытиеЖурналаРегистрации,
			УровеньЖурналаРегистрации.Ошибка,
			ЗаказКлиентаОбъект.Метаданные(),
			ЗаказКлиентаОбъект.Ссылка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат Ложь;
	КонецПопытки;
	
	// Реквизиты могли быть изменены в обработке заполнения
	ЗаполнитьЗначенияСвойств(ЗаказКлиентаОбъект, ДанныеЗаполнения);
	
	Если ЗначениеЗаполнено(ЗаказКлиентаОбъект.Соглашение) Тогда
		Если ЗначениеЗаполнено(ДанныеЗаполнения.Соглашение)
				И ЗаказКлиентаОбъект.Соглашение <> ДанныеЗаполнения.Соглашение Тогда
			ЗаказКлиентаОбъект.Соглашение = ДанныеЗаполнения.Соглашение;
		Иначе
			ОрганизацияСоглашения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаказКлиентаОбъект.Соглашение, "Организация");
			Если ЗначениеЗаполнено(ОрганизацияСоглашения)
					И ОрганизацияСоглашения <> ЗаказКлиентаОбъект.Организация Тогда
				ЗаказКлиентаОбъект.Соглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
	Иначе
		ЗаказКлиентаОбъект.Соглашение = ДанныеЗаполнения.Соглашение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаказКлиентаОбъект.Договор) Тогда
		Если ЗначениеЗаполнено(ДанныеЗаполнения.Договор)
				И ЗаказКлиентаОбъект.Договор <> ДанныеЗаполнения.Договор Тогда
			ЗаказКлиентаОбъект.Договор = ДанныеЗаполнения.Договор;
		Иначе
			ОрганизацияДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаказКлиентаОбъект.Договор, "Организация");
			Если ЗначениеЗаполнено(ОрганизацияДоговора)
					И ОрганизацияДоговора <> ЗаказКлиентаОбъект.Организация Тогда
				ЗаказКлиентаОбъект.Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
			КонецЕсли;
		КонецЕсли;
	Иначе
		ЗаказКлиентаОбъект.Договор = ДанныеЗаполнения.Договор;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗаказКлиентаОбъект.Соглашение) Тогда
		ЗаказКлиентаОбъект.ЗаполнитьУсловияПродажПоСоглашению(Ложь);
	КонецЕсли;
	
	ЗаказКлиентаОбъект.ПометкаУдаления       = Ложь;
	ЗаказКлиентаОбъект.Дата                  = ДанныеЗаполнения.Дата;
	ЗаказКлиентаОбъект.Организация           = ДанныеЗаполнения.Организация;
	ЗаказКлиентаОбъект.ХозяйственнаяОперация = ДанныеЗаполнения.ХозяйственнаяОперация;
	
	Если Не ЗначениеЗаполнено(ЗаказКлиентаОбъект.Контрагент) Тогда
		ЗаказКлиентаОбъект.Контрагент = ДанныеЗаполнения.Контрагент;
	КонецЕсли;
	
	Если ЗначенияФункциональныхОпций.ИспользоватьРасширенныеВозможностиЗаказаКлиента
			И Не ЗначенияФункциональныхОпций.ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента Тогда
		// Заказ только со склада
		ЗаказКлиентаОбъект.Статус = Перечисления.СтатусыЗаказовКлиентов.КОбеспечению;
	ИначеЕсли ЗначенияФункциональныхОпций.ИспользоватьРасширенныеВозможностиЗаказаКлиента
				И ЗначенияФункциональныхОпций.ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента Тогда
		// Заказ со склада и под заказ
		ЗаказКлиентаОбъект.Статус = Перечисления.СтатусыЗаказовКлиентов.КОбеспечению;
	Иначе
		// Заказ как счет
		ЗаказКлиентаОбъект.Статус = Перечисления.СтатусыЗаказовКлиентов.НеСогласован;
	КонецЕсли;
	
	ЗаказКлиентаОбъект.ЭтоЗаказКакСчет = Не ЗначенияФункциональныхОпций.ИспользоватьРасширенныеВозможностиЗаказаКлиента;
	
	ДатаОтгрузкиВСтроке = ТекущаяДатаСеанса();
	Если ЗначенияФункциональныхОпций.ИспользоватьСоглашенияСКлиентами
			И ЗначениеЗаполнено(ЗаказКлиентаОбъект.Соглашение) Тогда
		УсловияПродаж = ПродажиСервер.ПолучитьУсловияПродаж(ЗаказКлиентаОбъект.Соглашение, Истина);
		
		Если УсловияПродаж.СтатусСоглашения <> Перечисления.СтатусыСоглашенийСКлиентами.Закрыто Тогда
			Если ЗначениеЗаполнено(УсловияПродаж.СрокПоставки) Тогда
				ДатаОтгрузкиВСтроке = ОбщегоНазначенияУТКлиентСервер.РассчитатьДатуОкончанияПериода(
					ТекущаяДатаСеанса(),
					Перечисления.Периодичность.День,
					УсловияПродаж.СрокПоставки) + 1;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	ЗаказКлиентаОбъект.ДатаОтгрузки = ДатаОтгрузкиВСтроке;
	
	Если ЗаказКлиентаОбъект.ЭтоНовый() Тогда
		ЗаказКлиентаОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ОчиститьРеквизитыЗаказаПередЗаполнением(ЗаказКлиентаОбъект, ДанныеЗаполнения, Параметры)
	
	Если ЗаказКлиентаОбъект.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеЗаказа = Метаданные.Документы.ЗаказКлиента;
	
	ОчищаемыеРеквизиты = Новый Массив; // Массив из Строка
	
	Для Каждого Реквизит Из МетаданныеЗаказа.Реквизиты Цикл
		ОчищаемыеРеквизиты.Добавить(Реквизит.Имя);
	КонецЦикла;
	
	РеквизитыОчистки = Новый Структура(СтрСоединить(ОчищаемыеРеквизиты, ","));
	ЗаполнитьЗначенияСвойств(ЗаказКлиентаОбъект, РеквизитыОчистки);
	
	Если Параметры.ОчиститьТабличныеЧасти Тогда
		Для Каждого ТабличнаяЧасть Из МетаданныеЗаказа.ТабличныеЧасти Цикл
			//@skip-check dynamic-access-method-not-found
			ЗаказКлиентаОбъект[ТабличнаяЧасть.Имя].Очистить();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Выполняет постобработку табличной части "Товары" для заказа клиента.
//
// Параметры:
//   ЗаказКлиентаОбъект             - ДокументОбъект.ЗаказКлиента - заказ клиента к заполнению.
//   Параметры                      - Структура - параметры обработки табличной части: 
//     * СтруктураДействий            - Структура Из КлючИЗначение - структура с действиями, которые нужно произвести;
//                                    - Неопределено - использовать структуру действий по умолчанию.
//     * ПересчитатьКоличествоЕдиниц  - Булево - необходимо ли выполнять пересчет количества.
//     * ЗаполнитьЦены                - Булево - заполнять ли цены товаров.
//     * ЗаполнитьВариантОбеспечения  - Булево - заполнять ли варианты обеспечения.
//     * ОчиститьАвтоматическуюСкидку - Булево - очистить признак автоматических скидок.
//     * ПересчитатьЦенаВключаетНДС   - Булево - требуется ли обратный пересчет цен и сумм с (без) НДС.
//   ИмяСобытия                     - Строка - имя события журнала регистрации. 
//
// Возвращаемое значение:
//   Булево - результат обработки табличной части.
//
Функция ЗаказКлиентаОбработатьТЧ(ЗаказКлиентаОбъект, Параметры, ИмяСобытия)
	
	Если ЗаказКлиентаОбъект.Товары.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Параметры.СтруктураДействий = Неопределено Тогда
		СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(ЗаказКлиентаОбъект);
		Если Параметры.ПересчитатьЦенаВключаетНДС Тогда
			СтруктураПересчетаСуммы.Вставить("ЦенаВключаетНДС", Истина);
		КонецЕсли;
		
		СтруктураДействий = Новый Структура;
		Если Параметры.ПересчитатьКоличествоЕдиниц Тогда
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		КонецЕсли;
		СтруктураДействий.Вставить("ПроверитьЗаполнитьСклад", ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруЗаполненияСкладаВСтрокеТЧ(
																	ЗаказКлиентаОбъект, 
																	Справочники.Склады.ЭтоГруппаИСкладыИспользуютсяВТЧДокументовПродажи(ЗаказКлиентаОбъект.Склад)));
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС",      ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(
																	ЗаказКлиентаОбъект, 
																	Истина));
		СтруктураДействий.Вставить("ПересчитатьСумму",        "КоличествоУпаковок");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС",     СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС",    СтруктураПересчетаСуммы);
		
		Если Параметры.ОчиститьАвтоматическуюСкидку Тогда
			СтруктураДействий.Вставить("ОчиститьАвтоматическуюСкидку");
		КонецЕсли;
		
		Если Параметры.ЗаполнитьВариантОбеспечения Тогда
			ПараметрыДокумента = Документы.ЗаказКлиента.ПараметрыДокументаДляДействийОбеспечения(ЗаказКлиентаОбъект);
			ОбеспечениеВДокументахКлиентСервер.ДобавитьДействияОбеспечения(СтруктураДействий, "ВариантОбеспечения", ПараметрыДокумента);
		КонецЕсли;
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЗаказКлиентаОбъект, Документы.ЗаказКлиента);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ЗаказКлиентаОбъект, ПараметрыУказанияСерий);
	Иначе
		СтруктураДействий = ОбщегоНазначения.СкопироватьРекурсивно(Параметры.СтруктураДействий);
	КонецЕсли;
	
	Если Параметры.ЗаполнитьЦены Тогда
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("Дата",                         ЗаказКлиентаОбъект.Дата);
		ПараметрыЗаполнения.Вставить("Организация",                  ЗаказКлиентаОбъект.Организация);
		ПараметрыЗаполнения.Вставить("Валюта",                       ЗаказКлиентаОбъект.Валюта);
		ПараметрыЗаполнения.Вставить("Партнер",                      ЗаказКлиентаОбъект.Партнер);
		ПараметрыЗаполнения.Вставить("РассчитыватьНаборы",           Истина);
		ПараметрыЗаполнения.Вставить("НалогообложениеНДС",           ЗаказКлиентаОбъект.НалогообложениеНДС);
		ПараметрыЗаполнения.Вставить("ВозвращатьМногооборотнуюТару", ЗаказКлиентаОбъект.ВернутьМногооборотнуюТару);
		ПараметрыЗаполнения.Вставить("ПоляЗаполнения",               "Цена, ВидЦены, СрокПоставки");
		
		Если ЗаказКлиентаОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию
				И ПродажиСервер.ЭтоДоговорПоНовойКомиссии(ЗаказКлиентаОбъект.Договор) Тогда
			УчетныйВидЦеныДоговора = КомиссионнаяТорговляСервер.ВидЦеныДоговора(ЗаказКлиентаОбъект.Договор, Истина);
			ПараметрыЗаполнения.Вставить("ВидЦены", УчетныйВидЦеныДоговора);
		Иначе
			ПараметрыЗаполнения.Вставить("Соглашение", ЗаказКлиентаОбъект.Соглашение);
		КонецЕсли;
		
		СтруктураДействий.Вставить("ПересчитатьСуммуРучнойСкидки");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	КонецЕсли;
	
	Попытка
		Если Параметры.ЗаполнитьЦены Тогда
			ЦеныПредприятияЗаполнениеСервер.ЗаполнитьЦены(
				ЗаказКлиентаОбъект.Товары,
				, // Массив строк или структура отбора
				ПараметрыЗаполнения,
				СтруктураДействий);
		Иначе
			КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
			ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьТЧ(ЗаказКлиентаОбъект.Товары, 
				СтруктураДействий, 
				КэшированныеЗначения);
		КонецЕсли;
		
	Исключение
		ЗаписьЖурналаРегистрации(ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка,
			ЗаказКлиентаОбъект.Метаданные(),
			ЗаказКлиентаОбъект.Ссылка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат Ложь;
	КонецПопытки;
	
	Если Параметры.ПересчитатьЦенаВключаетНДС
			И Не ЗаказКлиентаОбъект.ЦенаВключаетНДС Тогда
		Для Каждого СтрокаТабличнойЧасти Из ЗаказКлиентаОбъект.Товары Цикл
			СтрокаТабличнойЧасти.Сумма             = СтрокаТабличнойЧасти.СуммаСНДС - СтрокаТабличнойЧасти.СуммаНДС;
			СтрокаТабличнойЧасти.СуммаРучнойСкидки = ?(СтрокаТабличнойЧасти.СуммаСНДС = 0, 
															0, 
															СтрокаТабличнойЧасти.СуммаРучнойСкидки * СтрокаТабличнойЧасти.Сумма / СтрокаТабличнойЧасти.СуммаСНДС);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого СтрокаТабличнойЧасти Из ЗаказКлиентаОбъект.Товары Цикл
		СтрокаТабличнойЧасти.Цена = Окр((СтрокаТабличнойЧасти.Сумма + СтрокаТабличнойЧасти.СуммаРучнойСкидки) 
				/ ?(СтрокаТабличнойЧасти.КоличествоУпаковок = 0, 1, СтрокаТабличнойЧасти.КоличествоУпаковок), 2, РежимОкругления.Окр15как20);
		
		Если СтрокаТабличнойЧасти.КоличествоУпаковок * СтрокаТабличнойЧасти.Цена <> 0 Тогда
			СтрокаТабличнойЧасти.ПроцентРучнойСкидки = ?(СтрокаТабличнойЧасти.КоличествоУпаковок * СтрокаТабличнойЧасти.Цена = 0, 0, 
				Окр(100 * СтрокаТабличнойЧасти.СуммаРучнойСкидки 
					/ (СтрокаТабличнойЧасти.КоличествоУпаковок * СтрокаТабличнойЧасти.Цена), 2, РежимОкругления.Окр15как20));
		Иначе
			СтрокаТабличнойЧасти.ПроцентРучнойСкидки = 0;
		КонецЕсли;
	КонецЦикла;
		
	Если Параметры.ОчиститьАвтоматическуюСкидку Тогда
		ЗаказКлиентаОбъект.СкидкиРассчитаны = Истина;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Заполняет документ реализации данными из другого объекта.
//
// Параметры:
//   РеализацияТоваровУслугОбъект - ДокументОбъект.РеализацияТоваровУслуг - документ реализации к заполнению.
//   ДанныеЗаполнения             - Структура Из КлючИЗначение - данные заполнения. 
//   ЗначенияФункциональныхОпций  - См. ПолучитьФункциональныеОпции. 
//   ИмяСобытия                   - Строка - имя события журнала регистрации. 
//
// Возвращаемое значение:
//   Булево - результат заполнения документа.
//
Функция РеализацияТоваровУслугЗаполнить(РеализацияТоваровУслугОбъект, ДанныеЗаполнения, ЗначенияФункциональныхОпций, 
			ИмяСобытия)
	
	Если ЗначенияФункциональныхОпций = Неопределено Тогда
		ЗначенияФункциональныхОпций = ПолучитьФункциональныеОпции(ДанныеЗаполнения.РеквизитыШапки.Соглашение);
	КонецЕсли;
	
	Попытка
		ЗаполнитьЗначенияСвойств(РеализацияТоваровУслугОбъект, ДанныеЗаполнения);
		ЗаполнитьЗначенияСвойств(РеализацияТоваровУслугОбъект, ДанныеЗаполнения.РеквизитыШапки);
		РеализацияТоваровУслугОбъект.Заполнить(ДанныеЗаполнения);
		
	Исключение
		ЗаписьЖурналаРегистрации(ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка,
			РеализацияТоваровУслугОбъект.Метаданные(),
			РеализацияТоваровУслугОбъект.Ссылка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат Ложь;
	КонецПопытки;
	
	Если ЗначениеЗаполнено(РеализацияТоваровУслугОбъект.Соглашение) Тогда
		УсловияПродаж = ПродажиСервер.ПолучитьУсловияПродаж(РеализацияТоваровУслугОбъект.Соглашение, Истина, Истина);
		РеализацияТоваровУслугОбъект.ЗаполнитьУсловияПродаж(УсловияПродаж);
	КонецЕсли;
	
	СтруктураОтветственного = ПродажиСервер.ПолучитьОтветственногоПоСкладу(РеализацияТоваровУслугОбъект.Склад);
	Если СтруктураОтветственного <> Неопределено Тогда
		РеализацияТоваровУслугОбъект.Отпустил          = СтруктураОтветственного.Ответственный;
		РеализацияТоваровУслугОбъект.ОтпустилДолжность = СтруктураОтветственного.ОтветственныйДолжность;
	КонецЕсли;
	
	РеализацияТоваровУслугОбъект.ПометкаУдаления       = Ложь;
	РеализацияТоваровУслугОбъект.Дата                  = ДанныеЗаполнения.Дата;
	РеализацияТоваровУслугОбъект.Организация           = ДанныеЗаполнения.РеквизитыШапки.Организация;
	РеализацияТоваровУслугОбъект.ХозяйственнаяОперация = ДанныеЗаполнения.РеквизитыШапки.ХозяйственнаяОперация;
	
	Если Не ЗначениеЗаполнено(РеализацияТоваровУслугОбъект.Контрагент) Тогда
		РеализацияТоваровУслугОбъект.Контрагент = ДанныеЗаполнения.РеквизитыШапки.Контрагент;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеализацияТоваровУслугОбъект.Соглашение) Тогда
		ОрганизацияСоглашения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеализацияТоваровУслугОбъект.Соглашение, "Организация");
		Если ЗначениеЗаполнено(ОрганизацияСоглашения)
				И ОрганизацияСоглашения <> РеализацияТоваровУслугОбъект.Организация Тогда
			РеализацияТоваровУслугОбъект.Соглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
		КонецЕсли;
	ИначеЕсли ЗначенияФункциональныхОпций.ИспользоватьСоглашенияСКлиентами 
				И Не ЗначениеЗаполнено(РеализацияТоваровУслугОбъект.Соглашение) Тогда
		РеализацияТоваровУслугОбъект.Соглашение = ДанныеЗаполнения.РеквизитыШапки.Соглашение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеализацияТоваровУслугОбъект.Договор)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеализацияТоваровУслугОбъект.Договор, "Организация") <> РеализацияТоваровУслугОбъект.Организация Тогда
		РеализацияТоваровУслугОбъект.Договор = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
	ИначеЕсли ЗначенияФункциональныхОпций.ИспользуютсяДоговорыКонтрагентов Тогда
		РеализацияТоваровУслугОбъект.Договор = ДанныеЗаполнения.РеквизитыШапки.Договор;
	КонецЕсли;
	
	Если РеализацияТоваровУслугОбъект.ЭтоНовый() Тогда
		РеализацияТоваровУслугОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Выполняет постобработку табличной части "Товары" для документа реализации.
//
// Параметры:
//   РеализацияТоваровУслугОбъект   - ДокументОбъект.РеализацияТоваровУслуг - документ реализации к заполнению.
//   Параметры                      - Структура - параметры обработки табличной части: 
//     * СтруктураДействий            - Структура Из КлючИЗначение - структура с действиями, которые нужно произвести;
//                                    - Неопределено - использовать структуру действий по умолчанию.
//     * ПересчитатьКоличествоЕдиниц  - Булево - необходимо ли выполнять пересчет количества.
//     * ЗаполнитьЦены                - Булево - заполнять ли цены товаров.
//     * ОчиститьАвтоматическуюСкидку - Булево - очистить признак автоматических скидок.
//     * ПересчитатьЦенаВключаетНДС   - Булево - требуется ли обратный пересчет цен и сумм с (без) НДС.
//   ИмяСобытия                     - Строка - имя события журнала регистрации. 
//
// Возвращаемое значение:
//   Булево - результат обработки табличной части.
//
Функция РеализацияТоваровУслугОбработатьТЧ(РеализацияТоваровУслугОбъект, Параметры, ИмяСобытия)
	
	Если РеализацияТоваровУслугОбъект.Товары.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Параметры.СтруктураДействий = Неопределено Тогда
		СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(РеализацияТоваровУслугОбъект);
		Если Параметры.ПересчитатьЦенаВключаетНДС Тогда
			СтруктураПересчетаСуммы.Вставить("ЦенаВключаетНДС", Истина);
		КонецЕсли;
		
		СтруктураДействий = Новый Структура;
		Если Параметры.ПересчитатьКоличествоЕдиниц Тогда
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		КонецЕсли;
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС",   ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(
																РеализацияТоваровУслугОбъект));
		СтруктураДействий.Вставить("ПересчитатьСумму",     "КоличествоУпаковок");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС",  СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ЗаполнитьКодТНВЭД",    РеализацияТоваровУслугОбъект.НалогообложениеНДС);
		СтруктураДействий.Вставить("ПересчитатьПроцентРучнойСкидки");
		СтруктураДействий.Вставить("ОчиститьСуммуВзаиморасчетов");
		
		Если Параметры.ОчиститьАвтоматическуюСкидку Тогда
			СтруктураДействий.Вставить("ОчиститьАвтоматическуюСкидку", Неопределено);
		КонецЕсли;
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(РеализацияТоваровУслугОбъект, Документы.РеализацияТоваровУслуг);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(РеализацияТоваровУслугОбъект, ПараметрыУказанияСерий);
	Иначе
		СтруктураДействий = ОбщегоНазначения.СкопироватьРекурсивно(Параметры.СтруктураДействий);
	КонецЕсли;
	
	Если Параметры.ЗаполнитьЦены Тогда
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("Дата",                         РеализацияТоваровУслугОбъект.Дата);
		ПараметрыЗаполнения.Вставить("Организация",                  РеализацияТоваровУслугОбъект.Организация);
		ПараметрыЗаполнения.Вставить("Валюта",                       РеализацияТоваровУслугОбъект.Валюта);
		ПараметрыЗаполнения.Вставить("Партнер",                      РеализацияТоваровУслугОбъект.Партнер);
		ПараметрыЗаполнения.Вставить("РассчитыватьНаборы",           Истина);
		ПараметрыЗаполнения.Вставить("НалогообложениеНДС",           РеализацияТоваровУслугОбъект.НалогообложениеНДС);
		ПараметрыЗаполнения.Вставить("ВозвращатьМногооборотнуюТару", РеализацияТоваровУслугОбъект.ВернутьМногооборотнуюТару);
		ПараметрыЗаполнения.Вставить("ПоляЗаполнения",               "Цена, ВидЦены, СрокПоставки");
		
		Если КомиссионнаяТорговляСервер.РеализацияЧерезКомиссионера(РеализацияТоваровУслугОбъект.ХозяйственнаяОперация) Тогда
			ПараметрыЗаполнения.Вставить("ВидЦены", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеализацияТоваровУслугОбъект.Договор, "ВидЦенПродажи"));
		Иначе
			ПараметрыЗаполнения.Вставить("Соглашение", РеализацияТоваровУслугОбъект.Соглашение);
		КонецЕсли;
		
		СтруктураДействий.Вставить("ПересчитатьСуммуРучнойСкидки");
		СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	КонецЕсли;
	
	Попытка
		Если Параметры.ЗаполнитьЦены Тогда
			ЦеныПредприятияЗаполнениеСервер.ЗаполнитьЦены(
				РеализацияТоваровУслугОбъект.Товары,
				, // Массив строк или структура отбора
				ПараметрыЗаполнения,
				СтруктураДействий);
		Иначе
			КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
			ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьТЧ(РеализацияТоваровУслугОбъект.Товары, 
				СтруктураДействий, 
				КэшированныеЗначения);
		КонецЕсли;
		
	Исключение
		ЗаписьЖурналаРегистрации(ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка,
			РеализацияТоваровУслугОбъект.Метаданные(),
			РеализацияТоваровУслугОбъект.Ссылка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат Ложь;
	КонецПопытки;
	
	Если Параметры.ПересчитатьЦенаВключаетНДС
			И Не РеализацияТоваровУслугОбъект.ЦенаВключаетНДС Тогда
		Для Каждого СтрокаТабличнойЧасти Из РеализацияТоваровУслугОбъект.Товары Цикл
			СтрокаТабличнойЧасти.Сумма             = СтрокаТабличнойЧасти.СуммаСНДС - СтрокаТабличнойЧасти.СуммаНДС;
			СтрокаТабличнойЧасти.СуммаРучнойСкидки = ?(СтрокаТабличнойЧасти.СуммаСНДС = 0, 
															0, 
															СтрокаТабличнойЧасти.СуммаРучнойСкидки * СтрокаТабличнойЧасти.Сумма / СтрокаТабличнойЧасти.СуммаСНДС);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого СтрокаТабличнойЧасти Из РеализацияТоваровУслугОбъект.Товары Цикл
		СтрокаТабличнойЧасти.Цена = Окр((СтрокаТабличнойЧасти.Сумма + СтрокаТабличнойЧасти.СуммаРучнойСкидки) 
				/ ?(СтрокаТабличнойЧасти.КоличествоУпаковок = 0, 1, СтрокаТабличнойЧасти.КоличествоУпаковок), 2, РежимОкругления.Окр15как20);
		
		Если СтрокаТабличнойЧасти.КоличествоУпаковок * СтрокаТабличнойЧасти.Цена <> 0 Тогда
			СтрокаТабличнойЧасти.ПроцентРучнойСкидки = ?(СтрокаТабличнойЧасти.КоличествоУпаковок * СтрокаТабличнойЧасти.Цена = 0, 0, 
				Окр(100 * СтрокаТабличнойЧасти.СуммаРучнойСкидки 
					/ (СтрокаТабличнойЧасти.КоличествоУпаковок * СтрокаТабличнойЧасти.Цена), 2, РежимОкругления.Окр15как20));
		Иначе
			СтрокаТабличнойЧасти.ПроцентРучнойСкидки = 0;
		КонецЕсли;
	КонецЦикла;
	
	СкладГруппа = Справочники.Склады.ЭтоГруппаИСкладыИспользуютсяВТЧДокументовПродажи(РеализацияТоваровУслугОбъект.Склад);
	СкладыСервер.ЗаполнитьСкладыВТабличнойЧасти(РеализацияТоваровУслугОбъект.Склад, СкладГруппа, РеализацияТоваровУслугОбъект.Товары, Ложь);
		
	Если Параметры.ОчиститьАвтоматическуюСкидку Тогда
		РеализацияТоваровУслугОбъект.СкидкиРассчитаны = Истина;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Заполняет корректировку реализации данными из другого объекта.
//
// Параметры:
//   КорректировкаРеализацииОбъект - ДокументОбъект.КорректировкаРеализации - корректировка реализации к заполнению.
//   ДанныеЗаполнения              - Структура Из КлючИЗначение - данные заполнения. 
//   ЗначенияФункциональныхОпций   - См. ПолучитьФункциональныеОпции. 
//   ИмяСобытия                    - Строка - имя события журнала регистрации. 
//
// Возвращаемое значение:
//   Булево - результат заполнения документа.
//
Функция КорректировкаРеализацииЗаполнить(КорректировкаРеализацииОбъект, ДанныеЗаполнения, ЗначенияФункциональныхОпций, 
			ИмяСобытия)
	
	Если ЗначенияФункциональныхОпций = Неопределено Тогда
		ЗначенияФункциональныхОпций = ПолучитьФункциональныеОпции(ДанныеЗаполнения.Соглашение);
	КонецЕсли;
	
	Попытка
		ЗаполнитьЗначенияСвойств(КорректировкаРеализацииОбъект, ДанныеЗаполнения);
		КорректировкаРеализацииОбъект.Заполнить(ДанныеЗаполнения);
		
	Исключение
		ЗаписьЖурналаРегистрации(ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка,
			КорректировкаРеализацииОбъект.Метаданные(),
			КорректировкаРеализацииОбъект.Ссылка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат Ложь;
	КонецПопытки;
	
	Документы.КорректировкаРеализации.ЗаполнитьТоварыПоИсходнымДанным(
		КорректировкаРеализацииОбъект.ДокументОснование,
		КорректировкаРеализацииОбъект.Товары,
		КорректировкаРеализацииОбъект.Расхождения,
		КорректировкаРеализацииОбъект.Ссылка);
	
	Если КорректировкаРеализацииОбъект.ЭтоНовый() Тогда
		КорректировкаРеализацииОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Выполняет постобработку табличных частей "Товары" и "Расхождения" для корректировки реализации.
//
// Параметры:
//   КорректировкаРеализацииОбъект - ДокументОбъект.КорректировкаРеализации - корректировка реализации к заполнению.
//   Параметры                     - Структура - параметры обработки табличной части: 
//     * СтруктураДействий           - Структура Из КлючИЗначение - структура с действиями, которые нужно произвести;
//                                   - Неопределено - использовать структуру действий по умолчанию.
//     * ЗаполнитьВариантОтражения   - Булево - заполнить вариант отражения в табличных частях.
//   ИмяСобытия                    - Строка - имя события журнала регистрации. 
//
// Возвращаемое значение:
//   Булево - результат обработки табличной части.
//
Функция КорректировкаРеализацииОбработатьТЧ(КорректировкаРеализацииОбъект, Параметры, ИмяСобытия)
	
	Если Параметры.СтруктураДействий = Неопределено Тогда
		СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(КорректировкаРеализацииОбъект);
		Если Параметры.ПересчитатьЦенаВключаетНДС Тогда
			СтруктураПересчетаСуммы.Вставить("ЦенаВключаетНДС", Истина);
		КонецЕсли;
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьСумму",     "КоличествоУпаковок");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС",  СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	Иначе
		СтруктураДействий = ОбщегоНазначения.СкопироватьРекурсивно(Параметры.СтруктураДействий);
	КонецЕсли;
	
	Если КорректировкаРеализацииОбъект.Товары.Количество() > 0 Тогда
		Если Параметры.ЗаполнитьВариантОтражения Тогда
			Для Каждого СтрокаТабличнойЧасти Из КорректировкаРеализацииОбъект.Товары Цикл
				Если Год(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КорректировкаРеализацииОбъект.ДокументОснование, "Дата")) <> Год(КорректировкаРеализацииОбъект.Дата) Тогда
					СтрокаТабличнойЧасти.ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.СписатьНаРасходы;
				ИначеЕсли Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ВариантОтражения) Тогда
					ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач = СкладыСервер.ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач(
						СтрокаТабличнойЧасти.Склад, 
						КорректировкаРеализацииОбъект.Дата);
						
					Если ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач Тогда
						СтрокаТабличнойЧасти.ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУчестьПриИнвентаризации;
					Иначе
						СтрокаТабличнойЧасти.ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
		ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьТЧ(КорректировкаРеализацииОбъект.Товары, 
			СтруктураДействий, 
			КэшированныеЗначения);
	КонецЕсли;
	
	КорректировкаРеализацииОбъект.ЗаполнитьРасхождения();
	
	Если КорректировкаРеализацииОбъект.Расхождения.Количество() > 0 Тогда
		Для Каждого СтрокаТабличнойЧасти Из КорректировкаРеализацииОбъект.Расхождения Цикл
			Если Год(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КорректировкаРеализацииОбъект.ДокументОснование, "Дата")) <> Год(КорректировкаРеализацииОбъект.Дата) Тогда
				СтрокаТабличнойЧасти.ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.СписатьНаРасходы;
			ИначеЕсли Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.ВариантОтражения) Тогда
				ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач = СкладыСервер.ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач(
					СтрокаТабличнойЧасти.Склад, 
					КорректировкаРеализацииОбъект.Дата);
					
				Если ИспользоватьОрдернуюСхемуПриОтраженииИзлишковНедостач Тогда
					СтрокаТабличнойЧасти.ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУчестьПриИнвентаризации;
				Иначе
					СтрокаТабличнойЧасти.ВариантОтражения = Перечисления.ВариантыОтраженияКорректировокРеализаций.УменьшитьРеализациюУвеличитьСкладскиеОстатки;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(КорректировкаРеализацииОбъект, Документы.КорректировкаРеализации);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(КорректировкаРеализацииОбъект, ПараметрыУказанияСерий.Товары);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(КорректировкаРеализацииОбъект, ПараметрыУказанияСерий.Расхождения);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Заполняет документ комиссии данными из другого объекта.
//
// Параметры:
//   ОтчетКомиссионераОбъект     - ДокументОбъект.ОтчетКомиссионера - документ комиссии к заполнению.
//   ДанныеЗаполнения            - Структура Из КлючИЗначение - данные заполнения. 
//   ЗначенияФункциональныхОпций - См. ПолучитьФункциональныеОпции. 
//   ИмяСобытия                  - Строка - имя события журнала регистрации. 
//
// Возвращаемое значение:
//   Булево - результат заполнения документа.
//
Функция ОтчетКомиссионераЗаполнить(ОтчетКомиссионераОбъект, ДанныеЗаполнения, ЗначенияФункциональныхОпций, 
			ИмяСобытия)
	
	Если ЗначенияФункциональныхОпций = Неопределено Тогда
		ЗначенияФункциональныхОпций = ПолучитьФункциональныеОпции(ДанныеЗаполнения.РеквизитыШапки.Соглашение);
	КонецЕсли;
	
	Попытка
		ЗаполнитьЗначенияСвойств(ОтчетКомиссионераОбъект, ДанныеЗаполнения);
		ЗаполнитьЗначенияСвойств(ОтчетКомиссионераОбъект, ДанныеЗаполнения.РеквизитыШапки);
		ОтчетКомиссионераОбъект.Заполнить(ДанныеЗаполнения);
		
	Исключение
		ЗаписьЖурналаРегистрации(ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка,
			ОтчетКомиссионераОбъект.Метаданные(),
			ОтчетКомиссионераОбъект.Ссылка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат Ложь;
	КонецПопытки;
	
	ОтчетКомиссионераОбъект.ПометкаУдаления       = Ложь;
	ОтчетКомиссионераОбъект.Дата                  = ДанныеЗаполнения.Дата;
	ОтчетКомиссионераОбъект.Организация           = ДанныеЗаполнения.РеквизитыШапки.Организация;
	ОтчетКомиссионераОбъект.ХозяйственнаяОперация = ДанныеЗаполнения.РеквизитыШапки.ХозяйственнаяОперация;
	
	Если Не ЗначениеЗаполнено(ОтчетКомиссионераОбъект.Контрагент) Тогда
		ОтчетКомиссионераОбъект.Контрагент = ДанныеЗаполнения.РеквизитыШапки.Контрагент;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтчетКомиссионераОбъект.Соглашение) Тогда
		ОрганизацияСоглашения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтчетКомиссионераОбъект.Соглашение, "Организация");
		Если ЗначениеЗаполнено(ОрганизацияСоглашения)
				И ОрганизацияСоглашения <> ОтчетКомиссионераОбъект.Организация Тогда
			ОтчетКомиссионераОбъект.Соглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
		КонецЕсли;
	ИначеЕсли ЗначенияФункциональныхОпций.ИспользоватьСоглашенияСКлиентами 
				И Не ЗначениеЗаполнено(ОтчетКомиссионераОбъект.Соглашение) Тогда
		ОтчетКомиссионераОбъект.Соглашение = ДанныеЗаполнения.РеквизитыШапки.Соглашение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтчетКомиссионераОбъект.Договор)
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтчетКомиссионераОбъект.Договор, "Организация") <> ОтчетКомиссионераОбъект.Организация Тогда
		ОтчетКомиссионераОбъект.Договор = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
	ИначеЕсли ЗначенияФункциональныхОпций.ИспользуютсяДоговорыКонтрагентов Тогда
		ОтчетКомиссионераОбъект.Договор = ДанныеЗаполнения.РеквизитыШапки.Договор;
	КонецЕсли;
	
	Если ОтчетКомиссионераОбъект.ЭтоНовый() Тогда
		ОтчетКомиссионераОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Выполняет постобработку табличной части "Товары" для документа комиссии.
//
// Параметры:
//   ОтчетКомиссионераОбъект       - ДокументОбъект.ОтчетКомиссионера - документ комиссии к заполнению.
//   Параметры                     - Структура - параметры обработки табличной части: 
//     * СтруктураДействий           - Структура Из КлючИЗначение - структура с действиями, которые нужно произвести;
//                                   - Неопределено - использовать структуру действий по умолчанию.
//     * ПересчитатьКоличествоЕдиниц - Булево - необходимо ли выполнять пересчет количества.
//     * ЗаполнитьЦены               - Булево - заполнять ли цены товаров.
//     * ПересчитатьЦенаВключаетНДС  - Булево - требуется ли обратный пересчет цен и сумм с (без) НДС.
//     * КомиссионерВедетУчетПоРНПТ  - Булево - ведет ли комиссионер учет по РНПТ.
//   Ошибка                        - см. НоваяОшибка.
//   ИмяСобытия                    - Строка - имя события журнала регистрации. 
//
// Возвращаемое значение:
//   Булево - результат обработки табличной части.
//
Функция ОтчетКомиссионераОбработатьТЧ(ОтчетКомиссионераОбъект, Параметры, Ошибка, ИмяСобытия = "")
	
	Если ОтчетКомиссионераОбъект.Товары.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Заполнение количества РНПТ
	Если Параметры.КомиссионерВедетУчетПоРНПТ
			И ЗначениеЗаполнено(ОтчетКомиссионераОбъект.Организация) Тогда
		ПараметрыПолученияКоэффициентаРНПТ = УчетПрослеживаемыхТоваровКлиентСерверЛокализация.ПараметрыПолученияКоэффициентаРНПТ(
			ОтчетКомиссионераОбъект,
			"Партнер");
		УчетПрослеживаемыхТоваровЛокализация.ЗаполнитьКоличествоПоРНПТВТабличнойЧасти(
			ПараметрыПолученияКоэффициентаРНПТ,
			ОтчетКомиссионераОбъект.Товары);
	КонецЕсли;
		
	Если Параметры.СтруктураДействий = Неопределено Тогда
		СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(ОтчетКомиссионераОбъект);
		
		СтруктураДействий = Новый Структура;
		Если Параметры.ПересчитатьКоличествоЕдиниц Тогда
			СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
		КонецЕсли;
		СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС",         ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(
																	ОтчетКомиссионераОбъект));
		СтруктураДействий.Вставить("ПересчитатьСумму",           "КоличествоУпаковок");
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС",        СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС",       СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуПродажиНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ЗаполнитьКодТНВЭД",          ОтчетКомиссионераОбъект.НалогообложениеНДС);
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ОтчетКомиссионераОбъект, Документы.ОтчетКомиссионера);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОтчетКомиссионераОбъект, ПараметрыУказанияСерий);
	Иначе
		СтруктураДействий = ОбщегоНазначения.СкопироватьРекурсивно(Параметры.СтруктураДействий);
	КонецЕсли;
	
	ОшибкаЗаполненияЦен = Ложь;
	
	Если Параметры.ЗаполнитьЦены Тогда
		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("Дата",               ?(ЗначениеЗаполнено(ОтчетКомиссионераОбъект.КонецПериода), 
																ОтчетКомиссионераОбъект.КонецПериода, ОтчетКомиссионераОбъект.Дата));
		ПараметрыЗаполнения.Вставить("Организация",        ОтчетКомиссионераОбъект.Организация);
		ПараметрыЗаполнения.Вставить("Валюта",             ОтчетКомиссионераОбъект.Валюта);
		ПараметрыЗаполнения.Вставить("Партнер",            ОтчетКомиссионераОбъект.Партнер);
		ПараметрыЗаполнения.Вставить("НалогообложениеНДС", ОтчетКомиссионераОбъект.НалогообложениеНДС);
		ПараметрыЗаполнения.Вставить("ПоляЗаполнения",     "Цена, ВидЦены, СрокПоставки");
		ПараметрыЗаполнения.Вставить("ПолеЦена",           "Цена");
		ПараметрыЗаполнения.Вставить("ПолеВидЦены",        "ВидЦены");
		
		Если ЗначениеЗаполнено(ОтчетКомиссионераОбъект.Договор) Тогда
			ДоговорВидЦены         = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОтчетКомиссионераОбъект.Договор, "ВидЦенУчетный");
			ДоговорЦенаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорВидЦены, "ЦенаВключаетНДС");
			
			Если Не ДоговорЦенаВключаетНДС = Неопределено 
					И Не ОтчетКомиссионераОбъект.ЦенаВключаетНДС = ДоговорЦенаВключаетНДС Тогда
				ОшибкаЗаполненияЦен = Истина;
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Признак ""Цена включает НДС"" различается в виде цен договора и документе %1. Цены не могут быть заполнены.';
						|en = 'The ""Price includes VAT"" flag differs in the price type of the contract and the %1 document. Cannot fill in the prices.'"),
					ОтчетКомиссионераОбъект.Ссылка);
				
				Если Ошибка.Детализация = Неопределено Тогда
					Ошибка.Детализация = Новый Массив;
				КонецЕсли;
				Ошибка.Детализация.Добавить(ТекстПредупреждения); // Массив
				
				ЗаписьЖурналаРегистрации(ИмяСобытия, 
					УровеньЖурналаРегистрации.Предупреждение,
					ОтчетКомиссионераОбъект.Метаданные(),
					ОтчетКомиссионераОбъект.Ссылка,
					ТекстПредупреждения);
			Иначе
				ПараметрыЗаполнения.Вставить("ВидЦены", ДоговорВидЦены);
			КонецЕсли;
			
		Иначе
			ПараметрыЗаполнения.Вставить("Соглашение", ОтчетКомиссионераОбъект.Соглашение);
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		ЦеныЗаполнены = Ложь;
		
		Если Параметры.ЗаполнитьЦены 
				И Не ОшибкаЗаполненияЦен Тогда
			ЦеныЗаполнены = ЦеныПредприятияЗаполнениеСервер.ЗаполнитьЦены(
				ОтчетКомиссионераОбъект.Товары,
				, // Массив строк или структура отбора
				ПараметрыЗаполнения,
				СтруктураДействий);
		КонецЕсли;
			
		Если Не ЦеныЗаполнены Тогда
			СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(ОтчетКомиссионераОбъект);
			Если Параметры.ПересчитатьЦенаВключаетНДС Тогда
				СтруктураПересчетаСуммы.Вставить("ЦенаВключаетНДС", Истина);
			КонецЕсли;
			
			Если СтруктураДействий.Свойство("ПересчитатьСуммуНДС") Тогда
				СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
			КонецЕсли;
			Если СтруктураДействий.Свойство("ПересчитатьСуммуСНДС") Тогда
				СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
			КонецЕсли;
			Если СтруктураДействий.Свойство("ПересчитатьСуммуПродажиНДС") Тогда
				СтруктураДействий.Вставить("ПересчитатьСуммуПродажиНДС", СтруктураПересчетаСуммы);
			КонецЕсли;
			
			КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
			ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьТЧ(ОтчетКомиссионераОбъект.Товары, 
				СтруктураДействий, 
				КэшированныеЗначения);
			
			Если Параметры.ПересчитатьЦенаВключаетНДС
					И Не ОтчетКомиссионераОбъект.ЦенаВключаетНДС Тогда
				Для Каждого СтрокаТабличнойЧасти Из ОтчетКомиссионераОбъект.Товары Цикл
					СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Сумма - СтрокаТабличнойЧасти.СуммаНДС;
				КонецЦикла;
				
				СтруктураДействий = Новый Структура;
				СтруктураДействий.Вставить("ПересчитатьЦенуСкидкуПоСуммеВПродажах", 
					ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаЦеныСкидкиВПродажахВТЧ(
						ОтчетКомиссионераОбъект,
						Истина));
				
				КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
				ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьТЧ(ОтчетКомиссионераОбъект.Товары, 
					СтруктураДействий, 
					КэшированныеЗначения);
			КонецЕсли;
		КонецЕсли;
		
	Исключение
		Если Ошибка.Детализация = Неопределено Тогда
			Ошибка.Детализация = Новый Массив;
		КонецЕсли;
		ТекстОшибки = НСтр("ru = 'При заполнении документа о продажах через комиссионера возникли ошибки. Подробности см. в журнале регистрации.';
							|en = 'Errors occurred while filling consignment sales documents. For details, see the event log.'");
		Ошибка.Детализация.Добавить(ТекстОшибки); // Массив
		
		ЗаписьЖурналаРегистрации(ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка,
			ОтчетКомиссионераОбъект.Метаданные(),
			ОтчетКомиссионераОбъект.Ссылка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат Ложь;
	КонецПопытки;
	
	Если Не ЗначениеЗаполнено(ОтчетКомиссионераОбъект.СтавкаНДСВознаграждения) Тогда
		ТекущаяСтрока = Новый Структура("СтавкаНДС", Справочники.СтавкиНДС.ПустаяСсылка());
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("СкорректироватьСтавкуНДС",
			ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(ОтчетКомиссионераОбъект));
		
		ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);
		ОтчетКомиссионераОбъект.СтавкаНДСВознаграждения = ТекущаяСтрока.СтавкаНДС;
	КонецЕсли;

	ПроцентНДС = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(ОтчетКомиссионераОбъект.СтавкаНДСВознаграждения);
	
	ОтчетКомиссионераОбъект.СуммаВознаграждения    = ОтчетКомиссионераОбъект.Товары.Итог("СуммаВознаграждения");
	ОтчетКомиссионераОбъект.СуммаНДСВознаграждения = Окр(ОтчетКомиссионераОбъект.СуммаВознаграждения
		* ПроцентНДС / (100 + ПроцентНДС), 2, РежимОкругления.Окр15как20);
		
	Если ОшибкаЗаполненияЦен Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Возврат Истина;

КонецФункции

// Заполняет акт о расхождениях после отгрузки данными из другого объекта.
//
// Параметры:
//   АктОРасхожденияхПослеОтгрузкиОбъект - ДокументОбъект.АктОРасхожденияхПослеОтгрузки - акт о расхождениях после 
//                                           отгрузки к заполнению.
//   ДанныеЗаполнения                    - Структура Из КлючИЗначение - данные заполнения. 
//   ЗначенияФункциональныхОпций         - см. ПолучитьФункциональныеОпции. 
//   ИмяСобытия                          - Строка - имя события журнала регистрации. 
//
// Возвращаемое значение:
//   Строка - описание ошибки при заполнении документа.
//
Функция АктОРасхожденияхПослеОтгрузкиЗаполнить(АктОРасхожденияхПослеОтгрузкиОбъект, ДанныеЗаполнения, 
			ЗначенияФункциональныхОпций, ИмяСобытия) Экспорт
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ЗначенияФункциональныхОпций = Неопределено Тогда
			ЗначенияФункциональныхОпций = ПолучитьФункциональныеОпции();
		КонецЕсли;
		
		Попытка
			ЗаполнитьЗначенияСвойств(АктОРасхожденияхПослеОтгрузкиОбъект, ДанныеЗаполнения);
			АктОРасхожденияхПослеОтгрузкиОбъект.Заполнить(ДанныеЗаполнения);
			
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При заполнении акта о расхождениях после отгрузки возникли ошибки: %1. Подробнее см. в журнале регистрации.';
					|en = 'Errors occurred while filling the sales dispute: %1. For details, see the event log.'"),
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, 
				УровеньЖурналаРегистрации.Ошибка,
				АктОРасхожденияхПослеОтгрузкиОбъект.Метаданные(),
				АктОРасхожденияхПослеОтгрузкиОбъект.Ссылка,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
				
			Возврат ТекстОшибки;
		КонецПопытки;
		
		АктОРасхожденияхПослеОтгрузкиОбъект.ПометкаУдаления = Ложь;
		АктОРасхожденияхПослеОтгрузкиОбъект.Дата            = ДанныеЗаполнения.Дата;
		
	Иначе
		Попытка
			АктОРасхожденияхПослеОтгрузкиОбъект.Заполнить(ДанныеЗаполнения);
			
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При заполнении акта о расхождениях после отгрузки возникли ошибки: %1. Подробнее см. в журнале регистрации.';
					|en = 'Errors occurred while filling the sales dispute: %1. For details, see the event log.'"),
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, 
				УровеньЖурналаРегистрации.Ошибка,
				АктОРасхожденияхПослеОтгрузкиОбъект.Метаданные(),
				АктОРасхожденияхПослеОтгрузкиОбъект.Ссылка,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
				
			Возврат ТекстОшибки;
		КонецПопытки;
	КонецЕсли;
	
	Если АктОРасхожденияхПослеОтгрузкиОбъект.ЭтоНовый() Тогда
		АктОРасхожденияхПослеОтгрузкиОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

// Заполняет документ передачи товаров данными из другого объекта.
//
// Параметры:
//   ПередачаТоваровХранителюОбъект - ДокументОбъект.ПередачаТоваровХранителю - документ передачи товаров к заполнению.
//   ДанныеЗаполнения               - Структура Из КлючИЗначение - данные заполнения. 
//   ЗначенияФункциональныхОпций    - см. ПолучитьФункциональныеОпции. 
//   ИмяСобытия                     - Строка - имя события журнала регистрации. 
//
// Возвращаемое значение:
//   Строка - описание ошибки при заполнении документа.
//
Функция ПередачаТоваровХранителюЗаполнить(ПередачаТоваровХранителюОбъект, ДанныеЗаполнения, ЗначенияФункциональныхОпций, 
			ИмяСобытия) Экспорт
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ЗначенияФункциональныхОпций = Неопределено Тогда
			ЗначенияФункциональныхОпций = ПолучитьФункциональныеОпции(ДанныеЗаполнения.РеквизитыШапки.Соглашение);
		КонецЕсли;
		
		Попытка
			ЗаполнитьЗначенияСвойств(ПередачаТоваровХранителюОбъект, ДанныеЗаполнения);
			ЗаполнитьЗначенияСвойств(ПередачаТоваровХранителюОбъект, ДанныеЗаполнения.РеквизитыШапки);
			ПередачаТоваровХранителюОбъект.Заполнить(ДанныеЗаполнения);
			
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При заполнении передачи товаров возникли ошибки: %1. Подробнее см. в журнале регистрации.';
					|en = 'Errors occurred while filling the goods transfer: %1. For details, see the event log.'"),
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, 
				УровеньЖурналаРегистрации.Ошибка,
				ПередачаТоваровХранителюОбъект.Метаданные(),
				ПередачаТоваровХранителюОбъект.Ссылка,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
				
			Возврат ТекстОшибки;
		КонецПопытки;
		
		ПередачаТоваровХранителюОбъект.ПометкаУдаления       = Ложь;
		ПередачаТоваровХранителюОбъект.Дата                  = ДанныеЗаполнения.Дата;
		ПередачаТоваровХранителюОбъект.Организация           = ДанныеЗаполнения.РеквизитыШапки.Организация;
		ПередачаТоваровХранителюОбъект.ХозяйственнаяОперация = ДанныеЗаполнения.РеквизитыШапки.ХозяйственнаяОперация;
		
		Если Не ЗначениеЗаполнено(ПередачаТоваровХранителюОбъект.Контрагент) Тогда
			ПередачаТоваровХранителюОбъект.Контрагент = ДанныеЗаполнения.РеквизитыШапки.Контрагент;
		КонецЕсли;
		
		ОбъектСоглашение = ПередачаТоваровХранителюОбъект.Соглашение;	// СправочникСсылка.СоглашенияСКлиентами
		ОбъектДоговор    = ПередачаТоваровХранителюОбъект.Договор;		// СправочникСсылка.ДоговорыКонтрагентов
		
		Если ЗначениеЗаполнено(ОбъектСоглашение) Тогда
			ОрганизацияСоглашения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектСоглашение, "Организация");
			Если ЗначениеЗаполнено(ОрганизацияСоглашения)
					И ОрганизацияСоглашения <> ПередачаТоваровХранителюОбъект.Организация Тогда
				ПередачаТоваровХранителюОбъект.Соглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
			КонецЕсли;
		ИначеЕсли ЗначенияФункциональныхОпций.ИспользоватьСоглашенияСКлиентами 
					И Не ЗначениеЗаполнено(ОбъектСоглашение) Тогда
			ПередачаТоваровХранителюОбъект.Соглашение = ДанныеЗаполнения.РеквизитыШапки.Соглашение;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОбъектДоговор)
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектДоговор, "Организация") <> ПередачаТоваровХранителюОбъект.Организация Тогда
			ПередачаТоваровХранителюОбъект.Договор = Справочники.СоглашенияСКлиентами.ПустаяСсылка();
		ИначеЕсли ЗначенияФункциональныхОпций.ИспользуютсяДоговорыКонтрагентов Тогда
			ПередачаТоваровХранителюОбъект.Договор = ДанныеЗаполнения.РеквизитыШапки.Договор;
		КонецЕсли;
		
	Иначе
		Попытка
			ПередачаТоваровХранителюОбъект.Заполнить(ДанныеЗаполнения);
			
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При заполнении передачи товаров возникли ошибки: %1. Подробнее см. в журнале регистрации.';
					|en = 'Errors occurred while filling the goods transfer: %1. For details, see the event log.'"),
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, 
				УровеньЖурналаРегистрации.Ошибка,
				ПередачаТоваровХранителюОбъект.Метаданные(),
				ПередачаТоваровХранителюОбъект.Ссылка,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
				
			Возврат ТекстОшибки;
		КонецПопытки;
	КонецЕсли;
	
	Если ПередачаТоваровХранителюОбъект.ЭтоНовый() Тогда
		ПередачаТоваровХранителюОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#Область ПартнерыИКонтрагентыСлужебный

// Конструктор данных партнера заказа.
//
// Возвращаемое значение:
//   Структура - данные о покупателе заказа:
//     * ТипКонтрагента           - ПеречислениеСсылка.ЮридическоеФизическоеЛицо - тип покупателя в сервисе.
//     * ИдентификаторКонтрагента - Строка - идентификатор покупателя в сервисе.
//     * НаименованиеКонтрагента  - Строка - наименование покупателя в сервисе.
//     * КонтрагентПроверен       - Булево - покупатель проверен в сервисе.
//     * Партнер                  - СправочникСсылка.Партнеры - партнер;
//     * Контрагент               - СправочникСсылка.Контрагенты - контрагент;
//     * Соглашение               - СправочникСсылка.СоглашенияСКлиентами - соглашение;
//     * Договор                  - СправочникСсылка.ДоговорыКонтрагентов - договор.
//
Функция НовыеДанныеПокупателяЗаказа()

	ДанныеПокупателяЗаказа = Новый Структура;
	ДанныеПокупателяЗаказа.Вставить("ТипКонтрагента",           Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
	ДанныеПокупателяЗаказа.Вставить("ИдентификаторКонтрагента", "");
	ДанныеПокупателяЗаказа.Вставить("НаименованиеКонтрагента",  "");
	ДанныеПокупателяЗаказа.Вставить("КонтрагентПроверен",       Ложь);
	ДанныеПокупателяЗаказа.Вставить("Партнер",                  Справочники.Партнеры.ПустаяСсылка());
	ДанныеПокупателяЗаказа.Вставить("Контрагент",               Справочники.Контрагенты.ПустаяСсылка());
	ДанныеПокупателяЗаказа.Вставить("Соглашение",               Справочники.СоглашенияСКлиентами.ПустаяСсылка());
	ДанныеПокупателяЗаказа.Вставить("Договор",                  Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	
	Возврат ДанныеПокупателяЗаказа;

КонецФункции

// Создает нового партнера и заполняет ссылку на созданный элемент во входящих данных заполнения.
//
// Параметры:
//   ДанныеЗаполнения - см. ДанныеЗаполненияКонтрагента.
//   ВидМаркетплейса  - Неопределено, ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//   ИспользоватьПривилегированныйРежим - Булево - признак использования привилегированного режима.
//
// Возвращаемое значение:
//   СправочникСсылка.Партнеры - созданный партнер или пустая ссылка.
//
Функция СоздатьПартнера(ДанныеЗаполнения, ВидМаркетплейса = Неопределено, ИспользоватьПривилегированныйРежим = Истина)

	ПартнерСсылка = Справочники.Партнеры.ПустаяСсылка();
	
	Если ПустаяСтрока(ДанныеЗаполнения.Наименование)
				И ПустаяСтрока(ДанныеЗаполнения.НаименованиеПолное)
			Или Не ИспользоватьПривилегированныйРежим
				И Не ПравоДоступа("Добавление", Метаданные.Справочники.Партнеры) Тогда
		Возврат ПартнерСсылка;
	КонецЕсли;
	
	Если ИспользоватьПривилегированныйРежим Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	НовыйПартнер = Справочники.Партнеры.СоздатьЭлемент();
	НовыйПартнер.Заполнить(ДанныеЗаполнения);
	
	ЗаполнитьЗначенияСвойств(НовыйПартнер, ДанныеЗаполнения);
	
	Если ПустаяСтрока(НовыйПартнер.Наименование) Тогда
		НовыйПартнер.Наименование = НовыйПартнер.НаименованиеПолное;
	КонецЕсли;
	
	Если ПустаяСтрока(НовыйПартнер.НаименованиеПолное) Тогда
		НовыйПартнер.НаименованиеПолное = НовыйПартнер.Наименование;
	КонецЕсли;
	
	НовыйПартнер.Клиент = Истина;
	НовыйПартнер.ЮрФизЛицо = ДанныеЗаполнения.ЮрФизЛицоПартнера;
	
	Если Не ЗначениеЗаполнено(НовыйПартнер.ГруппаДоступа) Тогда
		НовыйПартнер.ГруппаДоступа = ГруппаДоступаПартнера(НовыйПартнер.ЮрФизЛицо);
		ДанныеЗаполнения.ГруппаДоступа = НовыйПартнер.ГруппаДоступа;
	КонецЕсли;
	
	ЗаполнитьКонтактнуюИнформацию(НовыйПартнер, ДанныеЗаполнения);
	
	Попытка
		НовыйПартнер.Записать();
		ПартнерСсылка = НовыйПартнер.Ссылка;
		ДанныеЗаполнения.Партнер = ПартнерСсылка;
	Исключение
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось записать партнера ""%1"" по причине: %2';
				|en = 'Cannot save the ""%1"" partner due to: %2'", ОбщегоНазначения.КодОсновногоЯзыка()),
			НовыйПартнер.НаименованиеПолное,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(ВидМаркетплейса),
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			ТекстОшибки);
		КонецПопытки;
		
	Если ИспользоватьПривилегированныйРежим Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат ПартнерСсылка;

КонецФункции

// Находит группу доступа партнера по использованию в справочнике.
//
// Параметры:
//   ЮрФизЛицо - ПеречислениеСсылка.КомпанияЧастноеЛицо - статус партнера.
//
// Возвращаемое значение:
//   СправочникСсылка.ГруппыДоступаПартнеров - группа доступа партнера.
//
Функция ГруппаДоступаПартнера(ЮрФизЛицо)

	Если Не УправлениеДоступом.ОграничиватьДоступНаУровнеЗаписей()
			Или Не ПолучитьФункциональнуюОпцию("ИспользоватьГруппыДоступаПартнеров") Тогда
		Возврат Справочники.ГруппыДоступаПартнеров.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	Партнеры.ГруппаДоступа КАК ГруппаДоступа
		|ИЗ
		|	Справочник.Партнеры КАК Партнеры
		|ГДЕ
		|	НЕ Партнеры.ПометкаУдаления
		|	И НЕ Партнеры.ГруппаДоступа = ЗНАЧЕНИЕ(Справочник.ГруппыДоступаПартнеров.ПустаяСсылка)
		|	И Партнеры.Клиент
		|	И Партнеры.ЮрФизЛицо = &ЮрФизЛицо";
	Запрос.УстановитьПараметр("ЮрФизЛицо", ЮрФизЛицо);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Справочники.ГруппыДоступаПартнеров.ПустаяСсылка();
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.ГруппаДоступа;

КонецФункции

// Заполняет контактную информацию.
//
// Параметры:
//   КонтактноеЛицо   - СправочникОбъект.Контрагенты, СправочникОбъект.Партнеры - объект заполнения.
//   ДанныеЗаполнения - см. ДанныеЗаполненияКонтрагента.
//
Процедура ЗаполнитьКонтактнуюИнформацию(КонтактноеЛицо, ДанныеЗаполнения)

	ТипКонтактногоЛица = ТипЗнч(КонтактноеЛицо);
	Если ЗначениеЗаполнено(ДанныеЗаполнения.ЮридическийАдрес) Тогда
		Если ТипКонтактногоЛица = Тип("СправочникОбъект.Контрагенты") Тогда
			ВидИнформации = Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента;
		Иначе
			ВидИнформации = Справочники.ВидыКонтактнойИнформации.АдресПартнера;
		КонецЕсли;
		
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(
			КонтактноеЛицо,
			ДанныеЗаполнения.ЮридическийАдрес,
			ВидИнформации);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения.ФактическийАдрес)
			И ТипКонтактногоЛица = Тип("СправочникОбъект.Партнеры") Тогда
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(
			КонтактноеЛицо,
			ДанныеЗаполнения.ФактическийАдрес,
			Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

// Определяет объект расчетов по переданным параметрам.
//
// Параметры:
//   Параметры - Структура - параметры для поиска и заполнения объектов взаиморасчетов:
//     * ТипРасчетов        - ПеречислениеСсылка.ТипыРасчетовСПартнерами - тип расчетов;
//     * Организация        - СправочникСсылка.Организации - организация;
//     * Партнер            - СправочникСсылка.Партнеры - партнер;
//     * Контрагент         - СправочникСсылка.Контрагенты - контрагент;
//     * Договор            - СправочникСсылка.ДоговорыКонтрагентов - договор взаимозачета;
//     * Объект             - ОпределяемыйТип.ОбъектРасчетов - объект расчетов;
//     * ТипОбъектаРасчетов - ПеречислениеСсылка.ТипыОбъектовРасчетов - тип объекта расчетов.
//
// Возвращаемое значение:
//   - СправочникСсылка.ОбъектыРасчетов - объект расчетов.
//   - Неопределено - ошибка создания объекта расчетов.
//
Функция ОпределитьОбъектРасчетов(Параметры)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ОбъектыРасчетов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|ГДЕ
		|	НЕ ОбъектыРасчетов.ПометкаУдаления
		|	И ОбъектыРасчетов.ТипРасчетов = &ТипРасчетов
		|	И ОбъектыРасчетов.Организация = &Организация
		|	И ОбъектыРасчетов.Партнер = &Партнер
		|	И ОбъектыРасчетов.Контрагент = &Контрагент
		|	И ОбъектыРасчетов.Договор = &Договор
		|	И ОбъектыРасчетов.Объект = &Объект
		|	И ОбъектыРасчетов.ТипОбъектаРасчетов = &ТипОбъектаРасчетов";

	Для Каждого КлючИЗначение Из Параметры Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Истина);
	
	Если ВыборкаДанных.Следующий() Тогда
		Результат = ВыборкаДанных.Ссылка;
	
	Иначе
		Результат = Неопределено;
		НачатьТранзакцию();
		
		Попытка
			ОбъектРасчетовОбъект = Справочники.ОбъектыРасчетов.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(ОбъектРасчетовОбъект, Параметры);
			
			ПараметрыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Договор, 
				"Наименование, ВалютаВзаиморасчетов");
			
			ОбъектРасчетовОбъект.Наименование            = ПараметрыДоговора.Наименование;
			ОбъектРасчетовОбъект.ТипСсылки               = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Справочник.ДоговорыКонтрагентов");
			ОбъектРасчетовОбъект.ВалютаВзаиморасчетов    = ПараметрыДоговора.ВалютаВзаиморасчетов;
			ОбъектРасчетовОбъект.Валюта                  = ОбъектРасчетовОбъект.ВалютаВзаиморасчетов;
			ОбъектРасчетовОбъект.УникальныйИдентификатор = Строка(Новый УникальныйИдентификатор);
			
			ОбъектРасчетовОбъект.Записать();
			Результат = ОбъектРасчетовОбъект.Ссылка;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
		КонецПопытки;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Заполняет документ взаимозачета данными из другого объекта.
//
// Параметры:
//   ВзаимозачетЗадолженностиОбъект - ДокументОбъект.ВзаимозачетЗадолженности - документ взаимозачета к заполнению.
//   ДанныеЗаполнения               - Структура Из КлючИЗначение - данные заполнения. 
//   ЗначенияФункциональныхОпций    - См. ПолучитьФункциональныеОпции. 
//   ИмяСобытия                     - Строка - имя события журнала регистрации. 
//
// Возвращаемое значение:
//   Булево - результат заполнения документа.
//
Функция ВзаимозачетЗадолженностиЗаполнить(ВзаимозачетЗадолженностиОбъект, ДанныеЗаполнения, ЗначенияФункциональныхОпций, 
			ИмяСобытия)
	
	Если ЗначенияФункциональныхОпций = Неопределено Тогда
		ЗначенияФункциональныхОпций = ПолучитьФункциональныеОпции(ДанныеЗаполнения.Соглашение);
	КонецЕсли;
	
	Попытка
		ЗаполнитьЗначенияСвойств(ВзаимозачетЗадолженностиОбъект, ДанныеЗаполнения);
		ВзаимозачетЗадолженностиОбъект.Заполнить(ДанныеЗаполнения);
		
	Исключение
		ЗаписьЖурналаРегистрации(ИмяСобытия, 
			УровеньЖурналаРегистрации.Ошибка,
			ВзаимозачетЗадолженностиОбъект.Метаданные(),
			ВзаимозачетЗадолженностиОбъект.Ссылка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат Ложь;
	КонецПопытки;
	
	ВзаимозачетЗадолженностиОбъект.ПометкаУдаления = Ложь;
	ВзаимозачетЗадолженностиОбъект.Дата            = ДанныеЗаполнения.Дата;
	ВзаимозачетЗадолженностиОбъект.Организация     = ДанныеЗаполнения.Организация;
	
	Если ВзаимозачетЗадолженностиОбъект.ЭтоНовый() Тогда
		ВзаимозачетЗадолженностиОбъект.УстановитьНовыйНомер();
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Выполняет постобработку табличных частей "Задолженность" для документа взаимозачета.
//
// Параметры:
//   ВзаимозачетЗадолженностиОбъект - ДокументОбъект.ВзаимозачетЗадолженности - документ взаимозачета к заполнению.
//
// Возвращаемое значение:
//   Булево - результат обработки табличной части.
//
Функция ВзаимозачетЗадолженностиОбработатьТЧ(ВзаимозачетЗадолженностиОбъект)
	
	Если ВзаимозачетЗадолженностиОбъект.ДебиторскаяЗадолженность.Количество() = 0 
			И ВзаимозачетЗадолженностиОбъект.КредиторскаяЗадолженность.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	ВалютаРегламентированногоУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ВзаимозачетЗадолженностиОбъект.Организация);
	ВалютаУправленческогоУчета     = Константы.ВалютаУправленческогоУчета.Получить();
	
	Для Каждого СтрокаТабличнойЧасти Из ВзаимозачетЗадолженностиОбъект.ДебиторскаяЗадолженность Цикл
		Если СтрокаТабличнойЧасти.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета Тогда
			СтрокаТабличнойЧасти.СуммаРегл = СтрокаТабличнойЧасти.СуммаВзаиморасчетов;
		ИначеЕсли СтрокаТабличнойЧасти.СуммаРегл = 0 И ЗначениеЗаполнено(СтрокаТабличнойЧасти.ВалютаВзаиморасчетов) Тогда
			СтрокаТабличнойЧасти.СуммаРегл = РаботаСКурсамиВалютУТ.ПересчитатьВВалюту(СтрокаТабличнойЧасти.СуммаВзаиморасчетов,
				ВалютаРегламентированногоУчета,
				СтрокаТабличнойЧасти.ВалютаВзаиморасчетов,
				ВалютаРегламентированногоУчета,
				ВзаимозачетЗадолженностиОбъект.Дата,
				СтрокаТабличнойЧасти.ОбъектРасчетов);
		КонецЕсли;
			
		Если СтрокаТабличнойЧасти.ВалютаВзаиморасчетов = ВалютаУправленческогоУчета Тогда
			СтрокаТабличнойЧасти.СуммаУпр = СтрокаТабличнойЧасти.СуммаВзаиморасчетов;
		ИначеЕсли СтрокаТабличнойЧасти.СуммаУпр = 0 И ЗначениеЗаполнено(СтрокаТабличнойЧасти.ВалютаВзаиморасчетов) Тогда
			СтрокаТабличнойЧасти.СуммаУпр = РаботаСКурсамиВалютУТ.ПересчитатьВВалюту(СтрокаТабличнойЧасти.СуммаВзаиморасчетов,
				ВалютаРегламентированногоУчета,
				СтрокаТабличнойЧасти.ВалютаВзаиморасчетов,
				ВалютаУправленческогоУчета,
				ВзаимозачетЗадолженностиОбъект.Дата,
				СтрокаТабличнойЧасти.ОбъектРасчетов);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТабличнойЧасти Из ВзаимозачетЗадолженностиОбъект.КредиторскаяЗадолженность Цикл
		Если СтрокаТабличнойЧасти.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета Тогда
			СтрокаТабличнойЧасти.СуммаРегл = СтрокаТабличнойЧасти.СуммаВзаиморасчетов;
		ИначеЕсли СтрокаТабличнойЧасти.СуммаРегл = 0 И ЗначениеЗаполнено(СтрокаТабличнойЧасти.ВалютаВзаиморасчетов) Тогда
			СтрокаТабличнойЧасти.СуммаРегл = РаботаСКурсамиВалютУТ.ПересчитатьВВалюту(СтрокаТабличнойЧасти.СуммаВзаиморасчетов,
				ВалютаРегламентированногоУчета,
				СтрокаТабличнойЧасти.ВалютаВзаиморасчетов,
				ВалютаРегламентированногоУчета,
				ВзаимозачетЗадолженностиОбъект.Дата,
				СтрокаТабличнойЧасти.ОбъектРасчетов);
		КонецЕсли;
			
		Если СтрокаТабличнойЧасти.ВалютаВзаиморасчетов = ВалютаУправленческогоУчета Тогда
			СтрокаТабличнойЧасти.СуммаУпр = СтрокаТабличнойЧасти.СуммаВзаиморасчетов;
		ИначеЕсли СтрокаТабличнойЧасти.СуммаУпр = 0 И ЗначениеЗаполнено(СтрокаТабличнойЧасти.ВалютаВзаиморасчетов) Тогда
			СтрокаТабличнойЧасти.СуммаУпр = РаботаСКурсамиВалютУТ.ПересчитатьВВалюту(СтрокаТабличнойЧасти.СуммаВзаиморасчетов,
				ВалютаРегламентированногоУчета,
				СтрокаТабличнойЧасти.ВалютаВзаиморасчетов,
				ВалютаУправленческогоУчета,
				ВзаимозачетЗадолженностиОбъект.Дата,
				СтрокаТабличнойЧасти.ОбъектРасчетов);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Параметры:
//  СтатусЗаказа - ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - 
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ВариантыОбеспечения
//
Функция ВариантОбеспеченияСтрокПоСтатусуЗаказа(СтатусЗаказа)
	
	Результат = Перечисления.ВариантыОбеспечения.НеТребуется;
	
	Если СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки Тогда
		Результат = Перечисления.ВариантыОбеспечения.СоСклада;
		
	ИначеЕсли СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке Тогда
		Результат = Перечисления.ВариантыОбеспечения.СоСклада;
		
	ИначеЕсли СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен Тогда
		Результат = Перечисления.ВариантыОбеспечения.КОбеспечению;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  СтатусЗаказа - ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - 
//
// Возвращаемое значение:
//  ПеречислениеСсылка.ВариантыОбеспечения
//
Функция ВариантОбеспеченияСтрокБезОстаткаПоСтатусуЗаказа(СтатусЗаказа)
	
	Результат = Перечисления.ВариантыОбеспечения.НеТребуется;
	
	Если СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки Тогда
		Результат = Перечисления.ВариантыОбеспечения.КОбеспечению;
		
	ИначеЕсли СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке Тогда
		Результат = Перечисления.ВариантыОбеспечения.КОбеспечению;
		
	ИначеЕсли СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен Тогда
		Результат = Перечисления.ВариантыОбеспечения.КОбеспечению;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьДополнительныеСведенияПоТоварам(Заказ, СтрокаЗаказа, ИсточникДанных, ИдентификаторЭкземпляра = 1)
	
	УчетнаяЗапись = Заказ.УчетнаяЗапись;
	//@skip-check bsl-legacy-check-string-literal
	ТорговаяПлощадка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись,"ВидМаркетплейса");
	
	Для Индекс = 1 По СтрокаЗаказа.КоличествоУпаковок Цикл
		ДопСведения = Новый Структура;     
		Если  ТорговаяПлощадка = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда 
			ДопСведения.Вставить("ИдентификаторТовара",      ИсточникДанных.ИдентификаторОбъектаМаркетплейса); 
			ДопСведения.Вставить("ИдентификаторОбъектаМаркетплейса", ИсточникДанных.ИдентификаторПубликации);
		Иначе  
			ДопСведения.Вставить("ИдентификаторТовара",      ИсточникДанных.ИдентификаторSKU);
		КонецЕсли;
		ДопСведения.Вставить("ИдентификаторЭкземпляра",      ИдентификаторЭкземпляра);
		ДопСведения.Вставить("НомерОтправления",             ИсточникДанных.НомерОтправления);
		ДопСведения.Вставить("КодСтроки",                    СтрокаЗаказа.КодСтроки);
		ДопСведения.Вставить("ИдентификаторСтроки",          СтрокаЗаказа.ИдентификаторСтроки);
		ДопСведения.Вставить("ТребуетсяНомерГТД",            ИсточникДанных.ТребуетсяГТД);
		ДопСведения.Вставить("ТребуетсяРНПТ",                ИсточникДанных.ТребуетсяРНПТ);
		ДопСведения.Вставить("ТребуетсяСтранаПроисхождения", ИсточникДанных.ТребуетсяСтранаПроисхождения);
		ДопСведения.Вставить("ТребуетсяМаркировка",          ИсточникДанных.ТребуетсяМаркировка);
		ДопСведения.Вставить("ТребуетсяУИН",                 ИсточникДанных.ТребуетсяУИНЮвелирногоИзделия);
		ДопСведения.Вставить("Количество",                   1);
		
		СведенияПоТоварам = Заказ.СведенияПоТоварам; // Массив
		СведенияПоТоварам.Добавить(ДопСведения);
		ИдентификаторЭкземпляра = ИдентификаторЭкземпляра + 1;
	КонецЦикла;

КонецПроцедуры

// Параметры:
//  КоллекцияМетаданных - Неопределено -
//                      - Массив из ОбъектМетаданныхИзмерение - 
//                      - Массив из ОбъектМетаданныхРесурс - 
//                      - Массив из ОбъектМетаданныхРеквизит - 
//  ДобавляемыеПоля     - Массив из Строка - 
//  ИсключаемыеПоля     - Массив из Строка - 
//  ПустыеЗначенияПолей - Неопределено - 
//                      - Соответствие из Строка - 
//  ИмяТабличнойЧасти   - Строка - 
//
Процедура ДобавитьПоляКоллекцииМетаданных(КоллекцияМетаданных, ДобавляемыеПоля, ИсключаемыеПоля,
			ПустыеЗначенияПолей = Неопределено, ИмяТабличнойЧасти = "")
	
	Если КоллекцияМетаданных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ОбъектМетаданных Из КоллекцияМетаданных Цикл
		
		ИмяПоля = ОбъектМетаданных.Имя;
		
		Если ПустаяСтрока(ИмяТабличнойЧасти) Тогда
			ИмяИсключаемогоПоля = ИмяПоля;
		Иначе
			ИмяИсключаемогоПоля = СтрШаблон("%1.%2", ИмяТабличнойЧасти, ИмяПоля);
		КонецЕсли;
		
		Если ИсключаемыеПоля.Найти(ИмяИсключаемогоПоля) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДобавляемыеПоля.Добавить(ИмяПоля);
		
		Если ПустыеЗначенияПолей = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТипПоля = ОбъектМетаданных.Тип;
		ПустоеЗначениеПоля = "НЕОПРЕДЕЛЕНО";
		
		Если ТипПоля.Типы().Количество() = 1 Тогда
			ТипЗначения = ТипПоля.Типы()[0];
			
			Если ОбщегоНазначения.ЭтоСсылка(ТипЗначения) Тогда
				МетаданныеПоТипу = Метаданные.НайтиПоТипу(ТипЗначения);
				ПустоеЗначениеПоля = СтрШаблон("ЗНАЧЕНИЕ(%1.ПустаяСсылка)", МетаданныеПоТипу.ПолноеИмя());
				
			ИначеЕсли ТипЗначения = Тип("Строка") Тогда
				ПустоеЗначениеПоля = """""";
				
			ИначеЕсли ТипЗначения = Тип("Число") Тогда
				ПустоеЗначениеПоля = "0";
				
			ИначеЕсли ТипЗначения = Тип("Дата") Тогда
				ПустоеЗначениеПоля = "ДАТАВРЕМЯ(1, 1, 1)";
				
			ИначеЕсли ТипЗначения = Тип("Булево") Тогда
				ПустоеЗначениеПоля = "ЛОЖЬ";
			КонецЕсли;
			
		КонецЕсли;
		
		ПустыеЗначенияПолей.Вставить(ИмяПоля, ПустоеЗначениеПоля);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  НаименованиеПричины - Строка
// 
// Возвращаемое значение:
//  - Неопределено - нет причины отмены заказа клиента с переданным наименованием.
//  - ВыборкаИзРезультатаЗапроса:
//     * Ссылка - СправочникСсылка.ПричиныОтменыЗаказовКлиентов
//
Функция ВыборкаПричиныОтменыЗаказаКлиентаПоНаименованию(НаименованиеПричины)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПричиныОтмены.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПричиныОтменыЗаказовКлиентов КАК ПричиныОтмены
		|ГДЕ
		|	ПричиныОтмены.Наименование = &НаименованиеПричины
		|	И ПричиныОтмены.Наименование <> """"
		|	И НЕ ПричиныОтмены.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("НаименованиеПричины", НаименованиеПричины);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка;
	
КонецФункции

// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов
//  ПричинаОтмены - СправочникСсылка.ПричиныОтменыЗаказовКлиентов
//  ДанныеПричины - см. НовыеДанныеПричиныОтменыЗаказа
//  КэшПричинОтмены - Соответствие из КлючИЗначение:
//   * Ключ - Строка
//   * Значение - СправочникСсылка.ПричиныОтменыЗаказовКлиентов
//  ЗаписатьВРегистрСоответствия - Булево
//
Процедура ДобавитьПричинуОтменыВКэш(УчетнаяЗапись, ПричинаОтмены, ДанныеПричины, КэшПричинОтмены, ЗаписатьВРегистрСоответствия)
	
	КэшПричинОтмены.Вставить(ДанныеПричины.Идентификатор, ПричинаОтмены);
	Если ЗаписатьВРегистрСоответствия Тогда
		РегистрыСведений.СоответствияОбъектовМаркетплейсов.ЗаписатьОбъектСоответствия(
			УчетнаяЗапись,
			Перечисления.ВидыОбъектовМаркетплейсов.ПричинаОтмены,
			ДанныеПричины.Идентификатор,
			ПричинаОтмены,
			Новый Структура("НаименованиеОбъектаМаркетплейса", ДанныеПричины.Наименование));
	КонецЕсли;
	
КонецПроцедуры

#Область ОтправленияСлужебный

// Получает список причин отмены из сервиса.
//
// Параметры:
//   УчетнаяЗапись     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   НомераОтправлений - Строка, Массив Из Строка, Неопределено - номера отправлений.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ПолучитьПричиныОтмены(УчетнаяЗапись, НомераОтправлений = Неопределено) Экспорт
	
	Ошибка = НоваяОшибка();
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		Ошибка = ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьПричиныОтмены(УчетнаяЗапись, НомераОтправлений);
	ИначеЕсли ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет") Тогда
		Ошибка = ИнтеграцияСЯндексМаркетСервер.ПолучитьПричиныОтмены();
	КонецЕсли;
	
	Возврат Ошибка;
	
КонецФункции

// Параметры:
//  ИмяРегистра - Строка
//  Заказ - ДокументСсылка.ЗаказКлиента
//  НомераОтправлений - Массив из Строка
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//   * СтарыйНомерОтправления - Строка
//   * НомерРодительскогоОтправления - Строка
//
Функция ВыборкаДанныхДляЗаменыНомеровОтправленийПоЗаказу(ИмяРегистра, Заказ, НомераОтправлений)
	
	МетаданныеРегистра = ОбщегоНазначения.ОбъектМетаданныхПоПолномуИмени(ИмяРегистра); // ОбъектМетаданныхРегистрСведений
	
	ИзмеренияРегистра = Новый Массив; // Массив из Строка
	Для Каждого Измерение Из МетаданныеРегистра.Измерения Цикл
		ИзмеренияРегистра.Добавить(Измерение.Имя);
	КонецЦикла;
	
	Тексты = Новый Массив; // Массив из Строка
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.НомерОтправления КАК СтарыйНомерОтправления,
	|	&ТекстЗапросаИзмеренияРегистра КАК Поля
	|ПОМЕСТИТЬ ВТДанныеДляОбновления
	|ИЗ
	|	&ИмяРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.Заказ = &Заказ
	|	И ДанныеРегистра.НомерОтправления В(&НомераОтправлений)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ДанныеРегистра.НомерОтправления,
	|	&ТекстЗапросаИзмеренияРегистра КАК Поля
	|ИЗ
	|	&ИмяРегистра КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.Заказ = &Заказ
	|	И ДанныеРегистра.НомерРодительскогоОтправления В(&НомераОтправлений)
	|	И ДанныеРегистра.НомерОтправления <> ДанныеРегистра.НомерРодительскогоОтправления";
	
	ВыборкаИзмеренийРегистра = Новый Массив; // Массив из Строка
	Для Каждого ИмяПоля Из ИзмеренияРегистра Цикл
		ТекстПоля = СтрШаблон("ДанныеРегистра.%1 КАК %1", ИмяПоля);
		ВыборкаИзмеренийРегистра.Добавить(ТекстПоля);
	КонецЦикла;
	
	ТекстЗапросаИзмеренияРегистра = СтрСоединить(ВыборкаИзмеренийРегистра, "," + Символы.ПС);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяРегистра", ИмяРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаИзмеренияРегистра КАК Поля", ТекстЗапросаИзмеренияРегистра);
	
	Тексты.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДляОбновления.СтарыйНомерОтправления КАК СтарыйНомерОтправления,
	|	&ТекстЗапросаПоляРегистра КАК Поля
	|ИЗ
	|	ВТДанныеДляОбновления КАК ДанныеДляОбновления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИмяРегистра КАК ДанныеРегистра
	|		ПО &ТекстЗапросаСоединение";
	
	ТекстЗапросаПоляРегистра = ТекстЗапросаПолейОбъектаМетаданных(ИмяРегистра, "ДанныеРегистра");
	
	СоединениеПоИзмерениямРегистра = Новый Массив; // Массив из Строка
	Для Каждого ИмяПоля Из ИзмеренияРегистра Цикл
		ТекстПоля = СтрШаблон("ДанныеДляОбновления.%1 = ДанныеРегистра.%1", ИмяПоля);
		СоединениеПоИзмерениямРегистра.Добавить(ТекстПоля);
	КонецЦикла;
	
	ТекстЗапросаСоединение = СтрСоединить(СоединениеПоИзмерениямРегистра, " И " + Символы.ПС);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяРегистра", ИмяРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаПоляРегистра КАК Поля", ТекстЗапросаПоляРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаСоединение", ТекстЗапросаСоединение);
	
	Тексты.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Запрос.УстановитьПараметр("НомераОтправлений", НомераОтправлений);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат Выборка;
	
КонецФункции

Функция ДанныеЗаполненияПоНовымДаннымЗаписи(НовыеДанныеЗаписи, ЗаполнитьНомерРодительскогоОтправления)
	
	Результат = Новый Структура;
	
	Результат.Вставить("НомерОтправления", НовыеДанныеЗаписи.НомерОтправления);
	Если ЗаполнитьНомерРодительскогоОтправления Тогда
		Результат.Вставить("НомерРодительскогоОтправления", НовыеДанныеЗаписи.НомерОтправления);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НовыеДанныеЗаписи.Статус) Тогда
		Результат.Вставить("Статус", НовыеДанныеЗаписи.Статус);
		Результат.Вставить("ДатаУстановкиСтатуса", НовыеДанныеЗаписи.ДатаУстановкиСтатуса);
	КонецЕсли;
	
	Результат.Вставить(
		"ТребуетсяПолучениеЭтикеток",
		(НовыеДанныеЗаписи.Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке));
	
	Если Не ПустаяСтрока(НовыеДанныеЗаписи.ИдентификаторОтправления) Тогда
		Результат.Вставить("ИдентификаторОтправления", НовыеДанныеЗаписи.ИдентификаторОтправления);
	КонецЕсли;
	Если Не ПустаяСтрока(НовыеДанныеЗаписи.ИдентификаторОтгрузки) Тогда
		Результат.Вставить("ИдентификаторОтгрузки", НовыеДанныеЗаписи.ИдентификаторОтгрузки);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ЭлектронноеВзаимодействиеСлужебный

// Предназначена для распределения объектов, к которым подключаются команды ЭДО, по группам.
// 
// Параметры:
//   СоставКоманд - Структура - состав команд ЭДО:
//     * Исходящие    - Массив из Строка - объекты для команд работы с исходящими документами.
//     * Входящие     - Массив из Строка - объекты для команд работы со входящими документами.
//     * БезПодписи   - Массив из Строка - объекты для команд выполнения ЭДО без подписи.
//     * Внутренние   - Массив из Строка - объекты для команд внутреннего ЭДО.
//     * Интеркампани - Массив из Строка - объекты для команд обмена между организациями в рамках одной базы.
//     * Контрагенты  - Массив из Строка - объекты для команд работы с контрагентами.
//     * Организации  - Массив из Строка - объекты для команд работы с организациями.
//     * Договоры     - Массив из Строка - объекты для команд работы с договорами.
//
Процедура ПриОпределенииСоставаКомандЭДО(СоставКоманд)
	
	СоставКоманд.Входящие.Добавить(Метаданные.Документы.ПередачаТоваровХранителю.ПолноеИмя());
	
КонецПроцедуры

// Конструктор параметров файла прикладного электронного документа.
//
// Возвращаемое значение:
//   Структура:
//     * ТипЭлектронногоДокумента         - Неопределено, ОпределяемыйТип.ПрикладныеТипыЭлектронныхДокументовЭДО - прикладной 
//                                            тип электронного документа.
//     * НаименованиеДокументаОтправителя - Строка - наименование документа по данным отправителя.
//     * НомерДокументаОтправителя        - Строка - номер документа по данным отправителя.
//     * ДатаДокументаОтправителя         - Дата  - дата документа по данным отправителя.
//     * СуммаДокумента                   - Число - сумма документа по данным отправителя 
//     * Отправитель                      - см. ФорматыЭДО.НовоеОписаниеУчастникаЭДО.
//     * Получатель                       - см. ФорматыЭДО.НовоеОписаниеУчастникаЭДО.
//     * ЕстьМаркировка                   - Булево - присутствуют ли сведения о маркировке.
//     * ВидТорговойПлощадки              - ПеречислениеСсылка.ВидыМаркетплейсов, Неопределено - вид торговой площадки.
//     * Представление                    - ТабличныйДокумент - представление файла.
//     * ДанныеРазбора                    - ТаблицаЗначений - результат чтения файла в виде таблицы.
//
Функция НовыйПрикладнойТипЭлектронногоДокумента()
	
	Результат = Новый Структура;
	Результат.Вставить("ТипЭлектронногоДокумента",         Неопределено);
	Результат.Вставить("НаименованиеДокументаОтправителя", "");
	Результат.Вставить("НомерДокументаОтправителя",        "");
	Результат.Вставить("ДатаДокументаОтправителя",         Дата(1, 1, 1));
	Результат.Вставить("СуммаДокумента",                   0);
	Результат.Вставить("Отправитель",                      ФорматыЭДО.НовоеОписаниеУчастникаЭДО());
	Результат.Вставить("Получатель",                       ФорматыЭДО.НовоеОписаниеУчастникаЭДО());
	Результат.Вставить("ЕстьМаркировка",                   Ложь);
	Результат.Вставить("ВидТорговойПлощадки",              Неопределено);
	Результат.Вставить("Представление",                    Новый ТабличныйДокумент);
	Результат.Вставить("ДанныеРазбора",                    Новый ТаблицаЗначений);
	
	Возврат Результат;
	
КонецФункции

Функция НоваяТаблицаТоварыРасхождения(ТоварыИзДокумента)

	ТоварыРасхождения = Новый ТаблицаЗначений;
	ТоварыРасхождения = ТоварыИзДокумента.Скопировать();
	ТоварыРасхождения.Индексы.Добавить("Номенклатура, Характеристика");
	
	Если ТоварыРасхождения.Колонки.Найти("КоличествоПоДокументу") <> Неопределено Тогда
		ТоварыРасхождения.Колонки.Удалить("КоличествоПоДокументу");
		ТоварыРасхождения.Колонки.Удалить("КоличествоУпаковокПоДокументу");
		ТоварыРасхождения.Колонки.Удалить("СуммаПоДокументу");
	КонецЕсли;
	
	ТоварыРасхождения.Колонки.Количество.Имя         = "КоличествоПоДокументу";
	ТоварыРасхождения.Колонки.КоличествоУпаковок.Имя = "КоличествоУпаковокПоДокументу";
	ТоварыРасхождения.Колонки.Сумма.Имя              = "СуммаПоДокументу";
	
	ТоварыРасхождения.Колонки.Добавить("ТипНоменклатуры",               Новый ОписаниеТипов("ПеречислениеСсылка.ТипыНоменклатуры"));
	ТоварыРасхождения.Колонки.Добавить("Артикул",                       Метаданные.ОпределяемыеТипы.Артикул.Тип);
	ТоварыРасхождения.Колонки.Добавить("ХарактеристикиИспользуются",    Новый ОписаниеТипов("Булево"));
	ТоварыРасхождения.Колонки.Добавить("Количество",                    ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	ТоварыРасхождения.Колонки.Добавить("КоличествоУпаковок",            ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	ТоварыРасхождения.Колонки.Добавить("КоличествоУпаковокРасхождения", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	ТоварыРасхождения.Колонки.Добавить("Сумма",                         Метаданные.ОпределяемыеТипы.ДенежнаяСуммаНеотрицательная.Тип);
	ТоварыРасхождения.Колонки.Добавить("СуммаРасхождения",              Метаданные.ОпределяемыеТипы.ДенежнаяСуммаЛюбогоЗнака.Тип);
	ТоварыРасхождения.Колонки.Добавить("ЕстьРасхождения",               Новый ОписаниеТипов("Булево"));
	ТоварыРасхождения.Колонки.Добавить("ИндексНабора",                  ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	Если ТоварыРасхождения.Колонки.Найти("НоменклатураНабора") = Неопределено Тогда
		ТоварыРасхождения.Колонки.Добавить("НоменклатураНабора", Метаданные.ОпределяемыеТипы.НоменклатураБЭД.Тип);
	КонецЕсли;
	
	ТоварыРасхождения.Колонки.Добавить("Маркировка",                    Новый ОписаниеТипов("Строка"));
	ТоварыРасхождения.Колонки.Добавить("РасхожденийПоМаркировке",       ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТоварыРасхождения.Колонки.Добавить("СведенияОМаркировке");
	
	Возврат ТоварыРасхождения;

КонецФункции

Функция НоваяТаблицаШтрихкодыУпаковокРасхождения(ШтрихкодыУпаковокИзДокумента)

	ШтрихкодыУпаковокРасхождения = Новый ТаблицаЗначений;
	ШтрихкодыУпаковокРасхождения = ШтрихкодыУпаковокИзДокумента.Скопировать();
	ШтрихкодыУпаковокРасхождения.Индексы.Добавить("ЗначениеШтрихкода");
	
	ШтрихкодыУпаковокРасхождения.Колонки.Количество.Имя                         = "КоличествоПоДокументу";
	ШтрихкодыУпаковокРасхождения.Колонки.КоличествоПотребительскихУпаковок.Имя  = "КоличествоПотребительскихУпаковокПоДокументу";
	ШтрихкодыУпаковокРасхождения.Колонки.КоличествоВПотребительскойУпаковке.Имя = "КоличествоВПотребительскойУпаковкеПоДокументу";
	
	ШтрихкодыУпаковокРасхождения.Колонки.Добавить("Упаковка",                          Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
	ШтрихкодыУпаковокРасхождения.Колонки.Добавить("Количество",                        ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ШтрихкодыУпаковокРасхождения.Колонки.Добавить("КоличествоПотребительскихУпаковок", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ШтрихкодыУпаковокРасхождения.Колонки.Добавить("КоличествоРаспределено",            ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ШтрихкодыУпаковокРасхождения.Колонки.Добавить("КоличествоРаспределеноПоДокументу", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ШтрихкодыУпаковокРасхождения.Колонки.Добавить("Статус",                            Новый ОписаниеТипов("Строка"));
	ШтрихкодыУпаковокРасхождения.Колонки.Добавить("ТекстСообщения",                    Новый ОписаниеТипов("Строка"));
	
	Возврат ШтрихкодыУпаковокРасхождения;
	
КонецФункции

Функция ПрочитатьТабличныйДокумент(Знач ДвоичныеДанные, РасширениеФайла = Неопределено, ТекстОшибки = "")
	
	Если Не ЗначениеЗаполнено(РасширениеФайла) Тогда
		РасширениеФайла = ОпределитьРасширениеФайлаПоСигнатуре(ДвоичныеДанные);
	КонецЕсли;
	
	Попытка
		//@skip-check missing-temporary-file-deletion
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла(РасширениеФайла);
		ДвоичныеДанные.Записать(ИмяВременногоФайла);
		
		ДанныеФайла = Новый ТабличныйДокумент;
		ДанныеФайла.Прочитать(ИмяВременногоФайла, СпособЧтенияЗначенийТабличногоДокумента.Значение);
		ФайловаяСистема.УдалитьВременныйФайл(ИмяВременногоФайла);
		
	Исключение
		ДанныеФайла = Неопределено;
		ТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	Возврат ДанныеФайла;
	
КонецФункции

// Определяет расширение файла данных исходя из его сигнатуры. 
// Файл анализируются по первым 8 байтам на отношению к типам xlsx, xls, ods, pdf.
//
// Параметры:
//   ДвоичныеДанные - ДвоичныеДанные - файл документа.
//
// Возвращаемое значение:
//   Строка, Неопределено - расширение файла двоичных данных или Неопределено, если не удалось определить расширение. 
//
Функция ОпределитьРасширениеФайлаПоСигнатуре(Знач ДвоичныеДанные)
	
	Если ТипЗнч(ДвоичныеДанные) <> Тип("ДвоичныеДанные") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПотокДанных  = ДвоичныеДанные.ОткрытьПотокДляЧтения();
	ЧтениеДанных = Новый ЧтениеДанных(ПотокДанных);
	
	Символ1 = ЧтениеДанных.ПрочитатьБайт();
	Символ2 = ЧтениеДанных.ПрочитатьБайт();
	Символ3 = ЧтениеДанных.ПрочитатьБайт();
	Символ4 = ЧтениеДанных.ПрочитатьБайт();
	Символ5 = ЧтениеДанных.ПрочитатьБайт();
	Символ6 = ЧтениеДанных.ПрочитатьБайт();
	Символ7 = ЧтениеДанных.ПрочитатьБайт();
	Символ8 = ЧтениеДанных.ПрочитатьБайт();
	
	ПотокДанных.Закрыть();
	
	Если Символ1 = 208 И Символ2 = 207 И Символ3 = 17 И Символ4 = 224 И Символ5 = 161 И Символ6 = 177 И Символ7 = 26 
			И Символ8 = 225 Тогда
		// D0 CF 11 E0 A1 B1 1A E1
		Возврат "xls";
		
	ИначеЕсли (Символ1 = 80 И Символ2 = 75 И Символ3 = 3 И Символ4 = 4 И Символ5 = 20)
				Или (Символ1 = 80 И Символ2 = 75 И Символ3 = 3 И Символ4 = 4 И Символ5 = 10) Тогда
		// 50 4B 03 04 32 / 50 4B 03 04 16
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xlsx");
		ДвоичныеДанные.Записать(ИмяВременногоФайла);
		
		Попытка
			Архиватор = Новый ЧтениеZipФайла(ИмяВременногоФайла);
			Если Архиватор.Элементы.Найти("workbook.xml") = Неопределено Тогда
				Расширение = "ods";
			Иначе
				Расширение = "xlsx";
			КонецЕсли;
			Архиватор.Закрыть();
			
		Исключение
			Расширение = Неопределено;
		КонецПопытки;
		
		УдалитьФайлы(ИмяВременногоФайла);
		Возврат Расширение;
		
	ИначеЕсли Символ1 = 37 И Символ2 = 80 И Символ3 = 68 И Символ4 = 70 И Символ5 = 45 Тогда
		// 25 50 44 46 2D
		Возврат "pdf";
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Получает информацию о товарах из электронного документа прикладного типа.
//
// Параметры:
//   ДанныеЭлектронногоДокумента - см. ОтражениеВУчетеЭДО.НовыеДанныеЭлектронногоДокументаДляОтраженияВУчете.
//   ТекстОшибки                 - Строка - представление ошибки.
// 
// Возвращаемое значение:
//   - Неопределено
//   - см. НоваяТаблицаТоваровЭДО
//
Функция ИнформацияОТоварахИзТабличногоДокумента(ДанныеЭлектронногоДокумента, ТекстОшибки = "")
	
	УчетнаяЗапись = ДанныеЭлектронногоДокумента.ДополнительныеДанные.УчетнаяЗапись;	// СправочникСсылка.УчетныеЗаписиМаркетплейсов
	ВидТорговойПлощадки = ДанныеЭлектронногоДокумента.ДополнительныеДанные.ВидТорговойПлощадки;
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ВидТорговойПлощадки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЭлектронногоДокумента.ДополнительныеДанные.ДанныеРазбора) = Тип("ТаблицаЗначений") Тогда
		ДанныеРазбора = ДанныеЭлектронногоДокумента.ДополнительныеДанные.ДанныеРазбора;
	Иначе
		ДанныеРазбора = Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеРазбора) Тогда
		Файл            = Новый Файл(ДанныеЭлектронногоДокумента.ДанныеОсновногоФайла.ИмяФайла);
		РасширениеФайла = НРег(СтрЗаменить(Файл.Расширение, ".", ""));
		
		Параметры = НовыйПрикладнойТипЭлектронногоДокумента();
		ПриОпределенииПараметровФайлаПрикладногоЭлектронногоДокумента(
			ДанныеЭлектронногоДокумента.ДанныеОсновногоФайла.ДвоичныеДанные, 
			РасширениеФайла, 
			Параметры,
			ВидТорговойПлощадки,
			ТекстОшибки);
		
		Если Параметры.ТипЭлектронногоДокумента = Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'Не удалось прочитать содержимое файла.';
								|en = 'Cannot read the file content.'")
				+ Символы.ПС + ТекстОшибки;
			Возврат Неопределено;
		КонецЕсли;
		
		ДанныеРазбора = Параметры.ДанныеРазбора;
	КонецЕсли;
	
	ДанныеЭлектронногоДокумента.ДополнительныеДанные.ДанныеРазбора = ДанныеРазбора;
	
	ИнформацияОТоварах = НоваяТаблицаТоваровЭДО();
	
	Для Каждого СтрокаТаблицыЗначений Из ДанныеРазбора Цикл
		Запись = ИнформацияОТоварах.Добавить();
		
		Если ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
			Запись.ИД                                = СтрокаТаблицыЗначений.ИдентификаторПубликации;
			Запись.Штрихкод                          = СтрокаТаблицыЗначений.Штрихкод;
			Запись.Наименование                      = СтрокаТаблицыЗначений.ПредставлениеОбъекта;
			Запись.НаименованиеХарактеристики        = "";
			Запись.КодТовара                         = СтрокаТаблицыЗначений.ИдентификаторОбъекта;
			Запись.Артикул                           = СтрокаТаблицыЗначений.ИдентификаторПубликации;
			Запись.БазоваяЕдиницаНаименование        = "";
			Запись.МаркируемыйТовар                  = Ложь;
			Запись.КонтрольныеИдентификационныеЗнаки = ""; 
			Запись.КоличествоУпаковок                = СтрокаТаблицыЗначений.КоличествоУпаковок; 
			
		ИначеЕсли ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсWildberries Тогда
			Запись.ИД                                = СтрокаТаблицыЗначений.Штрихкод;
			Запись.Штрихкод                          = СтрокаТаблицыЗначений.Штрихкод;
			Запись.Наименование                      = СтрокаТаблицыЗначений.НоменклатураНаименование;
			Запись.НаименованиеХарактеристики        = СтрокаТаблицыЗначений.ХарактеристикаНаименование;
			Запись.КодТовара                         = "";
			Запись.Артикул                           = СтрокаТаблицыЗначений.Артикул;
			Запись.БазоваяЕдиницаНаименование        = СтрокаТаблицыЗначений.УпаковкаНаименование;
			Запись.МаркируемыйТовар                  = ЗначениеЗаполнено(СтрокаТаблицыЗначений.КонтрольныеИдентификационныеЗнаки);
			Запись.КонтрольныеИдентификационныеЗнаки = СтрокаТаблицыЗначений.КонтрольныеИдентификационныеЗнаки; 
			Запись.КоличествоУпаковок                = СтрокаТаблицыЗначений.КоличествоУпаковок; 
			
			Если Не ЗначениеЗаполнено(Запись.ИД) Тогда
				Запись.ИД = СтрокаТаблицыЗначений.Артикул 
					+ "#" + СтрокаТаблицыЗначений.Артикул + "/" + СтрокаТаблицыЗначений.ХарактеристикаНаименование 
					+ "#" + СтрокаТаблицыЗначений.Артикул + "/" + СтрокаТаблицыЗначений.УпаковкаНаименование;
			КонецЕсли;
		КонецЕсли;
		
		Если Запись.Сопоставление = Неопределено Тогда
			Запись.Сопоставление = Новый Структура;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицыЗначений.Номенклатура) Тогда
			Запись.Сопоставление.Вставить("ВариантУказанияНоменклатурыИБ", СтрокаТаблицыЗначений.ВариантУказанияНоменклатуры);
			Запись.Сопоставление.Вставить("НоменклатураИБ",                СтрокаТаблицыЗначений.Номенклатура);
			Запись.Сопоставление.Вставить("ХарактеристикаИБ",              СтрокаТаблицыЗначений.Характеристика);
			Запись.Сопоставление.Вставить("УпаковкаИБ",                    СтрокаТаблицыЗначений.Упаковка);
		КонецЕсли;
	КонецЦикла;
	
	Если ИнформацияОТоварах.Количество() > 0 Тогда
		ФорматыЭДО.ДополнитьИнформациюОТовареДаннымиДляСопоставления(ИнформацияОТоварах);
		Возврат ИнформацияОТоварах;
	КонецЕсли;
	
	ТекстОшибки = НСтр("ru = 'Данные о товарах отсутствуют.';
						|en = 'There is no data about goods.'");
	Возврат Неопределено;
	
КонецФункции

// Возвращает данные электронного документа.
//
// Параметры:
//   ДанныеЭлектронногоДокумента - см. ОтражениеВУчетеЭДО.НовыеДанныеЭлектронногоДокументаДляОтраженияВУчете.
//   ТекстОшибки                 - Строка - представление ошибки.
// 
// Возвращаемое значение:
//   См. ФорматыЭДО.ДанныеЭлектронногоДокумента.
//
Функция ПолучитьДанныеЭлектронногоДокумента(ДанныеЭлектронногоДокумента, ТекстОшибки = "")
	
	Если ТипЗнч(ДанныеЭлектронногоДокумента.ДополнительныеДанные.ДанныеРазбора) = Тип("Структура") Тогда
		ДанныеРазбора = ДанныеЭлектронногоДокумента.ДополнительныеДанные.ДанныеРазбора;
	Иначе
		ДанныеРазбора = Неопределено;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеРазбора) Тогда
		ПараметрыПолучения = ЭлектронныеДокументыЭДО.НовыеПараметрыПолученияДанныхДокумента();
		ПараметрыПолучения.ОсновнойФайл       = ДанныеЭлектронногоДокумента.ДанныеОсновногоФайла;
		ПараметрыПолучения.ДополнительныйФайл = ДанныеЭлектронногоДокумента.ДанныеДополнительногоФайла;
		
		ДанныеРазбора = ЭлектронныеДокументыЭДО.ДанныеДокументаДляЗагрузкиПросмотра(ПараметрыПолучения);
		
	ИначеЕсли Не ЗначениеЗаполнено(ДанныеРазбора.НовыйЭД) Тогда
		ДанныеРазбора.НовыйЭД = ДанныеРазбора.ДеревоРазбора.Строки.Найти("ОбменСКонтрагентами", "ТипОбъекта").Строки[0];
	КонецЕсли;
		
	Если Не ДанныеРазбора.Свойство("НовыйЭД") Тогда
		ДанныеРазбора = Неопределено;
		ТекстОшибки   = НСтр("ru = 'Не удалось прочитать данные электронного документа. Неизвестный формат обмена.';
							|en = 'Cannot read the electronic document data. Unknown exchange format.'");
	КонецЕсли;
	
	ДанныеЭлектронногоДокумента.ДополнительныеДанные.ДанныеРазбора = ДанныеРазбора;
	
	Возврат ДанныеРазбора;
	
КонецФункции

// Аналог функции см. ДеревоЭлектронногоДокументаБЭД.СтрокиТребующиеСопоставления.
// Определяет строки электронного документа, требующие сопоставления.
//
// Параметры:
//   ДеревоДокумента - см. ДеревоЭлектронногоДокументаБЭД.НайтиСоздатьТипОбъектаВДеревеРазбора.
//   ПолныйСписок    - Булево - получать ли полный список, без учета сопоставленной номенклатуры.
//   ТекстОшибки     - Строка - представление ошибки.
// 
// Возвращаемое значение:
//   - Неопределено
//   - Соответствие из КлючИЗначение - строки для сопоставления:
//       * Ключ     - см. СопоставлениеНоменклатурыКонтрагентов.ИдентификаторНоменклатурыКонтрагентаПоНатуральнымКлючам.
//       * Значение - см. ФорматыЭДО.НовыеДанныеДляСопоставленияТоваров.
//
Функция СтрокиТребующиеСопоставления(Знач ДеревоДокумента, Знач ПолныйСписок = Ложь, ТекстОшибки = "")
	
	ЭтоОбработкаФормата = ФорматыЭДО.ЭтоОбработкаФормата(ДеревоДокумента.ЗначениеРеквизита);
	
	Если ЭтоОбработкаФормата Тогда
		Попытка
			Результат = ДеревоДокумента.ЗначениеРеквизита.СтрокиТребующиеСопоставления();
			
		Исключение
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр(
				"ru = 'Менеджер %1, не удалось заполнить номенклатуру в дереве документа, по причине: %2';
				|en = 'Manager %1. Cannot fill items in the document tree due to: %2'"), 
				ТипЗнч(ДеревоДокумента.ЗначениеРеквизита), 
				ОписаниеОшибки());
			Возврат Неопределено;
		КонецПопытки;
		
	Иначе
		Результат = Новый Соответствие;
		
		// Для случаев, когда товары в дереве ЭД представлены отдельными корневыми строками с именем "СтрокаТЧ".
		Если ДеревоДокумента.ЗначениеРеквизита = Неопределено Тогда
			Для Каждого СтрокаДерева Из ДеревоДокумента.Строки Цикл
				Если СтрокаДерева.Реквизит <> "СтрокаТЧ" Тогда
					Продолжить;
				КонецЕсли;
				
				Сопоставление = Неопределено;
				СтрокаКолонкиСопоставления = Неопределено;
				Для Каждого СтрокаКолонки Из СтрокаДерева.Строки Цикл
					Если СтрокаКолонки.Реквизит = "Сопоставление" Тогда
						СтрокаКолонкиСопоставления = СтрокаКолонки;
						Сопоставление = СтрокаКолонки.ЗначениеРеквизита;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если Сопоставление = Неопределено Тогда
					Продолжить;
				ИначеЕсли Не Сопоставление.Свойство("Идентификатор") Тогда
					Продолжить;
				КонецЕсли;
				
				Если Не ПолныйСписок
						И Сопоставление.Свойство("НоменклатураИБ")
						И ЗначениеЗаполнено(Сопоставление.НоменклатураИБ) Тогда
					Продолжить;
				КонецЕсли;
				
				Результат.Вставить(СтрокаКолонкиСопоставления, Сопоставление);
			КонецЦикла;
			
		Иначе
			// Для случаев, когда товары в дереве ЭД подчинены одной корневой строке дерева.
			ИменаТаблицТоваров = Новый Массив;
			ИменаТаблицТоваров.Добавить("СведенияОТоварах");
			ИменаТаблицТоваров.Добавить("ТаблицаТоваров");
			ИменаТаблицТоваров.Добавить("ТаблицаУслуг");
			ИменаТаблицТоваров.Добавить("РезультатыПриемки");
			ИменаТаблицТоваров.Добавить("ТаблицаСчетаФактуры.СведенияОбОтгруженныхПозициях");
			
			Товары = Неопределено;
			Для Каждого ИмяТаблицы Из ИменаТаблицТоваров Цикл
				Товары = ДеревоДокумента.ЗначениеРеквизита.Строки.Найти(ИмяТаблицы, "ПолныйПуть", Истина);
				Если Товары <> Неопределено Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если Товары = Неопределено Тогда
				Возврат Результат;
			КонецЕсли;
			
			Для Каждого СтрокаТовара Из Товары.Строки Цикл
				Сопоставление = ДеревоЭлектронногоДокументаБЭДСлужебный.ИзвлечьСопоставлениеНоменклатурыИзДереваДокумента(
					СтрокаТовара);
				Если Сопоставление = Неопределено Тогда
					Продолжить;
				ИначеЕсли Не Сопоставление.Свойство("Идентификатор") Тогда
					Продолжить;
				КонецЕсли;
				
				Если Не ПолныйСписок
						И Сопоставление.Свойство("НоменклатураИБ")
						И ЗначениеЗаполнено(Сопоставление.НоменклатураИБ) Тогда
					Продолжить;
				КонецЕсли;
				
				Результат.Вставить(СтрокаТовара, Сопоставление);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ТоварыПодчиненыОднойКорневойСтрокеДерева(Знач СтрокиТребующиеСопоставления)
	
	Результат = Ложь;
	
	Если ЗначениеЗаполнено(СтрокиТребующиеСопоставления) Тогда
		Для Каждого КлючИЗначение Из СтрокиТребующиеСопоставления Цикл
			Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(КлючИЗначение.Ключ, "ЗначениеРеквизита") Тогда
				Результат = Истина;
			КонецЕсли;
			
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ОпределитьКлючевыеРеквизитыСопоставления(ВидТорговойПлощадки, ЭлементСопоставления, Идентификатор, 
			КодНоменклатуры, Штрихкод)
	
	Константа_Идентификатор = ИнтеграцияСМаркетплейсомOzonКлиентСервер.КонстантыСервиса()["УПД_ИдентификаторОбъекта"];	//@NON-NLS-1
	Константа_Штрихкод      = ИнтеграцияСЯндексМаркетКлиентСервер.КонстантыСервиса()["УПД_Штрихкод"];	//@NON-NLS-1
	
	ПутьКИдентификатору = ЭлементСопоставления.ПолныйПуть + ".ТоварКод";
	Если СтрНайти(ПутьКИдентификатору, "СведенияОТоварах") = 0 Тогда
		ПутьКИдентификатору = ЭлементСопоставления.ПолныйПуть + ".Код";
	КонецЕсли;
	ПутьКДопДанным = ЭлементСопоставления.ПолныйПуть + ".ДопДанныеПодписанные";
	
	Идентификатор   = "";
	КодНоменклатуры = "";
	Штрихкод        = "";
	
	Если ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		Идентификатор = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ЭлементСопоставления, 
			ПутьКИдентификатору, Ложь);
		ДопДанныеПодписанные = ЭлементСопоставления.Строки.Найти(ПутьКДопДанным, "ПолныйПуть", Истина);
		
		Если ЗначениеЗаполнено(ДопДанныеПодписанные.Значение) Тогда
			ПутьКДопДанным = ПутьКДопДанным + ".НомерСтроки";
			Для Каждого СтрокаДереваЗначений Из ДопДанныеПодписанные.Строки Цикл
				Имя = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаДереваЗначений, 
					ПутьКДопДанным + ".Идентификатор");
				Значение = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаДереваЗначений, 
					ПутьКДопДанным + ".Значение");
				
				Если ЗначениеЗаполнено(Имя)
						И ВРег(Имя) = Константа_Идентификатор
						И ЗначениеЗаполнено(Значение) Тогда
					КодНоменклатуры = СокрЛП(Значение);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсWildberries Тогда
		Идентификатор = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ЭлементСопоставления, 
			ПутьКИдентификатору, Ложь);
		Штрихкод = Идентификатор;
		
	ИначеЕсли ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		ДопДанныеПодписанные = ЭлементСопоставления.Строки.Найти(ПутьКДопДанным, "ПолныйПуть", Истина);
		
		Если ЗначениеЗаполнено(ДопДанныеПодписанные.Значение) Тогда
			ПутьКДопДанным = ПутьКДопДанным + ".НомерСтроки";
			Для Каждого СтрокаДереваЗначений Из ДопДанныеПодписанные.Строки Цикл
				Имя = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаДереваЗначений, 
					ПутьКДопДанным + ".Идентификатор");
				Значение = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(СтрокаДереваЗначений, 
					ПутьКДопДанным + ".Значение");
				
				Если ЗначениеЗаполнено(Имя)
						И ВРег(Имя) = Константа_Штрихкод
						И ЗначениеЗаполнено(Значение) Тогда
					Штрихкод = СокрЛП(Значение);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьШтрихкодУпаковкиИзСтрокиДерева(Штрихкоды, ЗначениеШтрихкода, 
			Знач Номенклатура, Знач Характеристика, Знач ВидУпаковки)
	
	Если ПустаяСтрока(ЗначениеШтрихкода) 
			Или ЗначениеШтрихкода = "-" Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицыЗначений = Штрихкоды.Добавить();
	СтрокаТаблицыЗначений.ЗначениеШтрихкода = ЗначениеШтрихкода;
	СтрокаТаблицыЗначений.Номенклатура      = Номенклатура;
	СтрокаТаблицыЗначений.Характеристика    = Характеристика;
	СтрокаТаблицыЗначений.ВидУпаковки       = ВидУпаковки;
	
КонецПроцедуры

#КонецОбласти

#Область СозданиеКарточкиНоменклатуры

// Возвращаемое значение:
//  Структура:
// * СоздаваемаяНоменклатура - см. НоваяСоздаваемаяНоменклатура
// * СоздаваемыеХарактеристики - см. НовыеСоздаваемыеХарактеристики
// * СоздаваемыеУпаковки - см. НовыеСоздаваемыеУпаковки
//
Функция НовыеДанныеДляСозданияНоменклатурыПоДаннымКонтрагента() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("СоздаваемаяНоменклатура",   НоваяСоздаваемаяНоменклатура());
	Результат.Вставить("СоздаваемыеХарактеристики", НовыеСоздаваемыеХарактеристики());
	Результат.Вставить("СоздаваемыеУпаковки",       НовыеСоздаваемыеУпаковки());
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//   * Ключ - СправочникСсылка.Номенклатура - Ссылка создаваемой номенклатуры
//   * Значение - см. НовоеОписаниеДанныхДляСозданияНоменклатуры
//
Функция НоваяСоздаваемаяНоменклатура()
	Возврат Новый Соответствие;
КонецФункции

// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//   * Ключ - СправочникСсылка.ХарактеристикиНоменклатуры - Ссылка создаваемой характеристики
//   * Значение - см. НовоеОписаниеДанныхДляСозданияХарактеристики
//
Функция НовыеСоздаваемыеХарактеристики()
	Возврат Новый Соответствие;
КонецФункции

// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//   * Ключ - СправочникСсылка.УпаковкиЕдиницыИзмерения - Ссылка создаваемой упаковки
//   * Значение - см. НовоеОписаниеДанныхДляСозданияУпаковок
//
Функция НовыеСоздаваемыеУпаковки()
	Возврат Новый Соответствие;
КонецФункции

Процедура УстановитьСсылкиНаНоменклатуруВДанныхКонтрагента(ДанныеДляСозданияНоменклатуры)
	
	ИменаПолей = ИменаПолейДляПоискаПоНаименованию("Номенклатура");
	
	РазличнаяНоменклатура = РазличныеНаименованияЭлементов(ДанныеДляСозданияНоменклатуры, ИменаПолей);
	
	Для Каждого КлючЗначение Из РазличнаяНоменклатура Цикл
		
		Ссылка = Справочники.Номенклатура.ПолучитьСсылку(Новый УникальныйИдентификатор);
		
		Отбор = Новый Структура;
		Отбор.Вставить(ИменаПолей.ИмяПоляНаименование, КлючЗначение.Ключ);
		
		НайденныеСтроки = ДанныеДляСозданияНоменклатуры.НайтиСтроки(Отбор);
		
		Для Каждого Строка Из НайденныеСтроки Цикл
			Строка[ИменаПолей.ИмяПоляСсылка] = Ссылка;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыполнитьПоискХарактеристикВДанныхКонтрагента(ДанныеДляСозданияНоменклатуры,
			РеквизитыНоменклатуры)
	
	ИспользованиеХарактеристик = РеквизитыНоменклатуры.ИспользованиеХарактеристик;
	
	Если ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать Тогда
		Возврат;
	КонецЕсли;
	
	ИменаПолей                 = ИменаПолейДляПоискаПоНаименованию("Характеристика");
	ПолноеИмяОбъектаМетаданных = Метаданные.Справочники.ХарактеристикиНоменклатуры.ПолноеИмя();
	
	Если ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры Тогда
		
		УстановитьСсылкиНаНовыеЭлементы(ДанныеДляСозданияНоменклатуры, ПолноеИмяОбъектаМетаданных, ИменаПолей);
		Возврат;
		
	КонецЕсли;
	
	Если ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры Тогда
		ВладелецХарактеристик = РеквизитыНоменклатуры.ВидНоменклатуры;
		
	ИначеЕсли ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры Тогда
		
		//@skip-check bsl-legacy-check-string-literal
		ВладелецХарактеристик = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			РеквизитыНоменклатуры.ВидНоменклатуры, "ВладелецХарактеристик");
		
	Иначе
		Возврат;
	КонецЕсли;
	
	РазличныеХарактеристики = РазличныеНаименованияЭлементов(ДанныеДляСозданияНоменклатуры, ИменаПолей);
	
	Если РазличныеХарактеристики.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСсылкиНаНайденныеЭлементы(
		ДанныеДляСозданияНоменклатуры,
		РазличныеХарактеристики,
		ВладелецХарактеристик,
		ПолноеИмяОбъектаМетаданных,
		ИменаПолей);
	
КонецПроцедуры

Процедура ВыполнитьПоискУпаковокВДанныхКонтрагента(ДанныеДляСозданияНоменклатуры,
			ВладелецХарактеристик)
	
	ИспользоватьУпаковки = ВладелецХарактеристик.ИспользоватьУпаковки;
	
	Если Не ИспользоватьУпаковки Тогда
		Возврат;
	КонецЕсли;
	
	ИменаПолей                 = ИменаПолейДляПоискаПоНаименованию("Упаковка");
	ПолноеИмяОбъектаМетаданных = Метаданные.Справочники.УпаковкиЕдиницыИзмерения.ПолноеИмя();
	
	Если ВладелецХарактеристик.НаборУпаковок = Справочники.НаборыУпаковок.ИндивидуальныйДляНоменклатуры Тогда
		
		УстановитьСсылкиНаНовыеЭлементы(ДанныеДляСозданияНоменклатуры, ПолноеИмяОбъектаМетаданных, ИменаПолей);
		Возврат;
		
	КонецЕсли;
	
	ВладелецУпаковок = ВладелецХарактеристик.НаборУпаковок;
	
	РазличныеУпаковки = РазличныеНаименованияЭлементов(ДанныеДляСозданияНоменклатуры, ИменаПолей);
	
	Если РазличныеУпаковки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСсылкиНаНайденныеЭлементы(
		ДанныеДляСозданияНоменклатуры,
		РазличныеУпаковки,
		ВладелецУпаковок,
		ПолноеИмяОбъектаМетаданных,
		ИменаПолей);
	
КонецПроцедуры

// Параметры:
//  Префикс - Строка - Префикс
// 
// Возвращаемое значение:
//  Структура:
//   * ИмяПоляСсылка - Строка - 
//   * ИмяПоляНаименование - Строка - 
//   * ИмяПоляСсылкаНайдена - Строка -
// 
Функция ИменаПолейДляПоискаПоНаименованию(Префикс)
	
	Результат = Новый Структура;
	
	Результат.Вставить("ИмяПоляСсылка",        Префикс);
	Результат.Вставить("ИмяПоляНаименование",  Префикс + "Наименование");
	Результат.Вставить("ИмяПоляСсылкаНайдена", Префикс + "Найдена");
	
	Возврат Результат;
	
КонецФункции

// Получает из переданной коллекции различные наименования элементов.
// 
// Параметры:
//  МассивСтрокДляОбхода - Массив из СтрокаТаблицыЗначений из см. НоваяТаблицаДанныхКонтрагентаДляСозданияНоменклатуры
//                       - см. НоваяТаблицаДанныхКонтрагентаДляСозданияНоменклатуры
//  ИменаПолей - см. ИменаПолейДляПоискаПоНаименованию
//
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//   * Ключ  - Строка - 
//   * Значение - Булево - Не используется
//
Функция РазличныеНаименованияЭлементов(МассивСтрокДляОбхода, ИменаПолей)
	
	РазличныеНаименования = Новый Соответствие;
	
	Для Каждого Строка Из МассивСтрокДляОбхода Цикл
		
		Ключ = Строка[ИменаПолей.ИмяПоляНаименование]; // Строка
		
		Если ПустаяСтрока(Ключ) Тогда
			Продолжить;
		КонецЕсли;
		
		Если РазличныеНаименования.Получить(Ключ) = Неопределено Тогда
			РазличныеНаименования.Вставить(Ключ, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РазличныеНаименования;
	
КонецФункции

Процедура УстановитьСсылкиНаНовыеЭлементы(ДанныеДляСозданияНоменклатуры, ПолноеИмяОбъектаМетаданных, ИменаПолей)
	
	ИменаПолейНоменклатуры = ИменаПолейДляПоискаПоНаименованию("Номенклатура");
	
	РазличнаяНоменклатура = РазличныеНаименованияЭлементов(ДанныеДляСозданияНоменклатуры, ИменаПолейНоменклатуры);
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяОбъектаМетаданных);
	
	Для Каждого КлючЗначение Из РазличнаяНоменклатура Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить(ИменаПолейНоменклатуры.ИмяПоляНаименование, КлючЗначение.Ключ);
		
		НайденныеСтроки = ДанныеДляСозданияНоменклатуры.НайтиСтроки(Отбор);
		
		РазличныеПодчиненныеЭлементы = РазличныеНаименованияЭлементов(НайденныеСтроки, ИменаПолей);
		
		Если РазличныеПодчиненныеЭлементы.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ПодчиненныйКлючЗначение Из РазличныеПодчиненныеЭлементы Цикл
			
			Ссылка = МенеджерОбъекта.ПолучитьСсылку(Новый УникальныйИдентификатор);
			
			Отбор.Вставить(ИменаПолей.ИмяПоляНаименование, ПодчиненныйКлючЗначение.Ключ);
			
			НайденныеСтрокиПодчиненных = ДанныеДляСозданияНоменклатуры.НайтиСтроки(Отбор);
			
			Для Каждого Строка Из НайденныеСтрокиПодчиненных Цикл
				
				Ключ = Строка[ИменаПолей.ИмяПоляНаименование]; // Строка
				
				Если ПустаяСтрока(Ключ) Тогда
					Продолжить;
				КонецЕсли;
				
				Строка[ИменаПолей.ИмяПоляСсылка]        = Ссылка;
				Строка[ИменаПолей.ИмяПоляСсылкаНайдена] = Ложь;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСсылкиНаНайденныеЭлементы(ДанныеДляСозданияНоменклатуры, РазличныеНаименования, Владелец,
			ПолноеИмяОбъектаМетаданных, ИменаПолей)
	
	Выборка = ВыборкаПоискЗначенийЭлементовСправочника(РазличныеНаименования, Владелец, ПолноеИмяОбъектаМетаданных);
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяОбъектаМетаданных);
	
	Для Каждого КлючЗначение Из РазличныеНаименования Цикл
		
		Наименование = КлючЗначение.Ключ;
		
		Отбор = Новый Структура;
		Отбор.Вставить("Наименование", Наименование);
		
		Выборка.Сбросить();
		
		Если Выборка.НайтиСледующий(Отбор) Тогда
			
			Ссылка        = Выборка.Ссылка;
			СсылкаНайдена = Истина;
			
		Иначе
			
			Ссылка        = СсылкаНовогоЭлемента(МенеджерОбъекта);
			СсылкаНайдена = Ложь;
			
		КонецЕсли;
		
		Отбор = Новый Структура;
		Отбор.Вставить(ИменаПолей.ИмяПоляНаименование, Наименование);
		
		НайденныеСтроки = ДанныеДляСозданияНоменклатуры.НайтиСтроки(Отбор);
		
		Для Каждого Строка Из НайденныеСтроки Цикл
			
			Строка[ИменаПолей.ИмяПоляСсылка]        = Ссылка;
			Строка[ИменаПолей.ИмяПоляСсылкаНайдена] = СсылкаНайдена;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Выборка поиск значений элементов справочника.
// 
// Параметры:
//  РазличныеНаименования - см. РазличныеНаименованияЭлементов
//  Владелец - СправочникСсылка.НаборыУпаковок, СправочникСсылка.ВидыНоменклатуры - 
//  ПолноеИмяОбъектаМетаданных - Строка - 
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//   * Наименование - Строка - 
//   * Ссылка - СправочникСсылка.ХарактеристикиНоменклатуры, СправочникСсылка.УпаковкиЕдиницыИзмерения -
// 
Функция ВыборкаПоискЗначенийЭлементовСправочника(РазличныеНаименования, Владелец, ПолноеИмяОбъектаМетаданных)
	
	ТаблицаНаименований = НоваяТаблицаНаименований();
	
	Для Каждого КлючЗначение Из РазличныеНаименования Цикл
		
		НоваяСтрока = ТаблицаНаименований.Добавить();
		НоваяСтрока.Наименование = КлючЗначение.Ключ;
		
	КонецЦикла;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаНаименований.Наименование КАК Наименование
		|ПОМЕСТИТЬ ВТТаблицаНаименований
		|ИЗ
		|	&ТаблицаНаименований КАК ТаблицаНаименований
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаНаименований.Наименование КАК Наименование,
		|	ДанныеСправочника.Ссылка КАК Ссылка
		|ИЗ
		|	ВТТаблицаНаименований КАК ТаблицаНаименований
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ &ПолноеИмяОбъектаМетаданных КАК ДанныеСправочника
		|		ПО (ДанныеСправочника.Владелец = &Владелец)
		|			И ТаблицаНаименований.Наименование = ДанныеСправочника.Наименование
		|			И (НЕ ДанныеСправочника.ПометкаУдаления)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолноеИмяОбъектаМетаданных", ПолноеИмяОбъектаМетаданных);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Владелец",            Владелец);
	Запрос.УстановитьПараметр("ТаблицаНаименований", ТаблицаНаименований);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции

// Возвращаемое значение:
//  ТаблицаЗначений:
//   * Наименование - Строка -
//
Функция НоваяТаблицаНаименований()
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  МенеджерОбъекта - СправочникМенеджер - 
// 
// Возвращаемое значение:
//  - СправочникСсылка.ХарактеристикиНоменклатуры
//  - СправочникСсылка.УпаковкиЕдиницыИзмерения
// 
Функция СсылкаНовогоЭлемента(МенеджерОбъекта)
	Возврат МенеджерОбъекта.ПолучитьСсылку(Новый УникальныйИдентификатор);
КонецФункции

Процедура ДобавитьОписаниеДанныхСоздаваемойНоменклатуры(СоздаваемаяНоменклатура, РеквизитыНоменклатуры, Строка)
	
	ОписаниеДанных = СоздаваемаяНоменклатура.Получить(Строка.Номенклатура);
	
	Если ОписаниеДанных <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеДанных = НовоеОписаниеДанныхДляСозданияНоменклатуры();
	
	ОписаниеДанных.Реквизиты.Ссылка       = Строка.Номенклатура;
	
	ОписаниеДанных.Реквизиты.Наименование       = Строка.НоменклатураНаименование;
	ОписаниеДанных.Реквизиты.НаименованиеПолное = Строка.НоменклатураНаименование;
	ОписаниеДанных.Реквизиты.Артикул            = Строка.НоменклатураКонтрагента.Артикул;
	
	Для Каждого Элемент Из РеквизитыНоменклатуры Цикл
		
		Если Элемент.Ключ = "ДополнительныеРеквизиты" Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеДанных.Реквизиты.Вставить(Элемент.Ключ, Элемент.Значение);
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
		ОписаниеДанных.ДополнительныеРеквизиты, РеквизитыНоменклатуры.ДополнительныеРеквизиты, Истина);
	
	СоздаваемаяНоменклатура.Вставить(Строка.Номенклатура, ОписаниеДанных);
	
КонецПроцедуры

Процедура ДобавитьОписаниеДанныхСоздаваемойХарактеристики(СоздаваемыеХарактеристики, РеквизитыНоменклатуры, Строка)
	
	Если Строка.ХарактеристикаНайдена Тогда
		Возврат;
		
	ИначеЕсли Строка.Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка() Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеДанных = СоздаваемыеХарактеристики.Получить(Строка.Характеристика);
	
	Если ОписаниеДанных <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеДанных = НовоеОписаниеДанныхДляСозданияХарактеристики();
	
	ОписаниеДанных.Реквизиты.Ссылка       = Строка.Характеристика;
	ОписаниеДанных.Реквизиты.Наименование = Строка.ХарактеристикаНаименование;
	
	ИспользуютсяИндивидуальныеХарактеристики = 
		(РеквизитыНоменклатуры.ИспользованиеХарактеристик 
			= Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры);
	
	Если ИспользуютсяИндивидуальныеХарактеристики Тогда
		ОписаниеДанных.Реквизиты.Владелец = Строка.Номенклатура;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
		ОписаниеДанных.ДополнительныеРеквизиты, РеквизитыНоменклатуры.ДополнительныеРеквизиты, Истина);
	
	СоздаваемыеХарактеристики.Вставить(Строка.Характеристика, ОписаниеДанных);
	
КонецПроцедуры

Процедура ДобавитьОписаниеДанныхСоздаваемойУпаковки(СоздаваемыеУпаковки, РеквизитыНоменклатуры, Строка)
	
	Если Строка.УпаковкаНайдена Тогда
		Возврат;
		
	ИначеЕсли Строка.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка() Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеДанных = СоздаваемыеУпаковки.Получить(Строка.Упаковка);
	
	Если ОписаниеДанных <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеДанных = НовоеОписаниеДанныхДляСозданияУпаковок();
	
	ОписаниеДанных.Реквизиты.Ссылка       = Строка.Упаковка;
	ОписаниеДанных.Реквизиты.Наименование = Строка.УпаковкаНаименование;
	
	Если РеквизитыНоменклатуры.НаборУпаковок = Справочники.НаборыУпаковок.ИндивидуальныйДляНоменклатуры Тогда
		ОписаниеДанных.Реквизиты.Владелец = Строка.Номенклатура;
	КонецЕсли;
	
	СоздаваемыеУпаковки.Вставить(Строка.Упаковка, ОписаниеДанных);
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
//   * Реквизиты - Структура:
//     ** Ссылка - СправочникСсылка.Номенклатура - 
//     ** Артикул - Строка - 
//     ** Наименование - Строка - 
//     ** НаименованиеПолное - Строка - 
//     ** ВидНоменклатуры - СправочникСсылка.ВидыНоменклатуры - 
//   * ДополнительныеРеквизиты - см. НовыеДополнительныеРеквизитыДляСозданияПоДаннымКонтрагента
//
Функция НовоеОписаниеДанныхДляСозданияНоменклатуры()
	
	Результат = Новый Структура;
	
	Реквизиты = Новый Структура;
	
	Реквизиты.Вставить("Ссылка",             Справочники.Номенклатура.ПустаяСсылка());
	Реквизиты.Вставить("Артикул",            "");
	Реквизиты.Вставить("Наименование",       "");
	Реквизиты.Вставить("НаименованиеПолное", "");
	Реквизиты.Вставить("ВидНоменклатуры",    Справочники.ВидыНоменклатуры.ПустаяСсылка());
	
	Результат.Вставить("Реквизиты",      Реквизиты);
	
	Результат.Вставить("ДополнительныеРеквизиты", НовыеДополнительныеРеквизитыДляСозданияПоДаннымКонтрагента());
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//   * Реквизиты - Структура:
//     ** Ссылка - СправочникСсылка.ХарактеристикиНоменклатуры - 
//     ** Владелец - СправочникСсылка.Номенклатура - 
//     ** Наименование - Строка - 
//   * ДополнительныеРеквизиты - см. НовыеДополнительныеРеквизитыДляСозданияПоДаннымКонтрагента
//
Функция НовоеОписаниеДанныхДляСозданияХарактеристики()
	
	Результат = Новый Структура;
	
	Реквизиты = Новый Структура;
	
	Реквизиты.Вставить("Ссылка",       Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	Реквизиты.Вставить("Владелец",     Справочники.Номенклатура.ПустаяСсылка());
	Реквизиты.Вставить("Наименование", "");
	
	Результат.Вставить("Реквизиты",               Реквизиты);
	Результат.Вставить("ДополнительныеРеквизиты", НовыеДополнительныеРеквизитыДляСозданияПоДаннымКонтрагента());
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//   * Реквизиты - Структура:
//     ** Ссылка - СправочникСсылка.УпаковкиЕдиницыИзмерения - 
//     ** Владелец - СправочникСсылка.Номенклатура - 
//     ** Наименование - Строка - 
//
Функция НовоеОписаниеДанныхДляСозданияУпаковок()
	
	Результат = Новый Структура;
	
	Реквизиты = Новый Структура;
	
	Реквизиты.Вставить("Ссылка",       Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	Реквизиты.Вставить("Владелец",     Справочники.Номенклатура.ПустаяСсылка());
	Реквизиты.Вставить("Наименование", "");
	
	Результат.Вставить("Реквизиты", Реквизиты);
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
// Соответствие из КлючИЗначение:
//   * Ключ - ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения - свойство, которое необходимо установить.
//   * Значение - Произвольный - новое значение свойства.
//
Функция НовыеДополнительныеРеквизитыДляСозданияПоДаннымКонтрагента()
	Возврат Новый Соответствие;
КонецФункции

// Параметры:
//  ИсточникКатегории - ПеречислениеСсылка.ИсточникиКатегорийДляМаркетплейса -
//  РеквизитыНоменклатуры - см. НовыеРеквизитыНоменклатурыДляСозданияПоДаннымКонтрагента
// 
// Возвращаемое значение:
//  - СправочникСсылка.ВидыНоменклатуры
//  - СправочникСсылка.Номенклатура
//  - СправочникСсылка.ТоварныеКатегории
//
Функция Категория1СДляСозданияНоменклатурыПоДаннымКонтрагента(ИсточникКатегории, РеквизитыНоменклатуры)
	
	Если ИсточникКатегории = Перечисления.ИсточникиКатегорийДляМаркетплейса.ИерархияНоменклатуры Тогда
		ИмяСвойства = "Родитель";
		
	ИначеЕсли ИсточникКатегории = Перечисления.ИсточникиКатегорийДляМаркетплейса.ТоварнаяКатегория Тогда
		ИмяСвойства = "ТоварнаяКатегория";
		
	Иначе
		ИмяСвойства = "ВидНоменклатуры";
	КонецЕсли;
	
	МассивТипов = Новый Массив; // Массив из Тип
	МассивТипов.Добавить(Тип("СправочникСсылка.ВидыНоменклатуры"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ТоварныеКатегории"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Номенклатура"));
	
	ОписаниеТипаКатегории = Новый ОписаниеТипов(МассивТипов);
	
	Категория1С = ОписаниеТипаКатегории.ПривестиЗначение(РеквизитыНоменклатуры[ИмяСвойства]);
	
	Возврат Категория1С;
	
КонецФункции

Процедура СоздатьНовыйЭлементСправочникаНоменклатура(ОписаниеДанных)
	
	СправочникОбъект = Справочники.Номенклатура.СоздатьЭлемент();
	
	СправочникОбъект.УстановитьСсылкуНового(ОписаниеДанных.Реквизиты.Ссылка);
	
	СправочникОбъект.ВидНоменклатуры = ОписаниеДанных.Реквизиты.ВидНоменклатуры;
	Справочники.Номенклатура.ЗаполнитьРеквизитыПоВидуНоменклатуры(СправочникОбъект);
	
	ЗаполнитьЗначенияСвойств(СправочникОбъект, ОписаниеДанных.Реквизиты);
	
	Если ОписаниеДанных.ДополнительныеРеквизиты.Количество() <> 0 Тогда
		//@skip-check invocation-parameter-type-intersect
		УправлениеСвойствами.УстановитьСвойстваУОбъекта(СправочникОбъект, ОписаниеДанных.ДополнительныеРеквизиты);
	КонецЕсли;
	
	СправочникОбъект.Записать();
	
КонецПроцедуры

Процедура СоздатьНовыйЭлементСправочникаХарактеристикиНоменклатуры(РеквизитыНоменклатуры, ОписаниеДанных)
	
	СправочникОбъект = Справочники.ХарактеристикиНоменклатуры.СоздатьЭлемент();
	
	СправочникОбъект.УстановитьСсылкуНового(ОписаниеДанных.Реквизиты.Ссылка);
	
	СправочникОбъект.Заполнить(Неопределено);
	
	ЗаполнитьЗначенияСвойств(СправочникОбъект, ОписаниеДанных.Реквизиты);
	
	ИспользованиеХарактеристик = РеквизитыНоменклатуры.ИспользованиеХарактеристик;
	
	Если ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры Тогда
		СправочникОбъект.Владелец = РеквизитыНоменклатуры.ВидНоменклатуры;
		
	ИначеЕсли ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры Тогда
		СправочникОбъект.Владелец = РеквизитыНоменклатуры.ВладелецХарактеристик;
	КонецЕсли;
	
	Если ОписаниеДанных.ДополнительныеРеквизиты.Количество() <> 0 Тогда
		//@skip-check invocation-parameter-type-intersect
		УправлениеСвойствами.УстановитьСвойстваУОбъекта(СправочникОбъект, ОписаниеДанных.ДополнительныеРеквизиты);
	КонецЕсли;
	
	СправочникОбъект.Записать();
	
КонецПроцедуры

Процедура СоздатьНовыйЭлементСправочникаУпаковкиЕдиницыИзмерения(РеквизитыНоменклатуры, ОписаниеДанных,
			ЗначенияКонстант)
	
	СправочникОбъект = Справочники.УпаковкиЕдиницыИзмерения.СоздатьЭлемент();
	
	СправочникОбъект.УстановитьСсылкуНового(ОписаниеДанных.Реквизиты.Ссылка);
	
	СправочникОбъект.Заполнить(Неопределено);
	
	ЗаполнитьЗначенияСвойств(СправочникОбъект, ОписаниеДанных.Реквизиты);
	
	Если РеквизитыНоменклатуры.НаборУпаковок <> Справочники.НаборыУпаковок.ИндивидуальныйДляНоменклатуры Тогда
		СправочникОбъект.Владелец = РеквизитыНоменклатуры.НаборУпаковок;
	КонецЕсли;
	
	ДанныеЗаполнения = Новый Структура;
	
	ДанныеЗаполнения.Вставить("ТипИзмеряемойВеличины", Перечисления.ТипыИзмеряемыхВеличин.Упаковка);
	ДанныеЗаполнения.Вставить("ТипУпаковки",           Перечисления.ТипыУпаковокНоменклатуры.Конечная);
	
	ДанныеЗаполнения.Вставить(
		"СкладскаяГруппа",
		ЗначениеНастроекПовтИсп.СкладскаяГруппаУпаковокПоУмолчанию(СправочникОбъект.СкладскаяГруппа));
		
	Если ЗначениеЗаполнено(РеквизитыНоменклатуры.ЕдиницаИзмерения) Тогда
		ДанныеЗаполнения.Вставить("ЕдиницаИзмерения", РеквизитыНоменклатуры.ЕдиницаИзмерения);
	Иначе
		ДанныеЗаполнения.Вставить("ЕдиницаИзмерения", ЗначенияКонстант.ЕдиницаКоличестваШтук);
	КонецЕсли;
	
	ДанныеЗаполнения.Вставить("Числитель",   1);
	ДанныеЗаполнения.Вставить("Знаменатель", 1);
	
	Если Не ЗначениеЗаполнено(СправочникОбъект.ВесЕдиницаИзмерения) Тогда
		
		СправочникОбъект.Вес = ПривестиЗначениеЕдиницыИзмеренияККонстанте(
			ОписаниеДанных.Реквизиты,
			ИмяСвойстваУпаковкиКоэффициентВесаВКг(),
			СправочникОбъект.Вес,
			ЗначенияКонстант.ЕдиницаВесаКоэффициент);
		
		ДанныеЗаполнения.Вставить("ВесЕдиницаИзмерения", ЗначенияКонстант.ЕдиницаВеса);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЗначенияКонстант.ЕдиницаГабаритов) Тогда
		ЗаполнитьЕдиницуГабаритовСоздаваемойУпаковки(СправочникОбъект, ЗначенияКонстант);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначенияКонстант.ЕдиницаГабаритов) Тогда
		
		ЕдиницаГабаритов            = ЗначенияКонстант.ЕдиницаГабаритов;
		ЕдиницаГабаритовКоэффициент = ЗначенияКонстант.ЕдиницаГабаритовКоэффициент;
		
	Иначе
		
		СправочникОбъект.Высота = ПривестиЗначениеЕдиницыИзмеренияККонстанте(
			ОписаниеДанных.Реквизиты,
			ИмяСвойстваУпаковкиКоэффициентГабаритовВМ(),
			СправочникОбъект.Высота,
			ЗначенияКонстант.ЕдиницаДлиныКоэффициент);;
		
		СправочникОбъект.Глубина = ПривестиЗначениеЕдиницыИзмеренияККонстанте(
			ОписаниеДанных.Реквизиты,
			ИмяСвойстваУпаковкиКоэффициентГабаритовВМ(),
			СправочникОбъект.Глубина,
			ЗначенияКонстант.ЕдиницаДлиныКоэффициент);
		
		СправочникОбъект.Ширина = ПривестиЗначениеЕдиницыИзмеренияККонстанте(
			ОписаниеДанных.Реквизиты,
			ИмяСвойстваУпаковкиКоэффициентГабаритовВМ(),
			СправочникОбъект.Ширина,
			ЗначенияКонстант.ЕдиницаДлиныКоэффициент);
		
		ЕдиницаГабаритов            = ЗначенияКонстант.ЕдиницаДлины;
		ЕдиницаГабаритовКоэффициент = ЗначенияКонстант.ЕдиницаДлиныКоэффициент;
		
	КонецЕсли;
	
	ДанныеЗаполнения.Вставить("ВысотаЕдиницаИзмерения",  ЕдиницаГабаритов);
	ДанныеЗаполнения.Вставить("ГлубинаЕдиницаИзмерения", ЕдиницаГабаритов);
	ДанныеЗаполнения.Вставить("ШиринаЕдиницаИзмерения",  ЕдиницаГабаритов);
	
	Объем = 
		СправочникОбъект.Высота * ЕдиницаГабаритовКоэффициент
		* СправочникОбъект.Глубина * ЕдиницаГабаритовКоэффициент
		* СправочникОбъект.Ширина * ЕдиницаГабаритовКоэффициент
		/ ЗначенияКонстант.ЕдиницаОбъемаКоэффициент;
	
	ДанныеЗаполнения.Вставить("Объем", Объем);
	ДанныеЗаполнения.Вставить("ОбъемЕдиницаИзмерения", ЗначенияКонстант.ЕдиницаОбъема);
	
	ОбщегоНазначенияУТКлиентСервер.ЗаполнитьЗначенияПустыхСвойств(СправочникОбъект, ДанныеЗаполнения);
	
	СправочникОбъект.Записать();
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
//   * ЕдиницаКоличестваШтук - СправочникСсылка.УпаковкиЕдиницыИзмерения - 
//   * ЕдиницаКоличестваШтукКоэффициент - Число - 
//   * ЕдиницаДлины - СправочникСсылка.УпаковкиЕдиницыИзмерения - 
//   * ЕдиницаДлиныКоэффициент - Число - 
//   * ЕдиницаВеса - СправочникСсылка.УпаковкиЕдиницыИзмерения - 
//   * ЕдиницаВесаКоэффициент - Число - 
//   * ЕдиницаОбъема - СправочникСсылка.УпаковкиЕдиницыИзмерения - 
//   * ЕдиницаОбъемаКоэффициент - Число - 
//   * ЕдиницаГабаритов - СправочникСсылка.УпаковкиЕдиницыИзмерения - 
//   * ЕдиницаГабаритовКоэффициент - Число - 
//
Функция ЗначенияКонстантДляСозданияУпаковок()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ЕдиницаКоличестваШтук",            Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	Результат.Вставить("ЕдиницаКоличестваШтукКоэффициент", 1);
	
	Результат.Вставить("ЕдиницаДлины",            Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	Результат.Вставить("ЕдиницаДлиныКоэффициент", 1);
	
	Результат.Вставить("ЕдиницаВеса",            Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	Результат.Вставить("ЕдиницаВесаКоэффициент", 1);
	
	Результат.Вставить("ЕдиницаОбъема",            Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	Результат.Вставить("ЕдиницаОбъемаКоэффициент", 1);
	
	Результат.Вставить("ЕдиницаГабаритов",            Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	Результат.Вставить("ЕдиницаГабаритовКоэффициент", 1);
	
	Выборка = ВыборкаЗначенийКонстантДляСозданияУпаковок();
	
	Пока Выборка.Следующий() Цикл
		
		ИмяСвойства    = Выборка.ИмяСвойства;
		ИмяКоэффициент = СтрШаблон("%1Коэффициент", ИмяСвойства);
		
		Результат[ИмяСвойства]    = Выборка.ЕдиницаИзмерения;
		Результат[ИмяКоэффициент] = Выборка.Коэффициент;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//   * ИмяСвойства - Строка - 
//   * ЕдиницаИзмерения - СправочникСсылка.УпаковкиЕдиницыИзмерения - 
//   * Коэффициент - Число - 
//
Функция ВыборкаЗначенийКонстантДляСозданияУпаковок()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	""ЕдиницаКоличестваШтук"" КАК ИмяСвойства,
		|	ЗначениеКонстанты.Значение КАК ЗначениеКонстанты
		|ПОМЕСТИТЬ ВТЗначенияКонстант
		|ИЗ
		|	Константа.ЕдиницаИзмеренияКоличестваШтук КАК ЗначениеКонстанты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""ЕдиницаДлины"",
		|	ЗначениеКонстанты.Значение
		|ИЗ
		|	Константа.ЕдиницаИзмеренияДлины КАК ЗначениеКонстанты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""ЕдиницаВеса"",
		|	ЗначениеКонстанты.Значение
		|ИЗ
		|	Константа.ЕдиницаИзмеренияВеса КАК ЗначениеКонстанты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""ЕдиницаОбъема"",
		|	ЗначениеКонстанты.Значение
		|ИЗ
		|	Константа.ЕдиницаИзмеренияОбъема КАК ЗначениеКонстанты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗначенияКонстант.ИмяСвойства КАК ИмяСвойства,
		|	ЗначенияКонстант.ЗначениеКонстанты КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(&ТекстЗапросаКоэффициентаУпаковки, 1) КАК Коэффициент
		|ИЗ
		|	ВТЗначенияКонстант КАК ЗначенияКонстант
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ДанныеЕдиницыИзмерения
		|		ПО ЗначенияКонстант.ЗначениеКонстанты = ДанныеЕдиницыИзмерения.Ссылка";
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКоэффициентаУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки("ДанныеЕдиницыИзмерения"));
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Процедура ЗаполнитьЕдиницуГабаритовСоздаваемойУпаковки(СправочникОбъект, ЗначенияКонстант)
	
	Если ЗначениеЗаполнено(СправочникОбъект.ВысотаЕдиницаИзмерения) Тогда
		ЕдиницаГабаритов = СправочникОбъект.ВысотаЕдиницаИзмерения;
		
	ИначеЕсли ЗначениеЗаполнено(СправочникОбъект.ГлубинаЕдиницаИзмерения) Тогда
		ЕдиницаГабаритов = СправочникОбъект.ГлубинаЕдиницаИзмерения;
		
	ИначеЕсли ЗначениеЗаполнено(СправочникОбъект.ШиринаЕдиницаИзмерения) Тогда
		ЕдиницаГабаритов = СправочникОбъект.ШиринаЕдиницаИзмерения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЕдиницаГабаритов) Тогда
		
		ЕдиницаГабаритовКоэффициент = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(
			ЕдиницаГабаритов, Справочники.Номенклатура.ПустаяСсылка());
		
	Иначе
		ЕдиницаГабаритовКоэффициент = 1;
	КонецЕсли;
	
	ЗначенияКонстант.ЕдиницаГабаритов            = ЕдиницаГабаритов;
	ЗначенияКонстант.ЕдиницаГабаритовКоэффициент = ЕдиницаГабаритовКоэффициент;
	
КонецПроцедуры

Функция ПривестиЗначениеЕдиницыИзмеренияККонстанте(Реквизиты, ИмяСвойства, КонвертируемоеЗначение, КоэффициентКонстанты)
	
	Результат = 0;
	
	Если КоэффициентКонстанты = 0 Тогда
		Возврат Результат;
		
	ИначеЕсли Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Реквизиты, ИмяСвойства) Тогда
		Возврат Результат;
	КонецЕсли;
	
	КоэффициентБазовойЕдиницы = Реквизиты[ИмяСвойства]; // Число
	
	Результат = КонвертируемоеЗначение * КоэффициентБазовойЕдиницы / КоэффициентКонстанты;
	
	Возврат Результат;
	
КонецФункции

// Возвращает имя свойства, добавляемого в реквизиты упаковки для того, чтобы указать коэффициент веса упаковки,
// приведенный в килограммы.
//
// Возвращаемое значение:
//  Строка
//
Функция ИмяСвойстваУпаковкиКоэффициентВесаВКг() Экспорт
	Возврат "КоэффициентВесаВКг";
КонецФункции

// Возвращает имя свойства, добавляемого в реквизиты упаковки для того, чтобы указать коэффициент габаритов
// упаковки, приведенный в метры.
//
// Возвращаемое значение:
//  Строка
//
Функция ИмяСвойстваУпаковкиКоэффициентГабаритовВМ() Экспорт
	Возврат "КоэффициентГабаритовВМ";
КонецФункции

// Возвращаемое значение:
//  Структура:
//   * НоменклатураКонтрагента - см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента
//   * НоменклатураИБ - см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы
//
Функция НовыйРезультатСозданияНовойНоменклатуры()
	
	Результат = Новый Структура;
	
	Результат.Вставить("НоменклатураКонтрагента", Новый Структура);
	Результат.Вставить("НоменклатураИБ",          Новый Структура);
	
	Возврат Результат;
	
КонецФункции

// Возвращает текст запроса поиска вида номенклатуры для идентификаторов товаров по идентификаторам их категорий.
//
// Параметры:
//   ВидМаркетплейса   - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//   ИсточникКатегории - ПеречислениеСсылка.ИсточникиКатегорийДляМаркетплейса - источник категории для торговой площадки.
//
// Возвращаемое значение:
//   Строка - текст запроса.
//
Функция ТекстЗапросаПоискаВидаНоменклатуры(ВидМаркетплейса, ИсточникКатегории)

	Тексты = Новый Массив; // Массив из Строка
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		Тексты.Добавить(ИнтеграцияСМаркетплейсомOzonСервер.ТекстЗапросаИдентификаторовКатегорийТоваров());
	Иначе
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ИдентификаторыКатегорий.ИдентификаторКатегории КАК ИдентификаторКатегории,
		|	&ВидНоменклатуры КАК ВидНоменклатуры,
		|	&Источник КАК Источник,
		|	&ЭтоГруппа КАК ИсточникЭтоГруппа
		|ИЗ
		|	ИдентификаторыКатегорий КАК ИдентификаторыКатегорий
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектов
		|		ПО (СоответствияОбъектов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись)
		|			И (СоответствияОбъектов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.КатегорияТоваров))
		|			И ИдентификаторыКатегорий.ИдентификаторКатегории = СоответствияОбъектов.ИдентификаторОбъектаМаркетплейса
		|			И (СоответствияОбъектов.ИдентификаторВладельцаОбъектаМаркетплейса = """")
		|			И (СоответствияОбъектов.Объект1С ССЫЛКА Справочник.Номенклатура)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ДанныеЗначенияКатегории
		|		ПО (СоответствияОбъектов.Объект1С = ДанныеЗначенияКатегории.Ссылка)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭтоГруппа";
	
	Если ИсточникКатегории = Перечисления.ИсточникиКатегорийДляМаркетплейса.ИерархияНоменклатуры Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВидНоменклатуры", "ДанныеЗначенияКатегории.ВидНоменклатуры");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Источник", "ДанныеЗначенияКатегории.Ссылка");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЭтоГруппа", "ДанныеЗначенияКатегории.ЭтоГруппа");
		
	ИначеЕсли ИсточникКатегории = Перечисления.ИсточникиКатегорийДляМаркетплейса.ТоварнаяКатегория Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Справочник.Номенклатура", "Справочник.ТоварныеКатегории");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВидНоменклатуры", "ДанныеЗначенияКатегории.Владелец");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Источник", "ЗНАЧЕНИЕ(Справочник.ТоварныеКатегории.ПустаяСсылка)");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЭтоГруппа", "ЛОЖЬ");
		
	ИначеЕсли ИсточникКатегории = Перечисления.ИсточникиКатегорийДляМаркетплейса.ВидНоменклатуры Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Справочник.Номенклатура", "Справочник.ВидыНоменклатуры");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВидНоменклатуры", "СоответствияОбъектов.Объект1С");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Источник", "ДанныеЗначенияКатегории.Ссылка");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЭтоГруппа", "ДанныеЗначенияКатегории.ЭтоГруппа");
		
	КонецЕсли;
	
	Тексты.Добавить(ТекстЗапроса);
	
	Возврат СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

// Выполняет поиск вида номенклатуры для группы.
//
// Параметры:
//   Родитель          - СправочникСсылка.ВидыНоменклатуры, СправочникСсылка.Номенклатура - анализируемая ссылка.
//   ИсточникКатегории - ПеречислениеСсылка.ИсточникиКатегорийДляМаркетплейса - источник категории.
//
// Возвращаемое значение:
//   СправочникСсылка.ВидыНоменклатуры - вид номенклатуры.
//
Функция ВидНоменклатурыПодчиненныхЭлементов(Родитель, ИсточникКатегории)
	
	Результат = Справочники.ВидыНоменклатуры.ПустаяСсылка();
	
	Если ИсточникКатегории = Перечисления.ИсточникиКатегорийДляМаркетплейса.ВидНоменклатуры Тогда
		
		ИмяПоляВидНоменклатуры = "ИсточникДанных.Ссылка";
		ИсточникДанных         = "Справочник.ВидыНоменклатуры";
		
	Иначе
		
		ИмяПоляВидНоменклатуры = "ИсточникДанных.ВидНоменклатуры";
		ИсточникДанных         = "Справочник.Номенклатура";
		
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	&Родитель КАК Ссылка
		|ПОМЕСТИТЬ ТаблицаРодителейПервоначальная
		|ИЗ
		|	&ИсточникДанных КАК ИсточникДанных
		|ГДЕ
		|	ИсточникДанных.Ссылка = &Родитель";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИсточникДанных", ИсточникДанных);
	
	Запрос.УстановитьПараметр("Родитель", Родитель);
	Запрос.Выполнить();
	
	Индекс = 0;
	ИмяТаблицыРодителей = "ТаблицаРодителейПервоначальная";
	
	Пока Истина Цикл
		
		Выборка = ВыборкаВидаНоменклатурыПодчиненныхЭлементов(
			Запрос, ИсточникДанных, ИмяПоляВидНоменклатуры, Индекс, ИмяТаблицыРодителей);
		
		Если Выборка = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Если Выборка.ЭтоГруппа Тогда
			Продолжить; // Среди подчиненных элементов только группы
		КонецЕсли;
		
		Результат = Выборка.ВидНоменклатуры;
		Прервать;
		
	КонецЦикла;
	
	МенеджерВременныхТаблиц.Закрыть();
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  Запрос - Запрос -
//  ИсточникДанных - Строка -
//  ИмяПоляВидНоменклатуры - Строка -
//  Индекс - Число -
//  ИмяТаблицыРодителей - Строка -
// 
// Возвращаемое значение:
//  - Неопределено - Если результат пуст.
//  - ВыборкаИзРезультатаЗапроса:
//     * ЭтоГруппа - Булево -
//     * ВидНоменклатуры - СправочникСсылка.ВидыНоменклатуры -
//
Функция ВыборкаВидаНоменклатурыПодчиненныхЭлементов(Запрос, ИсточникДанных, ИмяПоляВидНоменклатуры, Индекс,
			ИмяТаблицыРодителей)
	
	Индекс = Индекс + 1;
	
	ИмяТекущейТаблицы = СтрШаблон("ТаблицаДанных%1", Формат(Индекс, "ЧГ=0"));
	
	ТекстЗапросаПоиска =
		"ВЫБРАТЬ
		|	ИсточникДанных.Ссылка КАК Ссылка,
		|	&ВидНоменклатуры КАК ВидНоменклатуры,
		|	ИсточникДанных.ЭтоГруппа КАК ЭтоГруппа
		|ПОМЕСТИТЬ ТаблицаДанныхПереопределяемый
		|ИЗ
		|	&ИсточникДанных КАК ИсточникДанных
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ &ТаблицаРодителей КАК ТаблицаРодителей
		|		ПО ИсточникДанных.Родитель = ТаблицаРодителей.Ссылка
		|			И (НЕ ИсточникДанных.ПометкаУдаления)";
	
	ТекстЗапросаПоиска = СтрЗаменить(ТекстЗапросаПоиска, "&ВидНоменклатуры", ИмяПоляВидНоменклатуры);
	ТекстЗапросаПоиска = СтрЗаменить(ТекстЗапросаПоиска, "&ИсточникДанных", ИсточникДанных);
	ТекстЗапросаПоиска = СтрЗаменить(ТекстЗапросаПоиска, "&ТаблицаРодителей", ИмяТаблицыРодителей);
	ТекстЗапросаПоиска = СтрЗаменить(ТекстЗапросаПоиска, "ТаблицаДанныхПереопределяемый", ИмяТекущейТаблицы);
	
	ТекстЗапросаУничтожения = СтрШаблон("УНИЧТОЖИТЬ %1", ИмяТаблицыРодителей);
	ИмяТаблицыРодителей     = ИмяТекущейТаблицы;
	
	ТекстЗапросаРезультат = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИсточникДанных.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ИсточникДанных.ЭтоГруппа КАК ЭтоГруппа
		|ИЗ
		|	&ИсточникДанных КАК ИсточникДанных
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭтоГруппа";
	
	ТекстЗапросаРезультат = СтрЗаменить(ТекстЗапросаРезультат, "&ИсточникДанных", ИмяТекущейТаблицы);
	
	Запрос.Текст = 
		ТекстЗапросаПоиска 
		+ ОбщегоНазначения.РазделительПакетаЗапросов() 
		+ ТекстЗапросаУничтожения
		+ ОбщегоНазначения.РазделительПакетаЗапросов() 
		+ ТекстЗапросаРезультат;
		
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка;
	
КонецФункции

#КонецОбласти

#Область СопоставлениеНоменклатурыСлужебный

// Аналог функции см. ОтражениеВУчетеЭДО.НаборНоменклатурыКонтрагентовПоИнформацииОТоварах.
//
// Параметры:
//   ИнформацияОТоварах - см. ФорматыЭДО.ИнформацияОТовареИзФайла.
//   Владелец           - см. СопоставлениеНоменклатурыКонтрагентовСлужебный.ВладелецНоменклатурыКонтрагента.
// 
// Возвращаемое значение:
//   Массив из см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента.
//
Функция НаборНоменклатурыКонтрагентовПоИнформацииОТоварах(ИнформацияОТоварах, Владелец)
	
	Результат = Новый Массив;
	
	Если Не ЗначениеЗаполнено(ИнформацияОТоварах) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицыЗначений Из ИнформацияОТоварах Цикл
		Сопоставление = СтрокаТаблицыЗначений.Сопоставление;
		
		Если ТипЗнч(Сопоставление) <> Тип("Структура") Тогда
			Продолжить;
		КонецЕсли;
		Если Не Сопоставление.Свойство("Идентификатор") Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементКоллекции = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента();
		ЭлементКоллекции.Вставить("НоменклатураИБ");
		ЭлементКоллекции.Вставить("ХарактеристикаИБ");
		ЭлементКоллекции.Вставить("УпаковкаИБ");
		
		ЗаполнитьЗначенияСвойств(ЭлементКоллекции, Сопоставление);
		ЭлементКоллекции.Владелец = Владелец;
		
		Если Сопоставление.Свойство("ВариантУказанияНоменклатурыИБ") Тогда
			ЭлементКоллекции.ВариантУказанияНоменклатуры = Сопоставление.ВариантУказанияНоменклатурыИБ;
		КонецЕсли;
		
		СопоставлениеНоменклатурыКонтрагентовКлиентСервер.РазделитьИдентификаторНаЧасти(
			Сопоставление.Идентификатор, ЭлементКоллекции);
		
		Результат.Добавить(ЭлементКоллекции);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Аналог функции см. СопоставлениеНоменклатурыКонтрагентовСлужебный.НоваяТаблицаНоменклатурыКонтрагентов.
// Возвращает таблицу значений с колонками, описывающими номенклатуру контрагента.
// Таблица может быть инициализирована массивом структур с аналогичным составом свойств.
// В случае, если массив ИсторияИдентификаторов не пустой, создается столько строк, сколько элементов в массиве.
//
// Параметры:
//   НаборНоменклатурыКонтрагентов          - Массив из см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента.
//                                          - см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента.
//                                          - Неопределено - возвращаемая таблица не будет содержать строк.
//   РазвернутьДополнительныеИдентификаторы - Булево - флаг, отвечающий за разворачивание массива ИсторияИдентификаторов,
//                                              Значение по умолчанию Истина. При сбросе в Ложь анализ ИсторияИдентификаторов не выполняется.
//  
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * Владелец                           - ОпределяемыйТип.ВладелецНоменклатурыБЭД
//     * Идентификатор                      - Строка
//     * ДополнительныйИдентификатор        - Строка
//     * Наименование                       - Строка - наименование номенклатуры.
//     * Характеристика                     - Строка - наименование характеристики номенклатуры.
//     * НаименованиеУпаковки               - Строка - наименование упаковки номенклатуры.
//     * ЕдиницаИзмерения                   - Строка - наименование базовой единицы измерения номенклатуры.
//     * ЕдиницаИзмеренияКод                - Строка - код по ОКЕИ базовой единицы измерения номенклатуры.
//     * Артикул                            - Строка - артикул номенклатуры.
//     * СтавкаНДС                          - Строка - ставка НДС номенклатуры. 
//     * ШтрихкодКомбинации                 - Строка - штрихкод комбинации: номенклатура, характеристика, упаковка.
//     * ШтрихкодыНоменклатуры              - Строка - другие штрихкоды номенклатуры через запятую.
//     * ИдентификаторНоменклатурыСервиса   - Строка - идентификатор в сервисе 1С:Номенклатура.
//     * ИдентификаторХарактеристикиСервиса - Строка - идентификатор характеристики в сервисе 1С:Номенклатура.
//     * ИдентификаторНоменклатуры          - Строка - идентификатор номенклатуры.
//     * ИдентификаторХарактеристики        - Строка - идентификатор характеристики.
//     * ИдентификаторУпаковки              - Строка - идентификатор упаковки.
//     * ИсторияИдентификаторов             - Массив из Строка - альтернативные идентификаторы.
//     * КоличествоБазовойЕдиницыИзмерения  - Число  - количество базовой единицы измерения (числитель).
//     * КоличествоУпаковок                 - Число  - количество упаковок (знаменатель)
//     * ВариантУказанияНоменклатуры        - ПеречислениеСсылка.ВариантыУказанияНоменклатурыБЭД - значение в чьих терминах указана номенклатура.
//                                              По умолчанию номенклатура контрагента.
//     * НоменклатураКонтрагента            - СправочникСсылка.НоменклатураКонтрагентов - ссылка номенклатуры контрагентов.
//     * ТипНоменклатурыФНС                 - ПеречислениеСсылка.ТипыНоменклатурыФНС - соответствует значению Признак УПД.
//     * КодНоменклатуры                    - Строка - код номенклатуры.
//     * КодПоКТРУ                          - Строка - код по КТРУ.
//     * КодТНВЭД                           - Строка - код ТН ВЭД.
//     * СтранаПроисхожденияКод             - Строка - код страны происхождения.
//     * ПрослеживаемыйТовар                - Булево - признак прослеживаемости.
//     * МаркируемыйТовар                   - Булево - признак маркировки.
//
Функция НоваяТаблицаНоменклатурыКонтрагентов(Знач НаборНоменклатурыКонтрагентов = Неопределено,
			РазвернутьДополнительныеИдентификаторы = Истина)
	
	Результат = Новый ТаблицаЗначений;
	
	СтруктураТаблицы = СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента();
	СкопироватьКолонкиСтруктурыСопоставленияВТаблицу(СтруктураТаблицы, Результат);
	
	Результат.Колонки.Добавить("ВариантыСопоставленияНоменклатурыБЭД",   Новый ОписаниеТипов("Массив"));
	Результат.Колонки.Добавить("ВариантыСопоставленияХарактеристикиБЭД", Новый ОписаниеТипов("Массив"));
	Результат.Колонки.Добавить("ВариантыСопоставленияУпаковкиБЭД",       Новый ОписаниеТипов("Массив"));
	
	Результат.Колонки.Добавить("ИспользоватьХарактеристики",             Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ОбязательноеЗаполнениеХарактеристики",   Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ИспользоватьУпаковки",                   Новый ОписаниеТипов("Булево"));
	
	Если Результат.Колонки.Найти("НоменклатураИБ") = Неопределено Тогда
		Результат.Колонки.Добавить("НоменклатураИБ",              Метаданные.ОпределяемыеТипы.НоменклатураБЭД.Тип);
		Результат.Колонки.Добавить("ХарактеристикаИБ",            Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатурыБЭД.Тип);
		Результат.Колонки.Добавить("УпаковкаИБ",                  Метаданные.ОпределяемыеТипы.УпаковкаНоменклатурыБЭД.Тип);
		Результат.Колонки.Добавить("ЕдиницаИзмеренияПоУмолчанию", Метаданные.ОпределяемыеТипы.УпаковкаНоменклатурыБЭД.Тип);
	КонецЕсли;
	
	Результат.Колонки.Добавить("ХешПоисковойСтроки",          ОбщегоНазначения.ОписаниеТипаСтрока(32));
	Результат.Колонки.Добавить("ДополнительныйИдентификатор", ОбщегоНазначения.ОписаниеТипаСтрока(300));
	
	Если НаборНоменклатурыКонтрагентов = Неопределено Тогда
		НаборНоменклатурыКонтрагентов = Новый Массив;
	ИначеЕсли ТипЗнч(НаборНоменклатурыКонтрагентов) <> Тип("Массив") Тогда
		НаборНоменклатурыКонтрагентов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НаборНоменклатурыКонтрагентов);
	КонецЕсли;
	
	Для Каждого ЭлементКоллекции Из НаборНоменклатурыКонтрагентов Цикл
		ЕстьДополнительныеИдентификаторы = (ТипЗнч(ЭлементКоллекции.ИсторияИдентификаторов) = Тип("Массив")
			И ЭлементКоллекции.ИсторияИдентификаторов.Количество() > 1);
		
		Если Не ЕстьДополнительныеИдентификаторы Или Не РазвернутьДополнительныеИдентификаторы Тогда
			Запись = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, ЭлементКоллекции);
			
		Иначе
			Для Каждого ДополнительныйИдентификатор Из ЭлементКоллекции.ИсторияИдентификаторов Цикл
				Запись = Результат.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, ЭлементКоллекции);
				Запись.ДополнительныйИдентификатор = ДополнительныйИдентификатор;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Аналог процедуры СопоставлениеНоменклатурыКонтрагентовСлужебный.СкопироватьКолонкиСтруктурыСопоставленияБЭДВТаблицу.
// Копирует колонки структуры сопоставления в таблицу значений.
//
// Параметры:
//   СтруктураТаблицы - Структура - см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента, дополнительно могут передаваться ключи:
//     * НоменклатураИБ              - ОпределяемыйТип.НоменклатураБЭД.
//     * ХарактеристикаИБ            - ОпределяемыйТип.ХарактеристикаНоменклатурыБЭД.
//     * УпаковкаИБ                  - ОпределяемыйТип.УпаковкаНоменклатурыБЭД.
//     * ЕдиницаИзмеренияПоУмолчанию - ОпределяемыйТип.УпаковкаНоменклатурыБЭД.
//  Таблица           - ТаблицаЗначений - содержит колонки из ключей параметра СтруктураТаблицы.
//
Процедура СкопироватьКолонкиСтруктурыСопоставленияВТаблицу(Знач СтруктураТаблицы, Таблица)
	
	ТипСтрока300 = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(300));
	ТипЧисло15   = Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15, 3));
	
	Для Каждого Элемент Из СтруктураТаблицы Цикл
		ИмяКолонки = Элемент.Ключ;
		ТипКолонки = ТипСтрока300;
		
		Если Элемент.Ключ = "Владелец" Тогда
			ТипКолонки = Метаданные.ОпределяемыеТипы.ВладелецНоменклатурыБЭД.Тип;
		ИначеЕсли Элемент.Ключ = "НоменклатураИБ" Тогда
			ТипКолонки = Метаданные.ОпределяемыеТипы.НоменклатураБЭД.Тип;
		ИначеЕсли Элемент.Ключ = "ХарактеристикаИБ" Тогда
			ТипКолонки = Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатурыБЭД.Тип;
		ИначеЕсли Элемент.Ключ = "УпаковкаИБ"
			Или Элемент.Ключ = "ЕдиницаИзмеренияПоУмолчанию" Тогда
			ТипКолонки = Метаданные.ОпределяемыеТипы.УпаковкаНоменклатурыБЭД.Тип;
		ИначеЕсли Элемент.Ключ = "ВариантУказанияНоменклатуры" Тогда
			ТипКолонки = Новый ОписаниеТипов("ПеречислениеСсылка.ВариантыУказанияНоменклатурыБЭД");
		ИначеЕсли Элемент.Ключ = "НоменклатураКонтрагента" Тогда
			ТипКолонки = Новый ОписаниеТипов("СправочникСсылка.НоменклатураКонтрагентов");
		ИначеЕсли Элемент.Ключ = "КоличествоУпаковок"
			Или Элемент.Ключ = "КоличествоБазовойЕдиницыИзмерения" Тогда
				ТипКолонки = ТипЧисло15;
		ИначеЕсли Элемент.Ключ = "ИсторияИдентификаторов" Тогда
			ТипКолонки = Новый ОписаниеТипов("Массив");
		КонецЕсли;
		
		Таблица.Колонки.Добавить(ИмяКолонки, ТипКолонки);
	КонецЦикла;
	
КонецПроцедуры

// Позволяет провести поиск вариантов по дополнительным натуральным ключам.
//
// Параметры:
//   ТаблицаНоменклатурыКонтрагентов - ТаблицаЗначений - данные номенклатуры контрагентов, см. описание СопоставлениеНоменклатурыКонтрагентовСлужебный.ПреобразоватьНаборНоменклатурыКонтрагентовВТаблицу:
//     * Идентификатор                          - Строка - идентификатор записи.
//     * Наименование                           - Строка - наименование номенклатуры.
//     * Характеристика                         - Строка - характеристика номенклатуры.
//     * ЕдиницаИзмерения                       - Строка - единица измерения номенклатуры.
//     * ЕдиницаИзмеренияКод                    - Строка - код единицы измерения номенклатуры.
//     * Артикул                                - Строка - артикул номенклатуры.
//     * СтавкаНДС                              - Строка - ставка НДС номенклатуры. 
//     * ИдентификаторНоменклатурыСервиса       - Строка - идентификатор в сервисе 1С:Номенклатура.
//     * ИдентификаторХарактеристикиСервиса     - Строка - идентификатор характеристики в сервисе 1С:Номенклатура.
//     * ШтрихкодКомбинации                     - Строка - штрихкод по комбинации: номенклатура, характеристика, упаковка.
//     * ШтрихкодыНоменклатуры                  - Строка - штрихкоды номенклатуры через запятую.
//     * НаименованиеУпаковки                   - Строка - наименование упаковки номенклатуры.
//     * НоменклатураКонтрагента                - СправочникСсылка.НоменклатураКонтрагентов - ссылка номенклатуры контрагентов.
//     * Владелец                               - ОпределяемыйТип.ВладелецНоменклатурыБЭД, ОпределяемыйТип.Организация - владелец номенклатуры ИБ.
//     * НоменклатураИБ                         - ОпределяемыйТип.НоменклатураБЭД - ссылка номенклатура ИБ.
//     * ХарактеристикаИБ                       - ОпределяемыйТип.ХарактеристикаНоменклатурыБЭД - ссылка характеристики номенклатуры ИБ.
//     * УпаковкаИБ                             - ОпределяемыйТип.УпаковкаНоменклатурыБЭД - ссылка упаковки номенклатуры ИБ.
//     * ВариантыСопоставленияНоменклатурыБЭД   - Массив из см. СопоставлениеНоменклатурыКонтрагентовСлужебный.НовыйВариантСопоставленияНоменклатурыБЭД.
//     * ВариантыСопоставленияХарактеристикиБЭД - Массив из ОпределяемыйТип.ХарактеристикаНоменклатурыБЭД - набор ссылок характеристик.
//     * ВариантыСопоставленияУпаковкиБЭД       - Массив из ОпределяемыйТип.УпаковкаНоменклатурыБЭД - набор ссылок упаковок.
//     * ХешПоисковойСтроки                     - Строка - хеш правила поиска по части наименования, см. РегистрСведений.ПравилаПоискаПоЧастиНаименованияБЭД.ХешПоисковойСтроки.
//   ДополнительныеПараметры         - Структура - параметры поиска:
//     * СпособОбработки               - Строка - способ отражения в учете электронного документа.
//     * УчетнаяЗапись                 - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//   ПолныйПоиск                     - Булево - выполнять ли поиск без учета уже сопоставленных позиций.
// 
Процедура НайтиВариантыСопоставленияНоменклатуры(ТаблицаНоменклатурыКонтрагентов, Знач ДополнительныеПараметры, 
			Знач ПолныйПоиск = Ложь)
	
	Если Не ЗначениеЗаполнено(ТаблицаНоменклатурыКонтрагентов) Тогда
		Возврат;
		
	ИначеЕсли ТипЗнч(ДополнительныеПараметры) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСопоставления = НовыеПараметрыСопоставленияНоменклатуры();
	
	ЗаполнитьЗначенияСвойств(ПараметрыСопоставления, ДополнительныеПараметры);
	
	ДопустимыеСпособыОбработки = Новый Массив; // Массив из Строка
	
	ДопустимыеСпособыОбработки.Добавить("СверкаДокументаСТорговойПлощадкой");
	
	ДопустимыеСпособыОбработки.Добавить(
		ИнтеграцияСМаркетплейсамиКлиентСервер.СпособОбработкиСопоставленияНоменклатурыПоискВариантов());
		
	Если ДопустимыеСпособыОбработки.Найти(ПараметрыСопоставления.СпособОбработки) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"ОбщийМодуль.ИнтеграцияСМаркетплейсамиСервер.НайтиВариантыСопоставленияНоменклатуры");
	
	Если ЗначениеЗаполнено(ПараметрыСопоставления.УчетнаяЗапись) Тогда
		
		РеквизитыУчетнойЗаписи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ПараметрыСопоставления.УчетнаяЗапись, "ВидМаркетплейса, Партнер");
		
	Иначе
		
		РеквизитыУчетнойЗаписи = Новый Структура;
		
		РеквизитыУчетнойЗаписи.Вставить("ВидМаркетплейса", Перечисления.ВидыМаркетплейсов.ПустаяСсылка());
		РеквизитыУчетнойЗаписи.Вставить("Партнер", ТаблицаНоменклатурыКонтрагентов[0].Владелец);
		
	КонецЕсли;
	
	Если РеквизитыУчетнойЗаписи.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ИмяИдентификатора = "Артикул";
	Иначе
		ИмяИдентификатора = "Идентификатор";
	КонецЕсли;
	
	ВыборкаДанных = ВыборкаДанныхДляСопоставленияНоменклатуры(
		ТаблицаНоменклатурыКонтрагентов,
		РеквизитыУчетнойЗаписи.ВидМаркетплейса,
		ПараметрыСопоставления.УчетнаяЗапись,
		РеквизитыУчетнойЗаписи.Партнер,
		ИмяИдентификатора,
		ПолныйПоиск);
	
	Если ТаблицаНоменклатурыКонтрагентов.Количество() > 1000 Тогда
		
		ТаблицаНоменклатурыКонтрагентов.Индексы.Добавить("Идентификатор, НоменклатураИБ");
		ТаблицаНоменклатурыКонтрагентов.Индексы.Добавить("КодНоменклатуры, НоменклатураИБ");
		
		ИндексыДобавлены = Истина;
		
	Иначе
		ИндексыДобавлены = Ложь;
	КонецЕсли;
	
	Пока ВыборкаДанных.СледующийПоЗначениюПоля("ИдентификаторПубликации") Цикл
		
		Пока ВыборкаДанных.СледующийПоЗначениюПоля("КодНоменклатуры") Цикл
			
			Отбор = Новый Структура;
			
			Если ВыборкаДанных.Приоритет = 2 Тогда
				Отбор.Вставить("КодНоменклатуры", ВыборкаДанных.КодНоменклатуры);
			Иначе
				Отбор.Вставить(ИмяИдентификатора, ВыборкаДанных.ИдентификаторПубликации);
			КонецЕсли;
			
			Отбор.Вставить("НоменклатураИБ", ВыборкаДанных.НоменклатураПубликации);
			
			НайденныеСтроки = ТаблицаНоменклатурыКонтрагентов.НайтиСтроки(Отбор);
			
			Для Каждого Строка Из НайденныеСтроки Цикл
				
				ПриоритетныйВариант = Истина;
				
				Пока ВыборкаДанных.Следующий() Цикл
					
					ВариантСопоставления = СопоставлениеНоменклатурыКонтрагентовСлужебный.НовыйВариантСопоставленияНоменклатурыБЭД();
					ВариантСопоставления.НоменклатураИБ      = ВыборкаДанных.НоменклатураИБ;
					ВариантСопоставления.ПриоритетныйВариант = ПриоритетныйВариант;
					ВариантСопоставления.ШтрихкодСопоставлен = (ВыборкаДанных.Приоритет = 5);
					
					СопоставлениеНоменклатурыКонтрагентовСлужебный.ЗаполнитьВариантСопоставленияНоменклатуры(
						ВариантСопоставления,
						Строка.ВариантыСопоставленияНоменклатурыБЭД,
						ВыборкаДанных.НоменклатураИБ);
						
					Если Не ПриоритетныйВариант Тогда
						Продолжить;
					КонецЕсли;
					
					ЗаполнитьЗначенияСвойств(Строка, ВыборкаДанных);
					
					Если ВыборкаДанных.Приоритет <= 3 Тогда
						Строка.ВариантУказанияНоменклатуры = 
							СопоставлениеНоменклатурыКонтрагентовКлиентСервер.ВариантУказанияНоменклатураОрганизации();
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ВыборкаДанных.ХарактеристикаИБ)
						И Строка.ВариантыСопоставленияХарактеристикиБЭД.Найти(ВыборкаДанных.ХарактеристикаИБ) = Неопределено Тогда
						
						Строка.ВариантыСопоставленияХарактеристикиБЭД.Добавить(ВыборкаДанных.ХарактеристикаИБ);
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ВыборкаДанных.УпаковкаИБ) Тогда
						Строка.ВариантыСопоставленияУпаковкиБЭД.Добавить(ВыборкаДанных.УпаковкаИБ);
					КонецЕсли;
					
					ПриоритетныйВариант = Ложь; // Приоритетный - только первый
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ИндексыДобавлены Тогда
		ТаблицаНоменклатурыКонтрагентов.Индексы.Очистить();
	КонецЕсли;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
		Замер,
		ТаблицаНоменклатурыКонтрагентов.Количество() / 100);
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура - новые параметры сопоставления номенклатуры:
//   * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - 
//   * СпособОбработки - Строка - 
//
Функция НовыеПараметрыСопоставленияНоменклатуры()
	
	Результат = Новый Структура;
	
	Результат.Вставить("УчетнаяЗапись", Справочники.УчетныеЗаписиМаркетплейсов.ПустаяСсылка());
	Результат.Вставить("СпособОбработки", "");
	
	Возврат Результат;
	
КонецФункции

// Выборка данных для сопоставления номенклатуры.
// 
// Параметры:
//  ТаблицаНоменклатурыКонтрагентов  - см. СопоставлениеНоменклатурыКонтрагентовСлужебный.НоваяТаблицаНоменклатурыКонтрагентов
//  ВидМаркетплейса  - ПеречислениеСсылка.ВидыМаркетплейсов - 
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - 
//  Партнер - СправочникСсылка.Партнеры - 
//  ИмяИдентификатора - Строка - 
//  ПолныйПоиск - Булево - 
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//   * НоменклатураПубликации - СправочникСсылка.Номенклатура -
//   * ИдентификаторПубликации - Строка -
//   * КодНоменклатуры - Строка -
//   * ШтрихкодКомбинации - Строка -
//   * НоменклатураИБ - СправочникСсылка.Номенклатура -
//   * ХарактеристикаИБ - СправочникСсылка.ХарактеристикиНоменклатуры -
//   * УпаковкаИБ - СправочникСсылка.УпаковкиЕдиницыИзмерения -
//   * Приоритет - Число -
//
Функция ВыборкаДанныхДляСопоставленияНоменклатуры(ТаблицаНоменклатурыКонтрагентов, ВидМаркетплейса, УчетнаяЗапись,
			Партнер, ИмяИдентификатора, ПолныйПоиск)
	
	Тексты                   = Новый Массив; // Массив из Строка
	МассивУничтожаемыхТаблиц = Новый Массив; // Массив из Строка
	
#Область ОсновныеТаблицыПоиска
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаНоменклатурыКонтрагентов.НоменклатураИБ КАК НоменклатураИБ,
		|	&ТекстЗапросаИдентификатор КАК Идентификатор,
		|	ТаблицаНоменклатурыКонтрагентов.КодНоменклатуры КАК КодНоменклатуры,
		|	ТаблицаНоменклатурыКонтрагентов.ШтрихкодКомбинации КАК ШтрихкодКомбинации
		|ПОМЕСТИТЬ ТаблицаНоменклатурыКонтрагентов
		|ИЗ
		|	&ТаблицаНоменклатурыКонтрагентов КАК ТаблицаНоменклатурыКонтрагентов";
	
	ТекстЗапросаИдентификатор = СтрШаблон("ТаблицаНоменклатурыКонтрагентов.%1", ИмяИдентификатора);
	ТекстЗапроса              = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаИдентификатор", ТекстЗапросаИдентификатор);
	
	Если Не ПолныйПоиск Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	НоменклатураИБ";
		
	КонецЕсли;
	
	Тексты.Добавить(ТекстЗапроса);
	МассивУничтожаемыхТаблиц.Добавить("ТаблицаНоменклатурыКонтрагентов");
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаНоменклатурыКонтрагентов.НоменклатураИБ КАК НоменклатураПубликации,
		|	ТаблицаНоменклатурыКонтрагентов.Идентификатор КАК ИдентификаторПубликации,
		|	ТаблицаНоменклатурыКонтрагентов.КодНоменклатуры КАК КодНоменклатуры,
		|	ТаблицаНоменклатурыКонтрагентов.ШтрихкодКомбинации КАК ШтрихкодКомбинации
		|ПОМЕСТИТЬ ТаблицаИдентификаторов
		|ИЗ
		|	ТаблицаНоменклатурыКонтрагентов КАК ТаблицаНоменклатурыКонтрагентов
		|ГДЕ
		|	&УсловиеПоискаДляОбработанных
		|	И ТаблицаНоменклатурыКонтрагентов.Идентификатор <> """"
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторПубликации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаНоменклатурыКонтрагентов.НоменклатураИБ КАК НоменклатураПубликации,
		|	ТаблицаНоменклатурыКонтрагентов.Идентификатор КАК ИдентификаторПубликации,
		|	ТаблицаНоменклатурыКонтрагентов.КодНоменклатуры КАК КодНоменклатуры,
		|	ТаблицаНоменклатурыКонтрагентов.ШтрихкодКомбинации КАК ШтрихкодКомбинации
		|ПОМЕСТИТЬ ТаблицаКодовНоменклатуры
		|ИЗ
		|	ТаблицаНоменклатурыКонтрагентов КАК ТаблицаНоменклатурыКонтрагентов
		|ГДЕ
		|	&УсловиеПоискаДляОбработанных
		|	И ТаблицаНоменклатурыКонтрагентов.КодНоменклатуры <> """"
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КодНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаНоменклатурыКонтрагентов.НоменклатураИБ КАК НоменклатураПубликации,
		|	ТаблицаНоменклатурыКонтрагентов.Идентификатор КАК ИдентификаторПубликации,
		|	ТаблицаНоменклатурыКонтрагентов.КодНоменклатуры КАК КодНоменклатуры,
		|	ТаблицаНоменклатурыКонтрагентов.ШтрихкодКомбинации КАК ШтрихкодКомбинации
		|ПОМЕСТИТЬ ТаблицаШтрихкодовКомбинации
		|ИЗ
		|	ТаблицаНоменклатурыКонтрагентов КАК ТаблицаНоменклатурыКонтрагентов
		|ГДЕ
		|	&УсловиеПоискаДляОбработанных
		|	И ТаблицаНоменклатурыКонтрагентов.ШтрихкодКомбинации <> """"
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ШтрихкодКомбинации";
	
	Если Не ПолныйПоиск Тогда
		ТекстУсловиеПоискаДляОбработанных = "ТаблицаНоменклатурыКонтрагентов.НоменклатураИБ = &ПустаяНоменклатура";
	Иначе
		ТекстУсловиеПоискаДляОбработанных = "ИСТИНА";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоискаДляОбработанных", ТекстУсловиеПоискаДляОбработанных);
	
	Тексты.Добавить(ТекстЗапроса);
	
	МассивУничтожаемыхТаблиц.Добавить("ТаблицаИдентификаторов");
	МассивУничтожаемыхТаблиц.Добавить("ТаблицаКодовНоменклатуры");
	МассивУничтожаемыхТаблиц.Добавить("ТаблицаШтрихкодовКомбинации");
	
#КонецОбласти

#Область ДополнительныеВременныеТаблицы
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаИдентификаторов.НоменклатураПубликации КАК НоменклатураПубликации,
		|	ТаблицаИдентификаторов.ИдентификаторПубликации КАК ИдентификаторПубликации,
		|	ТаблицаИдентификаторов.КодНоменклатуры КАК КодНоменклатуры,
		|	ТаблицаИдентификаторов.ШтрихкодКомбинации КАК ШтрихкодКомбинации,
		|	СправочникНоменклатураКонтрагентов.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ НоменклатураКонтрагентовПоИдентификатору
		|ИЗ
		|	Справочник.НоменклатураКонтрагентов КАК СправочникНоменклатураКонтрагентов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаИдентификаторов КАК ТаблицаИдентификаторов
		|		ПО (ТаблицаИдентификаторов.ИдентификаторПубликации = СправочникНоменклатураКонтрагентов.Идентификатор)
		|ГДЕ
		|	СправочникНоменклатураКонтрагентов.Владелец = &Владелец
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка";
	
	Тексты.Добавить(ТекстЗапроса);
	МассивУничтожаемыхТаблиц.Добавить("НоменклатураКонтрагентовПоИдентификатору");
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаИдентификаторов.НоменклатураПубликации КАК НоменклатураПубликации,
		|	ТаблицаИдентификаторов.ИдентификаторПубликации КАК ИдентификаторПубликации,
		|	ТаблицаИдентификаторов.КодНоменклатуры КАК КодНоменклатуры,
		|	ТаблицаИдентификаторов.ШтрихкодКомбинации КАК ШтрихкодКомбинации,
		|	СправочникНоменклатураКонтрагентов.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ НоменклатураКонтрагентовПоАртикулу
		|ИЗ
		|	Справочник.НоменклатураКонтрагентов КАК СправочникНоменклатураКонтрагентов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаИдентификаторов КАК ТаблицаИдентификаторов
		|		ПО (ТаблицаИдентификаторов.ИдентификаторПубликации = СправочникНоменклатураКонтрагентов.Артикул)
		|ГДЕ
		|	СправочникНоменклатураКонтрагентов.Владелец = &Владелец
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка";
	
	Тексты.Добавить(ТекстЗапроса);
	МассивУничтожаемыхТаблиц.Добавить("НоменклатураКонтрагентовПоАртикулу");
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	4 КАК Приоритет,
		|	НоменклатураКонтрагентовПоИдентификатору.НоменклатураПубликации КАК НоменклатураПубликации,
		|	НоменклатураКонтрагентовПоИдентификатору.ИдентификаторПубликации КАК ИдентификаторПубликации,
		|	НоменклатураКонтрагентовПоИдентификатору.КодНоменклатуры КАК КодНоменклатуры,
		|	НоменклатураКонтрагентовПоИдентификатору.ШтрихкодКомбинации КАК ШтрихкодКомбинации,
		|	СправочникНоменклатураКонтрагентов.Номенклатура КАК НоменклатураИБ,
		|	СправочникНоменклатураКонтрагентов.Характеристика КАК ХарактеристикаИБ,
		|	СправочникНоменклатураКонтрагентов.Упаковка КАК УпаковкаИБ
		|ПОМЕСТИТЬ ВариантыСопоставленияДополнительные
		|ИЗ
		|	Справочник.НоменклатураКонтрагентов КАК СправочникНоменклатураКонтрагентов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НоменклатураКонтрагентовПоИдентификатору КАК НоменклатураКонтрагентовПоИдентификатору
		|		ПО СправочникНоменклатураКонтрагентов.Ссылка = НоменклатураКонтрагентовПоИдентификатору.Ссылка
		|ГДЕ
		|	СправочникНоменклатураКонтрагентов.Номенклатура <> &ПустаяНоменклатура
		|	И НЕ СправочникНоменклатураКонтрагентов.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4,
		|	НоменклатураКонтрагентовПоАртикулу.НоменклатураПубликации,
		|	НоменклатураКонтрагентовПоАртикулу.ИдентификаторПубликации,
		|	НоменклатураКонтрагентовПоАртикулу.КодНоменклатуры,
		|	НоменклатураКонтрагентовПоАртикулу.ШтрихкодКомбинации,
		|	СправочникНоменклатураКонтрагентов.Номенклатура,
		|	СправочникНоменклатураКонтрагентов.Характеристика,
		|	СправочникНоменклатураКонтрагентов.Упаковка
		|ИЗ
		|	Справочник.НоменклатураКонтрагентов КАК СправочникНоменклатураКонтрагентов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НоменклатураКонтрагентовПоАртикулу КАК НоменклатураКонтрагентовПоАртикулу
		|		ПО СправочникНоменклатураКонтрагентов.Ссылка = НоменклатураКонтрагентовПоАртикулу.Ссылка
		|ГДЕ
		|	СправочникНоменклатураКонтрагентов.Артикул <> СправочникНоменклатураКонтрагентов.Идентификатор
		|	И СправочникНоменклатураКонтрагентов.Номенклатура <> &ПустаяНоменклатура
		|	И НЕ СправочникНоменклатураКонтрагентов.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	5,
		|	ТаблицаШтрихкодовКомбинации.НоменклатураПубликации,
		|	ТаблицаШтрихкодовКомбинации.ИдентификаторПубликации,
		|	ТаблицаШтрихкодовКомбинации.КодНоменклатуры,
		|	ТаблицаШтрихкодовКомбинации.ШтрихкодКомбинации,
		|	ШтрихкодыНоменклатуры.Номенклатура,
		|	ШтрихкодыНоменклатуры.Характеристика,
		|	ШтрихкодыНоменклатуры.Упаковка
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаШтрихкодовКомбинации КАК ТаблицаШтрихкодовКомбинации
		|		ПО (ТаблицаШтрихкодовКомбинации.ШтрихкодКомбинации = ШтрихкодыНоменклатуры.Штрихкод)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НоменклатураИБ";
	
	Тексты.Добавить(ТекстЗапроса);
	МассивУничтожаемыхТаблиц.Добавить("ВариантыСопоставленияДополнительные");
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ВариантыСопоставленияДополнительные.НоменклатураПубликации КАК НоменклатураПубликации,
			|	ВариантыСопоставленияДополнительные.ИдентификаторПубликации КАК ИдентификаторПубликации,
			|	ВариантыСопоставленияДополнительные.КодНоменклатуры КАК КодНоменклатуры,
			|	ВариантыСопоставленияДополнительные.ШтрихкодКомбинации КАК ШтрихкодКомбинации,
			|	ВариантыСопоставленияДополнительные.НоменклатураИБ КАК НоменклатураИБ,
			|	ВариантыСопоставленияДополнительные.ХарактеристикаИБ КАК ХарактеристикаИБ,
			|	ВариантыСопоставленияДополнительные.УпаковкаИБ КАК УпаковкаИБ
			|ПОМЕСТИТЬ ТаблицаПоНоменклатуре
			|ИЗ
			|	ВариантыСопоставленияДополнительные КАК ВариантыСопоставленияДополнительные
			|ГДЕ
			|	НЕ ВариантыСопоставленияДополнительные.НоменклатураИБ = &ПустаяНоменклатура
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	НоменклатураИБ,
			|	ХарактеристикаИБ,
			|	УпаковкаИБ";
		
		Тексты.Добавить(ТекстЗапроса);
		МассивУничтожаемыхТаблиц.Добавить("ТаблицаПоНоменклатуре");
		
	КонецЕсли;
	
#КонецОбласти
	
#Область ПриоритетыПоиска
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СтатусыПубликацииОбъектов.УчетнаяЗапись КАК УчетнаяЗапись,
		|	СтатусыПубликацииОбъектов.Номенклатура КАК Номенклатура,
		|	СтатусыПубликацииОбъектов.Характеристика КАК Характеристика,
		|	СтатусыПубликацииОбъектов.Упаковка КАК Упаковка,
		|	СтатусыПубликацииОбъектов.ИдентификаторПредложения КАК ИдентификаторПредложения
		|ПОМЕСТИТЬ СтатусыПубликацииТоваровЯндексМаркет
		|ИЗ
		|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииОбъектов
		|ГДЕ
		|	ИСТИНА В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				ТаблицаИдентификаторов КАК ТаблицаИдентификаторов
		|			ГДЕ
		|				ТаблицаИдентификаторов.ИдентификаторПубликации = СтатусыПубликацииОбъектов.ИдентификаторПредложения)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторПредложения";
	
	Тексты.Добавить(ТекстЗапроса);
	МассивУничтожаемыхТаблиц.Добавить("СтатусыПубликацииТоваровЯндексМаркет");
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СтатусыПубликацииОбъектов.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
		|	СтатусыПубликацииОбъектов.ВидОбъектаМаркетплейса КАК ВидОбъектаМаркетплейса,
		|	СтатусыПубликацииОбъектов.Номенклатура КАК Номенклатура,
		|	СтатусыПубликацииОбъектов.Характеристика КАК Характеристика,
		|	СтатусыПубликацииОбъектов.Упаковка КАК Упаковка,
		|	СтатусыПубликацииОбъектов.ИдентификаторПубликации КАК ИдентификаторПубликации
		|ПОМЕСТИТЬ СтатусыПубликацииОбъектовМаркетплейсаOzon
		|ИЗ
		|	РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликацииОбъектов
		|ГДЕ
		|	ИСТИНА В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				ТаблицаИдентификаторов КАК ТаблицаИдентификаторов
		|			ГДЕ
		|				ТаблицаИдентификаторов.ИдентификаторПубликации = СтатусыПубликацииОбъектов.ИдентификаторПубликации)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторПубликации";
	
	Тексты.Добавить(ТекстЗапроса);
	МассивУничтожаемыхТаблиц.Добавить("СтатусыПубликацииОбъектовМаркетплейсаOzon");
	
	ТекстыПриоритетов = Новый Массив; // Массив из Строка
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВариантыСопоставленияДополнительные.Приоритет КАК Приоритет,
		|	ВариантыСопоставленияДополнительные.НоменклатураПубликации КАК НоменклатураПубликации,
		|	ВариантыСопоставленияДополнительные.ИдентификаторПубликации КАК ИдентификаторПубликации,
		|	ВариантыСопоставленияДополнительные.КодНоменклатуры КАК КодНоменклатуры,
		|	ВариантыСопоставленияДополнительные.ШтрихкодКомбинации КАК ШтрихкодКомбинации,
		|	ВариантыСопоставленияДополнительные.НоменклатураИБ КАК НоменклатураИБ,
		|	ВариантыСопоставленияДополнительные.ХарактеристикаИБ КАК ХарактеристикаИБ,
		|	ВариантыСопоставленияДополнительные.УпаковкаИБ КАК УпаковкаИБ
		|ПОМЕСТИТЬ ВариантыСопоставления
		|ИЗ
		|	ВариантыСопоставленияДополнительные КАК ВариантыСопоставленияДополнительные";
	
	ТекстыПриоритетов.Добавить(ТекстЗапроса);
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	1 КАК Приоритет,
			|	ТаблицаИдентификаторов.НоменклатураПубликации КАК НоменклатураПубликации,
			|	ТаблицаИдентификаторов.ИдентификаторПубликации КАК ИдентификаторПубликации,
			|	ТаблицаИдентификаторов.КодНоменклатуры КАК КодНоменклатуры,
			|	ТаблицаИдентификаторов.ШтрихкодКомбинации КАК ШтрихкодКомбинации,
			|	СтатусыПубликацииОбъектов.Номенклатура КАК НоменклатураИБ,
			|	СтатусыПубликацииОбъектов.Характеристика КАК ХарактеристикаИБ,
			|	СтатусыПубликацииОбъектов.Упаковка КАК УпаковкаИБ
			|ИЗ
			|	РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликацииОбъектов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаИдентификаторов КАК ТаблицаИдентификаторов
			|		ПО (ТаблицаИдентификаторов.ИдентификаторПубликации = СтатусыПубликацииОбъектов.ИдентификаторПубликации)
			|ГДЕ
			|	СтатусыПубликацииОбъектов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись
			|	И СтатусыПубликацииОбъектов.ВидОбъектаМаркетплейса = &ВидОбъектаТорговойПлощадки
			|	И СтатусыПубликацииОбъектов.Номенклатура <> &ПустаяНоменклатура";
		
		ТекстыПриоритетов.Добавить(ТекстЗапроса);
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	2 КАК Приоритет,
			|	ТаблицаКодовНоменклатуры.НоменклатураПубликации КАК НоменклатураПубликации,
			|	ТаблицаКодовНоменклатуры.ИдентификаторПубликации КАК ИдентификаторПубликации,
			|	ТаблицаКодовНоменклатуры.КодНоменклатуры КАК КодНоменклатуры,
			|	ТаблицаКодовНоменклатуры.ШтрихкодКомбинации КАК ШтрихкодКомбинации,
			|	СтатусыПубликацииОбъектов.Номенклатура КАК НоменклатураИБ,
			|	СтатусыПубликацииОбъектов.Характеристика КАК ХарактеристикаИБ,
			|	СтатусыПубликацииОбъектов.Упаковка КАК УпаковкаИБ
			|ИЗ
			|	РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликацииОбъектов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаКодовНоменклатуры КАК ТаблицаКодовНоменклатуры
			|		ПО (ТаблицаКодовНоменклатуры.КодНоменклатуры = СтатусыПубликацииОбъектов.ИдентификаторFBOSKU)
			|ГДЕ
			|	СтатусыПубликацииОбъектов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись
			|	И СтатусыПубликацииОбъектов.ВидОбъектаМаркетплейса = &ВидОбъектаТорговойПлощадки
			|	И СтатусыПубликацииОбъектов.Номенклатура <> &ПустаяНоменклатура";
		
		ТекстыПриоритетов.Добавить(ТекстЗапроса);
		
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	1 КАК Приоритет,
			|	ТаблицаИдентификаторов.НоменклатураПубликации КАК НоменклатураПубликации,
			|	ТаблицаИдентификаторов.ИдентификаторПубликации КАК ИдентификаторПубликации,
			|	ТаблицаИдентификаторов.КодНоменклатуры КАК КодНоменклатуры,
			|	ТаблицаИдентификаторов.ШтрихкодКомбинации КАК ШтрихкодКомбинации,
			|	СтатусыПубликацииОбъектов.Номенклатура КАК НоменклатураИБ,
			|	СтатусыПубликацииОбъектов.Характеристика КАК ХарактеристикаИБ,
			|	ВЫБОР
			|		КОГДА СтатусыПубликацииОбъектов.Упаковка = СправочникНоменклатура.ЕдиницаИзмерения
			|			ТОГДА &ПустаяУпаковка
			|		ИНАЧЕ СтатусыПубликацииОбъектов.Упаковка
			|	КОНЕЦ КАК УпаковкаИБ
			|ИЗ
			|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииОбъектов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
			|		ПО (СправочникНоменклатура.Ссылка = СтатусыПубликацииОбъектов.Номенклатура)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаИдентификаторов КАК ТаблицаИдентификаторов
			|		ПО (ТаблицаИдентификаторов.ИдентификаторПубликации = СтатусыПубликацииОбъектов.ИдентификаторПредложения)
			|ГДЕ
			|	СтатусыПубликацииОбъектов.УчетнаяЗапись = &УчетнаяЗапись";
		
		ТекстыПриоритетов.Добавить(ТекстЗапроса);
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	2 КАК Приоритет,
			|	ТаблицаПоНоменклатуре.НоменклатураПубликации КАК НоменклатураПубликации,
			|	ТаблицаПоНоменклатуре.ИдентификаторПубликации КАК ИдентификаторПубликации,
			|	ТаблицаПоНоменклатуре.КодНоменклатуры КАК КодНоменклатуры,
			|	ТаблицаПоНоменклатуре.ШтрихкодКомбинации КАК ШтрихкодКомбинации,
			|	СтатусыПубликацииОбъектов.Номенклатура КАК НоменклатураИБ,
			|	СтатусыПубликацииОбъектов.Характеристика КАК ХарактеристикаИБ,
			|	ВЫБОР
			|		КОГДА СтатусыПубликацииОбъектов.Упаковка = СправочникНоменклатура.ЕдиницаИзмерения
			|			ТОГДА &ПустаяУпаковка
			|		ИНАЧЕ СтатусыПубликацииОбъектов.Упаковка
			|	КОНЕЦ КАК УпаковкаИБ
			|ИЗ
			|	РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииОбъектов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
			|		ПО (СправочникНоменклатура.Ссылка = СтатусыПубликацииОбъектов.Номенклатура)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПоНоменклатуре КАК ТаблицаПоНоменклатуре
			|		ПО (СтатусыПубликацииОбъектов.УчетнаяЗапись = &УчетнаяЗапись)
			|			И (ТаблицаПоНоменклатуре.НоменклатураИБ = СтатусыПубликацииОбъектов.Номенклатура)
			|			И (ТаблицаПоНоменклатуре.ХарактеристикаИБ = СтатусыПубликацииОбъектов.Характеристика)
			|			И (ТаблицаПоНоменклатуре.УпаковкаИБ = ВЫБОР
			|				КОГДА СтатусыПубликацииОбъектов.Упаковка = СправочникНоменклатура.ЕдиницаИзмерения
			|					ТОГДА &ПустаяУпаковка
			|				ИНАЧЕ СтатусыПубликацииОбъектов.Упаковка
			|			КОНЕЦ)
			|ГДЕ
			|	НЕ СтатусыПубликацииОбъектов.НоменклатураПартнера = &ПустаяНоменклатураКонтрагентов";
		
		ТекстыПриоритетов.Добавить(ТекстЗапроса);
		
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	3 КАК Приоритет,
		|	ТаблицаИдентификаторов.НоменклатураПубликации КАК НоменклатураПубликации,
		|	ТаблицаИдентификаторов.ИдентификаторПубликации КАК ИдентификаторПубликации,
		|	ТаблицаИдентификаторов.КодНоменклатуры КАК КодНоменклатуры,
		|	ТаблицаИдентификаторов.ШтрихкодКомбинации КАК ШтрихкодКомбинации,
		|	СтатусыПубликацииОбъектов.Номенклатура КАК НоменклатураИБ,
		|	СтатусыПубликацииОбъектов.Характеристика КАК ХарактеристикаИБ,
		|	СтатусыПубликацииОбъектов.Упаковка КАК УпаковкаИБ
		|ИЗ
		|	СтатусыПубликацииТоваровЯндексМаркет КАК СтатусыПубликацииОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаИдентификаторов КАК ТаблицаИдентификаторов
		|		ПО (ТаблицаИдентификаторов.ИдентификаторПубликации = СтатусыПубликацииОбъектов.ИдентификаторПредложения)
		|ГДЕ
		|	СтатусыПубликацииОбъектов.УчетнаяЗапись <> &УчетнаяЗапись
		|	И СтатусыПубликацииОбъектов.Номенклатура <> &ПустаяНоменклатура";
	
	ТекстыПриоритетов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	3 КАК Приоритет,
		|	ТаблицаИдентификаторов.НоменклатураПубликации КАК НоменклатураПубликации,
		|	ТаблицаИдентификаторов.ИдентификаторПубликации КАК ИдентификаторПубликации,
		|	ТаблицаИдентификаторов.КодНоменклатуры КАК КодНоменклатуры,
		|	ТаблицаИдентификаторов.ШтрихкодКомбинации КАК ШтрихкодКомбинации,
		|	СтатусыПубликацииОбъектов.Номенклатура КАК НоменклатураИБ,
		|	СтатусыПубликацииОбъектов.Характеристика КАК ХарактеристикаИБ,
		|	СтатусыПубликацииОбъектов.Упаковка КАК УпаковкаИБ
		|ИЗ
		|	СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликацииОбъектов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаИдентификаторов КАК ТаблицаИдентификаторов
		|		ПО (ТаблицаИдентификаторов.ИдентификаторПубликации = СтатусыПубликацииОбъектов.ИдентификаторПубликации)
		|ГДЕ
		|	СтатусыПубликацииОбъектов.УчетнаяЗаписьМаркетплейса <> &УчетнаяЗапись
		|	И СтатусыПубликацииОбъектов.ВидОбъектаМаркетплейса = &ВидОбъектаТорговойПлощадки
		|	И СтатусыПубликацииОбъектов.Номенклатура <> &ПустаяНоменклатура";
	
	ТекстыПриоритетов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(ТекстыПриоритетов, ОбщегоНазначения.ТекстОбъединитьВсе());
	
	ТекстЗапроса = ТекстЗапроса + "
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторПубликации,
		|	КодНоменклатуры";
	
	Тексты.Добавить(ТекстЗапроса);
	
#КонецОбласти
	
	Для Каждого ИмяУдаляемойТаблицы Из МассивУничтожаемыхТаблиц Цикл
		Тексты.Добавить("УНИЧТОЖИТЬ " + ИмяУдаляемойТаблицы);
	КонецЦикла;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВариантыСопоставления.НоменклатураПубликации КАК НоменклатураПубликации,
		|	ВариантыСопоставления.ИдентификаторПубликации КАК ИдентификаторПубликации,
		|	ВариантыСопоставления.КодНоменклатуры КАК КодНоменклатуры,
		|	ВариантыСопоставления.ШтрихкодКомбинации КАК ШтрихкодКомбинации,
		|	ВариантыСопоставления.НоменклатураИБ КАК НоменклатураИБ,
		|	ВариантыСопоставления.ХарактеристикаИБ КАК ХарактеристикаИБ,
		|	ВариантыСопоставления.УпаковкаИБ КАК УпаковкаИБ,
		|	ВариантыСопоставления.Приоритет КАК Приоритет
		|ИЗ
		|	ВариантыСопоставления КАК ВариантыСопоставления
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИдентификаторПубликации,
		|	КодНоменклатуры,
		|	Приоритет";
	
	Тексты.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос       = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ТаблицаНоменклатурыКонтрагентов", ТаблицаНоменклатурыКонтрагентов);
	Запрос.УстановитьПараметр("ПустаяНоменклатураКонтрагентов",  Справочники.НоменклатураКонтрагентов.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяНоменклатура",              Справочники.Номенклатура.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяУпаковка",                  Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	Запрос.УстановитьПараметр("УчетнаяЗапись",                   УчетнаяЗапись);
	Запрос.УстановитьПараметр("ВидОбъектаТорговойПлощадки",      Перечисления.ВидыОбъектовМаркетплейсов.Товар);
	Запрос.УстановитьПараметр("Владелец",                        Партнер);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗапроса.Выбрать();
	
КонецФункции

#КонецОбласти

#Область ФорматыЭДО

// Аналог функции см. ФорматыЭДО.НоваяТаблицаТоваров.
// Конструктор таблицы с информацией о товарах из электронного документа.
// 
// Возвращаемое значение:
//   ТаблицаЗначений:
//     * ИД                                    - Строка
//     * Штрихкод                              - Строка
//     * Наименование                          - Строка
//     * НаименованиеХарактеристики            - Строка 
//     * КодТовара                             - Строка
//     * Артикул                               - Строка
//     * БазоваяЕдиницаКод                     - Строка
//     * БазоваяЕдиницаНаименование            - Строка
//     * БазоваяЕдиницаНаименованиеПолное      - Строка
//     * БазоваяЕдиницаМеждународноеСокращение - Строка
//     * БазоваяЕдиницаНаименованиеКраткое     - Строка
//     * СтавкаНДС                             - Строка
//     * Описание                              - Строка
//     * Сопоставление                         - см. ФорматыЭДО.НовыеДанныеДляСопоставленияТоваров.
//     * ИсторияИдентификаторов                - Неопределено, Массив из Строка -
//     * ТипНоменклатурыФНС                    - Неопределено, ПеречислениеСсылка.ТипыНоменклатурыФНС -
//     * КодПоКТРУ                             - Строка
//     * КодТНВЭД                              - Строка
//     * СтранаПроисхожденияКод                - Строка
//     * ПрослеживаемыйТовар                   - Булево
//     * МаркируемыйТовар                      - Булево
//     * КонтрольныеИдентификационныеЗнаки     - Строка
//     * КоличествоУпаковок                    - Число
//
Функция НоваяТаблицаТоваровЭДО()
	
	ТаблицаТоваров = Новый ТаблицаЗначений();
	
	ТаблицаТоваров.Колонки.Добавить("ИД",                                    ОбщегоНазначения.ОписаниеТипаСтрока(300));
	ТаблицаТоваров.Колонки.Добавить("Штрихкод",                              ОбщегоНазначения.ОписаниеТипаСтрока(300));
	ТаблицаТоваров.Колонки.Добавить("Наименование",                          ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	ТаблицаТоваров.Колонки.Добавить("НаименованиеХарактеристики",            ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	ТаблицаТоваров.Колонки.Добавить("КодТовара",                             ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаТоваров.Колонки.Добавить("Артикул",                               ОбщегоНазначения.ОписаниеТипаСтрока(300));
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаКод",                     ОбщегоНазначения.ОписаниеТипаСтрока(10));
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименование",            ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименованиеПолное",      ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаМеждународноеСокращение", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаТоваров.Колонки.Добавить("БазоваяЕдиницаНаименованиеКраткое",     ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаТоваров.Колонки.Добавить("СтавкаНДС",                             ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаТоваров.Колонки.Добавить("Описание",                              ОбщегоНазначения.ОписаниеТипаСтрока(3000));
	ТаблицаТоваров.Колонки.Добавить("Сопоставление");
	ТаблицаТоваров.Колонки.Добавить("ИсторияИдентификаторов",                Новый ОписаниеТипов("Массив"));
	ТаблицаТоваров.Колонки.Добавить("ТипНоменклатурыФНС");
	ТаблицаТоваров.Колонки.Добавить("КодПоКТРУ",                             ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТаблицаТоваров.Колонки.Добавить("КодТНВЭД",                              ОбщегоНазначения.ОписаниеТипаСтрока(10));
	ТаблицаТоваров.Колонки.Добавить("СтранаПроисхожденияКод",                ОбщегоНазначения.ОписаниеТипаСтрока(3));
	ТаблицаТоваров.Колонки.Добавить("ПрослеживаемыйТовар",                   Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("МаркируемыйТовар",                      Новый ОписаниеТипов("Булево"));
	ТаблицаТоваров.Колонки.Добавить("КонтрольныеИдентификационныеЗнаки",     ОбщегоНазначения.ОписаниеТипаСтрока(250));
	ТаблицаТоваров.Колонки.Добавить("КоличествоУпаковок",                    ОбщегоНазначения.ОписаниеТипаЧисло(20, 3));
	
	Возврат ТаблицаТоваров;
	
КонецФункции

#КонецОбласти

#Область ПодключаемыеКомандыЭДОСлужебный

// Аналог функции ПодключаемыеКомандыЭДОСлужебный.ДобавитьКомандыЭДО.
Процедура ДобавитьКомандыЭДО(Форма, КомандыЭДО, Знач МестоРазмещенияКоманд = Неопределено, ИсточникКомандЭДО = Неопределено)
	
	ПараметрыУправленияВидимостьюЭДО = ПараметрыУправленияВидимостьюЭДО();
	ПараметрыУправленияВидимостьюЭДО.УсловияВидимостиВТаблицеФормы = ТипЗнч(ИсточникКомандЭДО) = Тип("ДинамическийСписок");
	
	МестоРазмещения = МестоРазмещенияКоманд;
	Для Каждого ОписаниеКомандыЭДО Из КомандыЭДО Цикл // СтрокаТаблицаЗначений
		ОписаниеКомандыЭДО.ИмяКомандыНаФорме = СформироватьИмяКоманды(ОписаниеКомандыЭДО, МестоРазмещенияКоманд);
		ДобавитьКомандуФормы(Форма, ОписаниеКомандыЭДО);
		
		НачальнаяВидимость = ОпределитьВидимостьКоманды(ПараметрыУправленияВидимостьюЭДО, ОписаниеКомандыЭДО);
		МестоРазмещения    = НайтиСоздатьМестоРазмещения(Форма, ОписаниеКомандыЭДО, МестоРазмещения, МестоРазмещенияКоманд);
		ДобавитьКнопкуФормы(Форма, ОписаниеКомандыЭДО, МестоРазмещения, НачальнаяВидимость);
	КонецЦикла;
	
	ДополнитьПараметрыУправленияВидимостьюНаФорме(Форма, ПараметрыУправленияВидимостьюЭДО, ИсточникКомандЭДО);
	
КонецПроцедуры

// Аналог функции ПодключаемыеКомандыЭДОСлужебный.ПараметрыУправленияВидимостьюЭДО.
Функция ПараметрыУправленияВидимостьюЭДО()
	
	ПараметрыУправленияВидимостьюЭДО = Новый Структура;
	ПараметрыУправленияВидимостьюЭДО.Вставить("ЕстьУсловияВидимости",          Ложь);
	ПараметрыУправленияВидимостьюЭДО.Вставить("КомандыСУсловиямиВидимости",    Новый Массив);
	ПараметрыУправленияВидимостьюЭДО.Вставить("УсловияВидимостиВТаблицеФормы", Ложь);
	
	Возврат ПараметрыУправленияВидимостьюЭДО;
	
КонецФункции

// Аналог функции ПодключаемыеКомандыЭДОСлужебный.СформироватьИмяКоманды.
Функция СформироватьИмяКоманды(ОписаниеКомандыЭДО, МестоРазмещенияКоманд)
	
	ОписаниеКомандыЭДО = ОписаниеКомандыЭДО; // СтрокаДереваЗначений
	ВладелецКоманды = ОписаниеКомандыЭДО.Владелец(); // КоллекцияСтрокДереваЗначений
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1%2%3", 
		МестоРазмещенияКоманд.Имя, 
		ОписаниеКомандыЭДО.Идентификатор,
		ВладелецКоманды.Индекс(ОписаниеКомандыЭДО));
	
КонецФункции

// Аналог функции ПодключаемыеКомандыЭДОСлужебный.ДобавитьКомандуФормы.
Процедура ДобавитьКомандуФормы(Форма, ОписаниеКомандыЭДО)
	
	КомандаФормы = Форма.Команды.Добавить(ОписаниеКомандыЭДО.ИмяКомандыНаФорме);
	КомандаФормы.Действие                  = "Подключаемый_ВыполнитьКомандуЭДО";
	КомандаФормы.Заголовок                 = ОписаниеКомандыЭДО.Представление;
	КомандаФормы.ИзменяетСохраняемыеДанные = Ложь;
	
	Если ЗначениеЗаполнено(ОписаниеКомандыЭДО.Отображение) Тогда 
		КомандаФормы.Отображение = ОписаниеКомандыЭДО.Отображение;
	Иначе 
		КомандаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеКомандыЭДО.Картинка) Тогда
		КомандаФормы.Картинка = ОписаниеКомандыЭДО.Картинка;
	КонецЕсли;
	
КонецПроцедуры

// Аналог функции ПодключаемыеКомандыЭДОСлужебный.УсловиеВидимости.
Функция УсловиеВидимости(ОписаниеКомандыЭДО)
	
	УсловияВидимости = Новый Структура;
	УсловияВидимости.Вставить("ИмяВФорме",                   ОписаниеКомандыЭДО.ИмяКомандыНаФорме);
	УсловияВидимости.Вставить("ИмяРеквизитаУсловия",         Неопределено);
	УсловияВидимости.Вставить("ИмяАлгоритмаПроверкиУсловия", Неопределено);
	УсловияВидимости.Вставить("ЗначениеУсловия",             Неопределено);
	
	ЗаполнитьЗначенияСвойств(УсловияВидимости, ОписаниеКомандыЭДО.УправлениеВидимостью);
	Возврат УсловияВидимости;
	
КонецФункции

// Аналог функции ПодключаемыеКомандыЭДОСлужебный.ОпределитьВидимостьКоманды.
Функция ОпределитьВидимостьКоманды(ПараметрыУправленияВидимостьюЭДО, ОписаниеКомандыЭДО)
	
	Видимость = Не ОписаниеКомандыЭДО.Недоступна;
	
	Если ОписаниеКомандыЭДО.УправлениеВидимостью.Свойство("Использовать")
			И ОписаниеКомандыЭДО.УправлениеВидимостью.Использовать
			И Видимость Тогда
		УсловиеВидимости = УсловиеВидимости(ОписаниеКомандыЭДО);
		ПараметрыУправленияВидимостьюЭДО.КомандыСУсловиямиВидимости.Добавить(УсловиеВидимости);
		Видимость = Ложь;
	КонецЕсли;
	
	Возврат Видимость;
	
КонецФункции

// Аналог функции ПодключаемыеКомандыЭДОСлужебный.НайтиСоздатьМестоРазмещения.
Функция НайтиСоздатьМестоРазмещения(Форма, ОписаниеКомандыЭДО, МестоРазмещения, МестоРазмещенияКоманд)
	
	// Для платформенной команды ввода на основании не добавляем префикс, чтобы команды не отделялись чертой.
	Если ОписаниеКомандыЭДО.МестоРазмещения = "ФормаСоздатьНаОсновании" Тогда
		МестоРазмещенияИмя = ОписаниеКомандыЭДО.МестоРазмещения;
	Иначе
		МестоРазмещенияИмя = МестоРазмещенияКоманд.Имя + ОписаниеКомандыЭДО.МестоРазмещения;
	КонецЕсли;
	
	Если Форма.Элементы.Найти(МестоРазмещенияИмя) = Неопределено Тогда
		МестоРазмещения = Форма.Элементы.Добавить(МестоРазмещенияИмя, Тип("ГруппаФормы"), МестоРазмещения);
		МестоРазмещения.Вид       = ВидГруппыФормы.ГруппаКнопок;
		МестоРазмещения.Заголовок = СтрЗаменить(ОписаниеКомандыЭДО.МестоРазмещения, "КомандыЭДО", "");
	КонецЕсли;
	
	Возврат МестоРазмещения;
	
КонецФункции

// Аналог функции ПодключаемыеКомандыЭДОСлужебный.ДобавитьКнопкуФормы.
Процедура ДобавитьКнопкуФормы(Форма, ОписаниеКомандыЭДО, МестоРазмещения, Видимость)
	
	НовыйЭлемент = Форма.Элементы.Добавить(ОписаниеКомандыЭДО.ИмяКомандыНаФорме, Тип("КнопкаФормы"), МестоРазмещения);
	НовыйЭлемент.Вид                   = ВидКнопкиФормы.КнопкаКоманднойПанели;
	НовыйЭлемент.ИмяКоманды            = ОписаниеКомандыЭДО.ИмяКомандыНаФорме;
	НовыйЭлемент.Видимость             = Видимость;
	
	Если ОписаниеКомандыЭДО.ТолькоВоВсехДействиях = Истина Тогда
		НовыйЭлемент.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;
	КонецЕсли;
	
КонецПроцедуры

// Аналог функции ПодключаемыеКомандыЭДОСлужебный.ДополнитьПараметрыУправленияВидимостьюНаФорме.
Процедура ДополнитьПараметрыУправленияВидимостьюНаФорме(Форма, ПараметрыУправленияВидимостьюЭДО, ИсточникКомандЭДО)
	
	Если ЗначениеЗаполнено(ПараметрыУправленияВидимостьюЭДО.КомандыСУсловиямиВидимости) Тогда
		ПараметрыУправленияВидимостьюЭДО.ЕстьУсловияВидимости = Истина;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПараметрыУправленияВидимостьюЭДО") Тогда
			Если ТипЗнч(Форма.ПараметрыУправленияВидимостьюЭДО) = Тип("Структура")
					И Форма.ПараметрыУправленияВидимостьюЭДО.КомандыСУсловиямиВидимости <> Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыУправленияВидимостьюЭДО.КомандыСУсловиямиВидимости,
					Форма.ПараметрыУправленияВидимостьюЭДО.КомандыСУсловиямиВидимости, Истина);
			КонецЕсли;
		Иначе
			Реквизит = Новый РеквизитФормы("ПараметрыУправленияВидимостьюЭДО", Новый ОписаниеТипов);
			ДобавляемыеРеквизиты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Реквизит);
			Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		КонецЕсли;
		
		Форма.ПараметрыУправленияВидимостьюЭДО = ПараметрыУправленияВидимостьюЭДО;
		
		Если ТипЗнч(ИсточникКомандЭДО) = Тип("ДинамическийСписок") Тогда
			РазместитьПараметрыУправленияВидимостьюВДинамическомСписке(ИсточникКомандЭДО,
				ПараметрыУправленияВидимостьюЭДО);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Аналог функции ПодключаемыеКомандыЭДОСлужебный.РазместитьПараметрыУправленияВидимостьюВДинамическомСписке.
Процедура РазместитьПараметрыУправленияВидимостьюВДинамическомСписке(ИсточникКомандЭДО, ПараметрыУправленияВидимостьюЭДО)
	
	ПолеСоЗначениямиВидимости = ПодключаемыеКомандыЭДОКлиентСервер.ИмяПоляЗначенийВидимостиКомандЭДО();
	ОбщегоНазначенияБЭД.ДобавитьСлужебноеПолеВДинамическийСписок(ИсточникКомандЭДО, ПолеСоЗначениямиВидимости);
	ИсточникКомандЭДО.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ПараметрыУправленияВидимостьюЭДО", 
		ПараметрыУправленияВидимостьюЭДО);
	
КонецПроцедуры

#КонецОбласти

#Область РегламентныеЗаданияСлужебный

// Выполняет загрузку информации о продажах товаров через торговую площадку. Используется регламентным заданием.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения.
//   ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//
// Возвращаемое значение:
//   Булево - признак доступности выполнения регламентного задания.
//
Функция МожноВыполнитьРегламентноеЗадание(УчетнаяЗапись, ВидМаркетплейса)

	Результат = Ложь;
	
	Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		Результат = ИнтеграцияСМаркетплейсомOzonСервер.МожноВыполнитьРегламентноеЗадание(УчетнаяЗапись, Ложь);  
	ИначеЕсли ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет") Тогда
		Результат = ИнтеграцияСЯндексМаркетСервер.МожноВыполнитьРегламентноеЗадание(УчетнаяЗапись);  
	КонецЕсли;
	
	Возврат Результат;

КонецФункции   

#КонецОбласти

#Область НоменклатураКонтрагентовСлужебный

// Конструктор параметров обработки данных номенклатуры партнера.
// 
// Возвращаемое значение:
//  Структура:
//   * РезультатЗапроса - Неопределено, РезультатЗапроса - элемент пакета запросов из см. ДанныеНоменклатурыПартнераДляПереноса. 
//   * Дубли - Соответствие из КлючИЗначение:
//      ** Ключ - Строка - идентификатор элемента или наименование группы.
//      ** Значение - СправочникСсылка.НоменклатураКонтрагентов - элемент справочника.
//   * Родители - Соответствие из КлючИЗначение:
//      ** Ключ - СправочникСсылка.НоменклатураКонтрагентов - группа номенклатуры прежнего владельца.
//      ** Значение - СправочникСсылка.НоменклатураКонтрагентов - группа номенклатуры нового владельца.
//   * ЗаполняемыеСвойства - Строка - имена заполняемых свойств через запятую.
//   * ЭтоОбработкаГрупп - Булево - признак создания групп номенклатуры контрагентов.
//   * КопироватьЭлементы - Булево - признак необходимости копировать элементы вместо изменения владельца.
//   * КоличествоОбработано - Число - количество обработанных элементов.
//   * ИмпортируемыеТоварыСтарые - Неопределено - если КопироватьЭлементы = Ложь.
//                               - РегистрСведенийНаборЗаписей.ИмпортируемыеТоварыТорговыхПлощадок
//   * ИмпортируемыеТоварыНовые - Неопределено - если КопироватьЭлементы = Ложь.
//                              - РегистрСведенийНаборЗаписей.ИмпортируемыеТоварыТорговыхПлощадок
//   * СтатусыПубликацииСтарые - Неопределено - если КопироватьЭлементы = Ложь.
//                             - РегистрСведенийНаборЗаписей.СтатусыПубликацииОбъектовМаркетплейсаOzon
//                             - РегистрСведенийНаборЗаписей.СтатусыПубликацииТоваровЯндексМаркет
//   * СтатусыПубликацииНовые - Неопределено - если КопироватьЭлементы = Ложь.
//                            - РегистрСведенийНаборЗаписей.СтатусыПубликацииОбъектовМаркетплейсаOzon
//                            - РегистрСведенийНаборЗаписей.СтатусыПубликацииТоваровЯндексМаркет
//   * ПрефиксИмпортируемыеТовары - Строка - префикс полей запроса для данных регистра импортируемых товаров.
//   * ПрефиксСтатусыПубликации - Строка - префикс полей запроса для данных регистра со статусами публикации.
//   * ИменаКолонокИмпортируемыеТовары - Массив из Строка - имена полей запроса для данных регистра импортируемых товаров.
//   * ИменаКолонокСтатусыПубликации - Массив из Строка - имена полей запроса для данных регистра со статусами публикации.
//
Функция НовыеПараметрыОбработкиДанныхНоменклатурыПартнераДляПереноса()
	
	Результат = Новый Структура;
	
	Результат.Вставить("РезультатЗапроса", Неопределено);
	Результат.Вставить("Дубли", Новый Соответствие);
	Результат.Вставить("Родители", Новый Соответствие);
	Результат.Вставить("ЗаполняемыеСвойства", "");
	Результат.Вставить("ЭтоОбработкаГрупп", Ложь);
	Результат.Вставить("КопироватьЭлементы", Ложь);
	Результат.Вставить("КоличествоОбработано", 0);
	
	Результат.Вставить("ИмпортируемыеТоварыСтарые", Неопределено);
	Результат.Вставить("ИмпортируемыеТоварыНовые", Неопределено);
	Результат.Вставить("СтатусыПубликацииСтарые", Неопределено);
	Результат.Вставить("СтатусыПубликацииНовые", Неопределено);
	
	Результат.Вставить("ПрефиксИмпортируемыеТовары", "");
	Результат.Вставить("ПрефиксСтатусыПубликации", "");
	Результат.Вставить("ИменаКолонокИмпортируемыеТовары", Новый Массив);
	Результат.Вставить("ИменаКолонокСтатусыПубликации", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеНоменклатурыПартнераДляПереноса(ИмяРегистра, ИмяИзмеренияУчетнаяЗапись, Параметры, ПараметрыОбработки)
	
	ТекстЗапросаПолейНоменклатураКонтрагентов = ТекстЗапросаПолейОбъектаМетаданных(
		"Справочник.НоменклатураКонтрагентов", "ПредыдущаяНоменклатураКонтрагентов");
	
	Тексты = Новый Массив; // Массив из Строка
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПредыдущаяНоменклатураКонтрагентов.Ссылка КАК ПредыдущаяСсылка,
		|	ПредыдущаяНоменклатураКонтрагентов.Родитель КАК РеквизитУпорядочивания,
		|	ЕСТЬNULL(НоваяНоменклатураКонтрагентов.Ссылка, ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка)) КАК НоваяСсылка,
		|	ЛОЖЬ КАК ИмпортируемыеТоварыЕстьДанные,
		|	ЛОЖЬ КАК СтатусыПубликацииЕстьДанные,
		|	&ТекстЗапросаПоляСправочника КАК Поля
		|ИЗ
		|	Справочник.НоменклатураКонтрагентов КАК ПредыдущаяНоменклатураКонтрагентов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоваяНоменклатураКонтрагентов
		|		ПО (НоваяНоменклатураКонтрагентов.Владелец = &НовыйВладелец)
		|			И (НоваяНоменклатураКонтрагентов.Наименование = ПредыдущаяНоменклатураКонтрагентов.Наименование)
		|			И (НоваяНоменклатураКонтрагентов.ЭтоГруппа)
		|ГДЕ
		|	ПредыдущаяНоменклатураКонтрагентов.Владелец = &ТекущийВладелец
		|	И ПредыдущаяНоменклатураКонтрагентов.ЭтоГруппа";
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ТекстЗапросаПоляСправочника КАК Поля",
		ТекстЗапросаПолейНоменклатураКонтрагентов);
		
	Если Не ПараметрыОбработки.КопироватьЭлементы Тогда
		
		ТекстЗапроса = ТекстЗапроса + "
			|	И НоваяНоменклатураКонтрагентов.Ссылка ЕСТЬ NULL";
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	РеквизитУпорядочивания";
	
	Тексты.Добавить(ТекстЗапроса);
	
	Если ПараметрыОбработки.КопироватьЭлементы Тогда
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ПредыдущаяНоменклатураКонтрагентов.Ссылка КАК ПредыдущаяСсылка,
			|	ЕСТЬNULL(НоваяНоменклатураКонтрагентов.Ссылка, ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка)) КАК НоваяСсылка,
			|	ИмпортируемыеТовары.НоменклатураПартнера ЕСТЬ НЕ NULL  КАК ИмпортируемыеТоварыЕстьДанные,
			|	СтатусыПубликации.НоменклатураПартнера ЕСТЬ НЕ NULL  КАК СтатусыПубликацииЕстьДанные,
			|	&ТекстЗапросаПоляСправочника КАК Поля,
			|	&ТекстЗапросаПоляРегистраИмпортируемыеТовары КАК Поля1,
			|	&ТекстЗапросаПоляРегистраСтатусыПубликации КАК Поля2
			|ИЗ
			|	Справочник.НоменклатураКонтрагентов КАК ПредыдущаяНоменклатураКонтрагентов
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоваяНоменклатураКонтрагентов
			|		ПО (НоваяНоменклатураКонтрагентов.Владелец = &НовыйВладелец)
			|			И (НоваяНоменклатураКонтрагентов.Идентификатор = ПредыдущаяНоменклатураКонтрагентов.Идентификатор)
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИмпортируемыеТоварыТорговыхПлощадок КАК ИмпортируемыеТовары
			|		ПО (ИмпортируемыеТовары.НоменклатураПартнера = ПредыдущаяНоменклатураКонтрагентов.Ссылка)
			|			И (ИмпортируемыеТовары.УчетнаяЗапись = &УчетнаяЗапись)
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТекстЗапросаПолноеИмяРегистра КАК СтатусыПубликации
			|		ПО (СтатусыПубликации.НоменклатураПартнера = ПредыдущаяНоменклатураКонтрагентов.Ссылка)
			|			И (&ТекстЗапросаИмяПоляУчетнаяЗапись = &УчетнаяЗапись)
			|ГДЕ
			|	ПредыдущаяНоменклатураКонтрагентов.Владелец = &ТекущийВладелец
			|	И НЕ ПредыдущаяНоменклатураКонтрагентов.ЭтоГруппа";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТекстЗапросаПолноеИмяРегистра", ИмяРегистра);
		
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса,
			"&ТекстЗапросаИмяПоляУчетнаяЗапись",
			СтрШаблон("СтатусыПубликации.%1", ИмяИзмеренияУчетнаяЗапись));
		
		ТекстЗапросаПоляРегистраИмпортируемыеТовары = ТекстЗапросаПолейОбъектаМетаданных(
			"РегистрСведений.ИмпортируемыеТоварыТорговыхПлощадок",
			"ИмпортируемыеТовары", ,
			ПараметрыОбработки.ПрефиксИмпортируемыеТовары);
		
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса,
			"&ТекстЗапросаПоляРегистраИмпортируемыеТовары КАК Поля1",
			ТекстЗапросаПоляРегистраИмпортируемыеТовары);
		
		ТекстЗапросаПоляРегистраСтатусыПубликации = ТекстЗапросаПолейОбъектаМетаданных(
			ИмяРегистра,
			"СтатусыПубликации", ,
			ПараметрыОбработки.ПрефиксСтатусыПубликации);
		
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса,
			"&ТекстЗапросаПоляРегистраСтатусыПубликации КАК Поля2",
			ТекстЗапросаПоляРегистраСтатусыПубликации);
		
	Иначе
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ПредыдущаяНоменклатураКонтрагентов.Ссылка КАК ПредыдущаяСсылка,
			|	ЕСТЬNULL(НоваяНоменклатураКонтрагентов.Ссылка, ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка)) КАК НоваяСсылка,
			|	ЛОЖЬ КАК ИмпортируемыеТоварыЕстьДанные,
			|	ЛОЖЬ КАК СтатусыПубликацииЕстьДанные,
			|	&ТекстЗапросаПоляСправочника КАК Поля
			|ИЗ
			|	Справочник.НоменклатураКонтрагентов КАК ПредыдущаяНоменклатураКонтрагентов
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоваяНоменклатураКонтрагентов
			|		ПО (НоваяНоменклатураКонтрагентов.Владелец = &НовыйВладелец)
			|			И (НоваяНоменклатураКонтрагентов.Идентификатор = ПредыдущаяНоменклатураКонтрагентов.Идентификатор)
			|ГДЕ
			|	ПредыдущаяНоменклатураКонтрагентов.Владелец = &ТекущийВладелец
			|	И НЕ ПредыдущаяНоменклатураКонтрагентов.ЭтоГруппа
			|	И НоваяНоменклатураКонтрагентов.Ссылка ЕСТЬ NULL";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ТекстЗапросаПоляСправочника КАК Поля",
		ТекстЗапросаПолейНоменклатураКонтрагентов);
	
	ТекстЗапроса = ТекстЗапроса + "
			|УПОРЯДОЧИТЬ ПО
			|	ПредыдущаяСсылка";
	
	Тексты.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",   Параметры.УчетнаяЗапись);
	Запрос.УстановитьПараметр("ТекущийВладелец", Параметры.ТекущийВладелец);
	Запрос.УстановитьПараметр("НовыйВладелец",   Параметры.НовыйВладелец);
	
	УстановитьПривилегированныйРежим(Истина);
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ПакетЗапросов;
	
КонецФункции

Функция ИменаКолонокРезультатаЗапросаПоПрефиксу(РезультатЗапроса, Префикс)
	
	Результат = Новый Массив; // Массив из Строка
	
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		
		Если Не СтрНачинаетсяС(Колонка.Имя, Префикс) Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяКолонки = Сред(Колонка.Имя, СтрДлина(Префикс) + 1);
		
		Если ИмяКолонки = "ЕстьДанные" Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(ИмяКолонки);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ИменаРеквизитовСправочникаНоменклатураКонтрагентов(ТолькоРеквизитыГруппы)
	
	ИменаРеквизитов = Новый Массив; // Массив из Строка
	ИменаРеквизитов.Добавить("Наименование");
	ИменаРеквизитов.Добавить("ПометкаУдаления");
	
	ВариантыИспользования = Новый Массив; // Массив из ИспользованиеРеквизита
	ВариантыИспользования.Добавить(Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппыИЭлемента);
	Если ТолькоРеквизитыГруппы Тогда
		ВариантыИспользования.Добавить(Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы);
	Иначе
		ВариантыИспользования.Добавить(Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента);
	КонецЕсли;
	
	Для Каждого Реквизит Из Метаданные.Справочники.НоменклатураКонтрагентов.Реквизиты Цикл
		Если ВариантыИспользования.Найти(Реквизит.Использование) <> Неопределено Тогда
			ИменаРеквизитов.Добавить(Реквизит.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Результат = СтрСоединить(ИменаРеквизитов, ",");
	Возврат Результат;
	
КонецФункции

Процедура ПеренестиНоменклатуруПартнераСлужебный(ВидТорговойПлощадки, НовыйВладелец, Ошибка, ПараметрыОбработки)
	
	ВыборкаДанных = ВыборкаДанныхРезультатаЗапросаНоменклатураПартнераДляПереноса(
		ПараметрыОбработки.РезультатЗапроса);
	
	Пока ВыборкаДанных.СледующийПоЗначениюПоля("ПредыдущаяСсылка") Цикл
		
		Если ПараметрыОбработки.ЭтоОбработкаГрупп Тогда
			НоменклатураКонтрагентовСсылка = ПараметрыОбработки.Дубли.Получить(ВыборкаДанных.Наименование);
		Иначе
			НоменклатураКонтрагентовСсылка = ПараметрыОбработки.Дубли.Получить(ВыборкаДанных.Идентификатор);
		КонецЕсли;
		
		Если ПараметрыОбработки.КопироватьЭлементы И ЗначениеЗаполнено(ВыборкаДанных.НоваяСсылка) Тогда
			НоменклатураКонтрагентовСсылка = ВыборкаДанных.НоваяСсылка; // СправочникСсылка.НоменклатураКонтрагентов
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(НоменклатураКонтрагентовСсылка) Тогда
			
			Если ПараметрыОбработки.КопироватьЭлементы Тогда
				
				Если ПараметрыОбработки.ЭтоОбработкаГрупп Тогда
					НоменклатураКонтрагентовОбъект = Справочники.НоменклатураКонтрагентов.СоздатьГруппу();
				Иначе
					НоменклатураКонтрагентовОбъект = Справочники.НоменклатураКонтрагентов.СоздатьЭлемент();
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(
					НоменклатураКонтрагентовОбъект, ВыборкаДанных, ПараметрыОбработки.ЗаполняемыеСвойства);
				НоменклатураКонтрагентовОбъект.Родитель = ПараметрыОбработки.Родители.Получить(
					ВыборкаДанных.Родитель);
				
			Иначе
				НоменклатураКонтрагентовОбъект = ВыборкаДанных.ПредыдущаяСсылка.ПолучитьОбъект();
				НоменклатураКонтрагентовОбъект.ОбменДанными.Загрузка = Истина;
			КонецЕсли;
			
			НоменклатураКонтрагентовОбъект.Владелец             = НовыйВладелец;
			НоменклатураКонтрагентовОбъект.ВладелецНоменклатуры = НовыйВладелец;
			
			Попытка
				
				НоменклатураКонтрагентовОбъект.Записать();
				НоменклатураКонтрагентовСсылка = НоменклатураКонтрагентовОбъект.Ссылка;
				
				Если Не ПараметрыОбработки.КопироватьЭлементы Тогда
					// Не выполнять кэширование данных
				ИначеЕсли ПараметрыОбработки.ЭтоОбработкаГрупп Тогда
					ПараметрыОбработки.Дубли.Вставить(ВыборкаДанных.Наименование, НоменклатураКонтрагентовСсылка);
					ПараметрыОбработки.Родители.Вставить(ВыборкаДанных.ПредыдущаяСсылка, НоменклатураКонтрагентовСсылка);
				Иначе
					ПараметрыОбработки.Дубли.Вставить(ВыборкаДанных.Идентификатор, НоменклатураКонтрагентовСсылка);
				КонецЕсли;
				
			Исключение
				
				Если ПараметрыОбработки.КопироватьЭлементы Тогда
					ШаблонОшибки = НСтр("ru = 'При копировании позиции товарного каталога %1 возникли ошибки.';
										|en = 'Errors occurred when copying the %1 goods directory items.'",
						ОбщегоНазначения.КодОсновногоЯзыка());
				Иначе
					ШаблонОшибки = НСтр("ru = 'При переносе позиции товарного каталога %1 возникли ошибки.';
										|en = 'Errors occurred when transferring the %1 goods directory items.'",
						ОбщегоНазначения.КодОсновногоЯзыка());
				КонецЕсли;
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонОшибки,
					Строка(ВыборкаДанных.ПредыдущаяСсылка));
				
				Ошибка.КодОшибки = "НоменклатураКонтрагентов_Запись";
				Ошибка.Детализация.Добавить(ТекстОшибки);
				
				ЗаписьЖурналаРегистрации(
					СобытиеЖурналаРегистрации(ВидТорговойПлощадки),
					УровеньЖурналаРегистрации.Ошибка, , ,
					ТекстОшибки);
				
			КонецПопытки;
			
		ИначеЕсли ПараметрыОбработки.КопироватьЭлементы И ПараметрыОбработки.ЭтоОбработкаГрупп Тогда
			ПараметрыОбработки.Родители.Вставить(ВыборкаДанных.ПредыдущаяСсылка, НоменклатураКонтрагентовСсылка);
		КонецЕсли;
		
		Если Не ПараметрыОбработки.КопироватьЭлементы
				Или ПараметрыОбработки.ЭтоОбработкаГрупп
				Или Не ЗначениеЗаполнено(НоменклатураКонтрагентовСсылка) Тогда
			Продолжить;
		КонецЕсли;
		
		Пока ВыборкаДанных.Следующий() Цикл
			
			Если ВыборкаДанных.ИмпортируемыеТоварыЕстьДанные Тогда
				
				СтараяСтрокаИмпортируемыеТовары = ПараметрыОбработки.ИмпортируемыеТоварыСтарые.Добавить();
				НоваяСтрокаИмпортируемыеТовары = ПараметрыОбработки.ИмпортируемыеТоварыНовые.Добавить();
				
				Для Каждого ИмяКолонки Из ПараметрыОбработки.ИменаКолонокИмпортируемыеТовары Цикл
					
					Значение = ВыборкаДанных[ПараметрыОбработки.ПрефиксИмпортируемыеТовары + ИмяКолонки];
					
					СтараяСтрокаИмпортируемыеТовары[ИмяКолонки] = Значение;
					НоваяСтрокаИмпортируемыеТовары[ИмяКолонки] = Значение;
					
				КонецЦикла;
				
				НоваяСтрокаИмпортируемыеТовары.НоменклатураПартнера = НоменклатураКонтрагентовСсылка;
				
			КонецЕсли;
			
			Если ВыборкаДанных.СтатусыПубликацииЕстьДанные Тогда
				
				СтараяСтрокаСтатусыПубликации = ПараметрыОбработки.СтатусыПубликацииСтарые.Добавить();
				НоваяСтрокаСтатусыПубликации = ПараметрыОбработки.СтатусыПубликацииНовые.Добавить();
				
				Для Каждого ИмяКолонки Из ПараметрыОбработки.ИменаКолонокСтатусыПубликации Цикл
					
					Значение = ВыборкаДанных[ПараметрыОбработки.ПрефиксСтатусыПубликации + ИмяКолонки]; // Произвольный
					
					СтараяСтрокаСтатусыПубликации[ИмяКолонки] = Значение;
					НоваяСтрокаСтатусыПубликации[ИмяКолонки] = Значение;
					
				КонецЦикла;
				
				НоваяСтрокаСтатусыПубликации.НоменклатураПартнера = НоменклатураКонтрагентовСсылка;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПараметрыОбработки.КоличествоОбработано = ПараметрыОбработки.КоличествоОбработано + ВыборкаДанных.Количество();
	
КонецПроцедуры

// Параметры:
//  РезультатЗапроса - РезультатЗапроса - элемент пакета запросов из см. ДанныеНоменклатурыПартнераДляПереноса.
//
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса - содержит все поля справочника НоменклатураКонтрагентов, а также дополнительно:
//      * ПредыдущаяСсылка - СправочникСсылка.НоменклатураКонтрагентов - ссылка номенклатуры предыдущего владельца.
//      * НоваяСсылка - СправочникСсылка.НоменклатураКонтрагентов - ссылка номенклатуры нового владельца.
//      * Родитель - СправочникСсылка.НоменклатураКонтрагентов - ссылка на родителя номенклатуры предыдущего владельца.
//      * Идентификатор - Строка - идентификатор номенклатуры контрагента.
//      * Наименование - Строка - наименование номенклатуры контрагента.
//      * ИмпортируемыеТоварыЕстьДанные - Булево - признак наличия данных в регистре импортируемых товаров.
//      * СтатусыПубликацииЕстьДанные - Булево - признак наличия данных в регистре со статусами публикации.
//
Функция ВыборкаДанныхРезультатаЗапросаНоменклатураПартнераДляПереноса(РезультатЗапроса)
	Возврат РезультатЗапроса.Выбрать();
КонецФункции

// Возвращаемое значение:
//  Структура:
//   * НоменклатураПартнера - СправочникСсылка.НоменклатураКонтрагентов - Будет заполнена, если Отказ = Ложь
//   * Отказ - Булево - 
//   * ТекстОшибки - Строка - 
//
Функция НовыйРезультатСозданияОбновленияНоменклатурыПартнера()
	
	Результат = Новый Структура;
	
	Результат.Вставить("НоменклатураПартнера", Справочники.НоменклатураКонтрагентов.ПустаяСсылка());
	Результат.Вставить("Отказ", Ложь);
	Результат.Вставить("ТекстОшибки", "");
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеНоменклатурыПартнераЗаполнены(ДанныеНоменклатуры)
	
	Отказ = Ложь;
	
	Если ПустаяСтрока(ДанныеНоменклатуры.НаименованиеТовара)
			И ПустаяСтрока(ДанныеНоменклатуры.ПредставлениеТовара)
			И ПустаяСтрока(ДанныеНоменклатуры.ИдентификаторТовара)
			И ПустаяСтрока(ДанныеНоменклатуры.ИдентификаторКакАртикул)
			И ПустаяСтрока(ДанныеНоменклатуры.ИдентификаторМаркетплейса) Тогда
			
		Отказ = Истина;
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

Функция ЕстьИзмененияРеквизитовНоменклатурыКонтрагента(ДанныеНоменклатуры)
	
	Результат = Истина;
	
	Если Не ЗначениеЗаполнено(ДанныеНоменклатуры.НоменклатураПартнера) Тогда
		Возврат Результат;
	КонецЕсли;
	
	РеквизитыДляКонтроля = ИменаПолейЗаполненияНоменклатурыКонтрагентаПоДаннымНоменклатуры(Истина);
	
	Запрос = Новый Запрос;
	
	ТекстыОтбора = Новый Массив; // Массив из Строка
	
	Для Каждого КлючЗначение Из РеквизитыДляКонтроля Цикл
		
		ИмяРеквизита = КлючЗначение.Ключ;
		ИмяКлючаДанных = КлючЗначение.Значение; // Строка
		
		ЗначениеРеквизита = ДанныеНоменклатуры[ИмяКлючаДанных]; // Произвольный
		
		Если Не ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстОтбора = СтрШаблон("И Таблица.%1 = &%2", ИмяРеквизита, ИмяКлючаДанных);
		ТекстыОтбора.Добавить(ТекстОтбора);
		
		Запрос.УстановитьПараметр(ИмяКлючаДанных, ЗначениеРеквизита);
		
	КонецЦикла;
	
	ТекстЗапросаОтборРеквизитов = СтрСоединить(ТекстыОтбора, Символы.ПС);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИСТИНА КАК Результат
		|ИЗ
		|	Справочник.НоменклатураКонтрагентов КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка = &НоменклатураПартнера
		|	И &ТекстЗапросаОтборРеквизитов
		|	И НЕ Таблица.ПометкаУдаления";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "И &ТекстЗапросаОтборРеквизитов", ТекстЗапросаОтборРеквизитов);
	
	Запрос.УстановитьПараметр("НоменклатураПартнера", ДанныеНоменклатуры.НоменклатураПартнера);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат = РезультатЗапроса.Пустой();
	
	Возврат Результат;
	
КонецФункции

Функция ИменаПолейЗаполненияНоменклатурыКонтрагентаПоДаннымНоменклатуры(ДляКонтроля = Ложь)

	ИменаПолей = Новый Структура;
	
	ИменаПолей.Вставить("Артикул", "ИдентификаторКакАртикул");
	
	ИменаПолей.Вставить("Наименование",             "ПредставлениеТовара");
	ИменаПолей.Вставить("НаименованиеПолное",       "ПредставлениеТовара");
	ИменаПолей.Вставить("НаименованиеНоменклатуры", "НаименованиеТовара");
	ИменаПолей.Вставить("Идентификатор",            "ИдентификаторТовара");
	ИменаПолей.Вставить("КодНоменклатуры",          "ИдентификаторМаркетплейса");
	
	ИменаПолей.Вставить("ИдентификаторНоменклатуры",   "ИдентификаторТовара");
	ИменаПолей.Вставить("ИдентификаторХарактеристики", "ИдентификаторТовара");
	ИменаПолей.Вставить("ИдентификаторУпаковки",       "ИдентификаторТовара");
	
	ИменаПолей.Вставить("Недействителен", "Недействителен");
	
	Если ДляКонтроля Тогда
		
		ИменаПолей.Вставить("Номенклатура",   "Номенклатура");
		ИменаПолей.Вставить("Характеристика", "Характеристика");
		ИменаПолей.Вставить("Упаковка",       "Упаковка");
		ИменаПолей.Вставить("Штрихкод",       "Штрихкод");
		
	Иначе
		
		ИменаПолей.Вставить("ШтрихкодКомбинации",     "Штрихкод");
		ИменаПолей.Вставить("ШтрихкодыНоменклатуры",  "ШтрихкодыНоменклатуры");
		ИменаПолей.Вставить("СтранаПроисхожденияКод", "СтранаПроисхожденияКод");
		ИменаПолей.Вставить("КодТНВЭД",               "КодТНВЭД");
		
		ИменаПолей.Вставить("СтавкаНДС",                         "СтавкаНДС");
		ИменаПолей.Вставить("НаименованиеУпаковки",              "НаименованиеУпаковки");
		ИменаПолей.Вставить("ЕдиницаИзмерения",                  "ЕдиницаИзмерения");
		ИменаПолей.Вставить("ЕдиницаИзмеренияКод",               "ЕдиницаИзмеренияКод");
		ИменаПолей.Вставить("КоличествоБазовойЕдиницыИзмерения", "КоэффициентУпаковки");
		
	КонецЕсли;
	
	Возврат ИменаПолей;
	
КонецФункции

Процедура ЗаполнитьНоменклатуруКонтрагентаПоДаннымНоменклатуры(НоменклатураКонтрагента, ДанныеНоменклатуры)
	
	НоменклатураКонтрагента.НоменклатураКонтрагента = ДанныеНоменклатуры.НоменклатураПартнера;
	
	ИменаПолей = ИменаПолейЗаполненияНоменклатурыКонтрагентаПоДаннымНоменклатуры();
	
	Для Каждого КлючЗначение Из ИменаПолей Цикл
		
		ИмяРеквизита = КлючЗначение.Ключ;
		ИмяКлючаДанных = КлючЗначение.Значение;
		
		НоменклатураКонтрагента[ИмяРеквизита] = ДанныеНоменклатуры[ИмяКлючаДанных];
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПараметрыОбновленияДанныхСтатусовПубликацииПоНоменклатуреПартнера(ВидМаркетплейса)
	
	ПараметрыОбновления = Новый Структура;
	
	ПараметрыОбновления.Вставить("ПолноеИмяРегистра",    "");
	ПараметрыОбновления.Вставить("ИмяПоляУчетнаяЗапись", "");
	ПараметрыОбновления.Вставить("ДополнительныеОтборы", Новый Структура);
	
	ДополнительныеПоля = Новый Массив; // Массив из Строка
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		
		ПараметрыОбновления.ПолноеИмяРегистра = "РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет";
		ПараметрыОбновления.ИмяПоляУчетнаяЗапись = "УчетнаяЗапись";
		
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		
		ПараметрыОбновления.ПолноеИмяРегистра = "РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon";
		ПараметрыОбновления.ИмяПоляУчетнаяЗапись = "УчетнаяЗаписьМаркетплейса";
		
		ПараметрыОбновления.ДополнительныеОтборы.Вставить(
			"ВидОбъектаМаркетплейса", Перечисления.ВидыОбъектовМаркетплейсов.Товар);
		
		ПараметрыОбновления.ДополнительныеОтборы.Вставить("ОбъектПубликации", Неопределено);
		
		ДополнительныеПоля.Добавить("ИдентификаторПубликации");
		ДополнительныеПоля.Добавить("ИдентификаторОбъектаМаркетплейса");
		ДополнительныеПоля.Добавить("ИдентификаторКатегорииМаркетплейса");
		ДополнительныеПоля.Добавить("ДанныеОбъекта");
		
	Иначе
		
		ВызватьИсключение(
			НСтр("ru = 'По переданной учетной записи обновление данных недоступно';
				|en = 'Data update is not available for the transferred account'"),
			КатегорияОшибки.ОшибкаКонфигурации);
		
	КонецЕсли;
	
	ПараметрыОбновления.Вставить("ДополнительныеПоля", ДополнительныеПоля);
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Параметры:
//  УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//  ПараметрыОбновления - см. ПараметрыОбновленияДанныхСтатусовПубликацииПоНоменклатуреПартнера
//  МассивНоменклатурыПартнера - Массив из СправочникСсылка.НоменклатураКонтрагентов - Массив номенклатуры партнера.
//  ВернутьВыборку - Булево - признак возврата выборки (Истина) или результата запроса (Ложь).
//  ВернутьВсеПоляРегистра - Булево - признак возврата всех полей регистра.
//
// Возвращаемое значение:
//   - Неопределено - данные не найдены.
//   - РезультатЗапроса - если ВернутьВыборку = Истина.
//   - ВыборкаИзРезультатаЗапроса:
//      * УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//      * Номенклатура - СправочникСсылка.Номенклатура - номенклатура.
//      * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика.
//      * Упаковка - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка.
//      * НоменклатураПартнера - СправочникСсылка.НоменклатураКонтрагентов - номенклатура партнера.
//      * НоменклатураНовая - СправочникСсылка.Номенклатура - новая номенклатура.
//      * ХарактеристикаНовая - СправочникСсылка.ХарактеристикиНоменклатуры - новая характеристика.
//      * УпаковкаНовая - СправочникСсылка.УпаковкиЕдиницыИзмерения - новая упаковка.
//
Функция ВыборкаДанныхДляОбновленияСтатусовПубликацииПоНоменклатуреПартнера(УчетнаяЗаписьМаркетплейса,
			ПараметрыОбновления, МассивНоменклатурыПартнера, ВернутьВыборку = Истина, ВернутьВсеПоляРегистра = Ложь)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	&ТекстЗапросаУчетнаяЗапись КАК УчетнаяЗаписьМаркетплейса,
		|	&ТекстЗапросаДополнительныеПоля КАК ДополнительныеПоля,
		|	СтатусыПубликации.Номенклатура КАК Номенклатура,
		|	СтатусыПубликации.Характеристика КАК Характеристика,
		|	СтатусыПубликации.Упаковка КАК Упаковка,
		|	СтатусыПубликации.НоменклатураПартнера КАК НоменклатураПартнера,
		|	&ВсеПоляРегистра КАК ВсеПоляРегистра,
		|	ДанныеНоменклатурыКонтрагента.Номенклатура КАК НоменклатураНовая,
		|	ДанныеНоменклатурыКонтрагента.Характеристика КАК ХарактеристикаНовая,
		|	ВЫБОР
		|		КОГДА ДанныеНоменклатурыКонтрагента.Упаковка = СпрНоменклатура.ЕдиницаИзмерения
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|		ИНАЧЕ ДанныеНоменклатурыКонтрагента.Упаковка
		|	КОНЕЦ КАК УпаковкаНовая
		|ИЗ
		|	&ТекстЗапросаПолноеИмяРегистра КАК СтатусыПубликации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК ДанныеНоменклатурыКонтрагента
		|		ПО (СтатусыПубликации.НоменклатураПартнера = ДанныеНоменклатурыКонтрагента.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО (ДанныеНоменклатурыКонтрагента.Номенклатура = СпрНоменклатура.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК ПроверкаИзмененийДанных
		|		ПО (СтатусыПубликации.НоменклатураПартнера = ПроверкаИзмененийДанных.Ссылка)
		|			И (СтатусыПубликации.Номенклатура = ПроверкаИзмененийДанных.Номенклатура)
		|			И (СтатусыПубликации.Характеристика = ПроверкаИзмененийДанных.Характеристика)
		|			И (СтатусыПубликации.Упаковка = ВЫБОР
		|				КОГДА ПроверкаИзмененийДанных.Упаковка = СпрНоменклатура.ЕдиницаИзмерения
		|					ТОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|				ИНАЧЕ ПроверкаИзмененийДанных.Упаковка
		|			КОНЕЦ)
		|ГДЕ
		|	&ТекстЗапросаУчетнаяЗапись = &УчетнаяЗаписьМаркетплейса
		|	И &ТекстЗапросаДополнительныйОтбор
		|	И &ТекстЗапросаОтборПоНоменклатуреПартнера
		|	И ПроверкаИзмененийДанных.Ссылка ЕСТЬ NULL";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаПолноеИмяРегистра", ПараметрыОбновления.ПолноеИмяРегистра);
	
	ТекстЗапросаУчетнаяЗапись = СтрШаблон("СтатусыПубликации.%1", ПараметрыОбновления.ИмяПоляУчетнаяЗапись);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаУчетнаяЗапись", ТекстЗапросаУчетнаяЗапись);
	
	ТекстЗапросаДополнительныйОтбор = "";
	
	ДополнительныеПоля = Новый Массив; // Массив из Строка
	
	Для Каждого КлючЗначение Из ПараметрыОбновления.ДополнительныеОтборы Цикл
		ТекстЗапросаДополнительныйОтбор = СтрШаблон(
			"%1
			|	И СтатусыПубликации.%2 = &%2",
			ТекстЗапросаДополнительныйОтбор,
			КлючЗначение.Ключ);
		
		Если ВернутьВсеПоляРегистра Тогда
			ДополнительныеПоля.Добавить(СтрШаблон("СтатусыПубликации.%1 КАК %1,", КлючЗначение.Ключ));
		КонецЕсли;
	КонецЦикла;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ТекстЗапросаДополнительныйОтбор", ТекстЗапросаДополнительныйОтбор);
	
	Для Каждого ИмяПоля Из ПараметрыОбновления.ДополнительныеПоля Цикл
		ДополнительныеПоля.Добавить(СтрШаблон("СтатусыПубликации.%1 КАК %1,", ИмяПоля));
	КонецЦикла;
	
	ТекстЗапросаДополнительныеПоля = СтрСоединить(ДополнительныеПоля, Символы.ПС + Символы.Таб);
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса, "&ТекстЗапросаДополнительныеПоля КАК ДополнительныеПоля,", ТекстЗапросаДополнительныеПоля);
	
	Если МассивНоменклатурыПартнера <> Неопределено Тогда
		ТекстЗапросаОтборПоНоменклатуреПартнера = "СтатусыПубликации.НоменклатураПартнера В (&НоменклатураПартнера)";
	Иначе
		ТекстЗапросаОтборПоНоменклатуреПартнера = "ИСТИНА";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса, "&ТекстЗапросаОтборПоНоменклатуреПартнера", ТекстЗапросаОтборПоНоменклатуреПартнера);
	
	Если ВернутьВсеПоляРегистра Тогда
		ИсключаемыеПоляРегистра = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыОбновления.ДополнительныеПоля);
		ИсключаемыеПоляРегистра.Добавить(ПараметрыОбновления.ИмяПоляУчетнаяЗапись);
		ИсключаемыеПоляРегистра.Добавить("Номенклатура");
		ИсключаемыеПоляРегистра.Добавить("Характеристика");
		ИсключаемыеПоляРегистра.Добавить("Упаковка");
		ИсключаемыеПоляРегистра.Добавить("НоменклатураПартнера");
		
		ПоляРегистра = Новый Массив;
		
		МетаданныеРегистра = ОбщегоНазначения.ОбъектМетаданныхПоПолномуИмени(ПараметрыОбновления.ПолноеИмяРегистра);
		
		Для Каждого Ресурс Из МетаданныеРегистра.Ресурсы Цикл // ОбъектМетаданныхРесурс
			Если ИсключаемыеПоляРегистра.Найти(Ресурс.Имя) = Неопределено Тогда
				ПоляРегистра.Добавить(СтрШаблон("СтатусыПубликации.%1 КАК %1,", Ресурс.Имя));
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Реквизит Из МетаданныеРегистра.Реквизиты Цикл // ОбъектМетаданныхРеквизит
			Если ИсключаемыеПоляРегистра.Найти(Реквизит.Имя) = Неопределено Тогда
				ПоляРегистра.Добавить(СтрШаблон("СтатусыПубликации.%1 КАК %1,", Реквизит.Имя));
			КонецЕсли;
		КонецЦикла;
		
		ТекстЗаменыПолей = СтрСоединить(ПоляРегистра, Символы.ПС + Символы.Таб);
	Иначе
		ТекстЗаменыПолей = "";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВсеПоляРегистра КАК ВсеПоляРегистра,", ТекстЗаменыПолей);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", УчетнаяЗаписьМаркетплейса);
	Запрос.УстановитьПараметр("НоменклатураПартнера", МассивНоменклатурыПартнера);
	
	Для Каждого КлючЗначение Из ПараметрыОбновления.ДополнительныеОтборы Цикл
		Запрос.УстановитьПараметр(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ВернутьВыборку Тогда
		Возврат РезультатЗапроса.Выбрать();
	Иначе
		Возврат РезультатЗапроса;
	КонецЕсли;
	
КонецФункции

// Параметры:
//   МенеджерРегистра - РегистрСведенийМенеджерИмяРегистраСведений - менеджер регистра.
//   УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись сервиса.
//   ПараметрыОбновления - см. ПараметрыОбновленияДанныхСтатусовПубликацииПоНоменклатуреПартнера
//   Выборка - см. ВыборкаДанныхДляОбновленияСтатусовПубликацииПоНоменклатуреПартнера
//   Постфикс - Строка - Постфикс.
// 
// Возвращаемое значение:
//   РегистрСведенийНаборЗаписей - набор записей по менеджеру регистра.
//
Функция НовыйНаборЗаписейДляОбновленияСтатусовПубликацииСОтборами(МенеджерРегистра, УчетнаяЗаписьМаркетплейса,
			ПараметрыОбновления, Выборка = Неопределено, Постфикс = "")
	
	НаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();
	
	НаборЗаписей.Отбор[ПараметрыОбновления.ИмяПоляУчетнаяЗапись].Установить(УчетнаяЗаписьМаркетплейса);
	
	Для Каждого КлючЗначение Из ПараметрыОбновления.ДополнительныеОтборы Цикл
		НаборЗаписей.Отбор[КлючЗначение.Ключ].Установить(КлючЗначение.Значение);
	КонецЦикла;
	
	Если Выборка <> Неопределено Тогда
		НаборЗаписей.Отбор.Номенклатура.Установить(Выборка["Номенклатура" + Постфикс]);
		НаборЗаписей.Отбор.Характеристика.Установить(Выборка["Характеристика" + Постфикс]);
		НаборЗаписей.Отбор.Упаковка.Установить(Выборка["Упаковка" + Постфикс]);
		
		НаборЗаписей.Отбор.НоменклатураПартнера.Установить(Выборка.НоменклатураПартнера);
	КонецЕсли;
	
	Возврат НаборЗаписей;
	
КонецФункции

// Параметры:
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
//  ИсточникКатегории - ПеречислениеСсылка.ИсточникиКатегорийДляМаркетплейса - Источник категории
//  Выборка - см. ВыборкаДанныхДляОбновленияСтатусовПубликацииПоНоменклатуреПартнера
// 
// Возвращаемое значение:
//  Произвольный
//
Функция ДополнительныеДанныеДляОбновленияСтатусовПубликацииПоНоменклатуреПартнера(ВидМаркетплейса,
			ИсточникКатегории, Выборка)
	
	ДополнительныеДанные = Неопределено;
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		
		ДополнительныеДанные = 
			ИнтеграцияСМаркетплейсомOzonСервер.ДанныеДляОбновленияСтатусовПубликацииПоНоменклатуреПартнера(
				ИсточникКатегории, Выборка);
		
	КонецЕсли;
	
	Возврат ДополнительныеДанные;
	
КонецФункции

// Параметры:
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
//  НовыеЗаписи - см. НовыйНаборЗаписейДляОбновленияСтатусовПубликацииСОтборами
//  ДополнительныеДанные - см. ДополнительныеДанныеДляОбновленияСтатусовПубликацииПоНоменклатуреПартнера
//
Процедура ДополнитьНовыеЗаписиСтатусовПубликацииДополнительнымиДанными(ВидМаркетплейса, НовыеЗаписи,
			ДополнительныеДанные)
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		
		ИнтеграцияСМаркетплейсомOzonСервер.ДополнитьЗаписиСтатусовПубликацииДаннымиПубликации(
			НовыеЗаписи, ДополнительныеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ИмпортТоваровИзСервисаСлужебный

Функция ЕстьВключенноеРегламентноеЗаданиеРучногоИмпортаТоварногоКаталога(МетаданныеЗадания, КлючЗадания, ДанныеЗапуска)
	
	Результат = Ложь;
	
	Отбор = Новый Структура;
	
	Отбор.Вставить("Использование", Истина);
	Отбор.Вставить("Метаданные",    МетаданныеЗадания);
	Отбор.Вставить("Ключ",          КлючЗадания);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатПоиска = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат = (РезультатПоиска.Количество() > 0);
	
	Если Результат Тогда
		
		ТекущаяДата = ТекущаяДатаСеанса();
		
		ЧасВСекундах = 60 * 60;
		
		// Если регламентное задание создано больше часа назад, но так и не было удалено, считаем, что его нет.
		//
		Если ТекущаяДата - ДанныеЗапуска.ДатаСозданияРучногоИмпортаТоварногоКаталога > ЧасВСекундах Тогда
			Результат = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
