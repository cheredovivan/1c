////////////////////////////////////////////////////////////////////////////////
// Модуль содержит общие процедуры и функции регламентированных отчетов ФСРАР
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Возращает строковое представления числа без разделения на группы, 0 представляется как "0".
//
// Параметры:
// 		ИсходноеЧисло		- Число.
//
// Возвращаемое значение:
// 		Строка - представление числа без разделения на группы, 0 представляется как "0".
//
Функция СтрокаЧГ0(ИсходноеЧисло) Экспорт

	Возврат Формат(ИсходноеЧисло, "ЧН=; ЧГ=0");

КонецФункции

// Возвращает Индекс страницы в регистре по индексу страницы в Таблице страниц отчета.
// 
// Параметры:
// 	Форма				- Управляемая форма.
// 	ИдГруппы			- Строка - Идентификатор многострочной группы отчета.
// 	ИндексСтраницы		- Число - индекс страницы в таблице страниц отчета.
// 	
// Возвращаемое значение:
// 	Число.
//
Функция ПолучитьИндексСтраницыРегистра(Форма, ИдГруппы, ИндексСтраницы) Экспорт

	СервисРегистровГруппы = Форма["СервисРегистров" + ИдГруппы];

	Возврат СервисРегистровГруппы[ИндексСтраницы].ИндексСтраницыРегистра;

КонецФункции

// Увеличивает значение в колонке КоличествоСтрок таблицы СервисРегистров отчета для страницы с индексом ИндексСтраницы,
// до переданного значения ИндексСтроки. 
// Если значение ИндексСтроки не передано, значение КоличествоСтрок увеличивается на 1.
// В параметре КоличествоСтрок возвращается установленное значение.
// 
// Параметры:
// 	Форма				- Управляемая форма.
// 	ИдГруппы			- Строка - Идентификатор многострочной группы отчета.
// 	ИндексСтраницы		- Число - индекс страницы в таблице страниц отчета.
// 	КоличествоСтрок		- Число. Необязательный. В параметре возвращается установленное значение количества строк.
// 	НомерСтроки 		- Число. Необязательный. Номер последней строки.
//
Процедура УвеличитьКоличествоСтрокПоТекущейСтранице(Форма, ИдГруппы,
		ИндексСтраницы, КоличествоСтрок = Неопределено, НомерСтроки = Неопределено) Экспорт

	СервисРегистровГруппы = Форма["СервисРегистров" + ИдГруппы];

	Если НомерСтроки = Неопределено Тогда
	// По умолчанию просто наращиваем на 1.
		СервисРегистровГруппы[ИндексСтраницы].КоличествоСтрок = СервисРегистровГруппы[ИндексСтраницы].КоличествоСтрок
			+ 1;
	Иначе
	// Если передан индес строки - принудительно устанавливаем.
		СервисРегистровГруппы[ИндексСтраницы].КоличествоСтрок = НомерСтроки;
	КонецЕсли;

	КоличествоСтрок = СервисРегистровГруппы[ИндексСтраницы].КоличествоСтрок;

КонецПроцедуры

// Увеличивает значение в колонке КолвоСтрокПомНаУдаление таблицы СервисРегистров отчета для страницы 
// с индексом ИндексСтраницы на значение переданное в параметре Дельта. 
// 
// Параметры:
// 	Форма				- Управляемая форма.
// 	ИдГруппы			- Строка - Идентификатор многострочной группы отчета.
// 	ИндексСтраницы		- Число - индекс страницы в таблице страниц отчета.
// 	Дельта				- Число. Необязательный.
// 	
Процедура ИзменитьКоличествоПомеченныхНаУдалениеСтрокПоТекущейСтранице(Форма,
		ИдГруппы, ИндексСтраницы, Дельта = 1) Экспорт

	СервисРегистровГруппы = Форма["СервисРегистров" + ИдГруппы];
	СервисРегистровГруппы[ИндексСтраницы].КолвоСтрокПомНаУдаление = СервисРегистровГруппы[ИндексСтраницы].КолвоСтрокПомНаУдаление
		+ Дельта;

КонецПроцедуры

// Возвращает количество строк в таблице группы ИдГруппы по странице с индексом ИндексСтраницы.
// В зависимости от значения БезПомеченныхНаУдаление, возвращается общее количество строк, 
// включая помеченные на удаление, или не включая.
// 
// Параметры:
// 	Форма					- Управляемая форма.
// 	ИдГруппы				- Строка - Идентификатор многострочной группы отчета.
// 	ИндексСтраницы			- Число - индекс страницы в таблице страниц отчета.
// 	БезПомеченныхНаУдаление - Булево. Необязательный.
// 	
// Возвращаемое значение:
// 	Число.
//
Функция ПолучитьКоличествоСтрокПоСтраницеГруппы(Форма, ИдГруппы,
		ИндексСтраницы, БезПомеченныхНаУдаление = Ложь) Экспорт

	СервисРегистровГруппы = Форма["СервисРегистров" + ИдГруппы];

	Возврат ПолучитьКоличествоСтрокПоСтраницеИзСервисРегистровГруппы(
						СервисРегистровГруппы, ИндексСтраницы, БезПомеченныхНаУдаление);

КонецФункции

Функция ПолучитьКоличествоСтрокПоСтраницеИзСервисРегистровГруппы(
			СервисРегистровГруппы, ИндексСтраницы, БезПомеченныхНаУдаление = Ложь) Экспорт

	Если БезПомеченныхНаУдаление Тогда
		Возврат СервисРегистровГруппы[ИндексСтраницы].КоличествоСтрок
			- СервисРегистровГруппы[ИндексСтраницы].КолвоСтрокПомНаУдаление;
	Иначе
		Возврат СервисРегистровГруппы[ИндексСтраницы].КоличествоСтрок;
	КонецЕсли;

КонецФункции

// Возвращает количество строк в таблицах группы ИдГруппы по всем страницам отчета.
// В зависимости от значения БезПомеченныхНаУдаление, возвращается общее количество строк, 
// включая помеченные на удаление, или не включая.
// 
// Параметры:
// 	Форма				- Управляемая форма.
// 	ИдГруппы			- Строка - Идентификатор многострочной группы отчета.
// 	БезПомеченныхНаУдаление - Булево. Необязательный.
// 	
// Возвращаемое значение:
// 	Число.
//
Функция ПолучитьКоличествоСтрокПоГруппе(Форма, ИдГруппы,
		БезПомеченныхНаУдаление = Ложь) Экспорт

	КолСтрокПоГруппе = 0;

	СервисРегистровГруппы = Форма["СервисРегистров" + ИдГруппы];
	
	Возврат ПолучитьКоличествоСтрокПоГруппеИзСервисРегистровГруппы(СервисРегистровГруппы, БезПомеченныхНаУдаление);
	
КонецФункции

Функция ПолучитьКоличествоСтрокПоГруппеИзСервисРегистровГруппы(СервисРегистровГруппы,
		БезПомеченныхНаУдаление = Ложь) Экспорт

	КолСтрокПоГруппе = 0;

	КолСтраницПоГруппе = СервисРегистровГруппы.Количество();
	Для Инд = 0 По КолСтраницПоГруппе - 1 Цикл

		КоличествоСтрокНастранице = ПолучитьКоличествоСтрокПоСтраницеИзСервисРегистровГруппы(СервисРегистровГруппы,
																					Инд, БезПомеченныхНаУдаление);
		КолСтрокПоГруппе = КолСтрокПоГруппе + КоличествоСтрокНастранице;

	КонецЦикла;

	Возврат КолСтрокПоГруппе;

КонецФункции

// Возвращает количество строк в таблицах по всем группам и по всем страницам отчета.
// В зависимости от значения БезПомеченныхНаУдаление, возвращается общее количество строк, 
// включая помеченные на удаление, или не включая.
// 
// Параметры:
// 	Форма					- Управляемая форма.
// 	БезПомеченныхНаУдаление - Булево. Необязательный.
// 	
// Возвращаемое значение:
// 	Число.
//
Функция ПолучитьОбщееКоличествоСтрок(Форма,
		БезПомеченныхНаУдаление = Ложь) Экспорт

	КолСтрокПоВсемРазделам = 0;

	Для каждого ЭлементСтруктуры Из Форма.мСтруктураМногострочныхРазделов Цикл

		ИдГруппы = ЭлементСтруктуры.Ключ;
		КолСтрокПоВсемРазделам = КолСтрокПоВсемРазделам
			+ ПолучитьКоличествоСтрокПоГруппе(Форма, ИдГруппы, БезПомеченныхНаУдаление);

	КонецЦикла;

	Возврат КолСтрокПоВсемРазделам;

КонецФункции

// Возвращает имя регистра, как оно задано в конфигурации, в котором хранятся данные таблиц группы ИдГруппы.
// 
// Параметры:
// 	ИдГруппы					- Строка - Идентификатор многострочной группы отчета.
// 	СтруктураРеквизитовФормы	- Структура. Стандартный реквизит формы всех алкоотчетов. 
// 								Формируется при инициализации отчета. 
// 	
// Возвращаемое значение:
// 	Строка.
//
Функция ПолучитьИмяРегистраСведенийАЛКО(ИдГруппы,
		СтруктураРеквизитовФормы) Экспорт

	СоответствиеИдГруппыРегистр = СтруктураРеквизитовФормы.мСоответствиеИдГруппыРегистр;

	Возврат СоответствиеИдГруппыРегистр.Получить(ИдГруппы);

КонецФункции

// Возвращает идентификатор табличной группы по имени регистра сведений, в котором хранятся значения таблиц этой группы.
// 
// Параметры:
// 	ИмяРегистра					- Строка. Имя регистра, как оно задано в конфигурации.
// 	СтруктураРеквизитовФормы	- Структура. Стандартный реквизит формы всех алкоотчетов. 
// 								Формируется при инициализации отчета.
// Возвращаемое значение:
// 	Строка.
//
Функция ПолучитьИдГруппыПоИмениРегистраАЛКО(ИмяРегистра,
		СтруктураРеквизитовФормы) Экспорт

	СоответствиеРегистрИдГруппы = СтруктураРеквизитовФормы.мСоответствиеРегистрИдГруппы;

	Возврат СоответствиеРегистрИдГруппы.Получить(ИмяРегистра);

КонецФункции
 	
Функция ПолучитьТочныйИдГруппыПоРазделуАЛКО(Форма, Раздел) Экспорт

	СписокИдГруппРаздела = ПолучитьСписокИдГруппПоРазделуАЛКО(Раздел, Форма.СтруктураРеквизитовФормы);

	ИдГруппы = ПолучитьИдГруппыПоРазделуАЛКО(Раздел, Форма.СтруктураРеквизитовФормы);

	Если СписокИдГруппРаздела.Количество() > 1 Тогда

	// Определяем по текущей странице группы страниц.
		ГруппаСтраницыТаблицРаздела = Форма.Элементы.Найти("СтраницыТаблиц" + Раздел);

		Если НЕ ГруппаСтраницыТаблицРаздела = Неопределено Тогда

			ТекущаяСтраницаГруппыСтраницТаблицРаздела = ГруппаСтраницыТаблицРаздела.ТекущаяСтраница;
			ИмяТекущейСтраницы = ТекущаяСтраницаГруппыСтраницТаблицРаздела.Имя;

			Для каждого ЭлементИдГруппы Из СписокИдГруппРаздела Цикл

				ИмяТаблицыФормы = ЭлементИдГруппы.Представление;
				Если СтрЗаменить(ИмяТекущейСтраницы, "СтраницаТаблицы", "") = ИмяТаблицыФормы Тогда

					ИдГруппы = ЭлементИдГруппы.Значение;
					Прервать;

				КонецЕсли;

			КонецЦикла;

		КонецЕсли;

	КонецЕсли;

	Возврат ИдГруппы;

КонецФункции

Функция ПолучитьИдГруппыПоРазделуАЛКО(Раздел, СтруктураРеквизитовФормы) Экспорт

	СоответствиеРазделИдГруппы = СтруктураРеквизитовФормы.мСоответствиеРазделИдГруппы;

	Возврат СоответствиеРазделИдГруппы.Получить(Раздел);

КонецФункции

Функция ПолучитьСписокИдГруппПоРазделуАЛКО(Раздел,
		СтруктураРеквизитовФормы) Экспорт

	СтруктураИдГрупп = СтруктураРеквизитовФормы.мСтруктураИдГрупп;

	СписокИдГрупп = Неопределено;
	СтруктураИдГрупп.Свойство(Раздел, СписокИдГрупп);

	Возврат СписокИдГрупп;

КонецФункции

Функция ПолучитьРазделПоИдГруппыАЛКО(ИдГруппы,
		СтруктураРеквизитовФормы) Экспорт

	СоответствиеИдГруппыРаздел = СтруктураРеквизитовФормы.мСоответствиеИдГруппыРаздел;

	Возврат СоответствиеИдГруппыРаздел.Получить(ИдГруппы);

КонецФункции

Функция ПолучитьСтруктуруИдГруппИменРегистровАЛКО(СтруктураРеквизитовФормы) Экспорт

	Результат = Новый Структура;

	СоответствиеИдГруппыРегистр = СтруктураРеквизитовФормы.мСоответствиеИдГруппыРегистр;

	Для каждого Элемент Из СоответствиеИдГруппыРегистр Цикл

		ИдГруппы = Элемент.Ключ;
		ИмяРегистра = Элемент.Значение;
		Раздел = ПолучитьРазделПоИдГруппыАЛКО(ИдГруппы, СтруктураРеквизитовФормы);

		Результат.Вставить(ИдГруппы, ИмяРегистра);

		Если НЕ Результат.Свойство(Раздел) Тогда
			Результат.Вставить(Раздел, ИмяРегистра);
		Иначе
		// Несколько групп в разделе.
			Результат.Вставить(Раздел + ИдГруппы, ИмяРегистра);
		КонецЕсли;

		Результат.Вставить(Раздел + ИмяРегистра, ИдГруппы);

	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ПолучитьСписокИменРегистровАЛКО(СтруктураРеквизитовФормы) Экспорт

	СписокИмен = Новый СписокЗначений;

	СоответствиеИдГруппыРегистр = СтруктураРеквизитовФормы.мСоответствиеИдГруппыРегистр;

	Для каждого Элемент Из СоответствиеИдГруппыРегистр Цикл

	// ИдГруппы в Элемент.Ключ.
		ИмяРегистра = Элемент.Значение;

		СписокИмен.Добавить(ИмяРегистра);

	КонецЦикла;

	Возврат СписокИмен;

КонецФункции

Функция СформироватьОбособленноеПодразделениеАЛКОпоРеквизитам(
		Наименование, КПП, ПредставлениеАдреса, ЭтоПБОЮЛ, ОрганизацияИНН, 
		НуженВидПродажи = Ложь, ВидРозничнойПродажи = 0, ОтсутствуетИнтернет = Ложь,
		ФорматированнойСтрокой = Ложь) Экспорт
    
	НаименованиеОП = ПредставлениеОрганизацииСтрокой(Наименование, ОрганизацияИНН, КПП, 
						ПредставлениеАдреса, ЭтоПБОЮЛ, Истина, ФорматированнойСтрокой);

	НаименованиеОП = ?(НаименованиеОП = "", "Заполнить", НаименованиеОП);
	
	НуженВидПродажи = (ТипЗнч(ВидРозничнойПродажи) = Тип("Число")) И (ВидРозничнойПродажи > 0);
	
	Если НуженВидПродажи Тогда
	    
		Если НаименованиеОП <> "Заполнить" И ВидРозничнойПродажи <> 0 Тогда
		
			НаименованиеОП = НаименованиеОП + ", " 
							+ Символы.ПС
							+ "Вид розничной продажи " + Строка(ВидРозничнойПродажи)
							+ ?(ОтсутствуетИнтернет, ", Интернет отсутствует", "");
		
		КонецЕсли;	
	
	КонецЕсли;
	
	Если ФорматированнойСтрокой Тогда
	
		// Оформляем гиперссылкой.
		НаименованиеОП = "<a href=""Ссылка"">" + НаименованиеОП + "</a>";
		
		НаименованиеОП = ФорматированнаяСтрока(НаименованиеОП);	
	
	КонецЕсли;
	
	Возврат НаименованиеОП;

КонецФункции

// Форматирует строку в соответствии с заданным шаблоном.
// Возможные значения тегов в шаблоне:
// - <span style="Имя свойства: Имя элемента стиля">Строка</span> - оформляет текст описанными
//      в атрибуте style элементами стиля.
// - <b> Строка </b> - выделяет строку элементом стиля ВажнаяНадписьШрифт,
//      который соответствует полужирному шрифту.
// - <a href="Ссылка">Строка</a> - добавляет гиперссылку.
// - <img src="Календарь"> - добавляет картинку из библиотеки картинок.
// Атрибут style применяется для оформления текста. Атрибут может быть использован для тегов span и a.
// Вначале следует имя стилевого свойства, затем через двоеточие имя элемента стиля.
// Стилевые свойства:
//  - color - Определяет цвет текста. Например, color: ГиперссылкаЦвет;
//  - background-color - Определяет цвет фона у текста. Например, background-color: ИтогиФонГруппы;
//  - font - Определяет шрифт текста.Например, font: ОсновнойЭлементСписка.
// Стилевые свойства разделяются точкой с запятой. Например, style="color: ГиперссылкаЦвет; font: ОсновнойЭлементСписка"
// Вложенные теги не поддерживаются.
//
// Параметры:
//  ШаблонСтроки - Строка - строка, содержащая теги форматирования.
//  Параметр<n>  - Строка - значение подставляемого параметра.
//
// Возвращаемое значение:
//  ФорматированнаяСтрока - преобразованная строка.
//
// Пример:
//  1. СтроковыеФункцииКлиент.ФорматированнаяСтрока(НСтр("ru='
//       <span style=""color: ЗаблокированныйРеквизитЦвет; font: ВажнаяНадписьШрифт"">Минимальная</span> версия программы <b>1.1</b>. 
//       <a href = ""Обновление"">Обновите</a> программу.'"));
//  2. СтроковыеФункцииКлиент.ФорматированнаяСтрока(НСтр("ru='Режим: <img src=""РедактироватьВДиалоге"">
//       <a style=""color: ИзмененноеЗначениеРеквизитаЦвет; background-color: ИзмененноеЗначениеРеквизитаФон""
//       href=""e1cib/command/Обработка.Редактор"">Редактирование</a>'"));
//  3. СтроковыеФункцииКлиент.ФорматированнаяСтрока(НСтр("ru='Текущая дата <img src=""Календарь"">
//       <span style=""font:ВажнаяНадписьШрифт"">%1</span>'"), ТекущаяДатаСеанса());
//
Функция ФорматированнаяСтрока(Знач ШаблонСтроки, Знач Параметр1 = Неопределено, Знач Параметр2 = Неопределено,
	Знач Параметр3 = Неопределено, Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено) Экспорт	
	
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Возврат СтроковыеФункции.ФорматированнаяСтрока(
					ШаблонСтроки, 
					Параметр1,Параметр2, Параметр3,Параметр4,Параметр5);
	#Иначе
		Возврат СтроковыеФункцииКлиент.ФорматированнаяСтрока(
					ШаблонСтроки, 
					Параметр1,Параметр2, Параметр3,Параметр4,Параметр5);
	#КонецЕсли
	
КонецФункции

Функция СформироватьОбособленноеПодразделениеАЛКО(СведенияОбОП, ЭтоПБОЮЛ,
		ОрганизацияИНН, НуженВидПродажи = Ложь, ФорматированнойСтрокой = Ложь) Экспорт

	Если НЕ ТипЗнч(СведенияОбОП) = Тип("Структура") Тогда
		Возврат "Заполнить";
	КонецЕсли;

	Наименование = СтрЗначениеПоКлючуСтруктуры("Наименование", СведенияОбОП);
	КПП = СтрЗначениеПоКлючуСтруктуры("КПП", СведенияОбОП);
	ПредставлениеАдреса = СтрЗначениеПоКлючуСтруктуры("ПредставлениеАдреса", СведенияОбОП); 
	
	ОтсутствуетИнтернет = Ложь;
	ВидРозничнойПродажи = 0;
	
	СведенияОбОП.Свойство("ВидРозничнойПродажи", ВидРозничнойПродажи);
	СведенияОбОП.Свойство("ОтсутствуетИнтернет", ОтсутствуетИнтернет);
		
	НуженВидПродажи = (ТипЗнч(ВидРозничнойПродажи) = Тип("Число")) И (ВидРозничнойПродажи > 0);
	
	Возврат СформироватьОбособленноеПодразделениеАЛКОпоРеквизитам(
		Наименование, КПП, ПредставлениеАдреса, ЭтоПБОЮЛ, ОрганизацияИНН, 
		НуженВидПродажи, ВидРозничнойПродажи, ОтсутствуетИнтернет, ФорматированнойСтрокой);

КонецФункции

Функция ПредставлениеОрганизацииСтрокой(Наименование, ИНН, КПП = "",
		ПредставлениеАдреса = "", ЭтоПБОЮЛ = Неопределено,
		АдресНаНовойСтроке = Истина, ФорматированнойСтрокой = Ложь) Экспорт

	ВыводитьРеквизиты = ЗначениеЗаполнено(Наименование) ИЛИ ЗначениеЗаполнено(КПП)
		ИЛИ ЗначениеЗаполнено(СтрЗаменить(ПредставлениеАдреса, ",", ""));

	Если ВыводитьРеквизиты Тогда

		Если НЕ ТипЗнч(ЭтоПБОЮЛ) = Тип("Булево") Тогда

			ЭтоПБОЮЛ = ?((СтрДлина(СокрЛП(ИНН)) = 10)
				ИЛИ ЗначениеЗаполнено(КПП), Ложь, Истина);

		КонецЕсли;

		Результат = "";
		Если НЕ ПустаяСтрока(Наименование) Тогда
			Результат = СокрЛП(Наименование);
		КонецЕсли;

		Если НЕ (ПустаяСтрока(ИНН) или (ИНН = "Заполнить")) Тогда			
			Результат = Результат + ?(ПустаяСтрока(Результат), "", ", ") + "ИНН "
				+ СокрЛП(ИНН);
		КонецЕсли;

		Если НЕ (ЭтоПБОЮЛ или ПустаяСтрока(КПП) или (КПП = "Заполнить")) Тогда
			Результат = Результат + ?(ПустаяСтрока(Результат), "", ", ") + "КПП "
				+ СокрЛП(КПП);
		КонецЕсли;
		
		Если ФорматированнойСтрокой Тогда
			Результат = "<b>" + Результат + "</b>";
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(СтрЗаменить(ПредставлениеАдреса, ",", "")) Тогда
			Результат = Результат
				+ ?(ПустаяСтрока(Результат), "", ?(АдресНаНовойСтроке, Символы.ПС, ", "))
				+ СокрЛП(ПредставлениеАдреса);
		КонецЕсли;
	Иначе
		Результат = "";
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ПредставлениеОрганизации(Наименование, ИНН, КПП = "",
		ПредставлениеАдреса = "", ЭтоПБОЮЛ = Неопределено,
		АдресНаНовойСтроке = Истина, ФорматированнойСтрокой = Ложь) Экспорт

	Результат =  ПредставлениеОрганизацииСтрокой(Наименование, ИНН, КПП,
		ПредставлениеАдреса , ЭтоПБОЮЛ,
		АдресНаНовойСтроке, ФорматированнойСтрокой);
		
	Если ФорматированнойСтрокой Тогда
	
		// Оформляем гиперссылкой.
		Результат = "<a href=""Ссылка"">" + Результат + "</a>";
		
		Результат = ФорматированнаяСтрока(Результат);	
	
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ПолучитьКодКвартала(ПоказателиТитульногоЛиста) Экспорт

	Если НЕ ПустаяСтрока(ПоказателиТитульногоЛиста.ОтчетныйПериод1Кв) Тогда
		Возврат "3";
	ИначеЕсли НЕ ПустаяСтрока(ПоказателиТитульногоЛиста.ОтчетныйПериод2Кв) Тогда
		Возврат "6";
	ИначеЕсли НЕ ПустаяСтрока(ПоказателиТитульногоЛиста.ОтчетныйПериод3Кв) Тогда
		Возврат "9";
	ИначеЕсли НЕ ПустаяСтрока(ПоказателиТитульногоЛиста.ОтчетныйПериод4Кв) Тогда
		Возврат "0";
	Иначе
		Возврат "";
	КонецЕсли;

КонецФункции

Функция ЭтоУправляемаяФормаИлиФормаКлиентскогоПриложения(Форма) Экспорт

	СтрокаПредставленияТипа = ВРег(Строка(ТипЗнч(Форма)));
	
	// Не используется сравнение с Тип() поскольку в 8.2 например нет типа
	// ФормаКлиентскогоПриложения, а проверять через версию платформы дороже
	// простого сравнения представлений.
	Возврат (СтрокаПредставленияТипа = "УПРАВЛЯЕМАЯ ФОРМА" 
			ИЛИ СтрокаПредставленияТипа = "ФОРМА КЛИЕНТСКОГО ПРИЛОЖЕНИЯ"
			ИЛИ СтрокаПредставленияТипа = "MANAGED FORM"
			ИЛИ СтрокаПредставленияТипа = "CLIENT APPLICATION FORM");			
	
КонецФункции

Функция ЭтоФормаАЛКООтчета(Форма) Экспорт
	
	Если НЕ ЭтоУправляемаяФормаИлиФормаКлиентскогоПриложения(Форма) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяФормы = Форма.ИмяФормы;
	
	Возврат (
				СтрНайти(ИмяФормы, "РегламентированныйОтчетАлко") > 0
				И
				СтрНайти(ИмяФормы, "ФормаОтчета") > 0
			);

КонецФункции

Функция ЭтоСоответствие(Ответ) Экспорт

	Возврат (СтрНайти(НРег(Строка(ТипЗнч(Ответ))), "соответствие") > 0)

КонецФункции

Функция ЭтоСтруктура(Ответ) Экспорт

	Возврат (СтрНайти(НРег(Строка(ТипЗнч(Ответ))), "структура") > 0)

КонецФункции

Функция ЭтоМассив(Ответ) Экспорт

	Возврат (СтрНайти(НРег(Строка(ТипЗнч(Ответ))), "массив") > 0)

КонецФункции

Функция ВерсияПлатформы() Экспорт

	Возврат РегламентированнаяОтчетностьАЛКОВызовСервера.ВерсияПлатформы();

КонецФункции

// Сравнивает текущую версию платформы и версию в параметре.
// Параметры
//  СтрокаВерсии  – Строка – номер версии в формате РР.{П|ПП}.ЗЗ.СС 
//
// Результат Булево.
// Истина, если текущая версия платформы не ниже переданной в параметре.
Функция ВерсияПлатформыНеНижеВерсии(СтрокаВерсии) Экспорт

	ТекущаяВерсияПлатформы = ВерсияПлатформы();
	
	Возврат (ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ТекущаяВерсияПлатформы, СтрокаВерсии) >= 0);
	
КонецФункции

Функция ЗначениеПоКлючуСтруктуры(Ключ, Структура) Экспорт

	Значение = Неопределено;

	Структура.Свойство(Ключ, Значение);

	Возврат Значение;

КонецФункции

Функция СтрЗначениеПоКлючуСтруктуры(Ключ, Структура, ЧислоБезГрупп = Истина) Экспорт

	Значение = ЗначениеПоКлючуСтруктуры(Ключ, Структура);
    Если (ТипЗнч(Значение) = Тип("Число")) и ЧислоБезГрупп Тогда
		Возврат СтрокаЧГ0(Значение);
	КонецЕсли;

	Возврат Строка(Значение);
	
КонецФункции

Функция ДанныеПоАдресу(Данные, Адрес) Экспорт

	Перем ЗначениеДанных;

	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		ПозицияРазделителя = СтрНайти(Адрес, ".");
		Если ПозицияРазделителя = 0 Тогда
			АдресВерхнегоУровня = СокрЛП(Адрес);
			Данные.Свойство(АдресВерхнегоУровня, ЗначениеДанных);
		Иначе
			АдресВерхнегоУровня = СокрЛП(Лев(Адрес, ПозицияРазделителя - 1));
			ДанныеВерхнегоУровня = Неопределено;
			Если Данные.Свойство(АдресВерхнегоУровня, ДанныеВерхнегоУровня) Тогда
				АдресСледующегоУровня = СокрЛП(Сред(Адрес, ПозицияРазделителя + 1));
				ЗначениеДанных = ДанныеПоАдресу(ДанныеВерхнегоУровня, АдресСледующегоУровня);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат ЗначениеДанных;

КонецФункции

Функция ИнициализацияЗаголовкаАЛКО(Заголовок, ИмяФормы,
		СтруктураРеквизитовФормы) Экспорт

	Если СтрНайти(Заголовок, СтруктураРеквизитовФормы.СтрПериодОтчета) = 0 Тогда
		Заголовок = Заголовок + " за " + СтруктураРеквизитовФормы.СтрПериодОтчета;
	КонецЕсли;

	ОргСтр = " (" + СтруктураРеквизитовФормы.НаимОрганизации + ")";
	Если СтрНайти(Заголовок, ОргСтр) = 0 Тогда
		Заголовок = Заголовок + ОргСтр;
	КонецЕсли;

	Заголовок = Заголовок
		+ ?(СтрНайти(ИмяФормы, "Внешний") > 0, " - внешний отчет", "");

	Возврат Заголовок;

КонецФункции

Процедура ПоказатьПериодАЛКО(СтруктураРеквизитовФормы,
		СтруктураДанныхТитульный, ПринудительноЗаполнить = Ложь) Экспорт

	СтруктураРеквизитовФормы.СтрПериодОтчета = ПредставлениеПериода(НачалоДня(СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета), КонецДня(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета), "ФП = Истина");

	Если (НЕ ПринудительноЗаполнить)
			И СтруктураРеквизитовФормы.мДокументВосстановлен
			И (СтруктураРеквизитовФормы.мСкопированаФорма = Неопределено) Тогда

		Возврат;

	КонецЕсли;

	НомерКварталаСтр = КварталОтчетаСтрокой(СтруктураРеквизитовФормы);
	НомерГода = Формат(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета, "ДФ=гггг");
	ОтчетныйМесяц = Формат(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета, "ДФ=ММММ");

	// Отображение на титульном листе отчетного периода.
	Значение1Кв = ?(НомерКварталаСтр = "1", "V", "");
	СтруктураДанныхТитульный.Вставить("ОтчетныйПериод1Кв", Значение1Кв);

	Значение2Кв = ?(НомерКварталаСтр = "2", "V", "");
	СтруктураДанныхТитульный.Вставить("ОтчетныйПериод2Кв", Значение2Кв);

	Значение3Кв = ?(НомерКварталаСтр = "3", "V", "");
	СтруктураДанныхТитульный.Вставить("ОтчетныйПериод3Кв", Значение3Кв);

	Значение4Кв = ?(НомерКварталаСтр = "4", "V", "");
	СтруктураДанныхТитульный.Вставить("ОтчетныйПериод4Кв", Значение4Кв);

	СтруктураДанныхТитульный.Вставить("ОтчетныйГод", НомерГода);
	
	СтруктураДанныхТитульный.Вставить("ОтчетныйМесяц", ОтчетныйМесяц);

КонецПроцедуры

Функция КварталОтчетаСтрокой(СтруктураРеквизитовФормы) Экспорт

	Возврат Формат(СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета, "ДФ=к");	

КонецФункции
 
Функция УбратьЛишниеПробелы(Знач Стр) Экспорт

	ДлинаСтроки = 0;
	
	Пока СтрДлина(Стр) <> ДлинаСтроки Цикл
		
		ДлинаСтроки = СтрДлина(Стр);
		
		Стр = СтрЗаменить(Стр, "  ", " ");		
	
	КонецЦикла; 
	
	Возврат Стр;

КонецФункции

Функция ЭтоБуква(Символ) Экспорт

	// Цифры в Unicode 0030 - 0039
	// Латинские буквы в Unicode 0041 - 005A, 0061 - 007A
	// Русские буквы 0410 - 044F
	
	СтартЦифры = 48; //0030
	КонецЦифры = 57; //0039
	СтартБольшиеЛатинские = 65; //0041
	КонецБольшиеЛатинские = 90; //005A
	СтартМаленькиеЛатинские = 97; //0061
	КонецМаленькиеЛатинские = 122; //007A
	СтартРусские = 1040; //0410
	КонецРусские = 1103; //044F
	
	

	КодСимволаUnicode = КодСимвола(Символ, 1);

	Результат =	(КодСимволаUnicode >= СтартРусские И КодСимволаUnicode <= КонецРусские)
				ИЛИ
				(КодСимволаUnicode >= СтартБольшиеЛатинские И КодСимволаUnicode <= КонецБольшиеЛатинские)
				ИЛИ 
				(КодСимволаUnicode >= СтартМаленькиеЛатинские И КодСимволаUnicode <= КонецМаленькиеЛатинские);

	Возврат Результат;

КонецФункции

Функция ЭтоЦифра(Символ) Экспорт

	// Цифры в Unicode 0030 - 0039
	// Латинские буквы в Unicode 0041 - 005A, 0061 - 007A
	// Русские буквы 0410 - 044F
	
	СтартЦифры = 48; //0030
	КонецЦифры = 57; //0039
	
	КодСимволаUnicode = КодСимвола(Символ, 1);

	Результат =	(КодСимволаUnicode >= СтартЦифры И КодСимволаUnicode <= КонецЦифры);

	Возврат Результат;

КонецФункции

Функция ЕстьЦифра(Стр) Экспорт

	Если ПустаяСтрока(Стр) Тогда	
		Возврат Ложь	
	КонецЕсли;
	
	Длина = СтрДлина(Стр);
	Для Индекс = 1 По Длина Цикл
	
		Символ = Сред(Стр, Индекс, 1);
		Если ЭтоЦифра(Символ) Тогда		
			Возврат Истина		
		КонецЕсли; 
	
	КонецЦикла; 
	
	Возврат Ложь

КонецФункции

Функция ЗаменитьСимволНаРазделительЕслиСимволПередЦифрой(Знач Стр, Символ, Разделитель) Экспорт
	
	Если ПустаяСтрока(Стр) Тогда	
		Возврат Стр	
	КонецЕсли;
	
	Если СтрНайти(Стр, Символ) = 0 Тогда
		// Нет Символа.
		Возврат Стр	
	КонецЕсли;
	
	Для Цифра = 0 По 9 Цикл
	
		СтрПоиска = Символ + СтрокаЧГ0(Цифра);
		СтрЗамены = Разделитель + СтрокаЧГ0(Цифра);
		
		Стр = СтрЗаменить(Стр, СтрПоиска, СтрЗамены)
	
	КонецЦикла; 
	
	Возврат Стр
		
КонецФункции

// Заменяет в строке буквы русского алфавита на буквы латинского, в соответсвии с правилами транслитерации.
//
// Параметры:
// 		Стр				- Строка.
//
// Возвращаемое значение:
// 		Строка	
//
Функция Транслит(Стр) Экспорт

	СоответствиеСимволов = Новый Соответствие;
	СоответствиеСимволов.Вставить("А", "A");
	СоответствиеСимволов.Вставить("Б", "B");
	СоответствиеСимволов.Вставить("В", "V");
	СоответствиеСимволов.Вставить("Г", "G");
	СоответствиеСимволов.Вставить("Д", "D");
	СоответствиеСимволов.Вставить("Е", "E");
	СоответствиеСимволов.Вставить("Ё", "E");
	СоответствиеСимволов.Вставить("Ж", "ZH");
	СоответствиеСимволов.Вставить("З", "Z");
	СоответствиеСимволов.Вставить("И", "I");
	СоответствиеСимволов.Вставить("Й", "Y");
	СоответствиеСимволов.Вставить("К", "K");
	СоответствиеСимволов.Вставить("Л", "L");
	СоответствиеСимволов.Вставить("М", "M");
	СоответствиеСимволов.Вставить("Н", "N");
	СоответствиеСимволов.Вставить("О", "O");
	СоответствиеСимволов.Вставить("П", "P");
	СоответствиеСимволов.Вставить("Р", "R");
	СоответствиеСимволов.Вставить("С", "S");
	СоответствиеСимволов.Вставить("Т", "T");
	СоответствиеСимволов.Вставить("У", "U");
	СоответствиеСимволов.Вставить("Ф", "F");
	СоответствиеСимволов.Вставить("Х", "KH");
	СоответствиеСимволов.Вставить("Ц", "TS");
	СоответствиеСимволов.Вставить("Ч", "CH");
	СоответствиеСимволов.Вставить("Ш", "SH");
	СоответствиеСимволов.Вставить("Щ", "SHCH");
	СоответствиеСимволов.Вставить("Ъ", "");
	СоответствиеСимволов.Вставить("Ы", "Y");
	СоответствиеСимволов.Вставить("Ь", "");
	СоответствиеСимволов.Вставить("Э", "E");
	СоответствиеСимволов.Вставить("Ю", "YU");
	СоответствиеСимволов.Вставить("Я", "YA");

	Результат = "";

	Для Инд = 1 по СтрДлина(Стр) Цикл

		СимволРус = Сред(Стр, Инд, 1);
		СимволАнг = СоответствиеСимволов.Получить(ВРег(СимволРус));

		Если СимволАнг = Неопределено Тогда
		// это английский символ
			Результат = Результат + СимволРус;
		Иначе
			Если ВРег(СимволРус) = СимволРус Тогда
			// Верхний регистр
				Результат = Результат + СимволАнг;
			Иначе
				Результат = Результат + НРег(СимволАнг);
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

	Возврат Результат;

КонецФункции

Процедура ЗагрузитьРекивизитыЗаявленияОКорректировкиАЛКО(СтруктураРеквизитовФормы,
		СтруктураДанныхТитульный, ПринудительноЗаполнить = Ложь) Экспорт

	Если (НЕ ПринудительноЗаполнить)
			И СтруктураРеквизитовФормы.мДокументВосстановлен
			И (СтруктураРеквизитовФормы.мСкопированаФорма = Неопределено) Тогда
			// Все уже в СтруктураДанныхТитульный.
		Возврат;

	КонецЕсли;

	СданныйАлкоОтчет = ЗначениеПоКлючуСтруктуры("СданныйАлкоОтчет", СтруктураРеквизитовФормы);

	СтруктураДанныхТитульный.Вставить("СданныйАлкоОтчет", СданныйАлкоОтчет);

	НомерПриложения = ЗначениеПоКлючуСтруктуры("НомерПриложения", СтруктураРеквизитовФормы);
	СтруктураДанныхТитульный.Вставить("НомерПриложения", НомерПриложения);

КонецПроцедуры

Процедура СформироватьДеревоРазделовОтчетаАЛКО(Форма) Экспорт

	Форма["РазделыОтчета"].ПолучитьЭлементы().Очистить();

	Для Каждого ЭлементДереваСтраницОтчета Из Форма["мДеревоСтраницОтчета"].ПолучитьЭлементы() Цикл

		Если ЭлементДереваСтраницОтчета.ПоказатьСтраницу = 1 Тогда

			ЭлементРазделовОтчета = Форма["РазделыОтчета"].ПолучитьЭлементы().Добавить();
			ЭлементРазделовОтчета.КолонкаРазделыОтчета = ЭлементДереваСтраницОтчета.Представление;
			ЭлементРазделовОтчета.КолонкаРазделыОтчетаСокрНаим = ЭлементДереваСтраницОтчета.ИмяСтраницы;
			ЭлементРазделовОтчета.ИндексКартинки = 1;
			ЭлементРазделовОтчета.РазделМногостраничный = Ложь;
			ЭлементРазделовОтчета.РазделМногострочный = Ложь;

			НайденноеЗначение = Неопределено;

			Если Форма["мСтруктураМногостраничныхРазделов"].Свойство(ЭлементРазделовОтчета.КолонкаРазделыОтчетаСокрНаим, НайденноеЗначение) Тогда

				Если НЕ НайденноеЗначение = Неопределено Тогда

					НайденноеЗначение = Форма[НайденноеЗначение];

					ЭлементРазделовОтчета.ИндексКартинки = 0;
					ЭлементРазделовОтчета.РазделМногостраничный = Истина;

					Для НомерСтраницы = 1 По НайденноеЗначение.Количество() Цикл

						СтраницаРазделаОтчета = ЭлементРазделовОтчета.ПолучитьЭлементы().Добавить();

						СтраницаРазделаОтчета.КолонкаРазделыОтчета = "Стр. " + НомерСтраницы;
						СтраницаРазделаОтчета.КолонкаРазделыОтчетаСокрНаим = ЭлементРазделовОтчета.КолонкаРазделыОтчетаСокрНаим;
						СтраницаРазделаОтчета.КолонкаНомерСтраницыРазделаОтчета = НомерСтраницы;
						СтраницаРазделаОтчета.ИндексКартинки = 1;
						СтраницаРазделаОтчета.РазделМногостраничный = ЭлементРазделовОтчета.РазделМногостраничный;
						СтраницаРазделаОтчета.РазделМногострочный = ЭлементРазделовОтчета.РазделМногострочный;

					КонецЦикла;

				КонецЕсли;

			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Процедура ВосстановитьСведенияОбОрганизацииИзТитульногоЛистаСтарогоОбразцаАЛКО(ПоказателиОтчета,
		СтруктураДанныхТитульный, СтруктураРеквизитовФормы, ЭтоПБОЮЛ) Экспорт

	ПоказателиТаблПоле = ПоказателиОтчета["ПолеТабличногоДокументаТитульный"];

	Для Каждого Показатель Из ПоказателиТаблПоле Цикл

		ИмяПоказателя = Показатель.Ключ;

		Если ИмяПоказателя = "ИНН1_1" Тогда
			СтруктураДанныхТитульный.Вставить("ИНН1");
			Для Ном = 1 По 12 Цикл
				СтруктураДанныхТитульный.ИНН1 = СокрЛП(СтруктураДанныхТитульный.ИНН1)
					+ ПоказателиТаблПоле["ИНН1_" + Ном];
				СтруктураДанныхТитульный.Удалить("ИНН1_" + Ном);
			КонецЦикла;
			Если Лев(СтруктураДанныхТитульный.ИНН1, 2) = "00" Тогда
				СтруктураДанныхТитульный.ИНН1 = Сред(СтруктураДанныхТитульный.ИНН1, 3);
			КонецЕсли;
		ИначеЕсли ИмяПоказателя = "КПП1_1" Тогда
			СтруктураДанныхТитульный.Вставить("КПП1");
			Для Ном = 1 По 9 Цикл
				СтруктураДанныхТитульный.КПП1 = СокрЛП(СтруктураДанныхТитульный.КПП1)
					+ ПоказателиТаблПоле["КПП1_" + Ном];
				СтруктураДанныхТитульный.Удалить("КПП1_" + Ном);
			КонецЦикла;
		ИначеЕсли ИмяПоказателя = "ОтчетныйГод1" Тогда
			СтруктураДанныхТитульный.Вставить("ОтчетныйГод");
			Для Ном = 1 По 4 Цикл
				СтруктураДанныхТитульный.ОтчетныйГод = СокрЛП(СтруктураДанныхТитульный.ОтчетныйГод)
					+ ПоказателиТаблПоле["ОтчетныйГод" + Ном];
				СтруктураДанныхТитульный.Удалить("ОтчетныйГод" + Ном);
			КонецЦикла;
		ИначеЕсли ИмяПоказателя = "НомКорр1" Тогда
			СтруктураДанныхТитульный.Вставить("НомКорр");
			Для Ном = 1 По 2 Цикл
				СтруктураДанныхТитульный.НомКорр = СокрЛП(СтруктураДанныхТитульный.НомКорр)
					+ ПоказателиТаблПоле["НомКорр" + Ном];
				СтруктураДанныхТитульный.Удалить("НомКорр" + Ном);
			КонецЦикла;
		ИначеЕсли ИмяПоказателя = "ПочтовыйИндекс1" Тогда
			СтруктураДанныхТитульный.Вставить("ПочтовыйИндекс");
			Для Ном = 1 По 6 Цикл
				СтруктураДанныхТитульный.ПочтовыйИндекс = СокрЛП(СтруктураДанныхТитульный.ПочтовыйИндекс)
					+ ПоказателиТаблПоле["ПочтовыйИндекс" + Ном];
				СтруктураДанныхТитульный.Удалить("ПочтовыйИндекс" + Ном);
			КонецЦикла;
		ИначеЕсли ИмяПоказателя = "КодРегиона1" Тогда
			СтруктураДанныхТитульный.Вставить("КодРегиона");
			Для Ном = 1 По 2 Цикл
				СтруктураДанныхТитульный.КодРегиона = СокрЛП(СтруктураДанныхТитульный.КодРегиона)
					+ ПоказателиТаблПоле["КодРегиона" + Ном];
				СтруктураДанныхТитульный.Удалить("КодРегиона" + Ном);
			КонецЦикла;
		ИначеЕсли ИмяПоказателя = "Прил1" Тогда
			СтруктураДанныхТитульный.Вставить("Прил");
			Для Ном = 1 По 3 Цикл
				СтруктураДанныхТитульный.Прил = СокрЛП(СтруктураДанныхТитульный.Прил)
					+ ПоказателиТаблПоле["Прил" + Ном];
				СтруктураДанныхТитульный.Удалить("Прил" + Ном);
			КонецЦикла;

		КонецЕсли;

	КонецЦикла;

	СтруктураДанныхТитульный.Вставить("ЭтоПБОЮЛ", ЭтоПБОЮЛ);

	СтруктураРеквизитовФормы.Руководитель = Неопределено;
	ПоказателиТаблПоле.Свойство("ОргДиректор", СтруктураРеквизитовФормы.Руководитель);
	СтруктураРеквизитовФормы.Руководитель = ?(СтруктураРеквизитовФормы.Руководитель = Неопределено, "", СтруктураРеквизитовФормы.Руководитель);
	СтруктураДанныхТитульный.Вставить("ОргДиректор", СтруктураРеквизитовФормы.Руководитель);

	СтруктураРеквизитовФормы.Бухгалтер = Неопределено;
	ПоказателиТаблПоле.Свойство("ОргБухгалтер", СтруктураРеквизитовФормы.Бухгалтер);
	СтруктураРеквизитовФормы.Бухгалтер = ?(СтруктураРеквизитовФормы.Бухгалтер = Неопределено, "", СтруктураРеквизитовФормы.Бухгалтер);
	СтруктураДанныхТитульный.Вставить("ОргБухгалтер", СтруктураРеквизитовФормы.Бухгалтер);

	СтруктураРеквизитовФормы.ТелОрганизации = Неопределено;
	ПоказателиТаблПоле.Свойство("ТелОрганизации", СтруктураРеквизитовФормы.ТелОрганизации);
	СтруктураРеквизитовФормы.ТелОрганизации = ?(СтруктураРеквизитовФормы.ТелОрганизации = Неопределено, "", СтруктураРеквизитовФормы.ТелОрганизации);
	СтруктураДанныхТитульный.Вставить("ТелОрганизации", СтруктураРеквизитовФормы.ТелОрганизации);

КонецПроцедуры

Функция СкопироватьЗначение(знач Значение) Экспорт

// Поскольку передается по значению - уже имеем копию.
// только для простых типов!
	Возврат Значение;

КонецФункции

Функция СтруктураВСписокЗначений(СтруктураЗначений) Экспорт

	ЗначенияПолей = Новый СписокЗначений;

	Для каждого ЭлементСтруктуры Из СтруктураЗначений Цикл

		ПредставлениеВСписке = ЭлементСтруктуры.Ключ;
		ЗначениеВСписке = ЭлементСтруктуры.Значение;

		ЗначенияПолей.Добавить(ЗначениеВСписке, ПредставлениеВСписке);

	КонецЦикла;

	Возврат ЗначенияПолей;

КонецФункции

Функция ДесятичноеЧислоИзЧислаДругойСистемыСчисления(знач СтрокаЧислаВСистемеСчисления,
		СтрокаЦифрСистемыСчисления = "0123456789ABCDEF") Экспорт

	СтрокаЧислаВСистемеСчисления = ВРег(СтрокаЧислаВСистемеСчисления);

	ОснованиеСчисления = СтрДлина(СтрокаЦифрСистемыСчисления);

	ДлинаСтрокиЧислаВСистемеСчисления = СтрДлина(СтрокаЧислаВСистемеСчисления);

	Результат = 0;

	Для Позиция = 1 По ДлинаСтрокиЧислаВСистемеСчисления Цикл

		ТекущаяЦифра = Сред(СтрокаЧислаВСистемеСчисления, Позиция, 1);
		ДесятичноеПредставлениеЦифры = Найти(СтрокаЦифрСистемыСчисления, ТекущаяЦифра)
			- 1;

		Результат = Результат * ОснованиеСчисления + ДесятичноеПредставлениеЦифры;

	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ВерноеИмяКлючаСтруктуры(ИмяКлюча) Экспорт

	Если НЕ ТипЗнч(ИмяКлюча) = Тип("Строка") Тогда
		Возврат Ложь;
	КонецЕсли;

	ДлинаКлюча = СтрДлина(ИмяКлюча);
	Если ДлинаКлюча = 0 Тогда
		Возврат Ложь;
	КонецЕсли;

	// Ключ должен соответствовать требованиям к именам переменных 1С.
	// Т.е состоять из букв, цифр и знака подчеркивания, причем первый символ не может быть цифрой.
	// Знак подчеркивания тоже будем считать неверным для первого символа.

	// Цифры в Unicode 0030 - 0039
	// Латинские буквы в Unicode 0041 - 005A, 0061 - 007A
	// Русские буквы 0410 - 044F
	// Знак подчеркивания 005F
	СтартЦифры = 48; //0030
	КонецЦифры = 57; //0039
	СтартБольшиеЛатинские = 65; //0041
	КонецБольшиеЛатинские = 90; //005A
	СтартМаленькиеЛатинские = 97; //0061
	КонецМаленькиеЛатинские = 122; //007A
	СтартРусские = 1040; //0410
	КонецРусские = 1103; //044F
	КодПодчеркивания = 95; //005F
	Для Позиция = 1 По ДлинаКлюча Цикл

		КодСимволаUnicode = КодСимвола(ИмяКлюча, Позиция);

		Если НЕ ((КодСимволаUnicode >= СтартЦифры И КодСимволаUnicode <= КонецЦифры)
				ИЛИ (КодСимволаUnicode >= СтартБольшиеЛатинские
				И КодСимволаUnicode <= КонецБольшиеЛатинские)
				ИЛИ (КодСимволаUnicode >= СтартМаленькиеЛатинские
				И КодСимволаUnicode <= КонецМаленькиеЛатинские)
				ИЛИ (КодСимволаUnicode >= СтартРусские И КодСимволаUnicode <= КонецРусские)
				ИЛИ (КодСимволаUnicode = КодПодчеркивания)) Тогда
			Возврат Ложь;
		КонецЕсли;

	КонецЦикла;

	// Проверим чтобы первый символ не был цифрой или подчеркиванием.
	КодПервыйСимвол = КодСимвола(ИмяКлюча, 1);

	Если (КодПервыйСимвол >= СтартЦифры И КодПервыйСимвол <= КонецЦифры)
			ИЛИ (КодПервыйСимвол = КодПодчеркивания) Тогда

		Возврат Ложь;

	КонецЕсли;

	Возврат Истина;

КонецФункции

// Опеределяет все ли символы в переданной строке - цифры. 
// Не используется посимвольный перебор, благодаря чему работает очень быстро,
// что важно при большом объеме проверок.
// Не подходит для проверки строки произвольной длины.
//
// Параметры:
// 		ПроверяемаяСтрока		- Строка.
//
// Возвращаемое значение:
// 		Булево - Истина если в строке все символы являются цифрами.
//
Функция ВСтрокеВсеЦифры(ПроверяемаяСтрока) Экспорт

	Если ПустаяСтрока(ПроверяемаяСтрока) = 1 Тогда
	// пустая строка не является строкой цифр
		Возврат Ложь;
	КонецЕсли;

	//Добавим цифру "1" для обработки ситуаций:
	// - наличие лидирующих нулей;
	// - введен один единственный символ не цифра. 
	Попытка
		ДлинаПроверяемойСтроки = СтрДлина(Формат(Цел(Число("1"
			+ СокрЛП(ПроверяемаяСтрока))), "ЧГ=0; ЧВН="));
	Исключение
	// Если возникло исключение - значит не смогли преобразовать к числу "1" + СокрЛП(ПроверяемаяСтрока).
		Возврат Ложь;
	КонецПопытки;

	Возврат ДлинаПроверяемойСтроки = (СтрДлина(ПроверяемаяСтрока) + 1);

КонецФункции

Функция ЛицензируемаяДеятельностьАЛКО(ТаблицаСтраницыЛицензии,
		ИмяПоляКодЛицензии = "П000000000105") Экспорт

	Если ТаблицаСтраницыЛицензии.Количество() < 1
			Или ТаблицаСтраницыЛицензии[0].Данные.Количество() < 1
			Или ТаблицаСтраницыЛицензии[0].ДополнительныеДанные.Количество() < 1 Тогда
			// По умолчанию считаем нелицензируемой.
		Возврат Ложь;
	КонецЕсли;

	// Если деятельность нелицензируемая, то все страницы могут содержать в поле
	// П000000000105 только строку, 
	// соответствующую в макете Списки в области Виды Деятельности коду 12.

	// Код вида деятельности сохраняется в Дополнительных Данных
	// в структуре с ключем П000000000105

	// Фактически при реализации в виде многострочной страницы на обычных формах
	// это означает одну единственную страницу с индексом 0.
	СтруктураДополнительныеДанные = ТаблицаСтраницыЛицензии[0].ДополнительныеДанные[0].Значение;

	КодВидДеятельности = Неопределено;

	// КодВидДеятельности в структуре имеет тип строка 2 символа.
	КодВидДеятельности = РегламентированнаяОтчетностьАЛКОКлиентСервер.СтрЗначениеПоКлючуСтруктуры(ИмяПоляКодЛицензии, СтруктураДополнительныеДанные);

	// НЕ нелицензируемые виды деятельности начинаются от значения кода 08.
	Возврат КодТребуетЛицензию(КодВидДеятельности);

КонецФункции

Функция КодТребуетЛицензию(КодВидДеятельности) Экспорт

	КодЗаведомоНелицензионный = 100;
	ГраницаОпределенияЛицензионности = 8;
	
	// По умолчанию считаем нелицензируемой.
	КодВидДеятельности = ?(ЗначениеЗаполнено(КодВидДеятельности), Число(КодВидДеятельности), КодЗаведомоНелицензионный);

	// НЕ лицензируемые виды деятельности начинаются от значения кода 08.
	Возврат КодВидДеятельности < ГраницаОпределенияЛицензионности;
	
КонецФункции

Функция СтрокаБезСимволов(НеправильнаяСтрока, СтрокаУбираемых = " .,""'«»()<>:;[]{}\|/?*&^%$#№@!~`+=-_") Экспорт

	Результат = Строка(НеправильнаяСтрока);
	ДлинаСтрокиУбираемых = СтрДлина(СтрокаУбираемых);
	
	Для НомерСимвола = 1 По ДлинаСтрокиУбираемых Цикл
	
		УбираемыйСимвол = Сред(СтрокаУбираемых, НомерСимвола, 1);
		Результат = СтрЗаменить(Результат, УбираемыйСимвол, "");
	
	КонецЦикла; 
	
	Возврат Результат;
	
КонецФункции

Функция ПустойАдресXMLилиJSON(Вариант = "JSON", ДляСравнения = Ложь) Экспорт
	
	Если НЕ ТипЗнч(Вариант) = Тип("Строка") Тогда
		Вариант = "JSON";
	ИначеЕсли НЕ (Вариант = "JSON" ИЛИ Вариант = "XML") Тогда	
		Вариант = "JSON";		
	КонецЕсли; 

	Если Вариант = "JSON" Тогда
		
		Если ДляСравнения Тогда
		
			Результат = """area"": """",
						|""areaType"": """",
						|""city"": """",
						|""cityType"": """",
						|""street"": """",
						|""streetType"": """",
						|""id"": """",
						|""areaCode"": """",
						|""areaId"": """",
						|""district"": """",
						|""districtType"": """",
						|""districtId"": """",
						|""munDistrict"": """",
						|""munDistrictType"": """",
						|""munDistrictId"": """",
						|""cityId"": """",
						|""settlement"": """",
						|""settlementType"": """",
						|""settlementId"": """",
						|""cityDistrict"": """",
						|""cityDistrictType"": """",
						|""cityDistrictId"": """",
						|""territory"": """",
						|""territoryType"": """",
						|""territoryId"": """",
						|""locality"": """",
						|""localityType"": """",
						|""localityId"": """",
						|""streetId"": """",
						|""houseType"": """",
						|""houseNumber"": """",
						|""houseId"": """",
						|""buildings"": [],
						|""apartments"": [],
						|""codeKLADR"": """",
						|""oktmo"": """",
						|""okato"": """",
						|""asInDocument"": """",
						|""ifnsFLCode"": """",
						|""ifnsULCode"": """",
						|""ifnsFLAreaCode"": """",
						|""ifnsULAreaCode"": """"
						|}";
			
		Иначе
			
			Результат = "{
						|""value"": """",
						|""comment"": """",
						|""type"": ""Адрес"",
						|""country"": ""РОССИЯ"",
						|""addressType"": ""Административно-территориальный"",
						|""countryCode"": ""643"",
						|""ZIPcode"": """",
						|""area"": """",
						|""areaType"": """",
						|""city"": """",
						|""cityType"": """",
						|""street"": """",
						|""streetType"": """",
						|""id"": """",
						|""areaCode"": """",
						|""areaId"": """",
						|""district"": """",
						|""districtType"": """",
						|""districtId"": """",
						|""munDistrict"": """",
						|""munDistrictType"": """",
						|""munDistrictId"": """",
						|""cityId"": """",
						|""settlement"": """",
						|""settlementType"": """",
						|""settlementId"": """",
						|""cityDistrict"": """",
						|""cityDistrictType"": """",
						|""cityDistrictId"": """",
						|""territory"": """",
						|""territoryType"": """",
						|""territoryId"": """",
						|""locality"": """",
						|""localityType"": """",
						|""localityId"": """",
						|""streetId"": """",
						|""houseType"": """",
						|""houseNumber"": """",
						|""houseId"": """",
						|""buildings"": [],
						|""apartments"": [],
						|""codeKLADR"": """",
						|""oktmo"": """",
						|""okato"": """",
						|""asInDocument"": """",
						|""ifnsFLCode"": """",
						|""ifnsULCode"": """",
						|""ifnsFLAreaCode"": """",
						|""ifnsULAreaCode"": """"
						|}";	
		
		КонецЕсли; 
		
	Иначе
		
		Если ДляСравнения Тогда
			Результат =	"
						|      <СубъектРФ/>
						|      <Округ/>
						|      <СвРайМО>
						|        <Район/>
						|      </СвРайМО>
						|      <Город/>
						|      <ВнутригРайон/>
						|      <НаселПункт/>
						|      <Улица/>
						|    </Состав>
						|  </Состав>
						|</КонтактнаяИнформация>";
		Иначе
			Результат =	"<КонтактнаяИнформация xmlns:xsi=""http://www.w3.org/2001/XMLSchema-instance"" xmlns:xs=""http://www.w3.org/2001/XMLSchema"" xmlns=""http://www.v8.1c.ru/ssl/contactinfo"" Представление="""">
						|  <Комментарий/>
						|  <Состав xsi:type=""Адрес"" Страна=""РОССИЯ"">
						|    <Состав xsi:type=""АдресРФ"">
						|      <СубъектРФ/>
						|      <Округ/>
						|      <СвРайМО>
						|        <Район/>
						|      </СвРайМО>
						|      <Город/>
						|      <ВнутригРайон/>
						|      <НаселПункт/>
						|      <Улица/>
						|    </Состав>
						|  </Состав>
						|</КонтактнаяИнформация>";	
		КонецЕсли
				
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция XMLилиJSONНеПуст(XMLилиJSON) Экспорт
	
	Если ПустаяСтрока(XMLилиJSON) Тогда
		Возврат Ложь;	
	КонецЕсли; 
	
	Если СтрНайти(XMLилиJSON, ПустойАдресXMLилиJSON("JSON", Истина)) > 0 Тогда
		Возврат Ложь;	
	КонецЕсли;
	
	Если СтрНайти(XMLилиJSON, ПустойАдресXMLилиJSON("XML", Истина)) > 0 Тогда
		Возврат Ложь;	
	КонецЕсли;
	
	Возврат Истина;

КонецФункции
 
Процедура ЗаполнитьЗаписьРегистраЛицензийИзСтруктурыАдреса(Запись, СтруктураАдреса) Экспорт
	
	Запись.П000000000202 = СтруктураАдреса.Индекс;
    Запись.П000000000203 = СтруктураАдреса.КодРегиона;
	Запись.П000000000204 = СтруктураАдреса.Район;
	
	Запись.П000000000205 = ДополнитьСтроку(СтруктураАдреса.Город, СтруктураАдреса.НаселенныйПункт, " / ");
	
	Запись.П000000000206 = СтруктураАдреса.Улица;
	
	СтрокаДома = СтруктураАдреса.Дом;
	СтрокаДома = ДополнитьСтроку(СтрокаДома, СтруктураАдреса.Корпус, ", ");
	СтрокаДома = ДополнитьСтроку(СтрокаДома, СтруктураАдреса.Литера, ", ");
	СтрокаДома = ДополнитьСтроку(СтрокаДома, СтруктураАдреса.Квартира, ", ");
	
	Запись.П000000000207 = СтрокаДома;	

КонецПроцедуры

Функция ДополнитьСтроку(ДополняемаяСтрока, Дополнение, Разделитель = ", ") Экспорт
	
	Результат = ДополняемаяСтрока;
	
	Если Дополнение <> "" Тогда
		Если ДополняемаяСтрока <> "" Тогда
			Результат = ДополняемаяСтрока + Разделитель + Дополнение;
		Иначе	
			Результат = Дополнение;
		КонецЕсли;		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
 
Процедура ОбъединитьСтруктурыИлиСоответствия(Приемник, Источник, 
			ТолькоНепустыеИсточника = Ложь, ТолькоПустыеПриемника = Ложь) Экспорт
	
	ПриемникЭтоСтруктура = ЭтоСтруктура(Приемник);
	ПриемникЭтоСоответствие = ЭтоСоответствие(Приемник);
	
	Для каждого КлючИЗначение Из Источник Цикл
		
		Если ТолькоНепустыеИсточника Тогда
			// Заполняем только если в источнике заполнено.
			Если ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
				
				Если ТолькоПустыеПриемника Тогда
					// Заполняем только если в приемнике нет ключа или есть но значение не заполнено.
					Если 
						(ПриемникЭтоСтруктура И ( НЕ Приемник.Свойство(КлючИЗначение.Ключ)) )
						ИЛИ
						(ПриемникЭтоСтруктура И Приемник.Свойство(КлючИЗначение.Ключ) 
							И ( НЕ ЗначениеЗаполнено(Приемник[КлючИЗначение.Ключ]) ) )
						ИЛИ
						(ПриемникЭтоСоответствие И ( НЕ ЗначениеЗаполнено(Приемник[КлючИЗначение.Ключ]) ) )
						Тогда
							
						Приемник.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
					КонецЕсли;
					
				Иначе
					Приемник.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			Если ТолькоПустыеПриемника Тогда
				// Заполняем только если в приемнике нет ключа или есть но значение не заполнено.
				Если 
					(ПриемникЭтоСтруктура И ( НЕ Приемник.Свойство(КлючИЗначение.Ключ)) )
					ИЛИ
					(ПриемникЭтоСтруктура И Приемник.Свойство(КлючИЗначение.Ключ) 
						И ( НЕ ЗначениеЗаполнено(Приемник[КлючИЗначение.Ключ]) ) )
					ИЛИ
					(ПриемникЭтоСоответствие И ( НЕ ЗначениеЗаполнено(Приемник[КлючИЗначение.Ключ]) ) )
					Тогда
						
					Приемник.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
				КонецЕсли;
				
			Иначе
				Приемник.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			КонецЕсли;
			
		КонецЕсли;
		
		// В любом случае заполняем, если в Приемнике нет ключа.
		Если 
			(ПриемникЭтоСтруктура И (НЕ Приемник.Свойство(КлючИЗначение.Ключ)) )
			ИЛИ
			(ПриемникЭтоСоответствие И Приемник[КлючИЗначение.Ключ] = Неопределено)
			Тогда
				
			Приемник.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

Функция МассивНомеровВиноградныхДеклараций() Экспорт

	МассивВиноградных = Новый Массив;
	
	МассивВиноградных.Добавить(13);
	МассивВиноградных.Добавить(14);
	МассивВиноградных.Добавить(15);
	МассивВиноградных.Добавить(27);
	МассивВиноградных.Добавить(28);
	МассивВиноградных.Добавить(29); 
	МассивВиноградных.Добавить(32);
	МассивВиноградных.Добавить(33);
	МассивВиноградных.Добавить(34);
	
	Возврат МассивВиноградных;

КонецФункции 

Функция ЕстьАдресаВСтрокахТаблицОтчетаАЛКО(НомерАлкоОтчета) Экспорт
	
	Если НомерАлкоОтчета = 18 
		ИЛИ (НомерАлкоОтчета >= 20 И НомерАлкоОтчета <= 26)
		// Фармацевтические - закупки-поставки.
		ИЛИ НомерАлкоОтчета = 31 ИЛИ НомерАлкоОтчета = 32
		ИЛИ НомерАлкоОтчета = 35 ИЛИ НомерАлкоОтчета = 36
		Тогда
	
		Возврат Истина;
	
	КонецЕсли; 
	
	Возврат Ложь;

КонецФункции

Функция ПроцентБезКонвертацииАрхивации(ВидПечати, ТипЭкспорта) Экспорт

	Если ВидПечати = "ПечатьФайлZIP" ИЛИ ВидПечати = "Экспорт" Тогда
		
		Если ТипЭкспорта = ТипФайлаПакетаОтображаемыхДокументов.PDF Тогда
			ПроцентБезКонвертацииАрхивации = 14;
		ИначеЕсли ТипЭкспорта = ТипФайлаПакетаОтображаемыхДокументов.XLSX
			ИЛИ ТипЭкспорта = ТипФайлаПакетаОтображаемыхДокументов.XLS
			ИЛИ ТипЭкспорта = ТипФайлаПакетаОтображаемыхДокументов.ODS
			Тогда
			ПроцентБезКонвертацииАрхивации = 55;
		ИначеЕсли ТипЭкспорта = ТипФайлаТабличногоДокумента.MXL Тогда
		// Только сжать.
			ПроцентБезКонвертацииАрхивации = 95;
		ИначеЕсли ТипЭкспорта = ТипФайлаПакетаОтображаемыхДокументов.HTML4 
			ИЛИ ТипЭкспорта = ТипФайлаПакетаОтображаемыхДокументов.HTML5
			Тогда
			ПроцентБезКонвертацииАрхивации = 20;
		КонецЕсли;

	Иначе
		ПроцентБезКонвертацииАрхивации = 100;
	КонецЕсли;
	
	Возврат ПроцентБезКонвертацииАрхивации;

КонецФункции

Функция СоответсвиеРасширенийЭкспорта() Экспорт

	СоответсвиеРасширений = Новый Соответствие;
		
	СоответсвиеРасширений.Вставить(ТипФайлаПакетаОтображаемыхДокументов.PDF, ".pdf");
	СоответсвиеРасширений.Вставить(ТипФайлаПакетаОтображаемыхДокументов.XLS, ".xls");
	СоответсвиеРасширений.Вставить(ТипФайлаПакетаОтображаемыхДокументов.XLSX, ".xlsx");
	СоответсвиеРасширений.Вставить(ТипФайлаТабличногоДокумента.MXL, ".mxl");
	СоответсвиеРасширений.Вставить(ТипФайлаПакетаОтображаемыхДокументов.DOCX, ".docx");
	СоответсвиеРасширений.Вставить(ТипФайлаПакетаОтображаемыхДокументов.ODS, ".ods");
	СоответсвиеРасширений.Вставить(ТипФайлаПакетаОтображаемыхДокументов.HTML4, ".html");
	
	Возврат СоответсвиеРасширений;
	
КонецФункции

Функция ПравильноеИмяФайла(ИмяФайла) Экспорт

	ИмяФайла = Стрзаменить(ИмяФайла, " ", "-");
	ИмяФайла = Стрзаменить(ИмяФайла, ".", "");
	ИмяФайла = Стрзаменить(ИмяФайла, ",", "");
	ИмяФайла = Стрзаменить(ИмяФайла, ":", "");
	ИмяФайла = Стрзаменить(ИмяФайла, ";", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "?", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "!", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "№", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "#", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "@", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "~", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "`", "");
	ИмяФайла = Стрзаменить(ИмяФайла, """", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "'", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "/", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "\", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "|", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "[", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "]", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "{", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "}", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "+", "-");
	ИмяФайла = Стрзаменить(ИмяФайла, "*", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "&", "-");
	ИмяФайла = Стрзаменить(ИмяФайла, "^", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "=", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "%", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "$", "");
	ИмяФайла = Стрзаменить(ИмяФайла, ">", "");
	ИмяФайла = Стрзаменить(ИмяФайла, "<", "");
	
	Возврат ИмяФайла;

КонецФункции

Функция СкопироватьРекурсивноАЛКО(Источник, ФиксироватьДанные = Неопределено) Экспорт

	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Возврат ОбщегоНазначения.СкопироватьРекурсивно(Источник, ФиксироватьДанные);
	#Иначе
		Возврат ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Источник, ФиксироватьДанные);
	#КонецЕсли

КонецФункции   

Процедура Пауза(ЗадержкаВМиллиСек = 100, ОписаниеОповещения = Неопределено) Экспорт

#Если Сервер Или ВнешнееСоединение Тогда
#Иначе
	Если ОписаниеОповещения = Неопределено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения
	КонецЕсли;
#КонецЕсли

	ДождатьсяЗавершения = Истина; // Это важно.

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	ЭтоWindows = РегламентированнаяОтчетностьАЛКОВызовСервера.ЭтоWindowsСервер();
#Иначе
	ЭтоWindows = РегламентированнаяОтчетностьАЛКОКлиент.ЭтоWindowsКлиент();
#КонецЕсли

	Если ЭтоWindows Тогда
		ТекстКомандыПриложения = "ping 1.1.1.1 -n 1 -w " + СтрокаЧГ0(ЗадержкаВМиллиСек) + " >NUL";
	Иначе
		// Linux или OS X
		ТекстКомандыПриложения = "sleep " + СтрЗаменить(СтрокаЧГ0(Окр(ЗадержкаВМиллиСек / 1000, 3)), ",", ".") + "s";	
	КонецЕсли
	;

#Если Сервер Или ВнешнееСоединение Тогда
	КодВозврата = Неопределено;
	ЗапуститьПриложение(ТекстКомандыПриложения, , ДождатьсяЗавершения, КодВозврата);
#Иначе
	НачатьЗапускПриложения(ОписаниеОповещения, ТекстКомандыПриложения, , ДождатьсяЗавершения);
#КонецЕсли

КонецПроцедуры


Процедура СообщитьПользователюАЛКО(
		Знач ТекстСообщенияПользователю,
		Знач КлючДанных = Неопределено,
		Знач Поле = "",
		Знач ПутьКДанным = "",
		Отказ = Ложь) Экспорт
	
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщенияПользователю, КлючДанных, Поле, ПутьКДанным, Отказ);
	#Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщенияПользователю, КлючДанных, Поле, ПутьКДанным, Отказ);
	#КонецЕсли
	
КонецПроцедуры

Функция ОбособленноеПодразделениеЛиста(Форма, СтраницаРаздела) Экспорт
	
	СведенияОбОП = СкопироватьРекурсивноАЛКО(Форма.СтруктураРеквизитовФормы.ПустаяСтруктураСведенийОбОП);
	
	Если СтраницаРаздела.ДополнительныеДанные.Количество() = 0 Тогда
		СтраницаРаздела.ДополнительныеДанные.Добавить(Новый Структура);
	КонецЕсли;
	
	СохраненныеСведения = Неопределено;

	Если СтраницаРаздела.ДополнительныеДанные[0].Значение.Свойство("СведенияОбОП", СохраненныеСведения) Тогда
		ЗаполнитьЗначенияСвойств(СведенияОбОП, СохраненныеСведения);
	КонецЕсли;
	
	Возврат СведенияОбОП;

КонецФункции

Функция ТекущееОбособленноеПодразделение(Форма) Экспорт
	
	Возврат ОбособленноеПодразделениеЛиста(Форма, Форма.АктивнаяСтраницаРаздела)
	
КонецФункции

Функция НуженРеквизитОтсутствуетИнтернет(Форма) Экспорт
	
	Результат = Ложь;
	
	ИмяФормы = Форма.ИмяФормы;
	ЭтоПриложение7 = (СтрНайти(ИмяФормы, "РегламентированныйОтчетАлкоПриложение25") > 0);
	Если ЭтоПриложение7 Тогда
	
		ГодОтчета = Год(Форма.СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
		
		Если 
			ГодОтчета > 2025
			ИЛИ
			(ГодОтчета = 2025 И СтрНайти(ИмяФормы, "ФормаОтчета2025Кв1") > 0)
			Тогда
			
			Результат = Истина;
			
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция СтраницыРазделаОтчета(Форма, знач ИмяРаздела) Экспорт
	
	ИмяРаздела = СокрЛП(ИмяРаздела);
	
	Для каждого Раздел Из Форма.мСтруктураМногостраничныхРазделов Цикл

		НазваниеРаздела = Раздел.Ключ;
		
		Если НазваниеРаздела = ИмяРаздела Тогда
		
			ИмяТаблицыРаздела = Раздел.Значение;
			Возврат Форма[ИмяТаблицыРаздела];
		
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;

КонецФункции

Функция КлючОПбезНаименования(ПредставлениеАдреса, КПП = "", 
							  ВидРозничнойПродажи = 0, ОтсутствуетИнтернет = Ложь) Экспорт

	Возврат СтрокаБезСимволов(ПредставлениеАдреса)
			+ ?(ЗначениеЗаполнено(СокрЛП(Строка(КПП))), СокрЛП(Строка(КПП)), "")
			+ СтрокаЧГ0(ВидРозничнойПродажи) + Строка(ОтсутствуетИнтернет);

КонецФункции

Функция КлючОПбезНаименованияПоОП(ОП) Экспорт
	
	Перем ВидРозничнойПродажи, ОтсутствуетИнтернет;
	
	ОП.Свойство("ВидРозничнойПродажи", ВидРозничнойПродажи);
	ОП.Свойство("ОтсутствуетИнтернет", ОтсутствуетИнтернет);
	
	ВидРозничнойПродажи = ?(ВидРозничнойПродажи = Неопределено, 0, ВидРозничнойПродажи);
	ОтсутствуетИнтернет = ?(ОтсутствуетИнтернет = Неопределено, Ложь, ОтсутствуетИнтернет);
		
    Возврат КлючОПбезНаименования(ОП.ПредставлениеАдреса, ОП.КПП, 
								ВидРозничнойПродажи, ОтсутствуетИнтернет);
КонецФункции

Функция ВсеОПОтчетаИсключаяЛицензии(Форма) Экспорт
	
	Результат = Новый Соответствие;
	
	Для каждого Раздел Из Форма.мСтруктураМногостраничныхРазделов Цикл

		НазваниеРаздела = Раздел.Ключ;
		
		Если НазваниеРаздела <> "Лицензии" Тогда
		
			ИмяТаблицыРаздела = Раздел.Значение;
			ТаблицаСтраницРаздела = Форма[ИмяТаблицыРаздела];
			
			Для каждого СтраницаРаздела Из ТаблицаСтраницРаздела Цикл
			
				ТекОП = ОбособленноеПодразделениеЛиста(Форма, СтраницаРаздела);
				КлючТекОП = КлючОПбезНаименованияПоОП(ТекОП);
				
				Если ЗначениеЗаполнено(КлючТекОП) Тогда
					Результат[КлючТекОП] = ТекОП;
				КонецЕсли;
				
			КонецЦикла;
		
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

Функция ЕстьОПвДекларации(Форма, КоличествоОП, ПредставлениеАдреса, КПП = "", 
						  ВидРозничнойПродажи = 0, ОтсутствуетИнтернет = Ложь) Экспорт

	ВсеОП = ВсеОПОтчетаИсключаяЛицензии(Форма);
	
	КоличествоОП = ВсеОП.Количество();
	
	КлючОП = КлючОПбезНаименования(ПредставлениеАдреса, КПП, ВидРозничнойПродажи, ОтсутствуетИнтернет);
	
	Результат = (ВсеОП[КлючОП] <> Неопределено);
	
	Возврат Результат;

КонецФункции

Процедура ВосстановитьДанныеПодчиненныхЭлементовФормы(СтруктураДанныхПриемник,
		СтруктураДанныхИсточник, ПодчиненныеЭлементы, ИдГруппы = "") Экспорт

	Для каждого Элемент Из ПодчиненныеЭлементы Цикл

		ИмяРеквизита = Элемент.Имя;

		Если ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда

			ТекПодчиненныеЭлементы = Элемент.ПодчиненныеЭлементы;
			ВосстановитьДанныеПодчиненныхЭлементовФормы(СтруктураДанныхПриемник, СтруктураДанныхИсточник, ТекПодчиненныеЭлементы, ИдГруппы);
			
			Продолжить;
			
		КонецЕсли;
        		
		Если ТипЗнч(Элемент) = Тип("ПолеФормы") Тогда
			Если Элемент.Вид = ВидПоляФормы.ПолеВвода Тогда
				ЗначениеПоля = Неопределено;
				СтруктураДанныхИсточник.Свойство(ИмяРеквизита, ЗначениеПоля);
				СтруктураДанныхПриемник.Вставить(ИмяРеквизита, ЗначениеПоля);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Функция ПодсистемаСуществует(Имя) Экспорт
	
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Возврат ОбщегоНазначения.ПодсистемаСуществует(Имя);
	#Иначе
		Возврат ОбщегоНазначенияКлиент.ПодсистемаСуществует(Имя);
	#КонецЕсли
	
	Возврат Ложь;
	
КонецФункции

Функция ТекущийИнтерфейс85() Экспорт

	ТекущийВариант = КлиентскоеПриложение.ТекущийВариантИнтерфейса();
	
	Возврат (
				ТекущийВариант <> ВариантИнтерфейсаКлиентскогоПриложения.Такси
				И 
				ТекущийВариант <> ВариантИнтерфейсаКлиентскогоПриложения.Версия8_2
				И
				ПодсистемаСуществует("РегламентированнаяОтчетность.Интерфейс85")
			);

КонецФункции

Функция ЭтоИнтерфейс_8_5() Экспорт
	
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Возврат РегламентированнаяОтчетностьАЛКОВызовСервера.ТекущийИнтерфейс85();
	#Иначе
		Возврат ТекущийИнтерфейс85();
	#КонецЕсли
	
КонецФункции

// Возвращает приведенное к числу значение версии подсистемы.
// Параметры
//	 СтрокаВерсии - Строка. Строка версии вида "0.0.0.0".
// Возвращаемое значение:
//   Число.
//
Функция ПолучитьВерсиюЧислом(знач СтрокаВерсии) Экспорт

	ПервоеЧисло = 0;
	ВтороеЧисло = 0;
	ТретьеЧисло = 0;
	ЧетвертоеЧисло = 0;

	Если ТипЗнч(СтрокаВерсии) = Тип("Строка")
			И ЗначениеЗаполнено(СтрокаВерсии) Тогда

		СтрокаВерсииБезТочек = СтрЗаменить(СтрокаВерсии, ".", "");
		СтрокаВерсииБезТочек = СтрЗаменить(СтрокаВерсииБезТочек, " ", "");

		ПравильнаяСтрокаВерсии = ВСтрокеВсеЦифры(СтрокаВерсииБезТочек);

		Если НЕ ПравильнаяСтрокаВерсии Тогда
			Возврат 0;
		КонецЕсли;

		ПозТочка = СтрНайти(СтрокаВерсии, ".");
		Если ПозТочка > 1 Тогда

			ПервоеЧисло = Число(Лев(СтрокаВерсии, ПозТочка - 1));
			Если СтрДлина(СтрокаВерсии) >= ПозТочка + 1 Тогда
				СтрокаВерсии = Сред(СтрокаВерсии, ПозТочка + 1);
			Иначе
				СтрокаВерсии = "";
			КонецЕсли;

		ИначеЕсли ПозТочка = 1 Тогда

			ПервоеЧисло = 0;
			Если СтрДлина(СтрокаВерсии) >= ПозТочка + 1 Тогда
				СтрокаВерсии = Сред(СтрокаВерсии, ПозТочка + 1);
			Иначе
				СтрокаВерсии = "";
			КонецЕсли;

		Иначе

		// Нет точки.
			Если СтрДлина(СтрокаВерсии) > 0 Тогда
				ПервоеЧисло = Число(СтрокаВерсии);
			Иначе
				ПервоеЧисло = 0;
			КонецЕсли;
			СтрокаВерсии = "";

		КонецЕсли;

		ПозТочка = СтрНайти(СтрокаВерсии, ".");
		Если ПозТочка > 1 Тогда

			ВтороеЧисло = Число(Лев(СтрокаВерсии, ПозТочка - 1));
			Если СтрДлина(СтрокаВерсии) >= ПозТочка + 1 Тогда
				СтрокаВерсии = Сред(СтрокаВерсии, ПозТочка + 1);
			Иначе
				СтрокаВерсии = "";
			КонецЕсли;

		ИначеЕсли ПозТочка = 1 Тогда

			ВтороеЧисло = 0;
			Если СтрДлина(СтрокаВерсии) >= ПозТочка + 1 Тогда
				СтрокаВерсии = Сред(СтрокаВерсии, ПозТочка + 1);
			Иначе
				СтрокаВерсии = "";
			КонецЕсли;

		Иначе

		// Нет точки.
			Если СтрДлина(СтрокаВерсии) > 0 Тогда
				ВтороеЧисло = Число(СтрокаВерсии);
			Иначе
				ВтороеЧисло = 0;
			КонецЕсли;
			СтрокаВерсии = "";

		КонецЕсли;

		ПозТочка = СтрНайти(СтрокаВерсии, ".");
		Если ПозТочка > 1 Тогда

			ТретьеЧисло = Число(Лев(СтрокаВерсии, ПозТочка - 1));
			Если СтрДлина(СтрокаВерсии) >= ПозТочка + 1 Тогда
				СтрокаВерсии = Сред(СтрокаВерсии, ПозТочка + 1);
			Иначе
				СтрокаВерсии = "";
			КонецЕсли;

		ИначеЕсли ПозТочка = 1 Тогда

			ТретьеЧисло = 0;
			Если СтрДлина(СтрокаВерсии) >= ПозТочка + 1 Тогда
				СтрокаВерсии = Сред(СтрокаВерсии, ПозТочка + 1);
			Иначе
				СтрокаВерсии = "";
			КонецЕсли;

		Иначе

		// Нет точки.
			Если СтрДлина(СтрокаВерсии) > 0 Тогда
				ТретьеЧисло = Число(СтрокаВерсии);
			Иначе
				ТретьеЧисло = 0;
			КонецЕсли;
			СтрокаВерсии = "";

		КонецЕсли;

		ПозТочка = СтрНайти(СтрокаВерсии, ".");
		Если ПозТочка > 1 Тогда

			ЧетвертоеЧисло = Число(Лев(СтрокаВерсии, ПозТочка - 1));
			Если СтрДлина(СтрокаВерсии) >= ПозТочка + 1 Тогда
				СтрокаВерсии = Сред(СтрокаВерсии, ПозТочка + 1);
			Иначе
				СтрокаВерсии = "";
			КонецЕсли;

		ИначеЕсли ПозТочка = 1 Тогда

			ЧетвертоеЧисло = 0;
			Если СтрДлина(СтрокаВерсии) >= ПозТочка + 1 Тогда
				СтрокаВерсии = Сред(СтрокаВерсии, ПозТочка + 1);
			Иначе
				СтрокаВерсии = "";
			КонецЕсли;

		Иначе

		// Нет точки.
			Если СтрДлина(СтрокаВерсии) > 0 Тогда
				ЧетвертоеЧисло = Число(СтрокаВерсии);
			Иначе
				ЧетвертоеЧисло = 0;
			КонецЕсли;
			СтрокаВерсии = "";

		КонецЕсли;

	КонецЕсли;

	ЧислоСтрокаВерсии = ПервоеЧисло * 1000000000 + ВтороеЧисло * 1000000
		+ ТретьеЧисло * 1000 + ЧетвертоеЧисло;

	Возврат ЧислоСтрокаВерсии;

КонецФункции


// Меняет внешний вид управляемой формы в соответствии с настройками
// отчета из подсистемы ОтчетностьПоАлкогольнойПродукции.
//
// Параметры
//		Форма	- Управлемая форма отчета.
Процедура ИнициализироватьUIОтчетаАЛКО(Форма) Экспорт
	
	НастройкаUI = Неопределено;
	Форма.СтруктураРеквизитовФормы.Свойство("НастройкаUI", НастройкаUI);
	
	ИнициализироватьUIФормы(Форма, НастройкаUI);
	
КонецПроцедуры

Процедура МасштабироватьОтчетАЛКО(Форма, СписокМасштабируемыхИменЭлементов = Неопределено) Экспорт
	
	НастройкаUI = Неопределено;
	Форма.СтруктураРеквизитовФормы.Свойство("НастройкаUI", НастройкаUI);
	
	МасштабироватьФорму(Форма, НастройкаUI, СписокМасштабируемыхИменЭлементов);
	
КонецПроцедуры

Процедура МасштабироватьФорму(Форма, НастройкаUI = Неопределено, СписокМасштабируемыхИменЭлементов = Неопределено) Экспорт
	
	Если НастройкаUI = Неопределено Тогда	
		НастройкаUI = ДействующаяНастройкаUI(НастройкаUI);	
	КонецЕсли;
	
	ЭтоИнтерфейс_8_5 = ЭтоИнтерфейс_8_5();
	ЭтоФормаАЛКООтчета = ЭтоФормаАЛКООтчета(Форма);
	
	МасштабКлиентскогоПриложения = "";
	
	Если ЭтоИнтерфейс_8_5 Тогда	
		
		МодульРегламентированнаяОтчетностьИнтерфейс85ВызовСервера = ОбщийМодульКонфигурации("РегламентированнаяОтчетностьИнтерфейс85ВызовСервера");
		МасштабКлиентскогоПриложения = МодульРегламентированнаяОтчетностьИнтерфейс85ВызовСервера.МасштабКлиентскогоПриложения();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СписокМасштабируемыхИменЭлементов) Тогда
	
		МассивИменМасштабируемыхЭлементов = СтрРазделить(СписокМасштабируемыхИменЭлементов, ",", Ложь);
		
		Для каждого ИмяМасштабируемогоЭлемента Из МассивИменМасштабируемыхЭлементов Цикл
		
			МасштабируемыйЭлемент = Форма.Элементы.Найти(ИмяМасштабируемогоЭлемента);
			Если МасштабируемыйЭлемент <> Неопределено Тогда
			
				МасштабироватьЭлементФормы(Форма, МасштабируемыйЭлемент, НастройкаUI, ЭтоИнтерфейс_8_5,
											ЭтоФормаАЛКООтчета, МасштабКлиентскогоПриложения);
			
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		ПодчиненныеЭлементы = Форма.ПодчиненныеЭлементы;
		
		Для каждого ЭлементФормы Из ПодчиненныеЭлементы Цикл
		
			МасштабироватьЭлементФормы(Форма, ЭлементФормы, НастройкаUI, ЭтоИнтерфейс_8_5,
											ЭтоФормаАЛКООтчета, МасштабКлиентскогоПриложения);	
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Меняет внешний вид управляемой формы в соответствии с настройками.
//
// Параметры
//		Форма	- Управлемая форма.
//      НастройкаUI - Соответствие или Неопределено.
//
//			//== Общие == 
//			* ВключеноМасштабирование 	- Булево или Неопределено.
//
//			* МасштабироватьТолькоТаблицыФормы			- Булево или Неопределено.
//			* МасштабироватьОтдельноКаждуюТаблицуФормы	- Булево или Неопределено.
//			* МинимальноеКоличествоКолонокТаблицыФормыДляМасштабирования - Число или Неопределено.
//
//			* СписокИсключенияИменЭлементовПриМасштабировании_Интерфейс_8_5	- Строка или Неопределено.
//			Имена элементов, перечисленные через запятую, в отношении которых не будет применяться
//			масштабирование (только для интерфейса 8.5), включая все подчиненные элементы.
//
//			* НеМенятьШрифты			- Булево или Неопределено.
//			* НеМенятьЦвета				- Булево или Неопределено.
//			* ВыделятьЖирнымВыделенныеПоляНезависимоОтДругихНастроек		- Булево или Неопределено.
//			* ВыделятьПодчеркиваниемГиперссылкиНезависимоОтДругихНастроек	- Булево или Неопределено.
//
//			* ЗаголовкиСБольшойБуквы	- Булево или Неопределено.
//
//			* СписокИсключенияИменЭлементов									- Строка или Неопределено.
//			Имена элементов, перечисленные через запятую, в отношении которых не будет применяться 
//			настройка UI, включая все подчиненные элементы.
//
//
//
//			== Форма ==
//			* ФормаВертикальныйИнтервал		- Строка или Неопределено.
//			* ФормаГоризонтальныйИнтервал	- Строка или Неопределено.
//			Интервал между элементами формы принимает значения 
//			"Авто", "Двойной", "Нет", "Одинарный", "Половинный", "Полуторный".
//			== Группы ==
//			* ВертикальныйИнтервалГруппа	- Строка или Неопределено.
//			* ГоризонтальныйИнтервалГруппа	- Строка или Неопределено.
//			Интервал между элементами формы принимает значения 
//			"Авто", "Двойной", "Нет", "Одинарный", "Половинный", "Полуторный".
//			* МенятьШрифтЗаголовкаГруппа	- Булево или Неопределено.
//			* МенятьЦветЗаголовкаГруппа		- Булево или Неопределено.
//			* МенятьЦветФонаГруппа			- Булево или Неопределено.
//			== Страницы ==
//			* МенятьШрифтЗаголовкаСтраницы	- Булево или Неопределено.
//			* МенятьЦветЗаголовкаСтраницы	- Булево или Неопределено.
//			== Страница == 
//			* ВертикальныйИнтервалСтраница	- Строка или Неопределено.
//			* ГоризонтальныйИнтервалСтраница- Строка или Неопределено.
//			Интервал между элементами формы принимает значения 
//			"Авто", "Двойной", "Нет", "Одинарный", "Половинный", "Полуторный".
//			* МенятьШрифтЗаголовкаСтраница	- Булево или Неопределено.
//			* МенятьЦветЗаголовкаСтраница	- Булево или Неопределено.
//			* МенятьЦветФонаСтраница		- Булево или Неопределено.
//			== Поля ==
//			* МенятьШрифтЗаголовкаПолей	- Булево или Неопределено.
//			* МенятьЦветЗаголовкаПолей	- Булево или Неопределено.
//			* МенятьШрифтПолей			- Булево или Неопределено.
//			* МенятьЦветПолей			- Булево или Неопределено.
//			* МенятьЦветФонаПолей		- Булево или Неопределено.
//			* МенятьЦветРамкиПолей		- Булево или Неопределено.
//			== Выделенные поля ==
//			* МенятьШрифтЗаголовкаПолейВыделенное	- Булево или Неопределено.
//			* МенятьЦветЗаголовкаПолейВыделенное	- Булево или Неопределено.
//			* МенятьШрифтПолейВыделенное			- Булево или Неопределено.
//			* МенятьЦветПолейВыделенное				- Булево или Неопределено.
//			* МенятьЦветФонаПолейВыделенное			- Булево или Неопределено.
//			* МенятьЦветРамкиПолейВыделенное		- Булево или Неопределено.
//
//			== Таблицы формы ==
//
//			== Общие  ==
//			* МенятьФонТаблицы			- Булево или Неопределено.
//			* МенятьЦветРамкиТаблицы	- Булево или Неопределено.
//			* МенятьЦветТекстаТаблицы	- Булево или Неопределено.
//			* МенятьШрифтТекстаТаблицы	- Булево или Неопределено.
//
//			* ЗаголовкиКолонокСБольшойБуквы	- Булево или Неопределено.
//
//			* ЧередованиеЦветовСтрокТаблиц	- Булево или Неопределено.
//
//			* РежимВыделенияТаблиц			- Строка или Неопределено.
//			РежимВыделенияТаблиц принимает значения "Одиночный", "Множественный".
//			
//			* ИзменятьСоставСтрокТаблиц		- Булево или Неопределено.
// 					Внимание! Установка в Ложь отключает возможность добавления, копирования, удаления
// 					строк таблицы формы. Но сохраняется возможность редактирования существующих.
//			* ИзменятьПорядокСтрокТаблиц	- Булево или Неопределено.
//
//			== Заголовок таблиц ==
//			* МенятьШрифтЗаголовкаТаблиц	- Булево или Неопределено.
//			* МенятьЦветЗаголовкаТаблиц		- Булево или Неопределено.
//
//			== Заголовок колонок ==
//			* МенятьШрифтЗаголовкаКолонокТаблиц	- Булево или Неопределено.
//			* МенятьЦветЗаголовкаКолонокТаблиц	- Булево или Неопределено.
//			* МенятьФонЗаголовкаКолонокТаблиц	- Булево или Неопределено.
//
//			== Заголовок выделенных колонок ==
//			* МенятьШрифтЗаголовкаКолонокТаблицВыделенное	- Булево или Неопределено.
//			* МенятьЦветЗаголовкаКолонокТаблицВыделенное	- Булево или Неопределено.
//			* МенятьФонЗаголовкаКолонокТаблицВыделенное		- Булево или Неопределено.
//
//			== Подвал колонок ==
//			* МенятьШрифтПодвалаКолонокТаблиц	- Булево или Неопределено.
//			* МенятьЦветПодвалаКолонокТаблиц	- Булево или Неопределено.
//			* МенятьФонПодвалаКолонокТаблиц		- Булево или Неопределено.
//
//			== Подвал выделенных колонок ==
//			* МенятьШрифтПодвалаКолонокТаблицВыделенное	- Булево или Неопределено.
//			* МенятьЦветПодвалаКолонокТаблицВыделенное	- Булево или Неопределено.
//			* МенятьФонПодвалаКолонокТаблицВыделенное	- Булево или Неопределено.
//
//
//			* ИсточникСтиля	- 	Строка. 
//					Принимает значение "ИзСтиля_БРО_АЛКО" или любая другая строка.
//
//					Если значение равно "ИзСтиля_БРО_АЛКО" - цвета и шрифты берутся из
//					элементов стиля конфигурации с префиксом БРО_АЛКО_. 
//					Загружаются функцией 
//					РегламентированнаяОтчетностьАЛКОВызовСервера.Стили_БРО_АЛКО().
//
//					Если значение НЕ равно "ИзСтиля_БРО_АЛКО", стили определеяются в 
//					значении с ключем "Стиль".
//
//			Для всех ключей соответствия "НастройкаUI" значение Неопределено означает,
//			что будет использовано значение настройки по умолчанию.
//			Значения по умолчанию определяются в функции НастройкаUIПоУмолчанию().
//
//			Все ключи - необязательные.
//			Если ключ отсутствует - его значение Неопределено.
//
//			Если вместо соответствия передано Неопределено, все настройки по умолчанию
//			из функции НастройкаUIПоУмолчанию().
//
//			* Стиль - Соответствие или Неопределено.
//					Игнорируется, если ИсточникСтиля = "ИзСтиля_БРО_АЛКО".
//
//				** ГруппаВыделенноеПолеЗаголовокТекстЦвет 	- Цвет или Неопределено.
//				** ГруппаВыделенноеПолеЗаголовокШрифт		- Шрифт или Неопределено.
//				** ГруппаВыделенноеПолеРамкаЦвет 			- Цвет или Неопределено.
//				** ГруппаВыделенноеПолеТекстЦвет 			- Цвет или Неопределено.
//				** ГруппаВыделенноеПолеТекстШрифт			- Шрифт или Неопределено.
//				** ГруппаВыделенноеПолеФон 					- Цвет или Неопределено.
//				** ГруппаЗаголовокТекстЦвет 				- Цвет или Неопределено.
//				** ГруппаЗаголовокШрифт						- Шрифт или Неопределено.
//				** ГруппаПолеЗаголовокТекстЦвет 			- Цвет или Неопределено.
//				** ГруппаПолеЗаголовокШрифт					- Шрифт или Неопределено.
//				** ГруппаПолеРамкаЦвет 						- Цвет или Неопределено.
//				** ГруппаПолеТекстЦвет 						- Цвет или Неопределено.
//				** ГруппаПолеТекстШрифт						- Шрифт или Неопределено.
//				** ГруппаПолеФон 							- Цвет или Неопределено.
//				** ГруппаФон 								- Цвет или Неопределено.
//				** СтраницаВыделенноеПолеЗаголовокТекстЦвет - Цвет или Неопределено.
//				** СтраницаВыделенноеПолеЗаголовокШрифт		- Шрифт или Неопределено.
//				** СтраницаВыделенноеПолеРамкаЦвет 			- Цвет или Неопределено.
//				** СтраницаВыделенноеПолеТекстЦвет 			- Цвет или Неопределено.
//				** СтраницаВыделенноеПолеТекстШрифт			- Шрифт или Неопределено.
//				** СтраницаВыделенноеПолеФон 				- Цвет или Неопределено.
//				** СтраницаЗаголовокТекстЦвет 				- Цвет или Неопределено.
//				** СтраницаЗаголовокШрифт					- Шрифт или Неопределено.
//				** СтраницаПолеЗаголовокТекстЦвет 			- Цвет или Неопределено.
//				** СтраницаПолеЗаголовокШрифт				- Шрифт или Неопределено.
//				** СтраницаПолеРамкаЦвет 					- Цвет или Неопределено.
//				** СтраницаПолеТекстЦвет 					- Цвет или Неопределено.
//				** СтраницаПолеТекстШрифт					- Шрифт или Неопределено.
//				** СтраницаПолеФон 							- Цвет или Неопределено.
//				** СтраницаФон 								- Цвет или Неопределено.
//				** СтраницыЗаголовокТекстЦвет 				- Цвет или Неопределено.
//				** СтраницыЗаголовокШрифт					- Шрифт или Неопределено.
//				** ТаблицыЗаголовокКолонокТекстЦвет 		- Цвет или Неопределено.
//				** ТаблицыЗаголовокКолонокТекстЦветВыделенное 	- Цвет или Неопределено.
//				** ТаблицыЗаголовокКолонокТекстШрифт		- Шрифт или Неопределено.
//				** ТаблицыЗаголовокКолонокТекстШрифтВыделенное	- Шрифт или Неопределено.
//				** ТаблицыЗаголовокКолонокФон 				- Цвет или Неопределено.
//				** ТаблицыЗаголовокКолонокФонВыделенное 	- Цвет или Неопределено.
//				** ТаблицыЗаголовокЦвет 					- Цвет или Неопределено.
//				** ТаблицыЗаголовокШрифт					- Шрифт или Неопределено.
//				** ТаблицыПодвалКолонокТекстЦвет 			- Цвет или Неопределено.
//				** ТаблицыПодвалКолонокТекстЦветВыделенное 	- Цвет или Неопределено.
//				** ТаблицыПодвалКолонокТекстШрифт			- Шрифт или Неопределено.
//				** ТаблицыПодвалКолонокТекстШрифтВыделенное	- Шрифт или Неопределено.
//				** ТаблицыПодвалКолонокФон 					- Цвет или Неопределено.
//				** ТаблицыПодвалКолонокФонВыделенное 		- Цвет или Неопределено.
//				** ТаблицыРамкаЦвет 						- Цвет или Неопределено.
//				** ТаблицыТекстЦвет 						- Цвет или Неопределено.
//				** ТаблицыТекстШрифт						- Шрифт или Неопределено.
//				** ТаблицыФон	 							- Цвет или Неопределено.
//
//				Для всех ключей соответствия "Стиль" значение Неопределено означает, что 
//				цвет или шрифт меняться не будут. 
//				В случае если значение не отвечает типам Цвет или Шрифт для соответствующих ключей,
//				поведение как при значении Неопределено, т.е цвет или шрифт меняться не будут.
//
//				Все ключи - необязательные. 
//				Если ключ отсутствует в соответствии "Стиль" - его значение Неопределено.
//
//				Если вместо соответствия передано Неопределено или ключ "Стиль" отсутствует,
//				то поведение как если бы значения по всем ключам было Неопределено.
//
//				Выделенные поля определяются функцией ЭтоВыделенноеПолеСтрокой().
//				Для них действуют цвета и шрифты со строкой "Выделенное" в ключе.
Процедура ИнициализироватьUIФормы(Форма, НастройкаUI = Неопределено) Экспорт
	
	СброситьНастройкиОкна(Форма);
	
	Если НастройкаUI = Неопределено Тогда	
		НастройкаUI = ДействующаяНастройкаUI(НастройкаUI);	
	КонецЕсли;
	ЭтоИнтерфейс_8_5 = ЭтоИнтерфейс_8_5();
	ЭтоФормаАЛКООтчета = ЭтоФормаАЛКООтчета(Форма);
	
	МасштабКлиентскогоПриложения = "";
	
	Если ЭтоИнтерфейс_8_5 Тогда	
		
		МодульРегламентированнаяОтчетностьИнтерфейс85ВызовСервера = ОбщийМодульКонфигурации("РегламентированнаяОтчетностьИнтерфейс85ВызовСервера");
		МасштабКлиентскогоПриложения = МодульРегламентированнаяОтчетностьИнтерфейс85ВызовСервера.МасштабКлиентскогоПриложения();
		
	КонецЕсли;
	
	Форма.ВертикальныйИнтервал = ИнтервалМеждуЭлементамиФормы[НастройкаUI["ФормаВертикальныйИнтервал"]];
	Форма.ГоризонтальныйИнтервал = ИнтервалМеждуЭлементамиФормы[НастройкаUI["ФормаГоризонтальныйИнтервал"]];
	Форма.ЗакрыватьПриЗакрытииВладельца = Истина;
	Форма.ВертикальнаяПрокрутка = ВертикальнаяПрокруткаФормы.ИспользоватьПриНеобходимости;
	
	Форма.Масштаб = 100;
	
	ПодчиненныеЭлементы = Форма.ПодчиненныеЭлементы;
	
	Для каждого ЭлементФормы Из ПодчиненныеЭлементы Цикл
	
		ИнициализироватьUIЭлементаФормы(Форма, ЭлементФормы, НастройкаUI, ЭтоИнтерфейс_8_5,
										ЭтоФормаАЛКООтчета, МасштабКлиентскогоПриложения);	
		
	КонецЦикла;
	
КонецПроцедуры

Функция СуффиксИменЭлементовМенюПриКопировании() Экспорт

	Возврат "_Копия";

КонецФункции

Функция НастройкаUIПоУмолчанию() Экспорт

	НастройкаUI = Новый Соответствие;
	//== Общие == 
	НастройкаUI["ВключеноМасштабирование"] = Ложь;
	
	НастройкаUI["МасштабироватьТолькоТаблицыФормы"] = Истина;
	НастройкаUI["МасштабироватьОтдельноКаждуюТаблицуФормы"] = Истина;
	НастройкаUI["МинимальноеКоличествоКолонокТаблицыФормыДляМасштабирования"] = 10;
	
	НастройкаUI["НеМенятьШрифты"] 	= Истина;
	НастройкаUI["НеМенятьЦвета"] 	= Истина;
	НастройкаUI["ВыделятьЖирнымВыделенныеПоляНезависимоОтДругихНастроек"] 	= Ложь;
	НастройкаUI["ВыделятьПодчеркиваниемГиперссылкиНезависимоОтДругихНастроек"] 	= Ложь;
	
	НастройкаUI["СписокИсключенияИменЭлементов"] = 
		"ГруппаУправлениеЭДО,ГруппаПанельОтправки,ГруппаМенюРаздела";
	НастройкаUI["СписокИсключенияИменЭлементовПриМасштабировании_Интерфейс_8_5"] = 
		"ГруппаУправлениеЭДО,ГруппаПанельОтправки,ГруппаМенюРаздела";
	
	НастройкаUI["ЗаголовкиСБольшойБуквы"] = Истина;
	
	//== Форма == 
	// Интервал между элементами формы принимает значения 
	// "Авто", "Двойной", "Нет", "Одинарный", "Половинный", "Полуторный".
	НастройкаUI["ФормаВертикальныйИнтервал"] 	= "Авто";
	НастройкаUI["ФормаГоризонтальныйИнтервал"] 	= "Авто";
	
	//== Группы ==
	// Интервал между элементами формы принимает значения 
	// "Авто", "Двойной", "Нет", "Одинарный", "Половинный", "Полуторный".
	НастройкаUI["ВертикальныйИнтервалГруппа"] 	= "Авто";
	НастройкаUI["ГоризонтальныйИнтервалГруппа"] = "Авто";
	
	НастройкаUI["МенятьШрифтЗаголовкаГруппа"] 	= Ложь;
	НастройкаUI["МенятьЦветЗаголовкаГруппа"]	= Ложь;
	НастройкаUI["МенятьЦветФонаГруппа"]			= Ложь;
	
	//== Страницы ==
	НастройкаUI["МенятьШрифтЗаголовкаСтраницы"] = Ложь;
	НастройкаUI["МенятьЦветЗаголовкаСтраницы"]	= Ложь;
	
	//== Страница == 
	// Интервал между элементами формы принимает значения 
	// "Авто", "Двойной", "Нет", "Одинарный", "Половинный", "Полуторный".
	НастройкаUI["ВертикальныйИнтервалСтраница"] 	= "Авто";
	НастройкаUI["ГоризонтальныйИнтервалСтраница"]	= "Авто";
	
	НастройкаUI["МенятьШрифтЗаголовкаСтраница"] 	= Ложь;
	НастройкаUI["МенятьЦветЗаголовкаСтраница"]		= Ложь;
	НастройкаUI["МенятьЦветФонаСтраница"]			= Истина;
	
	//== Поля ==
	НастройкаUI["МенятьШрифтЗаголовкаПолей"] 	= Истина;
	НастройкаUI["МенятьЦветЗаголовкаПолей"]		= Истина; 
	НастройкаUI["МенятьШрифтПолей"] 			= Истина;
	НастройкаUI["МенятьЦветПолей"]				= Истина;
	НастройкаUI["МенятьЦветФонаПолей"]			= Истина;
	НастройкаUI["МенятьЦветРамкиПолей"]			= Истина;
	
	//== Выделенные поля ==
	НастройкаUI["МенятьШрифтЗаголовкаПолейВыделенное"] 	= Истина;
	НастройкаUI["МенятьЦветЗаголовкаПолейВыделенное"] 	= Истина;
	НастройкаUI["МенятьШрифтПолейВыделенное"]			= Истина; 
	НастройкаUI["МенятьЦветПолейВыделенное"] 			= Истина;
	НастройкаUI["МенятьЦветВыделенныхПолей"]			= Истина;
	НастройкаUI["МенятьЦветФонаПолейВыделенное"]		= Истина;
	НастройкаUI["МенятьЦветРамкиПолейВыделенное"]		= Истина;
	
	//== Таблицы формы ==
	
	//== Общие  ==
	НастройкаUI["МенятьФонТаблицы"] 			= Истина;
	НастройкаUI["МенятьЦветРамкиТаблицы"]		= Истина;	
	НастройкаUI["МенятьЦветТекстаТаблицы"] 		= Истина;
	НастройкаUI["МенятьШрифтТекстаТаблицы"] 	= Истина;
	
	НастройкаUI["ЗаголовкиКолонокСБольшойБуквы"] = Ложь;
	
	НастройкаUI["ЧередованиеЦветовСтрокТаблиц"] 	= Ложь;
	// Режим выделения таблицы формы принимает значения 
	// "Одиночный", "Множественный".
	НастройкаUI["РежимВыделенияТаблиц"] 			= "Одиночный";
	
	// Внимание, установка в Ложь отключает возможность добавления, копирования, удаления
	// строк. Но сохраняется возможность редактирования существующих.
	НастройкаUI["ИзменятьСоставСтрокТаблиц"]		= Истина;
	
	НастройкаUI["ИзменятьПорядокСтрокТаблиц"]		= Ложь;
	
	//== Заголовок таблиц ==
	НастройкаUI["МенятьШрифтЗаголовкаТаблиц"] 	= Истина;
	НастройкаUI["МенятьЦветЗаголовкаТаблиц"] 	= Истина;

	//== Заголовок колонок ==
	НастройкаUI["МенятьШрифтЗаголовкаКолонокТаблиц"] 	= Истина;
	НастройкаUI["МенятьЦветЗаголовкаКолонокТаблиц"] 	= Истина;
	НастройкаUI["МенятьФонЗаголовкаКолонокТаблиц"] 		= Истина;

	//== Заголовок выделенных колонок ==
	НастройкаUI["МенятьШрифтЗаголовкаКолонокТаблицВыделенное"] 	= Истина;
	НастройкаUI["МенятьЦветЗаголовкаКолонокТаблицВыделенное"] 	= Истина;
	НастройкаUI["МенятьФонЗаголовкаКолонокТаблицВыделенное"] 	= Истина;

	//== Подвал колонок ==
	НастройкаUI["МенятьШрифтПодвалаКолонокТаблиц"] 	= Истина;
	НастройкаUI["МенятьЦветПодвалаКолонокТаблиц"] 	= Истина;
	НастройкаUI["МенятьФонПодвалаКолонокТаблиц"] 	= Истина;

	//== Подвал выделенных колонок == 
	НастройкаUI["МенятьШрифтПодвалаКолонокТаблицВыделенное"] 	= Истина;
	НастройкаUI["МенятьЦветПодвалаКолонокТаблицВыделенное"] 	= Истина;
	НастройкаUI["МенятьФонПодвалаКолонокТаблицВыделенное"] 		= Истина;

	НастройкаUI["ИсточникСтиля"] = "ИзСтиля_БРО_АЛКО";
	
	Возврат НастройкаUI;

КонецФункции

Процедура СброситьНастройкиОкна(Форма) Экспорт

	РегламентированнаяОтчетностьАЛКОВызовСервера.СброситьНастройкиОкна(Форма.ИмяФормы);

КонецПроцедуры

Функция КоличествоВидимыхКолонокТаблицыФормы(ТаблицаФормы) Экспорт

	ПодчиненныеЭлементыТаблицаФормы = ТаблицаФормы.ПодчиненныеЭлементы;
	КоличествоКолонок = 0;
	Для каждого ЭлементТаблицы Из ПодчиненныеЭлементыТаблицаФормы Цикл
	
		Если ТипЗнч(ЭлементТаблицы) = Тип("ПолеФормы") Тогда
			
			Если ЭлементТаблицы.Видимость Тогда
				КоличествоКолонок = КоличествоКолонок + 1;
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат КоличествоКолонок;

КонецФункции

Процедура МасштабироватьТаблицуФормы(ТаблицаФормы, НастройкаUI = Неопределено, 
				ФормаОтчета = Неопределено, МасштабКлиентскогоПриложения = Неопределено,
				ЭтоФормаАЛКООтчета = Неопределено, ЭтоИнтерфейс_8_5 = Неопределено) Экспорт
	
	Если ЭтоИнтерфейс_8_5 = Неопределено Тогда
		ЭтоИнтерфейс_8_5 = ЭтоИнтерфейс_8_5();
	КонецЕсли;			
	
	Если ЭтоИнтерфейс_8_5 Тогда
		
		Если МасштабКлиентскогоПриложения = Неопределено Тогда
	
			МасштабКлиентскогоПриложения = "";
		    	
			МодульРегламентированнаяОтчетностьИнтерфейс85ВызовСервера = ОбщийМодульКонфигурации("РегламентированнаяОтчетностьИнтерфейс85ВызовСервера");
			МасштабКлиентскогоПриложения = МодульРегламентированнаяОтчетностьИнтерфейс85ВызовСервера.МасштабКлиентскогоПриложения();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЭтоУправляемаяФормаИлиФормаКлиентскогоПриложения(ФормаОтчета) Тогда
	
		ФормаОтчета = ТаблицаФормы;
		Пока НЕ ЭтоУправляемаяФормаИлиФормаКлиентскогоПриложения(ФормаОтчета) Цикл
		    ФормаОтчета = ФормаОтчета.Родитель;		
		КонецЦикла;	
	
	КонецЕсли;
	
	Если НЕ ЭтоСоответствие(НастройкаUI) Тогда
		
		НастройкаUI = Неопределено;
		ФормаОтчета.СтруктураРеквизитовФормы.Свойство("НастройкаUI", НастройкаUI);
	
	КонецЕсли;
	
	Если НастройкаUI = Неопределено Тогда	
		НастройкаUI = ДействующаяНастройкаUI(НастройкаUI);	
	КонецЕсли;
	
	МасштабТаблицы = 100;
	
	Если ЭтоФормаАЛКООтчета = Неопределено Тогда
		ЭтоФормаАЛКООтчета = ЭтоФормаАЛКООтчета(ФормаОтчета);
	КонецЕсли;
	
	КоличествоКолонок = КоличествоВидимыхКолонокТаблицыФормы(ТаблицаФормы);
	Если 	(
				НастройкаUI["ВключеноМасштабирование"]
				И
				НастройкаUI["МасштабироватьТолькоТаблицыФормы"]
				И
				НастройкаUI["МасштабироватьОтдельноКаждуюТаблицуФормы"]
				И
				НастройкаUI["МинимальноеКоличествоКолонокТаблицыФормыДляМасштабирования"] <= КоличествоКолонок
				И
				ЭтоФормаАЛКООтчета
			)
		Тогда
		
		МасштабТаблиц = ФормаОтчета["МасштабТаблиц"];
		
		МасштабТаблиц.Свойство(ТаблицаФормы.Имя, МасштабТаблицы);
		МасштабТаблицы = ?(МасштабТаблицы = Неопределено, 100, МасштабТаблицы);
		
	ИначеЕсли 	(
					НастройкаUI["ВключеноМасштабирование"]
					И
					НастройкаUI["МасштабироватьТолькоТаблицыФормы"]
					И
					(НЕ НастройкаUI["МасштабироватьОтдельноКаждуюТаблицуФормы"])
					И
					НастройкаUI["МинимальноеКоличествоКолонокТаблицыФормыДляМасштабирования"] <= КоличествоКолонок
					И
					ЭтоФормаАЛКООтчета
				)
		Тогда
		
		МасштабТаблицы = ФормаОтчета["МасштабФормы"];
		
	ИначеЕсли	(
					НастройкаUI["ВключеноМасштабирование"]
					И
					(НЕ НастройкаUI["МасштабироватьТолькоТаблицыФормы"])
					И
					ЭтоФормаАЛКООтчета
				)
		Тогда
		
		МасштабТаблицы = ФормаОтчета["МасштабФормы"];
		
	КонецЕсли;
	
	МасштабироватьКолонкиТаблицыФормы(ТаблицаФормы, НастройкаUI, ЭтоИнтерфейс_8_5, 
									ЭтоФормаАЛКООтчета, МасштабКлиентскогоПриложения, МасштабТаблицы);
	
КонецПроцедуры

Процедура ИнициализироватьUIТаблицыФормы(ТаблицаФормы, НастройкаUI = Неопределено, 
				ФормаОтчета = Неопределено, МасштабКлиентскогоПриложения = Неопределено,
				ЭтоФормаАЛКООтчета = Неопределено, ЭтоИнтерфейс_8_5 = Неопределено) Экспорт
	
	Если ЭтоИнтерфейс_8_5 = Неопределено Тогда
		ЭтоИнтерфейс_8_5 = ЭтоИнтерфейс_8_5();
	КонецЕсли;			
	
	
	Если ЭтоИнтерфейс_8_5 Тогда
		
		Если МасштабКлиентскогоПриложения = Неопределено Тогда
	
			МасштабКлиентскогоПриложения = "";
		    	
			МодульРегламентированнаяОтчетностьИнтерфейс85ВызовСервера = ОбщийМодульКонфигурации("РегламентированнаяОтчетностьИнтерфейс85ВызовСервера");
			МасштабКлиентскогоПриложения = МодульРегламентированнаяОтчетностьИнтерфейс85ВызовСервера.МасштабКлиентскогоПриложения();
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЭтоУправляемаяФормаИлиФормаКлиентскогоПриложения(ФормаОтчета) Тогда
	
		ФормаОтчета = ТаблицаФормы;
		Пока НЕ ЭтоУправляемаяФормаИлиФормаКлиентскогоПриложения(ФормаОтчета) Цикл
		    ФормаОтчета = ФормаОтчета.Родитель;		
		КонецЦикла;	
	
	КонецЕсли;
	
	Если НЕ ЭтоСоответствие(НастройкаUI) Тогда
		
		НастройкаUI = Неопределено;
		ФормаОтчета.СтруктураРеквизитовФормы.Свойство("НастройкаUI", НастройкаUI);
	
	КонецЕсли;
	
	Если НастройкаUI = Неопределено Тогда	
		НастройкаUI = ДействующаяНастройкаUI(НастройкаUI);	
	КонецЕсли;
	
	АктуальныйСтиль = ДействующийСтиль(НастройкаUI); 
	
	МасштабТаблицы = 100;
	
	Если ЭтоФормаАЛКООтчета = Неопределено Тогда
		ЭтоФормаАЛКООтчета = ЭтоФормаАЛКООтчета(ФормаОтчета);
	КонецЕсли;
	
	КоличествоКолонок = КоличествоВидимыхКолонокТаблицыФормы(ТаблицаФормы);
	Если 	(
				НастройкаUI["ВключеноМасштабирование"]
				И
				НастройкаUI["МасштабироватьТолькоТаблицыФормы"]
				И
				НастройкаUI["МасштабироватьОтдельноКаждуюТаблицуФормы"]
				И
				НастройкаUI["МинимальноеКоличествоКолонокТаблицыФормыДляМасштабирования"] <= КоличествоКолонок
				И
				ЭтоФормаАЛКООтчета
			)
		Тогда
		
		МасштабТаблиц = ФормаОтчета["МасштабТаблиц"];
		
		МасштабТаблиц.Свойство(ТаблицаФормы.Имя, МасштабТаблицы);
		МасштабТаблицы = ?(МасштабТаблицы = Неопределено, 100, МасштабТаблицы);
		
		Если СтрНайти(ФормаОтчета["СписокИменТаблиц"], "," + ТаблицаФормы.Имя + ",") = 0 Тогда
			
			Если ПустаяСтрока(ФормаОтчета["СписокИменТаблиц"]) Тогда
				ФормаОтчета["СписокИменТаблиц"] = ",";
			КонецЕсли;
			
			ФормаОтчета["СписокИменТаблиц"] = ФормаОтчета["СписокИменТаблиц"] + ТаблицаФормы.Имя + ",";
		
		КонецЕсли
		
	ИначеЕсли 	(
					НастройкаUI["ВключеноМасштабирование"]
					И
					НастройкаUI["МасштабироватьТолькоТаблицыФормы"]
					И
					(НЕ НастройкаUI["МасштабироватьОтдельноКаждуюТаблицуФормы"])
					И
					НастройкаUI["МинимальноеКоличествоКолонокТаблицыФормыДляМасштабирования"] <= КоличествоКолонок
					И
					ЭтоФормаАЛКООтчета
				)
		Тогда
		
		МасштабТаблицы = ФормаОтчета["МасштабФормы"];
		
		Если СтрНайти(ФормаОтчета["СписокИменТаблиц"], "," + ТаблицаФормы.Имя + ",") = 0 Тогда
			
			Если ПустаяСтрока(ФормаОтчета["СписокИменТаблиц"]) Тогда
				ФормаОтчета["СписокИменТаблиц"] = ",";
			КонецЕсли;
			
			ФормаОтчета["СписокИменТаблиц"] = ФормаОтчета["СписокИменТаблиц"] + ТаблицаФормы.Имя + ",";
		
		КонецЕсли
		
	ИначеЕсли	(
					НастройкаUI["ВключеноМасштабирование"]
					И
					(НЕ НастройкаUI["МасштабироватьТолькоТаблицыФормы"])
					И
					ЭтоФормаАЛКООтчета
				)
		Тогда
		
		МасштабТаблицы = ФормаОтчета["МасштабФормы"];
		
		Если СтрНайти(ФормаОтчета["СписокИменТаблиц"], "," + ТаблицаФормы.Имя + ",") = 0 Тогда
			
			Если ПустаяСтрока(ФормаОтчета["СписокИменТаблиц"]) Тогда
				ФормаОтчета["СписокИменТаблиц"] = ",";
			КонецЕсли;
			
			ФормаОтчета["СписокИменТаблиц"] = ФормаОтчета["СписокИменТаблиц"] + ТаблицаФормы.Имя + ",";
		
		КонецЕсли
		
	КонецЕсли;
	
	// Чередование и выделение.
	ТаблицаФормы.ЧередованиеЦветовСтрок = НастройкаUI["ЧередованиеЦветовСтрокТаблиц"];
	ТаблицаФормы.РежимВыделения = РежимВыделенияТаблицы[НастройкаUI["РежимВыделенияТаблиц"]];
	ТаблицаФормы.ИзменятьСоставСтрок = НастройкаUI["ИзменятьСоставСтрокТаблиц"];
	ТаблицаФормы.ИзменятьПорядокСтрок = НастройкаUI["ИзменятьПорядокСтрокТаблиц"];
	ТаблицаФормы.РастягиватьПоГоризонтали = Истина;
	
	// Цвета таблицы.
	Если НастройкаUI["МенятьФонТаблицы"] Тогда
		ПрисвоитьЦвет(ТаблицаФормы.ЦветФона, АктуальныйСтиль["ТаблицыФон"]);	
	КонецЕсли;
	
	Если НастройкаUI["МенятьЦветРамкиТаблицы"] Тогда
		ПрисвоитьЦвет(ТаблицаФормы.ЦветРамки, АктуальныйСтиль["ТаблицыРамкаЦвет"]);
	КонецЕсли;
	
	Если НастройкаUI["МенятьЦветТекстаТаблицы"] Тогда
		ПрисвоитьЦвет(ТаблицаФормы.ЦветТекста, АктуальныйСтиль["ТаблицыТекстЦвет"]);
	КонецЕсли;
	
	Если НастройкаUI["МенятьЦветЗаголовкаТаблиц"] Тогда
		ПрисвоитьЦвет(ТаблицаФормы.ЦветТекстаЗаголовка, АктуальныйСтиль["ТаблицыЗаголовокЦвет"]);
	КонецЕсли;
	 				
	// Шрифты таблицы.
	Если НастройкаUI["МенятьШрифтТекстаТаблицы"] Тогда
		ПрисвоитьШрифт(ТаблицаФормы.Шрифт, АктуальныйСтиль["ТаблицыТекстШрифт"]);	
	КонецЕсли;
	
	Если НастройкаUI["МенятьШрифтЗаголовкаТаблиц"] Тогда
		ПрисвоитьШрифт(ТаблицаФормы.ШрифтЗаголовка, АктуальныйСтиль["ТаблицыЗаголовокШрифт"]);	
	КонецЕсли;
	
	ИнициализироватьUIКолонокТаблицыФормы(ТаблицаФормы, НастройкаUI, АктуальныйСтиль, ЭтоИнтерфейс_8_5, 
									ЭтоФормаАЛКООтчета, МасштабКлиентскогоПриложения, МасштабТаблицы);
	
КонецПроцедуры

Функция ОбщийМодульКонфигурации(Имя) Экспорт

	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Возврат ОбщегоНазначения.ОбщийМодуль(Имя);
	#Иначе
		Возврат ОбщегоНазначенияКлиент.ОбщийМодуль(Имя);
	#КонецЕсли

КонецФункции

Функция ЭтоВыделенноеПолеСтрокой(ЭлементФормы, Форма = Неопределено) Экспорт

	ЗаголовокПоля = ЭлементФормы.Заголовок;
	ИмяПоля = ЭлементФормы.Имя;
	ПодсказкаПоля = ЭлементФормы.Подсказка;
		
	ЭтоВыделенноеПоле = 
		(
			ПризнакВыделенногоПоляВСтроке(ЗаголовокПоля)
			ИЛИ
			ПризнакВыделенногоПоляВСтроке(ПодсказкаПоля)
			ИЛИ
			ПризнакВыделенногоПоляВСтроке(ИмяПоля)
		);
		
	Если (НЕ ЭтоВыделенноеПоле) И ЭтоУправляемаяФормаИлиФормаКлиентскогоПриложения(Форма) Тогда
	
		ЭлементЗаголовка = ЭлементЗаголовка(Форма, ЭлементФормы);
		
		Если ЭлементЗаголовка <> Неопределено Тогда
			
			ЗаголовокПоля = ЭлементЗаголовка.Заголовок;
			ЭтоВыделенноеПоле = ПризнакВыделенногоПоляВСтроке(ЗаголовокПоля);
				
		КонецЕсли;
		
	КонецЕсли;
		
	ЭтоВыделенноеПолеСтр = ?(ЭтоВыделенноеПоле, "Выделенное", ""); 
	
	Возврат  ЭтоВыделенноеПолеСтр;

КонецФункции

Функция ЭтоПолеАдреса(ЭлементФормы, Форма = Неопределено) Экспорт

	ЗаголовокПоля = ЭлементФормы.Заголовок;
	ИмяПоля = ЭлементФормы.Имя;
	ПодсказкаПоля = ЭлементФормы.Подсказка;
		
	ЭтоАдресноеПоле = 
		(
			ПризнакАдресногоПоляВСтроке(ЗаголовокПоля)
			ИЛИ
			ПризнакАдресногоПоляВСтроке(ПодсказкаПоля)
			ИЛИ
			ПризнакАдресногоПоляВСтроке(ИмяПоля)
		);
		
	Если (НЕ ЭтоАдресноеПоле) И ЭтоУправляемаяФормаИлиФормаКлиентскогоПриложения(Форма) Тогда
	
		ЭлементЗаголовка = ЭлементЗаголовка(Форма, ЭлементФормы);
		
		Если ЭлементЗаголовка <> Неопределено Тогда
			
			ЗаголовокПоля = ЭлементЗаголовка.Заголовок;
			ЭтоАдресноеПоле = ПризнакАдресногоПоляВСтроке(ЗаголовокПоля);
				
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат  ЭтоАдресноеПоле;

КонецФункции

Процедура ПрисвоитьНепустоеЗначение(ПриемникЗначения, Значение) Экспорт

	Если Значение <> Неопределено Тогда
		//На случай структурированных типов.
		ПриемникЗначения = СкопироватьРекурсивноАЛКО(Значение);
	КонецЕсли;

КонецПроцедуры

Функция ЭлементПринадлежитСтранице(Элемент) Экспорт
	
	Результат = Ложь;
	
	РодительВехнегоУровня = Элемент.Родитель;
	ТипЗначенияРодителяВехнегоУровня = ТипЗнч(РодительВехнегоУровня);
	
	Пока СтрНайти(ВРег(Строка(ТипЗначенияРодителяВехнегоУровня)), "ФОРМА") = 0 Цикл
	
		Если ТипЗначенияРодителяВехнегоУровня = Тип("ГруппаФормы") Тогда
		
			ВидРодителяВехнегоУровня = РодительВехнегоУровня.Вид;
			Если ВидРодителяВехнегоУровня = ВидГруппыФормы.Страница Тогда
				Результат = Истина;
			КонецЕсли;
			
			Прервать;
			
		КонецЕсли;
		
		РодительВехнегоУровня = РодительВехнегоУровня.Родитель;
		ТипЗначенияРодителяВехнегоУровня = ТипЗнч(РодительВехнегоУровня);
	
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

Функция ШрифтНаБазеДругогоШрифта(
			Шрифт, Размер = Неопределено, Полужирный= Неопределено, 
			Наклонный= Неопределено, Подчеркнутый= Неопределено, 
			Зачеркнутый= Неопределено, Масштаб= Неопределено) Экспорт

	Возврат Новый Шрифт(Шрифт,,Размер, Полужирный, Наклонный, Подчеркнутый, Зачеркнутый, Масштаб);

КонецФункции

Функция ШрифтПоОписанию(
			СистемноеИмяШрифта, Размер = Неопределено, Полужирный= Неопределено, 
			Наклонный= Неопределено, Подчеркнутый= Неопределено, 
			Зачеркнутый= Неопределено, Масштаб= Неопределено) Экспорт

	Возврат Новый Шрифт(СистемноеИмяШрифта,Размер, Полужирный, Наклонный, Подчеркнутый, Зачеркнутый, Масштаб);

КонецФункции

Процедура УстановитьПоложениеЗаголовкаОписаниеНормативДокОсновнойФормы(Форма) Экспорт
	
	Если Форма.Элементы.ПолеРедакцияФормы.Видимость Тогда
		Форма.Элементы.ОписаниеНормативДок.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	Иначе
		Форма.Элементы.ОписаниеНормативДок.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодчеркнутьГиперссылку(ЭлементФормы, Масштаб) Экспорт

	СтруктураПроверки = Новый Структура("Гиперссылка");
	ЗаполнитьЗначенияСвойств(СтруктураПроверки, ЭлементФормы);
	
	Если ТипЗнч(СтруктураПроверки.Гиперссылка) = Тип("Булево") Тогда
	
		Если ЭлементФормы.Гиперссылка Тогда
		
			ШрифтЭлемента = ЭлементФормы.Шрифт;
			ПодчеркнутыйШрифтЭлемента = 
				РегламентированнаяОтчетностьАЛКОКлиентСервер.ШрифтНаБазеДругогоШрифта(
													ШрифтЭлемента,,,, Истина,, Масштаб);
			ЭлементФормы.Шрифт = ПодчеркнутыйШрифтЭлемента;
		
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры

Процедура ИзменитьМасштабЭлементаФормы(ЭлементФормы, МасштабФормы, ЭтоИнтерфейс_8_5, 
					МасштабКлиентскогоПриложения, ЭтоЭлементТаблицыФормы = Ложь) Экспорт
					
	
	Если НЕ ЭтоИнтерфейс_8_5 Тогда
		Возврат;
	КонецЕсли;
	
	Масштаб = 100;
	
	Если ЭтоСтруктура(МасштабФормы) Тогда
		
		МасштабФормы.Свойство(ЭлементФормы.Имя, Масштаб);
		Масштаб = ?(Масштаб = Неопределено, 100, Масштаб);
		
	ИначеЕсли ТипЗнч(МасштабФормы) = Тип("Число") Тогда
	    Масштаб = МасштабФормы;
	КонецЕсли;
	
	СтруктураПроверки = Новый Структура("Масштаб");
	ЗаполнитьЗначенияСвойств(СтруктураПроверки, ЭлементФормы);
	
	Если ТипЗнч(СтруктураПроверки.Масштаб) = Тип("Число") Тогда
	
		ЭлементФормы.Масштаб = Масштаб;
	
	КонецЕсли;
	
	МассивСвойствШрифт = ВариантыСвойстваШрифта(ЭлементФормы);
	
	Для каждого ВариантСвойстваШрифта Из МассивСвойствШрифт Цикл
	
		ИзменитьМасштабШрифтаЭлементаФормы(ЭлементФормы, Масштаб, ВариантСвойстваШрифта, 
				ЭтоИнтерфейс_8_5, МасштабКлиентскогоПриложения, ЭтоЭлементТаблицыФормы);
	
	КонецЦикла;

КонецПроцедуры

Процедура УстановитьЗащитуНаВсеОбластиТабличногоДокумента(ТабДокумент) Экспорт

	Если ТипЗнч(ТабДокумент) <> Тип("ТабличныйДокумент") Тогда
		Возврат;
	КонецЕсли;
	
	ОбластиТабДокумента = ТабДокумент.Области;
	
	Для каждого ОбластьТабДокумента Из ОбластиТабДокумента Цикл
	
		ОбластьТабДокумента.Защита = Истина;		
	
	КонецЦикла;

КонецПроцедуры

Процедура УстановитьВсеЗаголовкиВАвто(Форма) Экспорт

	ЭлементыФормы = Форма.Элементы;
	
	Для каждого ЭлементФормы Из ЭлементыФормы Цикл
		
		Если ТипЗнч(ЭлементФормы) = Тип("ПолеФормы")
			ИЛИ
			ТипЗнч(ЭлементФормы) = Тип("ТаблицаФормы")
			Тогда
		
		УстановитьЗаголовокВАвто(ЭлементФормы);	
		
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура УстановитьВсеЗаголовкиВАвтоУбратьМногострочностьЗаголовковЗаголовокСБольшойБуквы(
												Форма, НеТрогатьОтключенные = Истина) Экспорт

	ЭлементыФормы = Форма.Элементы;
	
	Для каждого ЭлементФормы Из ЭлементыФормы Цикл
		
		Если ТипЗнч(ЭлементФормы) = Тип("ПолеФормы")
			ИЛИ
			ТипЗнч(ЭлементФормы) = Тип("ТаблицаФормы")
			Тогда
		
			УстановитьЗаголовокВАвто(ЭлементФормы, НеТрогатьОтключенные);
			УбратьМногострочностьЗаголовкаИЗаголовокСБольшойБуквы(ЭлементФормы);
		
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура УстановитьЗаголовокВАвто(ЭлементФормы, НеТрогатьОтключенные = Истина) Экспорт

	УстановитьПоложениеЗаголовка(ЭлементФормы, "Авто", НеТрогатьОтключенные);

КонецПроцедуры

Процедура УстановитьПоложениеЗаголовка(ЭлементФормы, ПоложениеЗаголовка = "Авто", НеТрогатьОтключенные = Истина) Экспорт

	СтруктураПроверки = Новый Структура("ПоложениеЗаголовка");
	ЗаполнитьЗначенияСвойств(СтруктураПроверки, ЭлементФормы);
	
	Если ТипЗнч(СтруктураПроверки.ПоложениеЗаголовка) = Тип("ПоложениеЗаголовкаЭлементаФормы") Тогда
		
		Если 	НеТрогатьОтключенные 
				И
				ЭлементФормы.ПоложениеЗаголовка <> ПоложениеЗаголовкаЭлементаФормы.Нет 
			Тогда
			ЭлементФормы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы[ПоложениеЗаголовка];
		ИначеЕсли 	НЕ НеТрогатьОтключенные Тогда
			ЭлементФормы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы[ПоложениеЗаголовка];
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьАвтоМаксимальнуюШирину(ЭлементФормы, АвтоМаксимальнаяШирина = Истина) Экспорт

	СтруктураПроверки = Новый Структура("АвтоМаксимальнаяШирина");
	ЗаполнитьЗначенияСвойств(СтруктураПроверки, ЭлементФормы);
	
	Если ТипЗнч(СтруктураПроверки.АвтоМаксимальнаяШирина) = Тип("Булево") Тогда
		
		ЭлементФормы.АвтоМаксимальнаяШирина = АвтоМаксимальнаяШирина;
				
	КонецЕсли;

КонецПроцедуры

Процедура УбратьМногострочностьЗаголовкаИЗаголовокСБольшойБуквы(ЭлементФормы) Экспорт
	
	УстановитьЗаголовокСБольшойБуквы(ЭлементФормы);
		
	УбратьМногострочностьЗаголовка(ЭлементФормы);

КонецПроцедуры

Процедура УстановитьЗаголовокСБольшойБуквы(ЭлементФормы) Экспорт

	Если СтрДлина(ЭлементФормы.Заголовок) >= 3 
		И 
		Лев(ЭлементФормы.Заголовок, 3) <> ВРег(Лев(ЭлементФормы.Заголовок, 3)) Тогда
	
		ЭлементФормы.Заголовок = НРег(ЭлементФормы.Заголовок);
		ЭлементФормы.Заголовок = 
			ВРег(Лев(ЭлементФормы.Заголовок, 1)) 
			+ Сред(ЭлементФормы.Заголовок, 2);	
	
	КонецЕсли;

КонецПроцедуры

Процедура УбратьМногострочностьЗаголовка(ЭлементФормы) Экспорт

	ЭлементФормы.Заголовок = 
		СтрЗаменить(ЭлементФормы.Заголовок, Символы.ПС, " ");
	ЭлементФормы.Заголовок = 
		СтрЗаменить(ЭлементФормы.Заголовок, "  ", " ");

КонецПроцедуры

Функция ДействующаяНастройкаUI(НастройкаUI) Экспорт

	НастройкаUIПоумолчанию = НастройкаUIПоУмолчанию();
	Если ЭтоСоответствие(НастройкаUI) Тогда
		
		Для каждого КлючЗначение Из НастройкаUI Цикл
			
			ПрисвоитьНепустоеЗначение(
				НастройкаUIПоумолчанию[КлючЗначение.Ключ], 
				НастройкаUI[КлючЗначение.Ключ]);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ДействующаяНастройкаUI = НастройкаUIПоумолчанию;
	
	Возврат ДействующаяНастройкаUI;

КонецФункции

Функция ДействующийСтиль(НастройкаUI) Экспорт
	
	Если НастройкаUI["НеМенятьЦвета"] И НастройкаUI["НеМенятьШрифты"] Тогда
	
		АктуальныйСтиль = Новый Соответствие;
		Возврат АктуальныйСтиль;
	
	КонецЕсли;
	
	АктуальныйСтиль = РегламентированнаяОтчетностьАЛКОВызовСервера.Стили_БРО_АЛКО(); 
	
	Стиль = НастройкаUI["Стиль"];
	
	ЕстьИсточникСтиля = 
			НЕ 	
				(
				ТипЗнч(НастройкаUI["ИсточникСтиля"]) = Тип("Строка")
				И
				ВРег(СокрЛП(НастройкаUI["ИсточникСтиля"])) = ВРег("ИзСтиля_БРО_АЛКО")
				);
	УказанСтиль = (ЕстьИсточникСтиля И ЭтоСоответствие(Стиль));
	СтильСброшенНаСтандартныйИнтерфейса = ЕстьИсточникСтиля И (НЕ ЭтоСоответствие(Стиль));
	
	
	Для каждого КлючЗначение Из АктуальныйСтиль Цикл
	
		Если СтрНайти(ВРег(КлючЗначение.Ключ), ВРег("Шрифт")) > 0 Тогда
		
			Если НастройкаUI["НеМенятьШрифты"] ИЛИ СтильСброшенНаСтандартныйИнтерфейса Тогда
				АктуальныйСтиль[КлючЗначение.Ключ] = Неопределено;
			ИначеЕсли УказанСтиль Тогда
				
				Если ТипЗнч(Стиль[КлючЗначение.Ключ]) <> Тип("Шрифт") Тогда
					АктуальныйСтиль[КлючЗначение.Ключ] = Неопределено;
				Иначе
					ПрисвоитьШрифт(
						АктуальныйСтиль[КлючЗначение.Ключ], Стиль[КлючЗначение.Ключ]);
				КонецЕсли;
							
			КонецЕсли;
		Иначе
			// Это Цвет.
			Если НастройкаUI["НеМенятьЦвета"] ИЛИ СтильСброшенНаСтандартныйИнтерфейса Тогда
				АктуальныйСтиль[КлючЗначение.Ключ] = Неопределено;
			ИначеЕсли УказанСтиль Тогда
				
				Если ТипЗнч(Стиль[КлючЗначение.Ключ]) <> Тип("Цвет") Тогда
					АктуальныйСтиль[КлючЗначение.Ключ] = Неопределено;
				Иначе
					ПрисвоитьЦвет(АктуальныйСтиль[КлючЗначение.Ключ], Стиль[КлючЗначение.Ключ]);
				КонецЕсли;
							
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат АктуальныйСтиль;

КонецФункции

Функция МасштабВМенюТаблицы(НастройкаUI) Экспорт

	Возврат	(
				НастройкаUI["ВключеноМасштабирование"]
				И
				НастройкаUI["МасштабироватьТолькоТаблицыФормы"]
				И
				НастройкаUI["МасштабироватьОтдельноКаждуюТаблицуФормы"]
			);

КонецФункции


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭлементЗаголовка(Форма, ЭлементФормы) Экспорт

	ВсеЭлементыФормы = Форма.Элементы;
		
	ИмяЗаголовка = "Заголовок" + ЭлементФормы.Имя;
	ЭлементЗаголовка = ВсеЭлементыФормы.Найти(ИмяЗаголовка);
	
	Если ЭлементЗаголовка = Неопределено Тогда
		
		ИмяЗаголовка = ЭлементФормы.Имя + "Заголовок";
		ЭлементЗаголовка = ВсеЭлементыФормы.Найти(ИмяЗаголовка);
		
	КонецЕсли;
	
	Возврат ЭлементЗаголовка;

КонецФункции

Функция ПризнакВыделенногоПоляВСтроке(СтрокаПроверки)
	
	СтрокаПроверкиВРег = ВРег(СтрокаПроверки);
	
	Возврат 	(
					СтрНайти(СтрокаПроверкиВРег, "СУММА") > 0
					ИЛИ
					СтрНайти(СтрокаПроверкиВРег, "ВСЕГО") > 0
					ИЛИ
					СтрНайти(СтрокаПроверкиВРег, "ИТОГ") > 0
					ИЛИ
					СтрНайти(СтрокаПроверкиВРег, "ОСТАТОК") > 0
					ИЛИ
					СтрНайти(СтрокаПроверкиВРег, "ОСТАТКИ") > 0
				);

КонецФункции

Функция ПризнакАдресногоПоляВСтроке(СтрокаПроверки)
	
	СтрокаПроверкиВРег = ВРег(СтрокаПроверки);
	
	Возврат 	(
					СтрНайти(СтрокаПроверкиВРег, "АДРЕС") > 0
					ИЛИ
					СтрНайти(СтрокаПроверкиВРег, "МЕСТО") > 0
				);

КонецФункции

Процедура ПрисвоитьЦвет(ПриемникЗначения, Значение)

	Если ТипЗнч(Значение) = Тип("Цвет") Тогда
		ПриемникЗначения = Значение;
	КонецЕсли;

КонецПроцедуры

Процедура ПрисвоитьШрифт(ПриемникЗначения, Значение)

	Если ТипЗнч(Значение) = Тип("Шрифт") Тогда
		ПриемникЗначения = Значение;
	
	КонецЕсли;

КонецПроцедуры

Процедура ИзменитьМасштабШрифтаЭлементаФормы(ЭлементФормы, Масштаб, ВариантСвойстваШрифта, 
			ЭтоИнтерфейс_8_5, МасштабКлиентскогоПриложения, ЭтоЭлементТаблицыФормы = Ложь)

	ТипЗначения = ТипЗнч(ЭлементФормы);
	
	СтруктураПроверки = Новый Структура(ВариантСвойстваШрифта);
	ЗаполнитьЗначенияСвойств(СтруктураПроверки, ЭлементФормы);
	
	Если ТипЗнч(СтруктураПроверки[ВариантСвойстваШрифта]) = Тип("Шрифт") Тогда
	
		Шрифт = ЭлементФормы[ВариантСвойстваШрифта];
		
		МасштабИсходногоШрифта = Шрифт.Масштаб;
		МасштабНовогоШрифта = (Масштаб/МасштабИсходногоШрифта) * 100;
		
		Если ЭтоЭлементТаблицыФормы Тогда
			
			Если Шрифт.Вид <> ВидШрифта.Абсолютный Тогда
				
				ШрифтАвто = ШрифтАвто_8_5(ЭлементФормы, ВариантСвойстваШрифта, МасштабКлиентскогоПриложения, ЭтоЭлементТаблицыФормы);
			
				Полужирный = Шрифт.Полужирный;
				Полужирный = ?(Полужирный = Неопределено, Ложь, Полужирный);
				Полужирный = (Полужирный ИЛИ ШрифтАвто.Полужирный);
				
				НовыйШрифт = ШрифтПоОписанию(ШрифтАвто.ИмяШрифта, ШрифтАвто.Размер, Полужирный, 
										Шрифт.Наклонный, Шрифт.Подчеркивание, 
										Шрифт.Зачеркивание, МасштабНовогоШрифта);
			Иначе
				НовыйШрифт = ШрифтНаБазеДругогоШрифта(Шрифт,,,,,, МасштабНовогоШрифта);
			КонецЕсли;
									
		Иначе
			НовыйШрифт = ШрифтНаБазеДругогоШрифта(Шрифт,,,,,, МасштабНовогоШрифта);
		КонецЕсли;
		
		ЭлементФормы[ВариантСвойстваШрифта] = НовыйШрифт;
		
		Если ЭтоИнтерфейс_8_5 И ТипЗначения = Тип("ПолеФормы") Тогда
		
			ВидЗначения = ЭлементФормы.Вид;
			
			Если ВидЗначения = ВидПоляФормы.ПолеВвода Тогда
			
				МодульРегОтчетностьИнтерфейс85КлиентСервер = ОбщийМодульКонфигурации("РегламентированнаяОтчетностьИнтерфейс85КлиентСервер");
				МодульРегОтчетностьИнтерфейс85КлиентСервер.УстановитьРазмерТекстаПоляВвода(ЭлементФормы, Масштаб);
				
			КонецЕсли;
		
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры

Функция ВариантыСвойстваШрифта(ЭлементФормы)

	ТипЗначения = ТипЗнч(ЭлементФормы);
	
	Результат = Новый Массив;
	
	Если ТипЗначения = Тип("ГруппаФормы") Тогда
		
		Результат.Добавить("ШрифтЗаголовка");
		
	ИначеЕсли ТипЗначения = Тип("ДекорацияФормы") Тогда
		
		Результат.Добавить("Шрифт");
		
	ИначеЕсли ТипЗначения = Тип("КнопкаФормы") Тогда
		
		Результат.Добавить("Шрифт");
		
	ИначеЕсли ТипЗначения = Тип("ПолеФормы") Тогда
		
		Результат.Добавить("ШрифтЗаголовка");
		Результат.Добавить("ШрифтПодвала");
		
		ВидЗначения = ЭлементФормы.Вид;
		
		Если ВидЗначения = ВидПоляФормы.ПолеВвода Тогда
		
			Результат.Добавить("Шрифт");
			Результат.Добавить("ШрифтМножественныхЗначений");
			
		ИначеЕсли 
			ВидЗначения = ВидПоляФормы.ПолеНадписи
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеПереключателя
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеФлажка
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеТекстовогоДокумента
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеКартинки
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеПериода
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеКалендаря
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеФорматированногоДокумента
			Тогда
			
			Результат.Добавить("Шрифт");
			
		КонецЕсли;
		
	ИначеЕсли ТипЗначения = Тип("ТаблицаФормы") Тогда
		
		Результат.Добавить("Шрифт");
		Результат.Добавить("ШрифтЗаголовка");
				
	КонецЕсли;
	
	Возврат Новый ФиксированныйМассив(Результат);
	
КонецФункции

Функция ШрифтАвто_8_5(ЭлементФормы, ВариантСвойстваШрифта, МасштабКлиентскогоПриложения, ЭтоЭлементТаблицыФормы)
	
	ТипЗначения = ТипЗнч(ЭлементФормы);
	
	Результат = Новый Структура("ИмяШрифта, Размер, Полужирный");
	
	Если ТипЗначения = Тип("ГруппаФормы") Тогда
		
		Результат.ИмяШрифта = "Roboto";
		Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 12, 11);
		Результат.Полужирный = Истина;
		
	ИначеЕсли ТипЗначения = Тип("ДекорацияФормы") Тогда
		
		Результат.ИмяШрифта = "Roboto";
		Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 12, 11);
		Результат.Полужирный = Ложь;
		
	ИначеЕсли ТипЗначения = Тип("КнопкаФормы") Тогда
		
		Результат.ИмяШрифта = "Roboto";
		Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 12, 11);
		Результат.Полужирный = Истина;
		
	ИначеЕсли ТипЗначения = Тип("ПолеФормы") Тогда
		
		Если ЭтоЭлементТаблицыФормы Тогда
		
			Если ВариантСвойстваШрифта = "ШрифтЗаголовка" Тогда
			    
				Результат.ИмяШрифта = "Roboto Medium";
				Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 11, 9);
				Результат.Полужирный = Ложь;
				
			ИначеЕсли ВариантСвойстваШрифта = "ШрифтПодвала" Тогда
			
				Результат.ИмяШрифта = "Roboto Medium";
				Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 11, 9);
				Результат.Полужирный = Ложь;
			
			КонецЕсли;
			
		Иначе
			
			Если ВариантСвойстваШрифта = "ШрифтЗаголовка" Тогда
			
				Результат.ИмяШрифта = "Roboto Medium";
				Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 11, 9);
				Результат.Полужирный = Ложь;
				
			ИначеЕсли ВариантСвойстваШрифта = "ШрифтПодвала" Тогда
			
				Результат.ИмяШрифта = "Roboto Medium";
				Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 11, 9);
				Результат.Полужирный = Ложь;
			
			КонецЕсли;
		
		КонецЕсли;
		
		ВидЗначения = ЭлементФормы.Вид;
		
		Если ВидЗначения = ВидПоляФормы.ПолеВвода Тогда
		    
			Если ЭтоЭлементТаблицыФормы Тогда
		
				Если ВариантСвойстваШрифта = "Шрифт" Тогда
				
					Результат.ИмяШрифта = "Roboto";
					Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 11, 9);
					Результат.Полужирный = Ложь;
					
				ИначеЕсли ВариантСвойстваШрифта = "ШрифтМножественныхЗначений" Тогда
				
					Результат.ИмяШрифта = "Roboto";
					Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 11, 9);
					Результат.Полужирный = Ложь;
				
				КонецЕсли;
				
			Иначе
				
				Если ВариантСвойстваШрифта = "Шрифт" Тогда
				
					Результат.ИмяШрифта = "Roboto";
					Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 12, 11);
					Результат.Полужирный = Ложь;
					
				ИначеЕсли ВариантСвойстваШрифта = "ШрифтМножественныхЗначений" Тогда
				
					Результат.ИмяШрифта = "Roboto";
					Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 12, 11);
					Результат.Полужирный = Ложь;
				
				КонецЕсли;
			
			КонецЕсли;
			
		ИначеЕсли 
			ВидЗначения = ВидПоляФормы.ПолеНадписи
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеПереключателя
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеФлажка
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеТекстовогоДокумента
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеКартинки
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеПериода
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеКалендаря
			ИЛИ
			ВидЗначения = ВидПоляФормы.ПолеФорматированногоДокумента
			Тогда
			
			Если ЭтоЭлементТаблицыФормы Тогда
		
				Если ВариантСвойстваШрифта = "Шрифт" Тогда
				
					Результат.ИмяШрифта = "Roboto";
					Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 11, 9);
					Результат.Полужирный = Ложь;
					
				КонецЕсли;
				
			Иначе
				
				Если ВариантСвойстваШрифта = "Шрифт" Тогда
				
					Результат.ИмяШрифта = "Roboto";
					Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 12, 11);
					Результат.Полужирный = Ложь;
					
				КонецЕсли;
			
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗначения = Тип("ТаблицаФормы") Тогда
		
		Если ВариантСвойстваШрифта = "Шрифт" Тогда
		
			Результат.ИмяШрифта = "Roboto";
			Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 11, 9);
			Результат.Полужирный = Ложь;
			
		ИначеЕсли ВариантСвойстваШрифта = "ШрифтЗаголовка" Тогда
			
			Результат.ИмяШрифта = "Roboto Medium";
			Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 11, 9);
			Результат.Полужирный = Ложь;
			
		КонецЕсли;
			
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Результат.ИмяШрифта) Тогда
	
		Результат.ИмяШрифта = "Roboto";
		Результат.Размер = ?(МасштабКлиентскогоПриложения = "Обычный", 12, 11);
		Результат.Полужирный = Ложь;
	
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Процедура МасштабироватьЭлементФормы(Форма, ЭлементФормы, НастройкаUI, 
		ЭтоИнтерфейс_8_5 = Неопределено, ЭтоФормаАЛКООтчета = Неопределено, 
		МасштабКлиентскогоПриложения = Неопределено)

	Если ЭтоИнтерфейс_8_5 = Неопределено Тогда
		ЭтоИнтерфейс_8_5 = ЭтоИнтерфейс_8_5();
	КонецЕсли;
	
	Если ЭтоФормаАЛКООтчета = Неопределено Тогда
		ЭтоФормаАЛКООтчета = ЭтоФормаАЛКООтчета(Форма);
	КонецЕсли;
	
	Если МасштабКлиентскогоПриложения = Неопределено Тогда
	
		МасштабКлиентскогоПриложения = "";
	
		Если ЭтоИнтерфейс_8_5 Тогда	
			
			МодульРегламентированнаяОтчетностьИнтерфейс85ВызовСервера = ОбщийМодульКонфигурации("РегламентированнаяОтчетностьИнтерфейс85ВызовСервера");
			МасштабКлиентскогоПриложения = МодульРегламентированнаяОтчетностьИнтерфейс85ВызовСервера.МасштабКлиентскогоПриложения();
			
		КонецЕсли;
	
	КонецЕсли;
	
	ТипЗначенияЭлемента = ТипЗнч(ЭлементФормы);
	
	Если ЭтоИнтерфейс_8_5 И НастройкаUI["ВключеноМасштабирование"] И ЭтоФормаАЛКООтчета Тогда
		
		МассивИсключенныхИзМасштабированияИмен = 
			СтрРазделить(ВРег(НастройкаUI["СписокИсключенияИменЭлементовПриМасштабировании_Интерфейс_8_5"]), ",", Ложь);
		Если МассивИсключенныхИзМасштабированияИмен.Найти(ВРег(ЭлементФормы.Имя)) = Неопределено Тогда
			
			ЭтоЭлементТаблицыФормы = (ТипЗначенияЭлемента = Тип("ТаблицаФормы"));
			Если НастройкаUI["МасштабироватьТолькоТаблицыФормы"] Тогда
			
				Если ЭтоЭлементТаблицыФормы Тогда
					
					МасштабТаблицы = Форма["МасштабТаблиц"];
					ИзменитьМасштабЭлементаФормы(ЭлементФормы, МасштабТаблицы, ЭтоИнтерфейс_8_5, 
								МасштабКлиентскогоПриложения, ЭтоЭлементТаблицыФормы);
				
				КонецЕсли;
				
			Иначе
				
				МасштабФормы = Форма["МасштабФормы"];
				ИзменитьМасштабЭлементаФормы(ЭлементФормы, МасштабФормы, ЭтоИнтерфейс_8_5, 
								МасштабКлиентскогоПриложения, ЭтоЭлементТаблицыФормы);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипЗначенияЭлемента = Тип("ГруппаФормы") Тогда
		МасштабироватьГруппуФормы(Форма, ЭлементФормы, НастройкаUI,
										ЭтоИнтерфейс_8_5, ЭтоФормаАЛКООтчета,
										МасштабКлиентскогоПриложения);
		
	ИначеЕсли ТипЗначенияЭлемента = Тип("ТаблицаФормы") Тогда
		
		ИнициализироватьUIТаблицыФормы(ЭлементФормы, НастройкаUI, Форма, МасштабКлиентскогоПриложения, 
										ЭтоФормаАЛКООтчета, ЭтоИнтерфейс_8_5);
		
	КонецЕсли;

КонецПроцедуры

Процедура ИнициализироватьUIЭлементаФормы(Форма, ЭлементФормы, НастройкаUI, 
		ЭтоИнтерфейс_8_5 = Неопределено, ЭтоФормаАЛКООтчета = Неопределено, МасштабКлиентскогоПриложения = Неопределено, 
		РодительЭлемента = Неопределено, СтилиРодителя = Неопределено, РодительСтр = "Группа")
	
	Если ЭтоИнтерфейс_8_5 = Неопределено Тогда
		ЭтоИнтерфейс_8_5 = ЭтоИнтерфейс_8_5();
	КонецЕсли;
	
	Если ЭтоФормаАЛКООтчета = Неопределено Тогда
		ЭтоФормаАЛКООтчета = ЭтоФормаАЛКООтчета(Форма);
	КонецЕсли;
	
	Если МасштабКлиентскогоПриложения = Неопределено Тогда
	
		МасштабКлиентскогоПриложения = "";
	
		Если ЭтоИнтерфейс_8_5 Тогда	
			
			МодульРегламентированнаяОтчетностьИнтерфейс85ВызовСервера = ОбщийМодульКонфигурации("РегламентированнаяОтчетностьИнтерфейс85ВызовСервера");
			МасштабКлиентскогоПриложения = МодульРегламентированнаяОтчетностьИнтерфейс85ВызовСервера.МасштабКлиентскогоПриложения();
			
		КонецЕсли;
	
	КонецЕсли;
	
	Если	РодительЭлемента = Неопределено
			ИЛИ
			СтилиРодителя = Неопределено
			Тогда
		СтилиРодителя = ДействующийСтиль(НастройкаUI);
	КонецЕсли;
	
	ТипЗначенияЭлемента = ТипЗнч(ЭлементФормы);
	
	Если ЭтоИнтерфейс_8_5 И НастройкаUI["ВключеноМасштабирование"] И ЭтоФормаАЛКООтчета Тогда
		
		МассивИсключенныхИзМасштабированияИмен = 
			СтрРазделить(ВРег(НастройкаUI["СписокИсключенияИменЭлементовПриМасштабировании_Интерфейс_8_5"]), ",", Ложь);
		Если МассивИсключенныхИзМасштабированияИмен.Найти(ВРег(ЭлементФормы.Имя)) = Неопределено Тогда
			
			ЭтоЭлементТаблицыФормы = (ТипЗначенияЭлемента = Тип("ТаблицаФормы"));
			Если НастройкаUI["МасштабироватьТолькоТаблицыФормы"] Тогда
			
				Если ЭтоЭлементТаблицыФормы Тогда
					
					МасштабТаблицы = Форма["МасштабТаблиц"];
					ИзменитьМасштабЭлементаФормы(ЭлементФормы, МасштабТаблицы, ЭтоИнтерфейс_8_5, 
								МасштабКлиентскогоПриложения, ЭтоЭлементТаблицыФормы);
				
				КонецЕсли;
				
			Иначе
				
				МасштабФормы = Форма["МасштабФормы"];
				ИзменитьМасштабЭлементаФормы(ЭлементФормы, МасштабФормы, ЭтоИнтерфейс_8_5, 
								МасштабКлиентскогоПриложения, ЭтоЭлементТаблицыФормы);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	МассивИсключенныхИмен = СтрРазделить(ВРег(НастройкаUI["СписокИсключенияИменЭлементов"]), ",", Ложь);
	Если МассивИсключенныхИмен.Найти(ВРег(ЭлементФормы.Имя)) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НастройкаUI["ЗаголовкиСБольшойБуквы"] Тогда
	
		СтруктураПроверки = Новый Структура("Заголовок");
		ЗаполнитьЗначенияСвойств(СтруктураПроверки, ЭлементФормы);
		
		Если 	ТипЗнч(СтруктураПроверки.Заголовок) = Тип("Строка")
				И
				ЗначениеЗаполнено(СтруктураПроверки.Заголовок)
			Тогда
			
			ЭлементФормы.Заголовок = СокрЛП(ЭлементФормы.Заголовок);
			
			Если СтрДлина(ЭлементФормы.Заголовок) > 1 Тогда
				ЭлементФормы.Заголовок = ВРег(Лев(ЭлементФормы.Заголовок, 1)) + Сред(ЭлементФормы.Заголовок, 2);
			Иначе
				// Поскольку заголовок не пуст - в нем один символ.
				ЭлементФормы.Заголовок = ВРег(ЭлементФормы.Заголовок);
			КонецЕсли;
		
		КонецЕсли;
	
	КонецЕсли;
	
	Если ТипЗначенияЭлемента = Тип("ГруппаФормы") Тогда
		ИнициализироватьUIГруппыФормы(Форма, ЭлементФормы, НастройкаUI,
										ЭтоИнтерфейс_8_5, ЭтоФормаАЛКООтчета,
										МасштабКлиентскогоПриложения,
										РодительЭлемента, СтилиРодителя, РодительСтр);
		
	ИначеЕсли ТипЗначенияЭлемента = Тип("ДекорацияФормы") Тогда
		
		Если ЭлементФормы.Гиперссылка 
			И НастройкаUI["ВыделятьПодчеркиваниемГиперссылкиНезависимоОтДругихНастроек"] 
			Тогда
			
			Если ЭтоИнтерфейс_8_5 Тогда
				ПодчеркнутьГиперссылку(ЭлементФормы,);				
			КонецЕсли;
						
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияЭлемента = Тип("КнопкаФормы") Тогда
		
	ИначеЕсли ТипЗначенияЭлемента = Тип("ПолеФормы") Тогда
		
		// Корневые поля формы - ничего не меняем.
		Если РодительЭлемента = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		// Сначала надо понять, это поле итогов или нет.
		ЭтоВыделенноеПолеСтр = ЭтоВыделенноеПолеСтрокой(ЭлементФормы, Форма); 
		ЭтоПолеАдреса = ЭтоПолеАдреса(ЭлементФормы, Форма);
				
		ЦветФонаЗаголовка = РодительЭлемента.ЦветФона;
		
		ВидПоля = ЭлементФормы.Вид;
		
		Если 	ВидПоля = ВидПоляФормы.ПолеВвода
				ИЛИ
			    ВидПоля = ВидПоляФормы.ПолеНадписи
				ИЛИ
			    ВидПоля = ВидПоляФормы.ПолеТекстовогоДокумента
				ИЛИ
			    ВидПоля = ВидПоляФормы.ПолеФорматированногоДокумента
				ИЛИ
			    ВидПоля = ВидПоляФормы.ПолеПереключателя
				ИЛИ
			    ВидПоля = ВидПоляФормы.ПолеФлажка
			Тогда
			
			Если НастройкаUI[СтрШаблон("МенятьЦветПолей%1", ЭтоВыделенноеПолеСтр)] Тогда
				ПрисвоитьЦвет(ЭлементФормы.ЦветТекста,
			    	СтилиРодителя[СтрШаблон("%1%2ПолеТекстЦвет", РодительСтр, ЭтоВыделенноеПолеСтр)]);			
			КонецЕсли;
			
				
			Если НастройкаUI[СтрШаблон("МенятьЦветФонаПолей%1", ЭтоВыделенноеПолеСтр)] Тогда
				ПрисвоитьЦвет(ЭлементФормы.ЦветФона, 
					СтилиРодителя[СтрШаблон("%1%2ПолеФон", РодительСтр, ЭтоВыделенноеПолеСтр)]);			
			КонецЕсли;
			
			Если НастройкаUI[СтрШаблон("МенятьЦветРамкиПолей%1", ЭтоВыделенноеПолеСтр)] Тогда
				ПрисвоитьЦвет(ЭлементФормы.ЦветРамки, 
					СтилиРодителя[СтрШаблон("%1%2ПолеРамкаЦвет", РодительСтр, ЭтоВыделенноеПолеСтр)]);			
			КонецЕсли;
			
			Если НастройкаUI[СтрШаблон("МенятьШрифтПолей%1", ЭтоВыделенноеПолеСтр)] Тогда
				ПрисвоитьШрифт(ЭлементФормы.Шрифт,
					СтилиРодителя[СтрШаблон("%1%2ПолеТекстШрифт", РодительСтр, ЭтоВыделенноеПолеСтр)]);			
			КонецЕсли;	
			
			Если ЭтоВыделенноеПолеСтр = "Выделенное" Тогда
			
				Если НастройкаUI["ВыделятьЖирнымВыделенныеПоляНезависимоОтДругихНастроек"] Тогда
				
					ШрифтЭлемента = ЭлементФормы.Шрифт;
					ЖирныйШрифтЭлемента = ШрифтНаБазеДругогоШрифта(ШрифтЭлемента,, Истина,,,,);
					ЭлементФормы.Шрифт = ЖирныйШрифтЭлемента;
				
				КонецЕсли;	
			
			КонецЕсли;
			
			Если ЭлементФормы.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет Тогда
				
				// Ищем заголовок поля среди элементов ДекорацияФормы.
				
				ЭлементЗаголовка = ЭлементЗаголовка(Форма, ЭлементФормы);
				
				Если ЭлементЗаголовка <> Неопределено Тогда
					
					Если НастройкаUI[СтрШаблон("МенятьЦветЗаголовкаПолей%1", ЭтоВыделенноеПолеСтр)] Тогда
						ПрисвоитьЦвет(ЭлементЗаголовка.ЦветТекста,
					    	СтилиРодителя[СтрШаблон("%1%2ПолеЗаголовокТекстЦвет", РодительСтр, ЭтоВыделенноеПолеСтр)]);			
					КонецЕсли;
					
					ПрисвоитьЦвет(ЭлементЗаголовка.ЦветФона, ЦветФонаЗаголовка);
					
					Если НастройкаUI[СтрШаблон("МенятьШрифтЗаголовкаПолей%1", ЭтоВыделенноеПолеСтр)] Тогда
						ПрисвоитьШрифт(ЭлементЗаголовка.Шрифт,
							СтилиРодителя[СтрШаблон("%1%2ПолеЗаголовокШрифт", РодительСтр, ЭтоВыделенноеПолеСтр)]);
					КонецЕсли;
					
					Если ЭтоВыделенноеПолеСтр = "Выделенное" Тогда
			
						Если НастройкаUI["ВыделятьЖирнымВыделенныеПоляНезависимоОтДругихНастроек"] Тогда
						
							ШрифтЭлемента = ЭлементЗаголовка.Шрифт;
							ЖирныйШрифтЭлемента = ШрифтНаБазеДругогоШрифта(ШрифтЭлемента,, Истина,,,,);
							ЭлементЗаголовка.Шрифт = ЖирныйШрифтЭлемента;
						
						КонецЕсли;	
					
					КонецЕсли;
										
				КонецЕсли;
				
			Иначе
				
				Если НастройкаUI[СтрШаблон("МенятьЦветЗаголовкаПолей%1", ЭтоВыделенноеПолеСтр)] Тогда
					ПрисвоитьЦвет(ЭлементФормы.ЦветТекстаЗаголовка,
				    	СтилиРодителя[СтрШаблон("%1%2ПолеЗаголовокТекстЦвет", РодительСтр, ЭтоВыделенноеПолеСтр)]);			
				КонецЕсли;
				
				ПрисвоитьЦвет(ЭлементФормы.ЦветФонаЗаголовка, ЦветФонаЗаголовка);
				
				Если НастройкаUI[СтрШаблон("МенятьШрифтЗаголовкаПолей%1", ЭтоВыделенноеПолеСтр)] Тогда
					ПрисвоитьШрифт(ЭлементФормы.ШрифтЗаголовка,
						СтилиРодителя[СтрШаблон("%1%2ПолеЗаголовокШрифт", РодительСтр, ЭтоВыделенноеПолеСтр)]);
				КонецЕсли;
				
				Если ЭтоВыделенноеПолеСтр = "Выделенное" Тогда
			
					Если НастройкаUI["ВыделятьЖирнымВыделенныеПоляНезависимоОтДругихНастроек"] Тогда
					
						ШрифтЭлемента = ЭлементФормы.ШрифтЗаголовка;
						ЖирныйШрифтЭлемента = ШрифтНаБазеДругогоШрифта(ШрифтЭлемента,, Истина,,,,);
						ЭлементФормы.ШрифтЗаголовка = ЖирныйШрифтЭлемента;
					
					КонецЕсли;	
				
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если 	(
					ВидПоля = ВидПоляФормы.ПолеВвода
					ИЛИ
				    ВидПоля = ВидПоляФормы.ПолеНадписи
				)
				И
				ЭтоПолеАдреса
			Тогда
				
			ЭлементФормы.Высота = 0;
			
		КонецЕсли;
				
		// Отдельно - надпись в случае гиперссылки.
		Если ВидПоля = ВидПоляФормы.ПолеНадписи Тогда
			
			//Если ЭлементФормы.Гиперссылка Тогда
			//	ПрисвоитьЦвет(ЭлементФормы.ЦветТекста, СтилиРодителя["ЦветГиперссылкиБРО"]);
			//КонецЕсли;
			ПрисвоитьЦвет(ЭлементФормы.ЦветФона, ЦветФонаЗаголовка);
			
		КонецЕсли;
		
		Если НастройкаUI["ВыделятьПодчеркиваниемГиперссылкиНезависимоОтДругихНастроек"] Тогда
			
			Если ЭтоИнтерфейс_8_5 Тогда
				ПодчеркнутьГиперссылку(ЭлементФормы,);				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗначенияЭлемента = Тип("ТаблицаФормы") Тогда
		
		ИнициализироватьUIТаблицыФормы(ЭлементФормы, НастройкаUI, Форма, МасштабКлиентскогоПриложения, 
										ЭтоФормаАЛКООтчета, ЭтоИнтерфейс_8_5);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура МасштабироватьГруппуФормы(Форма, ГруппаФормы, НастройкаUI,
										ЭтоИнтерфейс_8_5, ЭтоФормаАЛКООтчета, 
										МасштабКлиентскогоПриложения)
	
	ПодчиненныеЭлементы = ГруппаФормы.ПодчиненныеЭлементы;
	
	Для каждого ПодчиненныйЭлементФормы Из ПодчиненныеЭлементы Цикл
	    
		МасштабироватьЭлементФормы(Форма, ПодчиненныйЭлементФормы, НастройкаUI,
										ЭтоИнтерфейс_8_5, ЭтоФормаАЛКООтчета,
										МасштабКлиентскогоПриложения);
	
	КонецЦикла;

КонецПроцедуры

Процедура ИнициализироватьUIГруппыФормы(Форма, ГруппаФормы, НастройкаUI,
										ЭтоИнтерфейс_8_5, ЭтоФормаАЛКООтчета, 
										МасштабКлиентскогоПриложения,
										РодительЭлемента, СтилиРодителя, РодительСтр)
	
	ВидГруппы = ГруппаФормы.Вид;
	
	Если ВидГруппы = ВидГруппыФормы.Страницы Тогда
		
		Если НастройкаUI["МенятьЦветЗаголовкаСтраницы"] Тогда
			ПрисвоитьЦвет(ГруппаФормы.ЦветТекстаЗаголовка, СтилиРодителя["СтраницыЗаголовокТекстЦвет"]);
		КонецЕсли;
		Если НастройкаUI["МенятьШрифтЗаголовкаСтраницы"] Тогда
			ПрисвоитьШрифт(ГруппаФормы.ШрифтЗаголовка, СтилиРодителя["СтраницыЗаголовокШрифт"]);
		КонецЕсли;
		
		РодительСтр = "Страница";
		
	ИначеЕсли 	
		ВидГруппы = ВидГруппыФормы.ОбычнаяГруппа 
		ИЛИ 
		ВидГруппы = ВидГруппыФормы.Страница 
		Тогда
		
		РодительСтр = ?(ЭлементПринадлежитСтранице(ГруппаФормы), "Страница", "Группа");
		
		// На случай, если в настройках стоит не Авто.
		ГруппаФормы.ВертикальныйИнтервал   = ИнтервалМеждуЭлементамиФормы[НастройкаUI[
												СтрШаблон("ВертикальныйИнтервал%1", РодительСтр)]];
		ГруппаФормы.ГоризонтальныйИнтервал = ИнтервалМеждуЭлементамиФормы[НастройкаUI[
												СтрШаблон("ГоризонтальныйИнтервал%1", РодительСтр)]];
		
		Если НастройкаUI[СтрШаблон("МенятьЦветЗаголовка%1", РодительСтр)] Тогда
			ПрисвоитьЦвет(ГруппаФормы.ЦветТекстаЗаголовка, 
						СтилиРодителя[СтрШаблон("%1ЗаголовокТекстЦвет", РодительСтр)]);
		КонецЕсли;
		Если НастройкаUI[СтрШаблон("МенятьШрифтЗаголовка%1", РодительСтр)] Тогда
			ПрисвоитьШрифт(ГруппаФормы.ШрифтЗаголовка, СтилиРодителя[СтрШаблон("%1ЗаголовокШрифт", РодительСтр)]);
		КонецЕсли;
		
		Если НастройкаUI[СтрШаблон("МенятьЦветФона%1", РодительСтр)] Тогда
			ПрисвоитьЦвет(ГруппаФормы.ЦветФона, СтилиРодителя[СтрШаблон("%1Фон", РодительСтр)]);
		КонецЕсли;
		
		Если ВидГруппы = ВидГруппыФормы.Страница Тогда
			ГруппаФормы.ВертикальнаяПрокруткаПриСжатии = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ПодчиненныеЭлементы = ГруппаФормы.ПодчиненныеЭлементы;
	
	Для каждого ПодчиненныйЭлементФормы Из ПодчиненныеЭлементы Цикл
	    
		ИнициализироватьUIЭлементаФормы(Форма, ПодчиненныйЭлементФормы, НастройкаUI,
										ЭтоИнтерфейс_8_5, ЭтоФормаАЛКООтчета,
										МасштабКлиентскогоПриложения,
										ГруппаФормы, СтилиРодителя, РодительСтр);
	
	КонецЦикла;

КонецПроцедуры

Процедура МасштабироватьКолонкиТаблицыФормы(ТаблицаФормы, НастройкаUI, ЭтоИнтерфейс_8_5, 
								ЭтоФормаАЛКООтчета, МасштабКлиентскогоПриложения, МасштабФормы)
	
	Если ЭтоИнтерфейс_8_5 И НастройкаUI["ВключеноМасштабирование"] И ЭтоФормаАЛКООтчета Тогда
		
		МассивИсключенныхИзМасштабированияИмен = 
			СтрРазделить(ВРег(НастройкаUI["СписокИсключенияИменЭлементовПриМасштабировании_Интерфейс_8_5"]), ",", Ложь);
		Если МассивИсключенныхИзМасштабированияИмен.Найти(ВРег(ТаблицаФормы.Имя)) = Неопределено Тогда
			ИзменитьМасштабЭлементаФормы(ТаблицаФормы, МасштабФормы, ЭтоИнтерфейс_8_5, 
												МасштабКлиентскогоПриложения, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	МассивИсключенныхИмен = СтрРазделить(ВРег(НастройкаUI["СписокИсключенияИменЭлементов"]), ",", Ложь);
	Если МассивИсключенныхИмен.Найти(ВРег(ТаблицаФормы.Имя)) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПодчиненныеЭлементы = ТаблицаФормы.ПодчиненныеЭлементы;
	
	Для каждого Элемент Из ПодчиненныеЭлементы Цикл
	
		ТипЗначенияПоля = ТипЗнч(Элемент);
		
		Если ТипЗначенияПоля = Тип("ПолеФормы") Тогда
		
			МасштабироватьКолонкуТаблицыФормы(Элемент, НастройкаUI, ЭтоИнтерфейс_8_5, 
							ЭтоФормаАЛКООтчета, МасштабКлиентскогоПриложения, МасштабФормы);
			
		ИначеЕсли ТипЗначенияПоля = Тип("ГруппаФормы") Тогда
			
			МасштабироватьКолонкиТаблицыФормы(Элемент, НастройкаUI, ЭтоИнтерфейс_8_5, 
							ЭтоФормаАЛКООтчета, МасштабКлиентскогоПриложения, МасштабФормы);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ИнициализироватьUIКолонокТаблицыФормы(ТаблицаФормы, НастройкаUI, Стиль, ЭтоИнтерфейс_8_5, 
													ЭтоФормаАЛКООтчета, МасштабКлиентскогоПриложения, МасштабФормы)
	
	Если ЭтоИнтерфейс_8_5 И НастройкаUI["ВключеноМасштабирование"] И ЭтоФормаАЛКООтчета Тогда
		
		МассивИсключенныхИзМасштабированияИмен = 
			СтрРазделить(ВРег(НастройкаUI["СписокИсключенияИменЭлементовПриМасштабировании_Интерфейс_8_5"]), ",", Ложь);
		Если МассивИсключенныхИзМасштабированияИмен.Найти(ВРег(ТаблицаФормы.Имя)) = Неопределено Тогда
			ИзменитьМасштабЭлементаФормы(ТаблицаФормы, МасштабФормы, ЭтоИнтерфейс_8_5, 
												МасштабКлиентскогоПриложения, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	МассивИсключенныхИмен = СтрРазделить(ВРег(НастройкаUI["СписокИсключенияИменЭлементов"]), ",", Ложь);
	Если МассивИсключенныхИмен.Найти(ВРег(ТаблицаФормы.Имя)) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НастройкаUI["ЗаголовкиКолонокСБольшойБуквы"] Тогда
	
		СтруктураПроверки = Новый Структура("Заголовок");
		ЗаполнитьЗначенияСвойств(СтруктураПроверки, ТаблицаФормы);
		
		Если 	ТипЗнч(СтруктураПроверки.Заголовок) = Тип("Строка")
				И
				ЗначениеЗаполнено(СтруктураПроверки.Заголовок)
			Тогда
			
			ТаблицаФормы.Заголовок = СокрЛП(ТаблицаФормы.Заголовок);
			
			Если СтрДлина(ТаблицаФормы.Заголовок) > 1 Тогда
				ТаблицаФормы.Заголовок = ВРег(Лев(ТаблицаФормы.Заголовок, 1)) + Сред(ТаблицаФормы.Заголовок, 2);
			Иначе
				// Поскольку заголовок не пуст - в нем один символ.
				ТаблицаФормы.Заголовок = ВРег(ТаблицаФормы.Заголовок);
			КонецЕсли;
		
		КонецЕсли;
	
	КонецЕсли;
	
	Если ТипЗнч(ТаблицаФормы) = Тип("ГруппаФормы") Тогда
	
		Если ТаблицаФормы.ОтображатьВШапке Тогда
			
			Если НастройкаUI["МенятьЦветЗаголовкаКолонокТаблиц"] Тогда
				ПрисвоитьЦвет(ТаблицаФормы.ЦветТекстаЗаголовка, Стиль["ТаблицыЗаголовокКолонокТекстЦвет"]);
			КонецЕсли;
			
			Если НастройкаUI["МенятьШрифтЗаголовкаКолонокТаблиц"] Тогда
				ПрисвоитьШрифт(ТаблицаФормы.ШрифтЗаголовка, Стиль["ТаблицыЗаголовокКолонокТекстШрифт"]);
			КонецЕсли;
			
			Если НастройкаUI["МенятьФонЗаголовкаКолонокТаблиц"] Тогда
				ПрисвоитьЦвет(ТаблицаФормы.ЦветФонаЗаголовка, Стиль["ТаблицыЗаголовокКолонокФон"]);
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЕсли;
	
	ПодчиненныеЭлементы = ТаблицаФормы.ПодчиненныеЭлементы;
	
	Для каждого Элемент Из ПодчиненныеЭлементы Цикл
	
		ТипЗначенияПоля = ТипЗнч(Элемент);
		
		Если ТипЗначенияПоля = Тип("ПолеФормы") Тогда
		
			ИнициализироватьUIКолонкиТаблицыФормы(Элемент, НастройкаUI, Стиль, ЭтоИнтерфейс_8_5, 
													ЭтоФормаАЛКООтчета, МасштабКлиентскогоПриложения, МасштабФормы);
			
		ИначеЕсли ТипЗначенияПоля = Тип("ГруппаФормы") Тогда
			
			ИнициализироватьUIКолонокТаблицыФормы(Элемент, НастройкаUI, Стиль, ЭтоИнтерфейс_8_5, 
												ЭтоФормаАЛКООтчета, МасштабКлиентскогоПриложения, МасштабФормы);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура МасштабироватьКолонкуТаблицыФормы(ПолеКолонки, НастройкаUI, ЭтоИнтерфейс_8_5,
							ЭтоФормаАЛКООтчета, МасштабКлиентскогоПриложения, МасштабФормы)
		
	Если ЭтоИнтерфейс_8_5 И НастройкаUI["ВключеноМасштабирование"] И ЭтоФормаАЛКООтчета Тогда
		
		МассивИсключенныхИзМасштабированияИмен = 
			СтрРазделить(ВРег(НастройкаUI["СписокИсключенияИменЭлементовПриМасштабировании_Интерфейс_8_5"]), ",", Ложь);
		Если МассивИсключенныхИзМасштабированияИмен.Найти(ВРег(ПолеКолонки.Имя)) = Неопределено Тогда
			ИзменитьМасштабЭлементаФормы(ПолеКолонки, МасштабФормы, ЭтоИнтерфейс_8_5,
												МасштабКлиентскогоПриложения, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнициализироватьUIКолонкиТаблицыФормы(ПолеКолонки, НастройкаUI, Стиль, ЭтоИнтерфейс_8_5,
											ЭтоФормаАЛКООтчета, МасштабКлиентскогоПриложения, МасштабФормы)
		
	Если ЭтоИнтерфейс_8_5 И НастройкаUI["ВключеноМасштабирование"] И ЭтоФормаАЛКООтчета Тогда
		
		МассивИсключенныхИзМасштабированияИмен = 
			СтрРазделить(ВРег(НастройкаUI["СписокИсключенияИменЭлементовПриМасштабировании_Интерфейс_8_5"]), ",", Ложь);
		Если МассивИсключенныхИзМасштабированияИмен.Найти(ВРег(ПолеКолонки.Имя)) = Неопределено Тогда
			ИзменитьМасштабЭлементаФормы(ПолеКолонки, МасштабФормы, ЭтоИнтерфейс_8_5,
												МасштабКлиентскогоПриложения, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	МассивИсключенныхИмен = СтрРазделить(ВРег(НастройкаUI["СписокИсключенияИменЭлементов"]), ",", Ложь);
	Если МассивИсключенныхИмен.Найти(ВРег(ПолеКолонки.Имя)) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НастройкаUI["ЗаголовкиКолонокСБольшойБуквы"] Тогда
	
		СтруктураПроверки = Новый Структура("Заголовок");
		ЗаполнитьЗначенияСвойств(СтруктураПроверки, ПолеКолонки);
		
		Если 	ТипЗнч(СтруктураПроверки.Заголовок) = Тип("Строка")
				И
				ЗначениеЗаполнено(СтруктураПроверки.Заголовок)
			Тогда
			
			ПолеКолонки.Заголовок = СокрЛП(ПолеКолонки.Заголовок);
			
			Если СтрДлина(ПолеКолонки.Заголовок) > 1 Тогда
				ПолеКолонки.Заголовок = ВРег(Лев(ПолеКолонки.Заголовок, 1)) + Сред(ПолеКолонки.Заголовок, 2);
			Иначе
				// Поскольку заголовок не пуст - в нем один символ.
				ПолеКолонки.Заголовок = ВРег(ПолеКолонки.Заголовок);
			КонецЕсли;
		
		КонецЕсли;
	
	КонецЕсли;
	
	ЭтоВыделенноеПолеСтр = ЭтоВыделенноеПолеСтрокой(ПолеКолонки);
	
	Если ПолеКолонки.ОтображатьВШапке Тогда
		
		Если НастройкаUI[СтрШаблон("МенятьЦветЗаголовкаКолонокТаблиц%1", ЭтоВыделенноеПолеСтр)] Тогда
			ПрисвоитьЦвет(ПолеКолонки.ЦветТекстаЗаголовка, 
					Стиль[СтрШаблон("ТаблицыЗаголовокКолонокТекстЦвет%1", ЭтоВыделенноеПолеСтр)]);
		КонецЕсли;
		
		Если НастройкаUI[СтрШаблон("МенятьШрифтЗаголовкаКолонокТаблиц%1", ЭтоВыделенноеПолеСтр)] Тогда
			ПрисвоитьШрифт(ПолеКолонки.ШрифтЗаголовка, 
					Стиль[СтрШаблон("ТаблицыЗаголовокКолонокТекстШрифт%1", ЭтоВыделенноеПолеСтр)]);
		КонецЕсли;
		
		Если ЭтоВыделенноеПолеСтр = "Выделенное" Тогда
			
			Если НастройкаUI["ВыделятьЖирнымВыделенныеПоляНезависимоОтДругихНастроек"] Тогда
			
				ШрифтЭлемента = ПолеКолонки.ШрифтЗаголовка;
				ЖирныйШрифтЭлемента = ШрифтНаБазеДругогоШрифта(ШрифтЭлемента,, Истина,,,,);
				ПолеКолонки.ШрифтЗаголовка = ЖирныйШрифтЭлемента;
				
				ШрифтЭлемента = ПолеКолонки.Шрифт;
				ЖирныйШрифтЭлемента = ШрифтНаБазеДругогоШрифта(ШрифтЭлемента,, Истина,,,,);
				ПолеКолонки.Шрифт = ЖирныйШрифтЭлемента;
			
			КонецЕсли;	
		
		КонецЕсли;
		
		Если НастройкаUI[СтрШаблон("МенятьФонЗаголовкаКолонокТаблиц%1", ЭтоВыделенноеПолеСтр)] Тогда
			
			// Фон заголовка колонки с отметкой удаления не окрашиваем.
			Если Лев(ПолеКолонки.Имя, 7) <> "Активно" Тогда
			
				ПрисвоитьЦвет(ПолеКолонки.ЦветФонаЗаголовка, 
					Стиль[СтрШаблон("ТаблицыЗаголовокКолонокФон%1", ЭтоВыделенноеПолеСтр)]);
			
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПолеКолонки.ОтображатьВПодвале Тогда
		
		Если НастройкаUI[СтрШаблон("МенятьЦветПодвалаКолонокТаблиц%1", ЭтоВыделенноеПолеСтр)] Тогда
			ПрисвоитьЦвет(ПолеКолонки.ЦветТекстаПодвала, 
					Стиль[СтрШаблон("ТаблицыПодвалКолонокТекстЦвет%1", ЭтоВыделенноеПолеСтр)]);
		КонецЕсли;
		
		Если НастройкаUI[СтрШаблон("МенятьШрифтПодвалаКолонокТаблиц%1", ЭтоВыделенноеПолеСтр)] Тогда
			ПрисвоитьШрифт(ПолеКолонки.ШрифтПодвала, 
					Стиль[СтрШаблон("ТаблицыПодвалКолонокТекстШрифт%1", ЭтоВыделенноеПолеСтр)]);
		КонецЕсли;
		
		Если НастройкаUI[СтрШаблон("МенятьФонПодвалаКолонокТаблиц%1", ЭтоВыделенноеПолеСтр)] Тогда
			ПрисвоитьЦвет(ПолеКолонки.ЦветФонаПодвала, 
					Стиль[СтрШаблон("ТаблицыПодвалКолонокФон%1", ЭтоВыделенноеПолеСтр)]);
		КонецЕсли;
		
		Если ЭтоВыделенноеПолеСтр = "Выделенное" Тогда
			
			Если НастройкаUI["ВыделятьЖирнымВыделенныеПоляНезависимоОтДругихНастроек"] Тогда
				
				ШрифтЭлемента = ПолеКолонки.ШрифтПодвала;
				ЖирныйШрифтЭлемента = ШрифтНаБазеДругогоШрифта(ШрифтЭлемента,, Истина,,,,);
				ПолеКолонки.ШрифтПодвала = ЖирныйШрифтЭлемента;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если 	ВРег(ПолеКолонки.Имя) <> ВРег("Активно")
			И
			ВРег(ПолеКолонки.Имя) <> ВРег("ИндексСтроки")
		Тогда
	
		ПолеКолонки.Ширина = 0;
	
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти
