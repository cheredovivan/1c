// @strict-types

#Область СлужебныеПроцедурыИФункции

#Область Использование

// Параметры:
//  ИдентификаторыЭДО - Массив из Строка
//  РасшифрованныеМаркеры - см. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
//  ИдентификаторФормы - Неопределено,УникальныйИдентификатор
// 
// Возвращаемое значение:
//  См. ДлительныеОперации.ВыполнитьФункцию
Функция ВключитьПоУчетнымЗаписямЭДОВФоне(Знач ИдентификаторыЭДО, Знач РасшифрованныеМаркеры, Знач ИдентификаторФормы = Неопределено) Экспорт
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	
	// См. АвтоматическоеПолучениеЭДО.ВключитьПоУчетнымЗаписямЭДО
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполненияВФоне,
		"АвтоматическоеПолучениеЭДО.ВключитьПоУчетнымЗаписямЭДО",
		ИдентификаторыЭДО, РасшифрованныеМаркеры);
	
КонецФункции

// Параметры:
//  ДлительнаяОперация - см. ДлительныеОперации.ВыполнитьФункцию
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
// 
// Возвращаемое значение:
//  Неопределено,Структура:
//  * ЕстьПредупреждения - Булево
//  * СостояниеПоИдентификаторамЭДО - см. РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.СостояниеПоИдентификаторамЭДО
Функция ИзменениеСостоянияПослеВключенияПоУчетнымЗаписямЭДО(Знач ДлительнаяОперация, КонтекстДиагностики) Экспорт
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат Неопределено;
	ИначеЕсли ДлительнаяОперация.Статус <> "Выполнено" Тогда	
		
		Если ДлительнаяОперация.Статус = "Ошибка" Тогда
			ВидОперации = НСтр("ru = 'Включение автоматического получения данных ЭДО';
								|en = 'Enable automatic receipt of EDI data'");
			ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка();
			Ошибка = ОбработкаНеисправностейБЭД.НоваяОшибка(ВидОперации, ВидОшибки,
				ДлительнаяОперация.ПодробноеПредставлениеОшибки, ДлительнаяОперация.КраткоеПредставлениеОшибки);
			ОбработкаНеисправностейБЭД.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
				ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().АвтоматическоеПолучениеЭДО);
		КонецЕсли;
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	РезультатВключения = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата); // См. АвтоматическоеПолучениеЭДО.ВключитьПоУчетнымЗаписямЭДО
	УдалитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
	
	ОбработкаНеисправностейБЭДКлиентСервер.ДополнитьКонтекстДиагностики(КонтекстДиагностики,
		РезультатВключения.КонтекстДиагностики);
	
	Если Не ЗначениеЗаполнено(РезультатВключения.СостояниеПоИдентификаторамЭДО) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = АвтоматическоеПолучениеЭДОКлиентСервер.НовоеИзменениеСостояния();
	Результат.ЕстьПредупреждения = РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.ЕстьПредупреждения();
	Результат.СостояниеПоИдентификаторамЭДО = РезультатВключения.СостояниеПоИдентификаторамЭДО;
	
	Возврат Результат;
	
КонецФункции

// См. АвтоматическоеПолучениеЭДО.ВключитьИспользованиеТокеновДоступа
Функция ВключитьИспользованиеТокеновДоступа(КонтекстДиагностики = Неопределено) Экспорт
	
	Возврат АвтоматическоеПолучениеЭДО.ВключитьИспользованиеТокеновДоступа(КонтекстДиагностики);
	
КонецФункции

// См. АвтоматическоеПолучениеЭДО.Отключить
Процедура Отключить(Знач ИдентификаторЭДО = Неопределено) Экспорт
	
	АвтоматическоеПолучениеЭДО.Отключить(ИдентификаторЭДО);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаДоступности

Процедура ВключитьПроверкуПодписейНаСервере() Экспорт
	
	КриптографияБЭД.УстановитьНастройкиСервернойКриптографии(, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область Получение

// Параметры:
//  АдресРезультата - Строка
// 
// Возвращаемое значение:
//  См. АвтоматическоеПолучениеЭДО.ПолучитьЧерезИнтервал
Функция РезультатПолученияЧерезИнтервал(Знач АдресРезультата) Экспорт
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата); // См. АвтоматическоеПолучениеЭДО.ПолучитьЧерезИнтервал
	УдалитьИзВременногоХранилища(АдресРезультата);
	Возврат Результат;
КонецФункции

#КонецОбласти

#Область Предупреждение

// Параметры:
//  ИдентификаторЭДО - Строка
// 
// Возвращаемое значение:
//  См. АвтоматическоеПолучениеЭДО.ОтборЖурналаРегистрации
Функция ОтборЖурналаРегистрации(Знач ИдентификаторЭДО) Экспорт
	Возврат АвтоматическоеПолучениеЭДО.ОтборЖурналаРегистрации(ИдентификаторЭДО);
КонецФункции

#КонецОбласти

#КонецОбласти
