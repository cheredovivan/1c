#Область ПрограммныйИнтерфейс

// Обработчик события ПриЗаписи источника оповещений пользователей.
// 
// Параметры:
//  Источник - СправочникОбъект, ДокументОбъект - Источник
//  Отказ - Булево - Отказ
Процедура ПриЗаписиИсточникаОповещенийПользователей(Источник, Отказ) Экспорт
	
	ПолноеИмяИсточника = Источник.Метаданные().ПолноеИмя();
	Если Источник.ОбменДанными.Загрузка 
		Или Источник.ДополнительныеСвойства.Свойство("ОтключитьОповещениеПользователей") 
		Или Не ИсточникДоступен(ПолноеИмяИсточника) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ВидыОповещенийПоРеквизитамИсточника = ВидыОповещенийПоРеквизитамИсточника(ПолноеИмяИсточника);
	Для каждого РеквизитИсточник Из ВидыОповещенийПоРеквизитамИсточника Цикл
		
		ИмяРеквизита = РеквизитИсточник.Ключ;
		ДоступныеВидыОповещений = РеквизитИсточник.Значение;
		
		Попытка
			ЗначениеРеквизита = Источник[ИмяРеквизита];
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Оповещения пользователей.Формирование оповещений';
											|en = 'User notifications.Generate notifications'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
		СформироватьЗаданияКОповещениюПользователей(ЗначениеРеквизита, ДоступныеВидыОповещений);
		
	КонецЦикла;
	
КонецПроцедуры

// Обработчик события ПриЗаписи источника оповещений пользователей.
// 
// Параметры:
//  Источник - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Источник оповещения.
//  Отказ - Булево - Отказ.
//  Замещение - Булево - признак замещение.
Процедура ПриЗаписиНабораЗаписейИсточникаОповещенийПользователей(Источник, Отказ, Замещение) Экспорт
	
	ПолноеИмяИсточника = Источник.Метаданные().ПолноеИмя();
	Если Источник.ОбменДанными.Загрузка 
		Или Источник.ДополнительныеСвойства.Свойство("ОтключитьОповещениеПользователей")
		Или Не ИсточникДоступен(ПолноеИмяИсточника) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ВидыОповещенийПоРеквизитамИсточника = ВидыОповещенийПоРеквизитамИсточника(ПолноеИмяИсточника);
	Для каждого РеквизитИсточник Из ВидыОповещенийПоРеквизитамИсточника Цикл
		
		ИмяРеквизита = РеквизитИсточник.Ключ;
		ДоступныеВидыОповещений = РеквизитИсточник.Значение;
		
		Попытка
			МассивИсточников = Источник.ВыгрузитьКолонку(ИмяРеквизита);
		Исключение
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Оповещения пользователей.Формирование оповещений';
											|en = 'User notifications.Generate notifications'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
		МассивИсточников = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивИсточников);
		СформироватьЗаданияКОповещениюПользователей(МассивИсточников, ДоступныеВидыОповещений);
		
	КонецЦикла;
КонецПроцедуры

// Формирует задания в регистр сведений "Задания к оповещению пользователей".
// 
// Параметры:
//  ИсточникОповещения - ЛюбаяСсылка, Массив Из ЛюбаяСсылка - ссылка-источник задания или массив ссылок, которые надо отразить.
//  ВидыОповещений - Массив Из СправочникСсылка.ВидыОповещенийПользователей - массив отражаемых видов оповещения по источнику.
//  ИнициаторОповещения - СправочникСсылка.Пользователи, Неопределено - пользователь инициатор оповещения.
//  Запустить - Булево - признак запуска выполнения заданий. По умолчанию ИСТИНА.
Процедура СформироватьЗаданияКОповещениюПользователей(ИсточникОповещения, ВидыОповещений, ИнициаторОповещения = Неопределено, Запустить = Истина) Экспорт
	
	Если Не ЗначениеЗаполнено(ИсточникОповещения) ИЛИ Не ЗначениеЗаполнено(ВидыОповещений) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИнициаторОповещения) Тогда
		ИнициаторОповещения = Пользователи.АвторизованныйПользователь();
	КонецЕсли;
	
	Очередь = РегистрыСведений.ЗаданияКОповещениюПользователей.ИмяОчередиЗаданий();
	
	ТаблицаЗаданий = ?(Запустить,
						ОтложенныеЗадания.ИнициализироватьТаблицуЗаданий(Очередь),
						Неопределено);
	РегистрыСведений.ЗаданияКОповещениюПользователей.ДобавитьЗаданияКОтражению(
		ИсточникОповещения,
		ВидыОповещений,
		ИнициаторОповещения,
		ТаблицаЗаданий);
	
	Если Запустить Тогда
		ОтложенныеЗадания.Запустить(ТаблицаЗаданий);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик регламентного задания.
// Выполняет оповещение пользователей по неоперативным оповещениям.
//
Процедура ОповещениеПользователейПоНеоперативнымВидамОповещений() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОповещенияПользователейОСобытиях);
	Если МонопольныйРежим() ИЛИ ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Автор = ОповещенияПользователейОСобытияхПовтИсп.АвторОповещенийПользователей();
	ПроверитьПодготовитьСоединениеССистемойВзаимодействия(Автор);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВидыОповещений.Ссылка КАК Ссылка,
	|	ВидыОповещений.ШаблонТекстаОповещения,
	|	ВидыОповещений.ПорогГруппировки,
	|	ВидыОповещений.ТекстСгруппированногоОповещения,
	|	ВидыОповещений.ИмяШаблонаСКД,
	|	ВидыОповещений.СхемаКомпоновкиДанных,
	|	ВидыОповещений.ХранилищеНастроекКомпоновкиДанных
	|ИЗ
	|	Справочник.ВидыОповещенийПользователей КАК ВидыОповещений
	|ГДЕ
	|	ВидыОповещений.Активен
	|	И НЕ ВидыОповещений.ПометкаУдаления
	|	И НЕ ВидыОповещений.ЭтоГруппа
	|	И ВидыОповещений.ТипОповещения = ЗНАЧЕНИЕ(Перечисление.ТипыОповещенийПользователей.Неоперативное)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОповещенийПользователей.ИсточникОповещения,
	|	ДанныеОповещенийПользователей.ВидОповещения,
	|	ДанныеОповещенийПользователей.КонтекстОповещения,
	|	ДанныеОповещенийПользователей.ДатаЗаписи
	|ИЗ
	|	РегистрСведений.ДанныеОповещенийПользователей КАК ДанныеОповещенийПользователей
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыОповещенийПользователей КАК ВидыОповещенийПользователей
	|		ПО ДанныеОповещенийПользователей.ВидОповещения = ВидыОповещенийПользователей.Ссылка
	|ГДЕ
	|	ДОБАВИТЬКДАТЕ(ДанныеОповещенийПользователей.ДатаЗаписи, ДЕНЬ, ВидыОповещенийПользователей.СрокХраненияКэша) <= &ТекущаяДата
	|";
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	Выборка = ПакетЗапросов[0].Выбрать();
	Пока Выборка.Следующий() Цикл
		//@skip-check query-in-loop
		ДанныеПоВидуОповещений = ДанныеПоНеоперативномуВидуОповещений(Выборка);
		ОбработатьНеоперативноеОповещениеПользователей(ДанныеПоВидуОповещений, Автор);
	КонецЦикла;
	
	ДанныеОповещенийУдалить = ПакетЗапросов[1].Выгрузить();
	
	Попытка
		НаборЗаписей = РегистрыСведений.ДанныеОповещенийПользователей.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(ДанныеОповещенийУдалить);
		НаборЗаписей.Записать(РежимЗамещения.Удаление);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Оповещения пользователей.Выполнение заданий';
										|en = 'User notifications.Perform jobs'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОписаниеОшибки);
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Описание видов оповещений пользователей.
// 
// Возвращаемое значение:
//	ТаблицаЗначений - Описание видов оповещений пользователей:
//		* Идентификатор - Строка - уникальный идентификатор вида оповещения.
//		* Наименование - Строка - наименование вида оповещения.
//		* ГруппаВидаОповещений - см. ОписаниеГруппыВидовОповещений.
//		* ТипОповещения - ПеречислениеСсылка.ТипыОповещенийПользователей - тип оповещения пользователей.
//		* Поставляемый - Булево - признак, что вид оповещения является поставляемым.
//		* Изменен - Булево - признак, что вид оповещения был изменен относительно поставляемого.
//		* ШаблонТекстаОповещения - Строка - шаблон текста оповещения.
//		* СрокХраненияКэша - Число - срок хранения кэша данных оповещения.
//		* ПорогГруппировки - Число - порог группировки оповещений.
//		* ТекстСгруппированногоОповещения - Строка - текст оповещения, сформированный для нескольких записей.
//		* ИмяШаблонаСКД - Строка - путь к макету СКД в метаданных.
//		* СхемаКомпоновкиДанных - ХранилищеЗначения - хранилище значения со схемой компоновки данных.
//		* ХранилищеНастроекКомпоновкиДанных - ХранилищеЗначения - хранилище значения с настройками схемы компоновки данных.
//		* ИсточникиДанных - Массив из см. НовоеОписаниеИсточникДанных - источники данных для оповещения.
Функция ОписаниеВидовОповещенийПользователей() Экспорт
	
	ОписаниеВидовОповещений = Новый ТаблицаЗначений();
	ОписаниеВидовОповещений.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	ОписаниеВидовОповещений.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	ОписаниеВидовОповещений.Колонки.Добавить("ГруппаВидаОповещений", Новый ОписаниеТипов("Структура"));
	ОписаниеВидовОповещений.Колонки.Добавить("ТипОповещения", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыОповещенийПользователей"));
	ОписаниеВидовОповещений.Колонки.Добавить("Поставляемый", Новый ОписаниеТипов("Булево"));
	ОписаниеВидовОповещений.Колонки.Добавить("Изменен", Новый ОписаниеТипов("Булево"));
	ОписаниеВидовОповещений.Колонки.Добавить("ШаблонТекстаОповещения", Новый ОписаниеТипов("Строка"));
	ОписаниеВидовОповещений.Колонки.Добавить("СрокХраненияКэша", Новый ОписаниеТипов("Число", 
		Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный)));
	ОписаниеВидовОповещений.Колонки.Добавить("ПорогГруппировки", Новый ОписаниеТипов("Число", 
		Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный)));	
	ОписаниеВидовОповещений.Колонки.Добавить("ТекстСгруппированногоОповещения", Новый ОписаниеТипов("Строка"));
	ОписаниеВидовОповещений.Колонки.Добавить("ИмяШаблонаСКД", Новый ОписаниеТипов("Строка"));
	ОписаниеВидовОповещений.Колонки.Добавить("СхемаКомпоновкиДанных", Новый ОписаниеТипов("ХранилищеЗначения"));
	ОписаниеВидовОповещений.Колонки.Добавить("ХранилищеНастроекКомпоновкиДанных", Новый ОписаниеТипов("ХранилищеЗначения"));
	ОписаниеВидовОповещений.Колонки.Добавить("ИсточникиДанных", Новый ОписаниеТипов("Массив"));
	
	ОписаниеВидовОповещений.Индексы.Добавить("Идентификатор");
	Возврат ОписаниеВидовОповещений;
КонецФункции

// Добавляет описание вида оповещений пользователей со значениями по умолчанию.
// 
// Параметры:
//  ОписаниеВидовОповещений - см. ОписаниеВидовОповещенийПользователей
// 
// Возвращаемое значение:
//  СтрокаТаблицыЗначений: см. ОписаниеВидовОповещенийПользователей
//@skip-check function-return-types
Функция ДобавитьОписаниеВидаОповещенийПользователей(ОписаниеВидовОповещений) Экспорт
	
	НовыйВидОповещений = ОписаниеВидовОповещений.Добавить();
	НовыйВидОповещений.ТипОповещения = Неопределено;
	НовыйВидОповещений.Поставляемый = Истина;
	НовыйВидОповещений.Изменен = Ложь;
	НовыйВидОповещений.СрокХраненияКэша = 365;
	НовыйВидОповещений.ПорогГруппировки = 5;
	НовыйВидОповещений.ИмяШаблонаСКД = ИмяПредопределенногоШаблона();
	НовыйВидОповещений.ИсточникиДанных = Новый Массив;

	Возврат НовыйВидОповещений;
	
КонецФункции

// Добавляет описание источника данных вида оповещений пользователей.
//
// Параметры:
// 	СтрокаТаблицыОписаниеВидовОповещений - СтрокаТаблицыЗначений: см. ОписаниеВидовОповещенийПользователей - строка таблицы с описание вида оповещения.
// 	ПолноеИмяИсточника - Строка - источник данных для оповещения в виде ОбъектМетаданных.ПолноеИмя().
// 	ИмяРеквизита - Строка - имя основного реквизита источника данных.
Процедура ДобавитьОписаниеИсточникаДанных(СтрокаТаблицыОписаниеВидовОповещений, ПолноеИмяИсточника, ИмяРеквизита = "") Экспорт
	ИсточникДанных = НовоеОписаниеИсточникДанных(ПолноеИмяИсточника, ИмяРеквизита);
	ИсточникиДанных = СтрокаТаблицыОписаниеВидовОповещений.ИсточникиДанных;
	ИсточникиДанных.Добавить(ИсточникДанных);
КонецПроцедуры

// Формирует описание источника данных вида оповещений пользователей.
//
// Параметры:
// 	ПолноеИмяИсточника - Строка - источник данных для оповещения в виде ОбъектМетаданных.ПолноеИмя().
// 	ИмяРеквизита - Строка - имя основного реквизита источника данных.
// 
// Возвращаемое значение:
//  Структура - Новое описание источника данных вида оповещений:
// * ТипИсточника - см. ОбщегоНазначения.ИдентификаторОбъектаМетаданных
// * РеквизитИсточника - Строка - наименование вида оповещения.
//
Функция НовоеОписаниеИсточникДанных(ПолноеИмяИсточника, ИмяРеквизита = "") Экспорт
	
	НовоеОписание = Новый Структура;
	НовоеОписание.Вставить("ТипИсточника", ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяИсточника));
	Если ИмяРеквизита = "" Тогда
		ИмяРеквизита = "Ссылка";
		ЧастиИмени = СтрРазделить(ПолноеИмяИсточника, ".");
		ЕстьРегистратор = ТипыЕстьРегистратор().Найти(ЧастиИмени[0]);
		Если ЕстьРегистратор <> Неопределено Тогда
			ИмяРеквизита = "Регистратор";
		КонецЕсли;
	КонецЕсли;
	НовоеОписание.Вставить("РеквизитИсточника", ИмяРеквизита);
	
	//@skip-check constructor-function-return-section
	Возврат НовоеОписание;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет задания оповещения пользователей по очереди заданий.
//
// Параметры:
//	Задания - ТаблицаЗначений - колонки соответствуют измерениям, ресурсам и реквизитам регистра сведений:
//		* ВидОповещения - СправочникСсылка.ВидыОповещенийПользователей - ссылка на вид оповещения
//		* ИдентификаторЗаписи - УникальныйИдентификатор - идентификатор записи очереди заданий
//		* ДатаЗаписи - Дата - служебная колонка
//		* ИсточникОповещения - ЛюбаяСсылка - ссылка на источник оповещения
//		* ИнициаторОповещения - СправочникСсылка.Пользователи - пользователь инициатор оповещения
//		* ИдентификаторЗадания - УникальныйИдентификатор - служебная колонка
//		* ДатаЗадания - Дата - служебная.
//	ИдентификаторыНеОбработанныхЗаписей - Соответствие из КлючИЗначение:
//		* Ключ - УникальныйИдентификатор - идентификатор не выполненных (выдающих ошибку) записей регистра.
//		* Значение - Строка - представление ошибки.
//	ДополнительныеСвойства - Неопределено, Структура - дополнительные свойства выполнения заданий.
//
Процедура ВыполнитьЗаданияКОповещениюПользователей(Задания, ИдентификаторыНеОбработанныхЗаписей, ДополнительныеСвойства = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Автор = ОповещенияПользователейОСобытияхПовтИсп.АвторОповещенийПользователей();
	ПроверитьПодготовитьСоединениеССистемойВзаимодействия(Автор);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задания.ВидОповещения КАК ВидОповещения,
	|	Задания.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	Задания.ДатаЗаписи КАК ДатаЗаписи,
	|	Задания.ИсточникОповещения КАК ИсточникОповещения,
	|	Задания.ИнициаторОповещения КАК ИнициаторОповещения,
	|	Задания.ИдентификаторЗадания КАК ИдентификаторЗадания,
	|	Задания.ДатаЗадания КАК ДатаЗадания
	|ПОМЕСТИТЬ ВтЗадания
	|ИЗ
	|	&Задания КАК Задания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задания.ВидОповещения КАК ВидОповещения,
	|	Задания.ИсточникОповещения КАК ИсточникОповещения,
	|	Задания.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	Задания.ИнициаторОповещения КАК ИнициаторОповещения,
	|	Задания.ДатаЗадания КАК ДатаЗадания
	|ИЗ
	|	ВтЗадания КАК Задания
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаЗадания
	|ИТОГИ ПО
	|	ВидОповещения";

	Запрос.УстановитьПараметр("Задания", Задания);

	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаДанныхПоВиду = ИнициализироватьТаблицуИсходныеДанные();
	
	ВыборкаВидОповещения = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаВидОповещения.Следующий() Цикл
		Выборка = ВыборкаВидОповещения.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ТаблицаДанныхПоВиду.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЦикла;
		
		//@skip-check query-in-loop
		ДанныеПоВидуОповещений = ДанныеПоОперативномуВидуОповещений(ВыборкаВидОповещения.ВидОповещения, ТаблицаДанныхПоВиду);
		ОбработатьОповещениеПользователей(ДанныеПоВидуОповещений, Автор, ИдентификаторыНеОбработанныхЗаписей);
		
		ТаблицаДанныхПоВиду.Очистить();
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Инициализирует компоновщик настроек.
// 
// Параметры:
//  СхемаИНастройкиДляИнициализации - СхемаКомпоновкиДанных - Схема компоновки для инициализации настроек.
//  Настройки - Неопределено, НастройкиКомпоновкиДанных - настройки для инициализации компоновщика. 
//  	Если не указано, то используются настройки по умолчанию.
// 
// Возвращаемое значение:
//  КомпоновщикНастроекКомпоновкиДанных - инициализированный компоновщик настроек
Функция ИнициализироватьКомпоновщикНастроек(СхемаИНастройкиДляИнициализации, Настройки = Неопределено) Экспорт
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	Если СхемаИНастройкиДляИнициализации = Неопределено Тогда
		Возврат КомпоновщикНастроек
	КонецЕсли;
	
	Попытка
		КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаИНастройкиДляИнициализации));
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Оповещения пользователей.Загрузка СКД';
										|en = 'User notifications.Import DCS'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Если ЗначениеЗаполнено(Настройки) Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	Иначе
		КомпоновщикНастроек.ЗагрузитьНастройки(СхемаИНастройкиДляИнициализации.НастройкиПоУмолчанию);
	КонецЕсли;
	КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	
	Возврат КомпоновщикНастроек
	
КонецФункции

// Позволяет проверить корректность схемы получения данных для формирования оперативных оповещений, при необходимости исправить ошибки,
// а также заполнить список критических ошибок, которые необходимо будет исправить пользователю.
//
// Параметры:
//   СхемаКомпоновкиДанных - СхемаКомпоновкиДанных - Схема получения данных оповещений.
//   СхемаСодержитКритическиеОшибки - Булево - флаг наличия критических ошибок, который необходимо взвести,
//   				чтобы пользователь не смог применить схему, пока не будут исправлены ошибки.
//   СписокКритическихОшибок - СписокЗначений Из Строка - Список, содержащий тексты сообщений о критических ошибках.
//   СписокПрочихСообщений - СписокЗначений Из Строка - Список, содержащий тексты сообщений о исправленных ошибках и прочие замечания.
//   				Данный список сообщений выводится пользователю один раз.
//
Процедура ПроверкаКорректностиСхемыПолученияДанныхОперативногоВидаОповещений(СхемаКомпоновкиДанных, СхемаСодержитКритическиеОшибки,
					СписокКритическихОшибок, СписокПрочихСообщений) Экспорт
	
	ОбязательныеПоля = ЗарезервированныеИменаПолей(Ложь);
	ПроверкаКорректностиСхемыПолученияДанныхОповещения(СхемаКомпоновкиДанных, СхемаСодержитКритическиеОшибки,
					СписокКритическихОшибок, СписокПрочихСообщений, ОбязательныеПоля);
	
КонецПроцедуры

// Позволяет проверить корректность схемы получения данных для формирования неоперативных оповещений, при необходимости исправить ошибки,
// а также заполнить список критических ошибок, которые необходимо будет исправить пользователю.
//
// Параметры:
//   СхемаКомпоновкиДанных - СхемаКомпоновкиДанных - Схема получения данных оповещений.
//   СхемаСодержитКритическиеОшибки - Булево - флаг наличия критических ошибок, который необходимо взвести,
//   				чтобы пользователь не смог применить схему, пока не будут исправлены ошибки.
//   СписокКритическихОшибок - СписокЗначений Из Строка - Список, содержащий тексты сообщений о критических ошибках.
//   СписокПрочихСообщений - СписокЗначений Из Строка - Список, содержащий тексты сообщений о исправленных ошибках и прочие замечания.
//   				Данный список сообщений выводится пользователю один раз.
//
Процедура ПроверкаКорректностиСхемыПолученияДанныхНеоперативногоВидаОповещений(СхемаКомпоновкиДанных, СхемаСодержитКритическиеОшибки,
					СписокКритическихОшибок, СписокПрочихСообщений) Экспорт
	
	ОбязательныеПоля = ЗарезервированныеИменаПолей(Истина);
	ПроверкаКорректностиСхемыПолученияДанныхОповещения(СхемаКомпоновкиДанных, СхемаСодержитКритическиеОшибки,
					СписокКритическихОшибок, СписокПрочихСообщений, ОбязательныеПоля);
	
КонецПроцедуры

// Проверка корректности схемы компоновки данных перед записью справочника.
// 
// Параметры:
//  ВидОповещенияОбъект - СправочникОбъект.ВидыОповещенийПользователей - Вид оповещения объект.
//  Отказ - Булево - признак наличия ошибки.
Процедура ПроверкаКорректностиСхемыПолученияДанныхПередЗаписью(ВидОповещенияОбъект, Отказ) Экспорт
	
	СхемаКомпоновкиДанных = ВидОповещенияОбъект.СхемаКомпоновкиДанных.Получить();
	Настройки = ВидОповещенияОбъект.ХранилищеНастроекКомпоновкиДанных.Получить();
	Если СхемаКомпоновкиДанных = Неопределено Тогда
		СхемаКомпоновкиДанных = МакетПоПолномуИмени(ВидОповещенияОбъект.ИмяШаблонаСКД);
	КонецЕсли;
	
	ОбязательныеПоля = ЗарезервированныеИменаПолей(ВидОповещенияОбъект.ТипОповещения = Перечисления.ТипыОповещенийПользователей.Неоперативное);
	СписокКритическихОшибок = Новый СписокЗначений();
	СписокПрочихСообщений = Новый СписокЗначений();
	
	ПроверкаКорректностиСхемыПолученияДанныхОповещения(СхемаКомпоновкиДанных, Отказ,
		СписокКритическихОшибок, СписокПрочихСообщений, ОбязательныеПоля, Настройки);
	Если Отказ Тогда
		Для Каждого КритическаяОшибка Из СписокКритическихОшибок Цикл
			ОбщегоНазначения.СообщитьПользователю(КритическаяОшибка.Значение, 
				ВидОповещенияОбъект, 
				"СхемаКомпоновкиДанных");
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает режим отображения сообщений для переданного обсуждения.
// 
// Параметры:
//  Обсуждение - ИдентификаторОбсужденияСистемыВзаимодействия - идентификатор обсуждения.
//  НеБеспокоить - Булево - признак установки отображения оповещений "Не беспокоить".
//  РежимНаблюдения - Неопределено, Булево -  Если Истина, при появлении новых сообщений в обсуждения генерируется оповещение. 
//  	Если Ложь, оповещение не генерируется.
//  Пользователь - Неопределено, СправочникСсылка.Пользователи - Пользователь.
Процедура УстановитьРежимОтображенияСообщений(Обсуждение, НеБеспокоить, РежимНаблюдения = Неопределено, Пользователь = Неопределено) Экспорт
	// Установка режимов для не текущего пользователя доступна только в привилегированном режиме.
	УстановитьПривилегированныйРежим(Истина);
	
	ОтображениеОповещений = ОтображениеОповещенийСистемыВзаимодействия.Обычное;
	Если НеБеспокоить Тогда
		ОтображениеОповещений = ОтображениеОповещенийСистемыВзаимодействия.НеБеспокоить;
	КонецЕсли;
	
	Попытка
		Если Пользователь = Неопределено Тогда
			Пользователь = СистемаВзаимодействия.ИдентификаторТекущегоПользователя();
		КонецЕсли;
		СистемаВзаимодействия.УстановитьОтображениеОповещенийОбсуждения(Обсуждение, ОтображениеОповещений, Пользователь);
		Если ЗначениеЗаполнено(РежимНаблюдения) Тогда
			СистемаВзаимодействия.УстановитьРежимНаблюдения(Обсуждение, РежимНаблюдения, Пользователь);
		КонецЕсли;
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Оповещения пользователей.Система взаимодействия';
										|en = 'User notifications.Collaboration system'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

// Поддерживаемые виды метаданных, по которым будут формироваться оповещения.
// 
// Возвращаемое значение:
//  Массив из см. ОповещенияПользователейОСобытиях.ОписаниеВидаМетаданных - Поддерживаемые виды метаданных
Функция ПоддерживаемыеВидыМетаданных() Экспорт
	
	Массив = Новый Массив;
	
	Массив.Добавить(ОписаниеВидаМетаданных("Справочники", НСтр("ru = 'Справочники';
																|en = 'Catalogs'")));
	Массив.Добавить(ОписаниеВидаМетаданных("Документы", НСтр("ru = 'Документы';
															|en = 'Documents'")));
	Массив.Добавить(ОписаниеВидаМетаданных("РегистрыСведений", НСтр("ru = 'Регистры сведений';
																	|en = 'Information registers'")));
	Массив.Добавить(ОписаниеВидаМетаданных("РегистрыНакопления", НСтр("ru = 'Регистры накопления';
																		|en = 'Accumulation registers'")));
	
	ОповещенияПользователейОСобытияхПереопределяемый.ДополнитьПоддерживаемыеВидыМетаданных(Массив);
	
	Возврат Массив
	
КонецФункции

// Описание вида метаданных.
// 
// Параметры:
//  Имя - Строка - Имя
//  Синоним - Строка - Синоним
// 
// Возвращаемое значение:
//  Структура - Описание вида метаданных:
// * Имя - Строка - 
// * Синоним - Строка - 
Функция ОписаниеВидаМетаданных(Имя, Синоним) Экспорт
	
	Структура = Новый Структура("Имя, Синоним", Имя, Синоним);
	Возврат Структура
	
КонецФункции

// Имя предопределенного шаблона.
// 
// Возвращаемое значение:
//  Строка - Имя предопределенного шаблона
Функция ИмяПредопределенногоШаблона() Экспорт
	
	Возврат Метаданные.Справочники.ВидыОповещенийПользователей.Макеты.Оповещения_Предопределенный.ПолноеИмя();
	
КонецФункции

// Есть изменения.
// 
// Параметры:
//  Значение1 - Произвольный - Значение1
//  Значение2 - Произвольный - Значение2
// 
// Возвращаемое значение:
//  Булево - Есть изменения
Функция ЕстьИзменения(Значение1, Значение2) Экспорт
	Если ОбщегоНазначения.ЗначениеВСтрокуXML(Значение1)
		<> ОбщегоНазначения.ЗначениеВСтрокуXML(Значение2) Тогда
		
		Возврат Истина
		
	КонецЕсли;
	
	Возврат Ложь
	
КонецФункции

// Определяет Индекс картинки.
// 
// Параметры:
//  ПолноеИмя - Строка - Полное имя объекта метаданных.
// 
// Возвращаемое значение:
//  Число - Индекс картинки
Функция ИндексКартинки(ПолноеИмя) Экспорт
	
	ИндексКартинки = 0;
	
	ЧастиИмени = СтрРазделить(ПолноеИмя, ".");
	Если ЧастиИмени.Количество() > 0 Тогда
		ИндексПоТипу = ИндексыКартинокПоТипам()[ЧастиИмени[0]];
		Если ИндексПоТипу <> Неопределено Тогда
			ИндексКартинки = ИндексПоТипу;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИндексКартинки
	
КонецФункции

// Индексы картинок по типам.
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//   * Ключ - Строка - Имя типа
//   * Значение - Число - индекс картинки.
Функция ИндексыКартинокПоТипам() Экспорт
	Соответствие = Новый Соответствие;
	
	Соответствие.Вставить("Константа", 3); //@NON-NLS-1
	Соответствие.Вставить("Constant", 3); //@NON-NLS-1
	Соответствие.Вставить("Константы", 3); //@NON-NLS-1
	Соответствие.Вставить("Constants", 3); //@NON-NLS-1
	
	Соответствие.Вставить("Справочник", 4); //@NON-NLS-1
	Соответствие.Вставить("Catalog", 4); //@NON-NLS-1
	Соответствие.Вставить("Справочники", 4); //@NON-NLS-1
	Соответствие.Вставить("Catalogs", 4); //@NON-NLS-1
	
	Соответствие.Вставить("Документ", 5); //@NON-NLS-1
	Соответствие.Вставить("Document", 5); //@NON-NLS-1
	Соответствие.Вставить("Документы", 5); //@NON-NLS-1
	Соответствие.Вставить("Documents", 5); //@NON-NLS-1
	
	Соответствие.Вставить("ЖурналДокументов", 6); //@NON-NLS-1
	Соответствие.Вставить("DocumentJournal", 6); //@NON-NLS-1
	Соответствие.Вставить("ЖурналыДокументов", 6); //@NON-NLS-1
	Соответствие.Вставить("DocumentJournals", 6); //@NON-NLS-1
	
	Соответствие.Вставить("Перечисление", 7); //@NON-NLS-1
	Соответствие.Вставить("Enum", 7); //@NON-NLS-1
	Соответствие.Вставить("Перечисления", 7); //@NON-NLS-1
	Соответствие.Вставить("Enums", 7); //@NON-NLS-1
	
	Соответствие.Вставить("Отчет", 8); //@NON-NLS-1
	Соответствие.Вставить("Report", 8); //@NON-NLS-1
	Соответствие.Вставить("Отчеты", 8); //@NON-NLS-1
	Соответствие.Вставить("Reports", 8); //@NON-NLS-1
	
	Соответствие.Вставить("Обработка", 9); //@NON-NLS-1
	Соответствие.Вставить("DataProcessor", 10); //@NON-NLS-1
	Соответствие.Вставить("Обработки", 9); //@NON-NLS-1
	Соответствие.Вставить("DataProcessors", 10); //@NON-NLS-1
	
	Соответствие.Вставить("ПланВидовХарактеристик", 10); //@NON-NLS-1
	Соответствие.Вставить("ChartOfCharacteristicTypes", 10); //@NON-NLS-1
	Соответствие.Вставить("ПланыВидовХарактеристик", 10); //@NON-NLS-1
	Соответствие.Вставить("ChartsOfCharacteristicTypes", 10); //@NON-NLS-1
	
	Соответствие.Вставить("ПланСчетов", 11); //@NON-NLS-1
	Соответствие.Вставить("ChartOfAccounts", 11); //@NON-NLS-1
	Соответствие.Вставить("ПланыСчетов", 11); //@NON-NLS-1
	Соответствие.Вставить("ChartsOfAccounts", 11); //@NON-NLS-1
	
	Соответствие.Вставить("ПланВидовРасчета", 12); //@NON-NLS-1
	Соответствие.Вставить("ChartOfCalculationTypes", 12); //@NON-NLS-1
	Соответствие.Вставить("ПланыВидовРасчета", 12); //@NON-NLS-1
	Соответствие.Вставить("ChartsOfCalculationTypes", 12); //@NON-NLS-1
	
	Соответствие.Вставить("РегистрСведений", 13); //@NON-NLS-1
	Соответствие.Вставить("InformationRegister", 13); //@NON-NLS-1
	Соответствие.Вставить("РегистрыСведений", 13); //@NON-NLS-1
	Соответствие.Вставить("InformationRegisters", 13); //@NON-NLS-1
	
	Соответствие.Вставить("РегистрНакопления", 14); //@NON-NLS-1
	Соответствие.Вставить("AccumulationRegister", 14); //@NON-NLS-1
	Соответствие.Вставить("РегистрыНакопления", 14); //@NON-NLS-1
	Соответствие.Вставить("AccumulationRegisters", 14); //@NON-NLS-1
	
	Соответствие.Вставить("РегистрБухгалтерии", 15); //@NON-NLS-1
	Соответствие.Вставить("AccountingRegister", 15); //@NON-NLS-1
	Соответствие.Вставить("РегистрыБухгалтерии", 15); //@NON-NLS-1
	Соответствие.Вставить("AccountingRegisters", 15); //@NON-NLS-1
	
	Соответствие.Вставить("РегистрРасчета", 16); //@NON-NLS-1
	Соответствие.Вставить("CalculationRegister", 16); //@NON-NLS-1
	Соответствие.Вставить("РегистрыРасчета", 16); //@NON-NLS-1
	Соответствие.Вставить("CalculationRegisters", 16); //@NON-NLS-1
	
	Соответствие.Вставить("БизнесПроцесс", 17); //@NON-NLS-1
	Соответствие.Вставить("BusinessProcess", 17); //@NON-NLS-1
	Соответствие.Вставить("БизнесПроцессы", 17); //@NON-NLS-1
	Соответствие.Вставить("BusinessProcesses", 17); //@NON-NLS-1
	
	Соответствие.Вставить("Задача", 18); //@NON-NLS-1
	Соответствие.Вставить("Task", 18); //@NON-NLS-1
	Соответствие.Вставить("Задачи", 18); //@NON-NLS-1
	Соответствие.Вставить("Tasks", 18); //@NON-NLS-1
	
	Возврат Соответствие
КонецФункции

// Определяет стандартный реквизит источника данных.
// 
// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных - объекта метаданных для которого получаем имя реквизита.
// 
// Возвращаемое значение:
//  Строка - Имя реквизита
Функция СтандартныйРеквизит(ОбъектМетаданных) Экспорт
	ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
	ИмяРеквизита = "Ссылка";
	
	МассивЕстьРегистратор = ТипыЕстьРегистратор();
	
	МассивРегистрСведений = Новый Массив;
	МассивРегистрСведений.Добавить("РегистрСведений"); //@NON-NLS-1
	МассивРегистрСведений.Добавить("InformationRegister"); //@NON-NLS-1
	
	ЧастиИмени = СтрРазделить(ПолноеИмя, ".");
	Если ЧастиИмени.Количество() > 0 Тогда
		ЕстьРегистратор = МассивЕстьРегистратор.Найти(ЧастиИмени[0]);
		Если ЕстьРегистратор <> Неопределено Тогда
			ИмяРеквизита = "Регистратор";
		КонецЕсли;
		Если МассивРегистрСведений.Найти(ЧастиИмени[0]) <> Неопределено Тогда
			Если ОбъектМетаданных.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
				ИмяРеквизита = "";
				Для каждого Измерение Из ОбъектМетаданных.Измерения Цикл
					Если Измерение.Ведущее Тогда
						ИмяРеквизита = Измерение.Имя;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если ИмяРеквизита = "" Тогда
					Для каждого Измерение Из ОбъектМетаданных.Измерения Цикл
						ТипыРеквизита = Измерение.Тип.Типы();
						ЭтоСсылочныйТип = Ложь;
						Для Каждого ТипРеквизита Из ТипыРеквизита Цикл
							ЭтоСсылочныйТип = ОбщегоНазначения.ЭтоСсылка(ТипРеквизита);
							Если ЭтоСсылочныйТип Тогда
								ИмяРеквизита = Измерение.Имя;
								Возврат ИмяРеквизита;
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИмяРеквизита
	
КонецФункции

// Источники оповещений исключения.
// 
// Возвращаемое значение:
//  Массив из Строка - Источники оповещений исключения
Функция ИсточникиОповещенийИсключения() Экспорт
	
	Исключения = Новый Массив();
	
	Исключения.Добавить(Метаданные.Справочники.ВерсииРасширений.ПолноеИмя());
	Исключения.Добавить(Метаданные.Справочники.ВидыОповещенийПользователей.ПолноеИмя());
	Исключения.Добавить(Метаданные.Справочники.ИдентификаторыОбъектовМетаданных.ПолноеИмя());
	Исключения.Добавить(Метаданные.Справочники.ИдентификаторыОбъектовРасширений.ПолноеИмя());
	Исключения.Добавить(Метаданные.Справочники.ОбработчикиОчередиЗаданий.ПолноеИмя());
	Исключения.Добавить(Метаданные.Справочники.ОчередьЗаданий.ПолноеИмя());
	Исключения.Добавить(Метаданные.Справочники.ШаблоныЗаданийОчереди.ПолноеИмя());
	
	Исключения.Добавить(Метаданные.РегистрыСведений.ЗапланированныеЗаданияОчереди.ПолноеИмя());
	Исключения.Добавить(Метаданные.РегистрыСведений.ЗапущенныеОбработчикиОчередиЗаданий.ПолноеИмя());
	Исключения.Добавить(Метаданные.РегистрыСведений.ИсторияЗапускаЗаданийОчереди.ПолноеИмя());
	Исключения.Добавить(Метаданные.РегистрыСведений.ОбновлениеКлючейДоступаКДанным.ПолноеИмя());
	Исключения.Добавить(Метаданные.РегистрыСведений.ОбновлениеКлючейДоступаПользователей.ПолноеИмя());
	Исключения.Добавить(Метаданные.РегистрыСведений.ОбновлениеКлючейДоступаТекущиеЗадания.ПолноеИмя());
	Исключения.Добавить(Метаданные.РегистрыСведений.ПараметрыРаботыВерсийРасширений.ПолноеИмя());
	Исключения.Добавить(Метаданные.РегистрыСведений.ПараметрыРаботыПрограммы.ПолноеИмя());
	Исключения.Добавить(Метаданные.РегистрыСведений.ПериодическиеСерверныеОповещения.ПолноеИмя());
	Исключения.Добавить(Метаданные.РегистрыСведений.ПраваРолей.ПолноеИмя());
	
	ОповещенияПользователейОСобытияхПереопределяемый.ДополнитьИсточникиОповещенийИсключения(Исключения);
	
	Возврат Исключения
	
КонецФункции

// Формирует массив объектов с описанием видов оповещений.
// 
// Возвращаемое значение:
//  Массив из Строка - Источники оповещений исключения
Функция ОбъектыСОписаниемВидовОповещений() Экспорт
	
	Массив = Новый Массив;
	
	ОповещенияПользователейОСобытияхПереопределяемый.ДополнитьОбъектыСОписаниемВидовОповещений(Массив);
	Возврат Массив;
	
КонецФункции

// Формирует соответсвие существующих групп оповещений.
// 
// Возвращаемое значение:
//  Соответствие ИЗ КлючИЗначение:
//   * Ключ - Строка - Источники оповещений исключения
//   * Значение - см. ОписаниеГруппыВидовОповещений
Функция ГруппыВидовОповещений() Экспорт
	
	ГруппыВидовОповещений = Новый Соответствие;
	
	ОповещенияПользователейОСобытияхПереопределяемый.ДополнитьГруппыВидовОповещений(ГруппыВидовОповещений);
	Возврат ГруппыВидовОповещений;
	
КонецФункции

// Описание группы видов оповещений.
// 
// Параметры:
//  Идентификатор - Строка - Идентификатор группы
//  Наименование - Строка - Наименование группы
// 
// Возвращаемое значение:
//  Структура - Описание группы видов оповещений:
// * Идентификатор - Строка - Идентификатор группы
// * Наименование - Строка - Идентификатор группы
Функция ОписаниеГруппыВидовОповещений(Идентификатор, Наименование) Экспорт
	
	ОписаниеГруппы = Новый Структура();
	ОписаниеГруппы.Вставить("Идентификатор", Идентификатор);
	ОписаниеГруппы.Вставить("Наименование", Наименование);
	Возврат ОписаниеГруппы
	
КонецФункции

// Возвращает описание группы вида оповещений.
// 
// Параметры:
//  КлючГруппы - Строка - Ключ группы
// 
// Возвращаемое значение:
//  см. ОписаниеГруппыВидовОповещений
Функция ГруппаВидаОповещений(КлючГруппы) Экспорт
	
	ГруппыВидовОповещений = ГруппыВидовОповещений();
	Группа = ГруппыВидовОповещений.Получить(КлючГруппы);
	//@skip-check constructor-function-return-section
	Возврат Группа;
	
КонецФункции

// Описание доступных видов оповещений пользователей.
// 
// Возвращаемое значение:
//  См. ОписаниеВидовОповещенийПользователей
Функция ОписаниеДоступныхВидовОповещенийПользователей() Экспорт
	
	ОписаниеВидовОповещений = ОписаниеВидовОповещенийПользователей();
	
	ОбъектыСОписаниемВидовОповещений = ОбъектыСОписаниемВидовОповещений();
	Для каждого ОбъектСОписаниемВидовОповещений Из ОбъектыСОписаниемВидовОповещений Цикл
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ОбъектСОписаниемВидовОповещений);
		МенеджерОбъекта.ПриОпределенииДоступныхВидовОповещенийПользователей(ОписаниеВидовОповещений);
	КонецЦикла;
	
	Возврат ОписаниеВидовОповещений
КонецФункции

// Возвращает описание вида оповещений пользователей по его идентификатору.
// 
// Параметры:
//  ИдентификаторОповещения - Строка, УникальныйИдентификатор - Идентификатор оповещения
// 
// Возвращаемое значение:
//  Неопределено, СтрокаТаблицыЗначений из см. ОписаниеВидовОповещенийПользователей - Описание вида оповещений пользователей
//@skip-check function-return-types
Функция ОписаниеВидаОповещенийПользователейПоИдентификатору(ИдентификаторОповещения) Экспорт
	
	Результат = Неопределено;
	
	ОписаниеВидовОповещений = ОписаниеДоступныхВидовОповещенийПользователей();
	НайденныеСтроки = ОписаниеВидовОповещений.НайтиСтроки(Новый Структура("Идентификатор", Строка(ИдентификаторОповещения)));
	Если НайденныеСтроки.Количество() > 0 Тогда
		Результат = НайденныеСтроки[0];
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Заполняет элементы справочника виды оповещений пользователей.
// 
// Параметры:
//  Перезаполнить - Булево - Ложь, если требуется только добавить новые виды оповещений,
//                            Истина, если требуется перезаполнить все виды оповещений.
Процедура ЗаполнитьВидыОповещенийПользователей(Перезаполнить=Ложь) Экспорт
	ОписаниеВидовОповещений = ОписаниеДоступныхВидовОповещенийПользователей();
	Для каждого ОписаниеВидаОповещения Из ОписаниеВидовОповещений Цикл
		НачатьТранзакцию();
		Попытка
			ГруппаВидОповещенийСсылка = Неопределено;
			Если ЗначениеЗаполнено(ОписаниеВидаОповещения.ГруппаВидаОповещений) Тогда
				ГруппаСсылкаНового = Справочники.ВидыОповещенийПользователей.ПолучитьСсылку(
					Новый УникальныйИдентификатор(ОписаниеВидаОповещения.ГруппаВидаОповещений.Идентификатор));
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("Справочник.ВидыОповещенийПользователей");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", ГруппаСсылкаНового);
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				Блокировка.Заблокировать();
				
				ГруппаВидОповещенийОбъект = ГруппаСсылкаНового.ПолучитьОбъект();
				Если ГруппаВидОповещенийОбъект = Неопределено Тогда
					ГруппаВидОповещенийОбъект = Справочники.ВидыОповещенийПользователей.СоздатьГруппу();
					ГруппаВидОповещенийОбъект.УстановитьСсылкуНового(ГруппаСсылкаНового);
					ГруппаВидОповещенийОбъект.Наименование = ОписаниеВидаОповещения.ГруппаВидаОповещений.Наименование;
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(ГруппаВидОповещенийОбъект);
				ИначеЕсли Перезаполнить Тогда
					ГруппаВидОповещенийОбъект.Наименование = ОписаниеВидаОповещения.ГруппаВидаОповещений.Наименование;
					ОбновлениеИнформационнойБазы.ЗаписатьДанные(ГруппаВидОповещенийОбъект);
				КонецЕсли;
				ГруппаВидОповещенийСсылка = ГруппаВидОповещенийОбъект.Ссылка;
			КонецЕсли;
			
			СсылкаНового = Справочники.ВидыОповещенийПользователей.ПолучитьСсылку(
				Новый УникальныйИдентификатор(ОписаниеВидаОповещения.Идентификатор));
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.ВидыОповещенийПользователей");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СсылкаНового);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			ВидОповещенийОбъект = СсылкаНового.ПолучитьОбъект();
			Если ВидОповещенийОбъект = Неопределено Тогда
				ВидОповещенийОбъект = Справочники.ВидыОповещенийПользователей.СоздатьЭлемент();
				ВидОповещенийОбъект.УстановитьСсылкуНового(СсылкаНового);
			ИначеЕсли Не Перезаполнить Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ВидОповещенийОбъект, ОписаниеВидаОповещения,,"ИсточникиДанных");
			ВидОповещенийОбъект.Родитель = ГруппаВидОповещенийСсылка;
			ВидОповещенийОбъект.ИсточникиДанных.Очистить();
			Для каждого ИсточникДанных Из ОписаниеВидаОповещения.ИсточникиДанных Цикл
				НоваяСтрока = ВидОповещенийОбъект.ИсточникиДанных.Добавить();
				Если ТипЗнч(ИсточникДанных.ТипИсточника) = Тип("Строка") Тогда
					НоваяСтрока.ТипИсточника = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ИсточникДанных.ТипИсточника);
				Иначе
					НоваяСтрока.ТипИсточника = ИсточникДанных.ТипИсточника;
				КонецЕсли;
				НоваяСтрока.РеквизитИсточника = ИсточникДанных.РеквизитИсточника;
			КонецЦикла;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ВидОповещенийОбъект);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Оповещения пользователей.Заполнение видов оповещений';
											|en = 'User notifications.Fill notification kinds'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОписаниеОшибки);
		КонецПопытки;
	КонецЦикла;
КонецПроцедуры

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "ОповещенияПользователейОСобытиях.ЗаполнитьОбработчикиРазделенныхДанных";
	Обработчик.Версия = "*";
	Обработчик.РежимВыполнения = "Оперативно";
	Обработчик.ОбщиеДанные = Истина;
	Обработчик.УправлениеОбработчиками = Истина;
	Обработчик.Комментарий = "";
	
	ДобавитьОбработчикДанныхДляПереходаНаНовуюВерсию(Обработчики);
	
КонецПроцедуры

// Обрабатывает данные для перехода на новую версию.
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию() Экспорт
	ЗаполнитьВидыОповещенийПользователей();
КонецПроцедуры

// Заполняет обработчик разделенных данных, зависимый от изменения неразделенных данных.
//
// Параметры:
//   Параметры - Структура - структура параметров обработчиков:
//     * РазделенныеОбработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ЗаполнитьОбработчикиРазделенныхДанных(Параметры = Неопределено) Экспорт
	
	Если Параметры <> Неопределено Тогда
		Обработчики = Параметры.РазделенныеОбработчики;
		ДобавитьОбработчикДанныхДляПереходаНаНовуюВерсию(Обработчики);
	КонецЕсли;
	
КонецПроцедуры

// Генерирует служебного пользователя для отправки оповещений.
// 
// Возвращаемое значение:
//  Неопределено, СправочникСсылка.Пользователи - Служебный пользователь
Функция СлужебныйПользователь() Экспорт
	
	Свойства = Пользователи.НовоеОписаниеПользователяИБ(, Истина);
	Свойства.Имя = ИмяСлужебногоПользователя();
		
	ПользовательСсылка = Справочники.Пользователи.ПолучитьСсылку(
		Новый УникальныйИдентификатор("10d6b342-4f1c-4a57-88bf-908f31a41f6e"));
	
	Попытка
		ПользовательИБ = Пользователи.СлужебныйПользовательИБ(Свойства, ПользовательСсылка);
	Исключение
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Оповещения пользователей.Создание/получение служебного пользователя';
				|en = 'User notifications.Create/get utility user'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.Пользователи,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
	Если ПользовательИБ = Неопределено 
		Или НЕ ОбщегоНазначения.СсылкаСуществует(ПользовательСсылка) Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПользовательСсылка;
	
КонецФункции

// Основные реквизиты вида оповещений.
// 
// Возвращаемое значение:
//  Массив Из Строка - Основные реквизиты вида оповещений.
Функция ОсновныеРеквизитыВидаОповещений() Экспорт
	Массив = Новый Массив;
	
	Массив.Добавить("Наименование");
	Массив.Добавить("ТипОповещения");
	Массив.Добавить("ШаблонТекстаОповещения");
	Массив.Добавить("ТекстСгруппированногоОповещения");
	Массив.Добавить("ИмяШаблонаСКД");
	
	Возврат Массив;
КонецФункции

// Получает макет по полному имени макета.
//
// Параметры:
//  ПолноеИмя - Строка - полное имя макета, полученное через метод платформы ПолноеИмя(). 
// 
// Возвращаемое значение:
//  ТабличныйДокумент, ТекстовыйДокумент, СхемаКомпоновкиДанных - макет по полному имени макета.
//
Функция МакетПоПолномуИмени(ПолноеИмя) Экспорт
	
	Макет = Неопределено;
	
	МетаданныеМакет = Метаданные.НайтиПоПолномуИмени(ПолноеИмя);
	Если МетаданныеМакет <> Неопределено Тогда
		Если Метаданные.ОбщиеМакеты.Содержит(МетаданныеМакет) Тогда
			Макет = ПолучитьОбщийМакет(МетаданныеМакет.Имя);
		Иначе
			МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
			Макет = МенеджерОбъекта.ПолучитьМакет(МетаданныеМакет.Имя);
		КонецЕсли;
	КонецЕсли;
	Возврат Макет;
	
КонецФункции

// Инициализирует таблицу данных по виду оповещения.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Проверить корректность вида оповещений пользователей:
// * ИсточникОповещения - ЛюбаяСсылка - 
// * КонтекстОповещения - ЛюбаяСсылка - 
// * ТекстОповещения - Строка, ФорматированнаяСтрока -
// * ПолучательОповещения - Массив из СправочникСсылка.Пользователи, СправочникСсылка.Пользователи - 
// * ПолучательОповещенияПослеПроверки - Массив из СправочникСсылка.Пользователи, СправочникСсылка.Пользователи -
// * ВидОповещения - СправочникСсылка.ВидыОповещенийПользователей - 
// * КэшКлючевыхПолей - ХранилищеЗначения - 
// * ДатаЗаписи - Дата - 
// * КомандаПерехода - см. Обсуждения.ОписаниеКнопки 
Функция ИнициализироватьТаблицуДанныеПоВидуОповещения() Экспорт
	
	МассивТиповСтрока = Новый Массив;
	МассивТиповСтрока.Добавить(Тип("Строка"));
	МассивТиповСтрока.Добавить(Тип("ФорматированнаяСтрока"));
	
	МассивТиповПолучатель = Новый Массив;
	МассивТиповПолучатель.Добавить(Тип("Массив"));
	МассивТиповПолучатель.Добавить(Тип("СправочникСсылка.Пользователи"));
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ИсточникОповещения", ОбщегоНазначения.ОписаниеТипаВсеСсылки());
	Таблица.Колонки.Добавить("КонтекстОповещения", ОбщегоНазначения.ОписаниеТипаВсеСсылки());
	Таблица.Колонки.Добавить("ТекстОповещения", Новый ОписаниеТипов(МассивТиповСтрока));
	Таблица.Колонки.Добавить("ПолучательОповещения", Новый ОписаниеТипов(МассивТиповПолучатель));
	Таблица.Колонки.Добавить("ПолучательОповещенияПослеПроверки", Новый ОписаниеТипов(МассивТиповПолучатель));
	Таблица.Колонки.Добавить("ВидОповещения", Новый ОписаниеТипов("СправочникСсылка.ВидыОповещенийПользователей"));
	Таблица.Колонки.Добавить("КэшКлючевыхПолей", Новый ОписаниеТипов("ХранилищеЗначения"));
	Таблица.Колонки.Добавить("ДатаЗаписи", Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	Таблица.Колонки.Добавить("КомандаПерехода", Новый ОписаниеТипов("Структура"));
	Таблица.Колонки.Добавить("ИдентификаторЗаписи", Новый ОписаниеТипов("УникальныйИдентификатор"));
	
	Возврат Таблица
КонецФункции

// Проверить корректность вида оповещений пользователей.
// 
// Параметры:
//  ВидОповещения - СправочникСсылка.ВидыОповещенийПользователей - проверяемый вид оповещения.
// 
// Возвращаемое значение:
//  см. ИнициализироватьТаблицуДанныеПоВидуОповещения
Функция ПроверитьКорректностьВидаОповещенийПользователей(ВидОповещения) Экспорт
	
	Отказ = Ложь;
	ТаблицаРезультат = ИнициализироватьТаблицуДанныеПоВидуОповещения();
	
	ВидОповещенияОбъект = ВидОповещения.ПолучитьОбъект();
	Если ВидОповещенияОбъект = Неопределено Тогда
		Возврат ТаблицаРезультат;
	КонецЕсли;
	
	ПроверкаКорректностиСхемыПолученияДанныхПередЗаписью(ВидОповещенияОбъект, Отказ);
	Если Отказ Тогда
		Возврат ТаблицаРезультат;
	КонецЕсли;
	
	Если ВидОповещенияОбъект.ИмяШаблонаСКД = ИмяПредопределенногоШаблона() Тогда
		СКД = ВидОповещенияОбъект.СхемаКомпоновкиДанных.Получить();
		Если СКД = Неопределено 
			ИЛИ НЕ ЕстьИзменения(СКД, МакетПоПолномуИмени(ВидОповещенияОбъект.ИмяШаблонаСКД)) Тогда
			ТекстОшибки = НСтр("ru = 'Не настроен запрос получения данных оповещения.';
								|en = 'The request for notification data is not configured.'");
			ВызватьИсключение(ТекстОшибки, КатегорияОшибки.ОшибкаНастроекКомпоновкиДанных);
		КонецЕсли;
	КонецЕсли;
	Если ВидОповещенияОбъект.ТипОповещения = Перечисления.ТипыОповещенийПользователей.Оперативное Тогда
		ТаблицаИсходныхДанных = ИнициализироватьТаблицуИсходныеДанные();
		ТаблицаРезультат = ДанныеПоОперативномуВидуОповещений(ВидОповещения, ТаблицаИсходныхДанных, Истина);
	Иначе
		ТаблицаРезультат = ДанныеПоНеоперативномуВидуОповещений(ВидОповещенияОбъект);
	КонецЕсли;
	
	Возврат ТаблицаРезультат
КонецФункции

// См. УправлениеСвойствамиПереопределяемый.ПриПолученииПредопределенныхНаборовСвойств
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	Набор = Наборы.Строки.Добавить();
	Набор.Имя = "Справочник_ВидыОповещенийПользователей";
	Набор.Идентификатор = Новый УникальныйИдентификатор("fc305419-3db3-4971-b4e5-c90826790356");
	Набор.Используется = ПолучитьФункциональнуюОпцию("ИспользоватьОповещенияПользователейОСобытиях");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПодготовитьСоединениеССистемойВзаимодействия(Автор)
	
	ЗаблокированоДляРаботыСВнешнимиРесурсами = РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована();
	ЗапрещенДоступКИнтернетСервисам = Не ОбщегоНазначения.РазрешенДоступКИнтернетСервисам();
	
	Если ЗаблокированоДляРаботыСВнешнимиРесурсами Тогда
		ОписаниеОшибки = НСтр("ru = 'Заблокирована работа с внешними ресурсами.';
								|en = 'Operations with external resources are locked.'");
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Оповещения пользователей.Выполнение заданий';
										|en = 'User notifications.Perform jobs'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОписаниеОшибки);
		ВызватьИсключение ОписаниеОшибки;
	ИначеЕсли ЗапрещенДоступКИнтернетСервисам Тогда
		ОписаниеОшибки = ОбщегоНазначения.ЗапрещенДоступКИнтернетСервисамТекстСообщения();
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Оповещения пользователей.Выполнение заданий';
										|en = 'User notifications.Perform jobs'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОписаниеОшибки);
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	Если Пользователи.АвторизованныйПользователь() = Пользователи.СсылкаНеуказанногоПользователя() Тогда
		ИдентификаторПользователяАвторОповещений = ОповещенияПользователейОСобытияхПовтИсп.ИдентификаторПользователяАвторОповещений(Автор);
		СистемаВзаимодействия.УстановитьТекущегоПользователя(ИдентификаторПользователяАвторОповещений);
	КонецЕсли;
	
	Если НЕ Обсуждения.СистемаВзаимодействийПодключена() Тогда
		ОписаниеОшибки = НСтр("ru = 'Обсуждения системы взаимодействия недоступны.';
								|en = 'Collaboration system conversations are unavailable.'");
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Оповещения пользователей.Выполнение заданий';
										|en = 'User notifications.Perform jobs'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОписаниеОшибки);
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	Попытка
		СистемаВзаимодействия.ПолучитьТипыВнешнихСистем();
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Оповещения пользователей.Выполнение заданий';
										|en = 'User notifications.Perform jobs'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОписаниеОшибки);
		ВызватьИсключение;
	КонецПопытки;
КонецПроцедуры

//@skip-check bsl-nstr-string-literal-format
Функция ДанныеПоОперативномуВидуОповещений(ВидОповещения, ТаблицаИсходныхДанных, РежимПроверки = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДанных.ИсточникОповещения КАК ИсточникОповещения,
	|	ТаблицаДанных.ИнициаторОповещения КАК ИнициаторОповещения,
	|	ТаблицаДанных.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	ВЫРАЗИТЬ(&ВидОповещения КАК Справочник.ВидыОповещенийПользователей) КАК ВидОповещения
	|ПОМЕСТИТЬ ВтДанные
	|ИЗ
	|	&ТаблицаДанных КАК ТаблицаДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыОповещения.Ссылка КАК ВидОповещения,
	|	ВидыОповещения.ШаблонТекстаОповещения КАК ШаблонТекстаОповещения,
	|	ВидыОповещения.ИмяШаблонаСКД КАК ИмяШаблонаСКД,
	|	ВидыОповещения.СхемаКомпоновкиДанных КАК СхемаКомпоновкиДанных,
	|	ВидыОповещения.ХранилищеНастроекКомпоновкиДанных КАК ХранилищеНастроекКомпоновкиДанных
	|ИЗ
	|	Справочник.ВидыОповещенийПользователей КАК ВидыОповещения
	|ГДЕ
	|	ВидыОповещения.Ссылка = &ВидОповещения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеИсточник.ИсточникОповещения КАК ИсточникОповещения,
	|	ДанныеИсточник.ВидОповещения КАК ВидОповещения,
	|	ДанныеИсточник.ВидОповещения.ШаблонТекстаОповещения КАК ТекстОповещения,
	|	ДанныеИсточник.ИнициаторОповещения КАК ИнициаторОповещения,
	|	ДанныеИсточник.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	ДанныеОповещений.КонтекстОповещения КАК КонтекстОповещения,
	|	ДанныеОповещений.КэшКлючевыхПолей КАК КэшКлючевыхПолей
	|ИЗ
	|	ВтДанные КАК ДанныеИсточник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеОповещенийПользователей КАК ДанныеОповещений
	|		ПО ДанныеОповещений.ИсточникОповещения = ДанныеИсточник.ИсточникОповещения
	|			И ДанныеОповещений.ВидОповещения = ДанныеИсточник.ВидОповещения
	|;
	|";
	Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаИсходныхДанных);
	Запрос.УстановитьПараметр("ВидОповещения", ВидОповещения);
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	ВыборкаВидОповещения = ПакетЗапросов[1].Выбрать();
	РезультатСКД = Неопределено;
	ШаблонТекстаОповещения = "";
	ТаблицаДанных = ИнициализироватьТаблицуДанныеПоВидуОповещения();
	
	Если ВыборкаВидОповещения.Следующий() Тогда
		РезультатСКД = ИнициализироватьСхемуКомпоновкиДанных(ВыборкаВидОповещения, 
			ТаблицаИсходныхДанных.ВыгрузитьКолонку("ИсточникОповещения"),
			РежимПроверки);
		ШаблонТекстаОповещения = ВыборкаВидОповещения.ШаблонТекстаОповещения;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = МенеджерВТКэшРеквизитов(РезультатСКД.КомпоновщикНастроекКомпоновкиДанных,
		ПакетЗапросов[2].Выбрать());
	РезультатКомпоновки = РезультатКомпоновкиДанных(РезультатСКД.СхемаКомпоновкиДанных,
		РезультатСКД.КомпоновщикНастроекКомпоновкиДанных,
		МенеджерВременныхТаблиц);
	Если РезультатКомпоновки.Количество() = 0 Тогда
		Возврат ТаблицаДанных;
	КонецЕсли;
	
	ТаблицаКэш = ИнициализироватьТаблицуКэшРеквизитов(РезультатКомпоновки.Колонки);
	ТаблицаРезультатКомпоновки = ОбработатьРезультатКомпоновки(РезультатКомпоновки, МенеджерВременныхТаблиц);
	
	КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	Для каждого СтрокаРезультат Из ТаблицаРезультатКомпоновки Цикл
		
		КодЯзыкаПользователя = ОповещенияПользователейОСобытияхПовтИсп.КодЯзыкаПользователя(
			Строка(СтрокаРезультат.ИдентификаторПользователяИБ));
	
		ТекстОповещения = НСтр(ШаблонТекстаОповещения, КодЯзыкаПользователя);
		ТекстОповещенияОсновной = НСтр(ШаблонТекстаОповещения, КодОсновногоЯзыка);
		
		Если ТекстОповещенияОсновной <> ТекстОповещения Тогда // Повторим оповещение на основном языке конфигурации.
			
			ТекстОповещенияСПодстановкой = ПодставитьЗначенияПоШаблону(ТекстОповещенияОсновной,
				СтрокаРезультат,
				ТаблицаРезультатКомпоновки.Колонки,
				РезультатСКД.ОформлениеПолей,
				КодОсновногоЯзыка);
				
			ЗаполнитьСтрокуДанныеВидаОповещений(ТаблицаДанных,
				СтрокаРезультат,
				ТекстОповещенияСПодстановкой,
				СтрокаРезультат.КонтекстОповещения);
			
		КонецЕсли;
		
		ТекстОповещенияСПодстановкой = ПодставитьЗначенияПоШаблону(ТекстОповещения,
			СтрокаРезультат,
			ТаблицаРезультатКомпоновки.Колонки,
			РезультатСКД.ОформлениеПолей,
			КодЯзыкаПользователя);
		
		ЗаполнитьСтрокуДанныеВидаОповещений(ТаблицаДанных,
			СтрокаРезультат,
			ТекстОповещенияСПодстановкой,
			СтрокаРезультат.КонтекстОповещения,
			СтрокаРезультат.ПолучательОповещенияПослеПроверки,,
			ТаблицаКэш);
		
	КонецЦикла;
	
	Возврат ТаблицаДанных
	
КонецФункции

//@skip-check bsl-nstr-string-literal-format
Функция ДанныеПоНеоперативномуВидуОповещений(РеквизитыВидаОповещения)
	
	ТаблицаДанных = ИнициализироватьТаблицуДанныеПоВидуОповещения();
	ВидОповещения = РеквизитыВидаОповещения.Ссылка;
	РезультатСКД = ИнициализироватьСхемуКомпоновкиДанных(ВидОповещения);
	
	ШаблонТекстаОповещения = РеквизитыВидаОповещения.ШаблонТекстаОповещения; // Строка
	ТекстСгруппированногоОповещения = РеквизитыВидаОповещения.ТекстСгруппированногоОповещения; // Строка
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	РезультатКомпоновки = РезультатКомпоновкиДанных(РезультатСКД.СхемаКомпоновкиДанных,
		РезультатСКД.КомпоновщикНастроекКомпоновкиДанных,
		МенеджерВременныхТаблиц);
	ДеревоРезультат = ОбработатьРезультатКомпоновкиСИтогами(РезультатКомпоновки, ВидОповещения); // ДеревоЗначений
	
	КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	
	Для каждого СтрокаИтог Из ДеревоРезультат.Строки Цикл
		
		Если ЗначениеЗаполнено(СтрокаИтог.КонтекстОповещения)
			И СтрокаИтог.КонтекстОповещения > РеквизитыВидаОповещения.ПорогГруппировки Тогда
			
			Если НЕ ЗначениеЗаполнено(СтрокаИтог.ПолучательОповещенияПослеПроверки) Тогда
				// Неконтекстные оповещения не могут быть без получателя.
				Продолжить;
			КонецЕсли;
			
			КомандаПерехода = КонструкторКомандыПерехода();
			СоответствиеСсылок = Новый Соответствие;
			ИдентификаторПользователяИБ = "";
			Для каждого СтрокаРезультат Из СтрокаИтог.Строки Цикл
				ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(СтрокаРезультат.КонтекстОповещения);
				Если СоответствиеСсылок[ИмяТаблицы] = Неопределено Тогда
					СоответствиеСсылок.Вставить(ИмяТаблицы, Новый Массив);
				КонецЕсли;
				СоответствиеСсылок[ИмяТаблицы].Добавить(СтрокаРезультат.КонтекстОповещения);
				ИдентификаторПользователяИБ = СтрокаРезультат.ИдентификаторПользователяИБ;
			КонецЦикла;
			
			КомандаПерехода.Данные.СоответствиеСсылок = СоответствиеСсылок;
			Если СоответствиеСсылок.Количество() = 1 Тогда
				КомандаПерехода.Данные.ИмяФормы = ИмяФормыСпискаПоСсылке(СтрокаРезультат.КонтекстОповещения, СтрокаРезультат.ПолучательОповещенияПослеПроверки);
				КомандаПерехода.ИмяДействия = "ПерейтиКФормеСпискаСОтбором";
			Иначе
				КомандаПерехода.Данные.ИмяФормы = "Справочник.ВидыОповещенийПользователей.Форма.ПереходПоСсылкам";
				КомандаПерехода.ИмяДействия = "ПерейтиКФормеПереходПоСсылкам";
			КонецЕсли;
			
			КодЯзыкаПользователя = ОповещенияПользователейОСобытияхПовтИсп.КодЯзыкаПользователя(
				Строка(ИдентификаторПользователяИБ));
			
			ПредставлениеПерейти = "ru = 'Перейти (%1)';
									|en = 'Go to (%1)'"; // @Nstr-1
			КомандаПерехода.Представление = НСтр(СтрШаблон(ПредставлениеПерейти, СтрокаИтог.Строки.Количество()),
				КодЯзыкаПользователя);
			
			ТекстОповещения = НСтр(ТекстСгруппированногоОповещения,
				КодЯзыкаПользователя);
				
			ЗаполнитьСтрокуДанныеВидаОповещений(ТаблицаДанных,
				СтрокаРезультат,
				ТекстОповещения,
				Неопределено,
				СтрокаРезультат.ПолучательОповещенияПослеПроверки,
				КомандаПерехода);
		Иначе
			Для каждого СтрокаРезультат Из СтрокаИтог.Строки Цикл
				
				КодЯзыкаПользователя = ОповещенияПользователейОСобытияхПовтИсп.КодЯзыкаПользователя(
					Строка(СтрокаРезультат.ИдентификаторПользователяИБ));
				
				ТекстОповещения = НСтр(ШаблонТекстаОповещения, КодЯзыкаПользователя);
				ТекстОповещенияОсновной = НСтр(ШаблонТекстаОповещения, КодОсновногоЯзыка);
				
				Если ТекстОповещенияОсновной <> ТекстОповещения Тогда // Повторим оповещение на основном языке конфигурации.
					
					ТекстОповещенияСПодстановкой = ПодставитьЗначенияПоШаблону(ТекстОповещенияОсновной,
						СтрокаРезультат,
						ДеревоРезультат.Колонки,
						РезультатСКД.ОформлениеПолей,
						КодОсновногоЯзыка);
						
					ЗаполнитьСтрокуДанныеВидаОповещений(ТаблицаДанных,
						СтрокаРезультат,
						ТекстОповещенияСПодстановкой,
						СтрокаРезультат.КонтекстОповещения);
					
				КонецЕсли;
				
				ТекстОповещенияСПодстановкой = ПодставитьЗначенияПоШаблону(
					ТекстОповещения,
					СтрокаРезультат,
					ДеревоРезультат.Колонки,
					РезультатСКД.ОформлениеПолей,
					КодЯзыкаПользователя);
					
				ЗаполнитьСтрокуДанныеВидаОповещений(ТаблицаДанных,
					СтрокаРезультат,
					ТекстОповещенияСПодстановкой,
					СтрокаРезультат.КонтекстОповещения,
					СтрокаРезультат.ПолучательОповещенияПослеПроверки);
					
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаДанных;
	
КонецФункции

Процедура ОбработатьОповещениеПользователей(ДанныеПоВидуОповещений, Автор, ИдентификаторыНеОбработанныхЗаписей)
	
	НаборЗаписей = РегистрыСведений.ДанныеОповещенийПользователей.СоздатьНаборЗаписей();
	ТаблицаНабораЗаписей = НаборЗаписей.Выгрузить();
	Для каждого СтрокаРезультат Из ДанныеПоВидуОповещений Цикл
		Попытка
			ОтправитьОповещение(Автор,
				СтрокаРезультат.ПолучательОповещенияПослеПроверки,
				СтрокаРезультат.ТекстОповещения,
				СтрокаРезультат.ВидОповещения,
				СтрокаРезультат.КонтекстОповещения,,
				Истина);
				
			// Закэшируем в регистре только то, о чем сообщили.
			ДобавитьОповещениеВНаборЗаписейДанныеОповещенийПользователей(СтрокаРезультат, ТаблицаНабораЗаписей);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
			Если ИдентификаторыНеОбработанныхЗаписей.Получить(СтрокаРезультат.ИдентификаторЗаписи) = Неопределено Тогда
				ИдентификаторыНеОбработанныхЗаписей.Вставить(СтрокаРезультат.ИдентификаторЗаписи, ОписаниеОшибки);
			КонецЕсли;
		КонецПопытки;
	КонецЦикла;
	
	Попытка
		НаборЗаписей.Загрузить(ТаблицаНабораЗаписей);
		НаборЗаписей.Записать(РежимЗамещения.Слияние);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Оповещения пользователей.Выполнение заданий';
										|en = 'User notifications.Perform jobs'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОписаниеОшибки);
	КонецПопытки;
	
КонецПроцедуры

Процедура ДобавитьОповещениеВНаборЗаписейДанныеОповещенийПользователей(ДобавляемаяСтрока, ТаблицаНабораЗаписей)
	
	СтруктураПоиска = Новый Структура("ИсточникОповещения, ВидОповещения, КонтекстОповещения");
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, ДобавляемаяСтрока);
	НайденныеСтроки = ТаблицаНабораЗаписей.НайтиСтроки(СтруктураПоиска); // свертка строк для записи в регистр
	Если НайденныеСтроки.Количество() > 0 Тогда
		НайденнаяСтрока = НайденныеСтроки[0];
		ТаблицаКэшКлючевыхПолейПриемник = НайденнаяСтрока.КэшКлючевыхПолей.Получить(); // ТаблицаЗначений
		ТаблицаКэшКлючевыхПолейИсточник = ДобавляемаяСтрока.КэшКлючевыхПолей.Получить(); // ТаблицаЗначений
		Если ТаблицаКэшКлючевыхПолейИсточник <> Неопределено Тогда
			Если ТаблицаКэшКлючевыхПолейПриемник = Неопределено Тогда
				НайденнаяСтрока.КэшКлючевыхПолей = ДобавляемаяСтрока.КэшКлючевыхПолей;
			Иначе
				СтруктураПоискаКэш = Новый Структура;
				Для каждого Колонка Из ТаблицаКэшКлючевыхПолейИсточник.Колонки Цикл
					СтруктураПоискаКэш.Вставить(Колонка.Имя);
				КонецЦикла;
				Для Каждого СтрокаКэшИсточник Из ТаблицаКэшКлючевыхПолейИсточник Цикл
					ЗаполнитьЗначенияСвойств(СтруктураПоискаКэш, СтрокаКэшИсточник);
					НайденныеСтрокиКэш = ТаблицаКэшКлючевыхПолейПриемник.НайтиСтроки(СтруктураПоискаКэш);
					Если НайденныеСтрокиКэш.Количество() = 0 Тогда
						НоваяСтрокаПриемник = ТаблицаКэшКлючевыхПолейПриемник.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрокаПриемник, СтрокаКэшИсточник);
					КонецЕсли;
				КонецЦикла;
				КэшКлючевыхПолей = Новый ХранилищеЗначения(ТаблицаКэшКлючевыхПолейПриемник);
				НайденнаяСтрока.КэшКлючевыхПолей = КэшКлючевыхПолей;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ЗаполнитьЗначенияСвойств(ТаблицаНабораЗаписей.Добавить(), ДобавляемаяСтрока);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьНеоперативноеОповещениеПользователей(ДанныеПоВидуОповещений, Автор)
	Для каждого СтрокаРезультат Из ДанныеПоВидуОповещений Цикл
		ОтправитьОповещение(Автор,
			СтрокаРезультат.ПолучательОповещенияПослеПроверки,
			СтрокаРезультат.ТекстОповещения,
			СтрокаРезультат.ВидОповещения,
			СтрокаРезультат.КонтекстОповещения,
			СтрокаРезультат.КомандаПерехода);
	КонецЦикла;
КонецПроцедуры

Процедура ОтправитьОповещение(Автор, Получатели, ТекстОповещения, ВидОповещения, КонтекстОповещения = Неопределено, КомандыПерехода = Неопределено, ВыдаватьИсключение = Ложь)
	
	Попытка
		МассивПолучателей = Новый Массив();
		Если ЗначениеЗаполнено(Получатели) Тогда
			Если ТипЗнч(Получатели) = Тип("Массив") Тогда
				Для Каждого ЭлементМассива Из Получатели Цикл
					Если ЗначениеЗаполнено(ЭлементМассива) Тогда
						ПользовательСистемыВзаимодействия = Обсуждения.ПользовательСистемыВзаимодействия(ЭлементМассива);
						Если НЕ ПользовательСистемыВзаимодействия.Заблокирован Тогда
							МассивПолучателей.Добавить(ПользовательСистемыВзаимодействия);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			Иначе
				ПользовательСистемыВзаимодействия = Обсуждения.ПользовательСистемыВзаимодействия(Получатели);
				Если НЕ ПользовательСистемыВзаимодействия.Заблокирован Тогда
					МассивПолучателей.Добавить(ПользовательСистемыВзаимодействия);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если МассивПолучателей.Количество() = 0 И НЕ ЗначениеЗаполнено(КонтекстОповещения) Тогда
			Возврат;
		КонецЕсли;
		
		Данные = Новый Структура("ВидОповещения", ВидОповещения);
		
		ОписаниеСообщения = Обсуждения.ОписаниеСообщения(ТекстОповещения);
		ОписаниеСообщения.Данные = Данные;
		
		ОписаниеСообщения.ПанельКнопок.ВидКнопок = ВидКнопкиПанелиКнопокСообщенияСистемыВзаимодействия.Гиперссылка;
		
		Если ЗначениеЗаполнено(КомандыПерехода) Тогда
			КомандыПереходаМассив = Новый Массив;
			Если ТипЗнч(КомандыПерехода) = Тип("Структура") Тогда
				КомандыПереходаМассив.Добавить(КомандыПерехода);
			Иначе
				КомандыПереходаМассив = КомандыПерехода;
			КонецЕсли;
			
			МассивКнопок = Новый Массив;
			Счетчик = 0;
			Для каждого КомандаПерехода Из КомандыПереходаМассив Цикл
				ОписаниеКнопки = Обсуждения.ОписаниеКнопки(
					ДействиеКнопкиПанелиКнопокСообщенияСистемыВзаимодействия.ОбработатьНаКлиенте,
					КомандаПерехода.Представление);
				ОписаниеКнопки.Данные = КомандаПерехода.Данные;
				ОписаниеКнопки.ИмяДействия = КомандаПерехода.ИмяДействия;
				МассивКнопок.Добавить(ОписаниеКнопки);
				Счетчик = Счетчик + 1;
				Если Счетчик%10 = 0 Тогда // в ряду не может быть больше 10 кнопок
					ОписаниеСообщения.ПанельКнопок.РядыКнопок.Добавить(МассивКнопок);
					МассивКнопок = Новый Массив;
				КонецЕсли;
			КонецЦикла;
			Если МассивКнопок.Количество() > 0 Тогда
				ОписаниеСообщения.ПанельКнопок.РядыКнопок.Добавить(МассивКнопок);
			КонецЕсли;
		КонецЕсли;
		
		Обсуждения.ОтправитьСообщение(Автор,
			МассивПолучателей,
			ОписаниеСообщения,
			КонтекстОповещения);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Оповещения пользователей.Выполнение заданий';
										|en = 'User notifications.Perform jobs'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,,
			ОписаниеОшибки);
		Если ВыдаватьИсключение Тогда
			ВызватьИсключение;
		КонецЕсли;
	КонецПопытки;
	
КонецПроцедуры

Процедура УстановитьОтборПоИсточникуОповещений(Настройки, ИсточникиОповещений)
	ЭлементОтбора = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));

	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИсточникОповещения");;
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ЭлементОтбора.ПравоеЗначение = ИсточникиОповещений;
КонецПроцедуры

// Заполнить строку данные вида оповещений.
// 
// Параметры:
//  ТаблицаДанных - см. ИнициализироватьТаблицуДанныеПоВидуОповещения
//  ИсходнаяСтрока - СтрокаТаблицыЗначений, СтрокаДереваЗначений - 
//  ТекстОповещения - Строка, ФорматированнаяСтрока - 
//  КонтекстОповещения - Неопределено - 
//  ПолучательОповещенияПослеПроверки - Неопределено - 
//  КомандаПерехода - Неопределено, Структура - Команда перехода:
// * Представление - Строка - 
// * ИмяДействия - Строка - 
// * Данные - Структура - :
// ** ИмяФормы - Строка - 
// ** СоответствиеСсылок - Неопределено - 
//  ТаблицаКэш - Неопределено, ТаблицаЗначений - 
Процедура ЗаполнитьСтрокуДанныеВидаОповещений(ТаблицаДанных, ИсходнаяСтрока, ТекстОповещения, КонтекстОповещения, ПолучательОповещенияПослеПроверки = Неопределено, КомандаПерехода = Неопределено, Знач ТаблицаКэш = Неопределено)
	
	КлючевыеПоляПоиска = Новый Структура("ИсточникОповещения, ВидОповещения");
	ЗаполнитьЗначенияСвойств(КлючевыеПоляПоиска, ИсходнаяСтрока);
	КлючевыеПоляПоиска.Вставить("КонтекстОповещения", КонтекстОповещения);
	КлючевыеПоляПоиска.Вставить("ТекстОповещения", ТекстОповещения);
	
	НайденныеСтроки = ТаблицаДанных.НайтиСтроки(КлючевыеПоляПоиска);
	Если НайденныеСтроки.Количество() > 0 И КонтекстОповещения <> Неопределено Тогда
		ИзмененнаяСтрока = НайденныеСтроки[0]; // Если строка с такими ключевыми полями существует, добавим получателя.
		МассивПолучателей = ИзмененнаяСтрока.ПолучательОповещения;
		Если МассивПолучателей.Найти(ИсходнаяСтрока.ПолучательОповещения) = Неопределено Тогда
			МассивПолучателей.Добавить(ИсходнаяСтрока.ПолучательОповещения);
		КонецЕсли;
		МассивПолучателейПослеПроверки = ИзмененнаяСтрока.ПолучательОповещенияПослеПроверки;
		Если МассивПолучателейПослеПроверки.Найти(ПолучательОповещенияПослеПроверки) = Неопределено Тогда
			МассивПолучателейПослеПроверки.Добавить(ПолучательОповещенияПослеПроверки);
		КонецЕсли;
		КэшКлючевыхПолей = ИзмененнаяСтрока.КэшКлючевыхПолей;
		Если ЗначениеЗаполнено(КэшКлючевыхПолей) Тогда
			ТаблицаКэшДобавить = КэшКлючевыхПолей.Получить();
			ЗаполнитьКэшКлючевыхПолейВСтроке(ИзмененнаяСтрока, ИсходнаяСтрока, ТаблицаКэшДобавить);
		КонецЕсли;
	Иначе
		НоваяСтрока = ТаблицаДанных.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ИсходнаяСтрока, , "КонтекстОповещения, ПолучательОповещения, ПолучательОповещенияПослеПроверки");
		
		МассивПолучателей = Новый Массив;
		МассивПолучателей.Добавить(ИсходнаяСтрока.ПолучательОповещения);
		
		МассивПолучателейПослеПроверки = Новый Массив;
		МассивПолучателейПослеПроверки.Добавить(ПолучательОповещенияПослеПроверки);
		
		НоваяСтрока.ТекстОповещения = ТекстОповещения;
		НоваяСтрока.КонтекстОповещения = КонтекстОповещения;
		НоваяСтрока.ПолучательОповещения = МассивПолучателей;
		НоваяСтрока.ПолучательОповещенияПослеПроверки = МассивПолучателейПослеПроверки;
		НоваяСтрока.КомандаПерехода = КомандаПерехода;
		НоваяСтрока.ДатаЗаписи = ТекущаяДатаСеанса();
		
		ЗаполнитьКэшКлючевыхПолейВСтроке(НоваяСтрока, ИсходнаяСтрока, ТаблицаКэш);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьКэшКлючевыхПолейВСтроке(НоваяСтрока, ИсходнаяСтрока, ТаблицаКэш)
	
	Если ТаблицаКэш = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрокаКэш = ТаблицаКэш.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрокаКэш, ИсходнаяСтрока);
	КэшКлючевыхПолей = Новый ХранилищеЗначения(ТаблицаКэш);
	
	НоваяСтрока.КэшКлючевыхПолей = КэшКлючевыхПолей;
КонецПроцедуры

Функция ИнициализироватьСхемуКомпоновкиДанных(РеквизитыВидаОповещений, ИсточникиОповещения = Неопределено, ОграничитьВыборку = Ложь)
	СхемаКомпоновкиДанных = РеквизитыВидаОповещений.СхемаКомпоновкиДанных.Получить();
	НастройкиКомпоновки = РеквизитыВидаОповещений.ХранилищеНастроекКомпоновкиДанных.Получить();
	
	Если СхемаКомпоновкиДанных = Неопределено Тогда
		СхемаКомпоновкиДанных = МакетПоПолномуИмени(РеквизитыВидаОповещений.ИмяШаблонаСКД);
	КонецЕсли;
	
	ОформлениеПолей = Новый Соответствие;
	
	Для Каждого НаборДанных Из СхемаКомпоновкиДанных.НаборыДанных Цикл
		Если ОграничитьВыборку Тогда
			ТекстЗапроса = СтрЗаменитьПоРегулярномуВыражению(НаборДанных.Запрос, 
				"^(?!\s*ВЫБРАТЬ\s+.*ПЕРВЫЕ\s*\d+)(\s*ВЫБРАТЬ)(\s+.*)$",
				"$1 ПЕРВЫЕ 10 $2", Истина, Истина); //@Non-NLS-1 @Non-NLS-2
			ТекстЗапроса = СтрЗаменитьПоРегулярномуВыражению(ТекстЗапроса, 
				"^(?!\s*SELECT\s+.*TOP\s*\d+)(\s*SELECT)(\s+.*)$",
				"$1 TOP 10 $2", Истина, Истина); //@Non-NLS-1 @Non-NLS-2
			НаборДанных.Запрос = ТекстЗапроса;
		КонецЕсли;
		Для каждого Поле Из НаборДанных.Поля Цикл
			ИмяПоля = "[" + Поле.ПутьКДанным + "]";
			ЗначенияПараметровОформления = Новый Структура;
			Для каждого ЭлементОформления Из Поле.Оформление.Элементы Цикл
				Если ЭлементОформления.Использование Тогда
					ЗначенияПараметровОформления.Вставить(ЭлементОформления.Параметр, ЭлементОформления.Значение);
				КонецЕсли;
			КонецЦикла;
			ОформлениеПолей.Вставить(ИмяПоля, ЗначенияПараметровОформления);
		КонецЦикла;
	КонецЦикла;
	
	КомпоновщикНастроекКомпоновкиДанных = ИнициализироватьКомпоновщикНастроек(СхемаКомпоновкиДанных, НастройкиКомпоновки);
	
	Если ЗначениеЗаполнено(ИсточникиОповещения) Тогда
		УстановитьОтборПоИсточникуОповещений(КомпоновщикНастроекКомпоновкиДанных.ФиксированныеНастройки, ИсточникиОповещения);
	КонецЕсли;
	
	Настройки = КомпоновщикНастроекКомпоновкиДанных.Настройки;
	Если Настройки.ПараметрыДанных.Элементы.Найти("ДатаАктуальности") <> Неопределено Тогда
		Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаАктуальности", ТекущаяДатаСеанса());
	КонецЕсли;
	
	Возврат Новый Структура("СхемаКомпоновкиДанных, КомпоновщикНастроекКомпоновкиДанных, ОформлениеПолей", СхемаКомпоновкиДанных, КомпоновщикНастроекКомпоновкиДанных, ОформлениеПолей);
КонецФункции

Функция МенеджерВТКэшРеквизитов(КомпоновщикНастроекКомпоновкиДанных, ВыборкаРеквизитов)
	
	Настройки = КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки(); // НастройкиКомпоновкиДанных
	
	ТаблицаКэш = Новый ТаблицаЗначений;
	ТаблицаКэш.Колонки.Добавить("ВидОповещения", Новый ОписаниеТипов("СправочникСсылка.ВидыОповещенийПользователей"), НСтр("ru = 'Вид оповещения';
																															|en = 'Notification kind'"));
	ТаблицаКэш.Колонки.Добавить("ИнициаторОповещения", Новый ОписаниеТипов("СправочникСсылка.Пользователи"), НСтр("ru = 'Инициатор оповещения';
																													|en = 'Notification initiator'"));
	ТаблицаКэш.Колонки.Добавить("ИдентификаторЗаписи", Новый ОписаниеТипов("УникальныйИдентификатор"), НСтр("ru = 'Идентификатор записи';
																											|en = 'Record ID'"));
	Для каждого ДоступноеПоле Из Настройки.Выбор.ДоступныеПоляВыбора.Элементы Цикл
		Если ДоступноеПоле.Папка Тогда
			Продолжить;
		КонецЕсли;
		Если ДоступноеПоле.Поле = Новый ПолеКомпоновкиДанных("ВидОповещения")
			ИЛИ ДоступноеПоле.Поле = Новый ПолеКомпоновкиДанных("ИнициаторОповещения") Тогда
			Продолжить;
		КонецЕсли;
		ТаблицаКэш.Колонки.Добавить(ДоступноеПоле.Поле, ДоступноеПоле.Тип, ДоступноеПоле.Заголовок);
	КонецЦикла;
	
	Пока ВыборкаРеквизитов.Следующий() Цикл
		НоваяСтрока = ТаблицаКэш.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаРеквизитов);
		КэшКлючевыхПолей = ВыборкаРеквизитов.КэшКлючевыхПолей;
		Если ЗначениеЗаполнено(КэшКлючевыхПолей) Тогда
			ЗначениеКэша = КэшКлючевыхПолей.Получить();
			Если ТипЗнч(ЗначениеКэша) = Тип("ТаблицаЗначений") И ЗначениеКэша.Количество() > 0 Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗначениеКэша[0]);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	//@skip-check bsl-ql-hub
	Запрос = Новый Запрос("ВЫБРАТЬ * ПОМЕСТИТЬ ВтКэшРеквизитов ИЗ &ТаблицаКэш КАК Т ИНДЕКСИРОВАТЬ ПО ИсточникОповещения;");
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаКэш", ТаблицаКэш);
	Запрос.Выполнить();
	
	Возврат МенеджерВременныхТаблиц
КонецФункции

Функция РезультатКомпоновкиДанных(СхемаКомпоновкиДанных, КомпоновщикНастроекКомпоновкиДанных, МенеджерВременныхТаблиц)
	
	// Компоновка макета
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновкиДанных, 
		КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки(),,,
		Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Инициализация процессора компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных,,,Истина,,МенеджерВременныхТаблиц);
	
	// Таблица значений, в которую будет получен результат
	Результат = Новый ТаблицаЗначений;
	
	// Получение результата
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);
	
	Возврат Результат
КонецФункции

Функция ОбработатьРезультатКомпоновки(РезультатКомпоновки, МенеджерВременныхТаблиц)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	//@skip-check bsl-ql-hub
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ
	|	ВтРезультатКомпоновки
	|ИЗ
	|	&РезультатКомпоновки КАК Т
	|ИНДЕКСИРОВАТЬ ПО
	|	Т.ИсточникОповещения,
	|	Т.КонтекстОповещения
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтДанные.ИсточникОповещения КАК ИсточникОповещения,
	|	ВтДанные.КонтекстОповещения КАК КонтекстОповещения,
	|	ВтКэшРеквизитов.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	ВтКэшРеквизитов.ВидОповещения КАК ВидОповещения,
	|	ВтДанные.ПолучательОповещения КАК ПолучательОповещения,
	|	ВЫБОР
	|		КОГДА ВтДанные.ПолучательОповещения = ВтКэшРеквизитов.ИнициаторОповещения
	|				ИЛИ НЕ НастройкиОповещений.Пользователь ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|		ИНАЧЕ ВтДанные.ПолучательОповещения
	|	КОНЕЦ КАК ПолучательОповещенияПослеПроверки
	|ПОМЕСТИТЬ
	|	ВтПолучатели
	|ИЗ
	|	ВтРезультатКомпоновки КАК ВтДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКэшРеквизитов КАК ВтКэшРеквизитов
	|		ПО ВтДанные.ИсточникОповещения = ВтКэшРеквизитов.ИсточникОповещения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтключенныеНастройкиОповещенийПользователей КАК НастройкиОповещений
	|		ПО (НастройкиОповещений.ВидОповещения = ВтКэшРеквизитов.ВидОповещения)
	|			И (НастройкиОповещений.Пользователь = ВтДанные.ПолучательОповещения)
	|ИНДЕКСИРОВАТЬ ПО
	|	ВтДанные.ИсточникОповещения,
	|	ВтДанные.КонтекстОповещения
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтДанные.*,
	|	ВтПолучатели.ПолучательОповещенияПослеПроверки КАК ПолучательОповещенияПослеПроверки,
	|	ВтПолучатели.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	ВтПолучатели.ВидОповещения КАК ВидОповещения,
	|	Пользователи.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
	|ИЗ
	|	ВтРезультатКомпоновки КАК ВтДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПолучатели КАК ВтПолучатели
	|		ПО ВтДанные.ИсточникОповещения = ВтПолучатели.ИсточникОповещения
	|		И ВтДанные.КонтекстОповещения = ВтПолучатели.КонтекстОповещения
	|		И ВтДанные.ПолучательОповещения = ВтПолучатели.ПолучательОповещения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО ВтПолучатели.ПолучательОповещения = Пользователи.Ссылка
	|";
	
	Запрос.УстановитьПараметр("РезультатКомпоновки", РезультатКомпоновки);
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ОбработатьРезультатКомпоновкиСИтогами(РезультатКомпоновки, ВидОповещения)
	Запрос = Новый Запрос;
	//@skip-check bsl-ql-hub
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ
	|	ВтРезультатКомпоновки
	|ИЗ
	|	&РезультатКомпоновки КАК Т
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтДанные.ПолучательОповещения КАК ПолучательОповещения,
	|	ВЫБОР
	|		КОГДА НЕ НастройкиОповещений.Пользователь ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|		ИНАЧЕ ВтДанные.ПолучательОповещения
	|	КОНЕЦ КАК ПолучательОповещенияПослеПроверки
	|ПОМЕСТИТЬ
	|	ВтПолучатели
	|ИЗ
	|	ВтРезультатКомпоновки КАК ВтДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтключенныеНастройкиОповещенийПользователей КАК НастройкиОповещений
	|		ПО (НастройкиОповещений.ВидОповещения = &ВидОповещения)
	|			И (НастройкиОповещений.Пользователь = ВтДанные.ПолучательОповещения)
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтДанные.*,
	|	&ВидОповещения КАК ВидОповещения,
	|	ВтПолучатели.ПолучательОповещенияПослеПроверки КАК ПолучательОповещенияПослеПроверки,
	|	Пользователи.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
	|ИЗ
	|	ВтРезультатКомпоновки КАК ВтДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтПолучатели КАК ВтПолучатели
	|		ПО ВтДанные.ПолучательОповещения = ВтПолучатели.ПолучательОповещения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК Пользователи
	|		ПО ВтПолучатели.ПолучательОповещенияПослеПроверки = Пользователи.Ссылка
	|ИТОГИ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВтДанные.КонтекстОповещения)
	|ПО
	|	ПолучательОповещенияПослеПроверки
	|";
	
	Запрос.УстановитьПараметр("РезультатКомпоновки", РезультатКомпоновки);
	Запрос.УстановитьПараметр("ВидОповещения", ВидОповещения);
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
КонецФункции

Функция ИнициализироватьТаблицуКэшРеквизитов(Колонки)
	
	ТаблицаКэш = Новый ТаблицаЗначений;
	
	МассивЗапрещено = Новый Массив;
	МассивЗапрещено.Добавить("ИсточникОповещения");
	МассивЗапрещено.Добавить("ВидОповещения");
	МассивЗапрещено.Добавить("КонтекстОповещения");
	МассивЗапрещено.Добавить("КэшКлючевыхПолей");
	МассивЗапрещено.Добавить("ДатаЗаписи");
	
	Для каждого Колонка Из Колонки Цикл
		Если МассивЗапрещено.Найти(Колонка.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ТаблицаКэш.Колонки.Добавить(Колонка.Имя,
			Колонка.ТипЗначения,
			Колонка.Заголовок,
			Колонка.Ширина);
	КонецЦикла;
	Возврат ТаблицаКэш
КонецФункции

Функция ПодставитьЗначенияПоШаблону(Знач СтрокаШаблон, СтрокаЗначений, КоллекцияИменПараметров, ОформлениеПолей, КодЯзыка)
	ПараметрыЗамены = Новый Соответствие;
	Для каждого Значение Из КоллекцияИменПараметров Цикл
		ИмяПараметра = "[" + Значение.Имя + "]";
		ЗначениеПараметра = СтрокаЗначений[КоллекцияИменПараметров.Индекс(Значение)];
		ПараметрыЗамены.Вставить(ИмяПараметра, ЗначениеПараметра);
	КонецЦикла;
	
	РегулярноеВыражение = "(\[[^\]]+\])|([^\[\]]+)"; //@Non-NLS-1
	РегулярноеВыражениеПараметр = "\[[^\s]+\]"; //@Non-NLS-1
	
	МассивСтрок = Новый Массив;
	РезультатыПоиска = СтрНайтиВсеПоРегулярномуВыражению(СтрокаШаблон, РегулярноеВыражение);
	Для Каждого РезультатПоиска Из РезультатыПоиска Цикл
		Если СтрПодобнаПоРегулярномуВыражению(РезультатПоиска.Значение, РегулярноеВыражениеПараметр) Тогда
			ТекущийПараметр = ПараметрыЗамены.Получить(РезультатПоиска.Значение);
			Если ТекущийПараметр <> Неопределено Тогда
				ОформлениеПоля = ОформлениеПолей.Получить(РезультатПоиска.Значение);
				ОформленнаяСтрока = ОформитьСтроку(РезультатПоиска.Значение, ТекущийПараметр, ОформлениеПоля, КодЯзыка);
				МассивСтрок.Добавить(ОформленнаяСтрока);
			Иначе
				МассивСтрок.Добавить(РезультатПоиска.Значение);
			КонецЕсли;
		Иначе
			МассивСтрок.Добавить(РезультатПоиска.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Результат = Новый ФорматированнаяСтрока(МассивСтрок);
	Возврат Результат
	
КонецФункции

Функция ПредставлениеДокумента(ДокументСсылка, КодЯзыка)
	
	// ДокументСсылка пока игнорируем, так как не поддерживается получение представлений документов 
	// на разных языках, возвращаем просто строку
	
	Возврат НСтр("ru = 'Перейти к документу';
				|en = 'Go to document'", КодЯзыка);
	
КонецФункции

Функция ЗарезервированныеИменаПолей(ТолькоОбязательные = Ложь)
	
	ЗарезервированныеИмена = ОбязательныеПоля();
	Если НЕ ТолькоОбязательные Тогда
		Для каждого НеобязательноеПоле Из НеобязательныеПоля() Цикл
			ЗарезервированныеИмена.Добавить(НеобязательноеПоле);
		КонецЦикла;
	КонецЕсли;
	Возврат ЗарезервированныеИмена;
	
КонецФункции

Функция ОбязательныеПоля()
	
	ЗарезервированныеИмена = Новый Массив;
	ЗарезервированныеИмена.Добавить("ПолучательОповещения");
	ЗарезервированныеИмена.Добавить("КонтекстОповещения");
	
	Возврат ЗарезервированныеИмена;
	
КонецФункции

Функция НеобязательныеПоля()
	
	ЗарезервированныеИмена = Новый Массив;
	ЗарезервированныеИмена.Добавить("ИсточникОповещения");
	
	Возврат ЗарезервированныеИмена;
	
КонецФункции

// Позволяет проверить корректность схемы получения данных для формирования оповещений, при необходимости исправить ошибки,
// а также заполнить список критических ошибок, которые необходимо будет исправить пользователю.
//
// Параметры:
//   СхемаКомпоновкиДанных - СхемаКомпоновкиДанных - Схема получения данных оповещений.
//   СхемаСодержитКритическиеОшибки - Булево - флаг наличия критических ошибок, который необходимо взвести,
//   				чтобы пользователь не смог применить схему, пока не будут исправлены ошибки.
//   СписокКритическихОшибок - СписокЗначений Из Строка - Список, содержащий тексты сообщений о критических ошибках.
//   СписокПрочихСообщений - СписокЗначений Из Строка - Список, содержащий тексты сообщений о исправленных ошибках и прочие замечания.
//   				Данный список сообщений выводится пользователю один раз.
//   ОбязательныеПоля - Массив Из Строка - Массив обязательных имен полей схемы компоновки данных.
//   КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - компоновщик настроек для проверки схемы.
//
Процедура ПроверкаКорректностиСхемыПолученияДанныхОповещения(СхемаКомпоновкиДанных, СхемаСодержитКритическиеОшибки,
					СписокКритическихОшибок, СписокПрочихСообщений, ОбязательныеПоля, КомпоновщикНастроек = Неопределено)
	Если СхемаКомпоновкиДанных = Неопределено Тогда
		ТекстОшибки = НСтр("ru = 'Схема компоновки данных не заполнена.';
							|en = 'The data composition schema is not filled.'");
		СписокКритическихОшибок.Добавить(ТекстОшибки);
	ИначеЕсли СхемаКомпоновкиДанных.НаборыДанных.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'В схеме компоновки нет ни одного набора данных.';
							|en = 'There are no data sets in the data composition schema.'");
		СписокКритическихОшибок.Добавить(ТекстОшибки);
	Иначе
		ТипСтрокаНеограниченнойДлины = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(0,ДопустимаяДлина.Переменная));
		КомпоновщикНастроекКомпоновкиДанных = ИнициализироватьКомпоновщикНастроек(СхемаКомпоновкиДанных, КомпоновщикНастроек);
		Настройки = КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки();
		Для Каждого ИмяПоля Из ОбязательныеПоля Цикл
			ОбязательноеПоле = Новый ПолеКомпоновкиДанных(ИмяПоля);
			НайденноеПоле = Настройки.Выбор.ДоступныеПоляВыбора.НайтиПоле(ОбязательноеПоле);
			Если НайденноеПоле = Неопределено Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Обязательное поле ""%1"" не найдено в доступных полях выбора схемы компоновки данных.';
						|en = 'The ""%1"" required field is not found in the available fields of the data composition schema.'"),
					ИмяПоля);
				СписокКритическихОшибок.Добавить(ТекстОшибки);
			КонецЕсли;
			ОбязательноеПолеВыбрано = Ложь;
			Для каждого ВыбранноеПоле Из Настройки.Выбор.Элементы Цикл
				Если ТипЗнч(ВыбранноеПоле) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
					Продолжить;
				КонецЕсли;
				Если ВыбранноеПоле.Поле = ОбязательноеПоле И ВыбранноеПоле.Использование Тогда
					ОбязательноеПолеВыбрано = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если Не ОбязательноеПолеВыбрано Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Обязательное поле ""%1"" не выбрано в схеме компоновки данных.';
						|en = 'The ""%1"" required field is not selected in the data composition schema.'"),
					ИмяПоля);
				СписокКритическихОшибок.Добавить(ТекстОшибки);
			КонецЕсли;
		КонецЦикла;
		ИмяПоля = "ДатаАктуальности";
		Если Настройки.ПараметрыДанных.Элементы.Найти(ИмяПоля) = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Отсутствует обязательный параметр ""%1"" в схеме компоновки данных.';
					|en = 'The ""%1"" required parameter is missing in the data composition schema.'"),
				ИмяПоля);
			СписокКритическихОшибок.Добавить(ТекстОшибки);
		КонецЕсли;
		Для Каждого ДоступноеПоле Из Настройки.Выбор.ДоступныеПоляВыбора.Элементы Цикл
			
			Если ДоступноеПоле.Поле = Новый ПолеКомпоновкиДанных("СистемныеПоля")
				Или ДоступноеПоле.Поле = Новый ПолеКомпоновкиДанных("ПараметрыДанных") Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ДоступноеПоле.ТипЗначения) Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не определен тип значения поля ""%1"" схемы компоновки данных. Тип значения не может быть пустым.';
						|en = 'The value type of the ""%1"" field of the data composition schema is undefined. The value type cannot be empty.'"),
					ДоступноеПоле.Поле);
				СписокКритическихОшибок.Добавить(ТекстОшибки);
			ИначеЕсли ДоступноеПоле.ТипЗначения = ТипСтрокаНеограниченнойДлины Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Поле ""%1"" имеет тип значения ""Строка неограниченной длины"". Необходимо ограничить длину строки.';
						|en = 'The ""%1"" field has the ""String of unlimited length"" value type. Limit the string length.'"),
					ДоступноеПоле.Поле);
				СписокКритическихОшибок.Добавить(ТекстОшибки);
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	СхемаСодержитКритическиеОшибки = СписокКритическихОшибок.Количество() > 0;
КонецПроцедуры

Функция ИсточникДоступен(ПолноеИмяИсточника)
	
	Если НЕ (ПолучитьФункциональнуюОпцию("ИспользоватьОповещенияПользователейОСобытиях") 
		И Обсуждения.СистемаВзаимодействийПодключена()) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ОповещенияПользователейОСобытияхПовтИсп.ИсточникиОповещенийИсключения().Найти(ПолноеИмяИсточника) <> Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина
	
КонецФункции

Функция ВидыОповещенийПоРеквизитамИсточника(ПолноеИмяИсточника)
	ВидыОповещенийПоРеквизитамИсточника = Новый Соответствие;
	
	ИдентификаторИсточника = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяИсточника);
	ДоступныеВидыОповещений = ОповещенияПользователейОСобытияхПовтИсп.ДоступныеВидыОповещений()[ИдентификаторИсточника];
	
	Если ДоступныеВидыОповещений = Неопределено Тогда
		Возврат ВидыОповещенийПоРеквизитамИсточника;
	КонецЕсли;
	
	Для каждого ДанныеВидаОповещения Из ДоступныеВидыОповещений Цикл
		МассивВидов = ВидыОповещенийПоРеквизитамИсточника.Получить(ДанныеВидаОповещения.РеквизитИсточника); // Массив
		Если МассивВидов = Неопределено Тогда
			МассивВидов = Новый Массив;
			ВидыОповещенийПоРеквизитамИсточника.Вставить(ДанныеВидаОповещения.РеквизитИсточника, МассивВидов);
		КонецЕсли;
		МассивВидов.Добавить(ДанныеВидаОповещения.ВидОповещения);
	КонецЦикла;
	
	Возврат ВидыОповещенийПоРеквизитамИсточника
	
КонецФункции

Функция ТекстПустоеЗначение(КодЯзыка)
	
	Текст = НСтр("ru = '<Пустое значение>';
				|en = '<empty value>'", КодЯзыка);
	
	Возврат Текст
	
КонецФункции

Функция ИмяФормыСпискаПоСсылке(Ссылка, Пользователь)
	ИмяФормы = "";
	Если ОбщегоНазначения.ЗначениеСсылочногоТипа(Ссылка) Тогда
		ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(Ссылка);
		ИмяФормы = СтрШаблон("%1.%2", ИмяТаблицы, "ФормаСписка");
	КонецЕсли;
	Возврат ИмяФормы
КонецФункции

Функция КонструкторКомандыПерехода()
	
	КомандаПерехода = Новый Структура();
	КомандаПерехода.Вставить("Представление", "");
	КомандаПерехода.Вставить("ИмяДействия", "");
	КомандаПерехода.Вставить("Данные", Новый Структура("ИмяФормы, СоответствиеСсылок", "", Неопределено));
	
	Возврат КомандаПерехода
	
КонецФункции

Функция ТипыЕстьРегистратор()
	
	Массив = Новый Массив;
	Массив.Добавить("РегистрНакопления"); //@NON-NLS-1
	Массив.Добавить("AccumulationRegister"); //@NON-NLS-1
	Массив.Добавить("РегистрБухгалтерии"); //@NON-NLS-1
	Массив.Добавить("AccountingRegister"); //@NON-NLS-1
	Массив.Добавить("РегистрРасчета"); //@NON-NLS-1
	Массив.Добавить("CalculationRegister"); //@NON-NLS-1 
	Массив.Добавить("РегистрСведений"); //@NON-NLS-1
	Массив.Добавить("InformationRegister"); //@NON-NLS-1
	
	Возврат Массив
	
КонецФункции

Функция ИмяСлужебногоПользователя()
	Возврат НСтр("ru = 'Оповещения системы';
				|en = 'System notifications'", ОбщегоНазначения.КодОсновногоЯзыка());
КонецФункции

Функция ИнициализироватьТаблицуИсходныеДанные()
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ИсточникОповещения", ОбщегоНазначения.ОписаниеТипаВсеСсылки());
	Таблица.Колонки.Добавить("ИнициаторОповещения", Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	Таблица.Колонки.Добавить("ИдентификаторЗаписи", Новый ОписаниеТипов("УникальныйИдентификатор"));
	Таблица.Колонки.Добавить("ВидОповещения", Новый ОписаниеТипов("СправочникСсылка.ВидыОповещенийПользователей"));
	
	Возврат Таблица
КонецФункции

Функция ОформитьСтроку(ИмяПараметра, ЗначениеПараметра, ОформлениеПоля, КодЯзыка)
	
	ТипЗначенияПараметра = ТипЗнч(ЗначениеПараметра);
	ЭтоСсылка = ОбщегоНазначения.ЭтоСсылка(ТипЗначенияПараметра);
	ПредставлениеПараметра = Строка(ЗначениеПараметра);
	
	Шрифт = Неопределено;
	ЦветТекста = Неопределено;
	ЦветФона = Неопределено;
	
	Если ЗначениеЗаполнено(ОформлениеПоля) Тогда
		Если ОформлениеПоля.Свойство("Формат") Тогда
			ПредставлениеПараметра = Формат(ЗначениеПараметра, ОформлениеПоля.Формат);
		КонецЕсли;
		Если ОформлениеПоля.Свойство("Шрифт") Тогда
			Шрифт = ОформлениеПоля.Шрифт;
		КонецЕсли;
		Если ОформлениеПоля.Свойство("ЦветТекста") Тогда
			ЦветТекста = ОформлениеПоля.ЦветТекста;
		КонецЕсли;
		Если ОформлениеПоля.Свойство("ЦветФона") Тогда
			ЦветФона = ОформлениеПоля.ЦветФона;
		КонецЕсли;
	КонецЕсли;
	Если ПустаяСтрока(ПредставлениеПараметра) Тогда
		ПредставлениеПараметра = ТекстПустоеЗначение(КодЯзыка);
	КонецЕсли;
	
	Если ЭтоСсылка Тогда
		
		Если Документы.ТипВсеСсылки().СодержитТип(ТипЗначенияПараметра) Тогда
			ПредставлениеПараметра = ПредставлениеДокумента(ЗначениеПараметра, КодЯзыка);
		КонецЕсли;
		
		Попытка
			НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(ЗначениеПараметра);
			СтрокаСсылка = Новый ФорматированнаяСтрока(ПредставлениеПараметра, Шрифт, ЦветТекста, ЦветФона, НавигационнаяСсылка);
		Исключение
			// Пришел тип, от которого нельзя получить навигационную ссылку. Выводим строку без гиперссылки.
			СтрокаСсылка = Новый ФорматированнаяСтрока(ПредставлениеПараметра, Шрифт, ЦветТекста, ЦветФона);
		КонецПопытки;
	Иначе
		СтрокаСсылка = Новый ФорматированнаяСтрока(ПредставлениеПараметра, Шрифт, ЦветТекста, ЦветФона);
	КонецЕсли;
	
	Возврат СтрокаСсылка
	
КонецФункции

Процедура ДобавитьОбработчикДанныхДляПереходаНаНовуюВерсию(Обработчики)
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "*";
	Обработчик.Процедура = "ОповещенияПользователейОСобытиях.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.РежимВыполнения = "Оперативно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a7e59df8-6b10-4dec-9136-3fb4d15c11cc");
	Обработчик.Комментарий = НСтр("ru = 'Заполняет элементы справочника ""Виды оповещений пользователей"".';
									|en = 'Fills the items of the ""User notification kinds"" catalog.'");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.ВидыОповещенийПользователей.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
КонецПроцедуры

#КонецОбласти
