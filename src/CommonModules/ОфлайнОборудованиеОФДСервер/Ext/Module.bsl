#Область ПрограммныйИнтерфейс

// Функция осуществляет загрузку документов из облака Эвотор
//
// Параметры:
//   Параметры - Структура
//   РезультатЗагрузки - Структура
//
Процедура НачатьЗагрузкуДокументов(Параметры, РезультатВыполнения, ВыходныеПараметры) Экспорт
	
	РезультатЗагрузки = Новый Структура;
	ЕстьОшибки = Ложь;
	СообщениеОбОшибке = "";
	
	Если НЕ Параметры.ПериодИзмененВручную Тогда
		ОфлайнОборудование1СЭвоторСервер.ПолучитьДатуПоследнейЗагрузки(Параметры);
	КонецЕсли;
	
	МодульОФД = ОбщегоНазначенияБПО.ОбщийМодуль("ОФД");
	РезультатЗагрузки = МодульОФД.ПродажиЗаПериод(Параметры.Идентификатор, Параметры.ДатаНачала, Параметры.ДатаОкончания);
	
	Если ЗначениеЗаполнено(РезультатЗагрузки.КодОшибки) Тогда
		
		ЕстьОшибки = Истина;
		СообщениеОбОшибке = РезультатЗагрузки.СообщениеОбОшибке;
		
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		СоздатьСообщениеОбОшибке(ВыходныеПараметры, СообщениеОбОшибке);
		РезультатВыполнения = Новый Структура("Результат, ВыходныеПараметры", Ложь, ВыходныеПараметры);
	Иначе
		ДокументыЗагрузки = ОбработатьСписокДокументов(РезультатЗагрузки.ДанныеСмен, ЕстьОшибки, СообщениеОбОшибке);
		
		Если ЕстьОшибки Тогда
			СоздатьСообщениеОбОшибке(ВыходныеПараметры, СообщениеОбОшибке);
			РезультатВыполнения = Новый Структура("Результат, ВыходныеПараметры", Ложь, ВыходныеПараметры);
		Иначе
			Результат = ОфлайнОборудование1СЭвоторВызовСервера.ЗаполнитьДатуПоследнейПопыткиЗагрузки(Параметры, ТекущаяДата());
			Если Не Результат Тогда
				СообщениеОбОшибке = НСтр("ru = 'При записи данных произошла ошибка';
										|en = 'An error occurred when saving data'");
				СоздатьСообщениеОбОшибке(ВыходныеПараметры, СообщениеОбОшибке);
				РезультатВыполнения = Новый Структура("Результат, ВыходныеПараметры", Ложь, ВыходныеПараметры);
			Иначе
				РезультатВыполнения = Новый Структура("Результат, ВыходныеПараметры", Результат, ДокументыЗагрузки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Функция осуществляет загрузку документов из облака Эвотор
//
// Параметры:
//   Параметры - Структура
//   РезультатЗагрузки - Структура
//
Процедура НачатьЗагрузкуОборотовКассовойСмены(Параметры, РезультатВыполнения, ВыходныеПараметры) Экспорт
	
	РезультатЗагрузки = Новый Структура;
	ЕстьОшибки = Ложь;
	СообщениеОбОшибке = "";
	
	МодульОФД = ОбщегоНазначенияБПО.ОбщийМодуль("ОФД");
	РезультатЗагрузки = МодульОФД.ОборотыКассовойСмены(Параметры.Идентификатор, Параметры.НомерСмены);
	
	Если ЗначениеЗаполнено(РезультатЗагрузки.КодОшибки) Тогда
		
		ЕстьОшибки = Истина;
		СообщениеОбОшибке = РезультатЗагрузки.СообщениеОбОшибке;
		
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		СоздатьСообщениеОбОшибке(ВыходныеПараметры, СообщениеОбОшибке);
		РезультатВыполнения = Новый Структура("Результат, ВыходныеПараметры", Ложь, ВыходныеПараметры);
	Иначе
		ОборотыСмены = ОбработатьОбороты(РезультатЗагрузки.ДанныеСмены, Параметры.НомерСмены, ЕстьОшибки, СообщениеОбОшибке);
		
		Если ЕстьОшибки Тогда
			СоздатьСообщениеОбОшибке(ВыходныеПараметры, СообщениеОбОшибке);
			РезультатВыполнения = Новый Структура("Результат, ВыходныеПараметры", Ложь, ВыходныеПараметры);
		Иначе
			РезультатВыполнения = Новый Структура("Результат, ВыходныеПараметры", Истина, ОборотыСмены);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Функция осуществляет загрузку документов из облака Эвотор
//
// Параметры:
//   Параметры - Структура
//   РезультатЗагрузки - Структура
//
Процедура НачатьЗагрузкуЧековКассовойСмены(Параметры, РезультатВыполнения, ВыходныеПараметры) Экспорт
	
	РезультатЗагрузки = Новый Структура;
	ЕстьОшибки = Ложь;
	СообщениеОбОшибке = "";
	
	МодульОФД = ОбщегоНазначенияБПО.ОбщийМодуль("ОФД");
	РезультатЗагрузки = МодульОФД.ЧекиКассовойСмены(Параметры.Идентификатор, Параметры.НомерСмены);
	
	Если ЗначениеЗаполнено(РезультатЗагрузки.КодОшибки) Тогда
		
		ЕстьОшибки = Истина;
		СообщениеОбОшибке = РезультатЗагрузки.СообщениеОбОшибке;
		
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		СоздатьСообщениеОбОшибке(ВыходныеПараметры, СообщениеОбОшибке);
		РезультатВыполнения = Новый Структура("Результат, ВыходныеПараметры", Ложь, ВыходныеПараметры);
	Иначе
		ДокументыЗагрузки = ОбработатьСписокДокументов(РезультатЗагрузки.ДанныеСмен, ЕстьОшибки, СообщениеОбОшибке);
		
		Если ЕстьОшибки Тогда
			СоздатьСообщениеОбОшибке(ВыходныеПараметры, СообщениеОбОшибке);
			РезультатВыполнения = Новый Структура("Результат, ВыходныеПараметры", Ложь, ВыходныеПараметры);
		Иначе
			РезультатВыполнения = Новый Структура("Результат, ВыходныеПараметры", Истина, ДокументыЗагрузки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура добавляет в массив выходных параметров сообщение об ошибке.
//		Параметры:
//			- ВыходныеПараметры - массив, в который будет помещено сообщение об ошибке.
//			- ТекстСообщения - текст сообщения, содержащий информация об ошибке.
Процедура СоздатьСообщениеОбОшибке(ВыходныеПараметры, ТекстСообщения)
	
	ВыходныеПараметры.Добавить(999);
	ВыходныеПараметры.Добавить(ТекстСообщения);
	
КонецПроцедуры

Функция ОбработатьСписокДокументов(МассивДанных, ЕстьОшибки, СообщениеОбОшибке)
	
	СтруктураВозвращаемыхДанных = Новый Массив;
	СтруктураДанных = МенеджерОфлайнОборудованияКлиентСервер.ЗагружаемыеДанныеИзККМ();
	
	Для Каждого Смена Из МассивДанных Цикл
		
		ОтчетОПродажахККМ = МенеджерОфлайнОборудованияКлиентСервер.ОтчетОПродажахККМ();
		ОтчетОПродажахККМ.ДатаОткрытияСмены = Смена.ДатаОткрытия;
		ОтчетОПродажахККМ.ДатаЗакрытияСмены = Смена.ДатаЗакрытия;
		ОтчетОПродажахККМ.НомерСмены        = Смена.НомерСмены;
		
		Для Каждого Чек Из Смена.ДанныеЧеков Цикл
			
			ЧекККМ = МенеджерОфлайнОборудованияКлиентСервер.ЧекККМ();
			ЧекККМ.ДатаЧека                = Чек.ДатаЧека;
			ЧекККМ.НомерЧека               = Чек.ФискальныйНомерЧека;
			
			Если Чек.ПризнакРасчета = "ПРИХОД" Тогда
				ЧекККМ.ТипРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.РасчетДенежнымиСредствамиПоКоду(1);
			ИначеЕсли Чек.ПризнакРасчета = "ВОЗВРАТПРИХОДА" Тогда
				ЧекККМ.ТипРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.РасчетДенежнымиСредствамиПоКоду(2);
			ИначеЕсли Чек.ПризнакРасчета = "РАСХОД" Тогда
				ЧекККМ.ТипРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.РасчетДенежнымиСредствамиПоКоду(3);
			ИначеЕсли Чек.ПризнакРасчета = "ВОЗВРАТРАСХОДА" Тогда
				ЧекККМ.ТипРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.РасчетДенежнымиСредствамиПоКоду(4);
			Иначе
				ЧекККМ.ТипРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.РасчетДенежнымиСредствамиПоКоду(1);
			КонецЕсли; 
			
			Если Чек.СистемаНалогообложения = "ОСН" Тогда
				ЧекККМ.СистемаНалогообложения = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.СистемаНалогообложенияККТПоКоду(0);
			ИначеЕсли Чек.СистемаНалогообложения = "УПРОЩЕННАЯДОХОД" Тогда
				ЧекККМ.СистемаНалогообложения = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.СистемаНалогообложенияККТПоКоду(1);
			ИначеЕсли Чек.СистемаНалогообложения = "УПРОЩЕННАЯДОХОДРАСХОД" Тогда
				ЧекККМ.СистемаНалогообложения = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.СистемаНалогообложенияККТПоКоду(2);
			ИначеЕсли Чек.СистемаНалогообложения = "ЕНВД" Тогда
				ЧекККМ.СистемаНалогообложения = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.СистемаНалогообложенияККТПоКоду(3);
			ИначеЕсли Чек.СистемаНалогообложения = "ЕСН" Тогда
				ЧекККМ.СистемаНалогообложения = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.СистемаНалогообложенияККТПоКоду(4);
			ИначеЕсли Чек.СистемаНалогообложения = "ПАТЕНТ" Тогда
				ЧекККМ.СистемаНалогообложения = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.СистемаНалогообложенияККТПоКоду(5);
			ИначеЕсли Чек.СистемаНалогообложения = "" Тогда
				ЧекККМ.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.ТипыСистемНалогообложенияККТ.ПустаяСсылка");
			КонецЕсли;
			
			Если Чек.ПризнакРасчета = "ПРИХОД" ИЛИ Чек.ПризнакРасчета = "ВОЗВРАТРАСХОДА" Тогда
				ЭтоПродажа = Истина;
			Иначе
				ЭтоПродажа = Ложь;
			КонецЕсли;
			Если Чек.ДанныеПоРасчетам.СуммаНаличными <> 0 Тогда
				ОплатаЧекаККМ = МенеджерОфлайнОборудованияКлиентСервер.ОплатаЧекаККМ();
				ОплатаЧекаККМ.СуммаНаличнойОплаты = ?(Чек.ДанныеПоРасчетам.СуммаНаличными > 0, Чек.ДанныеПоРасчетам.СуммаНаличными, -Чек.ДанныеПоРасчетам.СуммаНаличными);
				ОплатаЧекаККМ.СуммаЭлектроннойОплаты        = 0;
				ОплатаЧекаККМ.СуммаПостоплатой              = 0;
				ОплатаЧекаККМ.СуммаПредоплатой              = 0;
				ОплатаЧекаККМ.СуммаВстречнымПредоставлением = 0;
				ЧекККМ.Оплаты.Добавить(ОплатаЧекаККМ);
			ИначеЕсли Чек.ДанныеПоРасчетам.СуммаЭлектронными <> 0 Тогда
				ОплатаЧекаККМ = МенеджерОфлайнОборудованияКлиентСервер.ОплатаЧекаККМ();
				ОплатаЧекаККМ.СуммаЭлектроннойОплаты = ?(Чек.ДанныеПоРасчетам.СуммаЭлектронными > 0, Чек.ДанныеПоРасчетам.СуммаЭлектронными, -Чек.ДанныеПоРасчетам.СуммаЭлектронными);
				ОплатаЧекаККМ.СуммаНаличнойОплаты           = 0;
				ОплатаЧекаККМ.СуммаПостоплатой              = 0;
				ОплатаЧекаККМ.СуммаПредоплатой              = 0;
				ОплатаЧекаККМ.СуммаВстречнымПредоставлением = 0;
				ЧекККМ.Оплаты.Добавить(ОплатаЧекаККМ);
			ИначеЕсли Чек.ДанныеПоРасчетам.СуммаПостоплатами <> 0 Тогда
				ОплатаЧекаККМ = МенеджерОфлайнОборудованияКлиентСервер.ОплатаЧекаККМ();
				ОплатаЧекаККМ.СуммаПостоплатой = ?(Чек.ДанныеПоРасчетам.СуммаПостоплатами > 0, Чек.ДанныеПоРасчетам.СуммаПостоплатами, -Чек.ДанныеПоРасчетам.СуммаПостоплатами);
				ОплатаЧекаККМ.СуммаНаличнойОплаты           = 0;
				ОплатаЧекаККМ.СуммаЭлектроннойОплаты        = 0;
				ОплатаЧекаККМ.СуммаПредоплатой              = 0;
				ОплатаЧекаККМ.СуммаВстречнымПредоставлением = 0;
				ЧекККМ.Оплаты.Добавить(ОплатаЧекаККМ);
			ИначеЕсли Чек.ДанныеПоРасчетам.СуммаПредоплатами <> 0 Тогда
				ОплатаЧекаККМ = МенеджерОфлайнОборудованияКлиентСервер.ОплатаЧекаККМ();
				ОплатаЧекаККМ.СуммаПредоплатой = ?(Чек.ДанныеПоРасчетам.СуммаПредоплатами > 0, Чек.ДанныеПоРасчетам.СуммаПредоплатами, -Чек.ДанныеПоРасчетам.СуммаПредоплатами);
				ОплатаЧекаККМ.СуммаНаличнойОплаты           = 0;
				ОплатаЧекаККМ.СуммаПостоплатой              = 0;
				ОплатаЧекаККМ.СуммаЭлектроннойОплаты        = 0;
				ОплатаЧекаККМ.СуммаВстречнымПредоставлением = 0;
				ЧекККМ.Оплаты.Добавить(ОплатаЧекаККМ);
			КонецЕсли;
			
			ЧекККМ.ЭтоЧекКоррекции = Чек.ЯвляетсяЧекомКоррекции;
			Если Чек.ЯвляетсяЧекомКоррекции Тогда
				ЧекККМ.ДанныеКоррекции.ДатаКорректируемогоРасчета = Чек.ДанныеКоррекции.ДатаКорректируемогоРасчета;
				Если Чек.ДанныеКоррекции.Свойство("НомерПредписания") Тогда
					ЧекККМ.ДанныеКоррекции.НомерПредписания           = Чек.ДанныеКоррекции.НомерПредписания;
				КонецЕсли;
				ЧекККМ.ДанныеКоррекции.ТипКоррекции               = Чек.ДанныеКоррекции.ТипКоррекции;
				ЧекККМ.ДанныеКоррекции.ФискальныйНомер            = Чек.ДанныеКоррекции.ФискальныйНомер;
			КонецЕсли;
			
			Для Каждого Товар Из Чек.Товары Цикл
				
				ТоварККМ = МенеджерОфлайнОборудованияКлиентСервер.ТоварЧекаККМ();
				ТоварККМ.Наименование                 = Товар.Наименование;
				ТоварККМ.Количество                   = ?(Товар.Количество > 0, Товар.Количество, -Товар.Количество);
				ТоварККМ.Сумма                        = ?(Товар.Сумма > 0, Товар.Сумма, -Товар.Сумма);
				ТоварККМ.Цена                         = Товар.Стоимость;
				Если Товар.СтавкаНДС = 1 Тогда
					ТоварККМ.СтавкаНДС = "20";
				ИначеЕсли Товар.СтавкаНДС = 2 Тогда
					ТоварККМ.СтавкаНДС = "10";
				ИначеЕсли Товар.СтавкаНДС = 3 Тогда
					ТоварККМ.СтавкаНДС = "20/120";
				ИначеЕсли Товар.СтавкаНДС = 4 Тогда
					ТоварККМ.СтавкаНДС = "10/110";
				ИначеЕсли Товар.СтавкаНДС = 5 Тогда
					ТоварККМ.СтавкаНДС = "0";
				ИначеЕсли Товар.СтавкаНДС = 6 Тогда
					ТоварККМ.СтавкаНДС = "none";
				ИначеЕсли Товар.СтавкаНДС = 7 Тогда
					ТоварККМ.СтавкаНДС = "5";
				ИначеЕсли Товар.СтавкаНДС = 8 Тогда
					ТоварККМ.СтавкаНДС = "7";
				ИначеЕсли Товар.СтавкаНДС = 9 Тогда
					ТоварККМ.СтавкаНДС = "5/105";
				ИначеЕсли Товар.СтавкаНДС = 10 Тогда
					ТоварККМ.СтавкаНДС = "7/107"
				ИначеЕсли Товар.СтавкаНДС = 11 Тогда
					ТоварККМ.СтавкаНДС = "22"
				ИначеЕсли Товар.СтавкаНДС = 12 Тогда
					ТоварККМ.СтавкаНДС = "22/122"
				Иначе
					ТоварККМ.СтавкаНДС = "20";
				КонецЕсли; 
				Если Товар.ПризнакСпособаРасчета = "ПРЕДОПЛАТА100%" Тогда
					ТоварККМ.ПризнакСпособаРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПризнакСпособаРасчетаПоКоду(1);
				ИначеЕсли Товар.ПризнакСпособаРасчета = "ПРЕДОПЛАТА" Тогда
					ТоварККМ.ПризнакСпособаРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПризнакСпособаРасчетаПоКоду(2);
				ИначеЕсли Товар.ПризнакСпособаРасчета = "АВАНС" Тогда
					ТоварККМ.ПризнакСпособаРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПризнакСпособаРасчетаПоКоду(3);
				ИначеЕсли Товар.ПризнакСпособаРасчета = "ПОЛНЫЙРАСЧЕТ" Тогда
					ТоварККМ.ПризнакСпособаРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПризнакСпособаРасчетаПоКоду(4);
				ИначеЕсли Товар.ПризнакСпособаРасчета = "ЧАСТИЧНЫЙРАСЧЕТКРЕДИТ" Тогда
					ТоварККМ.ПризнакСпособаРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПризнакСпособаРасчетаПоКоду(5);
				ИначеЕсли Товар.ПризнакСпособаРасчета = "КРЕДИТ" Тогда
					ТоварККМ.ПризнакСпособаРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПризнакСпособаРасчетаПоКоду(6);
				ИначеЕсли Товар.ПризнакСпособаРасчета = "ОПЛАТАКРЕДИТА" Тогда
					ТоварККМ.ПризнакСпособаРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПризнакСпособаРасчетаПоКоду(7);
				Иначе
					ТоварККМ.ПризнакСпособаРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПризнакСпособаРасчетаПоКоду(4);
				КонецЕсли;
				ТоварККМ.ПризнакПредметаРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.ПризнакПредметаРасчетаПоКоду(Товар.ПризнакПредметаРасчета); 
				
				Если ЗначениеЗаполнено(Товар.КодПредметаРасчета) Тогда
					ТоварККМ.КодПредметаРасчета.Значение = СтрЗаменить(Товар.КодПредметаРасчета.ЗначениеКода, ОбщегоНазначенияБПОКлиентСервер.РазделительGS1(), ОбщегоНазначенияБПОКлиентСервер.ЭкранированныйСимволGS1());
					ТоварККМ.КодПредметаРасчета.ТипКода = Товар.КодПредметаРасчета.ТипКода;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Товар.МераКоличестваПредметаРасчета) Тогда
					ТоварККМ.МераКоличестваПредметаРасчета.МераКоличестваПредметаРасчета = ОборудованиеЧекопечатающиеУстройстваКлиентСервер.МераКоличестваПредметаРасчетаККТПоКоду(Товар.МераКоличестваПредметаРасчета.Идентификатор);
					ТоварККМ.МераКоличестваПредметаРасчета.Значение = Товар.МераКоличестваПредметаРасчета.Значение;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Товар.ДанныеАгента) Тогда
					ТоварККМ.ДанныеАгента.ИНН = Товар.ДанныеАгента.ИНН;
					ТоварККМ.ДанныеАгента.Наименование = Товар.ДанныеАгента.Наименование;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(Товар.ДанныеПоставщика) Тогда
					ТоварККМ.ДанныеПоставщика.ИНН = Товар.ДанныеПоставщика.ИНН;
					ТоварККМ.ДанныеПоставщика.Наименование = Товар.ДанныеПоставщика.Наименование;
				КонецЕсли;
				
				ЧекККМ.Товары.Добавить(ТоварККМ);
			КонецЦикла;
			ОтчетОПродажахККМ.Чеки.Добавить(ЧекККМ);
		КонецЦикла;
		СтруктураДанных.ОтчетыОПродажах.Добавить(ОтчетОПродажахККМ);
	КонецЦикла;
	
	Если СтруктураДанных.ОтчетыОПродажах.Количество() > 0 Тогда
		ЕстьОшибки = Ложь;
	Иначе
		ЕстьОшибки = Истина;
		СообщениеОбОшибке = НСтр("ru = 'Нет данных к загрузке';
								|en = 'No data to import'");
	КонецЕсли;
	
	СтруктураВозвращаемыхДанных.Добавить(СтруктураДанных);
	
	Возврат СтруктураВозвращаемыхДанных;
	
КонецФункции

Функция ОбработатьОбороты(ДанныеЗагрузки, НомерСмены, ЕстьОшибки, СообщениеОбОшибке)
	
	СтруктураВозвращаемыхДанных = МенеджерОфлайнОборудованияКлиентСервер.ДанныеОборотовКассовойСмены();
	ЗаполнитьЗначенияСвойств(СтруктураВозвращаемыхДанных, ДанныеЗагрузки);
	СтруктураВозвращаемыхДанных.ДатаЗакрытияСмены = ДанныеЗагрузки.ДатаЗакрытия;
	СтруктураВозвращаемыхДанных.ДатаОткрытияСмены = ДанныеЗагрузки.ДатаОткрытия;
	СтруктураВозвращаемыхДанных.НомерСмены = НомерСмены;
	Если ЗначениеЗаполнено(ДанныеЗагрузки) Тогда
		ЕстьОшибки = Ложь;
	Иначе
		ЕстьОшибки = Истина;
		СообщениеОбОшибке = НСтр("ru = 'Нет данных к загрузке';
								|en = 'No data to import'");
	КонецЕсли;
	
	Возврат СтруктураВозвращаемыхДанных;
	
КонецФункции

#КонецОбласти