
#Область СлужебныеПроцедурыИФункции

// Функция выполняет проверку использования объекта в длительной операции
// 
// Параметры:
//  Ссылка - ЛюбаяСсылка - Объект проверки.
//  УникальныйИдентификатор - УникальныйИдентификатор - идентификатор формы, в которой запускается проверка.
//  ФормаАдресХранилища - Строка - адрес хранилища в которое будут помещен результат проверки.
//  ЕстьСсылки - Булево - Флаг, указывающий на наличие ссылок на объект.
//  ИспользоватьЛокальнуюФункциюПроверки - Булево - Использовать локальную функцию проверки
// 
// Возвращаемое значение:
//  Структура - Проверить использование объекта:
//   * АдресХранилища  - Строка     - адрес временного хранилища, в которое будет помещен результат работы задания;
//   * ИдентификаторЗадания - УникальныйИдентификатор - уникальный идентификатор запущенного фонового задания;
//   * ЗаданиеВыполнено - Булево - Истина если задание было успешно выполнено за время вызова функции.
Функция ПроверитьИспользованиеОбъекта(Ссылка, УникальныйИдентификатор, ФормаАдресХранилища, ЕстьСсылки, ИспользоватьЛокальнуюФункциюПроверки = Ложь) Экспорт
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Объект", Ссылка);
	
	Если ИспользоватьЛокальнуюФункциюПроверки Тогда
		ПроцедураПроверки = ОбщегоНазначения.ИмяБазовогоТипаПоОбъектуМетаданных(Ссылка.Метаданные())
		                  + "." +Ссылка.Метаданные().Имя
		                  + ".ПроверитьИспользованиеОбъекта";
	Иначе
		ПроцедураПроверки = "ОбщегоНазначенияУТ.ПроверитьИспользованиеОбъекта";
	КонецЕсли;
	
	НаименованиеЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Проверка использования объекта %1';
			|en = 'Checking usage of object %1'"),
		Ссылка);

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
		ПроцедураПроверки,
		ПараметрыЗадания,
		ПараметрыВыполнения);
	
	ФормаАдресХранилища = Результат.АдресРезультата;
	
	Если Результат.Статус = "Выполнено" Тогда
		ЕстьСсылки = ПолучитьИзВременногоХранилища(ФормаАдресХранилища);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
