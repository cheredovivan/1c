///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП".
// ОбщийМодуль.СистемаБыстрыхПлатежейКлиентСервер.
//
// Клиент-серверные процедуры общих настроек:
//  - определение валидности URL переданной ссылки.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ОбработкаURL

// Определяет валидность URL переданной ссылки.
//
// Параметры:
//  Ссылка - Строка - URL кассовой ссылки.
//  ПроверкаПутиНаСервере - Булево - признак проверки заполненности пути в переданном URL.
//
// Возвращаемое значение:
//  Структура - Содержит результат проверки:
//    * URLВалиден - Булево - если Истина, URL валиден.
//    * СтруктураURI - Структура, Неопределено - составные части URL в виде структуры.
//
Функция СтруктураURLФункциональнойСсылки(
		Знач Ссылка,
		Знач ПроверкаПутиНаСервере) Экспорт
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("URLВалиден", Истина);
	РезультатПроверки.Вставить("СтруктураURI", Неопределено);
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(
		Ссылка);
	
	Если СтруктураURI.Схема <> "https"
		Или (СтруктураURI.Порт <> Неопределено И СтруктураURI.Порт <> 443)
		Или (ПроверкаПутиНаСервере И ПустаяСтрока(СтруктураURI.ПутьНаСервере)) Тогда
		РезультатПроверки.URLВалиден = Ложь;
	КонецЕсли;
	
	РезультатПроверки.СтруктураURI = СтруктураURI;
	
	Возврат РезультатПроверки;
	
КонецФункции

#КонецОбласти

#Область УстаревшиеПроцедурыИФункции

#Область СтатусыОпераций

// Устарела. Следует использовать НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполнена.
// Возвращает статус операции "Выполнена".
//
// Возвращаемое значение:
//  Строка - код состояния.
//
Функция СтатусОперацииВыполнена() Экспорт
	
	Возврат НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполнена();
	
КонецФункции

// Устарела. Следует использовать НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполняется.
// Возвращает статус операции "Выполняется".
//
// Возвращаемое значение:
//  Строка - код состояния.
//
Функция СтатусОперацииВыполняется() Экспорт
	
	Возврат НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполняется();
	
КонецФункции

// Устарела. Следует использовать НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОтменена.
// Возвращает статус операции "Отменена".
//
// Возвращаемое значение:
//  Строка - код состояния.
//
Функция СтатусОперацииОтменена() Экспорт
	
	Возврат НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОтменена();
	
КонецФункции

// Устарела. Следует использовать НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка.
// Возвращает статус операции "Ошибка".
//
// Возвращаемое значение:
//  Строка - код состояния.
//
Функция СтатусОперацииОшибка() Экспорт
	
	Возврат НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка();
	
КонецФункции

// Устарела. Следует использовать НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииТребуетсяПодтверждение.
// Возвращает статус операции "ТребуетсяПодтверждение".
//
// Возвращаемое значение:
//  Строка - код состояния.
//
Функция СтатусОперацииТребуетсяПодтверждение() Экспорт
	
	Возврат НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииТребуетсяПодтверждение();
	
КонецФункции

// Устарела. Следует использовать НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОтклонена.
// Возвращает статус операции "Отклонена".
//
// Возвращаемое значение:
//  Строка - код состояния.
//
Функция СтатусОперацииОтклонена() Экспорт
	
	Возврат НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОтклонена();
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает идентификатор режима создания новой настройки для мастера подключения.
//
// Возвращаемое значение:
//  Строка - идентификатор.
//
Функция ИдентификаторОперацииСозданиеНастройки() Экспорт
	
	Возврат "СозданиеНовойНастройки";
	
КонецФункции

// Возвращает идентификатор участника "UNKNOWN".
//
// Возвращаемое значение:
//  Строка - идентификатор.
//
Функция ИдентификаторНеизвестногоУчастника() Экспорт
	
	Возврат "UNKNOWN";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует текст подсказки настройки подключения к Системе быстрых платежей.
//
// Параметры:
//  НаименованиеУчастника - Строка - наименование банка участника СБП;
//  ПараметрыПодсказки - Структура - результат создания параметров подсказки:
//   * АдресЛичногоКабинета - Строка - ссылка для перехода в личный кабинет;
//   * ПартнерАгентаСБП - Булево - признак партнера Агента СБП;
//   * АдресСтраницыЗаявки - Строка - адрес страницы отправки заявки;
//   * ИдентификаторУчастника - Строка - идентификатор участника СБП.
//  ВариантНастройки - ПеречислениеСсылка.ВариантыНастройкиСБП - вариант настройкиСистемы быстрых платежей.
//
// Возвращаемое значение:
//  Строка, ФорматированнаяСтрока - подсказка при настройке подключения.
//
Функция ТекстПодсказкиПодключенияБезФорматирования(
		НаименованиеУчастника,
		ПараметрыПодсказки,
		ВариантНастройки) Экспорт
	
	Подсказка = "";
	Если ПараметрыПодсказки.ПартнерАгентаСБП
		И ВариантНастройки = ПредопределенноеЗначение("Перечисление.ВариантыНастройкиСБП.c2b") Тогда
		Подсказка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для подключения к Системе быстрых платежей заполните настройки. Получить данные для настройки можно в <a href = ""%1"">Агенте 1С:СБП</a> для %2.';
					|en = 'To connect to the Faster Payments System, fill the settings. You can get the setting data in <a href = ""%1"">1C:SBP Agent</a> for %2.'"),
					ПараметрыПодсказки.АдресЛичногоКабинета,
					НаименованиеУчастника);
	ИначеЕсли ЗначениеЗаполнено(ПараметрыПодсказки.АдресЛичногоКабинета) Тогда
		Подсказка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для подключения к Системе быстрых платежей заполните настройки. Получить данные для настройки можно в <a href = ""%1"">%2</a>.';
					|en = 'To connect to the Cloud cash account, fill the settings. You can get the setting data in <a href = ""%1"">%2</a>.'"),
					ПараметрыПодсказки.АдресЛичногоКабинета,
					НаименованиеУчастника);
	Иначе
		Подсказка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для подключения к Системе быстрых платежей заполните настройки  или отправьте <a href = ""%1"">заявку на подключение</a> в %2.';
					|en = 'To connect to the Faster Payments System, fill the settings or send an <a href = ""%1"">activation request</a> to %2.'"),
					ПараметрыПодсказки.АдресСтраницыЗаявки,
					НаименованиеУчастника);
	КонецЕсли;
	
	Если ПараметрыПодсказки.ИдентификаторУчастника = "1C-SBP" Тогда
		Подсказка = Подсказка
			+ Символы.ПС
			+ Символы.ПС
			+ НСтр("ru = 'Список доступных к подключению банков можно посмотреть в <a href = ""https://sbp.1c.ru/ui/notPartnerBanks"">Агенте 1С:СБП</a>.';
					|en = 'You can find the list of banks you can connect to in <a href = ""https://sbp.1c.ru/ui/notPartnerBanks"">1C:SBP Agent</a>.'");
	КонецЕсли;
	
	Возврат Подсказка
	
КонецФункции

#КонецОбласти
