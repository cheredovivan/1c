#Область ПрограммныйИнтерфейс

#Область Проведение

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - См. ПроведениеДокументов.СвойстваДокумента
//
// Возвращаемое значение:
//  Структура - См. ПроведениеДокументов.ПараметрыУчетногоМеханизма
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	// Проведение
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.ПартииПрочихРасходов);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.ОтклоненияВСтоимостиТоваров);
		
		//++ Локализация

		//++ НЕ УТ
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства);
		//-- НЕ УТ
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.ПартииПроизводственныхЗатрат);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.ПартииРасходовНаСебестоимостьТоваров);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.ПартииТоваровОрганизаций);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.ПартииТоваровПереданныеНаКомиссию);
		
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.ДетализацияПартийТоваровДляНДСиУСН);
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.ДетализацияПартийТоваровДляНДСиУСН2_4);
		//-- Локализация
		
	КонецЕсли;
	
	// Контроль
	Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.ПартииПрочихРасходов);
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.ОтклоненияВСтоимостиТоваров);
		
		//++ Локализация

		//++ НЕ УТ
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.ПартииНезавершенногоПроизводства);
		//-- НЕ УТ
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.ПартииПроизводственныхЗатрат);
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.ПартииРасходовНаСебестоимостьТоваров);
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.ПартииТоваровОрганизаций);
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.ПартииТоваровПереданныеНаКомиссию);
		
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.ДетализацияПартийТоваровДляНДСиУСН);
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.ДетализацияПартийТоваровДляНДСиУСН2_4);
		//-- Локализация
		
		Параметры.КонтрольныеРегистрыИзменений.Добавить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
		
	КонецЕсли;
	
	// Контроль даты запрета
	Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		Параметры.КонтрольныеРегистрыДатаЗапрета.Добавить(Метаданные.РегистрыНакопления.ПартииПрочихРасходов);
		Параметры.КонтрольныеРегистрыДатаЗапрета.Добавить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
		Параметры.КонтрольныеРегистрыДатаЗапрета.Добавить(Метаданные.РегистрыНакопления.ОтклоненияВСтоимостиТоваров);
	КонецЕсли;	
	
	Параметры.ЕстьПроизводныеДвижения = Документ.Движения.Найти(Метаданные.РегистрыНакопления.СебестоимостьТоваров.Имя) <> Неопределено;
	
	Возврат Параметры;
	
КонецФункции

// Возвращает тексты запросов для сторнирования движений при исправлении документов
// 
// Параметры:
// 	МетаданныеДокумента - ОбъектМетаданныхДокумент - Метаданные документа, который проводится.
// 
// Возвращаемое значение:
// 	Соответствие из КлючИЗначение - Соответствие полного имени регистра тексту запроса сторнирования:
//   * Ключ - Строка - имя регистра
//   * Значение - Строка - текст запроса
Функция ТекстыЗапросовСторнирования(МетаданныеДокумента) Экспорт
	
	ДвиженияДокумента = МетаданныеДокумента.Движения;

	ТекстыЗапросов = Новый Соответствие();

	// Заполнение полей должно соответствовать функциям:
	// РасчетСебестоимостиЗаполнениеПартий.ТекстСторноДвиженияИсправленныхДокументовТекущегоПериода()
	// РасчетСебестоимостиЗаполнениеПартий.ТекстСторноДвиженияИсправленныхДокументовПрошлогоПериода()
	
	МетаданныеРегистра = Метаданные.РегистрыНакопления.СебестоимостьТоваров;
	Если ДвиженияДокумента.Содержит(МетаданныеРегистра) Тогда
		ШаблонЗапроса = "
		|ВЫБРАТЬ
		|	ДокументыСторно.Дата КАК Период,
		|	НАЧАЛОПЕРИОДА(ДанныеРегистра.Период, МЕСЯЦ) КАК ПериодПервичныхДвижений,
		|	&ПоляВТ,
		// При сторнировании текущего периода:
		//	Для доп. расходов устанавливается тип записи "СторноДопРасходов"
		//	Тип записи "Дополнение" сохраняется
		//	Для расходного движения:
		//	- для типа записи "Потребление" с заполннным кор. разделом учета устанавливается тип записи "Сторно"
		//	- для типа записи "Потребление" с пустым кор. разделом учета устанавливается тип записи "СторноПотребления" 
		//	- для типа записи "СписаниеНаДругиеПартии" устанавливается тип записи "СторноСписанияНаДругиеПартии"
		//	- для типа записи "СписаниеОстаткаБезПартии" устанавливается тип записи "СторноСписанияОстаткаБезПартии"
		//	- для типа записи "Сторно" устанавливается тип записи "СторноВозврата"
		//	Для приходного движения:
		//	- для типа записи "Перемещение" устанавливается тип записи "ИсправлениеТекущегоПериода"
		//	- для типа записи "Потребление" устанавливается тип записи "СторноПотребления" (используется при фактуровке поставки)
		//	- для типа записи "Партия" устанавливается тип записи "КорректировкаПриобретения"
		//	- для типа записи "ВозвратНаДругойСклад" устанавливается тип записи "СторноВозвратНаДругойСклад"
		// При сторнировании прошлого периода:
		//	Для доп. расходов устанавливается тип записи "КорректировкаПриобретенияПрошлогоПериода"
		//	Для типа записи "Дополнение" устанавливается тип записи "ДополнениеПрошлыйПериод"
		//	Для типа записи "ВозвратПрошлыхПериодов" устанавливается тип записи "СторноВозвратаПрошлыхПериодов"
		//	Для приходного движения с типом записи "Перемещение" устанавливается тип записи "ИсправлениеПеремещенияПрошлогоПериода"
		//	Для расходного движения устанавливается тип записи "ИсправлениеПрошлогоПериода"
		//	Для приходного движения устанавливается тип записи "КорректировкаПриобретения", кроме типа записей "Потребление".
		|	(ВЫБОР
		// Исправление прошлого периода
		|		КОГДА ДанныеРегистра.Период < НАЧАЛОПЕРИОДА(ДокументыСторно.Дата, МЕСЯЦ) ТОГДА
		|			ВЫБОР КОГДА  ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение) 
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДополнениеПрошлыйПериод)
		|
		|			КОГДА ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВозвратПрошлыхПериодов)
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноВозвратаПрошлыхПериодов)
		|
		|			КОГДА ДанныеРегистра.ТипЗаписи В (
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыБезПартии),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретенияПрошлогоПериода),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаСтоимости),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВключениеАкцизаВСтоимость))
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретенияПрошлогоПериода)
		|
		|			КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			    И ДанныеРегистра.ТипЗаписи В (
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Перемещение),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПеремещениеОбособленно))
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ИсправлениеПеремещенияПрошлогоПериода)
		|				
		|			КОГДА ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				И НЕ ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
		|			 ИЛИ ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретения)
		|
		// Для сторно документа Приобретение товаров и услуг на фактуровку поставки тип записи устанавливается Дополнение,
		// т.к. для целей распределения партий нужно сформировать запись со стоимостью с учетом рассчитанных отклонений в стоимости.
		// Такие движения формируются в функции ТекстСторноДвиженияИсправленныхДокументовФактуровкиПоставок().
		// Условия заполнения типа записей Дополнение должно соответствовать условиям выборки данных в этой функции.
		|			КОГДА ДанныеРегистра.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
		|			 И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) = ТИП(Документ.ПриобретениеТоваровУслуг)
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)
		|
		|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ИсправлениеПрошлогоПериода) КОНЕЦ
		// Исправление текущего периода
		|		ИНАЧЕ
		|			ВЫБОР КОГДА  ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение) 
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение)
		// Для возврата товаров от клиента оставляем текущие типы записей
		|			КОГДА ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
		|			 И ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Перемещение)
		|				ТОГДА ДанныеРегистра.ТипЗаписи
		|
		|			КОГДА ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВозвратПрошлыхПериодов)
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноВозвратаПрошлыхПериодов)
		// Приходные движения
		|			КОГДА ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия) 
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретения)
		|
		|			КОГДА ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретения)
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
		|
		|			КОГДА ДанныеРегистра.ТипЗаписи В (
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыБезПартии),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретенияПрошлогоПериода),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаСтоимости),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВключениеАкцизаВСтоимость))
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноДопРасходов)
		|
		|			КОГДА ДанныеРегистра.ТипЗаписи В (
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Перемещение),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПеремещениеОбособленно))
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ИсправлениеТекущегоПериода)
		|
		|			КОГДА ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
		|			 И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноПотребления)
		|
		// Расходные движения
		// Для реализации товаров между организациями и реализации без перехода права собственности отдельное правило
		|			КОГДА ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
		|			 И ДанныеРегистра.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию),
		|		 										       ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности))
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноПотребления)
		|
		|			КОГДА ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
		|			 И ДанныеРегистра.КорРазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноПотребления)
		|
		|			КОГДА ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
		|			 И ДанныеРегистра.КорРазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Сторно)
		|
		|			КОГДА ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СписаниеНаДругиеПартии)
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноСписанияНаДругиеПартии)
		|
		|			КОГДА ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СписаниеОстаткаБезПартии)
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноСписанияОстаткаБезПартии)
		|
		|			КОГДА ДанныеРегистра.ТипЗаписи В (
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Сторно),
		|					ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноВозвратНаДругойСклад))
		|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноВозврата)
		|
		|			КОГДА ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВозвратНаДругойСклад)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СторноВозвратНаДругойСклад)
		|
		|			ИНАЧЕ ДанныеРегистра.ТипЗаписи КОНЕЦ
		|		КОНЕЦ) КАК ТипЗаписи,
		// При сторнировании текущего периода:
		//	У приходного движения ДокументИсточник заполняется сторнируемым документом, кроме сторно типа записи "Потребление" (сторно фактуровки поставки).
		//	Для сторно фактуровки поставки поле ДокументИсточник заполняется ссылкой на сторнирумый документ фактуровки.
		//	У расходного движения ДокументИсточник заполняется:
		//	- ссылкой на сторнируемый документ для выбытия товаров (если нет корреспондирующего приходного движения - не заполнено поле "КорРазделУчета"
		//	- ссылкой на исправительный документ для перемещений товаров (если есть корреспондирующее приходное движение) 
		|	(ВЫБОР
		// Для реализации товаров между организациями и реализации без перехода права собственности отдельное правило
		|		КОГДА ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
		|		 И ДанныеРегистра.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию),
		|		 										   ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности))
		|		 И ДанныеРегистра.Период >= НАЧАЛОПЕРИОДА(ДокументыСторно.Дата, МЕСЯЦ)
		|			ТОГДА ДанныеРегистра.Регистратор
		|
		|		КОГДА ДанныеРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)
		|		 И ДанныеРегистра.Период >= НАЧАЛОПЕРИОДА(ДокументыСторно.Дата, МЕСЯЦ)
		|		 И ДанныеРегистра.КорРазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
		|		 И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА ДокументыСторно.Ссылка
		// При сторнировании доп расходов с указанной партией ДокументИсточник заполняется партией (из исходного документа-источника)
		// Это необходимо для построения цепочки ДопРасходыСПартией <- СторноДопРасходов
		// При исправлении записей корректировки стоимости ДокументИсточник заполняется исходным документом-источником.
		|		КОГДА ДанныеРегистра.ТипЗаписи В (
		|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыСПартией),
		|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ДопРасходыБезПартии),
		|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаПриобретенияПрошлогоПериода),
		|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.КорректировкаСтоимости),
		|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ВключениеАкцизаВСтоимость))
		|			ТОГДА ДанныеРегистра.ДокументИсточник
		|
		|		ИНАЧЕ ДанныеРегистра.Регистратор КОНЕЦ) КАК ДокументИсточник,
		|	ИСТИНА КАК Сторно,
		// Поля, не заполняемые при учете по Среднескользящей
		// Аналогичные условия использются в ТекстСторнирующегоЗапроса() этого модуля
		|	ВЫБОР КОГДА ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
		|		И ДанныеРегистра.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
		//++ НЕ УТ
		|			И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Партия) <> ТИП(Справочник.ПартииПроизводства)
		//-- НЕ УТ
		|		ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ДанныеРегистра.Партия
		|	КОНЕЦ КАК Партия,
		|	ВЫБОР КОГДА ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
		|		И ДанныеРегистра.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
		//++ НЕ УТ
		|			И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Партия) <> ТИП(Справочник.ПартииПроизводства)
		//-- НЕ УТ
		|		ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ДанныеРегистра.АналитикаУчетаПартий
		|	КОНЕЦ КАК АналитикаУчетаПартий,
		|	ВЫБОР КОГДА ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
		//++ НЕ УТ
		|			И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Партия) <> ТИП(Справочник.ПартииПроизводства)
		//-- НЕ УТ
		|		ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ДанныеРегистра.АналитикаФинансовогоУчета
		|	КОНЕЦ КАК АналитикаФинансовогоУчета,
		|	ВЫБОР КОГДА ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
		//++ НЕ УТ
		|			И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Партия) <> ТИП(Справочник.ПартииПроизводства)
		//-- НЕ УТ
		|		ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ДанныеРегистра.ВидДеятельностиНДС
		|	КОНЕЦ КАК ВидДеятельностиНДС,
		|	ВЫБОР КОГДА ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
		|		ТОГДА ЛОЖЬ
		|		ИНАЧЕ ДанныеРегистра.РасчетПартий
		|	КОНЕЦ КАК РасчетПартий,
		|	ВЫБОР КОГДА ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
		|		ТОГДА ЛОЖЬ
		|		ИНАЧЕ ДанныеРегистра.РасчетСебестоимости
		|	КОНЕЦ КАК РасчетСебестоимости
		// Движения сторно используются при расчете отклонений для метода оценки Среднескользящая в общем модуле РасчетСебестоимостиПроведениеДокументов.
		|ПОМЕСТИТЬ ВТДанныеСторнированияСебестоимость
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		#ИмяДокумента КАК ДокументыСторно
		|	ПО
		|		ДанныеРегистра.Регистратор = ДокументыСторно.СторнируемыйДокумент
		|		И ДокументыСторно.Ссылка В (&Ссылка)
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТПараметрыПартионногоУчетаДляПроведения КАК УчетнаяПолитика
		|		ПО НАЧАЛОПЕРИОДА(ДокументыСторно.Дата, МЕСЯЦ) = УчетнаяПолитика.Период
		|		И ДанныеРегистра.Организация = УчетнаяПолитика.Организация
		|ГДЕ
		|	НЕ ДанныеРегистра.Сторно 
		|	И (ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
		|		ИЛИ (НЕ ДанныеРегистра.РасчетПартий
		|		   И НЕ ДанныеРегистра.РасчетСебестоимости))
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеРегистра.Период,
		|	&ПоляВыборки,
		|	ДанныеРегистра.ТипЗаписи,
		|	ДанныеРегистра.ДокументИсточник,
		|	ДанныеРегистра.Сторно,
		|	ДанныеРегистра.Партия,
		|	ДанныеРегистра.АналитикаУчетаПартий,
		|	ДанныеРегистра.АналитикаФинансовогоУчета,
		|	ДанныеРегистра.ВидДеятельностиНДС,
		|	ДанныеРегистра.РасчетПартий,
		|	ДанныеРегистра.РасчетСебестоимости
		|ИЗ
		|	ВТДанныеСторнированияСебестоимость КАК ДанныеРегистра
		|";
		
		МассивПолейВыборки = Новый Массив; // суммовые поля без изменения знака 
		МассивПолейВТ = Новый Массив; // суммовые поля со знаком минус
		
		Исключения = Новый Массив;
		Исключения.Добавить("Активность");
		Исключения.Добавить("НомерЗаписи");
		Исключения.Добавить("Регистратор");
		Исключения.Добавить("Период");
		Исключения.Добавить("Сторно");
		Исключения.Добавить("ТипЗаписи");
		Исключения.Добавить("ДокументИсточник");
		Исключения.Добавить("Партия");
		Исключения.Добавить("АналитикаУчетаПартий");
		Исключения.Добавить("АналитикаФинансовогоУчета");
		Исключения.Добавить("ВидДеятельностиНДС");
		Исключения.Добавить("РасчетПартий");
		Исключения.Добавить("РасчетСебестоимости");
	
		ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.СтандартныеРеквизиты, 
			МассивПолейВыборки,
			Исключения);
		ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.СтандартныеРеквизиты, 
			МассивПолейВТ,
			Исключения);
			
		ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.Измерения, 
			МассивПолейВыборки,
			Исключения);
		ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.Измерения, 
			МассивПолейВТ,
			Исключения);
			
		ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.Ресурсы, 
			МассивПолейВыборки,
			Исключения);
		ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.Ресурсы, 
			МассивПолейВТ,
			Исключения,
			Истина);
			
		ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.Реквизиты, 
			МассивПолейВыборки,
			Исключения);
		ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.Реквизиты, 
			МассивПолейВТ,
			Исключения,
			Истина);
			
		ТекстПоляВыборки = СтрСоединить(МассивПолейВыборки,"," + Символы.ПС + "	");
		ТекстПоляВТ = СтрСоединить(МассивПолейВТ,"," + Символы.ПС + "	");
		
		ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "&ПоляВыборки", ТекстПоляВыборки);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляВТ", ТекстПоляВТ);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяДокумента", МетаданныеДокумента.ПолноеИмя());
		
		ТекстыЗапросов.Вставить(МетаданныеРегистра.ПолноеИмя(), ТекстЗапроса);
		
	КонецЕсли;
	
	МетаданныеРегистра = Метаданные.РегистрыНакопления.ПартииПрочихРасходов;
	Если ДвиженияДокумента.Содержит(МетаданныеРегистра) Тогда
		ШаблонЗапроса = "
		|ВЫБРАТЬ
		|	ДокументыСторно.Ссылка  КАК Регистратор,
		|	ДокументыСторно.Дата КАК Период,
		|	&ПоляВыборки,
		// Суммы НДС при сторнировании не заполняем, т.к. распределение сторнированных сумм НДС выполняется в механизме учета НДС
		// по движениям регистра "НДС предъявленный". 
		|	0 КАК НДСРегл,
		|	0 КАК НДСУпр,
		|	ИСТИНА КАК Сторно
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов КАК ДанныеРегистра
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		#ИмяДокумента КАК ДокументыСторно
		|	ПО
		|		ДанныеРегистра.Регистратор = ДокументыСторно.СторнируемыйДокумент
		|		И ДокументыСторно.Ссылка В (&Ссылка)
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТПараметрыПартионногоУчетаДляПроведения КАК УчетнаяПолитика
		|		ПО НАЧАЛОПЕРИОДА(ДокументыСторно.Дата, МЕСЯЦ) = УчетнаяПолитика.Период
		|		И ДанныеРегистра.Организация = УчетнаяПолитика.Организация
		|ГДЕ
		|	НЕ ДанныеРегистра.Сторно 
		|	И (ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
		|		ИЛИ (НЕ ДанныеРегистра.РасчетПартий
		|		   И НЕ ДанныеРегистра.РасчетСебестоимости))
		|";
		
		МассивПолейВыборки = Новый Массив;
		
		Исключения = Новый Массив;
		Исключения.Добавить("Регистратор");
		Исключения.Добавить("Период");
		Исключения.Добавить("Сторно");
		Исключения.Добавить("НомерСтроки");
		Исключения.Добавить("Активность");
		Исключения.Добавить("НДСРегл");
		Исключения.Добавить("НДСУпр");
	
		ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.СтандартныеРеквизиты, 
			МассивПолейВыборки,
			Исключения);
		ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.Измерения, 
			МассивПолейВыборки,
			Исключения);
		ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.Ресурсы, 
			МассивПолейВыборки,
			Исключения,
			Истина);
		ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.Реквизиты, 
			МассивПолейВыборки,
			Исключения,
			Истина);
			
		ТекстПоляВыборки= СтрСоединить(МассивПолейВыборки,"," + Символы.ПС);
		
		ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "&ПоляВыборки", ТекстПоляВыборки);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяРегистра", МетаданныеРегистра.ПолноеИмя());
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяДокумента", МетаданныеДокумента.ПолноеИмя());
		
		ТекстыЗапросов.Вставить(МетаданныеРегистра.ПолноеИмя(), ТекстЗапроса);
	КонецЕсли;
	
	МетаданныеРегистра = Метаданные.РегистрыНакопления.ОтклоненияВСтоимостиТоваров;
	Если ДвиженияДокумента.Содержит(МетаданныеРегистра) Тогда
		ТекстыЗапросов.Вставить(МетаданныеРегистра.ПолноеИмя(),
			ТекстСторнирующегоЗапроса(
				МетаданныеРегистра,
				МетаданныеДокумента));
	КонецЕсли;
	
	Возврат ТекстыЗапросов;
	
КонецФункции

// Дополняет текст запроса механизма проверки даты запрета по таблице изменений.
// 
// Параметры:
// 	Запрос - Запрос - используется для установки параметров запроса.
// 
// Возвращаемое значение:
//	Соответствие - соответствие имен таблиц изменения регистров и текстов запросов.
//	
Функция ТекстыЗапросовКонтрольДатыЗапретаПоТаблицеИзменений(Запрос) Экспорт

	СоответствиеТекстовЗапросов = Новый Соответствие();
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийПартииПрочихРасходов", 
		РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаКонтрольДатыЗапрета(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийСебестоимостьТоваров", 
		РегистрыНакопления.СебестоимостьТоваров.ТекстЗапросаКонтрольДатыЗапрета(Запрос));
	
	Возврат СоответствиеТекстовЗапросов;
	
КонецФункции

// Процедура формирования движений по подчиненным партионного учета.
//
// Параметры:
//   ТаблицыДляДвижений - Структура - таблицы данных документа
//   Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//   Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "ПартииПрочихРасходов");
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "СебестоимостьТоваров");
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "ОтклоненияВСтоимостиТоваров");
	
	//++ Локализация
	// Отключим удаление движений у регистров ПУ 2.1 для некоторых типов документов
	ТипДокумента = ТипЗнч(Движения[0].Отбор.Регистратор.Значение);
	ЭтоОфлайн = ТипДокумента <> Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
		И ТипДокумента <> Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов")
		И ТипДокумента <> Тип("ДокументСсылка.ВнутреннееПотребление")
		И ТипДокумента <> Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
		И ТипДокумента <> Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
		//++ НЕ УТ
		И ТипДокумента <> Тип("ДокументСсылка.ДвижениеПродукцииИМатериалов")
		И ТипДокумента <> Тип("ДокументСсылка.ПередачаМатериаловВПроизводство")
		И ТипДокумента <> Тип("ДокументСсылка.ВыпускПродукции")
		И ТипДокумента <> Тип("ДокументСсылка.ОтчетОператораСистемыПлатон")
		//-- НЕ УТ
		И ТипДокумента <> Тип("ДокументСсылка.ЗаписьКнигиПокупок")
		И ТипДокумента <> Тип("ДокументСсылка.ЗаписьКнигиПродаж")
		И ТипДокумента <> Тип("ДокументСсылка.КорректировкаРеализации")
		И ТипДокумента <> Тип("ДокументСсылка.КорректировкаПриобретения")
		И ТипДокумента <> Тип("ДокументСсылка.СписаниеРасхожденийПоступлениеПриобретение");
	
	//++ НЕ УТ
	#Область ПартииНезавершенногоПроизводства
	
	ИмяТаблицы = "Таблица" + "ПартииНезавершенногоПроизводства";
	
	Если ТаблицыДляДвижений.Свойство(ИмяТаблицы)
		И ЗначениеЗаполнено(ТаблицыДляДвижений[ИмяТаблицы]) Тогда
		
		Движения.ПартииНезавершенногоПроизводства.Записывать = Истина;
		Движения.ПартииНезавершенногоПроизводства.Загрузить(
			ТаблицыДляДвижений[ИмяТаблицы]);
		
	ИначеЕсли Движения.Найти("ПартииНезавершенногоПроизводства") <> Неопределено
		И Движения.ПартииНезавершенногоПроизводства.Записывать И ЭтоОфлайн Тогда
		
		Движения.ПартииНезавершенногоПроизводства.Записывать = Ложь;
		
	КонецЕсли;
	
	#КонецОбласти
	//-- НЕ УТ
	
	#Область ПартииПроизводственныхЗатрат
	
	ИмяТаблицы = "Таблица" + "ПартииПроизводственныхЗатрат";
	
	Если ТаблицыДляДвижений.Свойство(ИмяТаблицы)
		И ЗначениеЗаполнено(ТаблицыДляДвижений[ИмяТаблицы]) Тогда
		
		Движения.ПартииПроизводственныхЗатрат.Записывать = Истина;
		Движения.ПартииПроизводственныхЗатрат.Загрузить(ТаблицыДляДвижений[ИмяТаблицы]);
		
	ИначеЕсли Движения.Найти("ПартииПроизводственныхЗатрат") <> Неопределено
		И Движения.ПартииПроизводственныхЗатрат.Записывать И ЭтоОфлайн
		И Не (Движения.Найти("МатериалыИРаботыВПроизводстве") <> Неопределено
			И Движения.МатериалыИРаботыВПроизводстве.Количество() = 0
			И Движения.МатериалыИРаботыВПроизводстве.Записывать) Тогда
		
		Движения.ПартииПроизводственныхЗатрат.Записывать = Ложь;
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ПартииРасходовНаСебестоимостьТоваров
	
	ИмяТаблицы = "Таблица" + "ПартииРасходовНаСебестоимостьТоваров";
	
	Если ТаблицыДляДвижений.Свойство(ИмяТаблицы)
		И ЗначениеЗаполнено(ТаблицыДляДвижений[ИмяТаблицы]) Тогда
		
		Движения.ПартииРасходовНаСебестоимостьТоваров.Записывать = Истина;
		Движения.ПартииРасходовНаСебестоимостьТоваров.Загрузить(
			ТаблицыДляДвижений[ИмяТаблицы]);
		
	ИначеЕсли Движения.Найти("ПартииРасходовНаСебестоимостьТоваров") <> Неопределено
		И Движения.ПартииРасходовНаСебестоимостьТоваров.Записывать И ЭтоОфлайн Тогда
		
		Движения.ПартииРасходовНаСебестоимостьТоваров.Записывать = Ложь;
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ПартииТоваровОрганизаций
	
	ИмяТаблицы = "Таблица" + "ПартииТоваровОрганизаций";
	
	Если ТаблицыДляДвижений.Свойство(ИмяТаблицы)
		И ЗначениеЗаполнено(ТаблицыДляДвижений[ИмяТаблицы]) Тогда
		
		Движения.ПартииТоваровОрганизаций.Записывать = Истина;
		Движения.ПартииТоваровОрганизаций.Загрузить(ТаблицыДляДвижений[ИмяТаблицы]);
		
	ИначеЕсли Движения.Найти("ПартииТоваровОрганизаций") <> Неопределено
		И Движения.ПартииТоваровОрганизаций.Записывать И ЭтоОфлайн Тогда
		
		Движения.ПартииТоваровОрганизаций.Записывать = Ложь;
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ПартииТоваровПереданныеНаКомиссию
	
	ИмяТаблицы = "Таблица" + "ПартииТоваровПереданныеНаКомиссию";
	
	Если ТаблицыДляДвижений.Свойство(ИмяТаблицы)
		И ЗначениеЗаполнено(ТаблицыДляДвижений[ИмяТаблицы]) Тогда
		
		Движения.ПартииТоваровПереданныеНаКомиссию.Записывать = Истина;
		Движения.ПартииТоваровПереданныеНаКомиссию.Загрузить(
			ТаблицыДляДвижений[ИмяТаблицы]);
		
	ИначеЕсли Движения.Найти("ПартииТоваровПереданныеНаКомиссию") <> Неопределено
		И Движения.ПартииТоваровПереданныеНаКомиссию.Записывать И ЭтоОфлайн Тогда
		
		Движения.ПартииТоваровПереданныеНаКомиссию.Записывать = Ложь;
		
	КонецЕсли;
	
	#КонецОбласти
	
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "ДетализацияПартийТоваровДляНДСиУСН");
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "ДетализацияПартийТоваровДляНДСиУСН2_4");
	//-- Локализация
	
КонецПроцедуры

// Возникает перед выполнением записи регистров документа.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц,
//      используемый для хранения таблиц контроля изменений регистров
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура ПередЗаписьюДвиженийДокумента(Документ, МенеджерВременныхТаблиц, Отказ) Экспорт
	
	РегистрыНакопления.СебестоимостьТоваров.ИнициализироватьСохранениеДвиженийНабораЗаписейЗаПериод(
		Документ.Движения.СебестоимостьТоваров, Документ.ДополнительныеСвойства);
	
КонецПроцедуры

// Возникает после выполнения записи регистров документа.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц,
//      используемый для хранения таблиц контроля изменений регистров
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура ПослеЗаписиДвиженийДокумента(Документ, МенеджерВременныхТаблиц, Отказ) Экспорт
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

// Формирует тексты запросов для контроля изменений записанных движений регистров.
//
// Параметры:
//  Запрос - Запрос - запрос, хранящий параметры используемые в списке запросов
//  ТекстыЗапроса - СписокЗначений из Строка - список текстов запросов и их имен.
//  Документ - ДокументОбъект, Структура - записываемый документ, содержит:
//  	* ДополнительныеСвойства - Структура:
//  		** ПроведениеДокументов - Структура:
//  			*** СвойстваДокумента - ФиксированнаяСтруктура:
//  				**** ЭтоНовый - Булево
//  				**** РежимЗаписи - РежимЗаписиДокумента
//  				**** РежимПроведения - РежимПроведенияДокумента
//  				**** Проведен - Булево
//  			*** ТаблицыКонтроля - Структура:
//  				**** ВТСреднескользящаяСебестоимостьТоваров - Структура:
//  					***** ЕстьЗаписиВТаблице - Булево
//  					***** Параметры - Структура:
//
Процедура ИнициализироватьДанныеКонтроляИзменений(Запрос, ТекстыЗапроса, Документ) Экспорт
	
	Если РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров().НеКонтролироватьПоследовательностьПриСреднескользящей Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "ВТСреднескользящаяСебестоимостьТоваров") Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаОрганизации = Документ.ДополнительныеСвойства.ПроведениеДокументов.ТаблицыКонтроля.ВТСреднескользящаяСебестоимостьТоваров.Параметры.ОрганизацииСреднескользящая;
	МинимальныйПериод = Дата(1,1,1);
	
	Для Каждого ТекСтр Из ТаблицаОрганизации Цикл
		
		Если НЕ ЗначениеЗаполнено(МинимальныйПериод) Тогда
			МинимальныйПериод = ТекСтр.Период;
		Иначе
			МинимальныйПериод = Мин(ТекСтр.Период, МинимальныйПериод);
		КонецЕсли;
		
	КонецЦикла;
	
	РежимЗаписи = ?(Документ.ДополнительныеСвойства.Свойство("РежимЗаписи"), Документ.ДополнительныеСвойства.РежимЗаписи, Неопределено);
	Запрос.УстановитьПараметр("ОтменаПроведения", РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения);
	Запрос.УстановитьПараметр("МинимальныйПериодДвиженийСебестоимости", МинимальныйПериод);
	Запрос.УстановитьПараметр("Регистратор", Документ.Ссылка);
	
	ТекстЗапросаКонтроля =
	"ВЫБРАТЬ
	|	ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоОперацияПриход,
	|	ВЫРАЗИТЬ(Т.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры) КАК АналитикаУчетаНоменклатуры,
	|	Т.РазделУчета,
	|	Т.ВидЗапасов,
	|	Т.Организация КАК Организация,
	|	СУММА(Т.КоличествоИзменение) КАК КоличествоИзменение,
	|	Т.Период,
	|	МАКСИМУМ(Отбор.КонецПериода) КАК КонецПериода
	|ПОМЕСТИТЬ ВТОтборДвиженияСебестоимостьТоваровПредварительная
	|ИЗ
	|	ТаблицаИзмененийСебестоимостьТоваров КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСреднескользящаяСебестоимостьТоваров КАК Отбор
	|		ПО Т.Организация = Отбор.Организация
	|		И Т.Период = Отбор.Период
	|ГДЕ
	|	НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (
	|		ТИП(Документ.РегистраторРасчетаСебестоимости),
	|		ТИП(Документ.РасчетСебестоимостиТоваров))
	|	И НЕ Т.Сторно
	// При отмене проведения документа не контролируем движения Расход.
	|	И НЕ (&ОтменаПроведения И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫРАЗИТЬ(Т.АналитикаУчетаНоменклатуры КАК Справочник.КлючиАналитикиУчетаНоменклатуры),
	|	Т.РазделУчета,
	|	Т.ВидЗапасов,
	|	Т.Организация,
	|	Т.Период
	|ИМЕЮЩИЕ
	|	СУММА(Т.КоличествоИзменение) <> 0
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|";

	ТекстыЗапроса.Добавить(ТекстЗапросаКонтроля);
	
	ТекстЗапросаКонтроля =
	"ВЫБРАТЬ
	|	Т.ЭтоОперацияПриход,
	|	Т.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	Т.АналитикаУчетаНоменклатуры.МестоХранения КАК МестоХранения,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.РазделУчета,
	|	Т.ВидЗапасов,
	|	Т.Организация,
	|	Т.Период,
	|	Т.КонецПериода
	|ПОМЕСТИТЬ ВТОтборДвиженияСебестоимостьТоваров
	|ИЗ
	|	ВТОтборДвиженияСебестоимостьТоваровПредварительная КАК Т
	|ГДЕ
	|	(Т.ЭтоОперацияПриход И Т.КоличествоИзменение < 0)
	|	ИЛИ
	|	(НЕ Т.ЭтоОперацияПриход	И Т.КоличествоИзменение > 0)
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	Организация
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапросаКонтроля);
	
	ТекстЗапросаКонтроля =
	"ВЫБРАТЬ
	|	Отбор.ЭтоОперацияПриход КАК ЭтоОперацияПриход,
	|	Отбор.Организация КАК Организация,
	|	Отбор.Номенклатура КАК Номенклатура,
	|	Отбор.МестоХранения КАК МестоХранения,
	|	МАКСИМУМ(Т.Период) КАК Период
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборДвиженияСебестоимостьТоваров КАК Отбор
	|		ПО Т.Организация = Отбор.Организация
	|		И Т.АналитикаУчетаНоменклатуры = Отбор.АналитикаУчетаНоменклатуры
	|		И Т.РазделУчета = Отбор.РазделУчета
	|		И Т.ВидЗапасов = Отбор.ВидЗапасов
	|		И Т.Регистратор <> &Регистратор
	|		И ВЫБОР
	|			КОГДА ЛОЖЬ
	|				ТОГДА ЛОЖЬ
	//++ НЕ УТКА
	|			КОГДА ТИПЗНАЧЕНИЯ(&Регистратор) = ТИП(Документ.ЭтапПроизводства2_2)
	|				ТОГДА НАЧАЛОПЕРИОДА(Т.Период, ДЕНЬ) > Отбор.Период
	//-- НЕ УТКА
	|			ИНАЧЕ ((Отбор.ЭтоОперацияПриход И Т.Период > Отбор.Период)
	|					ИЛИ (НЕ Отбор.ЭтоОперацияПриход И НАЧАЛОПЕРИОДА(Т.Период, ДЕНЬ) > Отбор.Период))
	|		  КОНЕЦ
	|		И (Т.Период < Отбор.КонецПериода ИЛИ Отбор.КонецПериода = ДАТАВРЕМЯ(1,1,1))
	|		И ((Отбор.ЭтоОперацияПриход И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|			ИЛИ (НЕ Отбор.ЭтоОперацияПриход И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)))
	|		И НЕ ТИПЗНАЧЕНИЯ(Т.Регистратор) В (
	|			ТИП(Документ.КорректировкаРегистров),
	|			ТИП(Документ.РегистраторРасчетаСебестоимости),
	|			ТИП(Документ.РасчетСебестоимостиТоваров))
	|ГДЕ
	|	Т.Период > &МинимальныйПериодДвиженийСебестоимости
	|	И (Т.Количество <> 0
	|		ИЛИ Т.Стоимость <> 0
	|		ИЛИ Т.СтоимостьБезНДС <> 0
	|		ИЛИ Т.СтоимостьРегл <> 0
	|		ИЛИ Т.СтоимостьУпр <> 0
	|		ИЛИ Т.СтоимостьЗабалансовая <> 0
	|		ИЛИ Т.СтоимостьЗабалансоваяРегл <> 0)
	|СГРУППИРОВАТЬ ПО
	|	Отбор.ЭтоОперацияПриход,
	|	Отбор.Организация,
	|	Отбор.Номенклатура,
	|	Отбор.МестоХранения
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	ЭтоОперацияПриход,
	|	Номенклатура,
	|	МестоХранения
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапросаКонтроля, "ОшибкиСебестоимостьТоваров");
	
КонецПроцедуры

// Выводит сообщения пользователю при наличии ошибок контроля изменений записанных движений регистров.
//
// Параметры:
//  РезультатыКонтроля - Структура - таблицы с результатами контроля изменений
//  Документ - ДокументОбъект - записываемый документ
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура СообщитьОРезультатахКонтроляИзменений(РезультатыКонтроля, Документ, Отказ) Экспорт
	
	Если РезультатыКонтроля.Свойство("ОшибкиСебестоимостьТоваров")
	 И ЗначениеЗаполнено(РезультатыКонтроля.ОшибкиСебестоимостьТоваров) Тогда
		
		Для Каждого СтрокаОшибки Из РезультатыКонтроля.ОшибкиСебестоимостьТоваров Цикл
			
			Если СтрокаОшибки.ЭтоОперацияПриход Тогда
				
				ТекстСообщения = НСтр("ru = 'После текущего документа уже отражен расход по организации ""%1"", номенклатуре ""%2"" и месту хранения ""%3"".
					|Для уменьшения ранее отраженного прихода необходимо использовать документ ""Сторно"".';
					|en = 'After the current document, the expense for the ""%1"" company, the ""%2"" item, and the ""%3"" storage location was recorded.
					|To reduce the previously recorded income, use the ""Storno"" document.'");
				
			Иначе
				
				ТекстСообщения = НСтр("ru = 'Дата расхода ранее последнего прихода по организации ""%1"", номенклатуре ""%2"" и месту хранения ""%3"".
					|Среднескользящая стоимость не может быть определена. Установите дату документа позднее %4.
					|В некоторых видах документов дата расхода указывается в табличных частях документа.';
					|en = 'The expense date is earlier than the last receipt for the ""%1"" company, the ""%2"" item and the ""%3"" storage location.
					|Cannot identify the moving average cost. Set the document date later than %4.
					|In some types of documents, the expense date is specified in the document tables.'");
				
			КонецЕсли;
		
		    ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстСообщения,
				СтрокаОшибки.Организация,
				СтрокаОшибки.Номенклатура,
				СтрокаОшибки.МестоХранения,
				Формат(СтрокаОшибки.Период, "ДЛФ=DT"));
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Документ,,, Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Генерирует текст запроса для сторнирования записей по регистру накопления.
// Для числовых ресурсов и числовых реквизитов регистра формирует записи со знаком минус.
// 
// Параметры:
// 	МетаданныеРегистра - ОбъектМетаданныхРегистрНакопления - Метаданные регистра накопления для сторнирования.
// 	МетаданныеДокумента - ОбъектМетаданныхДокумент - Метаданные регистра накопления для сторнирования.
// 	
// Возвращаемое значение:
// 	Строка - Текст сторнирующего запроса.
Функция ТекстСторнирующегоЗапроса(МетаданныеРегистра, МетаданныеДокумента) Экспорт
	
	ШаблонЗапроса =
	"ВЫБРАТЬ
	|	ДокументыСторно.Ссылка  КАК Регистратор,
	|	ДокументыСторно.Дата КАК Период,
	|	&ПоляВыборки,
	|	ИСТИНА КАК Сторно
	|ИЗ
	|	&ИмяРегистра КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ИмяДокумента КАК ДокументыСторно
	|		ПО ДанныеРегистра.Регистратор = ДокументыСторно.СторнируемыйДокумент
	|		И ДокументыСторно.Ссылка В (&Ссылка)
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТПараметрыПартионногоУчетаДляПроведения КАК УчетнаяПолитика
	|		ПО НАЧАЛОПЕРИОДА(ДокументыСторно.Дата, МЕСЯЦ) = УчетнаяПолитика.Период
	|		И &Организация = УчетнаяПолитика.Организация
	|ГДЕ
	|	НЕ ДанныеРегистра.Сторно 
	|	И (&ДопУсловия)";
	
	МассивПолейВыборки = Новый Массив;
	
	Исключения = Новый Массив;
	Исключения.Добавить("Регистратор");
	Исключения.Добавить("Период");
	Исключения.Добавить("Сторно");
	Исключения.Добавить("НомерСтроки");
	Исключения.Добавить("Активность");
	
	ПереопределяемыеПоля = Новый Массив;
	ПереопределяемыеПоля.Добавить("РасчетПартий");
	ПереопределяемыеПоля.Добавить("РасчетСебестоимости");
	ПереопределяемыеПоля.Добавить("Партия");
	ПереопределяемыеПоля.Добавить("АналитикаУчетаПартий");
	ПереопределяемыеПоля.Добавить("АналитикаФинансовогоУчета");
	ПереопределяемыеПоля.Добавить("ВидДеятельностиНДС");

	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Исключения, ПереопределяемыеПоля, Истина);
	
	ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.СтандартныеРеквизиты, 
		МассивПолейВыборки,
		Исключения);
	ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.Измерения, 
		МассивПолейВыборки,
		Исключения);
	ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.Ресурсы, 
		МассивПолейВыборки,
		Исключения,
		Истина);
	ПроведениеДокументов.ДобавитьПоляВыборкиСторнирующегоЗапроса(МетаданныеРегистра.Реквизиты, 
		МассивПолейВыборки,
		Исключения,
		Истина);
		
	// Поля, заполняемые по условию
	ЕстьОрганизация = МетаданныеРегистра.Измерения.Найти("Организация") <> Неопределено;
	ЕстьРасчетПартий = МетаданныеРегистра.Реквизиты.Найти("РасчетПартий") <> Неопределено;
	ЕстьРасчетСебестоимости = МетаданныеРегистра.Реквизиты.Найти("РасчетСебестоимости") <> Неопределено;
	
	Если ЕстьОрганизация Тогда
		ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "&Организация", "ДанныеРегистра.Организация");
	Иначе
		ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "&Организация", "ДанныеРегистра.АналитикаУчетаПоПартнерам.Организация");
	КонецЕсли;
	
	// Аналогичные условия используются для движения по регистру себестоимости в ТекстыЗапросовСторнирования() этого модуля.
	Для Каждого ИмяПоля Из ПереопределяемыеПоля Цикл
		
		Если МетаданныеРегистра.Измерения.Найти(ИмяПоля) = Неопределено Тогда
			
			Если ИмяПоля = "РасчетПартий" И ЕстьРасчетПартий Тогда
				ШаблонПоля = 
					"	ВЫБОР КОГДА ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
					|		ТОГДА ЛОЖЬ
					|		ИНАЧЕ ДанныеРегистра.РасчетПартий
					|	КОНЕЦ КАК РасчетПартий";
			ИначеЕсли ИмяПоля = "РасчетСебестоимости" И ЕстьРасчетСебестоимости Тогда
				ШаблонПоля = 
					"	ВЫБОР КОГДА ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
					|		ТОГДА ЛОЖЬ
					|		ИНАЧЕ ДанныеРегистра.РасчетСебестоимости
					|	КОНЕЦ КАК РасчетСебестоимости";
			Иначе
				Продолжить;
			КонецЕсли;
			
		Иначе
			Если ИмяПоля = "Партия" Тогда
				ШаблонПоля = 
					"	ВЫБОР КОГДА ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
					|	 И ДанныеРегистра.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
					//++ НЕ УТ
					|	 И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Партия) <> ТИП(Справочник.ПартииПроизводства)
					//-- НЕ УТ
					|		ТОГДА НЕОПРЕДЕЛЕНО
					|		ИНАЧЕ ДанныеРегистра.Партия
					|	КОНЕЦ КАК Партия";
			ИначеЕсли ИмяПоля = "АналитикаУчетаПартий" Тогда
				ШаблонПоля = 
					"	ВЫБОР КОГДА ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
					|	 И ДанныеРегистра.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
					//++ НЕ УТ
					|	 И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Партия) <> ТИП(Справочник.ПартииПроизводства)
					//-- НЕ УТ
					|		ТОГДА НЕОПРЕДЕЛЕНО
					|		ИНАЧЕ ДанныеРегистра.АналитикаУчетаПартий
					|	КОНЕЦ КАК АналитикаУчетаПартий";
			ИначеЕсли ИмяПоля = "АналитикаФинансовогоУчета" Тогда
				ШаблонПоля = 
					"	ВЫБОР КОГДА ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
					//++ НЕ УТ
					|	 И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Партия) <> ТИП(Справочник.ПартииПроизводства)
					//-- НЕ УТ
					|		ТОГДА НЕОПРЕДЕЛЕНО
					|		ИНАЧЕ ДанныеРегистра.АналитикаФинансовогоУчета
					|	КОНЕЦ КАК АналитикаФинансовогоУчета";
			ИначеЕсли ИмяПоля = "ВидДеятельностиНДС" Тогда
				ШаблонПоля = 
					"	ВЫБОР КОГДА ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ)
					//++ НЕ УТ
					|	 И ТИПЗНАЧЕНИЯ(ДанныеРегистра.Партия) <> ТИП(Справочник.ПартииПроизводства)
					//-- НЕ УТ
					|		ТОГДА НЕОПРЕДЕЛЕНО
					|		ИНАЧЕ ДанныеРегистра.ВидДеятельностиНДС
					|	КОНЕЦ КАК ВидДеятельностиНДС";
			Иначе
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		МассивПолейВыборки.Добавить(ШаблонПоля);
		
	КонецЦикла;
	
	ТекстПоляВыборки = СтрСоединить(МассивПолейВыборки,"," + Символы.ПС);
	
	ТекстДопУсловий = "";
	
	Если ЕстьРасчетПартий И ЕстьРасчетСебестоимости Тогда
		ТекстДопУсловий = "ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ) ИЛИ (НЕ ДанныеРегистра.РасчетПартий И НЕ ДанныеРегистра.РасчетСебестоимости)";
	ИначеЕсли ЕстьРасчетПартий Тогда
		ТекстДопУсловий = "ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ) ИЛИ НЕ ДанныеРегистра.РасчетПартий";
	ИначеЕсли ЕстьРасчетСебестоимости Тогда
		ТекстДопУсловий = "ЕСТЬNULL(УчетнаяПолитика.Среднескользящая, ЛОЖЬ) ИЛИ НЕ ДанныеРегистра.РасчетСебестоимости";
	Иначе
		ТекстДопУсловий = "ИСТИНА";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "&ПоляВыборки", ТекстПоляВыборки);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяРегистра",  МетаданныеРегистра.ПолноеИмя());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяДокумента", МетаданныеДокумента.ПолноеИмя());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДопУсловия",	  ТекстДопУсловий);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаписьДвиженийВРегистры

// Формирует записи в регистр заданий к расчету себестоимости, при изменении записей в регистрах.
//
// Параметры:
//  Документ - ДокументОбъект - регистратор движений регистров
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц, содержащий таблицы изменений регистров.
//
Процедура ОтразитьЗаданияКРасчетуСебестоимости(Документ, МенеджерВременныхТаблиц) Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда // задания устанавливаются только в главном узле.
		Возврат;
	КонецЕсли;
	
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("ПартионныйУчетСервер.ОтразитьЗаданияКРасчетуСебестоимости");
	
	Запрос = Новый Запрос();
	СоответствиеЗапросов = СоответствиеЗапросовКонтрольнымРегистрам(Запрос);
	
	ШаблонЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Месяц        КАК Месяц,
	|	Таблица.Организация  КАК Организация,
	|	Таблица.Документ     КАК Документ,
	|	Таблица.ИзмененыДанныеДляПартионногоУчетаВерсии21 КАК ИзмененыДанныеДляПартионногоУчетаВерсии21
	|ИЗ
	|	&КоллекцияДанных КАК Таблица
	|;
	|";
		
	ТекстВложенногоЗапроса = "";
	ТекстЗапросаВременныхТаблиц = "";
	ИменаВременныхТаблиц = "";
		
	Для Каждого ЭлементСоответствия Из СоответствиеЗапросов Цикл
		ДополнитьТекстЗапросаЗаданий(ЭлементСоответствия.Ключ,
			ЭлементСоответствия.Значение,
			МенеджерВременныхТаблиц.Таблицы,
			ТекстВложенногоЗапроса,
			ТекстЗапросаВременныхТаблиц,
			ИменаВременныхТаблиц);
	КонецЦикла;
	
	ТекстУничтожитьВт = ЗакрытиеМесяцаСервер.ТестЗапросаУничтоженияВременныхТаблиц(ИменаВременныхТаблиц);
	
	Если ЗначениеЗаполнено(ТекстВложенногоЗапроса) Тогда // есть хотя бы один контрольный регистр.
		ТекстЗапроса = ТекстЗапросаВременныхТаблиц
			+ СтрЗаменить(ШаблонЗапроса, "&КоллекцияДанных", "(" + ТекстВложенногоЗапроса + ")")
			+ ТекстУничтожитьВт;
			
		Запрос.Текст = ТекстЗапроса;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
		
			Выборка = Результат.Выбрать();
			НомерЗадания = Константы.НомерЗаданияКРасчетуСебестоимости.Получить();
			Пока Выборка.Следующий() Цикл
				РегистрыСведений.ЗаданияКРасчетуСебестоимости.СоздатьЗаписьРегистра(
					Выборка.Месяц,
					Выборка.Документ,
					Выборка.Организация,
					НомерЗадания,
					Выборка.ИзмененыДанныеДляПартионногоУчетаВерсии21);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, 1);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеМетодыРегистраЗаданий

Функция СоответствиеЗапросовКонтрольнымРегистрам(Запрос)
	
	СоответствиеТекстовЗапросов = Новый Соответствие();
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийВыпускПродукции", ТекстЗапросаВыпускПродукции(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийВыручкаИСебестоимостьПродаж", ТекстЗапросаВыручкаИСебестоимостьПродаж(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийПрочаяВыручка", ТекстЗапросаПрочаяВыручка(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийМатериалыИРаботыВПроизводстве", ТекстЗапросаМатериалыИРаботыВПроизводстве(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийПартииНезавершенногоПроизводства", ТекстЗапросаПартииНезавершенногоПроизводства(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийПартииПроизводственныхЗатрат", ТекстЗапросаПартииПроизводственныхЗатрат(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийПартииПрочихРасходов", ТекстЗапросаПартииПрочихРасходов(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийПартииРасходовНаСебестоимостьТоваров", ТекстЗапросаПартииРасходовНаСебестоимостьТоваров(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийПартииТоваровОрганизаций", ТекстЗапросаПартииТоваровОрганизаций(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийПартииТоваровПереданныеНаКомиссию", ТекстЗапросаПартииТоваровПереданныеНаКомиссию(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийПрочиеРасходы", ТекстЗапросаПрочиеРасходы(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийПрочиеРасходыНезавершенногоПроизводства", ТекстЗапросаПрочиеРасходыНезавершенногоПроизводства(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийСебестоимостьТоваров", ТекстЗапросаСебестоимостьТоваров(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийОтклоненияВСтоимостиТоваров", ТекстЗапросаОтклоненияВСтоимостиТоваров(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийТоварыКОформлениюОтчетовКомитенту", ТекстЗапросаТоварыКОформлениюОтчетовКомитенту(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийТоварыОрганизаций", ТекстЗапросаТоварыОрганизаций(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийТоварыОрганизацийКПередаче", ТекстЗапросаТоварыОрганизацийКПередаче(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийТоварыПереданныеНаКомиссию", ТекстЗапросаТоварыПереданныеНаКомиссию(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийТрудозатратыНезавершенногоПроизводства", ТекстЗапросаТрудозатратыНезавершенногоПроизводства(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийДетализацияПартийТоваровДляНДСиУСН", ТекстЗапросаДетализацияПартийТоваровДляНДСиУСН(Запрос));
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийДетализацияПартийТоваровДляНДСиУСН2_4", ТекстЗапросаДетализацияПартийТоваровДляНДСиУСН2_4(Запрос));
	//++ НЕ УТ
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийРезервыПредстоящихРасходов", ТекстЗапросаРезервыПредстоящихРасходов(Запрос));
	//-- НЕ УТ
	
	Возврат СоответствиеТекстовЗапросов;

КонецФункции

// Дополняет текст запроса по формированию заданий.
// 
// Параметры:
// 	ИмяТаблицы - Строка - имя таблица для поиска в менеджере временных таблиц.
// 	ТекстыЗапросов - См. ИнициализироватьСтруктуруТекстовЗапросов
// 	Таблицы - МенеджерВременныхТаблиц - менеджер временных таблиц запроса.
// 	ТекстЗапроса - Строка - строка с текстом запроса.
// 	ТекстЗапросаВременныхТаблиц - Строка - строка с текстом запроса временных таблиц.
// 	ИменаВременныхТаблиц - Строка - массив имен создаваемых временных таблиц для последующего уничтожения.
//
Процедура ДополнитьТекстЗапросаЗаданий(ИмяТаблицы, ТекстыЗапросов, Таблицы, ТекстЗапроса, ТекстЗапросаВременныхТаблиц, ИменаВременныхТаблиц) Экспорт
	Если Таблицы.Найти(ИмяТаблицы) <> Неопределено Тогда
		Если ЗначениеЗаполнено(ТекстЗапроса) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + ТекстыЗапросов.ТекстЗапроса;
		ТекстЗапросаВременныхТаблиц = ТекстЗапросаВременныхТаблиц + ТекстыЗапросов.ТекстЗапросаВременныхТаблиц;
		Если ЗначениеЗаполнено(ТекстыЗапросов.ИменаВременныхТаблиц) Тогда
			ИменаВременныхТаблиц = ИменаВременныхТаблиц + ", " + ТекстыЗапросов.ИменаВременныхТаблиц;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Инициализирует структуру текстов запросов
// 
// Параметры:
// 	ТекстЗапроса - Строка - строка с текстом запроса.
// 	ТекстЗапросаВременныхТаблиц - Строка - строка с текстом запроса временных таблиц.
// 	ИменаВременныхТаблиц - Строка - массив имен создаваемых временных таблиц для последующего уничтожения.
// Возвращаемое значение:
// 	Структура - Описание:
// * ТекстЗапроса - Строка - строка с текстом запроса.
// * ТекстЗапросаВременныхТаблиц - Строка - строка с текстом запроса временных таблиц.
// * ИменаВременныхТаблиц - Строка - массив имен создаваемых временных таблиц для последующего уничтожения.
//
Функция ИнициализироватьСтруктуруТекстовЗапросов(ТекстЗапроса, ТекстЗапросаВременныхТаблиц = "", ИменаВременныхТаблиц = "") Экспорт
	СтруктураТекстовЗапросов = Новый Структура("ТекстЗапроса, ТекстЗапросаВременныхТаблиц, ИменаВременныхТаблиц",
		ТекстЗапроса,
		ТекстЗапросаВременныхТаблиц,
		ИменаВременныхТаблиц);
	Возврат СтруктураТекстовЗапросов
КонецФункции

Функция ТекстЗапросаЗаданийУниверсальный(ИмяТаблицыИзменений, ИзмененыДанныеДляПартионногоУчетаВерсии21 = Ложь, ЕстьКорОрганизация = Ложь) Экспорт
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК Месяц,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Регистратор КАК Документ,
	|	&ИзмененыДанныеДляПартионногоУчетаВерсии21 КАК ИзмененыДанныеДляПартионногоУчетаВерсии21
	|ИЗ
	|	&ИмяТаблицыИзменений КАК Таблица
	|";
	
	Если ЕстьКорОрганизация Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК Месяц,
		|	Таблица.КорОрганизация КАК Организация,
		|	Таблица.Регистратор КАК Документ,
		|	&ИзмененыДанныеДляПартионногоУчетаВерсии21 КАК ИзмененыДанныеДляПартионногоУчетаВерсии21
		|ИЗ
		|	&ИмяТаблицыИзменений КАК Таблица
		|ГДЕ
		|	Таблица.КорОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицыИзменений", ИмяТаблицыИзменений);
	Если ИзмененыДанныеДляПартионногоУчетаВерсии21 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИзмененыДанныеДляПартионногоУчетаВерсии21", "ИСТИНА");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИзмененыДанныеДляПартионногоУчетаВерсии21", "ЛОЖЬ");
	КонецЕсли;
	СтруктураТекстовЗапросов = ИнициализироватьСтруктуруТекстовЗапросов(ТекстЗапроса);
	Возврат СтруктураТекстовЗапросов
КонецФункции

#Область ТекстыЗапросовЗаданийКРасчетуСебестоимости

Функция ТекстЗапросаВыпускПродукции(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийВыпускПродукции");
	Возврат СтруктураТекстовЗапросов
КонецФункции

Функция ТекстЗапросаВыручкаИСебестоимостьПродаж(Запрос)
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(ТаблицаИзменений.Период, МЕСЯЦ) КАК Месяц,
	|	КлючиАналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ТаблицаИзменений.Регистратор КАК Документ,
	|	ЛОЖЬ КАК ИзмененыДанныеДляПартионногоУчетаВерсии21
	|ИЗ
	|	ТаблицаИзмененийВыручкаИСебестоимостьПродаж КАК ТаблицаИзменений
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиАналитикаУчетаПоПартнерам
	|	ПО
	|		ТаблицаИзменений.АналитикаУчетаПоПартнерам = КлючиАналитикаУчетаПоПартнерам.КлючАналитики";
	
	СтруктураТекстовЗапросов = ИнициализироватьСтруктуруТекстовЗапросов(ТекстЗапроса);
	Возврат СтруктураТекстовЗапросов
КонецФункции

Функция ТекстЗапросаПрочаяВыручка(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийПрочаяВыручка");
	Возврат СтруктураТекстовЗапросов
КонецФункции

Функция ТекстЗапросаМатериалыИРаботыВПроизводстве(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийМатериалыИРаботыВПроизводстве");
	Возврат СтруктураТекстовЗапросов
КонецФункции

Функция ТекстЗапросаПартииНезавершенногоПроизводства(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийПартииНезавершенногоПроизводства", Истина);
	Возврат СтруктураТекстовЗапросов
КонецФункции

Функция ТекстЗапросаПартииПроизводственныхЗатрат(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийПартииПроизводственныхЗатрат");
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаПартииПрочихРасходов(Запрос)
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК МЕСЯЦ,
	|	Таблица.Организация                  КАК Организация,
	|	Таблица.Регистратор                  КАК Документ,
	|	ЛОЖЬ                                 КАК ИзмененыДанныеДляПартионногоУчетаВерсии21
	|ИЗ
	|	ТаблицаИзмененийПартииПрочихРасходов КАК Таблица
	|ГДЕ
	|	Таблица.ВариантРаспределенияРасходовУпр = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	ИЛИ Таблица.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|";
	СтруктураТекстовЗапросов = ИнициализироватьСтруктуруТекстовЗапросов(ТекстЗапроса);
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаПартииРасходовНаСебестоимостьТоваров(Запрос)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК МЕСЯЦ,
	|	Таблица.Организация                  КАК Организация,
	|	Таблица.Регистратор                  КАК Документ,
	|	ИСТИНА                               КАК ИзмененыДанныеДляПартионногоУчетаВерсии21
	|ИЗ
	|	ТаблицаИзмененийПартииРасходовНаСебестоимостьТоваров КАК Таблица
	|";
	
	СтруктураТекстовЗапросов = ИнициализироватьСтруктуруТекстовЗапросов(ТекстЗапроса);
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаПартииТоваровОрганизаций(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийПартииТоваровОрганизаций", Истина);
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаПартииТоваровПереданныеНаКомиссию(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийПартииТоваровПереданныеНаКомиссию", Истина);
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаПрочиеРасходы(Запрос)
	
	ТекстЗапросаВременныхТаблиц = "";
	ТекстУничтожитьВт = "";
	
	//++ НЕ УТ
	ТекстЗапросаВременныхТаблиц = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(ДД.Период, Месяц)) КАК НачалоПериода,
	|	МАКСИМУМ(КОНЕЦПЕРИОДА(ДД.Период, Месяц)) КАК ОкончаниеПериода
	|ПОМЕСТИТЬ
	|	ВтДатыТаблицаИзмененийПрочиеРасходы
	|ИЗ
	|	ТаблицаИзмененийПрочиеРасходы КАК ДД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 1 Служебная
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Резервы.Организация КАК Организация,
	|	Резервы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Резервы.ОбъектУчетаРезервов КАК ОбъектУчетаРезервов
	|ПОМЕСТИТЬ ВтДвижениеРезервов
	|ИЗ
	|	РегистрНакопления.РезервыПредстоящихРасходов КАК Резервы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаИзмененийПрочиеРасходы КАК ТаблицаПрочиеРасходы
	|		ПО ТаблицаПрочиеРасходы.Организация = Резервы.Организация
	|			И ТаблицаПрочиеРасходы.НаправлениеДеятельности = Резервы.НаправлениеДеятельности,
	|	ВтДатыТаблицаИзмененийПрочиеРасходы КАК ВтДаты
	|ГДЕ
	|	Резервы.Период МЕЖДУ ВтДаты.НачалоПериода И ВтДаты.ОкончаниеПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 2 Служебная
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтатьиРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	СтатьиРасходов.АналитикаРасходов КАК АналитикаРасходов,
	|	СтатьиРасходов.ЛюбаяАналитикаРасходов КАК ЛюбаяАналитикаРасходов,
	|	ВтДвижениеРезервов.Организация КАК Организация,
	|	ВтДвижениеРезервов.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ СтатьиРезервов
	|ИЗ
	|	ВтДвижениеРезервов КАК ВтДвижениеРезервов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыУчетаРезервовПредстоящихРасходов.ИспользованиеРезерва КАК СтатьиРасходов
	|		ПО ВтДвижениеРезервов.ОбъектУчетаРезервов = СтатьиРасходов.Ссылка
	|
	|;";
	
	ТекстУничтожитьВт = "ВтДатыТаблицаИзмененийПрочиеРасходы, ВтДвижениеРезервов, СтатьиРезервов";
	//-- НЕ УТ
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ТаблицаИзменений.Период, МЕСЯЦ) КАК Месяц,
	|	ТаблицаИзменений.Организация                  КАК Организация,
	|	ТаблицаИзменений.Регистратор                  КАК Документ,
	|	ЛОЖЬ                                          КАК ИзмененыДанныеДляПартионногоУчетаВерсии21
	|ИЗ
	|	ТаблицаИзмененийПрочиеРасходы КАК ТаблицаИзменений
	//++ НЕ УТ
	|	ЛЕВОЕ СОЕДИНЕНИЕ СтатьиРезервов КАК СтатьиРезервов
	|		ПО ТаблицаИзменений.Организация = СтатьиРезервов.Организация
	|		И ТаблицаИзменений.НаправлениеДеятельности = СтатьиРезервов.НаправлениеДеятельности
	|		И ТаблицаИзменений.СтатьяРасходов = СтатьиРезервов.СтатьяРасходов
	|		И (ТаблицаИзменений.АналитикаРасходов = СтатьиРезервов.АналитикаРасходов ИЛИ СтатьиРезервов.ЛюбаяАналитикаРасходов)
	//-- НЕ УТ
	|ГДЕ
	|	(ТаблицаИзменений.СтатьяРасходов.ВариантРаспределенияРасходовУпр В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПродаж),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты))
	|		И (ТаблицаИзменений.Сумма <> 0 ИЛИ ТаблицаИзменений.СуммаБезНДС <> 0 ИЛИ ТаблицаИзменений.СуммаУпр <> 0)
	|	ИЛИ ТаблицаИзменений.СтатьяРасходов.ВариантРаспределенияРасходовРегл В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПродаж),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты))
	|		И (ТаблицаИзменений.СуммаРегл <> 0)
	//++ НЕ УТ
	|	ИЛИ ТаблицаИзменений.СтатьяРасходов.ВариантРаспределенияРасходовНУ В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПродаж),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаОбъектыВозникновенияЗатрат),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты))
	|		И (ТаблицаИзменений.СуммаРегл + ТаблицаИзменений.ПостояннаяРазница + ТаблицаИзменений.ВременнаяРазница <> 0)
	|	ИЛИ ТаблицаИзменений.СтатьяРасходов.ВидРасходов В (&НормируемыеРасходы)
	|	ИЛИ НЕ СтатьиРезервов.СтатьяРасходов ЕСТЬ NULL
	//-- НЕ УТ
	|	)
	|	И НЕ ТаблицаИзменений.Регистратор ССЫЛКА Документ.РаспределениеДоходовПоНаправлениямДеятельности
	|	И (НЕ ТаблицаИзменений.Регистратор ССЫЛКА Документ.РаспределениеПрочихЗатрат
	|		ИЛИ (ТаблицаИзменений.Регистратор ССЫЛКА Документ.РаспределениеПрочихЗатрат
	|			И ВЫРАЗИТЬ(ТаблицаИзменений.Регистратор КАК Документ.РаспределениеПрочихЗатрат).НазначениеНастройкиРаспределения = 
	|				ЗНАЧЕНИЕ(Перечисление.НазначениеПравилРаспределенияРасходов.РаспределениеСтатейРасходовПоЭтапамПроизводства)))
	|
	|СГРУППИРОВАТЬ ПО
	|
	|	НАЧАЛОПЕРИОДА(ТаблицаИзменений.Период, МЕСЯЦ),
	|	ТаблицаИзменений.Регистратор,
	|	ТаблицаИзменений.Период,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Подразделение,
	|	ТаблицаИзменений.СтатьяРасходов,
	|	ТаблицаИзменений.АналитикаРасходов,
	|	ТаблицаИзменений.НаправлениеДеятельности,
	|	ТаблицаИзменений.ХозяйственнаяОперация,
	|	ТаблицаИзменений.АналитикаУчетаНоменклатуры
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.Сумма) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.СуммаБезНДС) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.СуммаРегл) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.ПостояннаяРазница) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.ВременнаяРазница) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.СуммаНДД) <> 0
	|";
	
	//++ НЕ УТ
	Запрос.УстановитьПараметр("НормируемыеРасходы", Перечисления.ВидыРасходовНУ.НормируемыеРасходы());
	//-- НЕ УТ

	СтруктураТекстовЗапросов = ИнициализироватьСтруктуруТекстовЗапросов(ТекстЗапроса, ТекстЗапросаВременныхТаблиц, ТекстУничтожитьВт);
	Возврат СтруктураТекстовЗапросов
КонецФункции

Функция ТекстЗапросаПрочиеРасходыНезавершенногоПроизводства(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийПрочиеРасходыНезавершенногоПроизводства");
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаСебестоимостьТоваров(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийСебестоимостьТоваров", Ложь, Истина);
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаОтклоненияВСтоимостиТоваров(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийОтклоненияВСтоимостиТоваров", Ложь, Ложь);
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаТоварыКОформлениюОтчетовКомитенту(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийТоварыКОформлениюОтчетовКомитенту", Истина);
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаТоварыОрганизаций(Запрос)
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК Месяц,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Регистратор КАК Документ,
	|	(ВЫБОР
	|		КОГДА ЛОЖЬ ТОГДА ЛОЖЬ // Для удобства расстановки тегов.
	//++ Локализация
	|		КОГДА ТИПЗНАЧЕНИЯ(Таблица.Регистратор) = ТИП(Документ.УведомлениеОВвозеПрослеживаемыхТоваров)
	|			ТОГДА ЛОЖЬ
	//-- Локализация
	|		ИНАЧЕ ИСТИНА КОНЕЦ) КАК ИзмененыДанныеДляПартионногоУчетаВерсии21
	|ИЗ
	|	ТаблицаИзмененийТоварыОрганизаций КАК Таблица
	|";
	СтруктураТекстовЗапросов = ИнициализироватьСтруктуруТекстовЗапросов(ТекстЗапроса);
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаТоварыОрганизацийКПередаче(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийТоварыОрганизацийКПередаче", Истина);
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаТоварыПереданныеНаКомиссию(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийТоварыПереданныеНаКомиссию", Истина);
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаТрудозатратыНезавершенногоПроизводства(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийТрудозатратыНезавершенногоПроизводства");
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаДетализацияПартийТоваровДляНДСиУСН(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийДетализацияПартийТоваровДляНДСиУСН");
	Возврат СтруктураТекстовЗапросов;
КонецФункции

Функция ТекстЗапросаДетализацияПартийТоваровДляНДСиУСН2_4(Запрос)
	СтруктураТекстовЗапросов = ТекстЗапросаЗаданийУниверсальный("ТаблицаИзмененийДетализацияПартийТоваровДляНДСиУСН2_4");
	Возврат СтруктураТекстовЗапросов;
КонецФункции

//++ НЕ УТ

Функция ТекстЗапросаРезервыПредстоящихРасходов(Запрос)
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(ТаблицаИзменений.Период, МЕСЯЦ) КАК Месяц,
	|	ТаблицаИзменений.Организация                  КАК Организация,
	|	ТаблицаИзменений.Регистратор                  КАК Документ,
	|	ЛОЖЬ                                          КАК ИзмененыДанныеДляПартионногоУчетаВерсии21
	|ИЗ
	|	ТаблицаИзмененийРезервыПредстоящихРасходов КАК ТаблицаИзменений
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыУчетаРезервовПредстоящихРасходов.ИспользованиеРезерва КАК Таблица
	|			ПО ТаблицаИзменений.ОбъектУчетаРезервов = Таблица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов Статьи
	|			ПО Таблица.СтатьяРасходов = Статьи.Ссылка
	|ГДЕ
	|	(Статьи.ВариантРаспределенияРасходовУпр
	|		= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|	ИЛИ Статьи.ВариантРаспределенияРасходовРегл
	|		= ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты))
	|
	|СГРУППИРОВАТЬ ПО
	|
	|	НАЧАЛОПЕРИОДА(ТаблицаИзменений.Период, МЕСЯЦ),
	|	ТаблицаИзменений.Регистратор,
	|	ТаблицаИзменений.Период,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.ВидРезервов,
	|	ТаблицаИзменений.НаправлениеДеятельности,
	|	ТаблицаИзменений.ОбъектУчетаРезервов
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.СуммаУпр) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.СуммаРегл) <> 0
	|";
	СтруктураТекстовЗапросов = ИнициализироватьСтруктуруТекстовЗапросов(ТекстЗапроса);
	Возврат СтруктураТекстовЗапросов;
КонецФункции
//-- НЕ УТ

#КонецОбласти

#КонецОбласти

#Область Проверки

// Возвращает месяц первых движений по регистру себестоимости.
// Можно считать, что эта дата соответствует началу ведения учета в ИБ.
//
// Возвращаемое значение:
//	Дата - Период первых движений по регистру себестоимости.
//
Функция ПериодПервыхДвиженийРегистраСебестоимость() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Т.Период КАК Период
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = НачалоМесяца(Выборка.Период);
	Иначе
		Результат = Дата(1,1,1);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает месяц последних движений по регистру себестоимости.
// Можно считать, что эта дата соответствует последним операциям учета в ИБ.
//
// Возвращаемое значение:
//	Дата - Период последних движений по регистру себестоимости.
//
Функция ПериодПоследнихДвиженийРегистраСебестоимость() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Т.Период КАК Период
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = НачалоМесяца(Выборка.Период);
	Иначе
		Результат = Дата(1,1,1);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает месяц первых движений, сформированных в партионном учете версии 2.2
//
// Возвращаемое значение:
//	Дата - Период первых движений, сформированных в партионном учете версии 2.2
//
Функция ПериодПервыхДвиженийПартионныйУчетВерсии22() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВЫБОР
	|		КОГДА Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СлужебноеПереходНаПартионныйУчет22)
	|			ТОГДА ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(Т.Период, МЕСЯЦ), СЕКУНДА, 1)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(Т.Период, МЕСЯЦ)
	|	КОНЕЦ КАК Период
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	|ГДЕ
	|	Т.ВидДеятельностиНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Т.Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Период;
	Иначе
		Результат = Дата(1,1,1);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПересчетПрошлыхПериодов

// Создает задания к расчету себестоимости при включении/выключении учета себестоимости по назначениям или
// при изменении периода включения учета себестоимости по назначениям.
//
Процедура ЗапланироватьПересчетСебестоимостиПриИзмененииУчетаСебестоимостиПоНазначениям() Экспорт
	
	// Для каждой организации определяется период, начиная с которого необходимо пересчитать себестоимость.
	// 1) При включении учета по назначениям или изменении даты включения берется минимальный период из двух:
	//	- период первого движения себестоимости, в котором ранее уже учитывались назначения
	//	- период первого движения себестоимости, после даты включения учета по назначениям
	// 2) При выключении учета по назначениям берется период первых движений себестоимости,
	//	в котором ранее уже учитывались назначения
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ключи.Ссылка
	|ПОМЕСТИТЬ АналитикаСНазначениями
	|ИЗ
	|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|ГДЕ
	|	Ключи.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|;
	|ВЫБРАТЬ
	|	Периоды.Организация КАК Организация,
	|	МИНИМУМ(Периоды.Месяц) КАК Месяц
	|ПОМЕСТИТЬ ВТПересчетСебестоимости
	|ИЗ (
	// При включении учета по назначениям или изменении даты определяем минимальный период до периода включения,
	// в котором есть движения с назначениями.
	|	ВЫБРАТЬ
	|		СебестоимостьТоваров.Организация КАК Организация,
	|		МИНИМУМ(НАЧАЛОПЕРИОДА(СебестоимостьТоваров.Период, МЕСЯЦ)) КАК Месяц
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
	|			ПО АналитикаСНазначениями.Ссылка = СебестоимостьТоваров.АналитикаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|			ПО (ИСТИНА)
	|	ГДЕ
	|		Константы.УчитыватьСебестоимостьТоваровПоНазначениям
	|		И СебестоимостьТоваров.Период < НАЧАЛОПЕРИОДА(Константы.ДатаВключенияОбособленногоУчетаСебестоимостиПоНазначениям, МЕСЯЦ)	
	|	СГРУППИРОВАТЬ ПО
	|		СебестоимостьТоваров.Организация
	|
	// При включении учета по назначениям или изменении даты определяем минимальный период, в которым есть движения после периода включения.
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		СебестоимостьТоваров.Организация КАК Организация,
	|		МИНИМУМ(НАЧАЛОПЕРИОДА(СебестоимостьТоваров.Период, МЕСЯЦ)) КАК Месяц
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|			ПО (ИСТИНА)
	|	ГДЕ
	|		Константы.УчитыватьСебестоимостьТоваровПоНазначениям
	|		И СебестоимостьТоваров.Период >= НАЧАЛОПЕРИОДА(Константы.ДатаВключенияОбособленногоУчетаСебестоимостиПоНазначениям, МЕСЯЦ)
	|	СГРУППИРОВАТЬ ПО
	|		СебестоимостьТоваров.Организация
	|
	// При выключении учета по назначениям определяем минимальный период, в котором есть движения с назначениями.
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		СебестоимостьТоваров.Организация КАК Организация,
	|		МИНИМУМ(НАЧАЛОПЕРИОДА(СебестоимостьТоваров.Период, МЕСЯЦ)) КАК Месяц
	|	ИЗ
	|		РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаСНазначениями КАК АналитикаСНазначениями
	|			ПО АналитикаСНазначениями.Ссылка = СебестоимостьТоваров.АналитикаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|			ПО (ИСТИНА)
	|	ГДЕ
	|		НЕ Константы.УчитыватьСебестоимостьТоваровПоНазначениям
	|	СГРУППИРОВАТЬ ПО
	|		СебестоимостьТоваров.Организация
	| 	) КАК Периоды
	|СГРУППИРОВАТЬ ПО
	|	Периоды.Организация
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|";
	
	СформироватьЗаданияКПересчетуСебестоимости(Запрос);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц, "АналитикаСНазначениями, ВТПересчетСебестоимости");
	
КонецПроцедуры

Процедура СформироватьЗаданияКПересчетуСебестоимости(Запрос) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задания.Организация КАК Организация,
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(Задания.Месяц, МЕСЯЦ)) КАК Месяц
	|ПОМЕСТИТЬ ВТЗаданияКРасчету
	|ИЗ
	|	РегистрСведений.ЗаданияКРасчетуСебестоимости КАК Задания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПересчетСебестоимости КАК Пересчеты
	|		ПО Задания.Организация = Пересчеты.Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	Задания.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Пересчеты.Организация,
	|	Пересчеты.Месяц
	|ИЗ
	|	ВТПересчетСебестоимости КАК Пересчеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаданияКРасчету КАК Задания
	|		ПО Пересчеты.Организация = Задания.Организация
	|ГДЕ
	|	(Задания.Месяц ЕСТЬ NULL 
	|			ИЛИ Задания.Месяц > Пересчеты.Месяц)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	РегистрыСведений.ЗаданияКРасчетуСебестоимости.СоздатьЗаписиРегистраПоДаннымВыборки(Выборка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
