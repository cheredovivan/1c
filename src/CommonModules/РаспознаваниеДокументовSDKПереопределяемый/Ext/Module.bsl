#Область ПрограммныйИнтерфейс

// Установка режима работы SDK
//
// Параметры:
//   Параметры - Структура:
//   * ТолькоУниверсальныйФормат - Булево
//
Процедура ПараметрыSDK(Параметры) Экспорт
	
	Параметры.ТолькоУниверсальныйФормат = Ложь;
	
КонецПроцедуры

// Установка режима авторизации
// Для установки используйте один из вариантов:
//  - РаспознаваниеДокументовSDK.ПараметрыАвторизацииНеАвторизован
//  - РаспознаваниеДокументовSDK.ПараметрыАвторизацииДесктоп
//  - РаспознаваниеДокументовSDK.ПараметрыАвторизацииМобильноеПриложение
//
// Параметры:
//   Параметры - см. РаспознаваниеДокументовSDK.ПараметрыАвторизации
//
Процедура ПараметрыАвторизации(Параметры) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыАвторизации = РаспознаваниеДокументов.ТекущиеПараметрыАвторизации();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ЗначениеЗаполнено(ПараметрыАвторизации.ТокенДоступа) Тогда 
		Параметры = РаспознаваниеДокументовSDK.ПараметрыАвторизацииДесктоп(ПараметрыАвторизации.ТокенДоступа);
	Иначе
		Параметры = РаспознаваниеДокументовSDK.ПараметрыАвторизацииНеАвторизован();
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает дополнительные свойства области авторизации
// Для сбора окружения для техподдержки
//
// Параметры:
//   Результат - см. РаспознаваниеДокументовSDK.ОбластьАвторизации
//
Процедура ОбластьАвторизации(Результат) Экспорт
	
	Попытка
		АбонентЭтогоПриложения = ПрограммныйИнтерфейсСервиса.АбонентЭтогоПриложения().Наименование;
	Исключение
		АбонентЭтогоПриложения = "<Неопределено>";
	КонецПопытки;
	
	Попытка
		АдресПриложения = ПрограммныйИнтерфейсСервиса.СвойстваПриложения(РаботаВМоделиСервиса.ЗначениеРазделителяСеанса()).АдресПриложения;
	Исключение
		АдресПриложения = "";
	КонецПопытки;
	
	ИнформацияМоделиСервиса = Новый Соответствие;
	ИнформацияМоделиСервиса.Вставить("РазделениеВключено", РаботаВМоделиСервиса.РазделениеВключено());
	ИнформацияМоделиСервиса.Вставить("ДоступноИспользованиеРазделенныхДанных", РаботаВМоделиСервиса.ДоступноИспользованиеРазделенныхДанных());
	ИнформацияМоделиСервиса.Вставить("ЗначениеРазделителяСеанса", РаботаВМоделиСервиса.ЗначениеРазделителяСеанса());
	ИнформацияМоделиСервиса.Вставить("АбонентЭтогоПриложения", АбонентЭтогоПриложения);
	ИнформацияМоделиСервиса.Вставить("АдресПриложения", АдресПриложения);
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДанныеАутентификации <> Неопределено Тогда
		ЛогинПользователяИТС  = ДанныеАутентификации.Логин;
	Иначе
		ЛогинПользователяИТС = "<Неопределено>";
	КонецЕсли;
	
	ИнформацияИТС = Новый Соответствие;
	ИнформацияИТС.Вставить("ЛогинПользователяИТС", ЛогинПользователяИТС);
	
	Результат["ВерсияБиблиотеки"] = РаспознаваниеДокументов.ВерсияБиблиотеки();
	Результат["ИнформацияМоделиСервиса"] = ИнформацияМоделиСервиса;
	Результат["ИнформацияИТС"] = ИнформацияИТС;
	Результат["ИдентификаторИнформационнойБазы"] = СтандартныеПодсистемыСервер.ИдентификаторИнформационнойБазы();
	
КонецПроцедуры

#КонецОбласти
