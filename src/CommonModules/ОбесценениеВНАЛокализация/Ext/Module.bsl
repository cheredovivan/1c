////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Обесценение внеоборотных активов".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// 
// Параметры:
// 	МеханизмыДокумента - Массив -
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт

	//++ Локализация
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- Локализация
	
КонецПроцедуры

// 
// Параметры:
// 	Запрос - Запрос - 
// 	ТекстыЗапроса - СписокЗначений - 
// 	Регистры - Строка -
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
ТекстЗапроса = "";
	
	//++ Локализация

ТекстыОтражения = Новый Массив;
	
#Область ТекстНачислениеОбесцененияОСНМА

	ТекстЗапроса = "
	|ВЫБРАТЬ //// Начисление обесценения ВНА (Дт 91.02 :: Кт 02.04, 05.04)
	|
	|	Операция.Ссылка КАК Ссылка, 
	|	Операция.Дата КАК Период,
	|	ОбесценениеВНА.Организация КАК Организация,
	|	ОбесценениеВНА.ИдентификаторФинЗаписи КАК ИдентификаторСтроки,
	|
	|	ОбесценениеВНА.УбытокОтОбесцененияРегл КАК Сумма,
	|
	|	ОбесценениеВНА.УбытокОтОбесцененияУпр КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ПрочиеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	ПрочиеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ПрочиеРасходы.Подразделение КАК ПодразделениеДт,
	|	ПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ПрочиеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	ПрочиеРасходы.АналитикаРасходов КАК СубконтоДт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|
	|	ОбесценениеВНА.УбытокОтОбесцененияРегл КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ОбесценениеВНА) КАК ВидСчетаКт,
	|	ОбесценениеВНА.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ОбесценениеВНА.Подразделение КАК ПодразделениеКт,
	|	ОбесценениеВНА.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	ОбесценениеВНА.ВнеоборотныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|
	|	ОбесценениеВНА.УбытокОтОбесцененияРегл КАК СуммаВРКт,
	|
	|	ВЫБОР
	|		КОГДА ОбесценениеВНА.ВнеоборотныйАктив ССЫЛКА Справочник.ОбъектыЭксплуатации
	|			ТОГДА 
	|				""Начисление обесценения ОС""
	|			ИНАЧЕ 
	|				""Начисление обесценения НМА""
	|		КОНЕЦ КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОбесценениеВНА КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ОбесценениеВНА КАК ОбесценениеВНА
	|	ПО
	|		ОбесценениеВНА.Регистратор = Операция.Ссылка
	|		И ОбесценениеВНА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ОбесценениеВНА.НастройкаХозяйственнойОперации В 
	|			(ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОбесценениеОС),
	|			ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОбесценениеНМА))
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
	|	ПО
	|		ПрочиеРасходы.Регистратор = Операция.Ссылка
	|		И ПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И ПрочиеРасходы.ИдентификаторФинЗаписи = ОбесценениеВНА.ИдентификаторФинЗаписи
	|ГДЕ
	|	ОбесценениеВНА.УбытокОтОбесцененияРегл <> 0
	|		ИЛИ ОбесценениеВНА.УбытокОтОбесцененияУпр <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Основные средства.
	|ВЫБРАТЬ //// Начисление обесценения ВНА за счет резерва дооценки (Дт 83 :: Кт 02.04), Стоимость.
	|
	|	Операция.Ссылка КАК Ссылка, 
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл  КАК Сумма,
	|	СтоимостьОС.РезервПереоценкиСтоимости КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РезервДоОценкиВНА) КАК ВидСчетаДт,
	|	ОбесценениеВНА.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ОбесценениеВНА.Подразделение КАК ПодразделениеДт,
	|	ОбесценениеВНА.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ОбесценениеВНА.ВнеоборотныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ОбесценениеВНА) КАК ВидСчетаКт,
	|	ОбесценениеВНА.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ОбесценениеВНА.Подразделение КАК ПодразделениеКт,
	|	ОбесценениеВНА.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	ОбесценениеВНА.ВнеоборотныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл  КАК СуммаВРКт,
	|
	|	""Начисление обесценения ОС за счет резерва дооценки, стоимость"" КАК Содержание
	|
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОбесценениеВНА КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ОбесценениеВНА КАК ОбесценениеВНА
	|	ПО
	|		ОбесценениеВНА.Регистратор = Операция.Ссылка
	|		И ОбесценениеВНА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ОбесценениеВНА.НастройкаХозяйственнойОперации В 
	|			(ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОбесценениеОС),
	|			ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОбесценениеНМА))
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.СтоимостьОС КАК СтоимостьОС
	|	ПО
	|		СтоимостьОС.Регистратор = Операция.Ссылка
	|		И СтоимостьОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И СтоимостьОС.ИдентификаторФинЗаписи = ОбесценениеВНА.ИдентификаторФинЗаписи
	|
	|ГДЕ
	|	СтоимостьОС.РезервПереоценкиСтоимостиРегл <> 0
	|	ИЛИ СтоимостьОС.РезервПереоценкиСтоимости <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ //// Начисление обесценения ВНА за счет резерва дооценки (Дт 02.04 :: Кт 83), Амортизация.
	|
	|	Операция.Ссылка КАК Ссылка, 
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	АмортизацияОС.РезервПереоценкиАмортизацииРегл  КАК Сумма,
	|	АмортизацияОС.РезервПереоценкиАмортизации КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ОбесценениеВНА) КАК ВидСчетаДт,
	|	ОбесценениеВНА.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ОбесценениеВНА.Подразделение КАК ПодразделениеДт,
	|	ОбесценениеВНА.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ОбесценениеВНА.ВнеоборотныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|
	|	АмортизацияОС.РезервПереоценкиАмортизацииРегл  КАК СуммаВРДт,
	|
	|ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РезервДоОценкиВНА) КАК ВидСчетаКт,
	|	ОбесценениеВНА.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ОбесценениеВНА.Подразделение КАК ПодразделениеКт,
	|	ОбесценениеВНА.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	ОбесценениеВНА.ВнеоборотныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	АмортизацияОС.РезервПереоценкиАмортизацииРегл КАК СуммаВРКт,
	|
	|	""Начисление обесценения ОС за счет резерва дооценки, амортизация"" КАК Содержание
	|
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОбесценениеВНА КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ОбесценениеВНА КАК ОбесценениеВНА
	|	ПО
	|		ОбесценениеВНА.Регистратор = Операция.Ссылка
	|		И ОбесценениеВНА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ОбесценениеВНА.НастройкаХозяйственнойОперации В 
	|			(ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОбесценениеОС),
	|			ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОбесценениеНМА))
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.АмортизацияОС КАК АмортизацияОС
	|	ПО
	|		АмортизацияОС.Регистратор = Операция.Ссылка
	|		И АмортизацияОС.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И АмортизацияОС.ИдентификаторФинЗаписи = ОбесценениеВНА.ИдентификаторФинЗаписи
	|
	|ГДЕ
	|	АмортизацияОС.РезервПереоценкиАмортизацииРегл <> 0
	|	ИЛИ АмортизацияОС.РезервПереоценкиАмортизации <> 0
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Нематериальные активы.
	|ВЫБРАТЬ //// Начисление обесценения ВНА за счет резерва дооценки (Дт 83 :: Кт 05.04), Стоимость.
	|
	|	Операция.Ссылка КАК Ссылка, 
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл  КАК Сумма,
	|	СтоимостьНМА.РезервПереоценкиСтоимости КАК СуммаУУ,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РезервДоОценкиВНА) КАК ВидСчетаДт,
	|	ОбесценениеВНА.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ОбесценениеВНА.Подразделение КАК ПодразделениеДт,
	|	ОбесценениеВНА.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ОбесценениеВНА.ВнеоборотныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ОбесценениеВНА) КАК ВидСчетаКт,
	|	ОбесценениеВНА.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ОбесценениеВНА.Подразделение КАК ПодразделениеКт,
	|	ОбесценениеВНА.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	ОбесценениеВНА.ВнеоборотныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл  КАК СуммаВРКт,
	|
	|	""Начисление обесценения НМА  за счет резерва дооценки, стоимость""  КАК Содержание
	|
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОбесценениеВНА КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ОбесценениеВНА КАК ОбесценениеВНА
	|	ПО
	|		ОбесценениеВНА.Регистратор = Операция.Ссылка
	|		И ОбесценениеВНА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ОбесценениеВНА.НастройкаХозяйственнойОперации В 
	|			(ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОбесценениеОС),
	|			ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОбесценениеНМА))
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.СтоимостьНМА КАК СтоимостьНМА
	|	ПО
	|		СтоимостьНМА.Регистратор = Операция.Ссылка
	|		И СтоимостьНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И СтоимостьНМА.ИдентификаторФинЗаписи = ОбесценениеВНА.ИдентификаторФинЗаписи
	|
	|ГДЕ
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл <> 0
	|	ИЛИ СтоимостьНМА.РезервПереоценкиСтоимости <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ //// Начисление обесценения ВНА за счет резерва дооценки (Дт 05.04 :: Кт 83), Амортизация.
	|
	|	Операция.Ссылка КАК Ссылка, 
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	АмортизацияНМА.РезервПереоценкиАмортизацииРегл  КАК Сумма,
	|	АмортизацияНМА.РезервПереоценкиАмортизации КАК СуммаУУ,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ОбесценениеВНА) КАК ВидСчетаДт,
	|	ОбесценениеВНА.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ОбесценениеВНА.Подразделение КАК ПодразделениеДт,
	|	ОбесценениеВНА.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ОбесценениеВНА.ВнеоборотныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|
	|	АмортизацияНМА.РезервПереоценкиАмортизацииРегл  КАК СуммаВРДт,
	|
	|ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РезервДоОценкиВНА) КАК ВидСчетаКт,
	|	ОбесценениеВНА.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ОбесценениеВНА.Подразделение КАК ПодразделениеКт,
	|	ОбесценениеВНА.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	ОбесценениеВНА.ВнеоборотныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	АмортизацияНМА.РезервПереоценкиАмортизацииРегл КАК СуммаВРКт,
	|
	|	""Начисление обесценения НМА  за счет резерва дооценки, амортизация"" КАК Содержание
	|
	|ИЗ 
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОбесценениеВНА КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ОбесценениеВНА КАК ОбесценениеВНА
	|	ПО
	|		ОбесценениеВНА.Регистратор = Операция.Ссылка
	|		И ОбесценениеВНА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ОбесценениеВНА.НастройкаХозяйственнойОперации В 
	|			(ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОбесценениеОС),
	|			ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОбесценениеНМА))
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.АмортизацияНМА КАК АмортизацияНМА
	|	ПО
	|		АмортизацияНМА.Регистратор = Операция.Ссылка
	|		И АмортизацияНМА.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И АмортизацияНМА.ИдентификаторФинЗаписи = ОбесценениеВНА.ИдентификаторФинЗаписи
	|
	|ГДЕ
	|	АмортизацияНМА.РезервПереоценкиАмортизацииРегл <> 0
	|	ИЛИ АмортизацияНМА.РезервПереоценкиАмортизации <> 0
	|";
	
	ТекстыОтражения.Добавить(ТекстЗапроса);
	
#КонецОбласти

	ТекстЗапроса = СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат "";
	
КонецФункции

#КонецОбласти

//++ Локализация

#Область Локализация

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	СписокОрганизаций = ОбесценениеВНАСервер.ПолучитьОрганизацииДляОбесценения(Запрос.Параметры.Организация);
	
	ЗапросКоличестваЕГДС = Новый Запрос;
	ЗапросКоличестваЕГДС.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ОбесценениеВНАЕдиницыГенерирующиеДенежныеСредства.НомерСтроки) КАК КоличествоЕГДС
	|ИЗ
	|	Документ.ОбесценениеВНА.ЕдиницыГенерирующиеДенежныеСредства КАК ОбесценениеВНАЕдиницыГенерирующиеДенежныеСредства
	|ГДЕ
	|	ОбесценениеВНАЕдиницыГенерирующиеДенежныеСредства.Ссылка = &Ссылка";
	ЗапросКоличестваЕГДС.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	Результат = ЗапросКоличестваЕГДС.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		КоличестовЕГДСВДокументе = Выборка.КоличествоЕГДС;
	КонецЦикла;
	
	Если СписокОрганизаций.Количество() > 1
			И КоличестовЕГДСВДокументе > 0 Тогда
		// Для организаций с филиалами может требоваться проведение по нескольким организациям.
		Запрос.УстановитьПараметр("ГраницаДокумента", Новый Граница(Запрос.Параметры.Период, ВидГраницы.Исключая));
		Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);
		
		ТекстЗапроса =
		"// В составе ЕГДС, указанных в документе обесценения, могут быть активы, принадлежащие филиалам.
		|// В таком случае, проводки должны быть по нескольким организациям.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СоставЕГДССрезПоследних.Организация
		|ПОМЕСТИТЬ СписокОрганизацийСАктивамиИзЕГДС
		|ИЗ
		|	РегистрСведений.СоставЕГДС.СрезПоследних(&ГраницаДокумента, Организация В (&СписокОрганизаций)) КАК СоставЕГДССрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОбесценениеВНА.ЕдиницыГенерирующиеДенежныеСредства КАК
		|			ОбесценениеВНАЕдиницыГенерирующиеДенежныеСредства
		|		ПО ОбесценениеВНАЕдиницыГенерирующиеДенежныеСредства.ЕГДС = СоставЕГДССрезПоследних.ЕГДС
		|			И ОбесценениеВНАЕдиницыГенерирующиеДенежныеСредства.ССылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|// Организация документа и её филиалы, в которых числятся активы из ЕГДС документа обесценения.
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	&Организация КАК Организация,
		|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	СписокОрганизацийСАктивамиИзЕГДС.Организация,
		|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
		|ИЗ
		|	СписокОрганизацийСАктивамиИзЕГДС КАК СписокОрганизацийСАктивамиИзЕГДС";

	Иначе
		// Документ без ЕГДС, или организация не имеет филиалов, или сама является филиалом.
		ТекстЗапроса =
		"////////////////////////////////////////////////////////////////////////////////
		|// Организация документа.
		|ВЫБРАТЬ
		|	&Период КАК Период,
		|	&Организация КАК Организация,
		|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения";
		
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДобавитьЗаданияНаДоначислениеНалогаНаИмуществоПослеОбесценения(ДокументОбесценения) Экспорт 
	
	Запрос = Новый Запрос;
	
	// Документы обесценения могут давать движения по головной организации и филиалам.
	// Получим все ОС по организациям филиальной сети, попавшим под обесценение.
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбесценениеВНА.Организация КАК Организация,
	|	ВЫРАЗИТЬ(ОбесценениеВНА.ВнеоборотныйАктив КАК Справочник.ОбъектыЭксплуатации) КАК ВнеоборотныйАктив,
	|	КОНЕЦПЕРИОДА(ОбесценениеВНА.Период, МЕСЯЦ) КАК Период
	|ПОМЕСТИТЬ ВТ_ОбесценениеОС
	|ИЗ
	|	РегистрНакопления.ОбесценениеВНА КАК ОбесценениеВНА
	|ГДЕ
	|	ОбесценениеВНА.Регистратор = &Регистратор
	|	И ОбесценениеВНА.ВнеоборотныйАктив ССЫЛКА Справочник.ОбъектыЭксплуатации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ВнеоборотныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.ПериодРасчета КАК Месяц,
	|	РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.ОсновноеСредство КАК ОбъектУчета,
	|	РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.Организация КАК Организация,
	|	&Регистратор КАК Документ
	|ИЗ
	|	РегистрСведений.РасчетНалогаНаИмуществоПоСреднегодовойСтоимости КАК РасчетНалогаНаИмуществоПоСреднегодовойСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбесценениеОС КАК ВТ_ОбесценениеОС
	|		ПО РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.Организация = ВТ_ОбесценениеОС.Организация
	|			И РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.ОсновноеСредство = ВТ_ОбесценениеОС.ВнеоборотныйАктив
	|			И (КОНЕЦПЕРИОДА(РасчетНалогаНаИмуществоПоСреднегодовойСтоимости.ПериодРасчета, МЕСЯЦ) = ВТ_ОбесценениеОС.Период)
	|" ;
	
	Запрос.текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Регистратор", ДокументОбесценения);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		РегистрыСведений.ЗаданияКДоначислениюНалогаНаИмущество.СоздатьЗаписиРегистраПоДаннымВыборки(Выборка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

//-- Локализация

#КонецОбласти
