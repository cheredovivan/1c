// @strict-types

#Область ПрограммныйИнтерфейс

// Открыть форму сервиса маркетплейсы.
// 
// Параметры:
//  ПараметрыФормы - См. НовыеПараметрыФормыСервисаМаркетплейсы
//  ДополнительныеПараметры - Неопределено - Дополнительные параметры,
//                          - См. НовыеДополнительныеПараметрыФормыСервисаМаркетплейсы
Процедура ОткрытьФормуСервисаМаркетплейсы(Знач ПараметрыФормы, Знач ДополнительныеПараметры = Неопределено) Экспорт
	
	// Дозаполнение обязательных параметров
	ИнтеграцияСМаркетплейсамиСИВызовСервера.ПараметрыСервисаМаркетплейсы(ПараметрыФормы);
	
	// Установим дополнительные параметры
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
		ПараметрыФормы.ДополнительныеПараметры = ДополнительныеПараметры;
	КонецЕсли;
	
	Если Не ПараметрыФормы.ПараметрыСервиса.ИнтернетПоддержкаПодключена Тогда
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
			МодульИнтернетПоддержкаПользователейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтернетПоддержкаПользователейКлиент");
			ОповещениеОЗавершении = Новый ОписаниеОповещения("ОткрытьФормуСервисаМаркетплейсыПродолжение", ЭтотОбъект, ПараметрыФормы);
			МодульИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОповещениеОЗавершении);
		Иначе
			ВызватьИсключение НСтр("ru = 'Сервис интернет-поддержки пользователей не подключен.';
									|en = 'Online support service is disabled.'");
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыФормы.ПараметрыСервиса.ДанныеИнтеграции.ДатаПодключенияСИ) Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОткрытьФормуСервисаМаркетплейсыЗавершение", ЭтотОбъект, ПараметрыФормы);
		ПодключитьСервисИнтеграции(ОповещениеОЗавершении, ПараметрыФормы.ПараметрыСервиса);
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуСервисаМаркетплейсыЗавершение(Истина, ПараметрыФормы);
	
КонецПроцедуры

// Завершение менеджер длительных операций.
// 
// Параметры:
//  Результат - Произвольный, Неопределено - Результат длительных операций
//  ДополнительныеПараметры - Структура, Произвольный, Неопределено - Дополнительные параметры
Процедура ЗавершениеМенеджерДлительныхОпераций(Результат, ДополнительныеПараметры) Экспорт
	Оповестить("Маркетплейсы_ЗавершениеМенеджерДлительныхОпераций", Результат, ДополнительныеПараметры);
КонецПроцедуры

#Область Конструкторы

// Новые параметры формы сервиса Марктеплейсы.
// 
// Возвращаемое значение:
//  Структура - Новые параметры открытия формы:
// * ИмяФормы - Строка - Имя открываемой формы
// * Уникальность - Строка, Произвольный - Значение ключа уникальности формы
// * РежимОткрытияОкнаФормы - РежимОткрытияОкнаФормы - Режим открытия окна формы
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// * ПараметрыСервиса - Структура - Параметры сервиса:
// ** ИнтернетПоддержкаПодключена - Булево - Признак подключения интернет-поддержки
// ** ДанныеИнтеграции - См. ИнтеграцияСМаркетплейсамиСИ.НовыеДанныеИнтеграции
// * ДополнительныеПараметры - См. НовыеДополнительныеПараметрыФормыСервисаМаркетплейсы
Функция НовыеПараметрыФормыСервисаМаркетплейсы() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ИмяФормы", "");
	Параметры.Вставить("Уникальность", "");
	Параметры.Вставить("РежимОткрытияОкнаФормы", РежимОткрытияОкнаФормы.Независимый);
	Параметры.Вставить("ВидМаркетплейса", ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ПустаяСсылка"));
	
	ПараметрыСервиса = Новый Структура;
	ПараметрыСервиса.Вставить("ИнтернетПоддержкаПодключена", Ложь);
	ПараметрыСервиса.Вставить("ДанныеИнтеграции", Новый Структура);
	
	Параметры.Вставить("ПараметрыСервиса", ПараметрыСервиса);
	Параметры.Вставить("ДополнительныеПараметры", НовыеДополнительныеПараметрыФормыСервисаМаркетплейсы());
	
	Возврат Параметры;
	
КонецФункции

// Новые дополнительные параметры формы сервиса Марктеплейсы.
// 
// Возвращаемое значение:
//  Структура - Новые параметры открытия формы:
// * ОписаниеОповещенияОЗакрытии - ОписаниеОповещения, Неопределено - Имя открываемой формы
Функция НовыеДополнительныеПараметрыФормыСервисаМаркетплейсы() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ОписаниеОповещенияОЗакрытии", Неопределено);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ДлительныеОперации

// Ожидать завершение длительных операций.
// 
// Параметры:
//  ФормаВладелец - ФормаКлиентскогоПриложения - Форма владелец
//  ПараметрыОперации - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций
Процедура ОжидатьЗавершениеДлительныхОпераций(ФормаВладелец, ПараметрыОперации) Экспорт
	
	Если ТипЗнч(ПараметрыОперации.ФоновоеЗадание) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание; // Структура
	ОповещениеОЗавершении = Новый ОписаниеОповещения(ПараметрыОперации.МетодЗавершение, ЭтотОбъект, ПараметрыОперации);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ФормаВладелец);
	ЗаполнитьЗначенияСвойств(ПараметрыОжидания, ПараметрыОперации);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

// Обработка после обработки результата длительных операций.
// 
// Параметры:
//  Результат - Структура, Неопределено - Результат:
//   * АдресРезультата - Строка - Адрес временного хранилища
//  ПараметрыОперации - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций
Процедура ПослеОбработкиРезультатаДлительныхОпераций(Результат, ПараметрыОперации) Экспорт
	
	Если Результат <> Неопределено И Не ПараметрыОперации.ОбработкаРезультатаНаСервере Тогда
		УдалитьИзВременногоХранилища(Результат.АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

// Поместить данные в буфер обмена.
// 
// Параметры:
//  Данные - Строка, ТекстHTML, Картинка - Данные
Процедура ПоместитьДанныеВБуферОбмена(Данные) Экспорт
	
	Если Не СредстваБуфераОбмена.ИспользованиеДоступно() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Буфер обмена недоступен.';
														|en = 'The clipboard is unavailable.'"));
		Возврат;
	КонецЕсли;
	
	ТекстHTML = "ТекстHTML";
	
	Если ТипЗнч(Данные) = Тип("Строка") Тогда
    	ФорматДанных = СтандартныйФорматДанныхБуфераОбмена.Текст;
    ИначеЕсли ТипЗнч(Данные) = Тип(ТекстHTML) Тогда
    	ФорматДанных = СтандартныйФорматДанныхБуфераОбмена.HTML;
    ИначеЕсли ТипЗнч(Данные) = Тип("Картинка") Тогда
    	ФорматДанных = СтандартныйФорматДанныхБуфераОбмена.Картинка;
	КонецЕсли;
	
    Если СредстваБуфераОбмена.ПоддерживаетсяФорматДанных(ФорматДанных) Тогда
        ПомещаемыеДанные = Новый ЭлементБуфераОбмена(ФорматДанных, Данные);
        СредстваБуфераОбмена.ПоместитьДанныеАсинх(ПомещаемыеДанные);
    Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Формат данных не поддерживается буфером обмена.';
														|en = 'The data format is not supported by the clipboard.'"));
    КонецЕсли;
	
КонецПроцедуры

// Обратиться в службу поддержки.
// 
// Параметры:
//  Сообщение - Строка - Предустановленное сообщение
Процедура ОбратитьсяВСлужбуПоддержки(Сообщение = "") Экспорт
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СообщенияВСлужбуТехническойПоддержки") Тогда
		
		МодульСообщенияВСлужбуТехническойПоддержкиКлиентСервер =
			ОбщегоНазначенияКлиент.ОбщийМодуль("СообщенияВСлужбуТехническойПоддержкиКлиентСервер");
		ДанныеСообщения = МодульСообщенияВСлужбуТехническойПоддержкиКлиентСервер.ДанныеСообщения();
		ДанныеСообщения.Получатель = "webIts";
		ДанныеСообщения.Тема = НСтр("ru = 'Интеграция с маркетплейсами';
									|en = 'Integration with marketplaces'");
		ДанныеСообщения.Сообщение = ?(ПустаяСтрока(Сообщение), НСтр("ru = '<Заполните текст сообщения>';
																	|en = '<Fill in message text>'"), Сообщение);
		
		МодульСообщенияВСлужбуТехническойПоддержкиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"СообщенияВСлужбуТехническойПоддержкиКлиент");
		МодульСообщенияВСлужбуТехническойПоддержкиКлиент.ОтправитьСообщение(
			ДанныеСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

//@skip-check property-return-type
#Область СлужебныеПроцедурыИФункции

Процедура ПодключитьСервисИнтеграции(ОповещениеОЗавершении, ПараметрыСервиса)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрытьПослеЗавершенияОперации", Истина);
	ПараметрыФормы.Вставить("ПараметрыСервиса", ПараметрыСервиса);
	
	ОткрытьФорму("Обработка.ПанельАдминистрированияИнтеграцияСМаркетплейсами.Форма.ПодключениеКСервисуИнтеграции",
		ПараметрыФормы, , , , , ОповещениеОЗавершении);
	
КонецПроцедуры

// Открыть форму сервиса Маркетплейсы продолжение.
// 
// Параметры:
//  Результат - Неопределено, Структура - Результат аутентификации пользователя ИТС:
//  * Логин - Строка - Логин пользователя ИТС
//  * Пароль - Строка - Пароль пользователя ИТС
//  ПараметрыФормы - См. НовыеПараметрыФормыСервисаМаркетплейсы
Процедура ОткрытьФормуСервисаМаркетплейсыПродолжение(Результат, ПараметрыФормы) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы.ПараметрыСервиса.ИнтернетПоддержкаПодключена = Истина;
	
	// После подключения интернет-поддержки пользователей требуется еще раз проверить наличие данных в безопасном хранилище
	ДанныеИнтеграции = ИнтеграцияСМаркетплейсамиСИВызовСервера.ПрочитатьДанныеИзБезопасногоХранилища();
	Если ЗначениеЗаполнено(ДанныеИнтеграции.ДатаПодключенияСИ) Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыФормы.ПараметрыСервиса.ДанныеИнтеграции, ДанныеИнтеграции);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыФормы.ПараметрыСервиса.ДанныеИнтеграции.ДатаПодключенияСИ) Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыФормы.ПараметрыСервиса.ДанныеИнтеграции, Результат);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОткрытьФормуСервисаМаркетплейсыЗавершение", ЭтотОбъект, ПараметрыФормы);
		ПодключитьСервисИнтеграции(ОповещениеОЗавершении, ПараметрыФормы.ПараметрыСервиса);
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуСервисаМаркетплейсыЗавершение(Результат, ПараметрыФормы);
	
КонецПроцедуры

// Открыть форму сервиса Маркетплейсы завершение.
// 
// Параметры:
//  Результат - Структура, Булево - Результат
//  ПараметрыФормы - Структура - НовыеПараметрыФормыСервисаМаркетплейсы
//                 - Неопределено - Параметры формы.
Процедура ОткрытьФормуСервисаМаркетплейсыЗавершение(Результат, ПараметрыФормы) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура") Тогда
		Источник = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Источник");
		Если Источник = "ПодключениеКСервисуИнтеграции" Тогда
			ЗаполнитьЗначенияСвойств(ПараметрыФормы.ПараметрыСервиса.ДанныеИнтеграции, Результат);
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(ПараметрыФормы.ИмяФормы) Тогда
		Возврат;
	КонецЕсли;
	
	Уникальность = ?(ЗначениеЗаполнено(ПараметрыФормы.Уникальность), ПараметрыФормы.Уникальность, ПараметрыФормы.ВидМаркетплейса);
	
	ДополнительныеПараметры = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ПараметрыФормы.ДополнительныеПараметры);
	ПараметрыФормы.ДополнительныеПараметры = Неопределено;
	ОписаниеОповещенияОЗакрытии = ДополнительныеПараметры.ОписаниеОповещенияОЗакрытии;
	
	РежимОткрытия = ПараметрыФормы.РежимОткрытияОкнаФормы;
	
	ОткрытьФорму(ПараметрыФормы.ИмяФормы, ПараметрыФормы, , Уникальность, , , ОписаниеОповещенияОЗакрытии, РежимОткрытия);
	
КонецПроцедуры

#Область РаботаСФормой

//@skip-check dynamic-access-method-not-found
// Установить видимость доступность на форме ВыгрузкаТоварногоКаталога.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения -
//  ОписаниеУчетнойЗаписи - Неопределено, Структура - см. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаУчетнойЗаписи
Процедура УстановитьВидимостьДоступность(Форма, ОписаниеУчетнойЗаписи = Неопределено) Экспорт
	
	ДоступностьПоУчетнойЗаписи = ЗначениеЗаполнено(Форма.УчетнаяЗапись);
	ДоступностьОстатки = Ложь;
	ДоступностьЦены = Ложь;
	ПодключеннаяФункциональность = Новый Структура("Остатки, Цены", Ложь, Ложь);
	
	Форма.Элементы.ОбщиеНастройки.Доступность = ДоступностьПоУчетнойЗаписи;
	Форма.Элементы.ГруппаСтраницы.Доступность = ДоступностьПоУчетнойЗаписи;
	
	Если ОписаниеУчетнойЗаписи = Неопределено И ДоступностьПоУчетнойЗаписи Тогда
		//@skip-check dynamic-access-method-not-found
		ОписаниеУчетнойЗаписи = Форма.Кэш.УчетныеЗаписи.Получить(Форма.УчетнаяЗапись); // Структура
	КонецЕсли;
	
	Если ОписаниеУчетнойЗаписи <> Неопределено Тогда
		ПодключеннаяФункциональность.Остатки = ОписаниеУчетнойЗаписи.Функциональность.Найти(1) <> Неопределено;
		ДоступностьОстатки = Форма.Кэш.ПравоНаОстатки 
			И ПодключеннаяФункциональность.Остатки И ОписаниеУчетнойЗаписи.ИспользоватьИнтеграциюПоОстаткам;
		ПодключеннаяФункциональность.Цены = ОписаниеУчетнойЗаписи.Функциональность.Найти(2) <> Неопределено;
		ДоступностьЦены = Форма.Кэш.ПравоНаЦены 
			И ПодключеннаяФункциональность.Цены И ОписаниеУчетнойЗаписи.ИспользоватьИнтеграциюПоЦенам;
	КонецЕсли;
	
	Форма.Элементы.ГруппаРаботаССопоставлениями.Доступность = ДоступностьОстатки Или ДоступностьЦены;
	Форма.Элементы.ГруппаРаботаСОстатками.Доступность = ДоступностьОстатки;
	Форма.Элементы.ГруппаРаботаСЦенами.Доступность = ДоступностьЦены;
	ЗаполнитьПодсказкуПоФункциональности(Форма, ОписаниеУчетнойЗаписи, ПодключеннаяФункциональность);
	
КонецПроцедуры

//@skip-check invocation-parameter-type-intersect
// Заполнить подсказку функциональности остатков.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - 
//  ОписаниеУчетнойЗаписи - см. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаУчетнойЗаписи
//  ОписаниеФункциональности - Структура:
//   * Остатки - Булево - Истина, если доступна функциональность обмена остатками
//   * Цены - Булево - Истина, если доступна функциональность обмена ценами
Процедура ЗаполнитьПодсказкуПоФункциональности(Форма, ОписаниеУчетнойЗаписи, ОписаниеФункциональности)

	ВыбранаУчетнаяЗапись = ЗначениеЗаполнено(Форма.УчетнаяЗапись);
	// Остатки
	Подсказки = Новый Массив; // Массив Из Строка
	
	Если Не Форма.Кэш.ПравоНаОстатки Тогда
		Подсказки.Добавить(
			НСтр("ru = 'Добавить права на редактирование остатков товаров маркетплейса.';
				|en = 'Add rights to edit the marketplace item balances.'"));
	КонецЕсли;
	Если ОписаниеУчетнойЗаписи <> Неопределено И Не ОписаниеУчетнойЗаписи.ИспользоватьИнтеграциюПоОстаткам Тогда
		Подсказки.Добавить(
			НСтр("ru = 'Включить обмен остатками.';
				|en = 'Enable balance exchange.'"));
	КонецЕсли;
	Если Не ОписаниеФункциональности.Остатки Тогда
		Если ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоWildberries(Форма.ВидМаркетплейса) Тогда
			Подсказки.Добавить(НСтр("ru = 'Подключить токен с категорией ""Маркетплейс"".';
									|en = 'Connect a token with the ""Marketplace"" category.'"));
		Иначе
			Подсказки.Добавить(НСтр("ru = 'Указать данные авторизации с соответствующими правами';
									|en = 'Specify the authorization data with appropriate rights'"));
		КонецЕсли;
	КонецЕсли;
	
	Если Подсказки.Количество() > 0 Тогда
		Подсказки.Вставить(0, НСтр("ru = 'Для работы с остатками необходимо:';
									|en = 'To operate with balances, you need:'"));
	КонецЕсли;
	ТекстОшибкиОстатки = СтрСоединить(ОбщегоНазначенияКлиентСервер.СвернутьМассив(Подсказки), Символы.ПС);
	
	ЭлементФормыДекорация = Форма.Элементы.ДекорацияПодсказкаФункциональностьОстатки;
	ЭлементФормыДекорация.РасширеннаяПодсказка.Заголовок = ТекстОшибкиОстатки;
	ЭлементФормыДекорация.Видимость = ВыбранаУчетнаяЗапись И Подсказки.Количество() > 0;
	
	//Цены
	Подсказки.Очистить(); // Массив Из Строка
	
	Если Не Форма.Кэш.ПравоНаЦены Тогда
		Подсказки.Добавить(
			НСтр("ru = 'Добавить права на установку цен номенклатуры.';
				|en = 'Add rights to set item prices.'"));
	КонецЕсли;
	Если ОписаниеУчетнойЗаписи <> Неопределено И Не ОписаниеУчетнойЗаписи.ИспользоватьИнтеграциюПоЦенам Тогда
		Подсказки.Добавить(
			НСтр("ru = 'Включить обмен ценами.';
				|en = 'Enable price exchange.'"));
	КонецЕсли;
	Если Не ОписаниеФункциональности.Цены Тогда
		Если ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоWildberries(Форма.ВидМаркетплейса) Тогда
			Подсказки.Добавить(НСтр("ru = 'Подключить токен с категорией ""Цены и скидки"".';
									|en = 'Connect a token with the ""Prices and discounts"" category.'"));
		Иначе
			Подсказки.Добавить(НСтр("ru = 'Указать данные авторизации с соответствующими правами';
									|en = 'Specify the authorization data with appropriate rights'"));
		КонецЕсли;
	КонецЕсли;
	
	Если Подсказки.Количество() > 0 Тогда
		Подсказки.Вставить(0, НСтр("ru = 'Для работы с ценами необходимо:';
									|en = 'To use prices, you need:'"));
	КонецЕсли;
	ТекстОшибкиОстатки = СтрСоединить(ОбщегоНазначенияКлиентСервер.СвернутьМассив(Подсказки), Символы.ПС);
	
	ЭлементФормыДекорация = Форма.Элементы.ДекорацияПодсказкаФункциональностьЦены;
	ЭлементФормыДекорация.РасширеннаяПодсказка.Заголовок = ТекстОшибкиОстатки;
	ЭлементФормыДекорация.Видимость = ВыбранаУчетнаяЗапись И Подсказки.Количество() > 0;
КонецПроцедуры

#КонецОбласти

#КонецОбласти