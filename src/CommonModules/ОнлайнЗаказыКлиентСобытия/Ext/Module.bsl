///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ОнлайнЗаказы".
// ОбщийМодуль.ОнлайнЗаказыКлиентСобытия.
//
// Клиентские процедуры обработки событий подсистемы Онлайн-заказы:
//  - выбор настройки подключения.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаСобытий

// Определяет поведение системы при выборе настройки подключения.
//
// Параметры:
//  Владелец - ФормаКлиентскогоПриложения - форма владелец.
//  Оповещение - ОписаниеОповещения, Неопределено - оповещение, которое
//    необходимо вызвать после завершения выбора настройки подключения. В случае успешного
//    завершения в результате оповещения будет возвращено Истина.
//  СпособОплаты - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - способ оплаты по настройке подключения.
//
Процедура ПриВыбореНастройкиПодключения(Владелец, Оповещение, СпособОплаты) Экспорт
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.НастройкиПлатежныхСистем") Тогда
		Возврат;
	КонецЕсли;
	
	МодульНастройкиПлатежныхСистемКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("НастройкиПлатежныхСистемКлиент");
	
	ПараметрыОткрытия = МодульНастройкиПлатежныхСистемКлиент.НовыйПараметрыОткрытияСпискаНастроек();
	ПараметрыОткрытия.РежимВыбора          = Истина;
	ПараметрыОткрытия.ЗакрыватьПриВыборе   = Истина;
	ПараметрыОткрытия.Владелец             = Владелец;
	ПараметрыОткрытия.ОписаниеОповещения   = Оповещение;
	ПараметрыОткрытия.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы;
	
	Если (СпособОплаты = ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.СБПc2b")
		Или СпособОплаты = ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.СБПb2b"))
		И ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		
		ПараметрыОткрытия.Подсистема = "СистемаБыстрыхПлатежей";
		
		ПараметрыФормы = Новый Структура;
		
		Если СпособОплаты = ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.СБПc2b") Тогда
			ПараметрыФормы.Вставить("ВариантНастройки", "c2b");
		Иначе
			ПараметрыФормы.Вставить("ВариантНастройки", "b2b");
		КонецЕсли;
		
		ПараметрыОткрытия.ПараметрыПодсистемы  = ПараметрыФормы;
		
	ИначеЕсли СпособОплаты = ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.Эквайринг")
		И ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
		
		ПараметрыОткрытия.Подсистема = "ИнтернетЭквайринг";
		
	Иначе
		Возврат;
	КонецЕсли;
	
	МодульНастройкиПлатежныхСистемКлиент.НастройкиПодключенияСлужебный(ПараметрыОткрытия);
	
КонецПроцедуры

// Определяет поведение системы при выборе настройки подключения к облачной кассе.
//
// Параметры:
//  Владелец - ФормаКлиентскогоПриложения - форма владелец.
//  Оповещение - ОписаниеОповещения, Неопределено - оповещение, которое
//    необходимо вызвать после завершения выбора облачной кассы. В случае успешного
//    завершения в результате оповещения будет возвращено Истина.
//  Организация - ОпределяемыйТип.Организация - организация, по которой должен быть выполнен подбор
//    настроек подключения к облачным кассам.
//
Процедура ПриВыбореОблачнойКассы(Владелец, Оповещение, Организация) Экспорт
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОблачныеКассы") Тогда
		Возврат;
	КонецЕсли;
	
	МодульОблачныеКассыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОблачныеКассыКлиент");
	МодульОблачныеКассыКлиент.НастройкаПодключенияОрганизации(Владелец, Оповещение, Организация);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти