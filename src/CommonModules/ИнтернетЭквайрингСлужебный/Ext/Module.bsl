///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ИнтернетЭквайринг".
// ОбщийМодуль.ИнтернетЭквайрингСлужебный.
// 
// Служебные серверные методы работы с Интернет-эквайрингом:
//  - формирование списка настроек для общей формы настроек платежных систем;
//  - взаимодействие с подсистемой Онлайн-Заказы;
//  - операции с токенами сервиса;
//  - проверка прав;
//  - конструкторы параметров различных методов;
//  - формирование данных для выполнения операций по получению токена и возврату оплаты;
//  - получение и сохранение настроек аутентификации и прикладных настроек подключения;
//  - вывод на форму и работа с реквизитами, созданными программно.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбщиеНастройкиПодсистем

// Формирует дерево для формы общих настроек платежных систем
// 
// Параметры:
//  Дерево - ДеревоЗначений - дерево, которое необходимо заполнить.
//    см. НастройкиПлатежныхСистемСлужебный.НовыйДеревоНастроек.
//  ПараметрыДерева - Структура - параметры вывода дерева.
//    см. НастройкиПлатежныхСистемСлужебный.НовыйПараметрыДерева.
Процедура ПриФормированииДереваПоПодсистеме(
		Дерево,
		ПараметрыДерева) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Настройки.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА Настройки.ЭтоГруппа
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Картинка,
	|	ВЫБОР
	|		КОГДА НЕ Настройки.ЭтоГруппа
	|		И НЕ Настройки.Используется
	|			ТОГДА ""НеИспользуется""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ИдентификаторУсловногоОформления,
	|	Настройки.ЭтоГруппа КАК ЭтоГруппа
	|ИЗ
	|	Справочник.НастройкиПодключенияКИнтернетЭквайрингу КАК Настройки
	|ГДЕ
	|	НЕ ПометкаУдаления
	|	И &Отбор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Настройки.Наименование
	|ИТОГИ
	|ПО
	|	Ссылка ИЕРАРХИЯ";
	
	Отбор = Новый Массив;
	Если ПараметрыДерева.ТолькоГруппы Тогда
		Отбор.Добавить("Настройки.ЭтоГруппа");
	КонецЕсли;
	
	ОбрабатываемыеПараметры = Новый Структура;
	ОбрабатываемыеПараметры.Вставить("Отбор", Неопределено);
	
	Если ТипЗнч(ПараметрыДерева.ПараметрыПодсистемы) = Тип("Структура")
		Или ТипЗнч(ПараметрыДерева.ПараметрыПодсистемы) = Тип("ФиксированнаяСтруктура") Тогда
		ЗаполнитьЗначенияСвойств(ОбрабатываемыеПараметры, ПараметрыДерева.ПараметрыПодсистемы);
	КонецЕсли;
	
	// Произвольные отборы
	НастройкиПлатежныхСистемСлужебный.ДополнитьЗапросСтандартнымОтбором(
		Отбор,
		ОбрабатываемыеПараметры.Отбор,
		Запрос);
	
	Если Отбор.Количество() = 0 Тогда
		ТекстЗамены = "ИСТИНА";
	Иначе
		ТекстЗамены = СтрСоединить(Отбор, " И ");
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&Отбор",
		ТекстЗамены);

	НастройкиПлатежныхСистемСлужебный.ЗапросВДерево(Запрос, Дерево);
	
	ПараметрыДерева.ВыводитьПодсистему = Истина;
	
	// Условное оформление
	УсловноеОформление = НастройкиПлатежныхСистемСлужебный.НовыйНастройкиУсловногоОформления();
	
	Оформление = НастройкиПлатежныхСистемСлужебный.НовыйПараметрыОформления();
	Оформление.ИмяПараметра = "ЦветТекста";
	Оформление.ЗначениеПараметра = ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет;

	Условие = НастройкиПлатежныхСистемСлужебный.НовыйУсловиеОформления();
	Условие.ЛевоеЗначение  = "ИдентификаторУсловногоОформления";
	Условие.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	Условие.ПравоеЗначение = "НеИспользуется";
	
	УсловноеОформление.Оформление.Добавить(Оформление);
	УсловноеОформление.Условия.Добавить(Условие);
	УсловноеОформление.ОформляемыеПоля.Добавить("Ссылка");
	
	ПараметрыДерева.УсловноеОформление.Добавить(УсловноеОформление);
	
КонецПроцедуры

#КонецОбласти

#Область ОнлайнЗаказы

// Выполняет заполнение параметров оплат через Интернет-эквайринг документа операции.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, для которого формируются
//    параметры оплат.
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения к 
//    Интернет-эквайрингу
//  
// Возвращаемое значение:
//  Структура - параметры оплат:
//    * ШаблоныНазначения - см. НастройкиПлатежныхСистемСлужебный.НовыйШаблоныНазначений.
//    * НазначениеПлатежа - Строка - назначение платежа для оплаты через Интернет-эквайринг
//
Функция ПараметрыОплаты(ДокументОперации, НастройкаПодключения) Экспорт
	
	ПараметрыОплаты = Новый Структура;
	ПараметрыОплаты.Вставить("НазначениеПлатежа", "");
	ПараметрыОплаты.Вставить("ШаблоныНазначения", Новый ТаблицаЗначений);
	
	ПараметрыОплаты.ШаблоныНазначения = РегистрыСведений.ШаблоныНазначенийПлатежей.ШаблоныНазначенийДокументаОперации(
		НастройкаПодключения,
		ДокументОперации);
	
	Возврат ПараметрыОплаты;

КонецФункции

// Выполняет обработку назначения платежа.
//  У Точки и Т-Банка есть ограничение - максимум 140 символов.
//
// Параметры:
//  НазначениеПлатежа - Строка - Назначение платежа.
//
Процедура ПодготовитьНазначениеПлатежа(НазначениеПлатежа) Экспорт
	
	Если СтрДлина(НазначениеПлатежа) > 140 Тогда
		НазначениеПлатежа = Лев(НазначениеПлатежа, 140);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет подготовку параметров оплаты заказа для записи в соответствующие подсистемы.
//
// Параметры:
//  ДанныеЗаказа - Структура - Содержит данные заказа:
//    * ИдентификаторЗаказа - Строка - Идентификатор онлайн-заказа;
//    * Статус - Строка - Статус онлайн-заказа;
//    * СуммаЗаказа - Число - Сумма онлайн-заказа;
//    * СпособОплаты - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - Способ оплаты онлайн-заказа;
//    * ДатаОплаты - Дата - дата получения данных об оплате сервисом;
//    * КонтрольнаяСумма - Строка - Значение контрольной суммы заказа;
//    * ВерсияКонтрольнойСуммы - Число - Версия расчета контрольной суммы заказа;
//    * ОплатаСБП - Структура, Неопределено - Содержит данные операции СБП, в том случае когда
//      значение Свойства СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b:
//      ***Идентификатор - Строка - идентификатор операции;
//      ***ИдентификаторОплаты - Строка - идентификатор оплаты в СБП;
//      ***ИдентификаторПлатежнойСистемы - Строка - идентификатор оплаты в Системе быстрых платежей;
//      ***ДатаОперации - Дата - фактическая дата оплаты в UTC;
//      ***ИдентификаторМерчанта - Строка - идентификатор мерчанта в Системе быстрых платежей;
//      ***НазначениеПлатежа - Строка - назначение платежа;
//    * ОплатаИнтернетЭквайринг - Структура, Неопределено - Содержит данные операции, в том случае когда
//      значение свойства СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг:
//      ***Идентификатор - Строка - идентификатор операции;
//      ***ИдентификаторОплаты - Строка - идентификатор операции оплаты;
//      ***ИдентификаторОперации - Строка - идентификатор текущей операции;
//      ***ДатаОперации - Дата - фактическая дата оплаты в UTC;
//      ***ИдентификаторМерчанта - Строка - идентификатор мерчанта;
//      ***НазначениеПлатежа - Строка - назначение платежа;
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу, Неопределено - Настройка
//    подключения указанная в настройке страницы онлайн-заказов, Неопределено в том случае, если настройка не
//    определена.
//  ПараметрыОплаты - Соответствие Из КлючИЗначение - Содержит параметры оплат в разрезе ее способов.
//  ПараметрыНастройкиОплаты - Структура - Содержит параметры настройки вида оплаты.
//
Процедура ЗаполнитьПараметрыОплаты(
	ДанныеЗаказа,
	НастройкаПодключения,
	ПараметрыОплаты,
	ПараметрыНастройкиОплаты) Экспорт
	
	ПараметрыНастройкиОплаты = ДанныеНастройкиОперацииПоДаннымОплаты(
		НастройкаПодключения,
		ДанныеЗаказа.ОплатаИнтернетЭквайринг.ИдентификаторМерчанта);
	
	ПараметрыОплатыИнтернетЭквайринг = ЗаполнитьПараметрыОперации(
		ДанныеЗаказа.ОплатаИнтернетЭквайринг,
		ДанныеЗаказа.СуммаЗаказа,
		НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполнена(),
		ПараметрыНастройкиОплаты.НастройкаПодключения);
	
	ПараметрыОплаты.Вставить(
		"ИнтернетЭквайринг",
		ПараметрыОплатыИнтернетЭквайринг);
	
КонецПроцедуры

// Выполняет запись данных оплаты заказа.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ по которому была выполнена оплата.
//  ПараметрыОперации - см. НовыйПараметрыОперации.
//  ПараметрыНастройкиОплаты - см. ДанныеНастройкиОперацииПоДаннымОплаты.
//
Процедура ЗаписатьДанныеОплаты(
		ДокументОперации,
		ПараметрыОперации,
		ПараметрыНастройкиОплаты) Экспорт
	
	РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ЗаписатьНовыеДанныеОплаты(
		ДокументОперации,
		ПараметрыОперации.СуммаОперации,
		ПараметрыНастройкиОплаты,
		ПараметрыОперации);
	
	ДанныеОплаты = Новый Структура;
	ДанныеОплаты.Вставить("ДатаОплаты",        ПараметрыОперации.ДатаОперации);
	ДанныеОплаты.Вставить("СуммаОплаты",       ПараметрыОперации.СуммаОперации);
	ДанныеОплаты.Вставить("НазначениеПлатежа", ПараметрыОперации.НазначениеПлатежа);
	
	РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.СохранитьДанныеОплаты(
		ПараметрыОперации.Идентификатор,
		ДанныеОплаты,
		ДокументОперации,
		ПараметрыОперации.ИдентификаторМерчанта);
	
КонецПроцедуры

// Возвращает данные оплат по документам платежей.
//
// Параметры:
//  ДокументыОпераций - Массив Из ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документы операций.
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - данные оплат по документам:
//   * Ключ - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ операции.
//   * Значение - см. ЗаполнитьПараметрыОперации
//
Функция ДанныеОпераций(ДокументыОпераций) Экспорт
	
	ДанныеОпераций = РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ДанныеОпераций(
		ДокументыОпераций);
	
	Результат = Новый Соответствие;
	
	Для Каждого ДанныеОперации Из ДанныеОпераций Цикл
		
		Если ДанныеОперации.Значение = Неопределено Тогда
			Результат.Вставить(
				ДанныеОперации.Ключ,
				Неопределено);
		Иначе
			ПараметрыОперации = ЗаполнитьПараметрыОперации(
				ДанныеОперации.Значение,
				ДанныеОперации.Значение.СуммаОперации,
				НастройкиПлатежныхСистемСлужебный.ПолучитьСтатусОперацииПоИдентификатору(ДанныеОперации.Значение.СтатусОперации),
				ДанныеОперации.Значение.НастройкаПодключения);
			
			Результат.Вставить(
				ДанныеОперации.Ключ,
				ПараметрыОперации);
				
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОперацииСТокенамиСервиса

// Получает токен сервиса для переданной настройки подключения.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения к 
//    Интернет-эквайрингу.
//  ИмяСервиса - Строка - имя сервиса в котором будет использоваться токен.
//
// Возвращаемое значение:
//  Структура - результат получения токена:
//    * Токен - Строка - токен сервиса.
//    * КодОшибки - Строка - строковый код возникшей ошибки, который может быть обработан вызывающим методом.
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ТокенСпособаОплаты(
	НастройкаПодключения,
	ИмяСервиса) Экспорт
	
	РезультатОперации = НовыйРезультатОперации();
	РезультатОперации.Вставить("Токен", Неопределено);
	
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Не заполнена настройка подключения к Интернет-эквайрингу.';
													|en = 'The Online acquiring connection setting is not filled in.'"); 
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Не заполнена настройка подключения к Интернет-эквайрингу.';
													|en = 'The Online acquiring connection setting is not filled in.'");
		Возврат РезультатОперации;
	КонецЕсли;
	
	ИдентификаторСервиса = НастройкиПлатежныхСистемСлужебный.ИдентификаторСервисаПоИмени(ИмяСервиса);
	
	Если ИдентификаторСервиса = Неопределено Тогда
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Передано неизвестное имя сервиса.';
													|en = 'An unknown service name is passed.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Передано неизвестное имя сервиса.';
													|en = 'An unknown service name is passed.'");
		Возврат РезультатОперации;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Настройки.ИдентификаторУчастника КАК ИдентификаторУчастника,
	|	Настройки.ИдентификаторМерчанта КАК ИдентификаторМерчанта,
	|	Настройки.Используется КАК Используется,
	|	Настройки.Наименование КАК Наименование
	|ИЗ
	|	Справочник.НастройкиПодключенияКИнтернетЭквайрингу КАК Настройки
	|ГДЕ
	|	Настройки.Ссылка = &НастройкаПодключения";
	
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если Не Выборка.Используется Тогда
			Возврат РезультатОперации;
		КонецЕсли;
		ПараметрыНастройки = НовыйПараметрыНастройкиПодключения();
		ЗаполнитьЗначенияСвойств(ПараметрыНастройки, Выборка);
		ПараметрыНастройки.НастройкаПодключения = НастройкаПодключения;
	Иначе
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Не удалось получить данные по настройке подключения к Интернет-эквайрингу.';
													|en = 'Cannot get the data of setting up connection to Online acquiring.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Не удалось получить данные по настройке подключения к Интернет-эквайрингу.';
													|en = 'Cannot get the data of setting up connection to Online acquiring.'");
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДанныеХранилища = НастройкиПодключения(
		НастройкаПодключения,
		Ложь,
		Ложь);
	
	ДанныеТокена = ДанныеХранилища.ДанныеТокенов[ИдентификаторСервиса];
	
	ДанныеТокенаСервиса = НовыйОписаниеТокеновСервиса();
	
	Если ДанныеТокена <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ДанныеТокенаСервиса, ДанныеТокена);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ДанныеТокенаСервиса.Токен) Тогда
		РезультатОперации.Токен = ДанныеТокенаСервиса.Токен;
		Возврат РезультатОперации;
	Иначе
		
		ИдентификаторТокена = Строка(Новый УникальныйИдентификатор);
		
		ПараметрыАутентификации = Новый Соответствие;
		
		Для Каждого ОписаниеПараметра Из ДанныеХранилища.ПараметрыАутентификации Цикл
			ПараметрыАутентификации.Вставить(
				ОписаниеПараметра.Идентификатор,
				ОписаниеПараметра.Значение);
		КонецЦикла;
		
		РезультатВызова = ИнтернетЭквайрингСервис.ОперацияЗапросТокенаАутентификацииСервиса(
			ПараметрыНастройки,
			ДанныеХранилища.СпособАутентификации,
			ПараметрыАутентификации,
			ИдентификаторТокена,
			ИмяСервиса);
		
		Если Не ПустаяСтрока(РезультатВызова.КодОшибки) Тогда
			ЗаполнитьЗначенияСвойств(РезультатОперации, РезультатВызова, , "КодОшибки");
			Возврат РезультатОперации;
		КонецЕсли;
		
		Если РезультатВызова.ТребуетсяНастройка Тогда
			ТекстОшибки = НСтр("ru = 'Для получения токена для Интернет-эквайринга необходимо указание дополнительной информации.
				|Зайдите в настройку подключения к Интернет-эквайрингу и нажмите кнопку ""Проверить""';
				|en = 'To get a token for Online acquiring, additional information is required.
				|Go to the Online acquiring connection settings and click Check'");
			РезультатОперации.СообщениеОбОшибке  = ТекстОшибки;
			РезультатОперации.ИнформацияОбОшибке = ТекстОшибки;
			Возврат РезультатОперации;
		КонецЕсли;
		
		ДанныеТокенаСервиса.Токен         = РезультатВызова.Токен;
		ДанныеТокенаСервиса.Идентификатор = ИдентификаторТокена;

		ДанныеХранилища.ДанныеТокенов.Вставить(
			ИдентификаторСервиса,
			ДанныеТокенаСервиса);
		
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
			НастройкаПодключения,
			ДанныеХранилища,
			КлючХранилищаДанныхАутентификации());
		УстановитьПривилегированныйРежим(Ложь);
		
		РезультатОперации.Токен = РезультатВызова.Токен;
		
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Отзывает токены сервиса для переданной настройки подключения.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения к
//    Интернет-эквайрингу.
//  РезультатОперации - Структура - результат получения токена:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который может быть обработан вызывающим методом.
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//  ИмяСервиса - Строка - имя сервиса в котором использовались токены.
//
Процедура ОтозватьТокенСпособаОплаты(
	НастройкаПодключения,
	РезультатОперации,
	ИмяСервиса) Экспорт
	
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Не заполнена настройка подключения к Интернет-эквайрингу';
													|en = 'The Online acquiring connection setting is not filled in'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Не заполнена настройка подключения к Интернет-эквайрингу.';
													|en = 'The Online acquiring connection setting is not filled in.'");
		Возврат;
	КонецЕсли;
	
	ИдентификаторСервиса = НастройкиПлатежныхСистемСлужебный.ИдентификаторСервисаПоИмени(ИмяСервиса);
	
	Если ИдентификаторСервиса = Неопределено Тогда
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Передано неизвестное имя сервиса.';
													|en = 'An unknown service name is passed.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Передано неизвестное имя сервиса.';
													|en = 'An unknown service name is passed.'");
		Возврат;
	КонецЕсли;
	
	ИдентификаторУчастника = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаПодключения, "ИдентификаторУчастника");
	
	Если Не ЗначениеЗаполнено(ИдентификаторУчастника) Тогда
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Не удалось получить идентификатор участника.';
													|en = 'Cannot get the participant ID.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Не удалось получить идентификатор участника.';
													|en = 'Cannot get the participant ID.'");
		Возврат;
	КонецЕсли;
	
	ДанныеХранилища = НастройкиПодключения(
		НастройкаПодключения,
		Ложь,
		Ложь);
	
	ДанныеТокена = ДанныеХранилища.ДанныеТокенов[ИдентификаторСервиса];
	
	ДанныеТокенаСервиса = НовыйОписаниеТокеновСервиса();
	Если ДанныеТокена <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ДанныеТокенаСервиса, ДанныеТокена);
	КонецЕсли;
	
	ИдентификаторыТокенов = Новый Массив;
	
	Если ЗначениеЗаполнено(ДанныеТокенаСервиса.Идентификатор) Тогда
		ИдентификаторыТокенов.Добавить(ДанныеТокенаСервиса.Идентификатор);
		
		ДанныеТокенаСервиса.Идентификатор = "";
		ДанныеТокенаСервиса.Токен = "";
		
		ДанныеХранилища.ДанныеТокенов.Вставить(
			ИдентификаторСервиса,
			ДанныеТокенаСервиса);
	КонецЕсли;

	Если ИдентификаторыТокенов.Количество() > 0 Тогда
		
		РезультатВызова = ИнтернетЭквайрингСервис.ОперацияОтзывТокеновАутентификацииСервиса(
			ИдентификаторыТокенов,
			ИмяСервиса);
			
		Если Не ПустаяСтрока(РезультатВызова.КодОшибки) Тогда
			ЗаполнитьЗначенияСвойств(РезультатОперации, РезультатВызова, , "КодОшибки");
			Возврат;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
			НастройкаПодключения,
			ДанныеХранилища,
			КлючХранилищаДанныхАутентификации());
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиПлатежныхСистем

// Определяет прикладные настройки подключения к сервису
// 
// Возвращаемое значение:
//  Структура - Прикладные настройки подключения:
//    * ОбъектМетаданных - ОбъектМетаданныхРегистрСведений - объект метаданных регистр сведений, в котором хранятся
//        настройки выполнения оплат. Регистр определяет связь мерчанта и аналитики ведения учета в программах 1С. На
//        основании данных регистра должен выполняется поиск настройки подключения при выполнении оплат и возвратов.
//    * ИсключаемыеПоля - Массив из Строка - наименования измерений, ресурсов или реквизитов, которые необходимо скрыть
//        на форме настройки подключения.
//    * ШаблоныНазначений - см. НастройкиПлатежныхСистемСлужебный.НовыйШаблоныНазначений.
//
Функция ПрикладныеНастройкиПодключения() Экспорт
	
	Настройки = Новый Структура;
	Настройки.Вставить("ОбъектМетаданных", Неопределено);
	Настройки.Вставить("ИсключаемыеПоля",  Новый Массив);
	Настройки.Вставить("ШаблоныНазначений", НастройкиПлатежныхСистемСлужебный.НовыйШаблоныНазначений());
	
	ИнтеграцияПодсистемБИП.ПриОпределенииНастроекПодключенияЭквайринг(Настройки);
	ИнтернетЭквайрингПереопределяемый.ПриОпределенииНастроекПодключения(Настройки);
	ИнтернетЭквайрингСобытия.ПриПроверкеНастройкиПодключенияПрограммы(Настройки);
	
	Возврат Настройки;
	
КонецФункции

// Возвращает тип данных СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу.
//
// Возвращаемое значение:
//  Тип - тип данных ссылка на справочник НастройкиПодключенияКИнтернетЭквайрингу.
//
Функция ТипНастройкаПодключения() Экспорт
	
	Возврат Тип("СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу");
	
КонецФункции

// Проверяет, является ли переданное значение ссылкой на справочник с настройками подключения
//  к Интернет-эквайрингу.
//
// Параметры:
//  НастройкаПодключения - Произвольный - значение, которое необходимо проверить.
//
// Возвращаемое значение:
//  Булево - Истина, если переданное значение является ссылкой на справочник 
//   НастройкиПодключенияКИнтернетЭквайрингу.
//
Функция ЭтоНастройкаПодключения(НастройкаПодключения) Экспорт
	
	Возврат ТипЗнч(НастройкаПодключения) = Тип("СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу");
	
КонецФункции

#КонецОбласти

#Область ПрочиеСлужебныеМетоды

// Определяет доступность использования функциональности чтения настроек на основании прав доступа пользователя.
//
// Возвращаемое значение:
//  Булево - если Истина, чтение операций доступно.
//
Функция ЧтениеНастроекДоступно() Экспорт
	
	Возврат Ложь;
	
КонецФункции

// Возвращает ключ для безопасного хранилища, определяющий данные аутентификации.
// 
// Возвращаемое значение:
//  Строка - ключ хранилища.
//
Функция КлючХранилищаДанныхАутентификации() Экспорт
	
	Возврат "ДанныеАутентификации";
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КонструкторыПараметров

// Создает описание результат операции в сервисе.
//
// Возвращаемое значение:
//  Структура - результат операции:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который может быть обработан вызывающим методом.
//    * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция НовыйРезультатОперации() Экспорт
	
	РезультатОперации = Новый Структура;
	РезультатОперации.Вставить("КодОшибки", "");
	РезультатОперации.Вставить("СообщениеОбОшибке", "");
	РезультатОперации.Вставить("ИнформацияОбОшибке", "");
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует новое описание параметров операции, используется в длительных операциях и переопределяемых методах.
//
// Возвращаемое значение:
//  Структура - содержит описание параметров операции:
//    * ДатаОперации - Дата - фактическая дата операции в UTC;
//    * СуммаОперации - Число - фактическая суммы возврата по документу;
//    * ИдентификаторОперации - Строка - идентификатор текущей операции;
//    * ИдентификаторОплаты - Строка - идентификатор операции оплаты.
//
Функция НовыйОписаниеПараметровОперации() Экспорт
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("ДатаОперации",          Дата(1, 1, 1));
	ПараметрыОперации.Вставить("СуммаОперации",         0);
	ПараметрыОперации.Вставить("ИдентификаторОперации", "");
	ПараметрыОперации.Вставить("ИдентификаторОплаты",   "");
	
	Возврат ПараметрыОперации;
	
КонецФункции

// Создает описание результат длительной операции в сервисе.
//
// Возвращаемое значение:
//  Структура - результат длительной операции:
//    * СтатусОперации - Строка - текущее состояние операции:
//       - "Выполняется" - подтверждение не получено;
//       - "Отменена" - оплата по оплата по ранее сформированной ссылке не возможна;
//       - "Выполнена" - участник Интернет-эквайринга подтвердил операцию;
//       - "ТребуетсяПодтверждение" - необходимо подтвердить действие;
//       - "Ошибка" - не удалось выполнить проверку операции из-за ошибки.
//    * ПараметрыОперации - Структура - содержит дополнительные данные по операции:
//        ** ДатаОперации - Дата - фактическая дата оплаты в UTC;
//        ** СуммаОперации - Число - фактическая суммы операции по документу;
//        ** ИдентификаторОперации - Строка - ключ контроля загрузки.
//    * КодОшибки - Строка - строковый код возникшей ошибки, который может быть обработан вызывающим методом;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция НовыйРезультатДлительнойОперации() Экспорт
	
	РезультатОперации = НовыйРезультатОперации();
	РезультатОперации.Вставить("СтатусОперации", "");
	РезультатОперации.Вставить("ПараметрыОперации", НовыйОписаниеПараметровОперации());
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует новое описание заказа на возврат. При проведении возврата проверяется заполнение всех свойств.
//
// Возвращаемое значение:
//  Структура - содержит описание заказа на возврат:
//    * СуммаВозврата - Число - Сумма, которую необходимо вернуть покупателю;
//    * ДатаВозврата - Дата - дата выполнения операции.
//
Функция ОписаниеЗаказаНаВозврат()
	
	ЗаказаНаОплату = Новый Структура;
	ЗаказаНаОплату.Вставить("ДатаВозврата",  Дата(1, 1, 1));
	ЗаказаНаОплату.Вставить("СуммаВозврата", 0);
	
	Возврат ЗаказаНаОплату;
	
КонецФункции

// Формирует пустое описание данных операции
// 
// Возвращаемое значение:
//  Структура - Новый данные операции:
//    * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка, по которой была
//        выполнена операция.
//    * Идентификатор - Строка - идентификатор операции.
//    * ИдентификаторОплаты - Строка - идентификатор операции оплаты.
//    * ИдентификаторОперации - Строка - идентификатор текущей операции.
//    * ИдентификаторМерчанта - Строка - идентификатор мерчанта.
//    * НазначениеПлатежа - Строка - назначение платежа при оплате по карте.
//    * СтатусОперации - Строка - текущий статус операции.
//    * ДатаОперации - Дата - дата выполнения операции.
//    * СуммаОперации - Число - фактическая сумма операции.
//    * Оплата - Булево - Истина, если это операция оплаты.
//    * ИдентификаторУчастника - Строка - идентификатор участника Интернет-эквайринга.
//    * Используется - Булево - признак использования настройки подключения.
//
Функция НовыйДанныеОперации() Экспорт
	
	ДанныеОперации = Новый Структура;
	ДанныеОперации.Вставить("НастройкаПодключения",   Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ПустаяСсылка());
	ДанныеОперации.Вставить("Идентификатор",          "");
	ДанныеОперации.Вставить("ИдентификаторОплаты",    "");
	ДанныеОперации.Вставить("ИдентификаторОперации",  "");
	ДанныеОперации.Вставить("ИдентификаторМерчанта",  "");
	ДанныеОперации.Вставить("НазначениеПлатежа",      "");
	ДанныеОперации.Вставить("СтатусОперации",         "");
	ДанныеОперации.Вставить("ДатаОперации",           Дата(1, 1, 1));
	ДанныеОперации.Вставить("СуммаОперации",          0);
	ДанныеОперации.Вставить("Оплата",                 Ложь);
	ДанныеОперации.Вставить("ИдентификаторУчастника", "");
	ДанныеОперации.Вставить("Используется",           Ложь);
	
	Возврат ДанныеОперации;
	
КонецФункции

// Формирует описание настройки подключения для выполнения запросов.
// 
// Возвращаемое значение:
//  Структура - Новый параметры настройки подключения:
//    * Наименование - Строка - Наименование настройки.
//    * ИдентификаторМерчанта - Строка - идентификатор мерчанта.
//    * ИдентификаторУчастника - Строка - идентификатор банка.
//    * Используется - Булево - Истина, если настройка используется.
//    * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения для
//        для которой получены параметры.
//
Функция НовыйПараметрыНастройкиПодключения() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("Наименование",           "");
	Результат.Вставить("ИдентификаторМерчанта",  "");
	Результат.Вставить("ИдентификаторУчастника", "");
	Результат.Вставить("Используется",           Ложь);
	Результат.Вставить(
		"НастройкаПодключения",
		Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ПустаяСсылка());
	
	Возврат Результат;
	
КонецФункции

// Возвращает новое описание параметров операции
//
// Возвращаемое значение:
//  Структура - параметры операции:
//    * ДатаОперации - Дата - фактическая дата операции.
//    * СуммаОперации - Число - сумма операции.
//    * Идентификатор - Строка - идентификатор операции.
//    * ИдентификаторОперации - Строка - идентификатор текущей операции.
//    * ИдентификаторОплаты - Строка - идентификатор операции оплаты.
//    * ИдентификаторМерчанта - Строка - идентификатор мерчанта.
//    * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения.
//    * НазначениеПлатежа - Строка - назначение платежа
//
Функция НовыйПараметрыОперации()
	
	Результат = Новый Структура;
	Результат.Вставить("Идентификатор",           "");
	Результат.Вставить("ИдентификаторОперации",   "");
	Результат.Вставить("ИдентификаторОплаты",     "");
	Результат.Вставить("ИдентификаторУчастника",  "");
	Результат.Вставить("ИдентификаторМерчанта",   "");
	Результат.Вставить("ДатаОперации",            Дата(1, 1, 1));
	Результат.Вставить("СуммаОперации",           0);
	Результат.Вставить("НастройкаПодключения",    Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ПустаяСсылка());
	Результат.Вставить("НазначениеПлатежа",       "");	
	
	Возврат Результат;
	
КонецФункции

// Создает описание структуры с данными токенов сервисов - потребителей.
//
// Возвращаемое значение:
//  Структура - данные токенов сервисов - потребителей:
//   * Идентификатор - Строка - идентификатор токена,
//   * Токен - Строка - Значение токена.
//   
Функция НовыйОписаниеТокеновСервиса()
	
	ОписаниеТокена = Новый Структура;
	ОписаниеТокена.Вставить("Идентификатор", "");
	ОписаниеТокена.Вставить("Токен",         "");
	
	Возврат ОписаниеТокена;
	
КонецФункции

// Формирует описание сохраняемых или загружаемых данных аутентификации.
// 
// Параметры:
//  СпособАутентификации - Строка - способ аутентификации в системах банка.
//  ПараметрыАутентификации - Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации.
//                          - Неопределено - начальное значение параметров аутентификации не задано.
// 
// Возвращаемое значение:
//  Структура - параметры аутентификации:
//    * СпособАутентификации - Строка - способ аутентификации в системах банка.
//    * ПараметрыАутентификации - Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации.
//    * ДанныеТокенов - Соответствие Из КлючИЗначение - данные токенов для различных сервисов:
//      ** Ключ - Строка - Имя сервиса.
//      ** Значение - см. НовыйОписаниеТокеновСервиса
// 
Функция НовыйДанныеАутентификации(
		СпособАутентификации = "",
		ПараметрыАутентификации = Неопределено) Экспорт

	Результат = Новый Структура;
	
	Результат.Вставить("СпособАутентификации", СпособАутентификации);
	Если ТипЗнч(ПараметрыАутентификации) <> Тип("Массив") Тогда
		Результат.Вставить("ПараметрыАутентификации", Новый Массив);
	Иначе
		Результат.Вставить("ПараметрыАутентификации", ПараметрыАутентификации);
	КонецЕсли;
	
	Результат.Вставить("ДанныеТокенов", Новый Соответствие);
	
	Возврат Результат;

КонецФункции

// Функция-конструктор параметров вывод прикладных настроек.
// 
// Возвращаемое значение:
//  Структура - параметры по умолчанию:
//    * Родитель - Форма, ГруппаФормы, Неопределено - форма или группа, внутри которой нужно разместить новые элементы.
//    * Настройка - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу, Неопределено - ссылка на настройку
//      подключения, в случае создания новой настройки передается Неопределено.
//    * ИдентификаторУчастника - Строка - идентификатор участника Интернет-эквайринга, для которого выполняется
//      настройка.
//    * ДополнительныеПараметры - Произвольный, Неопределено - Любые дополнительные параметры, которые могут использоваться
//      в переопределяемых модулях.
//    * ИмяРеквизитаНаименование - Строка - имя реквизита, в котором храниться наименование настройки.
//    * ИмяЭлементаНаименование - Строка - имя элемента, связанного с реквизитом наименование.
//    * ИмяДополнительнойИнформации - Строка - Имя декорации для вывода дополнительной информации.
//    * ПрикладныеПараметрыЗагружены - Булево - если параметр Настройка заполнен и значение этого параметра равно Ложь,
//        то будет выполнена загрузка значений настроек из базы.
//    * СохраняемыеДанные - Булево - если Истина, то для всех созданных реквизитов будет установлен флаг "Сохраняемые
//        данные".
//
Функция НовыйПараметрыВывода() Экспорт

	Результат = Новый Структура;
	
	Результат.Вставить("Родитель",                     Неопределено);
	Результат.Вставить("Настройка",                    Неопределено);
	Результат.Вставить("ИдентификаторУчастника",       "");
	Результат.Вставить("ДополнительныеПараметры",      Неопределено);
	Результат.Вставить("ИмяРеквизитаНаименование",     "Наименование");
	Результат.Вставить("ИмяЭлементаНаименование",      "Наименование");
	Результат.Вставить("ИмяДополнительнойИнформации",  "ДекорацияДополнительнаяИнформация");
	Результат.Вставить("ПрикладныеПараметрыЗагружены", Ложь);
	Результат.Вставить("СохраняемыеДанные",            Ложь);
	
	Возврат Результат;

КонецФункции

// Информация о динамически созданном реквизите прикладных настроек.
//
// Параметры:
//  Идентификатор - Строка - Идентификатор реквизита в прикладной логике.
//  Тип - ОписаниеТипов - тип вводимых данных.
//  Представление - Строка - представление поля.
//  ПроверкаЗаполнения - Строка - если Истина, то будет выполнена проверка реквизита на заполненность.
//  ПараметрыВыбора - ФиксированныйМассив Из ПараметрВыбора - настройка параметров выбора
//                  - Неопределено - если параметры выбора не заданы.
//  СвязиПараметровВыбора - ФиксированныйМассив Из СвязьПараметраВыбора - связи параметров выбора поля.
//                        - Неопределено - если связь параметров выбора не задана.
//
// Возвращаемое значение:
//  Структура - см. ИнтернетЭквайрингКлиентСервер.НовыйОписаниеДинамическогоРеквизита
//  
Функция НовыйОписаниеПрикладныхНастроек(
		Идентификатор,
		Тип,
		Представление,
		ПроверкаЗаполнения = Ложь,
		ПараметрыВыбора = Неопределено,
		СвязиПараметровВыбора = Неопределено)

	УИД = СтрЗаменить(
			Строка(Новый УникальныйИдентификатор()),
			"-",
			"");
		
	Результат = ИнтернетЭквайрингКлиентСервер.НовыйОписаниеДинамическогоРеквизита();
	Результат.Идентификатор           = Идентификатор;
	Результат.Представление           = Представление;
	Результат.ИмяРеквизита            = "ПрикладныеНастройки_" + УИД;
	Результат.ИмяЭлемента             = "ПолеПрикладныеНастройки_" + УИД;
	Результат.Тип                     = Тип;
	Результат.ПараметрыВыбора         = ПараметрыВыбора;
	Результат.СвязиПараметровВыбора   = СвязиПараметровВыбора;
	Результат.ПроверкаЗаполнения      = ПроверкаЗаполнения;
	Результат.ЭтоНастройкаПодключения = (Тип = Новый ОписаниеТипов("СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу"));
	
	Если Тип.СодержитТип(Тип("Строка")) Тогда
		Результат.ДлинаСтроки = Тип.КвалификаторыСтроки.Длина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция-конструктор параметров вывода дополнительных настроек аутентификации.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма на которую будут выводится настройки.
//  СоздаватьРеквизиты - Булево - признак необходимости создания новых реквизитов и элементов.
//  ДополнительныеПараметры - Массив Из см. НовыйПараметрыПоляАутентификации - описание реквизитов и
//    элементов формы в которые выводятся дополнительные параметры.
//  НовыеЗначения - Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации - новые значения.
//  ИмяГруппы - Строка - имя группы формы, в которой будут создаваться новые элементы
//  Загрузка - Булево - Истина, если выполняется загрузка значений реквизитов из базы.
//  СохраняемыеДанные - Булево - Истина, если для новых элементов необходимо установить признак изменения
//    сохраняемых данных.
// 
// Возвращаемое значение:
//  Структура - Новый параметры вывода дополнительных настроек:
//    * Форма - ФормаКлиентскогоПриложения - форма на которую будут выводится настройки.
//    * СоздаватьРеквизиты - Булево - признак необходимости создания новых реквизитов и элементов.
//    * ДополнительныеПараметры - Массив Из см. НовыйПараметрыПоляАутентификации - описание реквизитов и
//        элементов формы в которые выводятся дополнительные параметры.
//    * НовыеЗначения - Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации - новые значения.
//    * ИмяГруппы - Строка - имя группы формы, в которой будут создаваться новые элементы
//    * Загрузка - Булево - Истина, если выполняется загрузка значений реквизитов из базы.
//    * СохраняемыеДанные - Булево - Истина, если для новых элементов необходимо установить признак изменения
//        сохраняемых данных.
Функция НовыйПараметрыВыводаДополнительныхНастроек(
		Форма,
		СоздаватьРеквизиты,
		ДополнительныеПараметры,
		НовыеЗначения,
		ИмяГруппы,
		Загрузка = Ложь,
		СохраняемыеДанные = Ложь) Экспорт
		
	Результат = Новый Структура;
	
	Результат.Вставить("Форма",                   Форма);
	Результат.Вставить("СоздаватьРеквизиты",      СоздаватьРеквизиты);
	Результат.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	Результат.Вставить("НовыеЗначения",           НовыеЗначения);
	Результат.Вставить("ИмяГруппы",               ИмяГруппы);
	Результат.Вставить("Загрузка",                Загрузка);
	Результат.Вставить("СохраняемыеДанные",       СохраняемыеДанные);
	
	Возврат Результат
	
КонецФункции

#КонецОбласти

#Область ОбщегоНазначения

// Добавляет запись в журнал регистрации.
//
// Параметры:
//  СообщениеОбОшибке - Строка - комментарий к записи журнала регистрации.
//  Ошибка - Булево - если Истина будет установлен уровень журнала регистрации "Ошибка".
//  Данные -  Неопределено, Произвольный- Данные, с которыми связано событие. Рекомендуется указывать ссылки на объекты
//    данных (элементы справочников, документы, к которым относится событие).
//
Процедура ЗаписатьИнформациюВЖурналРегистрации(
		СообщениеОбОшибке,
		Ошибка = Истина,
		Данные = Неопределено) Экспорт
	
	УровеньЖР = ?(Ошибка, УровеньЖурналаРегистрации.Ошибка, УровеньЖурналаРегистрации.Информация);
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации(),
		УровеньЖР,
		,
		Данные,
		Лев(СообщениеОбОшибке, 5120));
	
КонецПроцедуры

// Возвращает имя события для журнала регистрации, которое используется для записи событий загрузки данных из внешних
// систем.
//
// Возвращаемое значение:
//  Строка - имя события.
//
Функция ИмяСобытияЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Интернет-эквайринг';
				|en = 'Online acquiring'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

// Формирует идентификатор для отображения скрытых символов на форме.
//
//
// Возвращаемое значение:
//  Строка - служебный идентификатор.
//
Функция СтрокаСекретныхДанныхПоУмолчанию() Экспорт

	Возврат "a08bf0a2-7d7a-461f-8624-3b54f47c02a9";
	
КонецФункции

#КонецОбласти

#Область ОнлайнЗаказы

// Определяет данные настройки подключения.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу, Неопределено - Настройка
//    подключения, указанная в настройке страницы онлайн-заказов, Неопределено в том случае, если настройка не
//    используется.
//  ИдентификаторМерчанта - Строка - идентификатор мерчанта.
//
// Возвращаемое значение:
//  Структура - параметры настройки:
//    * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения.
//    * ИдентификаторУчастника - Строка - идентификатор участника Интернет-эквайринга.
//
Функция ДанныеНастройкиОперацииПоДаннымОплаты(НастройкаПодключения, ИдентификаторМерчанта)
	
	Результат = Новый Структура;
	Результат.Вставить(
		"НастройкаПодключения",
		Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ПустаяСсылка());
	Результат.Вставить("ИдентификаторУчастника", "");
	
	Если ЗначениеЗаполнено(НастройкаПодключения) Тогда
		
		ДанныеНастройкиПодключения = Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ДанныеНастройкиПодключения(
			НастройкаПодключения);
		
		Если ДанныеНастройкиПодключения.ИдентификаторМерчанта = ИдентификаторМерчанта Тогда
			Результат.НастройкаПодключения = НастройкаПодключения;
			Результат.ИдентификаторУчастника = ДанныеНастройкиПодключения.ИдентификаторУчастника;
			Возврат Результат;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ДанныеНастройкиПодключенияПоИдентификаторуМерчанта(
		ИдентификаторМерчанта);
	
КонецФункции

// Подготавливает и возвращает параметры операции для передачи в отложенный обработчик.
//
// Параметры:
//  ДанныеОперации - Структура - - Содержит данные операции:
//    * Идентификатор - Строка - идентификатор операции;
//    * ИдентификаторОплаты - Строка - идентификатор операции оплаты;
//    * ИдентификаторОперации - Строка - идентификатор текущей операции;
//    * ДатаОперации - Дата - фактическая дата оплаты в UTC;
//    * ИдентификаторМерчанта - Строка - идентификатор мерчанта;
//    * НазначениеПлатежа - Строка - назначение платежа.
//  СуммаОперации - Число - сумма операции.
//  СтатусОперации - Строка - текущий статус операции.
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения.
//
// Возвращаемое значение:
// Структура - содержит заполненные параметры операции:
//   * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения.
//   * РезультатОбработки - Структура - содержит результаты обработки статуса операции:
//     ** СтатусОперации - Строка - текущее состояние операции оплаты. Возможные значения:
//         - "Выполняется" - подтверждение оплаты не получено;
//         - "Отменена" - оплата по ранее сформированной ссылке невозможна;
//         - "Выполнена" - участник Интернет-эквайринга подтвердил оплату;
//         - "Отклонена" - оплата по ранее сформированной ссылке невозможна;
//         - "Ошибка" - неизвестный статус операции.
//     ** ПараметрыОперации - см. НовыйПараметрыОперации.
//     ** СообщениеОбОшибке - Строка - информация об ошибке
//
Функция ЗаполнитьПараметрыОперации(
		ДанныеОперации,
		СуммаОперации,
		СтатусОперации,
		НастройкаПодключения)
	
	ПараметрыОперации = НовыйПараметрыОперации();
	
	ПараметрыОперации.Идентификатор          = ДанныеОперации.Идентификатор;
	ПараметрыОперации.ИдентификаторОплаты    = ДанныеОперации.ИдентификаторОплаты;
	ПараметрыОперации.ИдентификаторОперации  = ДанныеОперации.ИдентификаторОперации;
	ПараметрыОперации.ИдентификаторУчастника = ДанныеОперации.ИдентификаторУчастника;
	ПараметрыОперации.ИдентификаторМерчанта  = ДанныеОперации.ИдентификаторМерчанта;
	ПараметрыОперации.СуммаОперации          = СуммаОперации;
	ПараметрыОперации.НастройкаПодключения   = НастройкаПодключения;
	ПараметрыОперации.НазначениеПлатежа      = ДанныеОперации.НазначениеПлатежа;
	ПараметрыОперации.ДатаОперации           = ДанныеОперации.ДатаОперации;
	
	РезультатОбработки = Новый Структура;
	РезультатОбработки.Вставить("ПараметрыОперации", ПараметрыОперации);
	РезультатОбработки.Вставить("СообщениеОбОшибке", "");
	РезультатОбработки.Вставить("СтатусОперации",    СтатусОперации);
	
	ПараметрыОплатыИнтернетЭквайринг = Новый Структура(
		"НастройкаПодключения, РезультатОбработки",
		НастройкаПодключения,
		РезультатОбработки);
	
	Возврат ПараметрыОплатыИнтернетЭквайринг;
	
КонецФункции

#КонецОбласти

#Область ПроверкаДоступа

// Проверяет права на вызов прикладных операций Интернет-эквайринга.
// В случае отсутствия прав, вызывается исключение.
//
Процедура ПроверитьПраваНаВыполнениеОперации() Экспорт
	
	Если Не ИнтернетЭквайринг.ОперацииДоступны() Тогда
		ВызватьИсключение НСтр("ru = 'Нарушение прав доступа. Пользователю запрещено выполнение операций с использование Интернет-эквайринга.
			|Обратитесь к администратору.';
			|en = 'Access violation. Online acquiring transactions are unavailable for the user.
			|Contact the administrator.'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрикладныеОперации

// см. ИнтернетЭквайринг.ВозвратОплаты
// 
Функция ВозвратОплаты(
		ДокументВозврата,
		ОбъектОплаты,
		НастройкаПодключения,
		ДополнительныеПараметры) Экспорт
	
	РезультатОперации = НовыйРезультатДлительнойОперации();
	
	// Подготовка параметров настройки подключения.
	ПараметрыНастройкиПодключения =
		Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ДанныеНастройкиПодключения(
			НастройкаПодключения);
	
	// Проверка общих параметров подключения.
	ПроверитьОбщиеНастройкиПодключения(
		ДокументВозврата,
		ПараметрыНастройкиПодключения.НастройкаПодключения,
		ПараметрыНастройкиПодключения,
		РезультатОперации);
	
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	ИдентификаторыОперации = ИдентификаторыОперации(
		ОбъектОплаты,
		ПараметрыНастройкиПодключения.ИдентификаторУчастника);
	Если ЗначениеЗаполнено(ИдентификаторыОперации.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(РезультатОперации, ИдентификаторыОперации);
		Возврат РезультатОперации;
	КонецЕсли;
	
	ЗаказНаВозврат = ОписаниеЗаказаНаВозврат();
	
	ИнтеграцияПодсистемБИП.ПриФормированииЗаказаНаВозвратЭквайринг(
		ДокументВозврата,
		ЗаказНаВозврат,
		ПараметрыНастройкиПодключения.НастройкаПодключения,
		ДополнительныеПараметры);
	ИнтернетЭквайрингПереопределяемый.ПриФормированииЗаказаНаВозврат(
		ДокументВозврата,
		ЗаказНаВозврат,
		НастройкаПодключения,
		ДополнительныеПараметры);
	
	// Валидация данных заказа на возврат и настроек.
	ПроверитьЗаказНаВозврат(
		ЗаказНаВозврат,
		РезультатОперации);
	
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	ИдентификаторыОперации = ИдентификаторыОперации.ИдентификаторыОперации;
	
	Если ТипЗнч(ИдентификаторыОперации) <> Тип("Структура")
		Или Не ЗначениеЗаполнено(ИдентификаторыОперации.ИдентификаторОперации) Тогда
		
		РезультатОперации.СтатусОперации     = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка();
		РезультатОперации.КодОшибки          = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Не обнаружен идентификатор документа оплаты в Интернет-эквайринге.';
													|en = 'The payment document ID is not found in Online acquiring.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Не обнаружен идентификатор документа оплаты в Интернет-эквайринге.';
													|en = 'The payment document ID is not found in Online acquiring.'");
		
		ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;

	ПараметрыНастройкиПодключенияОплаты = ПараметрыНастройкиПодключенияПоДокументу(ОбъектОплаты);
	
	Если ЗначениеЗаполнено(ПараметрыНастройкиПодключенияОплаты.ИдентификаторУчастника)
		И ЗначениеЗаполнено(ПараметрыНастройкиПодключенияОплаты.НастройкаПодключения)
		И ПараметрыНастройкиПодключения.ИдентификаторУчастника <> ПараметрыНастройкиПодключенияОплаты.ИдентификаторУчастника Тогда
		
		ПараметрыНастройкиПодключения = ПараметрыНастройкиПодключенияОплаты;
		
	КонецЕсли;
	
	// Формирование идентификаторов возврата.
	Идентификатор = ОпределитьИдентификаторЗаказаНаВозврат(
		РезультатОперации,
		ДокументВозврата,
		ЗаказНаВозврат,
		ИдентификаторыОперации.ИдентификаторОплаты,
		ПараметрыНастройкиПодключения);
	
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	// Сохранить данные заказа для проверки идемпотентности.
	РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.СохранитьДанныеВозврата(
		Идентификатор,
		ЗаказНаВозврат,
		ДокументВозврата,
		ПараметрыНастройкиПодключения.ИдентификаторМерчанта);
	
	ПараметрыВозврата = Новый Структура;
	ПараметрыВозврата.Вставить("СуммаВозврата",          ЗаказНаВозврат.СуммаВозврата);
	ПараметрыВозврата.Вставить("ДатаВозврата",           ЗаказНаВозврат.ДатаВозврата);
	ПараметрыВозврата.Вставить("ИдентификаторОплаты",    ИдентификаторыОперации.ИдентификаторОплаты);
	ПараметрыВозврата.Вставить("ИдентификаторОперации",  ИдентификаторыОперации.ИдентификаторОперации);
	ПараметрыВозврата.Вставить("ИдентификаторУчастника", ИдентификаторыОперации.ИдентификаторУчастника);
	ПараметрыВозврата.Вставить("Идентификатор",          Строка(Идентификатор));
	
	РезультатВозврата = ИнтернетЭквайрингСервис.ОперацияВозвратОплаты(
		ПараметрыВозврата,
		ПараметрыНастройкиПодключения);
	
	Если ЗначениеЗаполнено(РезультатВозврата.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(
				РезультатОперации,
				РезультатВозврата,
				"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка();
		
		Если Не (РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиСервисВременноНеДоступен()
			Или РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиОшибкаСервиса()
			Или РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиОшибкаПодключения()
			Или РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиБлокировкаСервисовИПП()) Тогда
				
			РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ЗаписатьСтатусОперацииОшибка(
				ДокументВозврата,
				НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаОшибка());
			
		КонецЕсли;
		
		Возврат РезультатОперации;
	КонецЕсли;
	
	РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ЗаписатьСтатусОперации(
		ДокументВозврата,
		РезультатВозврата.ПараметрыОперации.ИдентификаторОперации,
		РезультатВозврата.ПараметрыОперации.ДатаОперации,
		РезультатВозврата.СтатусОперации,
		РезультатВозврата.ПараметрыОперации.СуммаОперации);
	
	ОбработатьСтатусВозврата(
		РезультатВозврата,
		РезультатВозврата.СтатусОперации,
		РезультатВозврата.ПричинаОтказа);
	
	Возврат РезультатВозврата;
	
КонецФункции

// Определяет идентификаторы операции оплаты или возврата на основе ссылки на документ либо строкового идентификатора
//
// Параметры:
//  ОбъектОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, отражающий операцию в прикладной
//    логике.
//  ИдентификаторУчастника - Строка - идентификатор участника Интернет-эквайринга, через которого выполняется возврат.
// 
// Возвращаемое значение:
//  Структура - Идентификаторы операции по типу:
//    * СтатусОперации - Строка - текущее состояние операции:
//       - "Выполняется" - подтверждение не получено;
//       - "Отменена" - оплата по оплата по ранее сформированной ссылке не возможна;
//       - "Выполнена" - участник Интернет-эквайринга подтвердил операцию;
//       - "Ошибка" - не удалось выполнить проверку операции из-за ошибки.
//    * ПараметрыОперации - Структура - содержит дополнительные данные по операции.
//    * КодОшибки - Строка - строковый код возникшей ошибки, который может быть обработан вызывающим методом.
//    * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//    * ИдентификаторыОперации - см. РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ИдентификаторыОперации.
//
Функция ИдентификаторыОперации(ОбъектОперации, ИдентификаторУчастника)
	
	РезультатОперации = НовыйРезультатДлительнойОперации();
	РезультатОперации.Вставить("ИдентификаторыОперации", Неопределено);
	
	Если Метаданные.ОпределяемыеТипы.ДокументОперацииИнтернетЭквайринга.Тип.СодержитТип(ТипЗнч(ОбъектОперации)) Тогда
		
		РезультатОперации.ИдентификаторыОперации = РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ИдентификаторыОперации(ОбъектОперации);
		
		Если РезультатОперации.ИдентификаторыОперации = Неопределено Тогда
			РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
			РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Не найдена операция по указанному документу.';
														|en = 'No transaction is found for the specified document.'");
			РезультатОперации.ИнформацияОбОшибке = РезультатОперации.СообщениеОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		Если РезультатОперации.ИдентификаторыОперации.ИдентификаторУчастника <> ИдентификаторУчастника Тогда
			РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
			РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Возврат оплаты возможен только через банк, через которой она была получена';
														|en = 'A refund is only available through the bank through which the payment was received.'");
			РезультатОперации.ИнформацияОбОшибке = РезультатОперации.СообщениеОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;

	Иначе
		
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Возврат для указанного документа не поддерживается.';
													|en = 'Refunds for the specified document are not supported.'");
		РезультатОперации.ИнформацияОбОшибке = РезультатОперации.СообщениеОбОшибке;
		Возврат РезультатОперации;
		
	КонецЕсли;

	Возврат РезультатОперации;
	
КонецФункции

// см. ИнтернетЭквайринг.СтатусВозврата
Функция СтатусВозврата(
		ДокументВозврата,
		ДлительныйВызов) Экспорт
	
	РезультатОперации = НовыйРезультатДлительнойОперации();
	
	// Подготовка параметров настройки подключения.
	ПараметрыНастройкиПодключения = ПараметрыНастройкиПодключенияПоДокументу(
		ДокументВозврата);
	
	// Проверка общих параметров подключения.
	ПроверитьОбщиеНастройкиПодключения(
		ДокументВозврата,
		ПараметрыНастройкиПодключения.НастройкаПодключения,
		ПараметрыНастройкиПодключения,
		РезультатОперации);
	
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка();
		Возврат РезультатОперации;
	КонецЕсли;
	
	ПараметрыСтатуса = РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ПараметрыОпределенияСтатусаОперации(
		ДокументВозврата);
	
	ОбработатьСтатусВозврата(
		РезультатОперации,
		ПараметрыСтатуса.СтатусОперации);
	
	Если РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполняется() Тогда
		
		Если ДлительныйВызов Тогда
			
			НастройкиВызова = НастройкиПлатежныхСистемСлужебный.НовыйИтеративныйВызовОперации();
			Пока НастройкиПлатежныхСистемСлужебный.ВозможенВызовОперации(НастройкиВызова) Цикл
				
				РезультатПроверки = ИнтернетЭквайрингСервис.ОперацияСтатусВозврата(
					ПараметрыСтатуса.ИдентификаторОперации,
					ПараметрыСтатуса.ДатаЗапросаСтатуса,
					ПараметрыНастройкиПодключения);
				
				Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
					ЗаполнитьЗначенияСвойств(
						РезультатОперации,
						РезультатПроверки,
						"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
					РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка();
					Возврат РезультатОперации;
				КонецЕсли;
				
				ПараметрыСтатуса.ДатаЗапросаСтатуса = ТекущаяДатаСеанса();
				
				ОбработатьСтатусВозврата(
					РезультатОперации,
					РезультатПроверки.СтатусВозврата,
					РезультатПроверки.ПричинаОтказа);
				
				Если РезультатОперации.СтатусОперации <> НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполняется() Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе
			
			РезультатПроверки = ИнтернетЭквайрингСервис.ОперацияСтатусВозврата(
				ПараметрыСтатуса.ИдентификаторОперации,
				ПараметрыСтатуса.ДатаЗапросаСтатуса,
				ПараметрыНастройкиПодключения);
			
			Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
				ЗаполнитьЗначенияСвойств(
					РезультатОперации,
					РезультатПроверки,
					"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
				РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка();
				Возврат РезультатОперации;
			КонецЕсли;
			
			ОбработатьСтатусВозврата(
				РезультатОперации,
				РезультатПроверки.СтатусВозврата,
				РезультатПроверки.ПричинаОтказа);
			
		КонецЕсли;
		
		РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ЗаписатьСтатусОперации(
			ДокументВозврата,
			ПараметрыСтатуса.ИдентификаторОперации,
			РезультатПроверки.ДатаОперации,
			РезультатПроверки.СтатусВозврата,
			РезультатПроверки.СуммаОперации,
			ПараметрыСтатуса.ДатаЗапросаСтатуса);
		
		ЗаполнитьЗначенияСвойств(РезультатОперации.ПараметрыОперации, РезультатПроверки);
		РезультатОперации.ПараметрыОперации.ИдентификаторОперации = ПараметрыСтатуса.ИдентификаторОперации;
		РезультатОперации.ПараметрыОперации.ИдентификаторОплаты   = ПараметрыСтатуса.ИдентификаторОплаты;

	КонецЕсли;

	Возврат РезультатОперации;
	
КонецФункции

// см. ИнтернетЭквайринг.СтатусыОпераций
// 
Функция СтатусыОпераций() Экспорт
	
	ОбработанныеОперации = Новый Массив;
	ДанныеОпераций = ДанныеОперацийСТерминальнымСтатусом(ОбработанныеОперации);
	
	ОбработатьСтатусыВозвратов(ДанныеОпераций, ОбработанныеОперации);
	
	Возврат ОбработанныеОперации;
	
КонецФункции

// Загружает статусы возвратов операций.
//
// Параметры:
//  ДанныеОпераций - см. ДанныеОперацийСТерминальнымСтатусом.
//  ОбработанныеОперации - Массив из ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - данные обработанных
//    документов.
//
Процедура ОбработатьСтатусыВозвратов(ДанныеОпераций, ОбработанныеОперации)
	
	ДокументыОплаты = Новый Массив;
	Для Каждого ОтложеннаяОперация Из ДанныеОпераций Цикл
		
		Если ОтложеннаяОперация.Оплата Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументыОплаты.Добавить(ОтложеннаяОперация.ДокументОперации);
		
	КонецЦикла;
	
	// Параметры нескольких настроек
	
	ПараметрыНастроекПодключения = РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ПараметрыНастроекПодключенияПоДокументам(ДокументыОплаты);
	
	Для Каждого ОтложеннаяОперация Из ДанныеОпераций Цикл
		
		Если ОтложеннаяОперация.Оплата Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыНастройкиПодключения = ПараметрыНастроекПодключения.Получить(
			ОтложеннаяОперация.ДокументОперации);
			
		Если ПараметрыНастройкиПодключения = Неопределено Тогда
			СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось найти параметры настройки подключения для документа %1';
					|en = 'Cannot find the connection setting parameters for the %1 document'"),
				Строка(ОтложеннаяОперация.ДокументОперации));
			ПриЗагрузкеСтатусаОперации(
				ОтложеннаяОперация.ДокументОперации,
				ОтложеннаяОперация.НастройкаПодключения,
				НовыйОписаниеПараметровОперации(),
				НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаОшибка(),
				СообщениеОбОшибке,
				ОбработанныеОперации);
			Продолжить;
		КонецЕсли;
		
		РезультатПроверки = ИнтернетЭквайрингСервис.ОперацияСтатусВозврата(
			ОтложеннаяОперация.Идентификатор,
			ОтложеннаяОперация.ДатаЗапросаСтатуса,
			ПараметрыНастройкиПодключения);
		
		Если ЗначениеЗаполнено(РезультатПроверки.КодОшибки) Тогда
			РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.УвеличитьКоличествоПопытокЗапросаСтатуса(
				ОтложеннаяОперация.ДокументОперации);
			МаксимальноеКоличествоПопыток = НастройкиПлатежныхСистемСлужебный.МаксимальноеКоличествоПопытокЗапросаСтатуса(
				"ИнтернетЭквайринг",
				Ложь);
			Если ОтложеннаяОперация.КоличествоПопыток >= МаксимальноеКоличествоПопыток Тогда
					ПриЗагрузкеСтатусаОперации(
						ОтложеннаяОперация.ДокументОперации,
						ОтложеннаяОперация.НастройкаПодключения,
						НовыйОписаниеПараметровОперации(),
						НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаОшибка(),
						РезультатПроверки.СообщениеОбОшибке,
						ОбработанныеОперации);
				КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ЗаписатьСтатусОперации(
			ОтложеннаяОперация.ДокументОперации,
			ОтложеннаяОперация.Идентификатор,
			РезультатПроверки.ДатаОперации,
			РезультатПроверки.СтатусВозврата,
			РезультатПроверки.СуммаОперации,
			ОтложеннаяОперация.ДатаЗапросаСтатуса);
		
		Если РезультатПроверки.СтатусВозврата = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаВыполнена()
			Или РезультатПроверки.СтатусВозврата = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаОтменена()
			Или РезультатПроверки.СтатусВозврата = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаНетИнформации()
			Или РезультатПроверки.СтатусВозврата = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаПревышение() Тогда
			
			ПараметрыОперации = НовыйОписаниеПараметровОперации();
			ЗаполнитьЗначенияСвойств(ПараметрыОперации, РезультатПроверки);
			
			ПриЗагрузкеСтатусаОперации(
				ОтложеннаяОперация.ДокументОперации,
				ОтложеннаяОперация.НастройкаПодключения,
				ПараметрыОперации,
				РезультатПроверки.СтатусВозврата,
				РезультатПроверки.СообщениеОбОшибке,
				ОбработанныеОперации);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Определяет операции по котором необходимо выполнить получение статуса в сервисе.
//
// Параметры:
//  ОбработанныеОперации - Массив из ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - данные обработанных
//    документов.
//
// Возвращаемое значение:
//   Массив из Структура - данные операция, по которым необходимо загрузить статус:
//    * ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, отражающий операцию в
//       информационной базе.
//    * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка, по которой была
//       выполнена операция.
//    * Идентификатор - Строка - идентификатор операции.
//    * ПериодИспользования - Дата - период использования.
//    * Оплата - Булево - Истина, если это операция оплаты.
//    * ЗапроситьСтатус - Булево - всегда Истина.
//    * ДатаЗапросаСтатуса - Дата - дата и время последнего запроса статуса.
//    * КоличествоПопыток - Число - текущее количество попыток.
Функция ДанныеОперацийСТерминальнымСтатусом(ОбработанныеОперации)
	
	ОтложенныеОперации = РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ОтложенныеОперации();
	
	ДанныеОпераций = Новый Массив;
	ДатаЗапросаСтатуса = ТекущаяДатаСеанса();
	
	Для Каждого КлючЗначение Из ОтложенныеОперации Цикл
		
		Для Каждого Операция Из КлючЗначение.Значение Цикл
			
			// Если операция уже в терминальном статусе, выполнять запрос
			// не имеет смысла, необходимо выполнить обработку в прикладной
			// логике и перейти к следующей.
			Если Операция.СтатусОперации <> НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаВПроцессе() Тогда
				ПриЗагрузкеСтатусаОперации(
					Операция.ДокументОперации,
					Операция.НастройкаПодключения,
					Операция.ПараметрыОперации,
					Операция.СтатусОперации,
					"",
					ОбработанныеОперации);
				Продолжить;
			КонецЕсли;
			
			Если Операция.Оплата Тогда
				// Обработка оплат выполняется в подсистеме Онлайн-Заказы
				Продолжить;
			КонецЕсли;
			
			// Формирование данных для запроса статуса статуса.
			ОтложеннаяОперация = Новый Структура;
			ОтложеннаяОперация.Вставить("ДокументОперации",     Операция.ДокументОперации);
			ОтложеннаяОперация.Вставить("НастройкаПодключения", Операция.НастройкаПодключения);
			ОтложеннаяОперация.Вставить("Идентификатор",        Операция.ИдентификаторОперации);
			ОтложеннаяОперация.Вставить("ПериодИспользования",  Операция.ПериодИспользования);
			ОтложеннаяОперация.Вставить("Оплата",               Операция.Оплата);
			ОтложеннаяОперация.Вставить("ЗапроситьСтатус",      Истина);
			ОтложеннаяОперация.Вставить("ДатаЗапросаСтатуса",   ДатаЗапросаСтатуса);
			ОтложеннаяОперация.Вставить("КоличествоПопыток",    Операция.КоличествоПопыток);
			
			ДанныеОпераций.Добавить(ОтложеннаяОперация);
				
		КонецЦикла;

	КонецЦикла;
	
	Возврат ДанныеОпераций;
	
КонецФункции

// Определяет алгоритм обработки операций, статус которых был получен регламентным заданием.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, который отражает операцию в
//    информационной базе;
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка выполнения операций;
//  ПараметрыОперации - Структура - дополнительные данные по оплате:
//    * ДатаОперации - Дата - фактическая дата оплаты в UTC;
//    * СуммаОперации - Число - фактическая суммы оплаты по документу;
//    * ИдентификаторОперации - Строка - идентификатор текущей операции;
//    * ИдентификаторОплаты- Строка - идентификатор операции оплаты;
//  СтатусОперацииСервис - Строка - текущее состояние операции операции. Для проверки статуса
//      операции, необходимо функции программного интерфейса общего модуля
//      ИнтернетЭквайрингКлиентСервер. Возможные значения:
//        - "Отменена" - по ранее сформированная операция отменена;
//        - "Выполнена" - выполнение операции подтверждено банком;
//        - "Ошибка" - не удалось выполнить проверку статуса операции из-за ошибки или банк вернул ошибку;
//  СообщениеОбОшибке - Строка - сообщение пользователю. Заполняется в случае ошибки;
//  ОбработанныеОперации - Массив из ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - данные обработанных
//    документов.
//
Процедура ПриЗагрузкеСтатусаОперации(
		ДокументОперации,
		НастройкаПодключения,
		ПараметрыОперации,
		СтатусОперацииСервис,
		СообщениеОбОшибке,
		ОбработанныеОперации)
	
	СтатусОперации = "";
	
	Если СтатусОперацииСервис = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаВыполнена() Тогда
		СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполнена();
	ИначеЕсли СтатусОперацииСервис = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаОтклонена()
		Или СтатусОперацииСервис = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаОтменена() Тогда
		СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОтменена();
	ИначеЕсли СтатусОперацииСервис = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаПревышение()
		Или СтатусОперацииСервис = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаНеОплачен()
		Или СтатусОперацииСервис = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаНетИнформации()
		Или СтатусОперацииСервис = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаОшибка() Тогда
		СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка();
	Иначе
		ЗаписатьИнформациюВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Передан не валидный статус операции %1 в отложенную обработку.';
					|en = 'Invalid status of the %1 transaction is transferred to the deferred data processor.'"),
				СтатусОперацииСервис),
			Истина);
		ИнтернетЭквайринг.УстановитьОтложенноеПолучениеСтатуса(
			ДокументОперации,
			Ложь);
	КонецЕсли;
	
	Попытка
		
		РезультатОбработки = Новый Структура;
		РезультатОбработки.Вставить("СтатусОперации", СтатусОперации);
		РезультатОбработки.Вставить("СообщениеОбОшибке", СообщениеОбОшибке);
		РезультатОбработки.Вставить("ПараметрыОперации", ПараметрыОперации);
		
		Обработан = Ложь;
		
		ИнтеграцияПодсистемБИП.ПриЗагрузкеСтатусаОперацииЭквайринг(
			ДокументОперации,
			НастройкаПодключения,
			РезультатОбработки,
			Обработан);
		ИнтернетЭквайрингПереопределяемый.ПриЗагрузкеСтатусаОперации(
			ДокументОперации,
			НастройкаПодключения,
			РезультатОбработки,
			Обработан);
		
		Если Обработан = Истина Тогда
			ИнтернетЭквайринг.УстановитьОтложенноеПолучениеСтатуса(
				ДокументОперации,
				Ложь);
			ОбработанныеОперации.Добавить(ДокументОперации);
		КонецЕсли;
		
	Исключение
		
		Если ТранзакцияАктивна() Тогда
			
			ОтменаТранзакции = Ложь;
			Пока ТранзакцияАктивна() Цикл
				ОтменитьТранзакцию(); // АПК:325 Отмена незакрытых транзакций.
				ОтменаТранзакции = Истина;
			КонецЦикла;
			
			Если ОтменаТранзакции Тогда
				ЗаписатьИнформациюВЖурналРегистрации(
					НСтр("ru = 'По завершении выполнения обработчика ИнтернетЭквайрингСлужебный.ПриЗагрузкеСтатусаОперации
						|не была закрыта транзакция.';
						|en = 'The transaction was not closed upon completion of the ИнтернетЭквайрингСлужебный.OnImportingOperationStatus
						|handler.'"),
					Истина);
			КонецЕсли;
			
		КонецЕсли;
		
		ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		
	КонецПопытки;
	
КонецПроцедуры

// Формирует настройки подключения для выполнения запросов.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга- документ, по данным которого необходимо
//  получить список настроек.
//
// Возвращаемое значение:
//  Структура - см. НовыйДанныеОперации.
//
Функция ПараметрыНастройкиПодключенияПоДокументу(ДокументОперации)
	
	ДокументыОпераций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокументОперации);
	
	ДанныеНесколькихОпераций =
		РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ДанныеОпераций(
			ДокументыОпераций);
	ДанныеОперации = ДанныеНесколькихОпераций.Получить(ДокументОперации);
	
	Если ДанныеОперации = Неопределено Тогда
		ВызватьИсключение НСтр(
			"ru = 'Не удалось определить параметры настройки подключения к интернет эквайрингу по документу. Обработка прервана.';
			|en = 'Cannot determine the Online acquiring connection parameters for the document. Processing is terminated.'");
	КонецЕсли;
	
	Возврат ДанныеОперации;
	
КонецФункции

// Формирует новый идентификатор заказа на возврат или получает существующий.
//
// Параметры:
//  РезультатОперации - см. НовыйРезультатДлительнойОперации.
//  ДокументВозврата - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, который отражает возврат в
//    информационной базе.
//  ЗаказНаВозврат - см. ОписаниеЗаказаНаВозврат.
//  ИдентификаторОплаты - Строка - идентификатор оплаты по которой производится возврат.
//  ПараметрыНастройкиПодключения - см. ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения.
//
// Возвращаемое значение:
//  Строка - внешний идентификатор документа возврата;
//
Функция ОпределитьИдентификаторЗаказаНаВозврат(
		РезультатОперации,
		ДокументВозврата,
		ЗаказНаВозврат,
		ИдентификаторОплаты,
		ПараметрыНастройкиПодключения)
	
	Идентификатор = РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ИдентификаторОперации(
		ДокументВозврата);
	
	// Первая итерация, дополнительных проверок не требуется.
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.НовыйИдентификаторОперации(
			ДокументВозврата,
			ПараметрыНастройкиПодключения,
			ИдентификаторОплаты);
	КонецЕсли;
	
	ПараметрыСтатуса = РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ПараметрыОпределенияСтатусаОперации(
		ДокументВозврата);
	
	// Если по возврату уже получен отрицательный терминальный статус
	// можно сгенерировать новый идентификатор операции.
	Если ПараметрыСтатуса = Неопределено
		Или ПараметрыСтатуса.СтатусОперации = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаНеОплачен()
		Или ПараметрыСтатуса.СтатусОперации = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаНетИнформации()
		Или ПараметрыСтатуса.СтатусОперации = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаОтклонена()
		Или ПараметрыСтатуса.СтатусОперации = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаПревышение()
		Или ПустаяСтрока(ПараметрыСтатуса.СтатусОперации) Тогда
		Возврат РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.НовыйИдентификаторОперации(
			ДокументВозврата,
			ПараметрыНастройкиПодключения,
			ИдентификаторОплаты);
	КонецЕсли;
	
	ИсторическиеДанные = РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ДанныеОперацииПоИдентификатору(
		Идентификатор);
	
	Если ИсторическиеДанные = Неопределено Тогда
		Возврат РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.НовыйИдентификаторОперации(
			ДокументВозврата,
			ПараметрыНастройкиПодключения,
			ИдентификаторОплаты);
	КонецЕсли;
	
	// Если по документу ранее был сформирован идентификатор оплаты, делать по нему возврат запрещено.
	// Необходимо получить статус оплаты, а для возврата сформировать новый документ.
	Если ИсторическиеДанные.Оплата Тогда
		РезультатОперации.КодОшибки          = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Для документа ранее был сформирован идентификатор оплаты, создание возврата запрещено.';
													|en = 'The payment ID was generated earlier for the document, cannot create a refund.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Для документа ранее был сформирован идентификатор оплаты, создание возврата запрещено.';
													|en = 'The payment ID was generated earlier for the document, cannot create a refund.'");
		Возврат Неопределено;
	КонецЕсли;
	
	// Если параметры заказа на возврат не изменились
	// можно выполнить повторный запрос, т.к. операция
	// создания заказа на возврат поддерживает идемпотентность.
	Если (ИсторическиеДанные.СуммаОперации = ЗаказНаВозврат.СуммаВозврата
		И ИсторическиеДанные.ДокументОперации = ДокументВозврата) Тогда
		
		Возврат Идентификатор;
		
	Иначе
		
		РезультатОперации.КодОшибки          = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Для документа уже сформирован возврат со значениями суммы, отличающейся от текущего документа. Создание возврата запрещено.';
													|en = 'Refund with a different amount is already generated for the document. Cannot create a refund.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Для документа уже сформирован возврат со значениями суммы, отличающейся от текущего документа. Создание возврата запрещено.';
													|en = 'Refund with a different amount is already generated for the document. Cannot create a refund.'");
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

// Заполняет результат операции на основании статуса операции возврата.
//
// Параметры:
//  РезультатОперации - см. НовыйРезультатДлительнойОперации.
//  СтатусВозврата - Строка - статус возврата.
//  ДополнительнаяИнформация - Строка - дополнительное описание статуса.
//
Процедура ОбработатьСтатусВозврата(РезультатОперации, СтатусВозврата, ДополнительнаяИнформация = "")
	
	Если СтатусВозврата = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаПревышение() Тогда
		РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка();
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
		ТекстОшибки = НСтр("ru = 'Превышена допустимая сумма возврата по заказу на оплату.';
							|en = 'The allowed refund amount for the order for payment is exceeded.'");
		Если ЗначениеЗаполнено(ДополнительнаяИнформация) Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + ДополнительнаяИнформация;
		КонецЕсли;
		РезультатОперации.СообщениеОбОшибке = ТекстОшибки;
		РезультатОперации.ИнформацияОбОшибке = ТекстОшибки;
	ИначеЕсли СтатусВозврата = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаНеОплачен() Тогда
		РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка();
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Оплата не выполнена, возврат невозможен.';
													|en = 'Payment is not made. Cannot make a refund.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Оплата не выполнена, возврат невозможен.';
													|en = 'Payment is not made. Cannot make a refund.'");
	ИначеЕсли СтатусВозврата = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаНетИнформации() Тогда
		РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка();
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Оплата не обнаружена, возврат невозможен.';
													|en = 'Payment is not found. Cannot make a refund.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Оплата не обнаружена, возврат невозможен.';
													|en = 'Payment is not found. Cannot make a refund.'");
	ИначеЕсли СтатусВозврата = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаОтменена() Тогда
		РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОтменена();
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеизвестнаяОшибка();
		РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Операция отменена по неизвестной причине, повторите операцию или обратитесь к администратору.';
													|en = 'The transaction is canceled for an unknown reason. Try again or contact the administrator.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Операция отменена по неизвестной причине, дополнительная информация об отмене записана в журнал регистрации.';
													|en = 'The transaction is canceled for an unknown reason. For more information about the cancellation, see the event log.'");
	ИначеЕсли СтатусВозврата = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаВыполнена() Тогда
		РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполнена();
	ИначеЕсли СтатусВозврата = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаВПроцессе() Тогда
		РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииВыполняется();
	Иначе
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неизвестный статус возврата %1';
				|en = 'Unknown return status (%1)'"),
			СтатусВозврата);
	КонецЕсли;
	
	Если РезультатОперации.СтатусОперации  = НастройкиПлатежныхСистемКлиентСервер.СтатусОперацииОшибка() Тогда
		ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкиПараметровПрикладныхОпераций

// Производит валидацию общих параметров оплаты.
//
// Параметры:
//  Документ - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, который отражает операцию в информационной
//    базе.
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка выполнения оплаты.
//  ПараметрыНастройкиПодключения - см. Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ДанныеНастройкиПодключения.
//  РезультатОперации  - Структура - результат выполняемой операции. Должен содержать следующие свойства:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который может быть обработан вызывающим методом.
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Процедура ПроверитьОбщиеНастройкиПодключения(
		Документ,
		НастройкаПодключения,
		ПараметрыНастройкиПодключения,
		РезультатОперации)
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Документ для выполнения операции неопределен. Обратитесь к администратору.';
													|en = 'The transaction document is not defined. Contact the administrator.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Для выполнения операции необходимо записать документ.';
													|en = 'To execute the transaction, save the document.'");
		
		ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
	КонецЕсли;
	
	ПроверитьОбщиеНастройкиПодключенияБезДокументаОперации(
		НастройкаПодключения,
		ПараметрыНастройкиПодключения,
		РезультатОперации);
	
КонецПроцедуры

// Производит валидацию общих параметров оплаты без учета документа-операции.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка выполнения оплаты;
//  ПараметрыНастройкиПодключения - Структура - см. ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения;
//  РезультатОперации  - Структура - результат выполняемой операции. Должен содержать следующие свойства:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который может быть обработан вызывающим методом;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Процедура ПроверитьОбщиеНастройкиПодключенияБезДокументаОперации(
		НастройкаПодключения,
		ПараметрыНастройкиПодключения,
		РезультатОперации)
	
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Не выполнена настройка подключения к Интернет-эквайрингу. Обратитесь к администратору.';
													|en = 'Connection to Online acquiring is not configured. Contact the administrator.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Для выполнения операции через Интернет-эквайринг необходимо выполнить настройку подключения.';
													|en = 'To use Online acquiring, set up the connection.'");
		
		ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
	КонецЕсли;
	
	Если Не ПараметрыНастройкиПодключения.Используется Тогда
		
		РезультатОперации.КодОшибки = "ИнтеграцияНеИспользуется";
		РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Настройка подключения не используется. Обратитесь к администратору.';
													|en = 'Connection setting is not used. Contact the administrator.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Настройка подключения не используется.';
													|en = 'Connection setting is not used.'");
		
		ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыНастройкиПодключения.ИдентификаторУчастника) Тогда
		
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'В настройке подключения не указан участник. Обратитесь к администратору.';
													|en = 'The participant is not specified in the connection settings. Contact the administrator.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'В настройке подключения не указан идентификатор участника';
													|en = 'The participant ID is not specified in the connection settings'");
		
		ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыНастройкиПодключения.ИдентификаторМерчанта) Тогда
		
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'В настройке подключения не указан мерчант. Обратитесь к администратору.';
													|en = 'The merchant is not specified in the connection settings. Contact the administrator.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'В настройке подключения не указан идентификатор мерчанта';
													|en = 'The merchant ID is not specified in the connection settings'");
		
		ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Производит валидацию параметров заказа на возврат.
//
// Параметры:
//  ЗаказНаВозврат - см. ОписаниеЗаказаНаВозврат.
//  РезультатОперации  - Структура - результат выполнения операции. Должен содержать следующие свойства:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который может быть обработан вызывающим методом:
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//
Процедура ПроверитьЗаказНаВозврат(ЗаказНаВозврат, РезультатОперации)
	
	Если Не ЗначениеЗаполнено(ЗаказНаВозврат.СуммаВозврата)
		Или Не ЗначениеЗаполнено(ЗаказНаВозврат.ДатаВозврата) Тогда

		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Некорректно сформирован заказ на возврат. Обратитесь к администратору.';
													|en = 'The order for refund is generated incorrectly. Contact the administrator.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'При проверке заказа на возврат через Интернет-эквайринг возникли ошибки.';
													|en = 'Errors occurred when checking the order for refund via Online acquiring.'");
		
		ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Токены

// Обрабатывает получение токена в фоне.
// Описание результата, помещаемого в хранилище - см. ИнтернетЭквайрингСервис.ОперацияЗапросТокенаАутентификацииСервиса.
//
// Параметры:
//  ПараметрыПроцедуры - Структура - произвольные параметры метода.
//  АдресРезультата - Строка - адрес хранилища результат получения информации.
//
Процедура ЗапросТокенаПриСозданииНастройкиВФоне(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	РезультатОперации = ЗапросТокенаПриСозданииНастройки(ПараметрыПроцедуры);
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Выполняет запрос на получения токена при создании нового подключения.
// 
// Параметры:
//  ПараметрыПроцедуры - Структура - описание новой настройки:
//    * СпособАутентификации - Строка - способ аутентификации участника.
//    * ИдентификаторМерчанта - Строка - идентификатор мерчанта.
//    * ИдентификаторУчастника - Строка - идентификатор участника Интернет-эквайринга.
//    * ПараметрыАутентификации - Соответствие Из КлючИЗначение - параметры аутентификации в системе банка:
//       ** Ключ - Строка - идентификатор параметра аутентификации.
//       ** Значение - Строка - значение параметра аутентификации.
//    * ДополнительныеПараметры - Соответствие Из КлючИЗначение - дополнительные параметры.
//    * ИдентификаторТокена - УникальныйИдентификатор - Идентификатор создаваемого токена.
//
Функция ЗапросТокенаПриСозданииНастройки(ПараметрыПроцедуры)

	ПараметрыНастройки = НовыйПараметрыНастройкиПодключения();
	ПараметрыНастройки.ИдентификаторМерчанта  = ПараметрыПроцедуры.ИдентификаторМерчанта;
	ПараметрыНастройки.ИдентификаторУчастника = ПараметрыПроцедуры.ИдентификаторУчастника;
	ПараметрыНастройки.Используется = Истина;
	
	Возврат ИнтернетЭквайрингСервис.ОперацияЗапросТокенаАутентификацииСервиса(
		ПараметрыНастройки,
		ПараметрыПроцедуры.СпособАутентификации,
		ПараметрыПроцедуры.ПараметрыАутентификации,
		ПараметрыПроцедуры.ИдентификаторТокена,
		ИдентификаторСервисаОнлайнЗаказы(),
		ПараметрыПроцедуры.ДополнительныеПараметры);
	
КонецФункции

// Обрабатывает отзыв токена, если токен был получен для новой настройки, но пользователь закрыл форму до сохранения
// результата.
//
// Параметры:
//  ПараметрыПроцедуры - Структура - произвольные параметры метода.
//  АдресРезультата - Строка - адрес хранилища результат получения информации.
//
Процедура ОтзывТокенаПриСозданииНастройкиВФоне(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	РезультатОперации = ИнтернетЭквайрингСервис.ОперацияОтзывТокеновАутентификацииСервиса(
		ПараметрыПроцедуры.Токены,
		ИдентификаторСервисаОнлайнЗаказы());
	
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Обрабатывает отзыв токена при снятии флага Использовать в настройке подключения к Интернет-эквайрингу
// результата.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения, для
//    которой нужно отозвать токен.
//  АдресРезультата - Строка - адрес хранилища результат получения информации.
//
Процедура ОтозватьТокенПриОтключенииНастройкиВФоне(НастройкаПодключения, АдресРезультата) Экспорт
	
	РезультатОперации = НовыйРезультатОперации();
	
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
		Возврат;
	КонецЕсли;
	
	ДанныеХранилища = НастройкиПодключения(
		НастройкаПодключения,
		Ложь,
		Ложь);
	
	ИмяСервиса = ИдентификаторСервисаОнлайнЗаказы();
	
	ИдентификаторСервиса = НастройкиПлатежныхСистемСлужебный.ИдентификаторСервисаПоИмени(ИмяСервиса);
	
	ДанныеТокена = ДанныеХранилища.ДанныеТокенов.Получить(ИдентификаторСервиса);
	
	ДанныеТокенаСервиса = НовыйОписаниеТокеновСервиса();
	
	Если ДанныеТокена <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ДанныеТокенаСервиса, ДанныеТокена);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеТокенаСервиса.Идентификатор) Тогда
		ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
		Возврат;
	КонецЕсли;
	
	Токены = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		ДанныеТокенаСервиса.Идентификатор);
	
	РезультатОперации = ИнтернетЭквайрингСервис.ОперацияОтзывТокеновАутентификацииСервиса(
		Токены,
		ИмяСервиса);
	
	Если Не ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		
		ДанныеХранилища.ДанныеТокенов.Вставить(ИдентификаторСервиса, Неопределено);
		
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
			НастройкаПодключения,
			ДанныеХранилища,
			КлючХранилищаДанныхАутентификации());
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Обрабатывает запрос токена, при установке флага Использовать в настройке подключения к Интернет-эквайрингу
// результата.
//
// Параметры:
//  ПараметрыПроцедуры - Структура - параметры метода:
//    * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения, для
//      которой нужно получить токен.
//    * ДополнительныеПараметры - Соответствие Из КлючИЗначение - дополнительные параметры запроса токена.
//                              - Неопределено - дополнительные параметры должны быть получены из базы.
//    * ИдентификаторТокена - Строка - идентификатор нового токена.
//  АдресРезультата - Строка - адрес хранилища результат получения информации.
//
Процедура ЗапроситьТокенПриЗаписиНастройкиВФоне(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	РезультатОперации = НовыйРезультатОперации();
	
	Если Не ЗначениеЗаполнено(ПараметрыПроцедуры.НастройкаПодключения) Тогда
		ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
		Возврат;
	КонецЕсли;
	
	ПараметрыНастройки = Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ДанныеНастройкиПодключения(
		ПараметрыПроцедуры.НастройкаПодключения);
	
	РезультатВызова = ИнтернетЭквайрингСервис.ОперацияЗапросТокенаАутентификацииСервиса(
		ПараметрыНастройки,
		Неопределено,
		Неопределено,
		ПараметрыПроцедуры.ИдентификаторТокена,
		ИдентификаторСервисаОнлайнЗаказы(),
		ПараметрыПроцедуры.ДополнительныеПараметры);
	
	ПоместитьВоВременноеХранилище(РезультатВызова, АдресРезультата);
	
КонецПроцедуры

// Сохраняет токен аутентификации сервиса в защищенном хранилище.
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения, для которой
//    выполняется сохранение нового значения токена.
//  Идентификатор - Строка - идентификатор токена в сервисе.
//  Токен - Строка - значение токена
//  ИмяСервиса - Строка - идентификатор сервиса.
// 
// Возвращаемое значение:
//  см. ИнтернетЭквайрингСлужебный.НовыйРезультатОперации.
Функция ЗаписатьТокенСервиса(
		НастройкаПодключения,
		Идентификатор,
		Токен,
		ИмяСервиса) Экспорт
	
	Результат = НовыйРезультатОперации();
	
	ИдентификаторСервиса = НастройкиПлатежныхСистемСлужебный.ИдентификаторСервисаПоИмени(ИмяСервиса);
	
	Если Не (ЗначениеЗаполнено(НастройкаПодключения)
		И ЗначениеЗаполнено(Идентификатор)
		И ЗначениеЗаполнено(Токен)
		И ЗначениеЗаполнено(ИдентификаторСервиса)) Тогда
		
		Результат.КодОшибки = "400";
		Результат.СообщениеОбОшибке = НСтр("ru = 'Не удалось сохранить новый токен аутентификации сервиса.
			|Заполнены не все параметры метода ИнтернетЭквайрингСлужебный.ЗаписатьТокенСервиса';
			|en = 'Cannot save a new service authentication token.
			|Not all the parameters of the ИнтернетЭквайрингСлужебный.ЗаписатьТокенСервиса method are filled'");
		Результат.ИнформацияОбОшибке = Результат.СообщениеОбОшибке;
		
		Возврат Результат;
		
	КонецЕсли;
	
	Попытка
	
		УстановитьПривилегированныйРежим(Истина);
		ТекущиеЗначения = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
			НастройкаПодключения,
			КлючХранилищаДанныхАутентификации());
		УстановитьПривилегированныйРежим(Ложь);
		
		ЭталонСохраненный = НовыйДанныеАутентификации();
		Если ТипЗнч(ТекущиеЗначения) = Тип("Структура")
				Или ТипЗнч(ТекущиеЗначения) = Тип("ФиксированнаяСтруктура") Тогда
			ЗаполнитьЗначенияСвойств(ЭталонСохраненный, ТекущиеЗначения);
		КонецЕсли;
	
		НовыйТокен = НовыйОписаниеТокеновСервиса();
		НовыйТокен.Идентификатор = Идентификатор;
		НовыйТокен.Токен         = Токен;
		
		ЭталонСохраненный.ДанныеТокенов.Вставить(
			ИдентификаторСервиса,
			НовыйТокен);
	
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
			НастройкаПодключения,
			ЭталонСохраненный,
			КлючХранилищаДанныхАутентификации());
		УстановитьПривилегированныйРежим(Ложь);
	
	Исключение
		
		ЗаписатьИнформациюВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось сохранить новый токен аутентификации сервиса:
						|%1.';
						|en = 'Cannot save a new service authentication token:
						|%1.'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
		
		Результат.КодОшибки = "500";
		Результат.СообщениеОбОшибке = НСтр("ru = 'Не удалось сохранить новый токен аутентификации сервиса.
			|Обратитесь к администратору.';
			|en = 'Cannot save a new service authentication token.
			|Contact the administrator.'");
		Результат.ИнформацияОбОшибке = НСтр("ru = 'Не удалось сохранить новый токен аутентификации сервиса.
			|Подробности смотрите в журнале регистрации.';
			|en = 'Cannot save a new service authentication token.
			|For more information, see the event log.'");
		
		Возврат Результат;
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Необходимо обновить токен.
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения по которой
//    необходимо обновить токен.
//  ТекущиеДанные - см. ИнтернетЭквайрингСлужебный.ЗначенияДинамическихРеквизитов - Текущие значения динамических
//    реквизитов настройки.
//  Используется - Булево - текущее значение признака использования настройки подключения.
// 
// Возвращаемое значение:
//  Булево - Необходимо обновить токен
Функция НеобходимоОбновитьТокен(
		НастройкаПодключения,
		ТекущиеДанные,
		Используется) Экспорт
	
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		Возврат Истина;
	КонецЕсли;
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		НастройкаПодключения,
		"Используется");
	
	Если Используется <> ЗначенияРеквизитов.Используется Тогда
		Возврат Истина;
	КонецЕсли;
	
	ДанныеХранилища = НастройкиПодключения(НастройкаПодключения);
	
	ЭталонСохраненный = НовыйДанныеАутентификации();
	Если ТипЗнч(ДанныеХранилища) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭталонСохраненный, ДанныеХранилища);
	КонецЕсли;
	
	ПараметрыАутентификации = Новый Соответствие;
	Для Каждого ОписаниеПараметра Из ЭталонСохраненный.ПараметрыАутентификации Цикл
		ПараметрыАутентификации.Вставить(
			ОписаниеПараметра.Идентификатор,
			ОписаниеПараметра.Значение);
	КонецЦикла;
	
	Если Не ОбщегоНазначения.ДанныеСовпадают(
		ТекущиеДанные.НастройкиАутентификации,
		ПараметрыАутентификации) Тогда
		Возврат Истина;
	КонецЕсли;

	ДополнительныеПараметры = Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ДополнительныеПараметрыНастройки(
		НастройкаПодключения,
		Ложь);

	Если Не ИнтернетЭквайрингКлиентСервер.ДанныеСовпадают(
		ТекущиеДанные.ДополнительныеПараметры,
		ДополнительныеПараметры,
		"ПредставлениеЗначения") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Выполняет поиск идентификатора сохраненного токена аутентификации сервиса онлайн заказов.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения, для которой
//    необходимо найти токен.
//  ИмяСервиса - Строка - идентификатор сервиса.
// 
// Возвращаемое значение:
//  Строка - идентификатор сохраненного токена.
//  Неопределено - нет сохраненного токена.
//
Функция ИдентификаторСохраненногоТокена(НастройкаПодключения, ИмяСервиса) Экспорт
	
	ИдентификаторСервиса = НастройкиПлатежныхСистемСлужебный.ИдентификаторСервисаПоИмени(ИмяСервиса);
	
	Если Не ЗначениеЗаполнено(ИдентификаторСервиса) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеХранилища = НастройкиПодключения(
		НастройкаПодключения,
		Истина,
		Ложь);
	
	ДанныеТокена = ДанныеХранилища.ДанныеТокенов[ИдентификаторСервиса];
	
	Если ТипЗнч(ДанныеТокена) <> Тип("Структура") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеТокенаСервиса = НовыйОписаниеТокеновСервиса();
	ЗаполнитьЗначенияСвойств(ДанныеТокенаСервиса, ДанныеТокена);
	
	Возврат ДанныеТокенаСервиса.Идентификатор;
	
КонецФункции

// Формирует представление, которое будет отображаться в списке выбора при заполнении дополнительных параметров при
//   получении токена.
//
// Параметры:
//  Значение - Произвольный - значение параметра.
//  Представление - Строка - представление параметра, полученное из сервиса.
//
// Возвращаемое значение:
//  Строка - обработанное представление.
//
Функция ПредставлениеЗначенияТребуемойНастройки(Знач Значение, Знач Представление)
	
	// При изменении метода доработать функцию УбратьЗначениеИзПредставленияТребуемойНастройки.
	
	Значение = Строка(Значение);
	Если СтрДлина(Значение) > 20 Тогда
		Значение = Прав(Значение, 20) + "...";
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 - %2';
			|en = '%1 - %2'"),
		Значение,
		Представление);
	
КонецФункции

// Удаляет из представления требуемой настройки значение. Функция выполняет действия, обратные методу
// ПредставлениеЗначенияТребуемойНастройки.
// 
// Параметры:
//  Значение - Произвольный - значение параметра.
//  Представление - Строка - представление настройки.
// 
// Возвращаемое значение:
//  Строка - результат преобразования.
//
Функция УбратьЗначениеИзПредставленияТребуемойНастройки(Знач Значение, Знач Представление)
	
	ЗначениеВПредставлении = Строка(Значение) + " - ";
	Результат = СтрЗаменить(Представление, ЗначениеВПредставлении, "");
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область НастройкиПодключения

// Получает актуальный список доступных участников Интернет-эквайринга.
// Описание результата, помещаемого в хранилище - см. ПолучитьУчастников
//
// Параметры:
//  ПараметрыПроцедуры - Структура - произвольные параметры метода.
//  АдресРезультата - Строка - адрес хранилища результат получения информации.
//
Процедура ПолучитьУчастниковВФоне(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	РезультатОперации = ПолучитьУчастников();
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Формирует список доступных участников Интернет-эквайринга на основе данных из сервиса.
// 
// Возвращаемое значение:
//  Структура - результат получения списка доступных участников Интернет-эквайринга:
//    * КодОшибки - Строка - строковый код возникшей ошибки.
//    * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//    * ДанныеУчастников - Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыУчастника
Функция ПолучитьУчастников()
	
	Результат = НовыйРезультатОперации();
	Результат.Вставить("ДанныеУчастников", Новый Массив);
	
	РезультатВызова = ИнтернетЭквайрингСервис.ПолучитьУчастников();
	
	Если ЗначениеЗаполнено(РезультатВызова.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(
			Результат,
			РезультатВызова,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат Результат;
	КонецЕсли;
	
	Результат.ДанныеУчастников = РезультатВызова.ДанныеУчастников;
	
	Возврат Результат;
	
КонецФункции
	
// Выполняет сохранение настроек параметров аутентификации в безопасном хранилище.
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка, для которой выполняется
//    сохранение параметров аутентификации.
//  ДанныеАутентификации - см. ИнтернетЭквайрингСлужебный.НовыйДанныеАутентификации.
//  ТокеныАутентификации - Соответствие Из КлючИЗначение - токены аутентификации в различных сервисах:
//    * Ключ - Строка - имя сервиса.
//    * Значение - Структура - описание токена:
//      ** Идентификатор - УникальныйИдентификатор - Уникальный идентификатор токена.
//      ** Токен - Строка - Значение токена.
//
// Возвращаемое значение:
//  Структура - результат сохранения:
//    * Отказ - Булево - Истина, если в процессе сохранения возникли ошибки.
//    * СообщениеОбОшибке - Строка - краткое представление ошибки.
//    * ПараметрыАутентификацииИзменены - Булево - Истина, если сохраненные параметры отличаются от записываемых.
//  
Функция СохранитьНастройкиАутентификации(
		НастройкаПодключения,
		ДанныеАутентификации,
		ТокеныАутентификации) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Отказ",                           Ложь);
	Результат.Вставить("СообщениеОбОшибке",               "");
	Результат.Вставить("ПараметрыАутентификацииИзменены", Ложь);
	
	Ошибки = Новый Массив;
	
	Эталон = НовыйДанныеАутентификации();
	Если ТипЗнч(ДанныеАутентификации) = Тип("Структура")
		Или ТипЗнч(ДанныеАутентификации) = Тип("ФиксированнаяСтруктура") Тогда
		
		ЗаполнитьЗначенияСвойств(Эталон, ДанныеАутентификации);
		
		Если Не ЗначениеЗаполнено(Эталон.СпособАутентификации) Тогда
			Ошибки.Добавить(
				НСтр("ru = 'Не указан способ аутентификации';
					|en = 'The authentication method is not specified'"));
		КонецЕсли;
		
	Иначе
		Ошибки.Добавить(
			НСтр("ru = 'Передано недопустимое значение параметра ""ДанныеАутентификации""';
				|en = 'Invalid value of the ДанныеАутентификации parameter is passed'"));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		Ошибки.Добавить(
			НСтр("ru = 'Не указана настройка';
				|en = 'Settings are not specified'"));
	КонецЕсли;

	Если Ошибки.Количество() <> 0 Тогда
		Результат.Отказ = Истина;
		Результат.СообщениеОбОшибке = СтрСоединить(Ошибки, Символы.ПС);
		Возврат Результат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ТекущиеЗначения = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		НастройкаПодключения,
		КлючХранилищаДанныхАутентификации());
	УстановитьПривилегированныйРежим(Ложь);

	ЭталонСохраненный = НовыйДанныеАутентификации();
	ЗначенияСохраненных = Новый Соответствие;
	Если ТипЗнч(ТекущиеЗначения) = Тип("Структура")
			Или ТипЗнч(ТекущиеЗначения) = Тип("ФиксированнаяСтруктура") Тогда
		ЗаполнитьЗначенияСвойств(ЭталонСохраненный, ТекущиеЗначения);
		Для Каждого ПараметрАутентификации Из ЭталонСохраненный.ПараметрыАутентификации Цикл
			ЗначенияСохраненных.Вставить(
				ПараметрАутентификации.Идентификатор,
				ПараметрАутентификации.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ПараметрАутентификации Из Эталон.ПараметрыАутентификации Цикл
		Если ПараметрАутентификации.Значение = СтрокаСекретныхДанныхПоУмолчанию() Тогда
			ПараметрАутентификации.Значение = ЗначенияСохраненных.Получить(
				ПараметрАутентификации.Идентификатор);
		КонецЕсли;
	КонецЦикла;
	
	Результат.ПараметрыАутентификацииИзменены = ПараметрыАутентификацииИзменены(
		Эталон,
		ЭталонСохраненный.СпособАутентификации,
		ЗначенияСохраненных);
	
	Эталон.ДанныеТокенов = ЭталонСохраненный.ДанныеТокенов;
	
	Для Каждого ДанныеТокена Из ТокеныАутентификации Цикл
		Эталон.ДанныеТокенов.Вставить(
			ДанныеТокена.Ключ,
			ДанныеТокена.Значение);
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
		НастройкаПодключения,
		Эталон,
		КлючХранилищаДанныхАутентификации());
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

// Обновляет прикладные параметры настройки подключения.
//
// Параметры:
//  НастройкиПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка, для которой выполняется
//    сохранение прикладных параметров.
//  ПрикладныеНастройки - Соответствие Из КлючИЗначение - значения прикладных настроек:
//   * Ключ - Строка - идентификатор реквизита (соответствует имени измерения, ресурса или реквизита прикладного
//       регистра, определяемого в методе ИнтернетЭквайрингПереопределяемый.ПриОпределенииНастроекПодключения).
//   * Значение - Произвольный - значение реквизита.
//  
// Возвращаемое значение:
//  Структура - результат сохранения:
//    * Отказ - Булево - Истина, если в процессе сохранения возникли ошибки.
//    * СообщениеОбОшибке - Строка - краткое представление ошибки.
//  
Функция СохранитьПрикладныеНастройки(
		НастройкиПодключения,
		ПрикладныеНастройки) Экспорт
		
	Результат = Новый Структура;
	Результат.Вставить("Отказ",             Ложь);
	Результат.Вставить("СообщениеОбОшибке", "");
	
	ИнтернетЭквайрингСобытия.ПриФормированииПрикладныхПараметровПередЗаписью(
		НастройкиПодключения,
		ПрикладныеНастройки);
	
	Попытка
		
		ИнтеграцияПодсистемБИП.ПриЗаписиНастроекПодключенияЭквайринг(
			ПрикладныеНастройки,
			Результат.Отказ,
			Результат.СообщениеОбОшибке);
		ИнтернетЭквайрингПереопределяемый.ПриЗаписиНастроекПодключения(
			ПрикладныеНастройки,
			Результат.Отказ,
			Результат.СообщениеОбОшибке);
			
	Исключение
		ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		Результат.Отказ             = Истина;
		Результат.СообщениеОбОшибке = ОбработкаОшибок.КраткоеПредставлениеОшибки(
			ИнформацияОбОшибке());
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Формирует описание настройки подключения.
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - ссылка на элемент справочника,
//    для которого нужно получить настройки.
//  СкрыватьПароли - Булево - если Истина, то значение полей, для которых установлен вывод в режиме пароля, заменяются
//    на значение идентификатора из метода СтрокаСекретныхДанныхПоУмолчанию.
//  СкрыватьТокены - Булево - если Истина, то значение полей Идентификатор и Токен, заменяются на значение
//    идентификатора из метода СтрокаСекретныхДанныхПоУмолчанию.
// 
// Возвращаемое значение:
//  см. НовыйДанныеАутентификации
//
Функция НастройкиПодключения(
		НастройкаПодключения,
		СкрыватьПароли = Истина,
		СкрыватьТокены = Истина)
	
	Результат = НовыйДанныеАутентификации();

	УстановитьПривилегированныйРежим(Истина);
	ДанныеХранилища = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		НастройкаПодключения,
		КлючХранилищаДанныхАутентификации());
	УстановитьПривилегированныйРежим(Ложь);
	
	Эталон = НовыйДанныеАутентификации();
	
	Если ТипЗнч(ДанныеХранилища) = Тип("Структура")
		Или ТипЗнч(ДанныеХранилища) = Тип("ФиксированнаяСтруктура") Тогда
		ЗаполнитьЗначенияСвойств(Эталон, ДанныеХранилища);
	КонецЕсли;
	
	ИдентификаторПоУмолчанию = СтрокаСекретныхДанныхПоУмолчанию();
	
	// Приводим структуру сохраненных значений к эталонному виду.
	Если ТипЗнч(Эталон.ПараметрыАутентификации) = Тип("Массив") Тогда
		Для Каждого ОписаниеПоля Из Эталон.ПараметрыАутентификации Цикл
			
			ПараметрАутентификации = ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации();
			ЗаполнитьЗначенияСвойств(ПараметрАутентификации, ОписаниеПоля);
			
			Если СкрыватьПароли
				И ОписаниеПоля.РежимПароля
				И ЗначениеЗаполнено(ОписаниеПоля.Значение) Тогда
				ПараметрАутентификации.Значение = ИдентификаторПоУмолчанию;
			КонецЕсли;
			
			Результат.ПараметрыАутентификации.Добавить(ПараметрАутентификации);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если СкрыватьТокены Тогда
		ДанныеТокенов = Новый Соответствие;
		Для Каждого КлючИЗначение Из Эталон.ДанныеТокенов Цикл
			СкрытыйТокен = НовыйОписаниеТокеновСервиса();
			СкрытыйТокен.Идентификатор = ИдентификаторПоУмолчанию;
			СкрытыйТокен.Токен         = ИдентификаторПоУмолчанию;
			ДанныеТокенов.Вставить(КлючИЗначение.Ключ, СкрытыйТокен);
		КонецЦикла;
		Результат.ДанныеТокенов = ДанныеТокенов;
	Иначе
		Результат.ДанныеТокенов = Эталон.ДанныеТокенов;
	КонецЕсли;
	
	Результат.СпособАутентификации = Эталон.СпособАутентификации;
	
	Возврат Результат;
	
КонецФункции

// Формирует описание настройки подключения. Все пароли и значения токенов аутентификации заменяются на идентификатор
//   из метода СтрокаСекретныхДанныхПоУмолчанию.
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - ссылка на элемент справочника,
//    для которого нужно получить настройки.
// 
// Возвращаемое значение:
//  см. НовыйДанныеАутентификации
//
Функция НастройкиПодключенияСкрытые(НастройкаПодключения) Экспорт
	
	Возврат НастройкиПодключения(
		НастройкаПодключения,
		Истина,
		Истина);
	
КонецФункции

// Подготовить параметры аутентификации для записи.
// 
// Параметры:
//  НастройкиАутентификации - Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации,
//    Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйОписаниеДинамическогоРеквизита - описание полей аутентификации.
//  ЗначенияНастроек - Соответствие Из КлючИЗначение - значения настроек:
//    * Ключ - Строка - идентификатор настройки
//    * Значение - Произвольный - значение настройки
// 
// Возвращаемое значение:
//  Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации - подготовленные для записи
///   параметры аутентификации.
//
Функция ПодготовитьПараметрыАутентификацииДляЗаписи(
		НастройкиАутентификации,
		ЗначенияНастроек) Экспорт
	
	ПараметрыАутентификации = Новый Массив;

	Для Каждого ОписаниеПоля Из НастройкиАутентификации Цикл
		
		НовыйПараметр = ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации();
		
		ЗаполнитьЗначенияСвойств(
			НовыйПараметр,
			ОписаниеПоля);
		
		НовыйПараметр.Значение =
			ЗначенияНастроек.Получить(
				ОписаниеПоля.Идентификатор);
		
		ПараметрыАутентификации.Добавить(НовыйПараметр);
		
	КонецЦикла;
	
	Возврат ПараметрыАутентификации;
	
КонецФункции

// Получает параметры участника Интернет-эквайринга по указанному идентификатору и помещает их во временное хранилище.
// Описание результата, помещаемого в хранилище - см. ПараметрыУчастникаПоИдентификатору
//
// Параметры:
//  ПараметрыПроцедуры - Структура - параметры метода:
//    * ИдентификаторУчастника - Строка - идентификатор участника, данные по которому нужно получить из сервиса.
//  АдресРезультата - Строка - адрес хранилища результат получения информации.
//
Процедура ПараметрыУчастникаПоИдентификаторуВФоне(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	РезультатОперации = ПараметрыУчастникаПоИдентификатору(
		ПараметрыПроцедуры.ИдентификаторУчастника);
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Получает параметры участника Интернет-эквайринга по указанному идентификатору.
// 
// Параметры:
//  ИдентификаторУчастника - Строка - идентификатор участника, данные по которому нужно получить из сервиса.
// 
// Возвращаемое значение:
//  Структура - результат обработки:
//   * КодОшибки - Строка - Код ошибки. Пустая строка, если в процессе обработки ошибок не возникло.
//   * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - Сообщение об ошибке для обычного пользователя.
//   * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - Сообщение об ошибке для администратора.
//   * ПараметрыУчастника - см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыУчастника.
//   
Функция ПараметрыУчастникаПоИдентификатору(ИдентификаторУчастника)
	
	Результат = НовыйРезультатОперации();
	Результат.Вставить(
		"ПараметрыУчастника",
		ИнтернетЭквайрингКлиентСервер.НовыйПараметрыУчастника());
	
	РезультатВызова = ИнтернетЭквайрингСервис.ПолучитьУчастников();
	
	Если ЗначениеЗаполнено(РезультатВызова.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(
			Результат,
			РезультатВызова,
			"КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
		Возврат Результат;
	КонецЕсли;
	
	УчастникНайден = Ложь;
	Для Каждого ДанныеУчастника Из РезультатВызова.ДанныеУчастников Цикл
		Если ИдентификаторУчастника = ДанныеУчастника.Идентификатор Тогда
			Результат.Вставить(
				"ПараметрыУчастника",
				ДанныеУчастника);
			УчастникНайден = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не УчастникНайден Тогда
		Результат.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеизвестнаяОшибка();
		Результат.ИнформацияОбОшибке = НСтр("ru = 'Не удалось найти параметры настройки подключения.
			|Возможно работа с указанным участником Интернет-эквайринга больше не поддерживается.';
			|en = 'Cannot find the connection setting parameters.
			|The operations with the specified Online acquiring participant might be no longer supported.'");
		Результат.СообщениеОбОшибке = Результат.ИнформацияОбОшибке;
	КонецЕсли;
	
	Возврат Результат; 
	
КонецФункции

// Сравнивает значения записываемых и сохраненных параметров аутентификации.
//
// Параметры:
//  НовоеЗначение - см. ИнтернетЭквайрингСлужебный.НовыйДанныеАутентификации. 
//  СпособАутентификации - Строка - сохраненное значение способа аутентификации.
//  ЗначенияСохраненных - Соответствие Из КлючИЗначение - сохраненные значения параметров аутентификации.
//  
// Возвращаемое значение:
//  Булево - Истина, если сохраняемые параметры отличаются; Ложь, если совпадают.
//  
Функция ПараметрыАутентификацииИзменены(
		НовоеЗначение,
		СпособАутентификации,
		ЗначенияСохраненных)
	
	Если НовоеЗначение.СпособАутентификации <> СпособАутентификации Тогда
		Возврат Истина;
	КонецЕсли;
	
	Для Каждого ОписаниеПараметра Из НовоеЗначение.ПараметрыАутентификации Цикл
		Если ОписаниеПараметра.Значение <> ЗначенияСохраненных.Получить(ОписаниеПараметра.Идентификатор) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ДинамическиеРеквизиты

// Формирует поля для ввода параметров аутентификации в системе Интернет-эквайринга.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, на которую нужно вывести поля аутентификации. На формы должен быть
//    реквизит ДинамическиеРеквизиты.
//  Родитель - Форма, ГруппаФормы - форма или группа, внутри которой нужно разместить новые элементы.
//  ПараметрыАутентификации - Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации.
//  СохраняемыеДанные - Булево - если Истина, то для всех созданных реквизитов будет установлен флаг "Сохраняемые
//    данные".
//
Процедура ВывестиПараметрыАутентификации(
		Форма,
		Родитель,
		ПараметрыАутентификации,
		СохраняемыеДанные = Ложь) Экспорт
	
	Эталон = ИнтернетЭквайрингКлиентСервер.НовыйДинамическиеРеквизитыПодключения();
	
	Если ТипЗнч(Форма.ДинамическиеРеквизиты) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Эталон, Форма.ДинамическиеРеквизиты);
	Иначе
		Форма.ДинамическиеРеквизиты = Эталон;
	КонецЕсли;

	УдаляемыеРеквизиты = Новый Массив;
	
	Для Каждого ОписаниеРеквизита Из Эталон.НастройкиАутентификации Цикл
		
		ОписаниеЭталон = ИнтернетЭквайрингКлиентСервер.НовыйОписаниеДинамическогоРеквизита();
		ЗаполнитьЗначенияСвойств(ОписаниеЭталон, ОписаниеРеквизита);
		
		Если ЗначениеЗаполнено(ОписаниеЭталон.ИмяРеквизита) Тогда
			УдаляемыеРеквизиты.Добавить(ОписаниеЭталон.ИмяРеквизита);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОписаниеЭталон.ИмяЭлемента) Тогда
			Форма.Элементы.Удалить(Форма.Элементы[ОписаниеЭталон.ИмяЭлемента]);
			Эталон.РеквизитПоИмениЭлемента.Удалить(ОписаниеЭталон.ИмяЭлемента);
			Эталон.ИдентификаторПоИмениЭлемента.Удалить(ОписаниеЭталон.ИмяЭлемента);
		КонецЕсли;
		
	КонецЦикла;
	
	Эталон.НастройкиАутентификации.Очистить();
	
	ДобавляемыеРеквизиты    = Новый Массив;
	ДобавляемыеЭлементы     = Новый Массив;
	НастройкиАутентификации = Новый Массив;
	
	Для Каждого ОписаниеРеквизита Из ПараметрыАутентификации Цикл
		ПолеЭталон = ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации();
		ЗаполнитьЗначенияСвойств(ПолеЭталон, ОписаниеРеквизита);
		
		УИД = СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-", "");
		ИмяРеквизита = "РеквизитАутентификации_" + УИД;
		ИмяЭлемента  = "ПолеАутентификации_" + УИД;

		ТипПоля = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(ПолеЭталон.ДлинаСтроки));

		ДобавляемыеРеквизиты.Добавить(
			Новый РеквизитФормы(
				ИмяРеквизита,
				ТипПоля,
				,
				ПолеЭталон.Представление,
				СохраняемыеДанные));
		
		ОписаниеЭлемента = Новый Структура();
		ОписаниеЭлемента.Вставить("ИмяЭлемента",        ИмяЭлемента);
		ОписаниеЭлемента.Вставить("ИмяРеквизита",       ИмяРеквизита);
		ОписаниеЭлемента.Вставить("РежимПароля",        ПолеЭталон.РежимПароля);
		ОписаниеЭлемента.Вставить("Значение",           ПолеЭталон.Значение);
		ОписаниеЭлемента.Вставить("ПроверкаЗаполнения", Истина);
		
		ДобавляемыеЭлементы.Добавить(ОписаниеЭлемента);
		
		НовоеОписание = ИнтернетЭквайрингКлиентСервер.НовыйОписаниеДинамическогоРеквизита();
		НовоеОписание.Идентификатор            = ПолеЭталон.Идентификатор;
		НовоеОписание.Представление            = ПолеЭталон.Представление;
		НовоеОписание.ИмяРеквизита             = ИмяРеквизита;
		НовоеОписание.ИмяЭлемента              = ИмяЭлемента;
		НовоеОписание.ЭтоИдентификаторМерчанта = ПолеЭталон.ЭтоИдентификаторМерчанта;
		НовоеОписание.РежимПароля              = ПолеЭталон.РежимПароля;
		НовоеОписание.Тип                      = ТипПоля;
		НовоеОписание.ДлинаСтроки              = ПолеЭталон.ДлинаСтроки;
		
		НастройкиАутентификации.Добавить(НовоеОписание);
		
		Эталон.РеквизитПоИмениЭлемента.Вставить(ИмяЭлемента, ИмяРеквизита);
		Эталон.ИдентификаторПоИмениЭлемента.Вставить(ИмяЭлемента, ПолеЭталон.Идентификатор);
		
	КонецЦикла;
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты, УдаляемыеРеквизиты);
	
	Для Каждого ОписаниеЭлемента Из ДобавляемыеЭлементы Цикл
		
		НовыйЭлемент = Форма.Элементы.Добавить(ОписаниеЭлемента.ИмяЭлемента, Тип("ПолеФормы"), Родитель);
		НовыйЭлемент.Вид                       = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным               = ОписаниеЭлемента.ИмяРеквизита;
		НовыйЭлемент.РежимПароля               = ОписаниеЭлемента.РежимПароля;
		НовыйЭлемент.АвтоОтметкаНезаполненного = ОписаниеЭлемента.ПроверкаЗаполнения;
		
		НовыйЭлемент.УстановитьДействие(
			"ПриИзменении",
			"Подключаемый_ПриИзмененииНастройкиАутентификации");
		НовыйЭлемент.УстановитьДействие(
			"НачалоВыбора",
			"Подключаемый_НачалоВыбораНастройкиАутентификации");
		
		Форма[ОписаниеЭлемента.ИмяРеквизита] = ОписаниеЭлемента.Значение;
		
	КонецЦикла;
	
	Форма.ДинамическиеРеквизиты.Вставить(
		"НастройкиАутентификации",
		НастройкиАутентификации);
	Форма.ДинамическиеРеквизиты.Вставить(
		"РеквизитПоИмениЭлемента",
		Эталон.РеквизитПоИмениЭлемента);
	Форма.ДинамическиеРеквизиты.Вставить(
		"ИдентификаторПоИмениЭлемента",
		Эталон.ИдентификаторПоИмениЭлемента);
	
КонецПроцедуры

// Выводит дополнительные настройки аутентификации на форму
// 
// Параметры:
//  Параметры - см. НовыйПараметрыВыводаДополнительныхНастроек.
Процедура ВывестиДополнительныеНастройкиАутентификации(Параметры) Экспорт
	
	ИзменениеЭлементов = Новый Структура;
	
	ИзменениеЭлементов.Вставить("УдаляемыеРеквизиты",  Новый Массив);
	ИзменениеЭлементов.Вставить("УдаляемыеЭлементы",    Новый Массив);
	ИзменениеЭлементов.Вставить("ДобавляемыеРеквизиты", Новый Массив);
	ИзменениеЭлементов.Вставить("ДобавляемыеЭлементы",  Новый Массив);

	ДополнительныеПараметры = Параметры.ДополнительныеПараметры;
	Форма = Параметры.Форма;

	ТекущиеЗначения = Новый Соответствие;
	Для Каждого ОписаниеРеквизита Из ДополнительныеПараметры Цикл
		ТекущиеЗначения.Вставить(
			ОписаниеРеквизита.Идентификатор,
			Форма[ОписаниеРеквизита.ИмяРеквизита]);
	КонецЦикла;
	
	Если Параметры.СоздаватьРеквизиты Тогда

		Для Каждого ОписаниеРеквизита Из ДополнительныеПараметры Цикл
			Если ЗначениеЗаполнено(ОписаниеРеквизита.ИмяРеквизита) Тогда
				ИзменениеЭлементов.УдаляемыеРеквизиты.Добавить(ОписаниеРеквизита.ИмяРеквизита);
			КонецЕсли;
			Если ЗначениеЗаполнено(ОписаниеРеквизита.ИмяЭлемента) Тогда
				ИзменениеЭлементов.УдаляемыеЭлементы.Добавить(ОписаниеРеквизита.ИмяЭлемента);
			КонецЕсли;
		КонецЦикла;
		
		ДополнительныеПараметры.Очистить();
		
		Для Каждого ОписаниеНастройки Из Параметры.НовыеЗначения Цикл
			
			ОписаниеРеквизита = ИнтернетЭквайрингКлиентСервер.НовыйОписаниеДинамическогоРеквизита();
			ОписаниеРеквизита.Идентификатор = ОписаниеНастройки.Идентификатор;
			ОписаниеРеквизита.Представление = ОписаниеНастройки.Представление;
			
			ОписаниеРеквизита.ИмяРеквизита = "ТребуемыеНастройки_" + ОписаниеНастройки.Идентификатор;
			ОписаниеРеквизита.ИмяЭлемента  = "ТребуемыеНастройки_" + ОписаниеНастройки.Идентификатор;
			ОписаниеРеквизита.ПроверкаЗаполнения = Истина;
			
			ОписаниеЭлемента = Новый Структура;
			ОписаниеЭлемента.Вставить("Идентификатор", ОписаниеРеквизита.Идентификатор);
			ОписаниеЭлемента.Вставить("ИмяРеквизита",  ОписаниеРеквизита.ИмяРеквизита);
			ОписаниеЭлемента.Вставить("ИмяЭлемента",   ОписаниеРеквизита.ИмяЭлемента);
			ОписаниеЭлемента.Вставить("Значения",      ОписаниеНастройки.ДоступныеЗначения);
			ОписаниеЭлемента.Вставить("Значение",      ОписаниеНастройки.Значение);
			ОписаниеЭлемента.Вставить("Представление", ОписаниеРеквизита.Представление);
			ОписаниеЭлемента.Вставить("Видимость",     ОписаниеНастройки.ТребуемыйПараметр);
			
			ИзменениеЭлементов.ДобавляемыеЭлементы.Добавить(ОписаниеЭлемента);
			ИзменениеЭлементов.ДобавляемыеРеквизиты.Добавить(ОписаниеРеквизита.ИмяРеквизита);

			ДополнительныеПараметры.Добавить(ОписаниеРеквизита);
		КонецЦикла;
		
	Иначе
		
		Для Каждого ОписаниеРеквизита Из ДополнительныеПараметры Цикл
			Для Каждого ОписаниеНастройки Из Параметры.НовыеЗначения Цикл
				Если ОписаниеНастройки.Идентификатор = ОписаниеРеквизита.Идентификатор Тогда
					Форма[ОписаниеРеквизита.ИмяРеквизита] = ОписаниеНастройки.Значение;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
	Если ИзменениеЭлементов.УдаляемыеРеквизиты.Количество() > 0
		Или ИзменениеЭлементов.ДобавляемыеРеквизиты.Количество() > 0
		Или ИзменениеЭлементов.ДобавляемыеЭлементы.Количество() > 0
		Или ИзменениеЭлементов.УдаляемыеЭлементы.Количество() > 0 Тогда
			
		ОбновитьДополнительныеПараметры(
			Форма,
			ИзменениеЭлементов,
			ТекущиеЗначения,
			Форма.Элементы[Параметры.ИмяГруппы],
			Параметры.Загрузка,
			Параметры.СохраняемыеДанные);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет удаление старых реквизитов и элементов формы, относящиеся к дополнительных настройкам аутентификации и
// создает новые.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма на которую будут выводится настройки.
//  ИзменениеЭлементов - Структура - описание изменяемых элементов:
//    * ДобавляемыеРеквизиты - Массив Из Строка - имена добавляемых реквизитов.
//    * УдаляемыеРеквизиты - Массив Из Строка - имена удаляемых реквизитов.
//    * УдаляемыеЭлементы - Массив Из Строка - имена удаляемых элементов.
//    * ДобавляемыеЭлементы - Массив Из Структура - описание добавляемых элементов.
//  ТекущиеЗначения - Соответствие Из КлючИЗначение - текущие значения дополнительных параметров
//  Родитель - ГруппаФормы - группа, в которой необходимо создать новые элементы.
//  Загрузка - Булево - Истина, если выполняется загрузка значений реквизитов из базы.
//  СохраняемыеДанные - Булево - Истина, если для новых элементов необходимо установить признак изменения
//    сохраняемых данных.
Процедура ОбновитьДополнительныеПараметры(
		Форма,
		ИзменениеЭлементов,
		ТекущиеЗначения,
		Родитель,
		Загрузка,
		СохраняемыеДанные)

	Элементы = Форма.Элементы;
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для Каждого ИмяРеквизита Из ИзменениеЭлементов.ДобавляемыеРеквизиты Цикл
		ДобавляемыеРеквизиты.Добавить(
			Новый РеквизитФормы(
				ИмяРеквизита,
				Новый ОписаниеТипов("Строка"),
				,
				,
				СохраняемыеДанные));
	КонецЦикла;

	Если ИзменениеЭлементов.УдаляемыеРеквизиты.Количество() > 0
		Или ДобавляемыеРеквизиты.Количество() > 0 Тогда
		Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты, ИзменениеЭлементов.УдаляемыеРеквизиты);
	КонецЕсли;
	
	Для Каждого ИмяЭлемента Из ИзменениеЭлементов.УдаляемыеЭлементы Цикл
		Элементы.Удалить(Элементы[ИмяЭлемента]);
	КонецЦикла;
	
	Для Каждого ОписаниеЭлемента Из ИзменениеЭлементов.ДобавляемыеЭлементы Цикл
		
		НовоеПоле = Элементы.Добавить(
			ОписаниеЭлемента.ИмяРеквизита,
			Тип("ПолеФормы"),
			Родитель);
		НовоеПоле.Вид                      = ВидПоляФормы.ПолеВвода;
		НовоеПоле.ПутьКДанным              = ОписаниеЭлемента.ИмяРеквизита;
		НовоеПоле.Заголовок                = ОписаниеЭлемента.Представление;
		НовоеПоле.Видимость                = ОписаниеЭлемента.Видимость;
		НовоеПоле.КнопкаСпискаВыбора       = Истина;
		НовоеПоле.КнопкаОчистки            = Истина;
		НовоеПоле.РежимВыбораИзСписка      = Истина;
		
		ТекущееЗначение = ТекущиеЗначения.Получить(ОписаниеЭлемента.Идентификатор);
	
		Если ТипЗнч(ОписаниеЭлемента.Значения) = Тип("Соответствие") Тогда
			Для Каждого КлючЗначение Из ОписаниеЭлемента.Значения Цикл
				ПредставлениеЗначения = ПредставлениеЗначенияТребуемойНастройки(
					КлючЗначение.Ключ,
					КлючЗначение.Значение);
				НовоеПоле.СписокВыбора.Добавить(
					КлючЗначение.Ключ,
					ПредставлениеЗначения);
				Если Загрузка Тогда
					Форма[ОписаниеЭлемента.ИмяРеквизита] = ОписаниеЭлемента.Значение;
				ИначеЕсли ТекущееЗначение = КлючЗначение.Ключ Тогда
					Форма[ОписаниеЭлемента.ИмяРеквизита] = ТекущееЗначение;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры

// Возвращает текущие значения программно созданных реквизитов.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, на которую выведены реквизиты.
// 
// Возвращаемое значение:
//  Структура - значения реквизитов:
//    * НастройкиАутентификации - Соответствие Из КлючИЗначение - значения параметров аутентификации:
//        ** Ключ - Строка - идентификатор параметра.
//        ** Значение - Строка - значение параметра
//    * ПрикладныеНастройки - Соответствие Из КлючИЗначение - значения прикладных настроек:
//        ** Ключ - Строка - идентификатор реквизита.
//        ** Значение - Произвольный - значение реквизита.
//    * ДополнительныеПараметры - Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации -
//        значения дополнительных настроек аутентификации.
//    * ИдентификаторМерчанта - Строка - идентификатор мерчанта.
//
Функция ЗначенияДинамическихРеквизитов(Форма) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НастройкиАутентификации", Новый Соответствие);
	Результат.Вставить("ПрикладныеНастройки",     Новый Соответствие);
	Результат.Вставить("ДополнительныеПараметры", Новый Массив);
	Результат.Вставить("ИдентификаторМерчанта",   "");
	
	Эталон = ИнтернетЭквайрингКлиентСервер.НовыйДинамическиеРеквизитыПодключения();
	
	Если ТипЗнч(Форма.ДинамическиеРеквизиты) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Эталон, Форма.ДинамическиеРеквизиты);
	Иначе
		Возврат Результат;
	КонецЕсли;
	
	ИменаНастроек = СтрРазделить(
		"НастройкиАутентификации,ПрикладныеНастройки",
		",");
		
	Для Каждого ИмяНастройки Из ИменаНастроек Цикл
		
		ДинамическиеРеквизиты = Эталон[ИмяНастройки];
		Если ТипЗнч(ДинамическиеРеквизиты) <> Тип("Массив") Тогда
			Продолжить;
		КонецЕсли;

		Для Каждого ОписаниеРеквизита Из ДинамическиеРеквизиты Цикл
			
			Если ОписаниеРеквизита.ЭтоНастройкаПодключения Тогда
				Продолжить;
			КонецЕсли;
			
			ЗначениеРеквизита = Форма[ОписаниеРеквизита.ИмяРеквизита];
			Если ТипЗнч(ЗначениеРеквизита) = Тип("Строка") Тогда
				ЗначениеРеквизита = НастройкиПлатежныхСистемКлиентСервер.УдалитьНечитаемыеСимволы(ЗначениеРеквизита);
			КонецЕсли;
			
			Результат[ИмяНастройки].Вставить(
				ОписаниеРеквизита.Идентификатор,
				ЗначениеРеквизита);
				
			Если ОписаниеРеквизита.ЭтоИдентификаторМерчанта Тогда
				Результат.ИдентификаторМерчанта = ЗначениеРеквизита;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого ОписаниеРеквизита Из Эталон.ДополнительныеПараметры Цикл
		ЗначениеПараметра = Форма[ОписаниеРеквизита.ИмяРеквизита];
		Если Не ЗначениеЗаполнено(ЗначениеПараметра) Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеНастройки = ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации();
		ЗаполнитьЗначенияСвойств(
			ОписаниеНастройки,
			ОписаниеРеквизита,
			"Идентификатор, Представление");
		ОписаниеНастройки.Значение = ЗначениеПараметра;
		
		Для Каждого ДоступноеЗначение Из Форма.Элементы[ОписаниеРеквизита.ИмяЭлемента].СписокВыбора Цикл
			Если ДоступноеЗначение.Значение = ОписаниеНастройки.Значение Тогда
				ОписаниеНастройки.ПредставлениеЗначения = УбратьЗначениеИзПредставленияТребуемойНастройки(
					ДоступноеЗначение.Значение,
					ДоступноеЗначение.Представление);
			КонецЕсли;
		КонецЦикла;
		
		Результат.ДополнительныеПараметры.Добавить(ОписаниеНастройки);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Выполняет обработку вывода полей прикладных настроек на экран.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, на которую нужно вывести прикладные настройки. На формы должен быть
//    реквизит ДинамическиеРеквизиты.
//  ПараметрыВывода - см. ИнтернетЭквайрингСлужебный.НовыйПараметрыВывода
//  
Процедура ВывестиПрикладныеНастройки(Форма, ПараметрыВывода) Экспорт
	
	Эталон = ИнтернетЭквайрингКлиентСервер.НовыйДинамическиеРеквизитыПодключения();
	
	Если ТипЗнч(Форма.ДинамическиеРеквизиты) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Эталон, Форма.ДинамическиеРеквизиты);
	Иначе
		Форма.ДинамическиеРеквизиты = Эталон;
	КонецЕсли;

	Если Эталон.ПрикладныеНастройки.Количество() = 0 Тогда
		СоздатьРеквизитыПрикладныхНастроек(Форма, ПараметрыВывода);
	КонецЕсли;
	
	НастроитьЭлементыФормыПодключения(Форма, ПараметрыВывода);
	
КонецПроцедуры

// Формирует поля для ввода прикладных параметров в системе Интернет-эквайринга.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, на которую нужно вывести прикладные настройки. На формы должен быть
//    реквизит ДинамическиеРеквизиты.
//  ПараметрыВывода - см. ИнтернетЭквайрингСлужебный.НовыйПараметрыВывода.
//  
Процедура СоздатьРеквизитыПрикладныхНастроек(
		Форма,
		ПараметрыВывода)
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Настройки = ПрикладныеНастройкиПодключения();
	
	Реквизиты = РеквизитыПоОбъектуМетаданных(Настройки);
	
	Для Каждого ОписаниеРеквизита Из Реквизиты Цикл
		
		Если ОписаниеРеквизита.ЭтоНастройкаПодключения Тогда
			Продолжить;
		КонецЕсли;
		
		ДобавляемыеРеквизиты.Добавить(
			Новый РеквизитФормы(
				ОписаниеРеквизита.ИмяРеквизита,
				ОписаниеРеквизита.Тип,
				,
				ОписаниеРеквизита.Представление,
				ПараметрыВывода.СохраняемыеДанные));
	КонецЦикла;
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);

	ТипБулево = Новый ОписаниеТипов("Булево");
	
	ЗаполнятьЗначения = Ложь;
	ЗначенияПараметров = Новый Соответствие;
	Если ЗначениеЗаполнено(ПараметрыВывода.Настройка)
		И Не ПараметрыВывода.ПрикладныеПараметрыЗагружены Тогда
		ЗначенияПараметров = ЗначенияПрикладныхНастроек(
			ПараметрыВывода.Настройка,
			Реквизиты,
			Настройки.ОбъектМетаданных.Имя);
		ЗаполнятьЗначения = Истина;
	КонецЕсли;
	
	Для Каждого ОписаниеРеквизита Из Реквизиты Цикл
		
		Если ОписаниеРеквизита.ЭтоНастройкаПодключения Тогда
			Продолжить;
		КонецЕсли;
		
		НовоеПоле = Форма.Элементы.Добавить(
			ОписаниеРеквизита.ИмяЭлемента,
			Тип("ПолеФормы"),
			ПараметрыВывода.Родитель);
		Если ОписаниеРеквизита.Тип = ТипБулево Тогда
			НовоеПоле.Вид = ВидПоляФормы.ПолеФлажка;
			НовоеПоле.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
		Иначе
			НовоеПоле.Вид = ВидПоляФормы.ПолеВвода;
		КонецЕсли;
		НовоеПоле.ПутьКДанным               = ОписаниеРеквизита.ИмяРеквизита;
		НовоеПоле.АвтоОтметкаНезаполненного = ОписаниеРеквизита.ПроверкаЗаполнения;
		НовоеПоле.РежимПароля               = ОписаниеРеквизита.РежимПароля;
		
		// АПК:280 -выкл
		//@skip-check empty-except-statement
		Попытка
			НовоеПоле.ПараметрыВыбора = ОписаниеРеквизита.ПараметрыВыбора;
		Исключение
			// Дополнительной обработки не требуется. Исключение может возникнуть, если настройка
			// функциональных опций конфликтует с настройкой параметров выбора.
		КонецПопытки;
		
		//@skip-check empty-except-statement
		Попытка
			НовоеПоле.СвязиПараметровВыбора = ОписаниеРеквизита.СвязиПараметровВыбора;
		Исключение
			// Дополнительной обработки не требуется. Исключение может возникнуть, если настройка
			// функциональных опций конфликтует с настройкой связи параметров выбора.
		КонецПопытки;
		// АПК:280 -вкл.
		
		НовоеПоле.УстановитьДействие(
			"ПриИзменении",
			"Подключаемый_ПриИзмененииНастройкиОплаты");
		
		// Значения в структуре затираются, чтобы не провоцировать ошибку
		// сериализации значений формы.
		ПустойМассив = Новый Массив;
		ОписаниеРеквизита.СвязиПараметровВыбора = Новый ФиксированныйМассив(ПустойМассив);
		ОписаниеРеквизита.ПараметрыВыбора       = Новый ФиксированныйМассив(ПустойМассив);
		
		Форма.ДинамическиеРеквизиты.РеквизитПоИмениЭлемента.Вставить(
			ОписаниеРеквизита.ИмяЭлемента,
			ОписаниеРеквизита.ИмяРеквизита);
		
		Форма.ДинамическиеРеквизиты.ИдентификаторПоИмениЭлемента.Вставить(
			ОписаниеРеквизита.ИмяЭлемента,
			ОписаниеРеквизита.Идентификатор);
		
		Если ЗаполнятьЗначения И Не ОписаниеРеквизита.ЭтоНастройкаПодключения Тогда
			Форма[ОписаниеРеквизита.ИмяРеквизита] = ЗначенияПараметров.Получить(ОписаниеРеквизита.Идентификатор);
		КонецЕсли;
		
	КонецЦикла;
	
	Форма.ДинамическиеРеквизиты.ПрикладныеНастройки = Реквизиты;
	
КонецПроцедуры

// Определяет имя атрибута, в котором хранится настройка подключения.
//
// Параметры:
//  ОбъектМетаданных - ОбъектМетаданныхРегистрСведений - объект для обработки.
//
// Возвращаемое значение:
//  Строка - имя атрибута.
//
Функция АтрибутНастройкаПодключения(ОбъектМетаданных) Экспорт
	
	ТипНастройка = Новый ОписаниеТипов("СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу");
	Для Каждого Изменение Из ОбъектМетаданных.Измерения Цикл
		Если Изменение.Тип = ТипНастройка Тогда
			Возврат Изменение.Имя;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Ресурс Из ОбъектМетаданных.Ресурсы Цикл
		Если Ресурс.Тип = ТипНастройка Тогда
			Возврат Ресурс.Имя;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Реквизит Из ОбъектМетаданных.Реквизиты Цикл
		Если Реквизит.Тип = ТипНастройка Тогда
			Возврат Реквизит.Имя;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

// Формирует перечень реквизитов для вывода на экран на основе объекта метаданных
// 
// Параметры:
//  Настройки - см. ПрикладныеНастройкиПодключения
//
// Возвращаемое значение:
//  Массив из Структура - см. ИнтернетЭквайрингКлиентСервер.НовыйОписаниеДинамическогоРеквизита
//
Функция РеквизитыПоОбъектуМетаданных(Настройки)
	
	Реквизиты = Новый Массив;
	
	Для Каждого Изменение Из Настройки.ОбъектМетаданных.Измерения Цикл
		Если Не СтрНачинаетсяС(Изменение.Имя, "Удалить")
			И Настройки.ИсключаемыеПоля.Найти(Изменение.Имя) = Неопределено Тогда
			Реквизиты.Добавить(
				НовыйОписаниеПрикладныхНастроек(
					Изменение.Имя,
					Изменение.Тип,
					Изменение.Синоним,
					(Изменение.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку),
					Изменение.ПараметрыВыбора,
					Изменение.СвязиПараметровВыбора));
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Ресурс Из Настройки.ОбъектМетаданных.Ресурсы Цикл
		Если Не СтрНачинаетсяС(Ресурс.Имя, "Удалить")
			И Настройки.ИсключаемыеПоля.Найти(Ресурс.Имя) = Неопределено Тогда
			Реквизиты.Добавить(
				НовыйОписаниеПрикладныхНастроек(
					Ресурс.Имя,
					Ресурс.Тип,
					Ресурс.Синоним,
					(Ресурс.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку),
					Ресурс.ПараметрыВыбора,
					Ресурс.СвязиПараметровВыбора));
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Реквизит Из Настройки.ОбъектМетаданных.Реквизиты Цикл
		Если Не СтрНачинаетсяС(Реквизит.Имя, "Удалить")
			И Настройки.ИсключаемыеПоля.Найти(Реквизит.Имя) = Неопределено Тогда
			Реквизиты.Добавить(
				НовыйОписаниеПрикладныхНастроек(
					Реквизит.Имя,
					Реквизит.Тип,
					Реквизит.Синоним,
					(Реквизит.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку),
					Реквизит.ПараметрыВыбора,
					Реквизит.СвязиПараметровВыбора));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Реквизиты;
	
КонецФункции

// Позволяет настроить элементы прикладных настроек на формах подключения к Интернет-эквайрингу.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, на которую нужно вывести прикладные настройки
//  ПараметрыВывода - см. ИнтернетЭквайрингСлужебный.НовыйПараметрыВывода
//  
Процедура НастроитьЭлементыФормыПодключения(
		Форма,
		ПараметрыВывода) Экспорт
	
	ОбщиеЭлементы = Новый Структура;
	
	ОбщиеЭлементы.Вставить(
		"Наименование",
		Форма.Элементы[ПараметрыВывода.ИмяЭлементаНаименование]);
	ОбщиеЭлементы.Вставить(
		"ЗначениеНаименования",
		ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
			Форма,
			ПараметрыВывода.ИмяРеквизитаНаименование));
	ОбщиеЭлементы.Вставить(
		"ДекорацияДополнительнаяИнформация",
		Форма.Элементы[ПараметрыВывода.ИмяДополнительнойИнформации]);
	
	ЭлементыНастроекОплаты = Новый Структура;
	ЗначенияНастроекОплаты = Новый Структура;
	
	Для Каждого ОписаниеРеквизита Из Форма.ДинамическиеРеквизиты.ПрикладныеНастройки Цикл
		Если ОписаниеРеквизита.ЭтоНастройкаПодключения Тогда
			Продолжить;
		КонецЕсли;
		ЭлементыНастроекОплаты.Вставить(ОписаниеРеквизита.Идентификатор, Форма.Элементы[ОписаниеРеквизита.ИмяЭлемента]);
		ЗначенияНастроекОплаты.Вставить(ОписаниеРеквизита.Идентификатор, Форма[ОписаниеРеквизита.ИмяРеквизита]);
	КонецЦикла;
	
	НастройкиЭлементов = Новый Структура;
	НастройкиЭлементов.Вставить("ЭлементыНастроекОплаты", ЭлементыНастроекОплаты);
	НастройкиЭлементов.Вставить("ЗначенияНастроекОплаты", ЗначенияНастроекОплаты);
	
	НастройкиФормы = Новый Структура;
	НастройкиФормы.Вставить("ОбщиеЭлементы",          ОбщиеЭлементы);
	НастройкиФормы.Вставить("НастройкаПодключения",   ПараметрыВывода.Настройка);
	НастройкиФормы.Вставить("ИдентификаторУчастника", ПараметрыВывода.ИдентификаторУчастника);
	
	ИнтеграцияПодсистемБИП.ПриНастройкеЭлементовФормыПодключенияЭквайринг(
		НастройкиФормы,
		НастройкиЭлементов,
		ПараметрыВывода.ДополнительныеПараметры);
	ИнтернетЭквайрингПереопределяемый.ПриНастройкеЭлементовФормыПодключения(
		НастройкиФормы,
		НастройкиЭлементов,
		ПараметрыВывода.ДополнительныеПараметры);
	
	// Переносим в форму значение реквизитов, если они были изменены внутри переопределяемого метода
	ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
		Форма,
		ПараметрыВывода.ИмяРеквизитаНаименование,
		ОбщиеЭлементы.ЗначениеНаименования);
	
	Для Каждого ОписаниеРеквизита Из Форма.ДинамическиеРеквизиты.ПрикладныеНастройки Цикл
		Если ОписаниеРеквизита.ЭтоНастройкаПодключения Тогда
			Продолжить;
		КонецЕсли;
		Форма[ОписаниеРеквизита.ИмяРеквизита] = ЗначенияНастроекОплаты[ОписаниеРеквизита.Идентификатор];
	КонецЦикла;
	
КонецПроцедуры

// Загружает значения прикладных настроек из регистра сведений.
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - Настройка подключения, для
//    которой необходимо получить информацию
//  Реквизиты - см. РеквизитыПоОбъектуМетаданных.
//  ИмяРегистра - Строка - имя регистра, в котором хранятся значения прикладных настроек.
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - значения прикладных настроек:
//    * Ключ - Строка - имя измерения/ресурса/реквизита регистра.
//    * Значение - Произвольный - значение измерения/ресурса/реквизита регистра
//
Функция ЗначенияПрикладныхНастроек(
		НастройкаПодключения,
		Реквизиты,
		ИмяРегистра)
	
	Результат  = Новый Соответствие;
	ВыборПолей = Новый Массив;
	Отбор      = "";
	
	Для Каждого ОписаниеПоля Из Реквизиты Цикл
		
		Если ОписаниеПоля.ЭтоНастройкаПодключения Тогда
			Отбор = ОписаниеПоля.Идентификатор + " = &НастройкаПодключения";
		Иначе
			ВыборПолей.Добавить(ОписаниеПоля.Идентификатор + " КАК " + ОписаниеПоля.Идентификатор);
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	%1
		|ИЗ
		|	РегистрСведений.%2 КАК Настройки
		|ГДЕ
		|	%3";
	
	Запрос.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Запрос.Текст,
		СтрСоединить(ВыборПолей, "," + Символы.ПС),
		ИмяРегистра,
		Отбор);
	
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Для Каждого ОписаниеПоля Из Реквизиты Цикл
			Если ОписаниеПоля.ЭтоНастройкаПодключения Тогда
				Продолжить;
			КонецЕсли;
			Результат.Вставить(
				ОписаниеПоля.Идентификатор,
				ВыборкаДетальныеЗаписи[ОписаниеПоля.Идентификатор]);
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Выполняет проверку заполненности всех полей аутентификации, которые были программно созданы на форме.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма на которой необходимо проверить введенные данные. На формы должен быть
//    реквизит ДинамическиеРеквизиты.
//  ТипПолей - Строка - тип полей, которые необходимо проверить. Одно из следующих значений:
//    -- "НастройкиАутентификации" - поля для ввода параметров подключения с сервисам банка.
//    -- "ПрикладныеНастройки" - поля для ввода прикладных настроек.
// 
// Возвращаемое значение:
//  Булево - Истина, если заполнены все поля. Ложь в противном случае. Если поля не созданы, то возвращается Ложь.
//    Если в параметре ТипПоля передано недопустимое значение, то возвращается Ложь.
//
Функция ДинамическиеРеквизитыЗаполнены(Форма, ТипПолей) Экспорт
	
	РезультатПроверки = ИнтернетЭквайрингКлиентСервер.ДинамическиеРеквизитыЗаполнены(Форма, ТипПолей);
	
	Если Не РезультатПроверки.ВсеЗаполнены Тогда
		
		Если ЗначениеЗаполнено(РезультатПроверки.СообщениеОбОшибке) Тогда
			ОбщегоНазначения.СообщитьПользователю(
				РезультатПроверки.СообщениеОбОшибке);
		КонецЕсли;
		
		Для каждого ОписаниеПоля Из РезультатПроверки.НезаполненныеПоля Цикл
			ОбщегоНазначения.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Поле ""%1"" не заполнено';
						|en = 'Field %1 is required'"),
					ОписаниеПоля.Представление),
				,
				ОписаниеПоля.ИмяРеквизита);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат РезультатПроверки.ВсеЗаполнены;
	
КонецФункции

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыИФункции

// Возвращает идентификатор сервиса Онлайн-заказов для получения токена.
//
// Возвращаемое значение:
//  Строка - Идентификатор сервиса.
//
Функция ИдентификаторСервисаОнлайнЗаказы() Экспорт
	
	ИдентификаторСервиса = "orders.1c.ru";
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОнлайнЗаказы") Тогда
		МодульОнлайнЗаказыСобытия = ОбщегоНазначения.ОбщийМодуль("ОнлайнЗаказыСобытия");
		ИдентификаторСервиса = МодульОнлайнЗаказыСобытия.ИдентификаторСервиса();
	КонецЕсли;

	Возврат ИдентификаторСервиса;
	
КонецФункции

#КонецОбласти

#КонецОбласти
