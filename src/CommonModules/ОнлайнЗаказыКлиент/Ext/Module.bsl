///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ОнлайнЗаказы".
// ОбщийМодуль.ОнлайнЗаказыКлиент.
//
// Клиентские процедуры настройки подключения к Онлайн-заказам:
//  - открытие формы настроек подключения;
//  - переход в журнал регистрации для просмотра лога;
//  - алгоритмы работы с формами шаблонов сообщений подсистемы;
//  - переход к форме отправки URL онлайн-заказа.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Открывает форму списка настроек онлайн-заказов.
//
// Параметры:
//  Владелец - ФормаКлиентскогоПриложения - форма которая будет установлена в качестве владельца.
//
Процедура НастройкиПодключения(Владелец) Экспорт
	
	ОткрытьФорму(
		"Справочник.НастройкиПодключенияКОнлайнЗаказам.ФормаСписка",
		,
		Владелец);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Открывает форму списка шаблонов сообщений.
//
// Параметры:
//  УникальныйИдентификатор - УникальныйИдентификатор - ключ уникальности используемый при открытии формы.
//
Процедура ОткрытьШаблоныСообщенийОнлайнЗаказов(УникальныйИдентификатор) Экспорт
	
	РезультатПроверки = ОнлайнЗаказыВызовСервера.ВсеШаблоныСозданы();
	
	Если РезультатПроверки.ВсеШаблоны Тогда
		ОткрытьФормуШаблонаСообщенийСОтбором(
			РезультатПроверки.Шаблоны,
			УникальныйИдентификатор);
	Иначе
		РезультатПроверки.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
		
		Оповещение = Новый ОписаниеОповещения(
			"ПослеОтветаНаВопросОСозданииШаблонов",
			ЭтотОбъект,
			РезультатПроверки);
		
		ПоказатьВопрос(
			Оповещение,
			НСтр("ru = 'Создать шаблоны сообщений для автоматического заполнения на основании данных документов?';
				|en = 'Do you want to create message templates for automatic population from document data?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет вызов вопроса пользователю о необходимости обновлении настроек подключения.
//
Процедура ПоказатьВопросОбновленияНастроекПодключенияПоНастройкеОплаты(
	НастройкаПодключения,
	Владелец,
	ЗакрытьВладельца) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("НастройкаПодключения", НастройкаПодключения);
	Параметры.Вставить("Владелец", Владелец);
	Параметры.Вставить("ЗакрытьВладельца", ЗакрытьВладельца);
	
	ОбработкаОтвета = Новый ОписаниеОповещения(
		"ПоказатьВопросОбновленияНастроекПодключенияПоНастройкеОплатыЗавершение",
		ЭтотОбъект,
		Параметры);
	
	ТекстВопроса = НСтр("ru = 'По настройке подключения обнаружены зависимые настройки
		|онлайн-заказов, необходимо выполнить обновление настроек в сервисе.
		|Обновить сейчас?';
		|en = 'Dependent settings of online orders are detected in the connection setting.
		|Update the service settings.
		|Do you want to update now?'");
	
	ПоказатьВопрос(ОбработкаОтвета, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПодключениеКОнлайнЗаказам

// Открывает форму настройки подключения к Онлайн-заказам.
//
// Параметры:
//  ОписаниеОповещения - ОписаниеОповещения, Неопределено - оповещение, которое
//    необходимо вызвать после завершения настройки подключения. В случае успешного
//    завершения настройки подключения в результате оповещения будет возвращено Истина;
//
Процедура СлужебнаяПодключитьОнлайнЗаказы(ОписаниеОповещения = Неопределено) Экспорт
	
	ОткрытьФорму(
		"Справочник.НастройкиПодключенияКОнлайнЗаказам.Форма.ФормаЭлемента",
		,
		,
		,
		,
		,
		ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область КодыОшибок

// Возвращает код ошибки "ОшибкаДанныхАутентификации".
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция КодОшибкиДанныеАутентификацииИППНеЗаполнены() Экспорт
	
	Возврат "ДанныеАутентификацииИППНеЗаполнены";
	
КонецФункции

#КонецОбласти

#Область ПодготовкаПлатежнойСсылки

// См. ОнлайнЗаказыСлужебный.ПриОпределенииКомандПодключенныхКОбъекту
//
Процедура Подключаемый_ОткрытьФормуURLЗаказа(
		ПараметрКоманды,
		ПараметрыВыполненияКоманды) Экспорт
	
	ОткрытьФормуURLЗаказа(ПараметрКоманды);
	
КонецПроцедуры

// Открывает форму формирования ссылки на оплату.
// Позволяет:
//  * Сформировать новую ссылку.
//  * Просмотреть сформированную ранее ссылку.
//  * Обновить данные в сервисе, если они изменились.
//  
// Параметры:
//  ДокументЗаказа - ОпределяемыйТип.ДокументОнлайнЗаказаБИП - основание онлайн-заказа,
//  для которого нужно сформировать/открыть/обновить ссылку.
//
Процедура ОткрытьФормуURLЗаказа(Знач ДокументЗаказа)
	
	Параметры = Новый Структура;
	Параметры.Вставить("ДокументЗаказа", ДокументЗаказа);
	
	ОбработкаПродолжения = Новый ОписаниеОповещения(
		"ОткрытьФормуURLЗаказаПродолжение",
		ЭтотОбъект,
		Параметры);
	
	ИнтернетПоддержкаПользователейКлиент.НачатьПроверкуИПодключениеИнтернетПоддержкиПользователей(
		ОбработкаПродолжения,
		НСтр("ru = 'Для использования онлайн-заказов необходимо подключить Интернет-поддержку пользователей.
			|Подключиться сейчас?';
			|en = 'To use online orders, enable online user support.
			|Do you want to enable now?'"));
	
КонецПроцедуры

// Подготавливает параметры открытия формы формирования платежной ссылки онлайн-заказа.
// Перед открытием проверяется наличие подключенной интернет-поддержки пользователей.
//
// Параметры:
//  ПодключенаИПП - Булево - признак подключения ИПП.
//  Параметры - Структура - содержит описание параметров открытия формы:
//   * ДокументЗаказа - ОпределяемыйТип.ДокументОнлайнЗаказаБИП - основание онлайн-заказа, для которого будет
//     формироваться ссылка.
//
Процедура ОткрытьФормуURLЗаказаПродолжение(
		Знач ПодключенаИПП,
		Знач Параметры) Экспорт
	
	Если Не ПодключенаИПП Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСозданияЗаказа = ОнлайнЗаказыВызовСервера.ПриОпределенииПараметровСозданияЗаказа(
		Параметры.ДокументЗаказа);
	
	Если Не ПараметрыСозданияЗаказа.СозданиеЗаказаДоступно Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ПараметрыСозданияЗаказа.СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ПараметрыСозданияЗаказа.СообщениеОбОшибке) Тогда
		ПоказатьПредупреждение(
			Неопределено,
			ПараметрыСозданияЗаказа.СообщениеОбОшибке);
	ИначеЕсли ПараметрыСозданияЗаказа.ИспользуемыеНастройкиПодключения.Количество() > 0 Тогда
		
		ОткрытьФормуURLЗаказаЗавершение(
			Параметры.ДокументЗаказа,
			ПараметрыСозданияЗаказа.ИспользуемыеНастройкиПодключения);
		
	ИначеЕсли ПараметрыСозданияЗаказа.НастройкаОнлайнЗаказовДоступна
		И ПараметрыСозданияЗаказа.ОтключенныеНастройкиПодключения.Количество() = 0 Тогда
		
		СоздатьНовуюНастройкуПодключения(
			ПараметрыСозданияЗаказа.Организация,
			Параметры.ДокументЗаказа);
		
	ИначеЕсли ПараметрыСозданияЗаказа.НастройкаОнлайнЗаказовДоступна
		И ПараметрыСозданияЗаказа.ОтключенныеНастройкиПодключения.Количество() > 0 Тогда
		
		ПараметрыЗавершения = Новый Структура;
		ПараметрыЗавершения.Вставить("ДокументЗаказа", Параметры.ДокументЗаказа);
		ПараметрыЗавершения.Вставить("НастройкаПодключения", ПараметрыСозданияЗаказа.ОтключенныеНастройкиПодключения[0]);
		
		ОбработкаОтвета = Новый ОписаниеОповещения(
			"ПоказатьВопросВключенияНастройкиПодключенияЗавершение",
			ЭтотОбъект,
			ПараметрыЗавершения);
		
		ТекстВопроса = НСтр("ru = 'Обнаружена неиспользуемая настройка подключения к онлайн-заказам. Включить использование?';
							|en = 'An unused online order connection setting is found. Do you want to enable it?'");
		
		КнопкиОтвета = Новый СписокЗначений;
		КнопкиОтвета.Добавить("Включить","Включить");
		КнопкиОтвета.Добавить("Отмена","Отмена");
		
		ПоказатьВопрос(
			ОбработкаОтвета,
			ТекстВопроса,
			КнопкиОтвета);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не обнаружены действующие настройки.';
				|en = 'Valid settings were not found.'"));
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// Выполняет запуск создания новой настройки подключения к онлайн-заказам в фоне.
//
// Параметры:
//  Организация - Структура, Неопределено - Результат выполнения фонового задания создания новой настройки.
//  ДокументЗаказа - ОпределяемыйТип.ДокументОнлайнЗаказаБИП - основание онлайн-заказа,
//  для которого будет формироваться ссылка по окончанию создания настройки.
//
Процедура СоздатьНовуюНастройкуПодключения(Организация, ДокументЗаказа)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДокументЗаказа", ДокументЗаказа);
	
	РезультатСоздания = ОнлайнЗаказыВызовСервера.НоваяНастройкаПодключенияВФоне(Организация);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"СоздатьНастройкуПодключенияЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры);
		
	Если РезультатСоздания.Статус = "Выполнено" Или РезультатСоздания.Статус = "Ошибка" Тогда
		СоздатьНастройкуПодключенияЗавершение(
			РезультатСоздания,
			ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется создание новой настройки онлайн-заказов.';
											|en = 'Creating a new online order setting.'");
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатСоздания,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

// Выполняет вызов обработки оповещения после окончания процесса создания торговой точки
//
// Параметры:
//  Результат - Структура, Неопределено - Результат выполнения фонового задания создания новой настройки.
//  ДополнительныеПараметры - Структура - содержит описание параметров для открытия формы URL онлайн-заказа.
//
Процедура СоздатьНастройкуПодключенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатСоздания = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		Если РезультатСоздания.Ошибка Тогда
			ПоказатьПредупреждение(
				Неопределено,
				РезультатСоздания.СообщениеОбОшибке);
		Иначе
			
			НастройкиПодключения = Новый Массив;
			НастройкиПодключения.Добавить(РезультатСоздания.Ссылка);
			
			ОткрытьФормуURLЗаказаЗавершение(
				ДополнительныеПараметры.ДокументЗаказа,
				НастройкиПодключения);
				
			ОповеститьОбИзменении(РезультатСоздания.Ссылка);
			
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		ДанныеОшибки = ОбщегоНазначенияКлиентСервер.УточнениеИсключения(
			Результат.ИнформацияОбОшибке,
			НСтр("ru = 'Ошибка создания новой настройки онлайн-заказов.';
				|en = 'An error occurred when creating a new online order setting.'"));
		
		ПоказатьПредупреждение(
			Неопределено,
			ДанныеОшибки.Текст);
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет поведение системы после ответа пользователя о создании новой торговой точки.
//
// Параметры:
//  Ответ - Структура - результат открытия окна с вопросом пользователю.
//  Параметры - Структура - содержит описание параметров открытия формы URL онлайн-заказа.
//
Процедура ПоказатьВопросВключенияНастройкиПодключенияЗавершение(
		Знач Ответ,
		Знач Параметры) Экспорт
	
	Если Ответ = "Включить" Тогда
		
		ВключитьНастройкуПодключения(
			Параметры.НастройкаПодключения,
			Параметры.ДокументЗаказа);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет запуск создания новой настройки подключения к онлайн-заказам в фоне.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//  настройка для которой выполняется операция включения.
//  ДокументЗаказа - ОпределяемыйТип.ДокументОнлайнЗаказаБИП - основание онлайн-заказа,
//  для которого будет формироваться ссылка по окончанию создания настройки.
//
Процедура ВключитьНастройкуПодключения(НастройкаПодключения, ДокументЗаказа)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДокументЗаказа", ДокументЗаказа);
	ДополнительныеПараметры.Вставить("НастройкаПодключения", НастройкаПодключения);
	
	РезультатВключения = ОнлайнЗаказыВызовСервера.ВключитьНастройкуПодключенияВФоне(НастройкаПодключения);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ВключитьНастройкуПодключенияЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры);
		
	Если РезультатВключения.Статус = "Выполнено" Или РезультатВключения.Статус = "Ошибка" Тогда
		ВключитьНастройкуПодключенияЗавершение(
			РезультатВключения,
			ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется включение настройки онлайн-заказов.';
											|en = 'Enabling the online order setting.'");
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВключения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

// Выполняет вызов обработки оповещения после окончания процесса создания торговой точки
//
// Параметры:
//  Результат - Структура, Неопределено - Результат выполнения фонового задания создания новой настройки.
//  ДополнительныеПараметры - Структура - содержит описание параметров для открытия формы URL онлайн-заказа.
//
Процедура ВключитьНастройкуПодключенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатВключения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		Если РезультатВключения.Ошибка Тогда
			ПоказатьПредупреждение(
				Неопределено,
				РезультатВключения.СообщениеОбОшибке);
		ИначеЕсли РезультатВключения.ОшибкаОтправкиВСервис Тогда
			
			ОбработкаОтвета = Новый ОписаниеОповещения(
				"ПоказатьВопросПовторнойОтправкиНастройкиЗавершение",
				ЭтотОбъект,
				ДополнительныеПараметры);
			
			ТекстВопроса = НСтр("ru = 'При отправке настройки подключения в сервис произошла ошибка. Повторить?';
								|en = 'An error occurred when sending the connection setting to the service. Try again?'");
			
			КнопкиОтвета = Новый СписокЗначений;
			КнопкиОтвета.Добавить("Повторить","Повторить");
			КнопкиОтвета.Добавить("Отмена","Отмена");
			
			ПоказатьВопрос(
				ОбработкаОтвета,
				ТекстВопроса,
				КнопкиОтвета);
			
		Иначе
			
			НастройкиПодключения = Новый Массив;
			НастройкиПодключения.Добавить(ДополнительныеПараметры.НастройкаПодключения);
			
			ОткрытьФормуURLЗаказаЗавершение(
				ДополнительныеПараметры.ДокументЗаказа,
				НастройкиПодключения);
			
			ОповеститьОбИзменении(ДополнительныеПараметры.НастройкаПодключения);
			
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		ДанныеОшибки = ОбщегоНазначенияКлиентСервер.УточнениеИсключения(
			Результат.ИнформацияОбОшибке,
			НСтр("ru = 'Ошибка включения настройки онлайн-заказов.';
				|en = 'An error occurred when enabling the online order setting.'"));
		
		ПоказатьПредупреждение(
			Неопределено,
			ДанныеОшибки.Текст);
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет поведение системы после ответа пользователя о повторной отправке настройки в сервис.
//
// Параметры:
//  Ответ - Структура - результат открытия окна с вопросом пользователю.
//  ДополнительныеПараметры - Структура - содержит описание параметров открытия формы URL онлайн-заказа.
//
Процедура ПоказатьВопросПовторнойОтправкиНастройкиЗавершение(
		Знач Ответ,
		Знач ДополнительныеПараметры) Экспорт
	
	Если Ответ = "Повторить" Тогда
		
		РезультатОбновления = ОнлайнЗаказыВызовСервера.ОбновитьНастройкуПодключенияВФоне(
			ДополнительныеПараметры.НастройкаПодключения);
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения(
			"ПовторноОтправитьНастройкуЗавершение",
			ЭтотОбъект,
			ДополнительныеПараметры);
			
		Если РезультатОбновления.Статус = "Выполнено" Или РезультатОбновления.Статус = "Ошибка" Тогда
			ПовторноОтправитьНастройкуЗавершение(
				РезультатОбновления,
				ДополнительныеПараметры);
			Возврат;
		КонецЕсли;
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется обновление настройки онлайн-заказов.';
												|en = 'Updating the online order setting.'");
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			РезультатОбновления,
			ОповещениеОЗавершении,
			ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет поведение системы после повторной отправки настройки в сервис.
//
// Параметры:
//  Ответ - Структура - результат открытия окна с вопросом пользователю.
//  ДополнительныеПараметры - Структура - содержит описание параметров открытия формы URL онлайн-заказа.
//
Процедура ПовторноОтправитьНастройкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОбновления = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		Если ПустаяСтрока(РезультатОбновления.КодОшибки) Тогда
			
			НастройкиПодключения = Новый Массив;
			НастройкиПодключения.Добавить(ДополнительныеПараметры.НастройкаПодключения);
			
			ОткрытьФормуURLЗаказаЗавершение(
				ДополнительныеПараметры.ДокументЗаказа,
				НастройкиПодключения);
			
			ОповеститьОбИзменении(ДополнительныеПараметры.НастройкаПодключения);
			
		Иначе
			
			ОбработкаОтвета = Новый ОписаниеОповещения(
				"ПоказатьВопросПовторнойОтправкиНастройкиЗавершение",
				ЭтотОбъект,
				ДополнительныеПараметры);
			
			ТекстВопроса = НСтр("ru = 'При отправке настройки подключения в сервис произошла ошибка. Повторить?';
								|en = 'An error occurred when sending the connection setting to the service. Try again?'");
			
			КнопкиОтвета = Новый СписокЗначений;
			КнопкиОтвета.Добавить("Повторить","Повторить");
			КнопкиОтвета.Добавить("Отмена","Отмена");
			
			ПоказатьВопрос(
				ОбработкаОтвета,
				ТекстВопроса,
				КнопкиОтвета);
			
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		ДанныеОшибки = ОбщегоНазначенияКлиентСервер.УточнениеИсключения(
			Результат.ИнформацияОбОшибке,
			НСтр("ru = 'Ошибка отправки настройки онлайн-заказов.';
				|en = 'An error occurred when sending the online order setting.'"));
		
		ПоказатьПредупреждение(
			Неопределено,
			ДанныеОшибки.Текст);
		
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму формирования платежной ссылки для основания заказа.
// Перед открытием проверяется наличие подключенной интернет-поддержки пользователей.
//
// Параметры:
//  ДокументЗаказа - ОпределяемыйТип.ДокументОнлайнЗаказаБИП - основание платежа, для которого будет формироваться ссылка.
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//    настройка выполнения операции.
//
Процедура ОткрытьФормуURLЗаказаЗавершение(
		ДокументЗаказа,
		НастройкиПодключения)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ДокументЗаказа",       ДокументЗаказа);
	ПараметрыФормы.Вставить("НастройкиПодключения", НастройкиПодключения);
	
	ОткрытьФорму(
		"ОбщаяФорма.ОтправкаURLОнлайнЗаказов",
		ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область Шаблоны

// Определяет поведение системы после ответа пользователя о создании шаблонов
//
// Параметры:
//  Результат - Структура - результат открытия окна с вопросом пользователю.
//  Параметры - Структура - содержит описание параметров открытия формы.
//
Процедура ПослеОтветаНаВопросОСозданииШаблонов(
		Результат,
		Параметры) Экспорт
	
	Шаблоны = Параметры.Шаблоны;
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		
		ОнлайнЗаказыВызовСервера.УстановитьИспользованиеШаблоновСообщенийПроверкаПодсистем();
		
		СозданныеШаблоны = ОнлайнЗаказыВызовСервера.СоздатьПредопределенныеШаблоныСообщенийПроверкаПодсистем();
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			Шаблоны,
			СозданныеШаблоны);
		
	КонецЕсли;
	
	ОткрытьФормуШаблонаСообщенийСОтбором(
		Шаблоны,
		Параметры.УникальныйИдентификатор);
	
КонецПроцедуры

// Осуществляет открытие формы шаблонов сообщений с отбором по предопределенным шаблонам подсистемы.
//
// Параметры:
//  ДанныеШаблонов - Массив из СправочникСсылка.ШаблоныСообщений - массив элементов по которым будет установлен отбор.
//  УникальныйИдентификатор - УникальныйИдентификатор - ключ уникальности используемый при открытии формы
//
Процедура ОткрытьФормуШаблонаСообщенийСОтбором(
		ДанныеШаблонов,
		УникальныйИдентификатор)
	
	ОтборФормы = Новый Структура();
	ОтборФормы.Вставить("Ссылка", ДанныеШаблонов);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Отбор", ОтборФормы);
	
	ИмяФормыСпискаШаблонов = "Справочник.ШаблоныСообщений.ФормаСписка";
	ОткрытьФорму(
		ИмяФормыСпискаШаблонов,
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеНастроекПодключения

// Выполняет вызов обработки оповещения после ответа пользователя
//
// Параметры:
//  Ответ - КодВозвратаДиалога - Содержит ответ пользователя.
//  Параметры - Структура - содержит описание оповещения при закрытии формы.
//
Процедура ПоказатьВопросОбновленияНастроекПодключенияПоНастройкеОплатыЗавершение(
		Знач Ответ,
		Знач Параметры) Экспорт
	
	ОбработатьОтветОбОбновленииНастроек(Ответ, Параметры);
	
КонецПроцедуры

Процедура ОбновитьНастройкиСтраницЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			ПоказатьОшибкуОбработкиНастроек(РезультатОперации.НеобработанныеНастройки);
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Настройки успешно обновлены.';
					|en = 'Settings are successfully updated.'"));
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ПоказатьВопросОшибкиОбновленияНастроек(ДополнительныеПараметры)
	КонецЕсли;
	
КонецПроцедуры

Процедура ПоказатьВопросОшибкиОбновленияНастроек(Параметры)
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения(
			"ПоказатьВопросОшибкиОбновленияНастроекЗавершение",
			ЭтотОбъект,
			Параметры),
		НСтр("ru = 'При обновлении настроек возникли ошибки. Повторить операцию?';
			|en = 'Errors occurred while updating the settings. Retry the operation?'"),
		РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

Процедура ПоказатьОшибкуОбработкиНастроек(НастройкиПодключения)
	
	ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Не удалось выполнить автоматическое обновление настроек подключения к онлайн-заказам:
			|%1
			|Для дальнейшего использования указанных настроек необходимо выполнить обновление вручную.';
			|en = 'Cannot auto update online order connection settings:
			|%1
			|To use the specified settings, update manually.'"),
		СтрСоединить(НастройкиПодключения, Символы.ПС));
	
	ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
	
КонецПроцедуры

Процедура ПоказатьВопросОшибкиОбновленияНастроекЗавершение(Ответ, Параметры) Экспорт
	
	ОбработатьОтветОбОбновленииНастроек(Ответ, Параметры);
	
КонецПроцедуры

Процедура ОбработатьОтветОбОбновленииНастроек(Ответ, Параметры)
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Если Параметры.ЗакрытьВладельца Тогда
			Параметры.Владелец.Закрыть();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	РезультатВыполнения = ОнлайнЗаказыВызовСервера.ОбновитьНастройкиСтраницЗапускЗадания(
		Параметры.Владелец.УникальныйИдентификатор,
		Параметры.НастройкаПодключения);
		
	Если Параметры.ЗакрытьВладельца Тогда
		Параметры.Владелец.Закрыть();
		Возврат;
	КонецЕсли;
	
	Если РезультатВыполнения.Статус = "Выполнено" Или РезультатВыполнения.Статус = "Ошибка" Тогда
		ОбновитьНастройкиСтраницЗавершение(
			РезультатВыполнения,
			Параметры);
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Параметры.Владелец);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется обновление настроек в сервисе.';
											|en = 'The service settings are being updated.'");
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОбновитьНастройкиСтраницЗавершение",
		ЭтотОбъект,
		Параметры);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
		
КонецПроцедуры

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыФункции

Процедура ОткрытьСтраницуПорталаПроблемыЗаказов() Экспорт
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://its.1c.ru/db/islhelper#content:9:hdoc");
	
КонецПроцедуры

// Открывает форму журнала регистрации с отбором
// по событию см. ИмяСобытияЖурналаРегистрации.
//
Процедура ОткрытьЖурналРегистрации() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("СобытиеЖурналаРегистрации", ИмяСобытияЖурналаРегистрации());
	ЖурналРегистрацииКлиент.ОткрытьЖурналРегистрации(Отбор);
	
КонецПроцедуры

// Возвращает имя события для журнала регистрации, которое используется
// для записи событий загрузки данных из внешних систем.
//
// Возвращаемое значение:
//  Строка - имя события.
//
Функция ИмяСобытияЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Онлайн-заказы';
				|en = 'Online orders'",
		ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	
КонецФункции

#КонецОбласти

#КонецОбласти