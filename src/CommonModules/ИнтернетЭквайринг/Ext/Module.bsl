///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ИнтернетЭквайринг".
// ОбщийМодуль.ИнтернетЭквайринг.
// 
// Серверные процедуры обмена данными с интернет-эквайрингом:
//  - определение доступности настроек подключения и возможности выполнения операций;
//  - выполнение операции возврата платежа;
//  - установка признака отложенного получения статуса и проверка статуса операций;
//  - удаление идентификаторов операций.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ПроверкаПрав

// Определяет доступность операций Интернет-эквайринга на основании прав доступа пользователя.
//
// Возвращаемое значение:
//  Булево - Истина, если у пользователя есть права на выполнение операций через Интернет-эквайринг.
//
Функция ОперацииДоступны() Экспорт
	
	Возврат ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга)
		И ИнтернетЭквайрингСлужебный.ЧтениеНастроекДоступно();
	
КонецФункции

// Определяет доступность использования функциональности подключения к Интернет-эквайрингу на основании прав доступа
// пользователя.
//
// Возвращаемое значение:
//  Булево - если Истина, настройка подключения к Интернет-эквайрингу доступна.
//
Функция НастройкаПодключенияДоступна() Экспорт
	
	Возврат ПравоДоступа("Изменение", Метаданные.Справочники.НастройкиПодключенияКИнтернетЭквайрингу)
		И ПравоДоступа("Добавление", Метаданные.Справочники.НастройкиПодключенияКИнтернетЭквайрингу)
		И ПравоДоступа("Просмотр", Метаданные.Обработки.ПодключениеКИнтернетЭквайрингу)
		И ПравоДоступа("Использование", Метаданные.Обработки.ПодключениеКИнтернетЭквайрингу);
	
КонецФункции

#КонецОбласти

#Область ОперацииВозвратов

// Выполняет возврат оплаты покупателю по ранее созданному заказу на оплату.
//
// Параметры:
//  ДокументВозврата - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, который отражает операцию
//    возврата в информационной базе;
//  ОбъектОплаты - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, который отражает операцию
//    оплаты в информационной базе;
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка выполнения операции.
//  ДополнительныеПараметры - Структура, Неопределено - дополнительные настройки формирования заказа на возврат. Будут
//    переданы в переопределяемые модули.
//
// Возвращаемое значение:
//  Структура - результат создания заказа на возврат оплаты:
//    * СтатусОперации - Строка - Для проверки статуса
//      операции, необходимо функции программного интерфейса общего модуля
//      ИнтернетЭквайрингКлиентСервер. Возможные значения:
//        - "Выполняется" - подтверждение возврата не получено. Для проверки
//          состояния возврата необходимо вызвать функцию ИнтернетЭквайринг.СтатусВозврата;
//        - "Выполнена" - выполнение возврата подтверждено банком;
//        - "Отклонена" - возврат по указанной оплате невозможен;
//        - "Ошибка" - не удалось выполнить проверку оплаты из-за ошибки, необходимо
//          проанализировать код ошибки;
//    * ПараметрыОперации - Структура - дополнительные данные по оплате:
//        ** ДатаОперации - Дата - фактическая дата оплаты в UTC;
//        ** СуммаОперации - Число - фактическая суммы возврата по документу;
//        ** ИдентификаторОперации - Строка - идентификатор выполненной операции;
//        ** ИдентификаторОплаты - Строка - идентификатор оплаты;
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом:
//        - <Пустая строка> - создание нового заказа выполнено успешно;
//        - "НеверныйФорматЗапроса" - передан некорректный запрос или настройка подключения;
//        - "НеверныйЛогинИлиПароль" - неверный логин или пароль или параметры
//          подключения к Интернет-эквайрингу;
//        - "ВозвратУжеВыполнен" - возврат по документу продажи уже выполнен или сумма возврата
//          превышает предельно допустимую;
//        - "ТребуетсяОплата" - требуется оплата сервиса;
//        - "ОтсутствуетДоступКСервису" - у пользователя нет доступа к сервису;
//        - "ПревышеноКоличествоПопыток" - превышено количество попыток
//          обращения к сервису с некорректным логином и паролем;
//        - "ОшибкаПодключения" - ошибка при подключении к сервису;
//        - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//        - "НеизвестнаяОшибка" - при получении информации возникла
//          неизвестная (не обрабатываемая) ошибка;
//        - "СервисВременноНеДоступен" - на сервере ведутся регламентные работы;
//        - "ИнтеграцияНеИспользуется" - использование отключено в настройках;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//
Функция ВозвратОплаты(
		ДокументВозврата,
		ОбъектОплаты,
		НастройкаПодключения,
		ДополнительныеПараметры = Неопределено) Экспорт
	
	ИнтернетЭквайрингСлужебный.ПроверитьПраваНаВыполнениеОперации();
	
	// Проверка входных параметров
	ИмяФункции = "ИнтернетЭквайринг.ВозвратОплаты";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"ДокументВозврата",
		ДокументВозврата,
		Метаданные.ОпределяемыеТипы.ДокументОперацииИнтернетЭквайринга.Тип);
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"НастройкаПодключения",
		НастройкаПодключения,
		Тип("СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу"));
	
	ТипыОбъектаОплаты = Новый Массив;
	Для Каждого ТипДокумента Из Метаданные.ОпределяемыеТипы.ДокументОперацииИнтернетЭквайринга.Тип.Типы() Цикл
		ТипыОбъектаОплаты.Добавить(ТипДокумента);
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"ОбъектОплаты",
		ОбъектОплаты,
		ТипыОбъектаОплаты);
	
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		ВызватьИсключение НСтр("ru = 'Не указана настройка подключения.';
								|en = 'Connection settings are not specified.'");
	КонецЕсли;
	
	Возврат ИнтернетЭквайрингСлужебный.ВозвратОплаты(
		ДокументВозврата,
		ОбъектОплаты,
		НастройкаПодключения,
		ДополнительныеПараметры);
	
КонецФункции

// Выполняет проверку статуса возврата в платежной системе.
//
// Параметры:
//  ДокументВозврата - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга -  документ, который отражает
//    операцию возврата в информационной базе;
//  ДлительныйВызов - Булево - если Истина, получение статуса оплаты будет выполнятся
//    в цикле. Длительность выполнения операции определяется на основании
//    данных константы ДлительностьОперацииПлатежныхСистем.
//
// Возвращаемое значение:
//  Структура - результат проверки статуса оплаты:
//    * СтатусОперации - Строка - Для проверки статуса
//      операции, необходимо функции программного интерфейса общего модуля
//      ИнтернетЭквайрингКлиентСервер. Возможные значения:
//        - "Выполняется" - подтверждение возврата не получено. Для проверки
//          состояния возврата необходимо вызвать функцию ИнтернетЭквайринг.СтатусВозврата;
//        - "Выполнена" - выполнение возврата подтверждено банком;
//        - "Отклонена" - возврат по указанной оплате невозможен;
//        - "Ошибка" - не удалось выполнить проверку оплаты из-за ошибки, необходимо
//          проанализировать код ошибки;
//    * ПараметрыОперации - Структура - дополнительные данные по оплате:
//        ** ДатаОперации - Дата - фактическая дата оплаты в UTC;
//        ** СуммаОперации - Число - фактическая суммы возврата по документу;
//        ** ИдентификаторОперации - Строка - идентификатор выполненной операции;
//        ** ИдентификаторОплаты - Строка - идентификатор оплаты;
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом:
//        - <Пустая строка> - создание нового заказа выполнено успешно;
//        - "НеверныйФорматЗапроса" - передан некорректный запрос или настройка подключения;
//        - "НеверныйЛогинИлиПароль" - неверный логин или пароль или параметры
//          подключения к Интернет-эквайрингу;
//        - "ВозвратУжеВыполнен" - возврат по документу продажи уже выполнен или сумма возврата
//          превышает предельно допустимую;
//        - "ТребуетсяОплата" - требуется оплата сервиса;
//        - "ОтсутствуетДоступКСервису" - у пользователя нет доступа к сервису;
//        - "ПревышеноКоличествоПопыток" - превышено количество попыток
//          обращения к сервису с некорректным логином и паролем;
//        - "ОшибкаПодключения" - ошибка при подключении к сервису;
//        - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//        - "НеизвестнаяОшибка" - при получении информации возникла
//          неизвестная (не обрабатываемая) ошибка;
//        - "СервисВременноНеДоступен" - на сервере ведутся регламентные работы;
//        - "ИнтеграцияНеИспользуется" - использование отключено в настройках;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция СтатусВозврата(
		ДокументВозврата,
		ДлительныйВызов = Истина) Экспорт

	ИнтернетЭквайрингСлужебный.ПроверитьПраваНаВыполнениеОперации();
	
	// Проверка входных параметров
	ИмяФункции = "ИнтернетЭквайринг.СтатусВозврата";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"ДокументВозврата",
		ДокументВозврата,
		Метаданные.ОпределяемыеТипы.ДокументОперацииИнтернетЭквайринга.Тип);

	Возврат ИнтернетЭквайрингСлужебный.СтатусВозврата(
		ДокументВозврата,
		ДлительныйВызов);
	
КонецФункции

#КонецОбласти

#Область ДополнительныеОперации

// Выполняет загрузку статусов операций Интернет-эквайринга, по которым было отложено получение результата.
//
// Возвращаемое значение:
//  Массив из ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - данные обработанных документов.
//
Функция СтатусыОпераций() Экспорт
	
	ИнтернетЭквайрингСлужебный.ПроверитьПраваНаВыполнениеОперации();
	
	Возврат ИнтернетЭквайрингСлужебный.СтатусыОпераций();
	
КонецФункции

// Удаляет идентификатор операции из информационной базы.
// Процедуру необходимо использовать только в том случае, если при вызове
// методов оплаты в качестве документа оплаты была передана ссылка нового
// и после выполнения оплаты объект не был записан. Во всех остальных случаях
// вызов метода не рекомендуется.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, который отражает
//    операцию в информационной базе.
//
Процедура УдалитьИдентификаторыОперации(ДокументОперации) Экспорт
	
	// Проверка входных параметров
	ИмяФункции = "ИнтернетЭквайринг.УдалитьИдентификаторыОперации";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"ДокументОперации",
		ДокументОперации,
		Метаданные.ОпределяемыеТипы.ДокументОперацииИнтернетЭквайринга.Тип);
	
	ИнтернетЭквайрингСлужебный.ПроверитьПраваНаВыполнениеОперации();
	
	Идентификатор = РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.ИдентификаторОперации(
		ДокументОперации);
	
	Если Идентификатор = Неопределено
		Или Идентификатор = "" Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга");
		ЭлементБлокировки.УстановитьЗначение("ДокументОперации", ДокументОперации);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Набор = РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.СоздатьНаборЗаписей();
		Набор.Отбор.ДокументОперации.Установить(ДокументОперации);
		Набор.Заполнить(Неопределено);
		Набор.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		
		ВызватьИсключение НСтр("ru = 'Не удалось удалить данные операции Интернет-эквайринга.';
								|en = 'Cannot delete the Online acquiring transaction data.'");
		
	КонецПопытки;
	
КонецПроцедуры

// Выполняет установку признака загрузки статуса регламентным заданием.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, который отражает операцию в
//    информационной базе;
//  Значение - Булево - если Истина, данные статуса будут загружены регламентным заданием;
//
// Возвращаемое значение:
//  Булево - Истина, если признак отложенной загрузки статуса установлен, Ложь если операция не найдена.
//
Функция УстановитьОтложенноеПолучениеСтатуса(ДокументОперации, Значение) Экспорт
	
	ИнтернетЭквайрингСлужебный.ПроверитьПраваНаВыполнениеОперации();
	
	Возврат РегистрыСведений.ИдентификаторыОперацийИнтернетЭквайринга.УстановитьОтложенноеПолучениеСтатуса(
		ДокументОперации,
		Значение);
	
КонецФункции

#КонецОбласти

#КонецОбласти
