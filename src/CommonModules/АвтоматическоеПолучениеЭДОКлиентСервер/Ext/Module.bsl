// @strict-types

#Область СлужебныеПроцедурыИФункции

#Область Получение

// Возвращаемое значение:
//  Число
Функция Интервал() Экспорт
	Возврат 300;
КонецФункции

// Возвращает имя регламентного задания "Получение электронных документов".
// 
// Возвращаемое значение:
//  Строка
Функция ИмяРегламентногоЗадания() Экспорт
	Возврат "АвтоматическоеПолучениеЭДО";
КонецФункции

// Возвращаемое значение:
//  Строка
Функция ПолноеИмяПодсистемы() Экспорт
	Возврат ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСКонтрагентами + ".АвтоматическоеПолучениеЭДО";
КонецФункции

// Возвращаемое значение:
//  Строка
Функция КлючПараметраПериодическойОтправкиДанных() Экспорт
	Возврат ПолноеИмяПодсистемы();
КонецФункции

// Возвращаемое значение:
//  Строка
Функция КлючРезультатаПериодическойОтправкиДанных() Экспорт
	Возврат ПолноеИмяПодсистемы();
КонецФункции

// Возвращаемое значение:
//  Строка
Функция КлючУведомленийКлиента() Экспорт
	Возврат ПолноеИмяПодсистемы();
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ЕстьПредупреждения - Булево
//  * СостояниеПоИдентификаторамЭДО - см. РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.СостояниеПоИдентификаторамЭДО
Функция НовоеИзменениеСостояния() Экспорт
	
	ИзменениеСостояния = Новый Структура;
	ИзменениеСостояния.Вставить("ЕстьПредупреждения", Ложь);
	ИзменениеСостояния.Вставить("СостояниеПоИдентификаторамЭДО", Новый Соответствие);
	Возврат ИзменениеСостояния;
	
КонецФункции

#КонецОбласти

#Область Предупреждение

#Если Не ВнешнееСоединение Тогда

// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Состояние - см. РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.СостояниеПоИдентификаторуЭДО
//  Параметры - см. АвтоматическоеПолучениеЭДО.НовыеПараметрыРеквизитаФормы
Процедура УстановитьВидимостьПредупрежденияВФормеЭлемента(Форма, Состояние, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Состояние)
		Или ПустаяСтрока(Параметры.ИмяГруппыПредупреждение) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	ВидимостьНадписиНеВыполнено = Ложь;
	ВидимостьНадписиОстановлено = Ложь;
	
	Если Состояние.Остановлено Тогда
		ВидимостьНадписиОстановлено = Истина;
	ИначеЕсли Состояние.ЕстьОшибки Тогда
		ВидимостьНадписиНеВыполнено = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "НадписьАвтоматическоеПолученияЭДООстановлено", "Видимость", ВидимостьНадписиОстановлено);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, "НадписьАвтоматическоеПолученияЭДОНеВыполнено", "Видимость", ВидимостьНадписиНеВыполнено);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы, Параметры.ИмяГруппыПредупреждение, "Видимость",
		ВидимостьНадписиНеВыполнено Или ВидимостьНадписиОстановлено);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ЕстьПредупреждения - Булево
//  Параметры - см. АвтоматическоеПолучениеЭДО.НовыеПараметрыРеквизитаФормы
Процедура УстановитьВидимостьПредупрежденияВФормеСписка(Форма, ЕстьПредупреждения, Параметры) Экспорт
	
	Видимость = ЕстьПредупреждения;
	
	Элементы = Форма.Элементы;
	
	Если ЗначениеЗаполнено(Параметры.ИмяГруппыПредупреждение) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, Параметры.ИмяГруппыПредупреждение, "Видимость", Видимость);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ИмяСписка) Тогда
		Элементы[Параметры.ИмяСписка].Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

#КонецОбласти

#КонецОбласти
