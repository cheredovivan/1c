
#Область ПрограммныйИнтерфейс

// Обновляет статусы и информацию о разрешении продажи товаров
// 
// Параметры:
//  Объект - ДокументОбъект.ЧекККМ - Документ, из которой происходит вызов процедуры:
//  	* Товары - ТаблицаЗначений - Товары документа
Процедура ОбновитьСтатусыИРазрешеннияТоваров(Объект) Экспорт
	
	//++ Локализация
	ВетеринарныеПрепараты = Перечисления.ВидыПродукцииИС.ВетеринарныеПрепараты;
	Если Не ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции(ВетеринарныеПрепараты) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	Номенклатура.ПродаетсяПоРазрешению
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В (&СписокТоваров)
	|		И Номенклатура.ПродаетсяПоРазрешению";
	Запрос.УстановитьПараметр("СписокТоваров", Объект.Товары.Выгрузить().ВыгрузитьКолонку("Номенклатура"));
	
	СтатусРазрешенийТоваров = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТовары Из Объект.Товары Цикл
		
		Если СтрокаТовары.ВидПродукцииИС = Перечисления.ВидыПродукцииИС.ВетеринарныеПрепараты Тогда
			
			ПараметрыОтбора = Новый Структура("Номенклатура", СтрокаТовары.Номенклатура);
			СтатусРазрешенияТовара = СтатусРазрешенийТоваров.НайтиСтроки(ПараметрыОтбора);
			
			Если СтатусРазрешенияТовара.Количество() = 0 Тогда
				СтрокаТовары.ПродаетсяПоРазрешению = Ложь;
				СтрокаТовары.РеквизитыРазрешенияНаПродажу = "";
				Продолжить;
			КонецЕсли;
			
			СтрокаТовары.ПродаетсяПоРазрешению = Истина;
			
			Если Не ЗначениеЗаполнено(СтрокаТовары.ИдентификаторСтроки) Тогда
				СтрокаТовары.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
			КонецЕсли;
			
			СтрокиРазрешенияНаПродажу = Объект.РазрешенияНаПродажу.НайтиСтроки(
				Новый Структура("ИдентификаторСтроки", СтрокаТовары.ИдентификаторСтроки));
			
			Если СтрокиРазрешенияНаПродажу.Количество() <> 0 Тогда
				Если ЗначениеЗаполнено(СтрокиРазрешенияНаПродажу[0].ТипДокумента) Тогда
					СтрокаТовары.РеквизитыРазрешенияНаПродажу = "" + СтрокиРазрешенияНаПродажу[0].ТипДокумента
						+ " №" + СтрокиРазрешенияНаПродажу[0].НомерДокумента + " от "
						+ Формат(СтрокиРазрешенияНаПродажу[0].ДатаДокумента, "ДФ=dd.MM.yyyy");
				КонецЕсли;
			Иначе
				СтрокаТовары.РеквизитыРазрешенияНаПродажу = "<Не указано разрешение>";
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	//-- Локализация
	
КонецПроцедуры

// Обновляет статус и информацию о разрешении продажи товара в текущей строке
// 
// Параметры:
//  ТекущаяСтрока - ДанныеФормыКоллекция - Обрабатываемая строка
//  Объект - ДокументОбъект.ЧекККМ - Документ, из которой происходит вызов процедуры:
//  	* Товары - ТаблицаЗначений - Товары документа
Процедура ОбновитьСтатусИРазрешенниеНаПродажу(ТекущаяСтрока, Объект) Экспорт
	
	//++ Локализация
	ВетеринарныеПрепараты = Перечисления.ВидыПродукцииИС.ВетеринарныеПрепараты;
	Если Не ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции(ВетеринарныеПрепараты) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура.Ссылка КАК Номенклатура,
	|	Номенклатура.ПродаетсяПоРазрешению
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура
	|		И Номенклатура.ПродаетсяПоРазрешению";
	Запрос.УстановитьПараметр("Номенклатура", ТекущаяСтрока.Номенклатура);
	
	СтатусРазрешенийТоваров = Запрос.Выполнить().Выгрузить();
		
	Если ТекущаяСтрока.ВидПродукцииИС = Перечисления.ВидыПродукцииИС.ВетеринарныеПрепараты Тогда
		
		ПараметрыОтбора = Новый Структура("Номенклатура", ТекущаяСтрока.Номенклатура);
		СтатусРазрешенияТовара = СтатусРазрешенийТоваров.НайтиСтроки(ПараметрыОтбора);
		
		Если СтатусРазрешенияТовара.Количество() = 0 Тогда
			ТекущаяСтрока.ПродаетсяПоРазрешению = Ложь;
			ТекущаяСтрока.РеквизитыРазрешенияНаПродажу = "";
			Возврат;
		КонецЕсли;
		
		ТекущаяСтрока.ПродаетсяПоРазрешению = Истина;
		
		Если Не ЗначениеЗаполнено(ТекущаяСтрока.ИдентификаторСтроки) Тогда
			ТекущаяСтрока.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
		
		СтрокиРазрешенияНаПродажу = Объект.РазрешенияНаПродажу.НайтиСтроки(
			Новый Структура("ИдентификаторСтроки", ТекущаяСтрока.ИдентификаторСтроки));
		
		Если СтрокиРазрешенияНаПродажу.Количество() <> 0 Тогда
			Если ЗначениеЗаполнено(СтрокиРазрешенияНаПродажу[0].ТипДокумента) Тогда
				ТекущаяСтрока.РеквизитыРазрешенияНаПродажу = "" + СтрокиРазрешенияНаПродажу[0].ТипДокумента
					+ " №" + СтрокиРазрешенияНаПродажу[0].НомерДокумента + " от "
					+ Формат(СтрокиРазрешенияНаПродажу[0].ДатаДокумента, "ДФ=dd.MM.yyyy");
			КонецЕсли;
		Иначе
			ТекущаяСтрока.РеквизитыРазрешенияНаПродажу = "<Не указано разрешение>";
		КонецЕсли;
		
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Обработать наличие требования указать разрешения на продажу товаров.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма РМК
Процедура ОбработатьРазрешенияНаПродажуТоваров(Форма) Экспорт
	
	//++ Локализация
	Если Не ИнтеграцияИСМПКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции(Перечисления.ВидыПродукцииИС.ВетеринарныеПрепараты) Тогда
		Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(Форма.Элементы, "ТоварыРеквизитыРазрешенияНаПродажу") Тогда
			Форма.Элементы.УказатьРазрешениеНаПродажу.Видимость = Ложь;
			Форма.Элементы.ТоварыРеквизитыРазрешенияНаПродажу.Видимость = Ложь;
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("ПродаетсяПоРазрешению", Истина);
	СтрокиТоваров = Форма.Объект.Товары.НайтиСтроки(СтруктураПоиска);
	
	Форма.Элементы.УказатьРазрешениеНаПродажу.Видимость = Истина;
	Форма.Элементы.УказатьРазрешениеНаПродажу.Доступность = (СтрокиТоваров.Количество() > 0);
	Форма.Элементы.ТоварыРеквизитыРазрешенияНаПродажу.Видимость = (СтрокиТоваров.Количество() > 0);
	//-- Локализация
	
КонецПроцедуры

// Удаляет разрешения на продажу без связанных товаров
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма РМК
Процедура УдалитьРазрешенияНаПродажуБезСвязанныхТоваров(Форма) Экспорт
	
	//++ Локализация
	ВетеринарныеПрепараты = Перечисления.ВидыПродукцииИС.ВетеринарныеПрепараты;
	Если Не ИнтеграцияИСМпКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции(ВетеринарныеПрепараты) Тогда
		Возврат;
	КонецЕсли;
	
	РазрешенияНаПродажуКУдалению = Новый Массив;
		
	Для Каждого СтрокаРазрешенияНаПродажу Из Форма.Объект.РазрешенияНаПродажу Цикл
		
		ПараметрыПоиска = Новый Структура("ИдентификаторСтроки", СтрокаРазрешенияНаПродажу.ИдентификаторСтроки);
		СтрокиТовары = Форма.Объект.Товары.НайтиСтроки(ПараметрыПоиска);
		
		Если СтрокиТовары.Количество() = 0
			ИЛИ Не СтрокиТовары[0].ПродаетсяПоРазрешению Тогда
			РазрешенияНаПродажуКУдалению.Добавить(СтрокаРазрешенияНаПродажу);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из РазрешенияНаПродажуКУдалению Цикл
		Форма.Объект.РазрешенияНаПродажу.Удалить(СтрокаКУдалению);
	КонецЦикла;
	//-- Локализация
	
КонецПроцедуры

// Удаляет разрешение на продажу, если в текущей строке для товара оно не требуется
// 
// Параметры:
//  ТекущаяСтрока - ДанныеФормыКоллекция - Обрабатываемая строка
//  Форма - ФормаКлиентскогоПриложения - Форма РМК
Процедура УдалитьРазрешениеНаПродажуБезСвязанногоТовара(ТекущаяСтрока, Форма) Экспорт
	
	//++ Локализация
	ВетеринарныеПрепараты = Перечисления.ВидыПродукцииИС.ВетеринарныеПрепараты;
	Если Не ИнтеграцияИСМпКлиентСерверПовтИсп.ВестиУчетМаркируемойПродукции(ВетеринарныеПрепараты) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТекущаяСтрока.ПродаетсяПоРазрешению Тогда
		
		ПараметрыПоиска = Новый Структура("ИдентификаторСтроки", ТекущаяСтрока.ИдентификаторСтроки);
		СтрокиРазрешенияНаПродажу = Форма.Объект.РазрешенияНаПродажу.НайтиСтроки(ПараметрыПоиска);
		
		Если СтрокиРазрешенияНаПродажу.Количество() > 0 Тогда
			Форма.Объект.РазрешенияНаПродажу.Удалить(СтрокиРазрешенияНаПродажу[0]);
		КонецЕсли;
		
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

#Область ФискальныеОперации

//++ Локализация

// Возвращает товары и штрихкоды товаров для ИСМП
// 
// Параметры:
//  ДокументСсылка - ДокументСсылка - Документ ссылка
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
// 
// Возвращаемое значение:
//  Структура - Получить товары и штрихкоды для ИСМП:
// * Товары - ТаблицаЗначений - 
// * Штрихкоды - Массив Из Структура - 
Функция ПолучитьТоварыИШтрихкодыДляИСМП(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ШтрихкодыУпаковок.ШтрихкодУпаковки               КАК ШтрихкодУпаковки,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеВариантУчета   КАК ЧастичноеВыбытиеВариантУчета,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеНоменклатура   КАК ЧастичноеВыбытиеНоменклатура,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеХарактеристика КАК ЧастичноеВыбытиеХарактеристика,
	|	ШтрихкодыУпаковок.ЧастичноеВыбытиеКоличество     КАК ЧастичноеВыбытиеКоличество,
	|	&ИдентификаторЗапросаГИСМТ                       КАК РазрешительныйРежимИдентификаторЗапросаГИСМТ,
	|	&ДатаПолученияЗапросаГИСМТ                       КАК РазрешительныйРежимДатаЗапросаГИСМТ,
	|	&ИдентификаторЭкземпляраПОГИСМТ                  КАК РазрешительныйРежимИдентификаторЭкземпляраПО,
	|	&ВерсияБазыГИСМТ                                 КАК РазрешительныйРежимВерсияБазы
	|ИЗ
	|	ТаблицаАкцизныеМарки КАК ШтрихкодыУпаковок";
	
	МассивУпаковок                  = Новый Массив();
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЧекККМ") Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИдентификаторЗапросаГИСМТ",      "ШтрихкодыУпаковок.РазрешительныйРежимИдентификаторЗапросаГИСМТ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДатаПолученияЗапросаГИСМТ",      "ШтрихкодыУпаковок.РазрешительныйРежимДатаЗапросаГИСМТ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИдентификаторЭкземпляраПОГИСМТ", "ШтрихкодыУпаковок.РазрешительныйРежимИдентификаторЭкземпляраПО");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ВерсияБазыГИСМТ",                "ШтрихкодыУпаковок.РазрешительныйРежимВерсияБазы");
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ИдентификаторЗапросаГИСМТ",      "");
	Запрос.УстановитьПараметр("ДатаПолученияЗапросаГИСМТ",      "");
	Запрос.УстановитьПараметр("ИдентификаторЭкземпляраПОГИСМТ", "");
	Запрос.УстановитьПараметр("ВерсияБазыГИСМТ",                "");
	
	ВыборкаИзРезультатаПоШтрихкодам = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаИзРезультатаПоШтрихкодам.Следующий() Цикл
		
		НовыйЭлемент = ШтрихкодированиеИС.НовыйЭлементКоллекцииУпаковокДляРаспределенияПоТоварам();
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, ВыборкаИзРезультатаПоШтрихкодам);
		
		МассивУпаковок.Добавить(НовыйЭлемент);
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Серии.НомерСтроки КАК НомерСтроки,
	|	Серии.СерияТаблицыСерий КАК СерияТаблицыСерий,
	|	Серии.КоличествоУпаковок КАК Количество,
	|	Серии.Номенклатура КАК Номенклатура,
	|	Серии.Характеристика КАК Характеристика
	|ИЗ
	|	Серии КАК Серии";
	
	ТаблицаСерий = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Товары.НомерСтроки,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия КАК СерияТаблицыТоваров,
	|	Товары.СтатусУказанияСерий,
	|	Товары.ОсобенностьУчета,
	|	Товары.Упаковка,
	|	Товары.Количество,
	|	Товары.КоличествоУпаковок,
	|	Товары.Цена,
	|	Товары.СуммаСкидки КАК СуммаСкидки,
	|	Товары.СтавкаНДС КАК СтавкаНДС,
	|	Товары.СуммаНДС КАК СуммаНДС,
	|	Товары.СуммаСНДС КАК СуммаСНДС
	|ИЗ
	|	Товары КАК Товары
	|ГДЕ
	|	Товары.ОсобенностьУчета В (
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность2025),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияБезВЕТИС),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Шины),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Фотоаппараты),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Велосипеды),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КреслаКоляски),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Духи),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.АльтернативныйТабак),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.УпакованнаяВода),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Антисептики),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.БАДы),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.НикотиносодержащаяПродукция),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Пиво),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПродукцияИзНатуральногоМеха),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.СоковаяПродукция),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.БезалкогольноеПиво),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхБезВЕТИС),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхВлажныеБезВЕТИС),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхВлажныеПодконтрольныеВЕТИС),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МясоПодконтрольноеВЕТИС),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ВетеринарныеПрепараты),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ИгрыИИгрушкиДляДетей),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.РадиоэлектроннаяПродукция),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТитановаяМеталлопродукция),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияБезВЕТИС),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КонсервированнаяПродукцияПодконтрольнаяВЕТИС),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.РастительныеМасла),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОптоволокноИОптоволоконнаяПродукция),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПарфюмерныеИКосметическиеСредстваИБытоваяХимия),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПечатнаяПродукция),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.СтроительныеМатериалы),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ОтопительныеПриборы),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПиротехническиеИзделияИСредстваПожарнойБезопасности),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукцияДо9Процентов),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТелефоныИНоутбуки),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КабельнаяПродукция),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Бакалея),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МоторныеМасла),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.АвтозапчастиИКомплектующиеТранспортныхСредств),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.БезалкогольныеНапитки),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПивоВПотребительскихУпаковках),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТехническиеСредстваРеабилитации),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МедицинскиеИзделия),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МедицинскиеИзделия20),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПолимерныеТрубы),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.СладостиИКондитерскиеИзделия),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПечатныеПлаты),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ДезинфицирующиеСредства),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.БАДы2025),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.СпортивноеПитание),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.РастворимыеЗавариваемыеНапитки),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТоварыЛичнойГигиены),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.БритвыИЛезвия),
	|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.НапиткиССодержаниемМолокаКакаоПрочие))";
	
	ТаблицаТовары = Запрос.Выполнить().Выгрузить();
	
	ПоляИзмерения = Новый Массив;
	ПоляИзмерения.Добавить("Номенклатура");
	ПоляИзмерения.Добавить("Характеристика");
	
	ПоляДетализации = Новый Массив;
	ПоляДетализации.Добавить("СерияТаблицыСерий");
	
	ПоляСумм = Новый Массив;
	ПоляСумм.Добавить("СуммаСкидки");
	ПоляСумм.Добавить("СуммаНДС");
	ПоляСумм.Добавить("СуммаСНДС");
	ПоляСумм.Добавить("Количество");
	
	ТаблицаРезультатРаспределения = РаспределитьДанныеПоТаблицам(ТаблицаТовары, ТаблицаСерий, ПоляИзмерения, ПоляДетализации, ПоляСумм, "Количество");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаРезультатРаспределения.НомерСтроки КАК НомерСтроки,
	|	ТаблицаРезультатРаспределения.Номенклатура КАК Номенклатура,
	|	ТаблицаРезультатРаспределения.Характеристика КАК Характеристика,
	|	ТаблицаРезультатРаспределения.СерияТаблицыТоваров КАК СерияТаблицыТоваров,
	|	ТаблицаРезультатРаспределения.СерияТаблицыСерий КАК СерияТаблицыСерий,
	|	ТаблицаРезультатРаспределения.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ТаблицаРезультатРаспределения.ОсобенностьУчета КАК ОсобенностьУчета,
	|	ТаблицаРезультатРаспределения.Упаковка КАК Упаковка,
	|	ТаблицаРезультатРаспределения.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаРезультатРаспределения.Количество КАК Количество,
	|	ТаблицаРезультатРаспределения.Цена КАК Цена,
	|	ТаблицаРезультатРаспределения.СуммаСкидки КАК СуммаСкидки,
	|	ТаблицаРезультатРаспределения.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаРезультатРаспределения.СуммаНДС КАК СуммаНДС,
	|	ТаблицаРезультатРаспределения.СуммаСНДС КАК СуммаСНДС
	|ПОМЕСТИТЬ ТаблицаРезультатРаспределения
	|ИЗ
	|	&ТаблицаРезультатРаспределения КАК ТаблицаРезультатРаспределения
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРезультатРаспределения.НомерСтроки,
	|	ТаблицаРезультатРаспределения.Номенклатура,
	|	ТаблицаРезультатРаспределения.Характеристика,
	|	ТаблицаРезультатРаспределения.СерияТаблицыТоваров,
	|	ТаблицаРезультатРаспределения.СерияТаблицыСерий,
	|	ТаблицаРезультатРаспределения.СтатусУказанияСерий,
	|	ТаблицаРезультатРаспределения.ОсобенностьУчета,
	|	ТаблицаРезультатРаспределения.Упаковка,
	|	ТаблицаРезультатРаспределения.КоличествоУпаковок,
	|	ТаблицаРезультатРаспределения.Цена,
	|	ТаблицаРезультатРаспределения.СуммаСкидки,
	|	ТаблицаРезультатРаспределения.СтавкаНДС,
	|	ТаблицаРезультатРаспределения.СтавкаНДС.Ставка КАК СтавкаНДСЧислом,
	|	ТаблицаРезультатРаспределения.СуммаНДС,
	|	ТаблицаРезультатРаспределения.СуммаСНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаРезультатРаспределения.СерияТаблицыСерий = НЕОПРЕДЕЛЕНО
	|			ТОГДА ТаблицаРезультатРаспределения.СерияТаблицыТоваров
	|		ИНАЧЕ ТаблицаРезультатРаспределения.СерияТаблицыСерий
	|	КОНЕЦ КАК Серия,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|				КОГДА ТаблицаРезультатРаспределения.СерияТаблицыСерий = НЕОПРЕДЕЛЕНО
	|					ТОГДА ТаблицаРезультатРаспределения.СерияТаблицыТоваров
	|				ИНАЧЕ ТаблицаРезультатРаспределения.СерияТаблицыСерий
	|			КОНЕЦ = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ УчитыватьСерии,
	|	ТаблицаРезультатРаспределения.Количество КАК Количество,
	|	ТаблицаРезультатРаспределения.НомерСтроки КАК ИндексИсходнойСтроки
	|ИЗ
	|	ТаблицаРезультатРаспределения КАК ТаблицаРезультатРаспределения";
	Запрос.УстановитьПараметр("ТаблицаРезультатРаспределения", ТаблицаРезультатРаспределения);
	
	Товары = Запрос.Выполнить().Выгрузить();
	
	Возврат Новый Структура("Товары, Штрихкоды", Товары, МассивУпаковок);
	
КонецФункции

// Получить данные для передачи МОТП сведений о розничной продаже
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - Документ
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - 
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные о продажах
//
Функция ДанныеДляИСМП(ДокументСсылка, МенеджерВременныхТаблиц) Экспорт
	
	ДанныеДокумента = ПолучитьТоварыИШтрихкодыДляИСМП(ДокументСсылка, МенеджерВременныхТаблиц);
	
	Товары = ДанныеДокумента.Товары;
	Штрихкоды = ДанныеДокумента.Штрихкоды;
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЧекККМ")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЧекККМВозврат")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЧекККМКоррекции") Тогда
		
		ДокументСтруктурой = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Склад,Статус,Дата");
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		
		ДокументСтруктурой = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Склад,ХозяйственнаяОперация,Статус,Дата");
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		
		ДокументСтруктурой = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Склад,Дата,ДокументОснование");
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		
		ДокументСтруктурой = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Склад,Дата,ХозяйственнаяОперация");
	Иначе
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Некорректно встроен механизм распределения серий по товаром для расчета данных ИСМП: %1';
					|en = 'A tool for distributing series by goods for calculating ISMT data: %1'"), 
				ОписаниеОшибки());
	КонецЕсли;
	
	ПолноеИмя = ДокументСсылка.Метаданные().ПолноеИмя();
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмя);
	
	ПараметрыУказанияСерий = МенеджерОбъекта.ПараметрыУказанияСерий(ДокументСтруктурой);
	
	ТоварыРазобранные =
		РаспределитьШтрихкодыПоТаблицеТоваров(
			ДокументСсылка,
			Штрихкоды,
			ПараметрыУказанияСерий,
			Товары);
	
	Возврат ТоварыРазобранные;
	
КонецФункции

// Распределить штрихкоды по таблице товаров.
// 
// Параметры:
//  ДокументСсылка - ДокументСсылка - Документ ссылка
//  МассивУпаковок - Массив ИЗ см. ШтрихкодированиеИС.НовыйЭлементКоллекцииУпаковокДляРаспределенияПоТоварам - Массив
//  штрихкодов упаковок для распределения.
//  ПараметрыУказанияСерий - Структура - см. НоменклатураКлиентСервер.ПараметрыУказанияСерий - Параметры указания серий
//  Товары - ТаблицаЗначений - Товары
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Распределить штрихкоды по таблице товаров
Функция РаспределитьШтрихкодыПоТаблицеТоваров(ДокументСсылка, МассивУпаковок, ПараметрыУказанияСерий, Товары) Экспорт
	
	ДанныеРаспределения = ДанныеРаспределенияШтрихкодовУпаковокПоТоварам(
		ДокументСсылка, МассивУпаковок, ПараметрыУказанияСерий, Товары, Истина);
	
	Если ДанныеРаспределения.ЕстьОшибки Тогда
		ВызватьИсключение СтрСоединить(ДанныеРаспределения.Ошибки, Символы.ПС);
	КонецЕсли;
	
	ТекстОшибки = "";
	Для Каждого РаспределеннаяСтрока Из ДанныеРаспределения.РаспределенныеСтроки Цикл
		Для Каждого СтрокаЗначения Из РаспределеннаяСтрока.Значение Цикл
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаЗначения, "ТекстОшибки")
				И СтрокаЗначения.ТекстОшибки <> Неопределено И СтрокаЗначения.ТекстОшибки <> "" Тогда
				ТекстОшибки = ТекстОшибки + СтрокаЗначения.ТекстОшибки + Символы.ПС;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если ТекстОшибки <> Неопределено И ТекстОшибки <> "" Тогда
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	СтавкаНДСБезНДС = Справочники.СтавкиНДС.БезНДС;
	СтавкаНДС0 = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС0);
	
	ТоварыРазобранные = НоваяТаблицаРаспределенныхТоваровПоШтрихкодам(Товары.СкопироватьКолонки());
	ОписанияНоменклатурыСЧастичнымВыбытием = Новый Соответствие;
	
	Для Каждого СтрокаТовары Из Товары Цикл
		
		КоэффциентУпаковкиДокумента = СтрокаТовары.КоличествоУпаковок / СтрокаТовары.Количество;
		
		РаспределениеПоСтроке = ДанныеРаспределения.РаспределенныеСтроки.Получить(СтрокаТовары);
		
		Если РаспределениеПоСтроке = Неопределено
			ИЛИ (НЕ ИнтеграцияГИСМ.ПодсистемаНеИспользуется()
				И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаТовары, "ОсобенностьУчета")
				И СтрокаТовары.ОсобенностьУчета = Перечисления.ОсобенностиУчетаНоменклатуры.ПродукцияИзНатуральногоМеха) Тогда
			
			СтрокаТоварыРазобранные = ТоварыРазобранные.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоварыРазобранные, СтрокаТовары);
			Продолжить;
		КонецЕсли;
		
		пКоличествоУпаковок = СтрокаТовары.КоличествоУпаковок;
		пСуммаСкидки        = СтрокаТовары.СуммаСкидки;
		пСуммаНДС           = СтрокаТовары.СуммаНДС;
		пСуммаСНДС          = СтрокаТовары.СуммаСНДС;
		
		ОтклонениеСуммыСкидки = 0;
		ОтклонениеСуммыНДС = 0;
		ОтклонениеСуммыСНДС = 0;
		
		Для Каждого СтрокаРаспределения Из РаспределениеПоСтроке Цикл
			
			Если СтрокаТовары.КоличествоУпаковок = 0 Тогда
				Прервать;
			КонецЕсли;
			
			СтрокаТоварыРазобранные = ТоварыРазобранные.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоварыРазобранные, СтрокаТовары);
			
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаРаспределения, "ТекстОшибки") Тогда
				СтрокаТоварыРазобранные.ТекстОшибки = СтрокаРаспределения.ТекстОшибки;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТоварыРазобранные.ТекстОшибки) Тогда
				СтрокаТовары.Количество = СтрокаТовары.Количество - СтрокаРаспределения.Количество;
				Продолжить;
			КонецЕсли;
			
			СтрокаТоварыРазобранные.Количество = 1;
			СтрокаТоварыРазобранные.КоличествоУпаковок = СтрокаТоварыРазобранные.Количество
			                                             * КоэффциентУпаковкиДокумента;
			
			ТребуетсяПересчетИзУпаковкиВБазовуюЕдиницуИзмерения = Ложь;
			КоэффициентПересчетаУпаковки = КоэффциентУпаковкиДокумента;
				
			Если ЗначениеЗаполнено(СтрокаРаспределения.Штрихкод) Тогда
				
				Если СтрокаРаспределения.ЧастичноеВыбытие Тогда
					
					ОписаниеНоменклатуры = ОписаниеНоменклатуры(
						СтрокаТовары.Номенклатура,
						ОписанияНоменклатурыСЧастичнымВыбытием);
					
					СтрокаТоварыРазобранные.Упаковка = ОписаниеНоменклатуры.УпаковкаЧастичногоВыбытия;
					
					КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(
							СтрокаТоварыРазобранные.Упаковка,
							СтрокаТоварыРазобранные.Номенклатура);
					КоэффициентПересчетаУпаковки = 1 / КоэффициентУпаковки;
							
					СтрокаТоварыРазобранные.Количество = Мин(СтрокаРаспределения.Количество, СтрокаТовары.Количество);
					СтрокаТоварыРазобранные.КоличествоУпаковок = СтрокаТоварыРазобранные.Количество
					                                             * КоэффициентПересчетаУпаковки;
					
					СтрокаТоварыРазобранные.ДополнениеКНаименованиюТовара = СтруктураНаименованияТовара(
						СтрокаТовары,
						СтрокаРаспределения);
					
				ИначеЕсли СтрокаРаспределения.Количество <> 1 Тогда
					
					СтрокаТоварыРазобранные.Количество         = СтрокаРаспределения.Количество;
					СтрокаТоварыРазобранные.КоличествоУпаковок = СтрокаРаспределения.Количество
					                                             * КоэффициентПересчетаУпаковки;
					
					СтрокаТоварыРазобранные.ДополнениеКНаименованиюТовара = СтруктураНаименованияТовара(
						СтрокаТовары,
						СтрокаРаспределения);
					
				ИначеЕсли СтрокаРаспределения.ВидУпаковки <> Перечисления.ВидыУпаковокИС.ОбъемноСортовойУчет Тогда
					
					СтрокаТоварыРазобранные.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
					СтрокаТоварыРазобранные.Количество = СтрокаРаспределения.Количество;
					
					ВДокументеПродажаУпаковки = ЗначениеЗаполнено(СтрокаТовары.Упаковка);
					Если ВДокументеПродажаУпаковки Тогда
						ТребуетсяПересчетИзУпаковкиВБазовуюЕдиницуИзмерения = Истина;
					Иначе
						СтрокаТоварыРазобранные.КоличествоУпаковок = СтрокаРаспределения.Количество;
						КоэффициентПересчетаУпаковки = 1;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			СуммаСкидки = СтрокаТовары.СуммаСкидки;
			СуммаСкидкиЭталон = СуммаСкидки;
			СуммаНДС    = СтрокаТовары.СуммаНДС;
			СуммаСНДС   = СтрокаТовары.СуммаСНДС;
			СуммаСНДСЭталон = СуммаСНДС;
			Если Не СтрокаТовары.Количество = СтрокаТоварыРазобранные.Количество Тогда
				СуммаСкидкиЭталон = пСуммаСкидки  / пКоличествоУпаковок * СтрокаТоварыРазобранные.КоличествоУпаковок;
				СуммаСНДСЭталон   = пСуммаСНДС    / пКоличествоУпаковок * СтрокаТоварыРазобранные.КоличествоУпаковок;
				СуммаСкидки       = Окр(СуммаСкидкиЭталон, 2);
				СуммаНДС          = Окр(пСуммаНДС / пКоличествоУпаковок * СтрокаТоварыРазобранные.КоличествоУпаковок, 2);
				СуммаСНДС         = Окр(СуммаСНДСЭталон, 2);
			КонецЕсли;
			
			СтрокаТоварыРазобранные.КоэффициентПересчетаУпаковки = КоэффициентПересчетаУпаковки;
			
			СтрокаТоварыРазобранные.СуммаСкидки = СуммаСкидки;
			СтрокаТоварыРазобранные.СуммаНДС    = СуммаНДС;
			СтрокаТоварыРазобранные.СуммаСНДС   = СуммаСНДС;
			СтрокаТоварыРазобранные.Штрихкод    = СтрокаРаспределения.Штрихкод;
			СтрокаТоварыРазобранные.РезультатРаспределения = СтрокаРаспределения;
			
			Если Не (СтрокаТоварыРазобранные.СуммаСНДС = СуммаСНДСЭталон) Тогда
				ОтклонениеСуммыСНДСПоСтроке = СтрокаТоварыРазобранные.СуммаСНДС - СуммаСНДСЭталон;
				Если ОтклонениеСуммыСНДС + ОтклонениеСуммыСНДСПоСтроке >= 0.01 Тогда
					СтрокаТоварыРазобранные.СуммаСНДС = СтрокаТоварыРазобранные.СуммаСНДС - 0.01;
					ОтклонениеСуммыСНДС = ОтклонениеСуммыСНДС + ОтклонениеСуммыСНДСПоСтроке - 0.01;
				ИначеЕсли ОтклонениеСуммыСНДС + ОтклонениеСуммыСНДСПоСтроке <= -0.01 Тогда
					СтрокаТоварыРазобранные.СуммаСНДС = СтрокаТоварыРазобранные.СуммаСНДС + 0.01;
					ОтклонениеСуммыСНДС = ОтклонениеСуммыСНДС + ОтклонениеСуммыСНДСПоСтроке + 0.01;
				Иначе
					ОтклонениеСуммыСНДС = ОтклонениеСуммыСНДС + ОтклонениеСуммыСНДСПоСтроке;
				КонецЕсли;
			КонецЕсли;

			Если Не (СтрокаТоварыРазобранные.СуммаСкидки = СуммаСкидкиЭталон) Тогда
				ОтклонениеСуммыСкидкиПоСтроке = СтрокаТоварыРазобранные.СуммаСкидки - СуммаСкидкиЭталон;
				Если ОтклонениеСуммыСкидки + ОтклонениеСуммыСкидкиПоСтроке >= 0.01 Тогда
					СтрокаТоварыРазобранные.СуммаСкидки = СтрокаТоварыРазобранные.СуммаСкидки - 0.01;
					ОтклонениеСуммыСкидки = ОтклонениеСуммыСкидки + ОтклонениеСуммыСкидкиПоСтроке - 0.01;
				ИначеЕсли ОтклонениеСуммыСкидки + ОтклонениеСуммыСкидкиПоСтроке <= -0.01 Тогда
					СтрокаТоварыРазобранные.СуммаСкидки = СтрокаТоварыРазобранные.СуммаСкидки + 0.01;
					ОтклонениеСуммыСкидки = ОтклонениеСуммыСкидки + ОтклонениеСуммыСкидкиПоСтроке + 0.01;
				Иначе
					ОтклонениеСуммыСкидки = ОтклонениеСуммыСкидки + ОтклонениеСуммыСкидкиПоСтроке;
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ (СтрокаТоварыРазобранные.СтавкаНДС = СтавкаНДСБезНДС
				ИЛИ СтрокаТоварыРазобранные.СтавкаНДС = СтавкаНДС0
				ИЛИ СтрокаТоварыРазобранные.СуммаНДС = СтрокаТовары.СуммаНДС) Тогда
				
				ОтклонениеСуммыНДСПоСтроке = ОтклонениеСуммыНДС(СтрокаТоварыРазобранные);
				Если ОтклонениеСуммыНДС + ОтклонениеСуммыНДСПоСтроке >= 0.01 Тогда
					СтрокаТоварыРазобранные.СуммаНДС = СтрокаТоварыРазобранные.СуммаНДС - 0.01;
					ОтклонениеСуммыНДС = ОтклонениеСуммыНДС + ОтклонениеСуммыНДСПоСтроке - 0.01;
				ИначеЕсли ОтклонениеСуммыНДС + ОтклонениеСуммыНДСПоСтроке <= -0.01 Тогда
					СтрокаТоварыРазобранные.СуммаНДС = СтрокаТоварыРазобранные.СуммаНДС + 0.01;
					ОтклонениеСуммыНДС = ОтклонениеСуммыНДС + ОтклонениеСуммыНДСПоСтроке + 0.01;
				Иначе
					ОтклонениеСуммыНДС = ОтклонениеСуммыНДС + ОтклонениеСуммыНДСПоСтроке;
				КонецЕсли;
				
			КонецЕсли;
			
			Если СтрокаТовары.Упаковка = СтрокаТоварыРазобранные.Упаковка Тогда
				Цена = СтрокаТовары.Цена;
			Иначе
				Цена = Окр((СтрокаТоварыРазобранные.СуммаСНДС + СтрокаТоварыРазобранные.СуммаСкидки) / СтрокаТоварыРазобранные.КоличествоУпаковок, 2);
			КонецЕсли;
			
			СтрокаТоварыРазобранные.Цена    = Цена;
			СтрокаТовары.Количество         = СтрокаТовары.Количество - СтрокаТоварыРазобранные.Количество;
			СтрокаТовары.КоличествоУпаковок = СтрокаТовары.КоличествоУпаковок
			                                  - СтрокаТоварыРазобранные.Количество * КоэффциентУпаковкиДокумента;
			СтрокаТовары.СуммаСкидки        = СтрокаТовары.СуммаСкидки - СтрокаТоварыРазобранные.СуммаСкидки;
			СтрокаТовары.СуммаНДС           = СтрокаТовары.СуммаНДС - СтрокаТоварыРазобранные.СуммаНДС;
			СтрокаТовары.СуммаСНДС          = СтрокаТовары.СуммаСНДС - СтрокаТоварыРазобранные.СуммаСНДС;
			
			СтрокаТоварыРазобранные.ТребуетсяПересчетИзУпаковкиВБазовуюЕдиницуИзмерения
			                        = ТребуетсяПересчетИзУпаковкиВБазовуюЕдиницуИзмерения;
			Если ТребуетсяПересчетИзУпаковкиВБазовуюЕдиницуИзмерения Тогда
				СтрокаТоварыРазобранные.Цена = Окр((СтрокаТоварыРазобранные.СуммаСНДС + СтрокаТоварыРазобранные.СуммаСкидки) / СтрокаТоварыРазобранные.КоэффициентПересчетаУпаковки, 2);
				СтрокаТоварыРазобранные.КоличествоУпаковок = СтрокаТоварыРазобранные.Количество;
				СтрокаТоварыРазобранные.КоэффициентПересчетаУпаковки = 1;
			КонецЕсли;
			
		КонецЦикла;
		
		Если СтрокаТовары.Количество > 0 Тогда
			СтрокаТоварыРазобранные = ТоварыРазобранные.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТоварыРазобранные, СтрокаТовары);
			
			СтрокаТоварыРазобранные.Цена = Окр((СтрокаТоварыРазобранные.СуммаСНДС + СтрокаТоварыРазобранные.СуммаСкидки) / СтрокаТоварыРазобранные.КоличествоУпаковок, 2);
			
			ОтклонениеСуммыНДСПоСтроке = ОтклонениеСуммыНДС(СтрокаТоварыРазобранные);
			Если ОтклонениеСуммыНДС + ОтклонениеСуммыНДСПоСтроке >= 0.01 Тогда
				СтрокаТоварыРазобранные.СуммаНДС = СтрокаТоварыРазобранные.СуммаНДС - 0.01;
			ИначеЕсли ОтклонениеСуммыНДС + ОтклонениеСуммыНДСПоСтроке <= -0.01 Тогда
				СтрокаТоварыРазобранные.СуммаНДС = СтрокаТоварыРазобранные.СуммаНДС + 0.01;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТоварыРазобранные;

КонецФункции

// Данные распределения штрихкодов упаковок по товарам.
// 
// Параметры:
//  ДокументСсылка - ДокументСсылка - Документ для получения данных распределения
//  МассивУпаковок - Массив ИЗ см. ШтрихкодированиеИС.НовыйЭлементКоллекцииУпаковокДляРаспределенияПоТоварам - Массив
//  штрихкодов упаковок для распределения.
//  ПараметрыУказанияСерий - Структура - см. НоменклатураКлиентСервер.ПараметрыУказанияСерий - Параметры указания серий
//  Товары - ТаблицаЗначений - Товары для распределения по штрихкодам упаковок
//  ЭтоПробитиеЧека - Булево - Истина, если выполняется пробитие чека на ККТ.
// 
// Возвращаемое значение:
//  см. ШтрихкодированиеИСМП.РаспределениеШтрихкодовУпаковокПоТоварам
Функция ДанныеРаспределенияШтрихкодовУпаковокПоТоварам(
	ДокументСсылка,
	МассивУпаковок,
	ПараметрыУказанияСерий,
	Товары,
	ЭтоПробитиеЧека = Ложь) Экспорт
	
	ДопустимыТоварыБезМарок = РозничныеПродажиЛокализация.ДопустимыТоварыБезМарокВДокументе(ДокументСсылка);
	
	ПараметрыРаспределения = ШтрихкодированиеИСМП.НовыеПараметрыРаспределенияШтрихкодовУпаковок();
	ПараметрыРаспределения.ДокументСсылка          = ДокументСсылка;
	ПараметрыРаспределения.ПараметрыУказанияСерий  = ПараметрыУказанияСерий;
	ПараметрыРаспределения.ДопустимыТоварыБезМарок = ДопустимыТоварыБезМарок;
	ПараметрыРаспределения.ЭтоПробитиеЧека         = ЭтоПробитиеЧека;
	
	ДанныеРаспределения = ШтрихкодированиеИСМП.РаспределениеШтрихкодовУпаковокПоТоварам(
		ПараметрыРаспределения,
		Товары,
		МассивУпаковок);
	
	Возврат ДанныеРаспределения;

КонецФункции

// Функция получает данные для формирования печатной формы КМ-6.
//
// Возвращаемое значение:
// 	Структура:
//		* РезультатЗапроса - РезультатЗапроса - Содержит информацию для заполнения печатной формы
//		* ЗаголовокДокумента - Строка - Отображаемый заголовок табличного документа
Функция ПолучитьДанныеДляПечатнойФормыКМ6(ПараметрыПечати, МассивОбъектов) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	// МассивОбъектов содержит документы одного вида.	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ТЧТовары.Сумма), 0) КАК СуммаПродаж,
	|	ЕСТЬNULL(СУММА(ТЧТовары.СуммаНДС), 0) КАК СуммаПродажНДС,
	|	0 КАК СуммаПродажОтчет,
	|	0 КАК СуммаПродажОтчетНДС,
	|	0 КАК СуммаВозвратов,
	|	0 КАК СуммаВозвратовНДС,
	|	0 КАК СуммаПродажПодарочныхСертификатов,
	|	0 КАК СуммаВозвратовПодарочныхСертификатов,	
	|	ОтчетОРозничныхПродажах.Ссылка КАК Ссылка,
	|	ОтчетОРозничныхПродажах.Ссылка КАК ОтчетОРозничныхПродажах	
	|ПОМЕСТИТЬ ПродажиИВозвраты
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЧекККМ.Товары КАК ТЧТовары
	|		ПО (ТЧТовары.Ссылка.КассоваяСмена = ОтчетОРозничныхПродажах.КассоваяСмена)
	|			И (ТЧТовары.Ссылка.Проведен)
	|			И (ТЧТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит))
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетОРозничныхПродажах.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ТЧТовары.Сумма), 0) КАК СуммаПродаж,
	|	ЕСТЬNULL(СУММА(ТЧТовары.СуммаНДС), 0) КАК СуммаПродажНДС,
	|	0 КАК СуммаПродажОтчет,
	|	0 КАК СуммаПродажОтчетНДС,
	|	0 КАК СуммаВозвратов,
	|	0 КАК СуммаВозвратовНДС,
	|	0 КАК СуммаПродажПодарочныхСертификатов,
	|	0 КАК СуммаВозвратовПодарочныхСертификатов,
	|	ОтчетОРозничныхВозвратах.Ссылка КАК Ссылка,
	|	ОтчетОРозничныхПродажах.Ссылка КАК ОтчетОРозничныхПродажах	
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах КАК ОтчетОРозничныхВозвратах
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах 
	|	ПО ОтчетОРозничныхПродажах.КассоваяСмена = ОтчетОРозничныхВозвратах.КассоваяСмена
	|		И ОтчетОРозничныхПродажах.НалогообложениеНДС = ОтчетОРозничныхВозвратах.НалогообложениеНДС	
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЧекККМ.Товары КАК ТЧТовары
	|		ПО (ТЧТовары.Ссылка.КассоваяСмена = ОтчетОРозничныхПродажах.КассоваяСмена)
	|			И (ТЧТовары.Ссылка.Проведен)
	|			И (ТЧТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит))
	|ГДЕ
	|	ОтчетОРозничныхВозвратах.Ссылка В(&МассивОбъектов)
	|	И ОтчетОРозничныхПродажах.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетОРозничныхПродажах.Ссылка,
	|	ОтчетОРозничныхВозвратах.Ссылка
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	ЕСТЬNULL(СУММА(ТЧТоварыОтчет.Сумма), 0),
	|	ЕСТЬNULL(СУММА(ТЧТоварыОтчет.СуммаНДС), 0),
	|	0,
	|	0,
	|	0,
	|	0,
	|	ОтчетОРозничныхПродажах.Ссылка КАК Ссылка,
	|	ОтчетОРозничныхПродажах.Ссылка КАК ОтчетОРозничныхПродажах	
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.Товары КАК ТЧТоварыОтчет
	|		ПО (ТЧТоварыОтчет.Ссылка = ОтчетОРозничныхПродажах.Ссылка)
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетОРозничныхПродажах.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	ЕСТЬNULL(СУММА(ТЧТоварыОтчет.Сумма), 0),
	|	ЕСТЬNULL(СУММА(ТЧТоварыОтчет.СуммаНДС), 0),
	|	0,
	|	0,
	|	0,
	|	0,
	|	ОтчетОРозничныхВозвратах.Ссылка КАК Ссылка,
	|	ОтчетОРозничныхПродажах.Ссылка КАК ОтчетОРозничныхПродажах
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах КАК ОтчетОРозничныхВозвратах
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах 
	|	ПО ОтчетОРозничныхПродажах.КассоваяСмена = ОтчетОРозничныхВозвратах.КассоваяСмена
	|		И ОтчетОРозничныхПродажах.НалогообложениеНДС = ОтчетОРозничныхВозвратах.НалогообложениеНДС
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.Товары КАК ТЧТоварыОтчет
	|	ПО (ТЧТоварыОтчет.Ссылка = ОтчетОРозничныхПродажах.Ссылка)
	|ГДЕ
	|	ОтчетОРозничныхВозвратах.Ссылка В(&МассивОбъектов)
	|	И ОтчетОРозничныхПродажах.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетОРозничныхПродажах.Ссылка,
	|	ОтчетОРозничныхВозвратах.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЕСТЬNULL(СУММА(ТЧТовары.Сумма), 0),
	|	ЕСТЬNULL(СУММА(ТЧТовары.СуммаНДС), 0),
	|	0,
	|	0,
	|	ОтчетОРозничныхПродажах.Ссылка,
	|	ОтчетОРозничныхПродажах.Ссылка КАК ОтчетОРозничныхПродажах	
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЧекККМВозврат.Товары КАК ТЧТовары
	|		ПО (ТЧТовары.Ссылка.КассоваяСмена = ОтчетОРозничныхПродажах.КассоваяСмена)
	|			И (ТЧТовары.Ссылка.Проведен)
	|			И (ТЧТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит))
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетОРозничныхПродажах.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЕСТЬNULL(СУММА(ТЧТовары.Сумма), 0),
	|	ЕСТЬNULL(СУММА(ТЧТовары.СуммаНДС), 0),
	|	0,
	|	0,
	|	ОтчетОРозничныхВозвратах.Ссылка КАК Ссылка,
	|	ОтчетОРозничныхПродажах.Ссылка КАК ОтчетОРозничныхПродажах	
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах КАК ОтчетОРозничныхВозвратах
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах 
	|	ПО ОтчетОРозничныхПродажах.КассоваяСмена = ОтчетОРозничныхВозвратах.КассоваяСмена
	|		И ОтчетОРозничныхПродажах.НалогообложениеНДС = ОтчетОРозничныхВозвратах.НалогообложениеНДС	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЧекККМВозврат.Товары КАК ТЧТовары
	|	ПО (ТЧТовары.Ссылка.КассоваяСмена = ОтчетОРозничныхПродажах.КассоваяСмена)
	|		И (ТЧТовары.Ссылка.Проведен)
	|		И (ТЧТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит))
	|ГДЕ
	|	ОтчетОРозничныхВозвратах.Ссылка В(&МассивОбъектов)
	|	И ОтчетОРозничныхПродажах.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетОРозничныхПродажах.Ссылка,
	|	ОтчетОРозничныхВозвратах.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЕСТЬNULL(СУММА(ТЧПодарочныеСертификаты.Сумма), 0),
	|	0,	
	|	ОтчетОРозничныхПродажах.Ссылка,
	|	ОтчетОРозничныхПродажах.Ссылка КАК ОтчетОРозничныхПродажах
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияПодарочныхСертификатов.ПодарочныеСертификаты КАК ТЧПодарочныеСертификаты
	|		ПО (ТЧПодарочныеСертификаты.Ссылка.КассоваяСмена = ОтчетОРозничныхПродажах.КассоваяСмена)
	|			И (ТЧПодарочныеСертификаты.Ссылка.Проведен)
	|			И (ТЧПодарочныеСертификаты.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит))
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетОРозничныхПродажах.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЕСТЬNULL(СУММА(ТЧПодарочныеСертификаты.Сумма), 0),
	|	0,
	|	ОтчетОРозничныхВозвратах.Ссылка КАК Ссылка,
	|	ОтчетОРозничныхПродажах.Ссылка КАК ОтчетОРозничныхПродажах	
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах КАК ОтчетОРозничныхВозвратах
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах 
	|	ПО ОтчетОРозничныхПродажах.КассоваяСмена = ОтчетОРозничныхВозвратах.КассоваяСмена
	|		И ОтчетОРозничныхПродажах.НалогообложениеНДС = ОтчетОРозничныхВозвратах.НалогообложениеНДС		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияПодарочныхСертификатов.ПодарочныеСертификаты КАК ТЧПодарочныеСертификаты
	|		ПО (ТЧПодарочныеСертификаты.Ссылка.КассоваяСмена = ОтчетОРозничныхПродажах.КассоваяСмена)
	|			И (ТЧПодарочныеСертификаты.Ссылка.Проведен)
	|			И (ТЧПодарочныеСертификаты.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит))
	|ГДЕ
	|	ОтчетОРозничныхВозвратах.Ссылка В(&МассивОбъектов)
	|	И ОтчетОРозничныхПродажах.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетОРозничныхПродажах.Ссылка,
	|	ОтчетОРозничныхВозвратах.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЕСТЬNULL(СУММА(ТЧПодарочныеСертификаты.Сумма), 0),
	|	ОтчетОРозничныхПродажах.Ссылка,
	|	ОтчетОРозничныхПродажах.Ссылка КАК ОтчетОРозничныхПродажах	
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратПодарочныхСертификатов.ПодарочныеСертификаты КАК ТЧПодарочныеСертификаты
	|		ПО (ТЧПодарочныеСертификаты.Ссылка.КассоваяСмена = ОтчетОРозничныхПродажах.КассоваяСмена)
	|			И (ТЧПодарочныеСертификаты.Ссылка.Проведен)
	|			И (ТЧПодарочныеСертификаты.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит))
	|ГДЕ
	|	ОтчетОРозничныхПродажах.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетОРозничныхПродажах.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ЕСТЬNULL(СУММА(ТЧПодарочныеСертификаты.Сумма), 0),
	|	ОтчетОРозничныхВозвратах.Ссылка КАК Ссылка,
	|	ОтчетОРозничныхПродажах.Ссылка КАК ОтчетОРозничныхПродажах	
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах КАК ОтчетОРозничныхВозвратах
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах 
	|	ПО ОтчетОРозничныхПродажах.КассоваяСмена = ОтчетОРозничныхВозвратах.КассоваяСмена
	|		И ОтчетОРозничныхПродажах.НалогообложениеНДС = ОтчетОРозничныхВозвратах.НалогообложениеНДС	
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратПодарочныхСертификатов.ПодарочныеСертификаты КАК ТЧПодарочныеСертификаты
	|		ПО (ТЧПодарочныеСертификаты.Ссылка.КассоваяСмена = ОтчетОРозничныхПродажах.КассоваяСмена)
	|			И (ТЧПодарочныеСертификаты.Ссылка.Проведен)
	|			И (ТЧПодарочныеСертификаты.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит))
	|ГДЕ
	|	ОтчетОРозничныхВозвратах.Ссылка В(&МассивОбъектов)
	|	И ОтчетОРозничныхПродажах.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтчетОРозничныхПродажах.Ссылка,
	|	ОтчетОРозничныхВозвратах.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПродажиИВозвраты.Ссылка КАК Ссылка,
	|	ПродажиИВозвраты.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах,
	|	СУММА(ПродажиИВозвраты.СуммаПродаж) КАК СуммаПродаж,
	|	СУММА(ПродажиИВозвраты.СуммаПродажНДС) КАК СуммаПродажНДС,
	|	СУММА(ПродажиИВозвраты.СуммаПродажОтчет) КАК СуммаПродажОтчет,
	|	СУММА(ПродажиИВозвраты.СуммаПродажОтчетНДС) КАК СуммаПродажОтчетНДС,
	|	СУММА(ПродажиИВозвраты.СуммаВозвратов) КАК СуммаВозвратов,
	|	СУММА(ПродажиИВозвраты.СуммаВозвратовНДС) КАК СуммаВозвратовНДС,
	|	СУММА(ПродажиИВозвраты.СуммаПродажПодарочныхСертификатов) КАК СуммаПродажПодарочныхСертификатов,
	|	СУММА(ПродажиИВозвраты.СуммаВозвратовПодарочныхСертификатов) КАК СуммаВозвратовПодарочныхСертификатов
	|ПОМЕСТИТЬ ПродажиВозвратыСгруппировано
	|ИЗ
	|	ПродажиИВозвраты КАК ПродажиИВозвраты
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажиИВозвраты.Ссылка,
	|	ПродажиИВозвраты.ОтчетОРозничныхПродажах	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах КАК Ссылка,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.Дата КАК ДатаДокумента,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.ЦенаВключаетНДС КАК ЦенаВключаетНДС,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.Номер КАК НомерДокумента,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.Валюта КАК Валюта,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.КассаККМ КАК КассаККМ,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.КассаККМ.ТипКассы КАК ТипКассы,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.КассаККМ.Представление КАК ККМПредставление,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.КассаККМ.СерийныйНомер КАК СерийныйНомер,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.КассаККМ.РегистрационныйНомер КАК РегистрационныйНомер,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.Организация КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.Организация.Префикс КАК Префикс,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.Ответственный.ФизическоеЛицо КАК Ответственный,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.КассоваяСмена КАК КассоваяСмена,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.КассоваяСмена.НачалоКассовойСмены КАК НачалоСмены,
	|	ПродажиВозвратыСгруппировано.ОтчетОРозничныхПродажах.КассоваяСмена.ОкончаниеКассовойСмены КАК ОкончаниеСмены,
	|	ПродажиВозвратыСгруппировано.СуммаПродаж КАК СуммаПродаж,
	|	ПродажиВозвратыСгруппировано.СуммаПродажНДС КАК СуммаПродажНДС,
	|	ПродажиВозвратыСгруппировано.СуммаПродажОтчет КАК СуммаПродажОтчет,
	|	ПродажиВозвратыСгруппировано.СуммаПродажОтчетНДС КАК СуммаПродажОтчетНДС,
	|	ПродажиВозвратыСгруппировано.СуммаВозвратов КАК СуммаВозвратов,
	|	ПродажиВозвратыСгруппировано.СуммаВозвратовНДС КАК СуммаВозвратовНДС,
	|	ПродажиВозвратыСгруппировано.СуммаПродажПодарочныхСертификатов КАК СуммаПродажПодарочныхСертификатов,
	|	ПродажиВозвратыСгруппировано.СуммаВозвратовПодарочныхСертификатов КАК СуммаВозвратовПодарочныхСертификатов
	|ИЗ
	|	ПродажиВозвратыСгруппировано КАК ПродажиВозвратыСгруппировано
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ПродажиВозвратыСгруппировано.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДанныхДляПечати    = Новый Структура("РезультатЗапроса", РезультатЗапроса);
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции	
	
// Функция получает данные для формирования печатной формы КМ-3.
//
// Возвращаемое значение:
// 	Структура:
//		* РезультатЗапроса - РезультатЗапроса - Содержит информацию для заполнения печатной формы
//		* ЗаголовокДокумента - Строка - Отображаемый заголовок табличного документа
Функция ПолучитьДанныеДляПечатнойФормыКМ3(ПараметрыПечати, МассивОбъектов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// МассивОбъектов содержит документы одного вида.	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Номер КАК Номер,
	|	Док.Дата КАК ДатаДокумента,
	|	Док.КассаККМ КАК КассаККМ,
	|	Док.КассаККМ.ТипКассы КАК ТипКассы,
	|	Док.КассаККМ.Представление КАК Покупатель,
	|	Док.Ответственный.ФизическоеЛицо КАК КассирККМ,
	|	Док.Валюта КАК Валюта,
	|	Док.Организация КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	Док.Организация.Представление КАК Поставщик,
	|	Док.КассаККМ.СерийныйНомер КАК СерийныйНомерККМ,
	|	Док.КассаККМ.РегистрационныйНомер КАК РегистрационныйНомерККМ,
	|	Док.КассаККМ.Наименование КАК ПредставлениеККМ
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК Док
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО Док.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|ГДЕ
	|	Док.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Номер КАК Номер,
	|	Док.Дата КАК ДатаДокумента,
	|	Док.КассаККМ КАК КассаККМ,
	|	Док.КассаККМ.ТипКассы КАК ТипКассы,
	|	Док.КассаККМ.Представление КАК Покупатель,
	|	Док.Ответственный.ФизическоеЛицо КАК КассирККМ,
	|	Док.Валюта КАК Валюта,
	|	Док.Организация КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	Док.Организация.Представление КАК Поставщик,
	|	Док.КассаККМ.СерийныйНомер КАК СерийныйНомерККМ,
	|	Док.КассаККМ.РегистрационныйНомер КАК РегистрационныйНомерККМ,
	|	Док.КассаККМ.Наименование КАК ПредставлениеККМ
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах КАК Док
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО Док.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|ГДЕ
	|	Док.Ссылка В(&МассивОбъектов)	
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокОтчетОРозничныхПродажах.Ссылка КАК Документ,
	|	ФискальныеОперации.НомерЧекаККМ КАК НомерЧека,
	|	ДокЧек.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ДокОтчетОРозничныхПродажах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЧекККМВозврат КАК ДокЧек
	|		ПО ДокОтчетОРозничныхПродажах.КассоваяСмена = ДокЧек.КассоваяСмена
	|			И (ДокЧек.Проведен)
	|			И (ДокЧек.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит))
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|		ПО ФискальныеОперации.ДокументОснование = ДокЧек.ЧекККМ
	|ГДЕ
	|	ДокОтчетОРозничныхПродажах.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокОтчетОРозничныхПродажах.Ссылка КАК Документ,
	|	ФискальныеОперации.НомерЧекаККМ КАК НомерЧека,
	|	ДокЧек.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ДокОтчетОРозничныхПродажах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратПодарочныхСертификатов КАК ДокЧек
	|		ПО ДокОтчетОРозничныхПродажах.КассоваяСмена = ДокЧек.КассоваяСмена
	|			И (ДокЧек.Проведен)
	|			И (ДокЧек.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит))
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|		ПО ФискальныеОперации.ДокументОснование = ДокЧек.РеализацияПодарочныхСертификатов
	|ГДЕ
	|	ДокОтчетОРозничныхПродажах.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокОтчетОРозничныхВозвратах.Ссылка КАК Документ,
	|	ФискальныеОперации.НомерЧекаККМ КАК НомерЧека,
	|	ДокЧек.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах КАК ДокОтчетОРозничныхВозвратах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЧекККМВозврат КАК ДокЧек
	|		ПО ДокОтчетОРозничныхВозвратах.КассоваяСмена = ДокЧек.КассоваяСмена
	|			И (ДокЧек.Проведен)
	|			И (ДокЧек.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит))
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|		ПО ФискальныеОперации.ДокументОснование = ДокЧек.ЧекККМ
	|ГДЕ
	|	ДокОтчетОРозничныхВозвратах.Ссылка В(&МассивОбъектов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокОтчетОРозничныхВозвратах.Ссылка КАК Документ,
	|	ФискальныеОперации.НомерЧекаККМ КАК НомерЧека,
	|	ДокЧек.СуммаДокумента КАК СуммаДокумента
	|ИЗ
	|	Документ.ОтчетОРозничныхВозвратах КАК ДокОтчетОРозничныхВозвратах
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратПодарочныхСертификатов КАК ДокЧек
	|		ПО ДокОтчетОРозничныхВозвратах.КассоваяСмена = ДокЧек.КассоваяСмена
	|			И (ДокЧек.Проведен)
	|			И (ДокЧек.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит))
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФискальныеОперации КАК ФискальныеОперации
	|		ПО ФискальныеОперации.ДокументОснование = ДокЧек.РеализацияПодарочныхСертификатов
	|ГДЕ
	|	ДокОтчетОРозничныхВозвратах.Ссылка В(&МассивОбъектов)
	|УПОРЯДОЧИТЬ ПО
	|	Документ
	|ИТОГИ ПО
	|	Документ";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	СтруктураДанныхДляПечати    = Новый Структура("РезультатЗапроса", РезультатЗапроса);
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Определяет по документу закупки, применяет ли организация Патент
// 
// Параметры:
//  ДокументЗакупки - ДокументСсылка - Документ закупки
// 
// Возвращаемое значение:
//  Булево - Документ закупки по Патенту
Функция ДокументЗакупкиПоПатенту(ДокументЗакупки) Экспорт
	
	ДокументЗакупкиПоПатенту = Ложь;
	
	Если ТипЗнч(ДокументЗакупки) = Тип("ДокументСсылка.ЗаказПоставщику")
		ИЛИ ТипЗнч(ДокументЗакупки) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
		ИЛИ ТипЗнч(ДокументЗакупки) = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов") Тогда
		
		ЗакупкаПодДеятельность = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументЗакупки, "ЗакупкаПодДеятельность");
		Если ЗакупкаПодДеятельность = Перечисления.ТипыНалогообложенияНДС.ПродажаПоПатенту Тогда
			ДокументЗакупкиПоПатенту = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДокументЗакупкиПоПатенту;
	
КонецФункции

// Возвращает заводской номер ФН
// 
// Параметры:
//  ИдентификаторУстройства - СправочникСсылка.ПодключаемоеОборудование - Идентификатор устройства
// 
// Возвращаемое значение:
//  Строка - номер ФН
Функция ЗаводскойНомерФискальногоНакопителя(ИдентификаторУстройства) Экспорт
	
	ЗаводскойНомерФН = "";

	Если ОбщегоНазначенияБПО.ИспользуетсяЧекопечатающиеУстройства() Тогда
		МодульОборудованиеЧекопечатающиеУстройства = ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеЧекопечатающиеУстройства");
		ПараметрыРегистрации = МодульОборудованиеЧекопечатающиеУстройства.ПараметрыРегистрацииУстройства(ИдентификаторУстройства);
		Если ПараметрыРегистрации.Свойство("ЗаводскойНомерФН") И ЗначениеЗаполнено(ПараметрыРегистрации.ЗаводскойНомерФН) Тогда
			ЗаводскойНомерФН = ПараметрыРегистрации.ЗаводскойНомерФН;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗаводскойНомерФН;
	
КонецФункции

//-- Локализация

// Проверяет, допустимы ли в документе товары
// 
// Параметры:
//  ДокументСсылка - ДокументСсылка - Документ для проверки
// 
// Возвращаемое значение:
//  Булево - Допустимы товары без марок в документе
Функция ДопустимыТоварыБезМарокВДокументе(ДокументСсылка) Экспорт
	
	ДопустимыТоварыБезМарокДляДокумента = Ложь;
	
	//++ Локализация
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЧекККМВозврат") Тогда
		
		ДопустимыТоварыБезМарокДляДокумента = Истина;
	КонецЕсли;
	//-- Локализация
	
	Возврат ДопустимыТоварыБезМарокДляДокумента;
	
КонецФункции

// Текст запроса для получения документов по распоряжению (заказу клиента)
// 
// Возвращаемое значение:
//  Строка - Текст запроса документы по распоряжению
Функция ТекстЗапросаДокументыПоРаспоряжению() Экспорт
	
	//@skip-check bsl-ql-hub
	ТекстЗапроса = "ВЫБРАТЬ
	|	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов
	|ПОМЕСТИТЬ ВТОбъектыРасчетов
	|ИЗ
	|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|ГДЕ
	|	ОбъектыРасчетов.Объект = &ОбъектРасчетов
	|	И НЕ ОбъектыРасчетов.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Дата КАК Дата
	|ПОМЕСТИТЬ ВТДокументыПоРаспоряжению
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Проведен
	|	И РеализацияТоваровУслуг.ЗаказКлиента = &Распоряжение
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ПриходныйКассовыйОрдер.Ссылка,
	|	ПриходныйКассовыйОрдер.Ссылка.Дата
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ПриходныйКассовыйОрдер
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ПриходныйКассовыйОрдер.ОбъектРасчетов = ОбъектыРасчетов.ОбъектРасчетов
	|			И (ПриходныйКассовыйОрдер.Ссылка.Проведен)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ОперацияПоПлатежнойКарте.Ссылка,
	|	ОперацияПоПлатежнойКарте.Ссылка.Дата
	|ИЗ
	|	Документ.ОперацияПоПлатежнойКарте.РасшифровкаПлатежа КАК ОперацияПоПлатежнойКарте
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ОперацияПоПлатежнойКарте.ОбъектРасчетов = ОбъектыРасчетов.ОбъектРасчетов
	|			И (ОперацияПоПлатежнойКарте.Ссылка.Проведен)
	//++ Локализация
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ОперацияПоЯндексКассе.Ссылка,
	|	ОперацияПоЯндексКассе.Ссылка.Дата
	|ИЗ
	|	Документ.ОперацияПоЯндексКассе КАК ОперацияПоЯндексКассе
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ОперацияПоЯндексКассе.ОбъектРасчетов = ОбъектыРасчетов.ОбъектРасчетов
	|			И (ОперацияПоЯндексКассе.Ссылка.Проведен)
	//-- Локализация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыПоРаспоряжению.Ссылка КАК Ссылка,
	|	ДокументыПоРаспоряжению.Дата КАК Дата,
	|	НАЧАЛОПЕРИОДА(ДокументыПоРаспоряжению.Дата, ДЕНЬ) КАК ДатаДень
	|ИЗ
	|	ВТДокументыПоРаспоряжению КАК ДокументыПоРаспоряжению
	//++ Локализация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОчередьЧековККТ КАК ОчередьЧековККТ
	|		ПО ДокументыПоРаспоряжению.Ссылка = ОчередьЧековККТ.ДокументОснование
	|ГДЕ
	|	ОчередьЧековККТ.ДокументОснование ЕСТЬ NULL
	//-- Локализация
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыПоРаспоряжению.Дата УБЫВ
	|ИТОГИ ПО
	|	ДатаДень";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает систему налогообложения организации на дату
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой определяется система налогообложения
// 	Дата - Дата - Дата, на которую определяется система налогообложения
// Возвращаемое значение:
// 	ПеречислениеСсылка.ТипыСистемНалогообложенияККТ - Система налогообложения для параметров фискальных чеков
Функция СистемаНалогообложенияФискальнойОперации(Организация, Дата = Неопределено) Экспорт
	
	Если ТипЗнч(Организация) <> Тип("СправочникСсылка.Организации")
		ИЛИ НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	Т.СистемаНалогообложения КАК СистемаНалогообложения,
	|	&ПараметрДляПодстановки КАК ОбъектНалогообложенияУСН
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&ТекущаяДата, Организация = &Организация) КАК Т
	|";
//++ Локализация
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПараметрДляПодстановки", "ЕСТЬNULL(Т2.ОбъектНалогообложенияУСН, НЕОПРЕДЕЛЕНО)");
	ТекстЗапроса = ТекстЗапроса + "
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаУСН.СрезПоследних(&ТекущаяДата, Организация = &Организация) КАК Т2
	|		ПО Т.Организация = Т2.Организация
	|";
//-- Локализация

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ПараметрДляПодстановки", Неопределено);
	
	Если Дата = Неопределено Тогда
		Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Иначе
		Запрос.УстановитьПараметр("ТекущаяДата", Дата);
	КонецЕсли;
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Организация,
		"ГоловнаяОрганизация, ОбособленноеПодразделение");
		
	Если РеквизитыОрганизации.ОбособленноеПодразделение = Истина Тогда
		Запрос.УстановитьПараметр("Организация", РеквизитыОрганизации.ГоловнаяОрганизация);
	Иначе
		Запрос.УстановитьПараметр("Организация", Организация);
	КонецЕсли;
	
	ДанныеСистемыНалогообложения = Запрос.Выполнить().Выбрать();
	ДанныеСистемыНалогообложения.Следующий();
	
	СистемаНалогообложения = Неопределено;
	
	Если ДанныеСистемыНалогообложения.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая Тогда
		
		СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ОСН;

//++ Локализация		
	ИначеЕсли ДанныеСистемыНалогообложения.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная Тогда
		
		Если ДанныеСистемыНалогообложения.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы Тогда
			
			СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоход;
			
		ИначеЕсли ДанныеСистемыНалогообложения.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы Тогда
			
			СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.УСНДоходРасход;
			
		КонецЕсли;
//-- Локализация
		
	КонецЕсли;
	
	Возврат СистемаНалогообложения;
	
КонецФункции

// Определяет, является ли номенклатура алкогольной продукцией, имеющая уникальный идентификатор (маркируовку)
// 
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - Проверяемая номенклатура
// 
// Возвращаемое значение:
//  Булево - Истина, если номенклатура является алкогольной продукцией, имеющая уникальный идентификатор (маркируовку)
Функция АлкогольнаяПродукцияЕГАИСМаркируемая(Номенклатура) Экспорт
	
	//++ Локализация
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|	Номенклатура.ВидАлкогольнойПродукции КАК ВидПродукции,
	|	Номенклатура.ВидАлкогольнойПродукции.Маркируемый КАК ВидПродукцииМаркируемый
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура
	|	И Номенклатура.ВидАлкогольнойПродукции.Маркируемый = ИСТИНА";
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	//-- Локализация
	
	//@skip-check bsl-legacy-check-method-for-statements-after-return
	Возврат Ложь;

КонецФункции

// Возвращает возможность печатать отдельный слип чек эквайринговой операции.
// Если Истина, то предполагается, что слип-чек эквайрингового терминала выводится отдельно и
// фискальный чек не содержит данные эквайринговой операции.
// 
// Возвращаемое значение:
// 	Булево
Функция ПечататьОтдельныйСлипЧекЭквайринговойОперации() Экспорт

	Результат = Ложь;
	
	//++ Локализация
	
	Результат = ПолучитьФункциональнуюОпцию("ПечататьОтдельныйСлипЧекЭквайринговойОперации");
	
	//-- Локализация
	
	Возврат Результат;

КонецФункции

// Проверяет, разрешена ли в документе продажа подарочного сертификата в зависимости
// от хозяйственной операции документа.
// 
// Параметры:
//  ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция, по которой проверяется
//  	разрешена ли продажа подарочного сертификата в документе.
// 
// Возвращаемое значение:
//  Булево - Разрешена продажа подарочного сертификата в документе
Функция РазрешенаПродажаПодарочногоСертификатаВДокументе(ХозяйственнаяОперация) Экспорт
	
	РазрешенаПродажаПодарочногоСертификатаВДокументе = Ложь;
	
	//++ Локализация
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов")
		И ПолучитьФункциональнуюОпцию("ИспользоватьПодарочныеСертификаты")
		И (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
			ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту) Тогда
		
		РазрешенаПродажаПодарочногоСертификатаВДокументе = Истина;
	КонецЕсли;
	//-- Локализация
	
	Возврат РазрешенаПродажаПодарочногоСертификатаВДокументе;
	
КонецФункции

// Записывает объект формы
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма клиентского приложения:
//  * Объект - ДокументОбъект - Основной реквизит формы документа
//  ПараметрыФискализации - Структура -
//  ПараметрыЗаписи - Структура -
//  УспешноЗаписанДокумент - Булево - признак успешной записи документа
Процедура ЗаписатьФискальнуюОперацию(Форма, ПараметрыФискализации, ПараметрыЗаписи, УспешноЗаписанДокумент) Экспорт
	
	//++ Локализация
	Если Не УспешноЗаписанДокумент Тогда
		Возврат;
	КонецЕсли;
		
	Объект = Форма.Объект;
	Если (ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ЧекККМ")) Тогда
		ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования(Форма);
		ОбщегоНазначенияИС.СписатьОстатокПоВскрытымПотребительскимУпаковкам(Объект, ПараметрыСканирования);
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

//++ Локализация

#Область ОплатаЭСФСС

// Установить функциональную опцию оплаты ЭС ФСС в реквизите формы.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура УстановитьФункциональнуюОпциюОплатаЭСФСС(Форма) Экспорт
	
	Форма.ИспользоватьОплатуЭСФСС = ПолучитьФункциональнуюОпцию("ОплатаЭлектроннымиСертификатамиФСС");
	
КонецПроцедуры

// Настроить видимость элементов оплаты ЭС ФСС на форме панели администрирования УТ "Продажи"
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ПанельАдминистрированияУТПродажиНастроитьВидимостьЭлементовОплатыЭСФСС(Форма) Экспорт
	
	Форма.Элементы.ГруппаПараметрыПодключенияНСПК.Видимость =
		ПолучитьФункциональнуюОпцию("ОплатаЭлектроннымиСертификатамиФСС");
	
КонецПроцедуры

// Заполнить основные параметры подключения к НСПК
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ПанельАдминистрированияУТПродажиЗаполнитьПараметрыПодключенияНСПК(Форма) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ОплатаЭлектроннымиСертификатамиФСС") Тогда
		Возврат;
	КонецЕсли;
	
	ОбщиеПараметрыПодключенияНСПК = ЭлектронныеСертификатыНСПКУТ.ОбщиеПараметрыПодключенияНСПК();
	Форма.АдресСервисаНСПК = ОбщиеПараметрыПодключенияНСПК.АдресСервисаНСПК;
	Форма.КлючДоступаНСПК = ОбщиеПараметрыПодключенияНСПК.КлючДоступаНСПК;
	
	Форма.ПроксиСерверНСПК = ОбщиеПараметрыПодключенияНСПК.ПараметрыПроксиНСПК.Сервер;
	Форма.ПроксиПортНСПК = ОбщиеПараметрыПодключенияНСПК.ПараметрыПроксиНСПК.Порт;
	Форма.ПроксиПользовательНСПК = ОбщиеПараметрыПодключенияНСПК.ПараметрыПроксиНСПК.Пользователь;
	Форма.ПроксиПарольНСПК = ОбщиеПараметрыПодключенияНСПК.ПараметрыПроксиНСПК.Пароль;
	
	ВерсияКомпонентыНСПК = ЭлектронныеСертификатыНСПК.ВерсияВК();
	Форма.Элементы.ГруппаПараметрыПодключенияНСПК.Заголовок = СтрШаблон(
		НСтр("ru = 'Настройки подключения к сервису НСПК (компонента v. %1)';
			|en = 'NSPK service connection settings (the add-in v. %1)'"), ВерсияКомпонентыНСПК);
	
КонецПроцедуры

// Настроить видимость элементов оплаты ЭС ФСС на форме элемента справочника "Кассы ККМ"
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура КассаККМНастроитьВидимостьЭлементовОплатыЭСФСС(Форма) Экспорт
	
	Форма.Элементы.ЛокализацияПараметрыНСПК.Видимость = Ложь;
	Если Форма.Объект.ТипКассы = Перечисления.ТипыКассККМ.ФискальныйРегистратор
			ИЛИ Форма.Объект.ТипКассы = Перечисления.ТипыКассККМ.АвтономнаяККМ Тогда
		Форма.Элементы.ЛокализацияПараметрыНСПК.Видимость = ПолучитьФункциональнуюОпцию("ОплатаЭлектроннымиСертификатамиФСС");
		Форма.Элементы.КлючОрганизацииНСПК.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
		
		Форма.Элементы.ТестовоеПодключениеНСПК.Доступность = Истина;
		Форма.Элементы.ТестовоеПодключениеНСПК.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
		Если Не ЗначениеЗаполнено(Форма.Объект.ПротоколОбменаНСПК) 
			Или Форма.Объект.ПротоколОбменаНСПК = Перечисления.ПротоколОбменаФЭСНСПК.ВерсияV1 Тогда
			Форма.Элементы.ТестовоеПодключениеНСПК.Доступность = Ложь;
			Форма.Элементы.ТестовоеПодключениеНСПК.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСправа;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

// Заполнить параметры подключения к НСПК для "Кассы ККМ"
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура КассаККМЗаполнитьПараметрыПодключенияНСПК(Форма) Экспорт
	
	Если НЕ (ПолучитьФункциональнуюОпцию("ОплатаЭлектроннымиСертификатамиФСС")
		И ЗначениеЗаполнено(Форма.Объект.Ссылка)
		И Форма.Объект.ТипКассы = Перечисления.ТипыКассККМ.ФискальныйРегистратор) Тогда
		
		Возврат;
	КонецЕсли;
	
	ПараметрыПодключенияНСПК = ЭлектронныеСертификатыНСПКУТ.ПараметрыПодключенияОрганизацииНСПК(Форма.Объект.Владелец);
	Форма.КлючОрганизацииНСПК = ПараметрыПодключенияНСПК.КлючОрганизацииНСПК;
	
КонецПроцедуры

// Записать параметры подключения к НСПК для "Кассы ККМ" 
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//  ТекущийОбъект - СправочникОбъект.КассыККМ - Касса ККМ
Процедура КассаККМЗаписатьПараметрыПодключенияОрганизацииНСПК(Форма, ТекущийОбъект) Экспорт
	
	Если НЕ (ПолучитьФункциональнуюОпцию("ОплатаЭлектроннымиСертификатамиФСС")
		И Форма.Объект.ТипКассы = Перечисления.ТипыКассККМ.ФискальныйРегистратор) Тогда
		
		Возврат;
	КонецЕсли;
	
	КлючОрганизацииНСПК = "";
	Если Не ПустаяСтрока(Форма.КлючОрганизацииНСПК) Тогда 
		КлючОрганизацииНСПК = Форма.КлючОрганизацииНСПК;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	СтароеЗначениеПараметровПодключения = ЭлектронныеСертификатыНСПКУТ.ПараметрыПодключенияОрганизацииНСПК(Форма.Объект.Владелец);
	Если КлючОрганизацииНСПК <> СтароеЗначениеПараметровПодключения.КлючОрганизацииНСПК Тогда
		ОрганизацияОбъект = Форма.Объект.Владелец.ПолучитьОбъект(); // СправочникОбъект.Организации
		ОрганизацияОбъект.КлючОрганизацииНСПК = КлючОрганизацииНСПК;
		Попытка
			ОрганизацияОбъект.Записать();
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ШаблонТекстаОшибки = НСтр("ru = 'Не удалось записать данные о ключе организации %1 для подключения к НСПК по кассе ККМ %2 по причине:
			|%3';
			|en = 'Cannot record data about the %1 company key when connecting to NSPK via cash register %2 due to:
			|%3'");
			ТекстОшибки = СтрШаблон(ШаблонТекстаОшибки, Форма.Объект.Владелец, Форма.Объект.Ссылка, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			ВызватьИсключение ТекстОШибки;
		КонецПопытки;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Настроить видимость элементов оплаты ЭС ФСС на форме элемента справочника "Организации"
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма клиентского приложения:
//  *Объект - СправочникОбъект - Основной реквизит формы справочника
Процедура ОрганизацияНастроитьВидимостьЭлементовОплатыЭСФСС(Форма) Экспорт
	
	Форма.Элементы.ЛокализацияПараметрыНСПК.Видимость = 
		ПолучитьФункциональнуюОпцию("ОплатаЭлектроннымиСертификатамиФСС")
		И ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций")
		И Форма.Объект.Ссылка <> Справочники.Организации.УправленческаяОрганизация;
	
КонецПроцедуры

// Настроить видимость элементов оплаты ЭС ФСС на форме элемента справочника "ЭквайринговыеТерминалы"
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ЭквайринговыйТерминалНастроитьВидимостьЭлементовОплатыЭСФСС(Форма) Экспорт
	
	Форма.Элементы.ЛокализацияГруппаЭлементыОплатыЭСФСС.Видимость = 
		ПолучитьФункциональнуюОпцию("ОплатаЭлектроннымиСертификатамиФСС");
	
КонецПроцедуры

// Возвращает сумму оплаты электронными сертификатам ФСС по документу
// 
// Параметры:
//  ОплатыПлатежнойКартой - ТаблицаЗначений - Оплаты платежной картой:
//  * ВидОплаты - ПеречислениеСсылка.ТипыПлатежнойСистемыККТ -
//  * Сумма - Число - 
// 
// Возвращаемое значение:
//  Число - сумма оплаты электронными сертификатами ФСС по документу
Функция СуммаОплатыЭСФССПоДокументу(ОплатыПлатежнойКартой) Экспорт
	
	СуммаОплатыЭСФСС = 0;
	
	Для Каждого СтрокаТЗ Из ОплатыПлатежнойКартой Цикл
		
		Если СтрокаТЗ.ВидОплаты = Перечисления.ТипыПлатежнойСистемыККТ.СертификатНСПК Тогда
			СуммаОплатыЭСФСС = СуммаОплатыЭСФСС + СтрокаТЗ.Сумма;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СуммаОплатыЭСФСС;
	
КонецФункции

// Возвращает сумму оплаты СБП по документу
// 
// Параметры:
//  ОплатыПлатежнойКартой - ТаблицаЗначений - Оплаты платежной картой:
//  * ВидОплаты - ПеречислениеСсылка.ТипыПлатежнойСистемыККТ -
//  * Сумма - Число - 
//  * СтатусОплатыСБП - ПеречислениеСсылка.ТипыСтатусовОплатыСБП - 
//  ТолькоОплатаВыполнена - Булево - Истина - Выполнена
// 
// Возвращаемое значение:
//  Число - сумма оплаты СБП по документу
Функция СуммаОплатыСБППоДокументу(ОплатыПлатежнойКартой, ТолькоОплатаВыполнена = Ложь) Экспорт
	
	СуммаОплатыСБП = 0;
	
	Для Каждого СтрокаТЗ Из ОплатыПлатежнойКартой Цикл
		
		Если СтрокаТЗ.ВидОплаты = Перечисления.ТипыПлатежнойСистемыККТ.СистемаБыстрыхПлатежей Тогда
			
			Если ТолькоОплатаВыполнена И СтрокаТЗ.СтатусОплатыСБП = Перечисления.ТипыСтатусовОплатыСБП.Выполнена
				ИЛИ Не ТолькоОплатаВыполнена Тогда
				
				СуммаОплатыСБП = СуммаОплатыСБП + СтрокаТЗ.Сумма;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СуммаОплатыСБП;
	
КонецФункции

#КонецОбласти

#Область ОплатаСБП

// Установить признак возможности использования оплаты СБП на формах РМК.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма документа, для которой определяется возможность использования оплаты СБП
Процедура УстановитьВозможностьИспользованиеОплатыСБПНаФормахРМК(Форма) Экспорт
	
	Форма.ИспользоватьОплатуСБП = ДляОрганизацииНастроеноПодключениеСБП(
		Форма.Объект.Организация,
		Форма.Объект.КассаККМ);
	
КонецПроцедуры

// Проверяет наличие настроек интеграции с СБП для организации.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
//  КассаККМ - СправочникСсылка.КассыККМ
//
// Возвращаемое значение:
//  Булево - 
//
Функция ДляОрганизацииНастроеноПодключениеСБП(Организация, КассаККМ) Экспорт

	Результат = Ложь;
	Если Справочники.НастройкиРМК.СписокДоговоровИПодключенийСБП(Организация, КассаККМ).Количество() > 0 Тогда
		Результат = Истина;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

// Получить из платежной системы идентификатор оплаты в платежной системе.
// 
// Параметры:
//  ПараметрыПроцедуры - Структура - Параметры процедуры
//  АдресРезультата - Строка - Адрес результата
Процедура ИдентификаторОплатыВПлатежнойСистеме(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	ПередВыполнениемОперацииВПлатежнойСистеме(ПараметрыПроцедуры.ДокументОплаты);
	
	РезультатОперации = ПереводыСБПc2b.ДинамическаяСсылка(
		ПараметрыПроцедуры.ДокументОплаты,
		ПараметрыПроцедуры.НастройкаПодключения,
		ПараметрыПроцедуры);
	
	Если ТипЗнч(РезультатОперации) = Тип("Структура") Тогда
		РезультатОперации.Вставить("ДокументОплаты", ПараметрыПроцедуры.ДокументОплаты);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Получить статус оплаты в платежной системе.
// 
// Параметры:
//  ПараметрыПроцедуры - Структура - Параметры процедуры
//  АдресРезультата - Строка - Адрес результата
Процедура СтатусОплатыВПлатежнойСистеме(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	ПередВыполнениемОперацииВПлатежнойСистеме(ПараметрыПроцедуры.ДокументОплаты);
	
	РезультатОперации = ПереводыСБПc2b.СтатусОплаты(
		ПараметрыПроцедуры.ДокументОплаты,
		Неопределено);
	
	Если ТипЗнч(РезультатОперации) = Тип("Структура") Тогда
		РезультатОперации.Вставить("СуммаОперации", СуммаОперации(ПараметрыПроцедуры.ДокументОплаты));
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Выполнить возврат оплаты в платежной системе
// 
// Параметры:
//  ПараметрыПроцедуры - Структура - Параметры процедуры
//  АдресРезультата - Строка - Адрес результата
Процедура ВыполнитьВозвратОплаты(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	ПередВыполнениемОперацииВПлатежнойСистеме(ПараметрыПроцедуры.ДокументВозврата);
	
	ОбъектОплаты = ?(ЗначениеЗаполнено(ПараметрыПроцедуры.ИдентификаторОплаты),
			ПараметрыПроцедуры.ИдентификаторОплаты,
			ПараметрыПроцедуры.ДокументОплаты);
	
	РезультатОперации = ПереводыСБПc2b.ВозвратОплаты(
		ПараметрыПроцедуры.ДокументВозврата,
		ОбъектОплаты,
		ПараметрыПроцедуры.НастройкаПодключения,
		ПараметрыПроцедуры.БанкКлиента,
		ПараметрыПроцедуры);
	
	Если ТипЗнч(РезультатОперации) = Тип("Структура") Тогда
		РезультатОперации.Вставить("ДокументВозврата", ПараметрыПроцедуры.ДокументВозврата);
	КонецЕсли;
		
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Получить статус возврата оплаты в платежной системе.
// 
// Параметры:
//  ПараметрыПроцедуры - Структура - Параметры процедуры
//  АдресРезультата - Строка - Адрес результата
Процедура СтатусВозвратаОплатыВПлатежнойСистеме(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	ПередВыполнениемОперацииВПлатежнойСистеме(ПараметрыПроцедуры.ДокументВозврата);
	
	РезультатОперации = ПереводыСБПc2b.СтатусВозврата(
		ПараметрыПроцедуры.ДокументВозврата,
		Неопределено);
	
	Если ТипЗнч(РезультатОперации) = Тип("Структура") Тогда
		РезультатОперации.Вставить("СуммаОперации", СуммаОперации(ПараметрыПроцедуры.ДокументВозврата));
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Подтверждает возврат оплаты и возвращает результат подтверждения
// 
// Параметры:
//  ДокументВозврата - ДокументСсылка - Документ возврата
// 
// Возвращаемое значение:
//  см. ПереводыСБПc2b.ПодтвердитьВозврат
Функция ПодтвердитьВозвратОплаты(ДокументВозврата) Экспорт
	
	ПередВыполнениемОперацииВПлатежнойСистеме(ДокументВозврата);
	
	Возврат ПереводыСБПc2b.ПодтвердитьВозврат(
		ДокументВозврата,
		Неопределено);
	
КонецФункции

// Активирует кассовую ссылку для приема оплаты
// 
// Параметры:
//  ПараметрыПроцедуры - Структура:
//  	*ДокументОплаты - см. ПереводыСБПc2b.АктивироватьКассовуюСсылку.ДокументОперации 
//  	*ДанныеСсылки - см. ПереводыСБПc2b.АктивироватьКассовуюСсылку.ДанныеСсылки 
//  	*ТорговаяТочка - см. ПереводыСБПc2b.АктивироватьКассовуюСсылку.НастройкаПодключения 
//  АдресРезультата - Строка - Адрес результата
Процедура АктивироватьКассовуюСсылку(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	ПередВыполнениемОперацииВПлатежнойСистеме(ПараметрыПроцедуры.ДокументОплаты);
	
	РезультатОперации = ПереводыСБПc2b.АктивироватьКассовуюСсылку(
		ПараметрыПроцедуры.ДокументОплаты,
		ПараметрыПроцедуры.ДанныеСсылки,
		ПараметрыПроцедуры.ТорговаяТочка,
		ПараметрыПроцедуры);
		
	Если ТипЗнч(РезультатОперации) = Тип("Структура") Тогда
		РезультатОперации.Вставить("ДокументОплаты", ПараметрыПроцедуры.ДокументОплаты);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Деактивирует кассовую ссылку, оплата по ней становится невозможной
// 
// Параметры:
//  ПараметрыПроцедуры - Структура:
//  	*ДанныеСсылки - см. ПереводыСБПc2b.ДеактивироватьКассовуюСсылку.ДанныеСсылки 
//  	*ТорговаяТочка - см. ПереводыСБПc2b.ДеактивироватьКассовуюСсылку.НастройкаПодключения 
//  АдресРезультата - Строка - Адрес результата
Процедура ДеактивироватьКассовуюСсылку(ПараметрыПроцедуры, АдресРезультата) Экспорт
	
	ПередВыполнениемОперацииВПлатежнойСистеме(ПараметрыПроцедуры.ДокументОплаты);
	
	РезультатОперации = ПереводыСБПc2b.ДеактивироватьКассовуюСсылку(
		ПараметрыПроцедуры.ДанныеСсылки,
		ПараметрыПроцедуры.ТорговаяТочка);
		
	Если ТипЗнч(РезультатОперации) = Тип("Структура") Тогда
		РезультатОперации.Вставить("ДокументОплаты", ПараметрыПроцедуры.ДокументОплаты);
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(РезультатОперации, АдресРезультата);
	
КонецПроцедуры

// Определяет настройку подключения к платежной системе по договору подключения.
// 
// Параметры:
//  ДоговорПодключения - СправочникСсылка.ДоговорыЭквайринга - Договор подключения к платежной системе
//  Магазин - СправочникСсылка.Склады - Магазин подключения к платежной ссылке
// 
// Возвращаемое значение:
//  СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - Настройка подключения к платежной системе
Функция НастройкаПодключения(ДоговорПодключения, Магазин) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Договор,
	|	НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Магазин,
	|	НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Интеграция
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииСПлатежнымиСистемамиУТ КАК НастройкиИнтеграцииСПлатежнымиСистемамиУТ
	|ГДЕ
	|	НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Договор = &ДоговорПодключения
	|	И НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Интеграция.Используется";
	Запрос.УстановитьПараметр("ДоговорПодключения", ДоговорПодключения);

	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ИнтеграцияОбщая = Неопределено;
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Магазин) Тогда
			Если ЗначениеЗаполнено(Выборка.Магазин) Тогда
				Если Выборка.Магазин = Магазин
					Или Магазин.ПринадлежитЭлементу(Выборка.Магазин) Тогда
					Возврат Выборка.Интеграция;
				КонецЕсли;
			Иначе
				ИнтеграцияОбщая = Выборка.Интеграция;
			КонецЕсли;
		Иначе
			Возврат Выборка.Интеграция;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИнтеграцияОбщая;
	
КонецФункции

// Настройки подключения к платежной системе по договору подключения.
// 
// Параметры:
//  ДоговорПодключения - СправочникСсылка.ДоговорыЭквайринга - Договор подключения к платежной системе
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Настройка подключения к платежной системе:
//	* Договор - СправочникСсылка.ДоговорыЭквайринга - 
//	* Магазин - СправочникСсылка.Склады - 
//	* НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
Функция НастройкиПодключения(ДоговорПодключения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Договор,
	|	НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Магазин,
	|	НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Интеграция КАК НастройкаПодключения
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииСПлатежнымиСистемамиУТ КАК НастройкиИнтеграцииСПлатежнымиСистемамиУТ
	|ГДЕ
	|	НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Договор = &ДоговорПодключения";
	Запрос.УстановитьПараметр("ДоговорПодключения", ДоговорПодключения);

	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Определяет договор подключения к платежной системе по настройке подключения.
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - Настройка подключения к платежной системе
//  ТолькоДействующийДоговор - Булево - Подбирать только действующий договор.
// 
// Возвращаемое значение:
//  СправочникСсылка.ДоговорыЭквайринга - договор подключения к платежной системе
//
Функция ДоговорПодключения(НастройкаПодключения, ТолькоДействующийДоговор = Ложь) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Договор,
	|	НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Интеграция
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииСПлатежнымиСистемамиУТ КАК НастройкиИнтеграцииСПлатежнымиСистемамиУТ
	|ГДЕ
	|	НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Интеграция = &НастройкаПодключения
	|	И (&ВсеДоговора 
	|		ИЛИ НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Договор.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует))";
	
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	Запрос.УстановитьПараметр("ВсеДоговора", Не ТолькоДействующийДоговор);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Ложь);

	Если Выборка.Следующий() Тогда
		Результат =  Выборка.Договор;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Возвращает гиперссылку настройки подключения к платежной системе по договору подключения.
// 
// Параметры:
//  ОбъектСсылка - СправочникСсылка.ДоговорыЭквайринга - Договор подключения к платежной системе
// 
// Возвращаемое значение:
//  ФорматированнаяСтрока - Гиперссылка настройки подключения к платежной системе
Функция СформироватьНадписьНастройкаПодключенияПлатежнойСистемы(ОбъектСсылка) Экспорт
	
	СтрокаНастройкаПодключенияПлатежнойСистемы =
		"<a href = НастроитьПодключениеКПлатежнойСистеме>+ " + НСтр("ru = 'Настроить подключение к платежной системе';
																	|en = 'Set up connection to the payment system'") + "</a>";

	Если ЗначениеЗаполнено(ОбъектСсылка) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(НастройкиИнтеграции.Интеграция) КАК КоличествоНастроекПодключения
		|ИЗ
		|	РегистрСведений.НастройкиИнтеграцииСПлатежнымиСистемамиУТ КАК НастройкиИнтеграции
		|ГДЕ
		|	НастройкиИнтеграции.Договор = &Ссылка
		|;
		|
		|ВЫБРАТЬ
		|	НастройкиИнтеграции.Интеграция КАК Интеграция
		|ИЗ
		|	РегистрСведений.НастройкиИнтеграцииСПлатежнымиСистемамиУТ КАК НастройкиИнтеграции
		|ГДЕ
		|	НастройкиИнтеграции.Договор = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ОбъектСсылка);
		
		УстановитьПривилегированныйРежим(Истина);
		
		Результат = Запрос.ВыполнитьПакет();
	
		СтрокаНастройкаПодключенияПлатежнойСистемы = "";
	
		КоличествоНастроекПодключения = Результат[0].Выгрузить()[0].КоличествоНастроекПодключения;
		Если КоличествоНастроекПодключения > 1 Тогда
		
			СтрокаНастройкаПодключенияПлатежнойСистемы =
				"<a href = СписокНастроекПодключения>"
				+ НСтр("ru = 'Настройки подключения';
						|en = 'Connection settings'") + " (" + КоличествоНастроекПодключения + ")</a>"
				+ "   " + "<a href = Добавить>+ " + НСтр("ru = 'Добавить';
														|en = 'Add'") + "</a>";
			
		ИначеЕсли КоличествоНастроекПодключения = 1 Тогда
			
			ИнтеграцияСсылка = Результат[1].Выгрузить()[0].Интеграция;
			СтрокаНастройкаПодключенияПлатежнойСистемы =
				"<a href = " + ПолучитьНавигационнуюСсылку(ИнтеграцияСсылка) + ">" + Строка(ИнтеграцияСсылка) + "</a>"
				+ "   " + "<a href = Добавить>+ " + НСтр("ru = 'Добавить';
														|en = 'Add'") + "</a>";
		Иначе
			СтрокаНастройкаПодключенияПлатежнойСистемы =
				"<a href = Добавить>+ " + НСтр("ru = 'Добавить';
												|en = 'Add'") + "</a>";
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	Возврат СтроковыеФункции.ФорматированнаяСтрока(СтрокаНастройкаПодключенияПлатежнойСистемы);
КонецФункции

// Определяет сумму, по который была выполнена последняя операции в платежной системе.
// Метод следует использовать для отложенного получения статуса выполнения операции.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ оплаты или возврата,
//      по которому были выполнены операции в платежных системах.
//
// Возвращаемое значение:
//  Число - Сумма последней операции с платежной системой
Функция СуммаОперации(ДокументОперации) Экспорт
	
	СуммаОплаты = 0;
	
	ДанныеОперацииПоДокументу = ПереводыСБПc2b.ОперацииПоДокументу(ДокументОперации);
	Если Не ДанныеОперацииПоДокументу.ДанныеОпераций.Количество() Тогда
		СуммаОплаты = ДанныеОперацииПоДокументу.Сумма;
	КонецЕсли;
	
	Возврат СуммаОплаты;
	
КонецФункции

// На основании контактной информации контрагента формирует список получателей сообщения СБП.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - 
//  ВариантОтправки - Строка -
//  Получатели - Массив Из Строка -
//
Процедура ПриФормированииСпискаПолучателейСообщенияСБП(ДокументОперации, ВариантОтправки, Получатели) Экспорт
	
	Если ТипЗнч(ДокументОперации) <> Тип("ДокументСсылка.ЗаказКлиента")
		И ТипЗнч(ДокументОперации) <> Тип("ДокументСсылка.СчетНаОплатуКлиенту")
		И ТипЗнч(ДокументОперации) <> Тип("ДокументСсылка.АктВыполненныхРабот")
		И ТипЗнч(ДокументОперации) <> Тип("ДокументСсылка.РеализацияТоваровУслуг")
		И ТипЗнч(ДокументОперации) <> Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизита = "Контрагент";
	Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОперации, ИмяРеквизита);
	
	Если Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	ВидыКонтактнойИнформацииДляОтбора= Новый Массив;
	ПараметрыОтбора = УправлениеКонтактнойИнформацией.ОтборКонтактнойИнформации();
	
	Если ВариантОтправки = "ЭлектроннаяПочта" Тогда
		ВидыКонтактнойИнформацииДляОтбора.Добавить(Справочники.ВидыКонтактнойИнформации.EmailКонтрагента);
	ИначеЕсли ВариантОтправки = "Телефон" Тогда
		ВидыКонтактнойИнформацииДляОтбора.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
	КонецЕсли;
	
	ПараметрыОтбора.ВидыКонтактнойИнформации = ВидыКонтактнойИнформацииДляОтбора;
	ПараметрыОтбора.Дата = ТекущаяДатаСеанса();
	
	КонтактнаяИнформацияКонтрагента = УправлениеКонтактнойИнформацией.КонтактнаяИнформация(Контрагент, ПараметрыОтбора);
	
	Для Каждого ЗаписьКонтактнойИнформации Из КонтактнаяИнформацияКонтрагента Цикл
		Если ЗначениеЗаполнено(ЗаписьКонтактнойИнформации.Представление) Тогда
			Получатели.Добавить(ЗаписьКонтактнойИнформации.Представление);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Управляет возможностью использования шаблонов сообщений СБП
//
// Параметры:
//  Используется - Булево -
//
Процедура ПриПроверкеИспользованияШаблоновСообщенийСБП(Используется) Экспорт
	
	Используется = Истина;
	
КонецПроцедуры

// Заполнение массива шаблонов сообщений по типам документов.
// 
// Параметры:
//  Шаблоны - Массив Из Структура - Шаблоны
//
Процедура ПриОпределенииПредопределенныхШаблоновСообщенийСБППоТипам(Шаблоны) Экспорт
	
	ДобавитьШаблонЗаказКлиента(Шаблоны);
	ДобавитьШаблонСчетНаОплатуКлиенту(Шаблоны);
	ДобавитьШаблонАктВыполненныхРабот(Шаблоны);
	ДобавитьШаблонРеализацияТоваровУслуг(Шаблоны);
	ДобавитьШаблонРеализацияУслугПрочихАктивов(Шаблоны);
	
КонецПроцедуры

#КонецОбласти

#Область БиблиотекаПодключаемогоОборудования

// Управление видимостью элементов формы админиистрирования настроек работы с подключаемым оборудованием.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения -
//
Процедура УправлениеВидимостьюЭлементовНастроекЖурналовОперацийБПО(Форма) Экспорт
	
	Форма.Элементы.ГруппаОперацииОчередьЧеков.Видимость = Истина;
	Форма.Элементы.ГруппаОперацииПроверкиКМ.Видимость = Истина;
	
КонецПроцедуры

#КонецОбласти

//-- Локализация

#Область КомиссионерВЧеке

// Возвращает перечень запросов (в виде списка) для составления пакетного запроса. 
// Последний запрос в списке фактически является результирующим. Состав полей последнего запроса:
//	   Номенклатура - СправочникСсылка.Номенклатура
//	   Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//	   Серия - СправочникСсылка.СерииНоменклатуры
//	   Комитент - СправочникСсылка.Организации,СправочникСсылка.Контрагенты.
//
// Параметры:
//	Организация - СправочникСсылка.Организации
//	Склад - СправочникСсылка.Склады
//	ИмяТаблицыКлючей - Строка - Имя ВТ, в которой будут переданы строки с ключами данных, пустая строка - для обмена(все товары). Обязательные колонки:
//	 *Номенклатура - СправочникСсылка.Номенклатура
//	 *Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//	 *Серия - СправочникСсылка.СерииНоменклатуры
//	СуффиксИменВТ - Строка - Окончание имен всех ВТ пакета, для исключения конфликтов имен в общем пакете
//	
// Возвращаемое значение:
//	Структура - Структура со свойствами:
//	*ПараметрыЗапроса - Структура - Структура со свойствами:
//	 **Ключ - Строка
//	*ПакетЗапросов - СписокЗначений из ЭлементСпискаЗначений:
//	 **Представление - Строка - Имя временной таблицы, создаваемой в запросе
//	 **Значение - Строка - Текст единичного запроса
//
Функция ЗапросДанныхКомитентовПоТоварам(Знач Организация, Знач Склад, Знач ИмяТаблицыКлючей = "", Знач СуффиксИменВТ = "") Экспорт
	
	ИспользоватьКомиссию = Истина;
	//++ Локализация
	ИспользоватьКомиссию = ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках");
	//-- Локализация
	ИспользоватьИнтеркампани = 
		ИспользоватьКомиссию И ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями");
	ЭтоЗапросОбмена = ПустаяСтрока(ИмяТаблицыКлючей);
	Если НЕ ИспользоватьКомиссию И НЕ ЭтоЗапросОбмена Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
		|	НЕОПРЕДЕЛЕНО КАК Комитент
		|ПОМЕСТИТЬ ДанныеОстатковВключаяКомитентов_ПодменныйСуффикс
		|ГДЕ
		|	ЛОЖЬ";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "_ПодменныйСуффикс", СокрЛП(СуффиксИменВТ));
		
		ЗапросыПакета = Новый СписокЗначений;
		ЗапросыПакета.Добавить(ТекстЗапроса, "ДанныеОстатковВключаяКомитентов"+СокрЛП(СуффиксИменВТ)); 
		Результат = Новый Структура("ПараметрыЗапроса, ПакетЗапросов", Новый Структура, ЗапросыПакета);
		
		Возврат Результат;
		
	КонецЕсли; 
	
	СуффикИменПоУмолчанию = "ДанныеКомитентовПоТоварам";
	СуффиксИмен = ?(ПустаяСтрока(СуффиксИменВТ), СуффикИменПоУмолчанию, СуффиксИменВТ);
	
	ПараметрыЗапросаИсходные = Новый Структура();
	
	ПараметрыЗапросаИсходные.Вставить("ОтборОрганизацияПродавец_ПодменныйСуффикс", Организация);
	ПараметрыЗапросаИсходные.Вставить("ОтборМестоХраненияПродавца_ПодменныйСуффикс", Склад);
	ПараметрыЗапросаИсходные.Вставить("НижняяДатаОткрытыхСмен_ПодменныйСуффикс", ТекущаяДатаСеанса()-60*60*24*2);
	
	СпособыПередачиТоваров = Новый Массив;
	СпособыПередачиТоваров.Добавить(Перечисления.СпособыПередачиТоваров.Продажа);
	//++ Локализация
	Если ИспользоватьКомиссию Тогда
		СпособыПередачиТоваров.Добавить(Перечисления.СпособыПередачиТоваров.ПередачаНаКомиссию);
	КонецЕсли;
	//-- Локализация
	ПараметрыЗапросаИсходные.Вставить("СпособыПередачиТоваров_ПодменныйСуффикс", СпособыПередачиТоваров);
	
	ОтборТипыЗапасов = Новый Массив;
	ОтборТипыЗапасов.Добавить(Перечисления.ТипыЗапасов.Товар);
	//++ Локализация
	Если ИспользоватьКомиссию Тогда
		ОтборТипыЗапасов.Добавить(Перечисления.ТипыЗапасов.КомиссионныйТовар);
	КонецЕсли;
	//-- Локализация
	ОтборТипыЗапасов.Добавить(Перечисления.ТипыЗапасов.ТоварНаХраненииСПравомПродажи);
	ПараметрыЗапросаИсходные.Вставить("ОтборТипыЗапасов_ПодменныйСуффикс", ОтборТипыЗапасов);
	
		ПараметрыЗапросаИсходные.Вставить("УсловиеИсключенияЗапроса_Интеркампани_ПодменныйСуффикс", ИспользоватьИнтеркампани);
	
	ЗапросыПакета = Новый СписокЗначений;
	ПараметрыЗапросаФинальные = Новый Структура;
	
	ПодменыИмен = Новый СписокЗначений();
	ПодменыИмен.Добавить("#КоллекцияТовары", ИмяТаблицыКлючей);
	Для Каждого Элем Из ПараметрыЗапросаИсходные Цикл
		НовоеИмяКлюча = СтрЗаменить(Элем.Ключ, "_ПодменныйСуффикс", СуффиксИмен);
		ПодменыИмен.Добавить(Элем.Ключ, НовоеИмяКлюча);
		ПараметрыЗапросаФинальные.Вставить(НовоеИмяКлюча,Элем.Значение);
	КонецЦикла;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияПродавец КАК ОрганизацияПродавец,
	|	НастройкаПередачиТоваровМеждуОрганизациями.СпособПередачиТоваров КАК СпособПередачиТоваров,
	|	НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияВладелец КАК ОрганизацияВладелец,
	|	НастройкаПередачиТоваровМеждуОрганизациями.ТипЗапасов КАК ТипЗапасовВладельца
	|ПОМЕСТИТЬ НастройкиПередачи_ПодменныйСуффикс
	|ИЗ
	|	РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК НастройкаПередачиТоваровМеждуОрганизациями
	|ГДЕ
	|	НастройкаПередачиТоваровМеждуОрганизациями.ОрганизацияПродавец = &ОтборОрганизацияПродавец_ПодменныйСуффикс
	|	И НастройкаПередачиТоваровМеждуОрганизациями.СпособПередачиТоваров В(&СпособыПередачиТоваров_ПодменныйСуффикс)
	|	И &УсловиеИсключенияЗапроса_Интеркампани_ПодменныйСуффикс
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОрганизацияВладелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЕстьЗаписи
	|ПОМЕСТИТЬ ЕстьНастройкиПередачи_ПодменныйСуффикс
	|ИЗ
	|	НастройкиПередачи_ПодменныйСуффикс КАК НастройкиПередачи
	|ГДЕ
	|	&УсловиеИсключенияЗапроса_Интеркампани_ПодменныйСуффикс
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокЧек.Ссылка КАК Ссылка,
	|	ДокЧек.Организация КАК Организация,
	|	NULL КАК СвязьСторно
	|ПОМЕСТИТЬ ЧекиОткрытыхСмен_ПодменныйСуффикс
	|ИЗ
	|	Документ.ЧекККМ КАК ДокЧек
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КассоваяСмена КАК ДокСмена
	|	ПО ДокЧек.КассоваяСмена = ДокСмена.Ссылка
	|ГДЕ
	|	ДокЧек.Склад = &ОтборМестоХраненияПродавца_ПодменныйСуффикс
	|	И ДокЧек.Организация = &ОтборОрганизацияПродавец_ПодменныйСуффикс
	|	И ДокЧек.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
	|	И ДокСмена.ОкончаниеКассовойСмены = ДАТАВРЕМЯ(1, 1, 1)
	|	И ДокЧек.Дата > &НижняяДатаОткрытыхСмен_ПодменныйСуффикс
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокЧек.Ссылка КАК Чек,
	|	ДокЧек.Организация КАК Организация,
	|	NULL
	|ИЗ
	|	Документ.ЧекККМВозврат КАК ДокЧек
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КассоваяСмена КАК ДокСмена
	|	ПО ДокЧек.КассоваяСмена = ДокСмена.Ссылка
	|ГДЕ
	|	ДокЧек.Склад = &ОтборМестоХраненияПродавца_ПодменныйСуффикс
	|	И ДокЧек.Организация = &ОтборОрганизацияПродавец_ПодменныйСуффикс
	|	И ДокЧек.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
	|	И ДокСмена.ОкончаниеКассовойСмены = ДАТАВРЕМЯ(1, 1, 1)
	|	И ДокЧек.Дата > &НижняяДатаОткрытыхСмен_ПодменныйСуффикс
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ 
	|	ДокЧек.ЧекККМ КАК Чек,
	|	ДокЧек.Организация КАК Организация,
	|	ДокЧек.Ссылка
	|ИЗ
	|	Документ.ЧекККМКоррекции КАК ДокЧек
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КассоваяСмена КАК ДокСмена
	|	ПО ДокЧек.КассоваяСмена = ДокСмена.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЧекККМ КАК ЧекСторнируемый
	|	ПО ДокЧек.ЧекККМ = ЧекСторнируемый.Ссылка
	|ГДЕ
	|	ДокЧек.Склад = &ОтборМестоХраненияПродавца_ПодменныйСуффикс
	|	И ДокЧек.Организация = &ОтборОрганизацияПродавец_ПодменныйСуффикс
	|	И ДокЧек.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
	|	И ДокСмена.ОкончаниеКассовойСмены = ДАТАВРЕМЯ(1, 1, 1)
	|	И ДокЧек.Дата > &НижняяДатаОткрытыхСмен_ПодменныйСуффикс
	|	И ЧекСторнируемый.Склад = &ОтборМестоХраненияПродавца_ПодменныйСуффикс
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокЧек.Ссылка КАК Чек,
	|	ДокЧек.Организация КАК Организация,
	|	ДокЧек.ЧекККМ
	|ИЗ
	|	Документ.ЧекККМКоррекции КАК ДокЧек
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КассоваяСмена КАК ДокСмена
	|	ПО ДокЧек.КассоваяСмена = ДокСмена.Ссылка
	|ГДЕ
	|	ДокЧек.Склад = &ОтборМестоХраненияПродавца_ПодменныйСуффикс
	|	И ДокЧек.Организация = &ОтборОрганизацияПродавец_ПодменныйСуффикс
	|	И ДокЧек.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
	|	И ДокСмена.ОкончаниеКассовойСмены = ДАТАВРЕМЯ(1, 1, 1)
	|	И ДокЧек.Дата > &НижняяДатаОткрытыхСмен_ПодменныйСуффикс
	|
	|ИНДЕКСИРОВАТЬ ПО 
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Чек.Ссылка КАК Чек,
	|	Чек.СвязьСторно КАК СвязьСторно,
	|	Чек.Организация КАК Организация,
	|	ЧекТовары.Номенклатура КАК Номенклатура,
	|	ЧекТовары.Характеристика КАК Характеристика,
	|	ЧекТовары.Серия КАК Серия,
	|	ЧекТовары.Комитент КАК КомитентЧека,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЧекТовары.Комитент) = ТИП(Справочник.Контрагенты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	КОНЕЦ КАК ТипЗапаса,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЧекТовары.Комитент) = ТИП(Справочник.Организации)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ПриоритетПоТипуЗапасаТовара,
	|	ВЫБОР
	|		КОГДА Чек.СвязьСторно ЕСТЬ NULL
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ * ЧекТовары.Количество КАК Количество
	|ПОМЕСТИТЬ ДанныеОткрытыхСмен_ПодменныйСуффикс
	|ИЗ
	|	ЧекиОткрытыхСмен_ПодменныйСуффикс КАК Чек
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЧекККМ.Товары КАК ЧекТовары
	|		ПО Чек.Ссылка = ЧекТовары.Ссылка
	|ГДЕ
	|	&УсловиеПодмены_ОтборЧековПоСоставу
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Чек.Ссылка,
	|	Чек.СвязьСторно,
	|	Чек.Организация,
	|	ЧекТовары.Номенклатура,
	|	ЧекТовары.Характеристика,
	|	ЧекТовары.Серия,
	|	ЧекТовары.Комитент,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЧекТовары.Комитент) = ТИП(Справочник.Контрагенты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЧекТовары.Комитент) = ТИП(Справочник.Организации)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ЧекТовары.Количество
	|ИЗ
	|	ЧекиОткрытыхСмен_ПодменныйСуффикс КАК Чек
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЧекККМВозврат.Товары КАК ЧекТовары
	|		ПО Чек.Ссылка = ЧекТовары.Ссылка
	|ГДЕ
	|	&УсловиеПодмены_ОтборЧековПоСоставу
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Чек.Ссылка,
	|	Чек.СвязьСторно,
	|	Чек.Организация,
	|	ЧекТовары.Номенклатура,
	|	ЧекТовары.Характеристика,
	|	ЧекТовары.Серия,
	|	ЧекТовары.Комитент,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЧекТовары.Комитент) = ТИП(Справочник.Контрагенты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ЧекТовары.Комитент) = ТИП(Справочник.Организации)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ЧекТовары.Количество * -1
	|ИЗ
	|	ЧекиОткрытыхСмен_ПодменныйСуффикс КАК Чек
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЧекККМКоррекции.Товары КАК ЧекТовары
	|		ПО Чек.Ссылка = ЧекТовары.Ссылка
	|ГДЕ
	|	&УсловиеПодмены_ОтборЧековПоСоставу
	|
	|ИНДЕКСИРОВАТЬ ПО 
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	ВидыЗапасов.ТипЗапасов КАК ТипЗапасов
	|ПОМЕСТИТЬ ОтборВидыЗапасов_ПодменныйСуффикс
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ 
	|	ВидыЗапасов.ТипЗапасов В (&ОтборТипыЗапасов_ПодменныйСуффикс)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	НастройкиПередачи.ОрганизацияПродавец КАК ОрганизацияПродавец,
	|	НастройкиПередачи.СпособПередачиТоваров КАК СпособПередачиТоваров,
	|	НастройкиПередачи.ОрганизацияВладелец КАК ОрганизацияВладелец,
	|	НастройкиПередачи.ТипЗапасовВладельца КАК ТипЗапасовВладельца,
	|	ВидыЗапасов.Ссылка КАК ВидЗапасовВладельца
	|ПОМЕСТИТЬ НастройкиПередачиДополненныеВидамиЗапасов_ПодменныйСуффикс
	|ИЗ
	|	ОтборВидыЗапасов_ПодменныйСуффикс КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ НастройкиПередачи_ПодменныйСуффикс КАК НастройкиПередачи
	|	ПО ВидыЗапасов.ТипЗапасов = НастройкиПередачи.ТипЗапасовВладельца
	|ГДЕ
	|	&УсловиеИсключенияЗапроса_Интеркампани_ПодменныйСуффикс
	|
	|ИНДЕКСИРОВАТЬ ПО 
	|	ВидЗапасовВладельца,
	|	ОрганизацияВладелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ТоварыОрганизацийОстатки.Организация КАК Организация,
	|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТоварыОрганизацийОстатки.ВидЗапасов КАК ВидЗапасов,
	|	ТоварыОрганизацийОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ОтаткиОрганизацийВладельцев_ПодменныйСуффикс
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(
	|			,
	|			Организация В
	|					(ВЫБРАТЬ
	|						НастройкиПередачи.ОрганизацияВладелец
	|					ИЗ
	|						НастройкиПередачи_ПодменныйСуффикс КАК НастройкиПередачи)
	|				И АналитикаУчетаНоменклатуры В
	|					(ВЫБРАТЬ
	|						КлючиАналитикиУчетаНоменклатуры.КлючАналитики
	|					ИЗ
	|						РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
	|					ГДЕ
	|						КлючиАналитикиУчетаНоменклатуры.МестоХранения = &ОтборМестоХраненияПродавца_ПодменныйСуффикс
	|						И &УсловиеПодмены_ОтборПоКоллекцииТоваров)
	|				И ВидЗапасов В (ВЫБРАТЬ Ссылка ИЗ ОтборВидыЗапасов_ПодменныйСуффикс)) КАК ТоварыОрганизацийОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЕстьНастройкиПередачи_ПодменныйСуффикс КАК ЕстьНастройкиПередачи
	|		ПО ЕстьНастройкиПередачи.ЕстьЗаписи
	|ГДЕ
	|	&УсловиеИсключенияЗапроса_Интеркампани_ПодменныйСуффикс
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидЗапасов,
	|	Организация 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Остаток"" КАК Чек,
	|	""Собственный"" КАК ДопИнфо,
	|	ТоварыОрганизацийОстатки.Организация КАК Организация,
	|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.Номенклатура КАК АналитикаНоменклатура,
	|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.Характеристика КАК АналитикаХарактеристика,
	|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.Серия КАК АналитикаСерия,
	|	ТоварыОрганизацийОстатки.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ВЫБОР 
	|		КОГДА ТоварыОрганизацийОстатки.ВидЗапасов.ТипЗапасов = 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА 3
	|		ИНАЧЕ 1
	|	КОНЕЦ ПриоритетПоТипуЗапасаТовара,
	|	ТоварыОрганизацийОстатки.ВидЗапасов.Контрагент КАК Комитент,
	|	ТоварыОрганизацийОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ДанныеПоВидамЗапасовДоСвертки_ПодменныйСуффикс
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(
	|			,
	|			Организация = &ОтборОрганизацияПродавец_ПодменныйСуффикс
	|				И АналитикаУчетаНоменклатуры В
	|					(ВЫБРАТЬ
	|						КлючиАналитикиУчетаНоменклатуры.КлючАналитики
	|					ИЗ
	|						РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
	|					ГДЕ
	|						КлючиАналитикиУчетаНоменклатуры.МестоХранения = &ОтборМестоХраненияПродавца_ПодменныйСуффикс
	|						И &УсловиеПодмены_ОтборПоКоллекцииТоваров)
	|				И ВидЗапасов В (ВЫБРАТЬ Ссылка ИЗ ОтборВидыЗапасов_ПодменныйСуффикс)) КАК ТоварыОрганизацийОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Остаток"",
	|	""Интеркампани"",
	|	ТоварыОрганизацийОстатки.Организация,
	|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.Номенклатура,
	|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.Характеристика,
	|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры.Серия,
	|	ВЫБОР
	|		КОГДА НастройкиПередачи.СпособПередачиТоваров = ЗНАЧЕНИЕ(Перечисление.СпособыПередачиТоваров.Продажа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	КОНЕЦ,
	|	2,
	|	ВЫБОР 
	|		КОГДА НастройкиПередачи.СпособПередачиТоваров = ЗНАЧЕНИЕ(Перечисление.СпособыПередачиТоваров.Продажа) 
	|			ТОГДА НЕОПРЕДЕЛЕНО 
	|		ИНАЧЕ НастройкиПередачи.ОрганизацияВладелец 
	|	КОНЕЦ,
	|	ТоварыОрганизацийОстатки.КоличествоОстаток
	|ИЗ
	|	ОтаткиОрганизацийВладельцев_ПодменныйСуффикс КАК ТоварыОрганизацийОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НастройкиПередачиДополненныеВидамиЗапасов_ПодменныйСуффикс КАК НастройкиПередачи
	|		ПО ТоварыОрганизацийОстатки.Организация = НастройкиПередачи.ОрганизацияВладелец
	|			И ТоварыОрганизацийОстатки.ВидЗапасов = НастройкиПередачи.ВидЗапасовВладельца
	|ГДЕ
	|	&УсловиеИсключенияЗапроса_Интеркампани_ПодменныйСуффикс
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеОткрытыхСмен.Чек,
	|	ДанныеОткрытыхСмен.СвязьСторно,
	|	ДанныеОткрытыхСмен.Организация,
	|	ДанныеОткрытыхСмен.Номенклатура,
	|	ДанныеОткрытыхСмен.Характеристика,
	|	ДанныеОткрытыхСмен.Серия,
	|	ДанныеОткрытыхСмен.ТипЗапаса,
	|	ДанныеОткрытыхСмен.ПриоритетПоТипуЗапасаТовара,
	|	ДанныеОткрытыхСмен.КомитентЧека,
	|	ДанныеОткрытыхСмен.Количество
	|ИЗ
	|	ДанныеОткрытыхСмен_ПодменныйСуффикс КАК ДанныеОткрытыхСмен
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаНоменклатура,
	|	АналитикаХарактеристика,
	|	АналитикаСерия
	|	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПоВидамЗапасов.Организация КАК Организация,
	|	ДанныеПоВидамЗапасов.АналитикаНоменклатура КАК АналитикаНоменклатура,
	|	ДанныеПоВидамЗапасов.АналитикаХарактеристика КАК АналитикаХарактеристика,
	|	ДанныеПоВидамЗапасов.АналитикаСерия КАК АналитикаСерия,
	|	ДанныеПоВидамЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ДанныеПоВидамЗапасов.ПриоритетПоТипуЗапасаТовара КАК ПриоритетПоТипуЗапасаТовара,
	|	ДанныеПоВидамЗапасов.Комитент КАК Комитент,
	|	СУММА(ДанныеПоВидамЗапасов.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ДанныеПоВидамЗапасов_ПодменныйСуффикс
	|ИЗ
	|	ДанныеПоВидамЗапасовДоСвертки_ПодменныйСуффикс КАК ДанныеПоВидамЗапасов
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПоВидамЗапасов.Организация,
	|	ДанныеПоВидамЗапасов.АналитикаНоменклатура,
	|	ДанныеПоВидамЗапасов.АналитикаХарактеристика,
	|	ДанныеПоВидамЗапасов.АналитикаСерия,
	|	ДанныеПоВидамЗапасов.ТипЗапасов,
	|	ДанныеПоВидамЗапасов.ПриоритетПоТипуЗапасаТовара,
	|	ДанныеПоВидамЗапасов.Комитент
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДанныеПоВидамЗапасов.КоличествоОстаток) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаНоменклатура,
	|	АналитикаХарактеристика,
	|	АналитикаСерия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПоВидамЗапасов.АналитикаНоменклатура КАК Номенклатура,
	|	ДанныеПоВидамЗапасов.АналитикаХарактеристика КАК Характеристика,
	|	ДанныеПоВидамЗапасов.АналитикаСерия КАК Серия,
	|	МИНИМУМ(ДанныеПоВидамЗапасов.ПриоритетПоТипуЗапасаТовара) КАК ПриоритетПоТипуЗапасаТовара
	|ПОМЕСТИТЬ ОтборПоПриоритету_ПодменныйСуффикс
	|	ИЗ
	|		ДанныеПоВидамЗапасов_ПодменныйСуффикс КАК ДанныеПоВидамЗапасов
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПоВидамЗапасов.АналитикаНоменклатура,
	|	ДанныеПоВидамЗапасов.АналитикаХарактеристика,
	|	ДанныеПоВидамЗапасов.АналитикаСерия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПоВидамЗапасов.АналитикаНоменклатура КАК Номенклатура,
	|	ДанныеПоВидамЗапасов.АналитикаХарактеристика КАК Характеристика,
	|	ДанныеПоВидамЗапасов.АналитикаСерия КАК Серия,
	|	ДанныеПоВидамЗапасов.ПриоритетПоТипуЗапасаТовара КАК ПриоритетПоТипуЗапасаТовара,
	|	Максимум(ДанныеПоВидамЗапасов.КоличествоОстаток) КАК КоличествоОстаток
	|	
	|ПОМЕСТИТЬ ОтборПоПриоритетуКоличеству_ПодменныйСуффикс
	|
	|ИЗ
	|	ДанныеПоВидамЗапасов_ПодменныйСуффикс КАК ДанныеПоВидамЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПоПриоритету_ПодменныйСуффикс КАК ОтборПоПриоритету
	|	ПО ДанныеПоВидамЗапасов.АналитикаНоменклатура = ОтборПоПриоритету.Номенклатура
	|	И ДанныеПоВидамЗапасов.АналитикаХарактеристика = ОтборПоПриоритету.Характеристика
	|	И ДанныеПоВидамЗапасов.АналитикаСерия = ОтборПоПриоритету.Серия
	|	И ДанныеПоВидамЗапасов.ПриоритетПоТипуЗапасаТовара = ОтборПоПриоритету.ПриоритетПоТипуЗапасаТовара
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПоВидамЗапасов.АналитикаНоменклатура,
	|	ДанныеПоВидамЗапасов.АналитикаХарактеристика,
	|	ДанныеПоВидамЗапасов.АналитикаСерия,
	|	ДанныеПоВидамЗапасов.ПриоритетПоТипуЗапасаТовара
	|
	|ИНДЕКСИРОВАТЬ ПО 
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПоВидамЗапасов.АналитикаНоменклатура КАК Номенклатура,
	|	ДанныеПоВидамЗапасов.АналитикаХарактеристика КАК Характеристика,
	|	ДанныеПоВидамЗапасов.АналитикаСерия КАК Серия,
	|	МАКСИМУМ(ДанныеПоВидамЗапасов.Комитент) КАК Комитент
	|ПОМЕСТИТЬ ДанныеОстатковВключаяКомитентов_ПодменныйСуффикс
	|
	|ИЗ
	|	ДанныеПоВидамЗапасов_ПодменныйСуффикс КАК ДанныеПоВидамЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОтборПоПриоритетуКоличеству_ПодменныйСуффикс КАК ОтборПоПриоритету
	|	ПО ДанныеПоВидамЗапасов.АналитикаНоменклатура = ОтборПоПриоритету.Номенклатура
	|	И ДанныеПоВидамЗапасов.АналитикаХарактеристика = ОтборПоПриоритету.Характеристика
	|	И ДанныеПоВидамЗапасов.АналитикаСерия = ОтборПоПриоритету.Серия
	|	И ДанныеПоВидамЗапасов.ПриоритетПоТипуЗапасаТовара = ОтборПоПриоритету.ПриоритетПоТипуЗапасаТовара
	|	И ДанныеПоВидамЗапасов.КоличествоОстаток = ОтборПоПриоритету.КоличествоОстаток
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПоВидамЗапасов.АналитикаНоменклатура,
	|	ДанныеПоВидамЗапасов.АналитикаХарактеристика,
	|	ДанныеПоВидамЗапасов.АналитикаСерия
	|ИНДЕКСИРОВАТЬ ПО 
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|";
	
	УсловиеПодмены = "&УсловиеПодмены_ОтборПоКоллекцииТоваров";
	Если ЭтоЗапросОбмена Тогда
		ЗначениеПодмены = "ИСТИНА";
	Иначе
		ЗначениеПодмены = "(КлючиАналитикиУчетаНоменклатуры.Номенклатура, КлючиАналитикиУчетаНоменклатуры.Характеристика, КлючиАналитикиУчетаНоменклатуры.Серия) 
							|		В (ВЫБРАТЬ Номенклатура, Характеристика, Серия ИЗ  #КоллекцияТовары)";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, УсловиеПодмены, ЗначениеПодмены);
	
	УсловиеПодмены = "&УсловиеПодмены_ОтборЧековПоСоставу";
	Если ЭтоЗапросОбмена Тогда
		ЗначениеПодмены = "ИСТИНА";
	Иначе
		ЗначениеПодмены = "	(ЧекТовары.Номенклатура, ЧекТовары.Характеристика, ЧекТовары.Серия) В
					|			(ВЫБРАТЬ
					|				КоллекцияТовары.Номенклатура,
					|				КоллекцияТовары.Характеристика,
					|				КоллекцияТовары.Серия
					|			ИЗ
					|				#КоллекцияТовары КАК КоллекцияТовары)";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, УсловиеПодмены, ЗначениеПодмены);
	
	ЗапросыПакета = РазбитьПакетныйЗапрос(ТекстЗапроса);

	УдаляемыеЗапросыПакета = Новый Массив;
	КорректируемыеЗапросыПакета = Новый Массив;
	Если НЕ ИспользоватьИнтеркампани Тогда
		УдаляемыеЗапросыПакета.Добавить("НастройкиПередачи_ПодменныйСуффикс");
		УдаляемыеЗапросыПакета.Добавить("ЕстьНастройкиПередачи_ПодменныйСуффикс");
		УдаляемыеЗапросыПакета.Добавить("ОтаткиОрганизацийВладельцев_ПодменныйСуффикс");
		УдаляемыеЗапросыПакета.Добавить("НастройкиПередачиДополненныеВидамиЗапасов_ПодменныйСуффикс");
		КорректируемыеЗапросыПакета.Добавить("ДанныеПоВидамЗапасовДоСвертки_ПодменныйСуффикс");
	КонецЕсли;
	
	РазмерПакета = ЗапросыПакета.Количество();
	Для СчИндексЗапроса = 1 по РазмерПакета Цикл
		ЭлементСписка = ЗапросыПакета.Получить(РазмерПакета - СчИндексЗапроса);
		Если УдаляемыеЗапросыПакета.Найти(ЭлементСписка.Представление) <> Неопределено Тогда
			ЗапросыПакета.Удалить(ЭлементСписка);
		ИначеЕсли КорректируемыеЗапросыПакета.Найти(ЭлементСписка.Представление) <> Неопределено Тогда
			РазделительЛитерал = "ОБЪЕДИНИТЬ ВСЕ";
			ЧастиОбъединения = 
				СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ЭлементСписка.Значение, РазделительЛитерал);
			КолвоЧастей = ЧастиОбъединения.Количество();
			ЕстьКоррекция = Ложь;
			Для СчИндексЧасти = 1 по КолвоЧастей-2 Цикл
				ЭлементОбъединения = ЧастиОбъединения[КолвоЧастей-1-СчИндексЧасти];
				Если НЕ ИспользоватьИнтеркампани 
					И СтрНайти(ЭлементОбъединения, "УсловиеИсключенияЗапроса_Интеркампани_ПодменныйСуффикс") Тогда
					ЧастиОбъединения.Удалить(КолвоЧастей-1-СчИндексЧасти);
					ЕстьКоррекция = Истина;
				КонецЕсли;
			КонецЦикла;
			Если ЕстьКоррекция Тогда
				ЭлементСписка.Значение = СтрСоединить(ЧастиОбъединения, РазделительЛитерал);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЭлемЗапрос Из ЗапросыПакета Цикл
		ПодменыИмен.Добавить(ЭлемЗапрос.Представление, СтрЗаменить(ЭлемЗапрос.Представление, "_ПодменныйСуффикс", СуффиксИмен));
		Для Каждого ЭлемПодмена Из ПодменыИмен Цикл
			ЭлемЗапрос.Значение = СтрЗаменить(ЭлемЗапрос.Значение, ЭлемПодмена.Значение, ЭлемПодмена.Представление);
			ЭлемЗапрос.Представление = СтрЗаменить(ЭлемЗапрос.Представление, ЭлемПодмена.Значение, ЭлемПодмена.Представление);
		КонецЦикла;
	КонецЦикла;
	
	Результат = Новый Структура("ПараметрыЗапроса, ПакетЗапросов", ПараметрыЗапросаФинальные, ЗапросыПакета);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПолучениеВнешнихКомпонент

// Заполнить описание настраиваемых регламентных заданий.
// 
// Параметры:
//  ОписаниеНастраиваемыхРегламентныхЗаданий - Структура - Описание настраиваемых регламентных заданий
Процедура ЗаполнитьОписаниеНастраиваемыхРегламентныхЗаданий(ОписаниеНастраиваемыхРегламентныхЗаданий) Экспорт
	
	//++ Локализация
	Если ТипЗнч(ОписаниеНастраиваемыхРегламентныхЗаданий) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеНастраиваемыхРегламентныхЗаданий.Вставить("ОчисткаИсторииОперацийПроверкиКМ", 
			Новый Структура("ИмяМетаданныхРегламентногоЗадания, МетаданныеДляПроверкиПравДоступаНаИзменение",
					Метаданные.РегламентныеЗадания.ОчисткаИсторииОперацийПроверкиКМ.Имя, Новый СписокЗначений));
	
	ОписаниеНастраиваемыхРегламентныхЗаданий.Вставить("ОчисткаИсторииОперацийОчередиЧеков", 
			Новый Структура("ИмяМетаданныхРегламентногоЗадания, МетаданныеДляПроверкиПравДоступаНаИзменение",
					Метаданные.РегламентныеЗадания.ОчисткаИсторииОперацийОчередиЧеков.Имя, Новый СписокЗначений));
	
	ОписаниеНастраиваемыхРегламентныхЗаданий.Вставить("ОбновленияВнешнихКомпонент", 
			Новый Структура("ИмяМетаданныхРегламентногоЗадания, МетаданныеДляПроверкиПравДоступаНаИзменение",
					Метаданные.РегламентныеЗадания.ОбновлениеВнешнихКомпонент.Имя, Новый СписокЗначений));
	//-- Локализация
	
КонецПроцедуры

// Устанавливает видимость разделов интернет поддержки на форме
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура УстановитьВидимостьРазделов(Форма) Экспорт
	
	//++ Локализация
	
	// ИнтернетПоддержкаПользователей.ПолучениеВнешнихКомпонент
	Если ОбщегоНазначенияБПО.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеВнешнихКомпонент") Тогда
		МодульПолучениеВнешнихКомпонент = ОбщегоНазначенияБПО.ОбщийМодуль("ПолучениеВнешнихКомпонент");
		Форма.Элементы.ГруппаВнешниеКомпоненты.Видимость = МодульПолучениеВнешнихКомпонент.ДоступнаЗагрузкаВнешнихКомпонент();
	Иначе
		Форма.Элементы.ГруппаВнешниеКомпоненты.Видимость = Ложь;
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.ПолучениеВнешнихКомпонент
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

// Получает платежную ссылку по переданному документу, преобразует ее в изображение QR кода СБП
// и устанавливает значение параметра QRКодКартинка. В случае успеха возвращает Истину.
// 
// Параметры:
//  ДанныеПечати - Структура:
//    *Организация - СправочникСсылка.Организации
//    *Ссылка - ДокументСсылка
//  QRКодКартинка - Картинка
//  Размер - Число
//  
// Возвращаемое значение:
//  Булево - 
//
Функция ВывестиQRКод(ДанныеПечати, QRКодКартинка, Размер) Экспорт
	
	Результат = Ложь;
	
//++ Локализация
	
	Если ПереводыСБПc2b.ПереводыСБПДоступны() Тогда
		
		ДанныеПлатежнойСсылки = РегистрыСведений.ИдентификаторыОперацийСБПc2b.ДанныеПлатежнойСсылки(ДанныеПечати.Ссылка);
		Если Не ДанныеПлатежнойСсылки = Неопределено Тогда
			
			ДанныеQRКода = СистемаБыстрыхПлатежей.ИзображениеQRКодаСБП(ДанныеПлатежнойСсылки.ПлатежнаяСсылка, Размер, 0);
			Если ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
				QRКодКартинка = Новый Картинка(ДанныеQRКода);
				Результат = Истина;
			Иначе
				Шаблон = Нстр("ru = 'Не удалось сформировать QR-код СБП для документа %1.
					|Технические подробности см. в журнале регистрации.';
					|en = 'Cannot generate a FPS QR code for the %1 document.
					|For technical details, see the event log.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, ДанныеПечати.Ссылка);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;

		КонецЕсли;
		
	КонецЕсли;
	
//-- Локализация

	Возврат Результат;
КонецФункции

// Управление видимостью элементов формы админиистрирования настроек работы с подключаемым оборудованием
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения -
//
Процедура ПанельАдминистрированияУТНастройкиРМКИОборудованияНастроитьВидимостьЭлементов(Форма) Экспорт
	//++ Локализация
	УправлениеВидимостьюЭлементовНастроекЖурналовОперацийБПО(Форма);
	//-- Локализация
КонецПроцедуры

// Описание обработчиков строк коллекции:
// 
// Параметры:
//  ОбработчикиСтрок - СписокЗначений из Строка - Содержит в себе соответствие действия над строкой и метода.
//
Процедура ПриДобавленииОбработчиковСтрокКоллекции(ОбработчикиСтрок) Экспорт
	
	//++ Локализация
	ОбработчикиСтрок.Добавить("ЗаполнитьСобственникаТовара",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ЗаполнитьСобственникаТовараВСтрокеТЧ");
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Локализация

// Проверяет, есть ли колонка в таблице
// 
// Параметры:
//  Таблица - ТаблицаЗначений - Таблица, в которой проверяется наличие колонки
//  ИмяКолонки - Строка - Имя искомой колонки
// 
// Возвращаемое значение:
//  Булево - Есть колонка в таблице
Функция ЕстьКолонкаВТаблице(Таблица, ИмяКолонки)
	
	Возврат ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Таблица, ИмяКолонки);
	
КонецФункции

// Новая таблица распределенных товаров по штрихкодам.
// 
// Параметры:
//  ТаблицаСОбщимиКолонками - ТаблицаЗначений - Таблица с общими колонками
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица для распределения товаров по штрихкодам, если нужно (маркированный товар)
Функция НоваяТаблицаРаспределенныхТоваровПоШтрихкодам(ТаблицаСОбщимиКолонками)
	
	Если Не ЕстьКолонкаВТаблице(ТаблицаСОбщимиКолонками, "Штрихкод") Тогда
		ТаблицаСОбщимиКолонками.Колонки.Добавить("Штрихкод");
	КонецЕсли;
	Если Не ЕстьКолонкаВТаблице(ТаблицаСОбщимиКолонками, "ТекстОшибки") Тогда
		ТаблицаСОбщимиКолонками.Колонки.Добавить("ТекстОшибки");
	КонецЕсли;
	Если Не ЕстьКолонкаВТаблице(ТаблицаСОбщимиКолонками, "КоэффициентПересчетаУпаковки") Тогда
		ТаблицаСОбщимиКолонками.Колонки.Добавить("КоэффициентПересчетаУпаковки");
	КонецЕсли;
	Если Не ЕстьКолонкаВТаблице(ТаблицаСОбщимиКолонками, "РезультатРаспределения") Тогда
		ТаблицаСОбщимиКолонками.Колонки.Добавить("РезультатРаспределения");
	КонецЕсли;
	Если Не ЕстьКолонкаВТаблице(ТаблицаСОбщимиКолонками, "ТребуетсяПересчетИзУпаковкиВБазовуюЕдиницуИзмерения") Тогда
		ТаблицаСОбщимиКолонками.Колонки.Добавить("ТребуетсяПересчетИзУпаковкиВБазовуюЕдиницуИзмерения");
	КонецЕсли;
	Если Не ЕстьКолонкаВТаблице(ТаблицаСОбщимиКолонками, "ДополнениеКНаименованиюТовара") Тогда
		ТаблицаСОбщимиКолонками.Колонки.Добавить("ДополнениеКНаименованиюТовара");
	КонецЕсли;
	
	Возврат ТаблицаСОбщимиКолонками;
	
КонецФункции

Функция ОписаниеНоменклатуры(Номенклатура, ОписанияНоменклатурыСЧастичнымВыбытием)
	
	ОписаниеНоменклатуры = ОписанияНоменклатурыСЧастичнымВыбытием.Получить(Номенклатура);
	Если ОписаниеНоменклатуры = Неопределено Тогда
		СоответствиеОписаниеНоменклатуры = ОбщегоНазначенияИС.ОписаниеНоменклатуры(Номенклатура);
		ОписаниеНоменклатуры = СоответствиеОписаниеНоменклатуры[Номенклатура];
		
		ОписанияНоменклатурыСЧастичнымВыбытием.Вставить(Номенклатура, ОписаниеНоменклатуры);
	КонецЕсли;
	
	Возврат ОписаниеНоменклатуры;
	
КонецФункции

// Структура наименования товара для маркированного товара
// 
// Параметры:
//  СтрокаТовары - СтрокаТаблицыЗначений - Строка таблицы товаров документа реализации:
//  *Упаковка - СправочникСсылка.УпаковкиЕдиницыИзмерения - Упаковка документа реализации
//  *Цена - Число - Цена единицы товара из документа реализации
//  СтрокаРаспределения - СтрокаТаблицыЗначений - Строка таблицы распределения товаров по штрихкода упаковки:
//  *Количество - Число - Количество товара в базовой единице измерения
//  *ЧастичноеВыбытиеКоличество - Число - Количество товара в единице измерения частичного выбытия
// 
// Возвращаемое значение:
//  Структура - Структура наименования товара:
// * Упаковка - СправочникСсылка.УпаковкиЕдиницыИзмерения - Упаковка документа реализации
// * Количество - Число - Количество товара в базовой единице измерения
// * КоличествоУпаковок - Число - Количество товара в единице измерения частичного выбытия
// * Цена - Число - Цена единицы товара из документа реализации
Функция СтруктураНаименованияТовара(СтрокаТовары, СтрокаРаспределения)
	
	СтруктураНаименованияТовара = Новый Структура;
	СтруктураНаименованияТовара.Вставить("Упаковка",           СтрокаТовары.Упаковка);
	СтруктураНаименованияТовара.Вставить("Количество",         СтрокаРаспределения.Количество);
	СтруктураНаименованияТовара.Вставить("КоличествоУпаковок", СтрокаРаспределения.ЧастичноеВыбытиеКоличество);
	СтруктураНаименованияТовара.Вставить("Цена",               СтрокаТовары.Цена);
	
	Возврат СтруктураНаименованияТовара;
	
КонецФункции

Функция ОтклонениеСуммыНДС(СтрокаТоварыРазобранные)
	
	СуммаСНДС = СтрокаТоварыРазобранные.СуммаСНДС;
	СтавкаНДСЧислом = СтрокаТоварыРазобранные.СтавкаНДСЧислом;
	
	СуммаНДСЭталон = СуммаСНДС / (100 + СтавкаНДСЧислом) * СтавкаНДСЧислом;
	ОтклонениеВСуммеНДС = СтрокаТоварыРазобранные.СуммаНДС - СуммаНДСЭталон;
	
	Возврат ОтклонениеВСуммеНДС;
	
КонецФункции

Функция СтруктураТаблицаРезультатРаспределения(ТаблицаОсновная, ТаблицаДополнительная, ПоляДетализации)
	
	ТаблицаРезультатРаспределения = ТаблицаОсновная.СкопироватьКолонки();
	
	Для Каждого СтрокаДетализации Из ПоляДетализации Цикл
		
		КолонкаДетализации = ТаблицаДополнительная.Колонки.Найти(СтрокаДетализации);
		
		Если КолонкаДетализации <> Неопределено Тогда
			ТаблицаРезультатРаспределения.Колонки.Добавить(КолонкаДетализации.Имя, КолонкаДетализации.ТипЗначения);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаРезультатРаспределения;
	
КонецФункции

Функция РаспределитьДанныеПоТаблицам(ТаблицаОсновная, ТаблицаДополнительная, ПоляИзмерения, ПоляДетализации, ПоляСумм, ПолеРасчетаБазы)
	
	ТаблицаРезультатРаспределения = СтруктураТаблицаРезультатРаспределения(ТаблицаОсновная, ТаблицаДополнительная, ПоляДетализации);
	
	// 
	Для Каждого СтрокаТаблицыОсновная Из ТаблицаОсновная Цикл
		
		СтруктураОтбора = Новый Структура;
		Для Каждого ПолеИзмерения Из ПоляИзмерения Цикл
			СтруктураОтбора.Вставить(ПолеИзмерения, СтрокаТаблицыОсновная[ПолеИзмерения]);
		КонецЦикла;
		
		МассивСтрокДляДобавленияВТаблицуРаспределения = ТаблицаДополнительная.НайтиСтроки(СтруктураОтбора);
		
		Если МассивСтрокДляДобавленияВТаблицуРаспределения.Количество() = 0 Тогда
			СтрокаТаблицыРезультатРаспределения = ТаблицаРезультатРаспределения.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыРезультатРаспределения, СтрокаТаблицыОсновная);
			
			Продолжить;
		КонецЕсли;
		
		Для каждого СтрокаДляДобавленияВТаблицуРаспределения из МассивСтрокДляДобавленияВТаблицуРаспределения Цикл
			
			Если СтрокаТаблицыОсновная[ПолеРасчетаБазы] = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаДляДобавленияВТаблицуРаспределения[ПолеРасчетаБазы] = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаТаблицыРезультатРаспределения = ТаблицаРезультатРаспределения.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыРезультатРаспределения, СтрокаТаблицыОсновная);
			
			Для Каждого ПолеДетализации Из ПоляДетализации Цикл
				СтрокаТаблицыРезультатРаспределения[ПолеДетализации] = СтрокаДляДобавленияВТаблицуРаспределения[ПолеДетализации];
			КонецЦикла;
			
			Если СтрокаДляДобавленияВТаблицуРаспределения[ПолеРасчетаБазы] < СтрокаТаблицыОсновная[ПолеРасчетаБазы] Тогда
				
				СтрокаТаблицыРезультатРаспределения[ПолеРасчетаБазы] = СтрокаДляДобавленияВТаблицуРаспределения[ПолеРасчетаБазы];
				
				Для Каждого ПолеСуммы Из ПоляСумм Цикл
					Если ПолеСуммы = ПолеРасчетаБазы Тогда
						Продолжить;
					КонецЕсли;
					
					СтрокаТаблицыРезультатРаспределения[ПолеСуммы] = СтрокаТаблицыОсновная[ПолеСуммы] / СтрокаТаблицыОсновная[ПолеРасчетаБазы] * СтрокаТаблицыРезультатРаспределения[ПолеРасчетаБазы];
				КонецЦикла;
				
			КонецЕсли;
			
			СтрокаТаблицыОсновная[ПолеРасчетаБазы] = СтрокаТаблицыОсновная[ПолеРасчетаБазы] - СтрокаТаблицыРезультатРаспределения[ПолеРасчетаБазы];
			Для Каждого ПолеСуммы Из ПоляСумм Цикл
				Если ПолеСуммы = ПолеРасчетаБазы Тогда
					Продолжить;
				КонецЕсли;
				
				СтрокаТаблицыОсновная[ПолеСуммы] = СтрокаТаблицыОсновная[ПолеСуммы] - СтрокаТаблицыРезультатРаспределения[ПолеСуммы];
			КонецЦикла;
			
			СтрокаДляДобавленияВТаблицуРаспределения[ПолеРасчетаБазы] = СтрокаДляДобавленияВТаблицуРаспределения[ПолеРасчетаБазы] - СтрокаТаблицыРезультатРаспределения[ПолеРасчетаБазы];
		
		КонецЦикла;
		
		Если СтрокаТаблицыОсновная[ПолеРасчетаБазы] > 0 Тогда
			СтрокаТаблицыРезультатРаспределения = ТаблицаРезультатРаспределения.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыРезультатРаспределения, СтрокаТаблицыОсновная);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТаблицаРезультатРаспределения;
	
КонецФункции

Процедура ПередВыполнениемОперацииВПлатежнойСистеме(ДокументОперации)
	
	Если Не ЗначениеЗаполнено(ДокументОперации)
		Или Не Метаданные.ОпределяемыеТипы.ДокументОперацииСБП.Тип.СодержитТип(ТипЗнч(ДокументОперации)) Тогда
		
		ВызватьИсключение НСтр("ru = 'Операция в платежной системе для документа не поддерживается.';
								|en = 'Transaction in the payment system for the document is not supported.'");
	КонецЕсли;
	
КонецПроцедуры

// Добавить шаблоны email и sms для документа "Заказ клиента"
// 
// Параметры:
//  Шаблоны - Массив Из Структура - Шаблоны
Процедура ДобавитьШаблонЗаказКлиента(Шаблоны)
	
	Шаблон = Новый Структура();
	Шаблон.Вставить("ТипШаблона", "Письмо");
	Шаблон.Вставить("ПолноеИмяТипаНазначения", "Документ.ЗаказКлиента");
	Шаблон.Вставить("Наименование", "Реквизиты для оплаты через СБП (Заказ клиента) (почта)");
	Шаблон.Вставить("Тема", "Реквизиты для оплаты: [ЗаказКлиента.Номер] от [ЗаказКлиента.Дата{ДЛФ='dd.MM.yyyy'}] на сумму [ЗаказКлиента.СуммаДокумента] руб.");
	ТекстШаблона = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<html>
		|<head>
		|<meta http-equiv=""Content-Type"" content=""text/html; charset=utf-8"" />
		|<meta http-equiv=""X-UA-Compatible"" content=""IE=Edge"" />
		|<meta name=""format-detection"" content=""telephone=no"" />
		|<style type=""text/css"">
		|body{margin:0;padding:8px;overflow:auto;width:100%;height:100%;}
		|p{line-height:1.15;margin:0;}
		|ol,ul{margin-top:0;margin-bottom:0;}
		|img{border:none;}
		|</style>
		|</head>
		|<body>
		|<p>%1</p>
		|<p></p>
		|<p><strong>Покупатель:</strong> %2<br>
		|<strong>Счет:</strong> %3 от %4 <br>
		|<strong>К оплате:</strong> %5</p>
		|<p></p>
		|<p>%6</p>
		|<p></p>
		|<p>%7</p>
		|<p>%8</p>
		|</body>
		|</html>",
		"Благодарим за заказ и просим оплатить.",
		"[ЗаказКлиента.ПартнерНаименование]",
		"[ЗаказКлиента.Номер]",
		"[ЗаказКлиента.Дата{ДЛФ='dd.MM.yyyy'}]",
		"[ЗаказКлиента.СуммаДокумента] руб.",
		"Для оплаты отсканируйте QR-код в Вашем мобильном банковском приложении
		| или перейдите по ссылке для  выбора приложения банка.",
		"[ЗаказКлиента.ПредставлениеСсылкиСБПQRКод]",
		"[ЗаказКлиента.ПредставлениеСсылкиСБП]");
	
	ТекстШаблона = СтрЗаменить(ТекстШаблона, Символы.ПС, "");
	Шаблон.Вставить("Текст", ТекстШаблона);
	Шаблоны.Добавить(Шаблон);

	// Демо: Реквизиты для оплаты, смс
	Шаблон = Новый Структура();
	Шаблон.Вставить("ТипШаблона", "SMS");
	Шаблон.Вставить("ПолноеИмяТипаНазначения", "Документ.ЗаказКлиента");
	Шаблон.Вставить("Наименование", НСтр("ru = 'Реквизиты для оплаты через СБП (Заказ клиента) (смс)';
										|en = 'Details for payment via FPS (Sales order) (SMS)'"));
	Шаблон.Вставить("Тема", "Реквизиты для оплаты: [ЗаказКлиента.Номер] от [ЗаказКлиента.Дата{ДЛФ='dd.MM.yyyy'}] на сумму [ЗаказКлиента.СуммаДокумента] руб.");
	ТекстШаблона = 
		"Благодарим за заказ и просим оплатить.
		|
		|Сумма заказа: [ЗаказКлиента.СуммаДокумента] руб.
		|
		|Заказ можно оплатить, перейдя по ссылке [ЗаказКлиента.ПредставлениеСсылкиСБП]";
	
	Шаблон.Вставить("Текст", ТекстШаблона);
	Шаблоны.Добавить(Шаблон);
	
КонецПроцедуры

// Добавить шаблоны email и sms для документа "Счет на оплату клиенту"
// 
// Параметры:
//  Шаблоны - Массив Из Структура - Шаблоны
Процедура ДобавитьШаблонСчетНаОплатуКлиенту(Шаблоны)
	
	Шаблон = Новый Структура();
	Шаблон.Вставить("ТипШаблона", "Письмо");
	Шаблон.Вставить("ПолноеИмяТипаНазначения", "Документ.СчетНаОплатуКлиенту");
	Шаблон.Вставить("Наименование", "Реквизиты для оплаты через СБП (Счет на оплату) (почта)");
	Шаблон.Вставить("Тема", "Реквизиты для оплаты: [СчетНаОплатуКлиенту.Номер] от [СчетНаОплатуКлиенту.Дата{ДЛФ='dd.MM.yyyy'}] на сумму [СчетНаОплатуКлиенту.СуммаДокумента] руб.");
	ТекстШаблона = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<html>
		|<head>
		|<meta http-equiv=""Content-Type"" content=""text/html; charset=utf-8"" />
		|<meta http-equiv=""X-UA-Compatible"" content=""IE=Edge"" />
		|<meta name=""format-detection"" content=""telephone=no"" />
		|<style type=""text/css"">
		|body{margin:0;padding:8px;overflow:auto;width:100%;height:100%;}
		|p{line-height:1.15;margin:0;}
		|ol,ul{margin-top:0;margin-bottom:0;}
		|img{border:none;}
		|</style>
		|</head>
		|<body>
		|<p>%1</p>
		|<p></p>
		|<p><strong>Покупатель:</strong> %2<br>
		|<strong>Счет:</strong> %3 от %4 <br>
		|<strong>К оплате:</strong> %5</p>
		|<p></p>
		|<p>%6</p>
		|<p></p>
		|<p>%7</p>
		|<p>%8</p>
		|</body>
		|</html>",
		"Благодарим за заказ и просим оплатить.",
		"[СчетНаОплатуКлиенту.ПартнерНаименование]",
		"[СчетНаОплатуКлиенту.Номер]",
		"[СчетНаОплатуКлиенту.Дата{ДЛФ='dd.MM.yyyy'}]",
		"[СчетНаОплатуКлиенту.СуммаДокумента] руб.",
		"Для оплаты отсканируйте QR-код в Вашем мобильном банковском приложении
		| или перейдите по ссылке для  выбора приложения банка.",
		"[СчетНаОплатуКлиенту.ПредставлениеСсылкиСБПQRКод]",
		"[СчетНаОплатуКлиенту.ПредставлениеСсылкиСБП]");
		
	ТекстШаблона = СтрЗаменить(ТекстШаблона, Символы.ПС, "");
	Шаблон.Вставить("Текст", ТекстШаблона);
	Шаблоны.Добавить(Шаблон);

	// Демо: Реквизиты для оплаты, смс
	Шаблон = Новый Структура();
	Шаблон.Вставить("ТипШаблона", "SMS");
	Шаблон.Вставить("ПолноеИмяТипаНазначения", "Документ.СчетНаОплатуКлиенту");
	Шаблон.Вставить("Наименование", НСтр("ru = 'Реквизиты для оплаты через СБП (Счет на оплату) (смс)';
										|en = 'Details for payment via FPS (Commercial invoice) (SMS)'"));
	Шаблон.Вставить("Тема", "Реквизиты для оплаты: [СчетНаОплатуКлиенту.Номер] от [СчетНаОплатуКлиенту.Дата{ДЛФ='dd.MM.yyyy'}] на сумму [СчетНаОплатуКлиенту.СуммаДокумента] руб.");
	ТекстШаблона = 
		"Благодарим за заказ и просим оплатить.
		|
		|Сумма заказа: [СчетНаОплатуКлиенту.СуммаДокумента] руб.
		|
		|Заказ можно оплатить, перейдя по ссылке [СчетНаОплатуКлиенту.ПредставлениеСсылкиСБП]";
		
	Шаблон.Вставить("Текст", ТекстШаблона);
	Шаблоны.Добавить(Шаблон);
	
КонецПроцедуры

// Добавить шаблоны email и sms для документа "Акт выполненных работ"
// 
// Параметры:
//  Шаблоны - Массив Из Структура - Шаблоны
Процедура ДобавитьШаблонАктВыполненныхРабот(Шаблоны)
	
	Шаблон = Новый Структура();
	Шаблон.Вставить("ТипШаблона", "Письмо");
	Шаблон.Вставить("ПолноеИмяТипаНазначения", "Документ.АктВыполненныхРабот");
	Шаблон.Вставить("Наименование", НСтр("ru = 'Реквизиты для оплаты через СБП (Акт выполненных работ) (почта)';
										|en = 'Details for payment via FPS (Customer invoice — Services) (email)'"));
	Шаблон.Вставить("Тема", "Реквизиты для оплаты: [АктВыполненныхРабот.Номер] от [АктВыполненныхРабот.Дата{ДЛФ='dd.MM.yyyy'}] на сумму [АктВыполненныхРабот.СуммаДокумента] руб.");
	ТекстШаблона = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<html>
		|<head>
		|<meta http-equiv=""Content-Type"" content=""text/html; charset=utf-8"" />
		|<meta http-equiv=""X-UA-Compatible"" content=""IE=Edge"" />
		|<meta name=""format-detection"" content=""telephone=no"" />
		|<style type=""text/css"">
		|body{margin:0;padding:8px;overflow:auto;width:100%;height:100%;}
		|p{line-height:1.15;margin:0;}
		|ol,ul{margin-top:0;margin-bottom:0;}
		|img{border:none;}
		|</style>
		|</head>
		|<body>
		|<p>%1</p>
		|<p></p>
		|<p><strong>Покупатель:</strong> %2<br>
		|<strong>Счет:</strong> %3 от %4 <br>
		|<strong>К оплате:</strong> %5</p>
		|<p></p>
		|<p>%6</p>
		|<p></p>
		|<p>%7</p>
		|<p>%8</p>
		|</body>
		|</html>",
		"Благодарим за заказ и просим оплатить.",
		"[АктВыполненныхРабот.ПартнерНаименование]",
		"[АктВыполненныхРабот.Номер]",
		"[АктВыполненныхРабот.Дата{ДЛФ='dd.MM.yyyy'}]",
		"[АктВыполненныхРабот.СуммаДокумента] руб.",
		"Для оплаты отсканируйте QR-код в Вашем мобильном банковском приложении
		| или перейдите по ссылке для  выбора приложения банка.",
		"[АктВыполненныхРабот.ПредставлениеСсылкиСБПQRКод]",
		"[АктВыполненныхРабот.ПредставлениеСсылкиСБП]");
		
	ТекстШаблона = СтрЗаменить(ТекстШаблона, Символы.ПС, "");
	Шаблон.Вставить("Текст", ТекстШаблона);
	Шаблоны.Добавить(Шаблон);

	// Демо: Реквизиты для оплаты, смс
	Шаблон = Новый Структура();
	Шаблон.Вставить("ТипШаблона", "SMS");
	Шаблон.Вставить("ПолноеИмяТипаНазначения", "Документ.АктВыполненныхРабот");
	Шаблон.Вставить("Наименование", НСтр("ru = 'Реквизиты для оплаты через СБП (Акт выполненных работ) (смс)';
										|en = 'Details for payment via FPS (Customer invoice — Services) (SMS)'"));
	Шаблон.Вставить("Тема", "Реквизиты для оплаты: [АктВыполненныхРабот.Номер] от [АктВыполненныхРабот.Дата{ДЛФ='dd.MM.yyyy'}] на сумму [АктВыполненныхРабот.СуммаДокумента] руб.");
	ТекстШаблона = 
		"Благодарим за заказ и просим оплатить.
		|
		|Сумма заказа: [АктВыполненныхРабот.СуммаДокумента] руб.
		|
		|Заказ можно оплатить, перейдя по ссылке [АктВыполненныхРабот.ПредставлениеСсылкиСБП]";
		
	Шаблон.Вставить("Текст", ТекстШаблона);
	Шаблоны.Добавить(Шаблон);
	
КонецПроцедуры

// Добавить шаблоны email и sms для документа "Реализация товаров и услуг"
// 
// Параметры:
//  Шаблоны - Массив Из Структура - Шаблоны
Процедура ДобавитьШаблонРеализацияТоваровУслуг(Шаблоны)
	
	Шаблон = Новый Структура();
	Шаблон.Вставить("ТипШаблона", "Письмо");
	Шаблон.Вставить("ПолноеИмяТипаНазначения", "Документ.РеализацияТоваровУслуг");
	Шаблон.Вставить("Наименование", "Реквизиты для оплаты через СБП (Реализация товаров и услуг) (почта)");
	Шаблон.Вставить("Тема", "Реквизиты для оплаты: [РеализацияТоваровУслуг.Номер] от [РеализацияТоваровУслуг.Дата{ДЛФ=dd.MM.yyyy}] на сумму [РеализацияТоваровУслуг.СуммаДокумента] руб.");
	ТекстШаблона = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<html>
		|<head>
		|<meta http-equiv=""Content-Type"" content=""text/html; charset=utf-8"" />
		|<meta http-equiv=""X-UA-Compatible"" content=""IE=Edge"" />
		|<meta name=""format-detection"" content=""telephone=no"" />
		|<style type=""text/css"">
		|body{margin:0;padding:8px;overflow:auto;width:100%;height:100%;}
		|p{line-height:1.15;margin:0;}
		|ol,ul{margin-top:0;margin-bottom:0;}
		|img{border:none;}
		|</style>
		|</head>
		|<body>
		|<p>%1</p>
		|<p></p>
		|<p><strong>Покупатель:</strong> %2<br>
		|<strong>Счет:</strong> %3 от %4 <br>
		|<strong>К оплате:</strong> %5</p>
		|<p></p>
		|<p>%6</p>
		|<p>%7</p>
		|<p>%8</p>
		|</body>
		|</html>",
		"Благодарим за заказ и просим оплатить.",
		"[РеализацияТоваровУслуг.ПартнерНаименование]",
		"[РеализацияТоваровУслуг.Номер]",
		"[РеализацияТоваровУслуг.Дата{ДЛФ='dd.MM.yyyy'}]",
		"[РеализацияТоваровУслуг.СуммаДокумента] руб.",
		"Для оплаты отсканируйте QR-код в Вашем мобильном банковском приложении или перейдите по ссылке для выбора приложения банка.",
		"[РеализацияТоваровУслуг.ПредставлениеСсылкиСБПQRКод]",
		"[РеализацияТоваровУслуг.ПредставлениеСсылкиСБП]");
		
	ТекстШаблона = СтрЗаменить(ТекстШаблона, Символы.ПС, "");
	Шаблон.Вставить("Текст", ТекстШаблона);
	Шаблоны.Добавить(Шаблон);

	// Демо: Реквизиты для оплаты, смс
	Шаблон = Новый Структура();
	Шаблон.Вставить("ТипШаблона", "SMS");
	Шаблон.Вставить("ПолноеИмяТипаНазначения", "Документ.РеализацияТоваровУслуг");
	Шаблон.Вставить("Наименование", "Реквизиты для оплаты через СБП (Реализация товаров и услуг) (смс)");
	Шаблон.Вставить("Тема", "Реквизиты для оплаты: [РеализацияТоваровУслуг.Номер] от [РеализацияТоваровУслуг.Дата{ДЛФ=dd.MM.yyyy}] на сумму [РеализацияТоваровУслуг.СуммаДокумента] руб.");
	ТекстШаблона = 
		"Благодарим за заказ и просим оплатить.
		|
		|Реквизиты для оплаты: [РеализацияТоваровУслуг.СуммаДокумента] руб.
		|
		|Заказ можно оплатить, перейдя по ссылке [РеализацияТоваровУслуг.ПредставлениеСсылкиСБП]";
		
	Шаблон.Вставить("Текст", ТекстШаблона);
	Шаблоны.Добавить(Шаблон);
	
КонецПроцедуры

// Добавить шаблоны email и sms для документа "Реализация услуг и прочих активов"
// 
// Параметры:
//  Шаблоны - Массив Из Структура - Шаблоны
Процедура ДобавитьШаблонРеализацияУслугПрочихАктивов(Шаблоны)
	
	Шаблон = Новый Структура();
	Шаблон.Вставить("ТипШаблона", "Письмо");
	Шаблон.Вставить("ПолноеИмяТипаНазначения", "Документ.РеализацияУслугПрочихАктивов");
	Шаблон.Вставить("Наименование", "Реквизиты для оплаты через СБП (Реализация услуг и прочих активов) (почта)");
	Шаблон.Вставить("Тема", "Реквизиты для оплаты: [РеализацияУслугПрочихАктивов.Номер] от [РеализацияУслугПрочихАктивов.Дата{ДЛФ=dd.MM.yyyy}] на сумму [РеализацияУслугПрочихАктивов.СуммаДокумента] руб.");
	ТекстШаблона = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"<html>
		|<head>
		|<meta http-equiv=""Content-Type"" content=""text/html; charset=utf-8"" />
		|<meta http-equiv=""X-UA-Compatible"" content=""IE=Edge"" />
		|<meta name=""format-detection"" content=""telephone=no"" />
		|<style type=""text/css"">
		|body{margin:0;padding:8px;overflow:auto;width:100%;height:100%;}
		|p{line-height:1.15;margin:0;}
		|ol,ul{margin-top:0;margin-bottom:0;}
		|img{border:none;}
		|</style>
		|</head>
		|<body>
		|<p>%1</p>
		|<p></p>
		|<p><strong>Покупатель:</strong> %2<br>
		|<strong>Счет:</strong> %3 от %4 <br>
		|<strong>К оплате:</strong> %5</p>
		|<p></p>
		|<p>%6</p>
		|<p></p>
		|<p>%7</p>
		|<p>%8</p>
		|</body>
		|</html>",
		"Благодарим за заказ и просим оплатить.",
		"[РеализацияУслугПрочихАктивов.ПартнерНаименование]",
		"[РеализацияУслугПрочихАктивов.Номер]",
		"[РеализацияУслугПрочихАктивов.Дата{ДЛФ='dd.MM.yyyy'}]",
		"[РеализацияУслугПрочихАктивов.СуммаДокумента] руб.",
		"Для оплаты отсканируйте QR-код в Вашем мобильном банковском приложении
		| или перейдите по ссылке для  выбора приложения банка.",
		"[РеализацияУслугПрочихАктивов.ПредставлениеСсылкиСБПQRКод]",
		"[РеализацияУслугПрочихАктивов.ПредставлениеСсылкиСБП]");
		
	ТекстШаблона = СтрЗаменить(ТекстШаблона, Символы.ПС, "");
	Шаблон.Вставить("Текст", ТекстШаблона);
	Шаблоны.Добавить(Шаблон);

	// Демо: Реквизиты для оплаты, смс
	Шаблон = Новый Структура();
	Шаблон.Вставить("ТипШаблона", "SMS");
	Шаблон.Вставить("ПолноеИмяТипаНазначения", "Документ.РеализацияУслугПрочихАктивов");
	Шаблон.Вставить("Наименование", "Реквизиты для оплаты через СБП (Реализация услуг и прочих активов) (смс)");
	Шаблон.Вставить("Тема", "Реквизиты для оплаты: [РеализацияУслугПрочихАктивов.Номер] от [РеализацияУслугПрочихАктивов.Дата{ДЛФ=dd.MM.yyyy}] на сумму [РеализацияУслугПрочихАктивов.СуммаДокумента] руб.");
	ТекстШаблона = 
		"Благодарим за заказ и просим оплатить.
		|
		|Сумма заказа: [РеализацияУслугПрочихАктивов.СуммаДокумента] руб.
		|
		|Заказ можно оплатить, перейдя по ссылке [РеализацияУслугПрочихАктивов.ПредставлениеСсылкиСБП]";
		
	Шаблон.Вставить("Текст", ТекстШаблона);
	Шаблоны.Добавить(Шаблон);
	
КонецПроцедуры

//-- Локализация

#Область КомиссионерВЧеке

// Представляет входящий текст пакетного запроса как список значений, где текст атомарного запроса это значении элемента,
// а имя создаваемой таблицы - это представление элемента списка. 
// Параметры:
//	ТекстПакетногоЗапроса - Строка - 	граничные условия декомпозиции пакетного запроса:
//			1) Разделитель запросов ";" размещается на отдельной строке.
// 			2) имя создаваемой таблицы размещается на отдельной строке вместе с ключевым литералом "ПОМЕСТИТЬ".
//
// Возвращаемое значение:
// СписокЗначений из Строка. 
// 	*значение - это текст атомарного запроса, 
// 	*представление - имя создаваемой временной таблицы этим запросом
Функция РазбитьПакетныйЗапрос(Знач ТекстПакетногоЗапроса)
	
	ТекстПакета = СокрЛП(ТекстПакетногоЗапроса);
	СписокЗапросов = Новый СписокЗначений;
	
	ТекЗапрос = "";
	ИмяНовойВТ = "";
	
	Для СчСтроки = 1 По СтрЧислоСтрок(ТекстПакета) Цикл
		
		ТекСтрока = СтрПолучитьСтроку(ТекстПакета, СчСтроки);
		СтрокаСодержимое = СокрЛП(ТекСтрока);
		
		Если СтрНачинаетсяС(СтрокаСодержимое, ";") Тогда
			// это разделитель запросов
			Если НЕ ПустаяСтрока(ТекЗапрос) Тогда
				СписокЗапросов.Добавить(ТекЗапрос, ИмяНовойВТ);
				ТекЗапрос = "";
				ИмяНовойВТ = "";
			КонецЕсли;
			
			Продолжить;
			
		ИначеЕсли СтрНачинаетсяС(ВРег(СтрокаСодержимое), "ПОМЕСТИТЬ") Тогда
			// это имя ВТ
			ИмяНовойВТ = СокрЛП(Сред(СтрокаСодержимое, 10));
		КонецЕсли;
		ТекЗапрос = ТекЗапрос + Символы.ПС + ТекСтрока;
	КонецЦикла;
	Если НЕ ПустаяСтрока(ТекЗапрос) Тогда
		СписокЗапросов.Добавить(ТекЗапрос, ИмяНовойВТ);
	КонецЕсли;
	
	Возврат СписокЗапросов;
КонецФункции

#КонецОбласти

#КонецОбласти