#Область ПрограммныйИнтерфейс

// Выполняет заполнение регистров взаиморасчетов в новой архитектуре по переданной структуре параметров.
//
// Параметры:
//	ОсновныеПараметры - Структура, ТаблицаЗначений - Параметры пересчета, см. СтруктураПараметровЗаполненияВзаиморасчетов
//	ДополнительныеСвойстваПроведения - Структура
//
Процедура ЗаполнитьОперативныеВзаиморасчеты(ОсновныеПараметры, ДополнительныеСвойстваПроведения = Неопределено) Экспорт
	
	#Область ИнициализацияЗапросаИПеременных
	
	ТаблицаПараметровРаспределения = ТаблицаПараметровРаспределения(ОсновныеПараметры);
	
	Если ТаблицаПараметровРаспределения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьЗаписиРасчетовСКлиентами    = Ложь;
	ЕстьЗаписиРасчетовСПоставщиками = Ложь;
	
	ЗапросГлобальныхПеременных = Новый Запрос;
	ЗапросГлобальныхПеременных.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтПараметры.ТипРасчетов КАК ТипРасчетов,
	|	ВтПараметры.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВтПараметры.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВтПараметры.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ВтПараметры.ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами,
	|	ВтПараметры.НачальноеЗаполнение КАК НачальноеЗаполнение,
	|	ВтПараметры.Регистратор КАК Регистратор,
	|	ВтПараметры.ЗаписыватьИзменения КАК ЗаписыватьИзменения,
	|	ВтПараметры.ФиксированныйКурсНакладной КАК ФиксированныйКурсНакладной,
	|	ВтПараметры.ФиксированныйКурсРегл КАК ФиксированныйКурсРегл,
	|	ВтПараметры.ФиксированныйКурсУпр КАК ФиксированныйКурсУпр,
	|	ВтПараметры.ГрафикВДоговоре КАК ГрафикВДоговоре,
	|	ВтПараметры.ДатаНачалаПересчета КАК ДатаНачалаПересчета,
	|	ВтПараметры.ПорядокОперации КАК ПорядокОперации,
	|	ВтПараметры.ПериодКонтроляИзменений КАК ПериодКонтроляИзменений,
	|	ВтПараметры.ПерезаполнениеРегистровВзаиморасчетов КАК ПерезаполнениеРегистровВзаиморасчетов
	|ПОМЕСТИТЬ ВтТаблицаПараметровРаспределения
	|ИЗ
	|	&ТаблицаПараметровРаспределения КАК ВтПараметры
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтПараметры.ТипРасчетов КАК ТипРасчетов,
	|	ВтПараметры.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВтПараметры.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ЕСТЬNULL(Справочник.Объект, НЕОПРЕДЕЛЕНО) КАК Объект,
	|	ЕСТЬNULL(Справочник.Договор, КлючиАналитикиУчетаПоПартнерам.Договор) КАК Договор,
	|	ВЫБОР КОГДА ЕСТЬNULL(Справочник.Договор.ПорядокЗачетаОплатНакладных, 
	|							ЗНАЧЕНИЕ(Перечисление.ПорядкиЗачетаОплатНакладных.ПустаяСсылка)) 
	|				= ЗНАЧЕНИЕ(Перечисление.ПорядкиЗачетаОплатНакладных.ПустаяСсылка)
	|		ТОГДА КонстантаПорядокЗачетаОплатНакладных.Значение
	|		ИНАЧЕ ЕСТЬNULL(Справочник.Договор.ПорядокЗачетаОплатНакладных, ЗНАЧЕНИЕ(Перечисление.ПорядкиЗачетаОплатНакладных.ПустаяСсылка))
	|	КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.ПорядкиЗачетаОплатНакладных.ПоДатеПлановогоПогашения) КАК ЗачетОплатПоДатеПлатежа,
	|	ВтПараметры.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ВтПараметры.ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами,
	|	ВтПараметры.НачальноеЗаполнение КАК НачальноеЗаполнение,
	|	ВтПараметры.Регистратор КАК Регистратор,
	|	ВтПараметры.ЗаписыватьИзменения КАК ЗаписыватьИзменения,
	|	ВтПараметры.ФиксированныйКурсНакладной КАК ФиксированныйКурсНакладной,
	|	ВтПараметры.ФиксированныйКурсРегл КАК ФиксированныйКурсРегл,
	|	ВтПараметры.ФиксированныйКурсУпр КАК ФиксированныйКурсУпр,
	|	ВтПараметры.ДатаНачалаПересчета КАК ДатаНачалаПересчета,
	|	ВтПараметры.ПорядокОперации КАК ПорядокОперации,
	|	ВтПараметры.ПериодКонтроляИзменений КАК ПериодКонтроляИзменений,
	|	ВтПараметры.ГрафикВДоговоре КАК ГрафикВДоговоре,
	|	ВтПараметры.ПерезаполнениеРегистровВзаиморасчетов КАК ПерезаполнениеРегистровВзаиморасчетов,
	|	ЕСТЬNULL(Справочник.Договор.ВариантКурсаДоговора, ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.Переменный)) КАК
	|		ВариантКурсаДоговора,
	|	Справочник.ТипОбъектаРасчетов КАК ТипОбъектаРасчетов,
	|	ЕСТЬNULL(Справочник.Объект.ГрафикИсполненияДоговора, НЕОПРЕДЕЛЕНО) КАК ГрафикИсполненияДоговора,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислительВалютыВзаиморасчетов, 0) КАК КурсЧислительВалютыВзаиморасчетов,
	|	ЕСТЬNULL(КурсыВалют.КурсЗнаменательВалютыВзаиморасчетов, 0) КАК КурсЗнаменательВалютыВзаиморасчетов,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислительВалютыУправленческогоУчета, 0) КАК КурсЧислительВалютыУправленческогоУчета,
	|	ЕСТЬNULL(КурсыВалют.КурсЗнаменательВалютыУправленческогоУчета, 0) КАК КурсЗнаменательВалютыУправленческогоУчета,
	|	КлючиАналитикиУчетаПоПартнерам.Организация КАК Организация,
	|	КлючиАналитикиУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	КлючиАналитикиУчетаПоПартнерам.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
	|ИЗ
	|	ВтТаблицаПараметровРаспределения КАК ВтПараметры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК Справочник
	|		ПО ВтПараметры.ОбъектРасчетов = Справочник.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВалютыИКурсыДокументов КАК КурсыВалют
	|			ПО (Справочник.Объект = КурсыВалют.Документ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютРасчетовПоДоговорам.СрезПоследних(,) КАК КурсыВалютПоДоговору
	|			ПО (Справочник.Договор = КурсыВалютПоДоговору.Договор)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиАналитикиУчетаПоПартнерам
	|			ПО ВтПараметры.АналитикаУчетаПоПартнерам = КлючиАналитикиУчетаПоПартнерам.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.ПорядокЗачетаОплатНакладных КАК КонстантаПорядокЗачетаОплатНакладных
	|			ПО ИСТИНА";
	
	ЗапросГлобальныхПеременных.УстановитьПараметр("ТаблицаПараметровРаспределения", ТаблицаПараметровРаспределения);
	ДополненнаяТаблицаПараметровРаспределения = ЗапросГлобальныхПеременных.Выполнить().Выгрузить();
	
	ДатаНачалаПересчетаМинимальный     = '39991231235959';
	ГлобальныеПеременные = Новый Структура; 
	
	Для Каждого СтрокаОсновныхПараметровРасчета Из ДополненнаяТаблицаПараметровРаспределения Цикл

		СтрокаОсновныхПараметровРасчета.ФиксированныйКурсРегл = ?(
			СтрокаОсновныхПараметровРасчета.КурсЗнаменательВалютыВзаиморасчетов = 0, 0,
			СтрокаОсновныхПараметровРасчета.КурсЧислительВалютыВзаиморасчетов
			/ СтрокаОсновныхПараметровРасчета.КурсЗнаменательВалютыВзаиморасчетов);
			
		СтрокаОсновныхПараметровРасчета.ФиксированныйКурсУпр = ?(
			СтрокаОсновныхПараметровРасчета.КурсЗнаменательВалютыВзаиморасчетов = 0
			Или СтрокаОсновныхПараметровРасчета.КурсЧислительВалютыУправленческогоУчета = 0, 0,
			СтрокаОсновныхПараметровРасчета.КурсЧислительВалютыВзаиморасчетов
			* СтрокаОсновныхПараметровРасчета.КурсЗнаменательВалютыУправленческогоУчета
			/ (СтрокаОсновныхПараметровРасчета.КурсЗнаменательВалютыВзаиморасчетов
			* СтрокаОсновныхПараметровРасчета.КурсЧислительВалютыУправленческогоУчета));
			
		СтрокаОсновныхПараметровРасчета.ФиксированныйКурсНакладной = СтрокаОсновныхПараметровРасчета.ТипОбъектаРасчетов
			= Перечисления.ТипыОбъектовРасчетов.Накладная И СтрокаОсновныхПараметровРасчета.ВариантКурсаДоговора
			= Перечисления.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки;
			
		// Если график исполнения в договоре, то вместо графика заказа будет уточняться график договора.
		// Если пользователь каким-то образом ввел заказы с графиками по такому договору, то все этапы таких заказов будут отнесены к графику договора
		// и закрываться будут по ФИФО независимо от привязки накладных к заказам
		СтрокаОсновныхПараметровРасчета.ГрафикВДоговоре = ЗначениеЗаполнено(СтрокаОсновныхПараметровРасчета.ГрафикИсполненияДоговора);
		
		Если Не ЗначениеЗаполнено(СтрокаОсновныхПараметровРасчета.ДатаНачалаПересчета) Тогда
			СтрокаОсновныхПараметровРасчета.ДатаНачалаПересчета = Дата(1,1,1,1,1,2);
		КонецЕсли;
		СтрокаОсновныхПараметровРасчета.ПорядокОперации = Формат(СтрокаОсновныхПараметровРасчета.ДатаНачалаПересчета, "ДФ=yyyyMMdd") + "000000000";
		
		Если НЕ ЕстьЗаписиРасчетовСКлиентами Тогда
			ЕстьЗаписиРасчетовСКлиентами = СтрокаОсновныхПараметровРасчета.ЭтоРасчетыСКлиентами;
		КонецЕсли;
		Если НЕ ЕстьЗаписиРасчетовСПоставщиками Тогда
			ЕстьЗаписиРасчетовСПоставщиками = НЕ СтрокаОсновныхПараметровРасчета.ЭтоРасчетыСКлиентами;
		КонецЕсли;
		
		ДатаНачалаПересчетаМинимальный = Мин(ДатаНачалаПересчетаМинимальный, СтрокаОсновныхПараметровРасчета.ДатаНачалаПересчета);
		
		ГлобальныеПеременные.Вставить("ЗаписыватьИзменения", СтрокаОсновныхПараметровРасчета.ЗаписыватьИзменения);
		ГлобальныеПеременные.Вставить("НачальноеЗаполнение", СтрокаОсновныхПараметровРасчета.НачальноеЗаполнение);
		ГлобальныеПеременные.Вставить("Регистратор", СтрокаОсновныхПараметровРасчета.Регистратор);
		
	КонецЦикла;
	
	РазмерПорцииЗаписи = РазмерПорцииЗаписи();
	ХозяйственныеОперацииНеОтгрузка = ХозяйственныеОперацииНеОтгрузка();
	ХозяйственныеОперацииПереноса = ХозяйственныеОперацииПереносаРасчетов();
	ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	ДатаНачалаПересчетаЕстьРазличные = ОбщегоНазначенияКлиентСервер.СвернутьМассив(
		ДополненнаяТаблицаПараметровРаспределения.ВыгрузитьКолонку("ДатаНачалаПересчета")).Количество() > 1;

	ГлобальныеПеременные.Вставить("ЕжедневнаяПереоценка",                               Константы.ПереоцениватьВалютныеСредстваПоДням.Получить());
	ГлобальныеПеременные.Вставить("РазмерПорцииЗаписи",                                 РазмерПорцииЗаписи);
	ГлобальныеПеременные.Вставить("ВалютаУправленческогоУчета",                         ВалютаУправленческогоУчета);
	ГлобальныеПеременные.Вставить("ЕстьЗаписиРасчетовСКлиентами",                       ЕстьЗаписиРасчетовСКлиентами);
	ГлобальныеПеременные.Вставить("ЕстьЗаписиРасчетовСПоставщиками",                    ЕстьЗаписиРасчетовСПоставщиками);
	ГлобальныеПеременные.Вставить("ДатаНачалаПересчетаЕстьРазличные",                   ДатаНачалаПересчетаЕстьРазличные);
	ГлобальныеПеременные.Вставить("ХозяйственныеОперацииПереноса",                      ХозяйственныеОперацииПереноса);
	
	Запрос                         = Новый Запрос;
	Менеджер                       = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = Менеджер;
	Запрос.УстановитьПараметр("ВалютаУпр",                   ВалютаУправленческогоУчета);
	Запрос.УстановитьПараметр("РазмерПорцииЗаписи",          РазмерПорцииЗаписи);
	Запрос.УстановитьПараметр("СписокТиповЗаказов",          СписокТиповРегистраторовПланов());
	Запрос.УстановитьПараметр("ОперацииСРучнымКурсом",        ХозяйственныеОперацииСРучнымКурсом());
	Запрос.УстановитьПараметр("ИдентификаторНеиспользуемойФинЗаписи", ПроведениеДокументов.ИдентификаторНеиспользуемойФинЗаписи());
	Запрос.УстановитьПараметр("ПустыеСсылкиНаЗаказы",        ПустыеСсылкиНаЗаказы());
	Запрос.УстановитьПараметр("СписокТиповУточняющихПланы", СписокТиповУточняющихПланы());
	Запрос.УстановитьПараметр("ДатаНачалаПересчетаМинимальный", ДатаНачалаПересчетаМинимальный);
	Запрос.УстановитьПараметр("ДополненныеПараметрыРаспределения", ДополненнаяТаблицаПараметровРаспределения);
	Запрос.УстановитьПараметр("СписокАналитикУчетаПоПартнерам", ОбщегоНазначенияКлиентСервер.СвернутьМассив(
		ДополненнаяТаблицаПараметровРаспределения.ВыгрузитьКолонку("АналитикаУчетаПоПартнерам")));
	
	#КонецОбласти
	
	#Область ПолучениеДанныхДляВсехЗаданий
	
	// Движения до начала пересчета и остатки.
	СформироватьВТДанныхРегистров(Запрос, ГлобальныеПеременные);
	ДатаНачалаКурсов = Дата(3000,1,1);
	
	Если ГлобальныеПеременные.ЕстьЗаписиРасчетовСКлиентами Тогда
		// Данные регистров с порядком меньше порядка начала расчета и скопированные сторно записи, далее дополняются.
		ТаблицаРасчетовСКлиентамиПоСрокам      = ЗаписиДоНачалаРасчетаПоСрокам(Запрос, ГлобальныеПеременные, Истина);
		ТаблицаПланОплатКлиентам               = ЗаписиДоНачалаРасчетаПланОплат(Запрос, ГлобальныеПеременные, Истина);
		ТаблицаПланОтгрузокПоставокКлиентам    = ЗаписиДоНачалаРасчетаПланОтгрузок(Запрос, ГлобальныеПеременные, Истина);
		
		ДанныеДляРаспределенияКлиенты = ПолучитьДанныеДляРаспределения(Запрос, Истина);
		ДанныеДляРаспределенияКлиенты.Индексы.Добавить("Регистратор");
		
		ТаблицаНакладныхКлиенты = ПолучитьДанныеНакладных(Запрос, Истина);
		
		ИсходныеРегистраторыКлиенты = Запрос.МенеджерВременныхТаблиц.Таблицы["ВтИсходныеРегистраторыКлиенты"].ПолучитьДанные().Выгрузить();
		ДвиженияИсходныхРегистраторовКлиенты = ДвиженияСторнируемыхДокументовДоНачалаРасчета(Запрос,Истина);
		ДвиженияИсходныхРегистраторовПланОплатКлиенты = ДвиженияСторнируемыхДокументовПланОплатДоНачалаРасчета(Запрос,Истина);
		ДвиженияИсходныхРегистраторовПланОтгрузокКлиенты = ДвиженияСторнируемыхДокументовПланОтгрузокДоНачалаРасчета(Запрос,Истина);
		
		ТаблицаОстатковВзаиморасчетовКлиенты = ТаблицаОстатковВзаиморасчетов(Запрос,Истина);
		
		ДатаНачалаКурсов = Мин(ДатаНачалаКурсов,
								?(ДанныеДляРаспределенияКлиенты.Количество() > 0,ДанныеДляРаспределенияКлиенты[0].Период,ТекущаяДатаСеанса()),
								?(ТаблицаОстатковВзаиморасчетовКлиенты.Количество() > 0,ТаблицаОстатковВзаиморасчетовКлиенты[0].ДатаВозникновения,ТекущаяДатаСеанса()))
		
	КонецЕсли;
	
	Если ГлобальныеПеременные.ЕстьЗаписиРасчетовСПоставщиками Тогда
		
		ТаблицаРасчетовСПоставщикамиПоСрокам     = ЗаписиДоНачалаРасчетаПоСрокам(Запрос, ГлобальныеПеременные, Ложь);
		
		ТаблицаПланОплатПоставщикам            = ЗаписиДоНачалаРасчетаПланОплат(Запрос, ГлобальныеПеременные, Ложь);
		ТаблицаПланОтгрузокПоставокПоставщикам = ЗаписиДоНачалаРасчетаПланОтгрузок(Запрос, ГлобальныеПеременные, Ложь);
		
		ДанныеДляРаспределенияПоставщики = ПолучитьДанныеДляРаспределения(Запрос, Ложь);
		ДанныеДляРаспределенияПоставщики.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
		Если ДанныеДляРаспределенияПоставщики.Количество() > 0 Тогда
			ДатаНачалаКурсов = Мин(ДатаНачалаКурсов,ДанныеДляРаспределенияПоставщики[0].Период);
		КонецЕсли;
		
		ТаблицаНакладныхПоставщики = ПолучитьДанныеНакладных(Запрос, Ложь);
		
		ИсходныеРегистраторыПоставщики = Запрос.МенеджерВременныхТаблиц.Таблицы["ВтИсходныеРегистраторыПоставщики"].ПолучитьДанные().Выгрузить();
		ДвиженияИсходныхРегистраторовПоставщики = ДвиженияСторнируемыхДокументовДоНачалаРасчета(Запрос, Ложь);
		ДвиженияИсходныхРегистраторовПланОплатПоставщики = ДвиженияСторнируемыхДокументовПланОплатДоНачалаРасчета(Запрос,Ложь);
		ДвиженияИсходныхРегистраторовПланОтгрузокПоставщики = ДвиженияСторнируемыхДокументовПланОтгрузокДоНачалаРасчета(Запрос,Ложь);
		
		ТаблицаОстатковВзаиморасчетовПоставщики = ТаблицаОстатковВзаиморасчетов(Запрос,Ложь);
		
		ДатаНачалаКурсов = Мин(ДатаНачалаКурсов,
								?(ДанныеДляРаспределенияПоставщики.Количество() > 0,ДанныеДляРаспределенияПоставщики[0].Период,ТекущаяДатаСеанса()),
								?(ТаблицаОстатковВзаиморасчетовПоставщики.Количество() > 0,ТаблицаОстатковВзаиморасчетовПоставщики[0].ДатаВозникновения,ТекущаяДатаСеанса()))
		
	КонецЕсли;

	#Область ТаблицыДляПлановыхРасчетов
		
		Если ГлобальныеПеременные.ЕстьЗаписиРасчетовСКлиентами Тогда
			
			// Остатки плановых оплат для уточнения и закрытия нераспределенными авансами.
			ОстаткиПлановыхОплатКлиенты = Запрос.МенеджерВременныхТаблиц.Таблицы["ВТРасчетыПланОплатОстаткиКлиенты"].ПолучитьДанные().Выгрузить(); //ТаблицаЗначений
			ОстаткиПлановыхОплатКлиенты.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			// Остатки плановых отгрузок для уточнения и закрытия нераспределенными авансами.
			ОстаткиПлановыхОтгрузокКлиенты = Запрос.МенеджерВременныхТаблиц.Таблицы["ВТРасчетыПланОтгрузокПоставокОстаткиКлиенты"].ПолучитьДанные().Выгрузить(); //ТаблицаЗначений
			ОстаткиПлановыхОтгрузокКлиенты.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			// Остатки и движения нераспределенных авансов.
			ДвиженияПредоплатыДляСуммированияКлиенты = ОстаткиНераспределенныхАвансовПлановойОплатыНаНачалоПересчета(Запрос, Истина);
			ДвиженияПредоплатыДляСуммированияКлиенты.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			ОстаткиАвансовКлиенты = ДвиженияПредоплатыДляСуммированияКлиенты.Скопировать(Новый Структура("Зачтено, ОстатокПоложительный", Ложь, Истина));
			ОстаткиАвансовКлиенты.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			ДвиженияПредоплатыДляСуммированияКлиенты.Свернуть("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов,ДатаДвижения,Регистратор,ПорядокОперации,Просуммировано","Сумма");
			
			//Расходные движения накладных, которые распределяются на остатки плановых графиков.
			УточненияГрафиковОплатКлиенты    = ПолучитьУточненияГрафиковОплат(Запрос, ГлобальныеПеременные, Истина);
			УточненияГрафиковОплатКлиенты.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			ЗакрытияГрафиковОтгрузокКлиенты = ПолучитьЗакрытияГрафиковОтгрузок(Запрос, ГлобальныеПеременные, Истина);
			ЗакрытияГрафиковОтгрузокКлиенты.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			//Приходные движения заказов, так же в эту таблицу дописываются движения уточнений графика.
			//Используется для переноса движений в таблицу записей регистра накопления и для определения остатков плановых оплат на каждый день.
			ДвиженияПлановыхОплатДляСуммированияКлиенты    = ПолучитьПриходныеДвиженияПоПлановымОплатам(Запрос, ГлобальныеПеременные, Истина);
			ДвиженияПлановыхОплатДляСуммированияКлиенты.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			ДвиженияПлановыхОтгрузокДляСуммированияКлиенты = ПолучитьПриходныеДвиженияПоПлановымОтгрузкам(Запрос, ГлобальныеПеременные, Истина);
			ДвиженияПлановыхОтгрузокДляСуммированияКлиенты.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
		КонецЕсли;
		
		Если ГлобальныеПеременные.ЕстьЗаписиРасчетовСПоставщиками Тогда
			
			// Остатки плановых оплат для уточнения и закрытия нераспределенными авансами.
			ОстаткиПлановыхОплатПоставщики = Запрос.МенеджерВременныхТаблиц.Таблицы["ВТРасчетыПланОплатОстаткиПоставщики"].ПолучитьДанные().Выгрузить(); //ТаблицаЗначений
			ОстаткиПлановыхОплатПоставщики.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			// Остатки плановых отгрузок для уточнения и закрытия нераспределенными авансами.
			ОстаткиПлановыхОтгрузокПоставщики = Запрос.МенеджерВременныхТаблиц.Таблицы["ВТРасчетыПланОтгрузокПоставокОстаткиПоставщики"].ПолучитьДанные().Выгрузить(); //ТаблицаЗначений
			ОстаткиПлановыхОтгрузокПоставщики.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			// Остатки и движения нераспределенных авансов.
			ДвиженияПредоплатыДляСуммированияПоставщики = ОстаткиНераспределенныхАвансовПлановойОплатыНаНачалоПересчета(Запрос, Ложь);
			ДвиженияПредоплатыДляСуммированияПоставщики.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			ОстаткиАвансовПоставщики = ДвиженияПредоплатыДляСуммированияПоставщики.Скопировать(Новый Структура("Зачтено, ОстатокПоложительный", Ложь, Истина));
			ОстаткиАвансовПоставщики.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			ДвиженияПредоплатыДляСуммированияПоставщики.Свернуть("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов,ДатаДвижения,Регистратор,ПорядокОперации,Просуммировано","Сумма");
			
			//Расходные движения накладных, которые распределяются на остатки плановых графиков.
			УточненияГрафиковОплатПоставщики    = ПолучитьУточненияГрафиковОплат(Запрос, ГлобальныеПеременные, Ложь);
			УточненияГрафиковОплатПоставщики.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			ЗакрытияГрафиковОтгрузокПоставщики = ПолучитьЗакрытияГрафиковОтгрузок(Запрос, ГлобальныеПеременные, Ложь);
			ЗакрытияГрафиковОтгрузокПоставщики.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			//Приходные движения заказов, так же в эту таблицу дописываются движения уточнений графика.
			//Используется для переноса движений в таблицу записей регистра накопления и для определения остатков плановых оплат на каждый день.
			ДвиженияПлановыхОплатДляСуммированияПоставщики    = ПолучитьПриходныеДвиженияПоПлановымОплатам(Запрос, ГлобальныеПеременные, Ложь);
			ДвиженияПлановыхОплатДляСуммированияПоставщики.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
			ДвиженияПлановыхОтгрузокДляСуммированияПоставщики = ПолучитьПриходныеДвиженияПоПлановымОтгрузкам(Запрос, ГлобальныеПеременные, Ложь);
			ДвиженияПлановыхОтгрузокДляСуммированияПоставщики.Индексы.Добавить("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов");
			
		КонецЕсли;
	
	#КонецОбласти
	
	СоздатьТаблицуДатЗаПериод(Запрос.МенеджерВременныхТаблиц, "ТаблицаДат", ДатаНачалаКурсов, НачалоДня(ТекущаяДатаСеанса()));
	
	#КонецОбласти
	
	Для Каждого СтрокаОсновныхПараметровРасчета Из ДополненнаяТаблицаПараметровРаспределения Цикл
		
		#Область ПолучениеДанныхДляРаспределения
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("ОбъектРасчетов",            СтрокаОсновныхПараметровРасчета.ОбъектРасчетов);
		СтруктураОтбора.Вставить("АналитикаУчетаПоПартнерам", СтрокаОсновныхПараметровРасчета.АналитикаУчетаПоПартнерам);
		СтруктураОтбора.Вставить("ВалютаРасчетов",            СтрокаОсновныхПараметровРасчета.ВалютаРасчетов);
		
		Если СтрокаОсновныхПараметровРасчета.ЭтоРасчетыСКлиентами Тогда
			
			ХозяйственныеОперации = Новый Соответствие;
			ХозяйственныеОперации.Вставить("ЗачетАванса",             Перечисления.ХозяйственныеОперации.ЗачетАвансаКлиента);
			ХозяйственныеОперации.Вставить("ПогашениеЗадолженности",  Перечисления.ХозяйственныеОперации.ПогашениеЗадолженностиКлиента);
			ХозяйственныеОперации.Вставить("ВозвратАванса",           Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту);
			ХозяйственныеОперации.Вставить("ПереоценкаПоложительная", Перечисления.ХозяйственныеОперации.КурсовыеРазницыКлиентыПрибыль);
			ХозяйственныеОперации.Вставить("ПереоценкаОтрицательная", Перечисления.ХозяйственныеОперации.КурсовыеРазницыКлиентыУбыток);
			ХозяйственныеОперации.Вставить("Планирование",            Перечисления.ХозяйственныеОперации.ПланированиеПоЗаказуКлиента);
			
			НастройкиХО = Новый Соответствие;
			НастройкиХО.Вставить("ЗачетОплаты", Справочники.НастройкиХозяйственныхОпераций.ЗачетАвансаКлиента);
			НастройкиХО.Вставить("ПереоценкаПоложительная", Справочники.НастройкиХозяйственныхОпераций.КурсовыеРазницыКлиентыПрибыль);
			НастройкиХО.Вставить("ПереоценкаОтрицательная", Справочники.НастройкиХозяйственныхОпераций.КурсовыеРазницыКлиентыУбыток);
			
			ДанныеДляРаспределения = ДанныеДляРаспределенияКлиенты.Скопировать(СтруктураОтбора);
			
			ИсходныеРегистраторы = ИсходныеРегистраторыКлиенты.Скопировать(СтруктураОтбора);
			ДвиженияИсходныхРегистраторов = ДвиженияИсходныхРегистраторовКлиенты.Скопировать(СтруктураОтбора);
			ДвиженияИсходныхРегистраторовПланОплат = ДвиженияИсходныхРегистраторовПланОплатКлиенты.Скопировать(СтруктураОтбора);
			ДвиженияИсходныхРегистраторовПланОтгрузок = ДвиженияИсходныхРегистраторовПланОтгрузокКлиенты.Скопировать(СтруктураОтбора);
			ТаблицаОстатковВзаиморасчетов = ТаблицаОстатковВзаиморасчетовКлиенты.Скопировать(СтруктураОтбора);
			
				ДвиженияПредоплатыДляСуммирования = ДвиженияПредоплатыДляСуммированияКлиенты.Скопировать(СтруктураОтбора);
				ДвиженияПлановыхОплатДляСуммирования = ДвиженияПлановыхОплатДляСуммированияКлиенты.Скопировать(СтруктураОтбора);
				ДвиженияПлановыхОтгрузокДляСуммирования = ДвиженияПлановыхОтгрузокДляСуммированияКлиенты.Скопировать(СтруктураОтбора);
				УточненияГрафиковОплат = УточненияГрафиковОплатКлиенты.Скопировать(СтруктураОтбора);
				ЗакрытияГрафиковОтгрузок = ЗакрытияГрафиковОтгрузокКлиенты.Скопировать(СтруктураОтбора);
			
				ОстаткиПлановыхОплат = ОстаткиПлановыхОплатКлиенты.Скопировать(СтруктураОтбора);
				ОстаткиПлановыхОплат.Индексы.Добавить("ДокументПлан,ВариантОплаты");
				ОстаткиПлановыхОплат.Индексы.Добавить("ДокументПлан");
				
				ОстаткиПлановыхОтгрузок = ОстаткиПлановыхОтгрузокКлиенты.Скопировать(СтруктураОтбора);
				ОстаткиПлановыхОтгрузок.Индексы.Добавить("ДокументПлан");
				
				ОстатокАвансов = ОстаткиАвансовКлиенты.Скопировать(СтруктураОтбора).Итог("Сумма");
			
			ДанныеНакладных = Новый Соответствие;
			ТаблицаДанныхНакладных = ТаблицаНакладныхКлиенты.Скопировать(СтруктураОтбора);
			Для Каждого Строка Из ТаблицаДанныхНакладных Цикл
				ДанныеНакладных.Вставить(Строка.Накладная,
											СтруктураСумм(Строка.Сумма,
											Строка.СуммаРегл,
											Строка.СуммаУпр));
			КонецЦикла;
			ТаблицаДанныхНакладных.Очистить();
			
			СтруктураОтбора.Удалить("ВалютаРасчетов");
			СтруктураОтбора.Вставить("Валюта",СтрокаОсновныхПараметровРасчета.ВалютаРасчетов);
			
			ТаблицаПланОплат = ТаблицаПланОплатКлиентам.Скопировать(СтруктураОтбора);
			ТаблицаПланОтгрузокПоставок = ТаблицаПланОтгрузокПоставокКлиентам.Скопировать(СтруктураОтбора);
			ТаблицаРасчетовПоСрокам = ТаблицаРасчетовСКлиентамиПоСрокам.Скопировать(СтруктураОтбора);
			
			ИмяРегистраРасчетов = "РасчетыСКлиентамиПоСрокам";
			ИмяРегистраПланаОплат = "РасчетыСКлиентамиПланОплат";
			ИмяРегистраПланаОтгрузкиПоставки = "РасчетыСКлиентамиПланОтгрузок";
		
			ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
			
		Иначе
			
			ХозяйственныеОперации = Новый Соответствие;
			ХозяйственныеОперации.Вставить("ЗачетАванса",             Перечисления.ХозяйственныеОперации.ЗачетАвансаПоставщику);
			ХозяйственныеОперации.Вставить("ПогашениеЗадолженности",  Перечисления.ХозяйственныеОперации.ПогашениеЗадолженностиПоставщику);
			ХозяйственныеОперации.Вставить("ВозвратАванса",           Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика);
			ХозяйственныеОперации.Вставить("ПереоценкаПоложительная", Перечисления.ХозяйственныеОперации.КурсовыеРазницыПоставщикиУбыток);
			ХозяйственныеОперации.Вставить("ПереоценкаОтрицательная", Перечисления.ХозяйственныеОперации.КурсовыеРазницыПоставщикиПрибыль);
			ХозяйственныеОперации.Вставить("Планирование",            Перечисления.ХозяйственныеОперации.ПланированиеПоЗаказуПоставщику);
			
			НастройкиХО = Новый Соответствие;
			НастройкиХО.Вставить("ЗачетОплаты", Справочники.НастройкиХозяйственныхОпераций.ЗачетАвансаПоставщику);
			НастройкиХО.Вставить("ПереоценкаПоложительная", Справочники.НастройкиХозяйственныхОпераций.КурсовыеРазницыПоставщикиУбыток);
			НастройкиХО.Вставить("ПереоценкаОтрицательная", Справочники.НастройкиХозяйственныхОпераций.КурсовыеРазницыПоставщикиПрибыль);
			
			ДанныеДляРаспределения = ДанныеДляРаспределенияПоставщики.Скопировать(СтруктураОтбора);
			
			ИсходныеРегистраторы = ИсходныеРегистраторыПоставщики.Скопировать(СтруктураОтбора);
			ДвиженияИсходныхРегистраторов = ДвиженияИсходныхРегистраторовПоставщики.Скопировать(СтруктураОтбора);
			ДвиженияИсходныхРегистраторовПланОплат = ДвиженияИсходныхРегистраторовПланОплатПоставщики.Скопировать(СтруктураОтбора);
			ДвиженияИсходныхРегистраторовПланОтгрузок = ДвиженияИсходныхРегистраторовПланОтгрузокПоставщики.Скопировать(СтруктураОтбора);
			ТаблицаОстатковВзаиморасчетов = ТаблицаОстатковВзаиморасчетовПоставщики.Скопировать(СтруктураОтбора);
				
				ДвиженияПредоплатыДляСуммирования = ДвиженияПредоплатыДляСуммированияПоставщики.Скопировать(СтруктураОтбора);
				ДвиженияПлановыхОплатДляСуммирования = ДвиженияПлановыхОплатДляСуммированияПоставщики.Скопировать(СтруктураОтбора);
				ДвиженияПлановыхОтгрузокДляСуммирования = ДвиженияПлановыхОтгрузокДляСуммированияПоставщики.Скопировать(СтруктураОтбора);
				УточненияГрафиковОплат = УточненияГрафиковОплатПоставщики.Скопировать(СтруктураОтбора);
				ЗакрытияГрафиковОтгрузок = ЗакрытияГрафиковОтгрузокПоставщики.Скопировать(СтруктураОтбора);
				
				ОстаткиПлановыхОплат = ОстаткиПлановыхОплатПоставщики.Скопировать(СтруктураОтбора);
				ОстаткиПлановыхОплат.Индексы.Добавить("ДокументПлан,ВариантОплаты");
				ОстаткиПлановыхОплат.Индексы.Добавить("ДокументПлан");
				
				ОстаткиПлановыхОтгрузок = ОстаткиПлановыхОтгрузокПоставщики.Скопировать(СтруктураОтбора);
				ОстаткиПлановыхОтгрузок.Индексы.Добавить("ДокументПлан");
				
				ОстатокАвансов = ОстаткиАвансовПоставщики.Скопировать(СтруктураОтбора).Итог("Сумма");
			
			ДанныеНакладных = Новый Соответствие;
			ТаблицаДанныхНакладных = ТаблицаНакладныхПоставщики.Скопировать(СтруктураОтбора);
			Для Каждого Строка Из ТаблицаДанныхНакладных Цикл
				ДанныеНакладных.Вставить(Строка.Накладная,
											СтруктураСумм(Строка.Сумма,
											Строка.СуммаРегл,
											Строка.СуммаУпр));
			КонецЦикла;
			ТаблицаДанныхНакладных.Очистить();
			
			СтруктураОтбора.Удалить("ВалютаРасчетов");
			СтруктураОтбора.Вставить("Валюта",СтрокаОсновныхПараметровРасчета.ВалютаРасчетов);
			
			ТаблицаПланОплат = ТаблицаПланОплатПоставщикам.Скопировать(СтруктураОтбора);
			ТаблицаПланОтгрузокПоставок = ТаблицаПланОтгрузокПоставокПоставщикам.Скопировать(СтруктураОтбора);
			ТаблицаРасчетовПоСрокам = ТаблицаРасчетовСПоставщикамиПоСрокам.Скопировать(СтруктураОтбора);
			
			ИмяРегистраРасчетов = "РасчетыСПоставщикамиПоСрокам";
			ИмяРегистраПланаОплат = "РасчетыСПоставщикамиПланОплат";
			ИмяРегистраПланаОтгрузкиПоставки = "РасчетыСПоставщикамиПланПоставок";
		
			ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
		КонецЕсли;
		
		ДвиженияИсходныхРегистраторов.Индексы.Добавить("ДокументРегистратор");
		ИсходныеРегистраторы.Индексы.Добавить("НовыйРегистратор");
		ИсходныеРегистраторы.Индексы.Добавить("СторнируемыйРегистратор");
		
		ГлобальныеПеременные.Вставить("Организация", СтрокаОсновныхПараметровРасчета.Организация);
		ГлобальныеПеременные.Вставить("ГрафикВДоговоре", СтрокаОсновныхПараметровРасчета.ГрафикВДоговоре);
		ГлобальныеПеременные.Вставить("ПорядокПереоценки", Новый Соответствие);
		ГлобальныеПеременные.Вставить("ХозяйственныеОперации", ХозяйственныеОперации);
		ГлобальныеПеременные.Вставить("НастройкиХО", НастройкиХО);
		ГлобальныеПеременные.Вставить("ВалютаРегламентированногоУчета", СтрокаОсновныхПараметровРасчета.ВалютаРегламентированногоУчета);
		ГлобальныеПеременные.Вставить("Договор", СтрокаОсновныхПараметровРасчета.Договор); 
		ГлобальныеПеременные.Вставить("ВариантКурсаДоговора", СтрокаОсновныхПараметровРасчета.ВариантКурсаДоговора);
		ГлобальныеПеременные.Вставить("ПорядокОперации", Формат(СтрокаОсновныхПараметровРасчета.ДатаНачалаПересчета, "ДФ=yyyyMMdd") + "000000000");
		ГлобальныеПеременные.Вставить("ДатаНачалаПересчета", СтрокаОсновныхПараметровРасчета.ДатаНачалаПересчета);
		ГлобальныеПеременные.Вставить("ФиксированныйКурсНакладной", СтрокаОсновныхПараметровРасчета.ФиксированныйКурсНакладной);
		ГлобальныеПеременные.Вставить("ФиксированныйКурсРегл", СтрокаОсновныхПараметровРасчета.ФиксированныйКурсРегл);
		ГлобальныеПеременные.Вставить("ФиксированныйКурсУпр", СтрокаОсновныхПараметровРасчета.ФиксированныйКурсУпр);
		ГлобальныеПеременные.Вставить("АналитикаУчетаПоПартнерам", СтрокаОсновныхПараметровРасчета.АналитикаУчетаПоПартнерам);
		ГлобальныеПеременные.Вставить("ВалютаРасчетов", СтрокаОсновныхПараметровРасчета.ВалютаРасчетов);
		ГлобальныеПеременные.Вставить("ДополнительныеСвойстваПроведения", ДополнительныеСвойстваПроведения);
		ГлобальныеПеременные.Вставить("ОбъектРасчетов", СтрокаОсновныхПараметровРасчета.ОбъектРасчетов);
		ГлобальныеПеременные.Вставить("ЭтоРасчетыСКлиентами", СтрокаОсновныхПараметровРасчета.ЭтоРасчетыСКлиентами);
		ГлобальныеПеременные.Вставить("ДанныеНакладных", ДанныеНакладных);
		ГлобальныеПеременные.Вставить("ДанныеКорректировокГрафиков", Новый Соответствие);
		ГлобальныеПеременные.Вставить("ЗачетОплатПоДатеПлатежа",СтрокаОсновныхПараметровРасчета.ЗачетОплатПоДатеПлатежа);
		
		Запрос.УстановитьПараметр("ОбъектРасчетов",             ГлобальныеПеременные.ОбъектРасчетов);
		Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам",  ГлобальныеПеременные.АналитикаУчетаПоПартнерам);
		Запрос.УстановитьПараметр("ВалютаРасчетов",             ГлобальныеПеременные.ВалютаРасчетов);
		Запрос.УстановитьПараметр("ТипРасчетов",                ТипРасчетов);
		Запрос.УстановитьПараметр("ДатаНачалаПересчета",        СтрокаОсновныхПараметровРасчета.ДатаНачалаПересчета);
		
		СтрокиОтгрузок = ДанныеДляРаспределения.НайтиСтроки(Новый Структура("ЭтоОплата",Ложь));
		СтрокиОстатков = ТаблицаОстатковВзаиморасчетов.НайтиСтроки(Новый Структура("Предоплата",0));
		
		НужнаПереоценка = (СтрокаОсновныхПараметровРасчета.ВалютаРасчетов <> СтрокаОсновныхПараметровРасчета.ВалютаРегламентированногоУчета 
			ИЛИ СтрокаОсновныхПараметровРасчета.ВалютаРасчетов <> ВалютаУправленческогоУчета)
			И (СтрокиОтгрузок.Количество() > 0 ИЛИ СтрокиОстатков.Количество() > 0)
			И НЕ СтрокаОсновныхПараметровРасчета.НачальноеЗаполнение;
		ГлобальныеПеременные.Вставить("НужнаПереоценка",НужнаПереоценка);
		Если НужнаПереоценка Тогда
			
			ТаблицаКурсовВалют = ПолучитьКурсыВалют(Запрос, ДатаНачалаКурсов, ГлобальныеПеременные);
			ГлобальныеПеременные.Вставить("ТаблицаКурсовВалют", ТаблицаКурсовВалют);
			
			ДатаНачалаКурсовыхРазниц = Мин(?(СтрокиОтгрузок.Количество() > 0, СтрокиОтгрузок[0].ДатаВозникновения, ТекущаяДатаСеанса()),
				?(СтрокиОстатков.Количество() > 0, СтрокиОстатков[0].ДатаВозникновения, ТекущаяДатаСеанса()));
			ДатаОкончанияКурсовыхРазниц = Макс(?(СтрокиОтгрузок.Количество() > 0, СтрокиОтгрузок[СтрокиОтгрузок.Количество()-1].ДатаВозникновения, ТекущаяДатаСеанса()),ТекущаяДатаСеанса());
			
			ДокументыРасчетаКурсовыхРазниц = Документы.РасчетКурсовыхРазниц.ДокументыПереоценки(СтрокаОсновныхПараметровРасчета.Организация,
				ДатаНачалаКурсовыхРазниц,
				ДатаОкончанияКурсовыхРазниц,
				ГлобальныеПеременные.ЭтоРасчетыСКлиентами);
				
			ГлобальныеПеременные.Вставить("ДокументыРасчетаКурсовыхРазниц", ДокументыРасчетаКурсовыхРазниц);
			
		КонецЕсли;
		
		ДанныеДляРаспределения.Индексы.Добавить("Регистратор");
		
		#КонецОбласти
		
		#Область РасчетПлановыхГрафиков
		
		ОстаткиПлановыхОтгрузокДляУточненияДатПогашенияАвансов = ОстаткиПлановыхОтгрузок.Скопировать();
		ОстаткиПлановыхОтгрузокДляУточненияДатПогашенияАвансов.Свернуть("ДатаПлановогоПогашения","Сумма,СуммаНераспределенныйАванс");
		ДвиженияПлановыхОтгрузок = ДвиженияПлановыхОтгрузокДляСуммирования.Скопировать();
		ДвиженияПлановыхОтгрузок.Свернуть("ПорядокОперации,ДатаПлановогоПогашения","Сумма");
		
		#Область ДвиженияПредоплатыДляСуммирования
		Сальдо = ТаблицаОстатковВзаиморасчетов.Итог("Предоплата") - ТаблицаОстатковВзаиморасчетов.Итог("Долг");
		Для Каждого СтрокаРасчетов Из ДанныеДляРаспределения Цикл
			Если СтрокаРасчетов.ЭтоОплата Тогда
				Если Сальдо + СтрокаРасчетов.Сумма > 0 Тогда
					СуммаДвижения = ?(Сальдо >= 0,СтрокаРасчетов.Сумма, Сальдо + СтрокаРасчетов.Сумма);
					НовСтр = ДвиженияПредоплатыДляСуммирования.Добавить();
					НовСтр.ДатаДвижения       = НачалоДня(СтрокаРасчетов.Период);
					НовСтр.Сумма             = СуммаДвижения;
					НовСтр.ПорядокОперации   = СтрокаРасчетов.ПорядокОперации;
					НовСтр.Регистратор       = СтрокаРасчетов.Регистратор;
					НовСтр.Просуммировано    = ЛОЖЬ;
				КонецЕсли;
				Сальдо = Сальдо + СтрокаРасчетов.Сумма;
			ИначеЕсли НЕ СтрокаРасчетов.ЭтоОплата Тогда
				Если Сальдо > 0 Тогда
					СуммаДвижения = ?(Сальдо - СтрокаРасчетов.Сумма >= 0,СтрокаРасчетов.Сумма, Сальдо);
					НовСтр = ДвиженияПредоплатыДляСуммирования.Добавить();
					НовСтр.ДатаДвижения       = НачалоДня(СтрокаРасчетов.Период);
					НовСтр.Сумма             = -СуммаДвижения;
					НовСтр.ПорядокОперации   = СтрокаРасчетов.ПорядокОперации;
					НовСтр.Регистратор       = СтрокаРасчетов.Регистратор;
					НовСтр.Просуммировано    = ЛОЖЬ;
				КонецЕсли;
				Сальдо = Сальдо - СтрокаРасчетов.Сумма;
			КонецЕсли;
		КонецЦикла;
		ДвиженияПредоплатыДляСуммирования.Сортировать("ДатаДвижения ВОЗР,ПорядокОперации ВОЗР");
		
		ДвиженияПредоплатыДляРаспределенияПоГрафикуОплаты = ДвиженияПредоплатыДляСуммирования.Скопировать();
		ДвиженияПредоплатыДляРаспределенияПоГрафикуОплаты.Свернуть("ДатаДвижения,Регистратор,ПорядокОперации","Сумма");
		ДвиженияПредоплатыДляРаспределенияПоГрафикуОплаты.Сортировать("ДатаДвижения УБЫВ");
		
		#КонецОбласти
			
			// 1. Определяем список дат, на конец которых нужно сделать перераспределение неиспользованных авансов.
			#Область СписокДатДляДвижений
			СписокДатДвиженийПредоплаты = ДвиженияПредоплатыДляСуммирования.Скопировать(Новый Структура("Просуммировано",Ложь),"ДатаДвижения").ВыгрузитьКолонку("ДатаДвижения");
			СписокДатДвиженийПлановыхОплат = ДвиженияПлановыхОплатДляСуммирования.Скопировать(,"Дата").ВыгрузитьКолонку("Дата");
			СписокДатДвиженийПлановыхОтгрузок = ДвиженияПлановыхОтгрузокДляСуммирования.Скопировать(,"Дата").ВыгрузитьКолонку("Дата");
			СписокДатУточненийОплат = УточненияГрафиковОплат.Скопировать(,"ДатаВозникновения").ВыгрузитьКолонку("ДатаВозникновения");
			СписокДатУточненийОтгрузок = ЗакрытияГрафиковОтгрузок.Скопировать(,"ДатаВозникновения").ВыгрузитьКолонку("ДатаВозникновения");
			СписокДатИсходныеРегистраторы = ИсходныеРегистраторы.Скопировать(,"НовыйПериодНачалоДня").ВыгрузитьКолонку("НовыйПериодНачалоДня");
			
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокДатДвиженийПредоплаты, СписокДатДвиженийПлановыхОплат);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокДатДвиженийПредоплаты, СписокДатДвиженийПлановыхОтгрузок);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокДатДвиженийПредоплаты, СписокДатУточненийОплат);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокДатДвиженийПредоплаты, СписокДатУточненийОтгрузок);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокДатДвиженийПредоплаты, СписокДатИсходныеРегистраторы);
			МассивДат = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СписокДатДвиженийПредоплаты);
			СписокДат = Новый СписокЗначений;
			СписокДат.ЗагрузитьЗначения(МассивДат);
			СписокДат.СортироватьПоЗначению(НаправлениеСортировки.Возр);
			#КонецОбласти
			
			Для Каждого ЗначениеСписка Из СписокДат Цикл
				
				ИсходныеРегистраторыПоТекущейДате = ИсходныеРегистраторы.НайтиСтроки(Новый Структура("НовыйПериодНачалоДня", ЗначениеСписка.Значение));
				
				// 2. Добавляем приходы по заказам и свернем остатки плановых оплат и отгрузок
				#Область ДобавлениеПриходныхДвиженийПлановыхОплатИСторно
				Пока ДвиженияПлановыхОплатДляСуммирования.Количество() > 0 Цикл
					Строка = ДвиженияПлановыхОплатДляСуммирования[0];
					Если Строка.Дата <= ЗначениеСписка.Значение Тогда
						НовСтр = ОстаткиПлановыхОплат.Добавить();
						НовСтр.ДокументПлан = Строка.Регистратор;
						НовСтр.ДатаВозникновения = Строка.ДатаВозникновения;
						НовСтр.ПорядокОперации = Строка.ПорядокОперации;
						НовСтр.ДатаПлановогоПогашения = Строка.ДатаПлановогоПогашения;
						НовСтр.ВариантОплаты = Строка.ВариантОплаты;
						НовСтр.КОплате = Строка.Сумма;
						НовСтр.КОплатеНераспределенныйАванс = 0;
						НовСтр.ЕстьКОплатеНераспределенныйАванс = Ложь;
						
						ДвиженияПлановыхОплатДляСуммирования.Удалить(0);
					Иначе
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Для Каждого СтрокаИсходногоРегистратора Из ИсходныеРегистраторыПоТекущейДате Цикл
					// Сторнируем только запись с Нераспределенный аванс = Ложь, т.к. с Истина пересчитается автоматически ниже.
					ДвиженияИсходногоДокументаПланОплат = ДвиженияИсходныхРегистраторовПланОплат.Скопировать(Новый Структура("ДокументРегистратор,НераспределенныйАванс", СтрокаИсходногоРегистратора.СторнируемыйРегистратор, ЛОЖЬ));
					ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаПланОплат.Скопировать(Новый Структура("ДокументРегистратор,ЗаписьДоНачалаРасчета,НераспределенныйАванс", СтрокаИсходногоРегистратора.СторнируемыйРегистратор,Неопределено, ЛОЖЬ)),
															ДвиженияИсходногоДокументаПланОплат);
					// Движения сторно (с минусом)
					Для Каждого ДвижениеИсходногоДокумента Из ДвиженияИсходногоДокументаПланОплат Цикл
						СтрокаСторно = ТаблицаПланОплат.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаСторно,ДвижениеИсходногоДокумента);
						СтрокаСторно.ДокументРегистратор = СтрокаИсходногоРегистратора.НовыйРегистратор;
						СтрокаСторно.Период              = СтрокаИсходногоРегистратора.НовыйПериод;
						СтрокаСторно.ПорядокОперации     = СтрокаИсходногоРегистратора.НовыйПорядокОперации;
						СтрокаСторно.КОплате             = -ДвижениеИсходногоДокумента.КОплате;
						СтрокаСторно.Сторно              = Истина;
					КонецЦикла;
					// Приходные движения по остаткам графика
					Для Каждого СтрокаИсходныхДвижений Из ДвиженияИсходногоДокументаПланОплат Цикл
						// Если отсторнировалось закрытие графика накладной - график нужно вернуть в остатки.
						НовСтр = ОстаткиПлановыхОплат.Добавить();
						НовСтр.ПорядокОперации = СтрокаИсходногоРегистратора.НовыйПорядокОперации;
						НовСтр.ДокументПлан = СтрокаИсходныхДвижений.ДокументПлан;
						НовСтр.ДатаВозникновения = СтрокаИсходныхДвижений.ДатаВозникновения;
						НовСтр.ДатаПлановогоПогашения = СтрокаИсходныхДвижений.ДатаПлановогоПогашения;
						НовСтр.ВариантОплаты = СтрокаИсходныхДвижений.ВариантОплаты;
						НовСтр.КОплате = СтрокаИсходныхДвижений.КОплате;
						НовСтр.КОплатеНераспределенныйАванс = 0;
					КонецЦикла;
				КонецЦикла;
				
				ОстаткиПлановыхОплат.Сортировать("ДатаПлановогоПогашения,ДатаВозникновения,ПорядокОперации,ВариантОплаты", Новый СравнениеЗначений);
				#КонецОбласти
				
				#Область ДобавлениеПриходныхДвиженийПлановыхОтгрузокИСторно
				Пока ДвиженияПлановыхОтгрузокДляСуммирования.Количество() > 0 Цикл
					Строка = ДвиженияПлановыхОтгрузокДляСуммирования[0];
					Если Строка.Дата <= ЗначениеСписка.Значение Тогда
						НовСтр = ОстаткиПлановыхОтгрузок.Добавить();
						НовСтр.ДокументПлан = Строка.Регистратор;
						НовСтр.ДатаВозникновения = Строка.ДатаВозникновения;
						НовСтр.ПорядокОперации = Строка.ПорядокОперации;
						НовСтр.ДатаПлановогоПогашения = Строка.ДатаПлановогоПогашения;
						НовСтр.Сумма = Строка.Сумма;
						НовСтр.СуммаНераспределенныйАванс = 0;
						НовСтр.ЕстьСуммаНераспределенныйАванс = Ложь;
						
						ДвиженияПлановыхОтгрузокДляСуммирования.Удалить(0);
					Иначе
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Для Каждого СтрокаИсходногоРегистратора Из ИсходныеРегистраторыПоТекущейДате Цикл
					ДвиженияИсходногоДокументаПланОтгрузок = ДвиженияИсходныхРегистраторовПланОтгрузок.Скопировать(Новый Структура("ДокументРегистратор,НераспределенныйАванс", СтрокаИсходногоРегистратора.СторнируемыйРегистратор,ЛОЖЬ));
					ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаПланОтгрузокПоставок.Скопировать(Новый Структура("ДокументРегистратор,ЗаписьДоНачалаРасчета,НераспределенныйАванс", СтрокаИсходногоРегистратора.СторнируемыйРегистратор,Неопределено,ЛОЖЬ)),
						ДвиженияИсходногоДокументаПланОтгрузок);
					// Движения сторно (с минусом)
					Для Каждого ДвижениеИсходногоДокумента Из ДвиженияИсходногоДокументаПланОтгрузок Цикл
						СтрокаСторно = ТаблицаПланОтгрузокПоставок.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаСторно,ДвижениеИсходногоДокумента);
						СтрокаСторно.ДокументРегистратор = СтрокаИсходногоРегистратора.НовыйРегистратор;
						СтрокаСторно.Период              = СтрокаИсходногоРегистратора.НовыйПериод;
						СтрокаСторно.ПорядокОперации     = СтрокаИсходногоРегистратора.НовыйПорядокОперации;
						
						СтрокаСторно.Сумма = -ДвижениеИсходногоДокумента.Сумма;
						СтрокаСторно.Сторно = Истина;
					КонецЦикла;
					// Приходные движения по остаткам графика
					Для Каждого СтрокаИсходныхДвижений Из ДвиженияИсходногоДокументаПланОтгрузок Цикл
						НовСтр = ОстаткиПлановыхОтгрузок.Добавить();
						НовСтр.ДокументПлан = СтрокаИсходныхДвижений.ДокументПлан;
						НовСтр.ДатаВозникновения = СтрокаИсходныхДвижений.ДатаВозникновения;
						НовСтр.ПорядокОперации = СтрокаИсходногоРегистратора.НовыйПорядокОперации;
						НовСтр.ДатаПлановогоПогашения = СтрокаИсходныхДвижений.ДатаПлановогоПогашения;
						НовСтр.Сумма = СтрокаИсходныхДвижений.Сумма;
						НовСтр.СуммаНераспределенныйАванс = 0;
					КонецЦикла;
				КонецЦикла;
				
				ОстаткиПлановыхОтгрузок.Сортировать("ДатаПлановогоПогашения,ДатаВозникновения");
				#КонецОбласти
				
				// 3. Добавляем уточнения графика оплат по заказам и сторнируем записи нераспределенных авансов, если они были.
				#Область РаспределениеФактическойОтгрузкиНаПлановыеОплаты
				
				счУточнения = 0;
				Пока счУточнения < УточненияГрафиковОплат.Количество() Цикл
					СтрокаУточнения = УточненияГрафиковОплат[счУточнения];
					Если СтрокаУточнения.ДатаВозникновения > ЗначениеСписка.Значение Тогда
						Прервать;
					КонецЕсли;
						
					// С учетом варианта оплаты
					СтрокиОстатков = ОстаткиПлановыхОплат.НайтиСтроки(Новый Структура("ДокументПлан,ВариантОплаты",СтрокаУточнения.Заказ,СтрокаУточнения.ВариантОплаты));
					Для Каждого СтрокаОстатков Из СтрокиОстатков Цикл
						Если СтрокаУточнения.Сумма = 0 Тогда
							Прервать;
						КонецЕсли;
						СуммаСписания = Мин(СтрокаОстатков.КОплате, СтрокаУточнения.Сумма);
						УменьшитьПланОплатыПоЗаказу(ТаблицаПланОплат, СтрокаОстатков, СтрокаУточнения, СуммаСписания);
						// Если не весь нераспределенный аванс уменьшили
						Если СуммаСписания > 0 Тогда
							СтрокиОстатковНераспределенногоАванса = ОстаткиПлановыхОплат.НайтиСтроки(Новый Структура("ЕстьКОплатеНераспределенныйАванс",Истина));
							Для Каждого СтрокаОстаткаНераспределенногоАванса Из СтрокиОстатковНераспределенногоАванса Цикл
								Если СуммаСписания = 0 Тогда
									Прервать;
								КонецЕсли;
								Если СтрокаОстаткаНераспределенногоАванса.КОплатеНераспределенныйАванс < 0 Тогда
									СуммаСторнирования = Макс(-СуммаСписания, СтрокаОстаткаНераспределенногоАванса.КОплатеНераспределенныйАванс);
									СторнироватьНераспределенныйАванс(ТаблицаПланОплат, СтрокаОстаткаНераспределенногоАванса, СтрокаУточнения, СуммаСторнирования);
									СуммаСписания = СуммаСписания + СуммаСторнирования;
								КонецЕсли;
							КонецЦикла;
						КонецЕсли;
					КонецЦикла;
					
					// Без учета варианта оплаты
					Если СтрокаУточнения.Сумма > 0 Тогда
						СтрокиОстатков = ОстаткиПлановыхОплат.НайтиСтроки(Новый Структура("ДокументПлан",СтрокаУточнения.Заказ));
						Для Каждого СтрокаОстатков Из СтрокиОстатков Цикл
							Если СтрокаУточнения.Сумма = 0 Тогда
								Прервать;
							КонецЕсли;
							СуммаСписания = Мин(СтрокаОстатков.КОплате, СтрокаУточнения.Сумма);
							УменьшитьПланОплатыПоЗаказу(ТаблицаПланОплат, СтрокаОстатков, СтрокаУточнения, СуммаСписания);
							// Если не весь нераспределенный аванс уменьшили
							Если СуммаСписания > 0 Тогда
								СтрокиОстатковНераспределенногоАванса = ОстаткиПлановыхОплат.НайтиСтроки(Новый Структура("ЕстьКОплатеНераспределенныйАванс",Истина));
								Для Каждого СтрокаОстаткаНераспределенногоАванса Из СтрокиОстатковНераспределенногоАванса Цикл
									Если СуммаСписания = 0 Тогда
										Прервать;
									КонецЕсли;
									Если СтрокаОстаткаНераспределенногоАванса.КОплатеНераспределенныйАванс < 0 Тогда
										СуммаСторнирования = Макс(-СуммаСписания, СтрокаОстаткаНераспределенногоАванса.КОплатеНераспределенныйАванс);
										СторнироватьНераспределенныйАванс(ТаблицаПланОплат, СтрокаОстаткаНераспределенногоАванса, СтрокаУточнения, СуммаСторнирования);
										СуммаСписания = СуммаСписания + СуммаСторнирования;
									КонецЕсли;
								КонецЦикла;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
						
					Если СтрокаУточнения.Сумма = 0 Тогда
						УточненияГрафиковОплат.Удалить(счУточнения);
					Иначе
						счУточнения = счУточнения + 1;
					КонецЕсли;
				КонецЦикла;
				#КонецОбласти
				
				// 4. Добавляем закрытие графика отгрузок по заказам и сторнируем записи нераспределенных авансов, если они были.
				#Область РаспределениеФактическойОтгрузкиНаПлановыеОтгрузки
				счЗакрытия = 0;
				Пока счЗакрытия < ЗакрытияГрафиковОтгрузок.Количество() Цикл
					СтрокаЗакрытия = ЗакрытияГрафиковОтгрузок[счЗакрытия];
					Если СтрокаЗакрытия.ДатаВозникновения > ЗначениеСписка.Значение Тогда
						Прервать;
					Иначе
						СтрокиОстатков = ОстаткиПлановыхОтгрузок.НайтиСтроки(Новый Структура("ДокументПлан",СтрокаЗакрытия.Заказ));
						Для Каждого СтрокаОстатков Из СтрокиОстатков Цикл
							Если СтрокаЗакрытия.Сумма = 0 Тогда
								Прервать;
							КонецЕсли;
							СуммаСписания = Мин(СтрокаОстатков.Сумма,СтрокаЗакрытия.Сумма);
							УменьшитьПланОтгрузкиПоЗаказу(ТаблицаПланОтгрузокПоставок, СтрокаОстатков, СтрокаЗакрытия, СуммаСписания);
							// Если не весь нераспределенный аванс уменьшили
							Если СуммаСписания > 0 Тогда
								СтрокиОстатковНераспределенногоАванса = ОстаткиПлановыхОтгрузок.НайтиСтроки(Новый Структура("ЕстьСуммаНераспределенныйАванс",Истина));
								Для Каждого СтрокаОстаткаНераспределенногоАванса Из СтрокиОстатковНераспределенногоАванса Цикл
									Если СуммаСписания = 0 Тогда
										Прервать;
									КонецЕсли;
									Если СтрокаОстаткаНераспределенногоАванса.СуммаНераспределенныйАванс < 0 Тогда
										СуммаСторнирования = Макс(-СуммаСписания, СтрокаОстаткаНераспределенногоАванса.СуммаНераспределенныйАванс);
										СторнироватьНераспределенныйАвансПланаОтгрузок(ТаблицаПланОтгрузокПоставок, СтрокаОстаткаНераспределенногоАванса, СтрокаЗакрытия, СуммаСторнирования);
										СуммаСписания = СуммаСписания + СуммаСторнирования;
									КонецЕсли;
								КонецЦикла;
							КонецЕсли;
						КонецЦикла;
						
					КонецЕсли;
					Если СтрокаЗакрытия.Сумма = 0 Тогда
						ЗакрытияГрафиковОтгрузок.Удалить(счЗакрытия);
					Иначе
						счЗакрытия = счЗакрытия + 1;
					КонецЕсли;
				КонецЦикла;
				#КонецОбласти
				
				// 5. Обновим остаток предоплаты, что бы понять сколько нужно перераспределить авансов.
				#Область РасчетОстаткаАвансов
				Для Каждого Строка Из ДвиженияПредоплатыДляСуммирования Цикл
					Если Строка.ДатаДвижения > ЗначениеСписка.Значение Тогда
						Прервать;
					КонецЕсли;
					Если НЕ Строка.Просуммировано Тогда
						Строка.Просуммировано         = Истина;
						ОстатокАвансов = ОстатокАвансов + Строка.Сумма;
					КонецЕсли;
				КонецЦикла;
				ДвиженияПредоплатыДляСуммированияПланОтгрузок = ДвиженияПредоплатыДляРаспределенияПоГрафикуОплаты.Скопировать();
				#КонецОбласти
				
				// Дельта авансов = Остаток авансов на конец дня - уже добавленные строки Нераспределенного аванса.
				СуммаАвансовДляРаспределенияПоГрафикуОплат = ОстатокАвансов + ОстаткиПлановыхОплат.Итог("КОплатеНераспределенныйАванс");
				СуммаАвансовДляРаспределенияПоГрафикуОтгрузок = ОстатокАвансов + ОстаткиПлановыхОтгрузок.Итог("СуммаНераспределенныйАванс");
				
				// 6. Распределение дельты суммы авансов за день по графикам плановой оплаты (движения с флагом "НераспределенныйАванс")
				#Область РаспределениеАвансовПоГрафикуОплаты
				// Если дельта положительная (сумма авансов увеличилась) то смотрим остатки с учетом уже зачтенных нераспределенных авансов (НераспределенныйАванс = Истина или Ложь)
				Если СуммаАвансовДляРаспределенияПоГрафикуОплат > 0 Тогда
					Для Каждого СтрокаОстатков Из ОстаткиПлановыхОплат Цикл
						Если СуммаАвансовДляРаспределенияПоГрафикуОплат = 0 Тогда
							Прервать;
						КонецЕсли;
						Если (СтрокаОстатков.КОплате + СтрокаОстатков.КОплатеНераспределенныйАванс) <= 0 Тогда
							Продолжить;
						КонецЕсли;
						Для Каждого ДвижениеПредоплаты Из ДвиженияПредоплатыДляРаспределенияПоГрафикуОплаты Цикл
							СуммаСписания = Мин(СуммаАвансовДляРаспределенияПоГрафикуОплат,ДвижениеПредоплаты.Сумма,СтрокаОстатков.КОплате + СтрокаОстатков.КОплатеНераспределенныйАванс);
							Если СуммаСписания <= 0 ИЛИ ДвижениеПредоплаты.ДатаДвижения > ЗначениеСписка.Значение Тогда
								Продолжить;
							КонецЕсли;
							
							УменьшитьПланОплатыПоЗаказу(ТаблицаПланОплат, СтрокаОстатков, ДвижениеПредоплаты, СуммаСписания, КонецДня(ЗначениеСписка.Значение));
							СуммаАвансовДляРаспределенияПоГрафикуОплат = СуммаАвансовДляРаспределенияПоГрафикуОплат - СуммаСписания;
							
						КонецЦикла;
					КонецЦикла;
				// Если дельта отрицательная то нужно уменьшить только зачтенные нераспределенные авансы (НераспределенныйАванс = Истина)
				ИначеЕсли СуммаАвансовДляРаспределенияПоГрафикуОплат < 0 Тогда
					сч = ОстаткиПлановыхОплат.Количество()-1;
					Пока сч >= 0 Цикл
						СтрокаОстатков = ОстаткиПлановыхОплат[сч];
						Если СуммаАвансовДляРаспределенияПоГрафикуОплат = 0 Тогда
							Прервать;
						КонецЕсли;
						Если СтрокаОстатков.КОплатеНераспределенныйАванс = 0 Тогда
							сч = сч -1;
							Продолжить;
						КонецЕсли;
						Для Каждого ДвижениеПредоплаты Из ДвиженияПредоплатыДляРаспределенияПоГрафикуОплаты Цикл
							СуммаСписания = -Мин(-СуммаАвансовДляРаспределенияПоГрафикуОплат,-ДвижениеПредоплаты.Сумма,-СтрокаОстатков.КОплатеНераспределенныйАванс);
							Если ДвижениеПредоплаты.ДатаДвижения > ЗначениеСписка.Значение ИЛИ СуммаСписания >= 0 Тогда
								Продолжить;
							КонецЕсли;
								
							СторнироватьНераспределенныйАванс(ТаблицаПланОплат, СтрокаОстатков, ДвижениеПредоплаты, СуммаСписания, КонецДня(ЗначениеСписка.Значение));
							СуммаАвансовДляРаспределенияПоГрафикуОплат = СуммаАвансовДляРаспределенияПоГрафикуОплат - СуммаСписания;
								
						КонецЦикла;
						сч = сч -1;
					КонецЦикла;
				КонецЕсли;
				#КонецОбласти
				
				// 7. Распределение дельты суммы авансов за день по графикам плановой отгрузки (движения с флагом "НераспределенныйАванс")
				#Область РаспределениеАвансовПоГрафикуОтгрузки
				// Если дельта положительная (сумма авансов увеличилась) то смотрим остатки с учетом уже зачтенных нераспределенных авансов (НераспределенныйАванс = Истина или Ложь)
				Если СуммаАвансовДляРаспределенияПоГрафикуОтгрузок > 0 Тогда
					Для Каждого СтрокаОстатков Из ОстаткиПлановыхОтгрузок Цикл
						Если СуммаАвансовДляРаспределенияПоГрафикуОтгрузок = 0 Тогда
							Прервать;
						КонецЕсли;
						Если (СтрокаОстатков.Сумма + СтрокаОстатков.СуммаНераспределенныйАванс) <= 0 Тогда
							Продолжить;
						КонецЕсли;
						Для Каждого ДвижениеПредоплаты Из ДвиженияПредоплатыДляСуммированияПланОтгрузок Цикл
							Если ДвижениеПредоплаты.ДатаДвижения = ЗначениеСписка.Значение //Подбираем движения текущего дня, ведь именно они изменили Остаток аванса
								И ДвижениеПредоплаты.ДатаДвижения >= СтрокаОстатков.ДатаВозникновения //График отгрузок уточняют только предоплаты введенные после заказа.
								И ДвижениеПредоплаты.Сумма > 0
								И СуммаАвансовДляРаспределенияПоГрафикуОтгрузок > 0 Тогда
								
								СуммаСписания = Мин(СуммаАвансовДляРаспределенияПоГрафикуОтгрузок,ДвижениеПредоплаты.Сумма,СтрокаОстатков.Сумма + СтрокаОстатков.СуммаНераспределенныйАванс);
								
								УменьшитьПланОтгрузкиПоЗаказу(ТаблицаПланОтгрузокПоставок, СтрокаОстатков, ДвижениеПредоплаты, СуммаСписания, КонецДня(ЗначениеСписка.Значение));
								
								СуммаАвансовДляРаспределенияПоГрафикуОтгрузок = СуммаАвансовДляРаспределенияПоГрафикуОтгрузок - СуммаСписания;
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				// Если дельта отрицательная то нужно уменьшить только зачтенные нераспределенные авансы (НераспределенныйАванс = Истина)
				ИначеЕсли СуммаАвансовДляРаспределенияПоГрафикуОтгрузок < 0 Тогда
					сч = ОстаткиПлановыхОтгрузок.Количество()-1;
					Пока сч >= 0 Цикл
						СтрокаОстатков = ОстаткиПлановыхОтгрузок[сч];
						Если СуммаАвансовДляРаспределенияПоГрафикуОтгрузок = 0 Тогда
							Прервать;
						КонецЕсли;
						Если СтрокаОстатков.СуммаНераспределенныйАванс = 0 Тогда
							сч = сч -1;
							Продолжить;
						КонецЕсли;
						Для Каждого ДвижениеПредоплаты Из ДвиженияПредоплатыДляСуммированияПланОтгрузок Цикл
							Если ДвижениеПредоплаты.ДатаДвижения = ЗначениеСписка.Значение
								И ДвижениеПредоплаты.Сумма < 0
								И СуммаАвансовДляРаспределенияПоГрафикуОтгрузок < 0 Тогда
								СуммаСписания = -Мин(-СуммаАвансовДляРаспределенияПоГрафикуОтгрузок,-ДвижениеПредоплаты.Сумма,-СтрокаОстатков.СуммаНераспределенныйАванс);
								Если ДвижениеПредоплаты.ДатаДвижения > ЗначениеСписка.Значение ИЛИ СуммаСписания >= 0 Тогда
									Продолжить;
								КонецЕсли;
								
								СторнироватьНераспределенныйАвансПланаОтгрузок(ТаблицаПланОтгрузокПоставок, СтрокаОстатков, ДвижениеПредоплаты, СуммаСписания, КонецДня(ЗначениеСписка.Значение));
								СуммаАвансовДляРаспределенияПоГрафикуОтгрузок = СуммаАвансовДляРаспределенияПоГрафикуОтгрузок - СуммаСписания;
								
							КонецЕсли;
						КонецЦикла;
						сч = сч -1;
					КонецЦикла;
				КонецЕсли;
				#КонецОбласти
				
				// 8. Удаление учтенных строк движений по предоплате
				#Область УдалениеУстаревшихСтрок
				сч = 0;
				Пока сч < ДвиженияПредоплатыДляРаспределенияПоГрафикуОплаты.Количество() Цикл
					Строка = ДвиженияПредоплатыДляРаспределенияПоГрафикуОплаты[сч];
					Если Строка.ДатаДвижения <= ЗначениеСписка.Значение И Строка.Сумма = 0 Тогда
						ДвиженияПредоплатыДляРаспределенияПоГрафикуОплаты.Удалить(сч);
					Иначе
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				#КонецОбласти
				
			КонецЦикла;
			
		#КонецОбласти

		#Область РаспределениеОплатНаНакладные
		Для Каждого СтрокаРасчетов Из ДанныеДляРаспределения Цикл
			
			ТаблицаОстатковВзаиморасчетов.Свернуть("РасчетныйДокумент,ДатаПлановогоПогашения,ДатаВозникновения,ПорядокОперации,ПорядокЗачетаПоДатеПлатежа,ДатаПереоценки","Долг,ДолгРегл,ДолгУпр,Предоплата,ПредоплатаРегл,ПредоплатаУпр");
			ПустыеСтроки = ТаблицаОстатковВзаиморасчетов.НайтиСтроки(Новый Структура("Предоплата,Долг",0,0));
			Для Каждого ПустаяСтрока Из ПустыеСтроки Цикл
				ТаблицаОстатковВзаиморасчетов.Удалить(ПустаяСтрока);
			КонецЦикла;
			
			Если СтрокаРасчетов.Сторно Тогда
			#Область Сторно
				ДобавитьСторноЗаписи(ГлобальныеПеременные,СтрокаРасчетов,ТаблицаРасчетовПоСрокам,ДвиженияИсходныхРегистраторов,ИсходныеРегистраторы,ТаблицаОстатковВзаиморасчетов);
				Если ТипЗнч(СтрокаРасчетов.Регистратор) = Тип("ДокументСсылка.Сторно") Тогда
					ДобавитьПочиночныеДвиженияПриНеобходимости(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаРасчетов);
					СписатьРеглУпрПоНулевымОстаткам(ГлобальныеПеременные,ТаблицаРасчетовПоСрокам,ТаблицаОстатковВзаиморасчетов,НачалоДня(СтрокаРасчетов.Период));
				КонецЕсли;
				Продолжить;
			#КонецОбласти
			КонецЕсли;
			
			Если СтрокаРасчетов.ЭтоОплата Тогда
				
				Если СтрокаРасчетов.НовыеДвиженияИсправления Тогда
				#Область НовыеДвиженияИсправления
					ПодходящиеСтроки = ТаблицаОстатковВзаиморасчетов.Скопировать(Новый Структура("Предоплата,РасчетныйДокумент", 0, СтрокаРасчетов.Регистратор));
					Для Каждого ПодходящаяСтрокаСторно Из ПодходящиеСтроки Цикл
						Если СтрокаРасчетов.Сумма = 0 Тогда
							Прервать;
						КонецЕсли;
						
						#Область ОпределениеСуммыСписания
						СуммаСписания = Мин(ПодходящаяСтрокаСторно.Долг,СтрокаРасчетов.Сумма);
						Если СуммаСписания = 0 Тогда
							Продолжить;
						КонецЕсли;
						СуммаСписанияРегл = СуммаСписания(0, ПодходящаяСтрокаСторно.ДолгРегл, ПодходящаяСтрокаСторно.Долг, СуммаСписания);
						СуммаСписанияУпр = СуммаСписания(0, ПодходящаяСтрокаСторно.ДолгУпр, ПодходящаяСтрокаСторно.Долг, СуммаСписания);
						#КонецОбласти
						
						#Область РасходДолг
						Если СтрокаРасчетов.ПериодЗачета >= ГлобальныеПеременные.ДатаНачалаПересчета Тогда
							ДобавитьРасходДолга(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, ПодходящаяСтрокаСторно, СтрокаРасчетов, СуммаСписания, СуммаСписанияРегл, СуммаСписанияУпр, ХозяйственныеОперации["ПогашениеЗадолженности"]);
						КонецЕсли;
						#КонецОбласти
						
						СтрокаРасчетов.Сумма       = СтрокаРасчетов.Сумма     - СуммаСписания;
						СтрокаРасчетов.СуммаРегл   = СтрокаРасчетов.СуммаРегл - СуммаСписанияРегл;
						СтрокаРасчетов.СуммаУпр    = СтрокаРасчетов.СуммаУпр  - СуммаСписанияУпр;
					КонецЦикла;
				#КонецОбласти
				КонецЕсли;
				
				Если ТипЗнч(СтрокаРасчетов.Регистратор) = Тип("ДокументСсылка.КорректировкаРеализации")
					Или ТипЗнч(СтрокаРасчетов.Регистратор) = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
				#Область КорректировкаНакладнойВМинус
					ОстаткиСвязанногоДокумента = ТаблицаОстатковВзаиморасчетов.НайтиСтроки(Новый Структура("Предоплата, РасчетныйДокумент", 0, СтрокаРасчетов.СвязанныйДокумент));
					Для Каждого СтрокаОстаткаСвязанногоДокумента Из ОстаткиСвязанногоДокумента Цикл
						Если СтрокаРасчетов.Сумма = 0 Тогда
							Прервать;
						КонецЕсли;
						Если СтрокаРасчетов.ПорядокОперации < СтрокаОстаткаСвязанногоДокумента.ПорядокОперации Тогда
							Продолжить;
						КонецЕсли;
						
						Если НужнаПереоценка Тогда
							ПереоценитьДолг(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаРасчетов.СвязанныйДокумент, СтрокаРасчетов.Период, Ложь);
						КонецЕсли;
						
						#Область ОпределениеСуммыСписания
						СуммаСписания = Мин(СтрокаРасчетов.Сумма, СтрокаОстаткаСвязанногоДокумента.Долг);
						Если СуммаСписания = 0 Тогда
							Продолжить;
						КонецЕсли;
						Если СтрокаРасчетов.РучнойКурсРегл
							Или ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент] = Неопределено Тогда
							СуммаКорректировкиРегл = Окр(СуммаСписания * СтрокаРасчетов.СуммаРегл / СтрокаРасчетов.Сумма,2);
						Иначе
							СуммаКорректировкиРегл = Окр(СуммаСписания
								* (ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент].СуммаДокументаРегл 
									/ ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент].СуммаДокумента),2);
						КонецЕсли;
								
						Если СтрокаРасчетов.РучнойКурсУпр
							Или ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент] = Неопределено Тогда
							СуммаКорректировкиУпр = Окр(СуммаСписания * СтрокаРасчетов.СуммаУпр / СтрокаРасчетов.Сумма,2);
						Иначе
							СуммаКорректировкиУпр = Окр(СуммаСписания
								* (ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент].СуммаДокументаУпр 
									/ ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент].СуммаДокумента),2);
						КонецЕсли;
						#КонецОбласти
						
						#Область РасходДолг
						ДобавитьРасходДолга(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаОстаткаСвязанногоДокумента, СтрокаРасчетов, СуммаСписания, СуммаКорректировкиРегл, СуммаКорректировкиУпр);
						#КонецОбласти
						
						СтрокаРасчетов.Сумма     = СтрокаРасчетов.Сумма     - СуммаСписания;
						СтрокаРасчетов.СуммаРегл = СтрокаРасчетов.СуммаРегл - СуммаКорректировкиРегл;
						СтрокаРасчетов.СуммаУпр  = СтрокаРасчетов.СуммаУпр  - СуммаКорректировкиУпр;
						
					КонецЦикла;
					
					Если СтрокаРасчетов.Сумма > 0 Тогда
						ПересчитатьСуммыЕслиЭтоКорректировка(ГлобальныеПеременные, СтрокаРасчетов);
					КонецЕсли;
					#КонецОбласти
				КонецЕсли;
				
				Если ТипЗнч(СтрокаРасчетов.Регистратор) = Тип("ДокументСсылка.КорректировкаГрафикаВзаиморасчетов") Тогда
					#Область КорректировкаГрафика
					Если СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КорректировкаГрафикаОплатыКлиентом
						ИЛИ СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КорректировкаГрафикаОплатыПоставщику Тогда
						ОстаткиРасчетногоДокумента = ТаблицаОстатковВзаиморасчетов.НайтиСтроки(Новый Структура("Предоплата, РасчетныйДокумент", 0, СтрокаРасчетов.РасчетныйДокумент));
						Для Каждого СтрокаРасчетногоСвязанногоДокумента Из ОстаткиРасчетногоДокумента Цикл
							Если СтрокаРасчетов.Сумма = 0 Тогда
								Прервать;
							КонецЕсли;
							
							Если НужнаПереоценка Тогда
								ПереоценитьДолг(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаРасчетов.РасчетныйДокумент, СтрокаРасчетов.Период, Ложь);
							КонецЕсли;
							
							#Область ОпределениеСуммыСписания
							СуммаСписания = Мин(СтрокаРасчетов.Сумма, СтрокаРасчетногоСвязанногоДокумента.Долг);
							Если СуммаСписания = 0 Тогда
								Продолжить;
							КонецЕсли;
							
							//Списываем долг текущему курсу долга, чтобы не возникало курсовых разниц.
							СуммаКорректировкиРегл = Окр(СуммаСписания * СтрокаРасчетногоСвязанногоДокумента.ДолгРегл / СтрокаРасчетногоСвязанногоДокумента.Долг,2);
							СуммаКорректировкиУпр = Окр(СуммаСписания * СтрокаРасчетногоСвязанногоДокумента.ДолгУпр / СтрокаРасчетногоСвязанногоДокумента.Долг,2);
							// Все суммы списаний корректировки графика нужно сложить, чтобы потом начислить такие-же суммы без образования курсовых разниц
							Если ГлобальныеПеременные.ДанныеКорректировокГрафиков[СтрокаРасчетов.Регистратор] = Неопределено Тогда
								ГлобальныеПеременные.ДанныеКорректировокГрафиков.Вставить(СтрокаРасчетов.Регистратор, СтруктураДанныхКорректировкиГрафика());
							КонецЕсли;
							ДанныеКорректировкиГрафика                 = ГлобальныеПеременные.ДанныеКорректировокГрафиков[СтрокаРасчетов.Регистратор];
							ДанныеКорректировкиГрафика.СуммаРасход     = ДанныеКорректировкиГрафика.СуммаРасход     + СуммаСписания;
							ДанныеКорректировкиГрафика.СуммаРасходРегл = ДанныеКорректировкиГрафика.СуммаРасходРегл + СуммаКорректировкиРегл;
							ДанныеКорректировкиГрафика.СуммаРасходУпр  = ДанныеКорректировкиГрафика.СуммаРасходУпр  + СуммаКорректировкиУпр;
							ДанныеКорректировкиГрафика.ДатаВозникновения = СтрокаРасчетногоСвязанногоДокумента.ДатаВозникновения;
							#КонецОбласти
							
							#Область РасходДолг
							ДобавитьРасходДолга(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаРасчетногоСвязанногоДокумента, СтрокаРасчетов, СуммаСписания, СуммаКорректировкиРегл, СуммаКорректировкиУпр);
							#КонецОбласти
							
							СтрокаРасчетов.Сумма     = СтрокаРасчетов.Сумма     - СуммаСписания;
							СтрокаРасчетов.СуммаРегл = СтрокаРасчетов.СуммаРегл - СуммаКорректировкиРегл;
							СтрокаРасчетов.СуммаУпр  = СтрокаРасчетов.СуммаУпр  - СуммаКорректировкиУпр;
						КонецЦикла;
						
						Если СтрокаРасчетов.Сумма > 0 Тогда
							СтрокаРасчетов.Сумма = 0;
						КонецЕсли;
						// Если еще не были добавлены суммы расхода, то приход не нужно добавлять, запишем пустую структуру
						Если ГлобальныеПеременные.ДанныеКорректировокГрафиков[СтрокаРасчетов.Регистратор] = Неопределено Тогда
							ГлобальныеПеременные.ДанныеКорректировокГрафиков.Вставить(СтрокаРасчетов.Регистратор, СтруктураДанныхКорректировкиГрафика());
						КонецЕсли;
					Иначе
						ЗаполнитьДанныеКорректировкиГрафика(ГлобальныеПеременные, СтрокаРасчетов.Регистратор, ДанныеДляРаспределения, СтрокаРасчетов.ЭтоОплата);
					КонецЕсли;
					#КонецОбласти
				КонецЕсли;
				
				Если СтрокаРасчетов.Сумма > 0 Тогда
					#Область ПогашениеЗадолженности
					ПодходящиеСтроки = ТаблицаОстатковВзаиморасчетов.Скопировать(Новый Структура("Предоплата", 0));
					Если СтрокаОсновныхПараметровРасчета.ЗачетОплатПоДатеПлатежа Тогда
						ПодходящиеСтроки.Сортировать("ДатаВозникновения ВОЗР, ПорядокЗачетаПоДатеПлатежа ВОЗР, ПорядокОперации ВОЗР");
					Иначе
						ПодходящиеСтроки.Сортировать("ДатаВозникновения ВОЗР, ПорядокОперации ВОЗР, ПорядокЗачетаПоДатеПлатежа ВОЗР");
					КонецЕсли;
					
					Для Каждого ПодходящаяСтрокаОтгрузки Из ПодходящиеСтроки Цикл
						Если СтрокаРасчетов.Сумма = 0 Тогда
							Прервать;
						КонецЕсли;
						
						Если НужнаПереоценка Тогда
							ПереоценитьДолг(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, ПодходящаяСтрокаОтгрузки.РасчетныйДокумент, СтрокаРасчетов.Период, Истина);
						КонецЕсли;
						
						#Область ОпределениеСуммыСписания
						СуммаСписания = Мин(ПодходящаяСтрокаОтгрузки.Долг,СтрокаРасчетов.Сумма);
						Если СуммаСписания = 0 Тогда
							Продолжить;
						КонецЕсли;
						
						//Списываем долг по курсу оплаты.
						СуммаСписанияРегл = СуммаСписания(0, СтрокаРасчетов.СуммаРегл, СтрокаРасчетов.Сумма, СуммаСписания);
						СуммаСписанияУпр = СуммаСписания(0, СтрокаРасчетов.СуммаУпр, СтрокаРасчетов.Сумма, СуммаСписания);
						#КонецОбласти
						
						#Область РасходДолг
						Если СтрокаРасчетов.ПериодЗачета >= ГлобальныеПеременные.ДатаНачалаПересчета Тогда
							Если ГлобальныеПеременные.ХозяйственныеОперацииПереноса.Найти(СтрокаРасчетов.ХозяйственнаяОперация) <> Неопределено Тогда
								ХозяйственнаяОперация         = СтрокаРасчетов.ХозяйственнаяОперация;
							Иначе
								ХозяйственнаяОперация         = ГлобальныеПеременные.ХозяйственныеОперации["ПогашениеЗадолженности"];
							КонецЕсли;
							ДобавитьРасходДолга(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, ПодходящаяСтрокаОтгрузки, СтрокаРасчетов, СуммаСписания, СуммаСписанияРегл, СуммаСписанияУпр, ХозяйственнаяОперация);
						КонецЕсли;
						#КонецОбласти
						
						СтрокаРасчетов.Сумма       = СтрокаРасчетов.Сумма     - СуммаСписания;
						СтрокаРасчетов.СуммаРегл   = СтрокаРасчетов.СуммаРегл - СуммаСписанияРегл;
						СтрокаРасчетов.СуммаУпр    = СтрокаРасчетов.СуммаУпр  - СуммаСписанияУпр;
					КонецЦикла;
					#КонецОбласти
				КонецЕсли;
					
				Если СтрокаРасчетов.Сумма > 0 Тогда
					
					#Область УточнениеДатПогашенияАвансов
						//Обновление остатков плановых отгрузок
						Пока ДвиженияПлановыхОтгрузок.Количество() > 0 Цикл
							Строка = ДвиженияПлановыхОтгрузок[0];
							Если Строка.ПорядокОперации < СтрокаРасчетов.ПорядокОперации Тогда
								НовСтр = ОстаткиПлановыхОтгрузокДляУточненияДатПогашенияАвансов.Добавить();
								НовСтр.ДатаПлановогоПогашения = Строка.ДатаПлановогоПогашения;
								НовСтр.Сумма = Строка.Сумма;
								НовСтр.СуммаНераспределенныйАванс = 0;
								ДвиженияПлановыхОтгрузок.Удалить(Строка);
							Иначе
								Прервать;
							КонецЕсли;
						КонецЦикла;
						
						ОстаткиПлановыхОтгрузокДляУточненияДатПогашенияАвансов.Свернуть("ДатаПлановогоПогашения","Сумма,СуммаНераспределенныйАванс");
						ОстаткиПлановыхОтгрузокДляУточненияДатПогашенияАвансов.Сортировать("ДатаПлановогоПогашения ВОЗР");
						КУточнению = СтрокаРасчетов.Сумма;
						Для Каждого СтрокаОстатков Из ОстаткиПлановыхОтгрузокДляУточненияДатПогашенияАвансов Цикл
							Если СтрокаОстатков.Сумма + СтрокаОстатков.СуммаНераспределенныйАванс <= 0 Тогда
								Продолжить;
							КонецЕсли;
							Если КУточнению = 0 Тогда
								Прервать;
							КонецЕсли;
							
							СуммаУточнения = Мин(СтрокаОстатков.Сумма + СтрокаОстатков.СуммаНераспределенныйАванс,КУточнению);
							Если СтрокаРасчетов.Сумма > СуммаУточнения Тогда
								СуммаУточненияРегл = СуммаСписания(0, СтрокаРасчетов.СуммаРегл, СтрокаРасчетов.Сумма, СуммаУточнения);
								СуммаУточненияУпр = СуммаСписания(0, СтрокаРасчетов.СуммаУпр, СтрокаРасчетов.Сумма, СуммаУточнения);
								Если НЕ ЗначениеЗаполнено(СтрокаРасчетов.ДатаПлановогоПогашения) Тогда
									НовСтр = ДанныеДляРаспределения.Вставить(ДанныеДляРаспределения.Индекс(СтрокаРасчетов)+1);
									ЗаполнитьЗначенияСвойств(НовСтр,СтрокаРасчетов);
									НовСтр.Сумма = СтрокаРасчетов.Сумма - СуммаУточнения;
									НовСтр.СуммаРегл= СтрокаРасчетов.СуммаРегл - СуммаУточненияРегл;
									НовСтр.СуммаУпр = СтрокаРасчетов.СуммаУпр - СуммаУточненияУпр;
									
									СтрокаРасчетов.Сумма = СуммаУточнения;
									СтрокаРасчетов.СуммаРегл = СуммаУточненияРегл;
									СтрокаРасчетов.СуммаУпр = СуммаУточненияУпр;
									СтрокаРасчетов.ДатаПлановогоПогашения = СтрокаОстатков.ДатаПлановогоПогашения;
									КУточнению = 0;
								Иначе
									КУточнению = КУточнению - СуммаУточнения;
								КонецЕсли;
								СтрокаОстатков.Сумма = СтрокаОстатков.Сумма - СуммаУточнения;
							Иначе
								Если НЕ ЗначениеЗаполнено(СтрокаРасчетов.ДатаПлановогоПогашения) Тогда
									СтрокаРасчетов.ДатаПлановогоПогашения = СтрокаОстатков.ДатаПлановогоПогашения;
								КонецЕсли;
								СтрокаОстатков.Сумма = СтрокаОстатков.Сумма - СтрокаРасчетов.Сумма;
								КУточнению = 0;
							КонецЕсли;
						КонецЦикла;
					#КонецОбласти
					
					#Область ПриходПредоплаты
					ДобавитьПриход(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаРасчетов, "Оплата");
					#КонецОбласти
				КонецЕсли;
				
			Иначе
				
				СтрокиДвиженийРасходаПланаОтгрузки = ТаблицаПланОтгрузокПоставок.НайтиСтроки(
					Новый Структура("ДокументРегистратор, УчтеноВОстаткахПлановыхОтгрузок,ВидДвижения",
						СтрокаРасчетов.Регистратор, Ложь, ВидДвиженияНакопления.Расход));
				Для Каждого СтрокаДвижений Из СтрокиДвиженийРасходаПланаОтгрузки Цикл
					НовСтр = ОстаткиПлановыхОтгрузокДляУточненияДатПогашенияАвансов.Добавить();
					НовСтр.ДатаПлановогоПогашения = СтрокаДвижений.ДатаПлановогоПогашения;
					НовСтр.Сумма = -СтрокаДвижений.Сумма;
					НовСтр.СуммаНераспределенныйАванс = 0;
					СтрокаДвижений.УчтеноВОстаткахПлановыхОтгрузок = Истина;
				КонецЦикла;
				
				Если СтрокаРасчетов.НовыеДвиженияИсправления Тогда
				#Область НовыеДвиженияИсправления
					ПодходящиеСтроки = ТаблицаОстатковВзаиморасчетов.Скопировать(Новый Структура("Долг,РасчетныйДокумент", 0, СтрокаРасчетов.Регистратор));
					Для Каждого ПодходящаяСтрокаСторно Из ПодходящиеСтроки Цикл
						Если СтрокаРасчетов.Сумма = 0 Тогда
							Прервать;
						КонецЕсли;
						
						#Область ОпределениеСуммыСписания
						СуммаСписания = Мин(ПодходящаяСтрокаСторно.Долг,СтрокаРасчетов.Сумма);
						Если СуммаСписания = 0 Тогда
							Продолжить;
						КонецЕсли;
						СуммаСписанияРегл = СуммаСписания(0, ПодходящаяСтрокаСторно.ДолгРегл, ПодходящаяСтрокаСторно.Долг, СуммаСписания);
						СуммаСписанияУпр = СуммаСписания(0, ПодходящаяСтрокаСторно.ДолгУпр, ПодходящаяСтрокаСторно.Долг, СуммаСписания);
						#КонецОбласти
						
						#Область РасходДолг
						Если СтрокаРасчетов.ПериодЗачета >= ГлобальныеПеременные.ДатаНачалаПересчета Тогда
							НовСтр_Предоплата_Расход = ДобавитьРасходПредоплаты(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ПодходящаяСтрокаСторно, СтрокаРасчетов, СуммаСписания, СуммаСписанияРегл, СуммаСписанияУпр);
							ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов, НовСтр_Предоплата_Расход);
						КонецЕсли;
						#КонецОбласти
						
						// Уменьшаем строку оплаты на зачтенную сумму.
						СтрокаРасчетов.Сумма       = СтрокаРасчетов.Сумма     - СуммаСписания;
						СтрокаРасчетов.СуммаРегл   = СтрокаРасчетов.СуммаРегл - СуммаСписанияРегл;
						СтрокаРасчетов.СуммаУпр    = СтрокаРасчетов.СуммаУпр  - СуммаСписанияУпр;
					КонецЦикла;
				#КонецОбласти
				КонецЕсли;
				
				Если ТипЗнч(СтрокаРасчетов.Регистратор) = Тип("ДокументСсылка.КорректировкаГрафикаВзаиморасчетов") Тогда
					
					#Область КорректировкаГрафика
					Если СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаКлиента
						ИЛИ СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаПоставщику Тогда
						ОстаткиРасчетногоДокумента = ТаблицаОстатковВзаиморасчетов.НайтиСтроки(Новый Структура("Долг, РасчетныйДокумент", 0, СтрокаРасчетов.РасчетныйДокумент));
						Для Каждого СтрокаОстаткаРасчетногоДокумента Из ОстаткиРасчетногоДокумента Цикл
							Если СтрокаРасчетов.Сумма = 0 Тогда
								Прервать;
							КонецЕсли;
							
							#Область ОпределениеСуммыСписания
							СуммаСписания = Мин(СтрокаРасчетов.Сумма, СтрокаОстаткаРасчетногоДокумента.Предоплата);
							Если СуммаСписания = 0 Тогда
								Продолжить;
							КонецЕсли;
							СуммаКорректировкиРегл = Окр(СуммаСписания * СтрокаОстаткаРасчетногоДокумента.ПредоплатаРегл / СтрокаОстаткаРасчетногоДокумента.Предоплата,2);
							СуммаКорректировкиУпр = Окр(СуммаСписания * СтрокаОстаткаРасчетногоДокумента.ПредоплатаУпр / СтрокаОстаткаРасчетногоДокумента.Предоплата,2);
							
							// Все суммы списаний корректировки графика нужно сложить, чтобы потом начислить такие-же суммы без образования кусровых разниц
							Если ГлобальныеПеременные.ДанныеКорректировокГрафиков[СтрокаРасчетов.Регистратор] = Неопределено Тогда
								ГлобальныеПеременные.ДанныеКорректировокГрафиков.Вставить(СтрокаРасчетов.Регистратор, СтруктураДанныхКорректировкиГрафика());
							КонецЕсли;
							ДанныеКорректировкиГрафика                 = ГлобальныеПеременные.ДанныеКорректировокГрафиков[СтрокаРасчетов.Регистратор];
							ДанныеКорректировкиГрафика.СуммаРасход     = ДанныеКорректировкиГрафика.СуммаРасход     + СуммаСписания;
							ДанныеКорректировкиГрафика.СуммаРасходРегл = ДанныеКорректировкиГрафика.СуммаРасходРегл + СуммаКорректировкиРегл;
							ДанныеКорректировкиГрафика.СуммаРасходУпр  = ДанныеКорректировкиГрафика.СуммаРасходУпр  + СуммаКорректировкиУпр;
							ДанныеКорректировкиГрафика.ДатаВозникновения = СтрокаОстаткаРасчетногоДокумента.ДатаВозникновения;
							#КонецОбласти
							
							#Область РасходПредоплаты
							НовСтр_Предоплата_Расход = ДобавитьРасходПредоплаты(ГлобальныеПеременные,ТаблицаРасчетовПоСрокам,СтрокаОстаткаРасчетногоДокумента,СтрокаРасчетов, СуммаСписания,СуммаКорректировкиРегл,СуммаКорректировкиУпр);
							ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов, НовСтр_Предоплата_Расход);
							#КонецОбласти
							
							//Уменьшаем строку оплаты на сумму сторно.
							СтрокаРасчетов.Сумма     = СтрокаРасчетов.Сумма     - СуммаСписания;
							СтрокаРасчетов.СуммаРегл = СтрокаРасчетов.СуммаРегл - СуммаКорректировкиРегл;
							СтрокаРасчетов.СуммаУпр  = СтрокаРасчетов.СуммаУпр  - СуммаКорректировкиУпр;
							
						КонецЦикла;
						Если СтрокаРасчетов.Сумма > 0 Тогда
							СтрокаРасчетов.Сумма = 0;
						КонецЕсли;
						// Если еще не были добавлены суммы расхода, то приход не нужно добавлять, запишем пустую структуру
						Если ГлобальныеПеременные.ДанныеКорректировокГрафиков[СтрокаРасчетов.Регистратор] = Неопределено Тогда
							ГлобальныеПеременные.ДанныеКорректировокГрафиков.Вставить(СтрокаРасчетов.Регистратор, СтруктураДанныхКорректировкиГрафика());
						КонецЕсли;
					Иначе
						ЗаполнитьДанныеКорректировкиГрафика(ГлобальныеПеременные, СтрокаРасчетов.Регистратор, ДанныеДляРаспределения, СтрокаРасчетов.ЭтоОплата);
					КонецЕсли;
					#КонецОбласти
				КонецЕсли;
				
				// Переброс аванса на расчетный документ - накладную.
				// Если "ИСТИНА" - будет просто уменьшена предоплата, иначе будет создана запись по приходу и расходу долга.
				ДвойнаяЗаписьНеНужна = ХозяйственныеОперацииНеОтгрузка.Найти(СтрокаРасчетов.ХозяйственнаяОперация) <> Неопределено;
				
				Если ТипЗнч(СтрокаРасчетов.Регистратор) = Тип("ДокументСсылка.КорректировкаРеализации")
					Или ТипЗнч(СтрокаРасчетов.Регистратор) = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
					ПересчитатьСуммыЕслиЭтоКорректировка(ГлобальныеПеременные, СтрокаРасчетов, "ОтгрузкаПоставка");
				КонецЕсли;
				
				#Область ЗачетАванса
				ПодходящиеСтроки = ТаблицаОстатковВзаиморасчетов.Скопировать(Новый Структура("Долг", 0));
				Если СтрокаОсновныхПараметровРасчета.ЗачетОплатПоДатеПлатежа Тогда
					ПодходящиеСтроки.Сортировать("ДатаВозникновения ВОЗР, ПорядокЗачетаПоДатеПлатежа ВОЗР, ПорядокОперации ВОЗР");
				Иначе
					ПодходящиеСтроки.Сортировать("ДатаВозникновения ВОЗР, ПорядокОперации ВОЗР, ПорядокЗачетаПоДатеПлатежа ВОЗР");
				КонецЕсли;
				
				Для Каждого ПодходящаяСтрокаОплаты Из ПодходящиеСтроки Цикл
					Если СтрокаРасчетов.Сумма = 0 Тогда
						Прервать;
					КонецЕсли;
					Если ПодходящаяСтрокаОплаты.Предоплата < 0 Тогда
						ВызватьИсключение(СтрШаблон(НСтр("ru = 'В регистрах взаиморасчетов обнаружены отрицательные остатки на дату %1. Проведение невозможно.';
														|en = 'AR/AP accounting registers show negative balances as of %1. Posting is unavailable.'"),
							СтрокаОсновныхПараметровРасчета.ДатаНачалаПересчета));
					КонецЕсли;
					
					#Область ОпределениеСуммыСписания
					
					Если НЕ ДвойнаяЗаписьНеНужна Тогда
						Если СтрокаРасчетов.Регистратор = ПодходящаяСтрокаОплаты.РасчетныйДокумент
							И ТипЗнч(СтрокаРасчетов.Регистратор) = Тип("ДокументСсылка.ОтчетКомиссионера")
							И СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.УдержаниеВознагражденияКомиссионера Тогда
							ХозяйственнаяОперацияЗачетаАванса = Перечисления.ХозяйственныеОперации.ЗачетВознагражденияОплатойКомиссионера;
						ИначеЕсли СтрокаРасчетов.Регистратор = ПодходящаяСтрокаОплаты.РасчетныйДокумент
							И ТипЗнч(СтрокаРасчетов.Регистратор) = Тип("ДокументСсылка.ОтчетКомитенту")
							И СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.УдержаниеВознагражденияКомитентом Тогда
							ХозяйственнаяОперацияЗачетаАванса = Перечисления.ХозяйственныеОперации.ЗачетВознагражденияОплатойКомитенту;
						ИначеЕсли СтрокаРасчетов.Регистратор = ПодходящаяСтрокаОплаты.РасчетныйДокумент
							И ТипЗнч(СтрокаРасчетов.Регистратор) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")
							И (СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.УдержаниеВознагражденияКомиссионера
								ИЛИ СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.УдержаниеВознагражденияКомитентом) Тогда
							Если ГлобальныеПеременные.ЭтоРасчетыСКлиентами Тогда
								ХозяйственнаяОперацияЗачетаАванса = Перечисления.ХозяйственныеОперации.ЗачетВознагражденияОплатойКомиссионера;
							Иначе
								ХозяйственнаяОперацияЗачетаАванса = Перечисления.ХозяйственныеОперации.ЗачетВознагражденияОплатойКомитенту;
							КонецЕсли;
						ИначеЕсли СтрокаРасчетов.Регистратор = ПодходящаяСтрокаОплаты.РасчетныйДокумент
							И ТипЗнч(СтрокаРасчетов.Регистратор) = Тип("ДокументСсылка.Бронирование")
							И (СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.БронированиеЧерезАгента
								ИЛИ СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИспользованиеБронированияПодотчетнымЛицом
								ИЛИ СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.БронированиеЧерезПодотчетноеЛицо) Тогда
							ХозяйственнаяОперацияЗачетаАванса = Перечисления.ХозяйственныеОперации.ИспользованиеБронированияПодотчетнымЛицом;
						Иначе
							ХозяйственнаяОперацияЗачетаАванса = ГлобальныеПеременные.ХозяйственныеОперации["ЗачетАванса"];
						КонецЕсли;
						ИдентификаторФинЗаписиЗачетаАванса = Новый УникальныйИдентификатор;
					КонецЕсли;
					
					СуммаСписания = Мин(СтрокаРасчетов.Сумма,ПодходящаяСтрокаОплаты.Предоплата);
					Если СуммаСписания = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					//Возврат аванса отражаем по курсу возврата. Произвольный взаимозачет всегда по своему курсу.
					Если (СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВзаимозачетЗадолженности
							ИЛИ СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИспользованиеБронированияПодотчетнымЛицом)
						И СтрокаРасчетов.РучнойКурсРегл
						ИЛИ СтрокаРасчетов.ЭтоВозвратАванса
							И СтрокаРасчетов.Регистратор = ПодходящаяСтрокаОплаты.РасчетныйДокумент
						ИЛИ СтрокаРасчетов.Сторно Тогда
						СуммаСписанияРегл = СуммаСписания(СтрокаРасчетов.ИсходнаяСуммаРегл/СтрокаРасчетов.ИсходнаяСумма,
							СтрокаРасчетов.СуммаРегл, СтрокаРасчетов.Сумма, СуммаСписания);
					// Остальные авансы по курсу аванса.
					Иначе
						СуммаСписанияРегл = СуммаСписания(0, ПодходящаяСтрокаОплаты.ПредоплатаРегл, ПодходящаяСтрокаОплаты.Предоплата, СуммаСписания);
					КонецЕсли;
					
					Если (СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВзаимозачетЗадолженности
							ИЛИ СтрокаРасчетов.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИспользованиеБронированияПодотчетнымЛицом)
						И СтрокаРасчетов.РучнойКурсУпр
						ИЛИ СтрокаРасчетов.ЭтоВозвратАванса 
							И СтрокаРасчетов.Регистратор = ПодходящаяСтрокаОплаты.РасчетныйДокумент
						ИЛИ СтрокаРасчетов.Сторно Тогда
						СуммаСписанияУпр = СуммаСписания(СтрокаРасчетов.ИсходнаяСуммаУпр/СтрокаРасчетов.ИсходнаяСумма,
							СтрокаРасчетов.СуммаУпр, СтрокаРасчетов.Сумма, СуммаСписания);
					Иначе
						СуммаСписанияУпр = СуммаСписания(0, ПодходящаяСтрокаОплаты.ПредоплатаУпр, ПодходящаяСтрокаОплаты.Предоплата, СуммаСписания);
					КонецЕсли;
					#КонецОбласти
					
					Если СтрокаРасчетов.РучнойКурсУпр ИЛИ СтрокаРасчетов.ЭтоВозвратАванса Тогда
						ПриходДолгУпр = СуммаСписания(СтрокаРасчетов.ИсходнаяСуммаУпр/СтрокаРасчетов.ИсходнаяСумма, 
							СтрокаРасчетов.СуммаУпр, СтрокаРасчетов.Сумма, СуммаСписания);
					Иначе
						ПриходДолгУпр = СуммаСписанияУпр;
					КонецЕсли;
					Если СтрокаРасчетов.РучнойКурсРегл ИЛИ СтрокаРасчетов.ЭтоВозвратАванса Тогда
						ПриходДолгРегл = СуммаСписания(СтрокаРасчетов.ИсходнаяСуммаРегл/СтрокаРасчетов.ИсходнаяСумма, 
							СтрокаРасчетов.СуммаРегл, СтрокаРасчетов.Сумма, СуммаСписания);
					Иначе
						ПриходДолгРегл = СуммаСписанияРегл;
					КонецЕсли;
				
					#Область РасходПредоплата
					Если СтрокаРасчетов.ПериодЗачета >= ГлобальныеПеременные.ДатаНачалаПересчета И НЕ СтрокаРасчетов.Сторно Тогда
						НовСтр_Предоплата_Расход = ДобавитьРасходПредоплаты(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ПодходящаяСтрокаОплаты, СтрокаРасчетов, СуммаСписания, СуммаСписанияРегл, СуммаСписанияУпр, ДвойнаяЗаписьНеНужна);
						ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов, НовСтр_Предоплата_Расход);
						Если НЕ ДвойнаяЗаписьНеНужна Тогда
							НовСтр_Предоплата_Расход.ХозяйственнаяОперация = ХозяйственнаяОперацияЗачетаАванса;
							НовСтр_Предоплата_Расход.ИдентификаторФинЗаписи = ИдентификаторФинЗаписиЗачетаАванса;
							НовСтр_Предоплата_Расход.НастройкаХозяйственнойОперации = ГлобальныеПеременные.НастройкиХО["ЗачетОплаты"];
						КонецЕсли;
					КонецЕсли;
					#КонецОбласти
				
					Если НЕ ДвойнаяЗаписьНеНужна Тогда
						Если СтрокаРасчетов.Период >= ГлобальныеПеременные.ДатаНачалаПересчета Тогда
						#Область ПриходДолг
						
							//Отражаем приход на сумму долга по курсу предоплаты.
							НовСтр_Долг_Приход = ТаблицаРасчетовПоСрокам.Добавить();
							НовСтр_Долг_Приход.ВидДвижения                   = ВидДвиженияНакопления.Приход;
							
							НовСтр_Долг_Приход.ДокументРегистратор           = СтрокаРасчетов.Регистратор;
							НовСтр_Долг_Приход.ВалютаДокумента               = СтрокаРасчетов.ВалютаДокумента;
							НовСтр_Долг_Приход.ПорядокОперации               = СтрокаРасчетов.ПорядокОперации;
							НовСтр_Долг_Приход.Сторно                        = Ложь;
							НовСтр_Долг_Приход.Период                        = СтрокаРасчетов.Период;
							НовСтр_Долг_Приход.ДатаВозникновения             = СтрокаРасчетов.ДатаВозникновения;
							НовСтр_Долг_Приход.ДатаПлановогоПогашения        = СтрокаРасчетов.ДатаПлановогоПогашения;
							НовСтр_Долг_Приход.РасчетныйДокумент             = ?(СтрокаРасчетов.РасчетныйДокумент <> СтрокаРасчетов.Регистратор, СтрокаРасчетов.РасчетныйДокумент, СтрокаРасчетов.Регистратор);
							НовСтр_Долг_Приход.ПорядокЗачета                 = СтрокаРасчетов.ПорядокЗачетаПоДатеПлатежа;
							НовСтр_Долг_Приход.ХозяйственнаяОперация         = СтрокаРасчетов.ХозяйственнаяОперация;
							НовСтр_Долг_Приход.НастройкаХозяйственнойОперации = СтрокаРасчетов.НастройкаХозяйственнойОперации;
							НовСтр_Долг_Приход.ИдентификаторФинЗаписи        = СтрокаРасчетов.ИдентификаторФинЗаписи;
							//возврат ДС, статья должна быть
							НовСтр_Долг_Приход.СтатьяДвиженияДенежныхСредств = СтрокаРасчетов.СтатьяДвиженияДенежныхСредств;
							НовСтр_Долг_Приход.СвязанныйДокумент             = Неопределено;
							
							//С приемником связан только регистратор отгрузки
							НовСтр_Долг_Приход.АналитикаУчетаПоПартнерамПриемник = СтрокаРасчетов.АналитикаУчетаПоПартнерамПриемник;
							НовСтр_Долг_Приход.ОбъектРасчетовПриемник            = СтрокаРасчетов.ОбъектРасчетовПриемник;
							НовСтр_Долг_Приход.ВалютаПриемник                    = СтрокаРасчетов.ВалютаПриемник;
							НовСтр_Долг_Приход.СуммаПриемник                     = СуммаСписания(0, СтрокаРасчетов.СуммаПриемник, СтрокаРасчетов.Сумма, СуммаСписания);
							СтрокаРасчетов.СуммаПриемник = СтрокаРасчетов.СуммаПриемник - НовСтр_Долг_Приход.СуммаПриемник;
							
							НовСтр_Долг_Приход.ПоДаннымОбъектаРасчетовИсточника  = СтрокаРасчетов.ПоДаннымОбъектаРасчетовИсточника;
							НовСтр_Долг_Приход.КорОбъектРасчетов                 = СтрокаРасчетов.КорОбъектРасчетов;
							НовСтр_Долг_Приход.КорАналитикаУчетаПоПартнерам      = СтрокаРасчетов.КорАналитикаУчетаПоПартнерам;
							
							НовСтр_Долг_Приход.Долг                          = СуммаСписания;
							НовСтр_Долг_Приход.ДолгРегл                      = ПриходДолгРегл;
							НовСтр_Долг_Приход.ДолгУпр                       = ПриходДолгУпр;
							НовСтр_Долг_Приход.ЗаписьДоНачалаРасчета         = Ложь;
							Если НовСтр_Долг_Приход.Сторно
								И СтрокаРасчетов.ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.Оплата Тогда
								НовСтр_Долг_Приход.ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.ПереносВзаиморасчетов;
							Иначе
								НовСтр_Долг_Приход.ОперацияВзаиморасчетов = СтрокаРасчетов.ОперацияВзаиморасчетов;
							КонецЕсли;
							НовСтр_Долг_Приход.Предоплата                    = 0;
							НовСтр_Долг_Приход.ПредоплатаРегл                = 0;
							НовСтр_Долг_Приход.ПредоплатаУпр                 = 0;
							
							ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов, НовСтр_Долг_Приход);
						#КонецОбласти
						КонецЕсли;
						
						#Область РасходДолг
						Если СтрокаРасчетов.ПериодЗачета>= ГлобальныеПеременные.ДатаНачалаПересчета Тогда
						//Переносим предоплату на расчетный документ отгрузки.
						НовСтр_Долг_Расход = ТаблицаРасчетовПоСрокам.Добавить();
						НовСтр_Долг_Расход.ВидДвижения                   = ВидДвиженияНакопления.Расход;
						НовСтр_Долг_Расход.ВалютаДокумента               = СтрокаРасчетов.ВалютаДокумента;
						НовСтр_Долг_Расход.Период                        = СтрокаРасчетов.ПериодЗачета;
						
						//Униплан
						НовСтр_Долг_Расход.ХозяйственнаяОперация         = НовСтр_Предоплата_Расход.ХозяйственнаяОперация;
						НовСтр_Долг_Расход.НастройкаХозяйственнойОперации = НовСтр_Предоплата_Расход.НастройкаХозяйственнойОперации;
						НовСтр_Долг_Расход.ИдентификаторФинЗаписи        = НовСтр_Предоплата_Расход.ИдентификаторФинЗаписи;
						
						НовСтр_Долг_Расход.ДатаПлановогоПогашения        = НовСтр_Долг_Приход.ДатаПлановогоПогашения;
						НовСтр_Долг_Расход.РасчетныйДокумент             = НовСтр_Долг_Приход.РасчетныйДокумент;
						НовСтр_Долг_Расход.ДатаВозникновения             = НовСтр_Долг_Приход.ДатаВозникновения;
						НовСтр_Долг_Расход.ПорядокЗачета                 = СтрокаРасчетов.ПорядокЗачета;
						НовСтр_Долг_Расход.ПорядокОперации               = СтрокаРасчетов.ПорядокЗачета;
						НовСтр_Долг_Расход.ДокументРегистратор           = СтрокаРасчетов.Регистратор;
						НовСтр_Долг_Расход.СтатьяДвиженияДенежныхСредств = СтрокаРасчетов.СтатьяДвиженияДенежныхСредств;
						НовСтр_Долг_Расход.Сторно                        = Ложь;
						НовСтр_Долг_Расход.СвязанныйДокумент             = Неопределено;
						
						НовСтр_Долг_Расход.АналитикаУчетаПоПартнерамПриемник  = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
						НовСтр_Долг_Расход.ОбъектРасчетовПриемник             = Справочники.ОбъектыРасчетов.ПустаяСсылка();
						НовСтр_Долг_Расход.ВалютаПриемник                     = Справочники.Валюты.ПустаяСсылка();
						НовСтр_Долг_Расход.СуммаПриемник                      = 0;
						
						НовСтр_Долг_Расход.ПоДаннымОбъектаРасчетовИсточника = ЛОЖЬ;
						НовСтр_Долг_Расход.КорОбъектРасчетов                = Справочники.ОбъектыРасчетов.ПустаяСсылка();
						НовСтр_Долг_Расход.КорАналитикаУчетаПоПартнерам     = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
						
						НовСтр_Долг_Расход.Долг                          = НовСтр_Предоплата_Расход.Предоплата;
						НовСтр_Долг_Расход.ДолгУпр                       = НовСтр_Предоплата_Расход.ПредоплатаУпр;
						НовСтр_Долг_Расход.ДолгРегл                      = НовСтр_Предоплата_Расход.ПредоплатаРегл;
						НовСтр_Долг_Расход.ЗаписьДоНачалаРасчета         = Ложь;
						Если НовСтр_Долг_Расход.Сторно
							И СтрокаРасчетов.ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.Оплата Тогда
							НовСтр_Долг_Расход.ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.ПереносВзаиморасчетов;
						Иначе
							НовСтр_Долг_Расход.ОперацияВзаиморасчетов = СтрокаРасчетов.ОперацияВзаиморасчетов;
						КонецЕсли;
						НовСтр_Долг_Расход.Предоплата                    = 0;
						НовСтр_Долг_Расход.ПредоплатаУпр                 = 0;
						НовСтр_Долг_Расход.ПредоплатаРегл                = 0;
						
						ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов, НовСтр_Долг_Расход, НовСтр_Долг_Приход);
						КонецЕсли;
						#КонецОбласти
					
					КонецЕсли;
				
					//Уменьшаем сумму строку отгрузки, которая пойдет в приход.
					Если СтрокаРасчетов.РучнойКурсРегл И НЕ ДвойнаяЗаписьНеНужна Тогда
						СтрокаРасчетов.СуммаРегл = СтрокаРасчетов.СуммаРегл - ПриходДолгРегл;
					Иначе
						//Выше могла произойти переоценка, поэтому сумму для переоценки нужно изменить так же как сумму регл
						ПересчитанныйОстатокРегл = СуммаСписания(СтрокаРасчетов.ИсходнаяСуммаРегл/СтрокаРасчетов.ИсходнаяСумма, СтрокаРасчетов.СуммаРегл, СтрокаРасчетов.Сумма, СтрокаРасчетов.Сумма-СуммаСписания);
						СтрокаРасчетов.СуммаРегл = ПересчитанныйОстатокРегл;
					КонецЕсли;
					
					Если СтрокаРасчетов.РучнойКурсУпр И НЕ ДвойнаяЗаписьНеНужна Тогда
						СтрокаРасчетов.СуммаУпр = СтрокаРасчетов.СуммаУпр - ПриходДолгУпр;
					Иначе
						ПересчитанныйОстатокУпр = СуммаСписания(СтрокаРасчетов.ИсходнаяСуммаУпр/СтрокаРасчетов.ИсходнаяСумма, СтрокаРасчетов.СуммаУпр, СтрокаРасчетов.Сумма, СтрокаРасчетов.Сумма-СуммаСписания);
						СтрокаРасчетов.СуммаУпр = ПересчитанныйОстатокУпр;
					КонецЕсли;
					СтрокаРасчетов.Сумма                  = СтрокаРасчетов.Сумма - СуммаСписания;
					
					//Хранит средневзвешанные курсы накладных для проведения корректировок.
					Если ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.Регистратор] = Неопределено Тогда
						ГлобальныеПеременные.ДанныеНакладных.Вставить(СтрокаРасчетов.Регистратор, СтруктураСумм());
					КонецЕсли;
					
					ДанныеНакладной                    = ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.Регистратор];
					ДанныеНакладной.СуммаДокумента     = ДанныеНакладной.СуммаДокумента     + СуммаСписания;
					ДанныеНакладной.СуммаДокументаРегл = ДанныеНакладной.СуммаДокументаРегл + СуммаСписанияРегл;
					ДанныеНакладной.СуммаДокументаУпр  = ДанныеНакладной.СуммаДокументаУпр  + СуммаСписанияУпр;
					
				КонецЦикла;
				
				#КонецОбласти
				
				Если СтрокаРасчетов.Сумма > 0 Тогда
					#Область ПриходПредоплаты
					ДобавитьПриход(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаРасчетов, "ОтгрузкаПоставка");
					#КонецОбласти
				КонецЕсли;
				
			КонецЕсли;
			
			Если СтрокаРасчетов.НовыеДвиженияИсправления Тогда
				ДобавитьПочиночныеДвиженияПриНеобходимости(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаРасчетов);
			КонецЕсли;
			
			СписатьРеглУпрПоНулевымОстаткам(ГлобальныеПеременные,ТаблицаРасчетовПоСрокам,ТаблицаОстатковВзаиморасчетов,НачалоДня(СтрокаРасчетов.Период));
			
		КонецЦикла;
		
		//Переоценить все остатки долгов
		Если НужнаПереоценка Тогда
			Для Каждого СтрокаОстатков Из ТаблицаОстатковВзаиморасчетов Цикл
				Если СтрокаОстатков.Долг > 0 Тогда
					ПереоценитьДолг(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаОстатков.РасчетныйДокумент, ТекущаяДатаСеанса(), Ложь);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		#КонецОбласти

		#Область ФиксацияРезультатов
		
		НаборЗаписейПоСрокам = РегистрыНакопления[ИмяРегистраРасчетов].СоздатьНаборЗаписей();
		НаборЗаписейПланОплат = РегистрыНакопления[ИмяРегистраПланаОплат].СоздатьНаборЗаписей();
		НаборЗаписейПланОтгрузокПоставок = РегистрыНакопления[ИмяРегистраПланаОтгрузкиПоставки].СоздатьНаборЗаписей();
		
		ТаблицаРасчетовПоСрокам.Свернуть("Период, Регистратор, ВидДвижения, АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта, ДокументРегистратор,
				|РасчетныйДокумент, ХозяйственнаяОперация, ДатаПлановогоПогашения, ДатаВозникновения, ПорядокЗачета, ПорядокОперации,
				|ВалютаДокумента, СвязанныйДокумент, СтатьяДвиженияДенежныхСредств, КорОбъектРасчетов, КорАналитикаУчетаПоПартнерам,
				|ОбъектРасчетовПриемник, АналитикаУчетаПоПартнерамПриемник, ВалютаПриемник, Сторно,
				|НастройкаХозяйственнойОперации,ИдентификаторФинЗаписи,ЗаписьДоНачалаРасчета,ПоДаннымОбъектаРасчетовИсточника,ОперацияВзаиморасчетов,ТипЗаписиВзаиморасчетов",
				"Предоплата, ПредоплатаРегл, ПредоплатаУпр, Долг, ДолгРегл, ДолгУпр,СуммаПриемник");
		
		Если Константы.УдалитьТипыЗаписейВзаиморасчетовЗаполнены.Получить() Тогда
			Для Каждого СтрокаДвижений Из ТаблицаРасчетовПоСрокам Цикл
				Если Не СтрокаДвижений.ЗаписьДоНачалаРасчета
					И Не ЗначениеЗаполнено(СтрокаДвижений.ТипЗаписиВзаиморасчетов) Тогда
					СтрокаДвижений.ТипЗаписиВзаиморасчетов = ТипЗаписиВзаиморасчетов(СтрокаДвижений);
				КонецЕсли;
			КонецЦикла;
		Иначе
			МассивСторно = Новый Массив;
			Для Каждого СтрокаДвижений Из ТаблицаРасчетовПоСрокам Цикл
				Если ТипЗнч(СтрокаДвижений.ДокументРегистратор) = Тип("ДокументСсылка.Сторно") Тогда
					МассивСторно.Добавить(СтрокаДвижений.ДокументРегистратор);
				КонецЕсли;
			КонецЦикла;
			СоответствиеСторнируемыхДокументов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивСторно, "СторнируемыйДокумент");
			Для Каждого СтрокаДвижений Из ТаблицаРасчетовПоСрокам Цикл
				ТипРегистратора = ТипЗнч(СтрокаДвижений.ДокументРегистратор);
				Если ТипРегистратора = Тип("ДокументСсылка.Сторно")
					И СоответствиеСторнируемыхДокументов[СтрокаДвижений.ДокументРегистратор] <> Неопределено Тогда
					ТипРегистратора = ТипЗнч(СоответствиеСторнируемыхДокументов[СтрокаДвижений.ДокументРегистратор]);
				КонецЕсли;
				ЗаполнитьТипЗаписиВзаиморасчетов(СтрокаДвижений, ТипРегистратора);
			КонецЦикла;
		КонецЕсли;
		
		ТаблицаРасчетовПоСрокам.ЗаполнитьЗначения(ГлобальныеПеременные.АналитикаУчетаПоПартнерам, "АналитикаУчетаПоПартнерам");
			ТаблицаРасчетовПоСрокам.ЗаполнитьЗначения(ГлобальныеПеременные.ОбъектРасчетов,            "ОбъектРасчетов");
			ТаблицаРасчетовПоСрокам.ЗаполнитьЗначения(ГлобальныеПеременные.ВалютаРасчетов,            "Валюта");
			ТаблицаРасчетовПоСрокам.Индексы.Добавить("Регистратор");
		
		Если СтрокаОсновныхПараметровРасчета.ЗаписыватьИзменения Тогда
			ТаблицаРегистраторов = ТаблицаСвободныхРегистраторовРасчета(ГлобальныеПеременные);
		КонецЕсли;
		
		Если НЕ СтрокаОсновныхПараметровРасчета.НачальноеЗаполнение И СтрокаОсновныхПараметровРасчета.ЗаписыватьИзменения Тогда
			
			НаборЗаписейПоСрокам.ДополнительныеСвойства.Вставить("Порядок", ГлобальныеПеременные.ПорядокОперации );
			ПодготовитьДанныеДляЗаписи(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, НаборЗаписейПоСрокам, ТаблицаРегистраторов);
			
			#Область ПроверкаИзмененийРегистров
			
			ТаблицаИзменений = Неопределено;
			
			Если Не СтрокаОсновныхПараметровРасчета.ПерезаполнениеРегистровВзаиморасчетов Тогда
			
				Запрос.Текст = "
				|ВЫБРАТЬ *
				|ПОМЕСТИТЬ ВтДвиженияПосле
				|ИЗ  &ТаблицаПоСрокам КАК ДвиженияПосле
				|;
				|ВЫБРАТЬ
				|	ВложенныйЗапрос.Период                        КАК Период,
				|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
				|	ВложенныйЗапрос.ОбъектРасчетов                КАК ОбъектРасчетов,
				|	ВложенныйЗапрос.Валюта                        КАК Валюта,
				|	ВложенныйЗапрос.РасчетныйДокумент             КАК РасчетныйДокумент,
				|	ВложенныйЗапрос.ДатаПлановогоПогашения        КАК ДатаПлановогоПогашения,
				|	ВложенныйЗапрос.ДатаВозникновения             КАК ДатаВозникновения,
				|	ВложенныйЗапрос.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
				|	ВложенныйЗапрос.ДокументРегистратор           КАК ДокументРегистратор,	
				|	СУММА(ВложенныйЗапрос.Предоплата)                    КАК Предоплата,
				|	СУММА(ВложенныйЗапрос.ПредоплатаРегл)                КАК ПредоплатаРегл,
				|	СУММА(ВложенныйЗапрос.ПредоплатаУпр)                 КАК ПредоплатаУпр,
				|	СУММА(ВложенныйЗапрос.Долг)                          КАК Долг,
				|	СУММА(ВложенныйЗапрос.ДолгРегл)                      КАК ДолгРегл,
				|	СУММА(ВложенныйЗапрос.ДолгУпр)                       КАК ДолгУпр,
				|	&СоздаватьЗаданияКЗакрытиюСледующегоМесяца    КАК СоздаватьЗаданияКЗакрытиюСледующегоМесяца
				|ПОМЕСТИТЬ ТаблицаИзмененийРасчетыСКлиентамиПоСрокам
				|ИЗ
				|	(ВЫБРАТЬ
				|		РасчетыПоСрокам.Период                        КАК Период,
				|		РасчетыПоСрокам.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
				|		РасчетыПоСрокам.ОбъектРасчетов                КАК ОбъектРасчетов,
				|		РасчетыПоСрокам.Валюта                        КАК Валюта,
				|		РасчетыПоСрокам.РасчетныйДокумент             КАК РасчетныйДокумент,
				|		ВЫБОР КОГДА РасчетыПоСрокам.Предоплата <> 0 
				|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
				|			ИНАЧЕ РасчетыПоСрокам.ДатаПлановогоПогашения
				|		КОНЕЦ                                         КАК ДатаПлановогоПогашения,
				|		РасчетыПоСрокам.ДатаВозникновения             КАК ДатаВозникновения,
				|		ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(РасчетыПоСрокам.ДокументРегистратор)
				|						В (ТИП(Документ.КорректировкаРеализации),
				|						   ТИП(Документ.КорректировкаПриобретения))
				|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
				|			ИНАЧЕ РасчетыПоСрокам.ХозяйственнаяОперация
				|		КОНЕЦ                                         КАК ХозяйственнаяОперация,
				|		РасчетыПоСрокам.ДокументРегистратор           КАК ДокументРегистратор,
				|		
				|		РасчетыПоСрокам.Предоплата                    КАК Предоплата,
				|		РасчетыПоСрокам.ПредоплатаРегл                КАК ПредоплатаРегл,
				|		РасчетыПоСрокам.ПредоплатаУпр                 КАК ПредоплатаУпр,
				|		РасчетыПоСрокам.Долг                          КАК Долг,
				|		РасчетыПоСрокам.ДолгРегл                      КАК ДолгРегл,
				|		РасчетыПоСрокам.ДолгУпр                       КАК ДолгУпр
				|		
				|	ИЗ
				|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокам
				|	ГДЕ
				|		РасчетыПоСрокам.ДокументРегистратор В (&МассивДокументов)
				|		И РасчетыПоСрокам.Активность
				|		И РасчетыПоСрокам.ОбъектРасчетов = &ОбъектРасчетов
				|		И РасчетыПоСрокам.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
				|		И РасчетыПоСрокам.Период >= &ПериодКонтроляИзменений
				|	
				|	ОБЪЕДИНИТЬ ВСЕ
				|	
				|	ВЫБРАТЬ
				|		ДвиженияПосле.Период                        КАК Период,
				|		ДвиженияПосле.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
				|		ДвиженияПосле.ОбъектРасчетов                КАК ОбъектРасчетов,
				|		ДвиженияПосле.Валюта                        КАК Валюта,
				|		ДвиженияПосле.РасчетныйДокумент             КАК РасчетныйДокумент,
				|		ВЫБОР КОГДА ДвиженияПосле.Предоплата <> 0 
				|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
				|			ИНАЧЕ ДвиженияПосле.ДатаПлановогоПогашения
				|		КОНЕЦ                                       КАК ДатаПлановогоПогашения,
				|		ДвиженияПосле.ДатаВозникновения             КАК ДатаВозникновения,
				|		ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДвиженияПосле.ДокументРегистратор)
				|						В (ТИП(Документ.КорректировкаРеализации),
				|						   ТИП(Документ.КорректировкаПриобретения))
				|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
				|			ИНАЧЕ ДвиженияПосле.ХозяйственнаяОперация
				|		КОНЕЦ                                       КАК ХозяйственнаяОперация,
				|		ДвиженияПосле.ДокументРегистратор           КАК ДокументРегистратор,
				|		
				|		-ДвиженияПосле.Предоплата                    КАК Предоплата,
				|		-ДвиженияПосле.ПредоплатаРегл                КАК ПредоплатаРегл,
				|		-ДвиженияПосле.ПредоплатаУпр                 КАК ПредоплатаУпр,
				|		-ДвиженияПосле.Долг                          КАК Долг,
				|		-ДвиженияПосле.ДолгРегл                      КАК ДолгРегл,
				|		-ДвиженияПосле.ДолгУпр                       КАК ДолгУпр
				|	ИЗ
				|		ВтДвиженияПосле КАК ДвиженияПосле
				|	ГДЕ
				|		ДвиженияПосле.Период >= &ПериодКонтроляИзменений) КАК ВложенныйЗапрос
				|СГРУППИРОВАТЬ ПО
				|	ВложенныйЗапрос.Период,
				|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
				|	ВложенныйЗапрос.ОбъектРасчетов,
				|	ВложенныйЗапрос.Валюта,
				|	ВложенныйЗапрос.РасчетныйДокумент,
				|	ВложенныйЗапрос.ДатаПлановогоПогашения,
				|	ВложенныйЗапрос.ДатаВозникновения,
				|	ВложенныйЗапрос.ХозяйственнаяОперация,
				|	ВложенныйЗапрос.ДокументРегистратор
				|ИМЕЮЩИЕ
				|	СУММА(ВложенныйЗапрос.Предоплата) <> 0
				|	ИЛИ СУММА(ВложенныйЗапрос.ПредоплатаРегл) <> 0
				|	ИЛИ СУММА(ВложенныйЗапрос.ПредоплатаУпр) <> 0
				|	ИЛИ СУММА(ВложенныйЗапрос.Долг) <> 0
				|	ИЛИ СУММА(ВложенныйЗапрос.ДолгРегл) <> 0
				|	ИЛИ СУММА(ВложенныйЗапрос.ДолгУпр) <> 0";
				
				Если НЕ ГлобальныеПеременные.ЭтоРасчетыСКлиентами Тогда
					Запрос.Текст = СтрЗаменить(Запрос.Текст,
						"РегистрНакопления.РасчетыСКлиентамиПоСрокам",
						"РегистрНакопления.РасчетыСПоставщикамиПоСрокам");
					Запрос.Текст = СтрЗаменить(Запрос.Текст,
						"ТаблицаИзмененийРасчетыСКлиентамиПоСрокам",
						"ТаблицаИзмененийРасчетыСПоставщикамиПоСрокам");
				КонецЕсли;
				
				РасчетыДляПроверки = ТаблицаРасчетовПоСрокам.Скопировать(Новый Структура("ЗаписьДоНачалаРасчета",Ложь));
				
				МассивДокументов = РасчетыДляПроверки.ВыгрузитьКолонку("ДокументРегистратор");
				МассивДокументов.Добавить(СтрокаОсновныхПараметровРасчета.Регистратор);
				
				Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
				Запрос.УстановитьПараметр("ТаблицаПоСрокам", РасчетыДляПроверки);
				Запрос.УстановитьПараметр("МассивЗадействованныхРегистраторов",
					ГлобальныеПеременные["МассивЗадействованныхРегистраторов"
						+ ?(ГлобальныеПеременные.ЭтоРасчетыСКлиентами,"РасчетыСКлиентамиПоСрокам","РасчетыСПоставщикамиПоСрокам")]);
				Запрос.УстановитьПараметр("СоздаватьЗаданияКЗакрытиюСледующегоМесяца", ТипЗнч(СтрокаОсновныхПараметровРасчета.Регистратор) = Тип("ДокументСсылка.ВводОстатковВзаиморасчетов"));
				Запрос.УстановитьПараметр("ПериодКонтроляИзменений", СтрокаОсновныхПараметровРасчета.ПериодКонтроляИзменений);
				
				Запрос.Выполнить();
				
				Если ПланыОбмена.ГлавныйУзел() = Неопределено Тогда
					РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов.СоздатьЗаданияНеоперативнойОчередиПриИзмененииРасчетовПоСрокам(
						Запрос.МенеджерВременныхТаблиц, ГлобальныеПеременные.ЭтоРасчетыСКлиентами);
				КонецЕсли;
				
				// Не анализируем изменения по документу, инициирующему перерасчет:
				НеотражаемыйДокумент = СтрокаОсновныхПараметровРасчета.Регистратор;
				
				Если ГлобальныеПеременные.ЭтоРасчетыСКлиентами Тогда
					
					Запрос.УстановитьПараметр("НеотражаемыйДокумент", НеотражаемыйДокумент);
					
					ТекстАктуализацииОтражения =
					"ВЫБРАТЬ
					|	ТаблицаИзменений.ДокументРегистратор КАК Регистратор
					|ИЗ
					|	ТаблицаИзмененийРасчетыСКлиентамиПоСрокам КАК ТаблицаИзменений
					|
					|ГДЕ
					|	ТаблицаИзменений.ДокументРегистратор <> &НеотражаемыйДокумент
					|
					|СГРУППИРОВАТЬ ПО
					|	ТаблицаИзменений.ДокументРегистратор";
							
					//++ НЕ УТ
					ТекстАктуализацииОтражения =
					"ВЫБРАТЬ
					|	ТаблицаИзменений.ДокументРегистратор КАК Регистратор,
					|	ТаблицаИзменений.Период КАК Период,
					|	ТаблицаИзменений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
					|ИЗ
					|	ТаблицаИзмененийРасчетыСКлиентамиПоСрокам КАК ТаблицаИзменений
					|
					|ГДЕ
					|	ТаблицаИзменений.ДокументРегистратор <> &НеотражаемыйДокумент
					|
					|СГРУППИРОВАТЬ ПО
					|	ТаблицаИзменений.ДокументРегистратор,
					|	ТаблицаИзменений.Период,
					|	ТаблицаИзменений.АналитикаУчетаПоПартнерам";
					//-- НЕ УТ
						
					Запрос.Текст = ТекстАктуализацииОтражения;
					
					ТаблицаИзменений = Запрос.Выполнить().Выгрузить();
					
					#Область ПроверкаДатыЗапрета
					
					ЗапросПроверкиДатыЗапрета = Новый Запрос("
					|ВЫБРАТЬ
					|	ЕСТЬNULL(МИНИМУМ(ТаблицаИзменений.Период),ДАТАВРЕМЯ(3999,1,1)) КАК Период
					|ИЗ
					|	ТаблицаИзмененийРасчетыСКлиентамиПоСрокам КАК ТаблицаИзменений
					|СГРУППИРОВАТЬ ПО
					|	ЕСТЬNULL(ТаблицаИзменений.ОбъектРасчетов.Организация, НЕОПРЕДЕЛЕНО)");
					
					ЗапросПроверкиДатыЗапрета.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
					ВыборкаМинимальногоПериода = ЗапросПроверкиДатыЗапрета.Выполнить().Выбрать();
					ДанныеДляПроверки = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
					Пока ВыборкаМинимальногоПериода.Следующий() Цикл
						НоваяСтрока = ДанныеДляПроверки.Добавить();
						НоваяСтрока.Дата   = НачалоДня(ВыборкаМинимальногоПериода.Период);
						НоваяСтрока.Объект = СтрокаОсновныхПараметровРасчета.Организация;
						НоваяСтрока.Раздел = "ФинансовыйКонтур";
					КонецЦикла;
					
					ОписаниеОшибки = НСтр("ru = 'Изменяемые данные взаиморасчетов находятся в закрытом периоде.';
											|en = 'AR/AP data being changed is in the closed period.'");
					
					Если ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ДанныеДляПроверки, НаборЗаписейПоСрокам) Тогда
						ВызватьИсключение ОписаниеОшибки;
					КонецЕсли;
					#КонецОбласти
					
					//++ НЕ УТ
					РеглУчетСервер.ЗарегистрироватьДокументыРасчетовСПартнерамиКОтражениюВРеглУчете(ТаблицаИзменений);
					//-- НЕ УТ
	
					//++ НЕ УТКА
					МеждународныйУчетПроведениеСервер.ЗарегистрироватьДокументыРасчетовСПартнерамиКОтражениюВМеждународномУчете(ТаблицаИзменений);
					//-- НЕ УТКА
					
					Запрос.Текст =
					"ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	НАЧАЛОПЕРИОДА(Изменения.Период, МЕСЯЦ)         КАК Период,
					|	Изменения.РасчетныйДокумент                    КАК Документ,
					|	Изменения.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент
					|ИЗ
					|	ТаблицаИзмененийРасчетыСКлиентамиПоСрокам КАК Изменения";
					
					ИзмененияНДСУП = Запрос.Выполнить().Выгрузить();
					
					ЭмуляцияДокумента = ПроведениеДокументов.ЭмуляцияДокумента(Неопределено, Неопределено);
					ЭмуляцияДокумента.ДополнительныеСвойства.ПроведениеДокументов.СвойстваДокумента =
						Новый Структура(ЭмуляцияДокумента.ДополнительныеСвойства.ПроведениеДокументов.СвойстваДокумента);
					ЭмуляцияДокумента.ДополнительныеСвойства.ПроведениеДокументов.СвойстваДокумента.РежимЗаписи = ?(
						ДополнительныеСвойстваПроведения <> Неопределено,
						ДополнительныеСвойстваПроведения.СвойстваДокумента.РежимЗаписи,
						РежимЗаписиДокумента.Проведение);
					ЗакрытиеМесяцаСервер.ОтразитьЗаданияКЗакрытиюМесяца(ЭмуляцияДокумента, Запрос.МенеджерВременныхТаблиц);
					
				Иначе
					
					Запрос.УстановитьПараметр("НеотражаемыйДокумент", НеотражаемыйДокумент);
					
					ТекстАктуализацииОтражения =
					"ВЫБРАТЬ
					|	ТаблицаИзменений.ДокументРегистратор КАК Регистратор
					|ИЗ
					|	ТаблицаИзмененийРасчетыСПоставщикамиПоСрокам КАК ТаблицаИзменений
					|
					|ГДЕ
					|	ТаблицаИзменений.ДокументРегистратор <> &НеотражаемыйДокумент
					|
					|СГРУППИРОВАТЬ ПО
					|	ТаблицаИзменений.ДокументРегистратор";
					
					//++ НЕ УТ
					ТекстАктуализацииОтражения =
					"ВЫБРАТЬ
					|	ТаблицаИзменений.ДокументРегистратор КАК Регистратор,
					|	ТаблицаИзменений.Период КАК Период,
					|	ТаблицаИзменений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
					|ИЗ
					|	ТаблицаИзмененийРасчетыСПоставщикамиПоСрокам КАК ТаблицаИзменений
					|
					|ГДЕ
					|	ТаблицаИзменений.ДокументРегистратор <> &НеотражаемыйДокумент
					|
					|СГРУППИРОВАТЬ ПО
					|	ТаблицаИзменений.ДокументРегистратор,
					|	ТаблицаИзменений.Период,
					|	ТаблицаИзменений.АналитикаУчетаПоПартнерам";
					//-- НЕ УТ
						
					Запрос.Текст = ТекстАктуализацииОтражения;
						
					ТаблицаИзменений = Запрос.Выполнить().Выгрузить();
					
					#Область ПроверкаДатыЗапрета
					
					ЗапросПроверкиДатыЗапрета = Новый Запрос("ВЫБРАТЬ
					|	ЕСТЬNULL(МИНИМУМ(ТаблицаИзменений.Период), ДАТАВРЕМЯ(3999, 1, 1)) КАК Период
					|ИЗ
					|	ТаблицаИзмененийРасчетыСПоставщикамиПоСрокам КАК ТаблицаИзменений
					|СГРУППИРОВАТЬ ПО
					|	ЕСТЬNULL(ТаблицаИзменений.ОбъектРасчетов.Организация, НЕОПРЕДЕЛЕНО)");
					
					ЗапросПроверкиДатыЗапрета.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
					ВыборкаМинимальногоПериода = ЗапросПроверкиДатыЗапрета.Выполнить().Выбрать();
					ДанныеДляПроверки = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
					
					Пока ВыборкаМинимальногоПериода.Следующий() Цикл
						НоваяСтрока = ДанныеДляПроверки.Добавить();
						НоваяСтрока.Дата = НачалоДня(ВыборкаМинимальногоПериода.Период);
						НоваяСтрока.Объект = СтрокаОсновныхПараметровРасчета.Организация;
						НоваяСтрока.Раздел = "ФинансовыйКонтур";
					КонецЦикла;
					ОписаниеОшибки = НСтр("ru = 'Изменяемые данные взаиморасчетов находятся в закрытом периоде.';
											|en = 'AR/AP data being changed is in the closed period.'");
					Если ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ДанныеДляПроверки, НаборЗаписейПоСрокам) Тогда
						ВызватьИсключение ОписаниеОшибки;
					КонецЕсли;
					#КонецОбласти
					
					//++ НЕ УТ
					РеглУчетСервер.ЗарегистрироватьДокументыРасчетовСПартнерамиКОтражениюВРеглУчете(ТаблицаИзменений);
					//-- НЕ УТ
	
					//++ НЕ УТКА
					МеждународныйУчетПроведениеСервер.ЗарегистрироватьДокументыРасчетовСПартнерамиКОтражениюВМеждународномУчете(ТаблицаИзменений);
					//-- НЕ УТКА
					Запрос.Текст =
					"ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	НАЧАЛОПЕРИОДА(Изменения.Период, МЕСЯЦ)         КАК Период,
					|	Изменения.ДокументРегистратор                          КАК Документ,
					|	Изменения.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент
					|ИЗ
					|	ТаблицаИзмененийРасчетыСПоставщикамиПоСрокам КАК Изменения
					|
					|ОБЪЕДИНИТЬ
					|
					|ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	НАЧАЛОПЕРИОДА(Изменения.Период, МЕСЯЦ)         КАК Период,
					|	Изменения.РасчетныйДокумент                    КАК Документ,
					|	Изменения.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент
					|ИЗ
					|	ТаблицаИзмененийРасчетыСПоставщикамиПоСрокам КАК Изменения";
					
					ИзмененияНДСУП = Запрос.Выполнить().Выгрузить();
					
					ЭмуляцияДокумента = ПроведениеДокументов.ЭмуляцияДокумента(Неопределено, Неопределено);
					ЭмуляцияДокумента.ДополнительныеСвойства.ПроведениеДокументов.СвойстваДокумента =
						Новый Структура(ЭмуляцияДокумента.ДополнительныеСвойства.ПроведениеДокументов.СвойстваДокумента);
					ЭмуляцияДокумента.ДополнительныеСвойства.ПроведениеДокументов.СвойстваДокумента.РежимЗаписи = ?(
						ДополнительныеСвойстваПроведения <> Неопределено,
						ДополнительныеСвойстваПроведения.СвойстваДокумента.РежимЗаписи,
						РежимЗаписиДокумента.Проведение);
					ЗакрытиеМесяцаСервер.ОтразитьЗаданияКЗакрытиюМесяца(ЭмуляцияДокумента, Запрос.МенеджерВременныхТаблиц);
					
				КонецЕсли;
				
				РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(Запрос, "ВтДвиженияПосле");
		
			КонецЕсли;
			
			#КонецОбласти
			
			ЗаписатьПорционно(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, НаборЗаписейПоСрокам);
			
			Если Не СтрокаОсновныхПараметровРасчета.ПерезаполнениеРегистровВзаиморасчетов Тогда
				Если ГлобальныеПеременные.ЭтоРасчетыСКлиентами Тогда
					УчетНДСУП.ОтразитьВУчетеНДСИзменениеРасчетовСКлиентами(ИзмененияНДСУП);
					// Уничтожаем временную таблицу изменений, т.к. могут записываться разные наборы записей с одним менеджером запросов.
					РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(Запрос, "ТаблицаИзмененийРасчетыСКлиентамиПоСрокам");
				Иначе
					УчетНДСУП.ОтразитьВУчетеНДСИзменениеРасчетовСПоставщиками(ИзмененияНДСУП);
					// Уничтожаем временную таблицу изменений, т.к. могут записываться разные наборы записей с одним менеджером запросов.
					РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(Запрос, "ТаблицаИзмененийРасчетыСПоставщикамиПоСрокам");
				КонецЕсли;
			КонецЕсли;
			
			КонецЕсли;
			
			Если ТаблицаИзменений <> Неопределено И ТаблицаИзменений.Количество() > 0 Тогда
				УдалитьСистемныеКорректировкиРегистров(СтрокаОсновныхПараметровРасчета.ДатаНачалаПересчета, СтрокаОсновныхПараметровРасчета.Организация,, СтрокаОсновныхПараметровРасчета.Контрагент);
			КонецЕсли;
		
		Если СтрокаОсновныхПараметровРасчета.ЗаписыватьИзменения Тогда
		
			//Приходные движения пишутся непосредственно документами
			ТаблицаПланОплат = ТаблицаПланОплат.Скопировать(Новый Структура("ВидДвижения", ВидДвиженияНакопления.Расход));
			ТаблицаПланОплат.Свернуть("Период, Регистратор, ВидДвижения, АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта, ДокументРегистратор,
					|ДокументПлан, ДатаПлановогоПогашения, ДатаВозникновения, ВариантОплаты, ПорядокОперации, НераспределенныйАванс, ДокументОплаты",
					"КОплате");
			
			ТаблицаПланОплат.ЗаполнитьЗначения(ГлобальныеПеременные.АналитикаУчетаПоПартнерам, "АналитикаУчетаПоПартнерам");
			ТаблицаПланОплат.ЗаполнитьЗначения(ГлобальныеПеременные.ОбъектРасчетов,            "ОбъектРасчетов");
			ТаблицаПланОплат.ЗаполнитьЗначения(ГлобальныеПеременные.ВалютаРасчетов,            "Валюта");
			ТаблицаПланОплат.Индексы.Добавить("Регистратор");
		
			ПодготовитьДанныеДляЗаписи(ГлобальныеПеременные, ТаблицаПланОплат, НаборЗаписейПланОплат, ТаблицаРегистраторов);
			ЗаписатьПорционно(ГлобальныеПеременные, ТаблицаПланОплат, НаборЗаписейПланОплат);
			
		КонецЕсли;
		
		Если СтрокаОсновныхПараметровРасчета.ЗаписыватьИзменения Тогда
			
			ТаблицаПланОтгрузокПоставок = ТаблицаПланОтгрузокПоставок.Скопировать(Новый Структура("ВидДвижения", ВидДвиженияНакопления.Расход));
			
			ТаблицаПланОтгрузокПоставок.ЗаполнитьЗначения(ГлобальныеПеременные.АналитикаУчетаПоПартнерам, "АналитикаУчетаПоПартнерам");
			ТаблицаПланОтгрузокПоставок.ЗаполнитьЗначения(ГлобальныеПеременные.ОбъектРасчетов,            "ОбъектРасчетов");
			ТаблицаПланОтгрузокПоставок.ЗаполнитьЗначения(ГлобальныеПеременные.ВалютаРасчетов,            "Валюта");
			ТаблицаПланОтгрузокПоставок.Индексы.Добавить("Регистратор");
		
			ПодготовитьДанныеДляЗаписи(ГлобальныеПеременные, ТаблицаПланОтгрузокПоставок, НаборЗаписейПланОтгрузокПоставок, ТаблицаРегистраторов);
			ЗаписатьПорционно(ГлобальныеПеременные, ТаблицаПланОтгрузокПоставок, НаборЗаписейПланОтгрузокПоставок);
			
		КонецЕсли;
		
		Если ПланыОбмена.ГлавныйУзел() = Неопределено И НЕ СтрокаОсновныхПараметровРасчета.НачальноеЗаполнение И СтрокаОсновныхПараметровРасчета.ЗаписыватьИзменения Тогда
			УдалитьНеиспользуемыеРегистраторыРасчетов(Запрос, ИмяРегистраРасчетов, ИмяРегистраПланаОплат, ИмяРегистраПланаОтгрузкиПоставки);
		КонецЕсли;
		
		// Передаем по стеку проведения таблицу получившихся взаиморасчетов, если это проведение документа.
		Если ДополненнаяТаблицаПараметровРаспределения.Количество() = 1 И Не СтрокаОсновныхПараметровРасчета.НачальноеЗаполнение
			И ДополнительныеСвойстваПроведения <> Неопределено Тогда
			
			Если ГлобальныеПеременные.ЭтоРасчетыСКлиентами Тогда
				ИмяРегистра = "РасчетыСКлиентамиПоСрокам";
			Иначе 
				ИмяРегистра = "РасчетыСПоставщикамиПоСрокам";
			КонецЕсли;
			ДополнительныеСвойстваПроведения.Вставить(
				ИмяРегистра,
				ТаблицаРасчетовПоСрокам);
			
		КонецЕсли;
		
		#КонецОбласти
		
	КонецЦикла;
	
КонецПроцедуры

//Вызывает пересчет измененных и связанных расчетов по таблице заданий.
//
// Параметры:
//	Параметры - см. ПараметрыРаспределенияРасчетов
//	ДополнительныеСвойства - Структура - Дополнительные свойства проведения.
//
Процедура ЗаполнитьОперативныеВзаиморасчетыПоТаблице(Параметры, ДополнительныеСвойства = Неопределено) Экспорт
	
	Если Параметры.ТаблицаИзменений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Регистратор             = Параметры.Регистратор;
	ТаблицаКРаспределению   = Параметры.ТаблицаИзменений;
	
	// Ищем все зависимые объекты расчетов, затрагиваемые перераспределением текущих и добавляем в общую таблицу
	Если Параметры.ДополнитьТаблицуИзмененийПриемниками И Параметры.РаспределениеСУчетомПриемников Тогда
		ДополнитьТаблицуИзмененийПриемниками(ТаблицаКРаспределению, Параметры);
	КонецЕсли;
	ТаблицаКРаспределению.Сортировать("ДатаНачалаПересчета, Документ, ПоДаннымОбъектаРасчетовИсточника");
	
	// Разделим все строки на порции: распределение каждой следующей порции зависит от распределения предыдущей
	ТаблицаКРаспределению.Колонки.Добавить("НомерПорции", Новый ОписаниеТипов("Число"));
	ТаблицаКРаспределению.Индексы.Добавить("АналитикаУчетаПоПартнерамПриемник, ОбъектРасчетовПриемник, ВалютаПриемник");
	Отбор = Новый Структура("АналитикаУчетаПоПартнерамПриемник, ОбъектРасчетовПриемник, ВалютаПриемник");
	
	МаксимальныйНомерПорции = 0;
	Если Параметры.РаспределениеСУчетомПриемников Тогда
		Для Каждого СтрокаКРаспределению Из ТаблицаКРаспределению Цикл
			
			Отбор.АналитикаУчетаПоПартнерамПриемник = СтрокаКРаспределению.АналитикаУчетаПоПартнерам;
			Отбор.ОбъектРасчетовПриемник = СтрокаКРаспределению.ОбъектРасчетов;
			Отбор.ВалютаПриемник = СтрокаКРаспределению.ВалютаРасчетов;
			
			СтрокиИсточники = ТаблицаКРаспределению.НайтиСтроки(Отбор);
			
			Для Каждого СтрокаИсточник Из СтрокиИсточники Цикл
				// СтрокаКРаспределению - это приемник найденной строки и должен распределиться в следующей проции если имеет тот же или поздний день
				Если СтрокаИсточник.ДатаНачалаПересчета <= СтрокаКРаспределению.ДатаНачалаПересчета 
					И СтрокаКРаспределению.НомерПорции <= СтрокаИсточник.НомерПорции Тогда
					СтрокаКРаспределению.НомерПорции = СтрокаИсточник.НомерПорции + 1;
					МаксимальныйНомерПорции = Макс(МаксимальныйНомерПорции, СтрокаКРаспределению.НомерПорции);
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
	ТаблицаКРаспределению.Индексы.Добавить("НомерПорции,ТипРасчетов,АналитикаУчетаПоПартнерам,ОбъектРасчетов,ВалютаРасчетов");
	Отбор = Новый Структура("НомерПорции,ТипРасчетов,АналитикаУчетаПоПартнерам,ОбъектРасчетов,ВалютаРасчетов");
	
	ОсновныеПараметры = СтруктураПараметровЗаполненияВзаиморасчетов();
	ОсновныеПараметры.ПерезаполнениеРегистровВзаиморасчетов = Параметры.ПерезаполнениеРегистровВзаиморасчетов;
	
	КортежиТаблицыИзменений = ТаблицаКРаспределению.Скопировать(, "НомерПорции,ТипРасчетов,АналитикаУчетаПоПартнерам,ОбъектРасчетов,ВалютаРасчетов");
	КортежиТаблицыИзменений.Свернуть("НомерПорции,ТипРасчетов,АналитикаУчетаПоПартнерам,ОбъектРасчетов,ВалютаРасчетов");
	КортежиТаблицыИзменений.Индексы.Добавить("НомерПорции");
	
	// Выполним по порядку распределение каждой порции
	Для НомерПорции = 0 По МаксимальныйНомерПорции Цикл
		
		СтрокиКортежей = КортежиТаблицыИзменений.НайтиСтроки(Новый Структура("НомерПорции", НомерПорции));
		ТаблицаПараметровРаспределения = ТаблицаПараметровРаспределения();
		
		Для Каждого СтрокаКортежа Из СтрокиКортежей Цикл
			
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаКортежа);
			
			СтрокиИзменений = ТаблицаКРаспределению.НайтиСтроки(Отбор);
			
			СтрокаОсновныхПараметровРасчета = ТаблицаПараметровРаспределения.Добавить();
			
			// Заполняем основные параметры по умолчанию
			ЗаполнитьЗначенияСвойств(СтрокаОсновныхПараметровРасчета, ОсновныеПараметры);
			// Заполняем ключевые поля - аналитика учета по партнерам, объект расчетов, валюта и тип расчетов
			ЗаполнитьЗначенияСвойств(СтрокаОсновныхПараметровРасчета, СтрокаКортежа);
			// Заполняем порядок факт и порядок план (минимум из изменяемых строк)
			Для Каждого СтрокаКРаспределению Из СтрокиИзменений Цикл
				
				Если СтрокаКРаспределению.НачальноеЗаполнение Тогда
					СтрокаОсновныхПараметровРасчета.НачальноеЗаполнение = Истина;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаОсновныхПараметровРасчета.ДатаНачалаПересчета) Тогда
					СтрокаОсновныхПараметровРасчета.ДатаНачалаПересчета = Мин(СтрокаКРаспределению.ДатаНачалаПересчета,
					                                                          СтрокаОсновныхПараметровРасчета.ДатаНачалаПересчета);
				Иначе
					СтрокаОсновныхПараметровРасчета.ДатаНачалаПересчета = СтрокаКРаспределению.ДатаНачалаПересчета;
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(СтрокаОсновныхПараметровРасчета.ПериодКонтроляИзменений)
					Или СтрокаОсновныхПараметровРасчета.ПериодКонтроляИзменений > СтрокаКРаспределению.ПериодКонтроляИзменений Тогда
					СтрокаОсновныхПараметровРасчета.ПериодКонтроляИзменений = СтрокаКРаспределению.ПериодКонтроляИзменений;
				КонецЕсли;
				
			КонецЦикла;
			
			СтрокаОсновныхПараметровРасчета.ЭтоРасчетыСКлиентами = СтрокаКортежа.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
			СтрокаОсновныхПараметровРасчета.Регистратор = Регистратор;
			
		КонецЦикла;
		
		ЗаполнитьОперативныеВзаиморасчеты(ТаблицаПараметровРаспределения, ДополнительныеСвойства);
		
	КонецЦикла;
	
КонецПроцедуры

//Пересчитывает все курсовые разницы за указанный период.
//
// Параметры:
//	МассивОрганизаций - Массив из СправочникСсылка.Организации- Перечень организаций по которым необходимо выполнить переоценку.
//	НачалоПериода - Дата - Начало периода пересчета курсовых разниц.
//	КонецПериода - Дата - Конец периода пересчета курсовых разниц.
//  Параметры - см. ПараметрыПереоценки
//
Процедура ВыполнитьПереоценкуЗаПериод(МассивОрганизаций, Знач НачалоПериода, Знач КонецПериода, Параметры) Экспорт
	
	ИмяЗамера = "Взаиморасчеты.РасчетКурсовыхРазницПоСрокам" + ?(Параметры.ЭтоРасчетыСКлиентами,".Клиенты",".Поставщики");
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(ИмяЗамера);
	Параметры.Вставить("ВсегоОбработано", 0);
	ДокументыПереоценки = Документы.РасчетКурсовыхРазниц.ДокументыПереоценкиПоОрганизациям(
		МассивОрганизаций, НачалоПериода, КонецПериода, Параметры.ЭтоРасчетыСКлиентами);	
	Параметры.Вставить("ДокументыПереоценки", ДокументыПереоценки);	
	ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереоценкаРасчетовСПоставщиками;
	Если Параметры.ЭтоРасчетыСКлиентами Тогда
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереоценкаРасчетовСКлиентами;
	КонецЕсли;
	Параметры.Вставить("ХозяйственнаяОперация", ХозяйственнаяОперация);
	Параметры.Вставить("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	
	НачалоПериода = НачалоМесяца(НачалоПериода);
	КонецПериода = КонецМесяца(НачалоПериода);
	//++ НЕ УТ

	//++ Локализация
	Параметры.Вставить("ИспользоватьРеглУчет", ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет"));
	Если НЕ Параметры.ПерезаполнениеРегистровНУ Тогда
		Для Каждого Документ Из ДокументыПереоценки Цикл
			ОчиститьРегистрыНУ(Документ.Ссылка, Параметры.ЭтоРасчетыСКлиентами);
		КонецЦикла;
		ЗаполнитьДвиженияНУзаПериод(МассивОрганизаций, НачалоПериода, Параметры.ЭтоРасчетыСКлиентами);
	КонецЕсли;
	//-- Локализация

	//-- НЕ УТ
	
	ДниПереоценки = ДниПереоценки(МассивОрганизаций, НачалоПериода, КонецПериода, Параметры);
	Параметры.Вставить("ДниПереоценки", ДниПереоценки);
	УдалитьКурсовыеРазницыЗаПериод(МассивОрганизаций, НачалоПериода, КонецПериода, Параметры);
	Для Каждого День Из ДниПереоценки Цикл
		Дата = День.Дата;
		Если КонецДня(День.Дата) = КонецМесяца(День.Дата) Тогда
			Дата = КонецДня(День.Дата);
		КонецЕсли;
		ВыполнитьПереоценкуНаДату(МассивОрганизаций, Дата, Параметры);
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, Параметры.ВсегоОбработано);
	
КонецПроцедуры

//Добавляет записи переоценки на одну дату.
//
// Параметры:
//	МассивОрганизаций - Массив из СправочникСсылка.Организации - Перечень организаций по которым необходимо выполнить переоценку.
//	Период - Дата - Дата, на конец которой необходимо выполнить переоценку.
//  Параметры - см. ПараметрыПереоценки 
//
Процедура ВыполнитьПереоценкуНаДату(МассивОрганизаций, Период, Параметры) Экспорт

	ЭтоРасчетыСКлиентами = Параметры.ЭтоРасчетыСКлиентами;
	ПереоценкаПоДням = Параметры.ПереоценкаПоДням;
	
	Запрос = Новый Запрос;
	#Область ТекстЗапроса
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	АналитикиУчетаПоПартнерам.КлючАналитики                              КАК АналитикаУчетаПоПартнерам,
	|	АналитикиУчетаПоПартнерам.Организация                                КАК Организация,
	|	АналитикиУчетаПоПартнерам.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	&ВалютаУправленческогоУчета                                          КАК ВалютаУправленческогоУчета,
	|	АналитикиУчетаПоПартнерам.Договор                                    КАК Договор,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ПереоцениватьТоварыУслугиКОтчетуКомитенту, ЛОЖЬ) КАК ПереоцениватьТоварыУслугиКОтчетуКомитенту,
	|	НЕ ЕСТЬNULL(ДоговорыКонтрагентов.ПриПереоценкеЗадолженностиКлиентаНеПрименятьПоложения67ФЗ, ЛОЖЬ) КАК ПрименятьПоложения67ФЗ,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ТипДоговора, НЕОПРЕДЕЛЕНО)             КАК ТипДоговора,
	|	ЕСТЬNULL(
	|		ЕСТЬNULL(
	|			ДоговорыКонтрагентов.ВариантКурсаДоговора, 
	|			ДоговорыМеждуОрганизациями.ВариантКурсаДоговора), 
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.Переменный))         КАК ВариантКурсаДоговора
	|ПОМЕСТИТЬ ВтАналитикиУчетаПоПартнерам
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикиУчетаПоПартнерам 
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО АналитикиУчетаПоПартнерам.Договор = ДоговорыКонтрагентов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыМеждуОрганизациями КАК ДоговорыМеждуОрганизациями
	|		ПО АналитикиУчетаПоПартнерам.Договор = ДоговорыМеждуОрганизациями.Ссылка
	|ГДЕ
	|	АналитикиУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ВалютаРегламентированногоУчета,
	|	ВалютаУправленческогоУчета,
	|	Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обороты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Обороты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Обороты.Валюта КАК Валюта,
	|	Обороты.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	Обороты.РасчетныйДокумент КАК РасчетныйДокумент,
	|	Обороты.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	Обороты.ДатаВозникновения КАК ДатаВозникновения,
	|	Обороты.ДокументОплаты КАК ДокументОплаты,
	|	СУММА(Обороты.ДолгРасход) КАК ДолгРасход,
	|	СУММА(Обороты.ДолгРеглРасход) КАК ДолгРеглРасход,
	|	Обороты.ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами
	|
	|ПОМЕСТИТЬ втОборотыПоДокументамОплаты
	|ИЗ
	|	(ВЫБРАТЬ
	|		Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|		Расчеты.Валюта КАК Валюта,
	|		Расчеты.АналитикаУчетаПоПартнерам.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|		Расчеты.РасчетныйДокумент КАК РасчетныйДокумент,
	|		Расчеты.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|		Расчеты.ДатаВозникновения КАК ДатаВозникновения,
	|		ВЫБОР КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА Расчеты.Долг
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ДолгРасход,
	|		ВЫБОР КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА Расчеты.ДолгРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ДолгРеглРасход,
	|		Расчеты.ДокументРегистратор КАК ДокументОплаты,
	|		&ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитикиУчетаПоПартнерам КАК Аналитика
	|			ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитика.АналитикаУчетаПоПартнерам
	|				И (Расчеты.Валюта <> Аналитика.ВалютаРегламентированногоУчета
	|					ИЛИ Расчеты.Валюта <> &ВалютаУправленческогоУчета)
	|		ГДЕ
	|			Расчеты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|			И Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			И ТИПЗНАЧЕНИЯ(Расчеты.ДокументРегистратор) <> ТИП(Документ.РасчетКурсовыхРазниц)
	|			И Расчеты.Долг <> 0
	|			И Расчеты.ХозяйственнаяОперация <> &ЗачетАванса
	|	) КАК Обороты
	|
	|СГРУППИРОВАТЬ ПО
	|	Обороты.АналитикаУчетаПоПартнерам,
	|	Обороты.ОбъектРасчетов,
	|	Обороты.Валюта,
	|	Обороты.ВалютаРегламентированногоУчета,
	|	Обороты.РасчетныйДокумент,
	|	Обороты.ДатаПлановогоПогашения,
	|	Обороты.ДатаВозникновения,
	|	Обороты.ДокументОплаты,
	|	Обороты.ЭтоРасчетыСКлиентами
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта,
	|	РасчетныйДокумент,
	|	ДатаПлановогоПогашения,
	|	ДатаВозникновения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обороты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Обороты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Обороты.Валюта КАК Валюта,
	|	Обороты.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	Обороты.РасчетныйДокумент КАК РасчетныйДокумент,
	|	Обороты.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	Обороты.ДатаВозникновения КАК ДатаВозникновения,
	|	СУММА(Обороты.ДолгРасход) КАК ДолгРасход,
	|	СУММА(Обороты.ДолгРеглРасход) КАК ДолгРеглРасход,
	|	Обороты.ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами
	|
	|ПОМЕСТИТЬ втОборотыЗаДень
	|ИЗ
	|	втОборотыПоДокументамОплаты КАК Обороты
	|
	|СГРУППИРОВАТЬ ПО
	|	Обороты.АналитикаУчетаПоПартнерам,
	|	Обороты.ОбъектРасчетов,
	|	Обороты.Валюта,
	|	Обороты.ВалютаРегламентированногоУчета,
	|	Обороты.РасчетныйДокумент,
	|	Обороты.ДатаПлановогоПогашения,
	|	Обороты.ДатаВозникновения,
	|	Обороты.ЭтоРасчетыСКлиентами
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта,
	|	РасчетныйДокумент,
	|	ДатаПлановогоПогашения,
	|	ДатаВозникновения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обороты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Обороты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Обороты.Валюта КАК Валюта,
	|	Обороты.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	Обороты.РасчетныйДокумент КАК РасчетныйДокумент,
	|	СУММА(Обороты.ДолгРасход) КАК ДолгРасход,
	|	СУММА(Обороты.ДолгРеглРасход) КАК ДолгРеглРасход,
	|	Обороты.ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами
	|
	|ПОМЕСТИТЬ втОборотыЗаДеньБезДат
	|ИЗ
	|	втОборотыПоДокументамОплаты КАК Обороты
	|
	|СГРУППИРОВАТЬ ПО
	|	Обороты.АналитикаУчетаПоПартнерам,
	|	Обороты.ОбъектРасчетов,
	|	Обороты.Валюта,
	|	Обороты.ВалютаРегламентированногоУчета,
	|	Обороты.РасчетныйДокумент,
	|	Обороты.ЭтоРасчетыСКлиентами
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта,
	|	РасчетныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиИОбороты.АналитикаУчетаПоПартнерам    КАК АналитикаУчетаПоПартнерам,
	|	ОстаткиИОбороты.ОбъектРасчетов               КАК ОбъектРасчетов,
	|	ОстаткиИОбороты.Валюта                       КАК Валюта,
	|	ОстаткиИОбороты.ВалютаРегл                   КАК ВалютаРегл,
	|	ОстаткиИОбороты.РасчетныйДокумент            КАК РасчетныйДокумент,
	|	ОстаткиИОбороты.ДатаПлановогоПогашения       КАК ДатаПлановогоПогашения,
	|	ОстаткиИОбороты.ДатаВозникновения            КАК ДатаВозникновения,
	|	ОстаткиИОбороты.ВариантКурсаДоговора         КАК ВариантКурсаДоговора,
	|	СУММА(ОстаткиИОбороты.ПредоплатаОстаток)     КАК ПредоплатаОстаток,
	|	СУММА(ОстаткиИОбороты.ПредоплатаРеглОстаток) КАК ПредоплатаРеглОстаток,
	|	СУММА(ОстаткиИОбороты.ПредоплатаУпрОстаток)  КАК ПредоплатаУпрОстаток,
	|	СУММА(ОстаткиИОбороты.ДолгОстаток)           КАК ДолгОстаток,
	|	СУММА(ОстаткиИОбороты.ДолгРеглОстаток)       КАК ДолгРеглОстаток,
	|	СУММА(ОстаткиИОбороты.ДолгУпрОстаток)        КАК ДолгУпрОстаток,
	|	ОстаткиИОбороты.ЭтоРасчетыСКлиентами         КАК ЭтоРасчетыСКлиентами
	|ПОМЕСТИТЬ втОстаткиНаКонецДня
	|ИЗ 
	|	(ВЫБРАТЬ
	|		РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		РасчетыПоСрокамОстатки.ОбъектРасчетов            КАК ОбъектРасчетов,
	|		РасчетыПоСрокамОстатки.Валюта                    КАК Валюта,
	|		АналитикиУчетаПоПартнерам.ВалютаРегламентированногоУчета   КАК ВалютаРегл,
	|		РасчетыПоСрокамОстатки.РасчетныйДокумент         КАК РасчетныйДокумент,
	|		РасчетыПоСрокамОстатки.ДатаПлановогоПогашения    КАК ДатаПлановогоПогашения,
	|		РасчетыПоСрокамОстатки.ДатаВозникновения         КАК ДатаВозникновения,
	|		АналитикиУчетаПоПартнерам.ВариантКурсаДоговора             КАК ВариантКурсаДоговора,
	|		РасчетыПоСрокамОстатки.ПредоплатаОстаток         КАК ПредоплатаОстаток,
	|		РасчетыПоСрокамОстатки.ПредоплатаРеглОстаток     КАК ПредоплатаРеглОстаток,
	|		РасчетыПоСрокамОстатки.ПредоплатаУпрОстаток      КАК ПредоплатаУпрОстаток,
	|		РасчетыПоСрокамОстатки.ДолгОстаток               КАК ДолгОстаток,
	|		РасчетыПоСрокамОстатки.ДолгРеглОстаток           КАК ДолгРеглОстаток,
	|		РасчетыПоСрокамОстатки.ДолгУпрОстаток            КАК ДолгУпрОстаток,
	|		&ЭтоРасчетыСКлиентами                            КАК ЭтоРасчетыСКлиентами
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки(
	|				&НачалоПериода,
	|				НЕ (Валюта, Валюта) В (
	|					ВЫБРАТЬ 
	|						АналитикиУчетаПоПартнерам.ВалютаРегламентированногоУчета,
	|						АналитикиУчетаПоПартнерам.ВалютаУправленческогоУчета
	|					ИЗ
	|						ВтАналитикиУчетаПоПартнерам КАК АналитикиУчетаПоПартнерам
	|					)
	|			) КАК РасчетыПоСрокамОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитикиУчетаПоПартнерам КАК АналитикиУчетаПоПартнерам
	|				ПО РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам = АналитикиУчетаПоПартнерам.АналитикаУчетаПоПартнерам
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		РасчетыПоСрокамОбороты.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
	|		РасчетыПоСрокамОбороты.ОбъектРасчетов                    КАК ОбъектРасчетов,
	|		РасчетыПоСрокамОбороты.Валюта                            КАК Валюта,
	|		АналитикиУчетаПоПартнерам.ВалютаРегламентированногоУчета   КАК ВалютаРегл,
	|		РасчетыПоСрокамОбороты.РасчетныйДокумент                 КАК РасчетныйДокумент,
	|		РасчетыПоСрокамОбороты.ДатаПлановогоПогашения            КАК ДатаПлановогоПогашения,
	|		РасчетыПоСрокамОбороты.ДатаВозникновения                 КАК ДатаВозникновения,
	|		АналитикиУчетаПоПартнерам.ВариантКурсаДоговора             КАК ВариантКурсаДоговора,
	|		ВЫБОР КОГДА РасчетыПоСрокамОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыПоСрокамОбороты.Предоплата
	|			ИНАЧЕ -РасчетыПоСрокамОбороты.Предоплата
	|		КОНЕЦ                                                      КАК ПредоплатаОстаток,
	|		ВЫБОР КОГДА РасчетыПоСрокамОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыПоСрокамОбороты.ПредоплатаРегл
	|			ИНАЧЕ -РасчетыПоСрокамОбороты.ПредоплатаРегл
	|		КОНЕЦ                                                      КАК ПредоплатаРеглОстаток,
	|		ВЫБОР КОГДА РасчетыПоСрокамОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыПоСрокамОбороты.ПредоплатаУпр
	|			ИНАЧЕ -РасчетыПоСрокамОбороты.ПредоплатаУпр
	|		КОНЕЦ                                                      КАК ПредоплатаУпрОстаток,
	|		ВЫБОР КОГДА РасчетыПоСрокамОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыПоСрокамОбороты.Долг
	|			ИНАЧЕ -РасчетыПоСрокамОбороты.Долг
	|		КОНЕЦ                                                      КАК ДолгОстаток,
	|		ВЫБОР КОГДА РасчетыПоСрокамОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыПоСрокамОбороты.ДолгРегл
	|			ИНАЧЕ -РасчетыПоСрокамОбороты.ДолгРегл
	|		КОНЕЦ                                                      КАК ДолгРеглОстаток,
	|		ВЫБОР КОГДА РасчетыПоСрокамОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыПоСрокамОбороты.ДолгУпр
	|			ИНАЧЕ -РасчетыПоСрокамОбороты.ДолгУпр
	|		КОНЕЦ                                                      КАК ДолгУпрОстаток,
	|		&ЭтоРасчетыСКлиентами                                      КАК ЭтоРасчетыСКлиентами
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокамОбороты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитикиУчетаПоПартнерам КАК АналитикиУчетаПоПартнерам
	|				ПО РасчетыПоСрокамОбороты.АналитикаУчетаПоПартнерам = АналитикиУчетаПоПартнерам.АналитикаУчетаПоПартнерам
	|				И (РасчетыПоСрокамОбороты.Валюта <> АналитикиУчетаПоПартнерам.ВалютаРегламентированногоУчета
	|						ИЛИ РасчетыПоСрокамОбороты.Валюта <> АналитикиУчетаПоПартнерам.ВалютаУправленческогоУчета)
	|	ГДЕ
	|		РасчетыПоСрокамОбороты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И ТИПЗНАЧЕНИЯ(РасчетыПоСрокамОбороты.ДокументРегистратор) <> ТИП(Документ.РасчетКурсовыхРазниц)
	|) КАК ОстаткиИОбороты
	|		
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИОбороты.АналитикаУчетаПоПартнерам ,
	|	ОстаткиИОбороты.ОбъектРасчетов,
	|	ОстаткиИОбороты.Валюта,
	|	ОстаткиИОбороты.ВалютаРегл,
	|	ОстаткиИОбороты.РасчетныйДокумент,
	|	ОстаткиИОбороты.ДатаПлановогоПогашения,
	|	ОстаткиИОбороты.ДатаВозникновения,
	|	ОстаткиИОбороты.ВариантКурсаДоговора,
	|	ОстаткиИОбороты.ЭтоРасчетыСКлиентами
	|	
	|ИМЕЮЩИЕ
	|	СУММА(ОстаткиИОбороты.ПредоплатаОстаток) <> 0
	|	ИЛИ СУММА(ОстаткиИОбороты.ПредоплатаРеглОстаток) <> 0
	|	ИЛИ СУММА(ОстаткиИОбороты.ПредоплатаУпрОстаток) <> 0
	|	ИЛИ СУММА(ОстаткиИОбороты.ДолгОстаток) <> 0
	|	ИЛИ СУММА(ОстаткиИОбороты.ДолгРеглОстаток) <> 0
	|	ИЛИ СУММА(ОстаткиИОбороты.ДолгУпрОстаток) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта,
	|	РасчетныйДокумент,
	|	ДатаПлановогоПогашения,
	|	ДатаВозникновения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсВалюты.Валюта КАК Валюта,
	|	КурсВалюты.БазоваяВалюта КАК БазоваяВалюта,
	|	КурсВалюты.КурсЧислитель КАК КурсЧислитель,
	|	КурсВалюты.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсыВалют
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&ГраницаКонецДня) КАК КурсВалюты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютПоДоговорам.Договор КАК Договор,
	|	КурсыВалютПоДоговорам.КурсЧислитель КАК КурсЧислитель,
	|	КурсыВалютПоДоговорам.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ втКурсыВалютПоДоговорам
	|ИЗ
	|	РегистрСведений.КурсыВалютРасчетовПоДоговорам.СрезПоследних(&ГраницаКонецДня) КАК КурсыВалютПоДоговорам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНаКонецДня.АналитикаУчетаПоПартнерам                      КАК АналитикаУчетаПоПартнерам,
	|	ОстаткиНаКонецДня.ОбъектРасчетов                                 КАК ОбъектРасчетов,
	|	ОстаткиНаКонецДня.Валюта                                         КАК Валюта,
	|	ОстаткиНаКонецДня.ВалютаРегл                                     КАК ВалютаРегл,
	|	ОстаткиНаКонецДня.РасчетныйДокумент                              КАК РасчетныйДокумент,
	|	ОстаткиНаКонецДня.ДатаПлановогоПогашения                         КАК ДатаПлановогоПогашения,
	|	ОстаткиНаКонецДня.ДатаВозникновения                              КАК ДатаВозникновения,
	|	АналитикиУчетаПоПартнерам.ВариантКурсаДоговора                   КАК ВариантКурсаДоговора,
	|	АналитикиУчетаПоПартнерам.ПереоцениватьТоварыУслугиКОтчетуКомитенту КАК ПереоцениватьТоварыУслугиКОтчетуКомитенту,
	|	АналитикиУчетаПоПартнерам.ПрименятьПоложения67ФЗ                 КАК ПрименятьПоложения67ФЗ,
	|	АналитикиУчетаПоПартнерам.ТипДоговора                            КАК ТипДоговора,
	|	ОстаткиНаКонецДня.ПредоплатаОстаток                              КАК ПредоплатаОстаток,
	|	ОстаткиНаКонецДня.ПредоплатаРеглОстаток                          КАК ПредоплатаРеглОстаток,
	|	ОстаткиНаКонецДня.ПредоплатаУпрОстаток                           КАК ПредоплатаУпрОстаток,
	|	ОстаткиНаКонецДня.ДолгОстаток                                    КАК ДолгОстаток,
	|	ОстаткиНаКонецДня.ДолгРеглОстаток                                КАК ДолгРеглОстаток,
	|	ОстаткиНаКонецДня.ДолгУпрОстаток                                 КАК ДолгУпрОстаток,
	|	ВЫБОР КОГДА АналитикиУчетаПоПартнерам.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|			ТОГДА ЕСТЬNULL(КурсыВалютПоДоговорам.КурсЧислитель,1)
	|		КОГДА АналитикиУчетаПоПартнерам.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|				И ОстаткиНаКонецДня.ОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|			ТОГДА ЕСТЬNULL(КурсыВалютПоДокументам.КурсЧислительВалютыВзаиморасчетов,1)
	|		ИНАЧЕ 
	|			ЕСТЬNULL(КурсыВалют.КурсЧислитель,1)
	|	КОНЕЦ                                                                           КАК КурсЧислительРегл,
	|	ВЫБОР КОГДА АналитикиУчетаПоПартнерам.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|			ТОГДА ЕСТЬNULL(КурсыВалютПоДоговорам.КурсЗнаменатель,1)
	|		КОГДА АналитикиУчетаПоПартнерам.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|				И ОстаткиНаКонецДня.ОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|			ТОГДА ЕСТЬNULL(КурсыВалютПоДокументам.КурсЗнаменательВалютыВзаиморасчетов,1)
	|		ИНАЧЕ
	|			ЕСТЬNULL(КурсыВалют.КурсЗнаменатель,1)
	|	КОНЕЦ                                                                           КАК КурсЗнаменательРегл,
	|	ВЫБОР КОГДА АналитикиУчетаПоПартнерам.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|					И ОстаткиНаКонецДня.Валюта = &ВалютаУправленческогоУчета
	|			ТОГДА ЕСТЬNULL(КурсыВалютПоДоговорам.КурсЧислитель,1)
	|		КОГДА АналитикиУчетаПоПартнерам.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|				И ОстаткиНаКонецДня.ОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|			ТОГДА 
	|				ЕСТЬNULL(КурсыВалютПоДокументам.КурсЧислительВалютыУправленческогоУчета,1)
	|		ИНАЧЕ
	|			ЕСТЬNULL(КурсВалютыУпр.КурсЧислитель,1)
	|	КОНЕЦ                                                                           КАК КурсЧислительУпр,
	|	ВЫБОР КОГДА АналитикиУчетаПоПартнерам.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|					И ОстаткиНаКонецДня.Валюта = &ВалютаУправленческогоУчета
	|			ТОГДА ЕСТЬNULL(КурсыВалютПоДоговорам.КурсЗнаменатель,1)
	|		КОГДА АналитикиУчетаПоПартнерам.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|				И ОстаткиНаКонецДня.ОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|			ТОГДА 
	|				ЕСТЬNULL(КурсыВалютПоДокументам.КурсЗнаменательВалютыУправленческогоУчета,1)
	|		ИНАЧЕ 
	|			ЕСТЬNULL(КурсВалютыУпр.КурсЗнаменатель,1)
	|	КОНЕЦ                                                                           КАК КурсЗнаменательУпр,
	|	ОстаткиНаКонецДня.ЭтоРасчетыСКлиентами                                          КАК ЭтоРасчетыСКлиентами
	|ПОМЕСТИТЬ ВтОстаткиСКурсами
	|ИЗ
	|	втОстаткиНаКонецДня КАК ОстаткиНаКонецДня
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитикиУчетаПоПартнерам КАК АналитикиУчетаПоПартнерам
	|			ПО ОстаткиНаКонецДня.АналитикаУчетаПоПартнерам = АналитикиУчетаПоПартнерам.АналитикаУчетаПоПартнерам
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсВалютыУпр
	|			ПО ОстаткиНаКонецДня.ВалютаРегл = КурсВалютыУпр.БазоваяВалюта
	|			И КурсВалютыУпр.Валюта = &ВалютаУправленческогоУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсыВалют
	|			ПО ОстаткиНаКонецДня.Валюта = КурсыВалют.Валюта
	|			И ОстаткиНаКонецДня.ВалютаРегл = КурсыВалют.БазоваяВалюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалютПоДоговорам КАК КурсыВалютПоДоговорам
	|			ПО КурсыВалютПоДоговорам.Договор = АналитикиУчетаПоПартнерам.Договор
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВалютыИКурсыДокументов КАК КурсыВалютПоДокументам
	|			ПО ОстаткиНаКонецДня.ОбъектРасчетов.Объект = КурсыВалютПоДокументам.Документ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта,
	|	РасчетныйДокумент,
	|	ДатаПлановогоПогашения,
	|	ДатаВозникновения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтОстаткиСКурсами.АналитикаУчетаПоПартнерам                      КАК АналитикаУчетаПоПартнерам,
	|	ВтОстаткиСКурсами.ОбъектРасчетов                                 КАК ОбъектРасчетов,
	|	ВтОстаткиСКурсами.Валюта                                         КАК Валюта,
	|	ВтОстаткиСКурсами.РасчетныйДокумент                              КАК РасчетныйДокумент,
	|	ВтОстаткиСКурсами.ДатаПлановогоПогашения                         КАК ДатаПлановогоПогашения,
	|	ВтОстаткиСКурсами.ДатаВозникновения                              КАК ДатаВозникновения,
	|	ВтОстаткиСКурсами.ВариантКурсаДоговора                           КАК ВариантКурсаДоговора,
	|	ВтОстаткиСКурсами.ПереоцениватьТоварыУслугиКОтчетуКомитенту      КАК ПереоцениватьТоварыУслугиКОтчетуКомитенту,
	|	ВтОстаткиСКурсами.ПрименятьПоложения67ФЗ                         КАК ПрименятьПоложения67ФЗ,
	|	ВтОстаткиСКурсами.ТипДоговора                                    КАК ТипДоговора,
	|	ВтОстаткиСКурсами.ПредоплатаОстаток                              КАК ПредоплатаОстаток,
	|	ВтОстаткиСКурсами.ПредоплатаРеглОстаток                          КАК ПредоплатаРеглОстаток,
	|	ВтОстаткиСКурсами.ПредоплатаУпрОстаток                           КАК ПредоплатаУпрОстаток,
	|	ЕСТЬNULL(ОборотыЗаДень.ДолгРасход, 0)                            КАК ДолгРасход,
	|	ЕСТЬNULL(ОборотыЗаДень.ДолгРеглРасход, 0)                        КАК ДолгРеглРасход,
	|	ВтОстаткиСКурсами.ДолгОстаток                                    КАК ДолгОстаток,
	|	ВтОстаткиСКурсами.ДолгРеглОстаток                                КАК ДолгРеглОстаток,
	|	ВтОстаткиСКурсами.КурсЧислительРегл 
	|		/ ВтОстаткиСКурсами.КурсЗнаменательРегл                      КАК КурсРегл,
	|	ВЫРАЗИТЬ(ВтОстаткиСКурсами.ДолгОстаток 
	|		* ВтОстаткиСКурсами.КурсЧислительРегл / ВтОстаткиСКурсами.КурсЗнаменательРегл КАК ЧИСЛО(31,2)) КАК ДолгРеглОстатокРасчетный,
	|	ВтОстаткиСКурсами.ДолгУпрОстаток                                 КАК ДолгУпрОстаток,
	|	ВЫРАЗИТЬ(ВтОстаткиСКурсами.ДолгОстаток 
	|		* ВтОстаткиСКурсами.КурсЧислительРегл / ВтОстаткиСКурсами.КурсЗнаменательРегл
	|			/ ВтОстаткиСКурсами.КурсЧислительУпр * ВтОстаткиСКурсами.КурсЗнаменательУпр КАК ЧИСЛО(31,2)) КАК ДолгУпрОстатокРасчетный,
	|	ВтОстаткиСКурсами.ЭтоРасчетыСКлиентами                           КАК ЭтоРасчетыСКлиентами
	|ПОМЕСТИТЬ ВтКРасчету
	|ИЗ
	|	ВтОстаткиСКурсами КАК ВтОстаткиСКурсами
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОборотыЗаДень КАК ОборотыЗаДень
	|		ПО ВтОстаткиСКурсами.АналитикаУчетаПоПартнерам = ОборотыЗаДень.АналитикаУчетаПоПартнерам
	|			И ВтОстаткиСКурсами.ОбъектРасчетов = ОборотыЗаДень.ОбъектРасчетов
	|			И ВтОстаткиСКурсами.Валюта = ОборотыЗаДень.Валюта
	|			И ВтОстаткиСКурсами.РасчетныйДокумент = ОборотыЗаДень.РасчетныйДокумент
	|			И ВтОстаткиСКурсами.ДатаПлановогоПогашения = ОборотыЗаДень.ДатаПлановогоПогашения
	|			И ВтОстаткиСКурсами.ДатаВозникновения = ОборотыЗаДень.ДатаВозникновения
	|ГДЕ
	|	(НЕ ОборотыЗаДень.ОбъектРасчетов ЕСТЬ NULL
	|		ИЛИ &ДатаРасчета = КОНЕЦПЕРИОДА(&ДатаРасчета, МЕСЯЦ)
	|		ИЛИ &ПереоценкаПоДням)
	|	И (ВтОстаткиСКурсами.ДолгРеглОстаток
	|			- ВЫРАЗИТЬ(ВтОстаткиСКурсами.ДолгОстаток 
	|			* ВтОстаткиСКурсами.КурсЧислительРегл / ВтОстаткиСКурсами.КурсЗнаменательРегл КАК ЧИСЛО(31,2)) >= 0.01 
	|		ИЛИ ВтОстаткиСКурсами.ДолгРеглОстаток
	|			- ВЫРАЗИТЬ(ВтОстаткиСКурсами.ДолгОстаток 
	|			* ВтОстаткиСКурсами.КурсЧислительРегл / ВтОстаткиСКурсами.КурсЗнаменательРегл КАК ЧИСЛО(31,2)) <= -0.01 
	|		ИЛИ ВтОстаткиСКурсами.ДолгУпрОстаток
	|			- ВЫРАЗИТЬ(ВтОстаткиСКурсами.ДолгОстаток 
	|			* ВтОстаткиСКурсами.КурсЧислительРегл / ВтОстаткиСКурсами.КурсЗнаменательРегл
	|				/ ВтОстаткиСКурсами.КурсЧислительУпр * ВтОстаткиСКурсами.КурсЗнаменательУпр КАК ЧИСЛО(31,2)) >= 0.01
	|		ИЛИ ВтОстаткиСКурсами.ДолгУпрОстаток
	|			- ВЫРАЗИТЬ(ВтОстаткиСКурсами.ДолгОстаток 
	|			* ВтОстаткиСКурсами.КурсЧислительРегл / ВтОстаткиСКурсами.КурсЗнаменательРегл
	|				/ ВтОстаткиСКурсами.КурсЧислительУпр * ВтОстаткиСКурсами.КурсЗнаменательУпр КАК ЧИСЛО(31,2)) <= -0.01
	|		ИЛИ ВтОстаткиСКурсами.ПредоплатаОстаток = 0
	|			И (ВтОстаткиСКурсами.ПредоплатаРеглОстаток <> 0
	|				ИЛИ ВтОстаткиСКурсами.ПредоплатаУпрОстаток <> 0)
	|		ИЛИ ВтОстаткиСКурсами.ДолгОстаток = 0
	|			И (ВтОстаткиСКурсами.ДолгРеглОстаток <> 0
	|				ИЛИ ВтОстаткиСКурсами.ДолгУпрОстаток <> 0))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта,
	|	РасчетныйДокумент,
	|	ДатаПлановогоПогашения,
	|	ДатаВозникновения
	|;
	//++ НЕ УТ

	//++ Локализация
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиСКурсом.АналитикаУчетаПоПартнерам                        КАК АналитикаУчетаПоПартнерам,
	|	ОстаткиСКурсом.АналитикаУчетаПоПартнерам.Организация            КАК Организация,
	|	ОстаткиСКурсом.АналитикаУчетаПоПартнерам.Контрагент             КАК Контрагент,
	|	ОстаткиСКурсом.АналитикаУчетаПоПартнерам.Договор                КАК Договор,
	|	ОстаткиСКурсом.ОбъектРасчетов                                   КАК ОбъектРасчетов,
	|	ОстаткиСКурсом.ОбъектРасчетов.ГруппаФинансовогоУчета            КАК ГруппаФинансовогоУчета,
	|	ОстаткиСКурсом.Валюта                                           КАК Валюта,
	|	ОстаткиСКурсом.РасчетныйДокумент                                КАК РасчетныйДокумент,
	|	ОстаткиСКурсом.ВариантКурсаДоговора                             КАК ВариантКурсаДоговора,
	|	ОстаткиСКурсом.ПереоцениватьТоварыУслугиКОтчетуКомитенту        КАК ПереоцениватьТоварыУслугиКОтчетуКомитенту,
	|	ОстаткиСКурсом.ПрименятьПоложения67ФЗ                           КАК ПрименятьПоложения67ФЗ,
	|	ОстаткиСКурсом.ТипДоговора                                      КАК ТипДоговора,
	|	СУММА(ОстаткиСКурсом.ДолгОстаток)                               КАК ДолгОстаток,
	|	СУММА(ОстаткиСКурсом.ДолгРеглОстаток)                           КАК ДолгРеглОстаток,
	|	СУММА(ОстаткиСКурсом.СуммаНУОстаток)                            КАК СуммаНУОстаток,
	|	СУММА(ОстаткиСКурсом.ОтложенныйДоходОстаток)                    КАК ОтложенныйДоходОстаток,
	|	СУММА(ОстаткиСКурсом.ОтложенныйРасходОстаток)                   КАК ОтложенныйРасходОстаток,
	|	СУММА(ОстаткиСКурсом.ПредоплатаОстаток)                         КАК ПредоплатаОстаток,
	|	СУММА(ОстаткиСКурсом.ПредоплатаРеглОстаток)                     КАК ПредоплатаРеглОстаток,
	|	ОстаткиСКурсом.КурсЧислительРегл                                КАК КурсЧислительРегл,
	|	ОстаткиСКурсом.КурсЗнаменательРегл                              КАК КурсЗнаменательРегл,
	|	ВЫБОР КОГДА ОстаткиСКурсом.ЭтоРасчетыСКлиентами
	|		ТОГДА ВЫБОР КОГДА СУММА(ОстаткиСКурсом.ПредоплатаРеглОстаток) <> 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыПолученные)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами)
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР КОГДА СУММА(ОстаткиСКурсом.ПредоплатаРеглОстаток) <> 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыВыданные)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками)
	|			КОНЕЦ
	|	КОНЕЦ                                                           КАК ВидСчета,
	|	ОстаткиСКурсом.ЭтоРасчетыСКлиентами                             КАК ЭтоРасчетыСКлиентами
	|ПОМЕСТИТЬ ВтОстаткиНУСКурсом
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВтОстаткиСКурсами.АналитикаУчетаПоПартнерам                 КАК АналитикаУчетаПоПартнерам,
	|		ВтОстаткиСКурсами.ОбъектРасчетов                            КАК ОбъектРасчетов,
	|		ВтОстаткиСКурсами.Валюта                                    КАК Валюта,
	|		ВтОстаткиСКурсами.РасчетныйДокумент                         КАК РасчетныйДокумент,
	|		ВтОстаткиСКурсами.ВариантКурсаДоговора                      КАК ВариантКурсаДоговора,
	|		ВтОстаткиСКурсами.ПереоцениватьТоварыУслугиКОтчетуКомитенту КАК ПереоцениватьТоварыУслугиКОтчетуКомитенту,
	|		ВтОстаткиСКурсами.ПрименятьПоложения67ФЗ                    КАК ПрименятьПоложения67ФЗ,
	|		ВтОстаткиСКурсами.ТипДоговора                               КАК ТипДоговора,
	|		ВтОстаткиСКурсами.ДолгОстаток                               КАК ДолгОстаток,
	|		ВтОстаткиСКурсами.ДолгРеглОстаток                           КАК ДолгРеглОстаток,
	|		0                                                           КАК СуммаНУОстаток,
	|		0                                                           КАК ОтложенныйДоходОстаток,
	|		0                                                           КАК ОтложенныйРасходОстаток,
	|		ВтОстаткиСКурсами.ПредоплатаОстаток                         КАК ПредоплатаОстаток,
	|		ВтОстаткиСКурсами.ПредоплатаРеглОстаток                     КАК ПредоплатаРеглОстаток,
	|		ВтОстаткиСКурсами.КурсЧислительРегл                         КАК КурсЧислительРегл,
	|		ВтОстаткиСКурсами.КурсЗнаменательРегл                       КАК КурсЗнаменательРегл,
	|		ВтОстаткиСКурсами.ЭтоРасчетыСКлиентами                      КАК ЭтоРасчетыСКлиентами
	|	ИЗ
	|		ВтОстаткиСКурсами КАК ВтОстаткиСКурсами
	|	ГДЕ
	|		&ДатаРасчета >= ДАТАВРЕМЯ(2022,1,1)
	|		И ВтОстаткиСКурсами.Валюта <> ВтОстаткиСКурсами.ВалютаРегл
	|		И &ИспользоватьРеглУчет
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		РасчетыНУОстатки.АналитикаУчетаПоПартнерам                  КАК АналитикаУчетаПоПартнерам,
	|		РасчетыНУОстатки.ОбъектРасчетов                             КАК ОбъектРасчетов,
	|		РасчетыНУОстатки.Валюта                                     КАК Валюта,
	|		РасчетыНУОстатки.РасчетныйДокумент                          КАК РасчетныйДокумент,
	|		АналитикиУчетаПоПартнерам.ВариантКурсаДоговора              КАК ВариантКурсаДоговора,
	|		АналитикиУчетаПоПартнерам.ПереоцениватьТоварыУслугиКОтчетуКомитенту КАК ПереоцениватьТоварыУслугиКОтчетуКомитенту,
	|		АналитикиУчетаПоПартнерам.ПрименятьПоложения67ФЗ            КАК ПрименятьПоложения67ФЗ,
	|		АналитикиУчетаПоПартнерам.ТипДоговора                       КАК ТипДоговора,
	|		0                                                           КАК ДолгОстаток,
	|		0                                                           КАК ДолгРеглОстаток,
	|		РасчетыНУОстатки.СуммаНУОстаток                             КАК СуммаНУОстаток,
	|		РасчетыНУОстатки.ОтложенныйДоходОстаток                     КАК ОтложенныйДоходОстаток,
	|		РасчетыНУОстатки.ОтложенныйРасходОстаток                    КАК ОтложенныйРасходОстаток,
	|		0                                                           КАК ПредоплатаОстаток,
	|		0                                                           КАК ПредоплатаРеглОстаток,
	|		ВЫБОР КОГДА АналитикиУчетаПоПартнерам.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				ТОГДА ЕСТЬNULL(КурсыВалютПоДоговорам.КурсЧислитель,1)
	|			КОГДА АналитикиУчетаПоПартнерам.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|					И РасчетыНУОстатки.ОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|				ТОГДА 
	|					ЕСТЬNULL(КурсыВалютПоДокументам.КурсЧислительВалютыВзаиморасчетов,1)
	|			ИНАЧЕ ЕСТЬNULL(КурсыВалют.КурсЧислитель,1)
	|		КОНЕЦ                                                           КАК КурсЧислительРегл,
	|		ВЫБОР КОГДА АналитикиУчетаПоПартнерам.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
	|				ТОГДА ЕСТЬNULL(КурсыВалютПоДоговорам.КурсЗнаменатель,1)
	|			КОГДА АналитикиУчетаПоПартнерам.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
	|					И РасчетыНУОстатки.ОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|				ТОГДА 
	|					ЕСТЬNULL(КурсыВалютПоДокументам.КурсЗнаменательВалютыВзаиморасчетов,1)
	|			ИНАЧЕ ЕСТЬNULL(КурсыВалют.КурсЗнаменатель,1)
	|		КОНЕЦ                                                       КАК КурсЗнаменательРегл,
	|		&ЭтоРасчетыСКлиентами                                       КАК ЭтоРасчетыСКлиентами
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиНУ.Остатки(
	|				&ГраницаКонецДня,
	|				НЕ (АналитикаУчетаПоПартнерам, Валюта) В (
	|					ВЫБРАТЬ 
	|						АналитикиУчетаПоПартнерам.АналитикаУчетаПоПартнерам,
	|						АналитикиУчетаПоПартнерам.ВалютаРегламентированногоУчета
	|					ИЗ
	|						ВтАналитикиУчетаПоПартнерам КАК АналитикиУчетаПоПартнерам
	|					)
	|			) КАК РасчетыНУОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитикиУчетаПоПартнерам КАК АналитикиУчетаПоПартнерам
	|				ПО РасчетыНУОстатки.АналитикаУчетаПоПартнерам = АналитикиУчетаПоПартнерам.АналитикаУчетаПоПартнерам
	|			ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК КурсыВалют
	|				ПО РасчетыНУОстатки.Валюта = КурсыВалют.Валюта
	|				И КурсыВалют.БазоваяВалюта = АналитикиУчетаПоПартнерам.ВалютаРегламентированногоУчета
	|			ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалютПоДоговорам КАК КурсыВалютПоДоговорам
	|				ПО КурсыВалютПоДоговорам.Договор = АналитикиУчетаПоПартнерам.Договор
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВалютыИКурсыДокументов КАК КурсыВалютПоДокументам
	|				ПО РасчетыНУОстатки.ОбъектРасчетов.Объект = КурсыВалютПоДокументам.Документ
	|	ГДЕ
	|		&ДатаРасчета >= ДАТАВРЕМЯ(2022,1,1)
	|		И &ИспользоватьРеглУчет
	|	) КАК ОстаткиСКурсом
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиСКурсом.АналитикаУчетаПоПартнерам,
	|	ОстаткиСКурсом.ОбъектРасчетов,
	|	ОстаткиСКурсом.Валюта,
	|	ОстаткиСКурсом.РасчетныйДокумент,
	|	ОстаткиСКурсом.ВариантКурсаДоговора,
	|	ОстаткиСКурсом.ПереоцениватьТоварыУслугиКОтчетуКомитенту,
	|	ОстаткиСКурсом.ПрименятьПоложения67ФЗ,
	|	ОстаткиСКурсом.ТипДоговора,
	|	ОстаткиСКурсом.КурсЧислительРегл,
	|	ОстаткиСКурсом.КурсЗнаменательРегл,
	|	ОстаткиСКурсом.ЭтоРасчетыСКлиентами
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта,
	|	РасчетныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обороты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Обороты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Обороты.Валюта КАК ВалютаРасчетов,
	|	Обороты.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	Обороты.РасчетныйДокумент КАК РасчетныйДокумент,
	|	Обороты.ДокументОплаты КАК ДокументОплаты,
	|	СУММА(Обороты.ДолгРасход) КАК ДолгРасход,
	|	СУММА(Обороты.ДолгРеглРасход) КАК ДолгРеглРасход,
	|	Обороты.ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами,
	|	Обороты.ДокументОплаты.МоментВремени КАК МоментВремени
	|ИЗ
	|	втОборотыПоДокументамОплаты КАК Обороты
	|
	|СГРУППИРОВАТЬ ПО
	|	Обороты.АналитикаУчетаПоПартнерам,
	|	Обороты.ОбъектРасчетов,
	|	Обороты.Валюта,
	|	Обороты.ВалютаРегламентированногоУчета,
	|	Обороты.РасчетныйДокумент,
	|	Обороты.ДокументОплаты,
	|	Обороты.ЭтоРасчетыСКлиентами
	|	
	|УПОРЯДОЧИТЬ ПО
	|	Обороты.ДокументОплаты.МоментВремени УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНУСКурсом.АналитикаУчетаПоПартнерам                КАК АналитикаУчетаПоПартнерам,
	|	ОстаткиНУСКурсом.Организация                              КАК Организация,
	|	ОстаткиНУСКурсом.Контрагент                               КАК Контрагент,
	|	ОстаткиНУСКурсом.Договор                                  КАК Договор,
	|	ОстаткиНУСКурсом.ОбъектРасчетов                           КАК ОбъектРасчетов,
	|	ОстаткиНУСКурсом.ГруппаФинансовогоУчета                   КАК ГруппаФинансовогоУчета,
	|	ОстаткиНУСКурсом.Валюта                                   КАК ВалютаРасчетов,
	|	ОстаткиНУСКурсом.РасчетныйДокумент                        КАК РасчетныйДокумент,
	|	ОстаткиНУСКурсом.ВариантКурсаДоговора                     КАК ВариантКурсаДоговора,
	|	ОстаткиНУСКурсом.ПереоцениватьТоварыУслугиКОтчетуКомитенту КАК ПереоцениватьТоварыУслугиКОтчетуКомитенту,
	|	ОстаткиНУСКурсом.ПрименятьПоложения67ФЗ                   КАК ПрименятьПоложения67ФЗ,
	|	ОстаткиНУСКурсом.ТипДоговора                              КАК ТипДоговора,
	|	ЕСТЬNULL(ОборотыЗаДень.ДолгРасход, 0)                     КАК ДолгРасход,
	|	ЕСТЬNULL(ОборотыЗаДень.ДолгРеглРасход, 0)                 КАК ДолгРеглРасход,
	|	ОстаткиНУСКурсом.ДолгОстаток                              КАК ДолгОстаток,
	|	ОстаткиНУСКурсом.ДолгРеглОстаток                          КАК ДолгРеглОстаток,
	|	ОстаткиНУСКурсом.СуммаНУОстаток                           КАК СуммаНУОстаток,
	|	ОстаткиНУСКурсом.ОтложенныйДоходОстаток                   КАК ОтложенныйДоходОстаток,
	|	ОстаткиНУСКурсом.ОтложенныйРасходОстаток                  КАК ОтложенныйРасходОстаток,
	|	ОстаткиНУСКурсом.ПредоплатаОстаток                        КАК ПредоплатаОстаток,
	|	ОстаткиНУСКурсом.ПредоплатаРеглОстаток                    КАК ПредоплатаРеглОстаток,
	|	ОстаткиНУСКурсом.КурсЧислительРегл                        КАК КурсЧислительРегл,
	|	ОстаткиНУСКурсом.КурсЗнаменательРегл                      КАК КурсЗнаменательРегл,
	|	ОстаткиНУСКурсом.ВидСчета                                 КАК ВидСчета,
	|	ЕСТЬNULL(НастройкиПоАналитике.СчетУчета,
	|		ЕСТЬNULL(НастройкиПоОрганизации.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка))) КАК СчетУчета,
	|	ОстаткиНУСКурсом.ЭтоРасчетыСКлиентами                     КАК ЭтоРасчетыСКлиентами
	|ИЗ
	|	ВтОстаткиНУСКурсом КАК ОстаткиНУСКурсом
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОборотыЗаДеньБезДат КАК ОборотыЗаДень
	|		ПО ОстаткиНУСКурсом.АналитикаУчетаПоПартнерам = ОборотыЗаДень.АналитикаУчетаПоПартнерам
	|			И ОстаткиНУСКурсом.ОбъектРасчетов = ОборотыЗаДень.ОбъектРасчетов
	|			И ОстаткиНУСКурсом.Валюта = ОборотыЗаДень.Валюта
	|			И ОстаткиНУСКурсом.РасчетныйДокумент = ОборотыЗаДень.РасчетныйДокумент
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК НастройкиПоОрганизации
	|		ПО НастройкиПоОрганизации.ВидСчета = ОстаткиНУСКурсом.ВидСчета
	|			И НастройкиПоОрганизации.Организация = ОстаткиНУСКурсом.Организация
	|			И НастройкиПоОрганизации.АналитикаУчета = ОстаткиНУСКурсом.ГруппаФинансовогоУчета
	|			И НЕ НастройкиПоОрганизации.Долгосрочный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНаСчетахУчета КАК НастройкиПоАналитике
	|		ПО НастройкиПоАналитике.ВидСчета = ОстаткиНУСКурсом.ВидСчета
	|			И НастройкиПоАналитике.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			И НастройкиПоАналитике.АналитикаУчета = ОстаткиНУСКурсом.ГруппаФинансовогоУчета
	|			И НЕ НастройкиПоАналитике.Долгосрочный
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНалогаНаПрибыль.СрезПоследних(&НачалоПериода,) КАК НастройкиУчетаНалогаНаПрибыль
	|		ПО ОстаткиНУСКурсом.Организация = НастройкиУчетаНалогаНаПрибыль.Организация
	|;
	//-- Локализация

	//-- НЕ УТ
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОстаткиНаКонецДня.ОбъектРасчетов              КАК ОбъектРасчетов,
	|	ОстаткиНаКонецДня.АналитикаУчетаПоПартнерам   КАК АналитикаУчетаПоПартнерам,
	|	ОстаткиНаКонецДня.Валюта                      КАК Валюта,
	|	ОстаткиНаКонецДня.ЭтоРасчетыСКлиентами        КАК ЭтоРасчетыСКлиентами
	|ПОМЕСТИТЬ втАналитикаОстатков
	|ИЗ 
	|	втОстаткиНаКонецДня КАК ОстаткиНаКонецДня
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистраторРасчетов.Ссылка                      КАК Ссылка,
	|	РегистраторРасчетов.ОбъектРасчетов              КАК ОбъектРасчетов,
	|	РегистраторРасчетов.АналитикаУчетаПоПартнерам   КАК АналитикаУчетаПоПартнерам,
	|	РегистраторРасчетов.Валюта                      КАК ВалютаРасчетов,
	|	АналитикаОстатков.ЭтоРасчетыСКлиентами          КАК ЭтоРасчетыСКлиентами,
	|	КОЛИЧЕСТВО(ЕСТЬNULL(Таблица.НомерСтроки,0))     КАК ВсегоЗаписей
	|ИЗ
	|	Документ.РегистраторРасчетов КАК РегистраторРасчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАналитикаОстатков КАК АналитикаОстатков
	|			ПО АналитикаОстатков.ОбъектРасчетов = РегистраторРасчетов.ОбъектРасчетов
	|				И АналитикаОстатков.АналитикаУчетаПоПартнерам = РегистраторРасчетов.АналитикаУчетаПоПартнерам
	|				И АналитикаОстатков.Валюта = РегистраторРасчетов.Валюта
	|				И РегистраторРасчетов.ТипРасчетов = &ТипРасчетов
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ #ИмяТаблицы КАК Таблица
	|			ПО Таблица.Регистратор = РегистраторРасчетов.Ссылка
	|		
	|СГРУППИРОВАТЬ ПО
	|	РегистраторРасчетов.Ссылка,
	|	РегистраторРасчетов.ОбъектРасчетов,
	|	РегистраторРасчетов.АналитикаУчетаПоПартнерам,
	|	РегистраторРасчетов.Валюта,
	|	АналитикаОстатков.ЭтоРасчетыСКлиентами
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ЕСТЬNULL(Таблица.НомерСтроки,0)) < &РазмерПорции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыПоСрокамОбороты.Регистратор                       КАК Регистратор,
	|	&ЭтоРасчетыСКлиентами                                    КАК ЭтоРасчетыСКлиентами
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокамОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитикиУчетаПоПартнерам КАК АналитикиУчетаПоПартнерам
	|			ПО РасчетыПоСрокамОбороты.АналитикаУчетаПоПартнерам = АналитикиУчетаПоПартнерам.АналитикаУчетаПоПартнерам
	|ГДЕ
	|	РасчетыПоСрокамОбороты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТИПЗНАЧЕНИЯ(РасчетыПоСрокамОбороты.ДокументРегистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.АналитикаУчетаПоПартнерам                      КАК АналитикаУчетаПоПартнерам,
	|	Т.ОбъектРасчетов                                 КАК ОбъектРасчетов,
	|	Т.ВалютаРасчетов                                 КАК ВалютаРасчетов,
	|	Т.ЭтоРасчетыСКлиентами                           КАК ЭтоРасчетыСКлиентами,
	|	Т.Организация                                    КАК Организация
	|ИЗ (
	|	ВЫБРАТЬ
	|		ВтКРасчету.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
	|		ВтКРасчету.ОбъектРасчетов                    КАК ОбъектРасчетов,
	|		ВтКРасчету.Валюта                            КАК ВалютаРасчетов,
	|		ВтКРасчету.ЭтоРасчетыСКлиентами              КАК ЭтоРасчетыСКлиентами,
	|		Аналитика.Организация                        КАК Организация
	|	ИЗ ВтКРасчету КАК ВтКРасчету
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|			ПО ВтКРасчету.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	//++ НЕ УТ

	//++ Локализация
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВтКРасчетуНУ.АналитикаУчетаПоПартнерам       КАК АналитикаУчетаПоПартнерам,
	|		ВтКРасчетуНУ.ОбъектРасчетов                  КАК ОбъектРасчетов,
	|		ВтКРасчетуНУ.Валюта                          КАК ВалютаРасчетов,
	|		ВтКРасчетуНУ.ЭтоРасчетыСКлиентами            КАК ЭтоРасчетыСКлиентами,
	|		Аналитика.Организация                        КАК Организация
	|	ИЗ ВтОстаткиНУСКурсом КАК ВтКРасчетуНУ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО ВтКРасчетуНУ.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	//-- Локализация

	//-- НЕ УТ
	|) КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтКРасчету.ЭтоРасчетыСКлиентами                  КАК ЭтоРасчетыСКлиентами,
	|	ВтКРасчету.АналитикаУчетаПоПартнерам             КАК АналитикаУчетаПоПартнерам,
	|	ВтКРасчету.ОбъектРасчетов                        КАК ОбъектРасчетов,
	|	ВтКРасчету.Валюта                                КАК ВалютаРасчетов,
	|	ВтКРасчету.РасчетныйДокумент                     КАК РасчетныйДокумент,
	|	ВтКРасчету.ДатаПлановогоПогашения                КАК ДатаПлановогоПогашения,
	|	ВтКРасчету.ДатаВозникновения                     КАК ДатаВозникновения,
	|	ВтКРасчету.ВариантКурсаДоговора                  КАК ВариантКурсаДоговора,
	|	ВтКРасчету.ДолгРеглОстаток                       КАК ДолгРеглОстаток,
	|	ВтКРасчету.ДолгРеглОстатокРасчетный              КАК ДолгРеглОстатокРасчетный,
	|	ВтКРасчету.ДолгУпрОстаток                        КАК ДолгУпрОстаток,
	|	ВтКРасчету.ДолгУпрОстатокРасчетный               КАК ДолгУпрОстатокРасчетный,
	|	ВтКРасчету.ПредоплатаОстаток                     КАК ПредоплатаОстаток,
	|	ВтКРасчету.ПредоплатаРеглОстаток                 КАК ПредоплатаРеглОстаток,
	|	ВтКРасчету.ПредоплатаУпрОстаток                  КАК ПредоплатаУпрОстаток,
	|	МАКСИМУМ(ЕСТЬNULL(РасчетыПоСрокам.ПорядокЗачета, """")) КАК ПорядокЗачета,
	|	МАКСИМУМ(ЕСТЬNULL(РасчетыПоСрокам.ВалютаДокумента, """")) КАК ВалютаДокумента
	|ИЗ ВтКРасчету КАК ВтКРасчету
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокам
	|		ПО ВтКРасчету.ЭтоРасчетыСКлиентами
	|			И ВтКРасчету.АналитикаУчетаПоПартнерам = РасчетыПоСрокам.АналитикаУчетаПоПартнерам
	|			И ВтКРасчету.ОбъектРасчетов = РасчетыПоСрокам.ОбъектРасчетов
	|			И ВтКРасчету.Валюта = РасчетыПоСрокам.Валюта
	|			И ВтКРасчету.РасчетныйДокумент = РасчетыПоСрокам.РасчетныйДокумент
	|			И ВтКРасчету.ДатаПлановогоПогашения = РасчетыПоСрокам.ДатаПлановогоПогашения
	|			И РасчетыПоСрокам.Активность
	|СГРУППИРОВАТЬ ПО
	|	ВтКРасчету.ЭтоРасчетыСКлиентами,
	|	ВтКРасчету.АналитикаУчетаПоПартнерам,
	|	ВтКРасчету.ОбъектРасчетов,
	|	ВтКРасчету.Валюта,
	|	ВтКРасчету.РасчетныйДокумент,
	|	ВтКРасчету.ДатаПлановогоПогашения,
	|	ВтКРасчету.ДатаВозникновения,
	|	ВтКРасчету.ВариантКурсаДоговора,
	|	ВтКРасчету.ПредоплатаОстаток,
	|	ВтКРасчету.ПредоплатаРеглОстаток,
	|	ВтКРасчету.ПредоплатаУпрОстаток,
	|	ВтКРасчету.ДолгРеглОстаток,
	|	ВтКРасчету.ДолгРеглОстатокРасчетный,
	|	ВтКРасчету.ДолгУпрОстаток,
	|	ВтКРасчету.ДолгУпрОстатокРасчетный";
	#КонецОбласти

	Запрос.УстановитьПараметр("ЭтоРасчетыСКлиентами", ЭтоРасчетыСКлиентами);
	Если ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяТаблицы", Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.ПолноеИмя());
		Запрос.УстановитьПараметр("ЗачетАванса", Перечисления.ХозяйственныеОперации.ЗачетАвансаКлиента);
		Запрос.УстановитьПараметр("ТипРасчетов", Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяТаблицы", Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
		Запрос.УстановитьПараметр("ЗачетАванса", Перечисления.ХозяйственныеОперации.ЗачетАвансаПоставщику);
		Запрос.УстановитьПараметр("ТипРасчетов", Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
				Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.ПолноеИмя(),
				Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
		//++ НЕ УТ
	
		//++ Локализация
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			Метаданные.РегистрыНакопления.РасчетыСКлиентамиНУ.ПолноеИмя(),
			Метаданные.РегистрыНакопления.РасчетыСПоставщикамиНУ.ПолноеИмя());
		//-- Локализация
		
		//-- НЕ УТ
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", Параметры.ВалютаУправленческогоУчета);
	//++ НЕ УТ
	
	//++ Локализация
	Запрос.УстановитьПараметр("ИспользоватьРеглУчет", Параметры.ИспользоватьРеглУчет);
	//-- Локализация
	
	//-- НЕ УТ
	
	ГраницаКонецДня = Новый Граница(КонецДня(Период),ВидГраницы.Включая);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Период));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Период));
	Запрос.УстановитьПараметр("ГраницаКонецДня", ГраницаКонецДня);
	Запрос.УстановитьПараметр("ДатаРасчета", Период);
	Запрос.УстановитьПараметр("РазмерПорции", РазмерПорцииЗаписи());
	Если ПереоценкаПоДням = Неопределено Тогда
		ПереоценкаПоДням = Константы.ПереоцениватьВалютныеСредстваПоДням.Получить();
	КонецЕсли;
	Запрос.УстановитьПараметр("ПереоценкаПоДням", ПереоценкаПоДням);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ПоследнийИндекс = РезультатыЗапроса.ВГраница();
	ПоляПоиска = "ЭтоРасчетыСКлиентами, АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов"; 
	//++ НЕ УТ

	//++ Локализация
	ТипЧисло31_2 = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(31, 2));
	ДокументыОплаты = РезультатыЗапроса[ПоследнийИндекс-6].Выгрузить(); // ТаблицаЗначений
	ДокументыОплаты.Колонки.Добавить("КурсоваяРазницаОплаты", ТипЧисло31_2);
	ДокументыОплаты.Колонки.Добавить("ПризнаниеДохода", ТипЧисло31_2);
	ДокументыОплаты.Колонки.Добавить("ПризнаниеРасхода", ТипЧисло31_2);
	ДокументыОплаты.Колонки.Добавить("ДолгОстаток", ТипЧисло31_2);
	ДокументыОплаты.Колонки.Добавить("СуммаНУОстаток", ТипЧисло31_2);
	ДокументыОплаты.Индексы.Добавить(ПоляПоиска + ", РасчетныйДокумент");
	СтруктураПоискаОплаты = Новый Структура(ПоляПоиска + ", РасчетныйДокумент");
	ДетальныеЗаписиНУ = РезультатыЗапроса[ПоследнийИндекс-5].Выгрузить();
	ДетальныеЗаписиНУ.Индексы.Добавить(ПоляПоиска);
	НачалоПрименения67ФЗПоРасходам = РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.НачалоПрименения67ФЗПоРасходам();
	//-- Локализация

	//-- НЕ УТ
	ОбъектыКПереоценке = РезультатыЗапроса[ПоследнийИндекс-1].Выгрузить();
	Параметры.ВсегоОбработано = Параметры.ВсегоОбработано + ОбъектыКПереоценке.Количество();
	ДетальныеЗаписи    = РезультатыЗапроса[ПоследнийИндекс].Выгрузить();
	ДетальныеЗаписи.Индексы.Добавить(ПоляПоиска);
	КурсовыеРазницыКУдалению = РезультатыЗапроса[ПоследнийИндекс-2].Выгрузить();
	РегистраторыРасчетов = РезультатыЗапроса[ПоследнийИндекс-3].Выгрузить();
	РегистраторыРасчетов.Индексы.Добавить(ПоляПоиска);
	СтруктураПоиска = Новый Структура(ПоляПоиска);
	
	Порядок = Порядок(Период,"",Тип("ДокументСсылка.РасчетКурсовыхРазниц"),"9","99");
	РазмерПорцииЗаписи = РазмерПорцииЗаписи();
	
	ТаблицаИзменений = Новый ТаблицаЗначений;
	ТаблицаИзменений.Колонки.Добавить("Регистратор", Документы.ТипВсеСсылки());
	//++ НЕ УТ
	ТаблицаИзменений.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаИзменений.Колонки.Добавить("АналитикаУчетаПоПартнерам", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаПоПартнерам"));
	//++ Локализация
	РегистрыНУ = Новый Структура("ЗаписыватьОднимНабором", Ложь);
	Если Параметры.ДокументыПереоценки.Количество() = 1 И ЭтоРасчетыСКлиентами <> Неопределено Тогда
		РегистрыНУ.ЗаписыватьОднимНабором = Истина;
		Если ЭтоРасчетыСКлиентами Тогда
			РегистрыНУ.Вставить("НаборЗаписейНУ", РегистрыНакопления.РасчетыСКлиентамиНУ.СоздатьНаборЗаписей());
		Иначе
			РегистрыНУ.Вставить("НаборЗаписейНУ", РегистрыНакопления.РасчетыСПоставщикамиНУ.СоздатьНаборЗаписей());
		КонецЕсли;
		ДокументРегистратор = Параметры.ДокументыПереоценки[0].Ссылка; //ДокументСсылка
		РегистрыНУ.НаборЗаписейНУ.Отбор.Регистратор.Установить(ДокументРегистратор);
		
		РегистрыНУ.Вставить("РасчетПереоценкиНУ", РегистрыСведений.РасчетПереоценкиВалютныхСредствНУ.СоздатьНаборЗаписей());
		РегистрыНУ.РасчетПереоценкиНУ.Отбор.Регистратор.Установить(ДокументРегистратор);
	КонецЕсли;
	//-- Локализация

	//-- НЕ УТ
	ДоговорСКомитентом = Перечисления.ТипыДоговоров.СКомитентом;
	Если ЭтоРасчетыСКлиентами Тогда
		ПереоценкаПоложительная 	= Перечисления.ХозяйственныеОперации.КурсовыеРазницыКлиентыПрибыль;
		ПереоценкаОтрицательная 	= Перечисления.ХозяйственныеОперации.КурсовыеРазницыКлиентыУбыток;
		ПереоценкаПоложительнаяСпр 	= Справочники.НастройкиХозяйственныхОпераций.КурсовыеРазницыКлиентыПрибыль;
		ПереоценкаОтрицательнаяСпр 	= Справочники.НастройкиХозяйственныхОпераций.КурсовыеРазницыКлиентыУбыток;
		НаборЗаписей = РегистрыНакопления.РасчетыСКлиентамиПоСрокам.СоздатьНаборЗаписей();
	Иначе
		ПереоценкаПоложительная 	= Перечисления.ХозяйственныеОперации.КурсовыеРазницыПоставщикиУбыток;
		ПереоценкаОтрицательная 	= Перечисления.ХозяйственныеОперации.КурсовыеРазницыПоставщикиПрибыль;
		ПереоценкаПоложительнаяСпр 	= Справочники.НастройкиХозяйственныхОпераций.КурсовыеРазницыПоставщикиУбыток;
		ПереоценкаОтрицательнаяСпр 	= Справочники.НастройкиХозяйственныхОпераций.КурсовыеРазницыПоставщикиПрибыль;
		НаборЗаписей = РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.СоздатьНаборЗаписей();
	КонецЕсли;
	НаборЗаписей.ДополнительныеСвойства.Вставить("ПроверятьИзмененияРегистра", Истина);
	Для Каждого СтрокаПереоценки Из ОбъектыКПереоценке Цикл
		//++ НЕ УТ

		//++ Локализация
		Если СтрокаПереоценки.ЭтоРасчетыСКлиентами Тогда
			Если НЕ РегистрыНУ.ЗаписыватьОднимНабором Тогда
				РегистрыНУ.Вставить("НаборЗаписейНУ", РегистрыНакопления.РасчетыСКлиентамиНУ.СоздатьНаборЗаписей());
			КонецЕсли;
		Иначе
			Если НЕ РегистрыНУ.ЗаписыватьОднимНабором Тогда
				РегистрыНУ.Вставить("НаборЗаписейНУ", РегистрыНакопления.РасчетыСПоставщикамиНУ.СоздатьНаборЗаписей());
			КонецЕсли;
		КонецЕсли;
		//-- Локализация

		//-- НЕ УТ
		
		Если Параметры.ДокументыПереоценки.Количество() = 1 Тогда
			ДокументРегистратор = Параметры.ДокументыПереоценки[0].Ссылка; //ДокументСсылка
		Иначе
			СтруктураПоискаДокумента = Новый Структура("Дата, Организация", НачалоМесяца(Период), СтрокаПереоценки.Организация);
			НайденныеДокументы = Параметры.ДокументыПереоценки.НайтиСтроки(СтруктураПоискаДокумента);// Массив из СтрокаТаблицыЗначений
			ДокументРегистратор = НайденныеДокументы[0].Ссылка; //ДокументСсылка
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаПереоценки);
		СтрокиДетальныхЗаписей = ДетальныеЗаписи.НайтиСтроки(СтруктураПоиска);
		//++ НЕ УТ

		//++ Локализация
		СтрокиДетальныхЗаписейНУ = ДетальныеЗаписиНУ.НайтиСтроки(СтруктураПоиска);
		Если НЕ РегистрыНУ.ЗаписыватьОднимНабором Тогда
			РегистрыНУ.НаборЗаписейНУ.Отбор.Регистратор.Установить(ДокументРегистратор);
			РегистрыНУ.Вставить("РасчетПереоценкиНУ", РегистрыСведений.РасчетПереоценкиВалютныхСредствНУ.СоздатьНаборЗаписей());
			РегистрыНУ.РасчетПереоценкиНУ.Отбор.Регистратор.Установить(ДокументРегистратор);
		КонецЕсли;
		//-- Локализация

		//-- НЕ УТ
		
		НовСтр = ТаблицаИзменений.Добавить();
		НовСтр.Регистратор = ДокументРегистратор;
		//++ НЕ УТ
		НовСтр.Период = НачалоДня(Период);
		НовСтр.АналитикаУчетаПоПартнерам = СтрокаПереоценки.АналитикаУчетаПоПартнерам;
		//-- НЕ УТ
		Если СтрокиДетальныхЗаписей.Количество() > 0 И НЕ Параметры.ПерезаполнениеРегистровНУ Тогда
			ТаблицаСвободныхРегистраторов = РегистраторыКурсовыхРазниц(СтрокаПереоценки, РегистраторыРасчетов, СтрокиДетальныхЗаписей.Количество() * 4);
			счРегистратор = 0;
			счСтрок = 0;
			
			РегистраторРасчетов = ТаблицаСвободныхРегистраторов[счРегистратор].Ссылка;
			НаборЗаписей.Отбор.Регистратор.Установить(РегистраторРасчетов);
			НаборЗаписей.Прочитать();
			УдалитьКурсовыеРазницыЗаДень(Период, НаборЗаписей);
			СтрокаКУдалению = КурсовыеРазницыКУдалению.Найти(РегистраторРасчетов,"Регистратор");
			Если СтрокаКУдалению <> Неопределено Тогда
				КурсовыеРазницыКУдалению.Удалить(СтрокаКУдалению);
			КонецЕсли;
			
			Для Каждого СтрокаДетальныхЗаписей Из СтрокиДетальныхЗаписей Цикл
				
				#Область ПоложительнаяКурсоваяРазница
				Если СтрокаДетальныхЗаписей.ДолгРеглОстаток < СтрокаДетальныхЗаписей.ДолгРеглОстатокРасчетный 
					ИЛИ СтрокаДетальныхЗаписей.ДолгУпрОстаток < СтрокаДетальныхЗаписей.ДолгУпрОстатокРасчетный Тогда
					НовСтр = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтр, СтрокаДетальныхЗаписей);
					
					НовСтр.Регистратор           = ТаблицаСвободныхРегистраторов[счРегистратор].Ссылка;
					НовСтр.ДокументРегистратор   = ДокументРегистратор;
					НовСтр.Период                = КонецДня(Период);
					НовСтр.ПорядокОперации       = Порядок;
					НовСтр.ПорядокЗачета         = Порядок;
					НовСтр.ВидДвижения           = ВидДвиженияНакопления.Приход;
					НовСтр.ХозяйственнаяОперация = ПереоценкаПоложительная;
					НовСтр.Валюта                = СтрокаДетальныхЗаписей.ВалютаРасчетов;
					НовСтр.НастройкаХозяйственнойОперации 	= ПереоценкаПоложительнаяСпр;
					НовСтр.ИдентификаторФинЗаписи			= Строка(Новый УникальныйИдентификатор);
					НовСтр.ТипЗаписиВзаиморасчетов			= Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеДолга;

					Если СтрокаДетальныхЗаписей.ДолгРеглОстаток < СтрокаДетальныхЗаписей.ДолгРеглОстатокРасчетный Тогда
						НовСтр.ДолгРегл = СтрокаДетальныхЗаписей.ДолгРеглОстатокРасчетный - СтрокаДетальныхЗаписей.ДолгРеглОстаток;
					КонецЕсли;
					Если СтрокаДетальныхЗаписей.ДолгУпрОстаток < СтрокаДетальныхЗаписей.ДолгУпрОстатокРасчетный Тогда
						НовСтр.ДолгУпр = СтрокаДетальныхЗаписей.ДолгУпрОстатокРасчетный - СтрокаДетальныхЗаписей.ДолгУпрОстаток;
					КонецЕсли;
					
					счСтрок = счСтрок + 1;
					Если счСтрок = РазмерПорцииЗаписи Тогда
						счСтрок = 0;
						счРегистратор = счРегистратор + 1;
						НаборЗаписей.Записать();
						НаборЗаписей.Отбор.Регистратор.Установить(ТаблицаСвободныхРегистраторов[счРегистратор].Ссылка);
						НаборЗаписей.Прочитать();
					КонецЕсли;
					
				КонецЕсли;
				#КонецОбласти
				
				#Область ОтрицательнаяКурсоваяРазница
				Если СтрокаДетальныхЗаписей.ДолгРеглОстаток > СтрокаДетальныхЗаписей.ДолгРеглОстатокРасчетный 
					ИЛИ СтрокаДетальныхЗаписей.ДолгУпрОстаток > СтрокаДетальныхЗаписей.ДолгУпрОстатокРасчетный Тогда
					НовСтр = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтр, СтрокаДетальныхЗаписей);
					
					НовСтр.Регистратор           = ТаблицаСвободныхРегистраторов[счРегистратор].Ссылка;
					НовСтр.ДокументРегистратор   = ДокументРегистратор;
					НовСтр.Период                = КонецДня(Период);
					НовСтр.ПорядокОперации       = Порядок;
					НовСтр.ПорядокЗачета         = Порядок;
					НовСтр.ВидДвижения           = ВидДвиженияНакопления.Расход;
					НовСтр.ХозяйственнаяОперация = ПереоценкаОтрицательная;
					НовСтр.Валюта                = СтрокаДетальныхЗаписей.ВалютаРасчетов;
					НовСтр.НастройкаХозяйственнойОперации 	= ПереоценкаОтрицательнаяСпр;
					НовСтр.ИдентификаторФинЗаписи			= Строка(Новый УникальныйИдентификатор);
					НовСтр.ТипЗаписиВзаиморасчетов			= Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеДолга;

					Если СтрокаДетальныхЗаписей.ДолгРеглОстаток > СтрокаДетальныхЗаписей.ДолгРеглОстатокРасчетный Тогда
						НовСтр.ДолгРегл = СтрокаДетальныхЗаписей.ДолгРеглОстаток - СтрокаДетальныхЗаписей.ДолгРеглОстатокРасчетный;
					КонецЕсли;
					Если СтрокаДетальныхЗаписей.ДолгУпрОстаток > СтрокаДетальныхЗаписей.ДолгУпрОстатокРасчетный Тогда
						НовСтр.ДолгУпр = СтрокаДетальныхЗаписей.ДолгУпрОстаток - СтрокаДетальныхЗаписей.ДолгУпрОстатокРасчетный;
					КонецЕсли;
					
					счСтрок = счСтрок + 1;
					Если счСтрок = РазмерПорцииЗаписи Тогда
						счСтрок = 0;
						счРегистратор = счРегистратор + 1;
						НаборЗаписей.Записать();
						НаборЗаписей.Отбор.Регистратор.Установить(ТаблицаСвободныхРегистраторов[счРегистратор].Ссылка);
						НаборЗаписей.Прочитать();
					КонецЕсли;
					
				КонецЕсли;
				#КонецОбласти
				
				#Область КорректировкаПредоплатыВНоль
				Если СтрокаДетальныхЗаписей.ПредоплатаОстаток = 0
					И (СтрокаДетальныхЗаписей.ПредоплатаРеглОстаток > 0
						ИЛИ СтрокаДетальныхЗаписей.ПредоплатаУпрОстаток > 0) Тогда
					
					НовСтр = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтр, СтрокаДетальныхЗаписей);
					
					НовСтр.Регистратор           = ТаблицаСвободныхРегистраторов[счРегистратор].Ссылка;
					НовСтр.ДокументРегистратор   = ДокументРегистратор;
					НовСтр.Период                = КонецДня(Период);
					НовСтр.ПорядокОперации       = Порядок;
					НовСтр.ПорядокЗачета         = Порядок;
					НовСтр.ВидДвижения           = ВидДвиженияНакопления.Расход;
					НовСтр.ХозяйственнаяОперация = ПереоценкаПоложительная;
					НовСтр.Валюта                = СтрокаДетальныхЗаписей.ВалютаРасчетов;
					НовСтр.НастройкаХозяйственнойОперации 	= ПереоценкаПоложительнаяСпр;
					НовСтр.ИдентификаторФинЗаписи			= Строка(Новый УникальныйИдентификатор);
					НовСтр.ТипЗаписиВзаиморасчетов			= Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеПредоплаты;

					Если СтрокаДетальныхЗаписей.ПредоплатаРеглОстаток > 0 Тогда
						НовСтр.ПредоплатаРегл = СтрокаДетальныхЗаписей.ПредоплатаРеглОстаток;
					КонецЕсли;
					Если СтрокаДетальныхЗаписей.ПредоплатаУпрОстаток > 0 Тогда
						НовСтр.ПредоплатаУпр = СтрокаДетальныхЗаписей.ПредоплатаУпрОстаток;
					КонецЕсли;
					
					счСтрок = счСтрок + 1;
					Если счСтрок = РазмерПорцииЗаписи Тогда
						счСтрок = 0;
						счРегистратор = счРегистратор + 1;
						НаборЗаписей.Записать();
						НаборЗаписей.Отбор.Регистратор.Установить(ТаблицаСвободныхРегистраторов[счРегистратор].Ссылка);
						НаборЗаписей.Прочитать();
					КонецЕсли;
					
				КонецЕсли;
				
				Если СтрокаДетальныхЗаписей.ПредоплатаОстаток = 0
					И (СтрокаДетальныхЗаписей.ПредоплатаРеглОстаток < 0
						ИЛИ СтрокаДетальныхЗаписей.ПредоплатаУпрОстаток < 0) Тогда
					
					НовСтр = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтр, СтрокаДетальныхЗаписей);
					
					НовСтр.Регистратор           = ТаблицаСвободныхРегистраторов[счРегистратор].Ссылка;
					НовСтр.ДокументРегистратор   = ДокументРегистратор;
					НовСтр.Период                = НачалоДня(Период);
					НовСтр.ПорядокОперации       = Порядок;
					НовСтр.ПорядокЗачета         = Порядок;
					НовСтр.ВидДвижения           = ВидДвиженияНакопления.Приход;
					НовСтр.ХозяйственнаяОперация = ПереоценкаОтрицательная;
					НовСтр.Валюта                = СтрокаДетальныхЗаписей.ВалютаРасчетов;
					НовСтр.НастройкаХозяйственнойОперации 	= ПереоценкаОтрицательнаяСпр;
					НовСтр.ИдентификаторФинЗаписи			= Строка(Новый УникальныйИдентификатор);
					НовСтр.ТипЗаписиВзаиморасчетов			= Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеПредоплаты;
					
					Если СтрокаДетальныхЗаписей.ПредоплатаРеглОстаток < 0 Тогда
						НовСтр.ПредоплатаРегл = -СтрокаДетальныхЗаписей.ПредоплатаРеглОстаток;
					КонецЕсли;
					Если СтрокаДетальныхЗаписей.ПредоплатаУпрОстаток < 0 Тогда
						НовСтр.ПредоплатаУпр = -СтрокаДетальныхЗаписей.ПредоплатаУпрОстаток;
					КонецЕсли;
					
					счСтрок = счСтрок + 1;
					Если счСтрок = РазмерПорцииЗаписи Тогда
						счСтрок = 0;
						счРегистратор = счРегистратор + 1;
						НаборЗаписей.Записать();
						НаборЗаписей.Отбор.Регистратор.Установить(ТаблицаСвободныхРегистраторов[счРегистратор].Ссылка);
						НаборЗаписей.Прочитать();
					КонецЕсли;
					
				КонецЕсли;
				#КонецОбласти
				
			КонецЦикла;
			НаборЗаписей.Записать();
		КонецЕсли;
		//++ НЕ УТ

		//++ Локализация
		#Область РасчетКурсовыхРазницНУ
		Для Каждого ЗаписьНУ Из СтрокиДетальныхЗаписейНУ Цикл
			
			РасчетыПоВознаграждению = ЗаписьНУ.ТипДоговора = ДоговорСКомитентом
										И ЭтоРасчетыСКлиентами;
			ПереоценкаПо67ФЗ = ЗаписьНУ.ПрименятьПоложения67ФЗ
				И (НЕ ЗаписьНУ.ПереоцениватьТоварыУслугиКОтчетуКомитенту
					ИЛИ РасчетыПоВознаграждению);
			
			КурсНаДату = ЗаписьНУ.КурсЧислительРегл/ЗаписьНУ.КурсЗнаменательРегл;
			АналитикаНУ = АналитикаКурсовыхНУ(ЗаписьНУ);
			АналитикаНУ.Период = КонецДня(Период);
			АналитикаНУ.ДокументРегистратор = ДокументРегистратор;
			АналитикаНУ.НачалоПрименения67ФЗПоРасходам = НачалоПрименения67ФЗПоРасходам;
			
			СуммыНУ = РассчитатьСуммыНУ(Период, КурсНаДату, ЗаписьНУ);
			
			Если Период = КонецМесяца(Период) Тогда
				ДобавитьОборотНУ(РегистрыНУ.НаборЗаписейНУ, АналитикаНУ, "СуммаНУ", СуммыНУ.КурсоваяРазницаОплаты);
				ДобавитьОборотНУ(РегистрыНУ.НаборЗаписейНУ, АналитикаНУ, "СуммаНУ", СуммыНУ.КурсоваяРазницаОстатка);
			Иначе
				ДобавитьОборотНУ(РегистрыНУ.НаборЗаписейНУ, АналитикаНУ, "СуммаНУ", СуммыНУ.КурсоваяРазницаНУ);
			КонецЕсли;
			Если ПереоценкаПо67ФЗ Тогда
				ДобавитьОборотНУ(РегистрыНУ.НаборЗаписейНУ, АналитикаНУ, "ОтложенныйДоход", СуммыНУ.ОтложенныйДоходОплата);
				ДобавитьОборотНУ(РегистрыНУ.НаборЗаписейНУ, АналитикаНУ, "ОтложенныйРасход", СуммыНУ.ОтложенныйРасходОплата);
				ДобавитьОборотНУ(РегистрыНУ.НаборЗаписейНУ, АналитикаНУ, "ОтложенныйДоход", СуммыНУ.ОтложенныйДоходОстаток);
				ДобавитьОборотНУ(РегистрыНУ.НаборЗаписейНУ, АналитикаНУ, "ОтложенныйРасход", СуммыНУ.ОтложенныйРасходОстаток);
			КонецЕсли;
			
			//РасчетПереоценкиНУ
			Знак = ?(ЗаписьНУ.ЭтоРасчетыСКлиентами, 1, -1);
			Если Период = КонецМесяца(Период) И Окр(СуммыНУ.КурсоваяРазницаОстатка,2) <> 0 
				И ПереоценкаПо67ФЗ Тогда
				НовСтр = РегистрыНУ.РасчетПереоценкиНУ.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, АналитикаНУ);
				НовСтр.Аналитика1 = АналитикаНУ.Контрагент; 
				НовСтр.Аналитика2 = АналитикаНУ.Договор; 
				НовСтр.Аналитика3 = АналитикаНУ.РасчетныйДокумент;
				
				НовСтр.ОстатокВВалюте = ЗаписьНУ.ДолгОстаток;
				НовСтр.ОстатокВРубляхНУ = ЗаписьНУ.СуммаНУОстаток + СуммыНУ.КурсоваяРазницаОплаты;
				НовСтр.ОстатокПоКурсуПереоценки = КурсНаДату * ЗаписьНУ.ДолгОстаток;
				НовСтр.СуммаПереоценкиНУ = Знак * СуммыНУ.КурсоваяРазницаОстатка;
				Если ЗаписьНУ.ЭтоРасчетыСКлиентами Тогда
					Если СуммыНУ.КурсоваяРазницаОстатка > 0 Тогда
						НовСтр.ОтложенныйДоход = СуммыНУ.КурсоваяРазницаОстатка;
					ИначеЕсли Период >= НачалоПрименения67ФЗПоРасходам Тогда
						НовСтр.ОтложенныйРасход = -СуммыНУ.КурсоваяРазницаОстатка;
					КонецЕсли;
				Иначе
					Если СуммыНУ.КурсоваяРазницаОстатка < 0 Тогда
						НовСтр.ОтложенныйДоход = -СуммыНУ.КурсоваяРазницаОстатка;
					ИначеЕсли Период >= НачалоПрименения67ФЗПоРасходам Тогда
						НовСтр.ОтложенныйРасход = СуммыНУ.КурсоваяРазницаОстатка;
					КонецЕсли;
				КонецЕсли;
				
				НовСтр.ОстатокОД = ЗаписьНУ.ОтложенныйДоходОстаток;
				Если Период >= НачалоПрименения67ФЗПоРасходам Тогда
					НовСтр.ОстатокОР = ЗаписьНУ.ОтложенныйРасходОстаток;
				КонецЕсли;
				
				НовСтр.ЭтоТребование = Истина;
				НовСтр.Курс = ЗаписьНУ.КурсЧислительРегл;
				НовСтр.Кратность = ЗаписьНУ.КурсЗнаменательРегл;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(СтруктураПоискаОплаты, ЗаписьНУ);
			СтрокиОплаты = ДокументыОплаты.Скопировать(СтруктураПоискаОплаты);
			Если СтрокиОплаты.Количество() > 0 И ПереоценкаПо67ФЗ Тогда
				
				ОстаткиНУ = Новый Структура;
				ОстаткиНУ.Вставить("ЭтоРасчетыСКлиентами", ЗаписьНУ.ЭтоРасчетыСКлиентами);
				ОстаткиНУ.Вставить("ДолгОстаток", ЗаписьНУ.ДолгОстаток);
				ОстаткиНУ.Вставить("СуммаНУОстаток", ЗаписьНУ.СуммаНУОстаток + СуммыНУ.КурсоваяРазницаОплаты);
				
				ВсегоОплатаЗаДень = СтрокиОплаты.Итог("ДолгРасход");
				ВсегоПризнаноДохода = -СуммыНУ.ПризнаниеДохода;
				ВсегоПризнаноРасхода = -СуммыНУ.ПризнаниеРасхода;
				КурсоваяРазницаОплаты = Макс(СуммыНУ.КурсоваяРазницаОплаты, -СуммыНУ.КурсоваяРазницаОплаты);
				ВсегоКурсоваяРазницаОплаты = КурсоваяРазницаОплаты;
				ЗнакКурсовой = 1;
				Если СуммыНУ.КурсоваяРазницаОплаты <> 0 Тогда
					ЗнакКурсовой = СуммыНУ.КурсоваяРазницаОплаты/КурсоваяРазницаОплаты;
				КонецЕсли;
				Для Индекс = 0 По СтрокиОплаты.Количество()-1 Цикл
					ПоследняяСтрока = Индекс = СтрокиОплаты.Количество()-1;
					Оплата = СтрокиОплаты[Индекс];
					
					Если ВсегоОплатаЗаДень <> 0 Тогда
						ДоляКурсовойРазницаОплаты = Окр(Оплата.ДолгРасход/ВсегоОплатаЗаДень * КурсоваяРазницаОплаты, 2);
						Оплата.КурсоваяРазницаОплаты = ЗнакКурсовой * ВзаиморасчетыСервер.СписатьСумму(ВсегоКурсоваяРазницаОплаты, ДоляКурсовойРазницаОплаты, ПоследняяСтрока);
						
						ПризнаниеДохода = Окр(Оплата.ДолгРасход/ВсегоОплатаЗаДень * (-СуммыНУ.ПризнаниеДохода), 2);
						Оплата.ПризнаниеДохода = -ВзаиморасчетыСервер.СписатьСумму(ВсегоПризнаноДохода, ПризнаниеДохода, ПоследняяСтрока);
						
						ПризнаниеРасхода = Окр(Оплата.ДолгРасход/ВсегоОплатаЗаДень * (-СуммыНУ.ПризнаниеРасхода),2);
						Оплата.ПризнаниеРасхода = -ВзаиморасчетыСервер.СписатьСумму(ВсегоПризнаноРасхода, ПризнаниеРасхода, ПоследняяСтрока);
					КонецЕсли;
					
					Оплата.ДолгОстаток = ОстаткиНУ.ДолгОстаток + Оплата.ДолгРасход;
					Оплата.СуммаНУОстаток = ОстаткиНУ.СуммаНУОстаток + Оплата.ДолгРеглРасход - Оплата.КурсоваяРазницаОплаты;
					ОстаткиНУ.ДолгОстаток = Оплата.ДолгОстаток;
					ОстаткиНУ.СуммаНУОстаток = Оплата.СуммаНУОстаток;
					
				КонецЦикла; //по документам оплаты
				
				СтрокиОплаты.Сортировать("МоментВремени");
				
				Для Каждого Оплата Из СтрокиОплаты Цикл
					
					Если Окр(Оплата.КурсоваяРазницаОплаты,2) = 0
						И Окр(Оплата.ПризнаниеДохода,2) = 0
						И Окр(Оплата.ПризнаниеРасхода,2) = 0 Тогда
						Продолжить;
					КонецЕсли;
					
					НовСтр = РегистрыНУ.РасчетПереоценкиНУ.Добавить();
					ЗаполнитьЗначенияСвойств(НовСтр, АналитикаНУ);
					НовСтр.Аналитика1 = АналитикаНУ.Контрагент; 
					НовСтр.Аналитика2 = АналитикаНУ.Договор; 
					НовСтр.Аналитика3 = АналитикаНУ.РасчетныйДокумент;
					НовСтр.ДокументОплаты = Оплата.ДокументОплаты;
					
					НовСтр.ОстатокВВалюте = Оплата.ДолгОстаток;
					НовСтр.ОстатокВРубляхНУ = Оплата.СуммаНУОстаток;
					НовСтр.ОстатокПоКурсуПереоценки = Окр(Оплата.ДолгРеглРасход/Оплата.ДолгРасход * Оплата.ДолгОстаток, 2);
					Если ЗаписьНУ.ЭтоРасчетыСКлиентами Тогда
						НовСтр.СуммаПереоценкиНУ = Оплата.КурсоваяРазницаОплаты;
					Иначе
						НовСтр.СуммаПереоценкиНУ = -Оплата.КурсоваяРазницаОплаты;
					КонецЕсли;
					
					НовСтр.ПогашениеВВалюте = Оплата.ДолгРасход;
					НовСтр.ОстатокОД = ЗаписьНУ.ОтложенныйДоходОстаток;
					НовСтр.ОтложенныйДоход = Оплата.ПризнаниеДохода;
					Если Период >= НачалоПрименения67ФЗПоРасходам Тогда
						НовСтр.ОстатокОР = ЗаписьНУ.ОтложенныйРасходОстаток;
						НовСтр.ОтложенныйРасход = Оплата.ПризнаниеРасхода;
					КонецЕсли;
					
					НовСтр.ЭтоТребование = Истина;
					НовСтр.Курс = ЗаписьНУ.КурсЧислительРегл;
					НовСтр.Кратность = ЗаписьНУ.КурсЗнаменательРегл;
					
				КонецЦикла; //по документам оплаты
			
			ИначеЕсли ЗаписьНУ.ДолгОстаток = 0 И Окр(СуммыНУ.КурсоваяРазницаОплаты,2) <> 0 
					И ПереоценкаПо67ФЗ Тогда
				
				НовСтр = РегистрыНУ.РасчетПереоценкиНУ.Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр, АналитикаНУ);
				НовСтр.Аналитика1 = АналитикаНУ.Контрагент; 
				НовСтр.Аналитика2 = АналитикаНУ.Договор; 
				НовСтр.Аналитика3 = АналитикаНУ.РасчетныйДокумент;
				НовСтр.ДокументОплаты = АналитикаНУ.ДокументРегистратор;
				
				НовСтр.ОстатокВВалюте = ЗаписьНУ.ДолгОстаток;
				НовСтр.ОстатокВРубляхНУ = ЗаписьНУ.СуммаНУОстаток;
				Если ЗаписьНУ.ЭтоРасчетыСКлиентами Тогда
					НовСтр.СуммаПереоценкиНУ = СуммыНУ.КурсоваяРазницаОплаты;
				Иначе
					НовСтр.СуммаПереоценкиНУ = -СуммыНУ.КурсоваяРазницаОплаты;
				КонецЕсли;
				НовСтр.ПогашениеВВалюте = ЗаписьНУ.ДолгРасход;
				
				НовСтр.ЭтоТребование = Истина;
				НовСтр.Курс = ЗаписьНУ.КурсЧислительРегл;
				НовСтр.Кратность = ЗаписьНУ.КурсЗнаменательРегл;
				
			КонецЕсли; //есть оплаты
		КонецЦикла;
		Если НЕ РегистрыНУ.ЗаписыватьОднимНабором Тогда
			Если РегистрыНУ.НаборЗаписейНУ.Количество() > 0 Тогда
				Если РегистрыНУ.НаборЗаписейНУ.Количество() > 1 Тогда
					Таблица = РегистрыНУ.НаборЗаписейНУ.Выгрузить();
					Таблица.Свернуть("ВидДвижения,Период,АналитикаУчетаПоПартнерам,ОбъектРасчетов,Валюта,РасчетныйДокумент,ДокументРегистратор","СуммаНУ,ОтложенныйДоход,ОтложенныйРасход");
					РегистрыНУ.НаборЗаписейНУ.Загрузить(Таблица);
				КонецЕсли;
				РегистрыНУ.НаборЗаписейНУ.Записать(Ложь);
			КонецЕсли;
			Если РегистрыНУ.РасчетПереоценкиНУ.Количество() > 0 Тогда
				РегистрыНУ.РасчетПереоценкиНУ.Записать(Ложь);
			КонецЕсли;
		КонецЕсли;
		#КонецОбласти
		//-- Локализация

		//-- НЕ УТ
		
	КонецЦикла; //по объектам переоценки
	
	//++ НЕ УТ

	//++ Локализация
	Если РегистрыНУ.ЗаписыватьОднимНабором Тогда
		Если РегистрыНУ.НаборЗаписейНУ.Количество() > 0 Тогда
			Если РегистрыНУ.НаборЗаписейНУ.Количество() > 1 Тогда
				ОбщегоНазначенияУТ.СвернутьНаборЗаписей(РегистрыНУ.НаборЗаписейНУ);
			КонецЕсли;
			РегистрыНУ.НаборЗаписейНУ.Записать(Ложь);
		КонецЕсли;
		Если РегистрыНУ.РасчетПереоценкиНУ.Количество() > 0 Тогда
			ОбщегоНазначенияУТ.СвернутьНаборЗаписей(РегистрыНУ.РасчетПереоценкиНУ);
			РегистрыНУ.РасчетПереоценкиНУ.Записать(Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Параметры.ПерезаполнениеРегистровНУ Тогда
	//-- Локализация

	//-- НЕ УТ
	Для Каждого СтрокаКУдалению Из КурсовыеРазницыКУдалению Цикл
		
		Если СтрокаКУдалению.ЭтоРасчетыСКлиентами Тогда
			НаборЗаписей = РегистрыНакопления.РасчетыСКлиентамиПоСрокам.СоздатьНаборЗаписей();
		Иначе
			НаборЗаписей = РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.СоздатьНаборЗаписей();
		КонецЕсли;
		НаборЗаписей.ДополнительныеСвойства.Вставить("ПроверятьИзмененияРегистра", Истина);
		
		НаборЗаписей.Отбор.Регистратор.Установить(СтрокаКУдалению.Регистратор);
		НаборЗаписей.Прочитать();
		Если УдалитьКурсовыеРазницыЗаДень(Период, НаборЗаписей) Тогда
			НаборЗаписей.Записать(); //удалим старые курсовые разницы
		КонецЕсли;
	КонецЦикла;
	//++ НЕ УТ

	//++ Локализация
	КонецЕсли;
	//-- Локализация

	//-- НЕ УТ
	
	ПоляСворачивания = "Регистратор";
	//++ НЕ УТ
	ПоляСворачивания = "Регистратор, Период, АналитикаУчетаПоПартнерам";
	//-- НЕ УТ
	ТаблицаИзменений.Свернуть(ПоляСворачивания);
	//++ НЕ УТ

	//++ Локализация
	Если НЕ Параметры.ПерезаполнениеРегистровНУ Тогда
	//-- Локализация
	РеглУчетСервер.ЗарегистрироватьДокументыРасчетовСПартнерамиКОтражениюВРеглУчете(ТаблицаИзменений);
	//++ НЕ УТКА
	МеждународныйУчетПроведениеСервер.ЗарегистрироватьДокументыРасчетовСПартнерамиКОтражениюВМеждународномУчете(ТаблицаИзменений);
	//-- НЕ УТКА

	//++ Локализация
	КонецЕсли;
	//-- Локализация

	//-- НЕ УТ
	
	Если ЭтоРасчетыСКлиентами <> Неопределено Тогда
		Для Каждого Организация Из МассивОрганизаций Цикл
			
			ДокументыРасчетаКурсовыхРазниц = Документы.РасчетКурсовыхРазниц.ДокументыПереоценки(Организация,
				Период, Период,
				ЭтоРасчетыСКлиентами);
			ДокументРегистратор = ДокументыРасчетаКурсовыхРазниц[НачалоМесяца(Период)];
			
			Если ЭтоРасчетыСКлиентами Тогда
				НаборДвиженийПоДокументам = РегистрыНакопления.РасчетыСКлиентамиПоДокументам.СоздатьНаборЗаписей();
			Иначе
				НаборДвиженийПоДокументам = РегистрыНакопления.РасчетыСПоставщикамиПоДокументам.СоздатьНаборЗаписей();
			КонецЕсли;
			
			НаборДвиженийПоДокументам.Отбор.Регистратор.Установить(ДокументРегистратор);
			НаборДвиженийПоДокументам.Прочитать();
			Если НаборДвиженийПоДокументам.Количество() > 0 Тогда
				НаборДвиженийПоДокументам.Очистить();
				НаборДвиженийПоДокументам.Записать();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

//Возвращает строковый порядок документа по переданным параметрам.
// 
// Параметры:
// 	Дата - Дата - Дата по которой нужно сделать строковый порядок.
// 	Номер - Строка - Номер документа.
// 	ТипДокумента - Тип - Тип ссылки документа.
// 	ВидОперацииДляСортировки - Строка - вид операции при порядке зачета "По виду документа"
// 	ТипСтрокой - Строка - см. НомерТипа.
// 	ПорядокОперацииВПределахДокумента - Строка - номер операция в пределах одного регистратора.
// Возвращаемое значение:
// 	Строка - Описание
Функция Порядок(Дата, Номер, ТипДокумента, ВидОперацииДляСортировки, ТипСтрокой = Неопределено, ПорядокОперацииВПределахДокумента = "1") Экспорт
	Если НЕ ЗначениеЗаполнено(Дата) Тогда 
		Возврат Неопределено;
	КонецЕсли;
	ДатаСтрокой = Формат(Дата, "ДФ=yyyyMMddHHmmss");
	Если ТипСтрокой = Неопределено Тогда
		ТипСтрокой = НомерТипа(ТипДокумента);
	КонецЕсли;
	Возврат Лев(ДатаСтрокой, 8) + ВидОперацииДляСортировки + Прав(ДатаСтрокой, 6) + ТипСтрокой + Номер + ПорядокОперацииВПределахДокумента;
КонецФункции

// Возвращает структуру параметров, необходимых для заполнения регистров взаиморасчетов в новой архитектуре.
// 
// Возвращаемое значение:
//  Структура - Структура параметров заполнения взаиморасчетов:
// * ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов - объект расчетов, в рамках которого происходит распределение взаиморасчетов.
// * АналитикаУчетаПоПартнерам - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам - аналитика, в рамках которой происходит распределение взаиморасчетов.
// * ВалютаРасчетов - СправочникСсылка.Валюты - Валюта взаиморасчетов.
// * ЭтоРасчетыСКлиентами - Булево - Определяет регистры для заполнения - с клиентами или с поставщиками.
// * ДатаНачалаПересчета - Дата - Момент времени начиная с которого необходимо выполнить распределение.
// * НачальноеЗаполнение - Булево - Истина, если это начальное заполнение.
// * Регистратор - ДокументСсылка, Неопределено - Документ регистратор, инициировавший распределение.
// * ДополнительныеСвойстваПроведения - Структура - Структура дополнительных свойств проведения, транслируемая из регистров накопления РасчетыСКлиентами РасчетыСПоставщиками:
// ** МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Дополняется таблицей сформированных движений для дальнейшего использования в регл и упр учете
// * ЗаписыватьИзменения - Булево - Ложь, если требуется получить таблицу сформированных движений без записи документов.
// * ПерезаполнениеРегистровВзаиморасчетов - Булево - Истина, если перезаполнение регистров взаиморасчетов вызывается из служебных обработок (например, "Заполнение регистров взаиморасчетов").
Функция СтруктураПараметровЗаполненияВзаиморасчетов() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("ОбъектРасчетов", Справочники.ОбъектыРасчетов.ПустаяСсылка());
	Структура.Вставить("АналитикаУчетаПоПартнерам", Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка());
	Структура.Вставить("ВалютаРасчетов", Справочники.Валюты.ПустаяСсылка());
	Структура.Вставить("ЭтоРасчетыСКлиентами", Истина);
	Структура.Вставить("ДатаНачалаПересчета", Дата(1,1,1));
	Структура.Вставить("НачальноеЗаполнение", Ложь);
	Структура.Вставить("Регистратор", Неопределено);
	Структура.Вставить("ДополнительныеСвойстваПроведения", Неопределено);
	Структура.Вставить("ЗаписыватьИзменения", Истина);
	Структура.Вставить("ПерезаполнениеРегистровВзаиморасчетов", Ложь);
	
	Возврат Структура;
	
КонецФункции

#Область РегистраторыРасчетов

//Возвращает таблицу служебных документов РегистраторРасчетов для перезаписи по переданным параметрам.
//
// Параметры:
//	Параметры - Структура - Описание:
//		* ОбъектРасчетов - ОпределяемыйТип.ОбъектРасчетов - Объект расчетов.
//		* АналитикаУчетаПоПартнерам - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам - Аналитика взаиморасчетов.
//		* Валюта - СправочникСсылка.Валюты - Валюта взаиморасчетов.
//		* ЭтоРасчетыСКлиентами - Булево - это расчеты с клиентами, а не с поставщиками.
//		* Организация - Неопределено, СправочникСсылка.Организации - Организация расчета.
//	ИмяТаблицы - Строка - Имя регистра накопления, по которому определятся свободный регистратор или нет.
//	КоличествоЗаписей - Число - Количество записей для последующей записи в регистр.
//	ТаблицаСвободныхРегистраторов - см. ТаблицаСвободныхРегистраторовРасчета.
//
// Возвращаемое значение:
//	ТаблицаЗначений - таблица со следующими колонками:
//		* Ссылка - ДокументСсылка.РегистраторРасчетов - найденный служебный документ;
//		* ТребуетсяОчистка - Булево - У данного документа есть движения в переданной таблице.
//
Функция СвободныеРегистраторыРасчетов(Параметры, ИмяТаблицы, КоличествоЗаписей, ТаблицаСвободныхРегистраторов)
	
	Если КоличествоЗаписей = 0 Тогда
		КоличествоРегистраторов = 0;
	Иначе
		КоличествоРегистраторов = Цел(КоличествоЗаписей/РазмерПорцииЗаписи()) + 1;
	КонецЕсли;
	
	ТаблицаДокументов = Новый ТаблицаЗначений();
	ТаблицаДокументов.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ДокументСсылка.РегистраторРасчетов"));
	ТаблицаДокументов.Колонки.Добавить("ТребуетсяОчистка", Новый ОписаниеТипов("Булево"));
	
	ТипРасчетов = ?(Параметры.ЭтоРасчетыСКлиентами, Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом, Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком);
	
	СвободныеРегистраторы = ТаблицаСвободныхРегистраторов.НайтиСтроки(Новый Структура("ИмяТаблицы", ИмяТаблицы));
	
	Для Каждого СтрокаСвободныеРегистраторы Из СвободныеРегистраторы Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаДокументов.Добавить(), СтрокаСвободныеРегистраторы);
	КонецЦикла;
	
	СписокТаблицРасчетовСПартнерами = Новый Массив;
	Если Параметры.ЭтоРасчетыСКлиентами Тогда
		СписокТаблицРасчетовСПартнерами.Добавить("РасчетыСКлиентамиПоСрокам");
		СписокТаблицРасчетовСПартнерами.Добавить("РасчетыСКлиентамиПланОплат");
		СписокТаблицРасчетовСПартнерами.Добавить("РасчетыСКлиентамиПланОтгрузок");
	Иначе
		СписокТаблицРасчетовСПартнерами.Добавить("РасчетыСПоставщикамиПоСрокам");
		СписокТаблицРасчетовСПартнерами.Добавить("РасчетыСПоставщикамиПланОплат");
		СписокТаблицРасчетовСПартнерами.Добавить("РасчетыСПоставщикамиПланПоставок");
	КонецЕсли;
	
	Пока ТаблицаДокументов.Количество() < КоличествоРегистраторов Цикл
		ДокументОбъект = Документы.РегистраторРасчетов.СоздатьДокумент();
		ДокументОбъект.Валюта = Параметры.ВалютаРасчетов;
		ДокументОбъект.АналитикаУчетаПоПартнерам = Параметры.АналитикаУчетаПоПартнерам;
		ДокументОбъект.Организация = ?(Параметры.Организация = Неопределено,
										ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.АналитикаУчетаПоПартнерам, "Организация"),
										Параметры.Организация);
		ДокументОбъект.ОбъектРасчетов = Параметры.ОбъектРасчетов;
		ДокументОбъект.ТипРасчетов = ТипРасчетов;
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		НовСтр = ТаблицаДокументов.Добавить();
		НовСтр.Ссылка = ДокументОбъект.Ссылка;
		НовСтр.ТребуетсяОчистка = Ложь;
		
		Для Каждого Таблица Из СписокТаблицРасчетовСПартнерами Цикл
			СвободныйРегистратор = ТаблицаСвободныхРегистраторов.Добавить();
			СвободныйРегистратор.ИмяТаблицы = Таблица;
			СвободныйРегистратор.Ссылка = ДокументОбъект.Ссылка;
			СвободныйРегистратор.ТребуетсяОчистка = Ложь;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаДокументов;
	
КонецФункции

// Таблица свободных регистраторов расчета.
// 
// Параметры:
//	Параметры - Структура - Описание:
//		* ОбъектРасчетов - ОпределяемыйТип.ОбъектРасчетов - Объект расчетов.
//		* АналитикаУчетаПоПартнерам - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам - Аналитика взаиморасчетов.
//		* Валюта - СправочникСсылка.Валюты - Валюта взаиморасчетов.
//		* ЭтоРасчетыСКлиентами - Булево - это расчеты с клиентами, а не с поставщиками.
//		* Организация - Неопределено, СправочникСсылка.Организации - Организация расчета.
// 
// Возвращаемое значение:
//	ТаблицаЗначений - таблица со следующими колонками:
//		* ИмяТаблицы - Строка - Имя регистра накопления;
//		* Ссылка - ДокументСсылка.РегистраторРасчетов - найденный служебный документ;
//		* ТребуетсяОчистка - Булево - У данного документа есть движения в переданной таблице.
//
//  
Функция ТаблицаСвободныхРегистраторовРасчета(Параметры)

	Если Параметры.ЭтоРасчетыСКлиентами Тогда
		ТаблицаРасчетыПоСрокам             = "РасчетыСКлиентамиПоСрокам";
		ТаблицаРасчетыПланОплат            = "РасчетыСКлиентамиПланОплат";
		ТаблицаРасчетыПланОтгрузокПоставок = "РасчетыСКлиентамиПланОтгрузок";
	Иначе
		ТаблицаРасчетыПоСрокам             = "РасчетыСПоставщикамиПоСрокам";
		ТаблицаРасчетыПланОплат            = "РасчетыСПоставщикамиПланОплат";
		ТаблицаРасчетыПланОтгрузокПоставок = "РасчетыСПоставщикамиПланПоставок";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Валюта",                    Параметры.ВалютаРасчетов);
	Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", Параметры.АналитикаУчетаПоПартнерам);
	Запрос.УстановитьПараметр("ОбъектРасчетов",            Параметры.ОбъектРасчетов);
	Запрос.УстановитьПараметр("ТаблицаРасчетыПоСрокам",    ТаблицаРасчетыПоСрокам);
	Запрос.УстановитьПараметр("ТаблицаРасчетыПланОплат",   ТаблицаРасчетыПланОплат);
	Запрос.УстановитьПараметр("ТаблицаРасчетыПланОтгрузокПоставок", ТаблицаРасчетыПланОтгрузокПоставок);
	
	ПустойРегистраторРасчетов = Документы.РегистраторРасчетов.ПустаяСсылка();
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Параметры,ТаблицаРасчетыПоСрокам + "Регистраторы") Тогда
		СписокПолныхРегистраторовРасчетыПоСрокам = ОбщегоНазначения.СкопироватьРекурсивно(Параметры[ТаблицаРасчетыПоСрокам + "Регистраторы"]);
	Иначе
		СписокПолныхРегистраторовРасчетыПоСрокам = Новый Массив;
	КонецЕсли;
	СписокПолныхРегистраторовРасчетыПоСрокам.Добавить(ПустойРегистраторРасчетов);
	
	Запрос.УстановитьПараметр("СписокПолныхРегистраторовРасчетыПоСрокам", СписокПолныхРегистраторовРасчетыПоСрокам);
	
	СписокПолныхРегистраторовПоТаблицам = Новый Массив;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Параметры, ТаблицаРасчетыПланОплат + "Регистраторы") Тогда
			СписокПолныхРегистраторовРасчетыПланОплат = ОбщегоНазначения.СкопироватьРекурсивно(Параметры[ТаблицаРасчетыПланОплат + "Регистраторы"]);
		Иначе
			СписокПолныхРегистраторовРасчетыПланОплат = Новый Массив;
		КонецЕсли;
		СписокПолныхРегистраторовРасчетыПланОплат.Добавить(ПустойРегистраторРасчетов);

		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Параметры, ТаблицаРасчетыПланОтгрузокПоставок + "Регистраторы") Тогда
			СписокПолныхРегистраторовРасчетыПланОтгрузокПоставок = ОбщегоНазначения.СкопироватьРекурсивно(Параметры[ТаблицаРасчетыПланОтгрузокПоставок + "Регистраторы"]);
		Иначе
			СписокПолныхРегистраторовРасчетыПланОтгрузокПоставок = Новый Массив;
		КонецЕсли;
		СписокПолныхРегистраторовРасчетыПланОтгрузокПоставок.Добавить(ПустойРегистраторРасчетов);
		
		Запрос.УстановитьПараметр("СписокПолныхРегистраторовРасчетыПланОплат", СписокПолныхРегистраторовРасчетыПланОплат);
		Запрос.УстановитьПараметр("СписокПолныхРегистраторовРасчетыПланОтгрузокПоставок", СписокПолныхРегистраторовРасчетыПланОтгрузокПоставок);
		
		Для Каждого Регистратор Из СписокПолныхРегистраторовРасчетыПоСрокам Цикл
			Если СписокПолныхРегистраторовРасчетыПланОплат.Найти(Регистратор) <> Неопределено
				И СписокПолныхРегистраторовРасчетыПланОтгрузокПоставок.Найти(Регистратор) <> Неопределено Тогда
				СписокПолныхРегистраторовПоТаблицам.Добавить(Регистратор);
			КонецЕсли;
		КонецЦикла;
	
	Запрос.УстановитьПараметр("СписокПолныхРегистраторовПоТаблицам", СписокПолныхРегистраторовПоТаблицам);
	
	МассивТекстовЗапроса = Новый Массив;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КоличествоЗаписейРегистраторовРасчетов.Документ КАК Регистратор
	|ПОМЕСТИТЬ ВтДоступныеРегистраторы
	|ИЗ
	|	РегистрСведений.КоличествоЗаписейРегистраторовРасчетов КАК КоличествоЗаписейРегистраторовРасчетов
	|ГДЕ
	|	КоличествоЗаписейРегистраторовРасчетов.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
	|	И КоличествоЗаписейРегистраторовРасчетов.ОбъектРасчетов = &ОбъектРасчетов
	|	И КоличествоЗаписейРегистраторовРасчетов.Валюта = &Валюта
	|	И НЕ КоличествоЗаписейРегистраторовРасчетов.Документ В (&СписокПолныхРегистраторовПоТаблицам)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&ТаблицаРасчетыПоСрокам КАК ИмяТаблицы,
	|	ДоступныеРегистраторы.Регистратор КАК Ссылка,
	|	СУММА(ЕСТЬNULL(КоличествоЗаписейРегистраторовРасчетов.КоличествоЗаписей, 0)) > 0 КАК ТребуетсяОчистка
	|ИЗ
	|	ВтДоступныеРегистраторы КАК ДоступныеРегистраторы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоличествоЗаписейРегистраторовРасчетов КАК КоличествоЗаписейРегистраторовРасчетов
	|		ПО ДоступныеРегистраторы.Регистратор = КоличествоЗаписейРегистраторовРасчетов.Документ
	|		И КоличествоЗаписейРегистраторовРасчетов.ИмяТаблицы = &ТаблицаРасчетыПоСрокам
	|ГДЕ
	|	НЕ ДоступныеРегистраторы.Регистратор В (&СписокПолныхРегистраторовРасчетыПоСрокам)
	|СГРУППИРОВАТЬ ПО
	|	ДоступныеРегистраторы.Регистратор";
		
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
			
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	&ТаблицаРасчетыПланОплат КАК ИмяТаблицы,
		|	ДоступныеРегистраторы.Регистратор КАК Ссылка,
		|	СУММА(ЕСТЬNULL(КоличествоЗаписейРегистраторовРасчетов.КоличествоЗаписей, 0)) > 0 КАК ТребуетсяОчистка
		|ИЗ
		|	ВтДоступныеРегистраторы КАК ДоступныеРегистраторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоличествоЗаписейРегистраторовРасчетов КАК КоличествоЗаписейРегистраторовРасчетов
		|		ПО ДоступныеРегистраторы.Регистратор = КоличествоЗаписейРегистраторовРасчетов.Документ
		|		И КоличествоЗаписейРегистраторовРасчетов.ИмяТаблицы = &ТаблицаРасчетыПланОплат
		|ГДЕ
		|	НЕ ДоступныеРегистраторы.Регистратор В (&СписокПолныхРегистраторовРасчетыПланОплат)
		|СГРУППИРОВАТЬ ПО
		|	ДоступныеРегистраторы.Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&ТаблицаРасчетыПланОтгрузокПоставок КАК ИмяТаблицы,
		|	ДоступныеРегистраторы.Регистратор КАК Ссылка,
		|	СУММА(ЕСТЬNULL(КоличествоЗаписейРегистраторовРасчетов.КоличествоЗаписей, 0)) > 0 КАК ТребуетсяОчистка
		|ИЗ
		|	ВтДоступныеРегистраторы КАК ДоступныеРегистраторы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоличествоЗаписейРегистраторовРасчетов КАК КоличествоЗаписейРегистраторовРасчетов
		|		ПО ДоступныеРегистраторы.Регистратор = КоличествоЗаписейРегистраторовРасчетов.Документ
		|		И КоличествоЗаписейРегистраторовРасчетов.ИмяТаблицы = &ТаблицаРасчетыПланОтгрузокПоставок
		|ГДЕ
		|	НЕ ДоступныеРегистраторы.Регистратор В (&СписокПолныхРегистраторовРасчетыПланОтгрузокПоставок)
		|СГРУППИРОВАТЬ ПО
		|	ДоступныеРегистраторы.Регистратор";
			
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
	ТаблицаДокументов = Запрос.Выполнить().Выгрузить(); // см.СвободныеРегистраторыРасчетов
	ТаблицаДокументов.Индексы.Добавить("ИмяТаблицы");
	
	Возврат ТаблицаДокументов;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеОчисткаРегистров

//Выполняет первоначальное заполнение регистров новой архитектуры
//
// Параметры:
//	Параметры - Структура - параметры обработчика ожидания
//	УникальныйИдентификатор - УникальныйИдентификатор - идентификатор обработчика ожидания.
//
Процедура ЗаполнитьРегистрыПриВключенииНовойАрхитектуры(Параметры, УникальныйИдентификатор) Экспорт
	
	Если НЕ ОбновлениеИнформационнойБазы.ОтложенноеОбновлениеЗавершено() Тогда
		Текст = НСтр("ru = 'Включение онлайн взаиморасчетов с партнерами возможно только после завершения отложенного обновления.';
					|en = 'You can only enable online settlements with partners after the deferred update is complete.'");
		ВызватьИсключение Текст;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗаданияКРаспределениюРасчетовСКлиентами");
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗаданияКРаспределениюРасчетовСПоставщиками");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		Блокировка.Заблокировать();
		
		РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(ТекущаяДатаСеанса());
		РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСПоставщиками(ТекущаяДатаСеанса());
		
		Если НЕ Параметры.Свойство("ПовторныйЗапуск") Тогда
			ЗаполнитьРегистрПереходаНаОнлайн();
		КонецЕсли;
		Константы.НачатПереходНаНовуюАрхитектуруВзаиморасчетов.Установить(Истина);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Текст = НСтр("ru = 'Включение онлайн взаиморасчетов с партнерами. Не удалось заполнить регистр перехода.';
					|en = 'Enabling online settlements with partners. Cannot fill in transfer register.'",
					ОбщегоНазначения.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(Текст,
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
	ОписаниеЗамераПерехода = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("Взаиморасчеты.ПереходНаНовуюАрхитектуру.Многопоточный");
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ОписаниеЗамераПерехода = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("Взаиморасчеты.ПереходНаНовуюАрхитектуру.ФайловаяБаза");
	КонецЕсли;
	Если НЕ Параметры.Свойство("ПовторныйЗапуск") Тогда
		ОчиститьРегистрыВзаиморасчетов();
		ВключитьИтогиРегистровРасчетов();
	КонецЕсли;
	
	//Фаза 1 - основной перенос
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ВсегоОбработано = ВыполнитьПереносОфлайнВзаиморасчетов();
	Иначе
		ПараметрыМП = ПараметрыМногопоточнойОбработкиРасчетов();
		ПараметрыМП.Процедура.Имя = "ОперативныеВзаиморасчетыСервер.ВыполнитьПереносОфлайнВзаиморасчетов";
		ПараметрыМП.Процедура.ПредставлениеЗадания = НСтр("ru = 'Перенос офлайн взаиморасчетов';
															|en = 'Transfer of offline settlements'");
		ПараметрыМП.Данные.УсловиеЗапроса = "Задания.ДатаПересчета = ДАТАВРЕМЯ(1,1,1)";
		
		ВсегоОбработано = МногопоточнаяОбработкаЗаданийКРаспределениюРасчетов(ПараметрыМП);
	КонецЕсли;
	
	//Фаза 2 - перенос хвостов (перепроведенные за время основного переноса)
	НомерПопытки = 0;
	Пока НомерПопытки < 5 Цикл
		Константы.НачатПереходНаНовуюАрхитектуруВзаиморасчетов.Установить(Ложь);
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗаданияКРаспределениюРасчетовСКлиентами");
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗаданияКРаспределениюРасчетовСПоставщиками");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(ТекущаяДатаСеанса());
			РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСПоставщиками(ТекущаяДатаСеанса());
			
			Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
				ВсегоОбработано = ВсегоОбработано + ВыполнитьПереносОфлайнВзаиморасчетов();
			Иначе
				ПараметрыМП.Данные.УсловиеЗапроса = "";
				КоличествоЗаписей = МногопоточнаяОбработкаЗаданийКРаспределениюРасчетов(ПараметрыМП);
				
				ВсегоОбработано = ВсегоОбработано + КоличествоЗаписей; 
			КонецЕсли;
			
			// Исправим развернутое сальдо.
			ИсправитьРазвернутоеСальдо(НачалоМесяца(ТекущаяДатаСеанса()));
			
			Константы.НоваяАрхитектураВзаиморасчетов.Установить(Истина);
			ХранилищеОбщихНастроек.Удалить("ПереходНаНовуюАрхитектуруВзаиморасчетов", "ВсегоКПереносу", Неопределено);
			//++ НЕ УТ
			Справочники.НастройкиХозяйственныхОпераций.ЗаменитьИсточникиРасчетовСПартнерами();
			//-- НЕ УТ
			
			ЗафиксироватьТранзакцию();
			
			Прервать;// Выходим из цикла попыток блокировки
			
		Исключение
			ОтменитьТранзакцию();
			
			НомерПопытки = НомерПопытки + 1;
			Текст =  НСтр("ru = 'Попытка';
							|en = 'Try'", ОбщегоНазначения.КодОсновногоЯзыка())+ " №" + НомерПопытки + Символы.ПС + Символы.ПС;
			Событие = НСтр("ru = 'Включение онлайн взаиморасчетов с партнерами. Этап 2.';
							|en = 'Enabling online settlements with partners. Stage 2.'", 
							ОбщегоНазначения.КодОсновногоЯзыка());
			ЗаписьЖурналаРегистрации(Событие,
				УровеньЖурналаРегистрации.Ошибка,,, Текст + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			Если НомерПопытки >= 5 Тогда
				ВызватьИсключение НСтр("ru = 'Не удалось завершить переход на онлайн взаиморасчеты. 
				|Ошибки см. в журнале регистрации.';
				|en = 'Cannot complete the transition to online settlements. 
				|See event log for errors.'");
			КонецЕсли;
		КонецПопытки;
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамераПерехода, ВсегоОбработано);
	
КонецПроцедуры

//Очищает новые регистры расчетов в транзакции с блокировкой всех расчетов.
Процедура ОчиститьРегистрыПриВыключенииНовойАрхитектуры(Параметры = Неопределено, УникальныйИдентификатор = Неопределено) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		ВернутьДвиженияПоРасчетамСКлиентами();
		//++ НЕ УТ
		Справочники.НастройкиХозяйственныхОпераций.ЗаменитьИсточникиРасчетовСПартнерами();
		//-- НЕ УТ
		Константы.НоваяАрхитектураВзаиморасчетов.Установить(Ложь);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Выключение онлайн взаиморасчетов с партнерами.';
										|en = 'Disable online mutual settlements with partners.'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
		
	ОчиститьРегистрыВзаиморасчетов();
	
	УдалитьСистемныеКорректировкиРегистров(Дата(1,1,1));
		
	ВключитьИтогиРегистровРасчетов();
	
КонецПроцедуры

//Заполнение новых регистров по всем объектам расчетов.
Процедура ЗаполнитьПоВсемРасчетам(НачальноеЗаполнение = Ложь) Экспорт
	
	ОчиститьРегистрыВзаиморасчетов(НачальноеЗаполнение);
	
	ПараметрыРаспределения = ПараметрыРаспределенияРасчетов();
	ПараметрыРаспределения.РаспределениеСУчетомПриемников = Истина;
	ПараметрыРаспределения.ТаблицаИзменений = ПолучитьВсеОбъекты(, Истина);
	ПараметрыРаспределения.ТаблицаИзменений.ЗаполнитьЗначения(НачальноеЗаполнение, "НачальноеЗаполнение");
	ПараметрыРаспределения.ПерезаполнениеРегистровВзаиморасчетов = Истина;
	
	ЗаполнитьОперативныеВзаиморасчетыПоТаблице(ПараметрыРаспределения);
	
КонецПроцедуры

#КонецОбласти

#Область ИсправлениеОшибок

// Формирует текст запроса для поиска некорректных данных в расчетных регистрах. 
// 
// Параметры:
// 	ИмяВТ - Строка - Имя временной таблицы для помещения результата
// 	БезРучныхКорректировок - Булево - необходимость исключения из результата запроса Аналитик расчетов по партнерам 
// 	                                  и Объектов расчетов, по которым введены ручные корректировки регистров.
// Возвращаемое значение:
// 	Строка - Текст запроса
Функция ТекстЗапросаНекорректныхОстаткиВзаиморасчетовПоСрокам(ИмяВТ = "", БезРучныхКорректировок = Истина) Экспорт
	
	МассивТекстовЗапросов = Новый Массив;
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Ключи.Ссылка      КАК АналитикаУчетаПоПартнерам,
		|	Ключи.Организация КАК Организация
		|ПОМЕСТИТЬ ВтАналитика
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаПоПартнерам КАК Ключи
		|ГДЕ
		|	Ключи.Организация В (&МассивОрганизаций)
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаПоПартнерам";
	МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	МИНИМУМ(ВложенныйЗапрос.Период) КАК Период
		|ПОМЕСТИТЬ ПериодВводаОстатков
		|ИЗ (
		|	ВЫБРАТЬ
		|		Ввод.Дата КАК Период
		|	ИЗ
		|		Документ.ВводОстатковВзаиморасчетов КАК Ввод
		|	ГДЕ
		|		Ввод.Проведен
		|	) КАК ВложенныйЗапрос
		|;
		|
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.ТипРасчетов                           КАК ТипРасчетов,
		|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам             КАК АналитикаУчетаПоПартнерам,
		|	ВложенныйЗапрос.ОбъектРасчетов                        КАК ОбъектРасчетов,
		|	ВложенныйЗапрос.Валюта                                КАК Валюта,
		|	СУММА(ВложенныйЗапрос.Сумма)                          КАК Сумма,
		|	СУММА(ВложенныйЗапрос.СуммаПоСрокам)                  КАК СуммаПоСрокам
		|ПОМЕСТИТЬ ВтОстатки
		|ИЗ (ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
		|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам                     КАК АналитикаУчетаПоПартнерам,
		|		РасчетыСКлиентами.ОбъектРасчетов                                КАК ОбъектРасчетов,
		|		РасчетыСКлиентами.Валюта                                        КАК Валюта,
		|		
		|		РасчетыСКлиентами.СуммаОстаток                                  КАК Сумма,
		|		0                                                               КАК СуммаПоСрокам
		|	ИЗ
		|		РегистрНакопления.РасчетыСКлиентами.Остатки(&НачалоМесяца, АналитикаУчетаПоПартнерам В
		|			(ВЫБРАТЬ
		|				ВтАналитика.АналитикаУчетаПоПартнерам
		|			ИЗ
		|				ВтАналитика КАК ВтАналитика)) КАК РасчетыСКлиентами
		|	ГДЕ
		|		РасчетыСКлиентами.СуммаОстаток <> 0
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)                     КАК ТипРасчетов,
		|		РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам                                 КАК АналитикаУчетаПоПартнерам,
		|		РасчетыСКлиентамиПоСрокам.ОбъектРасчетов                                            КАК ОбъектРасчетов,
		|		РасчетыСКлиентамиПоСрокам.Валюта                                                    КАК Валюта,
		|		
		|		0                                                                                   КАК Сумма,
		|		РасчетыСКлиентамиПоСрокам.ДолгОстаток - РасчетыСКлиентамиПоСрокам.ПредоплатаОстаток КАК СуммаПоСрокам
		|	ИЗ
		|		РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки(&НачалоМесяца, АналитикаУчетаПоПартнерам В
		|			(ВЫБРАТЬ
		|				ВтАналитика.АналитикаУчетаПоПартнерам
		|			ИЗ
		|				ВтАналитика КАК ВтАналитика)) КАК РасчетыСКлиентамиПоСрокам
		|	ГДЕ
		|		РасчетыСКлиентамиПоСрокам.ДолгОстаток <> 0
		|		ИЛИ РасчетыСКлиентамиПоСрокам.ПредоплатаОстаток <> 0
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов,
		|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам                     КАК АналитикаУчетаПоПартнерам,
		|		РасчетыСПоставщиками.ОбъектРасчетов                                КАК ОбъектРасчетов,
		|		РасчетыСПоставщиками.Валюта                                        КАК Валюта,
		|		
		|		РасчетыСПоставщиками.СуммаОстаток                                  КАК Сумма,
		|		0                                                                  КАК СуммаПоСрокам
		|	ИЗ
		|		РегистрНакопления.РасчетыСПоставщиками.Остатки(&НачалоМесяца, АналитикаУчетаПоПартнерам В
		|			(ВЫБРАТЬ
		|				ВтАналитика.АналитикаУчетаПоПартнерам
		|			ИЗ
		|				ВтАналитика КАК ВтАналитика)) КАК РасчетыСПоставщиками
		|	ГДЕ
		|		РасчетыСПоставщиками.СуммаОстаток <> 0
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)                        КАК ТипРасчетов,
		|		РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам                                    КАК АналитикаУчетаПоПартнерам,
		|		РасчетыСПоставщикамиПоСрокам.ОбъектРасчетов                                               КАК ОбъектРасчетов,
		|		РасчетыСПоставщикамиПоСрокам.Валюта                                                       КАК Валюта,
		|		
		|		0                                                                                         КАК Сумма,
		|		РасчетыСПоставщикамиПоСрокам.ПредоплатаОстаток - РасчетыСПоставщикамиПоСрокам.ДолгОстаток КАК СуммаПоСрокам
		|	ИЗ
		|		РегистрНакопления.РасчетыСПоставщикамиПоСрокам.Остатки(&НачалоМесяца, АналитикаУчетаПоПартнерам В
		|			(ВЫБРАТЬ
		|				ВтАналитика.АналитикаУчетаПоПартнерам
		|			ИЗ
		|				ВтАналитика КАК ВтАналитика)) КАК РасчетыСПоставщикамиПоСрокам
		|	ГДЕ
		|		РасчетыСПоставщикамиПоСрокам.ДолгОстаток <> 0
		|		ИЛИ РасчетыСПоставщикамиПоСрокам.ПредоплатаОстаток <> 0) КАК ВложенныйЗапрос
		|	ЛЕВОЕ СОЕДИНЕНИЕ ПериодВводаОстатков КАК ПериодВводаОстатков
		|		ПО ИСТИНА
		|ГДЕ
		|	&НачалоМесяца > ЕСТЬNULL(ПериодВводаОстатков.Период, ДАТАВРЕМЯ(1,1,1))
		|СГРУППИРОВАТЬ ПО 
		|	ВложенныйЗапрос.ТипРасчетов,
		|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
		|	ВложенныйЗапрос.ОбъектРасчетов,
		|	ВложенныйЗапрос.Валюта
		|ИМЕЮЩИЕ
		|	СУММА(ВложенныйЗапрос.СуммаПоСрокам) <> СУММА(ВложенныйЗапрос.Сумма)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)    КАК ТипРасчетов,
		|	РасчетыПоСрокам.АналитикаУчетаПоПартнерам                          КАК АналитикаУчетаПоПартнерам,
		|	РасчетыПоСрокам.ОбъектРасчетов                                     КАК ОбъектРасчетов,
		|	РасчетыПоСрокам.Валюта                                             КАК Валюта,
		|	ВЫБОР КОГДА НЕ РасчетыПоСрокам.РасчетныйДокумент ЕСТЬ NULL
		|		И НЕ РасчетыПоСрокам.ДатаПлановогоПогашения ЕСТЬ NULL
		|		И НЕ РасчетыПоСрокам.ДатаВозникновения ЕСТЬ NULL
		|		ТОГДА 0
		|		ИНАЧЕ 0 
		|	КОНЕЦ                                                              КАК Сумма,
		|	РасчетыПоСрокам.ДолгОстаток + РасчетыПоСрокам.ПредоплатаОстаток    КАК СуммаПоСрокам
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки(&НачалоМесяца, АналитикаУчетаПоПартнерам В
		|			(ВЫБРАТЬ
		|				ВтАналитика.АналитикаУчетаПоПартнерам
		|			ИЗ
		|				ВтАналитика КАК ВтАналитика)) КАК РасчетыПоСрокам
		|ГДЕ
		|	РасчетыПоСрокам.ДолгОстаток < 0
		|	ИЛИ РасчетыПоСрокам.ПредоплатаОстаток < 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов,
		|	РасчетыПоСрокам.АналитикаУчетаПоПартнерам                          КАК АналитикаУчетаПоПартнерам,
		|	РасчетыПоСрокам.ОбъектРасчетов                                     КАК ОбъектРасчетов,
		|	РасчетыПоСрокам.Валюта                                             КАК Валюта,
		|	ВЫБОР КОГДА НЕ РасчетыПоСрокам.РасчетныйДокумент ЕСТЬ NULL
		|		И НЕ РасчетыПоСрокам.ДатаПлановогоПогашения ЕСТЬ NULL
		|		И НЕ РасчетыПоСрокам.ДатаВозникновения ЕСТЬ NULL
		|		ТОГДА 0
		|		ИНАЧЕ 0 
		|	КОНЕЦ                                                              КАК Сумма,
		|	РасчетыПоСрокам.ДолгОстаток + РасчетыПоСрокам.ПредоплатаОстаток    КАК СуммаПоСрокам
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам.Остатки(&НачалоМесяца, АналитикаУчетаПоПартнерам В
		|			(ВЫБРАТЬ
		|				ВтАналитика.АналитикаУчетаПоПартнерам
		|			ИЗ
		|				ВтАналитика КАК ВтАналитика)) КАК РасчетыПоСрокам
		|ГДЕ
		|	РасчетыПоСрокам.ДолгОстаток < 0
		|	ИЛИ РасчетыПоСрокам.ПредоплатаОстаток < 0";
	Если Не БезРучныхКорректировок Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НачалоМесяца", "&НачалоПериода");
	КонецЕсли;
	МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "УНИЧТОЖИТЬ ПериодВводаОстатков";
	МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = ТекстЗапросаВтРучныхКорректировок();
	Если Не БезРучныхКорректировок Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НачалоМесяца", "&НачалоПериода");
	КонецЕсли;
	МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	Если БезРучныхКорректировок Тогда
		ТекстЗапроса = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Аналитика.Организация             КАК Организация,
			|	Остатки.ТипРасчетов               КАК ТипРасчетов,
			|	Остатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
			|	Остатки.ОбъектРасчетов            КАК ОбъектРасчетов,
			|	Остатки.Валюта                    КАК ВалютаРасчетов
			|ПОМЕСТИТЬ ИмяВТ
			|ИЗ ВтОстатки КАК Остатки
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитика КАК Аналитика
			|		ПО Аналитика.АналитикаУчетаПоПартнерам = Остатки.АналитикаУчетаПоПартнерам
			|ГДЕ
			|	НЕ (Остатки.АналитикаУчетаПоПартнерам, Остатки.ОбъектРасчетов) В
			|				(ВЫБРАТЬ
			|					ВтРучныеКорректировки.АналитикаУчетаПоПартнерам,
			|					ВтРучныеКорректировки.ОбъектРасчетов
			|				ИЗ
			|					ВтРучныеКорректировки)
			|	И НЕ Остатки.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)";
	Иначе
		ТекстЗапроса = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Аналитика.Организация             КАК Организация,
			|	Остатки.ТипРасчетов               КАК ТипРасчетов,
			|	Остатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
			|	Остатки.ОбъектРасчетов            КАК ОбъектРасчетов,
			|	Остатки.Валюта                    КАК ВалютаРасчетов,
			|	ВтРучныеКорректировки.Регистратор КАК РучнаяКорректировка
			|ПОМЕСТИТЬ ИмяВТ
			|ИЗ
			|	ВтОстатки КАК Остатки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитика КАК Аналитика
			|		ПО (Аналитика.АналитикаУчетаПоПартнерам = Остатки.АналитикаУчетаПоПартнерам)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРучныеКорректировки КАК ВтРучныеКорректировки
			|		ПО Остатки.АналитикаУчетаПоПартнерам = ВтРучныеКорректировки.АналитикаУчетаПоПартнерам
			|			И Остатки.ОбъектРасчетов = ВтРучныеКорректировки.ОбъектРасчетов
			|ГДЕ
			|	&ПериодВДиапазонеПересчета";
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяВТ) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ПОМЕСТИТЬ ИмяВТ","ПОМЕСТИТЬ "+ИмяВТ);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ПОМЕСТИТЬ ИмяВТ","");
	КонецЕсли;
	
	МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапросов, ";");
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Формирует текст запроса для поиска записей с развернутым сальдо в расчетных регистрах. 
// 
// Параметры:
// 	ИмяВТ - Строка - Имя временной таблицы для помещения результата
// 	БезРучныхКорректировок - Булево - необходимость исключения из результата запроса Аналитик расчетов по партнерам 
// Возвращаемое значение:
// 	Строка - Текст запроса
Функция ТекстЗапросаРазвернутоеСальдо(ИмяВт = "", БезРучныхКорректировок = Истина) Экспорт
	
	МассивТекстовЗапросов = Новый Массив;
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Ключи.Ссылка      КАК АналитикаУчетаПоПартнерам,
		|	Ключи.Организация КАК Организация
		|ПОМЕСТИТЬ ВтАналитика
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаПоПартнерам КАК Ключи
		|ГДЕ
		|	Ключи.Организация В (&МассивОрганизаций)
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаПоПартнерам";
	МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
		|	РасчетыСКлиентамиОстатки.АналитикаУчетаПоПартнерам              КАК АналитикаУчетаПоПартнерам,
		|	РасчетыСКлиентамиОстатки.ОбъектРасчетов                         КАК ОбъектРасчетов,
		|	РасчетыСКлиентамиОстатки.Валюта                                 КАК Валюта,
		|	РасчетыСКлиентамиОстатки.ДолгОстаток                            КАК ДолгОстаток,
		|	РасчетыСКлиентамиОстатки.ПредоплатаОстаток                      КАК ПредоплатаОстаток
		|ПОМЕСТИТЬ ВтСальдо
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки(&НачалоМесяца, АналитикаУчетаПоПартнерам В
		|			(ВЫБРАТЬ
		|				ВтАналитика.АналитикаУчетаПоПартнерам
		|			ИЗ
		|				ВтАналитика КАК ВтАналитика)) КАК РасчетыСКлиентамиОстатки
		|ГДЕ
		|	РасчетыСКлиентамиОстатки.ДолгОстаток > 0 
		|	И РасчетыСКлиентамиОстатки.ПредоплатаОстаток > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов,
		|	РасчетыСПоставщикамиОстатки.АналитикаУчетаПоПартнерам              КАК АналитикаУчетаПоПартнерам,
		|	РасчетыСПоставщикамиОстатки.ОбъектРасчетов                         КАК ОбъектРасчетов,
		|	РасчетыСПоставщикамиОстатки.Валюта                                 КАК Валюта,
		|	РасчетыСПоставщикамиОстатки.ДолгОстаток                            КАК ДолгОстаток,
		|	РасчетыСПоставщикамиОстатки.ПредоплатаОстаток                      КАК ПредоплатаОстаток
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам.Остатки(&НачалоМесяца, АналитикаУчетаПоПартнерам В
		|			(ВЫБРАТЬ
		|				ВтАналитика.АналитикаУчетаПоПартнерам
		|			ИЗ
		|				ВтАналитика КАК ВтАналитика)) КАК РасчетыСПоставщикамиОстатки
		|ГДЕ
		|	РасчетыСПоставщикамиОстатки.ДолгОстаток > 0 
		|	И РасчетыСПоставщикамиОстатки.ПредоплатаОстаток > 0";
	Если Не БезРучныхКорректировок Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НачалоМесяца", "&НачалоПериода");
	КонецЕсли;
	МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = ТекстЗапросаВтРучныхКорректировок();
	Если Не БезРучныхКорректировок Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НачалоМесяца", "&НачалоПериода");
	КонецЕсли;
	МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	Если БезРучныхКорректировок Тогда
		ТекстЗапроса = "
			|ВЫБРАТЬ
			|	Аналитика.Организация             КАК Организация,
			|	Сальдо.ТипРасчетов                КАК ТипРасчетов,
			|	Сальдо.АналитикаУчетаПоПартнерам  КАК АналитикаУчетаПоПартнерам,
			|	Сальдо.ОбъектРасчетов             КАК ОбъектРасчетов,
			|	Сальдо.Валюта                     КАК ВалютаРасчетов,
			|	Сальдо.ДолгОстаток                КАК ДолгОстаток,
			|	Сальдо.ПредоплатаОстаток          КАК ПредоплатаОстаток
			|ПОМЕСТИТЬ ИмяВТ
			|ИЗ ВтСальдо КАК Сальдо
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитика КАК Аналитика
			|		ПО Аналитика.АналитикаУчетаПоПартнерам = Сальдо.АналитикаУчетаПоПартнерам
			|ГДЕ
			|	НЕ (Сальдо.АналитикаУчетаПоПартнерам, Сальдо.ОбъектРасчетов) В
			|				(ВЫБРАТЬ
			|					ВтРучныеКорректировки.АналитикаУчетаПоПартнерам,
			|					ВтРучныеКорректировки.ОбъектРасчетов
			|				ИЗ
			|					ВтРучныеКорректировки)
			|	И НЕ Сальдо.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)";
	Иначе
		ТекстЗапроса = "
			|ВЫБРАТЬ
			|	Аналитика.Организация             КАК Организация,
			|	Сальдо.ТипРасчетов                КАК ТипРасчетов,
			|	Сальдо.АналитикаУчетаПоПартнерам  КАК АналитикаУчетаПоПартнерам,
			|	Сальдо.ОбъектРасчетов             КАК ОбъектРасчетов,
			|	Сальдо.Валюта                     КАК ВалютаРасчетов,
			|	Сальдо.ДолгОстаток                КАК ДолгОстаток,
			|	Сальдо.ПредоплатаОстаток          КАК ПредоплатаОстаток,
			|	ВтРучныеКорректировки.Регистратор КАК РучнаяКорректировка
			|ПОМЕСТИТЬ ИмяВТ
			|ИЗ
			|	ВтСальдо КАК Сальдо
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитика КАК Аналитика
			|		ПО (Аналитика.АналитикаУчетаПоПартнерам = Сальдо.АналитикаУчетаПоПартнерам)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРучныеКорректировки КАК ВтРучныеКорректировки
			|		ПО Сальдо.АналитикаУчетаПоПартнерам = ВтРучныеКорректировки.АналитикаУчетаПоПартнерам
			|			И Сальдо.ОбъектРасчетов = ВтРучныеКорректировки.ОбъектРасчетов
			|ГДЕ
			|	&ПериодВДиапазонеПересчета";
	КонецЕсли;
	
	Если ИмяВт <> "" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ПОМЕСТИТЬ ИмяВТ","ПОМЕСТИТЬ "+ИмяВт);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ПОМЕСТИТЬ ИмяВТ","");
	КонецЕсли;
	
	МассивТекстовЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапросов, ";");
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Временная таблица для определения наличичия ручных корректировок регистров 
// при проверке развернутого сальдо и некорректных остатков.
// 
// Возвращаемое значение:
//  Строка - Текст запроса
//
Функция ТекстЗапросаВтРучныхКорректировок()
	
	Возврат "
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ОбъектРасчетов КАК ОбъектРасчетов,
	|	МАКСИМУМ(ВложенныйЗапрос.Регистратор) КАК Регистратор
	|ПОМЕСТИТЬ ВтРучныеКорректировки
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		РасчетыСКлиентамиПоСрокам.ОбъектРасчетов            КАК ОбъектРасчетов,
	|		РасчетыСКлиентамиПоСрокам.Регистратор               КАК Регистратор
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентамиПоСрокам
	|	ГДЕ
	|		РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам В
	|			(ВЫБРАТЬ
	|				Т.АналитикаУчетаПоПартнерам
	|			ИЗ
	|				ВтАналитика КАК Т)
	|		И РасчетыСКлиентамиПоСрокам.Активность
	|		И РасчетыСКлиентамиПоСрокам.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|		И ВЫРАЗИТЬ(РасчетыСКлиентамиПоСрокам.Регистратор КАК Документ.КорректировкаРегистров).Операция = ЗНАЧЕНИЕ(Перечисление.ОперацииКорректировкиРегистров.РучнаяКорректировка)
	|		И РасчетыСКлиентамиПоСрокам.Период <= &НачалоМесяца
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам,
	|		РасчетыСПоставщикамиПоСрокам.ОбъектРасчетов,
	|		РасчетыСПоставщикамиПоСрокам.Регистратор
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиПоСрокам
	|	ГДЕ
	|		РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам В
	|			(ВЫБРАТЬ
	|				Т.АналитикаУчетаПоПартнерам
	|			ИЗ
	|				ВтАналитика КАК Т)
	|		И РасчетыСПоставщикамиПоСрокам.Активность
	|		И РасчетыСПоставщикамиПоСрокам.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|		И ВЫРАЗИТЬ(РасчетыСПоставщикамиПоСрокам.Регистратор КАК Документ.КорректировкаРегистров).Операция = ЗНАЧЕНИЕ(Перечисление.ОперацииКорректировкиРегистров.РучнаяКорректировка)
	|		И РасчетыСПоставщикамиПоСрокам.Период <= &НачалоМесяца
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам,
	|		РасчетыСКлиентами.ОбъектРасчетов,
	|		РасчетыСКлиентами.Регистратор
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|	ГДЕ
	|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам В
	|			(ВЫБРАТЬ
	|				Т.АналитикаУчетаПоПартнерам
	|			ИЗ
	|				ВтАналитика КАК Т)
	|		И РасчетыСКлиентами.Активность
	|		И РасчетыСКлиентами.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|		И ВЫРАЗИТЬ(РасчетыСКлиентами.Регистратор КАК Документ.КорректировкаРегистров).Операция = ЗНАЧЕНИЕ(Перечисление.ОперацииКорректировкиРегистров.РучнаяКорректировка)
	|		И РасчетыСКлиентами.Период <= &НачалоМесяца
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам,
	|		РасчетыСПоставщиками.ОбъектРасчетов,
	|		РасчетыСПоставщиками.Регистратор
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|	ГДЕ
	|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам В
	|			(ВЫБРАТЬ
	|				Т.АналитикаУчетаПоПартнерам
	|			ИЗ
	|				ВтАналитика КАК Т)
	|		И РасчетыСПоставщиками.Активность
	|		И РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|		И ВЫРАЗИТЬ(РасчетыСПоставщиками.Регистратор КАК Документ.КорректировкаРегистров).Операция = ЗНАЧЕНИЕ(Перечисление.ОперацииКорректировкиРегистров.РучнаяКорректировка)
	|		И РасчетыСПоставщиками.Период <= &НачалоМесяца) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ОбъектРасчетов
	|";
	
КонецФункции

// Исправление остатков по взаиморасчетам
// 
// Параметры:
// 	Период - Дата - Период проверки, на начало месяца периода будут проверены остатки, 
//		если они некорректные, то будет создана корректировка на конец предыдущего месяца.
//		Если на начало периода остатки в порядке, а на начало следующего месяца - нет, то будет запущен частичный пересчет расчетов.
// 	МассивОрганизаций - Массив из СправочникСсылка.Организации - Массив организация, по которым требуется исправить остатки взаиморасчетов
Процедура ИсправитьОстаткиВзаиморасчетов(Период, ЗНАЧ МассивОрганизаций = Неопределено) Экспорт
	
	ДатаКорректировки    = НачалоМесяца(Период)-1; //Для корректировки и движений
	ПорядокКорректировки = Формат(ДатаКорректировки,"ДФ=ггггММдд9ЧЧммсс99"); //Для запроса данных остатков.
	ПериодРасчета        = НачалоМесяца(Период); //Для сравнения остатков
	
	МассивОрганизаций = ?(МассивОрганизаций = Неопределено,
							Справочники.Организации.ДоступныеОрганизации(),
							МассивОрганизаций);
	
	ТипыРасчетов = Новый Массив;
	ТипыРасчетов.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
	ТипыРасчетов.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = ТекстЗапросаНекорректныхОстаткиВзаиморасчетовПоСрокам();
	Запрос.УстановитьПараметр("НачалоМесяца",       ПериодРасчета);
	Запрос.УстановитьПараметр("МассивОрганизаций",  МассивОрганизаций);
	Запрос.УстановитьПараметр("Порядок",            ПорядокКорректировки);
	
	//Удалим корректировки
	НекорректныеОстатки = Запрос.Выполнить().Выгрузить();
	
	Если НекорректныеОстатки.Количество() > 0 Тогда
		МассивОрганизацийДляОбработки = НекорректныеОстатки.Скопировать(,"Организация");
		МассивОрганизацийДляОбработки.Свернуть("Организация");
		МассивОрганизацийДляОбработки = МассивОрганизацийДляОбработки.ВыгрузитьКолонку("Организация");
		Для Каждого Организация Из МассивОрганизаций Цикл
			УдалитьСистемныеКорректировкиРегистров(ДатаКорректировки,Организация)
		КонецЦикла;
		
		Запрос.Текст = "УНИЧТОЖИТЬ ВтАналитика; УНИЧТОЖИТЬ ВтОстатки; УНИЧТОЖИТЬ ВтРучныеКорректировки";
		Запрос.Выполнить();
		
		//Перечитываем остатки
		Запрос.Текст = ТекстЗапросаНекорректныхОстаткиВзаиморасчетовПоСрокам();
		
		НекорректныеОстатки = Запрос.Выполнить().Выгрузить();
		НекорректныеОстатки.Индексы.Добавить("ТипРасчетов, Организация");
		СтруктураПоиска = Новый Структура("ТипРасчетов, Организация");
		
		МассивОрганизацийДляОбработки = НекорректныеОстатки.Скопировать(,"Организация");
		МассивОрганизацийДляОбработки.Свернуть("Организация");
		МассивОрганизацийДляОбработки = МассивОрганизацийДляОбработки.ВыгрузитьКолонку("Организация");
		
		Для Каждого Организация Из МассивОрганизацийДляОбработки Цикл
			
			КорректировкаОбъект = КорректировкаРегистров(ДатаКорректировки, Организация, Перечисления.ОперацииКорректировкиРегистров.ИсправлениеРазрывовОстатковВзаиморасчетов);
			
			Для Каждого ТипРасчетов Из ТипыРасчетов Цикл
				
				Если ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом Тогда
					Движения = КорректировкаОбъект.Движения["РасчетыСКлиентамиПоСрокам"]; // РегистрНакопленияНаборЗаписей
				Иначе
					Движения = КорректировкаОбъект.Движения["РасчетыСПоставщикамиПоСрокам"]; // РегистрНакопленияНаборЗаписей
				КонецЕсли;
				
				СтруктураПоиска.ТипРасчетов = ТипРасчетов;
				СтруктураПоиска.Организация = Организация;
				ЗаписиНекорректныхОстатков = НекорректныеОстатки.НайтиСтроки(СтруктураПоиска);
				
				Для Каждого ЗаписьНекорректныхОстатков Из ЗаписиНекорректныхОстатков Цикл
					
					//Рассчитаем корректные остатки.
					
					ДополнительныеСвойства = Новый Структура;
					
					ОсновныеПараметры = СтруктураПараметровЗаполненияВзаиморасчетов();
					
					ЗаполнитьЗначенияСвойств(ОсновныеПараметры, ЗаписьНекорректныхОстатков);
					ОсновныеПараметры.ЭтоРасчетыСКлиентами             = ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
					ОсновныеПараметры.ДополнительныеСвойстваПроведения = ДополнительныеСвойства;
					ОсновныеПараметры.ЗаписыватьИзменения              = Ложь;
					
					ЗаполнитьОперативныеВзаиморасчеты(ОсновныеПараметры, ОсновныеПараметры.ДополнительныеСвойстваПроведения);
					
					//Сравним с остатками до и рассчитаем записи корректировки.
					
					Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", ЗаписьНекорректныхОстатков.АналитикаУчетаПоПартнерам);
					Запрос.УстановитьПараметр("ОбъектРасчетов",            ЗаписьНекорректныхОстатков.ОбъектРасчетов);
					Запрос.УстановитьПараметр("Валюта",                    ЗаписьНекорректныхОстатков.ВалютаРасчетов);
					Если ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом Тогда
						Запрос.УстановитьПараметр("РасчетыПоСрокам",           ДополнительныеСвойства["РасчетыСКлиентамиПоСрокам"]);
					Иначе
						Запрос.УстановитьПараметр("РасчетыПоСрокам",           ДополнительныеСвойства["РасчетыСПоставщикамиПоСрокам"]);
					КонецЕсли;
					
					Запрос.Текст = "
						//Записи после
						|ВЫБРАТЬ
						|	РасчетыПоСрокамПосле.Период                        КАК Период,
						|	РасчетыПоСрокамПосле.ВидДвижения                   КАК ВидДвижения,
						|	
						|	РасчетыПоСрокамПосле.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
						|	РасчетыПоСрокамПосле.ОбъектРасчетов                КАК ОбъектРасчетов,
						|	РасчетыПоСрокамПосле.Валюта                        КАК Валюта,
						|	РасчетыПоСрокамПосле.РасчетныйДокумент             КАК РасчетныйДокумент,
						|	РасчетыПоСрокамПосле.ДатаПлановогоПогашения        КАК ДатаПлановогоПогашения,
						|	РасчетыПоСрокамПосле.ДатаВозникновения             КАК ДатаВозникновения,
						|	
						|	ВЫРАЗИТЬ(РасчетыПоСрокамПосле.Предоплата КАК ЧИСЛО(31,2))     КАК Предоплата,
						|	ВЫРАЗИТЬ(РасчетыПоСрокамПосле.ПредоплатаРегл КАК ЧИСЛО(31,2)) КАК ПредоплатаРегл,
						|	ВЫРАЗИТЬ(РасчетыПоСрокамПосле.ПредоплатаУпр КАК ЧИСЛО(31,2))  КАК ПредоплатаУпр,
						|	ВЫРАЗИТЬ(РасчетыПоСрокамПосле.Долг КАК ЧИСЛО(31,2))           КАК Долг,
						|	ВЫРАЗИТЬ(РасчетыПоСрокамПосле.ДолгРегл КАК ЧИСЛО(31,2))       КАК ДолгРегл,
						|	ВЫРАЗИТЬ(РасчетыПоСрокамПосле.ДолгУпр КАК ЧИСЛО(31,2))        КАК ДолгУпр,
						|	
						|	РасчетыПоСрокамПосле.ПорядокЗачета                 КАК ПорядокЗачета,
						|	РасчетыПоСрокамПосле.ПорядокОперации               КАК ПорядокОперации,
						|	РасчетыПоСрокамПосле.СвязанныйДокумент             КАК СвязанныйДокумент,
						|	РасчетыПоСрокамПосле.ВалютаДокумента               КАК ВалютаДокумента,
						|	РасчетыПоСрокамПосле.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств
						|ПОМЕСТИТЬ РасчетыПоСрокамПосле
						|ИЗ &РасчетыПоСрокам КАК РасчетыПоСрокамПосле
						|ИНДЕКСИРОВАТЬ ПО
						|	АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта, РасчетныйДокумент
						|;
						//Расхождения
						|ВЫБРАТЬ
						|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
						|	ВложенныйЗапрос.ОбъектРасчетов            КАК ОбъектРасчетов,
						|	ВложенныйЗапрос.Валюта                    КАК Валюта,
						|	ВложенныйЗапрос.РасчетныйДокумент         КАК РасчетныйДокумент,
						|	ВложенныйЗапрос.ДатаПлановогоПогашения    КАК ДатаПлановогоПогашения,
						|	ВложенныйЗапрос.ДатаВозникновения         КАК ДатаВозникновения,
						|	СУММА(ВложенныйЗапрос.Предоплата)         КАК Предоплата,
						|	СУММА(ВложенныйЗапрос.ПредоплатаРегл)     КАК ПредоплатаРегл,
						|	СУММА(ВложенныйЗапрос.ПредоплатаУпр)      КАК ПредоплатаУпр,
						|	СУММА(ВложенныйЗапрос.Долг)               КАК Долг,
						|	СУММА(ВложенныйЗапрос.ДолгРегл)           КАК ДолгРегл,
						|	СУММА(ВложенныйЗапрос.ДолгУпр)            КАК ДолгУпр
						|ПОМЕСТИТЬ ТаблицаИзменений
						|ИЗ
						|	(ВЫБРАТЬ
						|		РасчетыПоСрокамДо.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
						|		РасчетыПоСрокамДо.ОбъектРасчетов            КАК ОбъектРасчетов,
						|		РасчетыПоСрокамДо.Валюта                    КАК Валюта,
						|		РасчетыПоСрокамДо.РасчетныйДокумент         КАК РасчетныйДокумент,
						|		РасчетыПоСрокамДо.ДатаПлановогоПогашения    КАК ДатаПлановогоПогашения,
						|		РасчетыПоСрокамДо.ДатаВозникновения         КАК ДатаВозникновения,
						|		-РасчетыПоСрокамДо.ПредоплатаОстаток        КАК Предоплата,
						|		-РасчетыПоСрокамДо.ПредоплатаРеглОстаток    КАК ПредоплатаРегл,
						|		-РасчетыПоСрокамДо.ПредоплатаУпрОстаток     КАК ПредоплатаУпр,
						|		-РасчетыПоСрокамДо.ДолгОстаток              КАК Долг,
						|		-РасчетыПоСрокамДо.ДолгРеглОстаток          КАК ДолгРегл,
						|		-РасчетыПоСрокамДо.ДолгУпрОстаток           КАК ДолгУпр
						|	ИЗ
						|		РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки(&НачалоМесяца, 
						|			АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
						|			И ОбъектРасчетов = &ОбъектРасчетов
						|			И Валюта = &Валюта) КАК РасчетыПоСрокамДо
						|		
						|	ОБЪЕДИНИТЬ ВСЕ
						|	
						|	ВЫБРАТЬ
						|		РасчетыПоСрокамПосле.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
						|		РасчетыПоСрокамПосле.ОбъектРасчетов            КАК ОбъектРасчетов,
						|		РасчетыПоСрокамПосле.Валюта                    КАК Валюта,
						|		РасчетыПоСрокамПосле.РасчетныйДокумент         КАК РасчетныйДокумент,
						|		РасчетыПоСрокамПосле.ДатаПлановогоПогашения    КАК ДатаПлановогоПогашения,
						|		РасчетыПоСрокамПосле.ДатаВозникновения         КАК ДатаВозникновения,
						|		ВЫБОР КОГДА РасчетыПоСрокамПосле.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
						|				ТОГДА -РасчетыПоСрокамПосле.Предоплата
						|			ИНАЧЕ РасчетыПоСрокамПосле.Предоплата
						|		КОНЕЦ                                          КАК Предоплата,
						|		ВЫБОР КОГДА РасчетыПоСрокамПосле.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
						|				ТОГДА -РасчетыПоСрокамПосле.ПредоплатаРегл
						|			ИНАЧЕ РасчетыПоСрокамПосле.ПредоплатаРегл
						|		КОНЕЦ                                          КАК ПредоплатаРегл,
						|		ВЫБОР КОГДА РасчетыПоСрокамПосле.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
						|				ТОГДА -РасчетыПоСрокамПосле.ПредоплатаУпр
						|			ИНАЧЕ РасчетыПоСрокамПосле.ПредоплатаУпр
						|		КОНЕЦ                                          КАК ПредоплатаУпр,
						|		ВЫБОР КОГДА РасчетыПоСрокамПосле.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
						|				ТОГДА -РасчетыПоСрокамПосле.Долг
						|			ИНАЧЕ РасчетыПоСрокамПосле.Долг
						|		КОНЕЦ                                          КАК Долг,
						|		ВЫБОР КОГДА РасчетыПоСрокамПосле.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
						|				ТОГДА -РасчетыПоСрокамПосле.ДолгРегл
						|			ИНАЧЕ РасчетыПоСрокамПосле.ДолгРегл
						|		КОНЕЦ                                          КАК ДолгРегл,
						|		ВЫБОР КОГДА РасчетыПоСрокамПосле.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
						|				ТОГДА -РасчетыПоСрокамПосле.ДолгУпр
						|			ИНАЧЕ РасчетыПоСрокамПосле.ДолгУпр
						|		КОНЕЦ                                          КАК ДолгУпр
						|	ИЗ
						|		РасчетыПоСрокамПосле КАК РасчетыПоСрокамПосле
						|	ГДЕ
						|		РасчетыПоСрокамПосле.Период < &НачалоМесяца) КАК ВложенныйЗапрос
						|СГРУППИРОВАТЬ ПО
						|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
						|	ВложенныйЗапрос.ОбъектРасчетов,
						|	ВложенныйЗапрос.Валюта,
						|	ВложенныйЗапрос.РасчетныйДокумент,
						|	ВложенныйЗапрос.ДатаПлановогоПогашения,
						|	ВложенныйЗапрос.ДатаВозникновения
						|ИМЕЮЩИЕ
						|	СУММА(ВложенныйЗапрос.Предоплата) <> 0 ИЛИ СУММА(ВложенныйЗапрос.Долг) <> 0
						|ИНДЕКСИРОВАТЬ ПО
						|	АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта, РасчетныйДокумент
						|;
						|
						//Данные для движений
						|ВЫБРАТЬ
						|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                       КАК ВидДвижения,
						|	
						|	Изменения.АналитикаУчетаПоПартнерам                          КАК АналитикаУчетаПоПартнерам,
						|	Изменения.ОбъектРасчетов                                     КАК ОбъектРасчетов,
						|	Изменения.Валюта                                             КАК Валюта,
						|	Изменения.РасчетныйДокумент                                  КАК РасчетныйДокумент,
						|	Изменения.ДатаПлановогоПогашения                             КАК ДатаПлановогоПогашения,
						|	Изменения.ДатаВозникновения                                  КАК ДатаВозникновения,
						|	
						|	Изменения.Предоплата                                         КАК Предоплата,
						|	Изменения.ПредоплатаРегл                                     КАК ПредоплатаРегл,
						|	Изменения.ПредоплатаУпр                                      КАК ПредоплатаУпр,
						|	Изменения.Долг                                               КАК Долг,
						|	Изменения.ДолгРегл                                           КАК ДолгРегл,
						|	Изменения.ДолгУпр                                            КАК ДолгУпр,
						|	
						|	МИНИМУМ(РасчетыПоСрокамПосле.ПорядокЗачета)                  КАК ПорядокЗачета,
						|	МИНИМУМ(РасчетыПоСрокамПосле.ПорядокОперации)                КАК ПорядокОперации,
						|	МАКСИМУМ(РасчетыПоСрокамПосле.СвязанныйДокумент)             КАК СвязанныйДокумент,
						|	МИНИМУМ(РасчетыПоСрокамПосле.ВалютаДокумента)                КАК ВалютаДокумента,
						|	МАКСИМУМ(РасчетыПоСрокамПосле.СтатьяДвиженияДенежныхСредств) КАК СтатьяДвиженияДенежныхСредств,
						|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)    КАК ХозяйственнаяОперация
						|
						|ИЗ
						|	ТаблицаИзменений КАК Изменения
						|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетыПоСрокамПосле КАК РасчетыПоСрокамПосле
						|			ПО РасчетыПоСрокамПосле.АналитикаУчетаПоПартнерам    = Изменения.АналитикаУчетаПоПартнерам
						|				И РасчетыПоСрокамПосле.ОбъектРасчетов            = Изменения.ОбъектРасчетов
						|				И РасчетыПоСрокамПосле.Валюта                    = Изменения.Валюта
						|				И РасчетыПоСрокамПосле.РасчетныйДокумент         = Изменения.РасчетныйДокумент
						|ГДЕ
						|	Изменения.Предоплата > 0 ИЛИ Изменения.Долг > 0
						|СГРУППИРОВАТЬ ПО
						|	Изменения.АналитикаУчетаПоПартнерам,
						|	Изменения.ОбъектРасчетов,
						|	Изменения.Валюта,
						|	Изменения.РасчетныйДокумент,
						|	Изменения.ДатаПлановогоПогашения,
						|	Изменения.ДатаВозникновения,
						|	Изменения.Предоплата,
						|	Изменения.ПредоплатаРегл,
						|	Изменения.ПредоплатаУпр,
						|	Изменения.Долг,
						|	Изменения.ДолгРегл,
						|	Изменения.ДолгУпр
						|	
						|ОБЪЕДИНИТЬ ВСЕ
						|	
						|ВЫБРАТЬ
						|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
						|	
						|	Изменения.АналитикаУчетаПоПартнерам                       КАК АналитикаУчетаПоПартнерам,
						|	Изменения.ОбъектРасчетов                                  КАК ОбъектРасчетов,
						|	Изменения.Валюта                                          КАК Валюта,
						|	Изменения.РасчетныйДокумент                               КАК РасчетныйДокумент,
						|	Изменения.ДатаПлановогоПогашения                          КАК ДатаПлановогоПогашения,
						|	Изменения.ДатаВозникновения                               КАК ДатаВозникновения,
						|	
						|	-Изменения.Предоплата                                     КАК Предоплата,
						|	-Изменения.ПредоплатаРегл                                 КАК ПредоплатаРегл,
						|	-Изменения.ПредоплатаУпр                                  КАК ПредоплатаУпр,
						|	-Изменения.Долг                                           КАК Долг,
						|	-Изменения.ДолгРегл                                       КАК ДолгРегл,
						|	-Изменения.ДолгУпр                                        КАК ДолгУпр,
						|	
						|	МИНИМУМ(РасчетыПоСрокамДо.ПорядокЗачета)                  КАК ПорядокЗачета,
						|	МИНИМУМ(РасчетыПоСрокамДо.ПорядокОперации)                КАК ПорядокОперации,
						|	МАКСИМУМ(РасчетыПоСрокамДо.СвязанныйДокумент)             КАК СвязанныйДокумент,
						|	МИНИМУМ(РасчетыПоСрокамДо.ВалютаДокумента)                КАК ВалютаДокумента,
						|	МАКСИМУМ(РасчетыПоСрокамДо.СтатьяДвиженияДенежныхСредств) КАК СтатьяДвиженияДенежныхСредств,
						|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация
						|
						|ИЗ
						|	ТаблицаИзменений КАК Изменения
						|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокамДо
						|			ПО РасчетыПоСрокамДо.АналитикаУчетаПоПартнерам    = Изменения.АналитикаУчетаПоПартнерам
						|				И РасчетыПоСрокамДо.ОбъектРасчетов            = Изменения.ОбъектРасчетов
						|				И РасчетыПоСрокамДо.Валюта                    = Изменения.Валюта
						|				И РасчетыПоСрокамДо.РасчетныйДокумент         = Изменения.РасчетныйДокумент
						|				И РасчетыПоСрокамДо.Период                    < &НачалоМесяца
						|				И РасчетыПоСрокамДо.Активность
						|ГДЕ
						|	Изменения.Предоплата < 0 ИЛИ Изменения.Долг < 0
						|СГРУППИРОВАТЬ ПО
						|	Изменения.АналитикаУчетаПоПартнерам,
						|	Изменения.ОбъектРасчетов,
						|	Изменения.Валюта,
						|	Изменения.РасчетныйДокумент,
						|	Изменения.ДатаПлановогоПогашения,
						|	Изменения.ДатаВозникновения,
						|	-Изменения.Предоплата,
						|	-Изменения.ПредоплатаРегл,
						|	-Изменения.ПредоплатаУпр,
						|	-Изменения.Долг,
						|	-Изменения.ДолгРегл,
						|	-Изменения.ДолгУпр";
						
						Если ТипРасчетов <> Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом Тогда
							Запрос.Текст = СтрЗаменить(Запрос.Текст,
								"РегистрНакопления.РасчетыСКлиентамиПоСрокам",
								"РегистрНакопления.РасчетыСПоставщикамиПоСрокам");
						КонецЕсли;
						
						Результат = Запрос.Выполнить();
						Выборка = Результат.Выбрать();
						
						Запрос.Текст = "УНИЧТОЖИТЬ РасчетыПоСрокамПосле; УНИЧТОЖИТЬ ТаблицаИзменений";
						Запрос.Выполнить();
						
						Пока Выборка.Следующий() Цикл
							
							НовСтр = Движения.Добавить();
							ЗаполнитьЗначенияСвойств(НовСтр, Выборка);
							
							НовСтр.ДокументРегистратор = КорректировкаОбъект.Ссылка;
							НовСтр.Период = ДатаКорректировки;
							Если Выборка.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
								Если Выборка.Долг <> 0 Тогда
									НовСтр.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеДолга;
								Иначе
									НовСтр.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеПредоплаты;
								КонецЕсли;
							Иначе
								Если Выборка.Долг <> 0 Тогда
									НовСтр.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеДолга;
								Иначе
									НовСтр.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеПредоплаты;
								КонецЕсли;
							КонецЕсли;
							
						КонецЦикла;
						
				КонецЦикла;
				
				Движения.Записать();
				
				Для Каждого ЗаписьНекорректныхОстатков Из ЗаписиНекорректныхОстатков Цикл
					
					ОсновныеПараметры = СтруктураПараметровЗаполненияВзаиморасчетов();
					
					ЗаполнитьЗначенияСвойств(ОсновныеПараметры, ЗаписьНекорректныхОстатков);
					ОсновныеПараметры.ЭтоРасчетыСКлиентами = ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
					ОсновныеПараметры.ДатаНачалаПересчета = ПериодРасчета;
					
					ЗаполнитьОперативныеВзаиморасчеты(ОсновныеПараметры);
					
				КонецЦикла;
				
			КонецЦикла;
			
			Если КорректировкаОбъект.Движения.РасчетыСКлиентамиПоСрокам.Количество() = 0 
				И КорректировкаОбъект.Движения.РасчетыСПоставщикамиПоСрокам.Количество() = 0 Тогда
				КорректировкаОбъект.Удалить();
			Иначе
				ДополнитьКорректировкуДвижениямиПоПрочимДоходамРасходам(КорректировкаОбъект);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Запрос.Текст = "УНИЧТОЖИТЬ ВтАналитика; УНИЧТОЖИТЬ ВтОстатки; УНИЧТОЖИТЬ ВтРучныеКорректировки";
	Запрос.Выполнить();
	
	// Если на начало периода все хорошо, а на конец есть некорректные остатки - пересчитаем их.
	Запрос.Текст = ТекстЗапросаНекорректныхОстаткиВзаиморасчетовПоСрокам();
	Запрос.УстановитьПараметр("НачалоМесяца",       КонецМесяца(ПериодРасчета));
	Запрос.УстановитьПараметр("Порядок",            Формат(КонецМесяца(ПериодРасчета),"ДФ=ггггММдд9ЧЧммсс99"));
	НекорректныеОстатки = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Стр Из НекорректныеОстатки Цикл
		
		ОсновныеПараметры = СтруктураПараметровЗаполненияВзаиморасчетов();
		
		ЗаполнитьЗначенияСвойств(ОсновныеПараметры, Стр);
		ОсновныеПараметры.ЭтоРасчетыСКлиентами = Стр.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
		ОсновныеПараметры.ДатаНачалаПересчета  = ПериодРасчета;
		
		ЗаполнитьОперативныеВзаиморасчеты(ОсновныеПараметры);
		
	КонецЦикла;
	
КонецПроцедуры

// Исправление развернутого сальдо по взаиморасчетам
// 
// Параметры:
// 	Период - Дата - Период проверки, требуется указать конец месяца
// 	МассивОрганизаций - Массив из СправочникСсылка.Организации - Массив организация, по которым требуется исправить развернутое сальдо взаиморасчетов
Процедура ИсправитьРазвернутоеСальдо(Период, ЗНАЧ МассивОрганизаций = Неопределено) Экспорт
	
	ДатаКорректировки    = НачалоМесяца(Период)-1; //Для корректировки и движений
	ПорядокКорректировки = Формат(ДатаКорректировки,"ДФ=ггггММдд9ЧЧммсс99"); //Для запроса данных остатков.
	ПериодРасчета        = НачалоМесяца(Период); //Для сравнения остатков
	
	МассивОрганизаций = ?(МассивОрганизаций = Неопределено,
							Справочники.Организации.ДоступныеОрганизации(),
							МассивОрганизаций);
	
	ТипыРасчетов = Новый Массив;
	ТипыРасчетов.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
	ТипыРасчетов.Добавить(Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = ТекстЗапросаРазвернутоеСальдо("АналитикиСуммы");
	Запрос.УстановитьПараметр("НачалоМесяца",       ПериодРасчета);
	Запрос.УстановитьПараметр("МассивОрганизаций",  МассивОрганизаций);
	Запрос.УстановитьПараметр("Порядок",            ПорядокКорректировки);
	Запрос.Выполнить();
	
	РазвернутоеСальдо = Запрос.МенеджерВременныхТаблиц.Таблицы["АналитикиСуммы"].ПолучитьДанные().Выгрузить();
	
	МассивОрганизацийДляОбработки = РазвернутоеСальдо.Скопировать(,"Организация");
	МассивОрганизацийДляОбработки.Свернуть("Организация");
	МассивОрганизацийДляОбработки = МассивОрганизацийДляОбработки.ВыгрузитьКолонку("Организация");
	
	Если РазвернутоеСальдо.Количество() > 0 Тогда
		Для Каждого Организация Из МассивОрганизаций Цикл
			УдалитьСистемныеКорректировкиРегистров(ДатаКорректировки, Организация, Перечисления.ОперацииКорректировкиРегистров.ИсправлениеРазвернутогоСальдоВзаиморасчетов)
		КонецЦикла;
		
		Запрос.Текст = "УНИЧТОЖИТЬ ВтАналитика; УНИЧТОЖИТЬ ВтСальдо; УНИЧТОЖИТЬ АналитикиСуммы; УНИЧТОЖИТЬ ВтРучныеКорректировки";
		Запрос.Выполнить();
		
		//Перечитываем остатки
		
		Запрос.Текст = ТекстЗапросаРазвернутоеСальдо("АналитикиСуммы");
		Запрос.Выполнить();
		РазвернутоеСальдо = Запрос.МенеджерВременныхТаблиц.Таблицы["АналитикиСуммы"].ПолучитьДанные().Выгрузить(); //ТаблицаЗначений
		
		РазвернутоеСальдо.Индексы.Добавить("ТипРасчетов, Организация");
		СтруктураПоиска = Новый Структура("ТипРасчетов, Организация");
		
		Запрос.Текст = "ВЫБРАТЬ
			|	РасчетыСКлиентамиПоСрокамОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
			|	РасчетыСКлиентамиПоСрокамОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
			|	РасчетыСКлиентамиПоСрокамОстатки.Валюта КАК Валюта,
			|	РасчетыСКлиентамиПоСрокамОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
			|	РасчетыСКлиентамиПоСрокамОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
			|	РасчетыСКлиентамиПоСрокамОстатки.ДатаВозникновения КАК ДатаВозникновения,
			|	РасчетыСКлиентамиПоСрокамОстатки.ПредоплатаОстаток КАК Предоплата,
			|	РасчетыСКлиентамиПоСрокамОстатки.ПредоплатаРеглОстаток КАК ПредоплатаРегл,
			|	РасчетыСКлиентамиПоСрокамОстатки.ПредоплатаУпрОстаток КАК ПредоплатаУпр,
			|	РасчетыСКлиентамиПоСрокамОстатки.ДолгОстаток КАК Долг,
			|	РасчетыСКлиентамиПоСрокамОстатки.ДолгРеглОстаток КАК ДолгРегл,
			|	РасчетыСКлиентамиПоСрокамОстатки.ДолгУпрОстаток КАК ДолгУпр
			|ПОМЕСТИТЬ ВТРасчетыСКлиентамиПоСрокамОстатки
			|ИЗ
			|	РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки(&НачалоМесяца, 
			|		(АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта)
			|			В (ВЫБРАТЬ АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов 
			|				ИЗ АналитикиСуммы
			|				ГДЕ ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
			|				)) КАК РасчетыСКлиентамиПоСрокамОстатки
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта, РасчетныйДокумент
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РасчетыСПоставщикамиПоСрокамОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
			|	РасчетыСПоставщикамиПоСрокамОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
			|	РасчетыСПоставщикамиПоСрокамОстатки.Валюта КАК Валюта,
			|	РасчетыСПоставщикамиПоСрокамОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
			|	РасчетыСПоставщикамиПоСрокамОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
			|	РасчетыСПоставщикамиПоСрокамОстатки.ДатаВозникновения КАК ДатаВозникновения,
			|	РасчетыСПоставщикамиПоСрокамОстатки.ПредоплатаОстаток КАК ПредоплатаОстаток,
			|	РасчетыСПоставщикамиПоСрокамОстатки.ПредоплатаРеглОстаток КАК ПредоплатаРеглОстаток,
			|	РасчетыСПоставщикамиПоСрокамОстатки.ПредоплатаУпрОстаток КАК ПредоплатаУпрОстаток,
			|	РасчетыСПоставщикамиПоСрокамОстатки.ДолгОстаток КАК ДолгОстаток,
			|	РасчетыСПоставщикамиПоСрокамОстатки.ДолгРеглОстаток КАК ДолгРеглОстаток,
			|	РасчетыСПоставщикамиПоСрокамОстатки.ДолгУпрОстаток КАК ДолгУпрОстаток
			|ПОМЕСТИТЬ ВТРасчетыСПоставщикамиПоСрокамОстатки
			|ИЗ
			|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам.Остатки(&НачалоМесяца, 
			|		(АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта)
			|			В (ВЫБРАТЬ АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов 
			|					ИЗ АналитикиСуммы
			|					ГДЕ ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
			|					)) КАК РасчетыСПоставщикамиПоСрокамОстатки
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта, РасчетныйДокумент
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
			|	РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
			|	РасчетыПоСрокамОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
			|	РасчетыПоСрокамОстатки.Валюта КАК Валюта,
			|	РасчетыПоСрокамОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
			|	РасчетыПоСрокамОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
			|	РасчетыПоСрокамОстатки.ДатаВозникновения КАК ДатаВозникновения,
			|	РасчетыПоСрокамОстатки.Предоплата КАК Предоплата,
			|	РасчетыПоСрокамОстатки.ПредоплатаРегл КАК ПредоплатаРегл,
			|	РасчетыПоСрокамОстатки.ПредоплатаУпр КАК ПредоплатаУпр,
			|	РасчетыПоСрокамОстатки.Долг КАК Долг,
			|	РасчетыПоСрокамОстатки.ДолгРегл КАК ДолгРегл,
			|	РасчетыПоСрокамОстатки.ДолгУпр КАК ДолгУпр,
			|	МИНИМУМ(РасчетыПоСрокамДо.ПорядокЗачета) КАК ПорядокЗачета,
			|	МИНИМУМ(РасчетыПоСрокамДо.ПорядокОперации) КАК ПорядокОперации,
			|	МАКСИМУМ(РасчетыПоСрокамДо.СвязанныйДокумент) КАК СвязанныйДокумент,
			|	МИНИМУМ(РасчетыПоСрокамДо.ВалютаДокумента) КАК ВалютаДокумента,
			|	МАКСИМУМ(РасчетыПоСрокамДо.СтатьяДвиженияДенежныхСредств) КАК СтатьяДвиженияДенежныхСредств,
			|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка) КАК ХозяйственнаяОперация
			|ИЗ
			|	ВТРасчетыСКлиентамиПоСрокамОстатки КАК РасчетыПоСрокамОстатки
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокамДо
			|		ПО (РасчетыПоСрокамДо.АналитикаУчетаПоПартнерам = РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам)
			|			И (РасчетыПоСрокамДо.ОбъектРасчетов = РасчетыПоСрокамОстатки.ОбъектРасчетов)
			|			И (РасчетыПоСрокамДо.Валюта = РасчетыПоСрокамОстатки.Валюта)
			|			И (РасчетыПоСрокамДо.РасчетныйДокумент = РасчетыПоСрокамОстатки.РасчетныйДокумент)
			|			И (РасчетыПоСрокамДо.Период < &НачалоМесяца)
			|			И (РасчетыПоСрокамДо.Активность)
			|
			|СГРУППИРОВАТЬ ПО
			|	РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам,
			|	РасчетыПоСрокамОстатки.ОбъектРасчетов,
			|	РасчетыПоСрокамОстатки.Валюта,
			|	РасчетыПоСрокамОстатки.РасчетныйДокумент,
			|	РасчетыПоСрокамОстатки.ДатаПлановогоПогашения,
			|	РасчетыПоСрокамОстатки.ДатаВозникновения,
			|	РасчетыПоСрокамОстатки.Предоплата,
			|	РасчетыПоСрокамОстатки.ПредоплатаРегл,
			|	РасчетыПоСрокамОстатки.ПредоплатаУпр,
			|	РасчетыПоСрокамОстатки.Долг,
			|	РасчетыПоСрокамОстатки.ДолгРегл,
			|	РасчетыПоСрокамОстатки.ДолгУпр
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком),
			|	РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам,
			|	РасчетыПоСрокамОстатки.ОбъектРасчетов,
			|	РасчетыПоСрокамОстатки.Валюта,
			|	РасчетыПоСрокамОстатки.РасчетныйДокумент,
			|	РасчетыПоСрокамОстатки.ДатаПлановогоПогашения,
			|	РасчетыПоСрокамОстатки.ДатаВозникновения,
			|	РасчетыПоСрокамОстатки.ПредоплатаОстаток,
			|	РасчетыПоСрокамОстатки.ПредоплатаРеглОстаток,
			|	РасчетыПоСрокамОстатки.ПредоплатаУпрОстаток,
			|	РасчетыПоСрокамОстатки.ДолгОстаток,
			|	РасчетыПоСрокамОстатки.ДолгРеглОстаток,
			|	РасчетыПоСрокамОстатки.ДолгУпрОстаток,
			|	МИНИМУМ(РасчетыПоСрокамДо.ПорядокЗачета),
			|	МИНИМУМ(РасчетыПоСрокамДо.ПорядокОперации),
			|	МАКСИМУМ(РасчетыПоСрокамДо.СвязанныйДокумент),
			|	МИНИМУМ(РасчетыПоСрокамДо.ВалютаДокумента),
			|	МАКСИМУМ(РасчетыПоСрокамДо.СтатьяДвиженияДенежныхСредств),
			|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
			|ИЗ
			|	ВТРасчетыСПоставщикамиПоСрокамОстатки КАК РасчетыПоСрокамОстатки
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыПоСрокамДо
			|		ПО (РасчетыПоСрокамДо.АналитикаУчетаПоПартнерам = РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам)
			|			И (РасчетыПоСрокамДо.ОбъектРасчетов = РасчетыПоСрокамОстатки.ОбъектРасчетов)
			|			И (РасчетыПоСрокамДо.Валюта = РасчетыПоСрокамОстатки.Валюта)
			|			И (РасчетыПоСрокамДо.РасчетныйДокумент = РасчетыПоСрокамОстатки.РасчетныйДокумент)
			|			И (РасчетыПоСрокамДо.Период < &НачалоМесяца)
			|			И (РасчетыПоСрокамДо.Активность)
			|
			|СГРУППИРОВАТЬ ПО
			|	РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам,
			|	РасчетыПоСрокамОстатки.ОбъектРасчетов,
			|	РасчетыПоСрокамОстатки.Валюта,
			|	РасчетыПоСрокамОстатки.РасчетныйДокумент,
			|	РасчетыПоСрокамОстатки.ДатаПлановогоПогашения,
			|	РасчетыПоСрокамОстатки.ДатаВозникновения,
			|	РасчетыПоСрокамОстатки.ПредоплатаОстаток,
			|	РасчетыПоСрокамОстатки.ПредоплатаРеглОстаток,
			|	РасчетыПоСрокамОстатки.ПредоплатаУпрОстаток,
			|	РасчетыПоСрокамОстатки.ДолгОстаток,
			|	РасчетыПоСрокамОстатки.ДолгРеглОстаток,
			|	РасчетыПоСрокамОстатки.ДолгУпрОстаток
			|
			|УПОРЯДОЧИТЬ ПО
			|	МИНИМУМ(РасчетыПоСрокамДо.ПорядокОперации)";
		
		ДетальныеЗаписи = Запрос.Выполнить().Выгрузить();
		ДетальныеЗаписи.Индексы.Добавить("ТипРасчетов, АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта");
		СтруктураПоискаДетальныхЗаписей = Новый Структура("ТипРасчетов, АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта");
		
		МассивОрганизацийДляОбработки = РазвернутоеСальдо.Скопировать(,"Организация");
		МассивОрганизацийДляОбработки.Свернуть("Организация");
		МассивОрганизацийДляОбработки = МассивОрганизацийДляОбработки.ВыгрузитьКолонку("Организация");
		
		Для Каждого Организация Из МассивОрганизацийДляОбработки Цикл
			
			КорректировкаОбъект = КорректировкаРегистров(ДатаКорректировки, Организация, Перечисления.ОперацииКорректировкиРегистров.ИсправлениеРазвернутогоСальдоВзаиморасчетов);
			
			Для Каждого ТипРасчетов Из ТипыРасчетов Цикл
				
				ТипРасчетовСтрокой = ?(ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом,"РасчетыСКлиентами","РасчетыСПоставщиками");
				
				Движения = КорректировкаОбъект.Движения[ТипРасчетовСтрокой + "ПоСрокам"]; // РегистрНакопленияНаборЗаписей
				
				СтруктураПоиска.ТипРасчетов = ТипРасчетов;
				СтруктураПоиска.Организация = Организация;
				ЗаписиРазвернутогоСальдо = РазвернутоеСальдо.НайтиСтроки(СтруктураПоиска);
				
				Для Каждого ЗаписьРазвернутогоСальдо Из ЗаписиРазвернутогоСальдо Цикл
					ЗаполнитьЗначенияСвойств(СтруктураПоискаДетальныхЗаписей, ЗаписьРазвернутогоСальдо);
					СтруктураПоискаДетальныхЗаписей.Валюта = ЗаписьРазвернутогоСальдо.ВалютаРасчетов;
					ДетальныеЗаписиАналитики = ДетальныеЗаписи.НайтиСтроки(СтруктураПоискаДетальныхЗаписей);
					
					СуммаКРаспределениюДолг = Мин(ЗаписьРазвернутогоСальдо.ДолгОстаток, ЗаписьРазвернутогоСальдо.ПредоплатаОстаток);
					СуммаКРаспределениюПредоплата = СуммаКРаспределениюДолг;
					
					Для Каждого СтрокаДетальныхЗаписей Из ДетальныеЗаписиАналитики Цикл
						
						Если СуммаКРаспределениюДолг = 0 И СуммаКРаспределениюПредоплата = 0 Тогда
							Прервать;
						КонецЕсли;
						
						НовСтр = Движения.Добавить();
						ЗаполнитьЗначенияСвойств(НовСтр, СтрокаДетальныхЗаписей);
						
						НовСтр.Период              = ДатаКорректировки;
						НовСтр.ВидДвижения         = ВидДвиженияНакопления.Расход;
						НовСтр.ДокументРегистратор = КорректировкаОбъект.Ссылка;
						НовСтр.ПорядокОперации     = ПорядокКорректировки;
						
						Если СтрокаДетальныхЗаписей.Долг > 0 Тогда
							СуммаСписания = Мин(СуммаКРаспределениюДолг, СтрокаДетальныхЗаписей.Долг);
							
							НовСтр.ДолгРегл = СтрокаДетальныхЗаписей.ДолгРегл / СтрокаДетальныхЗаписей.Долг * СуммаСписания;
							НовСтр.ДолгУпр  = СтрокаДетальныхЗаписей.ДолгУпр / СтрокаДетальныхЗаписей.Долг * СуммаСписания;
							НовСтр.Долг     = СуммаСписания;
							НовСтр.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеДолга;
							
							СуммаКРаспределениюДолг = СуммаКРаспределениюДолг - СуммаСписания;
						КонецЕсли;
						
						Если СтрокаДетальныхЗаписей.Предоплата > 0 Тогда
							СуммаСписания = Мин(СуммаКРаспределениюПредоплата, СтрокаДетальныхЗаписей.Предоплата);
							
							НовСтр.ПредоплатаРегл = СтрокаДетальныхЗаписей.ПредоплатаРегл / СтрокаДетальныхЗаписей.Предоплата * СуммаСписания;
							НовСтр.ПредоплатаУпр  = СтрокаДетальныхЗаписей.ПредоплатаУпр / СтрокаДетальныхЗаписей.Предоплата * СуммаСписания;
							НовСтр.Предоплата     = СуммаСписания;
							НовСтр.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеПредоплаты;
							
							СуммаКРаспределениюПредоплата = СуммаКРаспределениюПредоплата - СуммаСписания;
						КонецЕсли;
						
					КонецЦикла;
				КонецЦикла;
				
				Движения.Записать();
				
				Для Каждого СтрокаАналитики Из ЗаписиРазвернутогоСальдо Цикл
					
					ОсновныеПараметры = СтруктураПараметровЗаполненияВзаиморасчетов();
					
					ЗаполнитьЗначенияСвойств(ОсновныеПараметры, СтрокаАналитики);
					ОсновныеПараметры.ЭтоРасчетыСКлиентами = ТипРасчетовСтрокой = "РасчетыСКлиентами";
					ОсновныеПараметры.ДатаНачалаПересчета = ПериодРасчета;
					
					ЗаполнитьОперативныеВзаиморасчеты(ОсновныеПараметры);
					
				КонецЦикла;
				
			КонецЦикла;
			
			Если КорректировкаОбъект.Движения.РасчетыСКлиентамиПоСрокам.Количество() = 0 
				И КорректировкаОбъект.Движения.РасчетыСПоставщикамиПоСрокам.Количество() = 0 Тогда
				КорректировкаОбъект.Удалить();
			Иначе
				ДополнитьКорректировкуДвижениямиПоПрочимДоходамРасходам(КорректировкаОбъект);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Запрос.Текст = "УНИЧТОЖИТЬ ВтАналитика; УНИЧТОЖИТЬ ВтСальдо; УНИЧТОЖИТЬ ВтРучныеКорректировки";
	Запрос.Выполнить();
	
	// Если на начало периода все хорошо, а на конец есть развернутое сальдо - пересчитаем их.
	Запрос.Текст = ТекстЗапросаРазвернутоеСальдо();
	Запрос.УстановитьПараметр("НачалоМесяца",       КонецМесяца(ПериодРасчета));
	Запрос.УстановитьПараметр("Порядок",            Формат(КонецМесяца(ПериодРасчета),"ДФ=ггггММдд9ЧЧммсс99"));
	НекорректныеОстатки = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Стр Из НекорректныеОстатки Цикл
		
		ОсновныеПараметры = СтруктураПараметровЗаполненияВзаиморасчетов();
		
		ЗаполнитьЗначенияСвойств(ОсновныеПараметры, Стр);
		ОсновныеПараметры.ЭтоРасчетыСКлиентами = Стр.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
		ОсновныеПараметры.ДатаНачалаПересчета = ПериодРасчета;
		
		ЗаполнитьОперативныеВзаиморасчеты(ОсновныеПараметры);
		
	КонецЦикла;
	
КонецПроцедуры

// Добавляет в движения данные по прочим доходам/расходам 
// 
// Параметры:
// 	КорректировкаОбъект - ДокументОбъект.КорректировкаРегистров - ДокументОбъект, с уже записанными движениями по расчетам.
Процедура ДополнитьКорректировкуДвижениямиПоПрочимДоходамРасходам(КорректировкаОбъект) Экспорт
	
	Если КорректировкаОбъект.ТаблицаРегистров.Найти("ПрочиеДоходы","Имя") = Неопределено Тогда
		НовСтр = КорректировкаОбъект.ТаблицаРегистров.Добавить();
		НовСтр.Имя = "ПрочиеДоходы";
	КонецЕсли;
	
	Если КорректировкаОбъект.ТаблицаРегистров.Найти("ПрочиеРасходы","Имя") = Неопределено Тогда
		НовСтр = КорректировкаОбъект.ТаблицаРегистров.Добавить();
		НовСтр.Имя = "ПрочиеРасходы";
	КонецЕсли;
	
	Если КорректировкаОбъект.ТаблицаРегистров.Найти("ПрочиеАктивыПассивы","Имя") = Неопределено Тогда
		НовСтр = КорректировкаОбъект.ТаблицаРегистров.Добавить();
		НовСтр.Имя = "ПрочиеАктивыПассивы";
	КонецЕсли;
	
	НачалоТекущегоПериода = КонецМесяца(КорректировкаОбъект.Дата)+1;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КорректировкаСсылка", КорректировкаОбъект.Ссылка);
	Запрос.УстановитьПараметр("Период", НачалоТекущегоПериода);
	Запрос.УстановитьПараметр("СтатьяДоходов", ПланыВидовХарактеристик.СтатьиДоходов.ПрибыльУбытокПрошлыхЛет);
	Запрос.УстановитьПараметр("СтатьяРасходов", ПланыВидовХарактеристик.СтатьиРасходов.ПрибыльУбытокПрошлыхЛет);
	Запрос.УстановитьПараметр("УправленческийУчетОрганизаций", РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций(НачалоТекущегоПериода));
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	ВложенныйЗапрос.Организация             КАК Организация,
	|	ВложенныйЗапрос.Подразделение           КАК Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяДоходов                          КАК СтатьяДоходов,
	|	ВложенныйЗапрос.Организация             КАК АналитикаДоходов,
	|	СУММА(ВложенныйЗапрос.Сумма)            КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаРегл)        КАК СуммаРегл,
	|	СУММА(ВЫБОР КОГДА &УправленческийУчетОрганизаций 
	|				ТОГДА ВложенныйЗапрос.СуммаУпр
	|				ИНАЧЕ 0
	|		КОНЕЦ)                              КАК СуммаУпр
	|ИЗ (ВЫБРАТЬ
	|		Расчеты.АналитикаУчетаПоПартнерам.Организация             КАК Организация,
	|		ЕСТЬNULL(Расчеты.ОбъектРасчетов.Подразделение,
	|			ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|		Расчеты.ОбъектРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		
	|		ВЫБОР КОГДА Расчеты.Долг < 0
	|			ТОГДА -Расчеты.Долг
	|			ИНАЧЕ Расчеты.Долг
	|		КОНЕЦ +
	|		ВЫБОР КОГДА Расчеты.Предоплата < 0
	|			ТОГДА -Расчеты.Предоплата
	|			ИНАЧЕ Расчеты.Предоплата
	|		КОНЕЦ                                                     КАК Сумма,
	|		ВЫБОР КОГДА Расчеты.ДолгРегл < 0
	|			ТОГДА -Расчеты.ДолгРегл
	|			ИНАЧЕ Расчеты.ДолгРегл
	|		КОНЕЦ +
	|		ВЫБОР КОГДА Расчеты.ПредоплатаРегл < 0
	|			ТОГДА -Расчеты.ПредоплатаРегл
	|			ИНАЧЕ Расчеты.ПредоплатаРегл
	|		КОНЕЦ                                                     КАК СуммаРегл,
	|		ВЫБОР КОГДА Расчеты.ДолгУпр < 0
	|			ТОГДА -Расчеты.ДолгУпр
	|			ИНАЧЕ Расчеты.ДолгУпр
	|		КОНЕЦ +
	|		ВЫБОР КОГДА Расчеты.ПредоплатаУпр < 0
	|			ТОГДА -Расчеты.ПредоплатаУпр
	|			ИНАЧЕ Расчеты.ПредоплатаУпр
	|		КОНЕЦ                                                     КАК СуммаУпр
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|	ГДЕ
	|		Расчеты.Активность
	|		И Расчеты.Регистратор = &КорректировкаСсылка
	|		И ((Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И (Расчеты.Предоплата > 0 ИЛИ Расчеты.Долг < 0))
	|			ИЛИ (Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И (Расчеты.Долг > 0 ИЛИ Расчеты.Предоплата < 0)))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Расчеты.АналитикаУчетаПоПартнерам.Организация             КАК Организация,
	|		ЕСТЬNULL(Расчеты.ОбъектРасчетов.Подразделение,
	|			ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|		Расчеты.ОбъектРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		
	|		ВЫБОР КОГДА Расчеты.Долг < 0
	|				ТОГДА -Расчеты.Долг
	|			ИНАЧЕ Расчеты.Долг
	|		КОНЕЦ +
	|		ВЫБОР КОГДА Расчеты.Предоплата < 0
	|			ТОГДА -Расчеты.Предоплата
	|			ИНАЧЕ Расчеты.Предоплата
	|		КОНЕЦ                                                     КАК Сумма,
	|		ВЫБОР КОГДА Расчеты.ДолгРегл < 0
	|			ТОГДА -Расчеты.ДолгРегл
	|			ИНАЧЕ Расчеты.ДолгРегл
	|		КОНЕЦ +
	|		ВЫБОР КОГДА Расчеты.ПредоплатаРегл < 0
	|			ТОГДА -Расчеты.ПредоплатаРегл
	|			ИНАЧЕ Расчеты.ПредоплатаРегл
	|		КОНЕЦ                                                     КАК СуммаРегл,
	|		ВЫБОР КОГДА Расчеты.ДолгУпр < 0
	|			ТОГДА -Расчеты.ДолгУпр
	|			ИНАЧЕ Расчеты.ДолгУпр
	|		КОНЕЦ +
	|		ВЫБОР КОГДА Расчеты.ПредоплатаУпр < 0
	|			ТОГДА -Расчеты.ПредоплатаУпр
	|			ИНАЧЕ Расчеты.ПредоплатаУпр
	|		КОНЕЦ                                                     КАК СуммаУпр
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
	|	ГДЕ	
	|		Расчеты.Активность
	|		И Расчеты.Регистратор = &КорректировкаСсылка
	|		И ((Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И (Расчеты.Предоплата > 0 ИЛИ Расчеты.Долг < 0))
	|			ИЛИ (Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И (Расчеты.Долг > 0 ИЛИ Расчеты.Предоплата < 0)))) КАК ВложенныйЗапрос
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности
	|;
	|ВЫБРАТЬ
	|	&Период                                 КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	ВложенныйЗапрос.Организация             КАК Организация,
	|	ВложенныйЗапрос.Подразделение           КАК Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяРасходов                         КАК СтатьяРасходов,
	|	ВложенныйЗапрос.Организация             КАК АналитикаРасходов,
	|	СУММА(ВложенныйЗапрос.Сумма)            КАК Сумма,
	|	СУММА(ВложенныйЗапрос.Сумма)            КАК СуммаБезНДС,
	|	СУММА(ВложенныйЗапрос.СуммаРегл)        КАК СуммаРегл,
	|	СУММА(ВЫБОР КОГДА &УправленческийУчетОрганизаций
	|				ТОГДА ВложенныйЗапрос.СуммаУпр
	|				ИНАЧЕ 0
	|		КОНЕЦ)                              КАК СуммаУпр,
	|	0                                       КАК ПостояннаяРазница,
	|	0                                       КАК ВременнаяРазница
	|ИЗ (ВЫБРАТЬ
	|		Расчеты.АналитикаУчетаПоПартнерам.Организация             КАК Организация,
	|		ЕСТЬNULL(Расчеты.ОбъектРасчетов.Подразделение,
	|			ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|		Расчеты.ОбъектРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		
	|		ВЫБОР КОГДА Расчеты.Долг < 0
	|			ТОГДА -Расчеты.Долг
	|			ИНАЧЕ Расчеты.Долг
	|		КОНЕЦ +
	|		ВЫБОР КОГДА Расчеты.Предоплата < 0
	|			ТОГДА -Расчеты.Предоплата
	|			ИНАЧЕ Расчеты.Предоплата
	|		КОНЕЦ                                                     КАК Сумма,
	|		ВЫБОР КОГДА Расчеты.ДолгРегл < 0
	|			ТОГДА -Расчеты.ДолгРегл
	|			ИНАЧЕ Расчеты.ДолгРегл
	|		КОНЕЦ +
	|		ВЫБОР КОГДА Расчеты.ПредоплатаРегл < 0
	|			ТОГДА -Расчеты.ПредоплатаРегл
	|			ИНАЧЕ Расчеты.ПредоплатаРегл
	|		КОНЕЦ                                                     КАК СуммаРегл,
	|		ВЫБОР КОГДА Расчеты.ДолгУпр < 0
	|			ТОГДА -Расчеты.ДолгУпр
	|			ИНАЧЕ Расчеты.ДолгУпр
	|		КОНЕЦ +
	|		ВЫБОР КОГДА Расчеты.ПредоплатаУпр < 0
	|			ТОГДА -Расчеты.ПредоплатаУпр
	|			ИНАЧЕ Расчеты.ПредоплатаУпр
	|		КОНЕЦ                                                     КАК СуммаУпр
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|	ГДЕ
	|		Расчеты.Активность
	|		И Расчеты.Регистратор = &КорректировкаСсылка
	|		И ((Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И (Расчеты.Предоплата > 0 ИЛИ Расчеты.Долг < 0))
	|			ИЛИ (Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И (Расчеты.Долг > 0 ИЛИ Расчеты.Предоплата < 0)))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Расчеты.АналитикаУчетаПоПартнерам.Организация             КАК Организация,
	|		ЕСТЬNULL(Расчеты.ОбъектРасчетов.Подразделение,
	|			ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|		Расчеты.ОбъектРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		
	|		ВЫБОР КОГДА Расчеты.Долг < 0
	|				ТОГДА -Расчеты.Долг
	|			ИНАЧЕ Расчеты.Долг
	|		КОНЕЦ +
	|		ВЫБОР КОГДА Расчеты.Предоплата < 0
	|			ТОГДА -Расчеты.Предоплата
	|			ИНАЧЕ Расчеты.Предоплата
	|		КОНЕЦ                                                     КАК Сумма,
	|		ВЫБОР КОГДА Расчеты.ДолгРегл < 0
	|			ТОГДА -Расчеты.ДолгРегл
	|			ИНАЧЕ Расчеты.ДолгРегл
	|		КОНЕЦ +
	|		ВЫБОР КОГДА Расчеты.ПредоплатаРегл < 0
	|			ТОГДА -Расчеты.ПредоплатаРегл
	|			ИНАЧЕ Расчеты.ПредоплатаРегл
	|		КОНЕЦ                                                     КАК СуммаРегл,
	|		ВЫБОР КОГДА Расчеты.ДолгУпр < 0
	|			ТОГДА -Расчеты.ДолгУпр
	|			ИНАЧЕ Расчеты.ДолгУпр
	|		КОНЕЦ +
	|		ВЫБОР КОГДА Расчеты.ПредоплатаУпр < 0
	|			ТОГДА -Расчеты.ПредоплатаУпр
	|			ИНАЧЕ Расчеты.ПредоплатаУпр
	|		КОНЕЦ                                                     КАК СуммаУпр
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
	|	ГДЕ	
	|		Расчеты.Активность
	|		И Расчеты.Регистратор = &КорректировкаСсылка
	|		И ((Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И (Расчеты.Предоплата > 0 ИЛИ Расчеты.Долг < 0))
	|			ИЛИ (Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И (Расчеты.Долг > 0 ИЛИ Расчеты.Предоплата < 0)))) КАК ВложенныйЗапрос
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.НаправлениеДеятельности
	|";
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Доходы = Результаты[0].Выгрузить();
	Расходы = Результаты[1].Выгрузить();
	
	ДвиженияДоходы = КорректировкаОбъект.Движения.ПрочиеДоходы;
	ДвиженияДоходы.Загрузить(Доходы);
	ДвиженияДоходы.Записать();
	
	ДвиженияРасходы = КорректировкаОбъект.Движения.ПрочиеРасходы;
	ДвиженияРасходы.Загрузить(Расходы);
	ДвиженияРасходы.Записать();
	
	КорректировкаОбъект.Записать();
	
	УправленческийУчетПроведениеСервер.ОбновитьДвиженияАктивовПассивов(КорректировкаОбъект.Ссылка, КорректировкаОбъект.Движения);
	
	АктивыПассивы = КорректировкаОбъект.Движения.ПрочиеАктивыПассивы.Выгрузить();
	АктивыПассивы.ЗаполнитьЗначения(НачалоТекущегоПериода, "Период");
	КорректировкаОбъект.Движения.ПрочиеАктивыПассивы.Загрузить(АктивыПассивы);
	КорректировкаОбъект.Движения.ПрочиеАктивыПассивы.Записать();
	
КонецПроцедуры

#КонецОбласти

// Возвращает структуру параметров необходимых для регистрации документа к отложенному распределению расчетов.
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * Регистратор - ДокументСсылка - Документ по которому необходимо зарегистрировать отложенные расчеты.
// * Загрузка - Булево - Значение флага ОбменДанными.Загрузка
// * РаспределениеВзаиморасчетовФоновымЗаданием - Булево - Распределение взаиморасчетов будет выполняться фоновым заданием, а не в транзакции документа.
// * РаспределениеСУчетомПриемников - Булево - При запуске распределения будут учтены связи объектов расчетов
// * ДополнитьТаблицуИзмененийПриемниками - Булево - Признак того, что для записей таблицы изменений необходимо получить приемники
// * ПерезаполнениеРегистровВзаиморасчетов - Булево - Истина, если перезаполнение регистров взаиморасчетов вызывается из служебных обработок (например, "Заполнение регистров взаиморасчетов").
// * ТаблицаИзменений - ТаблицаЗначений - Объекты расчетов по которым есть изменения:
//                                        ** ПорядокОперации - Строка.
//                                        ** АналитикаУчетаПоПартнерам - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам.
//                                        ** ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов.
//                                        ** ПоДаннымОбъектаРасчетовИсточника - Булево - Истина, если движение заполняется из приемника.
//                                        ** ВалютаРасчетов - СправочникСсылка.Валюты.
Функция ПараметрыРаспределенияРасчетов() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Регистратор", Неопределено);
	Результат.Вставить("Загрузка", Ложь);
	Результат.Вставить("РаспределениеВзаиморасчетовФоновымЗаданием", Ложь);
	Результат.Вставить("НачальноеЗаполнение", Ложь);
	Результат.Вставить("ТаблицаИзменений", Неопределено);
	Результат.Вставить("РаспределениеСУчетомПриемников", Ложь);
	Результат.Вставить("ДополнитьТаблицуИзмененийПриемниками", Истина);
	Результат.Вставить("ПерезаполнениеРегистровВзаиморасчетов", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Регистрирует объекты расчетов для отложенного расчета по срокам и планом
// 
// Параметры:
// 	Параметры - см. ПараметрыРаспределенияРасчетов
Процедура ЗарегистрироватьИзмененияКОтложенномуРаспределению(Параметры) Экспорт
	
	Для Каждого СтрокаИзменений Из Параметры.ТаблицаИзменений Цикл
		
		НаборЗаписей = РегистрыСведений.ЗаданияКРаспределениюРасчетов.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.ТипРасчетов.Установить(СтрокаИзменений.ТипРасчетов);
		НаборЗаписей.Отбор.АналитикаУчетаПоПартнерам.Установить(СтрокаИзменений.АналитикаУчетаПоПартнерам);
		НаборЗаписей.Отбор.ОбъектРасчетов.Установить(СтрокаИзменений.ОбъектРасчетов);
		НаборЗаписей.Отбор.Валюта.Установить(СтрокаИзменений.ВалютаРасчетов);
		
		Если СтрокаИзменений.ПорядокОперации = "" Тогда
			ДатаИзменений = Дата(1,1,1);
		Иначе
			ДатаИзменений = Дата(Лев(СтрокаИзменений.ПорядокОперации, 8));
		КонецЕсли;
		
		НаборЗаписей.Отбор.ДатаПересчета.Установить(ДатаИзменений);
		НаборЗаписей.Отбор.Документ.Установить(Параметры.Регистратор);
		
		НовСтр = НаборЗаписей.Добавить();
		НовСтр.ТипРасчетов = СтрокаИзменений.ТипРасчетов;
		НовСтр.АналитикаУчетаПоПартнерам = СтрокаИзменений.АналитикаУчетаПоПартнерам;
		НовСтр.ОбъектРасчетов = СтрокаИзменений.ОбъектРасчетов;
		НовСтр.Валюта = СтрокаИзменений.ВалютаРасчетов;
		НовСтр.ДатаПересчета = ДатаИзменений;
		НовСтр.Документ = Параметры.Регистратор;
		НовСтр.Приоритет = ?(СтрокаИзменений.ПоДаннымОбъектаРасчетовИсточника, 2, 1);
		НовСтр.КоличествоДокументов = 1;
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет распределение расчетов по текущим заданиям к распределению в регистрах ЗаданияКРаспределениюРасчетов и ЗаданияКРаспределениюВзаиморасчетов
// 
// Параметры:
// 	Параметры - Структура, Неопределено - В структуре передается таблица заданий для распределения:
// 	 *ДанныеКОтработке - см. ДанныеДляОбработки.
// 	АдресРезультата - Строка - адрес временного хранилища, в которое будет помещен результат работы процедуры.
Процедура ВыполнитьОтложенноеРаспределение(Параметры = Неопределено, АдресРезультата = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);

	Если НЕ Константы.НоваяАрхитектураВзаиморасчетов.Получить() Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры <> Неопределено И Параметры.Свойство("ДанныеКОбработке") Тогда
		ДанныеКОбработке = Параметры.ДанныеКОбработке;
	Иначе
		ПараметрыДанных = ПараметрыДанныхМногопоточнойОбработки();
		ДанныеКОбработке = ДанныеДляОбработки(ПараметрыДанных).Данные;
	КонецЕсли;
	
	ТаблицаОбъектовОплаты = Новый ТаблицаЗначений;
	ТаблицаОбъектовОплаты.Колонки.Добавить("ОбъектОплаты", Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.ОбъектРасчетов.Тип));
	ТаблицаОбъектовОплаты.Колонки.Добавить("ОбъектРасчетов", Новый ОписаниеТипов("СправочникСсылка.ОбъектыРасчетов"));
	ТаблицаОбъектовОплаты.Колонки.Добавить("ЭтоРасчетыСКлиентами", Новый ОписаниеТипов("Булево"));
	
	НеобходимоДополнениеТаблицыИзменений = Истина;
	Если Параметры <> Неопределено Тогда
		НеобходимоДополнениеТаблицыИзменений = Параметры.ДополнитьТаблицуИзмененийПриемниками;
	КонецЕсли;
	
	Если НеобходимоДополнениеТаблицыИзменений Тогда
		КвалификаторСтроки = Новый КвалификаторыСтроки(40);
		ДанныеКОбработке.Колонки.Добавить("ПорядокОперации", Новый ОписаниеТипов("Строка", , , , КвалификаторСтроки));
		ДанныеКОбработке.Колонки.Добавить("НачальноеЗаполнение", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ДанныеКОбработке Цикл

		Если СтрокаТаблицы.ПорядокОперации = "" Тогда
			СтрокаТаблицы.ПорядокОперации = Порядок(СтрокаТаблицы.ДатаНачалаПересчета, "", Тип("ДокументСсылка.КорректировкаРегистров"), "1", "21");
		КонецЕсли;
		
		Если НеобходимоДополнениеТаблицыИзменений Тогда
			СтрокаТаблицы.ПоДаннымОбъектаРасчетовИсточника = СтрокаТаблицы.Приоритет = 2;
		КонецЕсли;

	КонецЦикла;
	
	ПараметрыРаспределения = ПараметрыРаспределенияРасчетов();
	ПараметрыРаспределения.РаспределениеСУчетомПриемников = Истина;
	ПараметрыРаспределения.ТаблицаИзменений = ДанныеКОбработке;
	ПараметрыРаспределения.ДополнитьТаблицуИзмененийПриемниками = НеобходимоДополнениеТаблицыИзменений;
	
	НачатьТранзакцию();
	
	Попытка
		
		ЗаполнитьОперативныеВзаиморасчетыПоТаблице(ПараметрыРаспределения);
		
		Для Каждого Запись Из ДанныеКОбработке Цикл
			
			ОтметитьВыполнениеЗадания(Запись,"Документ");
			
			Если ЗначениеЗаполнено(Запись.Объект) Тогда
				НовСтр = ТаблицаОбъектовОплаты.Добавить();
				НовСтр.ЭтоРасчетыСКлиентами = Запись.ЭтоРасчетыСКлиентами;
				НовСтр.ОбъектРасчетов = Запись.ОбъектРасчетов;
				НовСтр.ОбъектОплаты = Запись.Объект;
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
		
	КонецПопытки;
	
	// Для определения состояний заказов используются данные распределенных расчетов
	ОтразитьСостоянияЗаказов(ДанныеКОбработке);
	
	РегистрыСведений.ГрафикПлатежей.РассчитатьГрафикПлатежейПоРасчетамСКлиентами(
				ТаблицаОбъектовОплаты.Скопировать(Новый Структура("ЭтоРасчетыСКлиентами", Истина)));
	РегистрыСведений.ГрафикПлатежей.РассчитатьГрафикПлатежейПоРасчетамСПоставщиками(
				ТаблицаОбъектовОплаты.Скопировать(Новый Структура("ЭтоРасчетыСКлиентами", Ложь)));
КонецПроцедуры

// Запускает распределение плановых расчетов по текущим заданиям к распределению в регистре ЗаданияКРаспределениюРасчетов
// в фоновом задании.
// 
// Параметры:
// 	РаспределитьФактическиеРасчеты - Булево - Истина, запуск распределения фактических расчетов
//
Процедура ЗапуститьОтложенноеРаспределениеВзаиморасчетов(РаспределитьФактическиеРасчеты = Ложь) Экспорт
	
	Если (Константы.РаспределятьВзаиморасчетыФоновымЗаданием.Получить() ИЛИ РаспределитьФактическиеРасчеты)
		И НЕ ОбщегоНазначения.ИнформационнаяБазаФайловая()
		И НЕ МенеджерОтложенногоРаспределенияВзаиморасчетовАктивен() Тогда
		
		ПараметрыЭкспортнойПроцедуры = Новый Структура("РаспределитьФактическиеРасчеты", РаспределитьФактическиеРасчеты);
		
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Неопределено);
		ПараметрыВыполнения.ОжидатьЗавершение = 0;
		Если РаспределитьФактическиеРасчеты Тогда
			ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Выполняется фоновое распределение фактических расчетов';
																	|en = 'Background allocation of actual AR/AP is in progress'");
		Иначе
			ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Выполняется фоновое распределение планов по расчетам';
																	|en = 'Background distribution of settlement plans is in progress'");
		КонецЕсли;
		ПараметрыВыполнения.КлючФоновогоЗадания = ИмяМетодаОтложенногоРаспределенияВзаиморасчетов();
		
		ДлительныеОперации.ВыполнитьВФоне(ИмяМетодаОтложенногоРаспределенияВзаиморасчетов(), ПараметрыЭкспортнойПроцедуры, ПараметрыВыполнения);
	КонецЕсли;
	
КонецПроцедуры

// Устарела.
// Запускает распределение фактических расчетов по текущим заданиям к распределению в регистре ЗаданияКРаспределениюРасчетов
// в фоновом задании.
//
// Метод предназначен для вызова из регламентного задания.
//
Процедура ЗапуститьОтложенноеРаспределениеФактическихВзаиморасчетов() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.УдалитьРаспределениеФактическихВзаиморасчетов);
	
КонецПроцедуры

// Запускает отложенное распределение взаиморасчетов по срокам
Процедура ПослеОбновленияРегистровВзаиморасчетов() Экспорт
	
	Если ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Неопределено, Метаданные.РегистрыНакопления.РасчетыСКлиентами)
			И ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Неопределено, Метаданные.РегистрыНакопления.РасчетыСКлиентамиПланОплат)
			И ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Неопределено, Метаданные.РегистрыНакопления.РасчетыСКлиентамиПланОтгрузок)
			И ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Неопределено, Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам)
			И ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Неопределено, Метаданные.РегистрыНакопления.РасчетыСПоставщиками)
			И ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Неопределено, Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПланОплат)
			И ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Неопределено, Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПланПоставок)
			И ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Неопределено, Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам)
			И ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Неопределено, Метаданные.РегистрыСведений.ВспомогательнаяИнформацияВзаиморасчетов)
			И НЕ ОбщегоНазначения.ИнформационнаяБазаФайловая()
		Тогда
		ЗапуститьОтложенноеРаспределениеВзаиморасчетов(Истина);
	КонецЕсли;
	
КонецПроцедуры

// Проверяет активность фонового задания по распределению расчетов.
//
// Возвращаемое значение:
//   Булево - Истина, если есть активные задания.
//
Функция МенеджерОтложенногоРаспределенияВзаиморасчетовАктивен() Экспорт
	
	ИмяМетодаЗадания = ИмяМетодаОтложенногоРаспределенияВзаиморасчетов();
	
	ОтборЗаданий = Новый Структура;
	ОтборЗаданий.Вставить("Ключ", ИмяМетодаЗадания);
	ОтборЗаданий.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	АктивныеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборЗаданий);
	
	Возврат АктивныеЗадания.Количество() > 0;
	
КонецФункции  

// Пустые ссылки на заказы.
// 
// Возвращаемое значение:
//  Массив из Тип - Пустые ссылки на заказы
Функция ПустыеСсылкиНаЗаказы() Экспорт
	
	ПустыеСсылкиНаЗаказы = Новый Массив;
	ПустыеСсылкиНаЗаказы.Добавить(Неопределено);
	ПустыеСсылкиНаЗаказы.Добавить(Документы.ЗаказКлиента.ПустаяСсылка());
	ПустыеСсылкиНаЗаказы.Добавить(Документы.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка());
	ПустыеСсылкиНаЗаказы.Добавить(Документы.ЗаказПоставщику.ПустаяСсылка());
	
	//++ НЕ УТ
	
	//++ Устарело_Переработка24
	ПустыеСсылкиНаЗаказы.Добавить(Документы.ЗаказПереработчику.ПустаяСсылка());
	//-- Устарело_Переработка24
	ПустыеСсылкиНаЗаказы.Добавить(Документы.ЗаказПереработчику2_5.ПустаяСсылка());
	//-- НЕ УТ
	
	//++ НЕ УТКА
	
	//++ Устарело_Переработка24
	ПустыеСсылкиНаЗаказы.Добавить(Документы.ЗаказДавальца.ПустаяСсылка());
	//-- Устарело_Переработка24
	ПустыеСсылкиНаЗаказы.Добавить(Документы.ЗаказДавальца2_5.ПустаяСсылка());
	//-- НЕ УТКА
	
	Возврат ПустыеСсылкиНаЗаказы;
КонецФункции

Функция СписокТиповУточняющихПланы()
	СписокТиповРегистраторовПланов = Новый СписокЗначений;
	//++ НЕ УТКА

	//++ Устарело_Переработка24
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ОтчетДавальцу"));
	//-- Устарело_Переработка24
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ОтчетДавальцу2_5"));
	//-- НЕ УТКА

	//++ НЕ УТ
	
	//++ Устарело_Переработка24
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ОтчетПереработчика"));
	//-- Устарело_Переработка24
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ОтчетПереработчика2_5"));
	//++ Локализация
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ПоступлениеДенежныхДокументов"));
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ОтчетОператораСистемыПлатон"));
	//-- Локализация
	
	//-- НЕ УТ
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.АктВыполненныхРабот"));
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ОтчетКомитентуОЗакупках"));
	
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ПриобретениеТоваровУслуг"));
	
	//Уточняют график договора
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ВыкупВозвратнойТарыКлиентом"));
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ВыкупТоваровХранителем"));
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.РеализацияУслугПрочихАктивов"));
	
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.Бронирование"));
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ВыкупВозвратнойТарыУПоставщика"));
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ВыкупПринятыхНаХранениеТоваров"));
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов"));
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ОтчетОСписанииТоваровСХранения"));
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт"));
	
	Возврат СписокТиповРегистраторовПланов;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПолучениеДанных

// Формирует временные таблицы для всех аналитик к расчету
Процедура СформироватьВТДанныхРегистров(Запрос, ГлобальныеПеременные)
	
	Тексты = Новый Массив;
	
	// Дополненные параметры, далее используются во всех соединениях для получения данных.
	Тексты.Добавить("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДополненныеПараметрыРаспределения.ОбъектРасчетов КАК ПараметрОбъектРасчетов,
	|	ДополненныеПараметрыРаспределения.АналитикаУчетаПоПартнерам КАК ПараметрАналитикаУчетаПоПартнерам,
	|	ДополненныеПараметрыРаспределения.ВалютаРасчетов КАК ПараметрВалютаРасчетов,
	|	ДополненныеПараметрыРаспределения.ПорядокОперации КАК ПараметрПорядок,
	|	ДополненныеПараметрыРаспределения.ЭтоРасчетыСКлиентами КАК ПараметрЭтоРасчетыСКлиентами,
	|	ДополненныеПараметрыРаспределения.НачальноеЗаполнение КАК ПараметрНачальноеЗаполнение,
	|	ДополненныеПараметрыРаспределения.ГрафикВДоговоре КАК ПараметрГрафикВДоговоре,
	|	ДополненныеПараметрыРаспределения.ГрафикИсполненияДоговора КАК ПараметрГрафикИсполненияДоговора,
	|	ДополненныеПараметрыРаспределения.ВалютаРегламентированногоУчета КАК ПараметрВалютаРегламентированногоУчета,
	|	ДополненныеПараметрыРаспределения.ДатаНачалаПересчета КАК ПараметрДатаНачалаПересчета,
	|	ДополненныеПараметрыРаспределения.ТипРасчетов КАК ПараметрТипРасчетов,
	|	ДополненныеПараметрыРаспределения.Регистратор КАК ПараметрРегистратор
	|ПОМЕСТИТЬ ВтДополненныеПараметрыРаспределения
	|ИЗ
	|	&ДополненныеПараметрыРаспределения КАК ДополненныеПараметрыРаспределения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПараметрАналитикаУчетаПоПартнерам,
	|	ПараметрОбъектРасчетов,
	|	ПараметрВалютаРасчетов");
	
	Если ГлобальныеПеременные.ЕстьЗаписиРасчетовСКлиентами Тогда
		Тексты.Добавить(ТекстЗапросаРасчетыПоСрокамОстатки(ГлобальныеПеременные, Истина,"ВТРасчетыПоСрокамОстаткиКлиенты"));
		Тексты.Добавить(ТекстЗапросаРасчетыДвиженияСМоментаПересчета(Истина));
		Тексты.Добавить(ТекстЗапросИсходныеРегистраторы(Истина));
		Тексты.Добавить(ТекстЗапросаРасчетыПланОплатОстатки(ГлобальныеПеременные,Истина, "ВТРасчетыПланОплатОстаткиКлиенты"));
		Тексты.Добавить(ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки(ГлобальныеПеременные, Истина, "ВТРасчетыПланОтгрузокПоставокОстаткиКлиенты"));
	КонецЕсли;
	МассивТекстов = Новый Массив;
	МассивТекстов.Добавить("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Период КАК Период,
	|	Расчеты.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРасчетыСКлиентамиПоСрокамРегистраторы
	|ИЗ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|ГДЕ
	|	Расчеты.АналитикаУчетаПоПартнерамПриемник В (&СписокАналитикУчетаПоПартнерам)
	|");

	Если ГлобальныеПеременные.ЕстьЗаписиРасчетовСПоставщиками Тогда
		Тексты.Добавить(ТекстЗапросаРасчетыПоСрокамОстатки(ГлобальныеПеременные,Ложь, "ВТРасчетыПоСрокамОстаткиПоставщики"));
		Тексты.Добавить(ТекстЗапросаРасчетыДвиженияСМоментаПересчета(Ложь));
		Тексты.Добавить(ТекстЗапросИсходныеРегистраторы(Ложь));
		Тексты.Добавить(ТекстЗапросаРасчетыПланОплатОстатки(ГлобальныеПеременные,Ложь, "ВТРасчетыПланОплатОстаткиПоставщики"));
		Тексты.Добавить(ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки(ГлобальныеПеременные, Ложь, "ВТРасчетыПланОтгрузокПоставокОстаткиПоставщики"));
	КонецЕсли;
	МассивТекстов.Добавить("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Период КАК Период,
	|	Расчеты.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРасчетыСПоставщикамиПоСрокамРегистраторы
	|ИЗ РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
	|ГДЕ
	|	Расчеты.АналитикаУчетаПоПартнерамПриемник В (&СписокАналитикУчетаПоПартнерам)
	|");
	
	#Область СлужебныеРегистраторыВзаиморасчетов
	МассивТекстов = Новый Массив;
	// Служебные регистраторы расчетов для ускорения запросов.
	МассивТекстов.Добавить("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Период КАК Период,
	|	Расчеты.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРасчетыСКлиентамиПоСрокамРегистраторы
	|ИЗ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметрыРасчета
	|			ПО Расчеты.АналитикаУчетаПоПартнерамПриемник = ВтПараметрыРасчета.ПараметрАналитикаУчетаПоПартнерам
	|");

	МассивТекстов.Добавить("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Период КАК Период,
	|	Расчеты.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТРасчетыСПоставщикамиПоСрокамРегистраторы
	|ИЗ РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметрыРасчета
	|			ПО Расчеты.АналитикаУчетаПоПартнерамПриемник = ВтПараметрыРасчета.ПараметрАналитикаУчетаПоПартнерам
	|");
	
	ТекстЗапросаВтТипыПриемников = "";
	
	// Определяет является ли движение-приемник отгрузкой или оплатой.
	Если ГлобальныеПеременные.ЕстьЗаписиРасчетовСКлиентами Тогда
		ТекстЗапросаВтТипыПриемников = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетыДвижения.Регистратор КАК Регистратор,
			|	РасчетыДвижения.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
			|	РасчетыДвижения.ОбъектРасчетов КАК ОбъектРасчетов,
			|	РасчетыДвижения.Валюта КАК Валюта,
			|	РасчетыДвижения.ЭтоОплата  КАК ПриемникЭтоОплата
			|ПОМЕСТИТЬ ВтТипыПриемников
			|ИЗ
			|	РасчетыДвиженияКлиенты КАК РасчетыДвижения
			|ГДЕ
			|	РасчетыДвижения.ПоДаннымОбъектаРасчетовИсточника
			|";
	КонецЕсли;
	
	Если ГлобальныеПеременные.ЕстьЗаписиРасчетовСПоставщиками Тогда
		ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетыДвижения.Регистратор КАК Регистратор,
			|	РасчетыДвижения.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
			|	РасчетыДвижения.ОбъектРасчетов КАК ОбъектРасчетов,
			|	РасчетыДвижения.Валюта КАК Валюта,
			|	РасчетыДвижения.ЭтоОплата КАК ПриемникЭтоОплата
			|ПОМЕСТИТЬ ВтТипыПриемников
			|ИЗ
			|	РасчетыДвиженияПоставщики КАК РасчетыДвижения
			|ГДЕ
			|	РасчетыДвижения.ПоДаннымОбъектаРасчетовИсточника
			|";
		Если ГлобальныеПеременные.ЕстьЗаписиРасчетовСКлиентами Тогда
			ТекстЗапросаВтТипыПриемников = ТекстЗапросаВтТипыПриемников + ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении();
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ВтТипыПриемников", "");
		КонецЕсли;
		ТекстЗапросаВтТипыПриемников = ТекстЗапросаВтТипыПриемников + ТекстЗапроса;
	КонецЕсли;
	МассивТекстов.Добавить(ТекстЗапросаВтТипыПриемников);
	
	Тексты.Добавить(СтрСоединить(МассивТекстов,";"));
	#КонецОбласти
	
	Запрос.Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ТекстЗапросаРасчетыПоСрокамОстатки(ГлобальныеПеременные, РасчетыСКлиентами = Истина, ИмяТаблицы = "")

	//Данные расчетов до начала пересчета.
	
	МассивТекстовПодзапроса = Новый Массив;
	
	ТекстПодзапросаОстатки = "
	|ВЫБРАТЬ
	|	РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыПоСрокамОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыПоСрокамОстатки.Валюта КАК Валюта,
	|	РасчетыПоСрокамОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыПоСрокамОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	РасчетыПоСрокамОстатки.ДатаВозникновения КАК ДатаВозникновения,
	|	РасчетыПоСрокамОстатки.ПредоплатаОстаток КАК ПредоплатаОстаток,
	|	РасчетыПоСрокамОстатки.ПредоплатаРеглОстаток КАК ПредоплатаРеглОстаток,
	|	РасчетыПоСрокамОстатки.ПредоплатаУпрОстаток КАК ПредоплатаУпрОстаток,
	|	РасчетыПоСрокамОстатки.ДолгОстаток КАК ДолгОстаток,
	|	РасчетыПоСрокамОстатки.ДолгРеглОстаток КАК ДолгРеглОстаток,
	|	РасчетыПоСрокамОстатки.ДолгУпрОстаток КАК ДолгУпрОстаток,
	|	0 КАК ПредоплатаОборот,
	|	0 КАК ПредоплатаРеглОборот,
	|	0 КАК ПредоплатаУпрОборот,
	|	0 КАК ДолгОборот,
	|	0 КАК ДолгРеглОборот,
	|	0 КАК ДолгУпрОборот
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки(&ДатаНачалаПересчетаМинимальный, (АналитикаУчетаПоПартнерам,
	|		ОбъектРасчетов, Валюта) В
	|		(ВЫБРАТЬ
	|			ВтПараметры.ПараметрАналитикаУчетаПоПартнерам,
	|			ВтПараметры.ПараметрОбъектРасчетов,
	|			ВтПараметры.ПараметрВалютаРасчетов
	|		ИЗ
	|			ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|		ГДЕ
	|			ВтПараметры.ПараметрЭтоРасчетыСКлиентами = &РасчетыСКлиентами)) КАК РасчетыПоСрокамОстатки";
	
	МассивТекстовПодзапроса.Добавить(ТекстПодзапросаОстатки);
	
	Если ГлобальныеПеременные.ДатаНачалаПересчетаЕстьРазличные Тогда
		
		ТекстПодзапросаДвижения = "
		|ВЫБРАТЬ
		|	РасчетыПоСрокам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	РасчетыПоСрокам.ОбъектРасчетов КАК ОбъектРасчетов,
		|	РасчетыПоСрокам.Валюта КАК Валюта,
		|	РасчетыПоСрокам.РасчетныйДокумент КАК РасчетныйДокумент,
		|	РасчетыПоСрокам.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
		|	РасчетыПоСрокам.ДатаВозникновения КАК ДатаВозникновения,
		|	СУММА(0) КАК ПредоплатаОстаток,
		|	СУММА(0) КАК ПредоплатаРеглОстаток,
		|	СУММА(0) КАК ПредоплатаУпрОстаток,
		|	СУММА(0) КАК ДолгОстаток,
		|	СУММА(0) КАК ДолгРеглОстаток,
		|	СУММА(0) КАК ДолгУпрОстаток,
		|	СУММА(ВЫБОР
		|		КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыПоСрокам.Предоплата
		|		ИНАЧЕ -РасчетыПоСрокам.Предоплата
		|	КОНЕЦ) КАК ПредоплатаОборот,
		|	СУММА(ВЫБОР
		|		КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыПоСрокам.ПредоплатаРегл
		|		ИНАЧЕ -РасчетыПоСрокам.ПредоплатаРегл
		|	КОНЕЦ) КАК ПредоплатаРеглОборот,
		|	СУММА(ВЫБОР
		|		КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыПоСрокам.ПредоплатаУпр
		|		ИНАЧЕ -РасчетыПоСрокам.ПредоплатаУпр
		|	КОНЕЦ) КАК ПредоплатаУпрОборот,
		|	СУММА(ВЫБОР
		|		КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыПоСрокам.Долг
		|		ИНАЧЕ -РасчетыПоСрокам.Долг
		|	КОНЕЦ) КАК ДолгОборот,
		|	СУММА(ВЫБОР
		|		КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыПоСрокам.ДолгРегл
		|		ИНАЧЕ -РасчетыПоСрокам.ДолгРегл
		|	КОНЕЦ) КАК ДолгРеглОборот,
		|	СУММА(ВЫБОР
		|		КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыПоСрокам.ДолгУпр
		|		ИНАЧЕ -РасчетыПоСрокам.ДолгУпр
		|	КОНЕЦ) КАК ДолгУпрОборот
		|ИЗ
		|	ВтДополненныеПараметрыРаспределения КАК ВтПараметры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокам
		|		ПО ВтПараметры.ПараметрОбъектРасчетов = РасчетыПоСрокам.ОбъектРасчетов
		|		И ВтПараметры.ПараметрАналитикаУчетаПоПартнерам = РасчетыПоСрокам.АналитикаУчетаПоПартнерам
		|		И ВтПараметры.ПараметрВалютаРасчетов = РасчетыПоСрокам.Валюта
		|		И РасчетыПоСрокам.Активность
		|		И РасчетыПоСрокам.Период >= &ДатаНачалаПересчетаМинимальный
		|		И РасчетыПоСрокам.Период < ВтПараметры.ПараметрДатаНачалаПересчета
		|		И ВтПараметры.ПараметрЭтоРасчетыСКлиентами = &РасчетыСКлиентами
		|СГРУППИРОВАТЬ ПО
		|	РасчетыПоСрокам.АналитикаУчетаПоПартнерам,
		|	РасчетыПоСрокам.ОбъектРасчетов,
		|	РасчетыПоСрокам.Валюта,
		|	РасчетыПоСрокам.РасчетныйДокумент,
		|	РасчетыПоСрокам.ДатаПлановогоПогашения,
		|	РасчетыПоСрокам.ДатаВозникновения";
		
		МассивТекстовПодзапроса.Добавить(ТекстПодзапросаДвижения);
		
	КонецЕсли;
	
	ТекстПодзапроса = СтрСоединить(МассивТекстовПодзапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстЗапросаРасчетыПоСрокамОстатки = "
	|ВЫБРАТЬ
	|	ОстаткиИОбороты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ОстаткиИОбороты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ОстаткиИОбороты.Валюта КАК Валюта,
	|	ОстаткиИОбороты.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ОстаткиИОбороты.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	ОстаткиИОбороты.ДатаВозникновения КАК ДатаВозникновения,
	|	СУММА(ОстаткиИОбороты.ПредоплатаОстаток + ОстаткиИОбороты.ПредоплатаОборот) КАК ПредоплатаОстаток,
	|	СУММА(ОстаткиИОбороты.ПредоплатаРеглОстаток + ОстаткиИОбороты.ПредоплатаРеглОборот) КАК ПредоплатаРеглОстаток,
	|	СУММА(ОстаткиИОбороты.ПредоплатаУпрОстаток + ОстаткиИОбороты.ПредоплатаУпрОборот) КАК ПредоплатаУпрОстаток,
	|	СУММА(ОстаткиИОбороты.ДолгОстаток + ОстаткиИОбороты.ДолгОборот) КАК ДолгОстаток,
	|	СУММА(ОстаткиИОбороты.ДолгРеглОстаток + ОстаткиИОбороты.ДолгРеглОборот) КАК ДолгРеглОстаток,
	|	СУММА(ОстаткиИОбороты.ДолгУпрОстаток + ОстаткиИОбороты.ДолгУпрОборот) КАК ДолгУпрОстаток
	|ПОМЕСТИТЬ ВТ_ОстаткиКлиенты
	|ИЗ
	|	&ТекстПодзапроса КАК ОстаткиИОбороты
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИОбороты.ОбъектРасчетов,
	|	ОстаткиИОбороты.АналитикаУчетаПоПартнерам,
	|	ОстаткиИОбороты.Валюта,
	|	ОстаткиИОбороты.РасчетныйДокумент,
	|	ОстаткиИОбороты.ДатаПлановогоПогашения,
	|	ОстаткиИОбороты.ДатаВозникновения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	Валюта,
	|	РасчетныйДокумент
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыПоСрокамОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыПоСрокамОстатки.Валюта КАК Валюта,
	|	РасчетыПоСрокамОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыПоСрокамОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	РасчетыПоСрокамОстатки.ДатаВозникновения КАК ДатаВозникновения,
	|	РасчетыПоСрокамОстатки.ПредоплатаОстаток КАК ПредоплатаОстаток,
	|	РасчетыПоСрокамОстатки.ПредоплатаРеглОстаток КАК ПредоплатаРеглОстаток,
	|	РасчетыПоСрокамОстатки.ПредоплатаУпрОстаток КАК ПредоплатаУпрОстаток,
	|	РасчетыПоСрокамОстатки.ДолгОстаток КАК ДолгОстаток,
	|	РасчетыПоСрокамОстатки.ДолгРеглОстаток КАК ДолгРеглОстаток,
	|	РасчетыПоСрокамОстатки.ДолгУпрОстаток КАК ДолгУпрОстаток,
	|	МАКСИМУМ(ВспомогательнаяИнформация.ПорядокОперации) КАК ПорядокОперации
	|ПОМЕСТИТЬ ВТОстаткиСПоследнимМоментомПриходногоДвиженияКлиенты
	|ИЗ
	|	ВТ_ОстаткиКлиенты КАК РасчетыПоСрокамОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК ВспомогательнаяИнформация
	|			ПО РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам     = ВспомогательнаяИнформация.АналитикаУчетаПоПартнерам
	|				И РасчетыПоСрокамОстатки.ОбъектРасчетов             = ВспомогательнаяИнформация.ОбъектРасчетов
	|				И РасчетыПоСрокамОстатки.Валюта                     = ВспомогательнаяИнформация.Валюта
	|				И РасчетыПоСрокамОстатки.РасчетныйДокумент          = ВспомогательнаяИнформация.РасчетныйДокумент
	|				И РасчетыПоСрокамОстатки.ДатаПлановогоПогашения     = ВспомогательнаяИнформация.ДатаПлановогоПогашения
	|				И РасчетыПоСрокамОстатки.ДатаВозникновения          = ВспомогательнаяИнформация.ДатаВозникновения
	|				И ВспомогательнаяИнформация.Активность
	|				И ВспомогательнаяИнформация.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И НЕ ВспомогательнаяИнформация.ДокументРегистратор ССЫЛКА Документ.РасчетКурсовыхРазниц
	|				И НЕ ВспомогательнаяИнформация.Сторно
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|			ПО ВтПараметры.ПараметрОбъектРасчетов = РасчетыПоСрокамОстатки.ОбъектРасчетов
	|				И ВтПараметры.ПараметрАналитикаУчетаПоПартнерам = РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам
	|				И ВтПараметры.ПараметрВалютаРасчетов = РасчетыПоСрокамОстатки.Валюта
	|				И ВспомогательнаяИнформация.Период < ВтПараметры.ПараметрДатаНачалаПересчета
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоСрокамОстатки.ОбъектРасчетов,
	|	РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам,
	|	РасчетыПоСрокамОстатки.Валюта,
	|	РасчетыПоСрокамОстатки.РасчетныйДокумент,
	|	РасчетыПоСрокамОстатки.ДатаВозникновения,
	|	РасчетыПоСрокамОстатки.ДатаПлановогоПогашения,
	|	РасчетыПоСрокамОстатки.ПредоплатаОстаток,
	|	РасчетыПоСрокамОстатки.ПредоплатаРеглОстаток,
	|	РасчетыПоСрокамОстатки.ПредоплатаУпрОстаток,
	|	РасчетыПоСрокамОстатки.ДолгОстаток,
	|	РасчетыПоСрокамОстатки.ДолгРеглОстаток,
	|	РасчетыПоСрокамОстатки.ДолгУпрОстаток
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыПоСрокамОстатки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыПоСрокамОстатки.Валюта КАК ВалютаРасчетов,
	|	РасчетыПоСрокамОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
	|	РасчетыПоСрокамОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	РасчетыПоСрокамОстатки.ДатаВозникновения КАК ДатаВозникновения,
	|	МИНИМУМ(ВспомогательнаяИнформация.ПорядокЗачета) КАК ПорядокЗачета,
	|	РасчетыПоСрокамОстатки.ПорядокОперации КАК ПорядокОперации,
	|	МАКСИМУМ(ВспомогательнаяИнформация.СвязанныйДокумент) КАК СвязанныйДокумент,
	|	МАКСИМУМ(ВспомогательнаяИнформация.ВалютаДокумента) КАК ВалютаДокумента,
	|	МАКСИМУМ(ВспомогательнаяИнформация.СтатьяДвиженияДенежныхСредств) КАК СтатьяДвиженияДенежныхСредств,
	|	МАКСИМУМ(ВспомогательнаяИнформация.ХозяйственнаяОперация) КАК ХозяйственнаяОперация,
	|	МАКСИМУМ(ВспомогательнаяИнформация.ДокументРегистратор) КАК ДокументРегистратор,
	|	РасчетыПоСрокамОстатки.ПредоплатаОстаток КАК Предоплата,
	|	РасчетыПоСрокамОстатки.ПредоплатаРеглОстаток КАК ПредоплатаРегл,
	|	РасчетыПоСрокамОстатки.ПредоплатаУпрОстаток КАК ПредоплатаУпр,
	|	РасчетыПоСрокамОстатки.ДолгОстаток КАК Долг,
	|	РасчетыПоСрокамОстатки.ДолгРеглОстаток КАК ДолгРегл,
	|	РасчетыПоСрокамОстатки.ДолгУпрОстаток КАК ДолгУпр,
	|	РасчетыПоСрокамОстатки.ПредоплатаОстаток <> 0 КАК ЕстьПредоплата,
	|	РасчетыПоСрокамОстатки.ДолгОстаток <> 0 КАК ЕстьДолг
	|ПОМЕСТИТЬ ВТРасчетыПоСрокамОстатки
	|ИЗ
	|	ВТОстаткиСПоследнимМоментомПриходногоДвиженияКлиенты КАК РасчетыПоСрокамОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК ВспомогательнаяИнформация
	|			ПО РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам     = ВспомогательнаяИнформация.АналитикаУчетаПоПартнерам
	|				И РасчетыПоСрокамОстатки.ОбъектРасчетов             = ВспомогательнаяИнформация.ОбъектРасчетов
	|				И РасчетыПоСрокамОстатки.Валюта                     = ВспомогательнаяИнформация.Валюта
	|				И РасчетыПоСрокамОстатки.РасчетныйДокумент          = ВспомогательнаяИнформация.РасчетныйДокумент
	|				И РасчетыПоСрокамОстатки.ДатаПлановогоПогашения     = ВспомогательнаяИнформация.ДатаПлановогоПогашения
	|				И РасчетыПоСрокамОстатки.ДатаВозникновения          = ВспомогательнаяИнформация.ДатаВозникновения
	|				И РасчетыПоСрокамОстатки.ПорядокОперации            = ВспомогательнаяИнформация.ПорядокОперации
	|				И ВспомогательнаяИнформация.Активность
	|				И ВспомогательнаяИнформация.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И НЕ ВспомогательнаяИнформация.ДокументРегистратор ССЫЛКА Документ.РасчетКурсовыхРазниц
	|				И НЕ ВспомогательнаяИнформация.Сторно
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|			ПО ВтПараметры.ПараметрОбъектРасчетов = РасчетыПоСрокамОстатки.ОбъектРасчетов
	|				И ВтПараметры.ПараметрАналитикаУчетаПоПартнерам = РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам
	|				И ВтПараметры.ПараметрВалютаРасчетов = РасчетыПоСрокамОстатки.Валюта
	|				И ВспомогательнаяИнформация.Период < ВтПараметры.ПараметрДатаНачалаПересчета
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоСрокамОстатки.ОбъектРасчетов,
	|	РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам,
	|	РасчетыПоСрокамОстатки.Валюта,
	|	РасчетыПоСрокамОстатки.РасчетныйДокумент,
	|	РасчетыПоСрокамОстатки.ДатаВозникновения,
	|	РасчетыПоСрокамОстатки.ПорядокОперации,
	|	РасчетыПоСрокамОстатки.ДатаПлановогоПогашения,
	|	РасчетыПоСрокамОстатки.ПредоплатаОстаток,
	|	РасчетыПоСрокамОстатки.ПредоплатаРеглОстаток,
	|	РасчетыПоСрокамОстатки.ПредоплатаУпрОстаток,
	|	РасчетыПоСрокамОстатки.ДолгОстаток,
	|	РасчетыПоСрокамОстатки.ДолгРеглОстаток,
	|	РасчетыПоСрокамОстатки.ДолгУпрОстаток,
	|	РасчетыПоСрокамОстатки.ПредоплатаОстаток <> 0,
	|	РасчетыПоСрокамОстатки.ДолгОстаток <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	ВалютаРасчетов,
	|	ЕстьПредоплата,
	|	ЕстьДолг";
	
	ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки, 
			"&ТекстПодзапроса", 
			"(" + ТекстПодзапроса + ")");
		
	Если Не ГлобальныеПеременные.ДатаНачалаПересчетаЕстьРазличные Тогда
		
		ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки,
			"СУММА(ОстаткиИОбороты.ПредоплатаОстаток + ОстаткиИОбороты.ПредоплатаОборот)", 
			"ОстаткиИОбороты.ПредоплатаОстаток + ОстаткиИОбороты.ПредоплатаОборот");
		
		ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки, 
			"СУММА(ОстаткиИОбороты.ПредоплатаРеглОстаток + ОстаткиИОбороты.ПредоплатаРеглОборот)",
			"ОстаткиИОбороты.ПредоплатаРеглОстаток + ОстаткиИОбороты.ПредоплатаРеглОборот");
		
		ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки, 
			"СУММА(ОстаткиИОбороты.ПредоплатаУпрОстаток + ОстаткиИОбороты.ПредоплатаУпрОборот)",
			"ОстаткиИОбороты.ПредоплатаУпрОстаток + ОстаткиИОбороты.ПредоплатаУпрОборот");
		
		ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки,
			"СУММА(ОстаткиИОбороты.ДолгОстаток + ОстаткиИОбороты.ДолгОборот)",
			"ОстаткиИОбороты.ДолгОстаток + ОстаткиИОбороты.ДолгОборот");
		
		ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки,
			"СУММА(ОстаткиИОбороты.ДолгРеглОстаток + ОстаткиИОбороты.ДолгРеглОборот)",
			"ОстаткиИОбороты.ДолгРеглОстаток + ОстаткиИОбороты.ДолгРеглОборот");
		
		ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки,
			"СУММА(ОстаткиИОбороты.ДолгУпрОстаток + ОстаткиИОбороты.ДолгУпрОборот)",
			"ОстаткиИОбороты.ДолгУпрОстаток + ОстаткиИОбороты.ДолгУпрОборот");
		
		ГруппировкаПодзапроса ="
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиИОбороты.ОбъектРасчетов,
		|	ОстаткиИОбороты.АналитикаУчетаПоПартнерам,
		|	ОстаткиИОбороты.Валюта,
		|	ОстаткиИОбороты.РасчетныйДокумент,
		|	ОстаткиИОбороты.ДатаПлановогоПогашения,
		|	ОстаткиИОбороты.ДатаВозникновения";
		
		ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки, ГруппировкаПодзапроса, "");
		
	КонецЕсли;
	
	Если НЕ РасчетыСКлиентами Тогда
		ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки,
			"РегистрНакопления.РасчетыСКлиентамиПоСрокам",
			"РегистрНакопления.РасчетыСПоставщикамиПоСрокам");
		ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки,
			"ВТ_ОстаткиКлиенты",
			"ВТ_ОстаткиПоставщики");
		ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки,
			"ВТОстаткиСПоследнимМоментомПриходногоДвиженияКлиенты",
			"ВТОстаткиСПоследнимМоментомПриходногоДвиженияПоставщики");
		ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки,
			"&РасчетыСКлиентами",
			"ЛОЖЬ");
	Иначе
		ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки,
			"&РасчетыСКлиентами",
			"ИСТИНА");
	КонецЕсли;
	
	Если НЕ ИмяТаблицы= "" Тогда
		ТекстЗапросаРасчетыПоСрокамОстатки = СтрЗаменить(ТекстЗапросаРасчетыПоСрокамОстатки,
			"ПОМЕСТИТЬ ВТРасчетыПоСрокамОстатки",
			"ПОМЕСТИТЬ " + ИмяТаблицы);
	КонецЕсли;
	
	Возврат ТекстЗапросаРасчетыПоСрокамОстатки;
	
КонецФункции

Функция ТекстЗапросаРасчетыДвиженияСМоментаПересчета(РасчетыСКлиентами = Истина)

	Если РасчетыСКлиентами Тогда
		#Область РасчетыСКлиентамиСМоментаПересчета
		ТекстЗапроса = "
		|ВЫБРАТЬ 
		|	Расчеты.Период КАК Период,
		|	Расчеты.ВидДвижения КАК ВидДвижения,
		|	Расчеты.Регистратор КАК Регистратор,
		|
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта КАК Валюта,
		|	
		// Далее используются флаги ЭтоОтгрузка и ЭтоОплата и суммы по модулю.
		|	ВЫБОР КОГДА Расчеты.Сумма < 0 ТОГДА -Расчеты.Сумма ИНАЧЕ Расчеты.Сумма КОНЕЦ КАК Сумма,
		|	ВЫБОР КОГДА Расчеты.КОплате < 0 ТОГДА -Расчеты.КОплате ИНАЧЕ Расчеты.КОплате КОНЕЦ КАК КОплате,
		|	ВЫБОР КОГДА Расчеты.КОтгрузке < 0 ТОГДА -Расчеты.КОтгрузке ИНАЧЕ Расчеты.КОтгрузке КОНЕЦ КАК КОтгрузке,
		|
		|	Расчеты.ОперацияВзаиморасчетов КАК ОперацияВзаиморасчетов,
		|	Расчеты.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	Расчеты.ПродажаПоЗаказу КАК ПродажаПоЗаказу,
		|	Расчеты.ДатаРегистратора КАК ДатаРегистратора,
		|	Расчеты.ДатаПлатежа КАК ДатаПлатежа,
		|	ВЫБОР КОГДА Расчеты.СуммаРегл < 0 ТОГДА -Расчеты.СуммаРегл ИНАЧЕ Расчеты.СуммаРегл КОНЕЦ КАК СуммаРегл,
		|	ВЫБОР КОГДА Расчеты.СуммаУпр < 0 ТОГДА -Расчеты.СуммаУпр ИНАЧЕ Расчеты.СуммаУпр КОНЕЦ КАК СуммаУпр,
		|	Расчеты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	Расчеты.РасчетныйДокумент КАК РасчетныйДокумент,
		|	Расчеты.ПорядокОперации КАК ПорядокОперации,
		|	Расчеты.ПорядокЗачетаПоДатеПлатежа КАК ПорядокЗачетаПоДатеПлатежа,
		|	Расчеты.СвязанныйДокумент КАК СвязанныйДокумент,
		|	Расчеты.ВариантОплаты КАК ВариантОплаты,
		|	Расчеты.ВалютаДокумента КАК ВалютаДокумента,
		|	Расчеты.КорОбъектРасчетов КАК КорОбъектРасчетов,
		|	Расчеты.КорАналитикаУчетаПоПартнерам КАК КорАналитикаУчетаПоПартнерам,
		|	Расчеты.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
		|	Расчеты.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
		|	Расчеты.ОбъектРасчетовПриемник КАК ОбъектРасчетовПриемник,
		|	Расчеты.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
		|	Расчеты.ВалютаПриемник КАК ВалютаПриемник,
		|	ВЫБОР КОГДА Расчеты.СуммаПриемник < 0 ТОГДА -Расчеты.СуммаПриемник ИНАЧЕ Расчеты.СуммаПриемник КОНЕЦ КАК СуммаПриемник,
		|	Расчеты.ПоДаннымОбъектаРасчетовИсточника КАК ПоДаннымОбъектаРасчетовИсточника,
		|	Расчеты.Сторно КАК Сторно,
		|
		|	Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И Расчеты.Сумма + Расчеты.КОплате < 0
		|		ИЛИ Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Расчеты.Сумма + Расчеты.КОплате > 0 КАК ЭтоОплата,
		|
		|	Расчеты.ВалютаДокумента = Параметры.ПараметрВалютаРегламентированногоУчета
		|		ИЛИ Параметры.ПараметрВалютаРасчетов = Параметры.ПараметрВалютаРегламентированногоУчета КАК РучнойКурсРегл,
		|
		|	Расчеты.ВалютаДокумента = &ВалютаУпр 
		|		ИЛИ Параметры.ПараметрВалютаРасчетов = &ВалютаУпр КАК РучнойКурсУпр
		|ПОМЕСТИТЬ РасчетыДвиженияКлиенты
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК Параметры
		|			ПО Расчеты.ОбъектРасчетов = Параметры.ПараметрОбъектРасчетов
		|				И Расчеты.АналитикаУчетаПоПартнерам = Параметры.ПараметрАналитикаУчетаПоПартнерам
		|				И Расчеты.Валюта = Параметры.ПараметрВалютаРасчетов
		|				И Расчеты.Период >= Параметры.ПараметрДатаНачалаПересчета
		|ГДЕ
		|	Расчеты.Активность
		|	И НЕ Расчеты.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|	И Параметры.ПараметрЭтоРасчетыСКлиентами
		|	И (Расчеты.Сумма <> 0 ИЛИ Расчеты.КОплате <> 0  ИЛИ Расчеты.КОтгрузке <> 0)
		|	
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектРасчетов,
		|	АналитикаУчетаПоПартнерам,
		|	Валюта,
		|	ВидДвижения";
		#КонецОбласти
	Иначе
		#Область РасчетыСПоставщикамиСМоментаПересчета
		
		ТекстЗапроса = "
		|ВЫБРАТЬ 
		|	Расчеты.Период КАК Период,
		|	Расчеты.ВидДвижения КАК ВидДвижения,
		|	Расчеты.Регистратор КАК Регистратор,
		|
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта КАК Валюта,
		|	
		|	ВЫБОР КОГДА Расчеты.Сумма < 0 ТОГДА -Расчеты.Сумма ИНАЧЕ Расчеты.Сумма КОНЕЦ КАК Сумма,
		|	ВЫБОР КОГДА Расчеты.КОплате < 0 ТОГДА -Расчеты.КОплате ИНАЧЕ Расчеты.КОплате КОНЕЦ КАК КОплате,
		|	ВЫБОР КОГДА Расчеты.КПоступлению < 0 ТОГДА -Расчеты.КПоступлению ИНАЧЕ Расчеты.КПоступлению КОНЕЦ КАК КОтгрузке,
		|	
		|	Расчеты.ОперацияВзаиморасчетов КАК ОперацияВзаиморасчетов,
		|	Расчеты.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	Расчеты.ЗакупкаПоЗаказу КАК ЗакупкаПоЗаказу,
		|	Расчеты.ДатаРегистратора КАК ДатаРегистратора,
		|	Расчеты.ДатаПлатежа КАК ДатаПлатежа,
		|	ВЫБОР КОГДА Расчеты.СуммаРегл < 0 ТОГДА -Расчеты.СуммаРегл ИНАЧЕ Расчеты.СуммаРегл КОНЕЦ КАК СуммаРегл,
		|	ВЫБОР КОГДА Расчеты.СуммаУпр < 0 ТОГДА -Расчеты.СуммаУпр ИНАЧЕ Расчеты.СуммаУпр КОНЕЦ КАК СуммаУпр,
		|	Расчеты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	Расчеты.РасчетныйДокумент КАК РасчетныйДокумент,
		|	Расчеты.ПорядокОперации КАК ПорядокОперации,
		|	Расчеты.ПорядокЗачетаПоДатеПлатежа КАК ПорядокЗачетаПоДатеПлатежа,
		|	Расчеты.СвязанныйДокумент КАК СвязанныйДокумент,
		|	Расчеты.ВариантОплаты КАК ВариантОплаты,
		|	Расчеты.ВалютаДокумента КАК ВалютаДокумента,
		|	Расчеты.КорОбъектРасчетов КАК КорОбъектРасчетов,
		|	Расчеты.КорАналитикаУчетаПоПартнерам КАК КорАналитикаУчетаПоПартнерам,
		|	Расчеты.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
		|	Расчеты.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
		|	Расчеты.ОбъектРасчетовПриемник КАК ОбъектРасчетовПриемник,
		|	Расчеты.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
		|	Расчеты.ВалютаПриемник КАК ВалютаПриемник,
		|	ВЫБОР КОГДА Расчеты.СуммаПриемник < 0 ТОГДА -Расчеты.СуммаПриемник ИНАЧЕ Расчеты.СуммаПриемник КОНЕЦ КАК СуммаПриемник,
		|	Расчеты.ПоДаннымОбъектаРасчетовИсточника КАК ПоДаннымОбъектаРасчетовИсточника,
		|	Расчеты.Сторно КАК Сторно,
		|
		|	(Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Расчеты.Сумма + Расчеты.КОплате < 0
		|		ИЛИ Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И Расчеты.Сумма + Расчеты.КОплате > 0) КАК ЭтоОплата,
		|	Расчеты.ВалютаДокумента = Параметры.ПараметрВалютаРегламентированногоУчета
		|			ИЛИ Параметры.ПараметрВалютаРасчетов = Параметры.ПараметрВалютаРегламентированногоУчета КАК РучнойКурсРегл,
		|	Расчеты.ВалютаДокумента = &ВалютаУпр 
		|			ИЛИ Параметры.ПараметрВалютаРасчетов = &ВалютаУпр КАК РучнойКурсУпр
		|ПОМЕСТИТЬ РасчетыДвиженияПоставщики
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК Параметры
		|			ПО Расчеты.ОбъектРасчетов = Параметры.ПараметрОбъектРасчетов
		|				И Расчеты.АналитикаУчетаПоПартнерам = Параметры.ПараметрАналитикаУчетаПоПартнерам
		|				И Расчеты.Валюта = Параметры.ПараметрВалютаРасчетов
		|				И Расчеты.Период >= Параметры.ПараметрДатаНачалаПересчета
		|ГДЕ
		|	Расчеты.Активность
		|	И НЕ Расчеты.Регистратор ССЫЛКА Документ.КорректировкаРегистров
		|	И НЕ Параметры.ПараметрЭтоРасчетыСКлиентами
		|	И (Расчеты.Сумма <> 0 ИЛИ Расчеты.КОплате <> 0  ИЛИ Расчеты.КПоступлению <> 0)
		|	
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектРасчетов,
		|	АналитикаУчетаПоПартнерам,
		|	Валюта,
		|	ВидДвижения";
		#КонецОбласти
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыПланОплатОстатки(ГлобальныеПеременные, РасчетыСКлиентами = Истина, ИмяТаблицы = "")
	
	МассивТекстовПодзапроса = Новый Массив;
	
	ТекстПодзапросаОстатки = "
	|ВЫБРАТЬ
	|	РасчетыПланОплат.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыПланОплат.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыПланОплат.Валюта КАК Валюта,
	|	РасчетыПланОплат.ДокументПлан КАК ДокументПлан,
	|	РасчетыПланОплат.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	РасчетыПланОплат.ДатаВозникновения КАК ДатаВозникновения,
	|	РасчетыПланОплат.ВариантОплаты КАК ВариантОплаты,
	|	РасчетыПланОплат.НераспределенныйАванс КАК НераспределенныйАванс,
	|	РасчетыПланОплат.КОплатеОстаток КАК КОплатеОстаток,
	|	0 КАК КОплатеООборот
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПланОплат.Остатки(&ДатаНачалаПересчетаМинимальный, (АналитикаУчетаПоПартнерам,
	|		ОбъектРасчетов, Валюта) В
	|		(ВЫБРАТЬ
	|			ВтПараметры.ПараметрАналитикаУчетаПоПартнерам,
	|			ВтПараметры.ПараметрОбъектРасчетов,
	|			ВтПараметры.ПараметрВалютаРасчетов
	|		ИЗ
	|			ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|		ГДЕ
	|			ВтПараметры.ПараметрЭтоРасчетыСКлиентами = &РасчетыСКлиентами)) КАК РасчетыПланОплат";
	
	МассивТекстовПодзапроса.Добавить(ТекстПодзапросаОстатки);
	
	Если ГлобальныеПеременные.ДатаНачалаПересчетаЕстьРазличные Тогда
		
		ТекстПодзапросаДвижения = "
		|ВЫБРАТЬ
		|	РасчетыПланОплат.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	РасчетыПланОплат.ОбъектРасчетов КАК ОбъектРасчетов,
		|	РасчетыПланОплат.Валюта КАК Валюта,
		|	РасчетыПланОплат.ДокументПлан КАК ДокументПлан,
		|	РасчетыПланОплат.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
		|	РасчетыПланОплат.ДатаВозникновения КАК ДатаВозникновения,
		|	РасчетыПланОплат.ВариантОплаты КАК ВариантОплаты,
		|	РасчетыПланОплат.НераспределенныйАванс КАК НераспределенныйАванс,
		|	СУММА(0) КАК КОплатеОстаток,
		|	СУММА(ВЫБОР
		|		КОГДА РасчетыПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыПланОплат.КОплате
		|		ИНАЧЕ -РасчетыПланОплат.КОплате
		|	КОНЕЦ) КАК КОплатеООборот
		|ИЗ
		|	ВтДополненныеПараметрыРаспределения КАК ВтПараметры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПланОплат КАК РасчетыПланОплат
		|		ПО ВтПараметры.ПараметрОбъектРасчетов = РасчетыПланОплат.ОбъектРасчетов
		|		И ВтПараметры.ПараметрАналитикаУчетаПоПартнерам = РасчетыПланОплат.АналитикаУчетаПоПартнерам
		|		И ВтПараметры.ПараметрВалютаРасчетов = РасчетыПланОплат.Валюта
		|		И РасчетыПланОплат.Активность
		|		И РасчетыПланОплат.Период >= &ДатаНачалаПересчетаМинимальный
		|		И РасчетыПланОплат.Период < ВтПараметры.ПараметрДатаНачалаПересчета
		|		И ВтПараметры.ПараметрЭтоРасчетыСКлиентами = &РасчетыСКлиентами
		|СГРУППИРОВАТЬ ПО
		|	РасчетыПланОплат.АналитикаУчетаПоПартнерам,
		|	РасчетыПланОплат.ОбъектРасчетов,
		|	РасчетыПланОплат.Валюта,
		|	РасчетыПланОплат.ДокументПлан,
		|	РасчетыПланОплат.ДатаПлановогоПогашения,
		|	РасчетыПланОплат.ДатаВозникновения,
		|	РасчетыПланОплат.ВариантОплаты,
		|	РасчетыПланОплат.НераспределенныйАванс";
		
		МассивТекстовПодзапроса.Добавить(ТекстПодзапросаДвижения);
		
	КонецЕсли;
	
	ТекстПодзапроса = СтрСоединить(МассивТекстовПодзапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстЗапросаРасчетыПланОплатОстатки = "
	|ВЫБРАТЬ
	|	ОстаткиИОбороты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ОстаткиИОбороты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ОстаткиИОбороты.Валюта КАК ВалютаРасчетов,
	|	ОстаткиИОбороты.ДокументПлан КАК ДокументПлан,
	|	ОстаткиИОбороты.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	ОстаткиИОбороты.ДатаВозникновения КАК ДатаВозникновения,
	|	ВтДополненныеПараметрыРаспределения.ПараметрПорядок КАК ПорядокОперации,
	|	ОстаткиИОбороты.ВариантОплаты КАК ВариантОплаты,
	|	СУММА(ВЫБОР КОГДА НЕ ОстаткиИОбороты.НераспределенныйАванс 
	|				ТОГДА ОстаткиИОбороты.КОплатеОстаток + ОстаткиИОбороты.КОплатеООборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КОплате,
	|	СУММА(ВЫБОР КОГДА ОстаткиИОбороты.НераспределенныйАванс 
	|				ТОГДА ОстаткиИОбороты.КОплатеОстаток + ОстаткиИОбороты.КОплатеООборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КОплатеНераспределенныйАванс,
	|	СУММА(ВЫБОР КОГДА ОстаткиИОбороты.НераспределенныйАванс 
	|				ТОГДА ОстаткиИОбороты.КОплатеОстаток + ОстаткиИОбороты.КОплатеООборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) < 0 КАК ЕстьКОплатеНераспределенныйАванс
	|ПОМЕСТИТЬ ВТРасчетыПланОплатОстатки
	|ИЗ
	|	&ТекстПодзапроса КАК ОстаткиИОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтДополненныеПараметрыРаспределения
	|			ПО ВтДополненныеПараметрыРаспределения.ПараметрАналитикаУчетаПоПартнерам = ОстаткиИОбороты.АналитикаУчетаПоПартнерам
	|				И ВтДополненныеПараметрыРаспределения.ПараметрОбъектРасчетов = ОстаткиИОбороты.ОбъектРасчетов
	|				И ВтДополненныеПараметрыРаспределения.ПараметрВалютаРасчетов = ОстаткиИОбороты.Валюта
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИОбороты.АналитикаУчетаПоПартнерам,
	|	ОстаткиИОбороты.ОбъектРасчетов,
	|	ОстаткиИОбороты.Валюта,
	|	ОстаткиИОбороты.ДокументПлан,
	|	ОстаткиИОбороты.ДатаПлановогоПогашения,
	|	ОстаткиИОбороты.ДатаВозникновения,
	|	ОстаткиИОбороты.ВариантОплаты,
	|	ВтДополненныеПараметрыРаспределения.ПараметрПорядок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	ВалютаРасчетов,
	|	ДокументПлан,
	|	ДатаПлановогоПогашения";
		
	ТекстЗапросаРасчетыПланОплатОстатки = СтрЗаменить(ТекстЗапросаРасчетыПланОплатОстатки,
		"&ТекстПодзапроса",
		"(" + ТекстПодзапроса + ")");
	
	Если НЕ РасчетыСКлиентами Тогда
		ТекстЗапросаРасчетыПланОплатОстатки = СтрЗаменить(ТекстЗапросаРасчетыПланОплатОстатки,
			"РегистрНакопления.РасчетыСКлиентамиПланОплат",
			"РегистрНакопления.РасчетыСПоставщикамиПланОплат");
		
		ТекстЗапросаРасчетыПланОплатОстатки = СтрЗаменить(ТекстЗапросаРасчетыПланОплатОстатки,
			"&РасчетыСКлиентами",
			"ЛОЖЬ");
	Иначе
		ТекстЗапросаРасчетыПланОплатОстатки = СтрЗаменить(ТекстЗапросаРасчетыПланОплатОстатки,
			"&РасчетыСКлиентами",
			"ИСТИНА");
	КонецЕсли; 
	
	Если НЕ ИмяТаблицы= "" Тогда
		ТекстЗапросаРасчетыПланОплатОстатки = СтрЗаменить(ТекстЗапросаРасчетыПланОплатОстатки,
			"ПОМЕСТИТЬ ВТРасчетыПланОплатОстатки",
			"ПОМЕСТИТЬ " + ИмяТаблицы);
	КонецЕсли;
	
	Возврат ТекстЗапросаРасчетыПланОплатОстатки;
	
КонецФункции

Функция ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки(ГлобальныеПеременные, РасчетыСКлиентами = Истина, ИмяТаблицы = "")
	
	МассивТекстовПодзапроса = Новый Массив;
	
	ТекстПодзапросаОстатки = "
	|ВЫБРАТЬ
	|	РасчетыПланОплат.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыПланОплат.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыПланОплат.Валюта КАК Валюта,
	|	РасчетыПланОплат.ДокументПлан КАК ДокументПлан,
	|	РасчетыПланОплат.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	РасчетыПланОплат.ДатаВозникновения КАК ДатаВозникновения,
	|	РасчетыПланОплат.НераспределенныйАванс КАК НераспределенныйАванс,
	|	РасчетыПланОплат.СуммаОстаток КАК СуммаОстаток,
	|	0 КАК СуммаООборот
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПланОтгрузок.Остатки(&ДатаНачалаПересчетаМинимальный, (АналитикаУчетаПоПартнерам,
	|		ОбъектРасчетов, Валюта) В
	|		(ВЫБРАТЬ
	|			ВтПараметры.ПараметрАналитикаУчетаПоПартнерам,
	|			ВтПараметры.ПараметрОбъектРасчетов,
	|			ВтПараметры.ПараметрВалютаРасчетов
	|		ИЗ
	|			ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|		ГДЕ
	|			ВтПараметры.ПараметрЭтоРасчетыСКлиентами = &РасчетыСКлиентами)) КАК РасчетыПланОплат";
	
	МассивТекстовПодзапроса.Добавить(ТекстПодзапросаОстатки);
	
	Если ГлобальныеПеременные.ДатаНачалаПересчетаЕстьРазличные Тогда
		
		ТекстПодзапросаДвижения = "
		|ВЫБРАТЬ
		|	РасчетыПланОплат.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	РасчетыПланОплат.ОбъектРасчетов КАК ОбъектРасчетов,
		|	РасчетыПланОплат.Валюта КАК Валюта,
		|	РасчетыПланОплат.ДокументПлан КАК ДокументПлан,
		|	РасчетыПланОплат.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
		|	РасчетыПланОплат.ДатаВозникновения КАК ДатаВозникновения,
		|	РасчетыПланОплат.НераспределенныйАванс КАК НераспределенныйАванс,
		|	СУММА(0) КАК СуммаОстаток,
		|	СУММА(ВЫБОР
		|		КОГДА РасчетыПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыПланОплат.Сумма
		|		ИНАЧЕ -РасчетыПланОплат.Сумма
		|	КОНЕЦ) КАК СуммаООборот
		|ИЗ
		|	ВтДополненныеПараметрыРаспределения КАК ВтПараметры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПланОтгрузок КАК РасчетыПланОплат
		|		ПО ВтПараметры.ПараметрОбъектРасчетов = РасчетыПланОплат.ОбъектРасчетов
		|		И ВтПараметры.ПараметрАналитикаУчетаПоПартнерам = РасчетыПланОплат.АналитикаУчетаПоПартнерам
		|		И ВтПараметры.ПараметрВалютаРасчетов = РасчетыПланОплат.Валюта
		|		И РасчетыПланОплат.Активность
		|		И РасчетыПланОплат.Период >= &ДатаНачалаПересчетаМинимальный
		|		И РасчетыПланОплат.Период < ВтПараметры.ПараметрДатаНачалаПересчета
		|		И ВтПараметры.ПараметрЭтоРасчетыСКлиентами = &РасчетыСКлиентами
		|СГРУППИРОВАТЬ ПО
		|	РасчетыПланОплат.АналитикаУчетаПоПартнерам,
		|	РасчетыПланОплат.ОбъектРасчетов,
		|	РасчетыПланОплат.Валюта,
		|	РасчетыПланОплат.ДокументПлан,
		|	РасчетыПланОплат.ДатаПлановогоПогашения,
		|	РасчетыПланОплат.ДатаВозникновения,
		|	РасчетыПланОплат.НераспределенныйАванс";
		
		МассивТекстовПодзапроса.Добавить(ТекстПодзапросаДвижения);
		
	КонецЕсли;
	
	ТекстПодзапроса = СтрСоединить(МассивТекстовПодзапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки = "
	|ВЫБРАТЬ
	|	ОстаткиИОбороты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ОстаткиИОбороты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ОстаткиИОбороты.Валюта КАК ВалютаРасчетов,
	|	ОстаткиИОбороты.ДокументПлан КАК ДокументПлан,
	|	ОстаткиИОбороты.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	ОстаткиИОбороты.ДатаВозникновения КАК ДатаВозникновения,
	|	ВтДополненныеПараметрыРаспределения.ПараметрПорядок КАК ПорядокОперации,
	|	СУММА(ВЫБОР КОГДА НЕ ОстаткиИОбороты.НераспределенныйАванс 
	|				ТОГДА ОстаткиИОбороты.СуммаОстаток + ОстаткиИОбороты.СуммаООборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР КОГДА ОстаткиИОбороты.НераспределенныйАванс 
	|				ТОГДА ОстаткиИОбороты.СуммаОстаток + ОстаткиИОбороты.СуммаООборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаНераспределенныйАванс,
	|	СУММА(ВЫБОР КОГДА ОстаткиИОбороты.НераспределенныйАванс 
	|				ТОГДА ОстаткиИОбороты.СуммаОстаток + ОстаткиИОбороты.СуммаООборот
	|			ИНАЧЕ 0
	|		КОНЕЦ) < 0 КАК ЕстьСуммаНераспределенныйАванс
	|ПОМЕСТИТЬ ВТРасчетыПланОтгрузокПоставокОстатки
	|ИЗ
	|	&ТекстПодзапроса КАК ОстаткиИОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтДополненныеПараметрыРаспределения
	|			ПО ВтДополненныеПараметрыРаспределения.ПараметрАналитикаУчетаПоПартнерам = ОстаткиИОбороты.АналитикаУчетаПоПартнерам
	|				И ВтДополненныеПараметрыРаспределения.ПараметрОбъектРасчетов = ОстаткиИОбороты.ОбъектРасчетов
	|				И ВтДополненныеПараметрыРаспределения.ПараметрВалютаРасчетов = ОстаткиИОбороты.Валюта
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиИОбороты.АналитикаУчетаПоПартнерам,
	|	ОстаткиИОбороты.ОбъектРасчетов,
	|	ОстаткиИОбороты.Валюта,
	|	ОстаткиИОбороты.ДокументПлан,
	|	ОстаткиИОбороты.ДатаПлановогоПогашения,
	|	ОстаткиИОбороты.ДатаВозникновения,
	|	ВтДополненныеПараметрыРаспределения.ПараметрПорядок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	ВалютаРасчетов";
	
	ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки = СтрЗаменить(ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки, 
			"&ТекстПодзапроса", 
			"(" + ТекстПодзапроса + ")");
			
	Если НЕ РасчетыСКлиентами Тогда
		ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки = СтрЗаменить(ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки,
			"РегистрНакопления.РасчетыСКлиентамиПланОтгрузок",
			"РегистрНакопления.РасчетыСПоставщикамиПланПоставок");
		
		ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки = СтрЗаменить(ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки,
			"&РасчетыСКлиентами",
			"ЛОЖЬ");
	Иначе
		ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки = СтрЗаменить(ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки,
			"&РасчетыСКлиентами",
			"ИСТИНА");
	КонецЕсли; 
	
	Если НЕ ИмяТаблицы= "" Тогда
		ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки = СтрЗаменить(ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки,
			"ПОМЕСТИТЬ ВТРасчетыПланОтгрузокПоставокОстатки",
			"ПОМЕСТИТЬ " + ИмяТаблицы);
	КонецЕсли;
	
	Возврат ТекстЗапросаРасчетыПланОтгрузокПоставокОстатки;
	
КонецФункции

// Для исправлений и сторно движения исходных документов, не входящих в период распределения, копируются "Как есть".
Функция ТекстЗапросИсходныеРегистраторы(ЭтоРасчетыСКлиентами)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Движения.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Движения.ОбъектРасчетов            КАК ОбъектРасчетов,
	|	Движения.Валюта                    КАК ВалютаРасчетов,
	|
	|	Реестр.СторнируемыйДокумент        КАК СторнируемыйРегистратор,
	|	Реестр.ИсправляемыйДокумент        КАК ИсправляемыйДокумент,
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(ЕСТЬNULL(ИсходныйДокумент.ДатаДокументаИБ,ДАТАВРЕМЯ(1,1,1)), ДЕНЬ)) КАК ИсходнаяДатаВозникновения,
	|
	|	Движения.Регистратор                         КАК НовыйРегистратор,
	|	МИНИМУМ(Движения.Период)                     КАК НовыйПериод,
	|	НАЧАЛОПЕРИОДА(МИНИМУМ(Движения.Период),ДЕНЬ) КАК НовыйПериодНачалоДня,
	|	МИНИМУМ(Движения.ПорядокОперации)            КАК НовыйПорядокОперации,
	|	ЛОЖЬ КАК Отсторнировано
	|ПОМЕСТИТЬ ВтИсходныеРегистраторыКлиенты
	|ИЗ
	|	РасчетыДвиженияКлиенты КАК Движения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК Реестр
	|			ПО Реестр.Ссылка = Движения.Регистратор
	|				И Реестр.СторноИсправление
	|				И Реестр.Проведен
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК ИсходныйДокумент
	|			ПО ИсходныйДокумент.Ссылка = Реестр.ИсправляемыйДокумент
	|СГРУППИРОВАТЬ ПО 
	|	Движения.АналитикаУчетаПоПартнерам,
	|	Движения.ОбъектРасчетов,
	|	Движения.Валюта,
	|	Реестр.СторнируемыйДокумент,
	|	Реестр.ИсправляемыйДокумент,
	|	Движения.Регистратор
	|";
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"РасчетыДвиженияКлиенты","РасчетыДвиженияПоставщики");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ВтИсходныеРегистраторыКлиенты","ВтИсходныеРегистраторыПоставщики");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает таблицу движений исходных документов, которые сторнируются в текущем распределении
Функция ДвиженияСторнируемыхДокументовДоНачалаРасчета(Запрос, ЭтоРасчетыСКлиентами = Ложь)
	
	Запрос.Текст = "
	|	ВЫБРАТЬ
	|		ИсходныеРасчетыПоСрокам.Период                             КАК Период,
	|		ИсходныеРасчетыПоСрокам.ВидДвижения                        КАК ВидДвижения,
	|		ИсходныеРасчетыПоСрокам.ДокументРегистратор                КАК ДокументРегистратор,
	|		ИсходныеРасчетыПоСрокам.ОбъектРасчетов                     КАК ОбъектРасчетов,
	|		ИсходныеРасчетыПоСрокам.АналитикаУчетаПоПартнерам          КАК АналитикаУчетаПоПартнерам,
	|		ИсходныеРасчетыПоСрокам.Валюта                             КАК ВалютаРасчетов,
	|		ИсходныеРасчетыПоСрокам.ДатаПлановогоПогашения             КАК ДатаПлановогоПогашения,
	|		ИсходныеРасчетыПоСрокам.ДатаВозникновения                  КАК ДатаВозникновения,
	|		ИсходныеРасчетыПоСрокам.ПорядокОперации                    КАК ПорядокОперации,
	|		ИсходныеРасчетыПоСрокам.ПорядокЗачета                      КАК ПорядокЗачета,
	|		ИсходныеРасчетыПоСрокам.РасчетныйДокумент                  КАК РасчетныйДокумент,
	|		ИсходныеРасчетыПоСрокам.ХозяйственнаяОперация              КАК ХозяйственнаяОперация,
	|		ЗНАЧЕНИЕ(Перечисление.ОперацииВзаиморасчетов.ПустаяСсылка) КАК ОперацияВзаиморасчетов,
	|		ИсходныеРасчетыПоСрокам.ТипЗаписиВзаиморасчетов            КАК ТипЗаписиВзаиморасчетов,
	|		ИсходныеРасчетыПоСрокам.КорОбъектРасчетов                  КАК КорОбъектРасчетов,
	|		ИсходныеРасчетыПоСрокам.КорАналитикаУчетаПоПартнерам       КАК КорАналитикаУчетаПоПартнерам,
	|		ИсходныеРасчетыПоСрокам.АналитикаУчетаПоПартнерамПриемник  КАК АналитикаУчетаПоПартнерамПриемник,
	|		ИсходныеРасчетыПоСрокам.ОбъектРасчетовПриемник             КАК ОбъектРасчетовПриемник,
	|		ИсходныеРасчетыПоСрокам.ВалютаПриемник                     КАК ВалютаПриемник,
	|		ИсходныеРасчетыПоСрокам.СуммаПриемник                      КАК СуммаПриемник,
	|		ИсходныеРасчетыПоСрокам.ПоДаннымОбъектаРасчетовИсточника   КАК ПоДаннымОбъектаРасчетовИсточника,
	|		
	|		ЛОЖЬ                                                       КАК Сторно,
	|		
	|		ИсходныеРасчетыПоСрокам.Долг           КАК Долг,
	|		ИсходныеРасчетыПоСрокам.ДолгРегл       КАК ДолгРегл,
	|		ИсходныеРасчетыПоСрокам.ДолгУпр        КАК ДолгУпр,
	|		ИсходныеРасчетыПоСрокам.Предоплата     КАК Предоплата,
	|		ИсходныеРасчетыПоСрокам.ПредоплатаРегл КАК ПредоплатаРегл,
	|		ИсходныеРасчетыПоСрокам.ПредоплатаУпр  КАК ПредоплатаУпр,
	|
	|		0 КАК Исправлено,
	|
	|		ИсходныеРасчетыПоСрокам.ВалютаДокумента                 КАК ВалютаДокумента,
	|		ИсходныеРасчетыПоСрокам.СтатьяДвиженияДенежныхСредств   КАК СтатьяДвиженияДенежныхСредств,
	|		ИсходныеРасчетыПоСрокам.ИдентификаторФинЗаписи          КАК ИдентификаторФинЗаписи,
	|		ИсходныеРасчетыПоСрокам.НастройкаХозяйственнойОперации  КАК НастройкаХозяйственнойОперации,
	|		ИсходныеРасчетыПоСрокам.СвязанныйДокумент               КАК СвязанныйДокумент,
	|		ЛОЖЬ                                                    КАК Отсторнировано
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК ИсходныеРасчетыПоСрокам
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтИсходныеРегистраторыКлиенты КАК ВтИсходныеРегистраторы
	|				ПО ВтИсходныеРегистраторы.СторнируемыйРегистратор = ИсходныеРасчетыПоСрокам.ДокументРегистратор
	|					И ВтИсходныеРегистраторы.АналитикаУчетаПоПартнерам = ИсходныеРасчетыПоСрокам.АналитикаУчетаПоПартнерам
	|					И ВтИсходныеРегистраторы.ОбъектРасчетов = ИсходныеРасчетыПоСрокам.ОбъектРасчетов
	|					И ВтИсходныеРегистраторы.ВалютаРасчетов = ИсходныеРасчетыПоСрокам.Валюта
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|				ПО ИсходныеРасчетыПоСрокам.АналитикаУчетаПоПартнерам = ВтПараметры.ПараметрАналитикаУчетаПоПартнерам
	|					И ИсходныеРасчетыПоСрокам.ОбъектРасчетов = ВтПараметры.ПараметрОбъектРасчетов
	|					И ИсходныеРасчетыПоСрокам.Валюта = ВтПараметры.ПараметрВалютаРасчетов
	|	ГДЕ
	|		НЕ ИсходныеРасчетыПоСрокам.Сторно
	|		И ИсходныеРасчетыПоСрокам.Период < ВтПараметры.ПараметрДатаНачалаПересчета";
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ВтИсходныеРегистраторыКлиенты","ВтИсходныеРегистраторыПоставщики");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"РегистрНакопления.РасчетыСКлиентамиПоСрокам","РегистрНакопления.РасчетыСПоставщикамиПоСрокам");
	КонецЕсли;
	
	Движения = Запрос.Выполнить().Выгрузить();
	Возврат Движения;
	
КонецФункции

// Возвращает таблицу движений исходных документов, которые сторнируются в текущем распределении
Функция ДвиженияСторнируемыхДокументовПланОплатДоНачалаРасчета(Запрос, ЭтоРасчетыСКлиентами = Ложь)
	
	Запрос.Текст = "
	|	ВЫБРАТЬ
	|		ИсходныеРасчеты.Период                        КАК Период,
	|		ИсходныеРасчеты.ВидДвижения                   КАК ВидДвижения,
	|		ИсходныеРасчеты.ДокументРегистратор           КАК ДокументРегистратор,
	|		ИсходныеРасчеты.ОбъектРасчетов                КАК ОбъектРасчетов,
	|		ИсходныеРасчеты.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|		ИсходныеРасчеты.Валюта                        КАК ВалютаРасчетов,
	|		ИсходныеРасчеты.ДокументПлан                  КАК ДокументПлан,
	|		ИсходныеРасчеты.ДатаПлановогоПогашения        КАК ДатаПлановогоПогашения,
	|		ИсходныеРасчеты.ДатаВозникновения             КАК ДатаВозникновения,
	|		ИсходныеРасчеты.ВариантОплаты                 КАК ВариантОплаты,
	|		ИсходныеРасчеты.НераспределенныйАванс         КАК НераспределенныйАванс,
	|		ИсходныеРасчеты.ПорядокОперации               КАК ПорядокОперации,
	|		ИсходныеРасчеты.ДокументОплаты                КАК ДокументОплаты,
	|		ЛОЖЬ                                          КАК Сторно,
	|		ИсходныеРасчеты.КОплате КАК КОплате
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПланОплат КАК ИсходныеРасчеты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтИсходныеРегистраторыКлиенты КАК ВтИсходныеРегистраторы
	|				ПО ВтИсходныеРегистраторы.СторнируемыйРегистратор = ИсходныеРасчеты.ДокументРегистратор
	|					И ВтИсходныеРегистраторы.АналитикаУчетаПоПартнерам = ИсходныеРасчеты.АналитикаУчетаПоПартнерам
	|					И ВтИсходныеРегистраторы.ОбъектРасчетов = ИсходныеРасчеты.ОбъектРасчетов
	|					И ВтИсходныеРегистраторы.ВалютаРасчетов = ИсходныеРасчеты.Валюта
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|				ПО ИсходныеРасчеты.АналитикаУчетаПоПартнерам = ВтПараметры.ПараметрАналитикаУчетаПоПартнерам
	|					И ИсходныеРасчеты.ОбъектРасчетов = ВтПараметры.ПараметрОбъектРасчетов
	|					И ИсходныеРасчеты.Валюта = ВтПараметры.ПараметрВалютаРасчетов
	|	ГДЕ
	|		НЕ ИсходныеРасчеты.Сторно
	|		И ИсходныеРасчеты.Период < ВтПараметры.ПараметрДатаНачалаПересчета
	|		И НЕ ИсходныеРасчеты.НераспределенныйАванс";
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ВтИсходныеРегистраторыКлиенты","ВтИсходныеРегистраторыПоставщики");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"РегистрНакопления.РасчетыСКлиентамиПланОплат","РегистрНакопления.РасчетыСПоставщикамиПланОплат");
	КонецЕсли;
	
	Движения = Запрос.Выполнить().Выгрузить();
	Возврат Движения;
	
КонецФункции

// Возвращает таблицу движений исходных документов, которые сторнируются в текущем распределении
Функция ДвиженияСторнируемыхДокументовПланОтгрузокДоНачалаРасчета(Запрос, ЭтоРасчетыСКлиентами = Ложь)
	
	Запрос.Текст = "
	|	ВЫБРАТЬ
	|		ИсходныеРасчеты.Период                         КАК Период,
	|		ИсходныеРасчеты.ВидДвижения                    КАК ВидДвижения,
	|		ИсходныеРасчеты.ДокументРегистратор            КАК ДокументРегистратор,
	|		ИсходныеРасчеты.ОбъектРасчетов                 КАК ОбъектРасчетов,
	|		ИсходныеРасчеты.АналитикаУчетаПоПартнерам      КАК АналитикаУчетаПоПартнерам,
	|		ИсходныеРасчеты.Валюта                         КАК ВалютаРасчетов,
	|		ИсходныеРасчеты.ДокументПлан                   КАК ДокументПлан,
	|		ИсходныеРасчеты.ДатаПлановогоПогашения         КАК ДатаПлановогоПогашения,
	|		ИсходныеРасчеты.ДатаВозникновения              КАК ДатаВозникновения,
	|		ИсходныеРасчеты.НераспределенныйАванс          КАК НераспределенныйАванс,
	|		ИсходныеРасчеты.ПорядокОперации                КАК ПорядокОперации,
	|		ЛОЖЬ                                           КАК Сторно,
	|		ИсходныеРасчеты.Сумма                          КАК Сумма,
	|		ИсходныеРасчеты.ДокументОплаты                 КАК ДокументОплаты
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПланОтгрузок КАК ИсходныеРасчеты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтИсходныеРегистраторыКлиенты КАК ВтИсходныеРегистраторы
	|				ПО ВтИсходныеРегистраторы.СторнируемыйРегистратор = ИсходныеРасчеты.ДокументРегистратор
	|					И ВтИсходныеРегистраторы.АналитикаУчетаПоПартнерам = ИсходныеРасчеты.АналитикаУчетаПоПартнерам
	|					И ВтИсходныеРегистраторы.ОбъектРасчетов = ИсходныеРасчеты.ОбъектРасчетов
	|					И ВтИсходныеРегистраторы.ВалютаРасчетов = ИсходныеРасчеты.Валюта
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|				ПО ИсходныеРасчеты.АналитикаУчетаПоПартнерам = ВтПараметры.ПараметрАналитикаУчетаПоПартнерам
	|					И ИсходныеРасчеты.ОбъектРасчетов = ВтПараметры.ПараметрОбъектРасчетов
	|					И ИсходныеРасчеты.Валюта = ВтПараметры.ПараметрВалютаРасчетов
	|	ГДЕ
	|		НЕ ИсходныеРасчеты.Сторно
	|		И ИсходныеРасчеты.Период < ВтПараметры.ПараметрДатаНачалаПересчета
	|		И НЕ ИсходныеРасчеты.НераспределенныйАванс";
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ВтИсходныеРегистраторыКлиенты","ВтИсходныеРегистраторыПоставщики");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"РегистрНакопления.РасчетыСКлиентамиПланОтгрузок","РегистрНакопления.РасчетыСПоставщикамиПланПоставок");
	КонецЕсли;
	
	Движения = Запрос.Выполнить().Выгрузить();
	Возврат Движения;
	
КонецФункции

// Возвращает записи до начала расчета (порядка), количество которых в служебном документе регистраторе меньше размера порции записи
// Эти записи будут перезаписаны. В распределении не участвуют.
Функция ЗаписиДоНачалаРасчетаПоСрокам(Запрос, ГлобальныеПеременные, ЭтоРасчетыСКлиентами = Ложь)
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Таблица.Документ КАК Регистратор
	|ПОМЕСТИТЬ ВтПолныеРегистраторы
	|ИЗ
	|РегистрСведений.КоличествоЗаписейРегистраторовРасчетов КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|			ПО Таблица.АналитикаУчетаПоПартнерам = ВтПараметры.ПараметрАналитикаУчетаПоПартнерам
	|				И Таблица.ОбъектРасчетов = ВтПараметры.ПараметрОбъектРасчетов
	|				И Таблица.Валюта = ВтПараметры.ПараметрВалютаРасчетов
	|				И ВтПараметры.ПараметрЭтоРасчетыСКлиентами = &ЭтоРасчетыСКлиентами
	|				И НЕ ВтПараметры.ПараметрНачальноеЗаполнение
	|ГДЕ
	|	Таблица.ИмяТаблицы = ""РасчетыСКлиентамиПоСрокам""
	|	И Таблица.ПериодЗаписи < ВтПараметры.ПараметрДатаНачалаПересчета
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Документ
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.КоличествоЗаписей) = &РазмерПорцииЗаписи
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Регистраторы.Регистратор КАК Регистратор
	|ИЗ
	|	ВтПолныеРегистраторы КАК Регистраторы
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.ВидДвижения КАК ВидДвижения,
	|	Таблица.Регистратор КАК Регистратор,
	|	Таблица.ДокументРегистратор КАК ДокументРегистратор,
	|	Таблица.ПорядокОперации КАК ПорядокОперации,
	|	Таблица.Сторно КАК Сторно,
	|	ИСТИНА КАК ЗаписьДоНачалаРасчета,
	|	ЗНАЧЕНИЕ(Перечисление.ОперацииВзаиморасчетов.ПустаяСсылка) КАК ОперацияВзаиморасчетов,
	|
	|	Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Таблица.Валюта КАК Валюта,
	|	Таблица.РасчетныйДокумент КАК РасчетныйДокумент,
	|	Таблица.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	Таблица.ДатаВозникновения КАК ДатаВозникновения,
	|	
	|	Таблица.Предоплата КАК Предоплата,
	|	Таблица.ПредоплатаРегл КАК ПредоплатаРегл,
	|	Таблица.ПредоплатаУпр КАК ПредоплатаУпр,
	|	Таблица.Долг КАК Долг,
	|	Таблица.ДолгРегл КАК ДолгРегл,
	|	Таблица.ДолгУпр КАК ДолгУпр,
	|
	|	//Аккумулируют сколько движений сторно уже добавлено
	|	0 КАК Исправлено,
	|	
	|	Таблица.ПорядокЗачета КАК ПорядокЗачета,
	|	Таблица.ВалютаДокумента КАК ВалютаДокумента,
	|	Таблица.СвязанныйДокумент КАК СвязанныйДокумент,
	|	Таблица.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Таблица.ТипЗаписиВзаиморасчетов КАК ТипЗаписиВзаиморасчетов,
	|	Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Таблица.КорАналитикаУчетаПоПартнерам КАК КорАналитикаУчетаПоПартнерам,
	|	Таблица.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|	Таблица.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	Таблица.КорОбъектРасчетов КАК КорОбъектРасчетов,
	|	Таблица.ПоДаннымОбъектаРасчетовИсточника КАК ПоДаннымОбъектаРасчетовИсточника,
	|	Таблица.ОбъектРасчетовПриемник КАК ОбъектРасчетовПриемник,
	|	Таблица.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
	|	Таблица.ВалютаПриемник КАК ВалютаПриемник,
	|	Таблица.СуммаПриемник КАК СуммаПриемник
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПолныеРегистраторы КАК ВтПолныеРегистраторы
	|			ПО ВтПолныеРегистраторы.Регистратор = Таблица.Регистратор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|			ПО Таблица.АналитикаУчетаПоПартнерам = ВтПараметры.ПараметрАналитикаУчетаПоПартнерам
	|				И Таблица.ОбъектРасчетов = ВтПараметры.ПараметрОбъектРасчетов
	|				И Таблица.Валюта = ВтПараметры.ПараметрВалютаРасчетов
	|				И ВтПараметры.ПараметрЭтоРасчетыСКлиентами = &ЭтоРасчетыСКлиентами
	|ГДЕ
	|	ВтПолныеРегистраторы.Регистратор ЕСТЬ NULL // Если количество записей <> порции то они будут перераспределены.
	|	И Таблица.Период < ВтПараметры.ПараметрДатаНачалаПересчета
	|	И Таблица.Активность
	|;
	|УНИЧТОЖИТЬ ВтПолныеРегистраторы";
	
	Если ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ЭтоРасчетыСКлиентами", "ИСТИНА");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РасчетыСКлиентамиПоСрокам", "РасчетыСПоставщикамиПоСрокам");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ЭтоРасчетыСКлиентами", "ЛОЖЬ");
		Запрос.Текст = СтрЗаменить(ЗАпрос.Текст, "ВтИсходныеРегистраторыКлиенты", "ВтИсходныеРегистраторыПоставщики")
	КонецЕсли;
	
	Результаты = Запрос.ВыполнитьПакет();
	ПолныеРегистраторы = Результаты[1].Выгрузить().ВыгрузитьКолонку("Регистратор");
	ЗаписиДоНачалаРасчета = Результаты[2].Выгрузить();
	
	Если ЭтоРасчетыСКлиентами Тогда
		ГлобальныеПеременные.Вставить("РасчетыСКлиентамиПоСрокамРегистраторы", ПолныеРегистраторы);
	Иначе
		ГлобальныеПеременные.Вставить("РасчетыСПоставщикамиПоСрокамРегистраторы", ПолныеРегистраторы);
	КонецЕсли;
	
	Возврат ЗаписиДоНачалаРасчета;
	
КонецФункции

// Возвращает записи до начала расчета (порядка), количество которых в служебном документе регистраторе меньше размера порции записи
Функция ЗаписиДоНачалаРасчетаПланОплат(Запрос, ГлобальныеПеременные, ЭтоРасчетыСКлиентами = Ложь)
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Таблица.Документ КАК Регистратор
	|ПОМЕСТИТЬ ВтПолныеРегистраторы
	|ИЗ
	|РегистрСведений.КоличествоЗаписейРегистраторовРасчетов КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|			ПО Таблица.АналитикаУчетаПоПартнерам = ВтПараметры.ПараметрАналитикаУчетаПоПартнерам
	|				И Таблица.ОбъектРасчетов = ВтПараметры.ПараметрОбъектРасчетов
	|				И Таблица.Валюта = ВтПараметры.ПараметрВалютаРасчетов
	|				И ВтПараметры.ПараметрЭтоРасчетыСКлиентами = &ЭтоРасчетыСКлиентами
	|				И НЕ ВтПараметры.ПараметрНачальноеЗаполнение
	|ГДЕ
	|	Таблица.ИмяТаблицы = ""РасчетыСКлиентамиПланОплат""
	|	И Таблица.ПериодЗаписи < ВтПараметры.ПараметрДатаНачалаПересчета
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Документ
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.КоличествоЗаписей) = &РазмерПорцииЗаписи
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|ВЫБРАТЬ
	|	Регистраторы.Регистратор КАК Регистратор
	|ИЗ
	|	ВтПолныеРегистраторы КАК Регистраторы
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.ВидДвижения КАК ВидДвижения,
	|	Таблица.Регистратор КАК Регистратор,
	|	Таблица.ДокументРегистратор КАК ДокументРегистратор,
	|	Таблица.ПорядокОперации КАК ПорядокОперации,
	|	Таблица.Сторно КАК Сторно,
	|	ИСТИНА КАК ЗаписьДоНачалаРасчета,
	|	
	|	Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Таблица.Валюта КАК Валюта,
	|	Таблица.ДокументПлан КАК ДокументПлан,
	|	Таблица.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	Таблица.ДатаВозникновения КАК ДатаВозникновения,
	|	Таблица.ВариантОплаты КАК ВариантОплаты,
	|	Таблица.НераспределенныйАванс КАК НераспределенныйАванс,
	|	
	|	Таблица.КОплате КАК КОплате,
	|	
	|	Таблица.ДокументОплаты КАК ДокументОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПланОплат КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПолныеРегистраторы КАК ВтПолныеРегистраторы
	|			ПО ВтПолныеРегистраторы.Регистратор = Таблица.Регистратор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|			ПО Таблица.АналитикаУчетаПоПартнерам = ВтПараметры.ПараметрАналитикаУчетаПоПартнерам
	|				И Таблица.ОбъектРасчетов = ВтПараметры.ПараметрОбъектРасчетов
	|				И Таблица.Валюта = ВтПараметры.ПараметрВалютаРасчетов
	|				И Таблица.Период < ВтПараметры.ПараметрДатаНачалаПересчета
	|				И ВтПараметры.ПараметрЭтоРасчетыСКлиентами = &ЭтоРасчетыСКлиентами
	|ГДЕ
	|	ВтПолныеРегистраторы.Регистратор ЕСТЬ NULL // Если количество записей <> порции то они будут перераспределены.
	|	И Таблица.Активность
	|	И Таблица.Регистратор ССЫЛКА Документ.РегистраторРасчетов
	|;
	|УНИЧТОЖИТЬ ВтПолныеРегистраторы";
	
	Если ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ЭтоРасчетыСКлиентами", "ИСТИНА");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РасчетыСКлиентамиПланОплат", "РасчетыСПоставщикамиПланОплат");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ЭтоРасчетыСКлиентами", "ЛОЖЬ");
		Запрос.Текст = СтрЗаменить(ЗАпрос.Текст, "ВтИсходныеРегистраторыКлиенты", "ВтИсходныеРегистраторыПоставщики")
	КонецЕсли;
	
	Результаты = Запрос.ВыполнитьПакет();
	ПолныеРегистраторы = Результаты[1].Выгрузить().ВыгрузитьКолонку("Регистратор");
	ЗаписиДоНачалаРасчета = Результаты[2].Выгрузить();
	
	Если ЭтоРасчетыСКлиентами Тогда
		ГлобальныеПеременные.Вставить("РасчетыСКлиентамиПланОплатРегистраторы", ПолныеРегистраторы);
	Иначе
		ГлобальныеПеременные.Вставить("РасчетыСПоставщикамиПланОплатРегистраторы", ПолныеРегистраторы);
	КонецЕсли;
	
	Возврат ЗаписиДоНачалаРасчета;
	
КонецФункции

// Возвращает записи до начала расчета (порядка), количество которых в служебном документе регистраторе меньше размера порции записи
Функция ЗаписиДоНачалаРасчетаПланОтгрузок(Запрос, ГлобальныеПеременные, ЭтоРасчетыСКлиентами = Ложь)
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Таблица.Документ КАК Регистратор
	|ПОМЕСТИТЬ ВтПолныеРегистраторы
	|ИЗ
	|РегистрСведений.КоличествоЗаписейРегистраторовРасчетов КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|			ПО Таблица.АналитикаУчетаПоПартнерам = ВтПараметры.ПараметрАналитикаУчетаПоПартнерам
	|				И Таблица.ОбъектРасчетов = ВтПараметры.ПараметрОбъектРасчетов
	|				И Таблица.Валюта = ВтПараметры.ПараметрВалютаРасчетов
	|				И ВтПараметры.ПараметрЭтоРасчетыСКлиентами = &ЭтоРасчетыСКлиентами
	|				И НЕ ВтПараметры.ПараметрНачальноеЗаполнение
	|ГДЕ
	|	Таблица.ИмяТаблицы = ""РасчетыСКлиентамиПланОтгрузок""
	|	И Таблица.ПериодЗаписи < ВтПараметры.ПараметрДатаНачалаПересчета
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Документ
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.КоличествоЗаписей) = &РазмерПорцииЗаписи
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Регистраторы.Регистратор КАК Регистратор
	|ИЗ
	|	ВтПолныеРегистраторы КАК Регистраторы
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.ВидДвижения КАК ВидДвижения,
	|	Таблица.Регистратор КАК Регистратор,
	|	Таблица.ДокументРегистратор КАК ДокументРегистратор,
	|	Таблица.ПорядокОперации КАК ПорядокОперации,
	|	Таблица.Сторно КАК Сторно,
	|	ИСТИНА КАК ЗаписьДоНачалаРасчета,
	|	
	|	Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Таблица.Валюта КАК Валюта,
	|	Таблица.ДокументПлан КАК ДокументПлан,
	|	Таблица.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|	Таблица.ДатаВозникновения КАК ДатаВозникновения,
	|	Таблица.НераспределенныйАванс КАК НераспределенныйАванс,
	|	
	|	Таблица.Сумма КАК Сумма,
	|	ИСТИНА КАК УчтеноВОстаткахПлановыхОтгрузок,
	|	
	|	Таблица.ДокументОплаты КАК ДокументОплаты
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПланОтгрузок КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПолныеРегистраторы КАК ВтПолныеРегистраторы
	|			ПО ВтПолныеРегистраторы.Регистратор = Таблица.Регистратор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|			ПО Таблица.АналитикаУчетаПоПартнерам = ВтПараметры.ПараметрАналитикаУчетаПоПартнерам
	|				И Таблица.ОбъектРасчетов = ВтПараметры.ПараметрОбъектРасчетов
	|				И Таблица.Валюта = ВтПараметры.ПараметрВалютаРасчетов
	|				И ВтПараметры.ПараметрЭтоРасчетыСКлиентами = &ЭтоРасчетыСКлиентами
	|ГДЕ
	|	ВтПолныеРегистраторы.Регистратор ЕСТЬ NULL // Если количество записей <> порции то они будут перераспределены.
	|	И Таблица.Период < ВтПараметры.ПараметрДатаНачалаПересчета
	|	И Таблица.Активность
	|;
	|УНИЧТОЖИТЬ ВтПолныеРегистраторы";
	
	Если ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ЭтоРасчетыСКлиентами", "ИСТИНА");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РасчетыСКлиентамиПланОтгрузок", "РасчетыСПоставщикамиПланПоставок");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ЭтоРасчетыСКлиентами", "ЛОЖЬ");
		Запрос.Текст = СтрЗаменить(ЗАпрос.Текст, "ВтИсходныеРегистраторыКлиенты", "ВтИсходныеРегистраторыПоставщики")
	КонецЕсли;
	
	Результаты = Запрос.ВыполнитьПакет();
	ПолныеРегистраторы = Результаты[1].Выгрузить().ВыгрузитьКолонку("Регистратор");
	ЗаписиДоНачалаРасчета = Результаты[2].Выгрузить();
	
	Если ЭтоРасчетыСКлиентами Тогда
		ГлобальныеПеременные.Вставить("РасчетыСКлиентамиПланОтгрузокРегистраторы", ПолныеРегистраторы);
	Иначе
		ГлобальныеПеременные.Вставить("РасчетыСПоставщикамиПланПоставокРегистраторы", ПолныеРегистраторы);
	КонецЕсли;
	
	Возврат ЗаписиДоНачалаРасчета;
	
КонецФункции

Функция ПолучитьДанныеДляРаспределения(Запрос, ЭтоРасчетыСКлиентами)
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ЭтоОплата                         КАК ЭтоОплата,
	|	ВложенныйЗапрос.Период                            КАК Период,
	|	ВложенныйЗапрос.ПорядокОперации                   КАК ПорядокОперации,
	|	ВложенныйЗапрос.ПорядокЗачетаПоДатеПлатежа        КАК ПорядокЗачетаПоДатеПлатежа,
	|	ВложенныйЗапрос.Регистратор                       КАК Регистратор,
	|	
	|	ВложенныйЗапрос.ОбъектРасчетов                    КАК ОбъектРасчетов,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ВалютаРасчетов                    КАК ВалютаРасчетов,
	|	ВложенныйЗапрос.РасчетныйДокумент                 КАК РасчетныйДокумент,
	|	ВложенныйЗапрос.ДатаПлановогоПогашения            КАК ДатаПлановогоПогашения,
	|	ВложенныйЗапрос.ДатаВозникновения                 КАК ДатаВозникновения,
		
	|	ВложенныйЗапрос.ОперацияВзаиморасчетов            КАК ОперацияВзаиморасчетов,
	|	ВложенныйЗапрос.ХозяйственнаяОперация             КАК ХозяйственнаяОперация,
	|	
	|	ВложенныйЗапрос.КорОбъектРасчетов                 КАК КорОбъектРасчетов,
	|	ВложенныйЗапрос.КорАналитикаУчетаПоПартнерам      КАК КорАналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ПоДаннымОбъектаРасчетовИсточника  КАК ПоДаннымОбъектаРасчетовИсточника,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
	|	ВложенныйЗапрос.ОбъектРасчетовПриемник            КАК ОбъектРасчетовПриемник,
	|	ВложенныйЗапрос.ВалютаПриемник                    КАК ВалютаПриемник,
	|	СУММА(ВложенныйЗапрос.СуммаПриемник)                           КАК СуммаПриемник,
	|	
	|	ВложенныйЗапрос.Сторно                                         КАК Сторно,
	|	ВложенныйЗапрос.СторнируемыйРегистратор                        КАК СторнируемыйРегистратор,
	|	ВложенныйЗапрос.НовыеДвиженияИсправления                       КАК НовыеДвиженияИсправления,
	|	
	|	СУММА(ВложенныйЗапрос.Сумма)                                   КАК Сумма,
	|	СУММА(ВложенныйЗапрос.Сумма)                                   КАК СуммаДляРаспределения,
	|	СУММА(ВложенныйЗапрос.Сумма) <> 0                              КАК ЕстьОстатокКРаспределению,
	|	СУММА(ВложенныйЗапрос.Сумма)                                   КАК ИсходнаяСумма,
	|	СУММА(ВложенныйЗапрос.СуммаРегл)                               КАК СуммаРегл,
	|	СУММА(ВложенныйЗапрос.СуммаРегл)                               КАК ИсходнаяСуммаРегл,
	|	СУММА(ВложенныйЗапрос.СуммаУпр)                                КАК СуммаУпр,
	|	СУММА(ВложенныйЗапрос.СуммаУпр)                                КАК ИсходнаяСуммаУпр,
	|	
	|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ВложенныйЗапрос.Регистратор) КАК ИдентификаторРегистратора,
	|	ВложенныйЗапрос.ВалютаДокумента                                   КАК ВалютаДокумента,
	|	ВложенныйЗапрос.СтатьяДвиженияДенежныхСредств                     КАК СтатьяДвиженияДенежныхСредств,
	|	ВложенныйЗапрос.ИдентификаторФинЗаписи                            КАК ИдентификаторФинЗаписи,
	|	ВложенныйЗапрос.НастройкаХозяйственнойОперации                    КАК НастройкаХозяйственнойОперации,
	|	ВложенныйЗапрос.ДатаПереоценки                                    КАК ДатаПереоценки,
	|	ВложенныйЗапрос.СвязанныйДокумент                                 КАК СвязанныйДокумент,
	|	
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, ДЕНЬ) >= ВтПараметрыРасчета.ПараметрДатаНачалаПересчета
	|			ТОГДА ВложенныйЗапрос.Период
	|		ИНАЧЕ ВтПараметрыРасчета.ПараметрДатаНачалаПересчета
	|	КОНЕЦ                                                                               КАК ПериодЗачета,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, ДЕНЬ) >= ВтПараметрыРасчета.ПараметрДатаНачалаПересчета
	|			ТОГДА ВложенныйЗапрос.ПорядокОперации
	|		ИНАЧЕ ВтПараметрыРасчета.ПараметрПорядок
	|	КОНЕЦ                                                                               КАК ПорядокЗачета,
	|	
	|	ВложенныйЗапрос.РучнойКурсРегл                                                      КАК РучнойКурсРегл,
	|	ВложенныйЗапрос.РучнойКурсУпр                                                       КАК РучнойКурсУпр,
	|	
	|	ВЫБОР
	|		КОГДА ВтПараметрыРасчета.ПараметрЭтоРасчетыСКлиентами И НЕ ВложенныйЗапрос.ЭтоОплата
	|			 	И (ВложенныйЗапрос.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств
	|					ИЛИ ВложенныйЗапрос.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер)
	|			ИЛИ НЕ ВтПараметрыРасчета.ПараметрЭтоРасчетыСКлиентами И НЕ ВложенныйЗапрос.ЭтоОплата
	|				И (ВложенныйЗапрос.Регистратор ССЫЛКА Документ.ПоступлениеБезналичныхДенежныхСредств
	|					ИЛИ ВложенныйЗапрос.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                                               КАК ЭтоВозвратАванса
	|ИЗ (
	|	//Отгрузки и оплаты без объекта расчетов источника
	|	ВЫБРАТЬ
	|		Расчеты.ЭтоОплата                                   КАК ЭтоОплата,
	|		Расчеты.Регистратор                                 КАК Регистратор,
	|		//Ввод остатков пишет в дату регистратора дату первичного документа, а движения должны делаться датой ввода остатков
	|		ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) В (ТИП(Документ.ВводОстатков),ТИП(Документ.ВводОстатковВзаиморасчетов),ТИП(Документ.АктПремииКлиенту),ТИП(Документ.АктПремииПоставщика))
	|				ТОГДА Расчеты.Период
	|			ИНАЧЕ Расчеты.ДатаРегистратора
	|		КОНЕЦ                                               КАК Период,
	|		Расчеты.ПорядокОперации                             КАК ПорядокОперации,
	|		Расчеты.ПорядокЗачетаПоДатеПлатежа                  КАК ПорядокЗачетаПоДатеПлатежа,
	|		
	|		Расчеты.АналитикаУчетаПоПартнерам                   КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетов                              КАК ОбъектРасчетов,
	|		Расчеты.Валюта                                      КАК ВалютаРасчетов,
	|		ВЫБОР 
	|			КОГДА Расчеты.РасчетныйДокумент <> Неопределено И Расчеты.РасчетныйДокумент <> ЗНАЧЕНИЕ(Документ.ПервичныйДокумент.ПустаяСсылка)
	|				ТОГДА Расчеты.РасчетныйДокумент
	|			КОГДА НЕ ВтИсходныеРегистраторы.ИсправляемыйДокумент ЕСТЬ NULL И НЕ Расчеты.Сторно
	|				ТОГДА ВтИсходныеРегистраторы.ИсправляемыйДокумент 
	|			ИНАЧЕ Расчеты.Регистратор
	|		КОНЕЦ                                               КАК РасчетныйДокумент,
	|		НАЧАЛОПЕРИОДА(ВЫБОР
	|			КОГДА Расчеты.ДатаПлатежа = ДАТАВРЕМЯ(1,1,1) И НЕ Расчеты.ЭтоОплата
	|				ТОГДА Расчеты.Период
	|			ИНАЧЕ Расчеты.ДатаПлатежа
	|		КОНЕЦ , ДЕНЬ)                                       КАК ДатаПлановогоПогашения,
	|		ВЫБОР 
	|			КОГДА НЕ ВтИсходныеРегистраторы.ИсправляемыйДокумент ЕСТЬ NULL И НЕ Расчеты.Сторно
	|					И НЕ ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) В (ТИП(Документ.ВводОстатков),ТИП(Документ.ВводОстатковВзаиморасчетов))
	|				ТОГДА ВтИсходныеРегистраторы.ИсходнаяДатаВозникновения
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(Расчеты.ДатаРегистратора, ДЕНЬ)
	|		КОНЕЦ                                               КАК ДатаВозникновения,
	|		
	|		Расчеты.Сумма                                       КАК Сумма,
	|		Расчеты.СуммаРегл                                   КАК СуммаРегл,
	|		Расчеты.СуммаУпр                                    КАК СуммаУпр,
	|		
	|		Расчеты.КорОбъектРасчетов                           КАК КорОбъектРасчетов,
	|		Расчеты.КорАналитикаУчетаПоПартнерам                КАК КорАналитикаУчетаПоПартнерам,
	|		Расчеты.ПоДаннымОбъектаРасчетовИсточника            КАК ПоДаннымОбъектаРасчетовИсточника,
	|		Расчеты.АналитикаУчетаПоПартнерамПриемник           КАК АналитикаУчетаПоПартнерамПриемник,
	|		Расчеты.ОбъектРасчетовПриемник                      КАК ОбъектРасчетовПриемник,
	|		Расчеты.ВалютаПриемник                              КАК ВалютаПриемник,
	|		Расчеты.СуммаПриемник                               КАК СуммаПриемник,
	|		
	|		Расчеты.Сторно                                                              КАК Сторно,
	|		НЕ Расчеты.Сторно И НЕ ВтИсходныеРегистраторы.СторнируемыйРегистратор ЕСТЬ NULL КАК НовыеДвиженияИсправления,
	|		ЕСТЬNULL(ВтИсходныеРегистраторы.СторнируемыйРегистратор, Неопределено)          КАК СторнируемыйРегистратор,
	|		
	|		Расчеты.ОперацияВзаиморасчетов                      КАК ОперацияВзаиморасчетов,
	|		Расчеты.ХозяйственнаяОперация                       КАК ХозяйственнаяОперация,
	|		Расчеты.НастройкаХозяйственнойОперации              КАК НастройкаХозяйственнойОперации,
	|		Расчеты.ИдентификаторФинЗаписи                      КАК ИдентификаторФинЗаписи,
	|		Расчеты.ВалютаДокумента                             КАК ВалютаДокумента,
	|		Расчеты.СтатьяДвиженияДенежныхСредств               КАК СтатьяДвиженияДенежныхСредств,
	|		Расчеты.СвязанныйДокумент                           КАК СвязанныйДокумент,
	|		Расчеты.РучнойКурсРегл 
	|			ИЛИ (Расчеты.ХозяйственнаяОперация В (&ОперацииСРучнымКурсом) И НЕ Расчеты.ЭтоОплата) КАК РучнойКурсРегл,
	|		Расчеты.РучнойКурсУпр 
	|			ИЛИ (Расчеты.ХозяйственнаяОперация В (&ОперацииСРучнымКурсом) И НЕ Расчеты.ЭтоОплата) КАК РучнойКурсУпр,
	|		ВЫБОР КОГДА (Расчеты.Регистратор ССЫЛКА Документ.ВводОстатков
	|						ИЛИ Расчеты.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов)
	|				ТОГДА Расчеты.Период
	|			ИНАЧЕ Расчеты.ДатаРегистратора
	|		КОНЕЦ                                              КАК ДатаПереоценки
	|	ИЗ
	|		РасчетыДвиженияКлиенты КАК Расчеты
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВтИсходныеРегистраторыКлиенты КАК ВтИсходныеРегистраторы
	|				ПО ВтИсходныеРегистраторы.НовыйРегистратор = Расчеты.Регистратор
	|					И ВтИсходныеРегистраторы.АналитикаУчетаПоПартнерам = Расчеты.АналитикаУчетаПоПартнерам
	|					И ВтИсходныеРегистраторы.ОбъектРасчетов = Расчеты.ОбъектРасчетов
	|					И ВтИсходныеРегистраторы.ВалютаРасчетов = Расчеты.Валюта
	|	ГДЕ
	|		НЕ Расчеты.ПоДаннымОбъектаРасчетовИсточника
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	//Движения из объекта расчетов источника ""Расчеты с клиентами по срокам""
	|	ВЫБРАТЬ
	|		ВтТипыПриемников.ПриемникЭтоОплата                       КАК ЭтоОплата,
	|		Расчеты.ДокументРегистратор                              КАК Регистратор,
	|		Расчеты.Период                                           КАК Период,
	|		Расчеты.ПорядокОперации                                  КАК ПорядокОперации,
	|		Расчеты.ПорядокЗачета                                    КАК ПорядокЗачетаПоДатеПлатежа,
	|		
	|		Расчеты.АналитикаУчетаПоПартнерамПриемник                КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетовПриемник                           КАК ОбъектРасчетов,
	|		Расчеты.ВалютаПриемник                                   КАК ВалютаРасчетов,
	|		Расчеты.РасчетныйДокумент                                КАК РасчетныйДокумент,
	|		ВЫБОР КОГДА Расчеты.ДатаПлановогоПогашения = ДАТАВРЕМЯ(1,1,1) И НЕ ВтТипыПриемников.ПриемникЭтоОплата
	|			ТОГДА Расчеты.ДатаВозникновения
	|			ИНАЧЕ Расчеты.ДатаПлановогоПогашения
	|		КОНЕЦ                                                    КАК ДатаПлановогоПогашения,
	|		Расчеты.ДатаВозникновения                                КАК ДатаВозникновения,
	|		
	|		Расчеты.СуммаПриемник                                    КАК Сумма,
	|		Расчеты.ДолгРегл + Расчеты.ПредоплатаРегл                КАК СуммаРегл,
	|		Расчеты.ДолгУпр + Расчеты.ПредоплатаУпр                  КАК СуммаУпр,
	|		
	|		Расчеты.ОбъектРасчетов                                   КАК КорОбъектРасчетов,
	|		Расчеты.АналитикаУчетаПоПартнерам                        КАК КорАналитикаУчетаПоПартнерам,
	|		ИСТИНА                                                   КАК ПоДаннымОбъектаРасчетовИсточника,
	|		ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) КАК АналитикаУчетаПоПартнерамПриемник,
	|		ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)        КАК ОбъектРасчетовПриемник,
	|		ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)                 КАК ВалютаПриемник,
	|		0                                                        КАК СуммаПриемник,
	|		
	|		Расчеты.Сторно                                                     КАК Сторно,
	|		НЕ Расчеты.Сторно И НЕ ИсправленияДокументов.Регистратор ЕСТЬ NULL КАК НовыеДвиженияИсправления,
	|		Неопределено                                                       КАК СторнируемыйРегистратор,
	|		
	|		ЗНАЧЕНИЕ(Перечисление.ОперацииВзаиморасчетов.ПереносВзаиморасчетов) КАК ОперацияВзаиморасчетов,
	|		Расчеты.ХозяйственнаяОперация                            КАК ХозяйственнаяОперация,
	|		Расчеты.НастройкаХозяйственнойОперации                   КАК НастройкаХозяйственнойОперации,
	|		Расчеты.ИдентификаторФинЗаписи                           КАК ИдентификаторФинЗаписи,
	|		Расчеты.ВалютаДокумента                                  КАК ВалютаДокумента,
	|		Расчеты.СтатьяДвиженияДенежныхСредств                    КАК СтатьяДвиженияДенежныхСредств,
	|		НЕОПРЕДЕЛЕНО                                             КАК СвязанныйДокумент,
	|		ИСТИНА                                                   КАК РучнойКурсРегл,
	|		ИСТИНА                                                   КАК РучнойКурсУпр,
	|		ВЫБОР КОГДА ВтТипыПриемников.ПриемникЭтоОплата 
	|				ТОГДА ДАТАВРЕМЯ(1,1,1)
	|			ИНАЧЕ Расчеты.Период
	|		КОНЕЦ                                                    КАК ДатаПереоценки
	|	ИЗ
	|		ВТРасчетыСКлиентамиПоСрокамРегистраторы КАК ВТРасчетыСКлиентамиПоСрокам
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметрыРасчета
	|					ПО Расчеты.ОбъектРасчетовПриемник = ВтПараметрыРасчета.ПараметрОбъектРасчетов
	|						И Расчеты.АналитикаУчетаПоПартнерамПриемник = ВтПараметрыРасчета.ПараметрАналитикаУчетаПоПартнерам
	|						И Расчеты.ВалютаПриемник = ВтПараметрыРасчета.ПараметрВалютаРасчетов
	|						И Расчеты.Период >= ВтПараметрыРасчета.ПараметрДатаНачалаПересчета
	|			ПО ВТРасчетыСКлиентамиПоСрокам.Период = Расчеты.Период
	|				И ВТРасчетыСКлиентамиПоСрокам.Регистратор = Расчеты.Регистратор
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТипыПриемников КАК ВтТипыПриемников
	|			ПО Расчеты.ДокументРегистратор = ВтТипыПриемников.Регистратор
	|				И Расчеты.ОбъектРасчетовПриемник = ВтТипыПриемников.ОбъектРасчетов
	|				И Расчеты.АналитикаУчетаПоПартнерамПриемник = ВтТипыПриемников.АналитикаУчетаПоПартнерам
	|				И Расчеты.ВалютаПриемник = ВтТипыПриемников.Валюта
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсправленияДокументов КАК ИсправленияДокументов
	|				ПО ИсправленияДокументов.Регистратор = Расчеты.ДокументРегистратор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	//Движения из объекта расчетов источника ""Расчеты с поставщиками по срокам""
	|	ВЫБРАТЬ
	|		ВтТипыПриемников.ПриемникЭтоОплата                       КАК ЭтоОплата,
	|		Расчеты.ДокументРегистратор                              КАК Регистратор,
	|		Расчеты.Период                                           КАК Период,
	|		Расчеты.ПорядокОперации                                  КАК ПорядокОперации,
	|		Расчеты.ПорядокЗачета                                    КАК ПорядокЗачетаПоДатеПлатежа,
	|		
	|		Расчеты.АналитикаУчетаПоПартнерамПриемник                КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетовПриемник                           КАК ОбъектРасчетов,
	|		Расчеты.ВалютаПриемник                                   КАК ВалютаРасчетов,
	|		Расчеты.РасчетныйДокумент                                КАК РасчетныйДокумент,
	|		ВЫБОР КОГДА Расчеты.ДатаПлановогоПогашения = ДАТАВРЕМЯ(1,1,1) И НЕ ВтТипыПриемников.ПриемникЭтоОплата 
	|			ТОГДА Расчеты.ДатаВозникновения
	|			ИНАЧЕ Расчеты.ДатаПлановогоПогашения
	|		КОНЕЦ                                                    КАК ДатаПлановогоПогашения,
	|		Расчеты.ДатаВозникновения                                КАК ДатаВозникновения,
	|		
	|		Расчеты.СуммаПриемник                                    КАК Сумма,
	|		Расчеты.ДолгРегл + Расчеты.ПредоплатаРегл                КАК СуммаРегл,
	|		Расчеты.ДолгУпр + Расчеты.ПредоплатаУпр                  КАК СуммаУпр,
	|		
	|		Расчеты.ОбъектРасчетов                                   КАК КорОбъектРасчетов,
	|		Расчеты.АналитикаУчетаПоПартнерам                        КАК КорАналитикаУчетаПоПартнерам,
	|		ИСТИНА                                                   КАК ПоДаннымОбъектаРасчетовИсточника,
	|		ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) КАК АналитикаУчетаПоПартнерамПриемник,
	|		ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)        КАК ОбъектРасчетовПриемник,
	|		ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)                 КАК ВалютаПриемник,
	|		0                                                        КАК СуммаПриемник,
	|	
	|		Расчеты.Сторно                                                     КАК Сторно,
	|		НЕ Расчеты.Сторно И НЕ ИсправленияДокументов.Регистратор ЕСТЬ NULL КАК НовыеДвиженияИсправления,
	|		Неопределено                                                       КАК СторнируемыйРегистратор,
	|	
	|		ВЫБОР
	|			КОГДА ВтТипыПриемников.ПриемникЭтоОплата 
	|				И Расчеты.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.БронированиеЧерезАгента),
	|													ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.БронированиеУПоставщика))
	|				 ТОГДА ЗНАЧЕНИЕ(Перечисление.ОперацииВзаиморасчетов.КорректировкаЗадолженности)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ОперацииВзаиморасчетов.ПереносВзаиморасчетов) 
	|		КОНЕЦ                                                    КАК ОперацияВзаиморасчетов,
	|		Расчеты.ХозяйственнаяОперация                            КАК ХозяйственнаяОперация,
	|		Расчеты.НастройкаХозяйственнойОперации                   КАК НастройкаХозяйственнойОперации,
	|		Расчеты.ИдентификаторФинЗаписи                           КАК ИдентификаторФинЗаписи,
	|		Расчеты.ВалютаДокумента                                  КАК ВалютаДокумента,
	|		Расчеты.СтатьяДвиженияДенежныхСредств                    КАК СтатьяДвиженияДенежныхСредств,
	|		НЕОПРЕДЕЛЕНО                                             КАК СвязанныйДокумент,
	|		ИСТИНА                                                   КАК РучнойКурсРегл,
	|		ИСТИНА                                                   КАК РучнойКурсУпр,
	|		ВЫБОР КОГДА ВтТипыПриемников.ПриемникЭтоОплата
	|			ТОГДА ДАТАВРЕМЯ(1,1,1)
	|			ИНАЧЕ Расчеты.Период
	|		КОНЕЦ                                                    КАК ДатаПереоценки
	|	ИЗ
	|		ВТРасчетыСПоставщикамиПоСрокамРегистраторы КАК ВТРасчетыСПоставщикамиПоСрокам
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты 
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметрыРасчета
	|					ПО Расчеты.ОбъектРасчетовПриемник = ВтПараметрыРасчета.ПараметрОбъектРасчетов
	|						И Расчеты.АналитикаУчетаПоПартнерамПриемник = ВтПараметрыРасчета.ПараметрАналитикаУчетаПоПартнерам
	|						И Расчеты.ВалютаПриемник = ВтПараметрыРасчета.ПараметрВалютаРасчетов
	|						И Расчеты.Период >= ВтПараметрыРасчета.ПараметрДатаНачалаПересчета
	|			ПО ВТРасчетыСПоставщикамиПоСрокам.Период = Расчеты.Период
	|				И ВТРасчетыСПоставщикамиПоСрокам.Регистратор = Расчеты.Регистратор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТипыПриемников КАК ВтТипыПриемников
	|			ПО Расчеты.ДокументРегистратор = ВтТипыПриемников.Регистратор
	|				И Расчеты.ОбъектРасчетовПриемник = ВтТипыПриемников.ОбъектРасчетов
	|				И Расчеты.АналитикаУчетаПоПартнерамПриемник = ВтТипыПриемников.АналитикаУчетаПоПартнерам
	|				И Расчеты.ВалютаПриемник = ВтТипыПриемников.Валюта
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсправленияДокументов КАК ИсправленияДокументов
	|				ПО ИсправленияДокументов.Регистратор = Расчеты.ДокументРегистратор
	|	) КАК ВложенныйЗапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметрыРасчета
	|			ПО ВложенныйЗапрос.ОбъектРасчетов = ВтПараметрыРасчета.ПараметрОбъектРасчетов
	|				И ВложенныйЗапрос.АналитикаУчетаПоПартнерам = ВтПараметрыРасчета.ПараметрАналитикаУчетаПоПартнерам
	|				И ВложенныйЗапрос.ВалютаРасчетов = ВтПараметрыРасчета.ПараметрВалютаРасчетов
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ЭтоОплата,
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.ОбъектРасчетов,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ВалютаРасчетов,
	|	ВложенныйЗапрос.ДатаПлановогоПогашения,
	|	ВложенныйЗапрос.ДатаВозникновения,
	|	ВложенныйЗапрос.ПорядокОперации,
	|	ВложенныйЗапрос.ПорядокЗачетаПоДатеПлатежа,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.РасчетныйДокумент,
	|	ВложенныйЗапрос.ОперацияВзаиморасчетов,
	|	ВложенныйЗапрос.ХозяйственнаяОперация,
	|	ВложенныйЗапрос.КорОбъектРасчетов,
	|	ВложенныйЗапрос.КорАналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ПоДаннымОбъектаРасчетовИсточника,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерамПриемник,
	|	ВложенныйЗапрос.ОбъектРасчетовПриемник,
	|	ВложенныйЗапрос.ВалютаПриемник,
	|	ВложенныйЗапрос.Сторно,
	|	ВложенныйЗапрос.СторнируемыйРегистратор,
	|	ВложенныйЗапрос.НовыеДвиженияИсправления,
	|	ВложенныйЗапрос.ВалютаДокумента,
	|	ВложенныйЗапрос.СтатьяДвиженияДенежныхСредств,
	|	ВложенныйЗапрос.ИдентификаторФинЗаписи,
	|	ВложенныйЗапрос.НастройкаХозяйственнойОперации,
	|	ВложенныйЗапрос.ДатаПереоценки,
	|	ВложенныйЗапрос.СвязанныйДокумент,
	|	ВложенныйЗапрос.РучнойКурсРегл,
	|	ВложенныйЗапрос.РучнойКурсУпр,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, ДЕНЬ) >= ВтПараметрыРасчета.ПараметрДатаНачалаПересчета
	|			ТОГДА ВложенныйЗапрос.Период
	|		ИНАЧЕ ВтПараметрыРасчета.ПараметрДатаНачалаПересчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, ДЕНЬ) >= ВтПараметрыРасчета.ПараметрДатаНачалаПересчета
	|			ТОГДА ВложенныйЗапрос.ПорядокОперации
	|		ИНАЧЕ ВтПараметрыРасчета.ПараметрПорядок
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВтПараметрыРасчета.ПараметрЭтоРасчетыСКлиентами И НЕ ВложенныйЗапрос.ЭтоОплата
	|			 	И (ВложенныйЗапрос.Регистратор ССЫЛКА Документ.СписаниеБезналичныхДенежныхСредств
	|					ИЛИ ВложенныйЗапрос.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер)
	|			ИЛИ НЕ ВтПараметрыРасчета.ПараметрЭтоРасчетыСКлиентами И НЕ ВложенныйЗапрос.ЭтоОплата
	|				И (ВложенныйЗапрос.Регистратор ССЫЛКА Документ.ПоступлениеБезналичныхДенежныхСредств
	|					ИЛИ ВложенныйЗапрос.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Сумма) > 0
	|УПОРЯДОЧИТЬ ПО
	|	ВложенныйЗапрос.ПорядокОперации ВОЗР,
	|	ВложенныйЗапрос.Сторно УБЫВ,
	|	ВложенныйЗапрос.ДатаВозникновения ВОЗР,
	|	//Сначала распределяем с заполненной датой погашения, потом с ""Автоматической""
	|	ВЫБОР КОГДА ВложенныйЗапрос.ДатаПлановогоПогашения = ДАТАВРЕМЯ(1,1,1)
	|		ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ВложенныйЗапрос.ДатаПлановогоПогашения,
	|	СУММА(ВложенныйЗапрос.Сумма)
	|";
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"РасчетыДвиженияКлиенты","РасчетыДвиженияПоставщики");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ВтИсходныеРегистраторыКлиенты","ВтИсходныеРегистраторыПоставщики");
	КонецЕсли;
	
	Результат     = Запрос.Выполнить();
	Данные = Результат.Выгрузить();
	
	Возврат Данные;
	
КонецФункции

// Возвращает таблицу курсов валют на промежуток между первой записью расчетов и текущей датой.
Функция ПолучитьКурсыВалют(Запрос, ДатаНачала, ГлобальныеПеременные)
	
	Запрос.УстановитьПараметр("ДатаНачала",                 ДатаНачала);
	Запрос.УстановитьПараметр("ВалютаРасчетов",             ГлобальныеПеременные.ВалютаРасчетов);
	Запрос.УстановитьПараметр("ВалютаУпр",                  ГлобальныеПеременные.ВалютаУправленческогоУчета);
	Запрос.УстановитьПараметр("ВалютаРегл",                 ГлобальныеПеременные.ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("Договор",                    ГлобальныеПеременные.Договор);
	Запрос.УстановитьПараметр("КурсУстановленныйВДоговоре", ГлобальныеПеременные.ВариантКурсаДоговора = Перечисления.ВариантыКурсаДоговора.УстановленныйВДоговоре);
	
	Если ГлобальныеПеременные.ВариантКурсаДоговора = Перечисления.ВариантыКурсаДоговора.УстановленныйВДоговоре Тогда
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	Курсы.Дата КАК Дата,
		|	СУММА(Курсы.КурсРегл) КАК КурсРегл,
		|	СУММА(Курсы.КурсУпр) КАК КурсУпр
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаДат.Дата КАК Дата,
		|		ВЫБОР КОГДА &ВалютаРасчетов <> &ВалютаРегл
		|				ТОГДА ЕСТЬNULL(КурсыВалютыРасчетов.КурсЧислитель / КурсыВалютыРасчетов.КурсЗнаменатель,0)
		|			ИНАЧЕ 1
		|		КОНЕЦ КАК КурсРегл,
		|		ВЫБОР КОГДА &ВалютаРасчетов = &ВалютаУпр
		|				ТОГДА ЕСТЬNULL(КурсыВалютыРасчетов.КурсЧислитель / КурсыВалютыРасчетов.КурсЗнаменатель,0)
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК КурсУпр
		|	ИЗ
		|		ТаблицаДат КАК ТаблицаДат
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютРасчетовПоДоговорам КАК КурсыВалютыРасчетов
		|				ПО ТаблицаДат.Дата = КурсыВалютыРасчетов.Период
		|					И КурсыВалютыРасчетов.Договор = &Договор
		|	
		|	ОБЪЕДИНИТЬ ВСЕ 
		|	
		|	ВЫБРАТЬ
		|		ТаблицаДат.Дата КАК Дата,
		|		0 КАК КурсРегл, 
		|		ВЫБОР КОГДА &ВалютаРасчетов = &ВалютаУпр
		|			ТОГДА 0
		|		ИНАЧЕ
		|			ВЫБОР КОГДА &ВалютаРегл <> &ВалютаУпр
		|					ТОГДА ЕСТЬNULL(КурсыВалютыУпр.КурсЧислитель / КурсыВалютыУпр.КурсЗнаменатель,0)
		|				ИНАЧЕ 1
		|			КОНЕЦ 
		|		КОНЕЦ КАК КурсУпр
		|	ИЗ
		|		ТаблицаДат КАК ТаблицаДат
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютыУпр
		|				ПО ТаблицаДат.Дата = КурсыВалютыУпр.Период
		|					И КурсыВалютыУпр.Валюта = &ВалютаУпр
		|					И КурсыВалютыУпр.БазоваяВалюта = &ВалютаРегл) КАК Курсы
		|
		|СГРУППИРОВАТЬ ПО
		|	Курсы.Дата
		|УПОРЯДОЧИТЬ ПО
		|	Дата ВОЗР
		|";
	Иначе
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	Курсы.Дата КАК Дата,
		|	СУММА(Курсы.КурсРегл) КАК КурсРегл,
		|	СУММА(Курсы.КурсУпр) КАК КурсУпр
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаДат.Дата КАК Дата,
		|		ЕСТЬNULL(ВЫБОР КОГДА КурсыВалютыРасчетов.Валюта = &ВалютаРасчетов И &ВалютаРасчетов <> &ВалютаРегл
		|					ТОГДА КурсыВалютыРасчетов.КурсЧислитель / КурсыВалютыРасчетов.КурсЗнаменатель
		|					ИНАЧЕ 0
		|				КОНЕЦ,
		|				ВЫБОР КОГДА &ВалютаРегл = &ВалютаРасчетов
		|					ТОГДА 1
		|					ИНАЧЕ 0
		|				КОНЕЦ) КАК КурсРегл,
		|		ЕСТЬNULL(ВЫБОР КОГДА КурсыВалютыРасчетов.Валюта = &ВалютаУпр И &ВалютаРегл <> &ВалютаУпр
		|					ТОГДА КурсыВалютыРасчетов.КурсЧислитель / КурсыВалютыРасчетов.КурсЗнаменатель
		|					ИНАЧЕ 0
		|				КОНЕЦ,
		|				ВЫБОР КОГДА &ВалютаРегл = &ВалютаУпр
		|						ТОГДА 1
		|					ИНАЧЕ 0
		|				КОНЕЦ) КАК КурсУпр
		|	ИЗ
		|		ТаблицаДат КАК ТаблицаДат
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалютыРасчетов
		|				ПО ТаблицаДат.Дата = КурсыВалютыРасчетов.Период
		|					И КурсыВалютыРасчетов.БазоваяВалюта = &ВалютаРегл
		|					И КурсыВалютыРасчетов.Валюта В (&ВалютаРасчетов,&ВалютаУпр)) КАК Курсы
		|
		|СГРУППИРОВАТЬ ПО
		|	Курсы.Дата
		|УПОРЯДОЧИТЬ ПО
		|	Дата ВОЗР
		|";
	КонецЕсли;
	
	КурсыВалюты =  Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СУММА(Курсы.КурсРегл) КАК КурсРегл,
	|	СУММА(Курсы.КурсУпр) КАК КурсУпр
	|ИЗ
	|	(ВЫБРАТЬ
	|		КурсыВалютыРасчетов.Период КАК Дата,
	|		ВЫБОР КОГДА НЕ &КурсУстановленныйВДоговоре И КурсыВалютыРасчетов.Валюта = &ВалютаРасчетов 
	|			ТОГДА КурсыВалютыРасчетов.КурсЧислитель / КурсыВалютыРасчетов.КурсЗнаменатель
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК КурсРегл,
	|		ВЫБОР КОГДА НЕ (&КурсУстановленныйВДоговоре И &ВалютаУпр = &ВалютаРасчетов) И КурсыВалютыРасчетов.Валюта = &ВалютаУпр 
	|			ТОГДА КурсыВалютыРасчетов.КурсЧислитель / КурсыВалютыРасчетов.КурсЗнаменатель
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК КурсУпр
	|	ИЗ
	|		РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&ДатаНачала, БазоваяВалюта = &ВалютаРегл И Валюта В (&ВалютаРасчетов,&ВалютаУпр)) КАК КурсыВалютыРасчетов
	|	
	|	ОБЪЕДИНИТЬ ВСЕ 
	|	
	|	ВЫБРАТЬ
	|		КурсыВалютыРасчетов.Период КАК Дата,
	|		КурсыВалютыРасчетов.КурсЧислитель / КурсыВалютыРасчетов.КурсЗнаменатель  КАК КурсРегл,
	|		ВЫБОР КОГДА &КурсУстановленныйВДоговоре И &ВалютаУпр = &ВалютаРасчетов
	|			ТОГДА КурсыВалютыРасчетов.КурсЧислитель / КурсыВалютыРасчетов.КурсЗнаменатель
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК КурсУпр
	|	ИЗ
	|		РегистрСведений.КурсыВалютРасчетовПоДоговорам.СрезПоследних(&ДатаНачала, Договор = &Договор) КАК КурсыВалютыРасчетов
	|	ГДЕ &КурсУстановленныйВДоговоре) КАК Курсы
	|;";
	
	КурсыВалютСрезПоследних = Запрос.Выполнить().Выгрузить();
	
	ТекущийКурсРегл = ?(КурсыВалютСрезПоследних.Количество() > 0, КурсыВалютСрезПоследних[0].КурсРегл, 1);
	ТекущийКурсУпр = ?(КурсыВалютСрезПоследних.Количество() > 0, КурсыВалютСрезПоследних[0].КурсУпр, 1);
	
	ТекущийКурсРегл = ?(ЗначениеЗаполнено(ТекущийКурсРегл), ТекущийКурсРегл, 1);
	ТекущийКурсУпр =  ?(ЗначениеЗаполнено(ТекущийКурсУпр), ТекущийКурсУпр, 1);
	
	СтрокиСПустымКурсомРегл = КурсыВалюты.НайтиСтроки(Новый Структура("КурсРегл", 0));
	Для Каждого СтрокаПустогоКурса ИЗ СтрокиСПустымКурсомРегл Цикл
		Если КурсыВалюты.Индекс(СтрокаПустогоКурса) = 0 Тогда 
			СтрокаПустогоКурса.КурсРегл = ТекущийКурсРегл;
			Если НЕ ЗначениеЗаполнено(СтрокаПустогоКурса.КурсУпр) Тогда
				СтрокаПустогоКурса.КурсУпр = ТекущийКурсУпр;
			КонецЕсли;
		Иначе
			СтрокаПустогоКурса.КурсРегл = КурсыВалюты[КурсыВалюты.Индекс(СтрокаПустогоКурса)-1].КурсРегл;
			Если НЕ ЗначениеЗаполнено(СтрокаПустогоКурса.КурсУпр) Тогда
				СтрокаПустогоКурса.КурсУпр = КурсыВалюты[КурсыВалюты.Индекс(СтрокаПустогоКурса)-1].КурсУпр;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СтрокиСПустымКурсомУпр = КурсыВалюты.НайтиСтроки(Новый Структура("КурсУпр", 0));
	Для Каждого СтрокаПустогоКурса ИЗ СтрокиСПустымКурсомУпр Цикл
		Если КурсыВалюты.Индекс(СтрокаПустогоКурса) = 0 Тогда 
			СтрокаПустогоКурса.КурсУпр = ТекущийКурсУпр;
		Иначе
			СтрокаПустогоКурса.КурсУпр = КурсыВалюты[КурсыВалюты.Индекс(СтрокаПустогоКурса)-1].КурсУпр;
		КонецЕсли;
	КонецЦикла;
	
	КурсыВалюты.Индексы.Добавить("Дата");
	
	Возврат КурсыВалюты;
	
КонецФункции

// Возвращает таблицу накладных и зачтенных на них, на момент пересчета, авансов.
//Данные используются для определения средневзвешенного курса накладной.
Функция ПолучитьДанныеНакладных(Запрос, ЭтоРасчетыСКлиентами)
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Таблица.Валюта КАК ВалютаРасчетов,
	|	Таблица.СвязанныйДокумент КАК СвязанныйДокумент
	|ПОМЕСТИТЬ ТаблицаСвязанныеДокументы
	|ИЗ
	|	РасчетыДвиженияКлиенты КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтПараметры.ПараметрТипРасчетов КАК ТипРасчетов,
	|	ВтПараметры.ПараметрАналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВтПараметры.ПараметрОбъектРасчетов КАК ОбъектРасчетов,
	|	ВтПараметры.ПараметрВалютаРасчетов КАК ВалютаРасчетов,
	|	ВтПараметры.ПараметрДатаНачалаПересчета КАК ДатаНачалаПересчета,
	|	ВтПараметры.ПараметрРегистратор КАК Регистратор,
	|	ТаблицаСвязанныеДокументы.СвязанныйДокумент КАК СвязанныйДокумент
	|ПОМЕСТИТЬ ВТСвязанныеДокументыКлиент
	|ИЗ
	|	ВтДополненныеПараметрыРаспределения КАК ВтПараметры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаСвязанныеДокументы КАК ТаблицаСвязанныеДокументы
	|		ПО ВтПараметры.ПараметрАналитикаУчетаПоПартнерам = ТаблицаСвязанныеДокументы.АналитикаУчетаПоПартнерам
	|		И ВтПараметры.ПараметрОбъектРасчетов = ТаблицаСвязанныеДокументы.ОбъектРасчетов
	|		И ВтПараметры.ПараметрВалютаРасчетов = ТаблицаСвязанныеДокументы.ВалютаРасчетов
	|ГДЕ
	|	ВтПараметры.ПараметрЭтоРасчетыСКлиентами = &ПараметрЭтоРасчетыСКлиентами
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	ВалютаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРасчетыПоСрокамДо.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВТРасчетыПоСрокамДо.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВТРасчетыПоСрокамДо.Валюта КАК ВалютаРасчетов,
	|	ВТРасчетыПоСрокамДо.ДокументРегистратор КАК Накладная,
	|	СУММА(ЕСТЬNULL(ВЫБОР
	|		КОГДА ВТРасчетыПоСрокамДо.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ВТРасчетыПоСрокамДо.Предоплата - ВТРасчетыПоСрокамДо.Долг
	|		ИНАЧЕ ВТРасчетыПоСрокамДо.Долг
	|	КОНЕЦ, 0)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(ВЫБОР
	|		КОГДА ВТРасчетыПоСрокамДо.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ВТРасчетыПоСрокамДо.ПредоплатаРегл - ВТРасчетыПоСрокамДо.ДолгРегл
	|		ИНАЧЕ ВТРасчетыПоСрокамДо.ДолгРегл
	|	КОНЕЦ, 0)) КАК СуммаРегл,
	|	СУММА(ЕСТЬNULL(ВЫБОР
	|		КОГДА ВТРасчетыПоСрокамДо.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА ВТРасчетыПоСрокамДо.ПредоплатаУпр - ВТРасчетыПоСрокамДо.ДолгУпр
	|		ИНАЧЕ ВТРасчетыПоСрокамДо.ДолгУпр
	|	КОНЕЦ, 0)) КАК СуммаУпр
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК ВТРасчетыПоСрокамДо
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСвязанныеДокументыКлиент КАК ТаблицаСвязанныхДокументов
	|		ПО ВТРасчетыПоСрокамДо.ДокументРегистратор = ТаблицаСвязанныхДокументов.СвязанныйДокумент
	|		И ВТРасчетыПоСрокамДо.АналитикаУчетаПоПартнерам = ТаблицаСвязанныхДокументов.АналитикаУчетаПоПартнерам
	|		И ВТРасчетыПоСрокамДо.ОбъектРасчетов = ТаблицаСвязанныхДокументов.ОбъектРасчетов
	|		И ВТРасчетыПоСрокамДо.Валюта = ТаблицаСвязанныхДокументов.ВалютаРасчетов
	|		И ВТРасчетыПоСрокамДо.Период < ТаблицаСвязанныхДокументов.ДатаНачалаПересчета
	|		И НЕ ВТРасчетыПоСрокамДо.ДокументРегистратор = ТаблицаСвязанныхДокументов.Регистратор
	|ГДЕ
	|	ВТРасчетыПоСрокамДо.Активность
	|СГРУППИРОВАТЬ ПО
	|	ВТРасчетыПоСрокамДо.АналитикаУчетаПоПартнерам,
	|	ВТРасчетыПоСрокамДо.ОбъектРасчетов,
	|	ВТРасчетыПоСрокамДо.Валюта,
	|	ВТРасчетыПоСрокамДо.ДокументРегистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаСвязанныхДокументов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаСвязанныхДокументов.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаСвязанныхДокументов.ВалютаРасчетов КАК ВалютаРасчетов,
	|	СвязанныеДокументы.Ссылка КАК Накладная,
	|	СвязанныеДокументы.СуммаВзаиморасчетов КАК Сумма,
	|	СвязанныеДокументы.СуммаРегл КАК СуммаРегл,
	|	СвязанныеДокументы.СуммаУпр КАК СуммаУпр
	|ИЗ
	|	Документ.ПервичныйДокумент КАК СвязанныеДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСвязанныеДокументыКлиент КАК ТаблицаСвязанныхДокументов
	|		ПО СвязанныеДокументы.Ссылка = ТаблицаСвязанныхДокументов.СвязанныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСвязанныеДокументыКлиент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТь ТаблицаСвязанныеДокументы";
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"РасчетыДвиженияКлиенты","РасчетыДвиженияПоставщики");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"РегистрНакопления.РасчетыСКлиентамиПоСрокам", "РегистрНакопления.РасчетыСПоставщикамиПоСрокам");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ВТСвязанныеДокументы", "ВТСвязанныеДокументыПоставщик");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПараметрЭтоРасчетыСКлиентами", "ЛОЖЬ");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПараметрЭтоРасчетыСКлиентами", "ИСТИНА");
	КонецЕсли;
	
	ТаблицаСвязанныхДокументов = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаСвязанныхДокументов;
	
КонецФункции

// Остатки предоплаты на начало пересчета за минусом уже списавших плановые оплаты.
// 
// Параметры:
//  Запрос - Запрос - Запрос
// 
// Возвращаемое значение:
//  ТаблицаЗначений
Функция ОстаткиНераспределенныхАвансовПлановойОплатыНаНачалоПересчета(Запрос, ЭтоРасчетыСКлиентами = Ложь)
	
	Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Остатки.ОбъектРасчетов                    КАК ОбъектРасчетов,
		|	Остатки.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
		|	Остатки.ВалютаРасчетов                    КАК ВалютаРасчетов,
		|	Остатки.РасчетныйДокумент                 КАК Регистратор,
		|	МАКСИМУМ(Остатки.ДатаВозникновения)       КАК ДатаДвижения,
		|	МАКСИМУМ(Остатки.ПорядокОперации)         КАК ПорядокОперации
		|ПОМЕСТИТЬ ВтДокументыПредоплаты
		|ИЗ
		|	ВТРасчетыПоСрокамОстатки КАК Остатки
		|ГДЕ
		|	Остатки.Предоплата >0
		|СГРУППИРОВАТЬ ПО
		|	Остатки.ОбъектРасчетов,
		|	Остатки.АналитикаУчетаПоПартнерам,
		|	Остатки.ВалютаРасчетов,
		|	Остатки.РасчетныйДокумент
		|;
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВложенныйЗапрос.ВалютаРасчетов КАК ВалютаРасчетов,
		|	ВложенныйЗапрос.ДатаДвижения      КАК ДатаДвижения,
		|	ВложенныйЗапрос.Регистратор       КАК Регистратор,
		|	ВложенныйЗапрос.ПорядокОперации   КАК ПорядокОперации,
		|	ВложенныйЗапрос.Зачтено           КАК Зачтено,
		|	ИСТИНА                            КАК Просуммировано,
		|	СУММА(ВложенныйЗапрос.Сумма)      КАК Сумма,
		|	СУММА(ВложенныйЗапрос.Сумма) > 0  КАК ОстатокПоложительный
		|ИЗ
		|	(
		|	ВЫБРАТЬ
		|		Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|		Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|		Расчеты.ВалютаРасчетов КАК ВалютаРасчетов,
		|		Расчеты.ДокументРегистратор КАК Регистратор,
		|		Расчеты.ДатаВозникновения   КАК ДатаДвижения,
		|		Расчеты.ПорядокОперации     КАК ПорядокОперации,
		|		ЛОЖЬ                        КАК Зачтено,
		|		Расчеты.Предоплата          КАК Сумма
		|	ИЗ
		|		ВТРасчетыПоСрокамОстатки КАК Расчеты
		|	ГДЕ
		|		Расчеты.Предоплата > 0
		|		И ТИПЗНАЧЕНИЯ(Расчеты.ДокументРегистратор) НЕ В (ТИП(Документ.КорректировкаПриобретения), ТИП(Документ.КорректировкаРеализации))
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДокументыПредоплаты.ОбъектРасчетов                   КАК ОбъектРасчетов,
		|		ДокументыПредоплаты.АналитикаУчетаПоПартнерам        КАК АналитикаУчетаПоПартнерам,
		|		ДокументыПредоплаты.ВалютаРасчетов                   КАК ВалютаРасчетов,
		|		ДокументыПредоплаты.Регистратор                      КАК Регистратор,
		|		ДокументыПредоплаты.ДатаДвижения                     КАК ДатаДвижения,
		|		ДокументыПредоплаты.ПорядокОперации                  КАК ПорядокОперации,
		|		ИСТИНА                                               КАК Зачтено,
		|		-ПланыОплат.КОплате                                  КАК Сумма
		|	ИЗ
		|		РегистрНакопления.РасчетыСКлиентамиПланОплат КАК ПланыОплат
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыПредоплаты КАК ДокументыПредоплаты
		|				ПО ДокументыПредоплаты.Регистратор = ПланыОплат.ДокументОплаты
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметрыРасчета
		|				ПО ПланыОплат.ОбъектРасчетов = ВтПараметрыРасчета.ПараметрОбъектРасчетов
		|					И ПланыОплат.АналитикаУчетаПоПартнерам = ВтПараметрыРасчета.ПараметрАналитикаУчетаПоПартнерам
		|					И ПланыОплат.Валюта = ВтПараметрыРасчета.ПараметрВалютаРасчетов
		|	ГДЕ
		|		ПланыОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		И ПланыОплат.НераспределенныйАванс
		|		И ПланыОплат.Период < ВтПараметрыРасчета.ПараметрДатаНачалаПересчета
		|		И ТИПЗНАЧЕНИЯ(ДокументыПредоплаты.Регистратор) НЕ В (ТИП(Документ.КорректировкаПриобретения), ТИП(Документ.КорректировкаРеализации))
		|) КАК ВложенныйЗапрос
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.ОбъектРасчетов,
		|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
		|	ВложенныйЗапрос.ВалютаРасчетов,
		|	ВложенныйЗапрос.Регистратор,
		|	ВложенныйЗапрос.ДатаДвижения,
		|	ВложенныйЗапрос.Зачтено,
		|	ВложенныйЗапрос.ПорядокОперации
		|УПОРЯДОЧИТЬ ПО
		|	ОбъектРасчетов,
		|	АналитикаУчетаПоПартнерам,
		|	ВалютаРасчетов,
		|	ДатаДвижения
		|";
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"РегистрНакопления.РасчетыСКлиентамиПланОплат ","РегистрНакопления.РасчетыСПоставщикамиПланОплат ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ВТРасчетыПоСрокамОстатки","ВТРасчетыПоСрокамОстаткиПоставщики");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ВтДокументыПредоплаты","ВтДокументыПредоплатыПоставщики");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ВТРасчетыПоСрокамОстатки","ВТРасчетыПоСрокамОстаткиКлиенты");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ВтДокументыПредоплаты","ВтДокументыПредоплатыКлиенты");
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ПолучитьПриходныеДвиженияПоПлановымОплатам(Запрос, ГлобальныеПеременные, ЭтоРасчетыСКлиентами = Ложь)
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Расчеты.ОбъектРасчетов                              КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам                   КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта                                      КАК ВалютаРасчетов,
	|	Расчеты.ДатаРегистратора                            КАК ДатаВозникновения,
	|	НАЧАЛОПЕРИОДА(Расчеты.ДатаРегистратора,ДЕНЬ)        КАК Дата,
	|	Расчеты.ПорядокОперации                             КАК ПорядокОперации,
	|	Расчеты.Регистратор                                 КАК Регистратор,
	|	Расчеты.ДатаПлатежа                                 КАК ДатаПлановогоПогашения,
	|	Расчеты.ВариантОплаты                               КАК ВариантОплаты,
	|	СУММА(Расчеты.КОплате)                              КАК Сумма
	|ИЗ
	|	РасчетыДвижения КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметрыРасчета
	|			ПО Расчеты.ОбъектРасчетов = ВтПараметрыРасчета.ПараметрОбъектРасчетов
	|				И Расчеты.АналитикаУчетаПоПартнерам = ВтПараметрыРасчета.ПараметрАналитикаУчетаПоПартнерам
	|				И Расчеты.Валюта = ВтПараметрыРасчета.ПараметрВалютаРасчетов
	|				// В РасчетыДвижения лишние записи заказов КОплате с периодом больше даты заказа
	|				И Расчеты.ДатаРегистратора >= ВтПараметрыРасчета.ПараметрДатаНачалаПересчета
	|ГДЕ
	|	(ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) В (&ТипыРегистраторов) ИЛИ &РеализацияВПути)
	|		И Расчеты.ВидДвижения = &Приход
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта,
	|	Расчеты.ДатаРегистратора,
	|	НАЧАЛОПЕРИОДА(Расчеты.ДатаРегистратора,ДЕНЬ),
	|	Расчеты.ПорядокОперации,
	|	Расчеты.Регистратор,
	|	Расчеты.ДатаПлатежа,
	|	Расчеты.ВариантОплаты
	|УПОРЯДОЧИТЬ ПО
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта,
	|	Расчеты.ДатаРегистратора
	|";
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"РасчетыДвижения","РасчетыДвиженияПоставщики");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&Приход","ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&РеализацияВПути","ЛОЖЬ");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"РасчетыДвижения","РасчетыДвиженияКлиенты");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&Приход","ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&РеализацияВПути","(Расчеты.ПродажаПоЗаказу В (&ПустыеСсылкиНаЗаказы) И НЕ ВтПараметрыРасчета.ПараметрГрафикВДоговоре)
																	|И	(ВЫРАЗИТЬ(Расчеты.Регистратор КАК Документ.РеализацияТоваровУслуг).Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено)
																	|		И ВЫРАЗИТЬ(Расчеты.Регистратор КАК Документ.РеализацияТоваровУслуг).ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту),
																	|												ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиентуРеглУчет),
																	|												ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
																	|												ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионера),
																	|												ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))
																	|	ИЛИ ВЫРАЗИТЬ(Расчеты.Регистратор КАК Документ.РеализацияУслугПрочихАктивов).Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.Отгружено)
																	|		И ВЫРАЗИТЬ(Расчеты.Регистратор КАК Документ.РеализацияУслугПрочихАктивов).ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
																	|									ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав))
																	|	)");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТипыРегистраторов", СписокТиповРегистраторовПланов());
	
	Результат  = Запрос.Выполнить();
	ПриходныеДвиженияГрафика = Результат.Выгрузить();
	
	Возврат ПриходныеДвиженияГрафика;
	
КонецФункции

//Возвращает таблицу закрывающих планы движений накладных
Функция ПолучитьУточненияГрафиковОплат(Запрос, ГлобальныеПеременные, ЭтоРасчетыСКлиентами = Ложь)
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Расчеты.ОбъектРасчетов                              КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам                   КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта                                      КАК ВалютаРасчетов,
	|	Расчеты.ДатаРегистратора                            КАК ДатаДвижения,
	|	НАЧАЛОПЕРИОДА(Расчеты.ДатаРегистратора,ДЕНЬ)        КАК ДатаВозникновения,
	|	Расчеты.ПорядокОперации                             КАК ПорядокОперации,
	|	Расчеты.Регистратор                                 КАК Регистратор,
	|	ВЫБОР КОГДА ВтПараметрыРасчета.ПараметрГрафикВДоговоре
	|			ТОГДА ВтПараметрыРасчета.ПараметрГрафикИсполненияДоговора
	|			ИНАЧЕ Расчеты.ПродажаПоЗаказу
	|	КОНЕЦ КАК Заказ,
	|	ВЫБОР КОГДА Расчеты.ВариантОплаты = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроляОплатыКлиентом.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроляОплатыКлиентом.КредитПослеОтгрузки)
	|		КОГДА Расчеты.ВариантОплаты = ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроляОплатыПоставщику.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроляОплатыПоставщику.КредитПослеПоступления)
	|		ИНАЧЕ Расчеты.ВариантОплаты 
	|	КОНЕЦ                                               КАК ВариантОплаты,
	|	//При АДДН и АЗДН сумма двигается по другому объекту расчетов
	|	Расчеты.КОплате                                     КАК Сумма
	|ИЗ
	|	РасчетыДвиженияКлиенты КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметрыРасчета
	|			ПО Расчеты.ОбъектРасчетов = ВтПараметрыРасчета.ПараметрОбъектРасчетов
	|				И Расчеты.АналитикаУчетаПоПартнерам = ВтПараметрыРасчета.ПараметрАналитикаУчетаПоПартнерам
	|				И Расчеты.Валюта = ВтПараметрыРасчета.ПараметрВалютаРасчетов
	|				// В РасчетыДвижения лишние записи заказов КОплате с периодом больше даты заказа
	|				И Расчеты.ДатаРегистратора >= ВтПараметрыРасчета.ПараметрДатаНачалаПересчета
	|ГДЕ
	|	Расчеты.ВидДвижения = &Расход И Расчеты.КОплате > 0
	|	И (Расчеты.ПродажаПоЗаказу НЕ В (&ПустыеСсылкиНаЗаказы) ИЛИ ВтПараметрыРасчета.ПараметрГрафикВДоговоре)
	|	И ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) В (&СписокТиповУточняющихПланы)
	|УПОРЯДОЧИТЬ ПО
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта,
	|	Расчеты.ДатаРегистратора,
	|	Расчеты.Период
	|";
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"РегистрНакопления.РасчетыСКлиентами ","РегистрНакопления.РасчетыСПоставщиками ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&Расход","ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ПродажаПоЗаказу","ЗакупкаПоЗаказу");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"РасчетыДвиженияКлиенты","РасчетыДвиженияПоставщики");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&Расход","ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)");
	КонецЕсли;
	
	Результат  = Запрос.Выполнить();
	УточненияГрафиковОплат = Результат.Выгрузить();
	УточненияГрафиковОплат.Индексы.Добавить("Заказ");
	
	Возврат УточненияГрафиковОплат;
	
КонецФункции

Функция ПолучитьПриходныеДвиженияПоПлановымОтгрузкам(Запрос, ГлобальныеПеременные, ЭтоРасчетыСКлиентами = Ложь)
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Расчеты.ОбъектРасчетов                              КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам                   КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта                                      КАК ВалютаРасчетов,
	|	Расчеты.ДатаРегистратора                            КАК ДатаВозникновения,
	|	НАЧАЛОПЕРИОДА(Расчеты.ДатаРегистратора,ДЕНЬ)        КАК Дата,
	|	Расчеты.ПорядокОперации                             КАК ПорядокОперации,
	|	Расчеты.Регистратор                                 КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(Расчеты.Период,ДЕНЬ)                  КАК ДатаПлановогоПогашения,
	|	Расчеты.КОтгрузке                                   КАК Сумма
	|ИЗ
	|	РасчетыДвиженияКлиенты КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметрыРасчета
	|			ПО Расчеты.ОбъектРасчетов = ВтПараметрыРасчета.ПараметрОбъектРасчетов
	|				И Расчеты.АналитикаУчетаПоПартнерам = ВтПараметрыРасчета.ПараметрАналитикаУчетаПоПартнерам
	|				И Расчеты.Валюта = ВтПараметрыРасчета.ПараметрВалютаРасчетов
	|				// В РасчетыДвижения лишние записи заказов КОтгрузке с периодом больше даты заказа
	|				И Расчеты.ДатаРегистратора >= ВтПараметрыРасчета.ПараметрДатаНачалаПересчета
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) В (&ТипыРегистраторов)
	|		И Расчеты.ВидДвижения = &Расход
	|УПОРЯДОЧИТЬ ПО 
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта,
	|	Расчеты.ДатаРегистратора,
	|	Расчеты.Период
	|";
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&Расход","ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"РасчетыДвиженияКлиенты","РасчетыДвиженияПоставщики");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&Расход","ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТипыРегистраторов", СписокТиповРегистраторовПланов());
	
	Результат  = Запрос.Выполнить();
	ПриходныеДвиженияГрафикаОтгрузки = Результат.Выгрузить();
	
	Возврат ПриходныеДвиженияГрафикаОтгрузки;
	
КонецФункции

//Возвращает таблицу закрывающих планы движений накладных
Функция ПолучитьЗакрытияГрафиковОтгрузок(Запрос, ГлобальныеПеременные, ЭтоРасчетыСКлиентами = Ложь)
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Расчеты.ДатаРегистратора                            КАК ДатаДвижения,
	|	НАЧАЛОПЕРИОДА(Расчеты.ДатаРегистратора,ДЕНЬ)        КАК ДатаВозникновения,
	|	Расчеты.ОбъектРасчетов                              КАК ОбъектРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам                   КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Валюта                                      КАК ВалютаРасчетов,
	|	Расчеты.ПорядокОперации                             КАК ПорядокОперации,
	|	Расчеты.Регистратор                                 КАК Регистратор,
	|	ВЫБОР КОГДА ВтПараметрыРасчета.ПараметрГрафикВДоговоре
	|			ТОГДА ВтПараметрыРасчета.ПараметрГрафикИсполненияДоговора
	|			ИНАЧЕ Расчеты.ПродажаПоЗаказу
	|	КОНЕЦ                                               КАК Заказ,
	|//При АДДН и АЗДН сумма двигается по другому объекту расчетов
	|	Расчеты.КОтгрузке КАК Сумма
	|ИЗ
	|	РасчетыДвиженияКлиенты КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметрыРасчета
	|			ПО Расчеты.ОбъектРасчетов = ВтПараметрыРасчета.ПараметрОбъектРасчетов
	|				И Расчеты.АналитикаУчетаПоПартнерам = ВтПараметрыРасчета.ПараметрАналитикаУчетаПоПартнерам
	|				И Расчеты.Валюта = ВтПараметрыРасчета.ПараметрВалютаРасчетов
	|				// В РасчетыДвижения лишние записи заказов КОтгрузке с периодом больше даты заказа
	|				И Расчеты.ДатаРегистратора >= ВтПараметрыРасчета.ПараметрДатаНачалаПересчета
	|ГДЕ
	|	Расчеты.КОтгрузке > 0 И Расчеты.ВидДвижения = &Приход
	|	И (Расчеты.ПродажаПоЗаказу НЕ В (&ПустыеСсылкиНаЗаказы) ИЛИ ВтПараметрыРасчета.ПараметрГрафикВДоговоре)
	|	И ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) В (&СписокТиповУточняющихПланы)
	|УПОРЯДОЧИТЬ ПО
	|	Расчеты.ДатаРегистратора
	|";
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&Приход","ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ПродажаПоЗаказу","ЗакупкаПоЗаказу");
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"РасчетыДвиженияКлиенты","РасчетыДвиженияПоставщики");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"&Приход","ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)");
	КонецЕсли;
	
	Результат  = Запрос.Выполнить();
	ЗакрытияГрафиковОтгрузок = Результат.Выгрузить();
	ЗакрытияГрафиковОтгрузок.Индексы.Добавить("Заказ");
	
	Возврат ЗакрытияГрафиковОтгрузок;
	
КонецФункции

Процедура ДополнитьТаблицуИзмененийПриемниками(ТаблицаИзменений, Параметры, РассчитываемыеИзмерения = Неопределено) Экспорт
	
	Если РассчитываемыеИзмерения = Неопределено Тогда
		РассчитываемыеИзмерения = ТаблицаИзменений.Скопировать(, "ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов,ОбъектРасчетовПриемник,АналитикаУчетаПоПартнерамПриемник,ВалютаПриемник");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаИзменений", ТаблицаИзменений);
	Запрос.УстановитьПараметр("РассчитываемыеИзмерения", РассчитываемыеИзмерения);
	
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	РассчитываемыеИзмерения.ОбъектРасчетов КАК ОбъектРасчетов,
		|	РассчитываемыеИзмерения.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	РассчитываемыеИзмерения.ВалютаРасчетов КАК ВалютаРасчетов,
		|	РассчитываемыеИзмерения.ОбъектРасчетовПриемник КАК ОбъектРасчетовПриемник,
		|	РассчитываемыеИзмерения.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
		|	РассчитываемыеИзмерения.ВалютаПриемник КАК ВалютаПриемник
		|ПОМЕСТИТЬ ВтРассчитываемыеИзмерения
		|ИЗ
		|	&РассчитываемыеИзмерения КАК РассчитываемыеИзмерения
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаПоПартнерам,
		|	ОбъектРасчетов,
		|	ВалютаРасчетов,
		|	АналитикаУчетаПоПартнерамПриемник,
		|	ОбъектРасчетовПриемник,
		|	ВалютаПриемник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаИзменений.ТипРасчетов КАК ТипРасчетов,
		|	ТаблицаИзменений.ПорядокОперации КАК ПорядокОперации,
		|	ТаблицаИзменений.ПериодКонтроляИзменений КАК ПериодКонтроляИзменений,
		|	ТаблицаИзменений.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ТаблицаИзменений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ТаблицаИзменений.ОбъектРасчетовПриемник КАК ОбъектРасчетовПриемник,
		|	ТаблицаИзменений.ВалютаПриемник КАК ВалютаПриемник,
		|	ТаблицаИзменений.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
		|	ТаблицаИзменений.ВалютаРасчетов КАК ВалютаРасчетов,
		|	ТаблицаИзменений.Документ КАК Документ,
		|	ТаблицаИзменений.НачальноеЗаполнение КАК НачальноеЗаполнение
		|ПОМЕСТИТЬ ТаблицаИзмененийСПриемниками
		|ИЗ
		|	&ТаблицаИзменений КАК ТаблицаИзменений
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ТипРасчетов,
		|	АналитикаУчетаПоПартнерам,
		|	ОбъектРасчетов,
		|	ВалютаРасчетов,
		|	ПорядокОперации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	ТаблицаИзменений.ТипРасчетов КАК ТипРасчетов,
		|	ТаблицаИзменений.ПорядокОперации КАК ПорядокОперации,
		|	ТаблицаИзменений.ПериодКонтроляИзменений КАК ПериодКонтроляИзменений,
		|	ТаблицаИзменений.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ТаблицаИзменений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ТаблицаИзменений.ОбъектРасчетовПриемник КАК ОбъектРасчетовПриемник,
		|	ТаблицаИзменений.ВалютаПриемник КАК ВалютаПриемник,
		|	ТаблицаИзменений.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
		|	ТаблицаИзменений.ВалютаРасчетов КАК ВалютаРасчетов,
		|	ТаблицаИзменений.Документ КАК Документ,
		|	ТаблицаИзменений.НачальноеЗаполнение КАК НачальноеЗаполнение
		|ПОМЕСТИТЬ ДокументыТаблицыИзмененийСПриемниками
		|ИЗ
		|	ТаблицаИзмененийСПриемниками КАК ТаблицаИзменений
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ,
		|	ТипРасчетов,
		|	АналитикаУчетаПоПартнерам,
		|	ОбъектРасчетов,
		|	ВалютаРасчетов,
		|	ПорядокОперации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	ТаблицаИзменений.ТипРасчетов КАК ТипРасчетов,
		|	ТаблицаИзменений.ПорядокОперации КАК ПорядокОперации,
		|	ТаблицаИзменений.ПериодКонтроляИзменений КАК ПериодКонтроляИзменений,
		|	ТаблицаИзменений.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ТаблицаИзменений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ТаблицаИзменений.ОбъектРасчетовПриемник КАК ОбъектРасчетовПриемник,
		|	ТаблицаИзменений.ВалютаПриемник КАК ВалютаПриемник,
		|	ТаблицаИзменений.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
		|	ТаблицаИзменений.ВалютаРасчетов КАК ВалютаРасчетов,
		|	ТаблицаИзменений.Документ КАК Документ,
		|	ТаблицаИзменений.НачальноеЗаполнение КАК НачальноеЗаполнение
		|ПОМЕСТИТЬ НепроведеныеДокументыТаблицыИзмененийСПриемниками
		|ИЗ
		|	ДокументыТаблицыИзмененийСПриемниками КАК ТаблицаИзменений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
		|		ПО (РеестрДокументов.Ссылка = ТаблицаИзменений.Документ)
		|			И (НЕ РеестрДокументов.Проведен)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ТипРасчетов,
		|	АналитикаУчетаПоПартнерам,
		|	ОбъектРасчетов,
		|	ВалютаРасчетов,
		|	ПорядокОперации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|
		// Поместим все данные по приемникам расчетов во временную таблицу
		|ВЫБРАТЬ
		|	Изменения.ТипРасчетов                               КАК ТипРасчетов,
		|	РасчетыСКлиентами.ПорядокОперации                   КАК ПорядокОперации,
		|	Изменения.НачальноеЗаполнение                       КАК НачальноеЗаполнение,
		|	РасчетыСКлиентами.Период                            КАК Период,
		|	Изменения.ПериодКонтроляИзменений                   КАК ПериодКонтроляИзменений,
		|	РасчетыСКлиентами.ДатаРегистратора                  КАК ДатаРегистратора,
		|	
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
		|	РасчетыСКлиентами.ОбъектРасчетов                    КАК ОбъектРасчетов,
		|	РасчетыСКлиентами.Валюта                            КАК ВалютаРасчетов,
		|	
		|	РасчетыСКлиентами.Регистратор                       КАК Документ,
		|	
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
		|	РасчетыСКлиентами.ОбъектРасчетовПриемник            КАК ОбъектРасчетовПриемник,
		|	РасчетыСКлиентами.ВалютаПриемник                    КАК ВалютаПриемник,
		|	
		|	РасчетыСКлиентами.ПоДаннымОбъектаРасчетовИсточника  КАК ПоДаннымОбъектаРасчетовИсточника
		|ПОМЕСТИТЬ ДанныеРасчетов
		|ИЗ
		|	ТаблицаИзмененийСПриемниками КАК Изменения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
		|		ПО Изменения.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
		|			И РасчетыСКлиентами.АналитикаУчетаПоПартнерам = Изменения.АналитикаУчетаПоПартнерам
		|			И РасчетыСКлиентами.ОбъектРасчетов = Изменения.ОбъектРасчетов
		|			И РасчетыСКлиентами.Валюта = Изменения.ВалютаРасчетов
		|			И РасчетыСКлиентами.ПорядокОперации >= Изменения.ПорядокОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРассчитываемыеИзмерения КАК РассчитываемыеИзмерения 
		|		ПО РасчетыСКлиентами.ОбъектРасчетов = РассчитываемыеИзмерения.ОбъектРасчетов
		|			И РасчетыСКлиентами.АналитикаУчетаПоПартнерам = РассчитываемыеИзмерения.АналитикаУчетаПоПартнерам
		|			И РасчетыСКлиентами.Валюта = РассчитываемыеИзмерения.ВалютаРасчетов 
		|			И РасчетыСКлиентами.ОбъектРасчетовПриемник = РассчитываемыеИзмерения.ОбъектРасчетовПриемник
		|			И РасчетыСКлиентами.АналитикаУчетаПоПартнерамПриемник = РассчитываемыеИзмерения.АналитикаУчетаПоПартнерамПриемник
		|			И РасчетыСКлиентами.ВалютаПриемник = РассчитываемыеИзмерения.ВалютаПриемник
		|ГДЕ
		|	РассчитываемыеИзмерения.АналитикаУчетаПоПартнерам ЕСТЬ NULL
		|	И РасчетыСКлиентами.ОбъектРасчетовПриемник <> ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
		|	И РасчетыСКлиентами.ОбъектРасчетовПриемник.ТипОбъектаРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.ПлатежВозврат)
		|	И РасчетыСКлиентами.Активность
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Изменения.ТипРасчетов                               КАК ТипРасчетов,
		|	РасчетыСКлиентами.ПорядокОперации                   КАК ПорядокОперации,
		|	Изменения.НачальноеЗаполнение                       КАК НачальноеЗаполнение,
		|	РасчетыСКлиентами.Период                            КАК Период,
		|	Изменения.ПериодКонтроляИзменений                   КАК ПериодКонтроляИзменений,
		|	РасчетыСКлиентами.Период                            КАК ДатаРегистратора,
		|	
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
		|	РасчетыСКлиентами.ОбъектРасчетов                    КАК ОбъектРасчетов,
		|	РасчетыСКлиентами.Валюта                            КАК ВалютаРасчетов,
		|	
		|	РасчетыСКлиентами.Регистратор                       КАК Документ,
		|	
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
		|	РасчетыСКлиентами.ОбъектРасчетовПриемник            КАК ОбъектРасчетовПриемник,
		|	РасчетыСКлиентами.ВалютаПриемник                    КАК ВалютаПриемник,
		|	
		|	ЛОЖЬ                                                КАК ПоДаннымОбъектаРасчетовИсточника
		|ИЗ
		|	НепроведеныеДокументыТаблицыИзмененийСПриемниками КАК Изменения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентами
		|		ПО Изменения.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
		|			И РасчетыСКлиентами.АналитикаУчетаПоПартнерам = Изменения.АналитикаУчетаПоПартнерам
		|			И РасчетыСКлиентами.ОбъектРасчетов = Изменения.ОбъектРасчетов
		|			И РасчетыСКлиентами.Валюта = Изменения.ВалютаРасчетов
		|			И РасчетыСКлиентами.ПорядокОперации >= Изменения.ПорядокОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРассчитываемыеИзмерения КАК РассчитываемыеИзмерения
		|		ПО РасчетыСКлиентами.ОбъектРасчетов = РассчитываемыеИзмерения.ОбъектРасчетов
		|			И РасчетыСКлиентами.АналитикаУчетаПоПартнерам = РассчитываемыеИзмерения.АналитикаУчетаПоПартнерам
		|			И РасчетыСКлиентами.Валюта = РассчитываемыеИзмерения.ВалютаРасчетов 
		|			И РасчетыСКлиентами.ОбъектРасчетовПриемник = РассчитываемыеИзмерения.ОбъектРасчетовПриемник
		|			И РасчетыСКлиентами.АналитикаУчетаПоПартнерамПриемник = РассчитываемыеИзмерения.АналитикаУчетаПоПартнерамПриемник
		|			И РасчетыСКлиентами.ВалютаПриемник = РассчитываемыеИзмерения.ВалютаПриемник
		|ГДЕ
		|	РассчитываемыеИзмерения.АналитикаУчетаПоПартнерам ЕСТЬ NULL
		|	И РасчетыСКлиентами.ОбъектРасчетовПриемник <> ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
		|	И РасчетыСКлиентами.ОбъектРасчетовПриемник.ТипОбъектаРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.ПлатежВозврат)
		|	И РасчетыСКлиентами.Активность
		|	И НЕ РасчетыСКлиентами.Сторно
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		// Добавим к пересчету возможные некорректные объекты расчетов в движениях пересчитываемого документа регистратора
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
		|		МИНИМУМ(РасчетыСКлиентамиПоСрокам.ПорядокОперации)              КАК ПорядокОперации,
		|		МАКСИМУМ(Изменения.НачальноеЗаполнение)                         КАК НачальноеЗаполнение,
		|		МИНИМУМ(РасчетыСКлиентамиПоСрокам.Период)                       КАК Период,
		|		МИНИМУМ(Изменения.ПериодКонтроляИзменений)                      КАК ПериодКонтроляИзменений,
		|		МИНИМУМ(РасчетыСКлиентамиПоСрокам.Период)                       КАК ДатаРегистратора,
		|		РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам             КАК АналитикаУчетаПоПартнерам,
		|		РасчетыСКлиентамиПоСрокам.ОбъектРасчетов                        КАК ОбъектРасчетов,
		|		РасчетыСКлиентамиПоСрокам.Валюта                                КАК ВалютаРасчетов,
		|		РасчетыСКлиентамиПоСрокам.ДокументРегистратор                   КАК Документ,
		|		РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерамПриемник     КАК АналитикаУчетаПоПартнерамПриемник,
		|		РасчетыСКлиентамиПоСрокам.ОбъектРасчетовПриемник                КАК ОбъектРасчетовПриемник,
		|		РасчетыСКлиентамиПоСрокам.ВалютаПриемник                        КАК ВалютаПриемник,
		|		ЛОЖЬ                                                            КАК ПоДаннымОбъектаРасчетовИсточника
		|	ИЗ
		|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентамиПоСрокам
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыТаблицыИзмененийСПриемниками КАК Изменения
		|				ПО Изменения.Документ = РасчетыСКлиентамиПоСрокам.ДокументРегистратор
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
		|				ПО РасчетыСКлиентами.Регистратор = РасчетыСКлиентамиПоСрокам.ДокументРегистратор 
		|					И РасчетыСКлиентами.АналитикаУчетаПоПартнерам = РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам
		|					И РасчетыСКлиентами.ОбъектРасчетов = РасчетыСКлиентамиПоСрокам.ОбъектРасчетов
		|					И РасчетыСКлиентами.Валюта = РасчетыСКлиентамиПоСрокам.Валюта
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВтРассчитываемыеИзмерения КАК РассчитываемыеИзмерения
		|			ПО РасчетыСКлиентамиПоСрокам.ОбъектРасчетов = РассчитываемыеИзмерения.ОбъектРасчетов
		|				И РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам = РассчитываемыеИзмерения.АналитикаУчетаПоПартнерам
		|				И РасчетыСКлиентамиПоСрокам.Валюта = РассчитываемыеИзмерения.ВалютаРасчетов			
		|				
		|	ГДЕ
		|		РасчетыСКлиентами.ОбъектРасчетов ЕСТЬ NULL
		|		И РассчитываемыеИзмерения.АналитикаУчетаПоПартнерам ЕСТЬ NULL
		|		И НЕ РасчетыСКлиентами.Сторно
		|
		|	СГРУППИРОВАТЬ ПО
		|		РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам,
		|		РасчетыСКлиентамиПоСрокам.ОбъектРасчетов,
		|		РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерамПриемник,
		|		РасчетыСКлиентамиПоСрокам.ОбъектРасчетовПриемник,
		|		РасчетыСКлиентамиПоСрокам.ВалютаПриемник,
		|		РасчетыСКлиентамиПоСрокам.Валюта,
		|		РасчетыСКлиентамиПоСрокам.ДокументРегистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Изменения.ТипРасчетов                                  КАК ТипРасчетов,
		|	РасчетыСПоставщиками.ПорядокОперации                   КАК ПорядокОперации,
		|	Изменения.НачальноеЗаполнение                          КАК НачальноеЗаполнение,
		|	РасчетыСПоставщиками.Период                            КАК Период,
		|	Изменения.ПериодКонтроляИзменений                      КАК ПериодКонтроляИзменений,
		|	РасчетыСПоставщиками.ДатаРегистратора                  КАК ДатаРегистратора,
		|	
		|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
		|	РасчетыСПоставщиками.ОбъектРасчетов                    КАК ОбъектРасчетов,
		|	РасчетыСПоставщиками.Валюта                            КАК ВалютаРасчетов,
		|	
		|	РасчетыСПоставщиками.Регистратор                       КАК Документ,
		|	
		|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
		|	РасчетыСПоставщиками.ОбъектРасчетовПриемник            КАК ОбъектРасчетовПриемник,
		|	РасчетыСПоставщиками.ВалютаПриемник                    КАК ВалютаПриемник,
		|	
		|	РасчетыСПоставщиками.ПоДаннымОбъектаРасчетовИсточника  КАК ПоДаннымОбъектаРасчетовИсточника
		|ИЗ
		|	ТаблицаИзмененийСПриемниками КАК Изменения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
		|		ПО Изменения.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
		|			И РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = Изменения.АналитикаУчетаПоПартнерам
		|			И РасчетыСПоставщиками.ОбъектРасчетов = Изменения.ОбъектРасчетов
		|			И РасчетыСПоставщиками.Валюта = Изменения.ВалютаРасчетов
		|			И РасчетыСПоставщиками.ПорядокОперации >= Изменения.ПорядокОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРассчитываемыеИзмерения КАК РассчитываемыеИзмерения
		|		ПО РасчетыСПоставщиками.ОбъектРасчетов = РассчитываемыеИзмерения.ОбъектРасчетов
		|			И РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = РассчитываемыеИзмерения.АналитикаУчетаПоПартнерам
		|			И РасчетыСПоставщиками.Валюта = РассчитываемыеИзмерения.ВалютаРасчетов 
		|			И РасчетыСПоставщиками.ОбъектРасчетовПриемник = РассчитываемыеИзмерения.ОбъектРасчетовПриемник
		|			И РасчетыСПоставщиками.АналитикаУчетаПоПартнерамПриемник = РассчитываемыеИзмерения.АналитикаУчетаПоПартнерамПриемник
		|			И РасчетыСПоставщиками.ВалютаПриемник = РассчитываемыеИзмерения.ВалютаПриемник
		|ГДЕ
		|	РассчитываемыеИзмерения.АналитикаУчетаПоПартнерам ЕСТЬ NULL
		|	И РасчетыСПоставщиками.ОбъектРасчетовПриемник <> ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
		|	И РасчетыСПоставщиками.ОбъектРасчетовПриемник.ТипОбъектаРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.ПлатежВозврат)
		|	И РасчетыСПоставщиками.Активность
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Изменения.ТипРасчетов                                  КАК ТипРасчетов,
		|	РасчетыСПоставщиками.ПорядокОперации                   КАК ПорядокОперации,
		|	Изменения.НачальноеЗаполнение                          КАК НачальноеЗаполнение,
		|	РасчетыСПоставщиками.Период                            КАК Период,
		|	Изменения.ПериодКонтроляИзменений                      КАК ПериодКонтроляИзменений,
		|	РасчетыСПоставщиками.Период                            КАК ДатаРегистратора,
		|	
		|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
		|	РасчетыСПоставщиками.ОбъектРасчетов                    КАК ОбъектРасчетов,
		|	РасчетыСПоставщиками.Валюта                            КАК ВалютаРасчетов,
		|	
		|	РасчетыСПоставщиками.Регистратор                       КАК Документ,
		|	
		|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
		|	РасчетыСПоставщиками.ОбъектРасчетовПриемник            КАК ОбъектРасчетовПриемник,
		|	РасчетыСПоставщиками.ВалютаПриемник                    КАК ВалютаПриемник,
		|	
		|	ЛОЖЬ                                                КАК ПоДаннымОбъектаРасчетовИсточника
		|ИЗ
		|	НепроведеныеДокументыТаблицыИзмененийСПриемниками КАК Изменения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщиками
		|		ПО Изменения.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
		|			И РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = Изменения.АналитикаУчетаПоПартнерам
		|			И РасчетыСПоставщиками.ОбъектРасчетов = Изменения.ОбъектРасчетов
		|			И РасчетыСПоставщиками.Валюта = Изменения.ВалютаРасчетов
		|			И РасчетыСПоставщиками.ПорядокОперации >= Изменения.ПорядокОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРассчитываемыеИзмерения КАК РассчитываемыеИзмерения
		|		ПО РасчетыСПоставщиками.ОбъектРасчетов = РассчитываемыеИзмерения.ОбъектРасчетов
		|			И РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = РассчитываемыеИзмерения.АналитикаУчетаПоПартнерам
		|			И РасчетыСПоставщиками.Валюта = РассчитываемыеИзмерения.ВалютаРасчетов 
		|			И РасчетыСПоставщиками.ОбъектРасчетовПриемник = РассчитываемыеИзмерения.ОбъектРасчетовПриемник
		|			И РасчетыСПоставщиками.АналитикаУчетаПоПартнерамПриемник = РассчитываемыеИзмерения.АналитикаУчетаПоПартнерамПриемник
		|			И РасчетыСПоставщиками.ВалютаПриемник = РассчитываемыеИзмерения.ВалютаПриемник
		|ГДЕ
		|	РассчитываемыеИзмерения.АналитикаУчетаПоПартнерам ЕСТЬ NULL
		|	И РасчетыСПоставщиками.ОбъектРасчетовПриемник <> ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
		|	И РасчетыСПоставщиками.ОбъектРасчетовПриемник.ТипОбъектаРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.ПлатежВозврат)
		|	И РасчетыСПоставщиками.Активность
		|	И НЕ РасчетыСПоставщиками.Сторно
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		// Добавим к пересчету возможные некорректные объекты расчетов в движениях пересчитываемого документа регистратора
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов,
		|		МИНИМУМ(РасчетыСПоставщикамиПоСрокам.ПорядокОперации)              КАК ПорядокОперации,
		|		МАКСИМУМ(Изменения.НачальноеЗаполнение)                            КАК НачальноеЗаполнение,
		|		МИНИМУМ(РасчетыСПоставщикамиПоСрокам.Период)                       КАК Период,
		|		МИНИМУМ(Изменения.ПериодКонтроляИзменений)                         КАК ПериодКонтроляИзменений,
		|		МИНИМУМ(РасчетыСПоставщикамиПоСрокам.Период)                       КАК ДатаРегистратора,
		|		РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам             КАК АналитикаУчетаПоПартнерам,
		|		РасчетыСПоставщикамиПоСрокам.ОбъектРасчетов                        КАК ОбъектРасчетов,
		|		РасчетыСПоставщикамиПоСрокам.Валюта                                КАК ВалютаРасчетов,
		|		РасчетыСПоставщикамиПоСрокам.ДокументРегистратор                   КАК Документ,
		|		РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерамПриемник     КАК АналитикаУчетаПоПартнерамПриемник,
		|		РасчетыСПоставщикамиПоСрокам.ОбъектРасчетовПриемник                КАК ОбъектРасчетовПриемник,
		|		РасчетыСПоставщикамиПоСрокам.ВалютаПриемник                        КАК ВалютаПриемник,
		|		ЛОЖЬ                                                               КАК ПоДаннымОбъектаРасчетовИсточника
		|	ИЗ
		|		РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиПоСрокам
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыТаблицыИзмененийСПриемниками КАК Изменения
		|				ПО Изменения.Документ = РасчетыСПоставщикамиПоСрокам.ДокументРегистратор
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
		|				ПО РасчетыСПоставщиками.Регистратор = РасчетыСПоставщикамиПоСрокам.ДокументРегистратор 
		|					И РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам
		|					И РасчетыСПоставщиками.ОбъектРасчетов = РасчетыСПоставщикамиПоСрокам.ОбъектРасчетов
		|					И РасчетыСПоставщиками.Валюта = РасчетыСПоставщикамиПоСрокам.Валюта
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВтРассчитываемыеИзмерения КАК РассчитываемыеИзмерения
		|				ПО РасчетыСПоставщикамиПоСрокам.ОбъектРасчетов = РассчитываемыеИзмерения.ОбъектРасчетов
		|				И РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам = РассчитываемыеИзмерения.АналитикаУчетаПоПартнерам
		|				И РасчетыСПоставщикамиПоСрокам.Валюта = РассчитываемыеИзмерения.ВалютаРасчетов
		|				
		|	ГДЕ
		|		РасчетыСПоставщиками.ОбъектРасчетов ЕСТЬ NULL
		|		И РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам ЕСТЬ NULL
		|		И НЕ РасчетыСПоставщиками.Сторно
		|
		|	СГРУППИРОВАТЬ ПО
		|		РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам,
		|		РасчетыСПоставщикамиПоСрокам.ОбъектРасчетов,
		|		РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерамПриемник,
		|		РасчетыСПоставщикамиПоСрокам.ОбъектРасчетовПриемник,
		|		РасчетыСПоставщикамиПоСрокам.ВалютаПриемник,
		|		РасчетыСПоставщикамиПоСрокам.Валюта,
		|		РасчетыСПоставщикамиПоСрокам.ДокументРегистратор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ТипРасчетов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Объекты расчетов приемники изменений, которые не добавлены к распределению
		|ВЫБРАТЬ
		|	МИНИМУМ(РасчетыСКлиентами.ПорядокОперации)                       КАК ПорядокОперации,
		|	МАКСИМУМ(РасчетыСКлиентами.НачальноеЗаполнение)                  КАК НачальноеЗаполнение,
		|	МИНИМУМ(РасчетыСКлиентами.Период)                                КАК Период,
		|	МИНИМУМ(РасчетыСКлиентами.ПериодКонтроляИзменений)               КАК ПериодКонтроляИзменений,
		|	МИНИМУМ(РасчетыСКлиентами.ДатаРегистратора)                      КАК ДатаРегистратора,
		|	
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерамПриемник              КАК АналитикаУчетаПоПартнерам,
		|	РасчетыСКлиентами.ОбъектРасчетовПриемник                         КАК ОбъектРасчетов,
		|	РасчетыСКлиентами.ВалютаПриемник                                 КАК ВалютаРасчетов,
		|	
		|	РасчетыСКлиентами.Документ                                       КАК Документ,
		|	
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) КАК АналитикаУчетаПоПартнерамПриемник,
		|	ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)                КАК ОбъектРасчетовПриемник,
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)                         КАК ВалютаПриемник,
		|	
		|	ИСТИНА                                                           КАК ПоДаннымОбъектаРасчетовИсточника,
		|	НЕОПРЕДЕЛЕНО                                                     КАК Заказ,
		|	1                                                                КАК КОплате,
		|	1                                                                КАК КОтгрузке,
		|	1                                                                КАК Сумма,
		|	1                                                                КАК СуммаРегл,
		|	1                                                                КАК СуммаУпр,
		|	1                                                                КАК Оплачивается
		|ПОМЕСТИТЬ РасчетыСКлиентамиИзменения
		|ИЗ
		|	ДанныеРасчетов КАК РасчетыСКлиентами
		|ГДЕ
		|	РасчетыСКлиентами.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
		|	И РасчетыСКлиентами.ОбъектРасчетовПриемник <> ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерамПриемник,
		|	РасчетыСКлиентами.ОбъектРасчетовПриемник,
		|	РасчетыСКлиентами.ВалютаПриемник,
		|	РасчетыСКлиентами.Документ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Строки для связи источника и приемника
		|ВЫБРАТЬ
		|	МИНИМУМ(РасчетыСКлиентами.ПорядокОперации)                   КАК ПорядокОперации,
		|	МАКСИМУМ(РасчетыСКлиентами.НачальноеЗаполнение)              КАК НачальноеЗаполнение,
		|	МИНИМУМ(РасчетыСКлиентами.Период)                            КАК Период,
		|	МИНИМУМ(РасчетыСКлиентами.ПериодКонтроляИзменений)           КАК ПериодКонтроляИзменений,
		|	МИНИМУМ(РасчетыСКлиентами.ДатаРегистратора)                  КАК ДатаРегистратора,
		|	
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам                  КАК АналитикаУчетаПоПартнерам,
		|	РасчетыСКлиентами.ОбъектРасчетов                             КАК ОбъектРасчетов,
		|	РасчетыСКлиентами.ВалютаРасчетов                             КАК ВалютаРасчетов,
		|	
		|	РасчетыСКлиентами.Документ                                   КАК Документ,
		|	
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерамПриемник          КАК АналитикаУчетаПоПартнерамПриемник,
		|	РасчетыСКлиентами.ОбъектРасчетовПриемник                     КАК ОбъектРасчетовПриемник,
		|	РасчетыСКлиентами.ВалютаПриемник                             КАК ВалютаПриемник,
		|	
		|	МАКСИМУМ(РасчетыСКлиентами.ПоДаннымОбъектаРасчетовИсточника) КАК ПоДаннымОбъектаРасчетовИсточника,
		|	НЕОПРЕДЕЛЕНО                                                 КАК Заказ,
		|	1                                                            КАК КОплате,
		|	1                                                            КАК КОтгрузке,
		|	1                                                            КАК Сумма,
		|	1                                                            КАК СуммаРегл,
		|	1                                                            КАК СуммаУпр,
		|	1                                                            КАК Оплачивается
		|ИЗ
		|	ДанныеРасчетов КАК РасчетыСКлиентами
		|ГДЕ
		|	РасчетыСКлиентами.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам,
		|	РасчетыСКлиентами.ОбъектРасчетов,
		|	РасчетыСКлиентами.ВалютаРасчетов,
		|	РасчетыСКлиентами.Документ,
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерамПриемник,
		|	РасчетыСКлиентами.ОбъектРасчетовПриемник,
		|	РасчетыСКлиентами.ВалютаПриемник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Объекты расчетов приемники изменений, которые не добавлены к распределению
		|ВЫБРАТЬ
		|	МИНИМУМ(РасчетыСПоставщиками.ПорядокОперации)                    КАК ПорядокОперации,
		|	МАКСИМУМ(РасчетыСПоставщиками.НачальноеЗаполнение)               КАК НачальноеЗаполнение,
		|	МИНИМУМ(РасчетыСПоставщиками.Период)                             КАК Период,
		|	МИНИМУМ(РасчетыСПоставщиками.ПериодКонтроляИзменений)            КАК ПериодКонтроляИзменений,
		|	МИНИМУМ(РасчетыСПоставщиками.ДатаРегистратора)                   КАК ДатаРегистратора,
		|	
		|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерамПриемник           КАК АналитикаУчетаПоПартнерам,
		|	РасчетыСПоставщиками.ОбъектРасчетовПриемник                      КАК ОбъектРасчетов,
		|	РасчетыСПоставщиками.ВалютаПриемник                              КАК ВалютаРасчетов,
		|	
		|	РасчетыСПоставщиками.Документ                                    КАК Документ,
		|	
		|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) КАК АналитикаУчетаПоПартнерамПриемник,
		|	ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)                КАК ОбъектРасчетовПриемник,
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)                         КАК ВалютаПриемник,
		|	
		|	ИСТИНА                                                           КАК ПоДаннымОбъектаРасчетовИсточника,
		|	НЕОПРЕДЕЛЕНО                                                     КАК Заказ,
		|	1                                                                КАК КОплате,
		|	1                                                                КАК КПоступлению,
		|	1                                                                КАК Сумма,
		|	1                                                                КАК СуммаРегл,
		|	1                                                                КАК СуммаУпр,
		|	1                                                                КАК Оплачивается
		|ПОМЕСТИТЬ РасчетыСПоставщикамиИзменения
		|ИЗ
		|	ДанныеРасчетов КАК РасчетыСПоставщиками
		|ГДЕ
		|	РасчетыСПоставщиками.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
		|	И РасчетыСПоставщиками.ОбъектРасчетовПриемник <> ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерамПриемник,
		|	РасчетыСПоставщиками.ОбъектРасчетовПриемник,
		|	РасчетыСПоставщиками.ВалютаПриемник,
		|	РасчетыСПоставщиками.Документ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Строки для связи источника и приемника
		|ВЫБРАТЬ
		|	МИНИМУМ(РасчетыСПоставщиками.ПорядокОперации)                   КАК ПорядокОперации,
		|	МАКСИМУМ(РасчетыСПоставщиками.НачальноеЗаполнение)              КАК НачальноеЗаполнение,
		|	МИНИМУМ(РасчетыСПоставщиками.Период)                            КАК Период,
		|	МИНИМУМ(РасчетыСПоставщиками.ПериодКонтроляИзменений)           КАК ПериодКонтроляИзменений,
		|	МИНИМУМ(РасчетыСПоставщиками.ДатаРегистратора)                  КАК ДатаРегистратора,
		|	
		|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам                  КАК АналитикаУчетаПоПартнерам,
		|	РасчетыСПоставщиками.ОбъектРасчетов                             КАК ОбъектРасчетов,
		|	РасчетыСПоставщиками.ВалютаРасчетов                             КАК ВалютаРасчетов,
		|	
		|	РасчетыСПоставщиками.Документ                                   КАК Документ,
		|	
		|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерамПриемник          КАК АналитикаУчетаПоПартнерамПриемник,
		|	РасчетыСПоставщиками.ОбъектРасчетовПриемник                     КАК ОбъектРасчетовПриемник,
		|	РасчетыСПоставщиками.ВалютаПриемник                             КАК ВалютаПриемник,
		|	
		|	МАКСИМУМ(РасчетыСПоставщиками.ПоДаннымОбъектаРасчетовИсточника) КАК ПоДаннымОбъектаРасчетовИсточника,
		|	НЕОПРЕДЕЛЕНО                                                    КАК Заказ,
		|	1                                                               КАК КОплате,
		|	1                                                               КАК КПоступлению,
		|	1                                                               КАК Сумма,
		|	1                                                               КАК СуммаРегл,
		|	1                                                               КАК СуммаУпр,
		|	1                                                               КАК Оплачивается
		|ИЗ
		|	ДанныеРасчетов КАК РасчетыСПоставщиками
		|ГДЕ
		|	РасчетыСПоставщиками.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам,
		|	РасчетыСПоставщиками.ОбъектРасчетов,
		|	РасчетыСПоставщиками.ВалютаРасчетов,
		|	РасчетыСПоставщиками.Документ,
		|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерамПриемник,
		|	РасчетыСПоставщиками.ОбъектРасчетовПриемник,
		|	РасчетыСПоставщиками.ВалютаПриемник;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДокументыТаблицыИзмененийСПриемниками;
		|УНИЧТОЖИТЬ НепроведеныеДокументыТаблицыИзмененийСПриемниками";
	
	Запрос.Выполнить();
	
	ТаблицаИзмененийПриемников = ТаблицаИзмененийДляПересчета(Запрос.МенеджерВременныхТаблиц, Параметры.Регистратор);
	
	Если ТаблицаИзмененийПриемников.Количество() > 0 Тогда
		Для Каждого СтрокаПриемника Из ТаблицаИзмененийПриемников Цикл
			ЗаполнитьЗначенияСвойств(РассчитываемыеИзмерения.Добавить(), СтрокаПриемника);
		КонецЦикла;
		ДополнитьТаблицуИзмененийПриемниками(ТаблицаИзмененийПриемников, Параметры, РассчитываемыеИзмерения);
	КонецЕсли;
	
	Для Каждого СтрокаПриемника Из ТаблицаИзмененийПриемников Цикл
		НоваяСтрока = ТаблицаИзменений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПриемника);
	КонецЦикла; 
	
КонецПроцедуры

Функция ТаблицаОстатковВзаиморасчетов(Запрос, ЭтоРасчетыСКлиентами);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Остатки.ОбъектРасчетов                                                            КАК ОбъектРасчетов,
	|	Остатки.АналитикаУчетаПоПартнерам                                                 КАК АналитикаУчетаПоПартнерам,
	|	Остатки.ВалютаРасчетов                                                            КАК ВалютаРасчетов,
	|
	|	Остатки.РасчетныйДокумент                                                         КАК РасчетныйДокумент,
	|	Остатки.ДатаВозникновения                                                         КАК ДатаВозникновения,
	|	Остатки.ДатаПлановогоПогашения                                                    КАК ДатаПлановогоПогашения,
	//Нужно для сортировки зачитываемых остатков при создании починочных записей.
	|	Остатки.ПорядокОперации                                                           КАК ПорядокОперации,
	|	Остатки.ПорядокЗачета                                                             КАК ПорядокЗачетаПоДатеПлатежа,
	|
	|	НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ВтПараметрыРасчета.ПараметрДатаНачалаПересчета,СЕКУНДА,-1),ДЕНЬ) КАК ДатаПереоценки,
	|	Остатки.Долг                                                                КАК Долг,
	|	Остатки.ДолгРегл                                                            КАК ДолгРегл,
	|	Остатки.ДолгУпр                                                             КАК ДолгУпр,
	|	Остатки.Предоплата                                                          КАК Предоплата,
	|	Остатки.ПредоплатаРегл                                                      КАК ПредоплатаРегл,
	|	Остатки.ПредоплатаУпр                                                       КАК ПредоплатаУпр
	|ИЗ
	|	ВТРасчетыПоСрокамОстаткиКлиенты КАК Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДополненныеПараметрыРаспределения КАК ВтПараметрыРасчета
	|			ПО Остатки.ОбъектРасчетов = ВтПараметрыРасчета.ПараметрОбъектРасчетов
	|				И Остатки.АналитикаУчетаПоПартнерам = ВтПараметрыРасчета.ПараметрАналитикаУчетаПоПартнерам
	|				И Остатки.ВалютаРасчетов = ВтПараметрыРасчета.ПараметрВалютаРасчетов
	|ГДЕ
	|	Остатки.ЕстьПредоплата ИЛИ Остатки.ЕстьДолг
	|УПОРЯДОЧИТЬ ПО
	|	Остатки.ПорядокОперации,
	|	ВЫБОР КОГДА Остатки.ДатаПлановогоПогашения = ДАТАВРЕМЯ(1,1,1) И Остатки.Предоплата > 0
	|		ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ 
	|";
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,"ВТРасчетыПоСрокамОстаткиКлиенты","ВТРасчетыПоСрокамОстаткиПоставщики");
	КонецЕсли;
	
	Результат     = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
КонецФункции

#КонецОбласти

#Область ОбщиеМетодыДобавленияСтрок

//Переоценивает долг до момента оплаты или по на текущий месяц
Процедура ПереоценитьДолг(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, РасчетныйДокумент, ЗНАЧ Дата, ПереоценитьНаПереданнуюДату = Ложь)
	
	Если ГлобальныеПеременные.ВариантКурсаДоговора = Перечисления.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки 
		ИЛИ НЕ ГлобальныеПеременные.Свойство("ТаблицаКурсовВалют") Тогда
		Возврат;
	КонецЕсли;
	
	Если Дата <> Неопределено Тогда
		Дата = НачалоДня(Дата);
	КонецЕсли;
	
	СтрокиДляПереоценки = ТаблицаОстатковВзаиморасчетов.НайтиСтроки(Новый Структура("РасчетныйДокумент",РасчетныйДокумент));
	Для Каждого СтрокаОтгрузки Из СтрокиДляПереоценки Цикл
		
		Если СтрокаОтгрузки.Долг = 0 И НЕ ПереоценитьНаПереданнуюДату Тогда
			Продолжить;
		КонецЕсли;
		
		Если ГлобальныеПеременные.ВариантКурсаДоговора = Перечисления.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки Тогда
		#Область ФиксированныйНаДатуОтгрузкиКурс
			// Переоценка до фиксированного курса возможна после частичной оплаты по другому ручному курсу или оплате в валюте, отличной от  валюты регл. учета
			Если Дата <> Неопределено Тогда
				СуммаПереоценкиРегл = Окр(СтрокаОтгрузки.Долг * ГлобальныеПеременные.ФиксированныйКурсРегл, 2) - СтрокаОтгрузки.СуммаРегл;
				СуммаПереоценкиУпр = Окр(СтрокаОтгрузки.Долг * ГлобальныеПеременные.ФиксированныйКурсУпр, 2) - СтрокаОтгрузки.СуммаУпр;
				ДобавитьЗаписьПереоценки(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, Дата, СтрокаОтгрузки, "Долг", СуммаПереоценкиРегл, СуммаПереоценкиУпр);
				СтрокаОтгрузки.ДатаПереоценки = Дата;
			КонецЕсли;
			Продолжить;
		#КонецОбласти
		КонецЕсли;
		
		#Область ЕжемесячнаяПереоценка
		Если ГлобальныеПеременные.ЕжедневнаяПереоценка Тогда
			ДатаКэш = НачалоДня(СтрокаОтгрузки.ДатаПереоценки);
		Иначе
			ДатаКэш = НачалоДня(КонецМесяца(СтрокаОтгрузки.ДатаПереоценки));
		КонецЕсли;
		
		ОкончательнаяДатаПереоценки = ?(Дата <> Неопределено, Дата, ТекущаяДатаСеанса());
		Пока ДатаКэш < ОкончательнаяДатаПереоценки Цикл
			
			СтрокаКурса = ГлобальныеПеременные.ТаблицаКурсовВалют.Найти(ДатаКэш, "Дата");
			
			Если СтрокаКурса <> Неопределено Тогда
				СуммаПереоценкиРегл = Окр(СтрокаОтгрузки.Долг * СтрокаКурса.КурсРегл, 2) - СтрокаОтгрузки.ДолгРегл;
				СуммаПереоценкиУпр = Окр(СтрокаОтгрузки.Долг * СтрокаКурса.КурсРегл / СтрокаКурса.КурсУпр, 2) - СтрокаОтгрузки.ДолгУпр;
				ДобавитьЗаписьПереоценки(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, КонецДня(ДатаКэш), СтрокаОтгрузки, "Долг", СуммаПереоценкиРегл, СуммаПереоценкиУпр);
				СтрокаОтгрузки.ДатаПереоценки = ДатаКэш;
			КонецЕсли;
			
			Если ГлобальныеПеременные.ЕжедневнаяПереоценка Тогда
				ДатаКэш = ДатаКэш + 86400;
			Иначе
				ДатаКэш = НачалоДня(КонецМесяца(КонецМесяца(ДатаКэш)+1));
			КонецЕсли;
			
		КонецЦикла;
		
		#КонецОбласти
		
		#Область ПереоценкаНаПереданнуюДату
		Если Дата <> Неопределено И Дата > СтрокаОтгрузки.ДатаПереоценки 
			И ПереоценитьНаПереданнуюДату Тогда
			
			СтрокаКурса = ГлобальныеПеременные.ТаблицаКурсовВалют.Найти(Дата, "Дата");
			Если СтрокаКурса <> Неопределено Тогда 
				СуммаПереоценкиРегл = Окр(СтрокаОтгрузки.Долг * СтрокаКурса.КурсРегл, 2) - СтрокаОтгрузки.ДолгРегл;
				СуммаПереоценкиУпр = Окр(СтрокаОтгрузки.Долг  * СтрокаКурса.КурсРегл / СтрокаКурса.КурсУпр, 2) - СтрокаОтгрузки.ДолгУпр;
				ДобавитьЗаписьПереоценки(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, Дата, СтрокаОтгрузки, "Долг", СуммаПереоценкиРегл, СуммаПереоценкиУпр);
				СтрокаОтгрузки.ДатаПереоценки = Дата;
			КонецЕсли;
			
		КонецЕсли;
		#КонецОбласти
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СписатьРеглУпрПоНулевымОстаткам(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов,ПериодПереоценки)
	СтрокиДляПереоценки = ТаблицаОстатковВзаиморасчетов.НайтиСтроки(Новый Структура("Долг,Предоплата",0,0));
	Для Каждого СтрокаДляПереоценки из СтрокиДляПереоценки Цикл
		Если СтрокаДляПереоценки.ДолгРегл <> 0 Или СтрокаДляПереоценки.ДолгУпр <> 0 Тогда
			ДобавитьЗаписьПереоценки(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, ПериодПереоценки, СтрокаДляПереоценки, "Долг", -СтрокаДляПереоценки.ДолгРегл, -СтрокаДляПереоценки.ДолгУпр);
		КонецЕсли;
		Если СтрокаДляПереоценки.ПредоплатаРегл <> 0 Или СтрокаДляПереоценки.ПредоплатаУпр <> 0 Тогда
			ДобавитьЗаписьПереоценки(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, ПериодПереоценки, СтрокаДляПереоценки, "Предоплата", -СтрокаДляПереоценки.ПредоплатаРегл, -СтрокаДляПереоценки.ПредоплатаУпр);
		КонецЕсли;
		ТаблицаОстатковВзаиморасчетов.Удалить(СтрокаДляПереоценки);
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьПриход(ГлобальныеПеременные,ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаРасчетов, ТипИсточника)
	
	Если СтрокаРасчетов.Период >= ГлобальныеПеременные.ДатаНачалаПересчета Тогда
	
		НовСтр = ТаблицаРасчетовПоСрокам.Добавить();
		НовСтр.ВидДвижения                   = ВидДвиженияНакопления.Приход;
		НовСтр.ДокументРегистратор           = СтрокаРасчетов.Регистратор;
		НовСтр.РасчетныйДокумент             = СтрокаРасчетов.РасчетныйДокумент;
		Если НЕ ЗначениеЗаполнено(НовСтр.РасчетныйДокумент) Тогда
			НовСтр.РасчетныйДокумент = СтрокаРасчетов.Регистратор;
		КонецЕсли;
		НовСтр.ХозяйственнаяОперация         = СтрокаРасчетов.ХозяйственнаяОперация;
		НовСтр.ВалютаДокумента               = СтрокаРасчетов.ВалютаДокумента;
		НовСтр.ДатаВозникновения             = ?(СтрокаРасчетов.Сторно, НачалоДня(СтрокаРасчетов.Период), СтрокаРасчетов.ДатаВозникновения);
		НовСтр.Период                        = СтрокаРасчетов.Период;
		НовСтр.ПорядокОперации               = СтрокаРасчетов.ПорядокОперации;
		НовСтр.СтатьяДвиженияДенежныхСредств = СтрокаРасчетов.СтатьяДвиженияДенежныхСредств;
		НовСтр.НастройкаХозяйственнойОперации = СтрокаРасчетов.НастройкаХозяйственнойОперации;
		НовСтр.ИдентификаторФинЗаписи        = СтрокаРасчетов.ИдентификаторФинЗаписи;
		НовСтр.КорОбъектРасчетов             = СтрокаРасчетов.КорОбъектРасчетов;
		НовСтр.КорАналитикаУчетаПоПартнерам  = СтрокаРасчетов.КорАналитикаУчетаПоПартнерам;
		НовСтр.АналитикаУчетаПоПартнерамПриемник = СтрокаРасчетов.АналитикаУчетаПоПартнерамПриемник;
		НовСтр.ОбъектРасчетовПриемник            = СтрокаРасчетов.ОбъектРасчетовПриемник;
		НовСтр.ВалютаПриемник                    = СтрокаРасчетов.ВалютаПриемник;
		НовСтр.СуммаПриемник                     = СтрокаРасчетов.СуммаПриемник;
		НовСтр.ПоДаннымОбъектаРасчетовИсточника  = СтрокаРасчетов.ПоДаннымОбъектаРасчетовИсточника;
		
		НовСтр.Сторно                        = Ложь;
		
		Если ТипИсточника = "Оплата" Тогда
		
			НовСтр.СвязанныйДокумент     = СтрокаРасчетов.СвязанныйДокумент;
		
			НовСтр.Долг           = 0;
			НовСтр.ДолгРегл       = 0;
			НовСтр.ДолгУпр        = 0;
		
			НовСтр.Предоплата     = СтрокаРасчетов.Сумма;
			НовСтр.ПредоплатаРегл = СтрокаРасчетов.СуммаРегл;
			НовСтр.ПредоплатаУпр  = СтрокаРасчетов.СуммаУпр;
			
			// Если остаток образован сторно записью, то дата погашения равна дате возникновения
			Если СтрокаРасчетов.Сторно Тогда
				НовСтр.ДатаПлановогоПогашения = НачалоДня(СтрокаРасчетов.Период);
			Иначе
				НовСтр.ДатаПлановогоПогашения = СтрокаРасчетов.ДатаПлановогоПогашения;
			КонецЕсли;
			
			НовСтр.ПорядокЗачета = ?(НовСтр.ДатаПлановогоПогашения= Дата(1,1,1),
												СтрокаРасчетов.ПорядокЗачетаПоДатеПлатежа,
												Формат(НовСтр.ДатаПлановогоПогашения , "ДФ=yyyyMMdd") + Прав(СтрокаРасчетов.ПорядокЗачетаПоДатеПлатежа,20));
		ИначеЕсли ТипИсточника = "ОтгрузкаПоставка" Тогда
		
			НовСтр.Долг           = СтрокаРасчетов.Сумма;
			НовСтр.ДолгРегл       = СтрокаРасчетов.СуммаРегл;
			НовСтр.ДолгУпр        = СтрокаРасчетов.СуммаУпр;
			
			НовСтр.Предоплата     = 0;
			НовСтр.ПредоплатаРегл = 0;
			НовСтр.ПредоплатаУпр  = 0;
			// Если остаток образован сторно записью, то дата погашения равна дате возникновения
			Если СтрокаРасчетов.Сторно Тогда
				НовСтр.ДатаПлановогоПогашения = НачалоДня(СтрокаРасчетов.Период);
			Иначе
				НовСтр.ДатаПлановогоПогашения = СтрокаРасчетов.ДатаПлановогоПогашения;
			КонецЕсли;
			НовСтр.ПорядокЗачета = СтрокаРасчетов.ПорядокЗачетаПоДатеПлатежа;
		КонецЕсли;
		НовСтр.ЗаписьДоНачалаРасчета = Ложь;
		НовСтр.ОперацияВзаиморасчетов = СтрокаРасчетов.ОперацияВзаиморасчетов;
		
		ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов, НовСтр);
	КонецЕсли;

	Если ТипИсточника = "ОтгрузкаПоставка" Тогда
		//Записи в ДанныеНакладных может не быть.
		Если ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.Регистратор] = Неопределено Тогда
			ГлобальныеПеременные.ДанныеНакладных.Вставить(СтрокаРасчетов.Регистратор, СтруктураСумм());
		КонецЕсли;
		
		// Если добавляем приход, значит авансы уже зачтены, и осталась часть долга по курсу накладной.
		ДанныеНакладной = ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.Регистратор];
		ДанныеНакладной.СуммаДокумента     = ДанныеНакладной.СуммаДокумента + СтрокаРасчетов.Сумма;
		ДанныеНакладной.СуммаДокументаРегл = ДанныеНакладной.СуммаДокументаРегл + СтрокаРасчетов.СуммаРегл;
		ДанныеНакладной.СуммаДокументаУпр  = ДанныеНакладной.СуммаДокументаУпр  + СтрокаРасчетов.СуммаУпр;
	КонецЕсли;
			
КонецПроцедуры

Процедура ДобавитьРасходДолга(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаОстатков, СтрокаРасчетов, СуммаСписания, СуммаСписанияРегл, СуммаСписанияУпр, ХозяйственнаяОперация = Неопределено);
	
	НовСтр_Долг_Расход = ТаблицаРасчетовПоСрокам.Добавить();
	НовСтр_Долг_Расход.ВидДвижения                   = ВидДвиженияНакопления.Расход;
	НовСтр_Долг_Расход.ДокументРегистратор           = СтрокаРасчетов.Регистратор;
	НовСтр_Долг_Расход.Период                        = СтрокаРасчетов.ПериодЗачета;
	НовСтр_Долг_Расход.ПорядокОперации               = СтрокаРасчетов.ПорядокЗачета;
	
	НовСтр_Долг_Расход.РасчетныйДокумент             = СтрокаОстатков.РасчетныйДокумент;
	НовСтр_Долг_Расход.ДатаВозникновения             = СтрокаОстатков.ДатаВозникновения;
	НовСтр_Долг_Расход.ДатаПлановогоПогашения        = СтрокаОстатков.ДатаПлановогоПогашения;
	НовСтр_Долг_Расход.ПорядокЗачета                 = СтрокаОстатков.ПорядокЗачетаПоДатеПлатежа;
	
	НовСтр_Долг_Расход.НастройкаХозяйственнойОперации = СтрокаРасчетов.НастройкаХозяйственнойОперации;
	НовСтр_Долг_Расход.ИдентификаторФинЗаписи         = СтрокаРасчетов.ИдентификаторФинЗаписи;
	
	НовСтр_Долг_Расход.ХозяйственнаяОперация         = ?(ХозяйственнаяОперация = Неопределено, СтрокаРасчетов.ХозяйственнаяОперация, ХозяйственнаяОперация);
	НовСтр_Долг_Расход.ОперацияВзаиморасчетов        = СтрокаРасчетов.ОперацияВзаиморасчетов;
	НовСтр_Долг_Расход.ВалютаДокумента               = СтрокаРасчетов.ВалютаДокумента;
	НовСтр_Долг_Расход.СтатьяДвиженияДенежныхСредств = СтрокаРасчетов.СтатьяДвиженияДенежныхСредств;
	НовСтр_Долг_Расход.Сторно                        = Ложь;
	НовСтр_Долг_Расход.СвязанныйДокумент             = СтрокаРасчетов.СвязанныйДокумент;
	
	//С приемником связан только регистратор оплаты
	НовСтр_Долг_Расход.АналитикаУчетаПоПартнерамПриемник = СтрокаРасчетов.АналитикаУчетаПоПартнерамПриемник;
	НовСтр_Долг_Расход.ОбъектРасчетовПриемник            = СтрокаРасчетов.ОбъектРасчетовПриемник;
	НовСтр_Долг_Расход.ВалютаПриемник                    = СтрокаРасчетов.ВалютаПриемник;
	НовСтр_Долг_Расход.СуммаПриемник                     = СуммаСписания(0, СтрокаРасчетов.СуммаПриемник, СтрокаРасчетов.Сумма, СуммаСписания);
	СтрокаРасчетов.СуммаПриемник = СтрокаРасчетов.СуммаПриемник - НовСтр_Долг_Расход.СуммаПриемник;
	НовСтр_Долг_Расход.ПоДаннымОбъектаРасчетовИсточника  = СтрокаРасчетов.ПоДаннымОбъектаРасчетовИсточника;
	НовСтр_Долг_Расход.КорОбъектРасчетов                 = СтрокаРасчетов.КорОбъектРасчетов;
	НовСтр_Долг_Расход.КорАналитикаУчетаПоПартнерам      = СтрокаРасчетов.КорАналитикаУчетаПоПартнерам;
	
	НовСтр_Долг_Расход.Долг     = СуммаСписания;
	НовСтр_Долг_Расход.ДолгРегл = СуммаСписанияРегл;
	НовСтр_Долг_Расход.ДолгУпр  = СуммаСписанияУпр;
	НовСтр_Долг_Расход.ЗаписьДоНачалаРасчета = Ложь;
	НовСтр_Долг_Расход.Предоплата = 0;
	НовСтр_Долг_Расход.ПредоплатаРегл = 0;
	НовСтр_Долг_Расход.ПредоплатаУпр = 0;
	
	ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов, НовСтр_Долг_Расход);
	
КонецПроцедуры

Функция ДобавитьРасходПредоплаты(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, СтрокаОстатков, СтрокаРасчетов, СуммаСписания, СуммаСписанияРегл, СуммаСписанияУпр, ЗаполнятьПриемникиИсточники = Истина);
	
	НовСтр_Предоплата_Расход                          = ТаблицаРасчетовПоСрокам.Добавить();
	НовСтр_Предоплата_Расход.ВидДвижения              = ВидДвиженияНакопления.Расход;
	
	НовСтр_Предоплата_Расход.ДокументРегистратор      = СтрокаРасчетов.Регистратор;
	НовСтр_Предоплата_Расход.ВалютаДокумента          = СтрокаРасчетов.ВалютаДокумента;
	НовСтр_Предоплата_Расход.Период                   = СтрокаРасчетов.ПериодЗачета;
	НовСтр_Предоплата_Расход.РасчетныйДокумент        = СтрокаОстатков.РасчетныйДокумент;
	НовСтр_Предоплата_Расход.ДатаВозникновения        = СтрокаОстатков.ДатаВозникновения;
	НовСтр_Предоплата_Расход.ДатаПлановогоПогашения   = СтрокаОстатков.ДатаПлановогоПогашения;
	НовСтр_Предоплата_Расход.ПорядокЗачета            = СтрокаОстатков.ПорядокЗачетаПоДатеПлатежа;
	НовСтр_Предоплата_Расход.ПорядокОперации          = СтрокаРасчетов.ПорядокЗачета;
	НовСтр_Предоплата_Расход.СтатьяДвиженияДенежныхСредств = СтрокаРасчетов.СтатьяДвиженияДенежныхСредств;
	
	Если ЗаполнятьПриемникиИсточники  Тогда
		НовСтр_Предоплата_Расход.АналитикаУчетаПоПартнерамПриемник = СтрокаРасчетов.АналитикаУчетаПоПартнерамПриемник;
		НовСтр_Предоплата_Расход.ОбъектРасчетовПриемник  = СтрокаРасчетов.ОбъектРасчетовПриемник;
		НовСтр_Предоплата_Расход.ВалютаПриемник  = СтрокаРасчетов.ВалютаПриемник;
		НовСтр_Предоплата_Расход.СуммаПриемник = СуммаСписания(0, СтрокаРасчетов.СуммаПриемник, СтрокаРасчетов.Сумма, СуммаСписания);
		СтрокаРасчетов.СуммаПриемник = СтрокаРасчетов.СуммаПриемник - НовСтр_Предоплата_Расход.СуммаПриемник;
		
		НовСтр_Предоплата_Расход.ПоДаннымОбъектаРасчетовИсточника = СтрокаРасчетов.ПоДаннымОбъектаРасчетовИсточника;
		НовСтр_Предоплата_Расход.КорОбъектРасчетов                = СтрокаРасчетов.КорОбъектРасчетов;
		НовСтр_Предоплата_Расход.КорАналитикаУчетаПоПартнерам     = СтрокаРасчетов.КорАналитикаУчетаПоПартнерам;
		
	Иначе
		НовСтр_Предоплата_Расход.АналитикаУчетаПоПартнерамПриемник = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
		НовСтр_Предоплата_Расход.ОбъектРасчетовПриемник  = Справочники.ОбъектыРасчетов.ПустаяСсылка();
		НовСтр_Предоплата_Расход.ВалютаПриемник  = Справочники.Валюты.ПустаяСсылка();
		НовСтр_Предоплата_Расход.СуммаПриемник = 0;
		
		НовСтр_Предоплата_Расход.ПоДаннымОбъектаРасчетовИсточника = ЛОЖЬ;
		НовСтр_Предоплата_Расход.КорОбъектРасчетов                = Справочники.ОбъектыРасчетов.ПустаяСсылка();
		НовСтр_Предоплата_Расход.КорАналитикаУчетаПоПартнерам     = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
	КонецЕсли;
	
	НовСтр_Предоплата_Расход.Сторно                   = Ложь;
	НовСтр_Предоплата_Расход.ХозяйственнаяОперация    = СтрокаРасчетов.ХозяйственнаяОперация;
	НовСтр_Предоплата_Расход.ИдентификаторФинЗаписи   = СтрокаРасчетов.ИдентификаторФинЗаписи;
	НовСтр_Предоплата_Расход.НастройкаХозяйственнойОперации = СтрокаРасчетов.НастройкаХозяйственнойОперации;
	
	НовСтр_Предоплата_Расход.Предоплата     = СуммаСписания;
	НовСтр_Предоплата_Расход.ПредоплатаРегл = СуммаСписанияРегл;
	НовСтр_Предоплата_Расход.ПредоплатаУпр  = СуммаСписанияУпр;
	НовСтр_Предоплата_Расход.ЗаписьДоНачалаРасчета = Ложь;
	НовСтр_Предоплата_Расход.ОперацияВзаиморасчетов = СтрокаРасчетов.ОперацияВзаиморасчетов;
	НовСтр_Предоплата_Расход.Долг           = 0;
	НовСтр_Предоплата_Расход.ДолгРегл       = 0;
	НовСтр_Предоплата_Расход.ДолгУпр        = 0;
	
	Возврат НовСтр_Предоплата_Расход;
КонецФункции

//Добавляет записи переоценки долга.
Процедура ДобавитьЗаписьПереоценки(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, Период, СтрокаРасчетов, Тип, СуммаРегл, СуммаУпр)
	
	Если НЕ ГлобальныеПеременные.Свойство("ТаблицаКурсовВалют")
		ИЛИ СуммаРегл = 0 И СуммаУпр = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПорядокПереоценки = ГлобальныеПеременные.ПорядокПереоценки.Получить(Период);
	Если ПорядокПереоценки = Неопределено Тогда
		ПорядокПереоценки = Порядок(Период,"", Тип("ДокументСсылка.РасчетКурсовыхРазниц"),"9","99");
		ГлобальныеПеременные.ПорядокПереоценки.Вставить(Период, ПорядокПереоценки);
	КонецЕсли;
	
	Если Период < ГлобальныеПеременные.ДатаНачалаПересчета Тогда
		Возврат;
	КонецЕсли;
	
	Если СуммаРегл < 0 ИЛИ СуммаУпр < 0 Тогда
		СтрокаПереоценки = ТаблицаРасчетовПоСрокам.Добавить();
		СтрокаПереоценки.ВидДвижения              = ВидДвиженияНакопления.Расход;
		СтрокаПереоценки.ДокументРегистратор      = ГлобальныеПеременные.ДокументыРасчетаКурсовыхРазниц[НачалоМесяца(Период)];
		СтрокаПереоценки.ХозяйственнаяОперация    = ГлобальныеПеременные.ХозяйственныеОперации["ПереоценкаОтрицательная"];
		СтрокаПереоценки.РасчетныйДокумент        = СтрокаРасчетов.РасчетныйДокумент;
		СтрокаПереоценки.ДатаВозникновения        = СтрокаРасчетов.ДатаВозникновения;
		СтрокаПереоценки.НастройкаХозяйственнойОперации = ГлобальныеПеременные.НастройкиХО["ПереоценкаОтрицательная"];
		СтрокаПереоценки.ИдентификаторФинЗаписи   = Новый УникальныйИдентификатор;
		СтрокаПереоценки.ПорядокЗачета            = ПорядокПереоценки;
		СтрокаПереоценки.Период                   = КонецДня(Период);
		СтрокаПереоценки.ПорядокОперации          = ПорядокПереоценки;
		СтрокаПереоценки.СтатьяДвиженияДенежныхСредств = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка();
		СтрокаПереоценки.Долг = 0;
		СтрокаПереоценки.Предоплата = 0;
		
		Если Тип = "Долг" Тогда
			СтрокаПереоценки.ДолгРегл             = ?(СуммаРегл < 0, -СуммаРегл, 0);
			СтрокаПереоценки.ДолгУпр              = ?(СуммаУпр < 0, -СуммаУпр, 0);
			СтрокаПереоценки.ПредоплатаРегл = 0;
			СтрокаПереоценки.ПредоплатаУпр = 0;
			СтрокаПереоценки.ДатаПлановогоПогашения   = СтрокаРасчетов.ДатаПлановогоПогашения;
		Иначе
			СтрокаПереоценки.ПредоплатаРегл       = ?(СуммаРегл < 0, -СуммаРегл, 0);
			СтрокаПереоценки.ПредоплатаУпр        = ?(СуммаУпр < 0, -СуммаУпр, 0);
			СтрокаПереоценки.ДатаПлановогоПогашения   = Дата(1,1,1);
			СтрокаПереоценки.ДолгРегл = 0;
			СтрокаПереоценки.ДолгУпр = 0;
		КонецЕсли;
		СтрокаПереоценки.ЗаписьДоНачалаРасчета = Ложь;
		СтрокаПереоценки.ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.КорректировкаЗадолженности;
		
		СтрокаПереоценки.КорОбъектРасчетов            = Справочники.ОбъектыРасчетов.ПустаяСсылка();
		СтрокаПереоценки.КорАналитикаУчетаПоПартнерам = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
		СтрокаПереоценки.АналитикаУчетаПоПартнерамПриемник = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
		СтрокаПереоценки.ОбъектРасчетовПриемник            = Справочники.ОбъектыРасчетов.ПустаяСсылка();
		СтрокаПереоценки.ВалютаПриемник            = Справочники.Валюты.ПустаяСсылка();
		СтрокаПереоценки.СуммаПриемник = 0;
		СтрокаПереоценки.ПоДаннымОбъектаРасчетовИсточника = ЛОЖЬ;
		СтрокаПереоценки.Сторно = Ложь;
		
		ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов, СтрокаПереоценки, СтрокаРасчетов);
	КонецЕсли;
	
	Если СуммаРегл > 0 ИЛИ СуммаУпр > 0 Тогда
		СтрокаПереоценки = ТаблицаРасчетовПоСрокам.Добавить();
		СтрокаПереоценки.ВидДвижения              = ВидДвиженияНакопления.Приход;
		СтрокаПереоценки.ДокументРегистратор      = ГлобальныеПеременные.ДокументыРасчетаКурсовыхРазниц[НачалоМесяца(Период)];
		СтрокаПереоценки.ХозяйственнаяОперация    = ГлобальныеПеременные.ХозяйственныеОперации["ПереоценкаПоложительная"];
		СтрокаПереоценки.РасчетныйДокумент        = СтрокаРасчетов.РасчетныйДокумент;
		СтрокаПереоценки.ДатаВозникновения        = СтрокаРасчетов.ДатаВозникновения;
		СтрокаПереоценки.НастройкаХозяйственнойОперации = ГлобальныеПеременные.НастройкиХО["ПереоценкаПоложительная"];
		СтрокаПереоценки.ИдентификаторФинЗаписи   = Новый УникальныйИдентификатор;
		СтрокаПереоценки.ПорядокЗачета            = ПорядокПереоценки;
		СтрокаПереоценки.Период                   = КонецДня(Период);
		СтрокаПереоценки.ПорядокОперации          = ПорядокПереоценки;
		СтрокаПереоценки.СтатьяДвиженияДенежныхСредств = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка();
		СтрокаПереоценки.Долг = 0;
		СтрокаПереоценки.Предоплата = 0;
		
		Если Тип = "Долг" Тогда
			СтрокаПереоценки.ДолгРегл               = ?(СуммаРегл > 0, СуммаРегл, 0);
			СтрокаПереоценки.ДолгУпр                = ?(СуммаУпр > 0, СуммаУпр, 0);
			СтрокаПереоценки.ПредоплатаРегл = 0;
			СтрокаПереоценки.ПредоплатаУпр = 0;
			СтрокаПереоценки.ДатаПлановогоПогашения = СтрокаРасчетов.ДатаПлановогоПогашения;
		Иначе
			СтрокаПереоценки.ПредоплатаРегл         = ?(СуммаРегл > 0, СуммаРегл, 0);
			СтрокаПереоценки.ПредоплатаУпр          = ?(СуммаУпр > 0, СуммаУпр, 0);
			СтрокаПереоценки.ДолгРегл = 0;
			СтрокаПереоценки.ДолгУпр = 0;
			СтрокаПереоценки.ДатаПлановогоПогашения   = Дата(1,1,1);
		КонецЕсли;
		СтрокаПереоценки.ЗаписьДоНачалаРасчета = Ложь;
		СтрокаПереоценки.ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.КорректировкаЗадолженности;
		
		СтрокаПереоценки.КорОбъектРасчетов            = Справочники.ОбъектыРасчетов.ПустаяСсылка();
		СтрокаПереоценки.КорАналитикаУчетаПоПартнерам = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
		СтрокаПереоценки.АналитикаУчетаПоПартнерамПриемник = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
		СтрокаПереоценки.ОбъектРасчетовПриемник            = Справочники.ОбъектыРасчетов.ПустаяСсылка();
		СтрокаПереоценки.ВалютаПриемник            = Справочники.Валюты.ПустаяСсылка();
		СтрокаПереоценки.СуммаПриемник = 0;
		СтрокаПереоценки.ПоДаннымОбъектаРасчетовИсточника = ЛОЖЬ;
		СтрокаПереоценки.Сторно = Ложь;
		
		ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов, СтрокаПереоценки, СтрокаРасчетов.ПорядокОперации, СтрокаРасчетов.ПорядокЗачетаПоДатеПлатежа);
	КонецЕсли;
	
КонецПроцедуры

//Пересчитывает сумму регл. и упр. учета строки расчетов по курсу связанного документа, если он проведен.
Процедура ПересчитатьСуммыЕслиЭтоКорректировка(ГлобальныеПеременные, СтрокаРасчетов, ТипИсточника = "")
	
	Если ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент] <> Неопределено
			И ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент].СуммаДокумента <> 0 Тогда
		
		Если ГлобальныеПеременные.ВалютаРегламентированногоУчета <> СтрокаРасчетов.ВалютаДокумента
				И ГлобальныеПеременные.ВалютаРегламентированногоУчета <> ГлобальныеПеременные.ВалютаРасчетов Тогда
			СтрокаРасчетов.СуммаРегл = Окр(СтрокаРасчетов.Сумма *
				(ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент].СуммаДокументаРегл
					/ ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент].СуммаДокумента),2);
		КонецЕсли;
		Если ГлобальныеПеременные.ВалютаУправленческогоУчета <> СтрокаРасчетов.ВалютаДокумента
				И ГлобальныеПеременные.ВалютаУправленческогоУчета <> ГлобальныеПеременные.ВалютаРасчетов Тогда
			СтрокаРасчетов.СуммаУпр = Окр(СтрокаРасчетов.Сумма *
				(ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент].СуммаДокументаУпр
					/ ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент].СуммаДокумента),2);
		КонецЕсли;
		
		Если ТипИсточника = "ОтгрузкаПоставка" Тогда
			
			СтрокаРасчетов.ИсходнаяСуммаРегл = Окр(СтрокаРасчетов.ИсходнаяСумма
				* (ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент].СуммаДокументаРегл
					/ ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент].СуммаДокумента),2);
			СтрокаРасчетов.ИсходнаяСуммаУпр = Окр(СтрокаРасчетов.ИсходнаяСумма
				* (ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент].СуммаДокументаУпр
					/ ГлобальныеПеременные.ДанныеНакладных[СтрокаРасчетов.СвязанныйДокумент].СуммаДокумента),2);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет сумму регл. и упр. учета строк оплаты или отгрузки/поставки перед добавлением приходных движений
// по курсу сохраненных сумм расходных движений корректировки графика.
Процедура ЗаполнитьДанныеКорректировкиГрафика(ГлобальныеПеременные, Регистратор, ДанныеДляРаспределения, ЭтоОплата)
	
	ДанныеКорректировкиГрафика = ГлобальныеПеременные.ДанныеКорректировокГрафиков[Регистратор];
	// Неопределено возможно, если записи корректировки по начислению и списанию уже добавлены ранее до начала пересчета
	Если ДанныеКорректировкиГрафика <> Неопределено
		И ДанныеКорректировкиГрафика.СуммаРасход >= 0 Тогда
		СтрокиРасчетов = ДанныеДляРаспределения.НайтиСтроки(Новый Структура("Регистратор",Регистратор));
		Для Каждого СтрокаРасчетов Из СтрокиРасчетов Цикл
			
			Если СтрокаРасчетов.Сумма = 0
				Или СтрокаРасчетов.ЭтоОплата <> ЭтоОплата Тогда
				Продолжить;
			КонецЕсли;
			
			Сумма = Мин(ДанныеКорректировкиГрафика.СуммаРасход, СтрокаРасчетов.Сумма);
			
			Если СтрокаРасчетов.Сумма = Сумма Тогда
				СтрокаРасчетов.СуммаРегл = СуммаСписания(0, ДанныеКорректировкиГрафика.СуммаРасходРегл, ДанныеКорректировкиГрафика.СуммаРасход, Сумма);
				СтрокаРасчетов.СуммаУпр  = СуммаСписания(0, ДанныеКорректировкиГрафика.СуммаРасходУпр,  ДанныеКорректировкиГрафика.СуммаРасход, Сумма);;
				СтрокаРасчетов.ДатаВозникновения = ДанныеКорректировкиГрафика.ДатаВозникновения;
			Иначе
				СтрокаРасчетов.Сумма = ДанныеКорректировкиГрафика.СуммаРасход;
				СтрокаРасчетов.СуммаРегл = ДанныеКорректировкиГрафика.СуммаРасходРегл;
				СтрокаРасчетов.СуммаУпр = ДанныеКорректировкиГрафика.СуммаРасходУпр;
				СтрокаРасчетов.ДатаВозникновения = ДанныеКорректировкиГрафика.ДатаВозникновения;
			КонецЕсли;
			
			ДанныеКорректировкиГрафика.СуммаРасход     = ДанныеКорректировкиГрафика.СуммаРасход     - СтрокаРасчетов.Сумма;
			ДанныеКорректировкиГрафика.СуммаРасходРегл = ДанныеКорректировкиГрафика.СуммаРасходРегл - СтрокаРасчетов.СуммаРегл;
			ДанныеКорректировкиГрафика.СуммаРасходУпр  = ДанныеКорректировкиГрафика.СуммаРасходУпр  - СтрокаРасчетов.СуммаУпр;
			
		КонецЦикла;
		// Все строки по приходным движениям уменьшили, больше в цикл по уменьшению суммы не нужно заходить
		Если ДанныеКорректировкиГрафика.СуммаРасход = 0 Тогда
			ГлобальныеПеременные.ДанныеКорректировокГрафиков[Регистратор] = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ТаблицаДвиженийПлановыхОплат - ТаблицаЗначений
//  СтрокаОстатка - СтрокаТаблицыЗначений
//  СтрокаУточнения - СтрокаТаблицыЗначений
//  СуммаСписания - Число
//  ДатаНераспределенногоАванса - Дата - Передается, если план оплаты заказа уменьшается нераспределенным авансом
Процедура УменьшитьПланОплатыПоЗаказу(ТаблицаДвиженийПлановыхОплат, СтрокаОстатка, СтрокаУточнения, СуммаСписания, ДатаНераспределенногоАванса = Неопределено)
	
	Если СуммаСписания = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаУточнения.Сумма = СтрокаУточнения.Сумма - СуммаСписания;
	
	// Если сначала провели документ уточнения, а потом заказ.
	Если НачалоДня(СтрокаУточнения.ДатаДвижения) < СтрокаОстатка.ДатаВозникновения Тогда
		ДокументРегистратор = СтрокаОстатка.ДокументПлан;
		ПорядокОперации = СтрокаОстатка.ПорядокОперации;
		Период = СтрокаОстатка.ДатаВозникновения;
	Иначе
		ДокументРегистратор = СтрокаУточнения.Регистратор;
		ПорядокОперации = СтрокаУточнения.ПорядокОперации;
		Период = СтрокаУточнения.ДатаДвижения;
	КонецЕсли;
	
	НовСтр = ТаблицаДвиженийПлановыхОплат.Добавить();
	НовСтр.ВидДвижения               = ВидДвиженияНакопления.Расход;
	НовСтр.ДокументРегистратор       = ДокументРегистратор;
	НовСтр.ПорядокОперации           = ПорядокОперации;
	НовСтр.ДокументОплаты            = СтрокаУточнения.Регистратор;
	
	НовСтр.ДокументПлан              = СтрокаОстатка.ДокументПлан;
	НовСтр.ВариантОплаты             = СтрокаОстатка.ВариантОплаты;
	НовСтр.ДатаВозникновения         = СтрокаОстатка.ДатаВозникновения;
	НовСтр.ДатаПлановогоПогашения    = СтрокаОстатка.ДатаПлановогоПогашения;
	
	// Закрытие графика оплаты фактической отгрузкой
	Если ДатаНераспределенногоАванса = Неопределено Тогда
		НовСтр.Период                    = Период;
		НовСтр.НераспределенныйАванс     = ЛОЖЬ;
		НовСтр.КОплате                   = СуммаСписания;
		
		СтрокаОстатка.КОплате = СтрокаОстатка.КОплате - СуммаСписания;
		
		// Если есть нераспределенные остатки по этой строке - сторнируем их.
		Если СтрокаОстатка.КОплатеНераспределенныйАванс < 0 Тогда
			СуммаСторнирования = Макс(-СуммаСписания, СтрокаОстатка.КОплатеНераспределенныйАванс);
			СторнироватьНераспределенныйАванс(ТаблицаДвиженийПлановыхОплат, СтрокаОстатка, СтрокаУточнения, СуммаСторнирования);
			СуммаСписания = СуммаСписания + СуммаСторнирования;
		КонецЕсли;
	// Предварительно закрытие графика оплаты фактической оплатой
	Иначе
		НовСтр.Период                    = ДатаНераспределенногоАванса;
		НовСтр.НераспределенныйАванс     = ИСТИНА;
		НовСтр.КОплате                   = СуммаСписания;
		
		СтрокаОстатка.КОплатеНераспределенныйАванс = СтрокаОстатка.КОплатеНераспределенныйАванс - СуммаСписания;
		СтрокаОстатка.ЕстьКОплатеНераспределенныйАванс = СтрокаОстатка.КОплатеНераспределенныйАванс < 0;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ТаблицаДвиженийПлановыхОплат - ТаблицаЗначений
//  СтрокаОстатка - СтрокаТаблицыЗначений
//  СтрокаУточнения - СтрокаТаблицыЗначений
//  СуммаСписания - Число
//  ДатаНераспределенногоАванса - Дата - Передается если сторнируется запись нераспределенного аванса другим нераспределенным авансом.
Процедура СторнироватьНераспределенныйАванс(ТаблицаДвиженийПлановыхОплат, СтрокаОстатка, СтрокаУточнения, СуммаСписания, ДатаНераспределенногоАванса = Неопределено)
	
	Если СуммаСписания = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Если сначала провели документ уточнения, а потом заказ.
	Если НачалоДня(СтрокаУточнения.ДатаДвижения) < СтрокаОстатка.ДатаВозникновения Тогда
		ДокументРегистратор = СтрокаОстатка.ДокументПлан;
		ПорядокОперации = СтрокаОстатка.ПорядокОперации;
		Период = СтрокаОстатка.ДатаВозникновения;
	Иначе
		ДокументРегистратор = СтрокаУточнения.Регистратор;
		ПорядокОперации = СтрокаУточнения.ПорядокОперации;
		Период = СтрокаУточнения.ДатаДвижения;
	КонецЕсли;
	
	НовСтр = ТаблицаДвиженийПлановыхОплат.Добавить();
	НовСтр.ВидДвижения               = ВидДвиженияНакопления.Расход;
	НовСтр.ДокументРегистратор       = ДокументРегистратор;
	НовСтр.ДокументПлан              = СтрокаОстатка.ДокументПлан;
	НовСтр.ВариантОплаты             = СтрокаОстатка.ВариантОплаты;
	НовСтр.Период                    = ?(ДатаНераспределенногоАванса = Неопределено, Период, ДатаНераспределенногоАванса);
	НовСтр.ПорядокОперации           = ПорядокОперации;
	НовСтр.ДатаВозникновения         = СтрокаОстатка.ДатаВозникновения;
	НовСтр.ДатаПлановогоПогашения    = СтрокаОстатка.ДатаПлановогоПогашения;
	НовСтр.НераспределенныйАванс     = ИСТИНА;
	НовСтр.ДокументОплаты            = СтрокаУточнения.Регистратор;
	НовСтр.КОплате                   = СуммаСписания;
	
	СтрокаОстатка.КОплатеНераспределенныйАванс = СтрокаОстатка.КОплатеНераспределенныйАванс - СуммаСписания;
	
КонецПроцедуры

// Параметры:
//  ТаблицаДвиженийПлановыхОтгрузок - ТаблицаЗначений
//  СтрокаОстатка - СтрокаТаблицыЗначений
//  СтрокаУточнения - СтрокаТаблицыЗначений
//  СуммаСписания - Число
//  ДатаНераспределенногоАванса - Дата
Процедура УменьшитьПланОтгрузкиПоЗаказу(ТаблицаДвиженийПлановыхОтгрузок, СтрокаОстатка, СтрокаУточнения, СуммаСписания, ДатаНераспределенногоАванса = Неопределено)
	
	Если СуммаСписания = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаУточнения.Сумма = СтрокаУточнения.Сумма - СуммаСписания;
	
	// Если сначала провели документ уточнения, а потом заказ.
	Если НачалоДня(СтрокаУточнения.ДатаДвижения) < СтрокаОстатка.ДатаВозникновения Тогда
		ДокументРегистратор = СтрокаОстатка.ДокументПлан;
		ПорядокОперации = СтрокаОстатка.ПорядокОперации;
		Период = СтрокаОстатка.ДатаВозникновения;
	Иначе
		ДокументРегистратор = СтрокаУточнения.Регистратор;
		ПорядокОперации = СтрокаУточнения.ПорядокОперации;
		Период = СтрокаУточнения.ДатаДвижения;
	КонецЕсли;
	
	НовСтр = ТаблицаДвиженийПлановыхОтгрузок.Добавить();
	НовСтр.ВидДвижения               = ВидДвиженияНакопления.Расход;
	НовСтр.ДокументРегистратор       = ДокументРегистратор;
	НовСтр.ПорядокОперации           = ПорядокОперации;
	НовСтр.ДокументОплаты            = СтрокаУточнения.Регистратор;
	
	НовСтр.ДокументПлан              = СтрокаОстатка.ДокументПлан;
	НовСтр.ДатаВозникновения         = СтрокаОстатка.ДатаВозникновения;
	НовСтр.ДатаПлановогоПогашения    = СтрокаОстатка.ДатаПлановогоПогашения;
	
	НовСтр.Сумма                     = СуммаСписания;
	
	НовСтр.УчтеноВОстаткахПлановыхОтгрузок = Ложь;
	Если ДатаНераспределенногоАванса = Неопределено Тогда
		НовСтр.Период                    = Период;
		НовСтр.НераспределенныйАванс     = ЛОЖЬ;
		
		СтрокаОстатка.Сумма = СтрокаОстатка.Сумма - СуммаСписания;
		
		// Если есть нераспределенные остатки по этой строке - сторнируем их.
		Если СтрокаОстатка.СуммаНераспределенныйАванс < 0 Тогда
			СуммаСторнирования = Макс(-СуммаСписания, СтрокаОстатка.СуммаНераспределенныйАванс);;
			СторнироватьНераспределенныйАвансПланаОтгрузок(ТаблицаДвиженийПлановыхОтгрузок, СтрокаОстатка, СтрокаУточнения, СуммаСторнирования);
			СуммаСписания = СуммаСписания + СуммаСторнирования;
		КонецЕсли;
		
	Иначе
		НовСтр.Период                    = ДатаНераспределенногоАванса;
		НовСтр.НераспределенныйАванс     = ИСТИНА;
		СтрокаОстатка.СуммаНераспределенныйАванс = СтрокаОстатка.СуммаНераспределенныйАванс - СуммаСписания;
		СтрокаОстатка.ЕстьСуммаНераспределенныйАванс = СтрокаОстатка.СуммаНераспределенныйАванс < 0;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ТаблицаДвиженийПлановыхОтгрузок - ТаблицаЗначений
//  СтрокаОстатка - СтрокаТаблицыЗначений
//  СтрокаУточнения - СтрокаТаблицыЗначений
//  СуммаСписания - Число
//  ДатаНераспределенногоАванса - Дата
Процедура СторнироватьНераспределенныйАвансПланаОтгрузок(ТаблицаДвиженийПлановыхОтгрузок, СтрокаОстатка, СтрокаУточнения, СуммаСписания, ДатаНераспределенногоАванса = Неопределено)
	
	Если СуммаСписания = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Если сначала провели документ уточнения, а потом заказ.
	Если НачалоДня(СтрокаУточнения.ДатаДвижения) < СтрокаОстатка.ДатаВозникновения Тогда
		ДокументРегистратор = СтрокаОстатка.ДокументПлан;
		ПорядокОперации = СтрокаОстатка.ПорядокОперации;
		Период = СтрокаОстатка.ДатаВозникновения;
	Иначе
		ДокументРегистратор = СтрокаУточнения.Регистратор;
		ПорядокОперации = СтрокаУточнения.ПорядокОперации;
		Период = СтрокаУточнения.ДатаДвижения;
	КонецЕсли;
	
	НовСтр = ТаблицаДвиженийПлановыхОтгрузок.Добавить();
	НовСтр.ВидДвижения               = ВидДвиженияНакопления.Расход;
	НовСтр.ДокументРегистратор       = ДокументРегистратор;
	НовСтр.ДокументПлан              = СтрокаОстатка.ДокументПлан;
	НовСтр.Период                    = ?(ДатаНераспределенногоАванса = Неопределено, Период, ДатаНераспределенногоАванса);
	НовСтр.ПорядокОперации           = ПорядокОперации;
	НовСтр.ДатаВозникновения         = СтрокаОстатка.ДатаВозникновения;
	НовСтр.ДатаПлановогоПогашения    = СтрокаОстатка.ДатаПлановогоПогашения;
	НовСтр.НераспределенныйАванс     = ИСТИНА;
	НовСтр.ДокументОплаты            = СтрокаУточнения.Регистратор;
	НовСтр.Сумма                   = СуммаСписания;
	
	СтрокаОстатка.СуммаНераспределенныйАванс = СтрокаОстатка.СуммаНераспределенныйАванс - СуммаСписания;
	СтрокаОстатка.ЕстьСуммаНераспределенныйАванс = СтрокаОстатка.СуммаНераспределенныйАванс < 0;
	
КонецПроцедуры

#Область Сторно

Процедура ДобавитьМинусовоеДвижениеСторно(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаИсходногоРегистратора, ДвижениеИсходногоДокумента)
	
	СтрокаСторно = ТаблицаРасчетовПоСрокам.Добавить();
	
	СтрокаСторно.ВидДвижения = ДвижениеИсходногоДокумента.ВидДвижения;
	
	СтрокаСторно.РасчетныйДокумент = ДвижениеИсходногоДокумента.РасчетныйДокумент;
	СтрокаСторно.ДатаПлановогоПогашения = ДвижениеИсходногоДокумента.ДатаПлановогоПогашения;
	СтрокаСторно.ДатаВозникновения = ДвижениеИсходногоДокумента.ДатаВозникновения;
	
	СтрокаСторно.ДокументРегистратор = СтрокаИсходногоРегистратора.НовыйРегистратор;
	СтрокаСторно.Период              = СтрокаИсходногоРегистратора.НовыйПериод;
	СтрокаСторно.ПорядокОперации     = СтрокаИсходногоРегистратора.НовыйПорядокОперации;
	СтрокаСторно.ПорядокЗачета       = СтрокаИсходногоРегистратора.НовыйПорядокОперации;
	
	СтрокаСторно.ХозяйственнаяОперация       = ДвижениеИсходногоДокумента.ХозяйственнаяОперация;
	СтрокаСторно.НастройкаХозяйственнойОперации  = ДвижениеИсходногоДокумента.НастройкаХозяйственнойОперации;
	СтрокаСторно.ИдентификаторФинЗаписи        = ДвижениеИсходногоДокумента.ИдентификаторФинЗаписи;
	СтрокаСторно.ВалютаДокумента               = ДвижениеИсходногоДокумента.ВалютаДокумента;
	СтрокаСторно.СтатьяДвиженияДенежныхСредств = ДвижениеИсходногоДокумента.СтатьяДвиженияДенежныхСредств;
	
	СтрокаСторно.Долг           = -ДвижениеИсходногоДокумента.Долг;
	СтрокаСторно.ДолгРегл       = -ДвижениеИсходногоДокумента.ДолгРегл;
	СтрокаСторно.ДолгУпр        = -ДвижениеИсходногоДокумента.ДолгУпр;
	СтрокаСторно.Предоплата     = -ДвижениеИсходногоДокумента.Предоплата;
	СтрокаСторно.ПредоплатаРегл = -ДвижениеИсходногоДокумента.ПредоплатаРегл;
	СтрокаСторно.ПредоплатаУпр  = -ДвижениеИсходногоДокумента.ПредоплатаУпр;
	СтрокаСторно.Сторно = Истина;
	
	СтрокаСторно.ЗаписьДоНачалаРасчета = Ложь;
	// Если движение исходного документа после начала расчетов, то тип записи еще не заполнен - определяем по операции взаиморасчетов
	СтрокаСторно.ОперацияВзаиморасчетов = ДвижениеИсходногоДокумента.ОперацияВзаиморасчетов;
	// Если движение исходного документа до начала расчетов, то сразу берем его тип записи 
	СтрокаСторно.ТипЗаписиВзаиморасчетов = ДвижениеИсходногоДокумента.ТипЗаписиВзаиморасчетов;
	
	СтрокаСторно.КорАналитикаУчетаПоПартнерам = ДвижениеИсходногоДокумента.КорАналитикаУчетаПоПартнерам;
	СтрокаСторно.КорОбъектРасчетов = ДвижениеИсходногоДокумента.КорОбъектРасчетов;
	СтрокаСторно.АналитикаУчетаПоПартнерамПриемник = ДвижениеИсходногоДокумента.АналитикаУчетаПоПартнерамПриемник;
	СтрокаСторно.ОбъектРасчетовПриемник = ДвижениеИсходногоДокумента.ОбъектРасчетовПриемник;
	СтрокаСторно.ВалютаПриемник = ДвижениеИсходногоДокумента.ВалютаПриемник;
	СтрокаСторно.СуммаПриемник = ДвижениеИсходногоДокумента.СуммаПриемник;
	СтрокаСторно.ПоДаннымОбъектаРасчетовИсточника = ДвижениеИсходногоДокумента.ПоДаннымОбъектаРасчетовИсточника;
	
	ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов, СтрокаСторно, ДвижениеИсходногоДокумента.ПорядокОперации, ДвижениеИсходногоДокумента.ПорядокЗачета);
	
КонецПроцедуры

//СтрокаОстатков - Строка таблицы остатков, из которой будут взяты ПорядокОперации и ПорядокЗачетаПоДатеПлатежа списываемого остатка для добавления строки в таблицу контроля.
Процедура ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов, СтрокаРасчетовПоСрокам, ПорядокОперации = Неопределено, ПорядокЗачетаПоДатеПлатежа = Неопределено)
	
	Если СтрокаРасчетовПоСрокам.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
		Множитель = -1;
	Иначе
		Множитель = 1;
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("РасчетныйДокумент,ДатаВозникновения,ДатаПлановогоПогашения");
	ЗаполнитьЗначенияСвойств(СтруктураПоиска,СтрокаРасчетовПоСрокам);
	СтрокиТаблицыКонтроля = ТаблицаОстатковВзаиморасчетов.НайтиСтроки(СтруктураПоиска);
	
	Если СтрокиТаблицыКонтроля.Количество() = 0 Тогда
		
		Если СтрокаРасчетовПоСрокам.ВидДвижения = ВидДвиженияНакопления.Расход
			И ПорядокОперации = Неопределено Тогда
			ВызватьИсключение(НСтр("ru = 'Не указана строка остатков для списания';
									|en = 'The line of balance to write off is not specified'", ОбщегоНазначения.КодОсновногоЯзыка()));
		КонецЕсли;
		
		СтрокаТаблицыКонтроля = ТаблицаОстатковВзаиморасчетов.Добавить();
		СтрокаТаблицыКонтроля.РасчетныйДокумент = СтрокаРасчетовПоСрокам.РасчетныйДокумент;
		СтрокаТаблицыКонтроля.ДатаВозникновения = СтрокаРасчетовПоСрокам.ДатаВозникновения;
		СтрокаТаблицыКонтроля.ДатаПлановогоПогашения = СтрокаРасчетовПоСрокам.ДатаПлановогоПогашения;
		СтрокаТаблицыКонтроля.ПорядокОперации = ?(ПорядокОперации=Неопределено, СтрокаРасчетовПоСрокам.ПорядокОперации, ПорядокОперации);
		СтрокаТаблицыКонтроля.ПорядокЗачетаПоДатеПлатежа = ?(ПорядокЗачетаПоДатеПлатежа=Неопределено, СтрокаРасчетовПоСрокам.ПорядокЗачета, ПорядокЗачетаПоДатеПлатежа);
		СтрокаТаблицыКонтроля.ДатаПереоценки = НачалоДня(СтрокаРасчетовПоСрокам.Период);
		
		СтрокаТаблицыКонтроля.Долг = СтрокаРасчетовПоСрокам.Долг * Множитель;
		СтрокаТаблицыКонтроля.ДолгРегл = СтрокаРасчетовПоСрокам.ДолгРегл * Множитель;
		СтрокаТаблицыКонтроля.ДолгУпр = СтрокаРасчетовПоСрокам.ДолгУпр * Множитель;
		СтрокаТаблицыКонтроля.Предоплата = СтрокаРасчетовПоСрокам.Предоплата * Множитель;
		СтрокаТаблицыКонтроля.ПредоплатаРегл = СтрокаРасчетовПоСрокам.ПредоплатаРегл * Множитель;
		СтрокаТаблицыКонтроля.ПредоплатаУпр = СтрокаРасчетовПоСрокам.ПредоплатаУпр * Множитель;
	Иначе
		СтрокаТаблицыКонтроля = СтрокиТаблицыКонтроля[0];
		СтрокаТаблицыКонтроля.Долг = СтрокаТаблицыКонтроля.Долг + СтрокаРасчетовПоСрокам.Долг * Множитель;
		СтрокаТаблицыКонтроля.ДолгРегл = СтрокаТаблицыКонтроля.ДолгРегл + СтрокаРасчетовПоСрокам.ДолгРегл * Множитель;
		СтрокаТаблицыКонтроля.ДолгУпр = СтрокаТаблицыКонтроля.ДолгУпр + СтрокаРасчетовПоСрокам.ДолгУпр * Множитель;
		СтрокаТаблицыКонтроля.Предоплата = СтрокаТаблицыКонтроля.Предоплата + СтрокаРасчетовПоСрокам.Предоплата * Множитель;
		СтрокаТаблицыКонтроля.ПредоплатаРегл = СтрокаТаблицыКонтроля.ПредоплатаРегл + СтрокаРасчетовПоСрокам.ПредоплатаРегл * Множитель;
		СтрокаТаблицыКонтроля.ПредоплатаУпр = СтрокаТаблицыКонтроля.ПредоплатаУпр + СтрокаРасчетовПоСрокам.ПредоплатаУпр * Множитель;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПочиночноеДвижениеНаМинусовойОстаток(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаКонтроля, СтрокаРасчетовРегистратора)
	
	ИсправляемМинусПоДолгу = СтрокаКонтроля.Долг < 0;
	
	Если ИсправляемМинусПоДолгу Тогда
		Ресурс = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.Долг.Имя;
		РесурсРегл = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ДолгРегл.Имя;
		РесурсУпр = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ДолгУпр.Имя;
		ПротивоположныйРесурс = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.Предоплата.Имя;
		ПротивоположныйРесурсРегл = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ПредоплатаРегл.Имя;
		ПротивоположныйРесурсУпр = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ПредоплатаУпр.Имя;
	Иначе
		Ресурс = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.Предоплата.Имя;
		РесурсРегл = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ПредоплатаРегл.Имя;
		РесурсУпр = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ПредоплатаУпр.Имя;
		ПротивоположныйРесурс = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.Долг.Имя;
		ПротивоположныйРесурсРегл = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ДолгРегл.Имя;
		ПротивоположныйРесурсУпр = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ДолгУпр.Имя;
	КонецЕсли;
	
	СуммаПочиночного = -СтрокаКонтроля[Ресурс];
	СуммаПочиночногоРегл = -СтрокаКонтроля[РесурсРегл];
	СуммаПочиночногоУпр = -СтрокаКонтроля[РесурсУпр];
	Пока СуммаПочиночного > 0 Цикл
		СуммаСписания = Неопределено;
		
		//Закрываем минус на другой минус в противоположном ресурсе
		Для Каждого СтрокаОстатков Из ТаблицаОстатковВзаиморасчетов Цикл
			Если СтрокаОстатков[ПротивоположныйРесурс] < 0 И СтрокаРасчетовРегистратора.Период >= СтрокаОстатков.ДатаВозникновения Тогда
				
				ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВосстановлениеДолгаАвансаПриСторноВзаиморасчетов;
				НастройкаХозяйственнойОперации = Справочники.НастройкиХозяйственныхОпераций.ВосстановлениеДолгаАвансаПриСторноВзаиморасчетов;
				
				СуммаСписания = Мин(-СтрокаОстатков[ПротивоположныйРесурс],СуммаПочиночного);
				СуммаСписанияРегл = СуммаСписания(0,СуммаПочиночногоРегл,СуммаПочиночного,СуммаСписания);
				СуммаСписанияУпр = СуммаСписания(0,СуммаПочиночногоУпр,СуммаПочиночного,СуммаСписания);
				
				#Область СвязанноеПриходноеПочиночноеДвижение
				НовСтр_Приход = ТаблицаРасчетовПоСрокам.Добавить();
				НовСтр_Приход.ВидДвижения = ВидДвиженияНакопления.Приход;
				
				НовСтр_Приход.РасчетныйДокумент = СтрокаОстатков.РасчетныйДокумент;
				НовСтр_Приход.ДатаПлановогоПогашения = СтрокаОстатков.ДатаПлановогоПогашения;
				НовСтр_Приход.ДатаВозникновения = СтрокаОстатков.ДатаВозникновения;
				
				НовСтр_Приход.ДокументРегистратор = СтрокаРасчетовРегистратора.Регистратор;
				НовСтр_Приход.Период              = СтрокаРасчетовРегистратора.Период;
				НовСтр_Приход.ПорядокОперации     = СтрокаРасчетовРегистратора.ПорядокОперации;
				НовСтр_Приход.ПорядокЗачета       = СтрокаРасчетовРегистратора.ПорядокОперации;
				НовСтр_Приход.ИдентификаторФинЗаписи  = СтрокаРасчетовРегистратора.ИдентификаторРегистратора;
				НовСтр_Приход.ВалютаДокумента               = СтрокаРасчетовРегистратора.ВалютаДокумента;
				НовСтр_Приход.СтатьяДвиженияДенежныхСредств = СтрокаРасчетовРегистратора.СтатьяДвиженияДенежныхСредств;
				
				НовСтр_Приход.ХозяйственнаяОперация           = ХозяйственнаяОперация;
				НовСтр_Приход.НастройкаХозяйственнойОперации  = НастройкаХозяйственнойОперации;
				
				НовСтр_Приход[Ресурс]        = 0;
				НовСтр_Приход[РесурсРегл]    = 0;
				НовСтр_Приход[РесурсУпр]     = 0;
				НовСтр_Приход[ПротивоположныйРесурс]        = СуммаСписания;
				НовСтр_Приход[ПротивоположныйРесурсРегл]    = СуммаСписанияРегл;
				НовСтр_Приход[ПротивоположныйРесурсУпр]     = СуммаСписанияУпр;
				
				НовСтр_Приход.Сторно = Ложь;
				
				НовСтр_Приход.ЗаписьДоНачалаРасчета = Ложь;
				НовСтр_Приход.ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.КорректировкаЗадолженности;
				
				НовСтр_Приход.КорАналитикаУчетаПоПартнерам = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
				НовСтр_Приход.КорОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка();
				НовСтр_Приход.АналитикаУчетаПоПартнерамПриемник = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
				НовСтр_Приход.ОбъектРасчетовПриемник = Справочники.ОбъектыРасчетов.ПустаяСсылка();
				НовСтр_Приход.ВалютаПриемник = Справочники.Валюты.ПустаяСсылка();
				НовСтр_Приход.СуммаПриемник = 0;
				НовСтр_Приход.ПоДаннымОбъектаРасчетовИсточника = Ложь;
				
				ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов,НовСтр_Приход);
				
				#КонецОбласти
				
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		//Закрываем минус на плюс в этом же ресурсе
		Если СуммаСписания = Неопределено Тогда
			Для Каждого СтрокаОстатков Из ТаблицаОстатковВзаиморасчетов Цикл
				Если СтрокаОстатков[Ресурс] > 0 И СтрокаРасчетовРегистратора.Период >= СтрокаОстатков.ДатаВозникновения Тогда
					
					Если Ресурс = "Долг" Тогда
						ХозяйственнаяОперация           = Перечисления.ХозяйственныеОперации.ПерезачетДолгаПриСторноВзаиморасчетов;
						НастройкаХозяйственнойОперации  = Справочники.НастройкиХозяйственныхОпераций.ПерезачетДолгаПриСторноВзаиморасчетов;
					Иначе
						ХозяйственнаяОперация           = Перечисления.ХозяйственныеОперации.ПерезачетАвансаПриСторноВзаиморасчетов;
						НастройкаХозяйственнойОперации  = Справочники.НастройкиХозяйственныхОпераций.ПерезачетАвансаПриСторноВзаиморасчетов;
					КонецЕсли;
					
					СуммаСписания = Мин(СтрокаОстатков[Ресурс],СуммаПочиночного);
					СуммаСписанияРегл = СуммаСписания(0,СуммаПочиночногоРегл,СуммаПочиночного,СуммаСписания);
					СуммаСписанияУпр = СуммаСписания(0,СуммаПочиночногоУпр,СуммаПочиночного,СуммаСписания);
					
					#Область СвязанноеРасходноеПочиночноеДвижение
					НовСтр_Расход = ТаблицаРасчетовПоСрокам.Добавить();
					НовСтр_Расход.ВидДвижения = ВидДвиженияНакопления.Расход;
					
					НовСтр_Расход.РасчетныйДокумент = СтрокаОстатков.РасчетныйДокумент;
					НовСтр_Расход.ДатаПлановогоПогашения = СтрокаОстатков.ДатаПлановогоПогашения;
					НовСтр_Расход.ДатаВозникновения = СтрокаОстатков.ДатаВозникновения;
					
					НовСтр_Расход.ДокументРегистратор = СтрокаРасчетовРегистратора.Регистратор;
					НовСтр_Расход.Период              = СтрокаРасчетовРегистратора.Период;
					НовСтр_Расход.ПорядокОперации     = СтрокаРасчетовРегистратора.ПорядокОперации;
					НовСтр_Расход.ПорядокЗачета       = СтрокаРасчетовРегистратора.ПорядокОперации;
					НовСтр_Расход.ИдентификаторФинЗаписи  = СтрокаРасчетовРегистратора.ИдентификаторРегистратора;
					НовСтр_Расход.ВалютаДокумента               = СтрокаРасчетовРегистратора.ВалютаДокумента;
					НовСтр_Расход.СтатьяДвиженияДенежныхСредств = СтрокаРасчетовРегистратора.СтатьяДвиженияДенежныхСредств;
					
					НовСтр_Расход.ХозяйственнаяОперация           = ХозяйственнаяОперация;
					НовСтр_Расход.НастройкаХозяйственнойОперации  = НастройкаХозяйственнойОперации;
					
					НовСтр_Расход[Ресурс]        = СуммаСписания;
					НовСтр_Расход[РесурсРегл]    = СуммаСписанияРегл;
					НовСтр_Расход[РесурсУпр]     = СуммаСписанияУпр;
					НовСтр_Расход[ПротивоположныйРесурс]        = 0;
					НовСтр_Расход[ПротивоположныйРесурсРегл]    = 0;
					НовСтр_Расход[ПротивоположныйРесурсУпр]     = 0;
					
					НовСтр_Расход.Сторно = Ложь;
					
					НовСтр_Расход.ЗаписьДоНачалаРасчета = Ложь;
					НовСтр_Расход.ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.КорректировкаЗадолженности;
					
					НовСтр_Расход.КорАналитикаУчетаПоПартнерам = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
					НовСтр_Расход.КорОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка();
					НовСтр_Расход.АналитикаУчетаПоПартнерамПриемник = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
					НовСтр_Расход.ОбъектРасчетовПриемник = Справочники.ОбъектыРасчетов.ПустаяСсылка();
					НовСтр_Расход.ВалютаПриемник = Справочники.Валюты.ПустаяСсылка();
					НовСтр_Расход.СуммаПриемник = 0;
					НовСтр_Расход.ПоДаннымОбъектаРасчетовИсточника = Ложь;
					
					ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов,НовСтр_Расход );
					
					Если Ресурс = "Долг" Тогда
						ПереоценитьДолг(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаОстатков.РасчетныйДокумент, СтрокаРасчетовРегистратора.Период, Истина);
					КонецЕсли;
					
					#КонецОбласти
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		// Если не удалось найти остатки для уменьшения - добавляем приход.
		Если СуммаСписания = Неопределено Тогда
			
			ХозяйственнаяОперация           = Перечисления.ХозяйственныеОперации.ВосстановлениеДолгаАвансаПриСторноВзаиморасчетов;
			НастройкаХозяйственнойОперации  = Справочники.НастройкиХозяйственныхОпераций.ВосстановлениеДолгаАвансаПриСторноВзаиморасчетов;
			
			СуммаСписания = СуммаПочиночного;
			СуммаСписанияРегл = СуммаПочиночногоРегл;
			СуммаСписанияУпр = СуммаПочиночногоУпр;
			
			#Область СвязанноеПриходноеПочиночноеДвижение
			НовСтр_Приход = ТаблицаРасчетовПоСрокам.Добавить();
			НовСтр_Приход.ВидДвижения = ВидДвиженияНакопления.Приход;
			
			НовСтр_Приход.РасчетныйДокумент = СтрокаРасчетовРегистратора.РасчетныйДокумент;
			НовСтр_Приход.ДатаПлановогоПогашения = СтрокаРасчетовРегистратора.ДатаПлановогоПогашения;
			НовСтр_Приход.ДатаВозникновения = СтрокаРасчетовРегистратора.ДатаВозникновения;
			
			НовСтр_Приход.ДокументРегистратор = СтрокаРасчетовРегистратора.Регистратор;
			НовСтр_Приход.Период              = СтрокаРасчетовРегистратора.Период;
			НовСтр_Приход.ПорядокОперации     = СтрокаРасчетовРегистратора.ПорядокОперации;
			НовСтр_Приход.ПорядокЗачета       = СтрокаРасчетовРегистратора.ПорядокОперации;
			НовСтр_Приход.ИдентификаторФинЗаписи  = СтрокаРасчетовРегистратора.ИдентификаторРегистратора;
			НовСтр_Приход.ВалютаДокумента               = СтрокаРасчетовРегистратора.ВалютаДокумента;
			НовСтр_Приход.СтатьяДвиженияДенежныхСредств = СтрокаРасчетовРегистратора.СтатьяДвиженияДенежныхСредств;
			
			НовСтр_Приход.ХозяйственнаяОперация           = ХозяйственнаяОперация;
			НовСтр_Приход.НастройкаХозяйственнойОперации  = НастройкаХозяйственнойОперации;
			
			НовСтр_Приход[ПротивоположныйРесурс]        = СуммаСписания;
			НовСтр_Приход[ПротивоположныйРесурсРегл]    = СуммаСписанияРегл;
			НовСтр_Приход[ПротивоположныйРесурсУпр]     = СуммаСписанияУпр;
			НовСтр_Приход[Ресурс]        = 0;
			НовСтр_Приход[РесурсРегл]    = 0;
			НовСтр_Приход[РесурсУпр]     = 0;
			
			НовСтр_Приход.Сторно = Ложь;
			
			НовСтр_Приход.ЗаписьДоНачалаРасчета = Ложь;
			НовСтр_Приход.ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.КорректировкаЗадолженности;
			
			НовСтр_Приход.КорАналитикаУчетаПоПартнерам = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
			НовСтр_Приход.КорОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка();
			НовСтр_Приход.АналитикаУчетаПоПартнерамПриемник = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
			НовСтр_Приход.ОбъектРасчетовПриемник = Справочники.ОбъектыРасчетов.ПустаяСсылка();
			НовСтр_Приход.ВалютаПриемник = Справочники.Валюты.ПустаяСсылка();
			НовСтр_Приход.СуммаПриемник = 0;
			НовСтр_Приход.ПоДаннымОбъектаРасчетовИсточника = Ложь;
			
			ДобавитьДвижениеВТаблицуКонтроляОстатков(ГлобальныеПеременные, ТаблицаОстатковВзаиморасчетов,НовСтр_Приход);
			
			#КонецОбласти
			
		КонецЕсли;
	
		#Область ДобавлениеДвиженияДляЗакрытияМинуса
	
		СтрокаПочиночная = ТаблицаРасчетовПоСрокам.Добавить();
		
		СтрокаПочиночная.ВидДвижения = ВидДвиженияНакопления.Приход;
		
		СтрокаПочиночная.РасчетныйДокумент = СтрокаКонтроля.РасчетныйДокумент;
		СтрокаПочиночная.ДатаПлановогоПогашения = СтрокаКонтроля.ДатаПлановогоПогашения;
		СтрокаПочиночная.ДатаВозникновения = СтрокаКонтроля.ДатаВозникновения;
		
		СтрокаПочиночная.ДокументРегистратор = СтрокаРасчетовРегистратора.Регистратор;
		СтрокаПочиночная.Период              = СтрокаРасчетовРегистратора.Период;
		СтрокаПочиночная.ПорядокОперации     = СтрокаРасчетовРегистратора.ПорядокОперации;
		СтрокаПочиночная.ПорядокЗачета       = СтрокаРасчетовРегистратора.ПорядокОперации;
		СтрокаПочиночная.ИдентификаторФинЗаписи  = СтрокаРасчетовРегистратора.ИдентификаторРегистратора;
		СтрокаПочиночная.ВалютаДокумента               = СтрокаРасчетовРегистратора.ВалютаДокумента;
		СтрокаПочиночная.СтатьяДвиженияДенежныхСредств = СтрокаРасчетовРегистратора.СтатьяДвиженияДенежныхСредств;
		
		СтрокаПочиночная.ХозяйственнаяОперация           = ХозяйственнаяОперация;
		СтрокаПочиночная.НастройкаХозяйственнойОперации  = НастройкаХозяйственнойОперации;
		СтрокаПочиночная.ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.КорректировкаЗадолженности;
		
		СтрокаПочиночная[Ресурс]         = СуммаСписания;
		СтрокаПочиночная[РесурсРегл]     = СуммаСписанияРегл;
		СтрокаПочиночная[РесурсУпр]      = СуммаСписанияУпр;
		СтрокаПочиночная[ПротивоположныйРесурс]        = 0;
		СтрокаПочиночная[ПротивоположныйРесурсРегл]    = 0;
		СтрокаПочиночная[ПротивоположныйРесурсУпр]     = 0;
		
		СтрокаПочиночная.Сторно = Ложь;
		
		СтрокаПочиночная.ЗаписьДоНачалаРасчета = Ложь;
		
		СтрокаПочиночная.КорАналитикаУчетаПоПартнерам = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
		СтрокаПочиночная.КорОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка();
		СтрокаПочиночная.АналитикаУчетаПоПартнерамПриемник = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
		СтрокаПочиночная.ОбъектРасчетовПриемник = Справочники.ОбъектыРасчетов.ПустаяСсылка();
		СтрокаПочиночная.ВалютаПриемник = Справочники.Валюты.ПустаяСсылка();
		СтрокаПочиночная.СуммаПриемник = 0;
		СтрокаПочиночная.ПоДаннымОбъектаРасчетовИсточника = Ложь;
		
		// Вместо добавления в таблицу контроля и сворачивания 
		СтрокаКонтроля.Долг = 0;
		СтрокаКонтроля.ДолгРегл = 0;
		СтрокаКонтроля.ДолгУпр = 0;
		СтрокаКонтроля.Предоплата = 0;
		СтрокаКонтроля.ПредоплатаРегл = 0;
		СтрокаКонтроля.ПредоплатаУпр = 0;
		
		Если Ресурс = "Долг" Тогда
			ПереоценитьДолг(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаКонтроля.РасчетныйДокумент, СтрокаРасчетовРегистратора.Период, Истина);
		КонецЕсли;
		
		#КонецОбласти
		
		СуммаПочиночного = СуммаПочиночного - СуммаСписания;
		СуммаПочиночногоРегл = СуммаПочиночногоРегл - СуммаСписанияРегл;
		СуммаПочиночногоУпр = СуммаПочиночногоУпр - СуммаСписанияУпр;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьПочиночныеДвиженияНаРазвернутоеСальдо(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СуммаДляСворачивания, СтрокаРасчетовРегистратора)
	
	СуммаСписанияДолга = СуммаДляСворачивания;
	СуммаСписанияПредоплаты = СуммаДляСворачивания;
	// Списываем по курсу предоплаты
	СуммаПредоплатыРегл = 0;
	СуммаПредоплатыУпр = 0;
	
	Для Каждого СтрокаОстатков Из ТаблицаОстатковВзаиморасчетов Цикл
		Если СуммаСписанияПредоплаты = 0 Тогда
			Прервать;
		КонецЕсли;
		Если СтрокаОстатков.Предоплата = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ДобавитьПочиночноеДвижениеРазвернутогоСальдо(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаОстатков, СтрокаРасчетовРегистратора, СуммаСписанияПредоплаты, СуммаПредоплатыРегл, СуммаПредоплатыУпр);
		
	КонецЦикла;
	
	Для Каждого СтрокаОстатков Из ТаблицаОстатковВзаиморасчетов Цикл
		Если СуммаСписанияДолга = 0 Тогда
			Прервать;
		КонецЕсли;
		Если СтрокаОстатков.Долг = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ДобавитьПочиночноеДвижениеРазвернутогоСальдо(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаОстатков, СтрокаРасчетовРегистратора, СуммаСписанияДолга, СуммаПредоплатыРегл, СуммаПредоплатыУпр);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьПочиночноеДвижениеРазвернутогоСальдо(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаОстатков,СтрокаРасчетовРегистратора,СуммаКСписанию, СуммаПредоплатыРегл,СуммаПредоплатыУпр)
	
	Если СтрокаОстатков.Долг > 0 И СуммаКСписанию > 0 Тогда
		Ресурс = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.Долг.Имя;
		РесурсРегл = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ДолгРегл.Имя;
		РесурсУпр = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ДолгУпр.Имя;
		ПротивоположныйРесурс = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.Предоплата.Имя;
		ПротивоположныйРесурсРегл = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ПредоплатаРегл.Имя;
		ПротивоположныйРесурсУпр = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ПредоплатаУпр.Имя;
		ЭтоДолг = Истина;
	Иначе
		Ресурс = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.Предоплата.Имя;
		РесурсРегл = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ПредоплатаРегл.Имя;
		РесурсУпр = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ПредоплатаУпр.Имя;
		ПротивоположныйРесурс = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.Долг.Имя;
		ПротивоположныйРесурсРегл = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ДолгРегл.Имя;
		ПротивоположныйРесурсУпр = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.ДолгУпр.Имя;
		ЭтоДолг = Ложь;
	КонецЕсли;
	
	СуммаСписания = Мин(СтрокаОстатков[Ресурс],СуммаКСписанию);
	
	Если ЭтоДолг Тогда
		СуммаСписанияРегл = СуммаСписания(0,СуммаПредоплатыРегл,СуммаКСписанию,СуммаСписания);
		СуммаСписанияУпр = СуммаСписания(0,СуммаПредоплатыУпр,СуммаКСписанию,СуммаСписания);
	Иначе
		СуммаСписанияРегл = СуммаСписания(0,СтрокаОстатков[РесурсРегл],СтрокаОстатков[Ресурс],СуммаСписания);
		СуммаСписанияУпр = СуммаСписания(0,СтрокаОстатков[РесурсУпр],СтрокаОстатков[Ресурс],СуммаСписания);
	КонецЕсли;
	
	СуммаКСписанию = СуммаКСписанию - СуммаСписания;
	
	СтрокаОстатков[Ресурс] = СтрокаОстатков[Ресурс] - СуммаСписания;
	СтрокаОстатков[РесурсРегл] = СтрокаОстатков[РесурсРегл] - СуммаСписанияРегл;
	СтрокаОстатков[РесурсУпр] = СтрокаОстатков[РесурсУпр] - СуммаСписанияУпр;
	
	НовСтр_Расход = ТаблицаРасчетовПоСрокам.Добавить();
	НовСтр_Расход.ВидДвижения = ВидДвиженияНакопления.Расход;
	
	НовСтр_Расход.РасчетныйДокумент = СтрокаОстатков.РасчетныйДокумент;
	НовСтр_Расход.ДатаПлановогоПогашения = СтрокаОстатков.ДатаПлановогоПогашения;
	НовСтр_Расход.ДатаВозникновения = СтрокаОстатков.ДатаВозникновения;
	
	НовСтр_Расход.ДокументРегистратор = СтрокаРасчетовРегистратора.Регистратор;
	НовСтр_Расход.Период              = СтрокаРасчетовРегистратора.Период;
	НовСтр_Расход.ПорядокОперации     = СтрокаРасчетовРегистратора.ПорядокОперации;
	НовСтр_Расход.ПорядокЗачета       = СтрокаРасчетовРегистратора.ПорядокОперации;
	НовСтр_Расход.ИдентификаторФинЗаписи  = СтрокаРасчетовРегистратора.ИдентификаторРегистратора;
	НовСтр_Расход.ВалютаДокумента               = СтрокаРасчетовРегистратора.ВалютаДокумента;
	НовСтр_Расход.СтатьяДвиженияДенежныхСредств = СтрокаРасчетовРегистратора.СтатьяДвиженияДенежныхСредств;
	
	НовСтр_Расход.ХозяйственнаяОперация           = ГлобальныеПеременные.ХозяйственныеОперации["ЗачетАванса"];
	НовСтр_Расход.НастройкаХозяйственнойОперации  = ГлобальныеПеременные.НастройкиХО["ЗачетОплаты"];
	
	НовСтр_Расход[Ресурс]        = СуммаСписания;
	НовСтр_Расход[РесурсРегл]    = СуммаСписанияРегл;
	НовСтр_Расход[РесурсУпр]     = СуммаСписанияУпр;
	НовСтр_Расход[ПротивоположныйРесурс]        = 0;
	НовСтр_Расход[ПротивоположныйРесурсРегл]    = 0;
	НовСтр_Расход[ПротивоположныйРесурсУпр]     = 0;
	
	НовСтр_Расход.Сторно = Ложь;
	
	НовСтр_Расход.ЗаписьДоНачалаРасчета = Ложь;
	НовСтр_Расход.ОперацияВзаиморасчетов = Перечисления.ОперацииВзаиморасчетов.КорректировкаЗадолженности;
	
	НовСтр_Расход.КорАналитикаУчетаПоПартнерам = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
	НовСтр_Расход.КорОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка();
	НовСтр_Расход.АналитикаУчетаПоПартнерамПриемник = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка();
	НовСтр_Расход.ОбъектРасчетовПриемник = Справочники.ОбъектыРасчетов.ПустаяСсылка();
	НовСтр_Расход.ВалютаПриемник = Справочники.Валюты.ПустаяСсылка();
	НовСтр_Расход.СуммаПриемник = 0;
	НовСтр_Расход.ПоДаннымОбъектаРасчетовИсточника = Ложь;
	
	Если Ресурс = "Долг" Тогда
		ПереоценитьДолг(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаОстатков.РасчетныйДокумент, СтрокаРасчетовРегистратора.Период, Истина);
	КонецЕсли;
	
	Если ЭтоДолг Тогда
		СуммаПредоплатыРегл = СуммаПредоплатыРегл - СуммаСписанияРегл;
		СуммаПредоплатыУпр = СуммаПредоплатыУпр - СуммаСписанияУпр;
	Иначе
		СуммаПредоплатыРегл = СуммаПредоплатыРегл + СуммаСписанияРегл;
		СуммаПредоплатыУпр = СуммаПредоплатыУпр + СуммаСписанияУпр;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПочиночныеДвиженияПриНеобходимости(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаСторно)
	
	// Проверяем на минусовой остаток
	ТаблицаОстатковВзаиморасчетов.Свернуть("РасчетныйДокумент,ДатаПлановогоПогашения,ДатаВозникновения,ПорядокОперации,ДатаПереоценки, ПорядокЗачетаПоДатеПлатежа","Долг,ДолгРегл,ДолгУпр,Предоплата,ПредоплатаРегл,ПредоплатаУпр");
	Если ГлобальныеПеременные.ЗачетОплатПоДатеПлатежа Тогда
		ТаблицаОстатковВзаиморасчетов.Сортировать("ПорядокЗачетаПоДатеПлатежа ВОЗР, ПорядокОперации ВОЗР");
	Иначе
		ТаблицаОстатковВзаиморасчетов.Сортировать("ПорядокОперации ВОЗР, ПорядокЗачетаПоДатеПлатежа ВОЗР");
	КонецЕсли;
	Для Каждого СтрокаКонтроля Из ТаблицаОстатковВзаиморасчетов Цикл
		Если СтрокаКонтроля.Долг < 0 Или СтрокаКонтроля.Предоплата < 0 Тогда
			ДобавитьПочиночноеДвижениеНаМинусовойОстаток(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов,СтрокаКонтроля, СтрокаСторно);
		КонецЕсли;
	КонецЦикла;
	
	// Проверяем на развернутое сальдо
	ИтогДолг = ТаблицаОстатковВзаиморасчетов.Итог("Долг");
	ИтогПредоплата = ТаблицаОстатковВзаиморасчетов.Итог("Предоплата");
	Если ИтогДолг> 0 И ИтогПредоплата > 0 Тогда
		СуммаДляСворачивания = Мин(ИтогДолг, ИтогПредоплата);
		ДобавитьПочиночныеДвиженияНаРазвернутоеСальдо(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СуммаДляСворачивания, СтрокаСторно);
	КонецЕсли;
	
КонецПроцедуры

//Добавляет все движения регистратора сторно - движения исходного документа с минусом.
//Вызывается при добавлении строк зачета, когда одна из строк - это движение сторно.
Процедура ДобавитьСторноЗаписи(ГлобальныеПеременные,СтрокаРасчетов, ТаблицаРасчетовПоСрокам, ДвиженияИсходныхРегистраторов, ИсходныеРегистраторы, ТаблицаОстатковВзаиморасчетов)
	
	Если НЕ СтрокаРасчетов.Сторно Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияИсходногоДокумента = ДвиженияИсходныхРегистраторов.Скопировать(Новый Структура("ДокументРегистратор", СтрокаРасчетов.СторнируемыйРегистратор));
	Если ДвиженияИсходногоДокумента.Количество() = 0 Тогда
		ДвиженияИсходногоДокумента = ТаблицаРасчетовПоСрокам.НайтиСтроки(Новый Структура("ДокументРегистратор,ЗаписьДоНачалаРасчета,Сторно", СтрокаРасчетов.СторнируемыйРегистратор,Ложь,Ложь));
	КонецЕсли;
	
	СтрокаИсходногоРегистратора = ИсходныеРегистраторы.Найти(СтрокаРасчетов.Регистратор, "НовыйРегистратор");
	Если СтрокаИсходногоРегистратора <> Неопределено И НЕ СтрокаИсходногоРегистратора.Отсторнировано Тогда
		Для Каждого ДвижениеИсходногоДокумента Из ДвиженияИсходногоДокумента Цикл
			Если ДвижениеИсходногоДокумента.Долг <> 0 И ДвижениеИсходногоДокумента.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
				ПереоценитьДолг(ГлобальныеПеременные,ТаблицаРасчетовПоСрокам,ТаблицаОстатковВзаиморасчетов,ДвижениеИсходногоДокумента.РасчетныйДокумент,СтрокаИсходногоРегистратора.НовыйПериод);
			КонецЕсли;
			ДобавитьМинусовоеДвижениеСторно(ГлобальныеПеременные, ТаблицаРасчетовПоСрокам, ТаблицаОстатковВзаиморасчетов, СтрокаИсходногоРегистратора, ДвижениеИсходногоДокумента);
		КонецЦикла;
		СтрокаИсходногоРегистратора.Отсторнировано = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПереходНаОнлайнВзаиморасчеты

Процедура ЗаполнитьРегистрПереходаНаОнлайн()
	
	#Область ТекстЗапроса
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(РасчетыСКлиентами.ДатаДвижения) КАК ДатаДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентами.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСКлиентами.Валюта КАК Валюта,
	|	КОЛИЧЕСТВО(*) КАК КоличествоДокументов,
	|	МАКСИМУМ(РасчетыСКлиентами.Приоритет) КАК Приоритет
	|ИЗ 
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		РасчетыСПартнерами.Период КАК ДатаДвижения,
	|		РасчетыСПартнерами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		РасчетыСПартнерами.ЗаказКлиента КАК ОбъектРасчетов,
	|		РасчетыСПартнерами.Валюта КАК Валюта,
	|		ВЫБОР КОГДА ЕСТЬNULL(Расчеты.ПоДаннымОбъектаРасчетовИсточника,ЛОЖЬ)
	|			ТОГДА 2
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК Приоритет
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РасчетыСПартнерами
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|				ПО Расчеты.Регистратор = РасчетыСПартнерами.Регистратор
	|					И Расчеты.АналитикаУчетаПоПартнерам = РасчетыСПартнерами.АналитикаУчетаПоПартнерам
	|					И Расчеты.ОбъектРасчетов = РасчетыСПартнерами.ЗаказКлиента
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		РасчетыСПартнерами.Период КАК ДатаДвижения,
	|		РасчетыСПартнерами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		РасчетыСПартнерами.ОбъектРасчетов КАК ОбъектРасчетов,
	|		РасчетыСПартнерами.Валюта КАК Валюта,
	|		ВЫБОР КОГДА РасчетыСПартнерами.ПоДаннымОбъектаРасчетовИсточника
	|			ТОГДА 2
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК Приоритет
	|	ИЗ 
	|		РегистрНакопления.РасчетыСКлиентами КАК РасчетыСПартнерами
	|	ГДЕ
	|		НЕ ИСТИНА В (
	|			ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РасчетыПоДокументам
	|			ГДЕ
	|				РасчетыСПартнерами.АналитикаУчетаПоПартнерам = РасчетыПоДокументам.АналитикаУчетаПоПартнерам
	|				И РасчетыСПартнерами.ОбъектРасчетов = РасчетыПоДокументам.ЗаказКлиента
	|				И РасчетыСПартнерами.Валюта = РасчетыПоДокументам.Валюта
	|		)
	|
	|	) КАК РасчетыСКлиентами
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентами.ОбъектРасчетов,
	|	РасчетыСКлиентами.Валюта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(РасчетыСПоставщиками.ДатаДвижения) КАК ДатаДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов,
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщиками.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСПоставщиками.Валюта КАК Валюта,
	|	КОЛИЧЕСТВО(*) КАК КоличествоДокументов,
	|	МАКСИМУМ(РасчетыСПоставщиками.Приоритет) КАК Приоритет
	|ИЗ 
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		РасчетыСПартнерами.Период КАК ДатаДвижения,
	|		РасчетыСПартнерами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		РасчетыСПартнерами.ЗаказПоставщику КАК ОбъектРасчетов,
	|		РасчетыСПартнерами.Валюта КАК Валюта,
	|		ВЫБОР КОГДА ЕСТЬNULL(Расчеты.ПоДаннымОбъектаРасчетовИсточника,ЛОЖЬ)
	|			ТОГДА 2
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК Приоритет
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыСПартнерами
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
	|				ПО Расчеты.Регистратор = РасчетыСПартнерами.Регистратор
	|					И Расчеты.АналитикаУчетаПоПартнерам = РасчетыСПартнерами.АналитикаУчетаПоПартнерам
	|					И Расчеты.ОбъектРасчетов = РасчетыСПартнерами.ЗаказПоставщику
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		РасчетыСПартнерами.Период КАК ДатаДвижения,
	|		РасчетыСПартнерами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		РасчетыСПартнерами.ОбъектРасчетов КАК ОбъектРасчетов,
	|		РасчетыСПартнерами.Валюта КАК Валюта,
	|		ВЫБОР КОГДА РасчетыСПартнерами.ПоДаннымОбъектаРасчетовИсточника
	|			ТОГДА 2
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК Приоритет
	|	ИЗ 
	|		РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПартнерами
	|	ГДЕ
	|		НЕ ИСТИНА В (
	|			ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыПоДокументам
	|			ГДЕ
	|				РасчетыСПартнерами.АналитикаУчетаПоПартнерам = РасчетыПоДокументам.АналитикаУчетаПоПартнерам
	|				И РасчетыСПартнерами.ОбъектРасчетов = РасчетыПоДокументам.ЗаказПоставщику
	|				И РасчетыСПартнерами.Валюта = РасчетыПоДокументам.Валюта
	|		)
	|
	|	) КАК РасчетыСПоставщиками
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщиками.ОбъектРасчетов,
	|	РасчетыСПоставщиками.Валюта
	|
	|УПОРЯДОЧИТЬ ПО
	|	КоличествоДокументов УБЫВ,
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов";
	#КонецОбласти
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	Набор = РегистрыСведений.ЗаданияКРаспределениюРасчетов.СоздатьНаборЗаписей();
	Набор.Записать();
	НомерСтроки = 0;
	Пока Выборка.Следующий() Цикл
		
		НомерСтроки = НомерСтроки + 1;
		ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
		Если НомерСтроки = 1000 Тогда
			Набор.Записать(Ложь);
			Набор.Очистить();
			НомерСтроки = 0;
		КонецЕсли;
		
	КонецЦикла;
	Набор.Записать(Ложь);
	ВсегоКПереносу = Выборка.Количество();
	
	ХранилищеОбщихНастроек.Сохранить("ПереходНаНовуюАрхитектуруВзаиморасчетов", "ВсегоКПереносу", ВсегоКПереносу);
	
	Текст = НСтр("ru = 'Включение онлайн взаиморасчетов с партнерами.';
				|en = 'Enable online mutual settlements with partners.'", ОбщегоНазначения.КодОсновногоЯзыка());
	ВсегоЗаписей = НСтр("ru = 'Всего кортежей к переносу';
						|en = 'Total tuples for transfer'")+": %1";
	ЗаписьЖурналаРегистрации(Текст,
		УровеньЖурналаРегистрации.Информация,,,СтрШаблон(ВсегоЗаписей, ВсегоКПереносу));
	
КонецПроцедуры

// Выполняет заполнение оперативных взаиморасчетов при включении опции Онлайн взаиморасчетов
// 
// Параметры:
//   Параметры - Структура - Необязательный.
//   АдресРезультата - Строка - Необязательный
//
// Возвращаемое значение:
//   Число - Количество обработанных записей
//
Функция ВыполнитьПереносОфлайнВзаиморасчетов(Параметры = Неопределено, АдресРезультата = Неопределено) Экспорт
	
	Если Параметры <> Неопределено И Параметры.Свойство("ДанныеКОбработке") Тогда
		ДанныеКОбработке = Параметры.ДанныеКОбработке;
	Иначе
		ПараметрыДанных = ПараметрыДанныхМногопоточнойОбработки();
		ДанныеКОбработке = ДанныеДляОбработки(ПараметрыДанных).Данные;
	КонецЕсли;
	
	ПорядокОбработки = СформироватьПорцииОбработкиДанныхРегистровВзаиморасчетов(ДанныеКОбработке, 1000);
	
	КоличествоОбработанныхЗаписей = 0;
	ПорядокОперации = Новый Соответствие;
	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ХозяйственныеОперацииНеОтгрузка", ХозяйственныеОперацииНеОтгрузка());

	СписокТиповИсходящихПлатежей = Новый Массив;
	СписокТиповИсходящихПлатежей.Добавить(Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств"));
	СписокТиповИсходящихПлатежей.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	Запрос.УстановитьПараметр("СписокТиповИсходящихПлатежей", СписокТиповИсходящихПлатежей);
	
	КвалификаторСтроки = Новый КвалификаторыСтроки(40);
	ДанныеКОбработке.Колонки.Добавить("ПорядокОперации", Новый ОписаниеТипов("Строка", , , , КвалификаторСтроки));
	ДанныеКОбработке.Колонки.Добавить("НачальноеЗаполнение", Новый ОписаниеТипов("Булево"));

	Для ТекущийПорядокОбработки = 1 По ПорядокОбработки Цикл

		ПорцияДанныхКОбработке = ДанныеКОбработке.Скопировать(Новый Структура("ПорядокОбработки", ТекущийПорядокОбработки));
		
		КоличествоЗаписейКОбработке = ПорцияДанныхКОбработке.Количество();
		Если КоличествоЗаписейКОбработке = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		КоличествоЗаписейКОбработкеКлиенты = ПорцияДанныхКОбработке.НайтиСтроки(Новый Структура("ТипРасчетов",
			Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом)).Количество();
			
		ЕстьЗаписиРасчетыСКлиентами = (КоличествоЗаписейКОбработкеКлиенты > 0);
		ЕстьЗаписиРасчетыСПоставщиками = (КоличествоЗаписейКОбработке - КоличествоЗаписейКОбработкеКлиенты > 0);

		НачатьТранзакцию();

		Попытка
			
			// 1. Перенесем РасчетыПоДокументам.
			Запрос.УстановитьПараметр("ДанныеКОбработке", ПорцияДанныхКОбработке);
			
			Если ЕстьЗаписиРасчетыСКлиентами Тогда
				Запрос.Текст = ТекстЗапросаРасчетыСКлиентамиПоДокументам();
				ТаблицаДетальныхЗаписейКлиенты = Запрос.Выполнить().Выгрузить();
				ТаблицаДетальныхЗаписейКлиенты.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта");
			КонецЕсли;

			Если ЕстьЗаписиРасчетыСПоставщиками Тогда
				Запрос.Текст = ТекстЗапросаРасчетыСПоставщикамиПоДокументам();
				ТаблицаДетальныхЗаписейПоставщики = Запрос.Выполнить().Выгрузить();
				ТаблицаДетальныхЗаписейПоставщики.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта");
			КонецЕсли;
			
			Для Каждого СтрокаКОбработке Из ПорцияДанныхКОбработке Цикл
				
				СтруктураОтбора = Новый Структура;
				СтруктураОтбора.Вставить("АналитикаУчетаПоПартнерам", СтрокаКОбработке.АналитикаУчетаПоПартнерам);
				СтруктураОтбора.Вставить("ОбъектРасчетов", СтрокаКОбработке.ОбъектРасчетов);
				СтруктураОтбора.Вставить("Валюта", СтрокаКОбработке.ВалютаРасчетов);
				
				Если СтрокаКОбработке.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом Тогда
					ТаблицаДетальныхЗаписей = ТаблицаДетальныхЗаписейКлиенты.Скопировать(СтруктураОтбора);
				Иначе
					ТаблицаДетальныхЗаписей = ТаблицаДетальныхЗаписейПоставщики.Скопировать(СтруктураОтбора);
				КонецЕсли;

				ЭтоРасчетыСКлиентами = (СтрокаКОбработке.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
				Если ТаблицаДетальныхЗаписей.Количество() > 0 Тогда
					ЗаписатьНовыеЗаписиРасчетовПоСрокам(ЭтоРасчетыСКлиентами, ТаблицаДетальныхЗаписей, ПорядокОперации);
					КоличествоОбработанныхЗаписей = КоличествоОбработанныхЗаписей + ТаблицаДетальныхЗаписей.Количество();
				КонецЕсли;
				
				СтрокаКОбработке.НачальноеЗаполнение = НЕ ЗначениеЗаполнено(СтрокаКОбработке.ДатаПересчета);
				
				Если ЗначениеЗаполнено(СтрокаКОбработке.ДатаПересчета) Тогда
					СтрокаКОбработке.ПорядокОперации = Формат(СтрокаКОбработке.ДатаПересчета, "ДФ=yyyyMMdd000000");
				КонецЕсли;
				
			КонецЦикла;

			ПараметрыРаспределения = ПараметрыРаспределенияРасчетов();
			ПараметрыРаспределения.РаспределениеСУчетомПриемников = Ложь;
			ПараметрыРаспределения.ТаблицаИзменений = ПорцияДанныхКОбработке;
			
			ЗаполнитьОперативныеВзаиморасчетыПоТаблице(ПараметрыРаспределения);
			
			Для Каждого Запись Из ПорцияДанныхКОбработке Цикл

				ДопИзмеренияОтбора = "";
				Если Запись.ДатаПересчета = Дата('00010101') Тогда
					ДопИзмеренияОтбора = "ДатаПересчета";
				КонецЕсли;
				ОтметитьВыполнениеЗадания(Запись, ДопИзмеренияОтбора);

			КонецЦикла;

			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Включение онлайн взаиморасчетов с партнерами. Работа потока.';
					|en = 'Enable online settlements with partners. Flow work.'",
				ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
		КонецПопытки;
		
	КонецЦикла;
	
	Если АдресРезультата <> Неопределено Тогда
		Результат = Новый Структура("КоличествоОбработанныхЗаписей", КоличествоОбработанныхЗаписей);
		ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	КонецЕсли;
	
	Возврат КоличествоОбработанныхЗаписей;
	
КонецФункции

Процедура ЗаписатьНовыеЗаписиРасчетовПоСрокам(ЭтоРасчетыСКлиентами, ТаблицаДетальныхЗаписей, ПорядокОперации)
	
	Если ЭтоРасчетыСКлиентами Тогда
		НаборЗаписей = РегистрыНакопления.РасчетыСКлиентамиПоСрокам.СоздатьНаборЗаписей();
	Иначе
		НаборЗаписей = РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.СоздатьНаборЗаписей();
	КонецЕсли;
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	
	ТаблицаДетальныхЗаписей.Колонки.Добавить("Сторно", Новый ОписаниеТипов("Булево"));
	ТаблицаДетальныхЗаписей.Колонки.Добавить("ТипЗаписиВзаиморасчетов", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыЗаписейВзаиморасчетов"));
	ТаблицаДетальныхЗаписей.Колонки.Добавить("Регистратор", Новый ОписаниеТипов("ДокументСсылка.РегистраторРасчетов"));
	ТаблицаДетальныхЗаписей.Индексы.Добавить("Регистратор");
	
	МассивСторно = Новый Массив;
	Для Каждого СтрокаДвижений Из ТаблицаДетальныхЗаписей Цикл
		Если ТипЗнч(СтрокаДвижений.ДокументРегистратор) = Тип("ДокументСсылка.Сторно") Тогда
			МассивСторно.Добавить(СтрокаДвижений.ДокументРегистратор);
		КонецЕсли;
	КонецЦикла;
	СоответствиеСторнируемыхДокументов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивСторно, "СторнируемыйДокумент");
	Для Каждого СтрокаДвижений Из ТаблицаДетальныхЗаписей Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаДвижений.ПорядокОперации) Тогда
			Если ПорядокОперации.Получить(СтрокаДвижений.Период) = Неопределено Тогда
				ПорядокОперации.Вставить(СтрокаДвижений.Период, Порядок(СтрокаДвижений.Период, "", Тип("ДокументСсылка.РасчетКурсовыхРазниц"),"9","99"));
			КонецЕсли;
			СтрокаДвижений.ПорядокОперации = ПорядокОперации[СтрокаДвижений.Период];
		КонецЕсли;
		ТипРегистратора = ТипЗнч(СтрокаДвижений.ДокументРегистратор);
		Если ТипРегистратора = Тип("ДокументСсылка.Сторно")
			И СоответствиеСторнируемыхДокументов[СтрокаДвижений.ДокументРегистратор] <> Неопределено Тогда
			ТипРегистратора = ТипЗнч(СоответствиеСторнируемыхДокументов[СтрокаДвижений.ДокументРегистратор]);
		КонецЕсли;
		ЗаполнитьТипЗаписиВзаиморасчетов(СтрокаДвижений, ТипРегистратора);
	КонецЦикла;
	
	ТаблицаАналитик = ТаблицаДетальныхЗаписей.Скопировать();
	ТаблицаАналитик.Свернуть("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта, Организация");
	Отбор = ТаблицаАналитик[0];
	
	СтруктураПараметровРегистраторов = Новый Структура;
	СтруктураПараметровРегистраторов.Вставить("ОбъектРасчетов",            Отбор.ОбъектРасчетов);
	СтруктураПараметровРегистраторов.Вставить("АналитикаУчетаПоПартнерам", Отбор.АналитикаУчетаПоПартнерам);
	СтруктураПараметровРегистраторов.Вставить("ВалютаРасчетов",            Отбор.Валюта);
	СтруктураПараметровРегистраторов.Вставить("Организация",               Отбор.Организация);
	СтруктураПараметровРегистраторов.Вставить("ЭтоРасчетыСКлиентами",      ЭтоРасчетыСКлиентами);
	
	ТаблицаРегистраторов = ТаблицаСвободныхРегистраторовРасчета(СтруктураПараметровРегистраторов);
	ПодготовитьДанныеДляЗаписи(СтруктураПараметровРегистраторов, ТаблицаДетальныхЗаписей, НаборЗаписей, ТаблицаРегистраторов);
	ЗаписатьПорционно(СтруктураПараметровРегистраторов, ТаблицаДетальныхЗаписей, НаборЗаписей);

КонецПроцедуры

Функция ТекстЗапросаРасчетыСКлиентамиПоДокументам()
	
	Возврат "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОсновныеПараметрыРасчета.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ОсновныеПараметрыРасчета.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ОсновныеПараметрыРасчета.ВалютаРасчетов КАК ВалютаРасчетов
	|ПОМЕСТИТЬ ВтОсновныеПараметрыРасчета
	|ИЗ
	|	&ДанныеКОбработке КАК ОсновныеПараметрыРасчета
	|ГДЕ
	|	ОсновныеПараметрыРасчета.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	ВалютаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВтРегистраторы
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОсновныеПараметрыРасчета КАК ОсновныеПараметрыРасчета
	|			ПО Расчеты.АналитикаУчетаПоПартнерам = ОсновныеПараметрыРасчета.АналитикаУчетаПоПартнерам
	|				И Расчеты.ЗаказКлиента = ОсновныеПараметрыРасчета.ОбъектРасчетов
	|				И Расчеты.Валюта = ОсновныеПараметрыРасчета.ВалютаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР КОГДА РасчетыСКлиентами.РасчетныйДокумент <> Неопределено 
	|		ТОГДА РасчетыСКлиентами.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСКлиентами.Регистратор 
	|	КОНЕЦ                                      КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(РасчетыСКлиентами.Период,ДЕНЬ) КАК Дата,
	|	МИНИМУМ(РасчетыСКлиентами.ПорядокОперации) КАК ПорядокОперации,
	|	МИНИМУМ(РасчетыСКлиентами.ПорядокЗачетаПоДатеПлатежа) КАК ПорядокЗачетаПоДатеПлатежа,
	|	МИНИМУМ(ВЫБОР КОГДА РасчетыСКлиентами.Регистратор ССЫЛКА Документ.ВводОстатков
	|					ИЛИ РасчетыСКлиентами.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов
	|						ТОГДА РасчетыСКлиентами.ДатаРегистратора
	|				ИНАЧЕ РасчетыСКлиентами.Период
	|			КОНЕЦ)                             КАК ДатаВозникновения,
	|	МИНИМУМ(РасчетыСКлиентами.ВалютаДокумента) КАК ВалютаДокумента,
	|	МАКСИМУМ(РасчетыСКлиентами.ДатаПлатежа)    КАК ДатаПлатежа
	|ПОМЕСТИТЬ ПорядокДокументов
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРегистраторы КАК Регистраторы
	|			ПО Регистраторы.Регистратор = РасчетыСКлиентами.Регистратор
	|ГДЕ
	|	РасчетыСКлиентами.Активность
	|	И НЕ РасчетыСКлиентами.ПорядокОперации = """"
	|	И РасчетыСКлиентами.Сумма <> 0
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА РасчетыСКлиентами.РасчетныйДокумент <> Неопределено 
	|		ТОГДА РасчетыСКлиентами.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСКлиентами.Регистратор 
	|	КОНЕЦ,
	|	НАЧАЛОПЕРИОДА(РасчетыСКлиентами.Период,ДЕНЬ)
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР КОГДА РасчетыСКлиентами.РасчетныйДокумент <> Неопределено 
	|		ТОГДА РасчетыСКлиентами.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСКлиентами.Регистратор 
	|	КОНЕЦ                                      КАК Регистратор,
	|	МИНИМУМ(РасчетыСКлиентами.ПорядокОперации) КАК ПорядокОперации,
	|	МИНИМУМ(РасчетыСКлиентами.ПорядокЗачетаПоДатеПлатежа) КАК ПорядокЗачетаПоДатеПлатежа,
	|	МИНИМУМ(ВЫБОР КОГДА РасчетыСКлиентами.Регистратор ССЫЛКА Документ.ВводОстатков
	|					ИЛИ РасчетыСКлиентами.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов
	|						ТОГДА РасчетыСКлиентами.ДатаРегистратора
	|				ИНАЧЕ РасчетыСКлиентами.Период
	|			КОНЕЦ)                             КАК ДатаВозникновения,
	|	МИНИМУМ(РасчетыСКлиентами.ВалютаДокумента) КАК ВалютаДокумента,
	|	МАКСИМУМ(РасчетыСКлиентами.ДатаПлатежа)    КАК ДатаПлатежа
	|ПОМЕСТИТЬ ДатыВозникновения
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРегистраторы КАК Регистраторы
	|			ПО Регистраторы.Регистратор = РасчетыСКлиентами.Регистратор
	|ГДЕ
	|	РасчетыСКлиентами.Активность
	|	И НЕ РасчетыСКлиентами.ПорядокОперации = """"
	|	И РасчетыСКлиентами.Сумма <> 0
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА РасчетыСКлиентами.РасчетныйДокумент <> Неопределено 
	|		ТОГДА РасчетыСКлиентами.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСКлиентами.Регистратор 
	|	КОНЕЦ
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам             КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентамиПоДокументам.ЗаказКлиента                          КАК ОбъектРасчетов,
	|	РасчетыСКлиентамиПоДокументам.ЗаказКлиента.ТипОбъектаРасчетов       КАК ТипОбъектаРасчетов,
	|	РасчетыСКлиентамиПоДокументам.Валюта                                КАК Валюта,
	|	РасчетыСКлиентамиПоДокументам.Регистратор                           КАК ДокументРегистратор,
	|	РасчетыСКлиентамиПоДокументам.Период                                КАК Период,
	|	РасчетыСКлиентамиПоДокументам.РасчетныйДокумент                     КАК РасчетныйДокумент,
	|	РасчетыСКлиентамиПоДокументам.ХозяйственнаяОперация                 КАК ХозяйственнаяОперация,
	|	РасчетыСКлиентамиПоДокументам.СтатьяДвиженияДенежныхСредств         КАК СтатьяДвиженияДенежныхСредств,
	|	ВЫБОР КОГДА ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И (РасчетыСКлиентамиПоДокументам.Долг > 0
	|			ИЛИ РасчетыСКлиентамиПоДокументам.ДолгРегл > 0 ИЛИ РасчетыСКлиентамиПоДокументам.ДолгУпр > 0
	|			ИЛИ РасчетыСКлиентамиПоДокументам.Предоплата < 0 ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаРегл < 0
	|			ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаУпр < 0)
	|		ИЛИ ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И (РасчетыСКлиентамиПоДокументам.Предоплата > 0
	|			ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаРегл > 0 ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаУпр > 0
	|			ИЛИ РасчетыСКлиентамиПоДокументам.Долг < 0 ИЛИ РасчетыСКлиентамиПоДокументам.ДолгРегл < 0 
	|			ИЛИ РасчетыСКлиентамиПоДокументам.ДолгУпр < 0) ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ                                                               КАК ВидДвижения,
	|	ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.РасчетныйДокумент КАК Документ.КорректировкаРеализации).ДокументОснование КАК СвязанныйДокумент,
	|
	|	ВЫБОР КОГДА РасчетыСКлиентамиПоДокументам.Предоплата < 0 ТОГДА
	|		-РасчетыСКлиентамиПоДокументам.Предоплата 
	|	ИНАЧЕ
	|		РасчетыСКлиентамиПоДокументам.Предоплата
	|	КОНЕЦ                                                               КАК Предоплата,
	|	ВЫБОР КОГДА РасчетыСКлиентамиПоДокументам.ПредоплатаУпр < 0 ТОГДА
	|		-РасчетыСКлиентамиПоДокументам.ПредоплатаУпр 
	|	ИНАЧЕ
	|		РасчетыСКлиентамиПоДокументам.ПредоплатаУпр
	|	КОНЕЦ                                                               КАК ПредоплатаУпр,
	|	ВЫБОР КОГДА РасчетыСКлиентамиПоДокументам.ПредоплатаРегл < 0 ТОГДА
	|		-РасчетыСКлиентамиПоДокументам.ПредоплатаРегл 
	|	ИНАЧЕ
	|		РасчетыСКлиентамиПоДокументам.ПредоплатаРегл
	|	КОНЕЦ                                                               КАК ПредоплатаРегл,
	|	ВЫБОР КОГДА РасчетыСКлиентамиПоДокументам.Долг < 0 ТОГДА
	|		-РасчетыСКлиентамиПоДокументам.Долг 
	|	ИНАЧЕ
	|		РасчетыСКлиентамиПоДокументам.Долг
	|	КОНЕЦ                                                               КАК Долг,
	|	ВЫБОР КОГДА РасчетыСКлиентамиПоДокументам.ДолгУпр < 0 ТОГДА
	|		-РасчетыСКлиентамиПоДокументам.ДолгУпр 
	|	ИНАЧЕ
	|		РасчетыСКлиентамиПоДокументам.ДолгУпр
	|	КОНЕЦ                                                               КАК ДолгУпр,
	|	ВЫБОР КОГДА РасчетыСКлиентамиПоДокументам.ДолгРегл < 0 ТОГДА
	|		-РасчетыСКлиентамиПоДокументам.ДолгРегл 
	|	ИНАЧЕ
	|		РасчетыСКлиентамиПоДокументам.ДолгРегл
	|	КОНЕЦ                                                               КАК ДолгРегл,
	|
	|	ВЫБОР КОГДА РасчетыСКлиентамиПоДокументам.Предоплата <> 0 ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА (ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И (РасчетыСКлиентамиПоДокументам.Долг > 0
	|				ИЛИ РасчетыСКлиентамиПоДокументам.ДолгРегл > 0 ИЛИ РасчетыСКлиентамиПоДокументам.ДолгУпр > 0
	|				ИЛИ РасчетыСКлиентамиПоДокументам.Предоплата < 0 ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаРегл < 0
	|				ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаУпр < 0)
	|			ИЛИ ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И (РасчетыСКлиентамиПоДокументам.Предоплата > 0
	|				ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаРегл > 0 ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаУпр > 0
	|				ИЛИ РасчетыСКлиентамиПоДокументам.Долг < 0 ИЛИ РасчетыСКлиентамиПоДокументам.ДолгРегл < 0 
	|				ИЛИ РасчетыСКлиентамиПоДокументам.ДолгУпр < 0))
	|			И НЕ ((РасчетыСКлиентамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатков ИЛИ
	|					РасчетыСКлиентамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов) И 
	|					РасчетыСКлиентамиПоДокументам.РасчетныйДокумент <> Неопределено)
	|			И НЕ РасчетыСКлиентамиПоДокументам.Регистратор ССЫЛКА Документ.РасчетКурсовыхРазниц ТОГДА
	|			ЕСТЬNULL(ДатыВозникновения.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДатыВозникновения.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
	|	КОНЕЦ                                                               КАК ДатаПлановогоПогашения,
	|	ЕСТЬNULL(ДатыВозникновения.ДатаВозникновения,ДАТАВРЕМЯ(1,1,1))      КАК ДатаВозникновения,
	|	ЕСТЬNULL(ПорядокДокументов.ПорядокОперации,"""")                    КАК ПорядокОперации,
	|	ЕСТЬNULL(ДатыВозникновения.ПорядокЗачетаПоДатеПлатежа,"""")         КАК ПорядокЗачетаРегистратора,
	|	ЕСТЬNULL(ДатыВозникновения.ПорядокЗачетаПоДатеПлатежа,"""")         КАК ПорядокЗачета,
	|	ЕСТЬNULL(ДатыВозникновения.ВалютаДокумента,
	|		ЕСТЬNULL(ДатыВозникновения.ВалютаДокумента,
	|			ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)))                  КАК ВалютаДокумента
	|ПОМЕСТИТЬ ВтРасчеты
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РасчетыСКлиентамиПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОсновныеПараметрыРасчета КАК ОсновныеПараметрыРасчета
	|			ПО РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам = ОсновныеПараметрыРасчета.АналитикаУчетаПоПартнерам
	|				И РасчетыСКлиентамиПоДокументам.ЗаказКлиента = ОсновныеПараметрыРасчета.ОбъектРасчетов
	|				И РасчетыСКлиентамиПоДокументам.Валюта = ОсновныеПараметрыРасчета.ВалютаРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокДокументов КАК ПорядокДокументов
	|			ПО РасчетыСКлиентамиПоДокументам.Регистратор = ПорядокДокументов.Регистратор
	|				И НАЧАЛОПЕРИОДА(РасчетыСКлиентамиПоДокументам.Период) = ПорядокДокументов.Дата
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыВозникновения КАК ДатыВозникновения
	|			ПО РасчетыСКлиентамиПоДокументам.РасчетныйДокумент = ДатыВозникновения.Регистратор
	|ГДЕ
	|	(РасчетыСКлиентамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатков
	|		ИЛИ РасчетыСКлиентамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов)
	|	И РасчетыСКлиентамиПоДокументам.Регистратор <> РасчетыСКлиентамиПоДокументам.РасчетныйДокумент
	|	И (РасчетыСКлиентамиПоДокументам.Предоплата <> 0
	|		ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаРегл <> 0
	|		ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаУпр <> 0
	|		ИЛИ РасчетыСКлиентамиПоДокументам.Долг <> 0
	|		ИЛИ РасчетыСКлиентамиПоДокументам.ДолгРегл <> 0
	|		ИЛИ РасчетыСКлиентамиПоДокументам.ДолгУпр <> 0)
	|	И РасчетыСКлиентамиПоДокументам.Активность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам             КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентамиПоДокументам.ЗаказКлиента                          КАК ОбъектРасчетов,
	|	РасчетыСКлиентамиПоДокументам.ЗаказКлиента.ТипОбъектаРасчетов       КАК ТипОбъектаРасчетов,
	|	РасчетыСКлиентамиПоДокументам.Валюта                                КАК Валюта,
	|	РасчетыСКлиентамиПоДокументам.Регистратор                           КАК ДокументРегистратор,
	|	РасчетыСКлиентамиПоДокументам.Период                                КАК Период,
	|	РасчетыСКлиентамиПоДокументам.РасчетныйДокумент                     КАК РасчетныйДокумент,
	|	РасчетыСКлиентамиПоДокументам.ХозяйственнаяОперация                 КАК ХозяйственнаяОперация,
	|	РасчетыСКлиентамиПоДокументам.СтатьяДвиженияДенежныхСредств         КАК СтатьяДвиженияДенежныхСредств,
	|ВЫБОР КОГДА ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) 
	|			И (РасчетыСКлиентамиПоДокументам.Долг > 0
	|				ИЛИ РасчетыСКлиентамиПоДокументам.ДолгРегл > 0 ИЛИ РасчетыСКлиентамиПоДокументам.ДолгУпр > 0
	|				ИЛИ РасчетыСКлиентамиПоДокументам.Предоплата < 0 ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаРегл < 0
	|				ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаУпр < 0
	|				ИЛИ (ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиПоДокументам.Регистратор) В (&СписокТиповИсходящихПлатежей) 
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоПлатежнойКарте).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//++ Локализация
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоЯндексКассе).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//-- Локализация
	|					)
	|						И РасчетыСКлиентамиПоДокументам.Предоплата > 0)
	|		ИЛИ ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И НЕ ((ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиПоДокументам.Регистратор) В (&СписокТиповИсходящихПлатежей)
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоПлатежнойКарте).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//++ Локализация
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоЯндексКассе).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//-- Локализация
	|			)
	|				И РасчетыСКлиентамиПоДокументам.Предоплата > 0)
	|			И (РасчетыСКлиентамиПоДокументам.Предоплата > 0
	|				ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаРегл > 0 ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаУпр > 0
	|				ИЛИ РасчетыСКлиентамиПоДокументам.Долг < 0 ИЛИ РасчетыСКлиентамиПоДокументам.ДолгРегл < 0 
	|				ИЛИ РасчетыСКлиентамиПоДокументам.ДолгУпр < 0) ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ                                                               КАК ВидДвижения,
	|	ВЫБОР КОГДА РасчетыСКлиентамиПоДокументам.Регистратор ССЫЛКА Документ.КорректировкаРеализации 
	|		ТОГДА ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.КорректировкаРеализации).ДокументОснование
	|		ИНАЧЕ ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.РасчетныйДокумент КАК Документ.КорректировкаРеализации).ДокументОснование
	|	КОНЕЦ КАК СвязанныйДокумент,
	|
	|	ВЫБОР КОГДА (ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиПоДокументам.Регистратор) В (&СписокТиповИсходящихПлатежей)
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоПлатежнойКарте).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//++ Локализация
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоЯндексКассе).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//-- Локализация
	|				)
	|		И РасчетыСКлиентамиПоДокументам.Предоплата > 0 
	|			ТОГДА 0
	|		КОГДА РасчетыСКлиентамиПоДокументам.Предоплата < 0 ТОГДА
	|			-РасчетыСКлиентамиПоДокументам.Предоплата 
	|	ИНАЧЕ
	|		РасчетыСКлиентамиПоДокументам.Предоплата
	|	КОНЕЦ                                                               КАК Предоплата,
	|	ВЫБОР КОГДА (ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиПоДокументам.Регистратор) В (&СписокТиповИсходящихПлатежей)
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоПлатежнойКарте).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//++ Локализация
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоЯндексКассе).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//-- Локализация
	|				)
	|		И РасчетыСКлиентамиПоДокументам.Предоплата > 0 
	|			ТОГДА 0
	|		КОГДА РасчетыСКлиентамиПоДокументам.ПредоплатаУпр < 0 ТОГДА
	|			-РасчетыСКлиентамиПоДокументам.ПредоплатаУпр 
	|	ИНАЧЕ
	|		РасчетыСКлиентамиПоДокументам.ПредоплатаУпр
	|	КОНЕЦ                                                               КАК ПредоплатаУпр,
	|	ВЫБОР КОГДА (ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиПоДокументам.Регистратор) В (&СписокТиповИсходящихПлатежей)
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоПлатежнойКарте).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//++ Локализация
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоЯндексКассе).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//-- Локализация
	|				)
	|		И РасчетыСКлиентамиПоДокументам.Предоплата > 0 
	|			ТОГДА 0
	|		КОГДА РасчетыСКлиентамиПоДокументам.ПредоплатаРегл < 0 ТОГДА
	|			-РасчетыСКлиентамиПоДокументам.ПредоплатаРегл
	|	ИНАЧЕ
	|		РасчетыСКлиентамиПоДокументам.ПредоплатаРегл
	|	КОНЕЦ                                                               КАК ПредоплатаРегл,
	|	ВЫБОР КОГДА РасчетыСКлиентамиПоДокументам.Долг < 0 ТОГДА
	|		-РасчетыСКлиентамиПоДокументам.Долг 
	|	ИНАЧЕ
	|		РасчетыСКлиентамиПоДокументам.Долг
	|		+ ВЫБОР КОГДА (ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиПоДокументам.Регистратор) В (&СписокТиповИсходящихПлатежей)
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоПлатежнойКарте).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//++ Локализация
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоЯндексКассе).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//-- Локализация
	|					)
	|			И РасчетыСКлиентамиПоДокументам.Предоплата > 0
	|				ТОГДА РасчетыСКлиентамиПоДокументам.Предоплата
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ                                                               КАК Долг,
	|	ВЫБОР КОГДА РасчетыСКлиентамиПоДокументам.ДолгУпр < 0 ТОГДА
	|		-РасчетыСКлиентамиПоДокументам.ДолгУпр 
	|	ИНАЧЕ
	|		РасчетыСКлиентамиПоДокументам.ДолгУпр
	|		+ ВЫБОР КОГДА (ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиПоДокументам.Регистратор) В (&СписокТиповИсходящихПлатежей)
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоПлатежнойКарте).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//++ Локализация
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоЯндексКассе).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//-- Локализация
	|						)
	|			И РасчетыСКлиентамиПоДокументам.ПредоплатаУпр > 0
	|				ТОГДА РасчетыСКлиентамиПоДокументам.ПредоплатаУпр
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ                                                               КАК ДолгУпр,
	|	ВЫБОР КОГДА РасчетыСКлиентамиПоДокументам.ДолгРегл < 0 ТОГДА
	|		-РасчетыСКлиентамиПоДокументам.ДолгРегл 
	|	ИНАЧЕ
	|		РасчетыСКлиентамиПоДокументам.ДолгРегл
	|		+ ВЫБОР КОГДА (ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиПоДокументам.Регистратор) В (&СписокТиповИсходящихПлатежей)
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоПлатежнойКарте).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//++ Локализация
	|					ИЛИ ЕСТЬNULL(ВЫРАЗИТЬ(РасчетыСКлиентамиПоДокументам.Регистратор КАК Документ.ОперацияПоЯндексКассе).ХозяйственнаяОперация,Неопределено) = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту)
	//-- Локализация
	|						)
	|			И РасчетыСКлиентамиПоДокументам.ПредоплатаРегл > 0
	|				ТОГДА РасчетыСКлиентамиПоДокументам.ПредоплатаРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ                                                               КАК ДолгРегл,
	|
	|	ВЫБОР КОГДА РасчетыСКлиентамиПоДокументам.Предоплата <> 0 ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА (ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И (РасчетыСКлиентамиПоДокументам.Долг > 0
	|				ИЛИ РасчетыСКлиентамиПоДокументам.ДолгРегл > 0 ИЛИ РасчетыСКлиентамиПоДокументам.ДолгУпр > 0)
	|			ИЛИ ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И (РасчетыСКлиентамиПоДокументам.Предоплата > 0
	|				ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаРегл > 0 ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаУпр > 0))
	|			И НЕ ((РасчетыСКлиентамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатков ИЛИ
	|					РасчетыСКлиентамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов) И
	|					РасчетыСКлиентамиПоДокументам.РасчетныйДокумент <> Неопределено)
	|			И НЕ РасчетыСКлиентамиПоДокументам.Регистратор ССЫЛКА Документ.РасчетКурсовыхРазниц ТОГДА
	|			ВЫБОР
	|				КОГДА ЕСТЬNULL(ДатыВозникновения.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1)) = ДАТАВРЕМЯ(1,1,1)
	|					ТОГДА ЕСТЬNULL(ПорядокДокументов.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
	|				ИНАЧЕ ЕСТЬNULL(ДатыВозникновения.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
	|			КОНЕЦ
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДатыВозникновения.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
	|	КОНЕЦ                                                               КАК ДатаПлановогоПогашения,
	|	ЕСТЬNULL(ДатыВозникновения.ДатаВозникновения,ДАТАВРЕМЯ(1,1,1))      КАК ДатаВозникновения,
	|	ЕСТЬNULL(ПорядокДокументов.ПорядокОперации,"""")                    КАК ПорядокОперации,
	|	ЕСТЬNULL(ПорядокДокументов.ПорядокЗачетаПоДатеПлатежа,"""")         КАК ПорядокЗачетаРегистратора,
	|	ЕСТЬNULL(ДатыВозникновения.ПорядокЗачетаПоДатеПлатежа,"""")         КАК ПорядокЗачета,
	|	ЕСТЬNULL(ДатыВозникновения.ВалютаДокумента,
	|		ЕСТЬNULL(ПорядокДокументов.ВалютаДокумента,
	|			ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)))                  КАК ВалютаДокумента
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК РасчетыСКлиентамиПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОсновныеПараметрыРасчета КАК ОсновныеПараметрыРасчета
	|			ПО РасчетыСКлиентамиПоДокументам.АналитикаУчетаПоПартнерам = ОсновныеПараметрыРасчета.АналитикаУчетаПоПартнерам
	|				И РасчетыСКлиентамиПоДокументам.ЗаказКлиента = ОсновныеПараметрыРасчета.ОбъектРасчетов
	|				И РасчетыСКлиентамиПоДокументам.Валюта = ОсновныеПараметрыРасчета.ВалютаРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокДокументов КАК ПорядокДокументов
	|			ПО РасчетыСКлиентамиПоДокументам.Регистратор = ПорядокДокументов.Регистратор
	|				И НАЧАЛОПЕРИОДА(РасчетыСКлиентамиПоДокументам.Период) = ПорядокДокументов.Дата
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыВозникновения КАК ДатыВозникновения
	|			ПО РасчетыСКлиентамиПоДокументам.РасчетныйДокумент = ДатыВозникновения.Регистратор
	|ГДЕ
	|	(НЕ (РасчетыСКлиентамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатков
	|		ИЛИ РасчетыСКлиентамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов)
	|	ИЛИ РасчетыСКлиентамиПоДокументам.Регистратор = РасчетыСКлиентамиПоДокументам.РасчетныйДокумент)
	|	И (РасчетыСКлиентамиПоДокументам.Предоплата <> 0
	|		ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаРегл <> 0
	|		ИЛИ РасчетыСКлиентамиПоДокументам.ПредоплатаУпр <> 0
	|		ИЛИ РасчетыСКлиентамиПоДокументам.Долг <> 0
	|		ИЛИ РасчетыСКлиентамиПоДокументам.ДолгРегл <> 0
	|		ИЛИ РасчетыСКлиентамиПоДокументам.ДолгУпр <> 0)
	|	И РасчетыСКлиентамиПоДокументам.Активность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.ОбъектРасчетов                КАК ОбъектРасчетов,
	|	Расчеты.Валюта                        КАК Валюта,
	|	Расчеты.ДокументРегистратор           КАК ДокументРегистратор,
	|	Расчеты.РасчетныйДокумент             КАК РасчетныйДокумент,
	|	Расчеты.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|	Расчеты.Период                        КАК Период,
	|	Расчеты.ДатаПлановогоПогашения        КАК ДатаПлановогоПогашения,
	|	Расчеты.ДатаВозникновения             КАК ДатаВозникновения,
	|	Расчеты.ВидДвижения                   КАК ВидДвижения,
	|	СУММА(Расчеты.Предоплата)             КАК Предоплата,
	|	СУММА(Расчеты.ПредоплатаУпр)          КАК ПредоплатаУпр,
	|	СУММА(Расчеты.ПредоплатаРегл)         КАК ПредоплатаРегл,
	|	СУММА(Расчеты.Долг)                   КАК Долг,
	|	СУММА(Расчеты.ДолгУпр)                КАК ДолгУпр,
	|	СУММА(Расчеты.ДолгРегл)               КАК ДолгРегл,
	|	Расчеты.СвязанныйДокумент             КАК СвязанныйДокумент,
	|	Расчеты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Расчеты.ПорядокОперации               КАК ПорядокОперации,
	|	Расчеты.ПорядокЗачета                 КАК ПорядокЗачета,
	|	Расчеты.ВалютаДокумента               КАК ВалютаДокумента,
	|	Расчеты.Организация                   КАК Организация
	|ИЗ (
	|	ВЫБРАТЬ
	|		ЗачетыОплат.АналитикаУчетаПоПартнерам                            КАК АналитикаУчетаПоПартнерам,
	|		ЗачетыОплат.ОбъектРасчетов                                       КАК ОбъектРасчетов,
	|		ЗачетыОплат.Валюта                                               КАК Валюта,
	|		ЗачетыОплат.ДокументРегистратор                                  КАК ДокументРегистратор,
	|		ЗачетыОплат.ДокументРегистратор                                  КАК РасчетныйДокумент,
	|		ЗачетыОплат.ХозяйственнаяОперация                                КАК ХозяйственнаяОперация,
	|		ЗачетыОплат.Период                                               КАК Период,
	|		ДатыВозникновения.ДатаПлатежа                                    КАК ДатаПлановогоПогашения,
	|		ЗачетыОплат.Период                                               КАК ДатаВозникновения,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                           КАК ВидДвижения,
	|		0                                                                КАК Предоплата,
	|		0                                                                КАК ПредоплатаУпр,
	|		0                                                                КАК ПредоплатаРегл,
	|		ЗачетыОплат.Предоплата                                           КАК Долг,
	|		ЗачетыОплат.ПредоплатаУпр                                        КАК ДолгУпр,
	|		ЗачетыОплат.ПредоплатаРегл                                       КАК ДолгРегл,
	|		Неопределено                                                     КАК СвязанныйДокумент,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)  КАК СтатьяДвиженияДенежныхСредств,
	|		ЗачетыОплат.ПорядокОперации                                      КАК ПорядокОперации,
	|		ЗачетыОплат.ПорядокЗачетаРегистратора                            КАК ПорядокЗачета,
	|		ЗачетыОплат.ВалютаДокумента                                      КАК ВалютаДокумента,
	|		ЗачетыОплат.АналитикаУчетаПоПартнерам.Организация                КАК Организация
	|	ИЗ ВтРасчеты КАК ЗачетыОплат
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыВозникновения КАК ДатыВозникновения
	|			ПО ЗачетыОплат.ДокументРегистратор = ДатыВозникновения.Регистратор
	|	ГДЕ	ЗачетыОплат.Предоплата > 0 
	|		И ЗачетыОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ЗачетыОплат.ХозяйственнаяОперация НЕ В (&ХозяйственныеОперацииНеОтгрузка)
	|		И ЗачетыОплат.ТипОбъектаРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.ПлатежВозврат)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗачетыОплат.АналитикаУчетаПоПартнерам                            КАК АналитикаУчетаПоПартнерам,
	|		ЗачетыОплат.ОбъектРасчетов                                       КАК ОбъектРасчетов,
	|		ЗачетыОплат.Валюта                                               КАК Валюта,
	|		ЗачетыОплат.ДокументРегистратор                                  КАК ДокументРегистратор,
	|		ЗачетыОплат.ДокументРегистратор                                  КАК РасчетныйДокумент,
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаКлиента)  КАК ХозяйственнаяОперация,
	|		ЗачетыОплат.Период                                               КАК Период,
	|		ДатыВозникновения.ДатаПлатежа                                    КАК ДатаПлановогоПогашения,
	|		ЗачетыОплат.Период                                               КАК ДатаВозникновения,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                           КАК ВидДвижения,
	|		0                                                                КАК Предоплата,
	|		0                                                                КАК ПредоплатаУпр,
	|		0                                                                КАК ПредоплатаРегл,
	|		ЗачетыОплат.Предоплата                                           КАК Долг,
	|		ЗачетыОплат.ПредоплатаУпр                                        КАК ДолгУпр,
	|		ЗачетыОплат.ПредоплатаРегл                                       КАК ДолгРегл,
	|		Неопределено                                                     КАК СвязанныйДокумент,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)  КАК СтатьяДвиженияДенежныхСредств,
	|		ЗачетыОплат.ПорядокОперации                                      КАК ПорядокОперации,
	|		ЗачетыОплат.ПорядокЗачетаРегистратора                            КАК ПорядокЗачета,
	|		ЗачетыОплат.ВалютаДокумента                                      КАК ВалютаДокумента,
	|		ЗачетыОплат.АналитикаУчетаПоПартнерам.Организация                КАК Организация
	|	ИЗ ВтРасчеты КАК ЗачетыОплат
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыВозникновения КАК ДатыВозникновения
	|			ПО ЗачетыОплат.ДокументРегистратор = ДатыВозникновения.Регистратор
	|	ГДЕ
	|		ЗачетыОплат.Предоплата > 0 
	|		И ЗачетыОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ЗачетыОплат.ХозяйственнаяОперация НЕ В (&ХозяйственныеОперацииНеОтгрузка)
	|		И ЗачетыОплат.ТипОбъектаРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.ПлатежВозврат)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РасчетыПоДокументам.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|		РасчетыПоДокументам.ОбъектРасчетов                КАК ОбъектРасчетов,
	|		РасчетыПоДокументам.Валюта                        КАК Валюта,
	|		РасчетыПоДокументам.ДокументРегистратор           КАК ДокументРегистратор,
	|		РасчетыПоДокументам.РасчетныйДокумент             КАК РасчетныйДокумент,
	|		ВЫБОР КОГДА РасчетыПоДокументам.Предоплата <> 0 И РасчетыПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И РасчетыПоДокументам.ХозяйственнаяОперация НЕ В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКредиторскойЗадолженности),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаКлиента)
	|			КОГДА РасчетыПоДокументам.Долг <> 0 И РасчетыПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И РасчетыПоДокументам.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности)
	|				И РасчетыПоДокументам.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса)
	|				И РасчетыПоДокументам.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
	|				И РасчетыПоДокументам.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.УдержаниеВознагражденияКомиссионера)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПогашениеЗадолженностиКлиента)
	|			ИНАЧЕ РасчетыПоДокументам.ХозяйственнаяОперация 
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|		РасчетыПоДокументам.Период                        КАК Период,
	|		РасчетыПоДокументам.ДатаПлановогоПогашения        КАК ДатаПлановогоПогашения,
	|		РасчетыПоДокументам.ДатаВозникновения             КАК ДатаВозникновения,
	|		РасчетыПоДокументам.ВидДвижения                   КАК ВидДвижения,
	|		РасчетыПоДокументам.Предоплата                    КАК Предоплата,
	|		РасчетыПоДокументам.ПредоплатаУпр                 КАК ПредоплатаУпр,
	|		РасчетыПоДокументам.ПредоплатаРегл                КАК ПредоплатаРегл,
	|		РасчетыПоДокументам.Долг                          КАК Долг,
	|		РасчетыПоДокументам.ДолгУпр                       КАК ДолгУпр,
	|		РасчетыПоДокументам.ДолгРегл                      КАК ДолгРегл,
	|		ВЫБОР КОГДА (РасчетыПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И РасчетыПоДокументам.Предоплата > 0)
	|			ИЛИ (РасчетыПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И РасчетыПоДокументам.Долг > 0)
	|			ТОГДА РасчетыПоДокументам.СвязанныйДокумент
	|			ИНАЧЕ Неопределено
	|		КОНЕЦ                                             КАК СвязанныйДокумент,
	|		РасчетыПоДокументам.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|		РасчетыПоДокументам.ПорядокОперации               КАК ПорядокОперации,
	|		РасчетыПоДокументам.ПорядокЗачета                 КАК ПорядокЗачета,
	|		РасчетыПоДокументам.ВалютаДокумента               КАК ВалютаДокумента,
	|		РасчетыПоДокументам.АналитикаУчетаПоПартнерам.Организация КАК Организация
	|	ИЗ ВтРасчеты КАК РасчетыПоДокументам) КАК Расчеты
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.АналитикаУчетаПоПартнерам,
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.Валюта,
	|	Расчеты.ДокументРегистратор,
	|	Расчеты.РасчетныйДокумент,
	|	Расчеты.ХозяйственнаяОперация,
	|	Расчеты.Период,
	|	Расчеты.ДатаПлановогоПогашения,
	|	Расчеты.ДатаВозникновения,
	|	Расчеты.ВидДвижения,
	|	Расчеты.СвязанныйДокумент,
	|	Расчеты.СтатьяДвиженияДенежныхСредств,
	|	Расчеты.ПорядокОперации,
	|	Расчеты.ПорядокЗачета,
	|	Расчеты.ВалютаДокумента,
	|	Расчеты.Организация
	|;
	|УНИЧТОЖИТЬ ВтОсновныеПараметрыРасчета;
	|УНИЧТОЖИТЬ ВтРегистраторы;
	|УНИЧТОЖИТЬ ПорядокДокументов;
	|УНИЧТОЖИТЬ ДатыВозникновения;
	|УНИЧТОЖИТЬ ВтРасчеты
	|";
	
КонецФункции

Функция ТекстЗапросаРасчетыСПоставщикамиПоДокументам()
	
	Возврат "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОсновныеПараметрыРасчета.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ОсновныеПараметрыРасчета.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ОсновныеПараметрыРасчета.ВалютаРасчетов КАК ВалютаРасчетов
	|ПОМЕСТИТЬ ВтОсновныеПараметрыРасчета
	|ИЗ
	|	&ДанныеКОбработке КАК ОсновныеПараметрыРасчета
	|ГДЕ
	|	ОсновныеПараметрыРасчета.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	ВалютаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВтРегистраторы
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОсновныеПараметрыРасчета КАК ОсновныеПараметрыРасчета
	|			ПО Расчеты.АналитикаУчетаПоПартнерам = ОсновныеПараметрыРасчета.АналитикаУчетаПоПартнерам
	|				И Расчеты.ЗаказПоставщику = ОсновныеПараметрыРасчета.ОбъектРасчетов
	|				И Расчеты.Валюта = ОсновныеПараметрыРасчета.ВалютаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР КОГДА РасчетыСПоставщиками.РасчетныйДокумент <> Неопределено 
	|		ТОГДА РасчетыСПоставщиками.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСПоставщиками.Регистратор 
	|	КОНЕЦ                                         КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(РасчетыСПоставщиками.Период,ДЕНЬ) КАК Дата,
	|	МИНИМУМ(РасчетыСПоставщиками.ПорядокОперации) КАК ПорядокОперации,
	|	МИНИМУМ(РасчетыСПоставщиками.ПорядокЗачетаПоДатеПлатежа) КАК ПорядокЗачетаПоДатеПлатежа,
	|	МИНИМУМ(ВЫБОР КОГДА РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ВводОстатков ИЛИ
	|					РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов
	|						ТОГДА РасчетыСПоставщиками.ДатаРегистратора
	|				ИНАЧЕ РасчетыСПоставщиками.Период
	|			КОНЕЦ)                                КАК ДатаВозникновения,
	|	МИНИМУМ(РасчетыСПоставщиками.ВалютаДокумента) КАК ВалютаДокумента,
	|	МАКСИМУМ(РасчетыСПоставщиками.ДатаПлатежа)    КАК ДатаПлатежа
	|ПОМЕСТИТЬ ПорядокДокументов
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРегистраторы КАК Регистраторы
	|			ПО Регистраторы.Регистратор = РасчетыСПоставщиками.Регистратор
	|ГДЕ
	|	РасчетыСПоставщиками.Активность
	|	И НЕ РасчетыСПоставщиками.ПорядокОперации = """"
	|	И (РасчетыСПоставщиками.Сумма <> 0 ИЛИ РасчетыСПоставщиками.КОплате <> 0)
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА РасчетыСПоставщиками.РасчетныйДокумент <> Неопределено 
	|		ТОГДА РасчетыСПоставщиками.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСПоставщиками.Регистратор 
	|	КОНЕЦ,
	|	НАЧАЛОПЕРИОДА(РасчетыСПоставщиками.Период,ДЕНЬ)
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР КОГДА РасчетыСПоставщиками.РасчетныйДокумент <> Неопределено 
	|		ТОГДА РасчетыСПоставщиками.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСПоставщиками.Регистратор 
	|	КОНЕЦ                                         КАК Регистратор,
	|	МИНИМУМ(РасчетыСПоставщиками.ПорядокОперации) КАК ПорядокОперации,
	|	МИНИМУМ(РасчетыСПоставщиками.ПорядокЗачетаПоДатеПлатежа) КАК ПорядокЗачетаПоДатеПлатежа,
	|	МИНИМУМ(ВЫБОР КОГДА РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ВводОстатков ИЛИ
	|					РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов
	|						ТОГДА РасчетыСПоставщиками.ДатаРегистратора
	|				ИНАЧЕ РасчетыСПоставщиками.Период
	|			КОНЕЦ)                                КАК ДатаВозникновения,
	|	МИНИМУМ(РасчетыСПоставщиками.ВалютаДокумента) КАК ВалютаДокумента,
	|	МАКСИМУМ(РасчетыСПоставщиками.ДатаПлатежа)    КАК ДатаПлатежа
	|ПОМЕСТИТЬ ДатыВозникновения
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтРегистраторы КАК Регистраторы
	|			ПО Регистраторы.Регистратор = РасчетыСПоставщиками.Регистратор
	|ГДЕ
	|	РасчетыСПоставщиками.Активность
	|	И НЕ РасчетыСПоставщиками.ПорядокОперации = """"
	|	И (РасчетыСПоставщиками.Сумма <> 0 ИЛИ РасчетыСПоставщиками.КОплате <> 0)
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА РасчетыСПоставщиками.РасчетныйДокумент <> Неопределено 
	|		ТОГДА РасчетыСПоставщиками.РасчетныйДокумент
	|		ИНАЧЕ РасчетыСПоставщиками.Регистратор 
	|	КОНЕЦ
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам      КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщикамиПоДокументам.ЗаказПоставщику                КАК ОбъектРасчетов,
	|	РасчетыСПоставщикамиПоДокументам.ЗаказПоставщику.ТипОбъектаРасчетов КАК ТипОбъектаРасчетов,
	|	РасчетыСПоставщикамиПоДокументам.Валюта                         КАК Валюта,
	|	РасчетыСПоставщикамиПоДокументам.Регистратор                    КАК ДокументРегистратор,
	|	РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент              КАК РасчетныйДокумент,
	|	РасчетыСПоставщикамиПоДокументам.ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
	|	РасчетыСПоставщикамиПоДокументам.СтатьяДвиженияДенежныхСредств  КАК СтатьяДвиженияДенежныхСредств,
	|	РасчетыСПоставщикамиПоДокументам.Период                         КАК Период,
	|	ВЫБОР КОГДА ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И (РасчетыСПоставщикамиПоДокументам.Долг < 0
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр < 0
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.Предоплата > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл > 0
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр > 0)
	|		ИЛИ ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И (РасчетыСПоставщикамиПоДокументам.Предоплата < 0
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр < 0
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.Долг > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл > 0 
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр > 0) ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ                                                           КАК ВидДвижения,
	|	ВЫРАЗИТЬ(РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент КАК Документ.КорректировкаПриобретения).ДокументОснование КАК СвязанныйДокумент,
	|
	|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.Предоплата < 0  ТОГДА
	|		-РасчетыСПоставщикамиПоДокументам.Предоплата 
	|	ИНАЧЕ
	|		РасчетыСПоставщикамиПоДокументам.Предоплата
	|	КОНЕЦ                                                           КАК Предоплата,
	|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр < 0  ТОГДА
	|		-РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр 
	|	ИНАЧЕ
	|		РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр
	|	КОНЕЦ                                                           КАК ПредоплатаУпр,
	|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл < 0  ТОГДА
	|		-РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл 
	|	ИНАЧЕ
	|		РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл
	|	КОНЕЦ                                                           КАК ПредоплатаРегл,
	|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.Долг < 0  ТОГДА
	|		-РасчетыСПоставщикамиПоДокументам.Долг 
	|	ИНАЧЕ
	|		РасчетыСПоставщикамиПоДокументам.Долг
	|	КОНЕЦ                                                           КАК Долг,
	|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ДолгУпр < 0  ТОГДА
	|		-РасчетыСПоставщикамиПоДокументам.ДолгУпр 
	|	ИНАЧЕ
	|		РасчетыСПоставщикамиПоДокументам.ДолгУпр
	|	КОНЕЦ                                                           КАК ДолгУпр,
	|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ДолгРегл < 0  ТОГДА
	|		-РасчетыСПоставщикамиПоДокументам.ДолгРегл 
	|	ИНАЧЕ
	|		РасчетыСПоставщикамиПоДокументам.ДолгРегл
	|	КОНЕЦ                                                           КАК ДолгРегл,
	|
	|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.Предоплата <> 0 ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА (ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И (РасчетыСПоставщикамиПоДокументам.Долг < 0
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр < 0
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.Предоплата > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл > 0
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр > 0)
	|		ИЛИ ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И (РасчетыСПоставщикамиПоДокументам.Предоплата < 0
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр < 0
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.Долг > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл > 0 
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр > 0)) 
	|			И НЕ ((РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатков ИЛИ
	|					РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов) И 
	|					РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент <> Неопределено)
	|			И НЕ РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.РасчетКурсовыхРазниц ТОГДА
	|			ЕСТЬNULL(ДатыВозникновения.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДатыВозникновения.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
	|	КОНЕЦ                                                           КАК ДатаПлановогоПогашения,
	|	ЕСТЬNULL(ДатыВозникновения.ДатаВозникновения, ДАТАВРЕМЯ(1,1,1)) КАК ДатаВозникновения,
	|	ЕСТЬNULL(ПорядокДокументов.ПорядокОперации,"""")                КАК ПорядокОперации,
	|	ЕСТЬNULL(ДатыВозникновения.ПорядокЗачетаПоДатеПлатежа,"""")     КАК ПорядокЗачетаРегистратора,
	|	ЕСТЬNULL(ДатыВозникновения.ПорядокЗачетаПоДатеПлатежа,"""")     КАК ПорядокЗачета,
	|	ЕСТЬNULL(ДатыВозникновения.ВалютаДокумента,
	|		ЕСТЬNULL(ДатыВозникновения.ВалютаДокумента,
	|			ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)))              КАК ВалютаДокумента
	|ПОМЕСТИТЬ ВтРасчеты
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыСПоставщикамиПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОсновныеПараметрыРасчета КАК ОсновныеПараметрыРасчета
	|			ПО РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам = ОсновныеПараметрыРасчета.АналитикаУчетаПоПартнерам
	|				И РасчетыСПоставщикамиПоДокументам.ЗаказПоставщику = ОсновныеПараметрыРасчета.ОбъектРасчетов
	|				И РасчетыСПоставщикамиПоДокументам.Валюта = ОсновныеПараметрыРасчета.ВалютаРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокДокументов КАК ПорядокДокументов
	|			ПО РасчетыСПоставщикамиПоДокументам.Регистратор = ПорядокДокументов.Регистратор
	|				И НАЧАЛОПЕРИОДА(РасчетыСПоставщикамиПоДокументам.Период,ДЕНЬ) = ПорядокДокументов.Дата
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыВозникновения КАК ДатыВозникновения
	|			ПО РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент = ДатыВозникновения.Регистратор
	|ГДЕ
	|	(РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатков
	|		ИЛИ РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов)
	|	И РасчетыСПоставщикамиПоДокументам.Регистратор <> РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент
	|	И (РасчетыСПоставщикамиПоДокументам.Предоплата <> 0
	|		ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл <> 0
	|		ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр <> 0
	|		ИЛИ РасчетыСПоставщикамиПоДокументам.Долг <> 0
	|		ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл <> 0
	|		ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр <> 0)
	|	И РасчетыСПоставщикамиПоДокументам.Активность
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам      КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщикамиПоДокументам.ЗаказПоставщику                КАК ОбъектРасчетов,
	|	РасчетыСПоставщикамиПоДокументам.ЗаказПоставщику.ТипОбъектаРасчетов КАК ТипОбъектаРасчетов,
	|	РасчетыСПоставщикамиПоДокументам.Валюта                         КАК Валюта,
	|	РасчетыСПоставщикамиПоДокументам.Регистратор                    КАК ДокументРегистратор,
	|	РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент              КАК РасчетныйДокумент,
	|	РасчетыСПоставщикамиПоДокументам.ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
	|	РасчетыСПоставщикамиПоДокументам.СтатьяДвиженияДенежныхСредств  КАК СтатьяДвиженияДенежныхСредств,
	|	РасчетыСПоставщикамиПоДокументам.Период                         КАК Период,
	|	ВЫБОР КОГДА ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И НЕ (ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиПоДокументам.Регистратор) В (ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств),ТИП(Документ.ПриходныйКассовыйОрдер))
	|						И РасчетыСПоставщикамиПоДокументам.Предоплата > 0)
	|				И (РасчетыСПоставщикамиПоДокументам.Долг < 0
	|					ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр < 0
	|					ИЛИ РасчетыСПоставщикамиПоДокументам.Предоплата > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл > 0
	|					ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр > 0 )
	|		ИЛИ ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
	|				И (РасчетыСПоставщикамиПоДокументам.Предоплата < 0
	|					ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл < 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр < 0
	|					ИЛИ РасчетыСПоставщикамиПоДокументам.Долг > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл > 0 
	|					ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр > 0
	|					ИЛИ ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиПоДокументам.Регистратор) В (ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств),ТИП(Документ.ПриходныйКассовыйОрдер))
	|						И РасчетыСПоставщикамиПоДокументам.Предоплата > 0) ТОГДА
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ                                                           КАК ВидДвижения,
	|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.КорректировкаПриобретения 
	|		ТОГДА ВЫРАЗИТЬ(РасчетыСПоставщикамиПоДокументам.Регистратор КАК Документ.КорректировкаПриобретения).ДокументОснование
	|		ИНАЧЕ ВЫРАЗИТЬ(РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент КАК Документ.КорректировкаПриобретения).ДокументОснование
	|	КОНЕЦ КАК СвязанныйДокумент,
	|
	//Некорректно отражались расчеты по документам
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиПоДокументам.Регистратор) В (ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств),ТИП(Документ.ПриходныйКассовыйОрдер))
	|		И РасчетыСПоставщикамиПоДокументам.Предоплата > 0 
	|			ТОГДА 0
	|		КОГДА РасчетыСПоставщикамиПоДокументам.Предоплата < 0  ТОГДА
	|			-РасчетыСПоставщикамиПоДокументам.Предоплата 
	|	ИНАЧЕ
	|		РасчетыСПоставщикамиПоДокументам.Предоплата
	|	КОНЕЦ                                                           КАК Предоплата,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиПоДокументам.Регистратор) В (ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств),ТИП(Документ.ПриходныйКассовыйОрдер))
	|		И РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр > 0 
	|			ТОГДА 0
	|		КОГДА РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр < 0  ТОГДА
	|			-РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр 
	|	ИНАЧЕ
	|		РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр
	|	КОНЕЦ                                                           КАК ПредоплатаУпр,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиПоДокументам.Регистратор) В (ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств),ТИП(Документ.ПриходныйКассовыйОрдер))
	|		И РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл > 0 
	|			ТОГДА 0
	|		КОГДА РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл < 0  ТОГДА
	|			-РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл 
	|	ИНАЧЕ
	|		РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл
	|	КОНЕЦ                                                           КАК ПредоплатаРегл,
	|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.Долг < 0  ТОГДА
	|		-РасчетыСПоставщикамиПоДокументам.Долг 
	|	ИНАЧЕ
	|		РасчетыСПоставщикамиПоДокументам.Долг
	|		+ ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиПоДокументам.Регистратор) В (ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств),ТИП(Документ.ПриходныйКассовыйОрдер))
	|			И РасчетыСПоставщикамиПоДокументам.Предоплата > 0
	|				ТОГДА РасчетыСПоставщикамиПоДокументам.Предоплата
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ                                                           КАК Долг,
	|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ДолгУпр < 0  ТОГДА
	|		-РасчетыСПоставщикамиПоДокументам.ДолгУпр 
	|	ИНАЧЕ
	|		РасчетыСПоставщикамиПоДокументам.ДолгУпр
	|		+ ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиПоДокументам.Регистратор) В (ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств),ТИП(Документ.ПриходныйКассовыйОрдер))
	|			И РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр > 0
	|				ТОГДА РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ                                                           КАК ДолгУпр,
	|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.ДолгРегл < 0  ТОГДА
	|		-РасчетыСПоставщикамиПоДокументам.ДолгРегл 
	|	ИНАЧЕ
	|		РасчетыСПоставщикамиПоДокументам.ДолгРегл
	|		+ ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиПоДокументам.Регистратор) В (ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств),ТИП(Документ.ПриходныйКассовыйОрдер))
	|			И РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл > 0
	|				ТОГДА РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ                                                           КАК ДолгРегл,
	|
	|	ВЫБОР КОГДА РасчетыСПоставщикамиПоДокументам.Предоплата <> 0 ТОГДА ДАТАВРЕМЯ(1,1,1)
	|		КОГДА (ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И 
	|				(РасчетыСПоставщикамиПоДокументам.Предоплата > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл > 0
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр > 0)
	|		ИЛИ ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И 
	|				(РасчетыСПоставщикамиПоДокументам.Долг > 0 ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл > 0 
	|				ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр > 0)) 
	|			И НЕ ((РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатков ИЛИ
	|					РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов) И 
	|					РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент <> Неопределено)
	|			И НЕ РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.РасчетКурсовыхРазниц ТОГДА
	|			ВЫБОР
	|				КОГДА ЕСТЬNULL(ДатыВозникновения.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1)) = ДАТАВРЕМЯ(1,1,1)
	|					ТОГДА ЕСТЬNULL(ПорядокДокументов.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
	|				ИНАЧЕ ЕСТЬNULL(ДатыВозникновения.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
	|			КОНЕЦ
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДатыВозникновения.ДатаПлатежа, ДАТАВРЕМЯ(1,1,1))
	|	КОНЕЦ                                                           КАК ДатаПлановогоПогашения,
	|	ЕСТЬNULL(ДатыВозникновения.ДатаВозникновения, ДАТАВРЕМЯ(1,1,1)) КАК ДатаВозникновения,
	|	ЕСТЬNULL(ПорядокДокументов.ПорядокОперации,"""")                КАК ПорядокОперации,
	|	ЕСТЬNULL(ПорядокДокументов.ПорядокЗачетаПоДатеПлатежа,"""")     КАК ПорядокЗачетаРегистратора,
	|	ЕСТЬNULL(ДатыВозникновения.ПорядокЗачетаПоДатеПлатежа,"""")     КАК ПорядокЗачета,
	|	ЕСТЬNULL(ДатыВозникновения.ВалютаДокумента,
	|		ЕСТЬNULL(ПорядокДокументов.ВалютаДокумента,
	|			ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)))              КАК ВалютаДокумента
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыСПоставщикамиПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОсновныеПараметрыРасчета КАК ОсновныеПараметрыРасчета
	|			ПО РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам = ОсновныеПараметрыРасчета.АналитикаУчетаПоПартнерам
	|				И РасчетыСПоставщикамиПоДокументам.ЗаказПоставщику = ОсновныеПараметрыРасчета.ОбъектРасчетов
	|				И РасчетыСПоставщикамиПоДокументам.Валюта = ОсновныеПараметрыРасчета.ВалютаРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокДокументов КАК ПорядокДокументов
	|			ПО РасчетыСПоставщикамиПоДокументам.Регистратор = ПорядокДокументов.Регистратор
	|				И НАЧАЛОПЕРИОДА(РасчетыСПоставщикамиПоДокументам.Период,ДЕНЬ) = ПорядокДокументов.Дата
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыВозникновения КАК ДатыВозникновения
	|			ПО РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент = ДатыВозникновения.Регистратор
	|ГДЕ
	|	(НЕ (РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатков 
	|			ИЛИ РасчетыСПоставщикамиПоДокументам.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов)
	|		ИЛИ РасчетыСПоставщикамиПоДокументам.Регистратор = РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент)
	|	И (РасчетыСПоставщикамиПоДокументам.Предоплата <> 0
	|		ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаРегл <> 0
	|		ИЛИ РасчетыСПоставщикамиПоДокументам.ПредоплатаУпр <> 0
	|		ИЛИ РасчетыСПоставщикамиПоДокументам.Долг <> 0
	|		ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгРегл <> 0
	|		ИЛИ РасчетыСПоставщикамиПоДокументам.ДолгУпр <> 0)
	|	И РасчетыСПоставщикамиПоДокументам.Активность
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.ОбъектРасчетов                КАК ОбъектРасчетов,
	|	Расчеты.Валюта                        КАК Валюта,
	|	Расчеты.ДокументРегистратор           КАК ДокументРегистратор,
	|	Расчеты.РасчетныйДокумент             КАК РасчетныйДокумент,
	|	Расчеты.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|	Расчеты.Период                        КАК Период,
	|	Расчеты.ДатаПлановогоПогашения        КАК ДатаПлановогоПогашения,
	|	Расчеты.ДатаВозникновения             КАК ДатаВозникновения,
	|	Расчеты.ВидДвижения                   КАК ВидДвижения,
	|	СУММА(Расчеты.Предоплата)             КАК Предоплата,
	|	СУММА(Расчеты.ПредоплатаУпр)          КАК ПредоплатаУпр,
	|	СУММА(Расчеты.ПредоплатаРегл)         КАК ПредоплатаРегл,
	|	СУММА(Расчеты.Долг)                   КАК Долг,
	|	СУММА(Расчеты.ДолгУпр)                КАК ДолгУпр,
	|	СУММА(Расчеты.ДолгРегл)               КАК ДолгРегл,
	|	Расчеты.СвязанныйДокумент             КАК СвязанныйДокумент,
	|	Расчеты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	Расчеты.ПорядокОперации               КАК ПорядокОперации,
	|	Расчеты.ПорядокЗачета                 КАК ПорядокЗачета,
	|	Расчеты.ВалютаДокумента               КАК ВалютаДокумента,
	|	Расчеты.Организация                   КАК Организация
	|ИЗ (
	|	ВЫБРАТЬ
	|		ЗачетыОплат.АналитикаУчетаПоПартнерам                            КАК АналитикаУчетаПоПартнерам,
	|		ЗачетыОплат.ОбъектРасчетов                                       КАК ОбъектРасчетов,
	|		ЗачетыОплат.Валюта                                               КАК Валюта,
	|		ЗачетыОплат.ДокументРегистратор                                  КАК ДокументРегистратор,
	|		ЗачетыОплат.ДокументРегистратор                                  КАК РасчетныйДокумент,
	|		ЗачетыОплат.ХозяйственнаяОперация                                КАК ХозяйственнаяОперация,
	|		ЗачетыОплат.Период                                               КАК Период,
	|		ДатыВозникновения.ДатаПлатежа                                    КАК ДатаПлановогоПогашения,
	|		ЗачетыОплат.Период                                               КАК ДатаВозникновения,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                           КАК ВидДвижения,
	|		0                                                                КАК Предоплата,
	|		0                                                                КАК ПредоплатаУпр,
	|		0                                                                КАК ПредоплатаРегл,
	|		ЗачетыОплат.Предоплата                                           КАК Долг,
	|		ЗачетыОплат.ПредоплатаУпр                                        КАК ДолгУпр,
	|		ЗачетыОплат.ПредоплатаРегл                                       КАК ДолгРегл,
	|		Неопределено                                                     КАК СвязанныйДокумент,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)  КАК СтатьяДвиженияДенежныхСредств,
	|		ЗачетыОплат.ПорядокОперации                                      КАК ПорядокОперации,
	|		ЗачетыОплат.ПорядокЗачетаРегистратора                            КАК ПорядокЗачета,
	|		ЗачетыОплат.ВалютаДокумента                                      КАК ВалютаДокумента,
	|		ЗачетыОплат.АналитикаУчетаПоПартнерам.Организация                КАК Организация
	|	ИЗ ВтРасчеты КАК ЗачетыОплат
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыВозникновения КАК ДатыВозникновения
	|			ПО ЗачетыОплат.ДокументРегистратор = ДатыВозникновения.Регистратор
	|	ГДЕ
	|		ЗачетыОплат.Предоплата > 0 
	|		И ЗачетыОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ЗачетыОплат.ХозяйственнаяОперация НЕ В (&ХозяйственныеОперацииНеОтгрузка)
	|		И ЗачетыОплат.ТипОбъектаРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.ПлатежВозврат)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗачетыОплат.АналитикаУчетаПоПартнерам                              КАК АналитикаУчетаПоПартнерам,
	|		ЗачетыОплат.ОбъектРасчетов                                         КАК ОбъектРасчетов,
	|		ЗачетыОплат.Валюта                                                 КАК Валюта,
	|		ЗачетыОплат.ДокументРегистратор                                    КАК ДокументРегистратор,
	|		ЗачетыОплат.ДокументРегистратор                                    КАК РасчетныйДокумент,
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаПоставщику) КАК ХозяйственнаяОперация,
	|		ЗачетыОплат.Период                                                 КАК Период,
	|		ДатыВозникновения.ДатаПлатежа                                      КАК ДатаПлановогоПогашения,
	|		ЗачетыОплат.Период                                                 КАК ДатаВозникновения,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                             КАК ВидДвижения,
	|		0                                                                  КАК Предоплата,
	|		0                                                                  КАК ПредоплатаУпр,
	|		0                                                                  КАК ПредоплатаРегл,
	|		ЗачетыОплат.Предоплата                                             КАК Долг,
	|		ЗачетыОплат.ПредоплатаУпр                                          КАК ДолгУпр,
	|		ЗачетыОплат.ПредоплатаРегл                                         КАК ДолгРегл,
	|		Неопределено                                                       КАК СвязанныйДокумент,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)    КАК СтатьяДвиженияДенежныхСредств,
	|		ЗачетыОплат.ПорядокОперации                                        КАК ПорядокОперации,
	|		ЗачетыОплат.ПорядокЗачетаРегистратора                            КАК ПорядокЗачета,
	|		ЗачетыОплат.ВалютаДокумента                                        КАК ВалютаДокумента,
	|		ЗачетыОплат.АналитикаУчетаПоПартнерам.Организация                  КАК Организация
	|	ИЗ ВтРасчеты КАК ЗачетыОплат
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыВозникновения КАК ДатыВозникновения
	|			ПО ЗачетыОплат.ДокументРегистратор = ДатыВозникновения.Регистратор
	|	ГДЕ
	|		ЗачетыОплат.Предоплата > 0 
	|		И ЗачетыОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И ЗачетыОплат.ХозяйственнаяОперация НЕ В (&ХозяйственныеОперацииНеОтгрузка)
	|		И ЗачетыОплат.ТипОбъектаРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.ПлатежВозврат)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РасчетыПоДокументам.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|		РасчетыПоДокументам.ОбъектРасчетов                КАК ОбъектРасчетов,
	|		РасчетыПоДокументам.Валюта                        КАК Валюта,
	|		РасчетыПоДокументам.ДокументРегистратор           КАК ДокументРегистратор,
	|		РасчетыПоДокументам.РасчетныйДокумент             КАК РасчетныйДокумент,
	|		ВЫБОР КОГДА РасчетыПоДокументам.Предоплата <> 0 И РасчетыПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И РасчетыПоДокументам.ХозяйственнаяОперация НЕ В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеКредиторскойЗадолженности),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаПоставщику)
	|			КОГДА РасчетыПоДокументам.Долг <> 0 И РасчетыПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И РасчетыПоДокументам.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВзаимозачетЗадолженности)
	|				И РасчетыПоДокументам.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса)
	|				И РасчетыПоДокументам.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПогашениеЗадолженностиПоставщику)
	|			ИНАЧЕ РасчетыПоДокументам.ХозяйственнаяОперация 
	|		КОНЕЦ КАК ХозяйственнаяОперация,
	|		РасчетыПоДокументам.Период                        КАК Период,
	|		РасчетыПоДокументам.ДатаПлановогоПогашения        КАК ДатаПлановогоПогашения,
	|		РасчетыПоДокументам.ДатаВозникновения             КАК ДатаВозникновения,
	|		РасчетыПоДокументам.ВидДвижения                   КАК ВидДвижения,
	|		РасчетыПоДокументам.Предоплата                    КАК Предоплата,
	|		РасчетыПоДокументам.ПредоплатаУпр                 КАК ПредоплатаУпр,
	|		РасчетыПоДокументам.ПредоплатаРегл                КАК ПредоплатаРегл,
	|		РасчетыПоДокументам.Долг                          КАК Долг,
	|		РасчетыПоДокументам.ДолгУпр                       КАК ДолгУпр,
	|		РасчетыПоДокументам.ДолгРегл                      КАК ДолгРегл,
	|		ВЫБОР КОГДА (РасчетыПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И РасчетыПоДокументам.Предоплата > 0)
	|			ИЛИ (РасчетыПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И РасчетыПоДокументам.Долг > 0)
	|			ТОГДА РасчетыПоДокументам.СвязанныйДокумент
	|			ИНАЧЕ Неопределено
	|		КОНЕЦ                                             КАК СвязанныйДокумент,
	|		РасчетыПоДокументам.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|		РасчетыПоДокументам.ПорядокОперации               КАК ПорядокОперации,
	|		РасчетыПоДокументам.ПорядокЗачета                 КАК ПорядокЗачета,
	|		РасчетыПоДокументам.ВалютаДокумента               КАК ВалютаДокумента,
	|		РасчетыПоДокументам.АналитикаУчетаПоПартнерам.Организация КАК Организация
	|	ИЗ ВтРасчеты КАК РасчетыПоДокументам) КАК Расчеты
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.АналитикаУчетаПоПартнерам,
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.Валюта,
	|	Расчеты.ДокументРегистратор,
	|	Расчеты.РасчетныйДокумент,
	|	Расчеты.ХозяйственнаяОперация,
	|	Расчеты.Период,
	|	Расчеты.ДатаПлановогоПогашения,
	|	Расчеты.ДатаВозникновения,
	|	Расчеты.ВидДвижения,
	|	Расчеты.СвязанныйДокумент,
	|	Расчеты.СтатьяДвиженияДенежныхСредств,
	|	Расчеты.ПорядокОперации,
	|	Расчеты.ПорядокЗачета,
	|	Расчеты.ВалютаДокумента,
	|	Расчеты.Организация
	|;
	|УНИЧТОЖИТЬ ВтОсновныеПараметрыРасчета;
	|УНИЧТОЖИТЬ ВтРегистраторы;
	|УНИЧТОЖИТЬ ПорядокДокументов;
	|УНИЧТОЖИТЬ ДатыВозникновения;
	|УНИЧТОЖИТЬ ВтРасчеты
	|";
	
КонецФункции

#КонецОбласти

#Область ОчисткаРегистровВзаиморасчетов

// Очищает регистры взаиморасчетов.
// 
// Параметры:
//  НачальноеЗаполнение - Булево - Это начальное заполнение, не нужно очищаться фактические расчеты.
Процедура ОчиститьРегистрыВзаиморасчетов(НачальноеЗаполнение = Ложь) Экспорт
	
	ОписаниеЗамераОчистки = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("Взаиморасчеты.ОчисткаРегистров.Многопоточный");
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ОписаниеЗамераОчистки = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("Взаиморасчеты.ОчисткаРегистров.ФайловаяБаза");
	КонецЕсли;
	
	ВсегоОбработано = 0;
	Если НачальноеЗаполнение Тогда
		ОчиститьРегистр("РасчетыСКлиентамиПланОплат", ВсегоОбработано);
		ОчиститьРегистр("РасчетыСКлиентамиПланОтгрузок", ВсегоОбработано);
		ОчиститьРегистр("РасчетыСПоставщикамиПланОплат", ВсегоОбработано);
		ОчиститьРегистр("РасчетыСПоставщикамиПланПоставок", ВсегоОбработано);
	Иначе
		ОписаниеЗамераОтключениеИтогов = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("Взаиморасчеты.ОчисткаРегистров.ОтключениеИтогов");
		ОтключитьИтогиРегистровРасчетов();
		ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамераОтключениеИтогов, 1);
		
		ОписаниеЗамераУдалениеДвижений = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("Взаиморасчеты.ОчисткаРегистров.УдалениеДвижений");
		ОчиститьРегистр("РасчетыСКлиентамиПоСрокам", ВсегоОбработано);
		ОчиститьРегистр("РасчетыСКлиентамиПланОплат", ВсегоОбработано);
		ОчиститьРегистр("РасчетыСКлиентамиПланОтгрузок", ВсегоОбработано);
		ОчиститьРегистр("РасчетыСПоставщикамиПоСрокам", ВсегоОбработано);
		ОчиститьРегистр("РасчетыСПоставщикамиПланОплат", ВсегоОбработано);
		ОчиститьРегистр("РасчетыСПоставщикамиПланПоставок", ВсегоОбработано);
		ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамераУдалениеДвижений, ВсегоОбработано / 100);
		
		ОписаниеЗамераВключитьИтоги = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("Взаиморасчеты.ОчисткаРегистров.ВключениеИтогов");
		ВключитьИтогиРегистровРасчетов();
		ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамераВключитьИтоги, 1);
		
		УдалитьСлужебныеРегистраторыМногопоточно();
		УдалитьСистемныеКорректировкиРегистров(Дата(1,1,1));
		ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамераОчистки, ВсегоОбработано / 100);
	КонецЕсли;
		
КонецПроцедуры

//Очищает переданный регистр расчетов.
Процедура ОчиститьРегистр(ИмяРегистра, ВсегоОбработано = 0) Экспорт
	
	Если НЕ ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		КоличествоЗаписей = ВыполнитьОчисткуРегистраМногопоточно(ИмяРегистра);
		ВсегоОбработано = ВсегоОбработано + КоличествоЗаписей;
		
	Иначе
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
		|ГДЕ
		|	Расчеты.Регистратор ССЫЛКА Документ.РегистраторРасчетов";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РегистрНакопления.РасчетыСКлиентамиПоСрокам", "РегистрНакопления." + ИмяРегистра);
		Запрос = Новый Запрос(ТекстЗапроса);
		Выборка = Запрос.Выполнить().Выбрать();
		ВсегоОбработано = ВсегоОбработано + Выборка.Количество();
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = РегистрыНакопления[ИмяРегистра].СоздатьНаборЗаписей(); // РегистрНакопленияНаборЗаписей
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

//Очищает переданный регистр расчетов в несколько потоков.
// 
// Параметры:
// 	ИмяРегистра - Строка - Имя очищаемого регистра накопления
// Возвращаемое значение:
// 	Число - Количество обработанных регистраторов
//
Функция ВыполнитьОчисткуРегистраМногопоточно(ИмяРегистра) Экспорт
	
	Потоки = Новый Массив;
	КоличествоСвободныхПотоков = 1; // КоличествоПотоковРаспределенияВзаиморасчетов(); до исправления 10231410
	КоличествоОбъектовДляОчистки = РазмерПорцииОбработкиВОдномПотоке() * КоличествоСвободныхПотоков;
	// для избежания взаимоблокировок при записи даты минимальных итогов в таблицу AccumRegOpt
	Первый = ДанныеДляОчистки(ИмяРегистра, Неопределено, 1);
	Если Первый.Следующий() Тогда
		НаборЗаписей = РегистрыНакопления[ИмяРегистра].СоздатьНаборЗаписей(); // РегистрНакопленияНаборЗаписей
		НаборЗаписей.Отбор.Регистратор.Установить(Первый.Регистратор);
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
	КонецЕсли;
	МоментВремени = Неопределено;
	КонтрольныйЗапуск = 3;
	КоличествоОбработанныхЗаписей = 0;
	Пока КоличествоОбъектовДляОчистки > 0 И КонтрольныйЗапуск > 0 Цикл
		
		Если КоличествоСвободныхПотоков > 0 Тогда
			
			РазмерВыборки = РазмерПорцииОбработкиВОдномПотоке() * КоличествоСвободныхПотоков;
			ВыборкаДляОчистки = ДанныеДляОчистки(ИмяРегистра, МоментВремени, РазмерВыборки);
			КоличествоОбработанныхЗаписей = КоличествоОбработанныхЗаписей + ВыборкаДляОчистки.Количество();
			КоличествоОбъектовДляОчистки = ВыборкаДляОчистки.Количество();
			Если КоличествоОбъектовДляОчистки > 0 Тогда
				
				ДанныеПотока = Новый Массив;
				Пока ВыборкаДляОчистки.Следующий() Цикл
					
					МоментВремени = ВыборкаДляОчистки.МоментВремени;
					ДанныеПотока.Добавить(ВыборкаДляОчистки.Регистратор);
					Если ДанныеПотока.Количество() = РазмерПорцииОбработкиВОдномПотоке() Тогда
						Поток = НовоеОписаниеПотока("ОперативныеВзаиморасчетыСервер.УдалитьЗаписиДокументов");
						Поток.НаименованиеЗадания = НСтр("ru = 'Очистка регистра';
														|en = 'Clearing register'") + " " + ИмяРегистра;
						Поток.ПараметрыПроцедуры.Вставить("ДанныеКОбработке", ДанныеПотока);
						Поток.ПараметрыПроцедуры.Вставить("ИмяРегистра", ИмяРегистра);
						ЗапуститьОбработкуВФоне(Поток);
						Потоки.Добавить(Поток);
						ДанныеПотока = Новый Массив;
					КонецЕсли;
					
				КонецЦикла;
				
				Если ДанныеПотока.Количество() > 0 Тогда
					Поток = НовоеОписаниеПотока("ОперативныеВзаиморасчетыСервер.УдалитьЗаписиДокументов");
					Поток.НаименованиеЗадания = НСтр("ru = 'Очистка регистра';
													|en = 'Clearing register'") + " " + ИмяРегистра;
					Поток.ПараметрыПроцедуры.Вставить("ДанныеКОбработке", ДанныеПотока);
					Поток.ПараметрыПроцедуры.Вставить("ИмяРегистра", ИмяРегистра);
					ЗапуститьОбработкуВФоне(Поток);
					Потоки.Добавить(Поток);
				КонецЕсли;
				
			ИначеЕсли КонтрольныйЗапуск > 0 Тогда
				МоментВремени = Неопределено;
				КонтрольныйЗапуск = КонтрольныйЗапуск - 1;
			КонецЕсли;
			
		КонецЕсли;
		
		ОжидатьЗавершениеПотока(Поток);
		
		ЗавершитьПотокиВыполнившиеФЗ(Потоки);
		
		КоличествоДоступныхПотоков = 1; // КоличествоПотоковРаспределенияВзаиморасчетов(); до исправления 10231410
		КоличествоСвободныхПотоков = Макс(КоличествоДоступныхПотоков - Потоки.Количество(), 0);
		
	КонецЦикла;
	
	ОжидатьЗавершениеВсехПотоков(Потоки);
	
	Возврат КоличествоОбработанныхЗаписей;
	
КонецФункции

//Очищает переданный регистр расчетов по регистратору
Процедура ОчиститьРегистрВзаиморасчетовПоДокументу(Регистратор, ИмяРегистра) Экспорт
	
	НаборЗаписей = РегистрыНакопления[ИмяРегистра].СоздатьНаборЗаписей(); // РегистрНакопленияНаборЗаписей
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	НаборЗаписей.Записать();

КонецПроцедуры

//Удаляет служебные регистраторы.
//
Процедура УдалитьСлужебныеРегистраторыМногопоточно()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РегистраторРасчетов.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РегистраторРасчетов КАК РегистраторРасчетов";
	Запрос = Новый Запрос(ТекстЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	
	КоличествоОбработанныхЗаписей = Выборка.Количество();
	РазмерПорцииОдномПотоке = Цел(Выборка.Количество()/КоличествоПотоковРаспределенияВзаиморасчетов()) + 1;
	Потоки = Новый Массив;
	ДанныеПотока = Новый Массив;
	Пока Выборка.Следующий() Цикл
		ДанныеПотока.Добавить(Выборка.Ссылка);
		Если ДанныеПотока.Количество() = РазмерПорцииОдномПотоке Тогда
			Поток = НовоеОписаниеПотока("ОперативныеВзаиморасчетыСервер.УдалитьСлужебныеРегистраторы");
			Поток.НаименованиеЗадания = НСтр("ru = 'Удаление служебных регистраторов';
											|en = 'Deleting internal recorders'");
			Поток.ПараметрыПроцедуры.Вставить("ДанныеКОбработке", ДанныеПотока);
			ЗапуститьОбработкуВФоне(Поток);
			Потоки.Добавить(Поток);
			ДанныеПотока = Новый Массив;
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеПотока.Количество() > 0 Тогда
		Поток = НовоеОписаниеПотока("ОперативныеВзаиморасчетыСервер.УдалитьСлужебныеРегистраторы");
		Поток.НаименованиеЗадания = НСтр("ru = 'Удаление служебных регистраторов';
										|en = 'Deleting internal recorders'");
		Поток.ПараметрыПроцедуры.Вставить("ДанныеКОбработке", ДанныеПотока);
		ЗапуститьОбработкуВФоне(Поток);
		Потоки.Добавить(Поток);
		ДанныеПотока = Новый Массив;
	КонецЕсли;
	
	ОжидатьЗавершениеВсехПотоков(Потоки,,КоличествоОбработанныхЗаписей);
	
КонецПроцедуры

Функция ДанныеДляОчистки(ИмяРегистра, МоментВремени, РазмерПорции = 0)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	Расчеты.МоментВремени КАК МоментВремени,
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|ГДЕ
	|	Расчеты.Регистратор ССЫЛКА Документ.РегистраторРасчетов
	|	И (&БезОтбора ИЛИ Расчеты.МоментВремени > &МоментВремени)
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.МоментВремени,
	|	Расчеты.Регистратор
	|УПОРЯДОЧИТЬ ПО
	|	Расчеты.МоментВремени"; // сортировать только по возрастанию периода для избежания взаимоблокировок при записи даты минимальных итогов в таблицу AccumRegOpt 
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РегистрНакопления.РасчетыСКлиентамиПоСрокам", "РегистрНакопления."+ИмяРегистра);
	Если РазмерПорции > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "1000", Формат(РазмерПорции, "ЧН=0; ЧГ=0"));
	КонецЕсли;
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	Если МоментВремени = Неопределено Тогда
		Запрос.УстановитьПараметр("МоментВремени", Новый МоментВремени('00010101'));
	КонецЕсли;
	Запрос.УстановитьПараметр("БезОтбора", МоментВремени = Неопределено);
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат Выборка;
	
КонецФункции

Процедура УдалитьЗаписиДокументов(Параметры, АдресРезультата = Неопределено) Экспорт
	
	ДанныеКОбработке = Параметры.ДанныеКОбработке;
	ИмяРегистра = Параметры.ИмяРегистра;
	КоличествоРегистраторов = ДанныеКОбработке.Количество();
	Для Каждого Регистратор Из ДанныеКОбработке Цикл
		НаборЗаписей = РегистрыНакопления[ИмяРегистра].СоздатьНаборЗаписей(); // РегистрНакопленияНаборЗаписей
		НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
	КонецЦикла;
	
	Если АдресРезультата <> Неопределено Тогда
		Результат = Новый Структура("КоличествоОбработанныхЗаписей", КоличествоРегистраторов);
		ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьСлужебныеРегистраторы(Параметры, АдресРезультата = Неопределено) Экспорт
	
	ДанныеКОбработке = Параметры.ДанныеКОбработке;
	КоличествоРегистраторов = ДанныеКОбработке.Количество();
	Для Каждого Регистратор Из ДанныеКОбработке Цикл
		
		ДокументОбъект = Регистратор.ПолучитьОбъект();
		ДокументОбъект.ОбменДанными.Загрузка = Истина;
		ДокументОбъект.Удалить();
		
	КонецЦикла;
	
	Если АдресРезультата <> Неопределено Тогда
		Результат = Новый Структура("КоличествоОбработанныхЗаписей", КоличествоРегистраторов);
		ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ФоновоеРаспределениеПлановыхОплат

Процедура ВыполнитьФоновоеРаспределениеРасчетов(Параметры = Неопределено, АдресРезультата = Неопределено) Экспорт
	
	РаспределитьФактическиеРасчеты = Ложь;
	Если ТипЗнч(Параметры) = Тип("Структура") И Параметры.Свойство("РаспределитьФактическиеРасчеты") Тогда
		РаспределитьФактическиеРасчеты = Параметры.РаспределитьФактическиеРасчеты;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	НачатьТранзакцию();
	Попытка
		
		ПричинаИсключения = "Блокировка";
		
		// для предотвращения повторного запуска мененеджера распределения заблокируем константу
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Константа.БлокировкаФоновогоРаспределенияРасчетов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		ПричинаИсключения = "РаботаУправляющегоПотока";
		КоличествоПопыток = 0;
		Пока КоличествоПопыток < 10 Цикл
			
			ОбщегоНазначенияУТ.Пауза(1);
			
			ПараметрыМП = ПараметрыМногопоточнойОбработкиРасчетов();
			ПараметрыМП.Процедура.Имя = "ОперативныеВзаиморасчетыСервер.ВыполнитьОтложенноеРаспределение";
			ПараметрыМП.Процедура.ПредставлениеЗадания = НСтр("ru = 'Распределение плановых расчетов';
																|en = 'Planned settlements distribution'");
			ПараметрыМП.Данные.УсловиеЗапроса = "Задания.ДатаПересчета = ДАТАВРЕМЯ(3000,1,1)";
			Если РаспределитьФактическиеРасчеты Тогда
				ПараметрыМП.Данные.УсловиеЗапроса = "";
				ПараметрыМП.Процедура.ПредставлениеЗадания = НСтр("ru = 'Распределение фактических расчетов';
																	|en = 'Actual AR/AP allocation'");
			КонецЕсли;
			
			ВсегоОбработано = МногопоточнаяОбработкаЗаданийКРаспределениюРасчетов(ПараметрыМП);
			
			КоличествоПопыток = КоличествоПопыток + 1;
			Если ВсегоОбработано > 0 Тогда
				КоличествоПопыток = 0;
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Если ПричинаИсключения <> "Блокировка" Тогда
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Событие = НСтр("ru = 'Фоновое распределение плановых расчетов';
							|en = 'Background planned settlements distribution'", ОбщегоНазначения.КодОсновногоЯзыка());
			Если РаспределитьФактическиеРасчеты Тогда
				Событие = НСтр("ru = 'Фоновое распределение фактических расчетов';
								|en = 'Background allocation of actual AR/AP'", ОбщегоНазначения.КодОсновногоЯзыка());
			КонецЕсли;
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
		КонецЕсли;
		
		Возврат;
		
	КонецПопытки;
	
	Константы.БлокировкаФоновогоРаспределенияРасчетов.Установить(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеФункции

#Область МенеджерМногопоточнойОбработкиРасчетов

Функция МногопоточнаяОбработкаЗаданийКРаспределениюРасчетов(Параметры)
	
	ПроцедураОбработки = Параметры.Процедура;
	ПараметрыДанных = Параметры.Данные;
	РазмерПорцииОбработкиВОдномПотоке = Параметры.РазмерПорцииОбработкиВОдномПотоке;

	Потоки = Новый Массив;
	КоличествоСвободныхПотоков = КоличествоПотоковРаспределенияВзаиморасчетов();
	КоличествоОбъектовДляОбработки = РазмерПорцииОбработкиВОдномПотоке * КоличествоСвободныхПотоков;
	ПараметрыДанных.ПоследняяЗапись = НовыйКурсор();
	КоличествоОбработанныхЗаписей = 0;
	КоличествоОбработанныхЗаданий = 0;
	Пока КоличествоОбъектовДляОбработки > 0 Цикл

		Если КоличествоСвободныхПотоков > 0 Тогда

			ПараметрыДанных.РазмерПорции = РазмерПорцииОбработкиВОдномПотоке * КоличествоСвободныхПотоков;
			ДанныеДляОбработки = ДанныеДляОбработки(ПараметрыДанных);
			Кортежи = ДанныеДляОбработки.Кортежи;
			ПараметрыДанных.ПоследняяЗапись = НовыйКурсор();
			Если Кортежи.Количество() = ПараметрыДанных.РазмерПорции Тогда
				ЗаполнитьЗначенияСвойств(ПараметрыДанных.ПоследняяЗапись, Кортежи[Кортежи.Количество() - 1]);
			КонецЕсли;

			ПараметрыДанных.РазмерПорции = 1;
			Если Кортежи.Количество()/КоличествоСвободныхПотоков > 1 Тогда
				РазмерПорции = Кортежи.Количество()/КоличествоСвободныхПотоков;
				Если Цел(РазмерПорции) <> РазмерПорции Тогда
					ПараметрыДанных.РазмерПорции = Цел(РазмерПорции) + 1;
				Иначе
					ПараметрыДанных.РазмерПорции = РазмерПорции;
				КонецЕсли;
			КонецЕсли;
			КоличествоОбъектовДляОбработки = 0;
			КортежиВОбработке = ПараметрыДанных.КортежиВОбработке.СкопироватьКолонки();
			Пока Кортежи.Количество() > 0 Цикл

				ДанныеПотока = ВыбратьДанныеДляПотока(ДанныеДляОбработки, ПараметрыДанных.РазмерПорции);
				Поток = НовоеОписаниеПотока(ПроцедураОбработки.Имя);
				Поток.НаименованиеЗадания = ПроцедураОбработки.ПредставлениеЗадания;
				Поток.ПараметрыПроцедуры.Вставить("ДанныеКОбработке", ДанныеПотока.Задания);
				Поток.ПараметрыПроцедуры.Вставить("ДополнитьТаблицуИзмененийПриемниками", Истина);
				Для Каждого Параметр Из ПроцедураОбработки.Параметры Цикл
					Поток.ПараметрыПроцедуры.Вставить(Параметр.Ключ, Параметр.Значение);
				КонецЦикла;
				ЗапуститьОбработкуВФоне(Поток);
				ДобавитьКортежиПотока(Поток, КортежиВОбработке, ДанныеПотока.Кортежи);
				КоличествоОбъектовДляОбработки = КоличествоОбъектовДляОбработки + ДанныеПотока.Задания.Количество();
				КоличествоОбработанныхЗаданий = КоличествоОбработанныхЗаданий + ДанныеПотока.Задания.Количество();
				Потоки.Добавить(Поток);

			КонецЦикла;

		КонецЕсли;

		ОжидатьЗавершениеПотока(Поток);

		ЗавершитьПотокиВыполнившиеФЗ(Потоки, КортежиВОбработке, КоличествоОбработанныхЗаписей);
		ЭтоПоследняяПорция = ПараметрыДанных.ПоследняяЗапись.ОбъектРасчетов = Неопределено;
		Если ЭтоПоследняяПорция Тогда
			ОжидатьЗавершениеВсехПотоков(Потоки, КортежиВОбработке, КоличествоОбработанныхЗаписей);
		КонецЕсли;
		ПараметрыДанных.КортежиВОбработке = КортежиВОбработке;

		КоличествоДоступныхПотоков = КоличествоПотоковРаспределенияВзаиморасчетов();
		КоличествоСвободныхПотоков = Макс(КоличествоДоступныхПотоков - Потоки.Количество(), 0);

	КонецЦикла;

	ОжидатьЗавершениеВсехПотоков(Потоки,,КоличествоОбработанныхЗаписей);

	Если КоличествоОбработанныхЗаписей = 0 Тогда
		Возврат КоличествоОбработанныхЗаданий;
	КонецЕсли;

	Возврат КоличествоОбработанныхЗаписей;
	
КонецФункции

Функция ПараметрыМногопоточнойОбработкиРасчетов(РазмерПорцииВОдномПотоке = Неопределено)
	
	Результат = Новый Структура;
	
	ПроцедураОбработки = Новый Структура;
	ПроцедураОбработки.Вставить("Имя", "");
	ПроцедураОбработки.Вставить("ПредставлениеЗадания", "");
	ПроцедураОбработки.Вставить("Параметры", Новый Структура);
	
	ДанныеОбработки = ПараметрыДанныхМногопоточнойОбработки();
	
	Результат.Вставить("РазмерПорцииОбработкиВОдномПотоке", РазмерПорцииОбработкиВОдномПотоке());
	Если РазмерПорцииВОдномПотоке <> Неопределено Тогда
		Результат.РазмерПорцииОбработкиВОдномПотоке = РазмерПорцииВОдномПотоке;
	КонецЕсли;
	Результат.Вставить("Процедура", ПроцедураОбработки);
	Результат.Вставить("Данные", ДанныеОбработки);
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрыДанныхМногопоточнойОбработки()
	
	ДанныеОбработки = Новый Структура;
	ДанныеОбработки.Вставить("ПоследняяЗапись", Неопределено);
	ДанныеОбработки.Вставить("РазмерПорции", Неопределено);
	ДанныеОбработки.Вставить("УсловиеЗапроса", "");
	ДанныеОбработки.Вставить("ПараметрыЗапроса", Новый Структура);
	
	КортежиВОбработке = Новый ТаблицаЗначений();
	КортежиВОбработке.Колонки.Добавить("ИдПотока", Новый ОписаниеТипов("УникальныйИдентификатор"));
	КортежиВОбработке.Колонки.Добавить("ТипРасчетов", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыРасчетовСПартнерами"));
	КортежиВОбработке.Колонки.Добавить("АналитикаУчетаПоПартнерам", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаПоПартнерам"));
	КортежиВОбработке.Колонки.Добавить("ОбъектРасчетов", Новый ОписаниеТипов("СправочникСсылка.ОбъектыРасчетов"));
	КортежиВОбработке.Колонки.Добавить("ВалютаРасчетов", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	
	ДанныеОбработки.Вставить("КортежиВОбработке", КортежиВОбработке);
	
	Возврат ДанныеОбработки;
	
КонецФункции

Функция НовыйКурсор()
	
	ПоследняяЗапись = Новый Структура("КоличествоДокументов,АналитикаУчетаПоПартнерам,ОбъектРасчетов");
	ПоследняяЗапись.КоличествоДокументов = 0;
	
	Возврат ПоследняяЗапись;
	
КонецФункции

Функция ДанныеДляОбработки(Параметры)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Кортежи.ТипРасчетов КАК ТипРасчетов,
	|	Кортежи.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Кортежи.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Кортежи.ВалютаРасчетов КАК ВалютаРасчетов
	|ПОМЕСТИТЬ втКортежиВРаботе
	|ИЗ
	|	&КортежиВОбработке КАК Кортежи
	|ИНДЕКСИРОВАТЬ ПО
	|	ТипРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	ВалютаРасчетов
	|	
	|;
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	Задания.КоличествоДокументов КАК КоличествоДокументов,
	|	Задания.ТипРасчетов КАК ТипРасчетов,
	|	Задания.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Задания.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Задания.Валюта КАК ВалютаРасчетов
	|ПОМЕСТИТЬ втКортежиКОбработке
	|ИЗ
	|	РегистрСведений.ЗаданияКРаспределениюРасчетов КАК Задания
	|	ЛЕВОЕ СОЕДИНЕНИЕ втКортежиВРаботе КАК КортежиВРаботе
	|	ПО Задания.ТипРасчетов = КортежиВРаботе.ТипРасчетов
	|		И Задания.ТипРасчетов = КортежиВРаботе.ТипРасчетов
	|		И Задания.АналитикаУчетаПоПартнерам = КортежиВРаботе.АналитикаУчетаПоПартнерам
	|		И Задания.ОбъектРасчетов = КортежиВРаботе.ОбъектРасчетов
	|		И Задания.Валюта = КортежиВРаботе.ВалютаРасчетов
	|ГДЕ 
	|	КортежиВРаботе.ОбъектРасчетов ЕСТЬ NULL
	|	И (Задания.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
	|			И Задания.ОбъектРасчетов > &ОбъектРасчетов
	|		ИЛИ Задания.АналитикаУчетаПоПартнерам > &АналитикаУчетаПоПартнерам
	|		ИЛИ &БезАналитики)
	|
	|СГРУППИРОВАТЬ ПО
	|	Задания.КоличествоДокументов,
	|	Задания.ТипРасчетов,
	|	Задания.АналитикаУчетаПоПартнерам,
	|	Задания.ОбъектРасчетов,
	|	Задания.Валюта
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТипРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	ВалютаРасчетов
	|;
	|
	|ВЫБРАТЬ
	|	КортежиКОбработке.КоличествоДокументов КАК КоличествоДокументов,
	|	КортежиКОбработке.ТипРасчетов КАК ТипРасчетов,
	|	КортежиКОбработке.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	КортежиКОбработке.ОбъектРасчетов КАК ОбъектРасчетов,
	|	КортежиКОбработке.ВалютаРасчетов КАК ВалютаРасчетов
	|ИЗ
	|	втКортежиКОбработке КАК КортежиКОбработке
	|
	|УПОРЯДОЧИТЬ ПО
	|	КоличествоДокументов УБЫВ,
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов
	|;
	|
	|ВЫБРАТЬ
	|	Задания.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ЭтоРасчетыСКлиентами,
	|	Задания.ТипРасчетов КАК ТипРасчетов,
	|	Задания.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Задания.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Задания.ОбъектРасчетов.Объект КАК Объект,
	|	Задания.Валюта КАК ВалютаРасчетов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) КАК АналитикаУчетаПоПартнерамПриемник,
	|	ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка) КАК ОбъектРасчетовПриемник,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаПриемник,
	|	Задания.Документ КАК Документ,
	|	МАКСИМУМ(Задания.Приоритет) КАК Приоритет,
	|	ВЫБОР КОГДА МАКСИМУМ(Задания.Приоритет) = 2
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПоДаннымОбъектаРасчетовИсточника,
	|	МИНИМУМ(Задания.ДатаПересчета) КАК ДатаПересчета,
	|	МИНИМУМ(Задания.ДатаПересчета) КАК ДатаНачалаПересчета,
	|	МИНИМУМ(Задания.ПериодКонтроляИзменений) КАК ПериодКонтроляИзменений,
	|	КортежиКОбработке.КоличествоДокументов КАК КоличествоДокументов
	|ИЗ
	|	РегистрСведений.ЗаданияКРаспределениюРасчетов КАК Задания
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКортежиКОбработке КАК КортежиКОбработке
	|	ПО Задания.ТипРасчетов = КортежиКОбработке.ТипРасчетов
	|		И Задания.АналитикаУчетаПоПартнерам = КортежиКОбработке.АналитикаУчетаПоПартнерам
	|		И Задания.ОбъектРасчетов = КортежиКОбработке.ОбъектРасчетов
	|		И Задания.Валюта = КортежиКОбработке.ВалютаРасчетов
	|ГДЕ
	|	&Условие
	|
	|СГРУППИРОВАТЬ ПО
	|	Задания.ТипРасчетов,
	|	Задания.АналитикаУчетаПоПартнерам,
	|	Задания.ОбъектРасчетов,
	|	Задания.Валюта,
	|	Задания.Документ,
	|	КортежиКОбработке.КоличествоДокументов
	|
	|УПОРЯДОЧИТЬ ПО
	|	МАКСИМУМ(Задания.Приоритет) ВОЗР,
	|	КоличествоДокументов УБЫВ,
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов";
	
	Если Параметры.РазмерПорции = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, " ПЕРВЫЕ 1000", "");
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "1000", Формат(Параметры.РазмерПорции, "ЧН=0; ЧГ=0"));
	УсловиеЗапроса = Параметры.УсловиеЗапроса;
	Если НЕ ЗначениеЗаполнено(УсловиеЗапроса) Тогда
		УсловиеЗапроса = "ИСТИНА";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", УсловиеЗапроса);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("КоличествоДокументов", 0);
	Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", Неопределено);
	Запрос.УстановитьПараметр("ОбъектРасчетов", Неопределено);
	Запрос.УстановитьПараметр("БезАналитики", Истина);
	Запрос.УстановитьПараметр("КортежиВОбработке", Параметры.КортежиВОбработке);
	
	ПоследняяЗапись = Параметры.ПоследняяЗапись;
	Если ПоследняяЗапись <> Неопределено Тогда
		Запрос.УстановитьПараметр("КоличествоДокументов", ПоследняяЗапись.КоличествоДокументов);
		Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", ПоследняяЗапись.АналитикаУчетаПоПартнерам);
		Запрос.УстановитьПараметр("ОбъектРасчетов", ПоследняяЗапись.ОбъектРасчетов);
		Запрос.УстановитьПараметр("БезАналитики", НЕ ЗначениеЗаполнено(ПоследняяЗапись.АналитикаУчетаПоПартнерам));
	КонецЕсли;

	Для Каждого Параметр Из Параметры.ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Данные = РезультатЗапроса[РезультатЗапроса.ВГраница()].Выгрузить();
	Данные.Индексы.Добавить("ТипРасчетов,АналитикаУчетаПоПартнерам,ОбъектРасчетов,ВалютаРасчетов");
	Кортежи = РезультатЗапроса[РезультатЗапроса.ВГраница()-1].Выгрузить();
	
	Результат = Новый Структура;
	Результат.Вставить("Кортежи", Кортежи);
	Результат.Вставить("Данные", Данные);
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьКортежиПотока(Поток,КортежиВОбработке,Кортежи)
	
	Если КортежиВОбработке = Неопределено Тогда
		КортежиВОбработке = Кортежи.Скопировать();
		КортежиВОбработке.ЗаполнитьЗначения(Поток.ИдентификаторЗадания,"ИдПотока");
		Возврат;
	КонецЕсли;
	
	Для Каждого Кортеж Из Кортежи Цикл
		НоваяСтрока = КортежиВОбработке.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Кортеж);
		НоваяСтрока.ИдПотока = Поток.ИдентификаторЗадания;
	КонецЦикла;
	
КонецПроцедуры

// Кортежи с наибольшим количеством документов первыми отправляются на обработку.
// Для более равномерной загрузки потоков и максимального распараллеливания обработки
// такие кортежи распределяются равномерно по всем свободными потоками,
// а не назначаются все "тяжелые" в один поток обработки.
//
Функция ВыбратьДанныеДляПотока(ДанныеКОбработке, РазмерПорции)
	
	ПоляКортежа = "ТипРасчетов,АналитикаУчетаПоПартнерам,ОбъектРасчетов,ВалютаРасчетов";
	Кортежи = ДанныеКОбработке.Кортежи;
	Задания = ДанныеКОбработке.Данные;
	ЗаданияПотока = Задания.СкопироватьКолонки();
	КортежиПотока = Задания.СкопироватьКолонки(ПоляКортежа);
	ЭтоПоследняяПорция = Кортежи.Количество()/РазмерПорции <= 1;
	Индекс = 0;
	Пока (КортежиПотока.Количество() < РазмерПорции ИЛИ ЭтоПоследняяПорция)
		И Кортежи.Количество() > 0 Цикл
		
		Кортеж = Кортежи[Индекс];
		Отбор = Новый Структура(ПоляКортежа);
		ЗаполнитьЗначенияСвойств(Отбор,Кортеж);
		ЗаписиКОбработке = Задания.НайтиСтроки(Отбор);
		Для Каждого Запись Из ЗаписиКОбработке Цикл
			ЗаполнитьЗначенияСвойств(ЗаданияПотока.Добавить(), Запись);
		КонецЦикла;
		ЗаполнитьЗначенияСвойств(КортежиПотока.Добавить(), Кортеж);
		
		Кортежи.Удалить(Кортеж);
		Индекс = Индекс + РазмерПорции;
		Если Индекс >= Кортежи.Количество() Тогда
			Индекс = 0;
		КонецЕсли;
	КонецЦикла;
	
	ДанныеПотока = Новый Структура;
	ДанныеПотока.Вставить("Задания", ЗаданияПотока);
	ДанныеПотока.Вставить("Кортежи", КортежиПотока);
	
	Возврат ДанныеПотока;
	
КонецФункции

Процедура ОтметитьВыполнениеЗадания(ОтборПоИзмерениям, ДополнительныеИзмеренияОтбора = "")
	
	ИзмеренияОтбораСтрокой =  
	"ТипРасчетов,
	|АналитикаУчетаПоПартнерам,
	|ОбъектРасчетов,
	|Валюта,
	|ВалютаРасчетов";
	Если НЕ ПустаяСтрока(ДополнительныеИзмеренияОтбора)  Тогда
		ИзмеренияОтбораСтрокой = ИзмеренияОтбораСтрокой + "," + ДополнительныеИзмеренияОтбора;
	КонецЕсли;
	
	ИзмеренияОтбора = Новый Структура(ИзмеренияОтбораСтрокой);
	ЗаполнитьЗначенияСвойств(ИзмеренияОтбора, ОтборПоИзмерениям);
	
	Набор = РегистрыСведений.ЗаданияКРаспределениюРасчетов.СоздатьНаборЗаписей();
	Для Каждого Измерение Из ИзмеренияОтбора Цикл
		Если Измерение.Значение <> Неопределено Тогда
			ИмяИзмерения = ?(Измерение.Ключ = "ВалютаРасчетов", "Валюта", Измерение.Ключ);
			Набор.Отбор[ИмяИзмерения].Установить(Измерение.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Набор.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСПотоками

Функция РазмерПорцииОбработкиВОдномПотоке()
	
	Возврат 1000;
	
КонецФункции

Функция КоличествоПотоковРаспределенияВзаиморасчетов()
	
	Количество = Константы.КоличествоПотоковРаспределенияВзаиморасчетов.Получить();
	Если Количество = 0 Тогда
		Количество = 8;
	КонецЕсли;
	Возврат Количество;
	
КонецФункции

// Новое описание потока.
// 
// Параметры:
//  ИмяМетода - Строка - Имя метода
// 
// Возвращаемое значение:
//  
Функция НовоеОписаниеПотока(ИмяМетода = "")
	
	Описание = Новый Структура;
	Описание.Вставить("ИдентификаторЗадания", Неопределено);
	Описание.Вставить("Процедура", ИмяМетода);
	Описание.Вставить("АдресРезультата", "");
	Описание.Вставить("НаименованиеЗадания", "");
	Описание.Вставить("ПараметрыПроцедуры", Новый Структура);
	Возврат Описание;
	
КонецФункции

Процедура ЗапуститьОбработкуВФоне(Поток)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Неопределено);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = Поток.НаименованиеЗадания;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
	
	РезультатЗапуска = ДлительныеОперации.ВыполнитьВФоне(Поток.Процедура, Поток.ПараметрыПроцедуры, ПараметрыВыполнения);
	
	Поток.АдресРезультата = РезультатЗапуска.АдресРезультата;
	Статус = РезультатЗапуска.Статус;
	
	Если Статус = "Выполняется" Тогда
		Поток.ИдентификаторЗадания = РезультатЗапуска.ИдентификаторЗадания;
	ИначеЕсли Статус <> "Выполняется" И Статус <> "Выполнено" Тогда
		ВызватьИсключение РезультатЗапуска.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОжидатьЗавершениеВсехПотоков(Потоки, КортежиВОбработке = Неопределено, КоличествоОбработанныхЗаписей = 0)
	Пока Потоки.Количество() > 0 Цикл
		Если НЕ ЗавершитьПотокиВыполнившиеФЗ(Потоки, КортежиВОбработке, КоличествоОбработанныхЗаписей) Тогда
			ОжидатьЗавершениеПотока(Потоки[0]);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ЗавершитьПотокиВыполнившиеФЗ(Потоки, КортежиВОбработке = Неопределено, КоличествоОбработанныхЗаписей = 0)
	
	ЕстьЗавершенныеПотоки = Ложь;
	
	Индекс = Потоки.Количество() - 1;
	Пока Индекс >= 0 Цикл
		Поток = Потоки[Индекс];
		ИдентификаторЗадания = Поток.ИдентификаторЗадания;
		
		Если ИдентификаторЗадания <> Неопределено Тогда
			ЗаданиеВыполнено = ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
		КонецЕсли;
		
		Если ИдентификаторЗадания = Неопределено ИЛИ ЗаданиеВыполнено Тогда
			Если ЗначениеЗаполнено(Поток.АдресРезультата) Тогда
				Результат = ПолучитьИзВременногоХранилища(Поток.АдресРезультата);
				Если Результат <> Неопределено И Результат.Свойство("КоличествоОбработанныхЗаписей") Тогда
					КоличествоОбработанныхЗаписей = КоличествоОбработанныхЗаписей + Результат.КоличествоОбработанныхЗаписей;
				КонецЕсли;
				УдалитьИзВременногоХранилища(Поток.АдресРезультата);
			КонецЕсли;
			Отбор = Новый Структура("ИдПотока",ИдентификаторЗадания);
			Если КортежиВОбработке <> Неопределено Тогда
				КортежиКУдалению = КортежиВОбработке.НайтиСтроки(Отбор);
				Для Каждого Кортеж Из КортежиКУдалению Цикл
					КортежиВОбработке.Удалить(Кортеж);
				КонецЦикла;
			КонецЕсли;
			Потоки.Удалить(Индекс);
			ЕстьЗавершенныеПотоки = Истина;
		КонецЕсли;
		
		Индекс = Индекс - 1;
	КонецЦикла;
	
	Возврат ЕстьЗавершенныеПотоки;
	
КонецФункции

Функция ОжидатьЗавершениеПотока(Поток, Длительность = 1)
	
	Если Поток <> Неопределено И Поток.ИдентификаторЗадания <> Неопределено Тогда
		Задание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(Поток.ИдентификаторЗадания);
		
		Если Задание <> Неопределено Тогда
			Попытка
				Задание.ОжидатьЗавершенияВыполнения(Длительность);
				Возврат Истина;
			Исключение
				// Специальная обработка не требуется, возможно исключение вызвано истечением времени ожидания.
				Возврат Ложь;
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

//Возвращает таблицу объектов расчетов по которым есть движения по ресурсам "Сумма", "КОплате", "КОтгрузке"
//
// Параметры:
//	Период - Дата - Необязательный, дата, после которой искать движения по регистрам.
//	ДетализироватьДоДокумента - Булево - По умолчанию Ложь.
//	
//	Возвращаемое значение:
//		ТаблицаЗначений - таблица объектов расчетов:
//			* АналитикаУчетаПоПартнерам - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам.
//			* ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов.
//			* ВалютаРасчетов - СправочникСсылка.Валюты.
//			* ЭтоРасчетыСКлиентами - Булево.
//			* АналитикаУчетаПоПартнерамПриемник - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам.
//			* ОбъектРасчетовПриемник - СправочникСсылка.ОбъектыРасчетов.
//			* ВалютаПриемник - СправочникСсылка.Валюты
//			* ПоДаннымОбъектаРасчетовИсточника - Булево
//
Функция ПолучитьВсеОбъекты(Период = Неопределено, ДетализироватьДоДокумента = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.Валюта КАК ВалютаРасчетов,
	|	ИСТИНА КАК ЭтоРасчетыСКлиентами,
	|	ЛОЖЬ КАК НачальноеЗаполнение,
	|	Расчеты.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
	|	Расчеты.ОбъектРасчетовПриемник КАК ОбъектРасчетовПриемник,
	|	Расчеты.ВалютаПриемник КАК ВалютаПриемник,
	|	ВЫБОР КОГДА НЕ СвязанныеРасчетыСКлиентами.Регистратор ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРасчетыСКлиентамиПриемник,
	|	&ПоДаннымОбъектаРасчетовИсточника
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами КАК СвязанныеРасчетыСКлиентами
	|			ПО СвязанныеРасчетыСКлиентами.АналитикаУчетаПоПартнерам = Расчеты.АналитикаУчетаПоПартнерамПриемник
	|			И СвязанныеРасчетыСКлиентами.ОбъектРасчетов = Расчеты.ОбъектРасчетовПриемник
	|			И СвязанныеРасчетыСКлиентами.Валюта = Расчеты.ВалютаПриемник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками КАК СвязанныеРасчетыСПоставщиками
	|			ПО СвязанныеРасчетыСПоставщиками.АналитикаУчетаПоПартнерам = Расчеты.АналитикаУчетаПоПартнерамПриемник
	|			И СвязанныеРасчетыСПоставщиками.ОбъектРасчетов = Расчеты.ОбъектРасчетовПриемник
	|			И СвязанныеРасчетыСПоставщиками.Валюта = Расчеты.ВалютаПриемник
	|ГДЕ
	|	(Расчеты.Сумма <> 0 ИЛИ Расчеты.КОплате <> 0 ИЛИ Расчеты.КОтгрузке <> 0)
	|	И Расчеты.Период >= &Период
	|	И Расчеты.Активность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.Валюта КАК ВалютаРасчетов,
	|	ЛОЖЬ КАК ЭтоРасчетыСКлиентами,
	|	ЛОЖЬ КАК НачальноеЗаполнение,
	|	Расчеты.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
	|	Расчеты.ОбъектРасчетовПриемник КАК ОбъектРасчетовПриемник,
	|	Расчеты.ВалютаПриемник КАК ВалютаПриемник,
	|	ВЫБОР КОГДА НЕ СвязанныеРасчетыСКлиентами.Регистратор ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРасчетыСКлиентамиПриемник,
	|	&ПоДаннымОбъектаРасчетовИсточника
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами КАК СвязанныеРасчетыСКлиентами
	|			ПО СвязанныеРасчетыСКлиентами.АналитикаУчетаПоПартнерам = Расчеты.АналитикаУчетаПоПартнерамПриемник
	|			И СвязанныеРасчетыСКлиентами.ОбъектРасчетов = Расчеты.ОбъектРасчетовПриемник
	|			И СвязанныеРасчетыСКлиентами.Валюта = Расчеты.ВалютаПриемник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками КАК СвязанныеРасчетыСПоставщиками
	|			ПО СвязанныеРасчетыСПоставщиками.АналитикаУчетаПоПартнерам = Расчеты.АналитикаУчетаПоПартнерамПриемник
	|			И СвязанныеРасчетыСПоставщиками.ОбъектРасчетов = Расчеты.ОбъектРасчетовПриемник
	|			И СвязанныеРасчетыСПоставщиками.Валюта = Расчеты.ВалютаПриемник
	|ГДЕ
	|	(Расчеты.Сумма <> 0 ИЛИ Расчеты.КОплате <> 0 ИЛИ Расчеты.КПоступлению <> 0)
	|	И Расчеты.Период >= &Период
	|	И Расчеты.Активность
	|";
	Запрос.УстановитьПараметр("Период", ?(Период = Неопределено,Дата(1,1,1),Период));
	Если ДетализироватьДоДокумента Тогда
		Запрос.УстановитьПараметр("ТипыБезПроверкиИзменения", СписокТиповРегистраторовПланов());
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"&ПоДаннымОбъектаРасчетовИсточника",
			"Расчеты.ПоДаннымОбъектаРасчетовИсточника КАК ПоДаннымОбъектаРасчетовИсточника,
			|Расчеты.ПорядокОперации КАК ПорядокОперации,
			|НАЧАЛОПЕРИОДА(Расчеты.Период, ДЕНЬ) КАК ДатаНачалаПересчета,
			|Расчеты.Регистратор КАК Документ,
			|ДАТАВРЕМЯ(3999,1,1) КАК ПериодКонтроляИзменений");
	Иначе
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"&ПоДаннымОбъектаРасчетовИсточника",
			"Расчеты.ПоДаннымОбъектаРасчетовИсточника КАК ПоДаннымОбъектаРасчетовИсточника,
			|ДАТАВРЕМЯ(3999,1,1) КАК ПериодКонтроляИзменений");
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

//Для целей сортировки документов в пределах 1 секунды.
// 
// Параметры:
// 	ТипДокумента - Тип - Тип документ, для которого подбирается номер.
// Возвращаемое значение:
// 	Строка - Номер для порядка.
Функция НомерТипа(ТипДокумента) Экспорт
	
	//	При добавлении документа - добавить в конец с новым номером, не меняя типы других документов.
	Тип = "";
	
	Если ТипДокумента = Тип("ДокументСсылка.РасчетКурсовыхРазниц") ИЛИ ТипДокумента = Тип("ДокументСсылка.РегистраторРасчетов")Тогда
		Тип="99";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
		Тип="01";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		Тип="14";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		Тип="16";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Тип="39";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		Тип="35";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		Тип="02";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств") Тогда
		Тип="41";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОперацияПоПлатежнойКарте") Тогда
		Тип="22";
	//++ Локализация
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОперацияПоЯндексКассе") Тогда
		Тип="23";
	//-- Локализация
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств") Тогда
		Тип="33";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВводОстатков")
		Или ТипДокумента = Тип("ДокументСсылка.ВводОстатковВзаиморасчетов") Тогда
		Тип="03";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВзаимозачетЗадолженности") Тогда
		Тип="04";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		Тип="07";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		Тип="06";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
		Тип="07";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВыкупВозвратнойТарыКлиентом") Тогда
		Тип="09";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВыкупВозвратнойТарыУПоставщика") Тогда
		Тип="10";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ГрафикИсполненияДоговора") Тогда
		Тип="12";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
		Тип="17";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств") Тогда
		Тип="18";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
		Тип="19";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		Тип="20";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.КорректировкаРегистров") Тогда
		Тип="21";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетКомиссионера") Тогда
		Тип="25";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетКомиссионераОСписании") Тогда
		Тип="26";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетКомитенту") Тогда
		Тип="27";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетКомитентуОСписании") Тогда
		Тип="28";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") Тогда
		Тип="30";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациямиОСписании") Тогда
		Тип="31";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
		Тип="32";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов") Тогда
		Тип="36";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
		Тип="37";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
		Тип="38";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		Тип="40";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.КорректировкаЗадолженности") Тогда
		Тип="42";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт") Тогда
		Тип="43";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВыкупПринятыхНаХранениеТоваров") Тогда
		Тип="11";
	//++ НЕ УТ
	
	//++ Устарело_Переработка24
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЗаказПереработчику") Тогда
		Тип="15";
	//-- Устарело_Переработка24
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЗаказПереработчику2_5") Тогда
		Тип="51";
	//++ Локализация
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВыбытиеДенежныхДокументов") Тогда
		Тип="08";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетОператораСистемыПлатон") Тогда
		Тип="44";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПоступлениеДенежныхДокументов") Тогда
		Тип="34";
	//-- Локализация
	
	//++ Устарело_Переработка24
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетПереработчика") Тогда
		Тип="29";
	//-- Устарело_Переработка24
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетПереработчика2_5") Тогда
		Тип="52";
	//-- НЕ УТ
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВыкупТоваровХранителем") Тогда
		Тип="45";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетОСписанииТоваровСХранения") Тогда
		Тип="46";
	//++ НЕ УТКА

	//++ Устарело_Переработка24
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЗаказДавальца") Тогда
		Тип="13";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетДавальцу") Тогда
		Тип="24";
	//-- Устарело_Переработка24
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЗаказДавальца2_5") Тогда
		Тип="53";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетДавальцу2_5") Тогда
		Тип="54";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетДавальцуМеждуОрганизациями") Тогда
		Тип="55";
	//-- НЕ УТКА
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетКомитентуОЗакупках") Тогда
		 Тип="47";
	//++ НЕ УТ
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтражениеЗарплатыВФинансовомУчете2_5") Тогда
		Тип="48";
	//-- НЕ УТ
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.Бронирование") Тогда
		Тип="49";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.Сторно") Тогда
		Тип="50";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.АннулированиеПодарочныхСертификатов") Тогда
		Тип="56";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ВозвратПодарочныхСертификатов") Тогда
		Тип="57";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.АктПремииКлиенту") Тогда
		Тип="58";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.АктПремииПоставщика") Тогда
		Тип="59";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.РеализацияПодарочныхСертификатов") Тогда
		Тип="60";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		Тип="61";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ЧекККМ") Тогда
		Тип="62";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПоступлениеТоваровОтКлиента") Тогда
		Тип="63";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.СписаниеОтгруженныхТоваров") Тогда
		Тип="64";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.КорректировкаГрафикаВзаиморасчетов") Тогда
		Тип="65";
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ОтчетБанкаПоОперациямЭквайринга") Тогда
		Тип="66";
	КонецЕсли;
	
	Если Тип = ""
		И ОбщегоНазначения.РежимОтладки() Тогда
		
		ПодробноеПредставлениеОшибки = СтрЗаменить(
			НСтр("ru = 'Не определен порядок распределения типа документа в пределах дня %1.';
				|en = 'The order of document type distribution within the day %1 is not defined.'"),
			"%1",
			ТипДокумента);
			
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Оперативные взаиморасчеты с контрагентами';
										|en = 'Real-time mutual settlements with counterparties'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
			
		ВызватьИсключение ПодробноеПредставлениеОшибки;
		
	Иначе
		Возврат Тип;
	КонецЕсли;
	
КонецФункции

//Возвращает структуру данных реализации для передачи между служебными процедурами.
//
// Возвращаемое значение:
//	Структура - структура данных реализации.
//		* СуммаОстаток для корректировок - при первом упоминании формируется запись с первоначальной суммой, далее из нее вычитаются оплаты и корректировки.
//		* Остальные суммы для расчета среднего курса и, наверное, формирования движений по Суммам документов в валюте регл.
Функция СтруктураСумм(СуммаДокумента = 0, СуммаДокументаРегл = 0, СуммаДокументаУпр = 0)
	СтруктураСумм = Новый Структура;
	СтруктураСумм.Вставить("СуммаДокумента", СуммаДокумента);
	СтруктураСумм.Вставить("СуммаДокументаРегл", СуммаДокументаРегл);
	СтруктураСумм.Вставить("СуммаДокументаУпр", СуммаДокументаУпр);
	Возврат СтруктураСумм;
КонецФункции

//Возвращает структуру данных корректировки графика взаиморасчетов для сохранения курса и даты возникновения
// в процессе переноса дат планового погашения.
//
// Возвращаемое значение:
//	Структура - структура для хранения расходных сумм корректировок графика:
//	 * СуммаРасход - Число - сумма расхода долга или предоплаты в валюте взаиморасчетов.
//	 * СуммаРасходРегл - Число - сумма расхода долга или предоплаты в валюте регламентированного учета.
//	 * СуммаРасходУпр - Число - сумма расхода долга или предоплаты в валюте управленческого учета.
//	 * ДатаВозникновения - Дата - Исходная дата возникновения задолженности.
Функция СтруктураДанныхКорректировкиГрафика()
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("СуммаРасход", 0);
	СтруктураДанных.Вставить("СуммаРасходРегл", 0);
	СтруктураДанных.Вставить("СуммаРасходУпр", 0);
	СтруктураДанных.Вставить("ДатаВозникновения", Дата(1,1,1));
	Возврат СтруктураДанных;
КонецФункции

//Типы регистраторов, которые двигают план оплат, отгрузок и поставок в приход.
//
// Возвращаемое значение:
// 	СписокЗначений из Тип - Список типов документов, которые двигают только плановые регистры расчетов. Используется для распредедления плановых расчетов.
Функция СписокТиповРегистраторовПланов() Экспорт
	СписокТиповРегистраторовПланов = Новый СписокЗначений;
	//++ НЕ УТКА

	//++ Устарело_Переработка24
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ЗаказДавальца"));
	//-- Устарело_Переработка24
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ЗаказДавальца2_5"));
	//-- НЕ УТКА

	//++ НЕ УТ
	
	//++ Устарело_Переработка24
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ЗаказПереработчику"));
	//-- Устарело_Переработка24
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ЗаказПереработчику2_5"));
	//-- НЕ УТ
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ЗаказКлиента"));
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента"));
	СписокТиповРегистраторовПланов.Добавить(Тип("ДокументСсылка.ГрафикИсполненияДоговора"));
	Возврат СписокТиповРегистраторовПланов;
КонецФункции

//Для данных хоз операций не будет сформирована двойная запись в регистрах расчетов по срокам.
// 
// Возвращаемое значение:
// 	Массив из ПеречислениеСсылка.ХозяйственныеОперации - Массив операций, для которых не нужно делать запись по переносу аванса.
//
Функция ХозяйственныеОперацииНеОтгрузка() Экспорт
	Массив = Новый Массив;
	Массив.Добавить(Перечисления.ХозяйственныеОперации.СписаниеКредиторскойЗадолженности);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.НачислениеКредиторскойЗадолженности);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.НачислениеДебиторскойЗадолженности);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.СписаниеБезнадежнойЗадолженностиЗаСчетРезервовПоСомнительнымДолгам);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ВозвратТоваровПоставщику);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.РезервированиеАвансаКлиента);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиентуНаПлатежнуюКарту);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ВозвратОплатыНаПлатежнуюКарту);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ОтражениеВозвратаОплатыЧерезКомиссионера);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ВозвратБронирования);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ИспользованиеБронированияПодотчетнымЛицом);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ВозвратБронированияПодотчетногоЛица);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Массив, ХозяйственныеОперацииПереносаРасчетов());
	
	Возврат Массив;
КонецФункции

//Для данных хоз операций не будет сформирована двойная запись в регистрах расчетов по срокам, а так же будет сохранена исходная хозяйственная операция.
// 
// Возвращаемое значение:
// 	Массив из ПеречислениеСсылка.ХозяйственныеОперации - Массив операций, для которых не нужно делать запись по переносу аванса.
//
Функция ХозяйственныеОперацииПереносаРасчетов()
	Массив = Новый Массив;
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ВзаимозачетЗадолженности);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ПереносАванса);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ПереносДолга);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ПереносПлатежаМеждуФилиалами);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ПереносЗадолженностиМеждуФилиалами);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.УдержаниеВознагражденияКомиссионера);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.УдержаниеВознагражденияКомитентом);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.КорректировкаГрафикаОплатыКлиентом);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.КорректировкаГрафикаОплатыПоставщику);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаКлиента);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаПоставщику);
	Возврат Массив;
КонецФункции

//По данным хоз. операциям суммы в валюте регламентированного и управленческого учета не будут пересчитаны с учетом авансов.
// 
// Возвращаемое значение:
// 	Массив из ПеречислениеСсылка.ХозяйственныеОперации - Массив непересчитываемых операций.
Функция ХозяйственныеОперацииСРучнымКурсом() Экспорт
	Массив = Новый Массив;
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ВзаимозачетЗадолженности);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхДокументовОтПоставщика);
	Массив.Добавить(Перечисления.ХозяйственныеОперации.ИспользованиеБронированияПодотчетнымЛицом);
	Возврат Массив;
КонецФункции

//Возвращает движения документов Реализация товаров и услуг с хоз. операцией Товары в пути к движениям,
//пригодным для офлайн взаиморасчетов.
//
Процедура ВернутьДвиженияПоРасчетамСКлиентами()
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	РасчетыСКлиентами.Регистратор                  КАК Регистратор,
	|	МАКСИМУМ(РасчетыСКлиентами.Период)             КАК МаксимальныйПериод
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|ГДЕ
	|	РасчетыСКлиентами.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РезервированиеАвансаКлиента),
	|												ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав),
	|												ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности))
	|	И ТИПЗНАЧЕНИЯ(РасчетыСКлиентами.Регистратор) В (ТИП(Документ.РеализацияТоваровУслуг),
	|													ТИП(Документ.РеализацияУслугПрочихАктивов))
	|	И РасчетыСКлиентами.Сумма <> 0
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентами.Регистратор
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РасчетыСКлиентами.Период) > 1");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыНакопления.РасчетыСКлиентами.СоздатьНаборЗаписей();
		НаборЗаписей.ОбменДанными.Загрузка = ИСТИНА;
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		НаборЗаписей.Прочитать();
		Для Каждого Запись Из НаборЗаписей Цикл
			Если Запись.Сумма <> 0 Тогда
				Запись.Период = Выборка.МаксимальныйПериод;
			КонецЕсли;
		КонецЦикла;
		НаборЗаписей.Записать();
	КонецЦикла;
КонецПроцедуры

//Количество одновременно записываемых записей регистров под одним служебным документом регистратором.
Функция РазмерПорцииЗаписи()
	Возврат 1000;
КонецФункции

Процедура ЗаписатьПорционно(ГлобальныеПеременные, ТаблицаЗаписей, НаборЗаписей) 
	
	ИмяРегистра = НаборЗаписей.Метаданные().Имя;
	
	Для Каждого РегистраторРасчетов Из ГлобальныеПеременные["МассивЗадействованныхРегистраторов"+ИмяРегистра] Цикл
		
		НаборЗаписей.Отбор.Регистратор.Установить(РегистраторРасчетов);
		НаборЗаписей.Загрузить(ТаблицаЗаписей.Скопировать(Новый Структура("Регистратор", РегистраторРасчетов)));
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
	Для Каждого СтрокаСвободныхРегистраторов Из ГлобальныеПеременные["МассивСвободныхРегистраторов"+ИмяРегистра] Цикл
		
		Если СтрокаСвободныхРегистраторов.ТребуетсяОчистка Тогда
			ТекущийРегистратор = СтрокаСвободныхРегистраторов.Ссылка; //ДокументСсылка.РегистраторРасчетов
			НаборЗаписей.Отбор.Регистратор.Установить(ТекущийРегистратор);
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляЗаписи(ГлобальныеПеременные, ТаблицаЗаписей, НаборЗаписей, ТаблицаСвободныхРегистраторов)
	РазмерПорцииЗаписи = РазмерПорцииЗаписи();
	ИмяРегистра = НаборЗаписей.Метаданные().Имя;
	
	МассивСвободныхРегистраторов = СвободныеРегистраторыРасчетов(ГлобальныеПеременные, ИмяРегистра, ТаблицаЗаписей.Количество(), ТаблицаСвободныхРегистраторов);
	МассивЗадействованныхРегистраторов = Новый Массив;
	
	Если МассивСвободныхРегистраторов.Количество() > 0 Тогда
		
		ТекущаяСтрока = 1;
		ТекущийРегистратор = 0;
		
		МассивСвободныхРегистраторов[ТекущийРегистратор].ТребуетсяОчистка = Ложь;
		МассивЗадействованныхРегистраторов.Добавить(МассивСвободныхРегистраторов[ТекущийРегистратор].Ссылка);
		
		Для Каждого Строка Из ТаблицаЗаписей Цикл
			Если ТипЗнч(Строка.ДокументРегистратор) = Тип("ДокументСсылка.КорректировкаРегистров") Тогда
				Продолжить;
			КонецЕсли;
			Если ТекущаяСтрока > РазмерПорцииЗаписи Тогда
				ТекущаяСтрока = 1;
				ТекущийРегистратор = ТекущийРегистратор + 1;
				МассивСвободныхРегистраторов[ТекущийРегистратор].ТребуетсяОчистка = Ложь;
				МассивЗадействованныхРегистраторов.Добавить(МассивСвободныхРегистраторов[ТекущийРегистратор].Ссылка);
			КонецЕсли;
			Строка.Регистратор = МассивСвободныхРегистраторов[ТекущийРегистратор].Ссылка;
			
			ТекущаяСтрока = ТекущаяСтрока + 1;
		КонецЦикла;
		
	КонецЕсли;
	
	ГлобальныеПеременные.Вставить("МассивСвободныхРегистраторов" + ИмяРегистра, МассивСвободныхРегистраторов);
	ГлобальныеПеременные.Вставить("МассивЗадействованныхРегистраторов" + ИмяРегистра, МассивЗадействованныхРегистраторов);
КонецПроцедуры

// Находит неиспользуемые регистраторы расчетов и удаляет их
//
// Параметры:
//   Запрос - Запрос - Запрос с установленными параметрами.
//   ИмяРегистраРасчетов - Строка.
//   ИмяРегистраПланаОплат - Строка.
//   ИмяРегистраПланаОтгрузкиПоставки - Строка.
//
Процедура УдалитьНеиспользуемыеРегистраторыРасчетов(Запрос, ИмяРегистраРасчетов, ИмяРегистраПланаОплат, ИмяРегистраПланаОтгрузкиПоставки)
	
	Запрос.Текст = "
	|
	|ВЫБРАТЬ
	|	РегистраторРасчетов.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.РегистраторРасчетов КАК РегистраторРасчетов
	|ГДЕ            
	|	РегистраторРасчетов.ОбъектРасчетов              = &ОбъектРасчетов
	|	И РегистраторРасчетов.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
	|	И РегистраторРасчетов.Валюта                    = &ВалютаРасчетов
	|	И РегистраторРасчетов.ТипРасчетов               = &ТипРасчетов
	|	И НЕ ИСТИНА В (
	|		ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			&ТаблицаПланаОплат КАК РасчетыПланОплат
	|		ГДЕ
	|			РасчетыПланОплат.Регистратор = РегистраторРасчетов.Ссылка
	|	)
	|	И НЕ ИСТИНА В (
	|		ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			&ТаблицаПланаОтгрузкиПоставки КАК РасчетыПланОтгрузокПоставок
	|		ГДЕ
	|			РасчетыПланОтгрузокПоставок.Регистратор = РегистраторРасчетов.Ссылка
	|	)
	|	И НЕ ИСТИНА В (
	|		ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			&ТаблицаРасчетов КАК РасчетыПоСрокам
	|		ГДЕ
	|			РасчетыПоСрокам.Регистратор = РегистраторРасчетов.Ссылка
	|	)
	|	
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"&ТаблицаРасчетов", "РегистрНакопления." + ИмяРегистраРасчетов);
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"&ТаблицаПланаОтгрузкиПоставки", "РегистрНакопления." + ИмяРегистраПланаОтгрузкиПоставки);
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"&ТаблицаПланаОплат", "РегистрНакопления." + ИмяРегистраПланаОплат);
	
	ВыборкаДокументов = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаДокументов.Следующий() Цикл
		ДокументОбъект = ВыборкаДокументов.Ссылка.ПолучитьОбъект(); // ДокументОбъект.РегистраторРасчетов
		ДокументОбъект.Удалить();
	КонецЦикла;
	
КонецПроцедуры

Процедура РассчитатьВсе(ПараметрыВыполнения, АдресХранилища) Экспорт
	
	РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСКлиентами(ПараметрыВыполнения.ОкончаниеПериодаРасчета, Неопределено);
	РаспределениеВзаиморасчетовВызовСервера.РаспределитьВсеРасчетыСПоставщиками(ПараметрыВыполнения.ОкончаниеПериодаРасчета, Неопределено);
	
КонецПроцедуры

// Находит по указанным параметрам корректировки регистров, очищает их и возвращает новую корректировку.
//
// Параметры:
//   ПериодКорректировки - Дата - Все корректировки позже этой даты будут очищены, а с равной датой еще и удалены.
//   Организация - СправочникСсылка.Организации.
//   Операция - ПеречислениеСсылка.ОперацииКорректировкиРегистров.
//
// Возвращаемое значение:
//   ДокументОбъект.КорректировкаРегистров - Пустой документ корректировки, с заполненными реквизитами.
//
Функция КорректировкаРегистров(ПериодКорректировки, Организация, Операция) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КорректировкаРегистров.Ссылка КАК Ссылка,
	|	КорректировкаРегистров.Дата КАК Дата
	|ИЗ
	|	Документ.КорректировкаРегистров КАК КорректировкаРегистров
	|ГДЕ
	|	КорректировкаРегистров.Дата >= &Дата
	|	И КорректировкаРегистров.Операция = &Операция
	|	И КорректировкаРегистров.Организация = &Организация
	|УПОРЯДОЧИТЬ ПО
	|	КорректировкаРегистров.Дата ВОЗР";
	Запрос.УстановитьПараметр("Дата", КонецМесяца(ПериодКорректировки));
	Запрос.УстановитьПараметр("Операция", Операция);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	КорректировкаОбъект = Неопределено;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект(); // ДокументОбъект.КорректировкаРегистров
		Для Каждого Регистр Из ДокументОбъект.ТаблицаРегистров Цикл
			Движения = ДокументОбъект.Движения[Регистр.Имя]; // РегистрНакопленияНаборЗаписей
			Движения.Записать();
		КонецЦикла;
		
		Если Выборка.Дата = КонецМесяца(ПериодКорректировки) Тогда
			КорректировкаОбъект = ДокументОбъект;
			КорректировкаОбъект.УстановитьПометкуУдаления(Ложь);
		Иначе
			ДокументОбъект.Удалить();
		КонецЕсли;
		
	КонецЦикла;
	
	Если КорректировкаОбъект = Неопределено Тогда
		КорректировкаОбъект = Документы.КорректировкаРегистров.СоздатьДокумент();
		КорректировкаОбъект.Дата        = ПериодКорректировки;
		КорректировкаОбъект.Операция    = Операция;
		КорректировкаОбъект.Организация = Организация;
		КорректировкаОбъект.УстановитьСсылкуНового(Документы.КорректировкаРегистров.ПолучитьСсылку());
		
		НовСтр = КорректировкаОбъект.ТаблицаРегистров.Добавить();
		НовСтр.Имя = "РасчетыСКлиентамиПоСрокам";
		НовСтр = КорректировкаОбъект.ТаблицаРегистров.Добавить();
		НовСтр.Имя = "РасчетыСПоставщикамиПоСрокам";
		
		КорректировкаОбъект.Записать();
	КонецЕсли;
	
	Возврат КорректировкаОбъект;
	
КонецФункции

Процедура УдалитьСистемныеКорректировкиРегистров(ДатаПересчета, Организация = Неопределено, Операция = Неопределено, Контрагент = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КорректировкаРегистров.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаРегистров КАК КорректировкаРегистров
	|ГДЕ
	|	КорректировкаРегистров.Дата >= &Дата
	|	И (КорректировкаРегистров.Организация = &Организация ИЛИ &ПоВсемОрганизациям)
	|	И КорректировкаРегистров.Операция В (&Операции)
	|	И &ПоВсемКонтрагентам
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючАналитики
	|			ПО КлючАналитики.Ссылка = Расчеты.АналитикаУчетаПоПартнерам
	|ГДЕ
	|	Расчеты.Период >= &Дата
	|	И КлючАналитики.Контрагент = &Контрагент
	|	И (КлючАналитики.Организация = &Организация ИЛИ &ПоВсемОрганизациям)
	|	И НЕ &ПоВсемКонтрагентам
	|	И Расчеты.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|	И ВЫРАЗИТЬ(Расчеты.Регистратор КАК Документ.КорректировкаРегистров).Операция В (&Операции)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючАналитики
	|			ПО КлючАналитики.Ссылка = Расчеты.АналитикаУчетаПоПартнерам
	|ГДЕ
	|	Расчеты.Период >= &Дата
	|	И КлючАналитики.Контрагент = &Контрагент
	|	И (КлючАналитики.Организация = &Организация ИЛИ &ПоВсемОрганизациям)
	|	И НЕ &ПоВсемКонтрагентам
	|	И Расчеты.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|	И ВЫРАЗИТЬ(Расчеты.Регистратор КАК Документ.КорректировкаРегистров).Операция В (&Операции)
	|
	|;
	|
	|ВЫБРАТЬ
	|	КлючиАналитикиУчетаПоПартнерам.Ссылка КАК АналитикаУчетаПоПартнерам
	|ИЗ
	|	Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиАналитикиУчетаПоПартнерам
	|ГДЕ
	|	(КлючиАналитикиУчетаПоПартнерам.Организация = &Организация ИЛИ &ПоВсемОрганизациям)
	|	И КлючиАналитикиУчетаПоПартнерам.Контрагент = &Контрагент
	|	И НЕ &ПоВсемКонтрагентам";
	Запрос.УстановитьПараметр("Дата", НачалоДня(ДатаПересчета));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", Организация = Неопределено);
	Запрос.УстановитьПараметр("ПоВсемКонтрагентам", Контрагент = Неопределено);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Операции = Новый Массив;
	Если ЗначениеЗаполнено(Операция) Тогда
		Операции.Добавить(Операция);
	Иначе
		Операции.Добавить(Перечисления.ОперацииКорректировкиРегистров.ИсправлениеРазрывовОстатковВзаиморасчетов);
		Операции.Добавить(Перечисления.ОперацииКорректировкиРегистров.ИсправлениеРазвернутогоСальдоВзаиморасчетов);
	КонецЕсли;
	Запрос.УстановитьПараметр("Операции", Операции);
	
	Результат = Запрос.ВыполнитьПакет();
	Выборка = Результат[0].Выбрать();
	ТаблицаАналитик = Результат[1].Выгрузить();
	ТаблицаАналитик.Индексы.Добавить("АналитикаУчетаПоПартнерам");
	
	ВРежимеЗагрузки = ДатаПересчета = Дата(1,1,1);
	
	Пока Выборка.Следующий() Цикл
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект(); // ДокументОбъект.КорректировкаРегистров
		
		Для Каждого Регистр Из ДокументОбъект.ТаблицаРегистров Цикл
			Движения = ДокументОбъект.Движения[Регистр.Имя]; // РегистрНакопленияНаборЗаписей
			Движения.ОбменДанными.Загрузка = ВРежимеЗагрузки;
			Если ЗначениеЗаполнено(Контрагент) 
				И (Регистр.Имя = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Имя 
					Или Регистр.Имя = Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.Имя) Тогда
					Движения.Прочитать();
					Счетчик = Движения.Количество();
					Пока Счетчик > 0 Цикл
						Счетчик = Счетчик - 1;
						Если ТаблицаАналитик.Найти(Движения[Счетчик].АналитикаУчетаПоПартнерам) <> Неопределено Тогда
							Движения.Удалить(Счетчик);
						КонецЕсли;
					КонецЦикла;
					
			КонецЕсли;
			Движения.Записать();
		КонецЦикла;
		
		Если ДокументОбъект.Движения.РасчетыСКлиентамиПоСрокам.Количество() = 0 
			И ДокументОбъект.Движения.РасчетыСПоставщикамиПоСрокам.Количество() = 0 Тогда
			ДокументОбъект.ОбменДанными.Загрузка = ВРежимеЗагрузки;
			ДокументОбъект.Удалить();
		Иначе
			ДополнитьКорректировкуДвижениямиПоПрочимДоходамРасходам(ДокументОбъект);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ТаблицаИзмененийДляПересчета(МенеджерВременныхТаблиц, Регистратор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ТипБезПроверкиИзменения", СписокТиповРегистраторовПланов().НайтиПоЗначению(ТипЗнч(Регистратор)) <> Неопределено);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ТипРасчетов                      КАК ТипРасчетов,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам        КАК АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ОбъектРасчетов                   КАК ОбъектРасчетов,
	|	ВложенныйЗапрос.ВалютаРасчетов                   КАК ВалютаРасчетов,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
	|	ВложенныйЗапрос.ОбъектРасчетовПриемник           КАК ОбъектРасчетовПриемник,
	|	ВложенныйЗапрос.ВалютаПриемник                   КАК ВалютаПриемник,
	|	ВложенныйЗапрос.Документ                         КАК Документ,
	|	ВложенныйЗапрос.ПоДаннымОбъектаРасчетовИсточника КАК ПоДаннымОбъектаРасчетовИсточника,
	|	МИНИМУМ(ВложенныйЗапрос.ДатаНачалаПересчета)     КАК ДатаНачалаПересчета,
	|	МИНИМУМ(ВложенныйЗапрос.ПорядокОперации)         КАК ПорядокОперации,
	|	МАКСИМУМ(ВложенныйЗапрос.НачальноеЗаполнение)    КАК НачальноеЗаполнение,
	|	МИНИМУМ(ВложенныйЗапрос.ПериодКонтроляИзменений) КАК ПериодКонтроляИзменений
	|ИЗ 
	|	&ТаблицаИзменений КАК ВложенныйЗапрос
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ТипРасчетов,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ОбъектРасчетов,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерамПриемник,
	|	ВложенныйЗапрос.ОбъектРасчетовПриемник,
	|	ВложенныйЗапрос.ВалютаПриемник,
	|	ВложенныйЗапрос.ВалютаРасчетов,
	|	ВложенныйЗапрос.Документ,
	|	ВложенныйЗапрос.ПоДаннымОбъектаРасчетовИсточника
	|УПОРЯДОЧИТЬ ПО
	|	ВложенныйЗапрос.ПоДаннымОбъектаРасчетовИсточника ВОЗР
	|";
	
	ТекстыВложенногоЗапроса = Новый Массив;
	Если МенеджерВременныхТаблиц.Таблицы.Найти("РасчетыСКлиентамиИзменения") <> Неопределено Тогда
		
		ТекстыВложенногоЗапроса.Добавить("
		|	ВЫБРАТЬ
		|		Изменения.ПорядокОперации                                                       КАК ПорядокОперации,
		|		НАЧАЛОПЕРИОДА(
		|			ВЫБОР КОГДА Изменения.Сумма = 0
		|					И (Изменения.КОплате <> 0 ИЛИ Изменения.КОтгрузке <> 0)
		|					И ТИПЗНАЧЕНИЯ(Изменения.Документ) <> ТИП(Документ.ВводОстатков)
		|					И ТИПЗНАЧЕНИЯ(Изменения.Документ) <> ТИП(Документ.ВводОстатковВзаиморасчетов)
		|				ТОГДА Изменения.ДатаРегистратора
		|				ИНАЧЕ Изменения.Период
		|			КОНЕЦ, ДЕНЬ)                                                                КАК ДатаНачалаПересчета,
		|		Изменения.НачальноеЗаполнение                                                   КАК НачальноеЗаполнение,
		|		ВЫБОР
		|			КОГДА &ТипБезПроверкиИзменения
		|				ТОГДА ДАТАВРЕМЯ(3999,1,1)
		|			ИНАЧЕ НАЧАЛОПЕРИОДА(Изменения.Период, ДЕНЬ)
		|		КОНЕЦ                                                                           КАК ПериодКонтроляИзменений,
		|		Изменения.АналитикаУчетаПоПартнерам                                             КАК АналитикаУчетаПоПартнерам,
		|		Изменения.ОбъектРасчетов                                                        КАК ОбъектРасчетов,
		|		Изменения.АналитикаУчетаПоПартнерамПриемник                                     КАК АналитикаУчетаПоПартнерамПриемник,
		|		Изменения.ОбъектРасчетовПриемник                                                КАК ОбъектРасчетовПриемник,
		|		Изменения.ВалютаПриемник                                                        КАК ВалютаПриемник,
		|		Изменения.Документ                                                              КАК Документ,
		|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)                 КАК ТипРасчетов,
		|		Изменения.ПоДаннымОбъектаРасчетовИсточника                                      КАК ПоДаннымОбъектаРасчетовИсточника,
		|		Изменения.ВалютаРасчетов                                                        КАК ВалютаРасчетов
		|	ИЗ
		|		РасчетыСКлиентамиИзменения КАК Изменения
		|	ГДЕ
		|		Изменения.Сумма <> 0 ИЛИ Изменения.СуммаРегл <> 0 ИЛИ Изменения.СуммаУпр<> 0 ИЛИ Изменения.КОплате <> 0 ИЛИ Изменения.КОтгрузке <> 0
		|
		|");
		
	КонецЕсли;
	
	Если МенеджерВременныхТаблиц.Таблицы.Найти("РасчетыСКлиентамиИзменения") <> Неопределено
		И МенеджерВременныхТаблиц.Таблицы.Найти("РасчетыСПоставщикамиИзменения") <> Неопределено Тогда
		ТекстыВложенногоЗапроса.Добавить("
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|");
	КонецЕсли;
	
	Если МенеджерВременныхТаблиц.Таблицы.Найти("РасчетыСПоставщикамиИзменения") <> Неопределено Тогда
		
		ТекстыВложенногоЗапроса.Добавить("
		|	ВЫБРАТЬ
		|		Изменения.ПорядокОперации                                                       КАК ПорядокОперации,
		|		НАЧАЛОПЕРИОДА(
		|			ВЫБОР КОГДА Изменения.Сумма = 0
		|					И (Изменения.КОплате <> 0 ИЛИ Изменения.КПоступлению <> 0)
		|					И ТИПЗНАЧЕНИЯ(Изменения.Документ) <> ТИП(Документ.ВводОстатков)
		|					И ТИПЗНАЧЕНИЯ(Изменения.Документ) <> ТИП(Документ.ВводОстатковВзаиморасчетов)
		|				ТОГДА Изменения.ДатаРегистратора
		|				ИНАЧЕ Изменения.Период
		|			КОНЕЦ, ДЕНЬ)                                                                КАК ДатаНачалаПересчета,
		|		Изменения.НачальноеЗаполнение                                                   КАК НачальноеЗаполнение,
		|		ВЫБОР
		|			КОГДА &ТипБезПроверкиИзменения
		|				ТОГДА ДАТАВРЕМЯ(3999,1,1)
		|			ИНАЧЕ НАЧАЛОПЕРИОДА(Изменения.Период, ДЕНЬ)
		|		КОНЕЦ                                                                           КАК ПериодКонтроляИзменений,
		|		Изменения.АналитикаУчетаПоПартнерам                                             КАК АналитикаУчетаПоПартнерам,
		|		Изменения.ОбъектРасчетов                                                        КАК ОбъектРасчетов,
		|		Изменения.АналитикаУчетаПоПартнерамПриемник                                     КАК АналитикаУчетаПоПартнерамПриемник,
		|		Изменения.ОбъектРасчетовПриемник                                                КАК ОбъектРасчетовПриемник,
		|		Изменения.ВалютаПриемник                                                        КАК ВалютаПриемник,
		|		Изменения.Документ                                                              КАК Документ,
		|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)              КАК ТипРасчетов,
		|		Изменения.ПоДаннымОбъектаРасчетовИсточника                                      КАК ПоДаннымОбъектаРасчетовИсточника,
		|		Изменения.ВалютаРасчетов                                                        КАК ВалютаРасчетов
		|	ИЗ
		|		РасчетыСПоставщикамиИзменения КАК Изменения
		|	ГДЕ
		|		Изменения.Сумма <> 0 ИЛИ Изменения.СуммаРегл <> 0 ИЛИ Изменения.СуммаУпр<> 0 ИЛИ Изменения.КОплате <> 0 ИЛИ Изменения.КПоступлению <> 0
		|
		|");
		
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТаблицаИзменений", СтрШаблон("(%1)", СтрСоединить(ТекстыВложенногоЗапроса)));
	
	ТаблицаИзменений = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаИзменений;
	
КонецФункции

Процедура ОтключитьИтогиРегистровРасчетов()
	
	РегистрыНакопления.РасчетыСКлиентамиПланОплат.УстановитьИспользованиеИтогов(Ложь);
	РегистрыНакопления.РасчетыСКлиентамиПланОтгрузок.УстановитьИспользованиеИтогов(Ложь);
	РегистрыНакопления.РасчетыСКлиентамиПоСрокам.УстановитьИспользованиеИтогов(Ложь);
	РегистрыНакопления.РасчетыСПоставщикамиПланОплат.УстановитьИспользованиеИтогов(Ложь);
	РегистрыНакопления.РасчетыСПоставщикамиПланПоставок.УстановитьИспользованиеИтогов(Ложь);
	РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.УстановитьИспользованиеИтогов(Ложь);
	
КонецПроцедуры

Процедура ВключитьИтогиРегистровРасчетов()
	
	РегистрыНакопления.РасчетыСКлиентамиПланОплат.УстановитьИспользованиеИтогов(Истина);
	РегистрыНакопления.РасчетыСКлиентамиПланОтгрузок.УстановитьИспользованиеИтогов(Истина);
	РегистрыНакопления.РасчетыСКлиентамиПоСрокам.УстановитьИспользованиеИтогов(Истина);
	РегистрыНакопления.РасчетыСПоставщикамиПланОплат.УстановитьИспользованиеИтогов(Истина);
	РегистрыНакопления.РасчетыСПоставщикамиПланПоставок.УстановитьИспользованиеИтогов(Истина);
	РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.УстановитьИспользованиеИтогов(Истина);
	
	РегистрыНакопления.РасчетыСКлиентамиПланОплат.ПересчитатьИтоги();
	РегистрыНакопления.РасчетыСКлиентамиПланОтгрузок.ПересчитатьИтоги();
	РегистрыНакопления.РасчетыСКлиентамиПоСрокам.ПересчитатьИтоги();
	РегистрыНакопления.РасчетыСПоставщикамиПланОплат.ПересчитатьИтоги();
	РегистрыНакопления.РасчетыСПоставщикамиПланПоставок.ПересчитатьИтоги();
	РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПересчитатьИтоги();
	
КонецПроцедуры

#Область РасчетКурсовыхРазниц

// Возвращает структуру параметров необходимых для регистрации документа к отложенному распределению расчетов.
// 
// Возвращаемое значение:
// 	Структура - Описание:
//  *ЭтоРасчетыСКлиентами - Булево - Истина если это расчеты с клиентами.
//  *ПереоценкаПоДням - Булево - Истина если необходимо переоценивать каждый день, иначе только по дням оплаты.
//  *ПерезаполнениеРегистровНУ - Булево - Истина если выполняется расчет курсовых по НУ.
//
Функция ПараметрыПереоценки(ЭтоРасчетыСКлиентами = Неопределено, ПереоценкаПоДням = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ЭтоРасчетыСКлиентами", ЭтоРасчетыСКлиентами);
	Результат.Вставить("ПереоценкаПоДням", ПереоценкаПоДням);
	Если ПереоценкаПоДням = Неопределено Тогда
		Результат.Вставить("ПереоценкаПоДням", Константы.ПереоцениватьВалютныеСредстваПоДням.Получить());
	КонецЕсли;
	Результат.Вставить("ПерезаполнениеРегистровНУ", Ложь);

	Возврат Результат;
	
КонецФункции

Функция УдалитьКурсовыеРазницыЗаДень(Период, НаборЗаписей)
	
	Индекс = 0;
	БылоУдаление = Ложь;
	Пока Индекс < НаборЗаписей.Количество() Цикл
		Запись = НаборЗаписей[Индекс];
		Если НачалоДня(Запись.Период) = НачалоДня(Период)
			И ТипЗнч(Запись.ДокументРегистратор) = Тип("ДокументСсылка.РасчетКурсовыхРазниц") Тогда
			НаборЗаписей.Удалить(Запись);
			БылоУдаление = Истина;
			Продолжить;
		КонецЕсли;
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Возврат БылоУдаление;
	
КонецФункции

// Удаляет движения курсовых разниц за период только по тем дням,
// по которым в текущем периоде больше не будет переоценки.
// Например оплата пренесена на другой дату и на текущую дату больше нет объектов для переоценки
// или был режим переоценки по дням, а стал режим переоценки на дату оплаты и конец месяца.
// 
Процедура УдалитьКурсовыеРазницыЗаПериод(МассивОрганизаций, НачалоПериода, КонецПериода, Параметры)
	
	Если Параметры.ЭтоРасчетыСКлиентами = Неопределено Тогда
		ИмяЗамера = "Взаиморасчеты.РасчетКурсовыхРазницПоСрокам.Удаление";
	Иначе
		ИмяЗамера = "Взаиморасчеты.РасчетКурсовыхРазницПоСрокам" + ?(Параметры.ЭтоРасчетыСКлиентами,".Клиенты",".Поставщики")+".Удаление";
	КонецЕсли;
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(ИмяЗамера);
	
	#Область ТекстЗапроса
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДниПереоценки.Дата КАК Дата
	|ПОМЕСТИТЬ втДниПереоценки
	|ИЗ
	|	&ДниПереоценки КАК ДниПереоценки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Дни осутствия курсовых разниц - дни в которых при новом расчете движений курсовых разниц не будет
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Дата КАК Дата,
	|	СУММА(ВложенныйЗапрос.ПредыдущаяПереоценка) КАК ПредыдущаяПереоценка
	|ПОМЕСТИТЬ втПредыдущаяПереоценка
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДниПереоценки.Дата КАК Дата,
	|		1 КАК ПредыдущаяПереоценка
	|	ИЗ
	|		втДниПереоценки КАК ДниПереоценки
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		НАЧАЛОПЕРИОДА(Расчеты.Период, ДЕНЬ) КАК Дата,
	|		-1 КАК ПредыдущаяПереоценка
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиАналитикиУчетаПоПартнерам
	|			ПО Расчеты.АналитикаУчетаПоПартнерам = КлючиАналитикиУчетаПоПартнерам.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|			ПО (КлючиАналитикиУчетаПоПартнерам.Организация = Организации.Ссылка)
	|	ГДЕ
	|		Расчеты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Организации.Ссылка В(&Организации)
	|		И Расчеты.ДокументРегистратор ССЫЛКА Документ.РасчетКурсовыхРазниц
	|) КАК ВложенныйЗапрос
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Дата
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.ПредыдущаяПереоценка) = -1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Регистраторы расчетов за указанные период, у которых есть движений курсовых разниц
	// при условии, что есть даты отсутствия курсовых разниц
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами,
	|	РасчетыПоСрокам.Регистратор КАК РегистраторРасчетов,
	|	КОЛИЧЕСТВО(Переоценка.Дата) КАК ДатыОтсутствияКурсовыхРазниц
	|ПОМЕСТИТЬ втРегистраторыРасчетов
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокам
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиАналитики
	|	ПО РасчетыПоСрокам.АналитикаУчетаПоПартнерам = КлючиАналитики.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ втПредыдущаяПереоценка КАК Переоценка
	|	ПО ИСТИНА
	|ГДЕ
	|	РасчетыПоСрокам.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И КлючиАналитики.Организация В (&Организации)
	|	И ТИПЗНАЧЕНИЯ(РасчетыПоСрокам.ДокументРегистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоСрокам.Регистратор
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(Переоценка.Дата) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Все движения регистраторов расчета, кроме движений курсовых разниц в дни отсутствия
	|ВЫБРАТЬ
	|	&ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами,
	|	*
	|ПОМЕСТИТЬ втДвиженияРегистраторов
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСПоСрокам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втРегистраторыРасчетов КАК РегистраторыРасчетов
	|		ПО РасчетыСПоСрокам.Регистратор = РегистраторыРасчетов.РегистраторРасчетов
	|ГДЕ
	|	НЕ (ТИПЗНАЧЕНИЯ(РасчетыСПоСрокам.ДокументРегистратор) = ТИП(Документ.РасчетКурсовыхРазниц)
	|		И НАЧАЛОПЕРИОДА(РасчетыСПоСрокам.Период, ДЕНЬ) В (ВЫБРАТЬ Дата ИЗ втПредыдущаяПереоценка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Регистраторы расчетов которые остались без движений
	|ВЫБРАТЬ
	|	РегистраторыРасчетов.ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами,
	|	РегистраторыРасчетов.РегистраторРасчетов КАК Регистратор
	|ИЗ
	|	втРегистраторыРасчетов КАК РегистраторыРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втДвиженияРегистраторов КАК ДвиженияРегистраторов
	|		ПО РегистраторыРасчетов.РегистраторРасчетов = ДвиженияРегистраторов.Регистратор
	|ГДЕ
	|	ДвиженияРегистраторов.Регистратор ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	втДвиженияРегистраторов КАК ДвиженияРегистраторов
	|
	|ИТОГИ
	|	МАКСИМУМ(ЭтоРасчетыСКлиентами)
	|ПО
	|	Регистратор";
	#КонецОбласти
	
	Если НЕ Параметры.ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
				Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.ПолноеИмя(),
				Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДниПереоценки", Параметры.ДниПереоценки);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	Запрос.УстановитьПараметр("Организации", МассивОрганизаций);
	Запрос.УстановитьПараметр("ЭтоРасчетыСКлиентами", Параметры.ЭтоРасчетыСКлиентами);
	РезультатПакета = Запрос.ВыполнитьПакет();
	ПоследнийПакет = РезультатПакета.ВГраница();
	
	Выборка = РезультатПакета[ПоследнийПакет].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВсегоОбработано = 0;
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоРасчетыСКлиентами Тогда
			НаборЗаписей = РегистрыНакопления.РасчетыСКлиентамиПоСрокам.СоздатьНаборЗаписей();
		Иначе
			НаборЗаписей = РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.СоздатьНаборЗаписей();
		КонецЕсли;
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		Движения = Выборка.Выбрать();
		Пока Движения.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Движения);
		КонецЦикла;
		НаборЗаписей.Записать();
 		ВсегоОбработано = ВсегоОбработано + 1;
	КонецЦикла;
	
	// Регистраторы которые остались без движений
	Выборка = РезультатПакета[ПоследнийПакет-1].Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоРасчетыСКлиентами Тогда
			НаборЗаписей = РегистрыНакопления.РасчетыСКлиентамиПоСрокам.СоздатьНаборЗаписей();
		Иначе
			НаборЗаписей = РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.СоздатьНаборЗаписей();
		КонецЕсли;
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		НаборЗаписей.Записать();
		ВсегоОбработано = ВсегоОбработано + 1;
	КонецЦикла;
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, ВсегоОбработано);
	
КонецПроцедуры

Функция ДниПереоценки(МассивОрганизаций, НачалоПериода, КонецПериода, Параметры)
	
	#Область ТекстЗапроса
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Валюта КАК Валюта,
	|	НАЧАЛОПЕРИОДА(Расчеты.Период, ДЕНЬ) КАК Период
	|ПОМЕСТИТЬ втОборотыВалюты
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиАналитикиУчетаПоПартнерам
	|		ПО Расчеты.АналитикаУчетаПоПартнерам = КлючиАналитикиУчетаПоПартнерам.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО КлючиАналитикиУчетаПоПартнерам.Организация = Организации.Ссылка
	|ГДЕ
	|	Расчеты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Организации.Ссылка В (&Организации)
	|	И (Расчеты.Валюта <> &ВалютаУправленческогоУчета ИЛИ Расчеты.Валюта <> Организации.ВалютаРегламентированногоУчета)
	|	И Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Расчеты.Долг <> 0
	|;";
	Если НЕ Параметры.ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
				Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.ПолноеИмя(),
				Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втОборотыВалюты.Период КАК Дата
	|ИЗ
	|	втОборотыВалюты КАК втОборотыВалюты
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КурсыВалют.Период
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|ГДЕ
	|	КурсыВалют.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И &ПереоценкаПоДням
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(&КонецПериода, ДЕНЬ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата";
	#КонецОбласти
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоПериода));
	Запрос.УстановитьПараметр("КонецПериода",  КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("Организации",   МассивОрганизаций);
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",  Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ПереоценкаПоДням",  Параметры.ПереоценкаПоДням);
	
	Выборка = Запрос.Выполнить().Выгрузить();
	
	Возврат Выборка;
	
КонецФункции

Функция РегистраторыКурсовыхРазниц(Параметры, РегистраторыРасчетов, КоличествоЗаписей)
	
	Если КоличествоЗаписей = 0 Тогда
		КоличествоРегистраторов = 0;
	Иначе
		КоличествоРегистраторов = Цел(КоличествоЗаписей/РазмерПорцииЗаписи()) + 1;
	КонецЕсли;
	
	Отбор = Новый Структура("ОбъектРасчетов,АналитикаУчетаПоПартнерам,ВалютаРасчетов,ЭтоРасчетыСКлиентами");
	ЗаполнитьЗначенияСвойств(Отбор, Параметры);
	
	ТипРасчетов = ?(Параметры.ЭтоРасчетыСКлиентами, Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом, Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком);
	ТаблицаДокументов = РегистраторыРасчетов.Скопировать(Отбор); // см.СвободныеРегистраторыРасчетов
	
	Пока ТаблицаДокументов.Количество() < КоличествоРегистраторов Цикл
		ДокументОбъект = Документы.РегистраторРасчетов.СоздатьДокумент();
		ДокументОбъект.Валюта = Параметры.ВалютаРасчетов;
		ДокументОбъект.АналитикаУчетаПоПартнерам = Параметры.АналитикаУчетаПоПартнерам;
		ДокументОбъект.Организация = ?(Параметры.Организация = Неопределено,
										ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.АналитикаУчетаПоПартнерам, "Организация"),
										Параметры.Организация);
		ДокументОбъект.ОбъектРасчетов = Параметры.ОбъектРасчетов;
		ДокументОбъект.ТипРасчетов = ТипРасчетов;
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		НовСтр = ТаблицаДокументов.Добавить();
		НовСтр.Ссылка = ДокументОбъект.Ссылка;
	КонецЦикла;
	
	Возврат ТаблицаДокументов;
	
КонецФункции

//++ НЕ УТ

//++ Локализация
Процедура ЗаполнитьДвиженияНУзаПериод(Организация, Период, ЭтоРасчетыСКлиентами, ДатаНачалаПересчета = Неопределено)
	
	НачалоВступленияВСилу67ФЗ = РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.НачалоВступленияВСилу67ФЗ();
	ИспользоватьРеглУчет = ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет");
	Если НачалоМесяца(Период) < НачалоВступленияВСилу67ФЗ ИЛИ НЕ ИспользоватьРеглУчет Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(Организация) = Тип("Массив") Тогда
		МассивОрганизаций = Организация;
		Если МассивОрганизаций.Количество() = 1 Тогда
			НачалоПрименения67ФЗПоДоходам = РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.НачалоПрименения67ФЗПоДоходам(МассивОрганизаций[0]);
		Иначе
			НачалоПрименения67ФЗПоДоходам = НачалоВступленияВСилу67ФЗ;
		КонецЕсли;
	Иначе
		МассивОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
		НачалоПрименения67ФЗПоДоходам = РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.НачалоПрименения67ФЗПоДоходам(Организация);
	КонецЕсли;

	НачалоПериода = НачалоМесяца(Период);
	Если ДатаНачалаПересчета <> Неопределено Тогда
		НачалоПериода = НачалоМесяца(ДатаНачалаПересчета);
	ИначеЕсли НачалоМесяца(Период) = НачалоПрименения67ФЗПоДоходам Тогда //это месяц перехода
		НачалоПериода = НачалоГода(НачалоПрименения67ФЗПоДоходам);
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	#Область ТекстЗапроса
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	АналитикиУчетаПоПартнерам.Ссылка КАК Ссылка,
	|	АналитикиУчетаПоПартнерам.Организация КАК Организация,
	|	АналитикиУчетаПоПартнерам.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
	|ПОМЕСТИТЬ ВтАналитикиУчетаПоПартнерам
	|ИЗ
	|	Справочник.КлючиАналитикиУчетаПоПартнерам КАК АналитикиУчетаПоПартнерам
	|ГДЕ
	|	АналитикиУчетаПоПартнерам.Организация В(&МассивОрганизаций)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(РасчетыКР.Дата, МЕСЯЦ) КАК Период,
	|	РасчетыКР.Ссылка КАК Ссылка,
	|	РасчетыКР.Организация КАК Организация,
	|	РасчетыКР.ПереоценкаПоДням КАК ПереоценкаПоДням,
	|	РасчетыКР.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаРасчетовСКлиентами) КАК ЭтоРасчетыСКлиентами
	|ПОМЕСТИТЬ втРегистраторыКурсовыхРазниц
	|ИЗ
	|	Документ.РасчетКурсовыхРазниц КАК РасчетыКР
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитикиУчетаПоПартнерам КАК Аналитика
	|		ПО РасчетыКР.Организация = Аналитика.Организация
	|ГДЕ
	|	РасчетыКР.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И (&ЭтоРасчетыСКлиентами И РасчетыКР.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаРасчетовСКлиентами)
	|		ИЛИ НЕ &ЭтоРасчетыСКлиентами И РасчетыКР.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаРасчетовСПоставщиками))
	|	И РасчетыКР.Проведен
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обороты.Месяц КАК Месяц,
	|	Обороты.Период КАК Период,
	|	Обороты.ВидДвижения КАК ВидДвижения,
	|	Обороты.ДокументРегистратор КАК ДокументРегистратор,
	|	Обороты.Организация КАК Организация,
	|	Обороты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Обороты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Обороты.Валюта КАК Валюта,
	|	Обороты.РасчетныйДокумент КАК РасчетныйДокумент,
	|	СУММА(Обороты.Долг) КАК Долг,
	|	СУММА(Обороты.ДолгРегл) КАК ДолгРегл,
	|	Обороты.ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами
	|
	|ПОМЕСТИТЬ втОстаткиИОбороты
	|ИЗ
	|	(ВЫБРАТЬ
	|		&НачалоВступленияВСилу67ФЗ КАК Месяц,
	|		&ДатаВводаОстатковНУ КАК Период,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|		НЕОПРЕДЕЛЕНО КАК ДокументРегистратор,
	|		Аналитика.Организация КАК Организация,
	|		Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|		Расчеты.Валюта КАК Валюта,
	|		Расчеты.РасчетныйДокумент КАК РасчетныйДокумент,
	|		Расчеты.ДолгОстаток КАК Долг,
	|		Расчеты.ДолгРеглОстаток КАК ДолгРегл,
	|		&ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки(
	|			&НачалоВступленияВСилу67ФЗ,
	|			(АналитикаУчетаПоПартнерам) В 
	|				(ВЫБРАТЬ 
	|					Аналитика.Ссылка
	|				ИЗ
	|					ВтАналитикиУчетаПоПартнерам КАК Аналитика)
	|			И НЕ (Валюта) В (
	|				ВЫБРАТЬ
	|					Аналитика.ВалютаРегламентированногоУчета
	|				ИЗ
	|					ВтАналитикиУчетаПоПартнерам КАК Аналитика
	|				)
	|		) КАК Расчеты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитикиУчетаПоПартнерам КАК Аналитика
	|			ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитика.Ссылка
	|	ГДЕ
	|		&НачалоПериода = &НачалоВступленияВСилу67ФЗ
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&НачалоВступленияВСилу67ФЗ КАК Месяц,
	|		&ДатаВводаОстатковНУ КАК Период,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|		НЕОПРЕДЕЛЕНО КАК ДокументРегистратор,
	|		Аналитика.Организация КАК Организация,
	|		Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|		Расчеты.Валюта КАК Валюта,
	|		Расчеты.РасчетныйДокумент КАК РасчетныйДокумент,
	|		0 КАК Долг,
	|		-Расчеты.СуммаНУОстаток КАК ДолгРегл,
	|		&ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиНУ.Остатки(
	|			&НачалоВступленияВСилу67ФЗ,
	|			(АналитикаУчетаПоПартнерам) В 
	|				(ВЫБРАТЬ 
	|					Аналитика.Ссылка
	|				ИЗ
	|					ВтАналитикиУчетаПоПартнерам КАК Аналитика)
	|			И НЕ (Валюта) В (
	|				ВЫБРАТЬ 
	|					Аналитика.ВалютаРегламентированногоУчета
	|				ИЗ
	|					ВтАналитикиУчетаПоПартнерам КАК Аналитика
	|				)
	|		) КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитикиУчетаПоПартнерам КАК Аналитика
	|			ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитика.Ссылка
	|	ГДЕ
	|		&НачалоПериода = &НачалоВступленияВСилу67ФЗ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(Расчеты.Период, МЕСЯЦ) КАК Месяц,
	|		Расчеты.Период КАК Период,
	|		Расчеты.ВидДвижения КАК ВидДвижения,
	|		Расчеты.ДокументРегистратор КАК ДокументРегистратор,
	|		Аналитика.Организация КАК Организация,
	|		Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|		Расчеты.Валюта КАК Валюта,
	|		Расчеты.РасчетныйДокумент КАК РасчетныйДокумент,
	|		Расчеты.Долг КАК Долг,
	|		Расчеты.ДолгРегл КАК ДолгРегл,
	|		&ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитикиУчетаПоПартнерам КАК Аналитика
	|			ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитика.Ссылка
	|	ГДЕ
	|		Расчеты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И Расчеты.Валюта <> Аналитика.ВалютаРегламентированногоУчета
	|		И ТИПЗНАЧЕНИЯ(Расчеты.ДокументРегистратор) <> ТИП(Документ.РасчетКурсовыхРазниц)
	|		И ТИПЗНАЧЕНИЯ(Расчеты.ДокументРегистратор) <> ТИП(Документ.ВводОстатковВзаиморасчетов)
	|		И ТИПЗНАЧЕНИЯ(Расчеты.ДокументРегистратор) <> ТИП(Документ.ВводОстатков)
	|		И ТИПЗНАЧЕНИЯ(Расчеты.ДокументРегистратор) <> ТИП(Документ.КорректировкаГрафикаВзаиморасчетов)
	|
	|) КАК Обороты
	|
	|СГРУППИРОВАТЬ ПО
	|	Обороты.Период,
	|	Обороты.Месяц,
	|	Обороты.ВидДвижения,
	|	Обороты.ДокументРегистратор,
	|	Обороты.Организация,
	|	Обороты.АналитикаУчетаПоПартнерам,
	|	Обороты.ОбъектРасчетов,
	|	Обороты.Валюта,
	|	Обороты.РасчетныйДокумент,
	|	Обороты.ЭтоРасчетыСКлиентами
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор,
	|	&ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиНУ КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтАналитикиУчетаПоПартнерам КАК Аналитика
	|		ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитика.Ссылка
	|ГДЕ
	|	Расчеты.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) <> ТИП(Документ.ВводОстатковВзаиморасчетов)
	|	И ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) <> ТИП(Документ.ВводОстатков)
	|	И ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) <> ТИП(Документ.РегламентнаяОперация)
	|	И ТИПЗНАЧЕНИЯ(Расчеты.ДокументРегистратор) <> ТИП(Документ.КорректировкаГрафикаВзаиморасчетов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистраторыКурсовыхРазниц.Период КАК Месяц,
	|	ОстаткиИОбороты.Период КАК Период,
	|	ОстаткиИОбороты.ВидДвижения КАК ВидДвижения,
	|	ЕСТЬNULL(ОстаткиИОбороты.ДокументРегистратор, НЕОПРЕДЕЛЕНО) КАК ДокументРегистратор,
	|	РегистраторыКурсовыхРазниц.Организация КАК Организация,
	|	РегистраторыКурсовыхРазниц.Ссылка КАК Регистратор,
	|	РегистраторыКурсовыхРазниц.ПереоценкаПоДням КАК ПереоценкаПоДням,
	|	ОстаткиИОбороты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ОстаткиИОбороты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ОстаткиИОбороты.Валюта КАК Валюта,
	|	ОстаткиИОбороты.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ЕСТЬNULL(ОстаткиИОбороты.Долг, 0)КАК Долг,
	|	ЕСТЬNULL(ОстаткиИОбороты.ДолгРегл, 0) КАК ДолгРегл,
	|	РегистраторыКурсовыхРазниц.ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами
	|ИЗ
	|	втРегистраторыКурсовыхРазниц КАК РегистраторыКурсовыхРазниц
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиИОбороты КАК ОстаткиИОбороты
	|		ПО ОстаткиИОбороты.Месяц = РегистраторыКурсовыхРазниц.Период
	|			И ОстаткиИОбороты.Организация = РегистраторыКурсовыхРазниц.Организация
	|			И ОстаткиИОбороты.ЭтоРасчетыСКлиентами = РегистраторыКурсовыхРазниц.ЭтоРасчетыСКлиентами
	|
	|УПОРЯДОЧИТЬ ПО
	|	Месяц
	|
	|ИТОГИ
	|	МАКСИМУМ(Организация),
	|	МАКСИМУМ(ЭтоРасчетыСКлиентами),
	|	МАКСИМУМ(ПереоценкаПоДням)
	|ПО
	|	Месяц,
	|	Регистратор";
	#КонецОбласти
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
					Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.ПолноеИмя(),
					Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			Метаданные.РегистрыНакопления.РасчетыСКлиентамиНУ.ПолноеИмя(),
			Метаданные.РегистрыНакопления.РасчетыСПоставщикамиНУ.ПолноеИмя());
	КонецЕсли;

	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("НачалоВступленияВСилу67ФЗ", НачалоВступленияВСилу67ФЗ);
	Запрос.УстановитьПараметр("ДатаВводаОстатковНУ", НачалоВступленияВСилу67ФЗ-1);
	Запрос.УстановитьПараметр("ЭтоРасчетыСКлиентами", ЭтоРасчетыСКлиентами);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Период));

	ПереоценкаПоДням = Константы.ПереоцениватьВалютныеСредстваПоДням.Получить();
	Если ЭтоРасчетыСКлиентами Тогда
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереоценкаРасчетовСКлиентами;
	Иначе
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереоценкаРасчетовСПоставщиками;
	КонецЕсли;

	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ПоследнийИндекс = РезультатЗапроса.ВГраница();
	Выборка = РезультатЗапроса[ПоследнийИндекс-1].Выбрать();
	Пока Выборка.Следующий() Цикл
		ОчиститьРегистрыНУ(Выборка.Регистратор, Выборка.ЭтоРасчетыСКлиентами);
	КонецЦикла;

	ПараметрыПереоценки = ПараметрыПереоценки(ЭтоРасчетыСКлиентами);
	ПараметрыПереоценки.ПерезаполнениеРегистровНУ = Истина;

	Месяцы = РезультатЗапроса[ПоследнийИндекс].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Месяцы.Следующий() Цикл
		Регистраторы = Месяцы.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Регистраторы.Следующий() Цикл
			Если Регистраторы.ЭтоРасчетыСКлиентами Тогда
				НаборЗаписейНУ = РегистрыНакопления.РасчетыСКлиентамиНУ.СоздатьНаборЗаписей();
			Иначе
				НаборЗаписейНУ = РегистрыНакопления.РасчетыСПоставщикамиНУ.СоздатьНаборЗаписей();
			КонецЕсли;
			ДокументРасчетаКР = Регистраторы.Регистратор;
			Если ДокументРасчетаКР = Неопределено Тогда
				ОбъектДокумента = Документы.РасчетКурсовыхРазниц.СоздатьДокумент();
				ОбъектДокумента.Дата = КонецМесяца(Месяцы.Месяц);
				ОбъектДокумента.Организация = Регистраторы.Организация;
				ОбъектДокумента.ХозяйственнаяОперация = ХозяйственнаяОперация;
				ОбъектДокумента.ПереоценкаПоДням = ПереоценкаПоДням;
				ОбъектДокумента.Проведен = Истина;
				ОбъектДокумента.Записать();
				ДокументРасчетаКР = ОбъектДокумента.Ссылка;
			КонецЕсли;
			НаборЗаписейНУ.Отбор.Регистратор.Установить(ДокументРасчетаКР);
			Движения = Регистраторы.Выбрать();
			Пока Движения.Следующий() Цикл
				Если Движения.ДолгРегл > 0 Тогда
					НовоеДвижение = НаборЗаписейНУ.Добавить();
					ЗаполнитьЗначенияСвойств(НовоеДвижение, Движения);
					НовоеДвижение.СуммаНУ = Движения.ДолгРегл;
					Если НовоеДвижение.ДокументРегистратор = Неопределено Тогда
						НовоеДвижение.ДокументРегистратор = ДокументРасчетаКР;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			НаборЗаписейНУ.Записать();
			Если НачалоМесяца(Месяцы.Месяц) <> НачалоМесяца(Период) Тогда
				ПараметрыПереоценки.ПереоценкаПоДням = Регистраторы.ПереоценкаПоДням;
				ВыполнитьПереоценкуЗаПериод(МассивОрганизаций, Месяцы.Месяц, Месяцы.Месяц, ПараметрыПереоценки);
			КонецЕсли;

		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПерезаполнитьОборотыНУ(Организация, Период, ЭтоРасчетыСКлиентами) Экспорт

	НачатьТранзакцию();
	Попытка
		
		НачалоВступленияВСилу67ФЗ = РегистрыСведений.НастройкиУчетаНалогаНаПрибыль.НачалоВступленияВСилу67ФЗ();
		ЗаполнитьДвиженияНУзаПериод(Организация, Период, ЭтоРасчетыСКлиентами, НачалоВступленияВСилу67ФЗ);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
		
	КонецПопытки;
	
КонецПроцедуры

Функция АналитикаКурсовыхНУ(Данные = Неопределено)
	
	Аналитика = Новый Структура;
	Аналитика.Вставить("Период");
	Аналитика.Вставить("АналитикаУчетаПоПартнерам");
	Аналитика.Вставить("ОбъектРасчетов");
	Аналитика.Вставить("РасчетныйДокумент");
	Аналитика.Вставить("Валюта");
	Аналитика.Вставить("ТипСуммы");
	Аналитика.Вставить("Счет");
	Аналитика.Вставить("Организация");
	Аналитика.Вставить("Контрагент");
	Аналитика.Вставить("Договор");
	Аналитика.Вставить("ДокументОплаты");
	Аналитика.Вставить("ДокументРегистратор");
	Аналитика.Вставить("НачалоПрименения67ФЗПоРасходам");
	
	Если Данные <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Аналитика, Данные);
		Аналитика.Валюта = Данные.ВалютаРасчетов;
		Аналитика.Счет = Данные.СчетУчета;
	КонецЕсли;
	
	Возврат Аналитика;
	
КонецФункции

Процедура ДобавитьОборотНУ(НаборЗаписейНУ, Аналитика, ИмяРесурса, СуммаНУ)
	
	ЗаписьРазрешена = ИмяРесурса <> "ОтложенныйРасход" 
						ИЛИ ИмяРесурса = "ОтложенныйРасход" 
							И Аналитика.Период >= Аналитика.НачалоПрименения67ФЗПоРасходам;
	
	Если Окр(СуммаНУ,2) <> 0 И ЗаписьРазрешена Тогда
		НовСтр = НаборЗаписейНУ.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр, Аналитика);
		НовСтр[ИмяРесурса] = СуммаНУ;
		Если НовСтр[ИмяРесурса] < 0 Тогда
			НовСтр[ИмяРесурса] = -НовСтр[ИмяРесурса];
			НовСтр.ВидДвижения = ВидДвиженияНакопления.Расход;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция РассчитатьСуммыНУ(Период, КурсНаДату, ЗаписьНУ, Оплата = Неопределено)
	
	Если Оплата = Неопределено Тогда
		Оплата = Новый Структура("ДолгРасход,ДолгРеглРасход");
		ЗаполнитьЗначенияСвойств(Оплата, ЗаписьНУ);
	КонецЕсли;

	НачальныйОстатокДолга = ЗаписьНУ.ДолгОстаток + Оплата.ДолгРасход;
	НачальныйОстатокДолгаНУ = ЗаписьНУ.СуммаНУОстаток + Оплата.ДолгРеглРасход;//в регистр еще не записана курсовая разница

	КурсоваяРазницаОплаты = 0;
	КурсоваяРазницаОстатка = 0;
	КурсоваяРазницаНУ = 0;
	ПризнаниеДохода = 0;
	ПризнаниеРасхода = 0;
	Если НачальныйОстатокДолга <> 0 Тогда
		КурсОстаткаНУ = НачальныйОстатокДолгаНУ/НачальныйОстатокДолга;
		Если Оплата.ДолгРасход <> 0 Тогда
			КурсОплаты = Оплата.ДолгРеглРасход/Оплата.ДолгРасход;
			КурсоваяРазницаОплаты = Окр(-Оплата.ДолгРасход * (КурсОстаткаНУ - КурсОплаты), 2);
		КонецЕсли;
		КурсоваяРазницаОстатка = Окр(Окр(КурсНаДату * ЗаписьНУ.ДолгОстаток,2) - (НачальныйОстатокДолгаНУ +(-Оплата.ДолгРасход * КурсОстаткаНУ)), 2);

		КурсоваяРазницаНУ = КурсоваяРазницаОплаты + ?(Период = КонецМесяца(Период), КурсоваяРазницаОстатка, 0);
		ПризнаниеДохода = Окр(-Оплата.ДолгРасход / НачальныйОстатокДолга * ЗаписьНУ.ОтложенныйДоходОстаток, 2);
		ПризнаниеРасхода = Окр(-Оплата.ДолгРасход / НачальныйОстатокДолга * ЗаписьНУ.ОтложенныйРасходОстаток, 2);
	КонецЕсли;

	ОтложенныйДоходОплата = 0; // реализованная
	ОтложенныйРасходОплата = 0;
	ОтложенныйДоходОстаток = 0; // не реализованная
	ОтложенныйРасходОстаток = 0;
	Если ЗаписьНУ.ДолгОстаток = 0 И Период = КонецМесяца(Период) Тогда
		КурсоваяРазницаНУ = -ЗаписьНУ.СуммаНУОстаток;
		КурсоваяРазницаОплаты = -ЗаписьНУ.СуммаНУОстаток;
		ОтложенныйДоходОплата = -ЗаписьНУ.ОтложенныйДоходОстаток;
		ОтложенныйРасходОплата = -ЗаписьНУ.ОтложенныйРасходОстаток;
	ИначеЕсли ЗаписьНУ.ЭтоРасчетыСКлиентами Тогда
		ОтложенныйДоходОплата = ПризнаниеДохода;
		ОтложенныйРасходОплата = ПризнаниеРасхода;
		ОтложенныйДоходОстаток = ?(Период = КонецМесяца(Период) И КурсоваяРазницаОстатка > 0, КурсоваяРазницаОстатка, 0);
		ОтложенныйРасходОстаток = ?(Период = КонецМесяца(Период) И КурсоваяРазницаОстатка < 0, -КурсоваяРазницаОстатка, 0);
	Иначе // это поставщик
		ОтложенныйДоходОплата = ПризнаниеДохода;
		ОтложенныйРасходОплата = ПризнаниеРасхода;
		ОтложенныйДоходОстаток = ?(Период = КонецМесяца(Период) И КурсоваяРазницаОстатка < 0, -КурсоваяРазницаОстатка, 0);
		ОтложенныйРасходОстаток = ?(Период = КонецМесяца(Период) И КурсоваяРазницаОстатка > 0, КурсоваяРазницаОстатка, 0);
	КонецЕсли;

	Результат = Новый Структура;
	Результат.Вставить("КурсоваяРазницаОплаты", КурсоваяРазницаОплаты);
	Результат.Вставить("КурсоваяРазницаОстатка", КурсоваяРазницаОстатка);
	Результат.Вставить("КурсоваяРазницаНУ", КурсоваяРазницаНУ);
	Результат.Вставить("ПризнаниеДохода", ПризнаниеДохода);
	Результат.Вставить("ПризнаниеРасхода", ПризнаниеРасхода);
	Результат.Вставить("ОтложенныйДоходОплата", ОтложенныйДоходОплата);
	Результат.Вставить("ОтложенныйРасходОплата", ОтложенныйРасходОплата);
	Результат.Вставить("ОтложенныйДоходОстаток", ОтложенныйДоходОстаток);
	Результат.Вставить("ОтложенныйРасходОстаток", ОтложенныйРасходОстаток);

	Возврат Результат;
	
КонецФункции

Процедура ОчиститьРегистрыНУ(Регистратор, ЭтоРасчетыСКлиентами)
	
	Если ЭтоРасчетыСКлиентами Тогда
		НаборЗаписейНУ = РегистрыНакопления.РасчетыСКлиентамиНУ.СоздатьНаборЗаписей();
	Иначе
		НаборЗаписейНУ = РегистрыНакопления.РасчетыСПоставщикамиНУ.СоздатьНаборЗаписей();
	КонецЕсли;
	НаборЗаписейНУ.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписейНУ.Записать();
	
	Если ТипЗнч(Регистратор) <> Тип("ДокументСсылка.Сторно") Тогда
		РасшифровкаРасчета = РегистрыСведений.РасчетПереоценкиВалютныхСредствНУ.СоздатьНаборЗаписей();
		РасшифровкаРасчета.Отбор.Регистратор.Установить(Регистратор);
		РасшифровкаРасчета.Записать();
	КонецЕсли;
	
КонецПроцедуры
//-- Локализация

//-- НЕ УТ
#КонецОбласти

Функция СуммаСписания(Курс, СуммаВВалюте, СуммаВзаиморасчетов, СуммаКСписанию) Экспорт
	
	Если СуммаКСписанию = СуммаВзаиморасчетов Тогда
		Возврат СуммаВВалюте;
	ИначеЕсли СуммаВВалюте = 0 Или СуммаВзаиморасчетов = 0 Тогда
		Возврат 0;
	Иначе
		Если Курс = 0 Тогда
			Если СуммаВВалюте > СуммаВзаиморасчетов Тогда
				Возврат Окр(СуммаВВалюте / СуммаВзаиморасчетов * СуммаКСписанию, 2);
			Иначе
				Возврат Окр(СуммаКСписанию / (СуммаВзаиморасчетов/СуммаВВалюте), 2);
			КонецЕсли;
		Иначе
			Возврат Окр(Курс * СуммаКСписанию,2);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ИмяМетодаОтложенногоРаспределенияВзаиморасчетов()
	Возврат "ОперативныеВзаиморасчетыСервер.ВыполнитьФоновоеРаспределениеРасчетов";
КонецФункции

Процедура СоздатьТаблицуДатЗаПериод(МенеджерВременныхТаблиц, ИмяВременнойТаблицы, ДатаНачала, ДатаОкончания)
	
	Если НЕ ЗначениеЗаполнено(ДатаНачала) ИЛИ ДатаНачала<Дата(1980,1,1) Тогда
		ВызватьИсключение(НСтр("ru = 'Не заполнена дата начала границы курсов.';
								|en = 'Start date of the rate boundary is not filled.'"));
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ 0 КАК Шаг
	|ПОМЕСТИТЬ ТаблицаИсходныхЦифр
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ 1
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ 2
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ 3
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ 4
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ 5
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ 6
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ 7
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ 8
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ 9
	|ИНДЕКСИРОВАТЬ ПО
	|	Шаг
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИсходныхЦифр1.Шаг * 1000 + ТаблицаИсходныхЦифр2.Шаг * 100 + ТаблицаИсходныхЦифр3.Шаг * 10 + ТаблицаИсходныхЦифр4.Шаг КАК Счетчик
	|ПОМЕСТИТЬ ТаблицаСчетчик
	|ИЗ
	|	ТаблицаИсходныхЦифр КАК ТаблицаИсходныхЦифр1,
	|	ТаблицаИсходныхЦифр КАК ТаблицаИсходныхЦифр2,
	|	ТаблицаИсходныхЦифр КАК ТаблицаИсходныхЦифр3,
	|	ТаблицаИсходныхЦифр КАК ТаблицаИсходныхЦифр4
	|ГДЕ
	|	(ТаблицаИсходныхЦифр1.Шаг * 1000 + ТаблицаИсходныхЦифр2.Шаг * 100 + ТаблицаИсходныхЦифр3.Шаг * 10 + ТаблицаИсходныхЦифр4.Шаг) <= РАЗНОСТЬДАТ(&ДатаНачала,&ДатаОкончания,ДЕНЬ)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(&ДатаНачала, ДЕНЬ, ТаблицаСчетчик.Счетчик) КАК Дата
	|ПОМЕСТИТЬ ИмяВТ
	|ИЗ
	|	ТаблицаСчетчик КАК ТаблицаСчетчик
	|ИНДЕКСИРОВАТЬ ПО
	|	Дата
	|";
	
	Запрос.УстановитьПараметр("ДатаНачала",НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ДатаОкончания",НачалоДня(ДатаОкончания));
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"ИмяВТ",ИмяВременнойТаблицы);
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ТаблицаПараметровРаспределения(ПараметрыРасчета = Неопределено)
	
	Если Не ПараметрыРасчета = Неопределено И ТипЗнч(ПараметрыРасчета) = Тип("ТаблицаЗначений") Тогда
		Возврат ПараметрыРасчета;
	КонецЕсли;
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ТипРасчетов", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыРасчетовСПартнерами"));
	Таблица.Колонки.Добавить("АналитикаУчетаПоПартнерам", Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаПоПартнерам"));
	Таблица.Колонки.Добавить("ОбъектРасчетов", Новый ОписаниеТипов("СправочникСсылка.ОбъектыРасчетов"));
	Таблица.Колонки.Добавить("ВалютаРасчетов", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	Таблица.Колонки.Добавить("ЭтоРасчетыСКлиентами", Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("НачальноеЗаполнение", Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("Регистратор", Документы.ТипВсеСсылки());
	Таблица.Колонки.Добавить("ЗаписыватьИзменения", Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ФиксированныйКурсНакладной", Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ФиксированныйКурсРегл", ОбщегоНазначения.ОписаниеТипаЧисло(10,4,ДопустимыйЗнак.Неотрицательный));
	Таблица.Колонки.Добавить("ФиксированныйКурсУпр", ОбщегоНазначения.ОписаниеТипаЧисло(10,4,ДопустимыйЗнак.Неотрицательный));
	Таблица.Колонки.Добавить("ГрафикВДоговоре", Новый ОписаниеТипов("Булево"));
	Таблица.Колонки.Добавить("ПорядокОперации", ОбщегоНазначения.ОписаниеТипаСтрока(50));
	Таблица.Колонки.Добавить("ДатаНачалаПересчета", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	Таблица.Колонки.Добавить("ПериодКонтроляИзменений", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	Таблица.Колонки.Добавить("ПерезаполнениеРегистровВзаиморасчетов", Новый ОписаниеТипов("Булево"));
	
	Если НЕ ПараметрыРасчета = Неопределено И ТипЗнч(ПараметрыРасчета) = Тип("Структура") Тогда
		СтрокаТаблицыПараметров = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыПараметров, ПараметрыРасчета); 
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

// Текст запроса получения количества записей в оперативных регистрах взаиморасчетов.
// 
// Параметры:
//  ИмяТаблицыДляОбработки - Строка - Имя временной таблицы:
// * АналитикаУчетаПоПартнерам - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам
// * ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов
// * Валюта - СправочникСсылка.Валюты
// * ТипРасчетов - ПеречислениеСсылка.ТипыРасчетовСПартнерами 
// 
// Возвращаемое значение:
//  Строка -  Текст запроса
//
Функция ТекстЗапросаКоличествоЗаписейВОперативныхРегистрахВзаиморасчетов(ИмяТаблицыДляОбработки) Экспорт

		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ВТДляОбработки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВТДляОбработки.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ВТДляОбработки.Валюта КАК Валюта,
		|	СУММА(РегистрКоличествоЗаписей.КоличествоЗаписей) КАК КоличествоЗаписей
		|ИЗ
		|	ВтДанныеДляОбработки КАК ВТДляОбработки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КоличествоЗаписейРегистраторовРасчетов КАК РегистрКоличествоЗаписей
		|		ПО ВТДляОбработки.АналитикаУчетаПоПартнерам = РегистрКоличествоЗаписей.АналитикаУчетаПоПартнерам
		|		И ВТДляОбработки.ОбъектРасчетов = РегистрКоличествоЗаписей.ОбъектРасчетов
		|		И ВТДляОбработки.Валюта = РегистрКоличествоЗаписей.Валюта
		|СГРУППИРОВАТЬ ПО
		|	ВТДляОбработки.АналитикаУчетаПоПартнерам,
		|	ВТДляОбработки.ОбъектРасчетов,
		|	ВТДляОбработки.Валюта";

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВтДанныеДляОбработки КАК ВТДляОбработки", ИмяТаблицыДляОбработки + " КАК ВТДляОбработки");
	
	Возврат ТекстЗапроса;

КонецФункции

// Сформировать порции обработки данных регистров расчета.
// 
// Параметры:
//  ДанныеКОбработке - ТаблицаЗначений - :
// * АналитикаУчетаПоПартнерам - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам - 
// * ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов - 
// * Валюта - СправочникСсылка.Валюты - 
//  РазмерПорцииОбработкиЗаписей - Число - Число записей в порции для обработки
// 
// Возвращаемое значение:
//  Число - Количество порций
//
Функция СформироватьПорцииОбработкиДанныхРегистровВзаиморасчетов(ДанныеКОбработке, РазмерПорцииОбработкиЗаписей = 1000) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Данные.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Данные.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Данные.ВалютаРасчетов КАК Валюта
	|ПОМЕСТИТЬ ВтДанныеДляОбработки
	|ИЗ
	|	&ДанныеКОбработке КАК Данные
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтДанныеДляОбработки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВтДанныеДляОбработки.Валюта КАК Валюта,
	|	ВтДанныеДляОбработки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВЫБОР
	|		КОГДА ОбъектыРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Договор)
	|			ТОГДА 100
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ВесовойКоэффициентОбработки
	|ИЗ
	|	ВтДанныеДляОбработки КАК ВтДанныеДляОбработки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ВтДанныеДляОбработки.ОбъектРасчетов = ОбъектыРасчетов.Ссылка";
	Запрос.УстановитьПараметр("ДанныеКОбработке", ДанныеКОбработке);
	ВесовыеКоэффициентыОбработкиЗаписей = Запрос.Выполнить().Выгрузить();
	ВесовыеКоэффициентыОбработкиЗаписей.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта");
	
	Если ДанныеКОбработке.Колонки.Найти("ПорядокОбработки") = Неопределено Тогда
		ДанныеКОбработке.Колонки.Добавить("ПорядокОбработки", ОбщегоНазначения.ОписаниеТипаЧисло(15, 0));
		ДанныеКОбработке.Индексы.Добавить("ПорядокОбработки");
	КонецЕсли;
	
	РазмерТекущейПорции = 0;
	ПорядокОбработки = 1;
	
	Для Каждого ЗаписьКОбработке Из ДанныеКОбработке Цикл

		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("АналитикаУчетаПоПартнерам", ЗаписьКОбработке.АналитикаУчетаПоПартнерам);
		СтруктураОтбора.Вставить("ОбъектРасчетов", ЗаписьКОбработке.ОбъектРасчетов);
		СтруктураОтбора.Вставить("Валюта", ЗаписьКОбработке.ВалютаРасчетов);

		ЗаписиПоОбъектуРасчетов = ВесовыеКоэффициентыОбработкиЗаписей.НайтиСтроки(СтруктураОтбора);
		ВесовойКоэффициентОбработки = ?(ЗаписиПоОбъектуРасчетов.Количество() > 0,
			ЗаписиПоОбъектуРасчетов[0].ВесовойКоэффициентОбработки, 0);

		Если Не РазмерТекущейПорции = 0 И (РазмерТекущейПорции + ВесовойКоэффициентОбработки) > РазмерПорцииОбработкиЗаписей Тогда

			ПорядокОбработки = ПорядокОбработки + 1;
			РазмерТекущейПорции = 0;

		КонецЕсли;

		ЗаписьКОбработке.ПорядокОбработки = ПорядокОбработки;
		РазмерТекущейПорции = РазмерТекущейПорции + ВесовойКоэффициентОбработки;

	КонецЦикла;
	
	Возврат ПорядокОбработки;

КонецФункции

Процедура ОтразитьСостоянияЗаказов(ОбработанныеДанные)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбработанныеДанные", ОбработанныеДанные);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ПОМЕСТИТЬ ВтОбработанныеДанные
	|ИЗ
	|	&ОбработанныеДанные КАК Таблица
	|
	|;
	|
	|ВЫБРАТЬ
	|	ОбъектыРасчетов.Объект КАК ОбработанныйДокумент
	|ИЗ
	|	ВтОбработанныеДанные КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ОбъектыРасчетов.Ссылка = Таблица.ОбъектРасчетов
	|ГДЕ
	|	ОбъектыРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Заказ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ 
	|	РасчетыСКлиентами.ПродажаПоЗаказу КАК ОбработанныйДокумент
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ОбъектыРасчетов.Объект = РасчетыСКлиентами.ПродажаПоЗаказу
	|		И НЕ ОбъектыРасчетов.ПометкаУдаления
	|ГДЕ
	|	(РасчетыСКлиентами.АналитикаУчетаПоПартнерам, РасчетыСКлиентами.ОбъектРасчетов)
	|	В (ВЫБРАТЬ Т.АналитикаУчетаПоПартнерам, Т.ОбъектРасчетов ИЗ ВтОбработанныеДанные КАК Т)
	|	И РасчетыСКлиентами.ПродажаПоЗаказу <> НЕОПРЕДЕЛЕНО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ 
	|	РасчетыСПоставщиками.ЗакупкаПоЗаказу КАК ОбработанныйДокумент
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ОбъектыРасчетов.Объект = РасчетыСПоставщиками.ЗакупкаПоЗаказу
	|		И НЕ ОбъектыРасчетов.ПометкаУдаления
	|ГДЕ
	|	(РасчетыСПоставщиками.АналитикаУчетаПоПартнерам, РасчетыСПоставщиками.ОбъектРасчетов)
	|	В (ВЫБРАТЬ Т.АналитикаУчетаПоПартнерам, Т.ОбъектРасчетов ИЗ ВтОбработанныеДанные КАК Т)
	|	И РасчетыСПоставщиками.ЗакупкаПоЗаказу <> НЕОПРЕДЕЛЕНО
	|";
	
	МассивДокументов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ОбработанныйДокумент");
	
	РегистрыКОтражению = СостоянияДокументов.РегистрыКОтражениюСостоянияЗаказов();
	РегистрыКОтражению.СостоянияЗаказовКлиентов    = Истина;
	РегистрыКОтражению.СостоянияЗаказовПоставщикам = Истина;
	
	СостоянияДокументов.ДобавитьЗаданияКОтражениюСостоянияЗаказов(Неопределено, МассивДокументов,, РегистрыКОтражению);
	
КонецПроцедуры

// Очищает оперативные регистры взаиморасчетов и удаляет служебные регистраторы.
// 
// Параметры:
//  ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов - Объект расчетов
//  АналитикаУчетаПоПартнерам - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам - Аналитика учета по партнерам
Процедура ОчиститьРегистрыВзаиморасчетовИУдалитьСлужебныеРегистраторы(ОбъектРасчетов = Неопределено, АналитикаУчетаПоПартнерам = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОбъектРасчетов", ОбъектРасчетов);
	Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", АналитикаУчетаПоПартнерам);
	Запрос.Текст = "ВЫБРАТЬ
	|	РегистраторРасчетов.Ссылка КАК Ссылка,
	|	РегистраторРасчетов.ТипРасчетов КАК ТипРасчетов
	|ИЗ
	|	Документ.РегистраторРасчетов КАК РегистраторРасчетов
	|ГДЕ
	|	РегистраторРасчетов.ОбъектРасчетов = &ОбъектРасчетов
	|	И РегистраторРасчетов.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам";
	
	Если ОбъектРасчетов = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РегистраторРасчетов.ОбъектРасчетов = &ОбъектРасчетов", "Истина");
	КонецЕсли;

	Если АналитикаУчетаПоПартнерам = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"РегистраторРасчетов.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам", "Истина");
	КонецЕсли;
	
	ТаблицаРегистраторРасчетов = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ТекущийДокумент Из ТаблицаРегистраторРасчетов Цикл

		Если ТекущийДокумент.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом Тогда
			ОчиститьРегистрВзаиморасчетовПоДокументу(ТекущийДокумент.Ссылка, "РасчетыСКлиентамиПоСрокам");
			ОчиститьРегистрВзаиморасчетовПоДокументу(ТекущийДокумент.Ссылка, "РасчетыСКлиентамиПланОплат");
			ОчиститьРегистрВзаиморасчетовПоДокументу(ТекущийДокумент.Ссылка, "РасчетыСКлиентамиПланОтгрузок");
		Иначе
			ОчиститьРегистрВзаиморасчетовПоДокументу(ТекущийДокумент.Ссылка, "РасчетыСПоставщикамиПоСрокам");
			ОчиститьРегистрВзаиморасчетовПоДокументу(ТекущийДокумент.Ссылка, "РасчетыСПоставщикамиПланОплат");
			ОчиститьРегистрВзаиморасчетовПоДокументу(ТекущийДокумент.Ссылка, "РасчетыСПоставщикамиПланПоставок");
		КонецЕсли;

	КонецЦикла;
	
	СписокДокументовКОбработке = ОбщегоНазначенияКлиентСервер.СвернутьМассив(
		ТаблицаРегистраторРасчетов.ВыгрузитьКолонку("Ссылка"));
		
	Если СписокДокументовКОбработке.Количество() > 0 Тогда

		Параметры = Новый Структура;
		Параметры.Вставить("ДанныеКОбработке", СписокДокументовКОбработке);

		УдалитьСлужебныеРегистраторы(Параметры);

	КонецЕсли;
	
КонецПроцедуры

Функция ТипЗаписиВзаиморасчетов(ЗаписьТаблицы)
	
	ЭтоПредоплата = (ЗаписьТаблицы.Предоплата <> Неопределено
			Или ЗаписьТаблицы.ПредоплатаУпр <> Неопределено
			Или ЗаписьТаблицы.ПредоплатаРегл <> Неопределено)
		И (ЗаписьТаблицы.Предоплата <> 0
			Или ЗаписьТаблицы.ПредоплатаУпр <> 0 
			Или ЗаписьТаблицы.ПредоплатаРегл <> 0);
	
	Возврат Перечисления.ТипыЗаписейВзаиморасчетов.ТипЗаписиПоОперации(
		ЗаписьТаблицы.ОперацияВзаиморасчетов,
		ЭтоПредоплата,
		ЗаписьТаблицы.ВидДвижения);
	
КонецФункции

#КонецОбласти

#Область ОбработчикОбновленияПоТипамЗаписейВзаиморасчетов

// Устарела. Удалить в следующей LTS. Заполняет в переданной строке движений поле ТипЗаписиВзаиморасчетов
//
// Параметры:
//  Запись - СтрокаТаблицыЗначений - движение по срокам
//  ТипРегистратора - Тип - Тип документа регистратора
//
Процедура ЗаполнитьТипЗаписиВзаиморасчетов(Запись, ТипРегистратора) Экспорт
	
	Если Запись.ВидДвижения = ВидДвиженияНакопления.Приход И Не Запись.Сторно
		Или Запись.ВидДвижения = ВидДвиженияНакопления.Расход И Запись.Сторно Тогда
		Если Запись.Предоплата <> 0 Тогда
			Если (ЭтоКорректировкаЗадолженности(Запись.ХозяйственнаяОперация)
				И Не ТипРегистратора = Тип("ДокументСсылка.КорректировкаРеализации")
				И Не ТипРегистратора = Тип("ДокументСсылка.КорректировкаПриобретения"))
				Или ТипРегистратора = Тип("ДокументСсылка.КорректировкаЗадолженности")
				Или Не ЗначениеЗаполнено(Запись.ХозяйственнаяОперация)
				Или ТипРегистратора = Тип("ДокументСсылка.Бронирование")
					И Запись.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗачетАвансаПоставщику Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеПредоплаты;
			ИначеЕсли ЭтоПереносВзаиморасчетов(Запись.ХозяйственнаяОперация)
				Или ТипРегистратора = Тип("ДокументСсылка.ВзаимозачетЗадолженности") Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУвеличениеПредоплаты;
			ИначеЕсли ЭтоКорректировкаНакладной(Запись.ХозяйственнаяОперация)
				Или ТипРегистратора = Тип("ДокументСсылка.КорректировкаРеализации")
				Или ТипРегистратора = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУвеличениеПредоплаты;
			ИначеЕсли ЭтоОплата(Запись.ХозяйственнаяОперация, Истина) Тогда 
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ОплатаУвеличениеПредоплаты;
			ИначеЕсли Запись.Сторно Или ТипРегистратора = Тип("ДокументСсылка.Бронирование")
				И (Запись.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗачетАвансаКлиента
					Или Запись.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗачетАвансаПоставщику) Тогда
				// Зачет оплаты, возникающий при сторно
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУвеличениеПредоплаты;
			ИначеЕсли ТипЗнч(Запись.Регистратор) = Тип("ДокументСсылка.КорректировкаРегистров") Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеПредоплаты;
			Иначе
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеПредоплаты;
			КонецЕсли;
		ИначеЕсли Запись.Долг <> 0 Тогда 
			Если ЭтоВозвратДенежныхСредств(Запись.ХозяйственнаяОперация) Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ВозвратДенежныхСредствУвеличениеДолга;
			ИначеЕсли ЭтоПереносВзаиморасчетов(Запись.ХозяйственнаяОперация)
				Или ТипРегистратора = Тип("ДокументСсылка.ВзаимозачетЗадолженности") Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУвеличениеДолга;
			ИначеЕсли ЭтоОтгрузка(Запись.ХозяйственнаяОперация) Тогда 
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ОтгрузкаУвеличениеДолга;
			ИначеЕсли ЭтоКорректировкаНакладной(Запись.ХозяйственнаяОперация)
				Или ТипРегистратора = Тип("ДокументСсылка.КорректировкаРеализации")
				Или ТипРегистратора = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУвеличениеДолга;
			ИначеЕсли ЭтоКорректировкаЗадолженности(Запись.ХозяйственнаяОперация)
				Или ТипРегистратора = Тип("ДокументСсылка.КорректировкаЗадолженности")
				Или Не ЗначениеЗаполнено(Запись.ХозяйственнаяОперация) Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеДолга;
			ИначеЕсли Запись.Сторно Или ТипРегистратора = Тип("ДокументСсылка.Бронирование")
				И (Запись.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗачетАвансаКлиента
					Или Запись.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗачетАвансаПоставщику) Тогда
				// Зачет оплаты, возникающий при сторно
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУвеличениеДолга;
			ИначеЕсли ТипЗнч(Запись.Регистратор) = Тип("ДокументСсылка.КорректировкаРегистров") Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеДолга;
			Иначе
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеДолга;
			КонецЕсли;
		ИначеЕсли Запись.ПредоплатаУпр <> 0 Или Запись.ПредоплатаРегл <> 0 Тогда
			Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеПредоплаты;
		Иначе
			Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеДолга;
		КонецЕсли;
	Иначе
		Если Запись.Предоплата <> 0 Тогда
			Если ЭтоВозвратДенежныхСредств(Запись.ХозяйственнаяОперация) Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ВозвратДенежныхСредствУменьшениеПредоплаты;
			ИначеЕсли ЭтоКорректировкаЗадолженности(Запись.ХозяйственнаяОперация)
				Или ТипРегистратора = Тип("ДокументСсылка.КорректировкаЗадолженности")
				Или Не ЗначениеЗаполнено(Запись.ХозяйственнаяОперация) Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеПредоплаты;
			ИначеЕсли ЭтоПереносВзаиморасчетов(Запись.ХозяйственнаяОперация)
				Или ТипРегистратора = Тип("ДокументСсылка.ВзаимозачетЗадолженности") Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУменьшениеПредоплаты;
			ИначеЕсли ЭтоОтгрузка(Запись.ХозяйственнаяОперация) Тогда
				Если Запись.Сторно Тогда
					// Для сторно зачет оплаты отражаем как корректировку накладной
					Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУменьшениеПредоплаты;
				Иначе
					Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеПредоплаты;
				КонецЕсли;
			ИначеЕсли ЭтоКорректировкаНакладной(Запись.ХозяйственнаяОперация) Тогда

				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеПредоплаты;
			ИначеЕсли ТипЗнч(Запись.Регистратор) = Тип("ДокументСсылка.КорректировкаРегистров") Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУменьшениеПредоплаты;
			Иначе
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеПредоплаты;
			КонецЕсли;
		ИначеЕсли Запись.Долг <> 0 Тогда
			Если ЭтоВозвратДенежныхСредств(Запись.ХозяйственнаяОперация) Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ВозвратДенежныхСредствУменьшениеДолга;
			ИначеЕсли ЭтоОплата(Запись.ХозяйственнаяОперация)
				И Не ТипРегистратора = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") 
				И Не ТипРегистратора = Тип("ДокументСсылка.ВозвратТоваровПоставщику") 
				И Не ТипРегистратора = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
				И НЕ ТипРегистратора = Тип("ДокументСсылка.КорректировкаЗадолженности")
				И НЕ ТипРегистратора = Тип("ДокументСсылка.КорректировкаРеализации") 
				И НЕ ТипРегистратора = Тип("ДокументСсылка.КорректировкаПриобретения") 
				И НЕ ТипРегистратора = Тип("ДокументСсылка.ВзаимозачетЗадолженности") Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ОплатаУменьшениеДолга;
			ИначеЕсли ЭтоПереносВзаиморасчетов(Запись.ХозяйственнаяОперация)
				Или ТипРегистратора = Тип("ДокументСсылка.ВзаимозачетЗадолженности") Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУменьшениеДолга;
			ИначеЕсли ЭтоОтгрузка(Запись.ХозяйственнаяОперация) Тогда
				Если Запись.Сторно Тогда
					Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУменьшениеДолга;
				Иначе
					Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга;
				КонецЕсли;
			ИначеЕсли ЭтоКорректировкаНакладной(Запись.ХозяйственнаяОперация)
				И НЕ ТипРегистратора = Тип("ДокументСсылка.КорректировкаЗадолженности")
				Или ТипРегистратора = Тип("ДокументСсылка.КорректировкаРеализации")
				Или ТипРегистратора = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУменьшениеДолга;
			ИначеЕсли ЭтоКорректировкаЗадолженности(Запись.ХозяйственнаяОперация)
				Или ТипРегистратора = Тип("ДокументСсылка.КорректировкаЗадолженности") 
				Или Не ЗначениеЗаполнено(Запись.ХозяйственнаяОперация) Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеДолга;
			ИначеЕсли ТипЗнч(Запись.Регистратор) = Тип("ДокументСсылка.КорректировкаРегистров") Тогда
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеДолга;
			Иначе
				Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеДолга;
			КонецЕсли;
		ИначеЕсли Запись.ПредоплатаУпр <> 0 Или Запись.ПредоплатаРегл <> 0 Тогда 
			Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеПредоплаты;
		Иначе
			Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеДолга;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Устарела. Удалить в следующей LTS.
//
// Параметры:
//  ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации 
// 
// Возвращаемое значение:
//  Булево - Переданная хозяйственная операция это корректировка задолженности
Функция ЭтоКорректировкаЗадолженности(ХозяйственнаяОперация)
	
	Возврат 
		// Увеличение предоплаты
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КурсовыеРазницыКлиентыУбыток
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КурсовыеРазницыПоставщикиПрибыль
		// Увеличение долга
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КурсовыеРазницыКлиентыПрибыль
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КурсовыеРазницыПоставщикиУбыток
		// Корректировки
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КорректировкаЗадолженности
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.НачислениеКредиторскойЗадолженности
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.НачислениеДебиторскойЗадолженности
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеДебиторскойЗадолженности
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеКредиторскойЗадолженности
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеБезнадежнойЗадолженностиЗаСчетРезервовПоСомнительнымДолгам
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковПремийКлиентам
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтражениеПремииКлиентаРетроБонус
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковПремийПоставщиков
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтражениеПремииПоставщикаРетроБонус
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиентуРеглУчет
		// Комиссионные вознаграждения
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.УдержаниеВознагражденияКомиссионера
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.УдержаниеВознагражденияКомитентом
		// Бронирования в части подотчетников
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.БронированиеЧерезПодотчетноеЛицо
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИспользованиеБронированияПодотчетнымЛицом
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратБронированияПодотчетногоЛица
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ШтрафыПриВозвратеБронированияПодотчетногоЛица
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратБронирования 
		// Сторно возврата бронирования
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СторноИспользованияБронированияПодотчетнымЛицом
		// Отражение зарплаты в финансовом учете
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.УдержаниеИзЗарплатыВСчетРеализацииСотруднику
		// Сторно
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВосстановлениеДолгаАвансаПриСторноВзаиморасчетов
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПерезачетДолгаПриСторноВзаиморасчетов
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПерезачетАвансаПриСторноВзаиморасчетов
	
КонецФункции

// Устарела. Удалить в следующей LTS.
//
// Параметры:
//  ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации 
// 
// Возвращаемое значение:
//  Булево - Переданная хозяйственная операция это корректировка накладной
Функция ЭтоКорректировкаНакладной(ХозяйственнаяОперация)
	
	Возврат 
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтРозничногоПокупателя
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратТоваровОтКлиента
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратТоваровЧерезКомиссионера
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратТоваровПоставщику
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоставкаПодПринципала
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратБронирования 
		// Возвраты товаров
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗадолженностиКлиента
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗадолженностиПоставщику
		// Отрицательные отчеты комитенту/комиссионера
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомитенту
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомиссионера
		// Отрицательное отражение вознаграждения
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомиссионераКомиссия
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомитентуКомиссия
	
КонецФункции

// Устарела. Удалить в следующей LTS.
//
// Параметры:
//  ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации 
// 
// Возвращаемое значение:
//  Булево - Переданная хозяйственная операция это перенос взаиморасчетов
Функция ЭтоПереносВзаиморасчетов(ХозяйственнаяОперация)
	
	Возврат 
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВзаимозачетЗадолженности
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтражениеОплатыЧерезКомиссионера
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереносАванса
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереносДолга
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереносЗадолженностиМеждуФилиалами
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереносПлатежаМеждуФилиалами
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РезервированиеАвансаКлиента
	
КонецФункции

// Устарела. Удалить в следующей LTS.
//
// Параметры:
//  ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации 
// 
// Возвращаемое значение:
//  Булево - Переданная хозяйственная операция это возврат денежных средств
Функция ЭтоВозвратДенежныхСредств(ХозяйственнаяОперация)
	
	Возврат 
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиентуНаПлатежнуюКарту
	
КонецФункции

// Устарела. Удалить в следующей LTS.
//
// Параметры:
//  ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации 
// 
// Возвращаемое значение:
//  Булево - Переданная хозяйственная операция это оплата
Функция ЭтоОплата(ХозяйственнаяОперация, ЭтоПриходПредоплаты=Ложь) Экспорт
	
	Возврат 
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.БронированиеУПоставщика
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.БронированиеЧерезАгента
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.БронированиеЧерезПодотчетноеЛицо
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхДокументовПоставщику
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеТаможне
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиентаПоПлатежнойКарте
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияПодарочныхСертификатов
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗадолженностиКлиента
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПогашениеЗадолженностиПоставщику
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ШтрафыПриВозвратеБронирования
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ШтрафыПриВозвратеБронированияПодотчетногоЛица
		// Ввод остатков
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковАвансовКлиентов
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковАвансовПоставщикам
		Или (ЭтоПриходПредоплаты
			И (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомиссионера
				Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомиссионераОСписании
				Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомитенту
				Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомитентуОСписании))
	
КонецФункции

// Устарела. Удалить в следующей LTS.
//
// Параметры:
//  ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации 
// 
// Возвращаемое значение:
//  Булево - Переданная хозяйственная операция это отгрузка
Функция ЭтоОтгрузка(ХозяйственнаяОперация)Экспорт
	
	Возврат 
		// Увеличение долга
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.БронированиеУПоставщика
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.БронированиеЧерезАгента
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыкупПринятыхНаХранениеТоваров
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыкупТоваровДавальца
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыкупТоваровХранителем
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыкупТоваровПереработчиком
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетДавальцуМеждуОрганизациями
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетПоКомиссииМеждуОрганизациямиОСписании
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомиссионера
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомиссионераКомиссия
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомиссионераОСписании
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомитенту
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомитентуКомиссия
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетКомитентуОСписании
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОформлениеГТДБрокером
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОформлениеГТДСамостоятельно
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПереходПраваСобственности
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоставкаПодПринципала
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхДокументовОтПоставщика
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПроизводствоУПереработчика
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПроизводствоУПереработчика2_5
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПроизводствоУПереработчикаВСтранахЕАЭС2_5
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияВнеоборотныхАктивов
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияВРозницу
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиентуРеглУчет
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияЧерезКомиссионера
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияОСсОтложеннымПереходомПрав
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияУслугПоАренде
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровДавальцаНаРасходы
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеПринятыхТоваровНаРасходы
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ШтрафыПриВозвратеБронирования
		// Уменьшение предоплаты и долга
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗачетАвансаКлиента
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗачетАвансаПоставщику
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗачетВознагражденияОплатойКомиссионера
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗачетВознагражденияОплатойКомитенту
		// Ввод остатков
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковЗадолженностиКлиентов
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам
	
КонецФункции

#КонецОбласти

// Распределять взаиморасчеты фоновым заданием.
// 
// Возвращаемое значение:
//  Булево - Распределять взаиморасчеты фоновым заданием
Функция РаспределятьВзаиморасчетыФоновымЗаданием() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Константы.РаспределятьВзаиморасчетыФоновымЗаданием.Получить()
		И Не ОбщегоНазначения.ИнформационнаяБазаФайловая() И Не ОбщегоНазначения.РежимОтладки();

КонецФункции

// Возвращает условия, при которых распределение взаиморасчетов можно выполнять в транзакции проведения документа.
// 
// Возвращаемое значение:
//  Структура - Условия распределения взаиморасчетов в транзакции документа:
// * КоличествоЗаписейОстатков - Число - Количество записей остатков в регистре расчетов по срокам
// * КоличествоДокументовВпереди - Число - Количество регистраторов впереди относительно проводимого документа по регистру расчетов
Функция УсловияДляРаспределенияВзаиморасчетовВТранзакцииДокумента() Экспорт
	
	УсловияРаспределенияВТранзакции = Новый Структура;
	УсловияРаспределенияВТранзакции.Вставить("КоличествоЗаписейОстатков", 20);
	УсловияРаспределенияВТранзакции.Вставить("КоличествоДокументовВпереди", 10);
	
	Возврат УсловияРаспределенияВТранзакции;

КонецФункции

#Область ПроверкиКорректностиДанныхВзаиморасчетов

// Формирует текст запроса сверки движений в оперативных и финансовых регистрах взаиморасчетов по суммам.
// Проверка выполняется по списку организаций в закрываемом периоде, который задается в параметрах закрытия месяца.
// По каждой организации из проверки исключаются периоды, в которых уже выполнена операция закрытия месяца
// по этапу "Формирование движений по расчетам с партнерами и переоценка расчетов".
// Результат выполнения запроса покажет выявленные расхождения по кортежам (АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта) с детализацией 
// до документа-регистратора, периода и организации и разместит результаты в следующих временных таблицах:
// 
// - РасчетыСКлиентамиИзменения - содержит информацию о расхождениях движений в регистрах "Расчеты с клиентами" и "Расчеты с клиентами по срокам".
// Таблица расхождений будет использоваться при формировании заданий к распределению взаиморасчетов.
// 
// - РасчетыСПоставщикамиИзменения - содержит информацию о расхождениях движений в регистрах "Расчеты с поставщиками" и "Расчеты с поставщиками по срокам".
// Таблица расхождений будет использоваться при формировании заданий к распределению взаиморасчетов.
// 
// - ДокументыСРасхождениямиВРегистрахВзаиморасчетов - содержит информацию о регистраторах, по которым обнаружены расхождения 
// в оперативных и финансовых регистрах взаиморасчетов.
// 
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапросаСверкаСуммОперативныхИФинансовыхРегистров() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	МИНИМУМ(ЗаданияКЗакрытиюМесяца.Месяц) КАК Месяц,
	|	ЗаданияКЗакрытиюМесяца.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ПериодыЗакрытия
	|ИЗ
	|	РегистрСведений.ЗаданияКЗакрытиюМесяца КАК ЗаданияКЗакрытиюМесяца
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаБлокировкиОтИзменений КАК НастройкаБлокировкиОтИзменений
	|		ПО ЗаданияКЗакрытиюМесяца.Организация = НастройкаБлокировкиОтИзменений.Организация
	|ГДЕ
	|	ЗаданияКЗакрытиюМесяца.Операция = ЗНАЧЕНИЕ(Справочник.ОперацииЗакрытияМесяца.ФормированиеДвиженийПоРасчетамСПартнерамиИПереоценкаРасчетов)
	|	И ЗаданияКЗакрытиюМесяца.Месяц >= ЕСТЬNULL(НастройкаБлокировкиОтИзменений.ЗаблокироватьПо, ДАТАВРЕМЯ(1, 1, 1))
	|	И ЗаданияКЗакрытиюМесяца.Организация В (&МассивОрганизаций)
	|	И НЕ &ЕстьЗаданияПоЭтапуДопроведенияДокументов
	|СГРУППИРОВАТЬ ПО
	|	ЗаданияКЗакрытиюМесяца.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРасхождений.ДокументРегистратор КАК Документ,
	|	ТаблицаРасхождений.Период КАК Период,
	|	ТаблицаРасхождений.Период КАК ДатаРегистратора,
	|	ТаблицаРасхождений.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ТаблицаРасхождений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаРасхождений.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаРасхождений.Валюта КАК ВалютаРасчетов,
	|	СУММА(ТаблицаРасхождений.СуммаОборот) - СУММА(ТаблицаРасхождений.СуммаПоСрокам) КАК Сумма,
	|	0 КАК КОплате,
	|	0 КАК КОтгрузке
	|ПОМЕСТИТЬ РасчетыСКлиентамиИзменения
	|ИЗ
	|	(ВЫБРАТЬ
	|		Расчеты.ДокументРегистратор КАК ДокументРегистратор,
	|		Расчеты.Период КАК Период,
	|		Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|		Расчеты.Валюта КАК Валюта,
	|		ВЫБОР
	|			КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА Расчеты.Предоплата - Расчеты.Долг
	|			ИНАЧЕ Расчеты.Долг - Расчеты.Предоплата
	|		КОНЕЦ КАК СуммаПоСрокам,
	|		0 КАК СуммаОборот
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПериодыЗакрытия КАК ВТ_ПериодыЗакрытия
	|			ПО Расчеты.Период >= ВТ_ПериодыЗакрытия.Месяц
	|			И Расчеты.Период <= &КонецПериода
	|			И Расчеты.АналитикаУчетаПоПартнерам.Организация = ВТ_ПериодыЗакрытия.Организация
	|	ГДЕ
	|		Расчеты.Активность
	|		И НЕ Расчеты.ДокументРегистратор ССЫЛКА Документ.КорректировкаРегистров
	|		И Расчеты.АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Расчеты.Регистратор КАК ДокументРегистратор,
	|		Расчеты.Период КАК Период,
	|		Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|		Расчеты.Валюта КАК Валюта,
	|		0 КАК СуммаПоСрокам,
	|		ВЫБОР
	|			КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Расчеты.Сумма
	|			ИНАЧЕ Расчеты.Сумма
	|		КОНЕЦ КАК СуммаОборот
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПериодыЗакрытия КАК ВТ_ПериодыЗакрытия
	|			ПО Расчеты.Период >= ВТ_ПериодыЗакрытия.Месяц
	|			И Расчеты.Период <= &КонецПериода
	|			И Расчеты.АналитикаУчетаПоПартнерам.Организация = ВТ_ПериодыЗакрытия.Организация
	|	ГДЕ
	|		Расчеты.Активность
	|		И НЕ Расчеты.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|		И Расчеты.АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)) КАК ТаблицаРасхождений
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасхождений.ДокументРегистратор,
	|	ТаблицаРасхождений.Период,
	|	ТаблицаРасхождений.АналитикаУчетаПоПартнерам,
	|	ТаблицаРасхождений.ОбъектРасчетов,
	|	ТаблицаРасхождений.Валюта,
	|	ТаблицаРасхождений.АналитикаУчетаПоПартнерам.Организация
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаРасхождений.СуммаПоСрокам) <> СУММА(ТаблицаРасхождений.СуммаОборот)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРасхождений.ДокументРегистратор КАК Документ,
	|	ТаблицаРасхождений.Период КАК Период,
	|	ТаблицаРасхождений.Период КАК ДатаРегистратора,
	|	ТаблицаРасхождений.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ТаблицаРасхождений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаРасхождений.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаРасхождений.Валюта КАК ВалютаРасчетов,
	|	СУММА(ТаблицаРасхождений.СуммаОборот) - СУММА(ТаблицаРасхождений.СуммаПоСрокам) КАК Сумма,
	|	0 КАК КОплате,
	|	0 КАК КПоступлению
	|ПОМЕСТИТЬ РасчетыСПоставщикамиИзменения
	|ИЗ
	|	(ВЫБРАТЬ
	|		Расчеты.ДокументРегистратор КАК ДокументРегистратор,
	|		Расчеты.Период КАК Период,
	|		Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|		Расчеты.Валюта КАК Валюта,
	|		ВЫБОР
	|			КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА Расчеты.Предоплата - Расчеты.Долг
	|			ИНАЧЕ Расчеты.Долг - Расчеты.Предоплата
	|		КОНЕЦ КАК СуммаПоСрокам,
	|		0 КАК СуммаОборот
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПериодыЗакрытия КАК ВТ_ПериодыЗакрытия
	|			ПО Расчеты.Период >= ВТ_ПериодыЗакрытия.Месяц
	|			И Расчеты.Период <= &КонецПериода
	|			И Расчеты.АналитикаУчетаПоПартнерам.Организация = ВТ_ПериодыЗакрытия.Организация
	|	ГДЕ
	|		Расчеты.Активность
	|		И НЕ Расчеты.ДокументРегистратор ССЫЛКА Документ.КорректировкаРегистров
	|		И Расчеты.АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Расчеты.Регистратор КАК ДокументРегистратор,
	|		Расчеты.Период КАК Период,
	|		Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|		Расчеты.Валюта КАК Валюта,
	|		0 КАК СуммаПоСрокам,
	|		ВЫБОР
	|			КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА Расчеты.Сумма
	|			ИНАЧЕ -Расчеты.Сумма
	|		КОНЕЦ КАК СуммаОборот
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПериодыЗакрытия КАК ВТ_ПериодыЗакрытия
	|			ПО Расчеты.Период >= ВТ_ПериодыЗакрытия.Месяц
	|			И Расчеты.Период <= &КонецПериода
	|			И Расчеты.АналитикаУчетаПоПартнерам.Организация = ВТ_ПериодыЗакрытия.Организация
	|	ГДЕ
	|		Расчеты.Активность
	|		И НЕ Расчеты.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|		И Расчеты.АналитикаУчетаПоПартнерам.Организация В (&МассивОрганизаций)) КАК ТаблицаРасхождений
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасхождений.ДокументРегистратор,
	|	ТаблицаРасхождений.Период,
	|	ТаблицаРасхождений.АналитикаУчетаПоПартнерам,
	|	ТаблицаРасхождений.ОбъектРасчетов,
	|	ТаблицаРасхождений.Валюта,
	|	ТаблицаРасхождений.АналитикаУчетаПоПартнерам.Организация
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаРасхождений.СуммаПоСрокам) <> СУММА(ТаблицаРасхождений.СуммаОборот)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСКлиентамиИзменения.Документ КАК Документ,
	|	РасчетыСКлиентамиИзменения.Организация КАК Организация,
	|	ИСТИНА КАК ЭтоРасчетыСКлиентами
	|ПОМЕСТИТЬ ДокументыСРасхождениямиВРегистрахВзаиморасчетов
	|ИЗ
	|	РасчетыСКлиентамиИзменения КАК РасчетыСКлиентамиИзменения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСПоставщикамиИзменения.Документ КАК Документ,
	|	РасчетыСПоставщикамиИзменения.Организация КАК Организация,
	|	ЛОЖЬ КАК ЭтоРасчетыСКлиентами
	|ИЗ
	|	РасчетыСПоставщикамиИзменения КАК РасчетыСПоставщикамиИзменения";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти
