////////////////////////////////////////////////////////////////////////////////////
// Модуль "УниверсальнаяОчередьОперативныхЗаданий" содержит процедуры и функции для
// работы с универсальной очереди оперативных заданий.
//
////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обрабатывает задания регистра УниверсальнаяОчередьОперативныхЗаданий.
//
// Параметры:
//  Данные - Структура             - ключи совпадают с именами измерений, ресурсов и реквизитов регистра
//                                   УниверсальнаяОчередьОперативныхЗаданий.
//         - ТаблицаЗначений       - имена колонок совпадают с именами измерений, ресурсов и реквизитов регистра
//                                   УниверсальнаяОчередьОперативныхЗаданий.
//         - Массив из ЛюбаяСсылка - массив объектов вложенной очереди - значение задания вложенной очереди передается
//                                   в параметр Задание.
//  Задание - Неопределено                                   - если параметр Данные является структурой или таблицей
//                                                             значений.
//          - ПеречислениеСсылка.ЗаданияУниверсальнойОчереди - если параметр Данные является массивом (обязательно для
//                                                             заполнения).
//  ДополнительныеДанные - Неопределено, ХранилищеЗначения - используется, если параметр Данные является массивом.
//  СгенерироватьИдентификаторЗаписи - Булево - если ИСТИНА, тогда идентификатор записи будет сгенерирован
//                                              автоматически. По умолчанию ИСТИНА.
//
Процедура ОбработатьЗадания(
			Данные,
			Задание = Неопределено,
			ДополнительныеДанные = Неопределено,
			СгенерироватьИдентификаторЗаписи = Истина) Экспорт
	
	ТипДанных = ТипЗнч(Данные);
	
	Если ТипДанных = Тип("Структура") Или ТипДанных = Тип("ТаблицаЗначений") Тогда
		
		ТекущиеДанные = Данные;
		
	ИначеЕсли ТипДанных = Тип("Массив") Тогда
		
		ТекущиеДанные = РегистрыСведений.УниверсальнаяОчередьОперативныхЗаданий.СоздатьНаборЗаписей().ВыгрузитьКолонки();
		
		Для каждого Объект Из Данные Цикл
			
			СтрокаТаблицы = ТекущиеДанные.Добавить();
			СтрокаТаблицы.Объект               = Объект;
			СтрокаТаблицы.Задание              = Задание;
			СтрокаТаблицы.ДополнительныеДанные = ДополнительныеДанные;
			
		КонецЦикла;
		
	Иначе
		
		ВызватьИсключение СтрШаблон(
							НСтр("ru = 'Неизвестный тип данных ""%1"" универсальной очереди оперативных заданий.';
								|en = 'Неизвестный тип данных ""%1"" универсальной очереди оперативных заданий.'"),
							ТипДанных);
		
	КонецЕсли;
	
	ОтложенныеЗадания.ОбработатьЗаданияОчереди(
		"УниверсальнаяОчередьОперативныхЗаданий",
		ТекущиеДанные,
		СгенерироватьИдентификаторЗаписи);
	
КонецПроцедуры

// Обрабатывает задание регистра УниверсальнаяОчередьОперативныхЗаданий.
//
// Параметры:
//  Объект               - ЛюбаяСсылка -
//  Задание              - ПеречислениеСсылка.ЗаданияУниверсальнойОчереди -
//  ДополнительныеДанные - ХранилищеЗначения, Неопределено -
//
Процедура ОбработатьЗадание(Объект, Задание, ДополнительныеДанные = Неопределено) Экспорт
	
	ОтложенныеЗадания.ОбработатьЗаданияОчереди(
		"УниверсальнаяОчередьОперативныхЗаданий",
		Новый Структура("Объект, Задание, ДополнительныеДанные", Объект, Задание, ДополнительныеДанные),
		Истина);
	
КонецПроцедуры

// Добавляет задания в регистр УниверсальнаяОчередьОперативныхЗаданий.
//
// Параметры:
//  Данные - Структура             - ключи совпадают с именами измерений, ресурсов и реквизитов регистра
//                                   УниверсальнаяОчередьОперативныхЗаданий.
//         - ТаблицаЗначений       - имена колонок совпадают с именами измерений, ресурсов и реквизитов регистра
//                                   УниверсальнаяОчередьОперативныхЗаданий.
//         - Массив из ЛюбаяСсылка - массив объектов вложенной очереди - значение задания вложенной очереди передается
//                                   в параметр Задание.
//  Задание - Неопределено                                   - если параметр Данные является структурой или таблицей
//                                                             значений.
//          - ПеречислениеСсылка.ЗаданияУниверсальнойОчереди - если параметр Данные является массивом (обязательно для
//                                                             заполнения).
//  ДополнительныеДанные - Неопределено, ХранилищеЗначения - используется, если параметр Данные является массивом.
//  СгенерироватьИдентификаторЗаписи - см. ОтложенныеЗадания.ДобавитьЗаданияВОчередь.СгенерироватьИдентификаторЗаписи
//  ТаблицаЗаданий                   - см. ОтложенныеЗадания.ДобавитьЗаданияВОчередь.ТаблицаЗаданий
//
Процедура ДобавитьЗадания(
			Данные,
			Задание = Неопределено,
			ДополнительныеДанные = Неопределено,
			СгенерироватьИдентификаторЗаписи = Ложь,
			ТаблицаЗаданий = Неопределено) Экспорт
	
	ТипДанных = ТипЗнч(Данные);
	
	Если ТипДанных = Тип("Структура") Или ТипДанных = Тип("ТаблицаЗначений") Тогда
		
		ТекущиеДанные = Данные;
		
	ИначеЕсли ТипДанных = Тип("Массив") Тогда
		
		ТекущиеДанные = РегистрыСведений.УниверсальнаяОчередьОперативныхЗаданий.СоздатьНаборЗаписей().ВыгрузитьКолонки();
		
		Для каждого Объект Из Данные Цикл
			
			СтрокаТаблицы = ТекущиеДанные.Добавить();
			СтрокаТаблицы.Объект               = Объект;
			СтрокаТаблицы.Задание              = Задание;
			СтрокаТаблицы.ДополнительныеДанные = ДополнительныеДанные;
			
		КонецЦикла;
		
	Иначе
		
		ВызватьИсключение СтрШаблон(
							НСтр("ru = 'Неизвестный тип данных ""%1"" универсальной очереди оперативных заданий.';
								|en = 'Неизвестный тип данных ""%1"" универсальной очереди оперативных заданий.'"),
							ТипДанных);
		
	КонецЕсли;
	
	ОтложенныеЗадания.ДобавитьЗаданияВОчередь(
		"УниверсальнаяОчередьОперативныхЗаданий",
		ТекущиеДанные,
		СгенерироватьИдентификаторЗаписи,
		ТаблицаЗаданий);
	
КонецПроцедуры

// Добавляет задание в регистр УниверсальнаяОчередьОперативныхЗаданий.
//
// Параметры:
//  Объект               - ЛюбаяСсылка -
//  Задание              - ПеречислениеСсылка.ЗаданияУниверсальнойОчереди -
//  ДополнительныеДанные - ХранилищеЗначения, Неопределено -
//  ТаблицаЗаданий       - см. ОтложенныеЗадания.ДобавитьЗаданияВОчередь.ТаблицаЗаданий
//
Процедура ДобавитьЗадание(Объект, Задание, ДополнительныеДанные = Неопределено, ТаблицаЗаданий = Неопределено) Экспорт
	
	ОтложенныеЗадания.ДобавитьЗаданияВОчередь(
		"УниверсальнаяОчередьОперативныхЗаданий",
		Новый Структура("Объект, Задание, ДополнительныеДанные", Объект, Задание, ДополнительныеДанные),
		Истина,
		ТаблицаЗаданий);
	
КонецПроцедуры

// Запускает выполнения заданий регистра УниверсальнаяОчередьОперативныхЗаданий.
//
// Параметры:
//  Очереди - ТаблицаЗначений, Неопределено -
//             1. ТаблицаЗаданий - см. ИнициализироватьТаблицуЗаданий
//             2. Неопределено   - запускает все задания регистра УниверсальнаяОчередьОперативныхЗаданий.
//
Процедура Запустить(Знач Очереди = Неопределено) Экспорт
	
	ОтложенныеЗадания.Запустить(?(ЗначениеЗаполнено(Очереди), Очереди, "УниверсальнаяОчередьОперативныхЗаданий"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Переопределяет параметры универсальной очереди оперативных заданий, так же, используется для добавления
// вложенных очередей (с помощью метода ДобавитьВложеннуюОчередь).
// 
// Параметры:
//  ТаблицаОчередей - см. ОтложенныеЗадания.ОчередиЗаданий
//
Процедура ОчередиЗаданий(ТаблицаОчередей) Экспорт
	
//++ НЕ УТКА
	ДобавитьВложеннуюОчередь(
		ТаблицаОчередей,
		Перечисления.ЗаданияУниверсальнойОчереди.ОбновлениеГрафикаДвиженияТоваровПослеРасчетаГрафикаПроизводства,
		"УправлениеПроизводством.ОбновитьПланируемоеПоступлениеТоваров");
		
	ДобавитьВложеннуюОчередь(
		ТаблицаОчередей,
		Перечисления.ЗаданияУниверсальнойОчереди.ОбновлениеПриоритетаВЭтапахПроизводства,
		"УправлениеПроизводством.ОбновитьПриоритетВЭтапахПроизводства");
		
	ДобавитьВложеннуюОчередь(
		ТаблицаОчередей,
		Перечисления.ЗаданияУниверсальнойОчереди.ОбновлениеСтруктурыЗаказаПриИзмененииМестаВОчереди,
		"СтруктураЗаказаСлужебный.ОбновитьСтруктуруЗаказаПриИзмененииМестаВОчереди");
//-- НЕ УТКА
	
	
	//++ Локализация
	ДобавитьВложеннуюОчередь(ТаблицаОчередей, 
		Перечисления.ЗаданияУниверсальнойОчереди.ОбновлениеСчетовФактурВыданных,
		"УчетНДСРФ.ОбновлениеСчетовФактурВыданных")
	//-- Локализация
				
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет вложенную очередь в универсальную очередь оперативных заданий.
// 
// Параметры:
//  ОчередиЗаданий            - см. ОтложенныеЗадания.ОчередиЗаданий
//  ЗначениеУказателяДействия - ПеречислениеСсылка.ЗаданияУниверсальнойОчереди - значение указателя действия
//  ИмяМетода                 - Строка - имя метода выполнения заданий, принцип формирования аналогично
//                                       аргументу ИмяМетода метода ОбщегоНазначения.ВыполнитьМетодКонфигурации
//                                       (см. ОбщегоНазначения.ВыполнитьМетодКонфигурации.ИмяМетода).
//
Процедура ДобавитьВложеннуюОчередь(ОчередиЗаданий, ЗначениеУказателяДействия, ИмяМетода)
	
	ОтложенныеЗадания.ДобавитьВложеннуюОчередь(
		ОчередиЗаданий,
		"УниверсальнаяОчередьОперативныхЗаданий",
		ЗначениеУказателяДействия,
		ИмяМетода)
	
КонецПроцедуры

#КонецОбласти