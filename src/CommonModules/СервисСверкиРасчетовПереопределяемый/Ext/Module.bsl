
#Область ПрограммныйИнтерфейс

// Возвращает наименование операции для целей сверки расчетов по типу документа и дополнительным сведениям
//
// Параметры:
//  ДокументСсылка - ДокументСсылка - ссылка на документ для которого требуется определить наименование операции
//  ДополнительныеСведения - Структура - ключи определяются конфигурацией-потребителем для точного определения операции
//
// Возвращаемое значение:
//  Строка - наименование операции для целей сопоставления документов на сервисе сверки, 
//  см. Перечисления.СервисСверкиРасчетовВидыОпераций.
//
Функция НаименованиеОперации(ДокументСсылка, ДополнительныеСведения) Экспорт
	
	РасчетыСКлиентом = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
	РасчетыСПоставщиком = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
	РасчетыСКредитором = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКредитором;
	РасчетыСДебитором = Перечисления.ТипыРасчетовСПартнерами.РасчетыСДебитором;
	//++ НЕ УТ
	РасчетыСАрендодателем = Перечисления.ТипыРасчетовСПартнерами.РасчетыСАрендодателем;
	//-- НЕ УТ
	ДанныеДокумента = Новый Структура("Организация, ХозяйственнаяОперация");
	ИменаРеквизитов = Новый Массив;
	Если // Документы интеркампани
		ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
		//++ НЕ УТКА
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетДавальцуМеждуОрганизациями")
		//-- НЕ УТКА
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомиссионера")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациямиОСписании")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
		// Документы оплаты
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств") Тогда
		ИменаРеквизитов.Добавить("Организация");
		
		Если НЕ (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")
					ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациямиОСписании")) Тогда
			ИменаРеквизитов.Добавить("ХозяйственнаяОперация");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИменаРеквизитов.Количество() Тогда
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка,ИменаРеквизитов);
		ЗаполнитьЗначенияСвойств(ДанныеДокумента, РеквизитыДокумента);
	КонецЕсли;
	
	// ДополнительныеСведения.ТипРасчетов определяется регистром по которому документ сделал движения
	// для расчетов по фин. инструментам регистром и типом договора
	ЭтоРасчетыПоОрганизации = ДополнительныеСведения.ОрганизацияВРегистре = ДанныеДокумента.Организация;
	ЭтоРасчетыПоОрганизацииКонтрагенту = НЕ ЭтоРасчетыПоОрганизации;
	ЭтоРасчетыСКлиентом = ДополнительныеСведения.ТипРасчетов = РасчетыСКлиентом;
	ЭтоРасчетыСПоставщиком = ДополнительныеСведения.ТипРасчетов = РасчетыСПоставщиком;
	ЭтоРасчетыСАрендодателем = Ложь;
	//++ НЕ УТ
	ЭтоРасчетыСАрендодателем = ДополнительныеСведения.ТипРасчетов = РасчетыСАрендодателем;
	//-- НЕ УТ
	ЭтоСторноОтчетаКомитента = ДополнительныеСведения.ТипДоговора = Перечисления.ТипыДоговоров.СКомитентом
		И ДополнительныеСведения.Дебет - ДополнительныеСведения.Кредит > 0;
	НаименованиеОперации = "";
//Реализация
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АктВыполненныхРабот")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВыкупВозвратнойТарыКлиентом")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВыкупТоваровХранителем")
		//++ НЕ УТКА
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетДавальцу")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетДавальцу2_5")
		ИЛИ ЭтоРасчетыСКлиентом И ЭтоРасчетыПоОрганизации
			И ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетДавальцуМеждуОрганизациями")
		//-- НЕ УТКА
		ИЛИ ЭтоРасчетыСКлиентом
			И (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомиссионера")//комитент отразил у себя продажи комиссионера
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомитенту"))//комиссионер продал услугу
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомитентуОЗакупках")
		ИЛИ ЭтоРасчетыСКлиентом И ЭтоРасчетыПоОрганизации
			И (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")//комиссионер продал товар, комитент отразил у себя продажи
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями"))
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		НаименованиеОперации = "Реализация";
		
//Поступление
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.Бронирование")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВыкупВозвратнойТарыУПоставщика")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВыкупПринятыхНаХранениеТоваров")
		//++ НЕ УТ
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПереработчика")
		//++ НЕ УТКА
		ИЛИ ЭтоРасчетыСПоставщиком И ЭтоРасчетыПоОрганизацииКонтрагенту
			И ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетДавальцуМеждуОрганизациями")
		//-- НЕ УТКА
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПереработчика2_5")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетОператораСистемыПлатон")
		//-- НЕ УТ
		ИЛИ ЭтоРасчетыСПоставщиком
			И (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомиссионера")//комитент отразил у себя долг за услуги комиссионера
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомитенту") И НЕ ЭтоСторноОтчетаКомитента)//комиссионер погасил долг за товар
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетОСписанииТоваровСХранения")
		ИЛИ ЭтоРасчетыСПоставщиком И ЭтоРасчетыПоОрганизацииКонтрагенту
			И (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")//комиссионер получил товар
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями"))
		//++ НЕ УТ
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеУслугПоАренде")
		//-- НЕ УТ
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов") Тогда
		НаименованиеОперации = "Поступление";
		
//КорректировкаРеализации
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АктПремииКлиенту")
		ИЛИ ЭтоРасчетыСКлиентом И ЭтоРасчетыПоОрганизации
			И ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациямиОСписании")//комитент списал товар
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
		ИЛИ ЭтоРасчетыСКлиентом И ЭтоРасчетыПоОрганизацииКонтрагенту
			И ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
		ИЛИ ЭтоРасчетыСКлиентом
			И (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВзаимозачетЗадолженности")
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаЗадолженности"))
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаРеализации")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомиссионераОСписании") Тогда//комитент списал товар
		НаименованиеОперации = "КорректировкаРеализации";
		
//КорректировкаПоступления
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АктПремииПоставщика")
		ИЛИ ЭтоРасчетыСПоставщиком И ЭтоРасчетыПоОрганизации
			И ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
		ИЛИ ЭтоРасчетыСПоставщиком
			И (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВзаимозачетЗадолженности")
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаЗадолженности"))
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровПоставщику")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомитентуОСписании")//комиссионер списал за товар
		//сторнирующий ОтчетКомитенту (возврат от комитента)
		ИЛИ ЭтоРасчетыСПоставщиком И ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетКомитенту") И ЭтоСторноОтчетаКомитента 
		ИЛИ ЭтоРасчетыСПоставщиком И ЭтоРасчетыПоОрганизацииКонтрагенту//комиссионер списал за товар
			И ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациямиОСписании")
		//++ НЕ УТ
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ИзменениеУсловийДоговораАренды")
		//-- НЕ УТ
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
		НаименованиеОперации = "КорректировкаПоступления";
		
//ОплатаОтПокупателя
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОперацияПоПлатежнойКарте")
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОперацияПоЯндексКассе")
		ИЛИ ЭтоРасчетыСКлиентом
			И (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств")
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер")) Тогда
		НаименованиеОперации = "ОплатаОтПокупателя";
		
//ОплатаПоставщику
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АвансовыйОтчет")
		ИЛИ (ЭтоРасчетыСПоставщиком ИЛИ ЭтоРасчетыСАрендодателем)
			И (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер")
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств")) Тогда
		НаименованиеОперации = "ОплатаПоставщику";
		
//ВозвратСредствПокупателю
	ИначеЕсли ЭтоРасчетыСКлиентом
		И (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер")
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств")) Тогда
		НаименованиеОперации = "ВозвратСредствПокупателю";
		
//ВозвратСредствОтПоставщика
	ИначеЕсли (ЭтоРасчетыСПоставщиком ИЛИ ЭтоРасчетыСАрендодателем)
			И (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств")
				ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер")) Тогда
		НаименованиеОперации = "ВозвратСредствОтПоставщика";
		
//ПоступлениеСредствПрочее
		
//ОплатаПрочая
		
//СторноДокумента
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.Сторно") Тогда
		НаименованиеОперации = "СторноДокумента";
		
//КурсовыеРазницы
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасчетКурсовыхРазниц") Тогда
		НаименованиеОперации = "КурсовыеРазницы";
		
//ОперацииПоЗаймам
	ИначеЕсли (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер")
					ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств")
					ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.РасходныйКассовыйОрдер")
					ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств")
					ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаЗадолженностиПоФинансовымИнструментам"))
				И (ДополнительныеСведения.ТипРасчетов = РасчетыСКредитором
					ИЛИ ДополнительныеСведения.ТипРасчетов = РасчетыСДебитором)
		Тогда
		НаименованиеОперации = "ОперацииПоЗаймам";
//++ НЕ УТ

//ПоступлениеДенежныхДокументов
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПоступлениеДенежныхДокументов") Тогда
		НаименованиеОперации = "ПоступлениеДенежныхДокументов";
//-- НЕ УТ
		
	Иначе//нечего не смогли определить
		НаименованиеОперации = "СторноДокумента";
		
	КонецЕсли;
	
	Возврат НаименованиеОперации;
	
КонецФункции

// Возвращает структуру дополнительных сведений, необходимых для определение наименования операции
//
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса
//
// Возвращаемое значение:
//  Структура - ключи определяются конфигурацией-потребителем для точного определения операции
//
Функция ДополнительныеСведенияДляОпределенияОперации(Выборка) Экспорт
	
	ДополнительныеСведения = Новый Структура();
	ДополнительныеСведения.Вставить("ОрганизацияВРегистре", Выборка.Организация);
	ДополнительныеСведения.Вставить("КонтрагентВРегистре", Выборка.Контрагент);
	ДополнительныеСведения.Вставить("ТипРасчетов", Выборка.ТипРасчетов);
	ДополнительныеСведения.Вставить("ТипДоговора", Выборка.ТипДоговора);
	ДополнительныеСведения.Вставить("Дебет", Выборка.Дебет);
	ДополнительныеСведения.Вставить("Кредит", Выборка.Кредит);
	
	Возврат ДополнительныеСведения;
	
КонецФункции

// Получает сведения к отправке по документам для сверки.
// 
// Пример запроса с полями которые должен возвращать запрос:
//  "ВЫБРАТЬ
//  |	ВТ_ДанныеРеестра.Организация КАК Организация,
//  |	ВТ_ДанныеРеестра.Контрагент КАК Контрагент,
//  |	ВТ_ДанныеРеестра.Документ КАК Документ,
//  |	ВТ_ДанныеРеестра.ИдентификаторДокумента КАК ИдентификаторДокумента,
//  |	ВТ_ДанныеРеестра.Статус КАК Статус,
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.НомерДокумента, """") КАК НомерДокумента,
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.ДатаДокумента, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаДокумента,
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаРегистратора,
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.НомерРегистратора, """") КАК НомерРегистратора,
//  |	ВТ_ДанныеРеестра.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
//  |	Организации.ИНН КАК ИНН,
//  |	ВЫБОР
//  |		КОГДА Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
//  |			ТОГДА Организации.КПП
//  |		ИНАЧЕ """"
//  |	КОНЕЦ КАК КПП,
//  |	Организации.НаименованиеПолное КАК НаименованиеПолное,
//  |	СУММА(ВТ_ДанныеРеестра.Дебет - ВТ_ДанныеРеестра.Кредит) КАК Сумма,
//  |	Организации.Наименование КАК Наименование,
//  |	ВТ_ДанныеРеестра.ЦифровойИндексОбособленногоПодразделения КАК ЦифровойИндексОбособленногоПодразделения,
//  |	МАКСИМУМ(ВТ_ДанныеРеестра.Счет) КАК Счет,
//  |	МАКСИМУМ(ВТ_ДанныеРеестра.ВидПрочихДоходовИРасходов) КАК ВидПрочихДоходовИРасходов,
//  |	СУММА(ВЫБОР
//  |			КОГДА ВТ_ДанныеРеестра.ВалютнаяСумма < 0
//  |				ТОГДА -ВТ_ДанныеРеестра.ВалютнаяСумма
//  |			ИНАЧЕ ВТ_ДанныеРеестра.ВалютнаяСумма
//  |		КОНЕЦ) КАК ВалютнаяСумма,
//  |	ВТ_ДанныеРеестра.Валюта КАК Валюта,
//  |	МАКСИМУМ(ВТ_ДанныеРеестра.КорСубконто3) КАК КорСубконто3,
//  |	МАКСИМУМ(ВТ_ДанныеРеестра.КорСубконто2) КАК КорСубконто2,
//  |	МАКСИМУМ(ВТ_ДанныеРеестра.КорСубконто1) КАК КорСубконто1,
//  |	МАКСИМУМ(ВТ_ДанныеРеестра.КорСчет) КАК КорСчет,
//  |	ВТ_ДанныеРеестра.ЭтоКорректировочныйДокумент КАК ЭтоКорректировочныйДокумент,
//  |	ВТ_ДанныеРеестра.ЭтоУниверсальныйДокумент КАК ЭтоУниверсальныйДокумент,
//  |	ВТ_ДанныеРеестра.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
//  |	ВТ_ДанныеРеестра.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
//  |	СУММА(ВТ_ДанныеРеестра.Кредит) КАК Кредит,
//  |	СУММА(ВТ_ДанныеРеестра.Дебет) КАК Дебет,
//  |	ЕСТЬNULL(ВТ_ДанныеПоНДС.НомерСчетаФактуры, """") КАК НомерСчетаФактуры,
//  |	ЕСТЬNULL(ВТ_ДанныеПоНДС.ДатаСчетаФактуры, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСчетаФактуры,
//  |	ЕСТЬNULL(ВТ_ДанныеПоНДС.ЭтоКорректировочныйСчетФактура, ЛОЖЬ) КАК ЭтоКорректировочныйСчетФактура,
//  |	МАКСИМУМ(ЕСТЬNULL(ВТ_ДанныеПоНДС.СуммаНДС + ВТ_ДанныеПоНДС.СуммаНДСРазницаУменьшение + ВТ_ДанныеПоНДС.СуммаНДСРазницаУвеличение, 0)) КАК СуммаНДС,
//  |	ВТ_ДанныеРеестра.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
//  |	ВТ_ДанныеРеестра.НаименованиеКонтрагента КАК НаименованиеКонтрагента,
//  |	ВТ_ДанныеРеестра.НаименованиеКонтрагентаПолное КАК НаименованиеКонтрагентаПолное,
//  |	ВТ_ДанныеРеестра.ИННКонтрагента КАК ИННКонтрагента,
//  |	ВТ_ДанныеРеестра.КППКонтрагента КАК КППКонтрагента,
//  |	ВЫБОР
//  |		КОГДА ЕСТЬNULL(ВТ_ДанныеПоНДС.НомерСчетаФактуры, """") ПОДОБНО ""%[А-Я]%""
//  |			ТОГДА ИСТИНА
//  |		ИНАЧЕ ЛОЖЬ
//  |	КОНЕЦ КАК ОбрабатыватьНомерСФ
//  |ИЗ
//  |	ВТ_ДанныеРеестра КАК ВТ_ДанныеРеестра
//  |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
//  |		ПО ВТ_ДанныеРеестра.Организация = Организации.Ссылка
//  |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеПоНДС КАК ВТ_ДанныеПоНДС
//  |		ПО ВТ_ДанныеРеестра.Документ = ВТ_ДанныеПоНДС.ДокументОснование
//  |ГДЕ
//  |	(&ЗаВесьПериод
//  |			ИЛИ ВТ_ДанныеРеестра.ДатаДокумента >= &ДатаНачала
//  |				И ВТ_ДанныеРеестра.ДатаДокумента <= &ДатаОкончания)
//  |
//  |СГРУППИРОВАТЬ ПО
//  |	ВТ_ДанныеРеестра.Организация,
//  |	ВТ_ДанныеРеестра.Контрагент,
//  |	ВТ_ДанныеРеестра.Документ,
//  |	ВТ_ДанныеРеестра.ИдентификаторДокумента,
//  |	ВТ_ДанныеРеестра.Статус,
//  |	ВТ_ДанныеРеестра.ИдентификаторОрганизации,
//  |	Организации.ИНН,
//  |	ВЫБОР
//  |		КОГДА Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
//  |			ТОГДА Организации.КПП
//  |		ИНАЧЕ """"
//  |	КОНЕЦ,
//  |	Организации.НаименованиеПолное,
//  |	Организации.Наименование,
//  |	ВТ_ДанныеРеестра.ЦифровойИндексОбособленногоПодразделения,
//  |	ВТ_ДанныеРеестра.Валюта,
//  |	ВТ_ДанныеРеестра.ЭтоКорректировочныйДокумент,
//  |	ВТ_ДанныеРеестра.ЭтоУниверсальныйДокумент,
//  |	ВТ_ДанныеРеестра.ДатаВходящегоДокумента,
//  |	ВТ_ДанныеРеестра.НомерВходящегоДокумента,
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.НомерДокумента, """"),
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.ДатаДокумента, ДАТАВРЕМЯ(1, 1, 1)),
//  |	ЕСТЬNULL(ВТ_ДанныеПоНДС.НомерСчетаФактуры, """"),
//  |	ЕСТЬNULL(ВТ_ДанныеПоНДС.ДатаСчетаФактуры, ДАТАВРЕМЯ(1, 1, 1)),
//  |	ЕСТЬNULL(ВТ_ДанныеПоНДС.ЭтоКорректировочныйСчетФактура, ЛОЖЬ),
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.ДатаРегистратора, ДАТАВРЕМЯ(1, 1, 1)),
//  |	ЕСТЬNULL(ВТ_ДанныеРеестра.НомерРегистратора, """"),
//  |	ВТ_ДанныеРеестра.ИдентификаторКонтрагента,
//  |	ВТ_ДанныеРеестра.НаименованиеКонтрагента,
//  |	ВТ_ДанныеРеестра.НаименованиеКонтрагентаПолное,
//  |	ВТ_ДанныеРеестра.ИННКонтрагента,
//  |	ВТ_ДанныеРеестра.КППКонтрагента,
//  |	ВЫБОР
//  |		КОГДА ЕСТЬNULL(ВТ_ДанныеПоНДС.НомерСчетаФактуры, """") ПОДОБНО ""%[А-Я]%""
//  |			ТОГДА ИСТИНА
//  |		ИНАЧЕ ЛОЖЬ
//  |	КОНЕЦ
//  |ИТОГИ
//  |	МАКСИМУМ(ИдентификаторОрганизации),
//  |	МАКСИМУМ(ИНН),
//  |	МАКСИМУМ(КПП),
//  |	МАКСИМУМ(НаименованиеПолное),
//  |	МАКСИМУМ(Наименование)
//  |ПО
//  |	Организация"
//
//
// Параметры:
//  ДатаНачала - Дата - дата, начиная с которой нужно получить сведения
//  ДатаОкончания - Дата - дата по которую нужно получить сведения
//
// Возвращаемое значение:
//  РезультатЗапроса
//
Функция ДанныеИнформационнойБазыКСверке(ДатаНачала = '00010101', ДатаОкончания = '00010101') Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Если ЗначениеЗаполнено(ДатаОкончания) Тогда
		Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
		Запрос.УстановитьПараметр("ЗаВесьПериод", Ложь);
	Иначе
		Запрос.УстановитьПараметр("ДатаОкончания", Дата(3999,12,31));
		Запрос.УстановитьПараметр("ЗаВесьПериод", НЕ ЗначениеЗаполнено(ДатаОкончания));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТипыСуммАренды", Документы.СверкаВзаиморасчетов2_5_11.ТипыСуммАрендыДляСверки());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РеестрСверки.Организация КАК Организация,
	|	РеестрСверки.Контрагент КАК Контрагент,
	|	РеестрСверки.Документ КАК Документ,
	|	РеестрСверки.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	РеестрСверки.Статус КАК Статус,
	|	ЕСТЬNULL(Контрагенты.ИНН, КонтрагентыОрганизации.ИНН) КАК ИННКонтрагента,
	|	ВЫБОР
	|		КОГДА Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|				ИЛИ КонтрагентыОрганизации.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ЕСТЬNULL(Контрагенты.КПП, КонтрагентыОрганизации.КПП)
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КППКонтрагента,
	|	ЕСТЬNULL(Контрагенты.НаименованиеПолное, КонтрагентыОрганизации.НаименованиеПолное) КАК НаименованиеКонтрагентаПолное,
	|	ЕСТЬNULL(Контрагенты.Наименование, КонтрагентыОрганизации.Наименование) КАК НаименованиеКонтрагента,
	|	ЕСТЬNULL(КонтрагентыБизнесСеть.Идентификатор, КонтрагентыОрганизацииБизнесСеть.Идентификатор) КАК ИдентификаторКонтрагента,
	|	РеестрСверки.ДатаДокумента КАК ДатаДокумента,
	|	РеестрСверки.НомерДокумента КАК НомерДокумента,
	|	РеестрСверки.ДатаДокумента КАК ДатаРегистратора,
	|	РеестрСверки.НомерДокумента КАК НомерРегистратора,
	|	Организации.ИНН КАК ИНН,
	|	ВЫБОР
	|		КОГДА Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА Организации.КПП
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КПП,
	|	Организации.Наименование КАК Наименование,
	|	Организации.НаименованиеПолное КАК НаименованиеПолное,
	|	ОрганизацииБизнесСеть.Идентификатор КАК ИдентификаторОрганизации,
	|	ЕСТЬNULL(ОсновныеПечатныеФормы.Идентификатор,"""") = ""УПД"" КАК РаспечатанУПД,
	|	ТИПЗНАЧЕНИЯ(РеестрСверки.Документ) = ТИП(Документ.ВзаимозачетЗадолженности) КАК ЭтоВзаимозачет,
	|	Организации.ЦифровойИндексОбособленногоПодразделения КАК ЦифровойИндексОбособленногоПодразделения
	|ПОМЕСТИТЬ ДокументыКОтправке
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК РеестрСверки
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
	|		ПО РеестрСверки.Организация = ОрганизацииБизнесСеть.Организация
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО РеестрСверки.Организация = Организации.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтрагентыБизнесСеть КАК КонтрагентыБизнесСеть
	|		ПО РеестрСверки.Контрагент = КонтрагентыБизнесСеть.Контрагент
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО РеестрСверки.Контрагент = Контрагенты.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииБизнесСеть КАК КонтрагентыОрганизацииБизнесСеть
	|		ПО РеестрСверки.Контрагент = КонтрагентыОрганизацииБизнесСеть.Организация
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК КонтрагентыОрганизации
	|		ПО РеестрСверки.Контрагент = КонтрагентыОрганизации.Ссылка
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеПечатныеФормыОбъектов КАК ОсновныеПечатныеФормы
	|		ПО РеестрСверки.Документ = ОсновныеПечатныеФормы.Объект
	|ГДЕ
	|	РеестрСверки.Статус = ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.КОтправке)
	|	И НЕ ОрганизацииБизнесСеть.Идентификатор ЕСТЬ NULL
	|	И Организации.ИНН <> """"
	|	И ВЫБОР
	|		КОГДА Организации.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА Организации.КПП <> """"
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|	И (НЕ КонтрагентыБизнесСеть.Идентификатор ЕСТЬ NULL
	|		ИЛИ НЕ КонтрагентыОрганизацииБизнесСеть.Идентификатор ЕСТЬ NULL)
	|	И ЕСТЬNULL(Контрагенты.ИНН, КонтрагентыОрганизации.ИНН) <> """"
	|	И ВЫБОР
	|		КОГДА Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|				ИЛИ КонтрагентыОрганизации.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ЕСТЬNULL(Контрагенты.КПП, КонтрагентыОрганизации.КПП) <> """"
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|	И РеестрСверки.ДатаДокумента <> ДАТАВРЕМЯ(1, 1, 1)
	|	И РеестрСверки.НомерДокумента <> """"
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Аналитика.КлючАналитики КАК КлючАналитики,
	|	Аналитика.Организация КАК Организация,
	|	Аналитика.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Аналитика.Партнер КАК Партнер,
	|	ВЫБОР КОГДА Аналитика.Договор = НЕОПРЕДЕЛЕНО
	|		ТОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|		ИНАЧЕ Аналитика.Договор
	|	КОНЕЦ КАК ДоговорПартнера,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ТипДоговора, ЕСТЬNULL(ДоговорыОрганизаций.ТипДоговора, НЕОПРЕДЕЛЕНО)) КАК ТипДоговора,
	|	Аналитика.Контрагент КАК Контрагент
	|ПОМЕСТИТЬ АналитикаПоПартнерам
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО Аналитика.Договор = ДоговорыКонтрагентов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыМеждуОрганизациями КАК ДоговорыОрганизаций
	|		ПО Аналитика.Договор = ДоговорыОрганизаций.Ссылка
	|ГДЕ
	|	(Аналитика.Организация, Аналитика.Контрагент) В 
	|		(ВЫБРАТЬ Организация, Контрагент ИЗ ДокументыКОтправке)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючАналитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""РасчетыСКлиентами"" КАК ТипРегистра,
	|	СторнируемыеЗаписи.Период КАК Период,
	|	СторнируемыеЗаписи.ДокументРегистратор КАК Регистратор,
	|	Расчеты.Период КАК ПериодИсправления,
	|	Расчеты.ДокументРегистратор КАК ДокументИсправления
	|ПОМЕСТИТЬ втИсправляемыеЗаписи
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаПоПартнерам КАК Аналитика
	|		ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Сторно КАК ДокументСторно
	|		ПО Расчеты.ДокументРегистратор = ДокументСторно.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК СторнируемыеЗаписи
	|		ПО СторнируемыеЗаписи.ДокументРегистратор = ДокументСторно.СторнируемыйДокумент
	|ГДЕ
	|	Расчеты.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ТИПЗНАЧЕНИЯ(Расчеты.ДокументРегистратор) = ТИП(Документ.Сторно)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""РасчетыСПоставщиками"" КАК ТипРегистра,
	|	СторнируемыеЗаписи.Период КАК Период,
	|	СторнируемыеЗаписи.ДокументРегистратор КАК Регистратор,
	|	Расчеты.Период КАК ПериодИсправления,
	|	Расчеты.ДокументРегистратор КАК ДокументИсправления
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаПоПартнерам КАК Аналитика
	|		ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Сторно КАК ДокументСторно
	|		ПО Расчеты.ДокументРегистратор = ДокументСторно.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК СторнируемыеЗаписи
	|		ПО СторнируемыеЗаписи.ДокументРегистратор = ДокументСторно.СторнируемыйДокумент
	|ГДЕ
	|	Расчеты.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ТИПЗНАЧЕНИЯ(Расчеты.ДокументРегистратор) = ТИП(Документ.Сторно)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""РасчетыПоФинансовымИнструментам"" КАК ТипРегистра,
	|	СторнируемыеЗаписи.Период КАК Период,
	|	СторнируемыеЗаписи.Регистратор КАК Регистратор,
	|	Расчеты.Период КАК ПериодИсправления,
	|	Расчеты.Регистратор КАК ДокументИсправления
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаПоПартнерам КАК Аналитика
	|		ПО Расчеты.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Сторно КАК ДокументСторно
	|		ПО Расчеты.Регистратор = ДокументСторно.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК СторнируемыеЗаписи
	|		ПО СторнируемыеЗаписи.Регистратор = ДокументСторно.СторнируемыйДокумент
	|ГДЕ
	|	Расчеты.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) = ТИП(Документ.Сторно)
	|	И (Расчеты.ТипСуммы В (&ТипыСуммАренды) 
	|		ИЛИ ТИПЗНАЧЕНИЯ(Расчеты.Договор) = ТИП(Справочник.ДоговорыКредитовИДепозитов))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	//Исправляемые записи документами исправления
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""РасчетыСКлиентами"" КАК ТипРегистра,
	|	ИсправляемыеЗаписи.Период КАК Период,
	|	ИсправляемыеЗаписи.ДокументРегистратор КАК Регистратор,
	|	Расчеты.Период КАК ПериодИсправления,
	|	Расчеты.ДокументРегистратор КАК ДокументИсправления
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК ИсправляемыеЗаписи
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтправке КАК ДокументыКОтправке
	|		ПО ИсправляемыеЗаписи.ДокументРегистратор = ДокументыКОтправке.Документ
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК Реестр
	|		ПО ИсправляемыеЗаписи.ДокументРегистратор = Реестр.ИсправляемыйДокумент
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|		ПО Реестр.Ссылка = Расчеты.ДокументРегистратор
	|ГДЕ
	|	Реестр.СторноИсправление
	|	И ТИПЗНАЧЕНИЯ(Расчеты.ДокументРегистратор) <> ТИП(Документ.Сторно)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""РасчетыСПоставщиками"" КАК ТипРегистра,
	|	ИсправляемыеЗаписи.Период КАК Период,
	|	ИсправляемыеЗаписи.ДокументРегистратор КАК Регистратор,
	|	Расчеты.Период КАК ПериодИсправления,
	|	Расчеты.ДокументРегистратор КАК ДокументИсправления
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК ИсправляемыеЗаписи
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтправке КАК ДокументыКОтправке
	|		ПО ИсправляемыеЗаписи.ДокументРегистратор = ДокументыКОтправке.Документ
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК Реестр
	|		ПО ИсправляемыеЗаписи.ДокументРегистратор = Реестр.ИсправляемыйДокумент
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
	|		ПО Реестр.Ссылка = Расчеты.ДокументРегистратор
	|ГДЕ
	|	Реестр.СторноИсправление
	|	И ТИПЗНАЧЕНИЯ(Расчеты.ДокументРегистратор) <> ТИП(Документ.Сторно)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""РасчетыПоФинансовымИнструментам"" КАК ТипРегистра,
	|	ИсправляемыеЗаписи.Период КАК Период,
	|	ИсправляемыеЗаписи.Регистратор КАК Регистратор,
	|	Расчеты.Период КАК ПериодИсправления,
	|	Расчеты.Регистратор КАК ДокументИсправления
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам КАК ИсправляемыеЗаписи
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтправке КАК ДокументыКОтправке
	|		ПО ИсправляемыеЗаписи.Регистратор = ДокументыКОтправке.Документ
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК Реестр
	|		ПО ИсправляемыеЗаписи.Регистратор = Реестр.ИсправляемыйДокумент
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоФинансовымИнструментам КАК Расчеты
	|		ПО Реестр.Ссылка = Расчеты.Регистратор
	|ГДЕ
	|	Реестр.СторноИсправление
	|	И ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) <> ТИП(Документ.Сторно)
	|	И (Расчеты.ТипСуммы В (&ТипыСуммАренды) 
	|		ИЛИ ТИПЗНАЧЕНИЯ(Расчеты.Договор) = ТИП(Справочник.ДоговорыКредитовИДепозитов))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументИсправления
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВложенныйЗапрос.Регистратор) = ТИП(Документ.ОтчетКомитенту)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	КОНЕЦ КАК ТипРасчетов,
	|	ВложенныйЗапрос.ТипДоговора КАК ТипДоговора,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВложенныйЗапрос.Контрагент КАК Контрагент,
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|
	|	ВЫБОР 
	|		КОГДА СУММА(ВложенныйЗапрос.Предоплата) - СУММА(ВложенныйЗапрос.Долг) > 0
	|				И МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА -(СУММА(ВложенныйЗапрос.Предоплата) - СУММА(ВложенныйЗапрос.Долг))
	|		КОГДА СУММА(ВложенныйЗапрос.Долг) - СУММА(ВложенныйЗапрос.Предоплата) > 0
	|				И НЕ МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА СУММА(ВложенныйЗапрос.Долг) - СУММА(ВложенныйЗапрос.Предоплата)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК Дебет,
	|	ВЫБОР 
	|		КОГДА СУММА(ВложенныйЗапрос.ПредоплатаРегл) - СУММА(ВложенныйЗапрос.ДолгРегл) > 0
	|				И МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА -(СУММА(ВложенныйЗапрос.ПредоплатаРегл) - СУММА(ВложенныйЗапрос.ДолгРегл))
	|		КОГДА СУММА(ВложенныйЗапрос.ДолгРегл) - СУММА(ВложенныйЗапрос.ПредоплатаРегл) > 0
	|				И НЕ МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА СУММА(ВложенныйЗапрос.ДолгРегл) - СУММА(ВложенныйЗапрос.ПредоплатаРегл)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК ДебетРегл,
	|	ВЫБОР 
	|		КОГДА СУММА(ВложенныйЗапрос.Долг) - СУММА(ВложенныйЗапрос.Предоплата) > 0
	|				И МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА -(СУММА(ВложенныйЗапрос.Долг) - СУММА(ВложенныйЗапрос.Предоплата))
	|		КОГДА СУММА(ВложенныйЗапрос.Предоплата) - СУММА(ВложенныйЗапрос.Долг) > 0
	|				И НЕ МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА СУММА(ВложенныйЗапрос.Предоплата) - СУММА(ВложенныйЗапрос.Долг)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК Кредит,
	|	ВЫБОР 
	|		КОГДА СУММА(ВложенныйЗапрос.ДолгРегл) - СУММА(ВложенныйЗапрос.ПредоплатаРегл) > 0
	|				И МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА -(СУММА(ВложенныйЗапрос.ДолгРегл) - СУММА(ВложенныйЗапрос.ПредоплатаРегл))
	|		КОГДА СУММА(ВложенныйЗапрос.ПредоплатаРегл) - СУММА(ВложенныйЗапрос.ДолгРегл) > 0
	|				И НЕ МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА СУММА(ВложенныйЗапрос.ПредоплатаРегл) - СУММА(ВложенныйЗапрос.ДолгРегл)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК КредитРегл,
	|	
	|	10 КАК ПорядокТипаПлатежа
	|ПОМЕСТИТЬ Обороты
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ИсправляемыеЗаписи.Регистратор ЕСТЬ НЕ NULL
	|				ТОГДА ИсправляемыеЗаписи.Период
	|			ИНАЧЕ
	|				РасчетыСКлиентами.Период
	|		КОНЕЦ КАК Период,
	|		Аналитика.ТипДоговора КАК ТипДоговора,
	|		Аналитика.Организация КАК Организация,
	|		Аналитика.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		Аналитика.Контрагент КАК Контрагент,
	|		ВЫБОР 
	|			КОГДА ИсправляемыеЗаписи.Регистратор ЕСТЬ НЕ NULL
	|				ТОГДА ЕСТЬNULL(ИсправляемыеЗаписи.Регистратор, НЕОПРЕДЕЛЕНО)
	|			ИНАЧЕ
	|				РасчетыСКлиентами.ДокументРегистратор
	|		КОНЕЦ КАК Регистратор,
	|		РасчетыСКлиентами.Валюта КАК Валюта,
	|		РасчетыСКлиентами.Сторно КАК Сторно,
	|		
	|		ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСКлиентами.Долг
	|			ИНАЧЕ -РасчетыСКлиентами.Долг
	|		КОНЕЦ КАК Долг,
	|		ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСКлиентами.ДолгРегл
	|			ИНАЧЕ -РасчетыСКлиентами.ДолгРегл
	|		КОНЕЦ КАК ДолгРегл,
	|		ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСКлиентами.Предоплата
	|			ИНАЧЕ -РасчетыСКлиентами.Предоплата
	|		КОНЕЦ КАК Предоплата,
	|		ВЫБОР КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСКлиентами.ПредоплатаРегл
	|			ИНАЧЕ -РасчетыСКлиентами.ПредоплатаРегл
	|		КОНЕЦ КАК ПредоплатаРегл
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаПоПартнерам КАК Аналитика
	|			ПО РасчетыСКлиентами.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсправляемыеЗаписи КАК ИсправляемыеЗаписи
	|			ПО РасчетыСКлиентами.ДокументРегистратор = ИсправляемыеЗаписи.ДокументИсправления
	|			И ИсправляемыеЗаписи.ТипРегистра = ""РасчетыСКлиентами""
	|	) КАК ВложенныйЗапрос
	|ГДЕ
	|	ВложенныйЗапрос.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.ТипДоговора,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.ГоловнаяОрганизация,
	|	ВложенныйЗапрос.Контрагент,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.Валюта
	|	
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Долг) <> 0 ИЛИ СУММА(ВложенныйЗапрос.ДолгРегл) <> 0 
	|	ИЛИ СУММА(ВложенныйЗапрос.Предоплата) <> 0 ИЛИ СУММА(ВложенныйЗапрос.ПредоплатаРегл) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВложенныйЗапрос.Регистратор) = ТИП(Документ.ОтчетКомиссионера)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|	КОНЕЦ КАК ТипРасчетов,
	|	ВложенныйЗапрос.ТипДоговора КАК ТипДоговора,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВложенныйЗапрос.Контрагент КАК Контрагент,
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|	
	|	ВЫБОР 
	|		КОГДА СУММА(ВложенныйЗапрос.Долг) - СУММА(ВложенныйЗапрос.Предоплата) > 0
	|				И МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА -(СУММА(ВложенныйЗапрос.Долг) - СУММА(ВложенныйЗапрос.Предоплата))
	|		КОГДА СУММА(ВложенныйЗапрос.Предоплата) - СУММА(ВложенныйЗапрос.Долг) > 0
	|				И НЕ МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА СУММА(ВложенныйЗапрос.Предоплата) - СУММА(ВложенныйЗапрос.Долг)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК Дебет,
	|	ВЫБОР 
	|		КОГДА СУММА(ВложенныйЗапрос.ДолгРегл) - СУММА(ВложенныйЗапрос.ПредоплатаРегл) > 0
	|				И МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА -(СУММА(ВложенныйЗапрос.ДолгРегл) - СУММА(ВложенныйЗапрос.ПредоплатаРегл))
	|		КОГДА СУММА(ВложенныйЗапрос.ПредоплатаРегл) - СУММА(ВложенныйЗапрос.ДолгРегл) > 0
	|				И НЕ МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА СУММА(ВложенныйЗапрос.ПредоплатаРегл) - СУММА(ВложенныйЗапрос.ДолгРегл)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК ДебетРегл,
	|	ВЫБОР 
	|		КОГДА СУММА(ВложенныйЗапрос.Предоплата) - СУММА(ВложенныйЗапрос.Долг) > 0
	|				И МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА -(СУММА(ВложенныйЗапрос.Предоплата) - СУММА(ВложенныйЗапрос.Долг))
	|		КОГДА СУММА(ВложенныйЗапрос.Долг) - СУММА(ВложенныйЗапрос.Предоплата) > 0
	|				И НЕ МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА СУММА(ВложенныйЗапрос.Долг) - СУММА(ВложенныйЗапрос.Предоплата)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК Кредит,
	|	ВЫБОР 
	|		КОГДА СУММА(ВложенныйЗапрос.ПредоплатаРегл) - СУММА(ВложенныйЗапрос.ДолгРегл) > 0
	|				И МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА -(СУММА(ВложенныйЗапрос.ПредоплатаРегл) - СУММА(ВложенныйЗапрос.ДолгРегл))
	|		КОГДА СУММА(ВложенныйЗапрос.ДолгРегл) - СУММА(ВложенныйЗапрос.ПредоплатаРегл) > 0
	|				И НЕ МАКСИМУМ(ВложенныйЗапрос.Сторно)
	|			ТОГДА СУММА(ВложенныйЗапрос.ДолгРегл) - СУММА(ВложенныйЗапрос.ПредоплатаРегл)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК КредитРегл,
	|	
	|	20 КАК ПорядокТипаПлатежа
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ИсправляемыеЗаписи.Регистратор ЕСТЬ НЕ NULL
	|				ТОГДА ИсправляемыеЗаписи.Период
	|			ИНАЧЕ
	|				РасчетыСПоставщиками.Период
	|		КОНЕЦ КАК Период,
	|		Аналитика.ТипДоговора КАК ТипДоговора,
	|		Аналитика.Организация КАК Организация,
	|		Аналитика.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		Аналитика.Контрагент КАК Контрагент,
	|		ВЫБОР
	|			КОГДА ИсправляемыеЗаписи.Регистратор ЕСТЬ НЕ NULL
	|				ТОГДА ЕСТЬNULL(ИсправляемыеЗаписи.Регистратор, НЕОПРЕДЕЛЕНО)
	|			ИНАЧЕ
	|				РасчетыСПоставщиками.ДокументРегистратор
	|		КОНЕЦ КАК Регистратор,
	|		РасчетыСПоставщиками.Валюта КАК Валюта,
	|		РасчетыСПоставщиками.Сторно КАК Сторно,
	|		
	|		ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСПоставщиками.Долг
	|			ИНАЧЕ -РасчетыСПоставщиками.Долг
	|		КОНЕЦ КАК Долг,
	|		ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСПоставщиками.ДолгРегл
	|			ИНАЧЕ -РасчетыСПоставщиками.ДолгРегл
	|		КОНЕЦ КАК ДолгРегл,
	|		ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСПоставщиками.Предоплата
	|			ИНАЧЕ -РасчетыСПоставщиками.Предоплата
	|		КОНЕЦ КАК Предоплата,
	|		ВЫБОР КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА РасчетыСПоставщиками.ПредоплатаРегл
	|			ИНАЧЕ -РасчетыСПоставщиками.ПредоплатаРегл
	|		КОНЕЦ КАК ПредоплатаРегл
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщиками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаПоПартнерам КАК Аналитика
	|			ПО РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсправляемыеЗаписи КАК ИсправляемыеЗаписи
	|			ПО РасчетыСПоставщиками.ДокументРегистратор = ИсправляемыеЗаписи.ДокументИсправления
	|			И ИсправляемыеЗаписи.ТипРегистра = ""РасчетыСПоставщиками""
	|	) КАК ВложенныйЗапрос
	|ГДЕ
	|	ВложенныйЗапрос.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.ТипДоговора,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.ГоловнаяОрганизация,
	|	ВложенныйЗапрос.Контрагент,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.Валюта
	|	
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Долг) <> 0 ИЛИ СУММА(ВложенныйЗапрос.ДолгРегл) <> 0 
	|	ИЛИ СУММА(ВложенныйЗапрос.Предоплата) <> 0 ИЛИ СУММА(ВложенныйЗапрос.ПредоплатаРегл) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.ТипРасчетов КАК ТипРасчетов,
	|	ВложенныйЗапрос.ТипДоговора КАК ТипДоговора,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ВложенныйЗапрос.Контрагент КАК Контрагент,
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|	
	|	СУММА(ВложенныйЗапрос.Дебет) КАК Дебет,
	|	СУММА(ВложенныйЗапрос.ДебетРегл) КАК ДебетРегл,
	|	СУММА(ВложенныйЗапрос.Кредит) КАК Кредит,
	|	СУММА(ВложенныйЗапрос.КредитРегл) КАК КредитРегл,
	|	
	|	ВложенныйЗапрос.ПорядокТипаПлатежа КАК ПорядокТипаПлатежа
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ИсправляемыеЗаписи.Регистратор ЕСТЬ НЕ NULL
	|				ТОГДА ИсправляемыеЗаписи.Период
	|			ИНАЧЕ
	|				РасчетыОбороты.Период
	|		КОНЕЦ КАК Период,
	|		ВЫБОР 
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКредитором)
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|					ИЛИ РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСДебитором)
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Аренда)
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСАрендодателем)
	|		КОНЕЦ КАК ТипРасчетов,
	|		РасчетыОбороты.Договор.ТипДоговора КАК ТипДоговора,
	|		Аналитика.Организация КАК Организация,
	|		Аналитика.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|		Аналитика.Контрагент КАК Контрагент,
	|		ВЫБОР
	|			КОГДА ИсправляемыеЗаписи.Регистратор ЕСТЬ НЕ NULL
	|				ТОГДА ИсправляемыеЗаписи.Регистратор
	|			ИНАЧЕ
	|				РасчетыОбороты.Регистратор
	|		КОНЕЦ КАК Регистратор,
	|		РасчетыОбороты.Валюта КАК Валюта,
	|	
	|		РасчетыОбороты.СуммаПриход КАК Дебет,
	|		РасчетыОбороты.СуммаРеглПриход КАК ДебетРегл,
	|		РасчетыОбороты.СуммаРасход КАК Кредит,
	|		РасчетыОбороты.СуммаРеглРасход КАК КредитРегл,
	|	
	|		ВЫБОР 
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм)
	|				ТОГДА 31
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Депозит)
	|					ИЛИ РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.ЗаймВыданный)
	|				ТОГДА 32
	|			КОГДА РасчетыОбороты.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерыДоговоровФинансовыхИнструментов.Аренда)
	|				ТОГДА 33
	|		КОНЕЦ КАК ПорядокТипаПлатежа
	|	ИЗ
	|		РегистрНакопления.РасчетыПоФинансовымИнструментам.Обороты(
	|			&ДатаНачала, &ДатаОкончания, Регистратор,
	|			ТипСуммы В (&ТипыСуммАренды) ИЛИ ТИПЗНАЧЕНИЯ(Договор) = ТИП(Справочник.ДоговорыКредитовИДепозитов)
	|		) КАК РасчетыОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаПоПартнерам КАК Аналитика
	|			ПО РасчетыОбороты.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИсправляемыеЗаписи КАК ИсправляемыеЗаписи
	|			ПО РасчетыОбороты.Регистратор = ИсправляемыеЗаписи.ДокументИсправления
	|			И ИсправляемыеЗаписи.ТипРегистра = ""РасчетыПоФинансовымИнструментам""
	|	ГДЕ
	|		РасчетыОбороты.СуммаОборот <> 0
	|	) КАК ВложенныйЗапрос
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ВложенныйЗапрос.Регистратор) <> ТИП(Документ.РасчетКурсовыхРазниц)
	|	И (&ЗаВесьПериод
	|		ИЛИ ВложенныйЗапрос.Период >= &ДатаНачала И ВложенныйЗапрос.Период <= &ДатаОкончания)
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.ТипРасчетов,
	|	ВложенныйЗапрос.ТипДоговора,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.ГоловнаяОрганизация,
	|	ВложенныйЗапрос.Контрагент,
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.Валюта,
	|	ВложенныйЗапрос.ПорядокТипаПлатежа
	|	
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Дебет) <> 0 ИЛИ СУММА(ВложенныйЗапрос.ДебетРегл) <> 0
	|	ИЛИ СУММА(ВложенныйЗапрос.Кредит) <> 0 ИЛИ СУММА(ВложенныйЗапрос.КредитРегл) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент,
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ТипРасчетов КАК ТипРасчетов,
	|	МАКСИМУМ(ВложенныйЗапрос.Ссылка) КАК Ссылка,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Контрагент КАК Контрагент,
	|	ВложенныйЗапрос.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ ДанныеСчетовФактур
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
	|		Основания.Ссылка КАК Ссылка,
	|		СчетФактура.Организация КАК Организация,
	|		СчетФактура.Контрагент КАК Контрагент,
	|		Основания.ДокументОснование КАК ДокументОснование
	|	ИЗ
	|		Документ.СчетФактураВыданный.ДокументыОснования КАК Основания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактура
	|		ПО СчетФактура.Ссылка = Основания.Ссылка
	|	ГДЕ
	|		Основания.ДокументОснование В
	|			(ВЫБРАТЬ Документ ИЗ ДокументыКОтправке)
	|		И СчетФактура.Проведен
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком) КАК ТипРасчетов,
	|		Основания.Ссылка КАК Ссылка,
	|		СчетФактура.Организация КАК Организация,
	|		СчетФактура.Контрагент КАК Контрагент,
	|		Основания.ДокументОснование КАК ДокументОснование
	|	ИЗ
	|		Документ.СчетФактураПолученный.ДокументыОснования КАК Основания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный КАК СчетФактура
	|		ПО СчетФактура.Ссылка = Основания.Ссылка
	|	ГДЕ
	|		Основания.ДокументОснование В
	|			(ВЫБРАТЬ Документ ИЗ ДокументыКОтправке)
	|		И СчетФактура.Проведен
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	//для интеркампани выведем счет фактуры организации контрагента
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом) КАК ТипРасчетов,
	|		Основания.Ссылка КАК Ссылка,
	|		СчетФактура.Контрагент КАК Организация,
	|		СчетФактура.Организация КАК Контрагент,
	|		Основания.ДокументОснование КАК ДокументОснование
	|	ИЗ
	|		Документ.СчетФактураВыданный.ДокументыОснования КАК Основания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактура
	|		ПО СчетФактура.Ссылка = Основания.Ссылка
	|	ГДЕ
	|		Основания.ДокументОснование В
	|			(ВЫБРАТЬ Документ ИЗ ДокументыКОтправке)
	|		И СчетФактура.Проведен
	|		И ТИПЗНАЧЕНИЯ(СчетФактура.Контрагент) = ТИП(Справочник.Организации)
	|	) КАК ВложенныйЗапрос
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ТипРасчетов,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Контрагент,
	|	ВложенныйЗапрос.ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеСчетовФактур.ТипРасчетов КАК ТипРасчетов,
	|	ДанныеСчетовФактур.Ссылка КАК Ссылка,
	|	ДанныеСчетовФактур.Организация КАК Организация,
	|	ДанныеСчетовФактур.Контрагент КАК Контрагент,
	|	ДанныеСчетовФактур.ДокументОснование КАК ДокументОснование,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры <> """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ, ЛОЖЬ) КАК ЭтоКорректировочныйСчетФактура,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры <> """"
	|				ТОГДА ЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры
	|			КОГДА ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры <> """"
	|				ТОГДА ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры
	|			ИНАЧЕ ЖурналУчетаСчетовФактур.НомерСчетаФактуры
	|		КОНЕЦ, """") КАК НомерСчетаФактуры,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры
	|			КОГДА ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры
	|			ИНАЧЕ ЖурналУчетаСчетовФактур.ДатаСчетаФактуры
	|		КОНЕЦ, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСчетаФактуры,
	|	СУММА(ЕСТЬNULL(ЖурналУчетаСчетовФактур.СуммаПоСчетуФактуре, 0)) КАК СуммаПоСчетуФактуре,
	|	СУММА(ЕСТЬNULL(ЖурналУчетаСчетовФактур.СуммаНДС, 0)) КАК СуммаНДС,
	|	СУММА(ЕСТЬNULL(ЖурналУчетаСчетовФактур.СуммаНДСРазницаУменьшение, 0)) КАК СуммаНДСРазницаУменьшение,
	|	СУММА(ЕСТЬNULL(ЖурналУчетаСчетовФактур.СуммаНДСРазницаУвеличение, 0)) КАК СуммаНДСРазницаУвеличение
	|ПОМЕСТИТЬ ДанныеПоНДС
	|ИЗ
	|	ДанныеСчетовФактур КАК ДанныеСчетовФактур
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЖурналУчетаСчетовФактур КАК ЖурналУчетаСчетовФактур
	|		ПО ДанныеСчетовФактур.Ссылка = ЖурналУчетаСчетовФактур.Регистратор
	|		И ДанныеСчетовФактур.Организация = ЖурналУчетаСчетовФактур.Организация
	|		И ДанныеСчетовФактур.Контрагент = ЖурналУчетаСчетовФактур.Контрагент
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеСчетовФактур.ТипРасчетов,
	|	ДанныеСчетовФактур.Ссылка,
	|	ДанныеСчетовФактур.Организация,
	|	ДанныеСчетовФактур.Контрагент,
	|	ДанныеСчетовФактур.ДокументОснование,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры <> """"
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ, ЛОЖЬ),
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры <> """"
	|				ТОГДА ЖурналУчетаСчетовФактур.НомерИсправленияКорректировочногоСчетаФактуры
	|			КОГДА ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры <> """"
	|				ТОГДА ЖурналУчетаСчетовФактур.НомерКорректировочногоСчетаФактуры
	|			ИНАЧЕ ЖурналУчетаСчетовФактур.НомерСчетаФактуры
	|		КОНЕЦ, """"),
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ЖурналУчетаСчетовФактур.ДатаИсправленияКорректировочногоСчетаФактуры
	|			КОГДА ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ЖурналУчетаСчетовФактур.ДатаКорректировочногоСчетаФактуры
	|			ИНАЧЕ ЖурналУчетаСчетовФактур.ДатаСчетаФактуры
	|		КОНЕЦ, ДАТАВРЕМЯ(1, 1, 1))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование,
	|	ТипРасчетов,
	|	Организация,
	|	Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Обороты.Валюта, НЕОПРЕДЕЛЕНО) КАК Валюта,
	|	Обороты.Валюта ЕСТЬ НЕ NULL КАК ЕстьОбороты,
	|	СУММА(ЕСТЬNULL(Обороты.ДебетРегл - Обороты.КредитРегл, 0)) КАК Сумма,
	|	СУММА(ЕСТЬNULL(Обороты.Дебет - Обороты.Кредит, 0)) КАК ВалютнаяСумма,
	|	ДокументыКОтправке.Документ КАК Документ,
	|	ДокументыКОтправке.РаспечатанУПД КАК РаспечатанУПД,
	|	ДокументыКОтправке.ЭтоВзаимозачет,
	|	ДокументыКОтправке.НомерДокумента КАК НомерДокумента,
	|	ДокументыКОтправке.ДатаДокумента КАК ДатаДокумента,
	|	ДокументыКОтправке.НомерРегистратора КАК НомерРегистратора,
	|	ДокументыКОтправке.ДатаРегистратора КАК ДатаРегистратора,
	|	ДокументыКОтправке.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	МАКСИМУМ(ЕСТЬNULL(Обороты.ТипРасчетов, НЕОПРЕДЕЛЕНО)) КАК ТипРасчетов,
	|	МАКСИМУМ(ЕСТЬNULL(Обороты.ТипДоговора, НЕОПРЕДЕЛЕНО)) КАК ТипДоговора,
	|	ДокументыКОтправке.Организация КАК Организация,
	|	ДокументыКОтправке.Контрагент КАК Контрагент,
	|	СУММА(ЕСТЬNULL(Обороты.ДебетРегл, 0)) КАК ДебетРегл,
	|	СУММА(ЕСТЬNULL(Обороты.КредитРегл, 0)) КАК КредитРегл,
	|	СУММА(ЕСТЬNULL(Обороты.Дебет, 0)) КАК Дебет,
	|	СУММА(ЕСТЬNULL(Обороты.Кредит, 0)) КАК Кредит,
	|	ДокументыКОтправке.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
	|	ДокументыКОтправке.НаименованиеКонтрагента КАК НаименованиеКонтрагента,
	|	ДокументыКОтправке.НаименованиеКонтрагентаПолное КАК НаименованиеКонтрагентаПолное,
	|	ДокументыКОтправке.ИННКонтрагента КАК ИННКонтрагента,
	|	ДокументыКОтправке.КППКонтрагента КАК КППКонтрагента,
	|	ДокументыКОтправке.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
	|	ДокументыКОтправке.ИНН КАК ИНН,
	|	ДокументыКОтправке.КПП КАК КПП,
	|	ДокументыКОтправке.НаименованиеПолное КАК НаименованиеПолное,
	|	ДокументыКОтправке.Наименование КАК Наименование,
	|	ДокументыКОтправке.ЦифровойИндексОбособленногоПодразделения КАК ЦифровойИндексОбособленногоПодразделения
	|ПОМЕСТИТЬ РеестрСОборотами
	|ИЗ
	|	ДокументыКОтправке КАК ДокументыКОтправке
	|		ЛЕВОЕ СОЕДИНЕНИЕ Обороты КАК Обороты
	|		ПО ДокументыКОтправке.Организация = Обороты.Организация
	|			И ДокументыКОтправке.Контрагент = Обороты.Контрагент
	|			И ДокументыКОтправке.Документ = Обороты.Регистратор
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Обороты.Регистратор) <> ТИП(Документ.РасчетКурсовыхРазниц)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыКОтправке.Организация,
	|	ДокументыКОтправке.Контрагент,
	|	ДокументыКОтправке.Документ,
	|	ДокументыКОтправке.ИдентификаторДокумента,
	|	ДокументыКОтправке.ИдентификаторОрганизации,
	|	ДокументыКОтправке.ИНН,
	|	ДокументыКОтправке.КПП,
	|	ДокументыКОтправке.НаименованиеПолное,
	|	ДокументыКОтправке.Наименование,
	|	ДокументыКОтправке.ЦифровойИндексОбособленногоПодразделения,
	|	ЕСТЬNULL(Обороты.Валюта, НЕОПРЕДЕЛЕНО),
	|	Обороты.Валюта ЕСТЬ НЕ NULL,
	|	ДокументыКОтправке.РаспечатанУПД,
	|	ДокументыКОтправке.ЭтоВзаимозачет,
	|	ДокументыКОтправке.НомерДокумента,
	|	ДокументыКОтправке.ДатаДокумента,
	|	ДокументыКОтправке.НомерРегистратора,
	|	ДокументыКОтправке.ДатаРегистратора,
	|	ДокументыКОтправке.ИдентификаторКонтрагента,
	|	ДокументыКОтправке.НаименованиеКонтрагента,
	|	ДокументыКОтправке.НаименованиеКонтрагентаПолное,
	|	ДокументыКОтправке.ИННКонтрагента,
	|	ДокументыКОтправке.КППКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеестрСОборотами.Валюта КАК Валюта,
	|	РеестрСОборотами.Сумма КАК Сумма,
	|	РеестрСОборотами.ВалютнаяСумма КАК ВалютнаяСумма,
	|	ЕСТЬNULL(ДанныеПоНДС.СуммаНДС + ДанныеПоНДС.СуммаНДСРазницаУменьшение + ДанныеПоНДС.СуммаНДСРазницаУвеличение, 0) КАК СуммаНДС,
	|	РеестрСОборотами.Документ КАК Документ,
	|	ВЫБОР КОГДА РеестрСОборотами.РаспечатанУПД И ЕСТЬNULL(ДанныеПоНДС.НомерСчетаФактуры, """") <> """"
	|			ТОГДА
	|				ЕСТЬNULL(ДанныеПоНДС.НомерСчетаФактуры, """")
	|		ИНАЧЕ
	|			РеестрСОборотами.НомерДокумента 
	|	КОНЕЦ КАК НомерДокумента,
	|	ВЫБОР КОГДА РеестрСОборотами.РаспечатанУПД 
	|				И ЕСТЬNULL(ДанныеПоНДС.ДатаСчетаФактуры, ДАТАВРЕМЯ(1, 1, 1)) <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА 
	|				ЕСТЬNULL(ДанныеПоНДС.ДатаСчетаФактуры, ДАТАВРЕМЯ(1, 1, 1))
	|		ИНАЧЕ
	|			РеестрСОборотами.ДатаДокумента 
	|	КОНЕЦ КАК ДатаДокумента,
	|	РеестрСОборотами.НомерРегистратора КАК НомерРегистратора,
	|	РеестрСОборотами.ДатаРегистратора КАК ДатаРегистратора,
	|	ЕСТЬNULL(ДанныеПоНДС.НомерСчетаФактуры, """") КАК НомерСчетаФактуры,
	|	ЕСТЬNULL(ДанныеПоНДС.ДатаСчетаФактуры, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСчетаФактуры,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДанныеПоНДС.НомерСчетаФактуры, """") ПОДОБНО ""%[А-Я]%""
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОбрабатыватьНомерСФ,
	|	ЕСТЬNULL(ДанныеПоНДС.ЭтоКорректировочныйСчетФактура, ЛОЖЬ) КАК ЭтоКорректировочныйСчетФактура,
	|	РеестрСОборотами.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	ВЫБОР 
	|		КОГДА НЕ РеестрСОборотами.ЕстьОбороты
	|			ИЛИ РеестрСОборотами.ЭтоВзаимозачет И РеестрСОборотами.Сумма = 0
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.ОтправленоКУдалению)
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.Получено)
	|	КОНЕЦ КАК Статус,
	|	РеестрСОборотами.ТипРасчетов КАК ТипРасчетов,
	|	РеестрСОборотами.ТипДоговора КАК ТипДоговора,
	|	РеестрСОборотами.Организация КАК Организация,
	|	РеестрСОборотами.Контрагент КАК Контрагент,
	|	РеестрСОборотами.ДебетРегл КАК ДебетРегл,
	|	РеестрСОборотами.КредитРегл КАК КредитРегл,
	|	РеестрСОборотами.Дебет КАК Дебет,
	|	РеестрСОборотами.Кредит КАК Кредит,
	|	РеестрСОборотами.ИдентификаторКонтрагента КАК ИдентификаторКонтрагента,
	|	РеестрСОборотами.НаименованиеКонтрагента КАК НаименованиеКонтрагента,
	|	РеестрСОборотами.НаименованиеКонтрагентаПолное КАК НаименованиеКонтрагентаПолное,
	|	РеестрСОборотами.ИННКонтрагента КАК ИННКонтрагента,
	|	РеестрСОборотами.КППКонтрагента КАК КППКонтрагента,
	|	РеестрСОборотами.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
	|	РеестрСОборотами.ИНН КАК ИНН,
	|	РеестрСОборотами.КПП КАК КПП,
	|	РеестрСОборотами.НаименованиеПолное КАК НаименованиеПолное,
	|	РеестрСОборотами.Наименование КАК Наименование,
	|	РеестрСОборотами.ЦифровойИндексОбособленногоПодразделения КАК ЦифровойИндексОбособленногоПодразделения
	|ИЗ
	|	РеестрСОборотами КАК РеестрСОборотами
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоНДС КАК ДанныеПоНДС
	|		ПО ДанныеПоНДС.ДокументОснование = РеестрСОборотами.Документ
	|			И ДанныеПоНДС.Организация = РеестрСОборотами.Организация
	|			И ДанныеПоНДС.Контрагент = РеестрСОборотами.Контрагент
	|ИТОГИ
	|	МАКСИМУМ(ИдентификаторОрганизации),
	|	МАКСИМУМ(ИНН),
	|	МАКСИМУМ(КПП),
	|	МАКСИМУМ(НаименованиеПолное),
	|	МАКСИМУМ(Наименование)
	|ПО
	|	Организация";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

// Возвращает данные из хранилища.
//
// Параметры:
//  Владелец - ПланОбменаСсылка, СправочникСсылка, Строка - 
//  Ключ    - Строка - Содержит список имен сохраненных данных, указанных через запятую.
// 
// Возвращаемое значение:
//  Произвольный, Структура, Неопределено - Данные из хранилища. Если указан один ключ,
//							то возвращается его значение, иначе структура.
//							Если данные отсутствуют - Неопределено.
//
Функция ПрочитатьДанныеИзХранилища(Владелец, Ключ) Экспорт
	
	Возврат ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Владелец, Ключ);
	
КонецФункции

// Записывает данные в хранилище.
//
// Параметры:
//  Владелец - ПланОбменаСсылка, СправочникСсылка, Строка - 
//
//  Данные  - Произвольный - Данные помещаемые хранилище. Неопределено - удаляет все данные.
//			 Для удаления данных по ключу следует использовать процедуру УдалитьДанныеИзХранилища.
//  Ключ	- Строка	   - Ключ сохраняемых настроек. Ключ должен соответствовать правилам, установленным для идентификаторов:
//						   Первым символом ключа должна быть буква или символ подчеркивания (_).
//						   Каждый из последующих символов может быть буквой, цифрой или символом подчеркивания (_).
//
Процедура ЗаписатьДанныеВХранилище(Владелец, Данные, Ключ) Экспорт
	
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Владелец, Данные, Ключ);
	
КонецПроцедуры

// Ищет контрагента по связке ИНН/КПП, заполняет структуру сведений о контрагенте
//
// Параметры:
//  РеквизитыКонтрагента - Структура из КлючИЗначение - со свойствами ИНН, КПП.
//  СведенияОКонтрагенте - Структура из КлючИЗначение - со свойствами:
//   *ИНН - Строка - заполняется, когда не найден по ИНН
//   *КПП - Строка - заполняется, когда не найден по КПП
//	Значение - СправочникСсылка.Контрагент - ссылка на справочник, если не найден, то пустая ссылка.
//
Процедура СведенияКонтрагентаПоИНН_КПП(РеквизитыКонтрагента, СведенияОКонтрагенте) Экспорт
	
	НайденныйКонтрагент = Справочники.Контрагенты.ПустаяСсылка();
	
	ИНН = Неопределено;
	
	Если НЕ РеквизитыКонтрагента.Свойство("ИНН", ИНН) ИЛИ НЕ ЗначениеЗаполнено(ИНН) Тогда
		СведенияОКонтрагенте.Значение = НайденныйКонтрагент; 
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Контрагенты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН = &ИНН И Контрагенты.КПП = &КПП
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Организации.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ИНН = &ИНН И Организации.КПП = &КПП";
		
		КПП = Неопределено;
		Если НЕ РеквизитыКонтрагента.Свойство("КПП", КПП) Тогда
			КПП = "";
		КонецЕсли;
		Запрос.УстановитьПараметр("ИНН", ИНН);
		Запрос.УстановитьПараметр("КПП", КПП);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СведенияОКонтрагенте.Значение = Выборка.Ссылка;
		КонецЕсли;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(СведенияОКонтрагенте.Значение) Тогда
		СведенияОКонтрагенте.КППКонтрагента = РеквизитыКонтрагента.КПП;
		// Из сервиса может вернуться неактуальный КПП, произведем поиск контрагента по ИНН
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Контрагенты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН = &ИНН
		|	И Контрагенты.ПометкаУдаления = ЛОЖЬ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Организации.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ИНН = &ИНН
		|	И Организации.ПометкаУдаления = ЛОЖЬ";
		Запрос.УстановитьПараметр("ИНН", РеквизитыКонтрагента.ИНН);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			СведенияОКонтрагенте.Значение  = Выборка.Ссылка;
		Иначе
			СведенияОКонтрагенте.ИННКонтрагента = РеквизитыКонтрагента.ИНН;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Установить права служебного пользователя сверки расчетов.
// 
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи
Процедура УстановитьПраваСлужебногоПользователяСверкиРасчетов(Пользователь) Экспорт
	
	Роли = Новый Массив;
	
	Роли.Добавить("ИспользованиеСервисаСверкиРасчетов");
	Роли.Добавить("БазовыеПраваБСП");
	Роли.Добавить("БазовыеПраваЭД");
	Роли.Добавить("ДобавлениеИзменениеДанныхБухгалтерии");
	Роли.Добавить("ДобавлениеИзменениеНастроекБизнесСеть");
	
	СвойстваПользователя = Неопределено;
	ИдентификаторПользователяИБ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "ИдентификаторПользователяИБ");
	Если ЗначениеЗаполнено(ИдентификаторПользователяИБ) Тогда
		СвойстваПользователя =  Пользователи.СвойстваПользователяИБ(ИдентификаторПользователяИБ);
	КонецЕсли;
	
	Если СвойстваПользователя = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СвойстваПользователя.Роли, Роли, Истина);
	СвойстваДляЗаписи = Новый Структура("Роли", СвойстваПользователя.Роли);
	Пользователи.УстановитьСвойстваПользователяИБ(СвойстваПользователя.УникальныйИдентификатор, СвойстваДляЗаписи);
	
КонецПроцедуры

// Проверяет нужно ли оповещать пользователя о расхождениях.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - ссылка на пользователя, которого нужно проверить на необходимость оповестить
//
// Возвращаемое значение:
//   Булево - признак того, что указанного пользователя нужно оповещать о расхождениях.
//
Функция ОповещатьПользователяОРасхождениях(Пользователь) Экспорт
	
	// Оповещаем всех пользователей которым доступна роль ИспользованиеСервисаСверкиРасчетов
	Возврат Пользователи.РолиДоступны("ИспользованиеСервисаСверкиРасчетов", Пользователь, Ложь);
	
КонецФункции

// Возвращает организации, которые нужно подключить к сверке.
//
// Параметры:
//  ТолькоСДвижениями - булево - признак того, что нужно регистрировать только те организации
//  по которым есть движения/документы.
//
// Возвращаемое значение:
//   Массив из СправочникСсылка.Организации - Организации для подключения.
//
Функция ОрганизацииДляПодключения(ТолькоСДвижениями = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодключенныеОрганизации", БизнесСеть.ПодключенныеОрганизации());
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодключенныеОрганизации", БизнесСеть.ПодключенныеОрганизации());
	Запрос.УстановитьПараметр("НачалоПериода", СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов());
	Запрос.УстановитьПараметр("УправленческаяОрганизация", Справочники.Организации.УправленческаяОрганизация);
	Если ТолькоСДвижениями И ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		// Единственную организацию подключаем "под капотом" вне зависимости от наличия движений.
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПодключенныеОрганизации.Организация КАК Организация
		|ПОМЕСТИТЬ ПодключенныеОрганизации
		|ИЗ
		|	&ПодключенныеОрганизации КАК ПодключенныеОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Организация
		|ПОМЕСТИТЬ втОрганизацииКПодключению
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПодключенныеОрганизации КАК ПодключенныеОрганизации
		|		ПО Организации.Ссылка = ПодключенныеОрганизации.Организация
		|ГДЕ
		|	НЕ Организации.ПометкаУдаления
		|	И Организации.Ссылка <> &УправленческаяОрганизация
		|	И ПодключенныеОрганизации.Организация ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВложенныйЗапрос.Организация КАК Организация
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
		|			ПО РасчетыСКлиентами.АналитикаУчетаПоПартнерам = Аналитика.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОрганизацииКПодключению КАК ОрганизацииКПодключению
		|			ПО Аналитика.Организация = ОрганизацииКПодключению.Организация
		|	ГДЕ
		|		РасчетыСКлиентами.Период >= &НачалоПериода
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщиками
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
		|			ПО РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = Аналитика.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОрганизацииКПодключению КАК ОрганизацииКПодключению
		|			ПО Аналитика.Организация = ОрганизацииКПодключению.Организация
		|	ГДЕ
		|		РасчетыСПоставщиками.Период >= &НачалоПериода
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		РасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам.Организация КАК Организация
		|	ИЗ
		|		РегистрНакопления.РасчетыПоФинансовымИнструментам КАК РасчетыПоФинансовымИнструментам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
		|			ПО РасчетыПоФинансовымИнструментам.АналитикаУчетаПоПартнерам = Аналитика.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОрганизацииКПодключению КАК ОрганизацииКПодключению
		|			ПО Аналитика.Организация = ОрганизацииКПодключению.Организация
		|	ГДЕ
		|		РасчетыПоФинансовымИнструментам.Период >= &НачалоПериода) КАК ВложенныйЗапрос";
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПодключенныеОрганизации.Организация КАК Организация
		|ПОМЕСТИТЬ ПодключенныеОрганизации
		|ИЗ
		|	&ПодключенныеОрганизации КАК ПодключенныеОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Организация
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПодключенныеОрганизации КАК ПодключенныеОрганизации
		|		ПО Организации.Ссылка = ПодключенныеОрганизации.Организация
		|ГДЕ
		|	НЕ Организации.ПометкаУдаления
		|	И Организации.Ссылка <> &УправленческаяОрганизация
		|	И ПодключенныеОрганизации.Организация ЕСТЬ NULL";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	ОрганизацииДляПодключения = Запрос.Выполнить().Выгрузить();
	
	Возврат ОрганизацииДляПодключения.ВыгрузитьКолонку("Организация");//Массив - 
	
КонецФункции

// Регистрирует документы к сверке с даты начала работы сверки расчетов (см. СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов)
//
// Параметры:
//  МассивОрганизаций  - массив - если передан, регистрируются документы только указанных организаций.
//  МассивКонтрагентов - массив - если передан, регистрируются документы только указанных контрагентов.
//
Процедура ЗарегистрироватьКСверкеРанееВведенныеДокументы(МассивОрганизаций, МассивКонтрагентов) Экспорт
	
	ТолькоНеОтправленныеНаСверку = Истина;
	Если ЗначениеЗаполнено(МассивОрганизаций) Тогда
		ТолькоНеОтправленныеНаСверку = Ложь;
	Иначе
		ПодключенныеОрганизации = СервисСверкиРасчетов.ПодключенныеОрганизации();
		МассивОрганизаций = ПодключенныеОрганизации.ВыгрузитьКолонку("Организация");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(МассивКонтрагентов) Тогда
		ТолькоНеОтправленныеНаСверку = Ложь;
	Иначе
		МассивКонтрагентов = СервисСверкиРасчетов.КонтрагентыДляСверки();
	КонецЕсли;
	
	ДатаНачалаСверкиРасчетов = СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов();
	ДатаОкончанияСверкиРасчетов = КонецКвартала(ТекущаяДатаСеанса());
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала",         ДатаНачалаСверкиРасчетов);
	Запрос.УстановитьПараметр("ДатаОкончания",      КонецДня(ДатаОкончанияСверкиРасчетов));
	Запрос.УстановитьПараметр("МассивОрганизаций",  МассивОрганизаций);
	Запрос.УстановитьПараметр("МассивКонтрагентов", МассивКонтрагентов);
	Запрос.УстановитьПараметр("ТипыСуммАренды", Документы.СверкаВзаиморасчетов2_5_11.ТипыСуммАрендыДляСверки());

	Если ТолькоНеОтправленныеНаСверку Тогда
		// Сначала убедимся, что есть хотя бы один незарегистрированный к сверке документ.
		Запрос.Текст = СервисСверкиРасчетовУТ.ТекстЗапросаНаличиеНезарегистрированныхДокументов();
		Результат = Запрос.Выполнить();
		Если Результат.Пустой() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Документы к регистрации найдены, выполняем запрос по всем незарегистрированным документам.
	Запрос.УстановитьПараметр("ТолькоНеОтправленныеНаСверку", ТолькоНеОтправленныеНаСверку);
	ДокументыКСверке = СервисСверкиРасчетовУТ.ДокументыКСверке(Запрос.Параметры);
	
	Для Каждого ДанныеКСверке Из ДокументыКСверке Цикл
		
		Если Не СервисСверкиРасчетов.ДокументПодлежитСверке(ДанныеКСверке.РасчетныйДокумент) Тогда
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(ДанныеКСверке.Организация);
		НаборЗаписей.Отбор.Контрагент.Установить(ДанныеКСверке.Контрагент);
		НаборЗаписей.Отбор.Документ.Установить(ДанныеКСверке.РасчетныйДокумент);
		
		Запись = НаборЗаписей.Добавить();
		Запись.Организация            = ДанныеКСверке.Организация;
		Запись.Контрагент             = ДанныеКСверке.Контрагент;
		Запись.Документ               = ДанныеКСверке.РасчетныйДокумент;
		Если ЗначениеЗаполнено(ДанныеКСверке.ДатаДокумента) Тогда
			Запись.ДатаДокумента      = ДанныеКСверке.ДатаДокумента;
		Иначе
			Запись.ДатаДокумента      = ДанныеКСверке.ДатаДокументаИБ;
		КонецЕсли;
		Если ЗначениеЗаполнено(ДанныеКСверке.НомерДокумента) Тогда
			ПолныйНомер = ДанныеКСверке.НомерДокумента;
		Иначе
			ПолныйНомер = ДанныеКСверке.НомерДокументаИБ;
		КонецЕсли;
		Запись.НомерДокумента         = СервисСверкиРасчетов.НомерБезПрефиксовИЛидирующихНулей(ПолныйНомер);
		Запись.Статус                 = Перечисления.СервисСверкиРасчетовСтатусыДокументов.КОтправке;
		Запись.ИдентификаторДокумента = ДанныеКСверке.РасчетныйДокумент.УникальныйИдентификатор();
		Запись.ИдентификаторСообщения = "";
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает признак того, что документ является счетом-фактурой.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - документ, который проверяется на соответствие типу счета-фактуры.
//
// Возвращаемое значение:
//  Булево - признак того, что документ является счетом-фактурой.
//
Функция ЭтоСчетФактура(ДокументОбъект) Экспорт
	
	Возврат ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетФактураВыданный")
		ИЛИ ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетФактураПолученный");
	
КонецФункции

// Регистрирует основания счета-фактуры к обмену. Сам счет-фактура не регистрируется к обмену,
// сведения о счете-фактуре отправляются вместе со сведениями об основании.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - документ-основание счета-фактуры.
//
Процедура ЗарегистрироватьОснованияСФКОбмену(ДокументОбъект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Для указанных видов счетов-фактур регистрируем все незарегистрированные основания к отправке,
	// для выданных УПД регистрируем основание повторно для актуализации номера.
	Если НЕ (ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетФактураВыданный")
			ИЛИ ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетФактураПолученный")) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Основания.ДокументОснование КАК Ссылка,
	|	СчетФактура.Организация КАК Организация,
	|	СчетФактура.Контрагент КАК Контрагент
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК Основания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактура
	|		ПО Основания.Ссылка = СчетФактура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|		ПО Основания.ДокументОснование = СервисСверкиРасчетовРеестрДокументов.Документ
	|			И (СервисСверкиРасчетовРеестрДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.КОтправке))
	|ГДЕ
	|	СервисСверкиРасчетовРеестрДокументов.Документ ЕСТЬ NULL
	|	И Основания.Ссылка = &ДокументСсылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Основания.ДокументОснование КАК Ссылка,
	|	СчетФактура.Контрагент КАК Организация,
	|	СчетФактура.Организация КАК Контрагент
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК Основания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактура
	|		ПО Основания.Ссылка = СчетФактура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
	|		ПО Основания.ДокументОснование = СервисСверкиРасчетовРеестрДокументов.Документ
	|			И (СервисСверкиРасчетовРеестрДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СервисСверкиРасчетовСтатусыДокументов.КОтправке))
	|ГДЕ
	|	СервисСверкиРасчетовРеестрДокументов.Документ ЕСТЬ NULL
	|	И Основания.Ссылка = &ДокументСсылка
	|	И ТИПЗНАЧЕНИЯ(СчетФактура.Контрагент) = ТИП(Справочник.Организации)";
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетФактураПолученный") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СчетФактураВыданный", "СчетФактураПолученный");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ЭтоВыданныйСФ", ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СчетФактураВыданный"));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		СписокДокументовКОбмену = РезультатЗапроса.Выгрузить();
		Для Каждого ДокументКОбмену Из СписокДокументовКОбмену Цикл
			Если Не СервисСверкиРасчетов.ДокументПодлежитСверке(ДокументКОбмену.Ссылка) Тогда
				Продолжить;
			КонецЕсли;
			СервисСверкиРасчетов.ЗарегистрироватьДокументКОтправке(ДокументКОбмену.Организация, ДокументКОбмену.Контрагент, ДокументКОбмену.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Удаляет повторяющиеся элементы массива.
//
// Параметры:
//  ОбрабатываемыйМассив - Массив - элементы произвольных типов, из которых удаляются неуникальные.
//
// Возвращаемое значение:
//   Массив - элементы ОбрабатываемыйМассив после удаления лишних.
//
Функция УдалитьПовторяющиесяЭлементыМассива(ОбрабатываемыйМассив) Экспорт
	
	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОбрабатываемыйМассив);
	
КонецФункции

// Возвращает сведения для регистрации организации на сервисе сверки.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - ссылка на организацию, которую требуется зарегистрировать на сервисе
//  Период - Дата - дата получения сведений по организации
//
// Возвращаемое значение:
//  Структура из КлючИЗначение: 
//   *НаименованиеДляПечатныхФорм - Строка
//   *ЮридическоеФизическоеЛицо - ПеречислениеСсылка.ЮрФизЛицо
//   *ИНН - Строка
//   *КПП - Строка
//   *ДолжностьРуководителяПредставление - Строка
//   *Руководитель - Строка
//   *ФИОФизлица - Строка
//   *Телефоны - Строка
//   *Email - Строка
//
Функция СведенияОЮрФизЛице(Организация, Период = '00010101') Экспорт
	
	СведенияОЮрФизЛице = Новый Структура();
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "Наименование,ИНН,КПП,ЮрФизЛицо");
	СведенияОЮрФизЛице.Вставить("ЮридическоеФизическоеЛицо",   РеквизитыОрганизации.ЮрФизЛицо); // в потребителе может быть перечисление
	СведенияОЮрФизЛице.Вставить("НаименованиеДляПечатныхФорм", РеквизитыОрганизации.Наименование);
	СведенияОЮрФизЛице.Вставить("ФИОФизлица",                  ""); // Фамилия Имя Отчество 
	СведенияОЮрФизЛице.Вставить("ИНН",                         РеквизитыОрганизации.ИНН);
	СведенияОЮрФизЛице.Вставить("КПП",                         РеквизитыОрганизации.КПП);
	СведенияОЮрФизЛице.Вставить("Руководитель",                "");
	СведенияОЮрФизЛице.Вставить("ДолжностьРуководителяПредставление", "");
	СведенияОЮрФизЛице.Вставить("Телефоны",                    "");
	СведенияОЮрФизЛице.Вставить("Email",                       "");
	СведенияОЮрФизЛице.Вставить("ОфициальноеНаименование",     РеквизитыОрганизации.Наименование);
	СведенияОЮрФизЛице.Вставить("ПолноеНаименование",          РеквизитыОрганизации.Наименование);
	
	Возврат СведенияОЮрФизЛице;
	
КонецФункции

// Использовать несколько организаций.
// 
// Возвращаемое значение:
//  Булево - Использовать несколько организаций
//
Функция ИспользоватьНесколькоОрганизаций() Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	
КонецФункции

Функция УправленческаяОрганизация() Экспорт

	Возврат Справочники.Организации.УправленческаяОрганизация;

КонецФункции

// Настройка обсуждений доступна.
// 
// Возвращаемое значение:
//  Булево - Настройка обсуждений доступна
//
Функция НастройкаОбсужденийДоступна() Экспорт
	
	Возврат ПравоДоступа("АдминистрированиеДанных", Метаданные);
	
КонецФункции

// Организация по умолчанию.
// 
// Возвращаемое значение:
//  СправочникСсылка.Организации - Организация по умолчанию
//
Функция ОрганизацияПоУмолчанию() Экспорт
	
	Возврат Справочники.Организации.ОрганизацияПоУмолчанию();
	
КонецФункции

// Реквизиты организации для подключения.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
// 
// Возвращаемое значение:
//  Структура - Реквизиты организации для подключения:
//   * ЭтоЮрЛицо - Булево, Неопределено - 
//   * Наименование - Строка -
// 
Функция РеквизитыОрганизацииДляПодключения(Организация) Экспорт
	
	РеквизитыОрганизацииДляПодключения = Новый Структура();
	ЮрФизЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮрФизЛицо");
	РеквизитыОрганизацииДляПодключения.Вставить("ЭтоЮрЛицо", ПартнерыИКонтрагенты.ЭтоЮрЛицо(ЮрФизЛицо));
	НаименованияНаДату = Справочники.Организации.НаименованияНаДату(Организация, ТекущаяДатаСеанса());
	РеквизитыОрганизацииДляПодключения.Вставить("Наименование", НаименованияНаДату.НаименованиеСокращенное);
	
	Возврат РеквизитыОрганизацииДляПодключения;
	
КонецФункции

// Это полный интерфейс.
// 
// Возвращаемое значение:
//  Булево - Это полный интерфейс
Функция ЭтоПолныйИнтерфейс() Экспорт
	
	Возврат Истина;
	
КонецФункции

// Контрагент подлежит сверке.
// 
// Параметры:
//  Контрагент - СправочникСсылка.Контрагенты
// 
// Возвращаемое значение:
//  Булево - Контрагент подлежит сверке
Функция КонтрагентПодлежитСверке(Контрагент) Экспорт
	
	Возврат ЗначениеЗаполнено(Контрагент) И НЕ ПустаяСтрока(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ИНН"));
	
КонецФункции
 
// Данные первичного документа.
// 
// Параметры:
//  ДокументСсылка - ДокументСсылка
//  ДокументОбъект - ДокументОбъект
// 
// Возвращаемое значение:
//  Структура - Данные первичного документа:
//   * Номер - Строка - 
//   * НомерРегистратора - Строка - 
//   * Дата - Дата - 
//   * ДатаРегистратора - Дата - 
Функция ДанныеПервичногоДокумента(ДокументСсылка, ДокументОбъект = Неопределено) Экспорт
	
	ДанныеПервичногоДокумента = Новый Структура();
	ДанныеПервичногоДокумента.Вставить("Ссылка", ДокументСсылка);
	ДанныеПервичногоДокумента.Вставить("Номер", "");
	ДанныеПервичногоДокумента.Вставить("НомерРегистратора", "");
	ДанныеПервичногоДокумента.Вставить("Дата", Дата(1,1,1));
	ДанныеПервичногоДокумента.Вставить("ДатаРегистратора", Дата(1,1,1));
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДокументОбъект = Неопределено Тогда
		МетаданныеДокумента = ДокументСсылка.Метаданные();
	Иначе
		МетаданныеДокумента = ДокументОбъект.Метаданные();
	КонецЕсли;
	
	ИменаРеквизитов = "Номер, Дата";
	
	ЕстьДатаПроведенияБанком = ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаПроведенияБанком", МетаданныеДокумента);
	ЕстьНомерВходящегоДокумента = ОбщегоНазначения.ЕстьРеквизитОбъекта("НомерВходящегоДокумента", МетаданныеДокумента);
	ЕстьДатаВходящегоДокумента = ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаВходящегоДокумента", МетаданныеДокумента);
	ЕстьНомерКомиссионера = ОбщегоНазначения.ЕстьРеквизитОбъекта("НомерКомиссионера", МетаданныеДокумента);
	ЕстьДатаКомиссионера = ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаКомиссионера", МетаданныеДокумента);
	
	Если ЕстьНомерВходящегоДокумента Тогда
		ИменаРеквизитов = ИменаРеквизитов + ", НомерВходящегоДокумента";
		Если ЕстьДатаВходящегоДокумента Тогда
			ИменаРеквизитов = ИменаРеквизитов + ", ДатаВходящегоДокумента";
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьНомерКомиссионера Тогда
		ИменаРеквизитов = ИменаРеквизитов + ", НомерКомиссионера";
		Если ЕстьДатаКомиссионера Тогда
			ИменаРеквизитов = ИменаРеквизитов + ", ДатаКомиссионера";
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьДатаПроведенияБанком Тогда
		ИменаРеквизитов = ИменаРеквизитов + ", ДатаПроведенияБанком";
	КонецЕсли;
	
	ЕстьХозяйственнаяОперация = ОбщегоНазначения.ЕстьРеквизитОбъекта("ХозяйственнаяОперация", МетаданныеДокумента);
	Если ЕстьХозяйственнаяОперация Тогда
		ИменаРеквизитов = ИменаРеквизитов + ", ХозяйственнаяОперация";
	КонецЕсли;
	
	ЕстьПроведеноБанком = ОбщегоНазначения.ЕстьРеквизитОбъекта("ПроведеноБанком", МетаданныеДокумента);
	Если ЕстьПроведеноБанком Тогда
		ИменаРеквизитов = ИменаРеквизитов + ", ПроведеноБанком";
	КонецЕсли;
	
	Если ДокументОбъект = Неопределено Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, ИменаРеквизитов);
	Иначе
		Реквизиты = Новый Структура(ИменаРеквизитов);
		ЗаполнитьЗначенияСвойств(Реквизиты, ДокументОбъект);
	КонецЕсли;
	
	// Не регистрируем для сверки документы у которых нет движений по расчетам
	Если ЕстьПроведеноБанком И НЕ Реквизиты.ПроведеноБанком
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
		 	И Реквизиты.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПриемНаКомиссию Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если Реквизиты.Свойство("ДатаПроведенияБанком") Тогда
		Если Реквизиты.Свойство("НомерВходящегоДокумента") Тогда
			ДанныеПервичногоДокумента.Номер = СокрЛП(Реквизиты.НомерВходящегоДокумента);
		КонецЕсли;
		Если Реквизиты.Свойство("ДатаВходящегоДокумента") И ЗначениеЗаполнено(Реквизиты.ДатаВходящегоДокумента) Тогда
			ДанныеПервичногоДокумента.Дата = Реквизиты.ДатаВходящегоДокумента;
		Иначе
			ДанныеПервичногоДокумента.Дата = Реквизиты.ДатаПроведенияБанком;
		КонецЕсли;
	ИначеЕсли Реквизиты.Свойство("НомерКомиссионера") Тогда
		ДанныеПервичногоДокумента.Номер = СокрЛП(Реквизиты.НомерКомиссионера);
		Если Реквизиты.Свойство("ДатаКомиссионера") Тогда
			ДанныеПервичногоДокумента.Дата = Реквизиты.ДатаКомиссионера;
		КонецЕсли;
	ИначеЕсли Реквизиты.Свойство("НомерВходящегоДокумента") Тогда
		ДанныеПервичногоДокумента.Номер = СокрЛП(Реквизиты.НомерВходящегоДокумента);
		Если Реквизиты.Свойство("ДатаВходящегоДокумента") Тогда
			ДанныеПервичногоДокумента.Дата = Реквизиты.ДатаВходящегоДокумента;
		КонецЕсли;
	Иначе
		ДанныеПервичногоДокумента.Номер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер, Ложь, Истина);
		ДанныеПервичногоДокумента.Дата  = Реквизиты.Дата;
	КонецЕсли;
	ДанныеПервичногоДокумента.НомерРегистратора = Реквизиты.Номер;
	ДанныеПервичногоДокумента.ДатаРегистратора = Реквизиты.Дата;
	
	Возврат ДанныеПервичногоДокумента;
	
КонецФункции

// Событие перед регистрацией документа к отправке.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - 
//  Отказ - Булево -
Процедура ПередРегистрациейКОтправке(ДокументОбъект, Отказ) Экспорт
	
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	ЕстьИсправление = ОбщегоНазначения.ЕстьРеквизитОбъекта("Исправление", МетаданныеДокумента);
	ЕстьИсправляемыйДокумент = ОбщегоНазначения.ЕстьРеквизитОбъекта("ИсправляемыйДокумент", МетаданныеДокумента);

	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.Сторно") Тогда
		Отказ = Истина;
		СтороныДокумента = СервисСверкиРасчетовУТ.СтороныДокумента(ДокументОбъект.СторнируемыйДокумент);
		Если ТипЗнч(ДокументОбъект.СторнируемыйДокумент) = Тип("ДокументСсылка.ВзаимозачетЗадолженности") Тогда
			СервисСверкиРасчетовУТ.ЗарегистрироватьВзаимозачет(ДокументОбъект.СторнируемыйДокумент, СтороныДокумента, Отказ);
		Иначе
			СервисСверкиРасчетов.ЗарегистрироватьДокументКОтправке(
				СтороныДокумента.Организация, СтороныДокумента.Контрагент, ДокументОбъект.СторнируемыйДокумент);
		КонецЕсли;
			
	ИначеЕсли ЕстьИсправление И ЕстьИсправляемыйДокумент И ДокументОбъект.Исправление Тогда
		Отказ = Истина;
		СтороныДокумента = СервисСверкиРасчетовУТ.СтороныДокумента(ДокументОбъект.ИсправляемыйДокумент);
		СервисСверкиРасчетов.ЗарегистрироватьДокументКОтправке(
			СтороныДокумента.Организация, СтороныДокумента.Контрагент, ДокументОбъект.ИсправляемыйДокумент);
	
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВзаимозачетЗадолженности") Тогда
		Если ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ОтражениеОплатыЧерезКомиссионера
			ИЛИ ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ОтражениеВозвратаОплатыЧерезКомиссионера
			ИЛИ ДокументОбъект.ВидОперации = Перечисления.ВидыОперацийВзаимозачетаЗадолженности.ОплатаПодарочнымСертификатом Тогда
			Отказ = Истина;
		Иначе
			СервисСверкиРасчетовУТ.ЗарегистрироватьВзаимозачет(ДокументОбъект.Ссылка, ДокументОбъект, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Событие перед регистрацией документа к отправке.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект
//  РеквизитыДокумента - Структура из КлючИЗначение - Реквизиты документа:
//   *Организация - СправочникСсылка.Организации
//   *Контрагент - СправочникСсылка.Контрагенты
//
Процедура УточнитьРеквизитыДокумента(ДокументОбъект, РеквизитыДокумента) Экспорт
	
	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ВозвратТоваровМеждуОрганизациями")
			ИЛИ ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПередачаТоваровМеждуОрганизациями") Тогда
		РеквизитыДокумента.Контрагент = ДокументОбъект.ОрганизацияПолучатель;
	//++ НЕ УТКА
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ОтчетДавальцуМеждуОрганизациями") Тогда
		РеквизитыДокумента.Контрагент = ДокументОбъект.Давалец;
	//-- НЕ УТКА
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ОтчетПоКомиссииМеждуОрганизациями")
			ИЛИ ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ОтчетПоКомиссииМеждуОрганизациямиОСписании") Тогда
		РеквизитыДокумента.Контрагент = ДокументОбъект.Комиссионер;
	
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств")
			И (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации
				ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации) Тогда
		РеквизитыДокумента.Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.БанковскийСчетОтправитель,"Владелец");
		
	ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СписаниеБезналичныхДенежныхСредств")
			И (ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию
				ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию) Тогда
		РеквизитыДокумента.Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.БанковскийСчетПолучатель,"Владелец");
		
	КонецЕсли;
	
КонецПроцедуры

// После регистрации к отправке.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
//  Контрагент - СправочникСсылка.Контрагенты
//  РеквизитыДокумента - Структура Из КлючИЗначение
Процедура ПослеРегистрацииКОтправке(Организация, Контрагент, РеквизитыДокумента) Экспорт
	
	Если ТипЗнч(РеквизитыДокумента.Ссылка) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
		//++ НЕ УТКА
		ИЛИ ТипЗнч(РеквизитыДокумента.Ссылка) = Тип("ДокументСсылка.ОтчетДавальцуМеждуОрганизациями")
		//-- НЕ УТКА
		ИЛИ ТипЗнч(РеквизитыДокумента.Ссылка) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
		ИЛИ ТипЗнч(РеквизитыДокумента.Ссылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")
		ИЛИ ТипЗнч(РеквизитыДокумента.Ссылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациямиОСписании") Тогда
		
		НаборЗаписей = РегистрыСведений.СервисСверкиРасчетовРеестрДокументов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Контрагент);
		НаборЗаписей.Отбор.Контрагент.Установить(Организация);
		НаборЗаписей.Отбор.Документ.Установить(РеквизитыДокумента.Ссылка);
		
		Запись = НаборЗаписей.Добавить();
		Запись.Организация = Контрагент;
		Запись.Контрагент  = Организация;
		Запись.Документ    = РеквизитыДокумента.Ссылка;
		
		Если ЗначениеЗаполнено(РеквизитыДокумента.Дата) Тогда
			ДатаДокумента = РеквизитыДокумента.Дата;
		Иначе
			ДатаДокумента = РеквизитыДокумента.ДатаРегистратора;
		КонецЕсли;
		Запись.ДатаДокумента  = ДатаДокумента;
		
		Если ЗначениеЗаполнено(РеквизитыДокумента.Номер) Тогда
			ПолныйНомер = РеквизитыДокумента.Номер;
		Иначе
			ПолныйНомер = РеквизитыДокумента.НомерРегистратора;
		КонецЕсли;
		Запись.НомерДокумента = СервисСверкиРасчетов.НомерБезПрефиксовИЛидирующихНулей(ПолныйНомер);
		
		Запись.ИдентификаторДокумента = РеквизитыДокумента.Ссылка.УникальныйИдентификатор();
		Запись.ИдентификаторСообщения = "";
		Запись.Статус                 = Перечисления.СервисСверкиРасчетовСтатусыДокументов.КОтправке;
		
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

// Контрагенты для сверки.
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.Контрагенты - Контрагенты для сверки
Функция КонтрагентыДляСверки() Экспорт
	
	ТекстыЗапросов = Новый Массив;
	ТекстЗапроса = "ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Контрагент
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	НЕ Контрагенты.ПометкаУдаления
	|	И Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|	И Контрагенты.ИНН <> """"
	|	И ВЫБОР
	|		КОГДА Контрагенты.ЮридическоеФизическоеЛицо = ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо)
	|			ТОГДА Контрагенты.КПП <> """"
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|";
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями") Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Организации.Ссылка КАК Контрагент
		|ИЗ
		|	Справочник.Организации КАК Организации
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
		|	ПО Организации.Ссылка = ОрганизацииБизнесСеть.Организация
		|ГДЕ
		|	НЕ Организации.ПометкаУдаления
		|	И Организации.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
		|	И Организации.ИНН <> """"
		|	И ВЫБОР
		|		КОГДА Организации.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
		|			ТОГДА Организации.КПП <> """"
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ
		|";
		ТекстыЗапросов.Добавить(ТекстЗапроса);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТаблицаКонтрагенты = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаКонтрагенты.ВыгрузитьКолонку("Контрагент");
	
КонецФункции

// Инициализация компоновщика настроек.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ОрганизацияИзменилась - Булево - Организация изменилась
//  ИмяВариантаНастроек - Строка - Имя варианта настроек
Процедура ИнициализацияКомпоновщикаНастроек(Форма, ОрганизацияИзменилась = Ложь, ИмяВариантаНастроек = "") Экспорт
	
	Форма.КомпоновщикИнициализирован = Истина;
	
	Форма.Элементы.НастройкиОтчета.Видимость = Истина;
	
	ИмяОтчета = "СверкаРасчетовСКонтрагентами";
	
	ИмяСхемы = "СхемаКомпоновкиДанных";
	Схема = Отчеты[ИмяОтчета].ПолучитьМакет(ИмяСхемы);
	
	Если ПустаяСтрока(ИмяВариантаНастроек) Тогда
		ИмяВариантаНастроек = ИмяОтчета;
	КонецЕсли;
	
	ВариантНастроек = Схема.ВариантыНастроек.Найти(ИмяВариантаНастроек);
	
	Если ВариантНастроек <> Неопределено Тогда
		Настройки = ВариантНастроек.Настройки;
	Иначе
		Настройки = Схема.НастройкиПоУмолчанию;
	КонецЕсли;
	
	Форма.СхемаКомпоновкиДанных = ПоместитьВоВременноеХранилище(Схема, Форма.УникальныйИдентификатор);
	
	// ИнициализироватьКомпоновщикНастроекСКД
	Попытка
		Форма.Отчет.КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Форма.СхемаКомпоновкиДанных));
	Исключение
		ИмяСобытияЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отчеты.%1';
				|en = 'Reports.%1'", ОбщегоНазначения.КодОсновногоЯзыка()),
			ИмяОтчета);
			
		ТекстПредставленияОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(
			ИмяСобытияЖурнала,
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстПредставленияОшибки);
		ВызватьИсключение;
	КонецПопытки;
	
	Форма.Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	
	Если ЭтоАдресВременногоХранилища(Форма.ПользовательскиеНастройки) Тогда
		ТекущиеПользовательскиеНастройки = ПолучитьИзВременногоХранилища(Форма.ПользовательскиеНастройки);
	Иначе
		ТекущиеПользовательскиеНастройки = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при создании формы отчета.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
Процедура ОтчетПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт

	Форма.ДанныеРасшифровки = ПоместитьВоВременноеХранилище(Форма.ДанныеРасшифровки, Форма.УникальныйИдентификатор);
	ЗаполнитьЗначенияСвойств(Форма.Отчет, Форма.Параметры);
	Если ЗначениеЗаполнено(Форма.Отчет.Организация) Тогда
		Форма.ПолеОрганизация = "_" + СтрЗаменить(Форма.Отчет.Организация.УникальныйИдентификатор(), "-", "");
	КонецЕсли;

КонецПроцедуры

// Заполнить список организаций.
// 
// Параметры:
//  ЭлементПолеОрганизация - ПолеВвода
//  СоответствиеОрганизаций - Соответствие из КлючИЗначение
//  Период - Дата
//  ИсключитьФилиалы - Булево
Процедура ЗаполнитьСписокОрганизаций(ЭлементПолеОрганизация, СоответствиеОрганизаций, Период = '00010101', ИсключитьФилиалы = Ложь) Экспорт
	
	СоответствиеОрганизаций = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсключитьФилиалы", ИсключитьФилиалы);
	
	Если ЗначениеЗаполнено(Период) Тогда
		Запрос.УстановитьПараметр("Период", НачалоГода(Период));
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Организация
		|ПОМЕСТИТЬ Организации
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	НЕ Организации.ПометкаУдаления
		|	И Организации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрганизацийПодразделений.Действует)
		|	И НЕ Организации.ОбособленноеПодразделение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		//++ НЕ УТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияРегистрацийВНалоговомОргане.СрезПоследних(&Период, ) КАК ИсторияРегистраций
		|		ПО Организации.Ссылка = ИсторияРегистраций.СтруктурнаяЕдиница
		//-- НЕ УТ
		|ГДЕ
		|	НЕ Организации.Предопределенный
		|	И НЕ Организации.ПометкаУдаления
		//++ НЕ УТ
		|	И (ЕСТЬNULL(ИсторияРегистраций.РегистрацияВНалоговомОргане.ДатаСнятияСУчета, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ ИсторияРегистраций.РегистрацияВНалоговомОргане.ДатаСнятияСУчета >= &Период)
		//-- НЕ УТ
		|	И Организации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрганизацийПодразделений.Действует)
		|	И Организации.ОбособленноеПодразделение
		|	И НЕ &ИсключитьФилиалы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Организации.Организация КАК Организация,
		|	Организации.Организация.Наименование КАК ОрганизацияПредставление,
		|	ЛОЖЬ КАК ВключатьОбособленныеПодразделения
		|ИЗ
		|	Организации КАК Организации
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОрганизацияПредставление";
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Организация,
		|	Организации.Наименование КАК ОрганизацияПредставление,
		|	ЛОЖЬ КАК ВключатьОбособленныеПодразделения
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	НЕ Организации.Предопределенный
		|	И НЕ Организации.ПометкаУдаления
		|	И Организации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрганизацийПодразделений.Действует)
		|	И (НЕ Организации.ОбособленноеПодразделение ИЛИ НЕ &ИсключитьФилиалы)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОрганизацияПредставление";
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЭлементПолеОрганизация.СписокВыбора.Очистить();
	МаксКоличествоСимволов = 40;
	Пока Выборка.Следующий() Цикл
		Ключ     = "_" + СтрЗаменить(Выборка.Организация.УникальныйИдентификатор(), "-", "");
		Значение = Новый Структура("Организация,ВключатьОбособленныеПодразделения", Выборка.Организация, ЛОЖЬ);
		СоответствиеОрганизаций.Вставить(Ключ, Значение);
		
		ОрганизацияПредставление = Выборка.ОрганизацияПредставление;
		ЭлементПолеОрганизация.СписокВыбора.Добавить(Ключ, ОрганизацияПредставление);
		
		МаксКоличествоСимволов = Макс(МаксКоличествоСимволов, СтрДлина(ОрганизацияПредставление));
	КонецЦикла;
	
	ЭлементПолеОрганизация.ШиринаСпискаВыбора = Окр(?(МаксКоличествоСимволов > 200, 200, МаксКоличествоСимволов) * 1.3);
	ЭлементПолеОрганизация.ВысотаСпискаВыбора = ?(ЭлементПолеОрганизация.СписокВыбора.Количество() > 15, 15, ЭлементПолеОрганизация.СписокВыбора.Количество());
	
КонецПроцедуры

// Обработка результата отчета.
// 
// Параметры:
//  ИдентификаторОтчета - Строка
//  Результат - ТабличныйДокумент
Процедура ОбработкаРезультатаОтчета(ИдентификаторОтчета, Результат) Экспорт
	

КонецПроцедуры

// Подготовить отчет.
// 
// Параметры:
//  ПараметрыОтчета - Структура Из КлючИЗначение
// 
// Возвращаемое значение:
//  Структура - Подготовить отчет:
// * Выполнено - Булево - 
// * Результат - ТабличныйДокумент - 
// * ДанныеРасшифровки - ДанныеРасшифровкиКомпоновкиДанных 
// * КонтрольноеСоотношениеИтоговВыполняется - Булево - 
// * КраткоеПредставлениеОшибки - Строка - 
// * ПодробноеПредставлениеОшибки - Строка - 
Функция ПодготовитьОтчет(ПараметрыОтчета) Экспорт
	
	Возврат СервисСверкиРасчетовУТ.ПодготовитьОтчетОРасхождениях(ПараметрыОтчета);
	
КонецФункции

// Получить данные сверки декларации.
// 
// Параметры:
//  СтруктураПараметров  - Структура из КлючИЗначение
//  КнигаПокупок - Булево - Книга покупок
//  ЭтоДекларация - Булево - Это декларация
//  ТолькоДопЛисты - Булево - Только допполнительные листы
// 
// Возвращаемое значение:
//  РезультатЗапроса - Получить данные сверки декларации
Функция ПолучитьДанныеСверкиДекларации(СтруктураПараметров, КнигаПокупок = Истина, ЭтоДекларация = Ложь, ТолькоДопЛисты = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",                    СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("ИдентификаторСверкиКонтрагенту", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	Запрос.УстановитьПараметр("НачалоПериода",                  СтруктураПараметров.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",                   СтруктураПараметров.КонецПериода);
	Запрос.УстановитьПараметр("ОтборПоКонтрагенту",             ?(ЗначениеЗаполнено(СтруктураПараметров.Контрагент), Истина, Ложь));
	Запрос.УстановитьПараметр("Контрагент",                     СтруктураПараметров.Контрагент);
	Запрос.УстановитьПараметр("ДанныеСверки",                   СтруктураПараметров.ДанныеСверки);
	Запрос.УстановитьПараметр("КодВидаОперации",                СтруктураПараметров.КодВидаОперации);
	Запрос.УстановитьПараметр("ОперацияНетВОрганизации",        СтруктураПараметров.ОперацияНетВОрганизации);
	Если КнигаПокупок Тогда
		Запрос.Текст = ТекстЗапросаДляКнигиПокупок(ЭтоДекларация);
	Иначе
		Запрос.Текст = ТекстЗапросаДляКнигиПродаж(ТолькоДопЛисты, ЭтоДекларация);
	КонецЕсли;
	
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

// Текст запроса для книги продаж.
// 
// Параметры:
//  ТолькоДопЛисты - Булево - Только доп листы
//  ЭтоДекларация - Булево - Это декларация
// 
// Возвращаемое значение:
//  Строка - Текст запроса для книги продаж
Функция ТекстЗапросаДляКнигиПродаж(ТолькоДопЛисты = Ложь, ЭтоДекларация = Ложь) Экспорт
	
	Если ЭтоДекларация Тогда
		Текст = 
		"ВЫБРАТЬ
		|	ДанныеСверки.СчетФактура КАК СчетФактура,
		|	ДанныеСверки.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.ДатаКорректировки <> ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДанныеСверки.ДатаКорректировки
		|		ИНАЧЕ ДанныеСверки.ДатаСчетаФактуры
		|	КОНЕЦ КАК ДатаСчетаФактуры,
		|	ДанныеСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеСверки.Покупатель КАК КонтрагентНаименование,
		|	ДанныеСверки.Контрагент КАК Контрагент,
		|	ДанныеСверки.ПокупательИНН КАК КонтрагентИНН,
		|	ДанныеСверки.ПокупательКПП КАК КонтрагентКПП,
		|	ДанныеСверки.НДС20 + ДанныеСверки.НДС18 + ДанныеСверки.НДС10 + ДанныеСверки.НДС7 + ДанныеСверки.НДС5 КАК НДС,
		|	ДанныеСверки.ВсегоПродаж КАК Всего,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.НомерКорректировки <> """"
		|			ТОГДА ДанныеСверки.НомерКорректировки
		|		ИНАЧЕ ДанныеСверки.НомерСчетаФактуры
		|	КОНЕЦ КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверкиПредварительные
		|ИЗ
		|	&ДанныеСверки КАК ДанныеСверки
		|ГДЕ
		|	ДанныеСверки.ВсегоПродаж > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
		|			ТОГДА СчетФактураВыданный.ДокументОснование
		|		ИНАЧЕ ДанныеДляСверкиПредварительные.СчетФактура
		|	КОНЕЦ КАК СчетФактура,
		|	ДанныеДляСверкиПредварительные.Организация КАК Организация,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации КАК КодВидаОперации,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА ВЫБОР
		|					КОГДА Организации.НаименованиеПолное = """"
		|						ТОГДА Организации.Наименование
		|					ИНАЧЕ Организации.НаименованиеПолное
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Контрагенты.НаименованиеПолное = """"
		|					ТОГДА Контрагенты.Наименование
		|				ИНАЧЕ Контрагенты.НаименованиеПолное
		|			КОНЕЦ
		|	КОНЕЦ КАК КонтрагентНаименование,
		|	ДанныеДляСверкиПредварительные.Контрагент КАК Контрагент,
		|	Контрагенты.ИНН КАК КонтрагентИНН,
		|	Контрагенты.КПП КАК КонтрагентКПП,
		|	ДанныеДляСверкиПредварительные.НДС КАК НДС,
		|	ДанныеДляСверкиПредварительные.Всего КАК Всего,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверки
		|ИЗ
		|	Справочник.Организации КАК Организации,
		|	ДанныеДляСверкиПредварительные КАК ДанныеДляСверкиПредварительные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|			ПО ДанныеДляСверкиПредварительные.Контрагент = Контрагенты.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданный
		|			ПО ДанныеДляСверкиПредварительные.СчетФактура = СчетФактураВыданный.Ссылка
		|ГДЕ
		|	Организации.Ссылка = &Организация
		|	И ТИПЗНАЧЕНИЯ(ДанныеДляСверкиПредварительные.СчетФактура) <> ТИП(Документ.СчетФактураПолученный)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.СчетФактура ССЫЛКА Документ.СчетФактураПолученный
		|			ТОГДА СчетФактураПолученный.ДокументОснование
		|		ИНАЧЕ ДанныеДляСверкиПредварительные.СчетФактура
		|	КОНЕЦ КАК СчетФактура,
		|	ДанныеДляСверкиПредварительные.Организация,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА ВЫБОР
		|					КОГДА Организации.НаименованиеПолное = """"
		|						ТОГДА Организации.Наименование
		|					ИНАЧЕ Организации.НаименованиеПолное
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Контрагенты.НаименованиеПолное = """"
		|					ТОГДА Контрагенты.Наименование
		|				ИНАЧЕ Контрагенты.НаименованиеПолное
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ДанныеДляСверкиПредварительные.Контрагент,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА Организации.ИНН
		|		ИНАЧЕ Контрагенты.ИНН
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА Организации.КПП
		|		ИНАЧЕ Контрагенты.КПП
		|	КОНЕЦ,
		|	ДанныеДляСверкиПредварительные.НДС КАК НДС,
		|	ДанныеДляСверкиПредварительные.Всего КАК Всего,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры
		|ИЗ
		|	Справочник.Организации КАК Организации,
		|	ДанныеДляСверкиПредварительные КАК ДанныеДляСверкиПредварительные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
		|		ПО ДанныеДляСверкиПредварительные.СчетФактура = СчетФактураПолученный.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ДанныеДляСверкиПредварительные.Контрагент = Контрагенты.Ссылка
		|ГДЕ
		|	Организации.Ссылка = &Организация
		|	И ТИПЗНАЧЕНИЯ(ДанныеДляСверкиПредварительные.СчетФактура) = ТИП(Документ.СчетФактураПолученный)";
	Иначе
		Текст = 
		"ВЫБРАТЬ
		|	ДанныеСверки.СчетФактура КАК СчетФактура,
		|	ДанныеСверки.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.ДатаКорректировки <> ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДанныеСверки.ДатаКорректировки
		|		ИНАЧЕ ДанныеСверки.ДатаСчетаФактуры
		|	КОНЕЦ КАК ДатаСчетаФактуры,
		|	ДанныеСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеСверки.Покупатель КАК КонтрагентНаименование,
		|	ДанныеСверки.Контрагент КАК Контрагент,
		|	ДанныеСверки.ПокупательИНН КАК КонтрагентИНН,
		|	ДанныеСверки.ПокупательКПП КАК КонтрагентКПП,
		|	ДанныеСверки.НДС20 + ДанныеСверки.НДС18 + ДанныеСверки.НДС10 + ДанныеСверки.НДС7 + ДанныеСверки.НДС5 КАК НДС,
		|	ДанныеСверки.ВсегоПродаж КАК Всего,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.НомерКорректировки <> """"
		|			ТОГДА ДанныеСверки.НомерКорректировки
		|		ИНАЧЕ ДанныеСверки.НомерСчетаФактуры
		|	КОНЕЦ КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверкиПредварительные
		|ИЗ
		|	&ДанныеСверки КАК ДанныеСверки
		|ГДЕ
		|	ДанныеСверки.ВсегоПродаж > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСверкиПредварительные.СчетФактура КАК СчетФактура,
		|	ДанныеДляСверкиПредварительные.Организация КАК Организация,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверкиПредварительные.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверкиПредварительные.Контрагент КАК Контрагент,
		|	ДанныеДляСверкиПредварительные.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверкиПредварительные.КонтрагентКПП КАК КонтрагентКПП,
		|	МАКСИМУМ(ДанныеДляСверкиПредварительные.НДС) КАК НДС,
		|	МАКСИМУМ(ЕСТЬNULL(НДСПродаж.СуммаБезНДС + НДСПродаж.НДС, ДанныеДляСверкиПредварительные.Всего)) КАК Всего,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверки
		|ИЗ
		|	ДанныеДляСверкиПредварительные КАК ДанныеДляСверкиПредварительные
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСПродаж
		|	ПО ДанныеДляСверкиПредварительные.СчетФактура = НДСПродаж.Регистратор
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДляСверкиПредварительные.Контрагент,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КонтрагентНаименование,
		|	ДанныеДляСверкиПредварительные.Организация,
		|	ДанныеДляСверкиПредварительные.СчетФактура,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации,
		|	ДанныеДляСверкиПредварительные.КонтрагентИНН,
		|	ДанныеДляСверкиПредварительные.КонтрагентКПП,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры";
		
	КонецЕсли;
	
	Текст = Текст + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(СервисСверкиРасчетовРеестрДокументов.Организация, ДанныеДляСверки.Организация) КАК Организация,
		|	ЕСТЬNULL(СервисСверкиРасчетовРеестрДокументов.Контрагент, ДанныеДляСверки.Контрагент) КАК Контрагент,
		|	ЕСТЬNULL(СервисСверкиРасчетовРеестрДокументов.Документ, ДанныеДляСверки.СчетФактура) КАК Документ,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(СервисСверкиРасчетовРеестрДокументов.ИдентификаторСообщения КАК СТРОКА(36)), """") КАК ИдентификаторСообщения,
		|	СервисСверкиРасчетовРеестрДокументов.ДатаДокумента КАК ДатаДокумента,
		|	СервисСверкиРасчетовРеестрДокументов.НомерДокумента КАК НомерДокумента,
		|	СервисСверкиРасчетовРеестрДокументов.Статус КАК Статус,
		|	ДанныеДляСверки.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверки.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП КАК КонтрагентКПП,
		|	ДанныеДляСверки.НДС КАК НДС,
		|	ДанныеДляСверки.Всего КАК Всего,
		|	ДанныеДляСверки.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	ЕСТЬNULL(СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку, ЛОЖЬ) КАК КонтрагентНеПроизводилСверку
		|ПОМЕСТИТЬ ЧтоНеСверилиПредварительнаяТаблица
		|ИЗ
		|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
		|		ПОЛНОЕ СОЕДИНЕНИЕ ДанныеДляСверки КАК ДанныеДляСверки
		|		ПО (ДанныеДляСверки.СчетФактура = СервисСверкиРасчетовРеестрДокументов.Документ)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|		ПО СервисСверкиРасчетовРеестрДокументов.Контрагент = СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовРеестрДокументов.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И ЕСТЬNULL(СервисСверкиРасчетовРеестрДокументов.Организация, ДанныеДляСверки.Организация) = &Организация
		|	И ЕСТЬNULL(СервисСверкиРасчетовОбнаруженныеРасхождения.Организация, &Организация) = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЧтоНеСверилиПредварительнаяТаблица.Организация КАК Организация,
		|	ЧтоНеСверилиПредварительнаяТаблица.Контрагент КАК Контрагент,
		|	ЧтоНеСверилиПредварительнаяТаблица.Документ КАК Документ,
		|	ЧтоНеСверилиПредварительнаяТаблица.ИдентификаторСообщения КАК ИдентификаторСообщения,
		|	ЧтоНеСверилиПредварительнаяТаблица.ДатаДокумента КАК ДатаДокумента,
		|	ЧтоНеСверилиПредварительнаяТаблица.НомерДокумента КАК НомерДокумента,
		|	ЧтоНеСверилиПредварительнаяТаблица.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ЧтоНеСверилиПредварительнаяТаблица.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ЧтоНеСверилиПредварительнаяТаблица.КодВидаОперации КАК КодВидаОперации,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентИНН КАК КонтрагентИНН,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентКПП КАК КонтрагентКПП,
		|	ЧтоНеСверилиПредварительнаяТаблица.НДС КАК НДС,
		|	ЧтоНеСверилиПредварительнаяТаблица.Всего КАК Всего,
		|	ЧтоНеСверилиПредварительнаяТаблица.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	1 КАК КоличествоДокументов
		|ИЗ
		|	ЧтоНеСверилиПредварительнаяТаблица КАК ЧтоНеСверилиПредварительнаяТаблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ЧтоНеСверилиПредварительнаяТаблица.Контрагент = Контрагенты.Ссылка
		|ГДЕ
		|	ЧтоНеСверилиПредварительнаяТаблица.Организация = &Организация
		|	И ЧтоНеСверилиПредварительнаяТаблица.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|	И НЕ(Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
		|					И Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель)
		|				ИЛИ ЧтоНеСверилиПредварительнаяТаблица.КодВидаОперации В (&КодВидаОперации))
		|	И (ЧтоНеСверилиПредварительнаяТаблица.ИдентификаторСообщения = """"
		|			ИЛИ ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНеПроизводилСверку)
		|ИТОГИ
		|	СУММА(НДС),
		|	СУММА(Всего),
		|	СУММА(КоличествоДокументов)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДляСверки.СчетФактура КАК СчетФактура,
		|	СервисСверкиРасчетовДанныеСервиса.Сумма КАК СуммаКонтрагента,
		|	СервисСверкиРасчетовДанныеСервиса.НДС КАК НДСКонтрагента,
		|	СервисСверкиРасчетовДанныеСервиса.НомерДокумента КАК НомерДокумента,
		|	СервисСверкиРасчетовДанныеСервиса.ДатаДокумента КАК ДатаДокумента,
		|	СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры КАК НомерСчетФактурыК,
		|	СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры КАК ДатаСчетФактурыК,
		|	ДанныеДляСверки.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверки.Контрагент КАК Контрагент,
		|	ДанныеДляСверки.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП КАК КонтрагентКПП,
		|	ДанныеДляСверки.Всего КАК Всего,
		|	ДанныеДляСверки.НДС КАК НДС,
		|	ДанныеДляСверки.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	1 КАК КоличествоДокументов
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовДанныеСервиса КАК СервисСверкиРасчетовДанныеСервиса
		|			ПО СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки
		|		ПО ДанныеДляСверки.СчетФактура = СервисСверкиРасчетовОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|	И СервисСверкиРасчетовДанныеСервиса.Организация = &Организация
		|	И (СервисСверкиРасчетовДанныеСервиса.Сумма <> СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации
		|			ИЛИ СервисСверкиРасчетовДанныеСервиса.НДС <> СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации
		|			ИЛИ СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры <> ДанныеДляСверки.НомерСчетаФактуры
		|			ИЛИ СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры <> НАЧАЛОПЕРИОДА(ДанныеДляСверки.ДатаСчетаФактуры, ДЕНЬ))
		|ИТОГИ
		|	СУММА(СуммаКонтрагента),
		|	СУММА(НДСКонтрагента),
		|	СУММА(Всего),
		|	СУММА(НДС),
		|	СУММА(КоличествоДокументов)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЧтоНеСверилиПредварительнаяТаблица.Документ КАК Документ,
		|	ЧтоНеСверилиПредварительнаяТаблица.Контрагент КАК Контрагент,
		|	ЧтоНеСверилиПредварительнаяТаблица.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ЧтоНеСверилиПредварительнаяТаблица.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ЧтоНеСверилиПредварительнаяТаблица.КодВидаОперации КАК КодВидаОперации,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентИНН КАК КонтрагентИНН,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентКПП КАК КонтрагентКПП,
		|	ЧтоНеСверилиПредварительнаяТаблица.НДС КАК НДС,
		|	ЧтоНеСверилиПредварительнаяТаблица.Всего КАК Всего,
		|	ЧтоНеСверилиПредварительнаяТаблица.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	1 КАК КоличествоДокументов
		|ИЗ
		|	ЧтоНеСверилиПредварительнаяТаблица КАК ЧтоНеСверилиПредварительнаяТаблица
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|		ПО ЧтоНеСверилиПредварительнаяТаблица.Документ = СервисСверкиРасчетовОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	ЕСТЬNULL(СервисСверкиРасчетовОбнаруженныеРасхождения.Организация, &Организация) = &Организация
		|	И ЧтоНеСверилиПредварительнаяТаблица.ИдентификаторСообщения <> """"
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Документ ЕСТЬ NULL
		|	И ЧтоНеСверилиПредварительнаяТаблица.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|	И ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНеПроизводилСверку = ЛОЖЬ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляСверки.СчетФактура,
		|	ДанныеДляСверки.Контрагент,
		|	ДанныеДляСверки.ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование,
		|	ДанныеДляСверки.КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП,
		|	ДанныеДляСверки.НДС,
		|	ДанныеДляСверки.Всего,
		|	ДанныеДляСверки.НомерСчетаФактуры,
		|	1
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ДанныеДляСверки.Контрагент = Контрагенты.Ссылка
		|ГДЕ
		|	(Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
		|					И Контрагенты.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель)
		|			ИЛИ ДанныеДляСверки.КодВидаОперации В (&КодВидаОперации))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляСверки.СчетФактура,
		|	ДанныеДляСверки.Контрагент,
		|	ДанныеДляСверки.ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование,
		|	ДанныеДляСверки.КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП,
		|	ДанныеДляСверки.НДС,
		|	ДанныеДляСверки.Всего,
		|	ДанныеДляСверки.НомерСчетаФактуры,
		|	1
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовДанныеСервиса КАК СервисСверкиРасчетовДанныеСервиса
		|			ПО СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки
		|		ПО ДанныеДляСверки.СчетФактура = СервисСверкиРасчетовОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	СервисСверкиРасчетовДанныеСервиса.Организация = &Организация
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
		|	И СервисСверкиРасчетовДанныеСервиса.Сумма = СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации
		|	И СервисСверкиРасчетовДанныеСервиса.НДС = СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации
		|	И СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры = ДанныеДляСверки.НомерСчетаФактуры
		|	И СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры = НАЧАЛОПЕРИОДА(ДанныеДляСверки.ДатаСчетаФактуры, ДЕНЬ)
		|ИТОГИ
		|	СУММА(НДС),
		|	СУММА(Всего),
		|	СУММА(КоличествоДокументов)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСверки.СчетФактура КАК СчетФактура,
		|	ДанныеДляСверки.Организация КАК Организация,
		|	ДанныеДляСверки.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверки.Контрагент КАК Контрагент,
		|	ДанныеДляСверки.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП КАК КонтрагентКПП,
		|	ДанныеДляСверки.НДС КАК НДС,
		|	ДанныеДляСверки.Всего КАК Всего,
		|	ДанныеДляСверки.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	1 КАК КоличествоДокументов
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|		ПО ДанныеДляСверки.СчетФактура = СервисСверкиРасчетовОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = &ИдентификаторСверкиКонтрагенту
		|	И ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
		|	И НЕ ДанныеДляСверки.КодВидаОперации В (&КодВидаОперации)
		|ИТОГИ
		|	СУММА(НДС),
		|	СУММА(Всего),
		|	СУММА(КоличествоДокументов)
		|ПО
		|	ОБЩИЕ";
	
	Если Не ТолькоДопЛисты Тогда
		Текст = Текст + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация КАК Организация,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент КАК Контрагент,
		|	СервисСверкиРасчетовДанныеСервиса.Сумма КАК Всего,
		|	СервисСверкиРасчетовДанныеСервиса.НДС КАК НДС,
		|	СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры КАК НомерСчетаФактуры,
		|	СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры КАК ДатаСчетаФактуры,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИННКонтрагента КАК КонтрагентИНН,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.КППКонтрагента КАК КонтрагентКПП,
		|	Контрагенты.НаименованиеПолное КАК КонтрагентНаименование,
		|	1 КАК КоличествоДокументов,
		|	НЕОПРЕДЕЛЕНО КАК СчетФактураДокумент,
		|	"""" КАК КодВидаОперации
		|ИЗ
		|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовДанныеСервиса КАК СервисСверкиРасчетовДанныеСервиса
		|		ПО СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент = Контрагенты.Ссылка
		|ГДЕ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СервисСверкиРасчетовДанныеСервиса.Организация = &Организация
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Документ = НЕОПРЕДЕЛЕНО
		|	И СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовДанныеСервиса.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И СервисСверкиРасчетовДанныеСервиса.Операция В(&ОперацияНетВОрганизации)
		|ИТОГИ
		|	СУММА(Всего),
		|	СУММА(НДС),
		|	СУММА(КоличествоДокументов)
		|ПО
		|	ОБЩИЕ";
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

// Текст запроса для книги покупок.
// 
// Параметры:
//  ЭтоДекларация - Булево - Это декларация
// 
// Возвращаемое значение:
//  Строка - Текст запроса для книги покупок
Функция ТекстЗапросаДляКнигиПокупок(ЭтоДекларация = Ложь) Экспорт
	
	Если ЭтоДекларация Тогда
		Текст = 
		"ВЫБРАТЬ
		|	ДанныеСверки.СчетФактура КАК СчетФактура,
		|	ДанныеСверки.Организация КАК Организация,
		|	ДанныеСверки.ВсегоПокупок КАК СуммаБезНДС,
		|	ДанныеСверки.НДС КАК НДС,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.ДатаКорректировки <> ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДанныеСверки.ДатаКорректировки
		|		ИНАЧЕ ДанныеСверки.ДатаСчетаФактуры
		|	КОНЕЦ КАК ДатаСчетаФактуры,
		|	ДанныеСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеСверки.Продавец КАК КонтрагентНаименование,
		|	ДанныеСверки.Контрагент КАК Контрагент,
		|	ДанныеСверки.ПродавецИНН КАК КонтрагентИНН,
		|	ДанныеСверки.ПродавецКПП КАК КонтрагентКПП,
		|	ДанныеСверки.ВсегоПокупок КАК Всего,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.НомерКорректировки <> """"
		|			ТОГДА ДанныеСверки.НомерКорректировки
		|		ИНАЧЕ ДанныеСверки.НомерСчетаФактуры
		|	КОНЕЦ КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверкиПредварительные
		|ИЗ
		|	&ДанныеСверки КАК ДанныеСверки
		|ГДЕ
		|	ДанныеСверки.ВсегоПокупок > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.СчетФактура ССЫЛКА Документ.СчетФактураПолученный
		|			ТОГДА СчетФактураПолученный.ДокументОснование
		|		ИНАЧЕ ДанныеДляСверкиПредварительные.СчетФактура
		|	КОНЕЦ КАК СчетФактура,
		|	ДанныеДляСверкиПредварительные.Организация КАК Организация,
		|	ДанныеДляСверкиПредварительные.СуммаБезНДС КАК СуммаБезНДС,
		|	ДанныеДляСверкиПредварительные.НДС КАК НДС,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации КАК КодВидаОперации,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА ВЫБОР
		|					КОГДА Организации.НаименованиеПолное = """"
		|						ТОГДА Организации.Наименование
		|					ИНАЧЕ Организации.НаименованиеПолное
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Контрагенты.НаименованиеПолное = """"
		|					ТОГДА Контрагенты.Наименование
		|				ИНАЧЕ Контрагенты.НаименованиеПолное
		|			КОНЕЦ
		|	КОНЕЦ КАК КонтрагентНаименование,
		|	ДанныеДляСверкиПредварительные.Контрагент КАК Контрагент,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА Организации.ИНН
		|		ИНАЧЕ Контрагенты.ИНН
		|	КОНЕЦ КАК КонтрагентИНН,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА Организации.КПП
		|		ИНАЧЕ Контрагенты.КПП
		|	КОНЕЦ КАК КонтрагентКПП,
		|	ДанныеДляСверкиПредварительные.Всего КАК Всего,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверки
		|ИЗ
		|	Справочник.Организации КАК Организации,
		|	ДанныеДляСверкиПредварительные КАК ДанныеДляСверкиПредварительные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ДанныеДляСверкиПредварительные.Контрагент = Контрагенты.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
		|		ПО ДанныеДляСверкиПредварительные.СчетФактура = СчетФактураПолученный.Ссылка
		|ГДЕ
		|	Организации.Ссылка = &Организация
		|	И ТИПЗНАЧЕНИЯ(ДанныеДляСверкиПредварительные.СчетФактура) <> ТИП(Документ.СчетФактураВыданный)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.СчетФактура ССЫЛКА Документ.СчетФактураВыданный
		|			ТОГДА СчетФактураВыданный.ДокументОснование
		|		ИНАЧЕ ДанныеДляСверкиПредварительные.СчетФактура
		|	КОНЕЦ,
		|	ДанныеДляСверкиПредварительные.Организация,
		|	ДанныеДляСверкиПредварительные.СуммаБезНДС,
		|	ДанныеДляСверкиПредварительные.НДС,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА ВЫБОР
		|					КОГДА Организации.НаименованиеПолное = """"
		|						ТОГДА Организации.Наименование
		|					ИНАЧЕ Организации.НаименованиеПолное
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА Контрагенты.НаименованиеПолное = """"
		|					ТОГДА Контрагенты.Наименование
		|				ИНАЧЕ Контрагенты.НаименованиеПолное
		|			КОНЕЦ
		|	КОНЕЦ,
		|	ДанныеДляСверкиПредварительные.Контрагент,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА Организации.ИНН
		|		ИНАЧЕ Контрагенты.ИНН
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДанныеДляСверкиПредварительные.Контрагент = &Организация
		|			ТОГДА Организации.КПП
		|		ИНАЧЕ Контрагенты.КПП
		|	КОНЕЦ,
		|	ДанныеДляСверкиПредварительные.Всего КАК Всего,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры
		|ИЗ
		|	Справочник.Организации КАК Организации,
		|	ДанныеДляСверкиПредварительные КАК ДанныеДляСверкиПредварительные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
		|		ПО ДанныеДляСверкиПредварительные.СчетФактура = СчетФактураВыданный.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ДанныеДляСверкиПредварительные.Контрагент = Контрагенты.Ссылка
		|ГДЕ
		|	Организации.Ссылка = &Организация
		|	И ТИПЗНАЧЕНИЯ(ДанныеДляСверкиПредварительные.СчетФактура) = ТИП(Документ.СчетФактураВыданный)";
	Иначе
		Текст = 
		"ВЫБРАТЬ
		|	ДанныеСверки.СчетФактура КАК СчетФактура,
		|	ДанныеСверки.Организация КАК Организация,
		|	ДанныеСверки.ВсегоПокупок КАК СуммаБезНДС,
		|	ДанныеСверки.НДС КАК НДС,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.ДатаКорректировки <> ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДанныеСверки.ДатаКорректировки
		|		ИНАЧЕ ДанныеСверки.ДатаСчетаФактуры
		|	КОНЕЦ КАК ДатаСчетаФактуры,
		|	ДанныеСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеСверки.Продавец КАК КонтрагентНаименование,
		|	ДанныеСверки.Контрагент КАК Контрагент,
		|	ДанныеСверки.ПродавецИНН КАК КонтрагентИНН,
		|	ДанныеСверки.ПродавецКПП КАК КонтрагентКПП,
		|	ДанныеСверки.ВсегоПокупок КАК Всего,
		|	ВЫБОР
		|		КОГДА ДанныеСверки.НомерКорректировки <> """"
		|			ТОГДА ДанныеСверки.НомерКорректировки
		|		ИНАЧЕ ДанныеСверки.НомерСчетаФактуры
		|	КОНЕЦ КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверкиПредварительные
		|ИЗ
		|	&ДанныеСверки КАК ДанныеСверки
		|ГДЕ
		|	ДанныеСверки.ВсегоПокупок > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСверкиПредварительные.СчетФактура КАК СчетФактура,
		|	ДанныеДляСверкиПредварительные.Организация КАК Организация,
		|	МАКСИМУМ(ДанныеДляСверкиПредварительные.СуммаБезНДС) КАК СуммаБезНДС,
		|	МАКСИМУМ(ДанныеДляСверкиПредварительные.НДС) КАК НДС,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверкиПредварительные.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверкиПредварительные.Контрагент КАК Контрагент,
		|	ДанныеДляСверкиПредварительные.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверкиПредварительные.КонтрагентКПП КАК КонтрагентКПП,
		|	МАКСИМУМ(ЕСТЬNULL(НДСПокупок.СуммаБезНДС + НДСПокупок.НДС, ДанныеДляСверкиПредварительные.Всего)) КАК Всего,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ПОМЕСТИТЬ ДанныеДляСверки
		|ИЗ
		|	ДанныеДляСверкиПредварительные КАК ДанныеДляСверкиПредварительные
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДСЗаписиКнигиПокупок КАК НДСПокупок
		|	ПО ДанныеДляСверкиПредварительные.СчетФактура = НДСПокупок.Регистратор
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДляСверкиПредварительные.СчетФактура,
		|	ДанныеДляСверкиПредварительные.Организация,
		|	ДанныеДляСверкиПредварительные.КонтрагентНаименование,
		|	ДанныеДляСверкиПредварительные.КонтрагентИНН,
		|	ДанныеДляСверкиПредварительные.КодВидаОперации,
		|	ДанныеДляСверкиПредварительные.СчетФактураДокумент,
		|	ДанныеДляСверкиПредварительные.НомерСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.Контрагент,
		|	ДанныеДляСверкиПредварительные.ДатаСчетаФактуры,
		|	ДанныеДляСверкиПредварительные.КонтрагентКПП";
		
	КонецЕсли;
	
	Текст = Текст + ОбщегоНазначения.РазделительПакетаЗапросов() +
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(СервисСверкиРасчетовРеестрДокументов.Организация, ДанныеДляСверки.Организация) КАК Организация,
		|	ЕСТЬNULL(СервисСверкиРасчетовРеестрДокументов.Документ, ДанныеДляСверки.СчетФактура) КАК Документ,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(СервисСверкиРасчетовРеестрДокументов.ИдентификаторСообщения КАК СТРОКА(36)), """") КАК ИдентификаторСообщения,
		|	СервисСверкиРасчетовРеестрДокументов.ДатаДокумента КАК ДатаДокумента,
		|	СервисСверкиРасчетовРеестрДокументов.НомерДокумента КАК НомерДокумента,
		|	СервисСверкиРасчетовРеестрДокументов.Статус КАК Статус,
		|	ДанныеДляСверки.СуммаБезНДС КАК СуммаБезНДС,
		|	ДанныеДляСверки.НДС КАК НДС,
		|	ДанныеДляСверки.СуммаБезНДС КАК СуммаБезНДСКнига,
		|	ДанныеДляСверки.НДС КАК НДСКнига,
		|	ДанныеДляСверки.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ЕСТЬNULL(СервисСверкиРасчетовРеестрДокументов.Контрагент, ДанныеДляСверки.Контрагент) КАК Контрагент,
		|	ДанныеДляСверки.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП КАК КонтрагентКПП,
		|	ДанныеДляСверки.НомерСчетаФактуры КАК НомерСчетаФактуры,
		|	ЕСТЬNULL(СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку, ЛОЖЬ) КАК КонтрагентНеПроизводилСверку
		|ПОМЕСТИТЬ ЧтоНеСверилиПредварительнаяТаблица
		|ИЗ
		|	РегистрСведений.СервисСверкиРасчетовРеестрДокументов КАК СервисСверкиРасчетовРеестрДокументов
		|		ПОЛНОЕ СОЕДИНЕНИЕ ДанныеДляСверки КАК ДанныеДляСверки
		|		ПО (ДанныеДляСверки.СчетФактура = СервисСверкиРасчетовРеестрДокументов.Документ)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|		ПО СервисСверкиРасчетовРеестрДокументов.Контрагент = СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &ОтборПоКонтрагенту
		|				ТОГДА СервисСверкиРасчетовРеестрДокументов.Контрагент = &Контрагент
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ
		|	И ЕСТЬNULL(СервисСверкиРасчетовРеестрДокументов.Организация, ДанныеДляСверки.Организация) = &Организация
		|	И ЕСТЬNULL(СервисСверкиРасчетовОбнаруженныеРасхождения.Организация, &Организация) = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЧтоНеСверилиПредварительнаяТаблица.Организация КАК Организация,
		|	ЧтоНеСверилиПредварительнаяТаблица.Документ КАК Документ,
		|	ЧтоНеСверилиПредварительнаяТаблица.ИдентификаторСообщения КАК ИдентификаторСообщения,
		|	ЧтоНеСверилиПредварительнаяТаблица.ДатаДокумента КАК ДатаДокумента,
		|	ЧтоНеСверилиПредварительнаяТаблица.НомерДокумента КАК НомерДокумента,
		|	ЧтоНеСверилиПредварительнаяТаблица.СуммаБезНДС КАК Всего,
		|	ЧтоНеСверилиПредварительнаяТаблица.НДС КАК НДС,
		|	1 КАК КоличествоДокументов,
		|	ЧтоНеСверилиПредварительнаяТаблица.СуммаБезНДСКнига КАК СуммаБезНДСКнига,
		|	ЧтоНеСверилиПредварительнаяТаблица.НДСКнига КАК НДСКнига,
		|	ЧтоНеСверилиПредварительнаяТаблица.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ЧтоНеСверилиПредварительнаяТаблица.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ЧтоНеСверилиПредварительнаяТаблица.КодВидаОперации КАК КодВидаОперации,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ЧтоНеСверилиПредварительнаяТаблица.Контрагент КАК Контрагент,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентИНН КАК КонтрагентИНН,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентКПП КАК КонтрагентКПП,
		|	ЧтоНеСверилиПредварительнаяТаблица.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ИЗ
		|	ЧтоНеСверилиПредварительнаяТаблица КАК ЧтоНеСверилиПредварительнаяТаблица
		|ГДЕ
		|	ЧтоНеСверилиПредварительнаяТаблица.Организация = &Организация
		|	И ЧтоНеСверилиПредварительнаяТаблица.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|	И (ЧтоНеСверилиПредварительнаяТаблица.ИдентификаторСообщения = """"
		|			ИЛИ ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНеПроизводилСверку)
		|	И НЕ ЧтоНеСверилиПредварительнаяТаблица.КодВидаОперации В (&КодВидаОперации)
		|ИТОГИ
		|	СУММА(Всего),
		|	СУММА(НДС),
		|	СУММА(КоличествоДокументов),
		|	СУММА(СуммаБезНДСКнига),
		|	СУММА(НДСКнига)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДляСверки.СчетФактура КАК СчетФактура,
		|	СервисСверкиРасчетовДанныеСервиса.Сумма КАК СуммаКонтрагента,
		|	СервисСверкиРасчетовДанныеСервиса.НДС КАК НДСКонтрагента,
		|	СервисСверкиРасчетовДанныеСервиса.НомерДокумента КАК НомерДокумента,
		|	СервисСверкиРасчетовДанныеСервиса.ДатаДокумента КАК ДатаДокумента,
		|	СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры КАК НомерСчетФактурыК,
		|	СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры КАК ДатаСчетФактурыК,
		|	ДанныеДляСверки.СуммаБезНДС КАК СуммаБезНДСКнига,
		|	ДанныеДляСверки.НДС КАК НДСКнига,
		|	1 КАК КоличествоДокументов,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации КАК Всего,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации КАК НДС,
		|	ДанныеДляСверки.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверки.Контрагент КАК Контрагент,
		|	ДанныеДляСверки.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП КАК КонтрагентКПП,
		|	ДанныеДляСверки.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовДанныеСервиса КАК СервисСверкиРасчетовДанныеСервиса
		|			ПО СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки
		|		ПО ДанныеДляСверки.СчетФактура = СервисСверкиРасчетовОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|	И СервисСверкиРасчетовДанныеСервиса.Организация = &Организация
		|	И (СервисСверкиРасчетовДанныеСервиса.Сумма <> СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации
		|			ИЛИ СервисСверкиРасчетовДанныеСервиса.НДС <> СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации
		|			ИЛИ СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры <> ДанныеДляСверки.НомерСчетаФактуры
		|			ИЛИ СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры <> НАЧАЛОПЕРИОДА(ДанныеДляСверки.ДатаСчетаФактуры, ДЕНЬ))
		|ИТОГИ
		|	СУММА(СуммаКонтрагента),
		|	СУММА(НДСКонтрагента),
		|	СУММА(СуммаБезНДСКнига),
		|	СУММА(НДСКнига),
		|	СУММА(КоличествоДокументов),
		|	СУММА(Всего),
		|	СУММА(НДС)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЧтоНеСверилиПредварительнаяТаблица.Документ КАК Документ,
		|	ЧтоНеСверилиПредварительнаяТаблица.СуммаБезНДС КАК Всего,
		|	ЧтоНеСверилиПредварительнаяТаблица.НДС КАК НДС,
		|	1 КАК КоличествоДокументов,
		|	ЧтоНеСверилиПредварительнаяТаблица.СуммаБезНДСКнига КАК СуммаБезНДСКнига,
		|	ЧтоНеСверилиПредварительнаяТаблица.НДСКнига КАК НДСКнига,
		|	ЧтоНеСверилиПредварительнаяТаблица.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ЧтоНеСверилиПредварительнаяТаблица.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ЧтоНеСверилиПредварительнаяТаблица.КодВидаОперации КАК КодВидаОперации,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентИНН КАК КонтрагентИНН,
		|	ЧтоНеСверилиПредварительнаяТаблица.Контрагент КАК Контрагент,
		|	ЧтоНеСверилиПредварительнаяТаблица.КонтрагентКПП КАК КонтрагентКПП,
		|	ЧтоНеСверилиПредварительнаяТаблица.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ИЗ
		|	ЧтоНеСверилиПредварительнаяТаблица КАК ЧтоНеСверилиПредварительнаяТаблица
		|		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|		ПО ЧтоНеСверилиПредварительнаяТаблица.Документ = СервисСверкиРасчетовОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	ЕСТЬNULL(СервисСверкиРасчетовОбнаруженныеРасхождения.Организация, &Организация) = &Организация
		|	И ЧтоНеСверилиПредварительнаяТаблица.ИдентификаторСообщения <> """"
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Документ ЕСТЬ NULL
		|	И ЧтоНеСверилиПредварительнаяТаблица.Документ В
		|			(ВЫБРАТЬ
		|				ДанныеДляСверки.СчетФактура
		|			ИЗ
		|				ДанныеДляСверки)
		|	И ЧтоНеСверилиПредварительнаяТаблица.КонтрагентНеПроизводилСверку = ЛОЖЬ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляСверки.СчетФактура,
		|	ДанныеДляСверки.Всего,
		|	ДанныеДляСверки.НДС,
		|	1,
		|	ДанныеДляСверки.Всего,
		|	ДанныеДляСверки.НДС,
		|	ДанныеДляСверки.ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование,
		|	ДанныеДляСверки.КонтрагентИНН,
		|	ДанныеДляСверки.Контрагент,
		|	ДанныеДляСверки.КонтрагентКПП,
		|	ДанныеДляСверки.НомерСчетаФактуры
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|ГДЕ
		|	ДанныеДляСверки.КодВидаОперации В(&КодВидаОперации)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеДляСверки.СчетФактура,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации,
		|	1,
		|	ДанныеДляСверки.СуммаБезНДС,
		|	ДанныеДляСверки.НДС,
		|	ДанныеДляСверки.ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование,
		|	ДанныеДляСверки.КонтрагентИНН,
		|	ДанныеДляСверки.Контрагент,
		|	ДанныеДляСверки.КонтрагентКПП,
		|	ДанныеДляСверки.НомерСчетаФактуры
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовДанныеСервиса КАК СервисСверкиРасчетовДанныеСервиса
		|			ПО СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = СервисСверкиРасчетовДанныеСервиса.ИдентификаторСверки
		|		ПО ДанныеДляСверки.СчетФактура = СервисСверкиРасчетовОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	СервисСверкиРасчетовДанныеСервиса.Организация = &Организация
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
		|	И СервисСверкиРасчетовДанныеСервиса.Сумма = СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации
		|	И СервисСверкиРасчетовДанныеСервиса.НДС = СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации
		|	И ДанныеДляСверки.НомерСчетаФактуры = СервисСверкиРасчетовДанныеСервиса.НомерСчетФактуры
		|	И СервисСверкиРасчетовДанныеСервиса.ДатаСчетФактуры = НАЧАЛОПЕРИОДА(ДанныеДляСверки.ДатаСчетаФактуры, ДЕНЬ)
		|ИТОГИ
		|	СУММА(Всего),
		|	СУММА(НДС),
		|	СУММА(КоличествоДокументов),
		|	СУММА(СуммаБезНДСКнига),
		|	СУММА(НДСКнига)
		|ПО
		|	ОБЩИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДляСверки.СчетФактура КАК СчетФактура,
		|	ДанныеДляСверки.Организация КАК Организация,
		|	ДанныеДляСверки.СуммаБезНДС КАК СуммаБезНДСКнига,
		|	ДанныеДляСверки.НДС КАК НДСКнига,
		|	1 КАК КоличествоДокументов,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации КАК Всего,
		|	СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации КАК НДС,
		|	ДанныеДляСверки.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
		|	ДанныеДляСверки.СчетФактураДокумент КАК СчетФактураДокумент,
		|	ДанныеДляСверки.КодВидаОперации КАК КодВидаОперации,
		|	ДанныеДляСверки.КонтрагентНаименование КАК КонтрагентНаименование,
		|	ДанныеДляСверки.Контрагент КАК Контрагент,
		|	ДанныеДляСверки.КонтрагентИНН КАК КонтрагентИНН,
		|	ДанныеДляСверки.КонтрагентКПП КАК КонтрагентКПП,
		|	ДанныеДляСверки.НомерСчетаФактуры КАК НомерСчетаФактуры
		|ИЗ
		|	ДанныеДляСверки КАК ДанныеДляСверки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
		|		ПО ДанныеДляСверки.СчетФактура = СервисСверкиРасчетовОбнаруженныеРасхождения.Документ
		|ГДЕ
		|	ДанныеДляСверки.Организация = &Организация
		|	И НЕ ДанныеДляСверки.КодВидаОперации В (&КодВидаОперации)
		|	И СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту = &ИдентификаторСверкиКонтрагенту
		|
		|УПОРЯДОЧИТЬ ПО
		|	Всего УБЫВ
		|ИТОГИ
		|	СУММА(СуммаБезНДСКнига),
		|	СУММА(НДСКнига),
		|	СУММА(КоличествоДокументов),
		|	СУММА(Всего),
		|	СУММА(НДС)
		|ПО
		|	ОБЩИЕ";
	
	Возврат Текст;
	
КонецФункции

// Считывает из регистра сегмент данных раздела для выгрузки электронного представления декларации по НДС.
// Параметры:
//   ДекларацияНДС - ДокументСсылка.РегламентированныйОтчет - ссылка на документ, в котором сохраняются данные декларации.
//   ИмяРаздела - Строка - имя раздела.
//   НомерПервойСтроки - Число - номер первой строки, отраженной в сегменте.
//   
// Возвращаемое значение:
//   ТаблицаЗначений - сегмент данных раздела для выгрузки.
//
Функция СегментДанныхРазделаДекларацииНДС(ДекларацияНДС, ИмяРаздела, НомерПервойСтроки) Экспорт
	
	Перем ЗаписьРегистраСведений;
	
	ВидДополнительногоФайла = "Данные" + ИмяРаздела + "." + Формат(НомерПервойСтроки, "ЧН=; ЧГ=0");
	
	ПараметрыЧтения = Новый Структура;
	ПараметрыЧтения.Вставить("РегламентированныйОтчет", ДекларацияНДС);
	ПараметрыЧтения.Вставить("ВидДополнительногоФайла", ВидДополнительногоФайла);
	
	//++ НЕ УТ
	ЗаписьРегистраСведений = РегистрыСведений.ДополнительныеФайлыРегламентированныхОтчетов.СоздатьМенеджерЗаписи();
	ЗаписьРегистраСведений.РегламентированныйОтчет = ПараметрыЧтения.РегламентированныйОтчет;
	ЗаписьРегистраСведений.ВидДополнительногоФайла = ПараметрыЧтения.ВидДополнительногоФайла;
	ЗаписьРегистраСведений.Прочитать();
	//-- НЕ УТ
	Если ЗаписьРегистраСведений <> Неопределено И ЗначениеЗаполнено(ЗаписьРегистраСведений.ВидДополнительногоФайла) Тогда
		Данные = ЗаписьРегистраСведений.СодержимоеФайла.Получить();
	Иначе
		Данные = Неопределено;
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

// См. СервисСверкиРасчетов.ДанныеОРелевантныхКонтрагентах()
//
// Возвращаемое значение:
//  Структура из КлючИЗначение
//
Функция ДанныеОРелевантныхКонтрагентах() Экспорт
	
	Возврат СервисСверкиРасчетовУТ.ДанныеОРелевантныхКонтрагентах();
	
КонецФункции

// Возвращает наименование картинки, являющейся логотипом помощника конфигурации.
//
// Возвращаемое значение:
//   Строка - Имя картинки.
//
Функция КартинкаЛоготипаПомощника() Экспорт
	
	Возврат "КотСервисСверкиРасчетов";
	
КонецФункции

// Очищает накопленный кэш данных контрагентов 1С:Бизнес-сеть при включении сервиса.
// До включения сервиса кэш данных не актуализировался в случае изменения КПП контрагента
// После включения сервиса кэш будет сформирован заново по актуальным контрагентам (не по всем).
Процедура СброситьКэшДанныхКонтрагентов() Экспорт
	
	НаборЗаписей = РегистрыСведений.КонтрагентыБизнесСеть.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Проверяет по переданному признаку является ли организация или контрагент юридическим лицом.
// Параметры:
//   ПризнакДляОпределенияЮрЛица - ПеречислениеСсылка.ЮрФизЛицо - в зависимости от потребителя может быть перечислением или строкой.
//
// Возвращаемое значение:
//   Булево - Истина если является юридическим лицом.
//
Функция ЭтоЮридическоеЛицо(ПризнакДляОпределенияЮрЛица) Экспорт
	
	Возврат ПартнерыИКонтрагенты.ЭтоЮрЛицо(ПризнакДляОпределенияЮрЛица);
	
КонецФункции

#КонецОбласти
