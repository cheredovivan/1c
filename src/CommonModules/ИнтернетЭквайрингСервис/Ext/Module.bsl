///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ИнтернетЭквайринг".
// ОбщийМодуль.ИнтернетЭквайрингСервис.
// 
// Методы работы с платежным сервисом:
//  - подготовка данных аутентификации, определение адреса сервиса;
//  - получение списка участников Интернет-эквайринга;
//  - запросы на получение и отзыв токена;
//  - формирование запросов на возврат и получение статуса возврата.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область ОбщиеМетоды

// Определяет URL для вызова сервиса.
//
// Параметры:
//  Операция - Строка - путь к ресурсу;
//  ИдентификаторURL - Строка - идентификатор сценария в URL.
//
// Возвращаемое значение:
//  Строка - URL операции.
//
Функция URLОперацииСервиса(Операция, ИдентификаторURL = "")
	
	Возврат "https://"
		+ НастройкиПлатежныхСистемСлужебный.ХостСервисаОбменаДанными()
		+ "/api/isl/acquiring"
		+ Операция;
	
КонецФункции

// Формирует стандартные заголовки для вызова операции.
// 
// Параметры:
//  ДанныеАутентификации - Неопределено, Структура - данные аутентификации в ИПП.
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - заголовки операции.
//
Функция НовыйЗаголовкиВызоваОперации(ДанныеАутентификации = Неопределено)
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("X-Correlation-ID", Строка(Новый УникальныйИдентификатор));
	Заголовки.Вставить("X-Program-Nick", ИнтернетПоддержкаПользователей.СлужебнаяИмяПрограммы());
	
	Если ДанныеАутентификации <> Неопределено Тогда
		Заголовки.Вставить(
			"Authorization",
			ЗаголовокАутентификации(ДанныеАутентификации));
	КонецЕсли;
	
	Возврат Заголовки;
	
КонецФункции

// Проверяет результат отправки сообщения в сервис на запрет доступа к интернет и к сервисам Интернет-поддержки.
// 
// Параметры:
//  РезультатОперации - см. ИнтернетЭквайрингСлужебный.НовыйРезультатОперации.
//  РезультатОтправки - см. ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет.
//  ТекстОшибки - Строка - текст сообщения об ошибке, который должен быть установлен в результат операции, если
//    ошибка отличается от запрета работы с интернет и сервисами Интернет-поддержки
//
Процедура СформироватьСообщениеОбОшибке(
		РезультатОперации,
		РезультатОтправки,
		ТекстОшибки)
	
	Если РезультатОтправки.КодСостояния = -1 Тогда
		РезультатОперации.СообщениеОбОшибке = ОбщегоНазначения.ЗапрещенДоступКИнтернетСервисамТекстСообщения();
	ИначеЕсли РезультатОтправки.КодСостояния = -2 Тогда
		РезультатОперации.СообщениеОбОшибке = ИнтернетПоддержкаПользователей.ЗапрещенДоступКИнтернетПоддержкеТекст();
	Иначе
		РезультатОперации.СообщениеОбОшибке = ТекстОшибки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Аутентификация

// Возвращает логин и пароль Интернет-поддержки или тикет аутентификации.
//
// Параметры:
//  URLОперации -Строка - URL операции, для которой получаются данные аутентификации.
//
// Возвращаемое значение:
//  Структура - структура, содержащая результаты определения параметров
//    аутентификации пользователя Интернет-поддержки:
//    * ДанныеАутентификации - Структура - параметры аутентификации пользователя Интернет-поддержки;
//    * ИнформацияОбОшибке - Строка    - информация об ошибке для пользователя.
//    * Ошибка - Булево - признак наличия ошибки.
//
Функция ДанныеАутентификации(URLОперации)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеАутентификации", Новый Структура);
	Результат.Вставить("ИнформацияОбОшибке",   "");
	Результат.Вставить("Ошибка",               Ложь);
	
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	
	Если РазделениеВключено Тогда
		
		МодульИнтернетПоддержкаПользователейВМоделиСервиса =
			ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователейВМоделиСервиса");
		РезультатПолученияТикета =
			МодульИнтернетПоддержкаПользователейВМоделиСервиса.ТикетАутентификацииНаПорталеПоддержки(
				URLОперации);
		
		Если ПустаяСтрока(РезультатПолученияТикета.КодОшибки) Тогда
			Результат.ДанныеАутентификации.Вставить("Тикет", РезультатПолученияТикета.Тикет);
		Иначе
			Результат.Ошибка = Истина;
			Результат.ИнформацияОбОшибке =
				НСтр("ru = 'Ошибка аутентификации в сервисе.
					|Подробнее см. в журнале регистрации.';
					|en = 'Authentication error in the service.
					|For more information, see the event log.'");
			ПодробнаяИнформацияОбОшибке =
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось вызвать операцию %1.
						|Не удалось выполнить аутентификацию.
						|%2';
						|en = 'Cannot call operation %1.
						|Cannot authenticate.
						|%2'"),
					URLОперации,
					РезультатПолученияТикета.ИнформацияОбОшибке);
			ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
				ПодробнаяИнформацияОбОшибке,
				Истина);
		КонецЕсли;
	Иначе
		
		Результат.ДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
		Если Результат.ДанныеАутентификации = Неопределено Тогда
			Результат.Ошибка             = Истина;
			Результат.ИнформацияОбОшибке =
				НСтр("ru = 'Для работы с сервисом необходимо подключить Интернет-поддержку пользователей.';
					|en = 'To use the service, enable online user support.'");
			ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
				Результат.ИнформацияОбОшибке);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Результат.Ошибка Тогда
		Результат.ДанныеАутентификации.Вставить("ЕстьРазделение", РазделениеВключено);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает данные аутентификации участника ИнтернетЭквайринга.
//
// Параметры:
//  ПараметрыНастройкиПодключения - см. ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения.
//
// Возвращаемое значение:
//  Структура - структура, содержащая результаты определения параметров
//    аутентификации мерчанта в системе банка:
//    * Аутентификация - Неопределено - параметры аутентификации не найдены.
//                     - Структура Из КлючИЗначение - параметры аутентификации:
//                         ** Ключ - Строка - идентификатор параметра аутентификации
//                         ** Значение - Строка - значение параметра аутентификации
//    * СпособАутентификации - Неопределено - значение настроек не обнаружено.
//                           - Строка - способ аутентификации в системах банка.
//                             Возможны следующие значения: BASIC, BEARER, HMAC_SHA256, HMAC_SHA512
//    * ИнформацияОбОшибке - Строка - информация об ошибке для пользователя;
//    * Ошибка - Булево - признак наличия ошибки.
//
Функция ДанныеАутентификацииУчастника(
		ПараметрыНастройкиПодключения)
	
	Результат = Новый Структура;
	Результат.Вставить("Аутентификация",          Неопределено);
	Результат.Вставить("СпособАутентификации",    Неопределено);
	Результат.Вставить("ИнформацияОбОшибке",      "");
	Результат.Вставить("Ошибка",                  Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеХранилища = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		ПараметрыНастройкиПодключения.НастройкаПодключения,
		ИнтернетЭквайрингСлужебный.КлючХранилищаДанныхАутентификации());
	УстановитьПривилегированныйРежим(Ложь);
	
	Эталон = ИнтернетЭквайрингСлужебный.НовыйДанныеАутентификации();
	
	Если ТипЗнч(ДанныеХранилища) = Тип("Структура")
		Или ТипЗнч(ДанныеХранилища) = Тип("ФиксированнаяСтруктура") Тогда
		ЗаполнитьЗначенияСвойств(Эталон, ДанныеХранилища);
	Иначе
		Результат.Ошибка = Истина;
		Результат.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не найдены данные аутентификации участника для настройки %1.';
				|en = 'The participant authentication data for the %1 setting is not found.'"),
			ПараметрыНастройкиПодключения.Наименование);
		Возврат Результат;
	КонецЕсли;
	
	ДанныеАутентификации = Новый Соответствие;
	
	// Приводим структуру сохраненных значений к эталонному виду.
	Если ТипЗнч(Эталон.ПараметрыАутентификации) = Тип("Массив") Тогда
		Для Каждого ОписаниеПоля Из Эталон.ПараметрыАутентификации Цикл
			
			ПараметрАутентификации = ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации();
			ЗаполнитьЗначенияСвойств(ПараметрАутентификации, ОписаниеПоля);
			
			ДанныеАутентификации.Вставить(
				ПараметрАутентификации.Идентификатор,
				ПараметрАутентификации.Значение);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Результат.СпособАутентификации = Эталон.СпособАутентификации;
	Результат.Аутентификация = ДанныеАутентификации;
	
	Возврат Результат;
	
КонецФункции

// Возвращает данные аутентификации Интернет-поддержки и участника ИнтернетЭквайринга.
//
// Параметры:
//  URLОперации -Строка - URL операции, для которой получаются данные аутентификации.
//  ПараметрыНастройкиПодключения - см. ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения.
//
// Возвращаемое значение:
//  Структура - структура, содержащая результаты определения параметров аутентификации:
//    * АутентификацияИПП - Неопределено - ошибка при определении параметров аутентификации.
//                        - Структура - параметры аутентификации пользователя Интернет-поддержки.
//    * ИдентификаторБанка - Неопределено - значение настроек не обнаружено.
//                             - Строка - идентификатор банка в платежной системе.
//    * АутентификацияБанк - Неопределено - значение настроек не обнаружено.
//                         - Структура Из КлючИЗначение - параметры аутентификации в системе банка.
//    * ИнформацияОбОшибке - Строка - информация об ошибке для пользователя;
//    * Ошибка - Булево - признак наличия ошибки.
//
Функция ДанныеАутентификацииИППиБанка(
		URLОперации,
		ПараметрыНастройкиПодключения)
	
	Результат = Новый Структура;
	Результат.Вставить("АутентификацияИПП",        Неопределено);
	Результат.Вставить("ИдентификаторБанка",       ПараметрыНастройкиПодключения.ИдентификаторУчастника);
	Результат.Вставить("АутентификацияБанк",       Неопределено);
	Результат.Вставить("ИнформацияОбОшибке",       "");
	Результат.Вставить("Ошибка",                   Ложь);
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		Результат.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		Результат.СообщениеОбОшибке  = РезультатИПП.ИнформацияОбОшибке;
		Результат.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат Результат;
	КонецЕсли;
	
	Результат.АутентификацияИПП = РезультатИПП.ДанныеАутентификации;
	
	РезультатЭквайринг = ДанныеАутентификацииУчастника(
		ПараметрыНастройкиПодключения);
	
	Если РезультатЭквайринг.Ошибка Тогда
		Результат.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		Результат.СообщениеОбОшибке  = РезультатЭквайринг.ИнформацияОбОшибке;
		Результат.ИнформацияОбОшибке = РезультатЭквайринг.ИнформацияОбОшибке;
		Возврат Результат;
	КонецЕсли;
	
	РезультатЭквайринг.Удалить("Ошибка");
	РезультатЭквайринг.Удалить("ИнформацияОбОшибке");
	
	Результат.АутентификацияБанк = РезультатЭквайринг;
	
	Возврат Результат;
	
КонецФункции

// Формирует строку аутентификации из заголовка запроса
//
// Параметры:
//  ДанныеАутентификации  - Структура, Неопределено - данные аутентификации ИПП.
//
// Возвращаемое значение:
//  Строка - результат преобразования.
//
Функция ЗаголовокАутентификации(ДанныеАутентификации)
	
	Если ДанныеАутентификации = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Если ДанныеАутентификации.ЕстьРазделение Тогда
		Результат = ИнтернетПоддержкаПользователей.ЗаголовокBearerАутентификации(ДанныеАутентификации.Тикет);
	Иначе
		Результат = ИнтернетПоддержкаПользователей.ЗаголовокБазовойСхемыАутентификации(
			ДанныеАутентификации.Логин,
			ДанныеАутентификации.Пароль);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ДополнениеJSON

// Добавляет в запись JSON данные аутентификации в системе банка.
//
// Параметры:
//  ЗаписьДанныхСообщения - ЗаписьJSON - запись, в которую необходимо
//    добавить данные аутентификации;
//  Аутентификация - Структура - параметры аутентификации мерчанта.
//  ДанныеДляПодписи - Массив Из Строка - данные шифрования
//
Процедура ЗаписатьДанныеАутентификацииУчастника(
		ЗаписьДанныхСообщения,
		Аутентификация,
		ДанныеДляПодписи)
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("apiSecretObject");
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	Если Аутентификация <> Неопределено Тогда

		Если Аутентификация.СпособАутентификации = СпособАутентификацииHMAC256() Тогда
			
			ВариантХеш = ХешФункция.SHA256;
			ИмяПараметра = "hmacSha256Value";
			
			СекретныйКлюч = ПолучитьДвоичныеДанныеИзHexСтроки(
				Аутентификация.Аутентификация.Получить("key"));
			
			РезультатШифрования = НастройкиПлатежныхСистемСлужебный.ЗначениеHMAC(
				СекретныйКлюч,
				ДанныеДляПодписи,
				ВариантХеш);
			
			Аутентификация.Аутентификация.Вставить(ИмяПараметра, РезультатШифрования);

		КонецЕсли;
		
		Для Каждого КлючЗначение Из Аутентификация.Аутентификация Цикл
			ЗаписьДанныхСообщения.ЗаписатьИмяСвойства(КлючЗначение.Ключ);
			ЗаписьДанныхСообщения.ЗаписатьЗначение(КлючЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
КонецПроцедуры

#КонецОбласти

#Область СпособыАутентификации

// Формирует идентификатор способа аутентификации HMAC_SHA256
//
// Возвращаемое значение:
//  Строка - идентификатор способа.
//
Функция СпособАутентификацииHMAC256()
	
	Возврат "HMAC_SHA256";
	
КонецФункции

// Определяет способы аутентификации, реализованные в текущей версии БИП
Функция ДопустимыеСпособыАутентификации()
	
	Результат = СтрРазделить("BEARER,BASIC,HMAC_SHA256", ",");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область УчастникиИнтернетЭквайринга

// Получает перечень доступных для взаимодействия участников Интернет-эквайринга.
//
// Возвращаемое значение:
//  Структура - результат операции:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который может быть обработан вызывающим методом.
//    * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//    * ДанныеУчастников - Неопределено - если в процессе получения информации возникли ошибки;
//                       - Массив из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыУчастника
Функция ПолучитьУчастников() Экспорт
	
	Результат = ИнтернетЭквайрингСлужебный.НовыйРезультатОперации();
	
	ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Загрузка участников Интернет-эквайринга.';
			|en = 'Import Online acquiring participants.'"),
		Ложь);
	
	Результат.Вставить("ДанныеУчастников", Неопределено);
	
	URLОперации = URLОперацииСервиса("/permit-all/supportive-api/providers/list");
	
	ПараметрыПодключения = НастройкиПлатежныхСистемПовтИсп.ИнициализироватьПараметрыПодключения();
	
	Заголовки = НовыйЗаголовкиВызоваОперации();
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "GET");
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить информацию об участниках Интернет-эквайринга.
				|
				|Техническая информация об ошибке:
				|При получении информации об участниках Интернет-эквайринга возникли ошибки.
				|URL: %1
				|Код ошибки: %2
				|Подробная информация:
				|%3';
				|en = 'Cannot get the information about the Online acquiring participants.
				|
				|View error details:
				|Errors occurred when receiving information about the Online acquiring participants.
				|URL: %1
				|Error code: %2
				|Detailed information:
				|%3'"),
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(ИнформацияОбОшибке),
			Истина);
		
		Результат.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеизвестнаяОшибка();
		Результат.ИнформацияОбОшибке = ИнформацияОбОшибке;
		
		СформироватьСообщениеОбОшибке(
			Результат,
			РезультатОтправки,
			НСтр("ru = 'Не удалось загрузить список участников Интернет-эквайринга.';
				|en = 'Cannot import the list of Online acquiring participants.'"));
		
		Возврат Результат;
		
	КонецЕсли;
	
	Результат.ДанныеУчастников = ОбработатьРезультатПолученияУчастников(РезультатОтправки.Содержимое);
	
	ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Завершена загрузка участников Интернет-эквайринга.';
			|en = 'Online acquiring participants are imported.'"),
		Ложь);
	
	Возврат Результат;
	
КонецФункции

// Выполняет обработку списка доступных участников Интернет-эквайринга, полученного из сервиса.
// 
// Возвращаемое значение:
// Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыУчастника
//
Функция ОбработатьРезультатПолученияУчастников(ТелоJSON)

	//[
	//    {
	//        "id": "Tochka",
	//        "name": "Точка Банк",
	//        "bik": "044525104",
	//        "manualUrl": "https://tochka.com/example-manual",
	//        "additionalFields": [
	//            {
	//                "id": "TOKEN",
	//                "merchantId": false,
	//                "label": "Токен",
	//                "length": 1000,
	//                "passwordMode": true
	//            }
	//        ]
	//    }
	//]

	РезультатЧтения = ОбщегоНазначения.JSONВЗначение(ТелоJSON);
	
	Если ТипЗнч(РезультатЧтения) <> Тип("Массив") Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Результат = Новый Массив;
	
	ДопустимыеСпособыАутентификации = ДопустимыеСпособыАутентификации();
	
	Для Каждого ОписаниеУчастника Из РезультатЧтения Цикл

		СпособАутентификации = ОписаниеУчастника.Получить("authorizationType");
	
		Если ДопустимыеСпособыАутентификации.Найти(СпособАутентификации) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
	
		Банк = ИнтернетЭквайрингКлиентСервер.НовыйПараметрыУчастника();
		Банк.Идентификатор = ОписаниеУчастника.Получить("id");
		Банк.Представление = ОписаниеУчастника.Получить("name");
		Банк.БИК = ОписаниеУчастника.Получить("bik");
		Банк.СпособАутентификации = СпособАутентификации;
		Банк.СсылкаСправка = ОписаниеУчастника.Получить("manualUrl");
		
		ПараметрыАутентификации = ОписаниеУчастника.Получить("additionalFields");
		
		Если ТипЗнч(ПараметрыАутентификации) = Тип("Массив") Тогда
			Для Каждого ОписаниеПоля Из ПараметрыАутентификации Цикл
				ДобавитьПолеАутентификации(Банк.ПоляАутентификации, ОписаниеПоля);
			КонецЦикла;
		КонецЕсли;
	
		Результат.Добавить(Банк);

	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Добавляет описание поля аутентификации в описание банка.
// 
// Параметры:
//  Поля - Массив Из Структура - поля аутентификации.
//  ОписаниеПоля - Соответствие Из КлючИЗначение - описание поля аутентификации.
Процедура ДобавитьПолеАутентификации(Поля, ОписаниеПоля)

	ПолеАутентификации = ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации();
	ПолеАутентификации.Идентификатор            = ОписаниеПоля.Получить("id");
	ПолеАутентификации.Представление            = ОписаниеПоля.Получить("label");
	ПолеАутентификации.ДлинаСтроки              = ОписаниеПоля.Получить("length");
	ПолеАутентификации.РежимПароля              = ОписаниеПоля.Получить("passwordMode");
	ПолеАутентификации.ЭтоИдентификаторМерчанта = ОписаниеПоля.Получить("merchantId");
	
	Если ТипЗнч(ПолеАутентификации.РежимПароля) <> Тип("Булево") Тогда
		ПолеАутентификации.РежимПароля = Ложь;
	КонецЕсли;
	
	Если ТипЗнч(ПолеАутентификации.ЭтоИдентификаторМерчанта) <> Тип("Булево") Тогда
		ПолеАутентификации.ЭтоИдентификаторМерчанта = Ложь;
	КонецЕсли;
	
	Поля.Добавить(ПолеАутентификации);
	
КонецПроцедуры

#КонецОбласти

#Область ВызовОперацийЗапросТокенаАутентификацииСервиса

// Получает токен для аутентификации в сервисе payment-gateway.
//
// Параметры:
//  ПараметрыНастройки - Структура - см. ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения;
//  СпособАутентификации - Строка - способ аутентификации в системах банка.
//                       - Неопределено - способ аутентификации должен быть получен из сохраненных данных.
//  ПараметрыАутентификации - Неопределено - данные аутентификации должны быть получены из сохраненных данных.
//                          - Соответствие Из КлючИЗначение - данные аутентификации:
//                              * Ключ - Строка - идентификатор параметра аутентификации.
//                              * Значение - Строка - значение параметра аутентификации.
//  ИдентификаторТокена - Строка - уникальный идентификатор токена;
//  ИмяСервиса - Строка - имя сервиса для которого выпускается токен.
//  ДополнительныеПараметры - Неопределено, Соответствие Из КлючИЗначение - дополнительные параметры настройки
//    подключения. Если значение не задано, то дополнительные параметры будут получены из базы.
//
// Возвращаемое значение:
//  Структура - получения данных аутентификации:
//    * Токен - Строка, Неопределено - идентификатор, по которому выполняется оплата;
//    * ТребуетсяНастройка - Булево - Истина, если для получения токена нужна дополнительная настройка.
//    * ТребуемыеНастройки - Неопределено, Массив Из см. НовыйОписаниеТребуемыхПолей - описание полей, значения которых
//        не хватает для успешного получения токена.
//    * ДополнительныеПараметры - Неопределено, Соответствие Из КлючИЗначение - дополнительные параметры настройки
//      подключения.
//    * КодОшибки - Строка - строковый код возникшей ошибки, который может быть обработан вызывающим методом;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияЗапросТокенаАутентификацииСервиса(
	ПараметрыНастройки,
	Знач СпособАутентификации,
	Знач ПараметрыАутентификации,
	ИдентификаторТокена,
	ИмяСервиса,
	Знач ДополнительныеПараметры = Неопределено) Экспорт
	
	ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Получение токена в сервисе payment-gateway.';
			|en = 'Get a token in the payment-gateway service.'"),
		Ложь);
	
	РезультатОперации = ИнтернетЭквайрингСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("ТребуетсяНастройка",      Ложь);
	РезультатОперации.Вставить("Токен",                   Неопределено);
	РезультатОперации.Вставить("ТребуемыеНастройки",      Неопределено);
	РезультатОперации.Вставить("ДополнительныеПараметры", Неопределено);
	
	ПараметрыПодключения = НастройкиПлатежныхСистемПовтИсп.ИнициализироватьПараметрыПодключения();

	URLОперации = URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/authorized/supportive-api/%1/tokens/request",
			ИмяСервиса));
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке  = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ДополнительныеПараметрыОбработка = Новый Соответствие;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Массив") Тогда
		Для Каждого ОписаниеНастройки Из ДополнительныеПараметры Цикл
			ДополнительныеПараметрыОбработка.Вставить(
				ОписаниеНастройки.Идентификатор,
				ОписаниеНастройки.Значение);
		КонецЦикла;
	ИначеЕсли ТипЗнч(ДополнительныеПараметры) = Тип("Соответствие") Тогда
		ДополнительныеПараметрыОбработка = ДополнительныеПараметры;
	Иначе
		ДополнительныеПараметрыОбработка = Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ДополнительныеПараметрыНастройки(
			ПараметрыНастройки.НастройкаПодключения);
	КонецЕсли;
	
	Если ПараметрыАутентификации = Неопределено Тогда
		ДанныеХранилища = ДанныеАутентификацииУчастника(ПараметрыНастройки);
		Если ДанныеХранилища.Ошибка Тогда
			РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке  = ДанныеХранилища.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = ДанныеХранилища.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		ПараметрыАутентификации = Новый Структура(
			"Аутентификация, СпособАутентификации",
			ДанныеХранилища.Аутентификация,
			ДанныеХранилища.СпособАутентификации);
	
	Иначе
		
		ПараметрыАутентификации = Новый Структура(
			"Аутентификация, СпособАутентификации",
			ПараметрыАутентификации,
			СпособАутентификации);
		
	КонецЕсли;
	
	ПараметрыЗапросаJSON = tokens_request(
		ПараметрыНастройки,
		ПараметрыАутентификации,
		ИдентификаторТокена,
		ДополнительныеПараметрыОбработка);
	
	Заголовки = НовыйЗаголовкиВызоваОперации(РезультатИПП.ДанныеАутентификации);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		
		СформироватьСообщениеОбОшибке(
			РезультатОперации,
			РезультатОтправки,
			НастройкиПлатежныхСистемСлужебный.ПереопределитьСообщениеПользователю(
				РезультатОперации.КодОшибки,
				РезультатОтправки.Содержимое,
				РезультатОтправки.КодСостояния));
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить токен аутентификации в сервисе.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При получении токена сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|X-Correlation-ID: %4
				|Подробная информация:
				|%5';
				|en = 'Cannot get the authentication token in the service.
				|
				|%1
				|
				|View error details:
				|The service returned an error when getting the token.
				|URL: %2
				|Error code: %3
				|X-Correlation-ID: %4
				|Detailed information:
				|%5'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			Заголовки.Получить("X-Correlation-ID"),
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ДанныеТокена = ПрочитатьДанные_tokens_request(РезультатОтправки.Содержимое);
	
	РезультатОперации.ТребуетсяНастройка      = ДанныеТокена.Статус = СтатусТребуетсяНастройка();
	РезультатОперации.Токен                   = ДанныеТокена.Токен;
	РезультатОперации.ТребуемыеНастройки      = ДанныеТокена.ТребуемыеНастройки;
	РезультатОперации.ДополнительныеПараметры = ДанныеТокена.ДополнительныеПараметры;
	
	ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Завершено получение токена.';
			|en = 'Token is received.'"),
		Ложь);
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для операции
// ​/isl​/acquiring​/permit-all​/supportive-api​/providers​/list
//
Функция tokens_request(
		ПараметрыНастройки,
		ПараметрыАутентификации,
		ИдентификаторТокена,
		ДополнительныеПараметры)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ДанныеДляПодписи = Новый Массив;
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("providerId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыНастройки.ИдентификаторУчастника);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("merchantId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыНастройки.ИдентификаторМерчанта);
	ДанныеДляПодписи.Добавить(ПараметрыНастройки.ИдентификаторМерчанта);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("clientTokenId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторТокена);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("additionalParams");
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	Для Каждого КлючЗначение Из ДополнительныеПараметры Цикл
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства(КлючЗначение.Ключ);
		ЗаписьДанныхСообщения.ЗаписатьЗначение(КлючЗначение.Значение);
	КонецЦикла;
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	ЗаписатьДанныеАутентификацииУчастника(
		ЗаписьДанныхСообщения,
		ПараметрыАутентификации,
		ДанныеДляПодписи);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Разбирает отчет сервиса на запрос получения токена
//
// Параметры:
//  ТелоJSON - Строка - тело ответа сервиса.
//
// Возвращаемое значение:
//  Структура - результат запроса токена:
//   * Статус - Строка, Неопределено - статус получения.
//   * Токен - Строка, Неопределено - токен.
//   * ТребуемыеНастройки - Массив Из см. НовыйОписаниеТребуемыхПолей - описание полей, значения которых не хватает для
//       успешного получения токена.
//   * ДополнительныеПараметры - Соответствие Из КлючИЗначение - дополнительные параметры настройки подключения.
// 
Функция ПрочитатьДанные_tokens_request(ТелоJSON)
	
	//	{
	//  "status": "SUCCESS", // SETTINGS_REQUIRED
	//  "serviceToken": "string",
	//  "requiredSettings": [
	//    {
	//      "fieldId": "string",
	//      "fieldLabel": "string",
	//      "allowedFieldValues": [
	//        {
	//          "key": "string",
	//          "value": "string"
	//        }
	//      ]
	//    }
	//  ],
	//  "additionalParams": {
	//    "additionalProp1": {},
	//    "additionalProp2": {},
	//    "additionalProp3": {}
	//  }
	//}
	
	РезультатЧтения = ОбщегоНазначения.JSONВЗначение(ТелоJSON); // Соответствие
	
	Результат = Новый Структура;
	
	Результат.Вставить("Статус",                  РезультатЧтения.Получить("status"));
	Результат.Вставить("Токен",                   РезультатЧтения.Получить("serviceToken"));
	Результат.Вставить("ТребуемыеНастройки",      Новый Массив);
	Результат.Вставить("ДополнительныеПараметры", Новый Соответствие);
	
	Если Результат.Статус = СтатусТребуетсяНастройка() Тогда
		ТребуемыеНастройки = РезультатЧтения.Получить("requiredSettings");
		Если ТипЗнч(ТребуемыеНастройки) = Тип("Массив") Тогда
			Для Каждого ОписаниеПоля Из ТребуемыеНастройки Цикл
				
				Эталон = НовыйОписаниеТребуемыхПолей();
				Если ТипЗнч(ОписаниеПоля) = Тип("Соответствие") Тогда
					Эталон.Идентификатор = ОписаниеПоля.Получить("fieldId");
					Эталон.Представление = ОписаниеПоля.Получить("fieldLabel");
					
					ДоступныеЗначения    = ОписаниеПоля.Получить("allowedFieldValues");
					Если ТипЗнч(ДоступныеЗначения) = Тип("Массив") Тогда
						Для Каждого Значение Из ДоступныеЗначения Цикл
							Если ТипЗнч(Значение) = Тип("Соответствие") Тогда
								Эталон.Значения.Вставить(
									Значение.Получить("key"),
									Значение.Получить("value"));
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
				
				Результат.ТребуемыеНастройки.Добавить(Эталон);
				
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;

	ДополнительныеПараметры = РезультатЧтения.Получить("additionalParams");
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из ДополнительныеПараметры Цикл
			Результат.ДополнительныеПараметры.Вставить(
				КлючЗначение.Ключ,
				КлючЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует пустое описание параметра, требуемого для аутентификации в системе банка
// 
// Возвращаемое значение:
//  Структура - Новый описание требуемых полей:
//   * Идентификатор - Строка - идентификатор параметра.
//   * Представление - Строка - представление параметра.
//   * Значения - Соответствие Из КлючИЗначение - значения параметра, доступные для выбора пользователем.
//
Функция НовыйОписаниеТребуемыхПолей()
	
	Результат = Новый Структура;
	
	Результат.Вставить("Идентификатор", "");
	Результат.Вставить("Представление", "");
	Результат.Вставить("Значения",      Новый Соответствие);
	
	Возврат Результат;
	
КонецФункции

// Возвращает строковое значение статуса указывающего на необходимость заполнения дополнительных настроек.
// 
// Возвращаемое значение:
//  Строка - статус.
//  
Функция СтатусТребуетсяНастройка()

	Возврат "SETTINGS_REQUIRED";
	
КонецФункции

#КонецОбласти

#Область ВызовОперацийОтзывТокеновАутентификацииСервиса

// Выполняет отзыв токенов аутентификации в сервисе payment-gateway.
//
// Параметры:
//  ИдентификаторыТокенов - Массив Из Строка - идентификаторы токенов для деактивации;
//  ИмяСервиса - Строка - имя сервиса для которого выпускается токен.
//
// Возвращаемое значение:
//  Структура - получения данных аутентификации:
//    *КодОшибки - Строка - строковый код возникшей ошибки, который
//     может быть обработан вызывающим методом;
//    *СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    *ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОперацияОтзывТокеновАутентификацииСервиса(
		ИдентификаторыТокенов,
		ИмяСервиса) Экспорт
	
	ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Отзыв токенов в сервисе payment-gateway.';
			|en = 'Revoke tokens in the payment-gateway service.'"),
		Ложь);
	
	РезультатОперации = ИнтернетЭквайрингСлужебный.НовыйРезультатОперации();
	
	ПараметрыПодключения = НастройкиПлатежныхСистемПовтИсп.ИнициализироватьПараметрыПодключения();
	
	URLОперации = URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/authorized/supportive-api/%1/tokens/revoke",
			ИмяСервиса));
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ПараметрыЗапросаJSON = tokens_revoke(
		ИдентификаторыТокенов);

	Заголовки = НовыйЗаголовкиВызоваОперации(РезультатИПП.ДанныеАутентификации);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		
		СформироватьСообщениеОбОшибке(
			РезультатОперации,
			РезультатОтправки,
			НастройкиПлатежныхСистемСлужебный.ПереопределитьСообщениеПользователю(
				РезультатОперации.КодОшибки,
				РезультатОтправки.Содержимое,
				РезультатОтправки.КодСостояния));
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось отозвать токены аутентификации в сервисе.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При получении токена сервис вернул ошибку.
				|URL: %2
				|Код ошибки: %3
				|X-Correlation-ID: %4
				|Подробная информация:
				|%5';
				|en = 'Cannot revoke authentication tokens in the service.
				|
				|%1
				|
				|View error details:
				|The service returned an error when getting the token.
				|URL: %2
				|Error code: %3
				|X-Correlation-ID: %4
				|Detailed information:
				|%5'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			Заголовки.Получить("X-Correlation-ID"),
			РезультатОтправки.ИнформацияОбОшибке);
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Отзыв токенов завершен.';
			|en = 'Tokens are revoked.'"),
		Ложь);

	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для операции
// ​/isl​/acquiring​/authorized​/supportive-api​/{serviceNick}​/tokens​/revoke
//
Функция tokens_revoke(ИдентификаторыТокенов)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("clientTokenIds");
	
	ЗаписьДанныхСообщения.ЗаписатьНачалоМассива();
	
	Для Каждого ИдентификаторТокена Из ИдентификаторыТокенов Цикл
		ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторТокена);
	КонецЦикла;
	
	ЗаписьДанныхСообщения.ЗаписатьКонецМассива();
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

#КонецОбласти

#Область ОперацииВозврата

// Создает возврат оплаты.
//
// Параметры:
//  ПараметрыВозврата - Структура - данные для формирования возврата;
//  ПараметрыНастройкиПодключения - см. ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения
//
// Возвращаемое значение:
//  Структура - результат выполнения операции возврата или операции проверки статуса возврата:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который может быть обработан вызывающим методом.
//    * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//    * СтатусОперации - Строка - состояние возврата в системе банка.
//    * ПараметрыОперации - см. ИнтернетЭквайрингСлужебный.НовыйОписаниеПараметровОперации.
//    * ПричинаОтказа - Строка - причина отказа, если статус операции равен EXCEEDED или REJECTED.
//
Функция ОперацияВозвратОплаты(ПараметрыВозврата, ПараметрыНастройкиПодключения) Экспорт
	
	ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Создание заказа на возврат через Интернет-эквайринг.';
			|en = 'Create an order for refund via Online acquiring.'"),
		Ложь);
	
	РезультатОперации = ИнтернетЭквайрингСлужебный.НовыйРезультатДлительнойОперации();
	РезультатОперации.Вставить("ПричинаОтказа", "");
	
	URLОперации = URLОперацииСервиса(
		"/authorized/provider-api/refunds/refunds/create");
	
	ДанныеАутентификации = ДанныеАутентификацииИППиБанка(
		URLОперации,
		ПараметрыНастройкиПодключения);
	
	Если ДанныеАутентификации.Ошибка Тогда
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке  = ДанныеАутентификации.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = ДанныеАутентификации.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	Заголовки = НовыйЗаголовкиВызоваОперации(ДанныеАутентификации.АутентификацияИПП);
	
	РезультатОтправки = ВызовОперацииВозвратОплаты(
		ПараметрыНастройкиПодключения,
		ПараметрыВозврата,
		Заголовки,
		ДанныеАутентификации.АутентификацияБанк,
		URLОперации);
	
	РезультатОперации.ПараметрыОперации.ИдентификаторОплаты = ПараметрыВозврата.ИдентификаторОперации;
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);

		СформироватьСообщениеОбОшибке(
			РезультатОперации,
			РезультатОтправки,
			НастройкиПлатежныхСистемСлужебный.ПереопределитьСообщениеПользователю(
				РезультатОперации.КодОшибки,
				РезультатОтправки.Содержимое,
				РезультатОтправки.КодСостояния));
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось создать заказ на возврат через Интернет-эквайринг.
				|
				|%1
				|
				|%2';
				|en = 'Cannot create an order for refund via Online acquiring.
				|
				|%1
				|
				|%2'"),
			РезультатОперации.СообщениеОбОшибке,
			НастройкиПлатежныхСистемСлужебный.ТехническаяИнформацияОбОшибке(
				URLОперации,
				РезультатОтправки.КодОшибки,
				Заголовки,
				РезультатОтправки.ИнформацияОбОшибке));
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ОбработатьОтвет_refund(
		РезультатОтправки.Содержимое,
		РезультатОперации);
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции возврата оплаты.
//
// Параметры:
//  ПараметрыНастройкиПодключения - Структура - см. ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения;
//  ПараметрыВозврата - Структура - данные для формирования возврата;
//  Заголовки - Соответствие Из КлючИЗначение - заголовки операции.
//  ДанныеАутентификации - Структура - данные аутентификации в сервисе банка.
//  URLОперации - Строка - url для вызова операции.
//
// Возвращаемое значение:
//  см. ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет
//
Функция ВызовОперацииВозвратОплаты(
		ПараметрыНастройкиПодключения,
		ПараметрыВозврата,
		Заголовки,
		ДанныеАутентификации,
		URLОперации)
	
	ПараметрыПодключения = НастройкиПлатежныхСистемПовтИсп.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = refunds_create_refund(
		ПараметрыВозврата,
		ДанныеАутентификации,
		ПараметрыНастройкиПодключения.ИдентификаторМерчанта);
	
	// Вызов операции выполняется в 6 итераций. Повторные вызовы операции требуются
	// для случаев, когда результат выполнения возврата не определен. Обычно операция возврата
	// не занимает много времени, но в ряде случаев время обработки может быть увеличено.
	// Между вызовами добавляется пауза, чтобы не нагружать сервис банка.
	//
	// Паузы между вызовами:
	// - между 2-м и 3-м вызовом делается пауза 3 секунды;
	// - между 3-м и 4-м вызовом делается пауза 10 секунд;
	// - между 4-м и 5-м вызовом делается пауза 15 секунд;
	// - между 5-м и 6-м вызовом делается пауза 15 секунд; 
	
	КоличествоОпераций = 0;
	Пока КоличествоОпераций <= 5 Цикл
		
		ПараметрыОтправки = Новый Структура;
		ПараметрыОтправки.Вставить("Метод"                   , "POST");
		ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
		ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
		ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
		ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
		ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
		ПараметрыОтправки.Вставить("Таймаут"                 , 30);
		
		РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
			URLОперации,
			,
			,
			ПараметрыОтправки);
		
		Если РезультатОтправки.КодСостояния < 500
			И РезультатОтправки.КодСостояния <> 0 Тогда
			Прервать;
		КонецЕсли;
		
		// Перед выполнением повторного вызова при работе в модели
		// сервиса необходимо получить новый тикет.
		Если ОбщегоНазначения.РазделениеВключено() Тогда
			
			РезультатИПП = ДанныеАутентификации(URLОперации);
			Если РезультатИПП.Ошибка Тогда
				Прервать;
			КонецЕсли;
			
			Заголовки = НовыйЗаголовкиВызоваОперации(РезультатИПП.ДанныеАутентификации);
			
		КонецЕсли;
		
		КоличествоОпераций = КоличествоОпераций + 1;
		
		Если КоличествоОпераций = 2 Тогда
			ИнтернетПоддержкаПользователей.Пауза(3);
		ИначеЕсли КоличествоОпераций = 3 Тогда
			ИнтернетПоддержкаПользователей.Пауза(5);
		ИначеЕсли КоличествоОпераций = 4 Тогда
			ИнтернетПоддержкаПользователей.Пауза(10);
		ИначеЕсли КоличествоОпераций = 5 Тогда
			ИнтернетПоддержкаПользователей.Пауза(15);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатОтправки;
	
КонецФункции

// Формирует параметры запроса для операции
// /isl/acquiring/authorized/provider-api/refunds/refunds/create
//
Функция refunds_create_refund(
		ПараметрыВозврата,
		ДанныеАутентификации,
		ИдентификаторМерчанта)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ДанныеДляПодписи = Новый Массив;
	
	// Параметры возврата.
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("providerId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыВозврата.ИдентификаторУчастника);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("merchantId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторМерчанта);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("paymentLinkId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыВозврата.ИдентификаторОплаты);
	ДанныеДляПодписи.Добавить(ПараметрыВозврата.ИдентификаторОплаты);

	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("paymentOperationId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыВозврата.ИдентификаторОперации);
	ДанныеДляПодписи.Добавить(ПараметрыВозврата.ИдентификаторОперации);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("idempotencyKey");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыВозврата.Идентификатор);
	ДанныеДляПодписи.Добавить(ПараметрыВозврата.Идентификатор);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("amount");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыВозврата.СуммаВозврата);
	ДанныеДляПодписи.Добавить(ПараметрыВозврата.СуммаВозврата);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("currency");
	ЗаписьДанныхСообщения.ЗаписатьЗначение("RUB");
	ДанныеДляПодписи.Добавить("RUB");
	
	ЗаписатьДанныеАутентификацииУчастника(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации,
		ДанныеДляПодписи);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции:
// /isl/acquiring/authorized/provider-api/refunds/refunds/create
//
Функция ПрочитатьДанные_refunds_create_refund(ТелоJSON)
	
	// Ответ сервиса:
	// 
	//{
	//  "paymentOperationId": "string",
	//  "status": "IN_PROGRESS" (IN_PROGRESS, SUCCESS, REJECTED),
	//  "statusDescription": "string",
	//  "operationDate": "2025-03-18T05:44:42.089Z",
	//  "amount": 0
	//}
	
	ТекстЖурналРегистрации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получен ответ платежной системы:
			|%1';
			|en = 'The payment system response is received:
			|%1'"),
		ТелоJSON);
	
	ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		ТекстЖурналРегистрации,
		Ложь);
	
	ИменаСвойствСоЗначениямиДата = Новый Массив;
	ИменаСвойствСоЗначениямиДата.Добавить("operationDate");
	
	ЧтениеОтвета = Новый ЧтениеJSON;
	ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
	Результат = ПрочитатьJSON(
		ЧтениеОтвета,
		Истина,
		ИменаСвойствСоЗначениямиДата,
		,
		"ВосстановитьДатуJSON",
		НастройкиПлатежныхСистемСлужебный,
		,
		ИменаСвойствСоЗначениямиДата);
	
	Возврат Результат;
	
КонецФункции

// Обрабатывает ответ сервиса на запрос нового возврата.
//
// Параметры:
//  СодержимоеОтвета - Строка - содержание ответа сервиса.
//  РезультатОперации - см. ИнтернетЭквайрингСервис.ОперацияВозвратОплаты.
//
Процедура ОбработатьОтвет_refund(СодержимоеОтвета, РезультатОперации)
	
	ДанныеВозврата = ПрочитатьДанные_refunds_create_refund(
		СодержимоеОтвета);

	РезультатОперации.СтатусОперации = ДанныеВозврата.Получить("status");
	РезультатОперации.ПараметрыОперации.ИдентификаторОперации = ДанныеВозврата.Получить("refundOperationId");
	РезультатОперации.ПараметрыОперации.ИдентификаторОплаты   = ДанныеВозврата.Получить("paymentOperationId");
	РезультатОперации.ПараметрыОперации.ДатаОперации          = Дата(1, 1, 1);
	РезультатОперации.ПараметрыОперации.СуммаОперации         = ДанныеВозврата.Получить("amount");
	
	ОписаниеСтатуса = ДанныеВозврата.Получить("statusDescription");
	
	Если ЗначениеЗаполнено(ОписаниеСтатуса) Тогда
		РезультатОперации.ПричинаОтказа = ОписаниеСтатуса;
	КонецЕсли;
	
	Если РезультатОперации.СтатусОперации = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаОтменена() Тогда
		
		Если ЗначениеЗаполнено(ОписаниеСтатуса) Тогда
			ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось создать заказ на возврат через Интернет-эквайринг.
					|
					|%1
					|
					|Техническая информация об ошибке:
					|Операция была отменена.';
					|en = 'Cannot create an order for refund via Online acquiring.
					|
					|%1
					|
					|View error details:
					|The transaction was canceled.'"),
				ОписаниеСтатуса);
		Иначе
			ИнформацияОбОшибке = НСтр("ru = 'Не удалось создать заказ на возврат через Интернет-эквайринг.
				|
				|Причина отмены операции не указана.
				|
				|Техническая информация об ошибке:
				|Операция была отменена.';
				|en = 'Cannot create an order for refund via Online acquiring.
				|
				|The reason for transaction cancellation is not specified.
				|
				|View error details:
				|The transaction was canceled.'");
		КонецЕсли;
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ИнформацияОбОшибке,
			Истина);
		
	Иначе
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			НСтр("ru = 'Завершено создание заказа на возврат.';
				|en = 'Cannot create an order for refund.'"),
			Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

// Получает статус возврат оплаты.
//
// Параметры:
//  ИдентификаторОперации - Строка - идентификатор возврата в платежном сервисе;
//  ДатаЗапросаСтатуса - Дата - дата последнего запроса статуса оплаты;
//  ПараметрыНастройкиПодключения - Структура - параметры выполнения операции
//    см. ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения;
//
// Возвращаемое значение:
//  Структура - результат проверки статуса заказа на возврат:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом;
//    * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора;
//    * СтатусВозврата - Строка - состояние возврата;
//    * ИдентификаторОперации - Строка - идентификатор операции возврата;
//    * ИдентификаторОплаты - Строка - идентификатор операции оплаты, по которой выполнен возврат;
//    * ДатаОперации - Дата - дата регистрации операции
//    * СуммаОперации - Число - фактическая сумма возврата по документу;
//    * ПричинаОтказа - Строка - причина отказа, если статус операции равен EXCEEDED или REJECTED.
//
Функция ОперацияСтатусВозврата(
		ИдентификаторОперации,
		ДатаЗапросаСтатуса,
		ПараметрыНастройкиПодключения) Экспорт
	
	РезультатОперации = ИнтернетЭквайрингСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("СтатусВозврата",        "");
	РезультатОперации.Вставить("ДатаОперации",          Дата(1, 1, 1));
	РезультатОперации.Вставить("СуммаОперации",         0);
	РезультатОперации.Вставить("ИдентификаторОперации", "");
	РезультатОперации.Вставить("ИдентификаторОплаты",   "");
	РезультатОперации.Вставить("ПричинаОтказа",         "");
	
	ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
		НСтр("ru = 'Проверка статуса заказа на возврат через Интернет-эквайринг.';
			|en = 'Checking the status of the order for refund via Online acquiring.'"),
		Ложь);
	
	URLОперации = URLОперацииСервиса(
			"/authorized/provider-api/refunds/refunds/get-by-ids");
	
	ДанныеАутентификации = ДанныеАутентификацииИППиБанка(
		URLОперации,
		ПараметрыНастройкиПодключения);
		
	Если ДанныеАутентификации.Ошибка Тогда
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке  = ДанныеАутентификации.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = ДанныеАутентификации.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	Заголовки = НовыйЗаголовкиВызоваОперации(ДанныеАутентификации.АутентификацияИПП);

	РезультатОтправки = ВызовОперацииСтатусВозврата(
		ИдентификаторОперации,
		ПараметрыНастройкиПодключения,
		Заголовки,
		ДанныеАутентификации.АутентификацияБанк,
		URLОперации);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = НастройкиПлатежныхСистемСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
			
		СформироватьСообщениеОбОшибке(
			РезультатОперации,
			РезультатОтправки,
			НастройкиПлатежныхСистемСлужебный.ПереопределитьСообщениеПользователю(
				РезультатОперации.КодОшибки,
				РезультатОтправки.Содержимое,
				РезультатОтправки.КодСостояния));
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить статус возврата в платежном сервисе.
				|
				|%1
				|
				|%2';
				|en = 'Cannot get the refund status in the payment service.
				|
				|%1
				|
				|%2'"),
			РезультатОперации.СообщениеОбОшибке,
			НастройкиПлатежныхСистемСлужебный.ТехническаяИнформацияОбОшибке(
				URLОперации,
				РезультатОтправки.КодОшибки,
				Заголовки,
				РезультатОтправки.ИнформацияОбОшибке));
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Строка(РезультатОперации.ИнформацияОбОшибке),
			Истина);
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ОбработатьОтвет_get_refund(
		РезультатОтправки.Содержимое,
		РезультатОперации);
	
	Возврат РезультатОперации;
	
КонецФункции

// Выполняет вызов операции определения статуса возврата оплаты.
//
// Параметры:
//  ИдентификаторОперации - Строка - идентификатор операции отмена платежа в платежном сервисе;
//  ПараметрыНастройкиПодключения - Структура - параметры выполнения операции
//    см. ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения;
//  Заголовки - Соответствие Из КлючИЗначение - заголовки операции.
//  ДанныеАутентификации - Структура - данные аутентификации в сервисе банка;
//  URLОперации - Строка - url для вызова операции;
//
// Возвращаемое значение:
//  см. ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет
//
Функция ВызовОперацииСтатусВозврата(
		ИдентификаторОперации,
		ПараметрыНастройкиПодключения,
		Заголовки,
		ДанныеАутентификации,
		URLОперации)
	
	ПараметрыПодключения = НастройкиПлатежныхСистемПовтИсп.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = get_refund(
		ИдентификаторОперации,
		ДанныеАутентификации,
		ПараметрыНастройкиПодключения.ИдентификаторУчастника,
		ПараметрыНастройкиПодключения.ИдентификаторМерчанта);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
		
	Возврат РезультатОтправки;
	
КонецФункции

// Формирует параметры запроса для операции
// /isl/acquiring/authorized/provider-api/refunds/refunds/get-by-id
//
Функция get_refund(
		ИдентификаторОперации,
		ДанныеАутентификации,
		ИдентификаторУчастника,
		ИдентификаторМерчанта)
	
	//	{
	//  "providerId": "string",
	//  "merchantId": "string",
	//  "apiSecretObject": {
	//    "token": "string",
	//    "userName": "string",
	//    "password": "string",
	//    "key": "string"
	//  },
	//  "refundOperationIds": [string]
	//}
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ДанныеДляПодписи = Новый Массив;
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("providerId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторУчастника);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("merchantId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторМерчанта);

	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("refundOperationIds");
	ЗаписьДанныхСообщения.ЗаписатьНачалоМассива();
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторОперации);
	ДанныеДляПодписи.Добавить(ИдентификаторОперации);
	ЗаписьДанныхСообщения.ЗаписатьКонецМассива();

	ЗаписатьДанныеАутентификацииУчастника(
		ЗаписьДанныхСообщения,
		ДанныеАутентификации,
		ДанныеДляПодписи);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Обрабатывает ответ сервиса на запрос о статусе возврата.
//
// Параметры:
//  СодержимоеОтвета - Строка - содержание ответа сервиса.
//  РезультатОперации - см. ИнтернетЭквайрингСервис.ОперацияВозвратОплаты.
//
Процедура ОбработатьОтвет_get_refund(СодержимоеОтвета, РезультатОперации)
	
	ДанныеВозврата = ПрочитатьДанные_refunds_create_refund(
		СодержимоеОтвета);
	
	Если ТипЗнч(ДанныеВозврата) = Тип("Массив") Тогда
		Если ДанныеВозврата.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		ДанныеВозврата = ДанныеВозврата[0];
	КонецЕсли;
	
	РезультатОперации.СтатусВозврата        = ДанныеВозврата.Получить("status");
	РезультатОперации.ИдентификаторОперации = ДанныеВозврата.Получить("refundOperationId");
	РезультатОперации.ИдентификаторОплаты   = ДанныеВозврата.Получить("paymentOperationId");
	РезультатОперации.ДатаОперации          = Дата(1, 1, 1);
	РезультатОперации.СуммаОперации         = ДанныеВозврата.Получить("amount");
	
	ОписаниеСтатуса = ДанныеВозврата.Получить("statusDescription");
	
	Если ЗначениеЗаполнено(ОписаниеСтатуса) Тогда
		РезультатОперации.ПричинаОтказа = ОписаниеСтатуса;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
