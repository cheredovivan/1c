// @strict-types

#Область СлужебныеПроцедурыИФункции

// Обработка проверки заполнения на сервере.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Отказ - Булево
//  ПроверяемыеРеквизиты - Массив из Строка
Процедура ОбработкаПроверкиЗаполненияНаСервере(Форма, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	ТекущийОбъект = Форма.РеквизитФормыВЗначение("Объект"); // ДокументОбъект
	
	Если Не ТекущийОбъект.ПроверитьЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда
		КадровыеРешения.ПроверитьНаличиеДубляДокумента(ТекущийОбъект, Отказ);
	КонецЕсли;
	
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Объект"));
	
КонецПроцедуры

// Форма документа после записи на сервере.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ОбъектНазначения - Произвольный
//  ПараметрыЗаписи - Структура
Процедура ФормаДокументаПослеЗаписиНаСервере(Форма, ОбъектНазначения, ПараметрыЗаписи) Экспорт
	ФормаДокументаПриПолученииДанных(Форма);
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнениеФормы = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнениеФормы");
		МодульАдаптацияУвольнениеФормы.ПослеЗаписиНаСервере(Форма, ОбъектНазначения, ПараметрыЗаписи);
	КонецЕсли;
КонецПроцедуры

// Заполнить ссылку на кадровый приказ.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения -
Процедура ФормаДокументаПриПолученииДанных(Форма) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнениеФормы = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнениеФормы");
		МодульАдаптацияУвольнениеФормы.ФормаДокументаПриПолученииДанных(Форма);
	КонецЕсли;
	
	Приказ = КадровыеРешения.СвязанныйСРешениемПриказ(Форма.Объект.Ссылка);
	
	Если ЗначениеЗаполнено(Приказ) Тогда
		Форма.КадровыйПриказИнфоНадпись = Новый ФорматированнаяСтрока(Строка(Приказ), , , , ПолучитьНавигационнуюСсылку(Приказ));
	Иначе
		Форма.КадровыйПриказИнфоНадпись = "";
	КонецЕсли;
	
	Форма.Элементы.КадровыйПриказИнфоНадпись.Видимость = ЗначениеЗаполнено(Форма.КадровыйПриказИнфоНадпись);
	
КонецПроцедуры

Процедура ДополнитьФормуКадровогоДокумента(Форма, ДобавлятьЭлементыФормы = Истина, ДобавлятьРеквизитыФормы = Истина, ОтложенноеИзменение = Ложь) Экспорт
	
	КадровыйДокумент = Форма.Объект.Ссылка;
	Если Не ЗначениеЗаполнено(КадровыйДокумент) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не КадровыеРешения.ИспользуетсяРешение(КадровыйДокумент) Тогда
		Возврат;
	КонецЕсли;
	
	ГруппаРешение = Форма.Элементы.Найти("ГруппаРешение");
	
	Если ГруппаРешение = НеОпределено Тогда
		Возврат;
	КонецЕсли;
		
	Если ДобавлятьРеквизитыФормы Тогда
		
		ДобавляемыеРеквизиты = Новый Массив;
		ДобавляемыеРеквизиты.Добавить(
			Новый РеквизитФормы("РешениеИнфоНадпись",
								Новый ОписаниеТипов("ФорматированнаяСтрока"),,
								НСтр("ru = 'Решение';
									|en = 'Decision'")));
		
		МассивИменРеквизитовФормы = Новый Массив;
		ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(Форма, МассивИменРеквизитовФормы);
		ЗарплатаКадры.ИзменитьРеквизитыФормы(Форма, ДобавляемыеРеквизиты, МассивИменРеквизитовФормы,, ОтложенноеИзменение);
		
	КонецЕсли;
	
	Если ДобавлятьЭлементыФормы И Форма.Элементы.Найти("РешениеИнфоНадпись") = Неопределено Тогда
		
		Элемент = Форма.Элементы.Вставить("РешениеИнфоНадпись", Тип("ПолеФормы"), ГруппаРешение);
		Элемент.Вид = ВидПоляФормы.ПолеНадписи;
		Элемент.ПутьКДанным = "РешениеИнфоНадпись";
		Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элемент.УстановитьДействие("ОбработкаНавигационнойСсылки", "Подключаемый_РешениеИнфоНадписьОбработкаНавигационнойСсылки");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОтображениеНадписейВКадровомДокументе(Форма) Экспорт
	
	Если Форма.Элементы.Найти("РешениеИнфоНадпись") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Решение = Форма.Объект.Решение;
	
	Если ЗначениеЗаполнено(Решение) Тогда
		Форма.РешениеИнфоНадпись = Новый ФорматированнаяСтрока(Строка(Решение), , , , "ДокументРешение");
	Иначе
		Форма.РешениеИнфоНадпись = Новый ФорматированнаяСтрока(НСтр("ru = 'Указать решение о приеме на работу...';
																	|en = 'Specify an employment decision...'"), , , , Решение.Метаданные().Имя);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаНавигационнойСсылкиРешенияЗавершение(Форма, Результат, ДополнительныеПараметры) Экспорт
	
	Форма.Объект.Решение = Результат;
	Форма.Модифицированность = Истина;
	
	УстановитьОтображениеНадписейВКадровомДокументе(Форма);
	
КонецПроцедуры

#КонецОбласти