
#Область СлужебныйПрограммныйИнтерфейс

Процедура ВыполнитьЗапланированныеКорректировкиДвижений(Движения) Экспорт
	
	ДвиженияВСтруктуре = ТипЗнч(Движения) = Тип("Структура");
	
	Для Каждого Движение Из Движения Цикл
		
		Набор = ?(ДвиженияВСтруктуре, Движение.Значение, Движение);
		
		Если Набор.ДополнительныеСвойства.Свойство("КорректировкиПоУчетам") Тогда
			Для Каждого ПараметрыКорректировки Из Набор.ДополнительныеСвойства.КорректировкиПоУчетам Цикл
				Модуль = ОбщегоНазначения.ОбщийМодуль(ПараметрыКорректировки.ИмяМодуля);
				Модуль.ВыполнитьКорректировкуДвижений(Набор, ПараметрыКорректировки);
			КонецЦикла;
		КонецЕсли;
		
		Если Набор.ДополнительныеСвойства.Свойство("ОтложенныеСторноЗаписи") Тогда
			Для Каждого Строка Из Набор.ДополнительныеСвойства.ОтложенныеСторноЗаписи Цикл
				ЗаполнитьЗначенияСвойств(Набор.Добавить(), Строка);
			КонецЦикла;
			Набор.ДополнительныеСвойства.Удалить("ОтложенныеСторноЗаписи");
			Набор.Записывать = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДополнитьКоллекциюВспомогательнымиДвижениями(Движения, ДокументСсылка, СтруктураВидовУчета) Экспорт

	МассивДобавляемыхКлючей = Новый Массив;
	МассивВспомогательныхРегистров = Новый Массив;
	ДвиженияМетаданные = ДокументСсылка.Метаданные().Движения;
	
	Если СтруктураВидовУчета.СтраховыеВзносы Тогда
		МассивВспомогательныхРегистров.Добавить(Метаданные.РегистрыНакопления.БухучетНачисленияУдержанияПоСотрудникам);
		МассивВспомогательныхРегистров.Добавить(Метаданные.РегистрыНакопления.БухучетНачисленияУдержанияПоКонтрагентамАкционерам);
	КонецЕсли;
	
	Для Каждого ВспомогательныйРегистр Из МассивВспомогательныхРегистров Цикл
		
		Если ДвиженияМетаданные.Содержит(ВспомогательныйРегистр) И Не Движения.Свойство(ВспомогательныйРегистр.Имя) Тогда
			
			МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ВспомогательныйРегистр.ПолноеИмя());
			РегистрНаборЗаписей = МенеджерРегистра.СоздатьНаборЗаписей();
			РегистрНаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
			РегистрНаборЗаписей.Прочитать();
			Движения.Вставить(ВспомогательныйРегистр.Имя, РегистрНаборЗаписей);
			МассивДобавляемыхКлючей.Добавить(ВспомогательныйРегистр.Имя);

		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтрСоединить(МассивДобавляемыхКлючей, ",");
	
КонецФункции

#КонецОбласти

