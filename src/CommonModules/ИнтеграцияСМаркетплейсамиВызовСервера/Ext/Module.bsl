
#Область СлужебныйПрограммныйИнтерфейс

Функция ВидМаркетплейсаУчетнойЗаписи(УчетнаяЗапись) Экспорт
	Результат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	Возврат Результат;
КонецФункции

#Область НоменклатураКонтрагентов

// Возвращает признак наличия номенклатуры для переданного партнера.
//
// Параметры:
//   Параметры - см. ИнтеграцияСМаркетплейсамиКлиентСервер.ПараметрыИзмененияВладельцаТоварногоКаталога.
//
// Возвращаемое значение:
//   Булево - признак наличия номенклатуры для переданного партнера.
//
Функция ПроверкаНаличияНоменклатурыПартнера(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьЗаписи
		|ИЗ
		|	Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
		|ГДЕ
		|	НоменклатураКонтрагентов.ВладелецНоменклатуры = &Партнер";
	Запрос.УстановитьПараметр("Партнер", Параметры.ТекущийВладелец);
	
	ДопТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ЕстьЗаписи
		|ИЗ
		|	&ИмяРегистра КАК ДанныеРегистра
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
		|		ПО ДанныеРегистра.НоменклатураПартнера = НоменклатураКонтрагентов.Ссылка
		|ГДЕ
		|	ДанныеРегистра.УчетнаяЗапись = &УчетнаяЗапись
		|	И НоменклатураКонтрагентов.Ссылка ЕСТЬ NULL";
	
	Если Параметры.ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ИмяРегистра = "РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon";
		
		ДопТекстЗапроса = ДопТекстЗапроса + "
			|	И ДанныеРегистра.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)";
		
		ДопТекстЗапроса = СтрЗаменить(
			ДопТекстЗапроса,
			"ДанныеРегистра.УчетнаяЗапись",
			"ДанныеРегистра.УчетнаяЗаписьМаркетплейса");
	Иначе
		ИмяРегистра = "";
	КонецЕСли;
	
	Если Не ПустаяСтрока(ИмяРегистра) Тогда
		ДопТекстЗапроса = СтрЗаменить(
			ДопТекстЗапроса,
			"&ИмяРегистра",
			ИмяРегистра);
		
		Запрос.Текст = Запрос.Текст + ОбщегоНазначения.ТекстОбъединитьВсе() + ДопТекстЗапроса;
		Запрос.УстановитьПараметр("УчетнаяЗапись", Параметры.УчетнаяЗапись);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Не Результат.Пустой();

КонецФункции

// Возвращает запущенную длительную операцию.
//
// Параметры:
//   Параметры - см. ИнтеграцияСМаркетплейсамиКлиентСервер.ПараметрыИзмененияВладельцаТоварногоКаталога.
//
// Возвращаемое значение:
//   см. ДлительныеОперации.ВыполнитьФункцию.
//
Функция ПеренестиНоменклатуруПартнера(Параметры) Экспорт

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Параметры.УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне               = Истина;
	ПараметрыВыполнения.ПрерватьВыполнениеЕслиОшибка = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания  =
		ИнтеграцияСМаркетплейсамиПовтИсп.ПредставлениеВидаТорговойПлощадки(Параметры.ВидТорговойПлощадки)
		+ ". " + НСтр("ru = 'Запись позиций товарного каталога.';
						|en = 'Save goods directory items.'");
	
	ИмяМетода = "ИнтеграцияСМаркетплейсамиСервер.ПеренестиНоменклатуруПартнера";
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		ИмяМетода,
		Параметры,
		Истина);

КонецФункции

// Возвращает Данные номенклатуры контрагентов.
//
// Параметры:
//   Ссылки - СправочникСсылка.НоменклатураКонтрагентов - номенклатура контрагента.
//
// Возвращаемое значение:
//   Массив из см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента.
//
Функция НоменклатураКонтрагентовПоСсылкам(Знач Ссылки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НоменклатураКонтрагентов.Владелец КАК Владелец,
		|	НоменклатураКонтрагентов.Идентификатор КАК Идентификатор
		|ПОМЕСТИТЬ втОтбор
		|ИЗ
		|	Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
		|ГДЕ
		|	НоменклатураКонтрагентов.Ссылка В(&Ссылки)";
	
	Запрос.УстановитьПараметр("Ссылки", Ссылки);
	
	Возврат СопоставлениеНоменклатурыКонтрагентов.ДанныеНоменклатурыКонтрагентаПоЗапросу(Запрос);
	
КонецФункции

#КонецОбласти

#Область Номенклатура

// Определяет вид номенклатуры для набора номенклатуры контрагентов.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//   НаборНоменклатурыКонтрагентов - Массив из см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента.
//
// Возвращаемое значение:
//   СправочникСсылка.ВидыНоменклатуры - вид номенклатуры.
//
Функция ВидНовойНоменклатуры(УчетнаяЗапись, НаборНоменклатурыКонтрагентов) Экспорт

	Возврат ИнтеграцияСМаркетплейсамиСервер.ВидНовойНоменклатуры(УчетнаяЗапись, НаборНоменклатурыКонтрагентов);

КонецФункции

// Получает данные для номенклатуры по категории сервиса.
//
// Параметры:
//   УчетнаяЗапись           - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//   НоменклатураКонтрагента - см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента.
//   РеквизитыНоменклатуры   - Неопределено - не заданы по умолчанию;
//                             см. ИнтеграцияСМаркетплейсамиСервер.НовыеРеквизитыНоменклатурыДляСозданияПоДаннымКонтрагента.
//
// Возвращаемое значение:
//   Произвольный - значения реквизитов для заполнения (см. ИнтеграцияСМаркетплейсамиСервер.НовоеОписаниеДанныхДляСозданияНоменклатуры).
//
Функция ПолучитьДанныеНоменклатурыПоКатегорииСервиса(УчетнаяЗапись, НоменклатураКонтрагента, РеквизитыНоменклатуры = Неопределено) Экспорт

	Результат = Неопределено;
	
	НаборНоменклатурыКонтрагентов = ИнтеграцияСМаркетплейсамиСервер.НоваяТаблицаДанныхКонтрагентаДляСозданияНоменклатуры();
	
	НоваяСтрока = НаборНоменклатурыКонтрагентов.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, НоменклатураКонтрагента);
	НоваяСтрока.НоменклатураНаименование = НоменклатураКонтрагента.Наименование;
	НоваяСтрока.НоменклатураКонтрагента  = НоменклатураКонтрагента;
	
	РеквизитыНоменклатурыДляЗаполнения =
		ИнтеграцияСМаркетплейсамиСервер.НовыеРеквизитыНоменклатурыДляСозданияПоДаннымКонтрагента(РеквизитыНоменклатуры);
	
	ДанныеДляЗаполненияНоменклатуры = ИнтеграцияСМаркетплейсамиСервер.ДанныеДляСозданияНоменклатурыПоДаннымКонтрагента(
		УчетнаяЗапись,
		НаборНоменклатурыКонтрагентов,
		РеквизитыНоменклатурыДляЗаполнения);
	
	Если ЗначениеЗаполнено(ДанныеДляЗаполненияНоменклатуры)
			И ЗначениеЗаполнено(НоваяСтрока.Номенклатура) Тогда
		Результат = ДанныеДляЗаполненияНоменклатуры.СоздаваемаяНоменклатура[НоваяСтрока.Номенклатура];
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		Результат = Новый Структура;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ИмпортТоваровИзСервиса

// Проверяет, не запущено ли регламентное задание импорта товарного каталога по учетной записи.
// Если не запущено, добавляет / обновляет регламентное задание по ручному запуску импорта.
// Регламентное задание добавляется с расписанием по однократному запуску при первой возможности.
// После выполнения это регламентное задание само себя удалит.
//
// Параметры:
//   Параметры - см. ИнтеграцияСМаркетплейсамиКлиентСервер.ПараметрыПолученияТоваровИзСервиса
//
Процедура ПроверитьИЗапуститьРучнойИмпортТоварногоКаталога(Параметры) Экспорт
	
	УчетнаяЗапись = Параметры.ПараметрыМетода.УчетнаяЗапись;
	
	ВидМаркетплейса = ВидМаркетплейсаУчетнойЗаписи(УчетнаяЗапись);
	
	Если ВидМаркетплейса <> Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Маркетплейс %1 не поддерживается';
				|en = 'The %1 marketplace is not supported'"), Строка(ВидМаркетплейса));
		
	КонецЕсли;
	
	ПараметрыПроверки = ИнтеграцияСМаркетплейсамиСервер.НовыеПараметрыПроверкиНаличияДанныхИЗапускаИмпортаКаталога();
	ПараметрыПроверки.УчетнаяЗапись = УчетнаяЗапись;
	ПараметрыПроверки.КлючЗадания   = Параметры.КлючЗадания;
	
	РезультатПроверкиИмпорта = ИнтеграцияСМаркетплейсамиСервер.ПроверитьНаличиеДанныхИЗапускИмпортаКаталога(
		ПараметрыПроверки);
	
	Если Не РезультатПроверкиИмпорта.ЗаданиеАктивно Тогда
		
		ДобавитьИзменитьРегламентноеЗаданиеРучногоИмпортаТоварногоКаталога(
			ВидМаркетплейса, УчетнаяЗапись, Параметры.РегламентноеЗадание, Параметры.КлючЗадания);
		
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСервер.ОтправитьСерверноеУведомлениеНаКлиент(
		ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяСобытияНачалаИмпортаТоварногоКаталога(),
		УчетнаяЗапись);
	
КонецПроцедуры

// Возвращает пользовательское представление результата выполнения импорта.
//
// Параметры:
//   Завершено - Неопределено, Дата - дата завершения, если без ошибок.
//
// Возвращаемое значение:
//   Строка - представление результата выполнения импорта.
//
Функция ПредставлениеРезультатаВыполненияЗаданияИмпортаКаталога(Завершено = Неопределено) Экспорт

	Если Завершено = Дата(1, 1, 1) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Завершено = Неопределено Тогда
		Шаблон = НСтр("ru = 'Загрузка данных о товарах с торговой площадки завершена с ошибками.';
						|en = 'Data on goods from the trading platform is imported with errors.'");
	Иначе
		Шаблон = НСтр("ru = 'Загрузка данных о товарах с торговой площадки завершена %1.';
						|en = 'Data on goods from the trading platform is imported %1.'");
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Шаблон,
		Формат(Завершено, "ДЛФ=DT"));

КонецФункции

// Возвращает запущенную длительную операцию.
//
// Параметры:
//   Параметры      - см. ИнтеграцияСМаркетплейсамиКлиентСервер.ПараметрыПолученияТоваровИзСервиса.
//   ЗапуститьВФоне - Булево - признак запуска в фоновом режиме.
//   ПроверитьЗапущенноеЗадание - Булево - признак необходимости проверки наличия запущенного задания по
//                                импорту товарного каталога.
//
// Возвращаемое значение:
//   см. ДлительныеОперации.НовыйРезультатВыполненияОперации.
//
Функция ПолучитьТоварыИзСервиса(Параметры, ЗапуститьВФоне = Истина, ПроверитьЗапущенноеЗадание = Истина) Экспорт
	
	Если ПроверитьЗапущенноеЗадание Тогда
		
		ПараметрыПроверки = ИнтеграцияСМаркетплейсамиСервер.НовыеПараметрыПроверкиНаличияДанныхИЗапускаИмпортаКаталога();
		
		ПараметрыПроверки.УчетнаяЗапись = Параметры.ПараметрыМетода.УчетнаяЗапись;
		ПараметрыПроверки.КлючЗадания   = Параметры.КлючЗадания;
		
		РезультатПроверкиИмпорта = ИнтеграцияСМаркетплейсамиСервер.ПроверитьНаличиеДанныхИЗапускИмпортаКаталога(
			ПараметрыПроверки);
		
		Если РезультатПроверкиИмпорта.ЗаданиеАктивно Тогда
			
			Результат = ДлительныеОперации.НовыйРезультатВыполненияОперации();
			Результат.Статус = "Выполняется";
			
			Возврат Результат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЕстьИдентификаторы = ИнтеграцияСМаркетплейсамиКлиентСервер.ЕстьПереданныеИдентификаторы(
		Параметры.ПараметрыМетода.Идентификаторы);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(Параметры.УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне      = ЗапуститьВФоне;
	ПараметрыВыполнения.ЗапуститьНеВФоне    = Не ЗапуститьВФоне;
	ПараметрыВыполнения.КлючФоновогоЗадания = Параметры.КлючЗадания;
	
	Если ЗначениеЗаполнено(Параметры.АдресРезультата) Тогда
		ПараметрыВыполнения.АдресРезультата = Параметры.АдресРезультата;
	ИначеЕсли ЗначениеЗаполнено(Параметры.УникальныйИдентификатор) Тогда
		ПараметрыВыполнения.АдресРезультата =
			ПоместитьВоВременноеХранилище(Неопределено, Параметры.УникальныйИдентификатор);
	КонецЕсли;
	
	ПараметрыВыполнения.ПрерватьВыполнениеЕслиОшибка = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания  =
		ИнтеграцияСМаркетплейсамиПовтИсп.ПредставлениеВидаТорговойПлощадки(Параметры.ПараметрыМетода.ВидТорговойПлощадки)
		+ ". " + НСтр("ru = 'Получение данных товарного каталога из сервиса.';
						|en = 'Receive goods directory data from the service.'");
		
	Если Не ЗапуститьВФоне Тогда
		
		ИнтеграцияСМаркетплейсамиСервер.ОтправитьСерверноеУведомлениеНаКлиент(
			ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяСобытияНачалаИмпортаТоварногоКаталога(),
			Параметры.ПараметрыМетода.УчетнаяЗапись);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(Параметры.НастройкиПорцииМетода.ИмяМетодаПолученияПорций) Тогда
		НаборПараметровФункции = Новый Структура;
		НаборПараметровФункции.Вставить("ИмяМетодаПолученияПорций", Параметры.НастройкиПорцииМетода.ИмяМетодаПолученияПорций);
		НаборПараметровФункции.Вставить("Контекст", Новый Структура);
		НаборПараметровФункции.Контекст.Вставить("Параметры", Параметры.ПараметрыМетода);
		НаборПараметровФункции.Контекст.Вставить("РазмерПорции", Параметры.НастройкиПорцииМетода.РазмерПорции);
		НаборПараметровФункции.Контекст.Вставить("ПараметрыПолученияПорции", Неопределено);
		
		//@skip-check invocation-parameter-type-intersect
		Результат = ДлительныеОперации.ВыполнитьФункциюВНесколькоПотоков(
			Параметры.ИмяМетода,
			ПараметрыВыполнения,
			НаборПараметровФункции);
	Иначе
		Результат = ДлительныеОперации.ВыполнитьФункцию(
			ПараметрыВыполнения,
			Параметры.ИмяМетода,
			Параметры.ПараметрыМетода);
	КонецЕсли;
	
	Если ЕстьИдентификаторы Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ЗапуститьВФоне Тогда
		
		ИнтеграцияСМаркетплейсамиСервер.ОтправитьСерверноеУведомлениеНаКлиент(
			ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяСобытияНачалаИмпортаТоварногоКаталога(),
			Параметры.ПараметрыМетода.УчетнаяЗапись);
		
	Иначе
		ПриЗавершенииИмпортаТоварногоКаталога(Параметры.ПараметрыМетода, Параметры.РегламентноеЗадание);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает номенклатуру, характеристику, упаковку по переданным идентификаторам.
//
// Параметры:
//   УчетнаяЗапись              - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//   ИдентификаторыПубликации   - Неопределено, Массив из Строка - идентификаторы товаров продавца.
//   ИдентификаторыМаркетплейса - Неопределено, Массив из Строка - идентификаторы товаров по данным торговой площадки.
//   ИдентификаторыSKU          - Неопределено, Массив из Строка - идентификаторы SKU по данным торговой площадки.
//
// Возвращаемое значение:
//   Соответствие из Строка - в ключе идентификатор,
//                            в значении структура с ключами: Номенклатура, Характеристика, Упаковка.
//
Функция ДанныеИдентификаторовТоваров(УчетнаяЗапись, ИдентификаторыПубликации = Неопределено,
			ИдентификаторыМаркетплейса = Неопределено, ИдентификаторыSKU = Неопределено) Экспорт

	ДанныеТоваров = Новый Соответствие;
	
	ШаблонТекстаЗапроса =
		"ВЫБРАТЬ
		|	&ИмяИдентификатора КАК ИмяИдентификатора,
		|	&Идентификатор КАК Идентификатор,
		|	НоменклатураКонтрагентов.Ссылка КАК НоменклатураПартнера,
		|	НоменклатураКонтрагентов.Номенклатура КАК Номенклатура,
		|	НоменклатураКонтрагентов.Характеристика КАК Характеристика,
		|	ВЫБОР
		|		КОГДА НоменклатураКонтрагентов.Упаковка = СправНоменклатура.ЕдиницаИзмерения
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|		ИНАЧЕ НоменклатураКонтрагентов.Упаковка
		|	КОНЕЦ КАК Упаковка
		|ИЗ
		|	&СтатусыПубликации КАК СтатусыПубликации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
		|		ПО СтатусыПубликации.НоменклатураПартнера = НоменклатураКонтрагентов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправНоменклатура
		|		ПО (НоменклатураКонтрагентов.Номенклатура = СправНоменклатура.Ссылка)
		|ГДЕ
		|	СтатусыПубликации.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись
		|	И &Идентификатор В (&СписокИдентификаторов)";
	
	Идентификаторы = Новый Соответствие;
	
	ВидМаркетплейса = ВидМаркетплейсаУчетнойЗаписи(УчетнаяЗапись);
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ИмяРегистраСтатусов = "РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon";
		
		Идентификаторы.Вставить("ИдентификаторПубликации",   "СтатусыПубликации.ИдентификаторПубликации");
		Идентификаторы.Вставить("ИдентификаторМаркетплейса", "СтатусыПубликации.ИдентификаторОбъектаМаркетплейса");
		Идентификаторы.Вставить("ИдентификаторSKU",          "СтатусыПубликации.ИдентификаторFBOSKU");
		
	ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		ИмяРегистраСтатусов = "РегистрСведений.СтатусыПубликацииТоваровЯндексМаркет";
		
		Идентификаторы.Вставить("ИдентификаторПубликации",   "СтатусыПубликации.ИдентификаторПредложения");
		Идентификаторы.Вставить("ИдентификаторМаркетплейса", "СтатусыПубликации.ИдентификаторТовараПлощадки");
		Идентификаторы.Вставить("ИдентификаторSKU",          "СтатусыПубликации.ИдентификаторТовараПлощадки");
		
	Иначе
		Возврат ДанныеТоваров;
	КонецЕсли;
	
	ШаблонТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&СтатусыПубликации", ИмяРегистраСтатусов);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	
	ЧастиТекстаЗапроса = Новый Массив;
	
	Если ЗначениеЗаполнено(ИдентификаторыПубликации) Тогда
		ЧастьТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&Идентификатор", Идентификаторы["ИдентификаторПубликации"]);
		ЧастьТекстаЗапроса = СтрЗаменить(ЧастьТекстаЗапроса, "&СписокИдентификаторов", "&ИдентификаторыПубликации");
		
		ЧастиТекстаЗапроса.Добавить(ЧастьТекстаЗапроса);
		
		Запрос.УстановитьПараметр("ИмяИдентификатора", "ИдентификаторПубликации");
		Запрос.УстановитьПараметр("ИдентификаторыПубликации", ИдентификаторыПубликации);
		
		ДанныеТоваров.Вставить("ИдентификаторПубликации", Новый Соответствие);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторыМаркетплейса) Тогда
		ЧастьТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&Идентификатор", Идентификаторы["ИдентификаторМаркетплейса"]);
		ЧастьТекстаЗапроса = СтрЗаменить(ЧастьТекстаЗапроса, "&СписокИдентификаторов", "&ИдентификаторыМаркетплейса");
		
		ЧастиТекстаЗапроса.Добавить(ЧастьТекстаЗапроса);
		
		Запрос.УстановитьПараметр("ИмяИдентификатора", "ИдентификаторМаркетплейса");
		Запрос.УстановитьПараметр("ИдентификаторыМаркетплейса", ИдентификаторыМаркетплейса);
		
		ДанныеТоваров.Вставить("ИдентификаторМаркетплейса", Новый Соответствие);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторыSKU) Тогда
		ЧастьТекстаЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "&Идентификатор", Идентификаторы["ИдентификаторSKU"]);
		ЧастьТекстаЗапроса = СтрЗаменить(ЧастьТекстаЗапроса, "&СписокИдентификаторов", "&ИдентификаторыSKU");
		
		ЧастиТекстаЗапроса.Добавить(ЧастьТекстаЗапроса);
		
		Запрос.УстановитьПараметр("ИмяИдентификатора", "ИдентификаторSKU");
		Запрос.УстановитьПараметр("ИдентификаторыSKU", ИдентификаторыSKU);
		
		ДанныеТоваров.Вставить("ИдентификаторSKU", Новый Соответствие);
	КонецЕсли;
	
	Если ЧастиТекстаЗапроса.Количество() = 0 Тогда
		Возврат ДанныеТоваров;
	КонецЕсли;
	
	Запрос.Текст = СтрСоединить(ЧастиТекстаЗапроса, ОбщегоНазначения.ТекстОбъединитьВсе());
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаДанныхИдентификаторов = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока ВыборкаДанныхИдентификаторов.Следующий() Цикл
		ДанныеТовара = Новый Структура;
		ДанныеТовара.Вставить("Номенклатура",         Справочники.Номенклатура.ПустаяСсылка());
		ДанныеТовара.Вставить("Характеристика",       Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		ДанныеТовара.Вставить("Упаковка",             Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
		ДанныеТовара.Вставить("НоменклатураПартнера", Справочники.НоменклатураКонтрагентов.ПустаяСсылка());
		
		ЗаполнитьЗначенияСвойств(ДанныеТовара, ВыборкаДанныхИдентификаторов);
		ДанныеТоваров[ВыборкаДанныхИдентификаторов.ИмяИдентификатора].Вставить(
			ВыборкаДанныхИдентификаторов.Идентификатор, ДанныеТовара);
	КонецЦикла;
	
	Возврат ДанныеТоваров;

КонецФункции

// Добавляет дополнительные реквизиты и метки в переданный объект Владелец.
//
// Параметры:
//  Владелец - СправочникОбъект.Номенклатура.
//  Свойства - Массив из Соответствие:
//     * Ключ - ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения - свойство, которое необходимо установить.
//     * Значение - Произвольный - новое значение свойства.
//
Процедура УстановитьСвойстваДополнительныеОбъекта(НоменклатураОбъект, ДополнительныеРеквизиты) Экспорт

	УправлениеСвойствами.УстановитьСвойстваУОбъекта(
		НоменклатураОбъект,
		ДополнительныеРеквизиты);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИмпортТоваровИзСервисаСлужебный

Процедура ДобавитьИзменитьРегламентноеЗаданиеРучногоИмпортаТоварногоКаталога(ВидМаркетплейса, УчетнаяЗапись,
			ИмяРегламентногоЗадания, КлючЗадания)
	
	Если ПустаяСтрока(ИмяРегламентногоЗадания) Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеЗадания    = Метаданные.РегламентныеЗадания[ИмяРегламентногоЗадания];
	КлючРучногоЗадания   = ИнтеграцияСМаркетплейсамиКлиентСервер.КлючЗаданияРучногоИмпортаТоварногоКаталога(
		КлючЗадания);
	
	ЗаданиеСуществует    = Ложь;
	ИдентификаторЗадания = Неопределено; // СправочникСсылка.ОчередьЗаданий, УникальныйИдентификатор
	
	Отбор = Новый Структура;
	Отбор.Вставить("Метаданные", МетаданныеЗадания);
	Отбор.Вставить("Ключ",       КлючРучногоЗадания);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатПоиска = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатПоиска.Количество() > 0 Тогда
		
		ЗаданиеСуществует    = Истина;
		ИдентификаторЗадания = РезультатПоиска[0].УникальныйИдентификатор;
		
	КонецЕсли;
	
	ПараметрыЗадания = ПараметрыРегламентногоЗаданияРучногоИмпортаТоварногоКаталога(
		ВидМаркетплейса, УчетнаяЗапись, МетаданныеЗадания, КлючРучногоЗадания);
	
	НачатьТранзакцию();
	
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		Если ЗаданиеСуществует Тогда
			РегламентныеЗаданияСервер.ИзменитьЗадание(ИдентификаторЗадания, ПараметрыЗадания);
		Иначе
			РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
		КонецЕсли;
		
		ЗаписатьДанныеЗапускаИмпортаТоварногоКаталога(УчетнаяЗапись, Истина);
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации(ВидМаркетплейса),
			УровеньЖурналаРегистрации.Ошибка, ,
			УчетнаяЗапись,
			ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

// Параметры:
//  ВидМаркетплейса   - ПеречислениеСсылка.ВидыМаркетплейсов - 
//  УчетнаяЗапись     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - 
//  КлючЗадания       - Строка - 
//  МетаданныеЗадания - ОбъектМетаданныхРегламентноеЗадание - 
// 
// Возвращаемое значение:
//  См. НовыеПараметрыРегламентногоЗадания
//
Функция ПараметрыРегламентногоЗаданияРучногоИмпортаТоварногоКаталога(ВидМаркетплейса, УчетнаяЗапись, 
			МетаданныеЗадания, КлючЗадания)
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		
		Префиксы = ИнтеграцияСМаркетплейсомOzonКлиентСервер.ПрефиксыСервиса();
		
		Наименование = ИнтеграцияСМаркетплейсомOzonКлиентСервер.НаименованиеРегламентногоЗадания(
			Префиксы.ИмпортТоварногоКаталога);
		
	Иначе
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Маркетплейс %1 не поддерживается';
				|en = 'The %1 marketplace is not supported'"), Строка(ВидМаркетплейса));
		
	КонецЕсли;
	
	ПараметрыЗадания = НовыеПараметрыРегламентногоЗадания();
	
	ПараметрыЗадания.Использование = Истина;
	ПараметрыЗадания.Метаданные    = МетаданныеЗадания;
	ПараметрыЗадания.Ключ          = КлючЗадания;
	ПараметрыЗадания.Наименование  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 (%2, запуск вручную)';
			|en = '%1 (%2, manual start)'"), Наименование, Строка(УчетнаяЗапись));
		
	ПараметрыМетода = ИнтеграцияСМаркетплейсомOzonКлиентСервер.ДополнительныеПараметрыРегламентногоЗадания(
		ИнтеграцияСМаркетплейсамиКлиентСервер.ПрефиксИмпортаТоварногоКаталога());
	
	ПараметрыМетода.ОбновлятьСопоставленные = Ложь;
	ПараметрыМетода.ЭтоЗапускВручную        = Истина;
	
	ПараметрыФункции = Новый Массив; // Массив из СправочникСсылка.УчетныеЗаписиМаркетплейсов, Структура
	
	ПараметрыФункции.Добавить(УчетнаяЗапись);
	ПараметрыФункции.Добавить(ПараметрыМетода);
	
	ПараметрыЗадания.Параметры = ПараметрыФункции;
	
	Возврат ПараметрыЗадания;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//   * Использование - Булево - 
//   * Метаданные    - Неопределено, ОбъектМетаданныхРегламентноеЗадание - 
//   * Ключ          - Строка - 
//   * Наименование  - Строка - 
//   * Параметры     - Массив из Произвольный - 
//   * Расписание    - РасписаниеРегламентногоЗадания - 
//   * ИнтервалПовтораПриАварийномЗавершении    - Число - 
//   * КоличествоПовторовПриАварийномЗавершении - Число - 
//
Функция НовыеПараметрыРегламентногоЗадания()
	
	Результат = Новый Структура;
	
	Результат.Вставить("Использование", Ложь);
	Результат.Вставить("Метаданные",    Неопределено); // ОбъектМетаданныхРегламентноеЗадание
	Результат.Вставить("Ключ",          "");
	Результат.Вставить("Наименование",  "");
	Результат.Вставить("Параметры",     Новый Массив);
	Результат.Вставить("Расписание",    Новый РасписаниеРегламентногоЗадания);
	
	Результат.Вставить("ИнтервалПовтораПриАварийномЗавершении",    10);
	Результат.Вставить("КоличествоПовторовПриАварийномЗавершении", 3);
	
	Возврат Результат;
	
КонецФункции

Процедура УдалитьРегламентноеЗаданиеРучногоИмпортаТоварногоКаталога(УчетнаяЗапись, ИмяРегламентногоЗадания)
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат;
		
	ИначеЕсли ПустаяСтрока(ИмяРегламентногоЗадания) Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеЗадания = Метаданные.РегламентныеЗадания[ИмяРегламентногоЗадания];
	КлючЗадания       = ИнтеграцияСМаркетплейсамиКлиентСервер.КлючЗаданияРучногоИмпортаТоварногоКаталога(
		Неопределено, Строка(УчетнаяЗапись.УникальныйИдентификатор()));
	
	Отбор = Новый Структура;
	
	Отбор.Вставить("Метаданные", МетаданныеЗадания);
	Отбор.Вставить("Ключ",       КлючЗадания);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатПоиска = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатПоиска.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗадания = РезультатПоиска[0].УникальныйИдентификатор;
	
	НачатьТранзакцию();
	
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		РегламентныеЗаданияСервер.УдалитьЗадание(ИдентификаторЗадания);
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, ,
			УчетнаяЗапись,
			ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ПриЗавершенииИмпортаТоварногоКаталога(Параметры, ИмяРегламентногоЗадания = "")
	
	ОжидаемыеПараметры = ИнтеграцияСМаркетплейсамиКлиентСервер.ПараметрыЗапросаПолученияТоваровИзСервиса();
	
	ЗаполнитьЗначенияСвойств(ОжидаемыеПараметры, Параметры);
	
	ЕстьИдентификаторы = ИнтеграцияСМаркетплейсамиКлиентСервер.ЕстьПереданныеИдентификаторы(
		ОжидаемыеПараметры.Идентификаторы);
	
	Если ЕстьИдентификаторы Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьДанныеЗапускаИмпортаТоварногоКаталога(ОжидаемыеПараметры.УчетнаяЗапись, , Истина);
	
	Если ОжидаемыеПараметры.ЭтоЗапускВручную Тогда
		
		Если ПустаяСтрока(ИмяРегламентногоЗадания) Тогда
			
			ИмяРегламентногоЗадания = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяРегламентногоЗаданияИмпортаТоварногоКаталога(
				ОжидаемыеПараметры.ВидТорговойПлощадки);
			
		КонецЕсли;
		
		УдалитьРегламентноеЗаданиеРучногоИмпортаТоварногоКаталога(
			ОжидаемыеПараметры.УчетнаяЗапись, ИмяРегламентногоЗадания);
		
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСервер.ОтправитьСерверноеУведомлениеНаКлиент(
		ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяСобытияЗавершенияИмпортаТоварногоКаталога(),
		ОжидаемыеПараметры.УчетнаяЗапись);
	
КонецПроцедуры

Процедура ЗаписатьДанныеЗапускаИмпортаТоварногоКаталога(УчетнаяЗапись, ЭтоСозданиеРучногоИмпорта = Ложь,
			ЭтоЗавершениеИмпорта = Ложь)
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат;
	КонецЕсли;
	
	КлючНастроек = ИнтеграцияСМаркетплейсамиКлиентСервер.КлючЗаданияИмпортаТоварногоКаталога(
		Строка(УчетнаяЗапись.УникальныйИдентификатор()));
	
	УстановитьПривилегированныйРежим(Истина);
	
	Настройки = ИнтеграцияСМаркетплейсамиСервер.ДанныеЗапускаИмпортаТоварногоКаталога(КлючНастроек);
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Если ЭтоСозданиеРучногоИмпорта Тогда
		Настройки.ДатаСозданияРучногоИмпортаТоварногоКаталога = ТекущаяДата;
	КонецЕсли;
	
	Если ЭтоЗавершениеИмпорта Тогда
		Настройки.ДатаЗавершения = ТекущаяДата;
	КонецЕсли;
	
	ОбщегоНазначения.ХранилищеСистемныхНастроекСохранить(
		ИнтеграцияСМаркетплейсамиКлиентСервер.КлючОбъектаДанныхЗапускаИмпортаТоварногоКаталога(),
		КлючНастроек,
		Настройки, ,
		"");
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Параметры:
//  АдресРезультата - Строка - адрес временного хранилища, в которое был помещен результат работы фонового задания.
//  Параметры - Структура - произвольные параметры.
//
// Возвращаемое значение:
//  Структура:
//   * Состояния - Структура:
//      ** КодОшибки - Строка
//      ** ОписаниеОшибки - Строка
//      ** Детализация - Массив из Строка
//   * Сообщения - Массив из СообщениеПользователю
//
Функция РезультатВыполненияФоновогоЗадания(АдресРезультата, Параметры) Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("Состояния", Новый Массив);
	Результат.Вставить("Сообщения", Новый Массив);
	
	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда
		Результат.Состояния.Добавить(РезультатВыполнения);
		
	ИначеЕсли ТипЗнч(РезультатВыполнения) = Тип("Соответствие") Тогда
		
		Для Каждого ПорцияВыполнения Из РезультатВыполнения Цикл
			
			ПорцияРезультат = ПорцияВыполнения.Значение;
			
			Если ПорцияРезультат.Статус = "Ошибка" Тогда
				
				ОбщегоНазначения.СообщитьПользователю(
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ПорцияРезультат.ИнформацияОбОшибке));
				
			ИначеЕсли ПорцияРезультат.Статус = "Выполнено" Тогда
				
				Для Каждого Сообщение Из ПорцияРезультат.Сообщения Цикл
					Результат.Сообщения.Добавить(Сообщение);
				КонецЦикла;
				
				РезультатВыполненияПорции = ПолучитьИзВременногоХранилища(ПорцияРезультат.АдресРезультата);
				УдалитьИзВременногоХранилища(ПорцияРезультат.АдресРезультата);
				
				Если РезультатВыполненияПорции <> Неопределено Тогда
					Результат.Состояния.Добавить(РезультатВыполненияПорции);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ПриЗавершенииИмпортаТоварногоКаталога(Параметры);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

// Возвращает данные заказа торговой площадки по ключу записи регистра сведений.
//
// Параметры:
//  КлючЗаписи - РегистрСведенийКлючЗаписи.ЗаказыТорговыхПлощадок
// 
// Возвращаемое значение:
//  См. ИнтеграцияСМаркетплейсамиКлиентСервер.НовыеДанныеЗаказаДляЗаполненияДанныхЭкземпляров
//
Функция ДанныеЗаказаТорговойПлощадкиПоКлючу(КлючЗаписи) Экспорт

	Результат = ИнтеграцияСМаркетплейсамиКлиентСервер.НовыеДанныеОтправленияДляОбработки();
	
	ДанныеЗаказа = РегистрыСведений.ЗаказыТорговыхПлощадок.ПолучитьДанныеЗаказа(
		КлючЗаписи.Заказ, КлючЗаписи.ДокументОтгрузки);
		
	Если ДанныеЗаказа <> Неопределено Тогда
		
		Результат.Заказ = ДанныеЗаказа.Заказ;
		Результат.ДокументЗаказа = ДанныеЗаказа.Заказ;
		Результат.НомерЗаказа = ДанныеЗаказа.НомерЗаказа;
		Результат.НомерОтправления = ДанныеЗаказа.НомерОтправления;
		Результат.СтатусОтправления = ДанныеЗаказа.Статус;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти
