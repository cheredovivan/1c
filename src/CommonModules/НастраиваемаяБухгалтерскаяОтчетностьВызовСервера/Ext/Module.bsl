
#Область СлужебныйПрограммныйИнтерфейс

// В фоновом режиме вызывает метод СправочникМенеджер.НастройкиЗаполненияБухОтчетности.СгенерироватьНастройкиПоУмолчанию
// 
// Параметры:
//  Настройка - СправочникСсылка.НастройкиЗаполненияБухОтчетности - Настройка
//  ВыбранныеФормы - массив из ПеречислениеСсылка.ФормыБухгалтерскойОтчетности - 
//  ТекущиеЭлементыНастройки - массив из СправочникСсылка.СтрокиБухОтчетности - 
//  ИДФормыОтчета - Строка - идентификатор редакции формы
//  Версия - Строка - Описание версии редакции формы
// 
// Возвращаемое значение:
//  Структура - См. ДлительныеОперации.ВыполнитьПроцедуру
Функция СгенерироватьНастройкиПоУмолчанию(Настройка, ВыбранныеФормы, ТекущиеЭлементыНастройки, ИДФормыОтчета, Версия) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор());
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование структуры справочников настройки заполнения бухгалтерской отчетности.';
															|en = 'Generate the structure of catalogs of setting accounting reporting filling.'");
	ПараметрыВыполнения.КлючФоновогоЗадания = Новый УникальныйИдентификатор();
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
		
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения, 
		"Справочники.НастройкиЗаполненияБухОтчетности.СгенерироватьНастройкиПоУмолчанию", 
		Настройка,
		ВыбранныеФормы,
		ТекущиеЭлементыНастройки,
		ИДФормыОтчета,
		Версия);

КонецФункции

// Возвращает ссылки на существующие строки настройки.
// 
// Параметры:
//  Настройка - СправочникСсылка.НастройкиЗаполненияБухОтчетности - Настройка
//  ВыбранныеФормы - массив из ПеречислениеСсылка.ФормыБухгалтерскойОтчетности
// 
// Возвращаемое значение:
//  Массив из См. СправочникМенеджер.НастройкиЗаполненияБухОтчетности.ТекущиеЭлементыНастройки
Функция ТекущиеЭлементыНастройки(Настройка, ВыбранныеФормы) Экспорт
	
	Возврат Справочники.НастройкиЗаполненияБухОтчетности.ТекущиеЭлементыНастройки(Настройка, ВыбранныеФормы);
	
КонецФункции

// Возвращает ссылки на существующие строки настройки.
// 
// Параметры:
//  ИДФормыОтчета - Строка
// 
// Возвращаемое значение:
//  Массив из См. СправочникМенеджер.НастройкиЗаполненияБухОтчетности.СоставФормБухотчетности
Функция СоставФормБухотчетности(ИДФормыОтчета) Экспорт
	
	Возврат Справочники.НастройкиЗаполненияБухОтчетности.СоставФормБухотчетности(ИДФормыОтчета);
	
КонецФункции

Функция АктуальнаяВерсияРедакцииФормы(ИДФормыОтчета) Экспорт
	
	Версия = "";
	АктуальныеПравила = Справочники.НастройкиЗаполненияБухОтчетности.АктуальныеРедакцииПоставляемыхПравил();
	
	Правила = АктуальныеПравила.Найти(ИДФормыОтчета,"РедакцияФормы");
	Если Правила<> Неопределено Тогда
		Версия = Правила.Версия;
	КонецЕсли;
	
	Возврат Версия;
	
КонецФункции

#КонецОбласти