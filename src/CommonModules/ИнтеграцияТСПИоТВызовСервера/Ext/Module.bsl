
#Область ПрограммныйИнтерфейс

// Обработать штрихкоды после уточнения на ТС ПИоТ.
// 
// Параметры:
//  ДанныеКодовМаркировки        - СтрокаТаблицыЗначений из см. ШтрихкодированиеОбщегоНазначенияИС.ИнициализацияТаблицыДанныхКодовМаркировки
//  ПараметрыСканирования        - см. ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования
//  ФормаУникальныйИдентификатор - УникальныйИдентификатор - источник вызова
//  АварийныйРежим               - Булево - Истина, если по результату проверки марки в ТС ПИоТ получен ответ 203
// 
// Возвращаемое значение:
//  - см. ШтрихкодированиеОбщегоНазначенияИС.ИнициализироватьРезультатОбработкиШтрихкода.
Функция ОбработатьШтрихкодыПослеУточненияНаТСПИоТ(ДанныеКодовМаркировки, ПараметрыСканирования, ФормаУникальныйИдентификатор, АварийныйРежим) Экспорт
	
	РезультатыПроверок = Новый Соответствие;
	
	ДанныеПоШтрихкодам = ШтрихкодированиеОбщегоНазначенияИС.ИнициализацияДанныхПоШтрихкодам();
	ДанныеПоШтрихкодам.ДанныеКодовМаркировки = ШтрихкодированиеОбщегоНазначенияИС.ИнициализацияТаблицыДанныхКодовМаркировки();
	
	ПараметрыСканированияЛокальные = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыСканирования, Ложь);
	
	ПараметрыСканированияЛокальные.РазрешеноЗапрашиватьКодМаркировки             = Ложь;
	ПараметрыСканированияЛокальные.КэшМаркируемойПродукции                       = Неопределено;
	ПараметрыСканированияЛокальные.ВыводитьСообщенияОбОшибках                    = Ложь;
	ПараметрыСканированияЛокальные.ИспользуетсяСоответствиеШтрихкодовСтрокДерева = Ложь;
	ПараметрыСканированияЛокальные.ВозможнаЗагрузкаТСД                           = Ложь;
	
	ВидыПродукции = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеКодовМаркировки.ВидПродукции);
	
	Если ШтрихкодированиеОбщегоНазначенияИС.ДопустимаТабачнаяПродукция(ПараметрыСканированияЛокальные)
		И (ШтрихкодированиеОбщегоНазначенияИС.ПрисутствуетТабачнаяПродукция(ВидыПродукции)
		Или ДанныеПоШтрихкодам.ПрисутствуетТабачнаяПродукция) Тогда
		
		ШтрихкодированиеОбщегоНазначенияМОТП.ДобавитьКолонкиТаблицыДанныеКодовМаркировки(ДанныеПоШтрихкодам, ПараметрыСканированияЛокальные);
		
	КонецЕсли;
	
	Если ШтрихкодированиеОбщегоНазначенияИС.ДопустимаПродукцияИСМП(ПараметрыСканированияЛокальные)
		И ШтрихкодированиеОбщегоНазначенияИС.ПрисутствуетПродукцияИСМП(ВидыПродукции) Тогда
		
		ШтрихкодированиеОбщегоНазначенияИСМП.ДобавитьКолонкиТаблицыДанныеКодовМаркировки(ДанныеПоШтрихкодам, ПараметрыСканированияЛокальные);
		
	КонецЕсли;
	
	НоваяСтрокаТаблицыКодовМаркировки = ДанныеПоШтрихкодам.ДанныеКодовМаркировки.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицыКодовМаркировки, ДанныеКодовМаркировки);
	
	НоваяСтрокаТаблицыКодовМаркировки.ТребуетсяПроверкаРазрешительногоРежимаНаТСПИоТ = Ложь;
	
	Если ОбщегоНазначенияИСМПКлиентСервер.ЭтоРасширеннаяВерсияГосИС()
		И ЗначениеЗаполнено(НоваяСтрокаТаблицыКодовМаркировки.АдресДереваУпаковок) Тогда
		
		ШтрихкодированиеИСМПВызовСервера.ДополнитьДеревоДаннымиРазрешительногоРежимаГИСМТ(НоваяСтрокаТаблицыКодовМаркировки.АдресДереваУпаковок, НоваяСтрокаТаблицыКодовМаркировки);
		ДеревоУпаковок = ПолучитьИзВременногоХранилища(НоваяСтрокаТаблицыКодовМаркировки.АдресДереваУпаковок);
		
		ДанныеПоШтрихкодам.ВложенныеШтрихкоды = ШтрихкодированиеИС.ИнициализацияВложенныхШтрихкодов(ДеревоУпаковок, Ложь);
		
	КонецЕсли;
	
	ШтрихкодированиеОбщегоНазначенияИС.ЗаполнитьВидыПродукцииВДанныхПоШтрихкодам(ДанныеПоШтрихкодам, ПараметрыСканированияЛокальные);
	
	ЕстьОшибки = ШтрихкодированиеОбщегоНазначенияИС.ВыполнитьПроверкуНаОшибкиДанныхПоШтрихкодам(ДанныеПоШтрихкодам, ПараметрыСканированияЛокальные);
	
	ДанныеПоШтрихкодам.ЭтоСканированиеВФормеПроверкиИПодбора = Истина;
	РезультатыОбработок = ШтрихкодированиеОбщегоНазначенияИС.ОбработатьДанныеПоШтрихкодам(ДанныеПоШтрихкодам, ПараметрыСканированияЛокальные, ЕстьОшибки, ФормаУникальныйИдентификатор, РезультатыПроверок);
	
	// Логирование результатов разбора штрихкода
	ЛогированиеЗапросовИСМПВызовСервера.Вывести("",, РезультатыОбработок);
	
	РезультатОбработки = РезультатыОбработок[ДанныеКодовМаркировки.Штрихкод];
	
	Если РезультатОбработки <> Неопределено Тогда
		РезультатОбработки.ПроверкаНаТСПИоТЗавершена = Истина;
	КонецЕсли;
	
	Если АварийныйРежим Тогда
		ОбщегоНазначенияИСМП.ВключитьАварийныйРежимРазрешительнойСистемы();
	КонецЕсли;
	
	//@skip-check constructor-function-return-section
	Возврат РезультатОбработки;
	//Возврат РезультатыОбработок;
	
КонецФункции

#КонецОбласти