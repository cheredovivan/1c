
#Область ПрограммныйИнтерфейс

// Функция выполняет проверку использования объекта в длительной операции
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма проверки использования ссылки в других объектах.
//  ПараметрыОбработчикаОжидания - см. ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания
//  ИспользоватьЛокальнуюФункциюПроверки - Булево - Использовать локальную функцию проверки
//
Процедура ПроверитьИспользованиеОбъекта(Форма, ПараметрыОбработчикаОжидания, ИспользоватьЛокальнуюФункциюПроверки = Ложь) Экспорт
	
	ЕстьСсылки = Неопределено;
	Результат = ЗапретРедактированияРеквизитовОбъектовУТВызовСервера.ПроверитьИспользованиеОбъекта(
		Форма.Параметры.Объект,
		Форма.УникальныйИдентификатор,
		Форма.АдресХранилища,
		ЕстьСсылки,
		ИспользоватьЛокальнуюФункциюПроверки);
	
	Если НЕ Результат.Статус = "Выполнено" Тогда
		Форма.ИдентификаторЗадания = Результат.ИдентификаторЗадания;
		Форма.АдресХранилища       = Результат.АдресРезультата;
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПроверитьИспользованиеОбъектаОповещение", ЭтотОбъект, Форма);
	
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			Результат, ОповещениеОЗавершении, ПараметрыОжидания);
	Иначе
		ЗапретРедактированияРеквизитовОбъектовУТКлиентСервер.ПолучитьРезультатПроверки(Форма, ЕстьСсылки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьИспользованиеОбъектаОповещение(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Результат.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		ЗапретРедактированияРеквизитовОбъектовУТКлиентСервер.ПолучитьРезультатПроверки(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти