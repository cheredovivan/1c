#Область СлужебныеПроцедурыИФункции

// Функция возвращает имя регистра в котором требуется отразить документ.
//
// Параметры:
//  ОбъектМетаданныхИмя - Строка - имя объекта метаданных документа.
//  
// Возвращаемое значение:
//  Строка - имя регистра сведений в котором нужно выполнить отражение документа.
//
Функция ИмяРегистраДляОтраженияДокумента(ОбъектМетаданныхИмя) Экспорт

	СоответствиеИзмерений = СоответствиеИзмеренийРегистровСостоянийДокументов();
	ИмяРегистра = "";
	
	Для каждого ЭлементСоответствия Из СоответствиеИзмерений Цикл
	
		ОбъектМетаданных = ЭлементСоответствия.Ключ.Измерения.Найти(ЭлементСоответствия.Значение);
		
		Если ОбъектМетаданных <> Неопределено Тогда
		
			ТипыЗначений = ОбъектМетаданных.Тип.Типы();
			
			Для каждого ТипЗначенияИзмерения Из ТипыЗначений Цикл
				
				ДокументСсылка = Новый(ТипЗначенияИзмерения);
				ОбъектМетаданных = ДокументСсылка.Метаданные();
				Если ОбъектМетаданных.Имя = ОбъектМетаданныхИмя Тогда
				
					ИмяРегистра = ЭлементСоответствия.Ключ.Имя;
				
				КонецЕсли;
			
			КонецЦикла;
		
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат ИмяРегистра;

КонецФункции

// Функция определяет необходимость отражения документа в регистрах состояний документов в текущем сеансе.
//
// Параметры:
//  ДокументИсточникИмя - Строка - имя объекта метаданных документа.
//  
// Возвращаемое значение:
//  Булево - признак отражения в текущем сеансе.
//
Функция ВыполнитьОтражениеВТекущемСеансе(ДокументИсточникИмя) Экспорт

	ТипыДокументовВыполненияОтраженияВТекущемСеансе = ТипыДокументовДляРасчетаСтатусаВТранзакции();
	ВыполнитьОтражениеВТекущемСеансе = ТипыДокументовВыполненияОтраженияВТекущемСеансе.Найти(ДокументИсточникИмя) <> Неопределено;
	Возврат ВыполнитьОтражениеВТекущемСеансе;

КонецФункции

// Функция возвращает имена типов данных документов, для которых требуется отражение в текущем сеансе.
// 
// Возвращаемое значение:
//  Массив из Строка - массив имен документов.
//
Функция ТипыДокументовДляРасчетаСтатусаВТранзакции() Экспорт

	СоответствиеИзмерений = СоответствиеИзмеренийРегистровСостоянийДокументов();
	ТипыДокументов = Новый Массив;
	
	Для каждого ЭлементСоответствия Из СоответствиеИзмерений Цикл
	
		ОбъектМетаданных = ЭлементСоответствия.Ключ.Измерения.Найти(ЭлементСоответствия.Значение);
		
		Если ОбъектМетаданных <> Неопределено Тогда
		
			ТипыЗначений = ОбъектМетаданных.Тип.Типы();
			
			Для каждого ТипЗначенияИзмерения Из ТипыЗначений Цикл
				
				ДокументСсылка = Новый(ТипЗначенияИзмерения);
				ОбъектМетаданных = ДокументСсылка.Метаданные();
				
				Если ТипыДокументов.Найти(ОбъектМетаданных.Имя) = Неопределено Тогда
				
					ТипыДокументов.Добавить(ОбъектМетаданных.Имя);
				
				КонецЕсли;	
			
			КонецЦикла;
		
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат ТипыДокументов;

КонецФункции

// Функция возвращает имена типов данных документов, для которых требуется отражение в текущем сеансе.
// 
// Возвращаемое значение:
// 	Соответствие Из КлючИЗначение - Содержит тексты запросов сторнирования, где:
// 		* Ключ - ОбъектМетаданныхРегистрСведений - имена регистров отражения документов.
// 		* Значение - Строка - имя измерения с отражаемыми в нем типами документов.
//
Функция СоответствиеИзмеренийРегистровСостоянийДокументов() Экспорт
	
	МетаданныеРегистровСведений = Метаданные.РегистрыСведений;
	
	СоответствиеИзмерений = Новый Соответствие;
	СоответствиеИзмерений.Вставить(МетаданныеРегистровСведений.СостоянияЗаказовКлиентов, "Заказ");
	СоответствиеИзмерений.Вставить(МетаданныеРегистровСведений.СостоянияЗаказовПоставщикам, "Заказ");
	СоответствиеИзмерений.Вставить(МетаданныеРегистровСведений.СостоянияВнутреннихЗаказов, "Заказ");
	//++ НЕ УТКА
	СоответствиеИзмерений.Вставить(МетаданныеРегистровСведений.СостоянияЭтаповПроизводства, "Этап");
	СоответствиеИзмерений.Вставить(МетаданныеРегистровСведений.СостоянияЗаказовНаПроизводство, "Заказ");
	//-- НЕ УТКА
	
	Возврат СоответствиеИзмерений;

КонецФункции

#КонецОбласти
