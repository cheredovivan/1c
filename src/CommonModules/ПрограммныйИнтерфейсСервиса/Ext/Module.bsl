////////////////////////////////////////////////////////////////////////////////
// ПрограммныйИнтерфейсСервиса: выполнение штатных функций МС прикладным кодом
// через внешний API 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает свойства версии программного интерфейса менеджера сервиса.
// 
// Возвращаемое значение:
//  Структура - свойства версии внешнего программного интерфейса:
//   * Версия - Число - номер версии внешнего программного интерфейса.
//   * ВерсияМенеджераСервиса - Строка - номер версии менеджера сервиса.
//   * ЧасовойПоясМенеджераСервиса - Строка - часовой пояс менеджера сервиса.
//   * ВнешнийАдресМенеджераСервиса - Строка
//
Функция СвойстваВерсииИнтерфейса() Экспорт

	Возврат ПрограммныйИнтерфейсСервисаПовтИсп.СвойстваВерсииИнтерфейса();

КонецФункции

// Возвращает действующий токен доступа пользователя.
// 
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - если не указан, используется текущий пользователь.
//  ВызыватьИсключениеПриОшибке - Булево - по умолчанию = Истина
//  СвойстваОтвета - Структура - возвращаемый параметр:
//   * Ошибка - Булево - ошибка при получении токена
//   * Сообщение - Строка - сообщение об ошибке.
//   * ВремяЖизни - Дата - Срок действия токена.
// 
// Возвращаемое значение:
//  Строка - токен доступа.
//  
Функция ДействующийТокенДоступа(Пользователь = Неопределено, ВызыватьИсключениеПриОшибке = Истина, СвойстваОтвета = Неопределено) Экспорт
	
	СвойстваОтвета = Новый Структура;
	СвойстваОтвета.Вставить("Ошибка", Ложь);
	СвойстваОтвета.Вставить("Сообщение", "");
	СвойстваОтвета.Вставить("ВремяЖизни", '00010101');
	
	Если Пользователь = Неопределено Тогда
		Пользователь =  Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);
	ДанныеТокенаДоступа = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Пользователь, "ДанныеТокенаДоступа");
	УстановитьПривилегированныйРежим(Ложь);
	УстановитьОтключениеБезопасногоРежима(Ложь);
	
	Если Не ДанныеТокенаДоступа = Неопределено И ДанныеТокенаДоступа.ВремяЖизни > ТекущаяДатаСеанса() Тогда
		СвойстваОтвета.ВремяЖизни = ДанныеТокенаДоступа.ВремяЖизни;
		Возврат ДанныеТокенаДоступа.ТокенДоступа;
	КонецЕсли;
	
	ДанныеТокенаДоступа = НовыйТокенДоступа(Пользователь, ВызыватьИсключениеПриОшибке);
	
	Если Не ДанныеТокенаДоступа.Ошибка Тогда
		УстановитьОтключениеБезопасногоРежима(Истина);
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Пользователь, ДанныеТокенаДоступа, "ДанныеТокенаДоступа");
		УстановитьПривилегированныйРежим(Ложь);
		УстановитьОтключениеБезопасногоРежима(Ложь);
	КонецЕсли;
	
	СвойстваОтвета.Ошибка = ДанныеТокенаДоступа.Ошибка;
	СвойстваОтвета.Сообщение = ДанныеТокенаДоступа.Сообщение;
	СвойстваОтвета.ВремяЖизни = ДанныеТокенаДоступа.ВремяЖизни;
	
	Возврат ДанныеТокенаДоступа.ТокенДоступа;
	
КонецФункции

// Получает новый токен доступа пользователя.
// 
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - если не указан, используется текущий пользователь.
//  ВызыватьИсключениеПриОшибке - Булево
// 
// Возвращаемое значение:
//  Структура - данные нового токена доступа:
//   * ТокенДоступа - Строка - если токен получен успешно
//                  - Неопределено - если при получении токена произошла ошибка
//   * ВремяЖизни - Дата - время жизни токена доступа
//   * Ошибка - Булево - ошибка при получении токена
//   * Сообщение - Строка - сообщение об ошибке 
Функция НовыйТокенДоступа(Пользователь = Неопределено, ВызыватьИсключениеПриОшибке = Истина) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ТокенДоступа", Неопределено);
	Результат.Вставить("ВремяЖизни", '00010101');
	Результат.Вставить("Ошибка", Ложь);
	Результат.Вставить("Сообщение", "");
	
	Если Пользователь = Неопределено Тогда
		Пользователь =  Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	ПолучаемыеСвойства = Новый Массив;
	ПолучаемыеСвойства.Добавить("ИдентификаторПользователяИБ");
	ПолучаемыеСвойства.Добавить("ИдентификаторПользователяСервиса");
	СвойстваПользователя = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Пользователь, ПолучаемыеСвойства);
	
	АдресПровайдера = РаботаВМоделиСервиса.АдресПровайдераАутентификации();
	Если Не ЗначениеЗаполнено(АдресПровайдера) Тогда
		Сообщение = НСтр("ru = 'Не указан адрес провайдера аутентификации.';
						|en = 'Authentication provider address required.'");
		Если ВызыватьИсключениеПриОшибке Тогда
			ВызватьИсключение Сообщение;
		Иначе
			Результат.Ошибка = Истина;
			Результат.Сообщение = Сообщение;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);
	
	// Пользователя нужно считать из базы, т.к. при смене пароля ТекущийПользователь() содержит старый пароль
	ТекущийПользователь = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
		СвойстваПользователя.ИдентификаторПользователяИБ);

	Если ТекущийПользователь = Неопределено Тогда
		// Выполняется запрос от администратора информационной базы после входа в область данных.
		ВызватьИсключение ПрограммныйИнтерфейсСервисаСлужебный.ТекстИсключенияВыполненияЗапросаОтАдминистратора();
	КонецЕсли;
	
	ХешПароля = СтрРазделить(ТекущийПользователь.СохраняемоеЗначениеПароля, ",")[0];
	
	УстановитьПривилегированныйРежим(Ложь);
	УстановитьОтключениеБезопасногоРежима(Ложь);
	
	ДанныеСервера = ОбщегоНазначенияКлиентСервер.СтруктураURI(АдресПровайдера);
	
	СхемаSSL = "https";
	Если НРег(ДанныеСервера.Схема) = СхемаSSL Тогда
		ЗащищенноеСоединение =  ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
	Иначе
		ЗащищенноеСоединение = Неопределено;
	КонецЕсли;
	
	Данные = Новый Структура;
	Данные.Вставить("grant_type", "client_credentials");
	Данные.Вставить("client_id", Строка(СвойстваПользователя.ИдентификаторПользователяСервиса));
	Данные.Вставить("client_secret", ХешПароля);
	
	УстановитьОтключениеБезопасногоРежима(Истина);
	СоединениеHTTP = Новый HTTPСоединение(ДанныеСервера.Хост, ДанныеСервера.Порт, , ,
		ПолучениеФайловИзИнтернета.ПолучитьПрокси(ДанныеСервера.Схема), 5, ЗащищенноеСоединение);
	УстановитьОтключениеБезопасногоРежима(Ложь);
	
	МоментПолучения = ТекущаяДатаСеанса();
	ЗапросHTTP = Новый HTTPЗапрос(СтрШаблон("%1/token", ДанныеСервера.ПутьНаСервере));
	ЗапросHTTP.Заголовки.Вставить("Content-Type", "application/json; charset=utf-8");
	ЗапросHTTP.УстановитьТелоИзСтроки(РаботаВМоделиСервисаБТС.СтрокаИзСтруктурыJSON(Данные));
	
	Попытка
		ОтветHTTP = СоединениеHTTP.ВызватьHTTPМетод("POST", ЗапросHTTP);
	Исключение
		Если ВызыватьИсключениеПриОшибке Тогда
			ВызватьИсключение
		Иначе
			Результат.Ошибка = Истина;
			Результат.Сообщение = ТехнологияСервиса.ПодробныйТекстОшибки(ИнформацияОбОшибке());
			Возврат Результат;
		КонецЕсли;
	КонецПопытки;
	
	Если Не ОтветHTTP.КодСостояния = 200 Тогда
		ТелоОтвета = ОтветHTTP.ПолучитьТелоКакСтроку();
		Сообщение = СтрШаблон("%1 %2", ОтветHTTP.КодСостояния, ТелоОтвета);
		Если ВызыватьИсключениеПриОшибке Тогда
			ВызватьИсключение Сообщение;
		Иначе
			Результат.Ошибка = Истина;
			Результат.Сообщение = Сообщение;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	ТипСодержимого = ОбщегоНазначенияБТСКлиентСервер.ЗаголовокHTTP(ОтветHTTP, "Content-Type");
	Если СтрНайти(ТипСодержимого, "application/json") = 0 Тогда
		Сообщение = НСтр("ru = 'Используется OpenID-провайдер платформы 1С:Предприятие
		|Для получения токенов доступа нужно использовать OpenID-провайдер Менеджера сервиса (см. https://its.1c.ru/db/freshpub#content:130:hdoc).';
		|en = 'The OpenID provider of the 1C:Enterprise platform is used
		|To get access tokens, use the service manager OpenID provider (see https://its.1c.ru/db/freshpub#content:130:hdoc).'");
		Если ВызыватьИсключениеПриОшибке Тогда
			ВызватьИсключение Сообщение;
		Иначе
			Результат.Ошибка = Истина;
			Результат.Сообщение = Сообщение;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	ПотокДанных = ОтветHTTP.ПолучитьТелоКакПоток();
	Данные = РаботаВМоделиСервисаБТС.СтруктураИзПотокаJSON(ПотокДанных);
	
	Результат.ТокенДоступа = Данные.access_token;
	Результат.ВремяЖизни = МоментПолучения + Данные.expires_in;
	
	Возврат Результат;
	
КонецФункции

#Область Account

// Возвращает список абонентов текущего пользователя.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - список абонентов:
//    * Наименование - Строка - наименование абонента
//    * Код - Число - код абонента
//    * РольПользователя - ПеречислениеСсылка.РолиПользователейАбонентов - роль текущего пользователя абонента.
//
Функция Абоненты() Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/list";
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияАбонент();
	Переименования.Вставить("role", Служебный.ОписаниеКолонки("РольПользователя", 
		Новый ОписаниеТипов("ПеречислениеСсылка.РолиПользователейАбонентов")));

	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.account, Переименования);

КонецФункции

// Возвращает дополнительные сведения (реквизиты и свойства) абонента этого приложения.
// Реализует метод внешнего программного интерфейса - account/attached_info.
// 
// Параметры:
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Структура - дополнительные реквизиты и свойства абонента:
//   * ПубличныйИдентификатор - Строка - публичный идентификатор абонента
//   * Реквизиты - ТаблицаЗначений - дополнительные реквизиты абонента:
//     ** Ключ - Строка - имя дополнительного реквизита
//     ** Тип - Строка - тип значения 
//     ** Значение - Строка, Число, Дата, Булево - значение дополнительного реквизита
//   * Свойства - ТаблицаЗначений - дополнительные свойства абонента:
//     ** Ключ - Строка - имя дополнительного свойства
//     ** Тип - Строка - тип значения
//     ** Значение - Строка, Число, Дата, Булево - значение дополнительного свойства
//
Функция ДополнительныеСведенияАбонента(ВызыватьИсключениеПриОшибке = Истина, 
	ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/attached_info";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;

	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодАбонента());
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, 
		ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
		
	ДанныеОтвета.Удалить("general");
	
	Переименования = Новый Соответствие;
	Переименования.Вставить("public_id", Служебный.ОписаниеКолонки(
		"ПубличныйИдентификатор", ОбщегоНазначения.ОписаниеТипаСтрока(36)));
	Переименования.Вставить("properties", "Свойства");
	Переименования.Вставить("fields", "Реквизиты");
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета, Переименования);
	
	Результат.Свойства = Служебный.МассивСтруктурВТаблицуЗначений(
		Результат.Свойства, Служебный.ПереименованияДополнительныхСведений());
	Результат.Реквизиты = Служебный.МассивСтруктурВТаблицуЗначений(
		Результат.Реквизиты, Служебный.ПереименованияДополнительныхСведений());
	
	Возврат Результат;
	
КонецФункции

// Обновляет дополнительные сведения (реквизиты и свойства) абонента.
// Реализует метод внешнего программного интерфейса - account/update_attached_info.
//
// Параметры:
//  ДопСведения - см. НовыйДопСведенияАбонента
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//  
// Возвращаемое значение:
//  Булево - установка доп. сведений: Истина - установлено, Ложь - произошла ошибка.
//
Функция ОбновитьДопСведенияАбонента(ДопСведения, 
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/update_attached_info";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;

	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", ДопСведения.КодАбонента);

	Если ДопСведения.Свойство("ПубличныйИдентификатор") Тогда
		ДанныеЗапроса.Вставить("public_id", ДопСведения.ПубличныйИдентификатор);
	КонецЕсли;
	Переименования = Служебный.ПереименованияДополнительныхСведений(Ложь);
	Если ДопСведения.Свойство("Реквизиты") Тогда
		ДанныеЗапроса.Вставить("fields", Служебный.ТаблицаЗначенийВМассивСтруктур(
			ДопСведения.Реквизиты, Переименования));
	КонецЕсли;
	Если ДопСведения.Свойство("Свойства") Тогда
		ДанныеЗапроса.Вставить("properties", Служебный.ТаблицаЗначенийВМассивСтруктур(
			ДопСведения.Свойства, Переименования));
	КонецЕсли;

	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);

	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Возвращает дополнительные сведения (реквизиты и свойства) указанного абонента,
// необходимые для оформления платной подписки.
// Реализует метод внешнего программного интерфейса - account/attached_info_for_subscribing
//
// Параметры:
//  КодАбонента - Число - код абонента, для которого нужно получить доп. сведения.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  Структура - дополнительные реквизиты и свойства абонента:
//   * ЕстьОшибки - Булево - признак наличия ошибок заполнения.
//   * Реквизиты - ТаблицаЗначений - дополнительные реквизиты абонента:
//     ** Ключ - Строка - имя дополнительного реквизита
//     ** Заголовок - Строка - заголовок дополнительного реквизита
//     ** Тип - Строка - тип значения 
//     ** Значение - Строка, Число, Дата, Булево - значение дополнительного реквизита
//     ** ЗаполнятьОбязательно - Булево - признак обязательности заполнения
//     ** Подсказка - Строка - подсказка заполнения
//     ** Ошибка - Булево - признак ошибки заполнения
//     ** Сообщение - Строка - сообщение об ошибке.
//   * Свойства - ТаблицаЗначений - дополнительные свойства абонента:
//     ** Ключ - Строка - имя дополнительного свойства
//     ** Заголовок - Строка - заголовок дополнительного реквизита
//     ** Тип - Строка - тип значения
//     ** Значение - Строка, Число, Дата, Булево - значение дополнительного свойства
//     ** ЗаполнятьОбязательно - Булево - признак признак обязательности заполнения
//     ** Подсказка - Строка - подсказка заполнения
//     ** Ошибка - Булево - признак ошибки заполнения
//     ** Сообщение - Строка - сообщение об ошибке.
//
Функция ОбязательныеСведенияДляОформленияПодписки(КодАбонента, 
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/attached_info_for_subscribing";

	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;

	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодАбонента);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияДополнительныхСведений();
	Переименования.Вставить("required", Служебный.ОписаниеКолонки(
		"ЗаполнятьОбязательно", Новый ОписаниеТипов("Булево")));
	Переименования.Вставить("tooltip", Служебный.ОписаниеКолонки(
		"Подсказка", Новый ОписаниеТипов("Строка")));
	Переименования.Вставить("error", Служебный.ОписаниеКолонки(
		"Ошибка", Новый ОписаниеТипов("Булево")));
	Переименования.Вставить("message", Служебный.ОписаниеКолонки(
		"Сообщение", Новый ОписаниеТипов("Строка")));
		
	Если ДанныеОтвета <> Неопределено Тогда
		Свойства = Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.properties, Переименования);
		Реквизиты = Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.fields, Переименования);
	Иначе
		Свойства = Служебный.НовыйДополнительныеСведения();
		Реквизиты = Служебный.НовыйДополнительныеСведения();
	КонецЕсли;

	Возврат Новый Структура("ЕстьОшибки, Свойства, Реквизиты", ДанныеОтвета.errors, Свойства, Реквизиты);

КонецФункции

// Возвращает значения дополнительного сведения.
// 
// Параметры:
//  ИмяСведения - Строка - имя сведения, для которого нужно получить значения.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  ТаблицаЗначений - значения дополнительного сведения:
//   * Наименование - Строка - длина 100
//   * Вес - Число - точность 10,2  
Функция ЗначенияДополнительногоСведения(ИмяСведения, 
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "attached_info_values";

	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;

	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("key", ИмяСведения);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод, , Ложь);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);

	Переименования = Новый Соответствие;
	Переименования.Вставить("name", Служебный.ОписаниеКолонки(
		"Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(100)));
	Переименования.Вставить("weight", Служебный.ОписаниеКолонки(
		"Вес", ОбщегоНазначения.ОписаниеТипаЧисло(10, 2)));
	
	Если ДанныеОтвета <> Неопределено Тогда
		Значения = Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.values, Переименования);
	Иначе 
		Значения = Новый ТаблицаЗначений;
	КонецЕсли;
	
	Возврат Значения;

КонецФункции

#КонецОбласти

#Область Account_Users

// Возвращает список пользователей абонента этого приложения.
// Реализует метод внешнего программного интерфейса - account/users/list.
// 
// Параметры:
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//  
//  Возвращаемое значение:
//  ТаблицаЗначений - пользователи абонента:
//   * Логин - Строка - логин (имя) пользователя
//   * ПолноеИмя - Строка - полное имя пользователя
//   * Почта - Строка - электронная почта пользователя
//   * РольПользователя - ПеречислениеСсылка.РолиПользователейАбонентов - роль пользователя
//   * РазрешенноеКоличествоСеансов - Число - разрешенное количество сеансов
//   * ВременныйДоступ - Булево - временный доступ
//
Функция ПользователиАбонента(ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/users/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодАбонента());
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	
	Переименования = Служебный.ПереименованияПользователиАбонента();
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.user, Переименования);
	
КонецФункции

// Возвращает свойства пользователя абонента по логину.
// Реализует метод внешнего программного интерфейса - account/users/info.
// Параметры:
//  Логин - Строка - Логин (Имя) пользователя.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Структура - свойства пользователя абонента:
//   * Логин - Строка - логин (имя) пользователя
//   * ПолноеИмя - Строка - полное имя пользователя
//   * Почта - Строка - электронная почта пользователя
//   * Телефон - Строка - телефон пользователя
//   * РольПользователя - ПеречислениеСсылка.РолиПользователейАбонентов - роли пользователя
//   * РазрешенноеКоличествоСеансов - Число - разрешенное количество сеансов
//   * ВременныйДоступ - Булево - временный доступ
//   * ДополнительныеРеквизиты - ТаблицаЗначений - дополнительные реквизиты пользователя абонента:
//     ** Ключ - Строка - имя дополнительного реквизита
//     ** Заголовок - Строка - заголовок дополнительного реквизита
//     ** Тип - Строка - тип значения 
//     ** Значение - Строка, Число, Дата, Булево - значение дополнительного реквизита
Функция СвойстваПользователяАбонента(Логин,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/users/info";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодАбонента());
	ДанныеЗапроса.Вставить("login", Логин);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияПользователиАбонента();
	
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета.user, Переименования);
	Если Результат.Свойство("fields") Тогда
		Переименования = Служебный.ПереименованияДополнительныхСведений();
		ДополнительныеРеквизиты = Служебный.МассивСтруктурВТаблицуЗначений(Результат.fields, Переименования);
		Служебный.ОбработатьДатуВДополнительныхРеквизитах(ДополнительныеРеквизиты);
		Результат.Удалить("fields");
		Результат.Вставить("ДополнительныеРеквизиты", ДополнительныеРеквизиты);
	Иначе
		Результат.Вставить("ДополнительныеРеквизиты", Служебный.НовыйДополнительныеСведения());
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Создает новую учетную запись пользователя сервиса и подключает созданного пользователя к абоненту этого приложения. 
//
// Параметры:
//  ПараметрыСоздания - см. НовыйПараметрыСозданияПользователя
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - признак добавления пользователя Истина - пользователь добавлен, Ложь - произошла ошибка.
//
Функция СоздатьПользователяАбонента(ПараметрыСоздания,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/users/create";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодАбонента());
	ДанныеЗапроса.Вставить("role", Перечисления.РолиПользователейАбонентов.ИмяПоЗначению(ПараметрыСоздания.РольПользователя));
	ДанныеЗапроса.Вставить("login", ПараметрыСоздания.Логин);
	ДанныеЗапроса.Вставить("password", ПараметрыСоздания.Пароль);
	ДанныеЗапроса.Вставить("email", ПараметрыСоздания.Почта);
	ДанныеЗапроса.Вставить("email_required", ПараметрыСоздания.ПочтаОбязательна);
	ДанныеЗапроса.Вставить("name", ПараметрыСоздания.ПолноеИмя);
	ДанныеЗапроса.Вставить("phone", ПараметрыСоздания.Телефон);
	ДанныеЗапроса.Вставить("timezone", ПараметрыСоздания.ЧасовойПояс);
	ДанныеЗапроса.Вставить("description", ПараметрыСоздания.Описание);
	ДанныеЗапроса.Вставить("invite", ПараметрыСоздания.ТолькоПригласить);
	Если ПараметрыСоздания.Свойство("ДополнительныеРеквизиты") 
	   И ПараметрыСоздания.ДополнительныеРеквизиты.Количество() > 0 Тогда
		ДополнительныеРеквизиты = Новый Массив; // Массив из Структура
		Для Каждого Строка Из ПараметрыСоздания.ДополнительныеРеквизиты Цикл
			ИмяТипЗначение = Новый Структура;
			ИмяТипЗначение.Вставить("key", Строка.Ключ);
			Если ЗначениеЗаполнено(Строка.Тип) Тогда
				ИмяТипЗначение.Вставить("type", Строка.Тип);
			КонецЕсли;
			ИмяТипЗначение.Вставить("value", Строка.Значение);
			ДополнительныеРеквизиты.Добавить(ИмяТипЗначение);
		КонецЦикла;
		ДанныеЗапроса.Вставить("fields", ДополнительныеРеквизиты);
	КонецЕсли;
		
	Если ПараметрыСоздания.УстановитьПраваПриложений Тогда
		
		ПраваНаПриложения = Новый Массив;
		Для каждого Строка Из ПараметрыСоздания.ПраваНаПриложения Цикл
			
			ПравоНаПриложение = Новый Структура;
			ПравоНаПриложение.Вставить("id", Строка.КодПриложения);
			ПравоНаПриложение.Вставить("role", Перечисления.ПраваПользователяПриложения.ИмяПоЗначению(Строка.Право));
			
			ПраваНаПриложения.Добавить(ПравоНаПриложение);
			
		КонецЦикла;
		
		ДанныеЗапроса.Вставить("tenants", ПраваНаПриложения);
		
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);

	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Добавляет существующего пользователя сервиса к абоненту этого приложения. 
//
// Параметры:
//  ПараметрыДобавления - см. НовыйПараметрыДобавленияПользователя
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - признак создания учетной записи Истина - создана, Ложь - произошла ошибка.
//
Функция ДобавитьПользователяАбонента(ПараметрыДобавления,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/users/add";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодАбонента());
	ДанныеЗапроса.Вставить("role", Перечисления.РолиПользователейАбонентов.ИмяПоЗначению(ПараметрыДобавления.РольПользователя));
	ДанныеЗапроса.Вставить("login", ПараметрыДобавления.Логин);
	ДанныеЗапроса.Вставить("description", ПараметрыДобавления.Описание);
	ДанныеЗапроса.Вставить("force", ПараметрыДобавления.Принудительно);
	Если ПараметрыДобавления.Свойство("ДополнительныеРеквизиты") 
	   И ПараметрыДобавления.ДополнительныеРеквизиты.Количество() > 0 Тогда
		ДополнительныеРеквизиты = Новый Массив; // Массив из Структура
		Для Каждого Строка Из ПараметрыДобавления.ДополнительныеРеквизиты Цикл
			ИмяТипЗначение = Новый Структура;
			ИмяТипЗначение.Вставить("key", Строка.Ключ);
			Если ЗначениеЗаполнено(Строка.Тип) Тогда
				ИмяТипЗначение.Вставить("type", Строка.Тип);
			КонецЕсли;
			ИмяТипЗначение.Вставить("value", Строка.Значение);
			ДополнительныеРеквизиты.Добавить(ИмяТипЗначение);
		КонецЦикла;
		ДанныеЗапроса.Вставить("fields", ДополнительныеРеквизиты);
	КонецЕсли;
		
	Если ПараметрыДобавления.УстановитьПраваПриложений Тогда
		
		ПраваНаПриложения = Новый Массив;
		Для каждого Строка Из ПараметрыДобавления.ПраваНаПриложения Цикл
			
			ПравоНаПриложение = Новый Структура;
			ПравоНаПриложение.Вставить("id", Строка.КодПриложения);
			ПравоНаПриложение.Вставить("role", Перечисления.ПраваПользователяПриложения.ИмяПоЗначению(Строка.Право));
			
			ПраваНаПриложения.Добавить(ПравоНаПриложение);
			
		КонецЦикла;
		
		ДанныеЗапроса.Вставить("tenants", ПраваНаПриложения);
		
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);

	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Обновляет свойства пользователя абонента.
// Реализует метод внешнего программного интерфейса - account/users/update.
//
// Параметры:
//  Параметры - см. НовыйПараметрыОбновленияПользователяАбонента
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - признак создания учетной записи Истина - создана, Ложь - произошла ошибка.
Функция ОбновитьПользователяАбонента(Параметры,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/users/update";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("login", Параметры.Логин);
	
	Если Параметры.ОбновитьРазрешенноеКоличествоСеансов Тогда
		ДанныеЗапроса.Вставить("session_restriction", Параметры.РазрешенноеКоличествоСеансов);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Роль) Тогда
		ДанныеЗапроса.Вставить("role", Перечисления.РолиПользователейАбонентов.ИмяПоЗначению(Параметры.Роль));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Приложения) Тогда
		ПриложенияПользователя = Новый Массив;
		Для каждого Строка Из Параметры.Приложения Цикл
			
			ПриложениеПользователя = Новый Структура;
			ПриложениеПользователя.Вставить("id", Строка.КодПриложения);
			ПриложениеПользователя.Вставить("role", Перечисления.ПраваПользователяПриложения.ИмяПоЗначению(Строка.Право));
			
			ПриложенияПользователя.Добавить(ПриложениеПользователя);
			
		КонецЦикла;
		
		ДанныеЗапроса.Вставить("tenants", ПриложенияПользователя);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ДополнительныеРеквизиты) Тогда
		
		ДополнительныеРеквизиты = Новый Массив; // Массив из Структура
		Для Каждого Строка Из Параметры.ДополнительныеРеквизиты Цикл
			ДополнительныйРеквизит = Новый Структура;
			ДополнительныйРеквизит.Вставить("key", Строка.Ключ);
			Если ЗначениеЗаполнено(Строка.Тип) Тогда
				ДополнительныйРеквизит.Вставить("type", Строка.Тип);
			КонецЕсли;
			ДополнительныйРеквизит.Вставить("value", Строка.Значение);
			ДополнительныеРеквизиты.Добавить(ДополнительныйРеквизит);
		КонецЦикла;
		
		ДанныеЗапроса.Вставить("fields", ДополнительныеРеквизиты);
		
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);
	
	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Отменяет пользователю сервиса с указанным логином доступ к абоненту этого приложения.
//
// Параметры:
//  ПараметрыУдаления - см. НовыйПараметрыУдаленияПользователя
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - результат отмены права доступа к абоненту: Истина - право отменено, Ложь - произошла ошибка.
//
Функция УдалитьПользователяАбонента(ПараметрыУдаления,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/users/delete";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодАбонента());
	ДанныеЗапроса.Вставить("login", ПараметрыУдаления.Логин);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Устанавливает указанному пользователю абонента этого приложения указанную роль.
// Реализует метод внешнего программного интерфейса - account/users/set_role.
// 
// Параметры:
//  Логин - Строка - логин (имя) пользователя. 
//  Роль - ПеречислениеСсылка.РолиПользователейАбонентов - устанавливаемая роль пользователя.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - установки роли Истина - установлено, Ложь - произошла ошибка.
//
Функция УстановитьРольПользователяАбонента(Логин, Роль,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
		
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/users/set_role";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодАбонента());
	ДанныеЗапроса.Вставить("login", Логин);
	ДанныеЗапроса.Вставить("role", Перечисления.РолиПользователейАбонентов.ИмяПоЗначению(Роль));
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);

	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
			
КонецФункции

// Устанавливает указанному пользователю абонента этого приложения ограничение на количество
// одновременных сеансов работы с приложениями.
// Реализует метод внешнего программного интерфейса - account/users/set_session_restriction.
// 
// Параметры:
//  ПараметрыУстановкиОграничения - см. НовыйПараметрыУстановкиОграниченияКоличестваСеансов 
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - результат установки ограничения: Истина - установлено, Ложь - произошла ошибка.
//
Функция УстановитьОграничениеКоличестваСеансовПользователяАбонента(ПараметрыУстановкиОграничения,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
		
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/users/set_session_restriction";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодАбонента());
	ДанныеЗапроса.Вставить("login", ПараметрыУстановкиОграничения.Логин);
	ДанныеЗапроса.Вставить("restriction", ПараметрыУстановкиОграничения.РазрешенноеКоличествоСеансов);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);

	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Обновляет дополнительные реквизиты пользователя абонента
// Реализует метод внешнего программного интерфейса - account/users/update_attached_info.
// 
// Параметры:
//  Логин - Строка - логин (имя) пользователя. 
//  ДопРеквизиты - ТаблицаЗначений - дополнительные реквизиты пользователя абонента:
//     * Ключ - Строка - имя дополнительного реквизита
//     * Тип - Строка - тип значения 
//     * Значение - Строка, Число, Дата, Булево - значение дополнительного реквизита
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - установка доп. реквизитов: Истина - установлено, Ложь - произошла ошибка.
Функция ОбновитьДопРеквизитыПользователяАбонента(Логин, ДопРеквизиты,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/users/update_attached_info";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодАбонента());
	ДанныеЗапроса.Вставить("login", Логин);
	ДанныеЗапроса.Вставить("fields", Новый Массив);
	
	Для Каждого Строка Из ДопРеквизиты Цикл
		Данные = Новый Структура;
		Данные.Вставить("key", Строка.Ключ);
		Данные.Вставить("value", Строка.Значение);
		Если ЗначениеЗаполнено(Строка.Тип) Тогда
			Данные.Вставить("type", Строка.Тип);
		КонецЕсли;
		ДанныеЗапроса.fields.Добавить(Данные);
	КонецЦикла;

	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);

	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

// Возвращает список приложений, к которым разрешен доступ пользователю абонента
//
// Параметры:
//  Параметры - см. НовыйПараметрыЗапросаПриложенийПользователяАбонента.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// 
// Возвращаемое значение:
//  ТаблицаЗначений - пользователи, которым разрешен доступ:
//   * Код - Число - код (номер) приложения
//   * Право - ПеречислениеСсылка.ПраваПользователяПриложения - право пользователя на приложение в менеджере сервиса.
//   * ТребуетсяНастройкаВПриложении - Булево - признак необходимости настройки прав пользователя в приложении.
//
Функция ПриложенияПользователяАбонента(Параметры,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/users/tenant/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("login", Параметры.Логин);
	
	ДобавитьАвторизациюАбонента(ДанныеЗапроса, Параметры.КодАбонента);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Переименования = Новый Соответствие;
	Переименования.Вставить("id", Служебный.ОписаниеКолонки("Код",
		ОбщегоНазначения.ОписаниеТипаЧисло(7, 0, ДопустимыйЗнак.Неотрицательный)));
	Переименования.Вставить("role", Служебный.ОписаниеКолонки("Право", 
		Новый ОписаниеТипов("ПеречислениеСсылка.ПраваПользователяПриложения")));
	Переименования.Вставить("setup_required", Служебный.ОписаниеКолонки("ТребуетсяНастройкаВПриложении",
		Новый ОписаниеТипов("Булево")));
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.tenants, Переименования);
	
КонецФункции

// Возвращает шаблон параметров создания пользователя для метода ПрограммныйИнтерфейсСервиса.СоздатьПользователяАбонента.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров создания пользователя:
//	 * Логин - Строка - логин (имя пользователя)
//	 * Пароль - Строка - пароль пользователя
//   * ПочтаОбязательна - Булево - признак обязательной установки почты (по умолчанию = Истина)
//   * Почта - Строка - электронная почта
//   * РольПользователя - ПеречислениеСсылка.РолиПользователейАбонентов - роль (по умолчанию = ПользовательАбонента)
//   * ПолноеИмя - Строка - полное имя (наименование) пользователя
//   * Телефон - Строка - телефон пользователя
//   * ЧасовойПояс - Строка - рабочий часовой пояс пользователя
//   * Описание - Строка - описание пользователя
//   * ТолькоПригласить - Булево - если Истина, то пользователь буден приглашен присоединиться к абоненту
//   * ДополнительныеРеквизиты - ТаблицаЗначений - дополнительные реквизиты пользователя абонента:
//     ** Ключ - Строка - имя реквизита
//     ** Тип - Строка - тип реквизита. 
//              Простые: string, decimal, date, boolean
//              Ссылочные: user, subscriber, tariff, service_provider_tariff, tariff_period, 
//                         subscription, service, additional_value, additional_value_group.
//     ** Значение - Строка, Число, Дата, Булево - значение реквизита.
//   * УстановитьПраваПриложений - Булево - необходимость установки прав на приложения.
//   * ПраваНаПриложения - ТаблицаЗначений - устанавливаемые права на приложения:
//     ** КодПриложения - Число - код приложения (номер области)
//     ** Право - ПеречислениеСсылка.ПраваПользователяПриложения - устанавливаемая роль пользователя.
//
Функция НовыйПараметрыСозданияПользователя() Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Логин", "");
	Параметры.Вставить("Пароль", "");
	Параметры.Вставить("ПочтаОбязательна", Истина);
	Параметры.Вставить("Почта", "");
	Параметры.Вставить("РольПользователя", Перечисления.РолиПользователейАбонентов.ПользовательАбонента);
	Параметры.Вставить("ПолноеИмя", "");
	Параметры.Вставить("Телефон", "");
	Параметры.Вставить("ЧасовойПояс", "");
	Параметры.Вставить("Описание", "");
	Параметры.Вставить("ТолькоПригласить", Ложь);
	
	Параметры.Вставить("ДополнительныеРеквизиты", Служебный.НовыйДополнительныеСведения());
	
	Параметры.Вставить("УстановитьПраваПриложений", Ложь);
	ПраваНаПриложения = Новый ТаблицаЗначений;
	ПраваНаПриложения.Колонки.Добавить("КодПриложения", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	ПраваНаПриложения.Колонки.Добавить("Право", Новый ОписаниеТипов("ПеречислениеСсылка.ПраваПользователяПриложения"));
	Параметры.Вставить("ПраваНаПриложения", ПраваНаПриложения);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает шаблон параметров обновления пользователя для метода ПрограммныйИнтерфейсСервиса.ОбновитьПользователяАбонента.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров обновления пользователя:
//	 * Логин - Строка - логин (имя пользователя)
//	 * ОбновитьРазрешенноеКоличествоСеансов - Булево - признак обновления разрешенного количества сеансов
//	 * РазрешенноеКоличествоСеансов - Число - разрешенное количество сеансов
//	 * Роль - ПеречислениеСсылка.РолиПользователейАбонентов - роль пользователя
//	 * Приложения - ТаблицаЗначений - добавляемые приложения:
//	   ** КодПриложения - Число - код приложения (номер области)
//	   ** Право - ПеречислениеСсылка.ПраваПользователяПриложения - право пользователя на текущее приложение
//	 * ДополнительныеРеквизиты - ТаблицаЗначений - дополнительные реквизиты пользователя абонента:
//	   ** Ключ - Строка - имя дополнительного реквизита
//	   ** Тип - Строка - тип значения 
//	   ** Значение - Строка, Число, Дата, Булево - значение дополнительного реквизита
Функция НовыйПараметрыОбновленияПользователяАбонента() Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Логин", "");
	Параметры.Вставить("ОбновитьРазрешенноеКоличествоСеансов", Ложь);
	Параметры.Вставить("РазрешенноеКоличествоСеансов", 0);
	Параметры.Вставить("Роль", Перечисления.РолиПользователейАбонентов.ПустаяСсылка());
	
	ПриложенияПользователя = Новый ТаблицаЗначений;
	ПриложенияПользователя.Колонки.Добавить("КодПриложения", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	ПриложенияПользователя.Колонки.Добавить("Право", Новый ОписаниеТипов("ПеречислениеСсылка.ПраваПользователяПриложения"));
	Параметры.Вставить("Приложения", ПриложенияПользователя);
	
	Параметры.Вставить("ДополнительныеРеквизиты", Служебный.НовыйДополнительныеСведения());
	
	Возврат Параметры;
	
КонецФункции

// Возвращает шаблон параметров добавления существующего пользователя для метода ПрограммныйИнтерфейсСервиса.СоздатьПользователяАбонента.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров создания пользователя:
//	 * Логин - Строка - логин (имя пользователя)
//   * РольПользователя - ПеречислениеСсылка.РолиПользователейАбонентов - роль (по умолчанию = ПользовательАбонента)
//   * Описание - Строка - описание пользователя
//   * Принудительно - Булево - выполнить даже при наличии предупреждений
//   * ДополнительныеРеквизиты - ТаблицаЗначений - дополнительные реквизиты пользователя абонента:
//     ** Ключ - Строка - имя реквизита
//     ** Тип - Строка - тип реквизита. 
//              Простые: string, decimal, date, boolean
//              Ссылочные: user, subscriber, tariff, service_provider_tariff, tariff_period, 
//                         subscription, service, additional_value, additional_value_group.
//     ** Значение - Строка, Число, Дата, Булево - значение реквизита.
//   * УстановитьПраваПриложений - Булево - необходимость установки прав на приложения.
//   * ПраваНаПриложения - ТаблицаЗначений - устанавливаемые права на приложения:
//     ** КодПриложения - Число - код приложения (номер области)
//     ** Право - ПеречислениеСсылка.ПраваПользователяПриложения - устанавливаемая роль пользователя.
//
Функция НовыйПараметрыДобавленияПользователя() Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Логин", "");
	Параметры.Вставить("РольПользователя", Перечисления.РолиПользователейАбонентов.ПользовательАбонента);
	Параметры.Вставить("Описание", "");
	Параметры.Вставить("Принудительно", Ложь);
	
	Параметры.Вставить("ДополнительныеРеквизиты", Служебный.НовыйДополнительныеСведения());
	
	Параметры.Вставить("УстановитьПраваПриложений", Ложь);
	ПраваНаПриложения = Новый ТаблицаЗначений;
	ПраваНаПриложения.Колонки.Добавить("КодПриложения", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	ПраваНаПриложения.Колонки.Добавить("Право", Новый ОписаниеТипов("ПеречислениеСсылка.ПраваПользователяПриложения"));
	Параметры.Вставить("ПраваНаПриложения", ПраваНаПриложения);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает шаблон параметров для метода
// ПрограммныйИнтерфейсСервиса.УдалитьПользователяАбонента.
// 
// Возвращаемое значение:
//  Структура:
//   * Логин - Строка - логин (имя пользователя)
Функция НовыйПараметрыУдаленияПользователя() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Логин", "");
	
	Возврат Параметры;
	
КонецФункции

// Возвращает шаблон параметров для метода 
// ПрограммныйИнтерфейсСервиса.УстановитьОграничениеКоличестваСеансовПользователяАбонента.
// 
// Возвращаемое значение:
//  Структура:
//   * Логин - Строка - логин (имя пользователя)
//   * РазрешенноеКоличествоСеансов - Число - устанавливаемое значение.
Функция НовыйПараметрыУстановкиОграниченияКоличестваСеансов() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Логин", "");
	Параметры.Вставить("РазрешенноеКоличествоСеансов", 0);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает доступные дополнительные реквизиты для пользователя абонента.
// Реализует метод внешнего программного интерфейса - account/users/available_attached_info.
// Параметры:
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  ТаблицаЗначений - дополнительные реквизиты пользователя абонента:
//   * Ключ - Строка - имя дополнительного реквизита
//   * Заголовок - Строка - заголовок дополнительного реквизита
//   * Тип - Строка - тип значения 
Функция ДоступныеДополнительныеРеквизитыПользователяАбонента(
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/users/available_attached_info";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке,
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияДополнительныхСведений();
	ДополнительныеРеквизиты = Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.fields, Переименования);
	
	Возврат ДополнительныеРеквизиты;
	
КонецФункции

// Возвращает шаблон параметров для метода
// ПрограммныйИнтерфейсСервиса.ПриложенияПользователяАбонента.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров добавления пользователя в приложение:
//   * КодАбонента - Число - код абонента
//   * Логин - Строка - логин пользователя
//
Функция НовыйПараметрыЗапросаПриложенийПользователяАбонента() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("КодАбонента", 0);
	Параметры.Вставить("Логин", "");
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти 

#Область Account_Servants

// Возвращает список обслуживающих организаций абонента этого приложения.
// Реализует метод внешнего программного интерфейса - account/servants/list
//
// Возвращаемое значение:
//  ТаблицаЗначений - обслуживающие организации абонента:
//   * Код - Число - код (номер) обслуживающей организации
//   * Наименование - Строка - наименование обслуживающей организации
//   * Идентификатор - Строка - идентификатор абонента
//   * Город - Строка - город
//   * Сайт - Строка - сайт
//   * Почта - Строка - электронная почта
//   * Телефон - Строка - телефон
//   * РазрешеноПодписыватьНаТарифы - Булево - разрешено подписывать на тарифы
//   * РазрешеноАвтоматическоеВыставлениеСчетов - Булево - разрешено автоматическое выставление счетов
//   * РазрешеноПереопределениеТарифов - Булево - разрешено переопределение тарифов
//   * ТолькоСтраницаВыбораТарифа - Булево - отображать только страницу выбора тарифа в форме оплаты
//
Функция ОбслуживающиеОрганизацииАбонента() Экспорт

	Возврат ПрограммныйИнтерфейсСервисаПовтИсп.ОбслуживающиеОрганизацииАбонента();

КонецФункции

// Возвращает HTML-страницу выбора тарифа обслуживающей организации абонента этого приложения.
// Реализует метод внешнего программного интерфейса -  account/servants/tariff_selection_page
// Параметры:
//  КодОО - Число - код обслуживающей организации.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Строка - HTML-страница выбора тарифа обслуживающей организации.
//
Функция СтраницаВыбораТарифаОбслуживающейОрганизации(КодОО,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/servants/tariff_selection_page";

	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;

	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("servant", КодОО);

	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(
		Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета <> Неопределено Тогда
		Возврат ДанныеОтвета.html;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

// Возвращает список рекомендуемых тарифов, определенных обслуживающей организацией.
// Реализует метод внешнего программного интерфейса - account/servants/recommended_tariffs
//
// Параметры:
//  КодОО - Число - код обслуживающей организации.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  ТаблицаЗначений - список тарифов:
//   * КодТарифаПровайдера - Строка - код тарифа провайдера
//   * КодТарифаОбслуживающейОрганизации - Строка - код тарифа обслуживающей организации.
Функция РекомендуемыеТарифы(КодОО,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/servants/recommended_tariffs";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("servant", КодОО);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияРекомендуемыеТарифы();
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.recommended_tariffs, Переименования);

КонецФункции

// Возвращает список тарифов обслуживающей организации.
// Реализует метод внешнего программного интерфейса - account/servant_tariffs/list
//
// Параметры:
//  Отбор - см. НовыйОтборСпискаТарифовОбслуживающейОрганизации
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  ТаблицаЗначений - список тарифов:
//   * Код - Строка - код тарифа
//   * Наименование - Строка - наименование тарифа
//   * КодТарифаПровайдера - Строка - код базового тарифа
//   * КраткоеОписание - Строка - краткое описание тарифа
//   * ПериодыДействия - ТаблицаЗначений - периоды действия. Заполняется если передан параметр получения ПараметрПолученияПериодыДействия:
//     ** Код - Строка - код периода действия
//     ** Наименование - Строка - наименование периода действия
//     ** Сумма - Число - стоимость тарифа с этим периодом действия
//     ** Рекомендуемый - Булево - признак, что период действия является рекомендуемым 
//     ** Комментарий - Строка - комментарий к периоду действия.
//
Функция ТарифыОбслуживающейОрганизации(Отбор, 
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/servant_tariffs/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("servant", Отбор.КодОбслуживающейОрганизации);
	ВерсияИнтерфейса = СвойстваВерсииИнтерфейса().Версия;
	
	Если ВерсияИнтерфейса >= 19 Тогда
		ДанныеЗапроса.Вставить("scope", Отбор.ПараметрыПолучения);
	КонецЕсли;
	
	Если ВерсияИнтерфейса >= 23 И Отбор.ДоступныеТарифы.Количество() > 0 Тогда
		ДанныеЗапроса.Вставить("available_tariffs", Отбор.ДоступныеТарифы);
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	ПереименованияПериодыДействия = Служебный.ПереименованияТарифОбслуживающейОрганизацииПериодыДействия();
	ПолеПериодыДействия = "validity_periods";
	ПолеТариф = "servant_tariff";
	
	Если ВерсияИнтерфейса >= 19	И Не Отбор.ПараметрыПолучения.Найти(ПараметрПолученияПериодыДействия()) = Неопределено Тогда
		Для Каждого Элемент Из ДанныеОтвета[ПолеТариф] Цикл
			Элемент[ПолеПериодыДействия] = Служебный.МассивСтруктурВТаблицуЗначений(Элемент[ПолеПериодыДействия], ПереименованияПериодыДействия);
		КонецЦикла;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияТарифОбслуживающейОрганизации(Метод);
	Результат = Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета[ПолеТариф], Переименования);

	Возврат Результат;

КонецФункции

// Возвращает информацию о тарифе обслуживающей организаций.
//
// Параметры:
//  КодОО - Число - код обслуживающей организации.
//  КодТарифа - Строка - код тарифа обслуживающей организации. 
// 
// Возвращаемое значение:
//  Структура - информация о тарифе:
//   * Код - Строка - код тарифа
//   * Наименование - Строка - наименование тарифа
//   * КодТарифаПровайдера - Строка - код тарифа провайдера
//   * КраткоеОписание - Строка - краткое описание тарифа
//   * ОписаниеДляАбонентов - ФорматированныйДокумент - описание тарифа для абонентов.
//   * ПериодыДействия - ТаблицаЗначений - периоды действия тарифа:
//     ** Код - Строка - код периода действия
//     ** Наименование - Строка - наименование периода действия
//     ** Сумма - Число - стоимость
//     ** Рекомендуемый - Булево - признак того, что период является рекомендуемым к покупке
//     ** Комментарий - Строка - комментарий к периоду действия
//
Функция ТарифОбслуживающейОрганизации(КодОО, КодТарифа) Экспорт
	
	Возврат ПрограммныйИнтерфейсСервисаПовтИсп.ТарифОбслуживающейОрганизации(КодОО, КодТарифа);
	
КонецФункции

#КонецОбласти 

#Область Backup

// Возвращает список резервных копий абонента
//
// Параметры:
//  Параметры - см. НовыйПараметрыПолученияСпискаРезервныхКопий
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
Функция СписокРезервныхКопийАбонента(Параметры = Неопределено,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "backup/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	
	Если ЗначениеЗаполнено(Параметры.НачалоПериода) Тогда
		ДанныеЗапроса.Вставить("start_date", Параметры.НачалоПериода);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.КонецПериода) Тогда
		ДанныеЗапроса.Вставить("end_date", Параметры.КонецПериода);
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияРезервныеКопии();
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.backup, Переименования);
	
КонецФункции

// Возвращает свойства резервной копии по идентификатору.
// Реализует метод внешнего программного интерфейса - backup/info.
// 
// Параметры:
//  ИдентификаторКопии - Строка - идентификатор резервной копии
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Структура - свойства резервной копии:
//   * ИдентификаторРК - УникальныйИдентификатор
//   * НомерОбласти - Число
//   * НаименованиеОбласти - Строка
//   * Размер - Число
//   * ВерсияКонфигурации - Строка
//   * ИмяВидаПриложения - Строка
//   * Исходная - Булево
//   * ДатаСозданияUTC - Дата
//   * ПоТребованию - Булево
//   * Ежегодная - Булево
//   * Ежемесячная - Булево
//   * Ежедневная - Булево
//   * Комментарий - Строка
//   * ЧасовойПояс - Строка
//   * ИмяФайла - Строка
Функция СвойстваРезервнойКопии(ИдентификаторКопии,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "backup/info";

	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;

	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", ИдентификаторКопии);

	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияРезервныеКопии();
	
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета.backup, Переименования);
	
	Возврат Результат;
	
КонецФункции

// Возвращает шаблон параметров создания приложения для метода ПрограммныйИнтерфейсСервиса.СписокРезервныхКопийАбонента.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров создания приложения:
//   * НачалоПериода - Дата - дата начала периода.
//   * КонецПериода - Дата - дата конца периода.
Функция НовыйПараметрыПолученияСпискаРезервныхКопий() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("НачалоПериода", '00010101');
	Параметры.Вставить("КонецПериода", '00010101');
	
	Возврат Параметры;
	
КонецФункции

// Возвращает данные для скачивания резервной копий
//
// Параметры:
//  Параметры - см. НовыйПараметрыПолученияФайлаРезервнойКопии
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  Структура - данные скачивания резервной копии:
//   * АдресФайла - Строка - URL файла резервной копии.
//   * ИмяФайла - Строка - рекомендуемое имя файла для сохранения.
//   * ИдентификаторЗадания - УникальныйИдентификатор - идентификатор задания подготовки резервной копии.
//   * Токен - Строка
//   * СрокДействияТокена - Дата
Функция ДанныеДляПолученияФайлаРезервнойКопии(Параметры = Неопределено,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "backup/file_token/download";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	
	ДанныеЗапроса.Вставить("id", Параметры.ИдентификаторРК);
	
	Если ЗначениеЗаполнено(Параметры.ИдентификаторЗадания) Тогда
		ДанныеЗапроса.Вставить("task_id", Параметры.ИдентификаторЗадания);
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Новый Соответствие;
	Переименования.Вставить("url", Служебный.ОписаниеКолонки(
		"АдресФайла", ОбщегоНазначения.ОписаниеТипаСтрока(0)));
	Переименования.Вставить("file_name", Служебный.ОписаниеКолонки(
		"ИмяФайла", ОбщегоНазначения.ОписаниеТипаСтрока(0)));
	Переименования.Вставить("task_id", Служебный.ОписаниеКолонки(
		"ИдентификаторЗадания", ОбщегоНазначения.ОписаниеТипаСтрока(36)));
	Переименования.Вставить("token", Служебный.ОписаниеКолонки(
		"Токен", ОбщегоНазначения.ОписаниеТипаСтрока(0)));
	Переименования.Вставить("expires_at", Служебный.ОписаниеКолонки(
		"СрокДействияТокена", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя)));
	
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета, Переименования);
	
	Возврат Результат;
	
КонецФункции

// Возвращает шаблон параметров получения файла резервной копии для метода ПрограммныйИнтерфейсСервиса.ДанныеДляПолученияФайлаРезервнойКопии.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров получения файла резервной копии:
//   * ИдентификаторРК - Строка - идентификатор резервной копии.
//   * ИдентификаторЗадания - Строка - идентификатор задания подготовки резервной копии.
Функция НовыйПараметрыПолученияФайлаРезервнойКопии() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИдентификаторРК", "");
	Параметры.Вставить("ИдентификаторЗадания", "");
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область Customers

// Возвращает список обслуживаемых абонентов.
// Реализует метод внешнего программного интерфейса - account/customers/list
//
// Параметры:
//  КодОО - Число - код (номер) ведущего абонента (если не указан, используется абонент текущего приложения).
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  ТаблицаЗначений:
//   * Наименование - Строка - наименование абонента (длина - 64)
//   * Код - Строка - код (номер) обслуживаемого абонента (длина - 12).
Функция ОбслуживаемыеАбоненты(КодОО = Неопределено, 
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/customers/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Если КодОО = Неопределено Тогда
		КодОО = КодАбонента();
	КонецЕсли; 
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодОО);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.customer, Служебный.ПереименованияАбонент());

КонецФункции

// Возвращает свойства обслуживаемого абонента.
// Реализует метод внешнего программного интерфейса - account/customers/info
//
// Параметры:
//  КодАбонента - Число - код (номер) обслуживаемого абонента.
//  КодОО - Число - код (номер) ведущего абонента (если не указан, используется абонент текущего приложения).
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  Структура:
//   * Наименование - Строка - наименование абонента (длина - 64)
//   * Код - Строка - код (номер) обслуживаемого абонента (длина - 12)
//   * Сайт - Строка - сайт из КИ (длина - 500)
//   * Город - Строка - город из КИ (длина - 500)
//   * Почта - Строка - почта из КИ (длина - 500)
//   * Телефон - Строка - телефон из КИ (длина - 500)
//
Функция СвойстваОбслуживаемогоАбонента(КодАбонента, КодОО = Неопределено,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/customers/info";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Если КодОО = Неопределено Тогда
		КодОО = КодАбонента();
	КонецЕсли; 
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодОО);
	ДанныеЗапроса.Вставить("account", КодАбонента);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Служебный.ПереименоватьСвойства(ДанныеОтвета.customer, Служебный.ПереименованияАбонент(Истина));
	
КонецФункции

// Возвращает дополнительные сведения (реквизиты и свойства) обслуживаемого абонента.
// Реализует метод внешнего программного интерфейса - account/customers/attached_info
//
// Параметры:
//  КодАбонента - Число - код (номер) обслуживаемого абонента.
//  КодОО - Число - код (номер) ведущего абонента (если не указан, используется абонент текущего приложения).
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//  
// Возвращаемое значение:
//  Структура - дополнительные реквизиты и свойства абонента:
//   * ПубличныйИдентификатор - Строка - публичный идентификатор абонента
//   * Реквизиты - ТаблицаЗначений - дополнительные реквизиты абонента:
//     ** Ключ - Строка - имя дополнительного реквизита
//     ** Тип - Строка - тип значения 
//     ** Значение - Строка, Число, Дата, Булево - значение дополнительного реквизита
//   * Свойства - ТаблицаЗначений - дополнительные свойства абонента:
//     ** Ключ - Строка - имя дополнительного свойства
//     ** Тип - Строка - тип значения
//     ** Значение - Строка, Число, Дата, Булево - значение дополнительного свойства
Функция ДопСведенияОбслуживаемогоАбонента(КодАбонента, КодОО = Неопределено,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/customers/attached_info";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Если КодОО = Неопределено Тогда
		КодОО = КодАбонента();
	КонецЕсли; 
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодОО);
	ДанныеЗапроса.Вставить("account", КодАбонента);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
		
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеОтвета.Удалить("general");
	
	Переименования = Новый Соответствие;
	Переименования.Вставить("public_id", Служебный.ОписаниеКолонки(
		"ПубличныйИдентификатор", ОбщегоНазначения.ОписаниеТипаСтрока(36)));
	Переименования.Вставить("properties", "Свойства");
	Переименования.Вставить("fields", "Реквизиты");
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета, Переименования);
	
	Результат.Свойства = Служебный.МассивСтруктурВТаблицуЗначений(
		Результат.Свойства, Служебный.ПереименованияДополнительныхСведений());
	Результат.Реквизиты = Служебный.МассивСтруктурВТаблицуЗначений(
		Результат.Реквизиты, Служебный.ПереименованияДополнительныхСведений());
	
	Возврат Результат;
	
КонецФункции

// Обновляет дополнительные сведения (реквизиты и свойства) обслуживаемого абонента.
// Реализует метод внешнего программного интерфейса - account/customers/update_attached_info.
//
// Параметры:
//  ДопСведения - см. НовыйДопСведенияАбонента
//  КодОО - Число - код (номер) ведущего абонента (если не указан, используется абонент текущего приложения).
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//  
// Возвращаемое значение:
//  Булево - установка доп. сведений: Истина - установлено, Ложь - произошла ошибка.
//
Функция ОбновитьДопСведенияОбслуживаемогоАбонента(ДопСведения, КодОО = Неопределено,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/customers/update_attached_info";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Если КодОО = Неопределено Тогда
		КодОО = КодАбонента();
	КонецЕсли; 
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодОО);
	ДанныеЗапроса.Вставить("account", ДопСведения.КодАбонента);
	ДанныеЗапроса.Вставить("public_id", ДопСведения.ПубличныйИдентификатор);
	
	Переименования = Служебный.ПереименованияДополнительныхСведений(Ложь);
	Если ДопСведения.Свойство("Реквизиты") Тогда
		ДанныеЗапроса.Вставить("fields", Служебный.ТаблицаЗначенийВМассивСтруктур(
			ДопСведения.Реквизиты, Переименования));
	КонецЕсли;
	Если ДопСведения.Свойство("Свойства") Тогда
		ДанныеЗапроса.Вставить("properties", Служебный.ТаблицаЗначенийВМассивСтруктур(
			ДопСведения.Свойства, Переименования));
	КонецЕсли;

	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);

	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Возвращает шаблон дополнительных сведений абонента.
// 
// Возвращаемое значение:
//  Структура:
//   * КодАбонента - Число - код абонента
//   * ПубличныйИдентификатор - Строка - публичный идентификатор абонента (длина - 36), может отсутствовать.
//   * Реквизиты - ТаблицаЗначений - дополнительные реквизиты абонента: 
//     ** Ключ - Строка - имя дополнительного реквизита
//     ** Тип - Строка - тип значения (не обязательно, если у доп. реквизита один тип значения)
//     ** Значение - Строка, Число, Дата, Булево - значение дополнительного реквизита
//   * Свойства - ТаблицаЗначений - дополнительные свойства абонента: 
//     ** Ключ - Строка - имя дополнительного свойства
//     ** Тип - Строка - тип значения (не обязательно, если у доп. свойства один тип значения)
//     ** Значение - Строка, Число, Дата, Булево - значение дополнительного свойства
//
Функция НовыйДопСведенияАбонента() Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	
	Результат = Новый Структура;
	Результат.Вставить("КодАбонента", 0);
	Результат.Вставить("ПубличныйИдентификатор", "");
	Результат.Вставить("Реквизиты", Служебный.НовыйДополнительныеСведения());
	Результат.Вставить("Свойства", Служебный.НовыйДополнительныеСведения());
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область CustomerSubscriptions

// Возвращает список подписок на тарифы обслуживаемых абонентов.
// Реализует метод внешнего программного интерфейса - account/customer_subscriptions/list
//
// Параметры:
//  Отбор - см. НовыйОтборПодписокНаТарифы
//  КодОО - Число - код (номер) ведущего абонента (если не указан, используется абонент текущего приложения).
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  ТаблицаЗначений - существующие подписки на тарифы:
//   * Номер - Строка - номер подписки
//   * Дата - Дата - дата регистрации подписки
//   * ДатаПодключения - Дата - дата подключения тарифа по подписке
//   * ДатаОтключения - Дата - дата отключения тарифа
//   * КодОбслуживаемогоАбонента - Число - код (номер) обслуживаемого абонента
//   * КодВедущегоАбонента - Число - код (номер) ведущего абонента
//   * КодТарифаОбслуживающейОрганизации - Строка - код тарифа обслуживающей организации
//   * КодТарифаПровайдера - Строка - код тарифа в подписке
//   * КодПериодаДействия - Строка - код периода действия
//   * Количество - Число - количество тарифов в подписке
//   * НомерОсновнойПодписки - Строка - номер основной подписки, если текущая подписка на тариф-расширение.
//   * ТипПодписки - ПеречислениеСсылка.ТипыПодписокСервиса - тип подписки
//   * НомерСчетаНаОплату - Строка - номер счета на оплату
//   * ИдентификаторСчетаНаОплату - УникальныйИдентификатор - идентификатор счета на оплату
//
Функция ПодпискиНаТарифы(Отбор, КодОО = Неопределено,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/customer_subscriptions/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Если КодОО = Неопределено Тогда
		КодОО = КодАбонента();
	КонецЕсли; 
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("servant", КодОО);
	
	Если ТипЗнч(Отбор) = Тип("Структура") Тогда
		ПоляОтбора = Новый Соответствие;
		ПоляОтбора.Вставить("КодОбслуживаемогоАбонента", "account");
		ПоляОтбора.Вставить("ТолькоАктивные", "active");
		ПоляОтбора.Вставить("ТолькоОсновные", "basic");
		ПоляОтбора.Вставить("НачалоПериода", "start_date");
		ПоляОтбора.Вставить("КонецПериода", "end_date");
		Для Каждого Элемент Из ПоляОтбора Цикл
			Если Отбор.Свойство(Элемент.Ключ) И ЗначениеЗаполнено(Отбор[Элемент.Ключ]) Тогда
				ДанныеЗапроса.Вставить(Элемент.Значение, Отбор[Элемент.Ключ]);
			КонецЕсли; 
		КонецЦикла;
	Иначе
		Отбор = НовыйОтборПодписокНаТарифы();
	КонецЕсли; 
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
		
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияПодпискаНаТариф(Истина);
	
	Результат = Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.subscription, Переименования);
	Результат.Сортировать("Дата");
	ВерсияИнтерфейса = СвойстваВерсииИнтерфейса().Версия;
	Для Индекс = 1 По Результат.Количество() Цикл
		Строка = Результат[Результат.Количество() - Индекс];
		Если Отбор.ТолькоОсновные И ВерсияИнтерфейса < 14 И Не ПустаяСтрока(Строка.НомерОсновнойПодписки) Тогда
			Результат.Удалить(Строка);
			Продолжить;
		КонецЕсли;
		Если Строка.Количество = 0 Тогда
			Строка.Количество = 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// Возвращает свойства подписки на тариф обслуживаемого абонента.
// Реализует метод внешнего программного интерфейса - account/customer_subscriptions/info
// 
// Параметры:
//  НомерПодписки - Строка - номер подписки.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//  
// Возвращаемое значение:
//  Структура - свойства подписки на тариф:
//   * Номер - Строка - номер подписки
//   * Дата - Дата - дата регистрации подписки
//   * ДатаПодключения - Дата - дата подключения тарифа по подписке
//   * ДатаОтключения - Дата - дата отключения тарифа
//   * КодОбслуживаемогоАбонента - Число - код (номер) обслуживаемого абонента
//   * КодВедущегоАбонента - Число - код (номер) ведущего абонента
//   * КодТарифаОбслуживающейОрганизации - Строка - код тарифа обслуживающей организации
//   * КодТарифаПровайдера - Строка - код тарифа в подписке
//   * КодПериодаДействия - Строка - код периода действия
//   * Количество - Число - количество тарифов в подписке
//   * НомерОсновнойПодписки - Строка - номер основной подписки, если текущая подписка на тариф-расширение.
//   * ТипПодписки - ПеречислениеСсылка.ТипыПодписокСервиса - тип подписки
//   * НомерСчетаНаОплату - Строка - номер счета на оплату
//   * ИдентификаторСчетаНаОплату - УникальныйИдентификатор - идентификатор счета на оплату
Функция СвойстваПодпискиНаТариф(НомерПодписки,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/customer_subscriptions/info";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", НомерПодписки);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке,
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияПодпискаНаТариф(Истина);
	
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета.subscription, Переименования);
	Если Результат.Количество = 0 Тогда
		Результат.Количество = 1;
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

// Возвращает свойства подписки на тариф абонента.
// Реализует метод внешнего программного интерфейса - subscription/info
// 
// Параметры:
//  НомерПодписки - Строка - номер подписки.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//  
// Возвращаемое значение:
//  Структура - свойства подписки на тариф:
//   * Номер - Строка - номер подписки
//   * Дата - Дата - дата регистрации подписки
//   * ДатаПодключения - Дата - дата подключения тарифа по подписке
//   * ДатаОтключения - Дата - дата отключения тарифа
//   * КодОбслуживаемогоАбонента - Число - код (номер) обслуживаемого абонента
//   * КодВедущегоАбонента - Число - код (номер) ведущего абонента
//   * КодТарифаОбслуживающейОрганизации - Строка - код тарифа обслуживающей организации
//   * КодТарифаПровайдера - Строка - код тарифа в подписке
//   * КодПериодаДействия - Строка - код периода действия
//   * Количество - Число - количество тарифов в подписке
//   * НомерОсновнойПодписки - Строка - номер основной подписки, если текущая подписка на тариф-расширение.
//   * ТипПодписки - ПеречислениеСсылка.ТипыПодписокСервиса - тип подписки
//   * НомерСчетаНаОплату - Строка - номер счета на оплату
//   * ИдентификаторСчетаНаОплату - УникальныйИдентификатор - идентификатор счета на оплату
Функция СвойстваПодпискиНаТарифАбонента(НомерПодписки,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "subscription/info";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", НомерПодписки);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке,
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Переименования = Служебный.ПереименованияПодпискаНаТариф(Истина);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета.subscription, Переименования);
	Если Результат.Количество = 0 Тогда
		Результат.Количество = 1;
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

// Создает новую подписку на основной тариф обслуживаемого абонента
// Реализует метод внешнего программного интерфейса - account/customer_subscriptions/create
//
// Параметры:
//  ДанныеПодписки - см. НовыйШаблонПодпискиНаОсновнойТариф
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  Неопределено, Структура - если оформление подписки успешно выполнено:
//   * Номер - Строка - номер созданной подписки.
//   * ДатаОтключения - Дата - дата отключения созданной подписки.
Функция СоздатьПодпискуНаОсновнойТариф(ДанныеПодписки,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/customer_subscriptions/create";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ИменаСвойств = Служебный.ИменаСвойствСозданияПодписки();
	Для Каждого Элемент Из ИменаСвойств Цикл
		Если ДанныеПодписки.Свойство(Элемент.Ключ) И ЗначениеЗаполнено(ДанныеПодписки[Элемент.Ключ]) Тогда
			ДанныеЗапроса.Вставить(Элемент.Значение, ДанныеПодписки[Элемент.Ключ]);
		КонецЕсли; 
	КонецЦикла; 
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
		
	Если ОсновныеСвойстваОтвета.КодОтвета >=10200 И ОсновныеСвойстваОтвета.КодОтвета <= 10299 Тогда
		Переименования = Служебный.ПереименованияОтветСозданияПодпискиНаТариф();
		Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета, Переименования);
		Результат.Удалить("general");
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Метод создает новую подписку или несколько подписок на расширение тарифа обслуживаемого абонента.
// Реализует метод внешнего программного интерфейса - account/customer_subscriptions/create_enhanced
//
// Параметры:
//  ДанныеРасширенияТарифа - см. НовыйШаблонПодпискиНаРасширениеТарифа
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  Неопределено, ТаблицаЗначений - если оформление подписки успешно выполнено:
//   * Номер - Строка - номер созданной подписки.
//   * ДатаОтключения - Дата - дата отключения созданной подписки.
Функция СоздатьПодпискуНаРасширениеТарифа(ДанныеРасширенияТарифа,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "account/customer_subscriptions/create_enhanced";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	
	ИменаСвойств = Служебный.ИменаСвойствСозданияПодписки(Истина);
	Для Каждого Элемент Из ИменаСвойств Цикл
		Если ДанныеРасширенияТарифа.Свойство(Элемент.Ключ) И ЗначениеЗаполнено(ДанныеРасширенияТарифа[Элемент.Ключ]) Тогда
			ДанныеЗапроса.Вставить(Элемент.Значение, ДанныеРасширенияТарифа[Элемент.Ключ]);
		КонецЕсли; 
	КонецЦикла; 
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
		
	Если ОсновныеСвойстваОтвета.КодОтвета >=10200 И ОсновныеСвойстваОтвета.КодОтвета <= 10299 Тогда
		Переименования = Служебный.ПереименованияОтветСозданияПодпискиНаТариф();
		Результат = Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.subscription_info, Переименования);
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Возвращает шаблон новой подписки на основной тариф обслуживаемого абонента.
//
// Возвращаемое значение:
//  Структура:
//   * КодВедущегоАбонента - Число - код (номер) ведущего абонента
//   * КодОбслуживаемогоАбонента - Число - код (номер) обслуживаемого абонента
//   * КодТарифаОбслуживающейОрганизации - Строка - код тарифа обслуживающей организации
//   * КодТарифаПровайдера - Строка - код тарифа в подписке
//   * КодПериодаДействия - Строка - код периода действия подписки
//   * ДатаПодключения - Дата - дата подключения тарифа по подписке
//   * ДатаОтключения - Дата - дата отключения тарифа
Функция НовыйШаблонПодпискиНаОсновнойТариф() Экспорт

	Шаблон = Новый Структура;
	Шаблон.Вставить("КодВедущегоАбонента", 0);
	Шаблон.Вставить("КодОбслуживаемогоАбонента", 0);
	Шаблон.Вставить("КодТарифаОбслуживающейОрганизации", "");
	Шаблон.Вставить("КодТарифаПровайдера", "");
	Шаблон.Вставить("КодПериодаДействия", "");
	Шаблон.Вставить("ДатаПодключения", '00010101');
	Шаблон.Вставить("ДатаОтключения", '00010101');
	
	Возврат Шаблон;
	
КонецФункции

// Возвращает шаблон новой подписки на расширение тарифа обслуживаемого абонента.
//
// Возвращаемое значение:
//  Структура:
//   * КодВедущегоАбонента - Число - код (номер) ведущего абонента
//   * КодОбслуживаемогоАбонента - Число - код (номер) обслуживаемого абонента
//   * КодТарифаОбслуживающейОрганизации - Строка - код тарифа обслуживающей организации
//   * КодТарифаПровайдера - Строка - код тарифа в подписке
//   * КодПериодаДействия - Строка - код периода действия подписки
//   * ДатаПодключения - Дата - дата подключения тарифа по подписке
//   * ДатаОтключения - Дата - дата отключения тарифа
//   * НомерОсновнойПодписки - Строка - номер подписки на основной тариф
//   * Количество - Число - количество тарифов в подписке.
Функция НовыйШаблонПодпискиНаРасширениеТарифа() Экспорт
	
	Шаблон = НовыйШаблонПодпискиНаОсновнойТариф();
	Шаблон.Вставить("НомерОсновнойПодписки", "");
	Шаблон.Вставить("Количество", 0);
	
	Возврат Шаблон;
	
КонецФункции

#КонецОбласти

#Область AccountingSystem

// Возвращает список учетных систем, доступных абоненту.
// Реализует метод внешнего программного интерфейса - accounting_system/list
//
// Параметры:
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  ТаблицаЗначений - список учетных систем абонента:
//   * Код - Число - код учетной системы
//   * Наименование - Строка - наименование учетной системы
//   * КодВладельца - Число - код абонента-владельца учетной системы
//   * ЗагружатьДанные - Булево - признак загрузки данных
//   * ЛогинПользовательДляЗагрузки - Строка - логин пользователя для загрузки данных
//   * ВыгружатьДанные - Булево - признак выгрузки данных
//   * АдресДляВыгрузки - Строка - URL-адрес учетной системы для выгрузки данных
//   * ЛогинПользователяДляВыгрузки - Строка - логин пользователя для выгрузки данных
//
Функция СписокУчетныхСистем(ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Метод = "accounting_system/list";
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);
		
	Переименования = Служебный.ПереименованияУчетнаяСистема(Метод);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.accounting_system, Переименования);
	
КонецФункции

// Возвращает свойства учетной системы абонента по коду учетной системы.
// Реализует метод внешнего программного интерфейса - accounting_system/info
//
// Параметры:
//  Код - Число - код учетной системы абонента.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Структура - свойства учетной системы:
//   * Код - Число - код учетной системы абонента
//   * Наименование - Строка - наименование учетной системы
//   * Описание - Строка - описание учетной системы
//   * КодВладельца - Число - код абонента-владельца учетной системы
//   * ЗагружатьДанные - Булево - признак загрузки данных
//   * ЛогинПользователяДляЗагрузки - Строка - логин пользователя для загрузки данных в менеджер сервиса
//   * ПравилаЗагрузки - ТаблицаЗначений - правила загрузки данных:
//     ** КодПравила - Строка - код правила загрузки данных
//     ** Адрес - Строка - адрес объекта для загрузки данных
//   * ВыгружатьДанные - Булево - признак выгрузки данных
//   * АдресДляВыгрузки - Строка - URL-адрес учетной системы для выгрузки данных в учетную систему
//   * ЛогинПользователяДляВыгрузки - Строка - логин пользователя для выгрузки данных 
//   * ПравилаВыгрузки - ТаблицаЗначений - правила выгрузки данных:
//     ** КодПравила - Строка - код правила выгрузки данных
//     ** ИдентификаторСтроки - УникальныйИдентификатор - идентификатор строки правила выгрузки 
//     ** КодУсловия - Строка - код условия использования правила выгрузки
//     ** Адрес - Строка - адрес объекта для выгрузки данных
//     ** БыстраяОтправка - Булево - признак быстрой отправки
//     ** ВыгрузкаПоРасписанию - Булево - признак выгрузки по расписанию
//     ** ОтборПоПоставщику - Булево - признак отбора по поставщику
//   * ПравилаОбработкиОтветов - ТаблицаЗначений:
//     ** ИдентификаторСтрокиПравилаВыгрузки - УникальныйИдентификатор - идентификатор строки правила выгрузки
//     ** КодПравила - Строка - код правила обработки ответа
//     ** КодыОтветов - Массив Из Число - коды ответов для вызова правила обработки.
//   
Функция СвойстваУчетнойСистемы(Код, 
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Метод = "accounting_system/info";
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", Код);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);
		
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияУчетнаяСистема(Метод);
	
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета.accounting_system, Переименования);
	
	Результат.ПравилаВыгрузки = Служебный.МассивСтруктурВТаблицуЗначений(
		Результат.ПравилаВыгрузки, Служебный.ПереименованияУчетнаяСистемаПравилаВыгрузки());
		
	Результат.ПравилаЗагрузки = Служебный.МассивСтруктурВТаблицуЗначений(
		Результат.ПравилаЗагрузки, Служебный.ПереименованияУчетнаяСистемаПравилаЗагрузки());
		
	Результат.ПравилаОбработкиОтветов = Служебный.МассивСтруктурВТаблицуЗначений(
		Результат.ПравилаОбработкиОтветов, Служебный.ПереименованияУчетнаяСистемаПравилаОбработкиОтветов());
	
	Возврат Результат;
	
КонецФункции

// Создает новую или обновляет существующую учетную систему биллинга.
// Реализует метод внешнего программного интерфейса - accounting_system/create_update_billing
//
// Параметры:
//  ПараметрыСозданияОбновления - см. НовыйПараметрыСозданияОбновленияУчетнойСистемыБиллинга
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  Структура - свойства учетной системы:
//   * Код - Число - код учетной системы абонента
//   * Наименование - Строка - наименование учетной системы
//
Функция СоздатьОбновитьУчетнуюСистемуБиллинга(ПараметрыСозданияОбновления, 
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Метод = "accounting_system/create_update_billing";
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	Если ЗначениеЗаполнено(ПараметрыСозданияОбновления.КодУчетнойСистемы) Тогда
		ДанныеЗапроса.Вставить("id", ПараметрыСозданияОбновления.КодУчетнойСистемы);
	КонецЕсли; 
	ДанныеЗапроса.Вставить("import_login", ПараметрыСозданияОбновления.ЛогинПользователяДляЗагрузки);
	ДанныеЗапроса.Вставить("export_url", ПараметрыСозданияОбновления.АдресДляВыгрузки);
	ДанныеЗапроса.Вставить("export_login", ПараметрыСозданияОбновления.ЛогинПользователяДляВыгрузки);
	ДанныеЗапроса.Вставить("export_password", ПараметрыСозданияОбновления.ПарольДляВыгрузки);

	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
		
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Новый Соответствие;
	Переименования.Вставить("id", Служебный.ОписаниеКолонки(
		"Код", ОбщегоНазначения.ОписаниеТипаЧисло(9, 0, ДопустимыйЗнак.Неотрицательный)));
	Переименования.Вставить("name", Служебный.ОписаниеКолонки(
		"Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(100)));
	
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета, Переименования);
	Результат.Удалить("general");
	
	Возврат Результат;

КонецФункции

// Возвращает шаблон параметров создания\обновления учетной системы биллинга.
//
// Возвращаемое значение:
//  Структура - шаблон параметров:
//   * ЛогинПользователяДляЗагрузки - Строка - логин пользователя для загрузки данных в менеджер сервиса
//   * ЛогинПользователяДляВыгрузки - Строка - логин пользователя для выгрузки данных в учетную систему
//   * АдресДляВыгрузки - Строка - URL-адрес учетной системы для выгрузки данных в учетную систему
//   * ПарольДляВыгрузки - Строка - пароль пользователя для выгрузки данных в учетную систему 
//   * КодУчетнойСистемы - Число - код обновляемой учетной системы абонента.
//   								Если не указан, создается новая учетная система.
Функция НовыйПараметрыСозданияОбновленияУчетнойСистемыБиллинга() Экспорт

	Параметры = Новый Структура;
	Параметры.Вставить("КодУчетнойСистемы", 0);
	Параметры.Вставить("ЛогинПользователяДляЗагрузки", "");
	Параметры.Вставить("АдресДляВыгрузки", "");
	Параметры.Вставить("ЛогинПользователяДляВыгрузки", "");
	Параметры.Вставить("ПарольДляВыгрузки", "");
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти 

#Область Application

// Возвращает список прикладных конфигураций, доступных абоненту этого приложения.
// 
// Параметры:
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//   
// Возвращаемое значение:
//  ТаблицаЗначений - доступные конфигурации:
//    * Код - Строка - код конфигурации
//    * Наименование - Строка - синоним конфигурации 
//    * Имя - Строка - имя конфигурации (как оно задано в конфигураторе).
//    * Описание - Строка - описание конфигурации 
//    * КодАбонента - Число - код абонента. 
//
Функция Конфигурации(ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "application/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("account", КодАбонента());
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияКонфигурация();
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.application, Переименования);
	
КонецФункции

// Возвращает свойства прикладной конфигурации.
// 
// Параметры:
//  КодКонфигурации - Строка - Код конфигурации.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//   
// Возвращаемое значение:
//  ТаблицаЗначений - доступные конфигурации:
//    * Код - Строка - код конфигурации
//    * Наименование - Строка - синоним конфигурации 
//    * Имя - Строка - имя конфигурации (как оно задано в конфигураторе).
//    * Описание - Строка - описание конфигурации.
//    * Картинка - Строка - пиктограмма конфигурации в формате Base64.
//    * КодАбонента - Число - код абонента.
//
Функция Конфигурация(КодКонфигурации,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "application/info";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодКонфигурации);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияКонфигурация();
	Возврат Служебный.ПереименоватьСвойства(ДанныеОтвета.application, Переименования);
	
КонецФункции

#КонецОбласти

#Область AppType

// Возвращает список вариантов перехода видов приложений
// Реализует метод внешнего программного интерфейса - apptype/transition_options/list
// 
// Параметры:
//  Отбор - см. НовыйОтборВариантовПерехода
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Неопределено, ТаблицаЗначений - список обработчиков перехода видов приложения:
//   * НомерВерсииКонфигурации - Строка - номер версии конфигурации
//   * КодПредыдущегоВидаПриложения - Строка - код предыдущего вида приложения
//   * КодВидаПриложения - Строка - код целевого вида приложения
//   * ИмяВидаПриложения - Строка - имя целевого вида приложения
//   * НаименованиеВидаПриложения - Строка - наименование целевого вида приложения 
//
Функция СписокВариантовПереходаВидовПриложений(Отбор = Неопределено,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Метод = "apptype/transition_options/list";
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	
	Если ТипЗнч(Отбор) = Тип("Структура") Тогда
		ПоляОтбора = Новый Соответствие;
		ПоляОтбора.Вставить("КодКонфигурации", "conf_id");
		ПоляОтбора.Вставить("ВерсияКонфигурации", "conf_version");
		ПоляОтбора.Вставить("КодВидаПриложения", "apptype_id");
		Для Каждого Элемент Из ПоляОтбора Цикл
			Если Отбор.Свойство(Элемент.Ключ) И ЗначениеЗаполнено(Отбор[Элемент.Ключ]) Тогда
				ДанныеЗапроса.Вставить(Элемент.Значение, Отбор[Элемент.Ключ]);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияВариантыПереходаВидовПриложений();
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.transition_options, Переименования);
	
КонецФункции

// Возвращает шаблон отбора обработчиков перехода.
//
// Возвращаемое значение:
//  Структура:
//   * КодКонфигурации - Строка - код конфигурации
//   * ВерсияКонфигурации - Строка - версия конфигурации
//   * КодВидаПриложения - Строка - код вида приложения
Функция НовыйОтборВариантовПерехода() Экспорт
	
	ОтборВариантовПерехода = Новый Структура;
	ОтборВариантовПерехода.Вставить("КодКонфигурации", "");
	ОтборВариантовПерехода.Вставить("ВерсияКонфигурации", "");
	ОтборВариантовПерехода.Вставить("КодВидаПриложения", "");
	
	Возврат ОтборВариантовПерехода;
	
КонецФункции

#КонецОбласти

#Область Bill

// Возвращает список счетов на оплату
// Реализует метод внешнего программного интерфейса - bill/list
// 
// Параметры:
//  Отбор - см. НовыйОтборСчетовНаОплату
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  ТаблицаЗначений - список счетов на оплату:
//	 * Номер - Строка - номер счета
//	 * Дата - Дата - дата создания счета
//	 * ДатаИзменения - Дата - дата изменения счета
//	 * ИдентификаторСчета - УникальныйИдентификатор - идентификатор счета
//	 * КодПродавца - Число - код (номер) абонента-продавца
//	 * КодПокупателя - Число - код (номер) абонента-покупателя
//	 * Сумма - Число - сумма счета
//	 * Продление - Булево - признак продления
//	 * ПлатежнаяСсылка - Строка - платежная ссылка
//	 * Оплачен - Булево - признак оплаты счета
//	 * ДополнительнаяИнформация - Строка - дополнительная информация по счету
//	 * Комментарий - Строка - комментарий счета
Функция СписокСчетовНаОплату(Отбор = Неопределено,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Метод = "bill/list";
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	
	Если ТипЗнч(Отбор) = Тип("Структура") Тогда
		ПоляОтбора = Новый Соответствие;
		ПоляОтбора.Вставить("Продавец", "seller_id");
		ПоляОтбора.Вставить("Покупатель", "customer_id");
		ПоляОтбора.Вставить("НачалоПериода", "start_date");
		ПоляОтбора.Вставить("КонецПериода", "end_date");
		Для Каждого Элемент Из ПоляОтбора Цикл
			Если Отбор.Свойство(Элемент.Ключ) И ЗначениеЗаполнено(Отбор[Элемент.Ключ]) Тогда
				ДанныеЗапроса.Вставить(Элемент.Значение, Отбор[Элемент.Ключ]);
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);
		
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияСчет(Метод);
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.bill, Переименования);
	
КонецФункции

// Возвращает данные счета на оплату по номеру или идентификатору счета.
// Реализует метод внешнего программного интерфейса - bill/info
//
// Параметры:
//  ИдентификаторСчета - УникальныйИдентификатор - идентификатор счета (не указывается, если передан номер счета)
//  НомерСчета - Строка - номер счета (не указывается, если передан идентификатор счета)
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Структура - данные счета:
//	 * Номер - Строка - номер счета
//	 * Дата - Дата - дата создания счета
//	 * ДатаИзменения - Дата - дата изменения счета
//	 * ИдентификаторСчета - УникальныйИдентификатор - идентификатор счета
//	 * КодПродавца - Число - код (номер) абонента-продавца
//	 * КодПокупателя - Число - код (номер) абонента-покупателя
//	 * Сумма - Число - сумма счета
//	 * Продление - Булево - признак продления
//	 * ПлатежнаяСсылка - Строка - платежная ссылка
//	 * Оплачен - Булево - признак оплаты счета
//	 * ДополнительнаяИнформация - Строка - дополнительная информация по счету
//	 * Комментарий - Строка - комментарий счета
//   * Тарифы - ТаблицаЗначений - тарифы:
//     ** КодТарифаПровайдера - Строка - код тарифа провайдера
//     ** КодТарифаОбслуживающейОрганизации - Строка - код тарифа обслуживающей организации
//     ** КодПериодаДействия - Строка - код периода действия тарифа
//     ** Количество - Число - количество тарифов в подписке
//     ** Сумма - Число - стоимость тарифа с учетом периода действия
//     ** НомерОснования - Строка - номер подписки-основания
//   * Услуги - ТаблицаЗначений - услуги:
//     ** Услуга - Строка - наименование услуги
//     ** Сумма - Число - стоимость услуги
//   * Файлы - ТаблицаЗначений - файлы представления текущего счета на оплату:
//     ** Идентификатор - УникальныйИдентификатор - идентификатор файла
//     ** Описание - Строка - имя файла представления счета
//   * ДополнительныеРеквизиты - ТаблицаЗначений - дополнительные реквизиты счета:
//     ** Ключ - Строка - имя дополнительного реквизита
//     ** Тип - Строка - тип значения 
//     ** Значение - Строка, Число, Дата, Булево - значение дополнительного реквизита
//	 * Состояние - Структура - данные состояния счета:
//     ** Ошибка - Булево - признак ошибки
//     ** Описание - Строка - описание состояния
//     ** Имя - Строка - имя состояния:
//         "created" - создан
//         "wait_sending" - ожидание отправки
//         "wait_registration" - ожидание оформления
//         "wait_payment" - ожидание оплаты
//         "paid" - оплачен
//         "billing_error" - ошибка выставления счета.
Функция ДанныеСчетаНаОплату(ИдентификаторСчета = Неопределено, НомерСчета = Неопределено,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Метод = "bill/info";
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	Если Не ПустаяСтрока(НомерСчета) Тогда
		ДанныеЗапроса.Вставить("id", НомерСчета);
	ИначеЕсли ТипЗнч(ИдентификаторСчета) = Тип("УникальныйИдентификатор") 
		И ИдентификаторСчета <> ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор() Тогда
		ДанныеЗапроса.Вставить("bill_id", ИдентификаторСчета);
	КонецЕсли; 
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке,
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияСчет(Метод);
	Переименования.Вставить("tariffs", "Тарифы");
	Переименования.Вставить("services", "Услуги");
	Переименования.Вставить("files", "Файлы");
	Переименования.Вставить("fields", "ДополнительныеРеквизиты");
	Переименования.Вставить("status", "Состояние");
	
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета.bill, Переименования);
	Результат.Тарифы = Служебный.МассивСтруктурВТаблицуЗначений(Результат.Тарифы, Служебный.ПереименованияСчетТарифы());
	Результат.Услуги = Служебный.МассивСтруктурВТаблицуЗначений(Результат.Услуги, Служебный.ПереименованияСчетУслуги());
	Результат.Файлы = Служебный.МассивСтруктурВТаблицуЗначений(Результат.Файлы, Служебный.ПереименованияСчетФайлы());
	Результат.ДополнительныеРеквизиты = Служебный.МассивСтруктурВТаблицуЗначений(
		Результат.ДополнительныеРеквизиты, Служебный.ПереименованияДополнительныхСведений());
	Результат.Состояние = Служебный.ПереименоватьСвойства(Результат.Состояние, Служебный.ПереименованияСчетСостояние());
	
	Возврат Результат;

КонецФункции

// Создает новый счет на оплату.
// Реализует метод внешнего программного интерфейса - bill/create
// 
// Параметры:
//  ДанныеСчета - Структура:
//	 * ИдентификаторСчета - УникальныйИдентификатор - идентификатор счета
//	 * КодПродавца - Число - код (номер) абонента-продавца
//	 * КодПокупателя - Число - код (номер) абонента-покупателя
//	 * Сумма - Число - сумма счета
//	 * Продление - Булево - признак продления
//	 * ПлатежнаяСсылка - Строка - платежная ссылка
//	 * Оплачен - Булево - признак оплаты счета
//	 * ДополнительнаяИнформация - Строка - дополнительная информация по счету
//	 * Комментарий - Строка - комментарий счета
//   * Тарифы - ТаблицаЗначений - тарифы:
//     ** КодТарифаПровайдера - Строка - код тарифа провайдера
//     ** КодТарифаОбслуживающейОрганизации - Строка - код тарифа обслуживающей организации
//     ** КодПериодаДействия - Строка - код периода действия тарифа
//     ** Количество - Число - количество тарифов в подписке
//     ** Сумма - Число - стоимость тарифа с учетом периода действия
//     ** НомерОснования - Строка - номер подписки-основания
//   * Услуги - ТаблицаЗначений - услуги:
//     ** Услуга - Строка - наименование услуги
//     ** Сумма - Число - стоимость услуги
//   * Файлы - ТаблицаЗначений - файлы представления текущего счета на оплату:
//     ** Идентификатор - УникальныйИдентификатор - идентификатор файла
//     ** Описание - Строка - имя файла представления счета
//   * ДополнительныеРеквизиты - ТаблицаЗначений - дополнительные реквизиты счета:
//     ** Ключ - Строка - имя дополнительного реквизита
//     ** Тип - Строка - тип значения (не обязательно, если у доп. реквизита один тип значения)
//     ** Значение - Строка, Число, Дата, Булево - значение дополнительного реквизита
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  Структура:
//   * Номер - Строка - номер созданного счета на оплату
//   * ИдентификаторСчета - УникальныйИдентификатор - идентификатор созданного счета на оплату.	
Функция СоздатьСчетНаОплату(ДанныеСчета,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Метод = "bill/create";
	ДанныеЗапроса = Служебный.ДанныеЗапросаСозданияИзмененияСчетаНаОплату(ДанныеСчета, Метод);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);

	Результат = Новый Структура;
	Результат.Вставить("Номер");
	Результат.Вставить("ИдентификаторСчета", ОбщегоНазначенияБТС.ПустойУникальныйИдентификатор());
	
	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Переименования = Служебный.ПереименованияСчетРезультатСозданияИзменения();
		Служебный.ПереименоватьСвойства(ДанныеОтвета, Переименования);
		ЗаполнитьЗначенияСвойств(Результат, ДанныеОтвета);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Изменяет существующий счет на оплату.
// Реализует метод внешнего программного интерфейса - bill/update
// 
// Параметры:
//  ДанныеСчета - Структура:
//   * ИдентификаторСчета - УникальныйИдентификатор - идентификатор счета
//   * КодПродавца - Число - код (номер) абонента-продавца
//	 * КодПокупателя - Число - код (номер) абонента-покупателя
//	 * Сумма - Число - сумма счета
//	 * Продление - Булево - признак продления
//	 * ПлатежнаяСсылка - Строка - платежная ссылка
//	 * Оплачен - Булево - признак оплаты счета
//	 * ДополнительнаяИнформация - Строка - дополнительная информация по счету
//	 * Комментарий - Строка - комментарий счета
//   * Тарифы - ТаблицаЗначений - тарифы:
//     ** КодТарифаПровайдера - Строка - код тарифа провайдера
//     ** КодТарифаОбслуживающейОрганизации - Строка - код тарифа обслуживающей организации
//     ** КодПериодаДействия - Строка - код периода действия тарифа
//     ** Количество - Число - количество тарифов в подписке
//     ** Сумма - Число - стоимость тарифа с учетом периода действия
//     ** НомерОснования - Строка - номер подписки-основания
//   * Услуги - ТаблицаЗначений - услуги:
//     ** Услуга - Строка - наименование услуги
//     ** Сумма - Число - стоимость услуги
//   * Файлы - ТаблицаЗначений - файлы представления текущего счета на оплату:
//     ** Идентификатор - УникальныйИдентификатор - идентификатор файла
//     ** Описание - Строка - имя файла представления счета
//   * ДополнительныеРеквизиты - ТаблицаЗначений - дополнительные реквизиты счета:
//     ** Ключ - Строка - имя дополнительного реквизита
//     ** Тип - Строка - тип значения (не обязательно, если у доп. реквизита один тип значения)
//     ** Значение - Строка, Число, Дата, Булево - значение дополнительного реквизита
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  Структура:
//   * Номер - Строка - номер измененного счета на оплату
//   * ИдентификаторСчета - УникальныйИдентификатор - идентификатор измененного счета на оплату.	
Функция ИзменитьСчетНаОплату(ДанныеСчета,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Метод = "bill/update";
	
	ДанныеЗапроса = Служебный.ДанныеЗапросаСозданияИзмененияСчетаНаОплату(ДанныеСчета, Метод) ;
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);

	Результат = Новый Структура;
	Результат.Вставить("Номер");
	Результат.Вставить("ИдентификаторСчета", ОбщегоНазначенияБТС.ПустойУникальныйИдентификатор());
	
	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Переименования = Служебный.ПереименованияСчетРезультатСозданияИзменения();
		Служебный.ПереименоватьСвойства(ДанныеОтвета, Переименования);
		ЗаполнитьЗначенияСвойств(Результат, ДанныеОтвета);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции	

// Возвращает талон на загрузку файла в существующий счет на оплату
// Реализует метод внешнего программного интерфейса - bill/file_token/upload
//
// Параметры:
//  ПараметрыПолучения - см. НовыйПараметрыПолученияТалонаНаЗагрузкуФайла
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//  
// Возвращаемое значение:
//  Структура:
//  * НомерСчета - Строка - номер счета
//  * ИдентификаторСчета - УникальныйИдентификатор - идентификатор счета
//  * НаправлениеПередачиДанных - Строка - направление передачи файла = "upload"
//  * ТалонНаЗагрузкуФайла - Строка - талон на загрузку файла
//  * АдресДляЗагрузки - Строка - URL для загрузки файла методом PUT
Функция СчетНаОплатуФайлыТалонНаЗагрузку(ПараметрыПолучения,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Метод = "bill/file_token/upload";
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	Если ПараметрыПолучения.Свойство("НомерСчета") Тогда
		НомерСчета = ПараметрыПолучения.НомерСчета;
	Иначе
		НомерСчета = "";
	КонецЕсли; 
	Если ПараметрыПолучения.Свойство("ИдентификаторСчета") Тогда
		ИдентификаторСчета = ПараметрыПолучения.ИдентификаторСчета;	
	Иначе
		ИдентификаторСчета = ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор();
	КонецЕсли; 
	Если Не ПустаяСтрока(НомерСчета) Тогда
		ДанныеЗапроса.Вставить("id", НомерСчета);
	ИначеЕсли ТипЗнч(ИдентификаторСчета) = Тип("УникальныйИдентификатор") 
		И ИдентификаторСчета <> ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор() Тогда
		ДанныеЗапроса.Вставить("bill_id", ИдентификаторСчета);
	КонецЕсли; 
	ДанныеЗапроса.Вставить("name", ПараметрыПолучения.ИмяФайла);
	ДанныеЗапроса.Вставить("size", ПараметрыПолучения.Размер);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
		
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияСчетТалонНаЗагрузку();
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета, Переименования);
	Результат.Удалить("general");
	
	Возврат Результат;
	
КонецФункции

// Создать подписки на тарифы на основании счета на оплату.
// 
// Параметры:
//  ПараметрыСоздания - см. НовыйПараметрыСозданияПодписокНаТарифыНаОснованииСчетаНаОплату
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Массив из Строка - номера созданных подписок
Функция СоздатьПодпискиНаТарифыНаОснованииСчетаНаОплату(ПараметрыСоздания, 
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Метод = "bill/create_subscription";
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	Если ПараметрыСоздания.Свойство("НомерСчета") Тогда
		НомерСчета = ПараметрыСоздания.НомерСчета;
	Иначе
		НомерСчета = "";
	КонецЕсли; 
	Если ПараметрыСоздания.Свойство("ИдентификаторСчета") Тогда
		ИдентификаторСчета = ПараметрыСоздания.ИдентификаторСчета;
	Иначе
		ИдентификаторСчета = ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор();
	КонецЕсли; 
	Если Не ПустаяСтрока(НомерСчета) Тогда
		ДанныеЗапроса.Вставить("id", НомерСчета);
	ИначеЕсли ТипЗнч(ИдентификаторСчета) = Тип("УникальныйИдентификатор") 
		И ИдентификаторСчета <> ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор() Тогда
		ДанныеЗапроса.Вставить("bill_id", ИдентификаторСчета);
	КонецЕсли;
	 
	Если ЗначениеЗаполнено(ПараметрыСоздания.ДатаПодключения) Тогда
		ДанныеЗапроса.Вставить("start", ПараметрыСоздания.ДатаПодключения);
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	СозданныеПодписки = Новый СписокЗначений;
	Для Каждого Элемент Из ДанныеОтвета.subscription Цикл
		СозданныеПодписки.Добавить(Элемент.id);
	КонецЦикла;
	
	СозданныеПодписки.СортироватьПоЗначению();
	
	Возврат СозданныеПодписки.ВыгрузитьЗначения();

КонецФункции

// Создает новое электронное письмо для отправки в обслуживающую организацию 
// с информацией о необходимости получить счет для оплаты услуг.
// Реализует метод внешнего программного интерфейса - bill/email_create
// 
// Параметры:
//  ПараметрыЗапросаПисьмомСчета - см. НовыйПараметрыЗапросаПисьмомСчета
//	ВызыватьИсключениеПриОшибке - Булево - по умолчанию = Истина
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// Возвращаемое значение:
//  Структура:
//   * Ошибка - Булево - Признак наличия ошибки при создании письма
//   * Сообщение - Строка - Строковое описание ошибки.	
Функция СоздатьЗапросПисьмомСчетаДляОплатыУслуг(ПараметрыЗапросаПисьмомСчета, 
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Метод = "bill/email_create";
	
	ВерсияИнтерфейса = СвойстваВерсииИнтерфейса().Версия;

	Результат = Новый Структура("Ошибка,Сообщение", Ложь, "");
	Если ВерсияИнтерфейса >= 30 Тогда
		
		ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
		ДанныеЗапроса.Вставить("servant", ПараметрыЗапросаПисьмомСчета.КодВедущегоАбонента);
		ДанныеЗапроса.Вставить("account", ПараметрыЗапросаПисьмомСчета.КодАбонента);
		ДанныеЗапроса.Вставить("copy", ПараметрыЗапросаПисьмомСчета.ОтправитьКопиюСебе);
		ДанныеЗапроса.Вставить("comment", ПараметрыЗапросаПисьмомСчета.Комментарий);		
		
		РезультатВызова = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
		ДанныеОтвета = Служебный.РезультатВыполнения(РезультатВызова, ВызыватьИсключениеПриОшибке, 
			ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);
		
		Если ДанныеОтвета.general.error Тогда
			Результат.Ошибка = Истина;
			Результат.Сообщение = ДанныеОтвета.general.message;			
		КонецЕсли;
		 
	Иначе
		
		Результат.Ошибка = Истина;
		Результат.Сообщение = НСтр("ru = 'Отправка письма для запроса счета на оплату не поддерживается 
		|в текущей версии менеджера сервиса.';
		|en = 'Sending an email to request a proforma invoice is not supported 
		|in the current Service Manager version.'");
		
	КонецЕсли;

		
	Возврат Результат;	
	
КонецФункции

// Возвращает шаблон отбора счетов на оплату.
// 
// Возвращаемое значение:
//  Структура:
//   * КодПродавца - Число - код абонента-продавца
//   * КодПокупателя - Число - код абонента-покупателя
//   * НачалоПериода - Дата - дата начала периода
//   * КонецПериода - Дата - дата конца периода 
//   
Функция НовыйОтборСчетовНаОплату() Экспорт

	ОтборСчетовНаОплату = Новый Структура;
	ОтборСчетовНаОплату.Вставить("Продавец", 0);
	ОтборСчетовНаОплату.Вставить("Покупатель", 0);
	ОтборСчетовНаОплату.Вставить("НачалоПериода", '00010101');
	ОтборСчетовНаОплату.Вставить("КонецПериода", '00010101');
	
	Возврат ОтборСчетовНаОплату;
	
КонецФункции

// Возвращает шаблон нового счета на оплату.
//
// Возвращаемое значение:
//  Структура:
//   * ИдентификаторСчета - УникальныйИдентификатор - идентификатор счета
//	 * КодПродавца - Число - код (номер) абонента-продавца
//	 * КодПокупателя - Число - код (номер) абонента-покупателя
//	 * Сумма - Число - сумма счета
//	 * Продление - Булево - признак продления
//	 * ПлатежнаяСсылка - Строка - платежная ссылка
//	 * Оплачен - Булево - признак оплаты счета
//	 * ДополнительнаяИнформация - Строка - дополнительная информация по счету
//	 * Комментарий - Строка - комментарий счета
//   * Тарифы - ТаблицаЗначений - тарифы:
//     ** КодТарифаПровайдера - Строка - код тарифа провайдера
//     ** КодТарифаОбслуживающейОрганизации - Строка - код тарифа обслуживающей организации
//     ** КодПериодаДействия - Строка - код периода действия тарифа
//     ** НомерОснования - Строка - номер подписки-основания
//     ** Сумма - Число - стоимость тарифа с учетом периода действия
//   * Услуги - ТаблицаЗначений - услуги:
//     ** Услуга - Строка - наименование услуги
//     ** Сумма - Число - стоимость услуги
//   * Файлы - ТаблицаЗначений - файлы представления текущего счета на оплату:
//     ** Идентификатор - УникальныйИдентификатор - идентификатор файла
//     ** Описание - Строка - имя файла представления счета
//   * ДополнительныеРеквизиты - ТаблицаЗначений - дополнительные реквизиты счета:
//     ** Ключ - Строка - имя дополнительного реквизита
//     ** Тип - Строка - тип значения (не обязательно, если у доп. реквизита один тип значения)
//     ** Значение - Строка, Число, Дата, Булево - значение дополнительного реквизита
Функция НовыйШаблонСчетаНаОплату() Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	
	ДанныеСчета = Новый Структура;
	ДанныеСчета.Вставить("ИдентификаторСчета", Новый ОписаниеТипов("УникальныйИдентификатор"));
	ДанныеСчета.Вставить("КодПродавца", ОбщегоНазначения.ОписаниеТипаЧисло(12, 0, ДопустимыйЗнак.Неотрицательный));
	ДанныеСчета.Вставить("КодПокупателя", ОбщегоНазначения.ОписаниеТипаЧисло(12, 0, ДопустимыйЗнак.Неотрицательный));
	ДанныеСчета.Вставить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(31, 2, ДопустимыйЗнак.Неотрицательный));
	ДанныеСчета.Вставить("Продление", Новый ОписаниеТипов("Булево"));
	ДанныеСчета.Вставить("ПлатежнаяСсылка", ОбщегоНазначения.ОписаниеТипаСтрока(1024));
	ДанныеСчета.Вставить("Оплачен", Новый ОписаниеТипов("Булево"));
	ДанныеСчета.Вставить("ДополнительнаяИнформация", Новый ОписаниеТипов("Строка"));
	ДанныеСчета.Вставить("Комментарий", Новый ОписаниеТипов("Строка"));
	
	ДанныеСчета.Вставить("Тарифы", НовыйТарифыСчетаНаОплату());
	ДанныеСчета.Вставить("Услуги", НовыйУслугиСчетаНаОплату());
	ДанныеСчета.Вставить("Файлы", НовыйФайлыСчетаНаОплату());
	ДанныеСчета.Вставить("ДополнительныеРеквизиты", Служебный.НовыйДополнительныеСведения());
	
	Возврат ДанныеСчета;
	
КонецФункции

// Возвращает шаблон параметров получения талона на загрузку файла.
// 
// Возвращаемое значение:
//  Структура:
//   * НомерСчета - Строка - номер счета (не указывается, если передан идентификатор счета)
//   * ИдентификаторСчета - УникальныйИдентификатор - идентификатор счета (не указывается, если передан номер счета)
//   * ИмяФайла - Строка - имя файла с расширением
//   * Размер - Строка - размер файла в байтах.
Функция НовыйПараметрыПолученияТалонаНаЗагрузкуФайла() Экспорт

	Параметры = Новый Структура;
	Параметры.Вставить("НомерСчета", "");
	Параметры.Вставить("ИдентификаторСчета", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	Параметры.Вставить("ИмяФайла", "");
	Параметры.Вставить("Размер", 0);
	
	Возврат Параметры;

КонецФункции

// Возвращает шаблон параметров создания подписок на тарифы на основании счета на оплату.
// 
// Возвращаемое значение:
//  Структура:
//   * НомерСчета - Строка - номер счета (не указывается, если передан идентификатор счета)
//   * ИдентификаторСчета - УникальныйИдентификатор - идентификатор счета (не указывается, если передан номер счета)
//   * ДатаПодключения - Дата - дата подключения создаваемых подписок на тарифы.
Функция НовыйПараметрыСозданияПодписокНаТарифыНаОснованииСчетаНаОплату() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("НомерСчета", "");
	Параметры.Вставить("ИдентификаторСчета", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	Параметры.Вставить("ДатаПодключения", '00010101000000');
	
	Возврат Параметры;
	
КонецФункции

// Возвращает шаблон табличной части "Тарифы" счета на оплату.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новый тарифы счета на оплату:
// * КодТарифаПровайдера - Строка - длина 9
// * КодТарифаОбслуживающейОрганизации - Строка - длина 9
// * КодПериодаДействия - Строка - длина 10
// * Количество - Число - разрядность 10,0
// * Сумма  - Число - разрядность 31,2
// * НомерОснования - Строка - разрядность 9.
Функция НовыйТарифыСчетаНаОплату() Экспорт
	
 	Тарифы = Новый ТаблицаЗначений;
	Тарифы.Колонки.Добавить("КодТарифаПровайдера", ОбщегоНазначения.ОписаниеТипаСтрока(9));
	Тарифы.Колонки.Добавить("КодТарифаОбслуживающейОрганизации", ОбщегоНазначения.ОписаниеТипаСтрока(9));
	Тарифы.Колонки.Добавить("КодПериодаДействия", ОбщегоНазначения.ОписаниеТипаСтрока(10));
	Тарифы.Колонки.Добавить("Количество", ОбщегоНазначения.ОписаниеТипаЧисло(10,0));
	Тарифы.Колонки.Добавить("Сумма", ОбщегоНазначения.ОписаниеТипаЧисло(31,2));
	Тарифы.Колонки.Добавить("НомерОснования", ОбщегоНазначения.ОписаниеТипаСтрока(9));
	
	Возврат Тарифы;
	
КонецФункции

// Возвращает шаблон табличной части "Услуги" счета на оплату.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новый услуги счета на оплату:
// * Услуга - Строка - длина 1000
// * Сумма - Число - разрядность 31,2.
Функция НовыйУслугиСчетаНаОплату() Экспорт
	
	Услуги = Новый ТаблицаЗначений;
	Услуги.Колонки.Добавить("Услуга", ОбщегоНазначения.ОписаниеТипаСтрока(1000));
	Услуги.Колонки.Добавить("Сумма",ОбщегоНазначения.ОписаниеТипаЧисло(31,2));
	
	Возврат Услуги;

КонецФункции

// Возвращает шаблон табличной части "Файлы" счета на оплату.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новый файлы счета на оплату:
// * Идентификатор - УникальныйИдентификатор
// * Описание - Строка -длина 150.
Функция НовыйФайлыСчетаНаОплату() Экспорт
	
	Файлы = Новый ТаблицаЗначений;
	Файлы.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("УникальныйИдентификатор"));
	Файлы.Колонки.Добавить("Описание", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	
	Возврат Файлы;

КонецФункции

// Возвращает шаблон параметров создания запроса письмом счета на оплату услугу.
// 
// Возвращаемое значение:
//  Структура:
//   * КодВедущегоАбонента - Число - Код абонента обслуживающей организации
//   * КодАбонента - Число - Код абонента, формирующего письмо с запросом счета на оплату
//   * ОтправитьКопиюСебе - Булево - Добавлять в копию письма почтовый адрес абонента
//   * Комментарий - Строка - Произвольный комментарий абонента для обслуживающей организации.
Функция НовыйПараметрыЗапросаПисьмомСчета() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("КодВедущегоАбонента", 0);
	Параметры.Вставить("КодАбонента", 0);
	Параметры.Вставить("ОтправитьКопиюСебе", Ложь);
	Параметры.Вставить("Комментарий", "");
	
	Возврат Параметры;
КонецФункции

#КонецОбласти

#Область Tenant

// Возвращает данные абонента этого приложения.
//
// Параметры:
//	Пользователь - СправочникСсылка.Пользователи - пользователь абонента, которого требуется определить. Если не указан, используется текущий.
//	Токен - Строка - ключ, выданный администратору ИБ для осуществления операции.
// 
// Возвращаемое значение:
//  Структура - данные абонента:
//    * Наименование - Строка - наименование абонента
//    * Код - Число - код абонента
//    * РольПользователя - ПеречислениеСсылка.РолиПользователейАбонентов - роль текущего пользователя абонента.
//
Функция АбонентЭтогоПриложения(Знач Пользователь = Неопределено, Знач Токен = Неопределено) Экспорт
	
	Возврат ПрограммныйИнтерфейсСервисаПовтИсп.АбонентЭтогоПриложения(Пользователь, Токен);
	
КонецФункции

// Возвращает список приложений, доступных пользователю абонента этого приложения. 
// 
// Параметры:
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  ТаблицаЗначений - доступные приложения:
//   * Код - Число - код (номер) приложения
//   * Наименование - Строка - наименование
//   * КодАбонентаВладельца - Число - код абонента, владельца приложения
//   * КодКонфигурации - Строка - код конфигурации
//   * ВерсияКонфигурации - Строка - версия конфигурации
//   * НаименованиеКонфигурации - Строка - наименование конфигурации
//   * КодВидаПриложения - Строка - код вида приложения
//   * ИмяВидаПриложения - Строка - имя вида приложения
//   * НаименованиеВидаПриложения - Строка - наименование вида приложения
//   * СостояниеПриложения - ПеречислениеСсылка.СостоянияПриложений - состояние приложения
//   * АдресПриложения - Строка - URL-адрес приложения
//   * КодЯзыкаПользователя - Строка - код языка пользователя в приложении
//   * ЧасовойПояс - Строка - часовой пояс приложения
//
Функция Приложения(ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);

	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Переименования = Служебный.ПереименованияПриложение();

	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.tenant, Переименования);

КонецФункции

// Возвращает список приложений, доступных пользователю абонента этого приложения. 
// 
// Параметры:
//  КодАбонента - Число - код (номер) абонента.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  ТаблицаЗначений - доступные приложения:
//   * Код - Число - код (номер) приложения
//   * Наименование - Строка - наименование
//   * КодАбонентаВладельца - Число - код абонента, владельца приложения
//   * КодКонфигурации - Строка - код конфигурации
//   * ВерсияКонфигурации - Строка - версия конфигурации
//   * НаименованиеКонфигурации - Строка - наименование конфигурации
//   * КодВидаПриложения - Строка - код вида приложения
//   * ИмяВидаПриложения - Строка - имя вида приложения
//   * НаименованиеВидаПриложения - Строка - наименование вида приложения
//   * СостояниеПриложения - ПеречислениеСсылка.СостоянияПриложений - состояние приложения
//   * АдресПриложения - Строка - URL-адрес приложения
//   * КодЯзыкаПользователя - Строка - код языка пользователя в приложении
//   * ЧасовойПояс - Строка - часовой пояс приложения
//
Функция ПриложенияАбонента(КодАбонента, ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДобавитьАвторизациюАбонента(ДанныеЗапроса, КодАбонента);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);

	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Переименования = Служебный.ПереименованияПриложение();

	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.tenant, Переименования);

КонецФункции

// Возвращает информацию об указанном приложении.
//
// Параметры:
//  КодПриложения - Строка - код приложения (номер области).
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Структура - свойства приложения:
//   * Код - Число - код (номер) приложения
//   * Наименование - Строка- наименование
//   * КодАбонентаВладельца - Число - код абонента, владельца приложения
//   * КодКонфигурации - Строка - код конфигурации
//   * ВерсияКонфигурации - Строка - версия конфигурации
//   * НаименованиеКонфигурации - Строка - наименование конфигурации
//   * КодВидаПриложения - Строка - код вида приложения
//   * ИмяВидаПриложения - Строка - имя вида приложения
//   * НаименованиеВидаПриложения - Строка - наименование вида приложения
//   * СостояниеПриложения - ПеречислениеСсылка.СостоянияПриложений - состояние приложения
//   * АдресПриложения - Строка - URL-адрес приложения
//   * КодЯзыкаПользователя - Строка - код языка пользователя в приложении
//   * ЧасовойПояс - Строка - часовой пояс приложения
//   * ВерсияПлатформы - Строка - версия платформы
//   * РазмерПриложения - Число - размер приложения
//   * ДатаРасчетаРазмераПриложения - Дата - дата расчета размера приложения
//
Функция СвойстваПриложения(КодПриложения,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/info";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодПриложения);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);

	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияПриложение();
	
	Возврат Служебный.ПереименоватьСвойства(ДанныеОтвета.tenant, Переименования);
	
КонецФункции

// Возвращает список пользователей, которым разрешен доступ к указанному приложению.
//
// Параметры:
//  КодПриложения - Число - код приложения (номер области)
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// 
// Возвращаемое значение:
//  ТаблицаЗначений - пользователи, которым разрешен доступ:
//   * Логин - Строка - логин (имя) пользователя
//   * Право - ПеречислениеСсылка.ПраваПользователяПриложения - право пользователя на текущее приложение в менеджере сервиса.
//   * ТребуетсяНастройкаВПриложении - Булево - признак необходимости настройки прав пользователя в приложении.
//
Функция ПользователиПриложения(КодПриложения,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/users/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодПриложения);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Переименования = Новый Соответствие;
	Переименования.Вставить("login", Служебный.ОписаниеКолонки("Логин", ОбщегоНазначения.ОписаниеТипаСтрока(50)));
	Переименования.Вставить("role", Служебный.ОписаниеКолонки("Право", 
		Новый ОписаниеТипов("ПеречислениеСсылка.ПраваПользователяПриложения")));
	Переименования.Вставить("setup_required", Служебный.ОписаниеКолонки("ТребуетсяНастройкаВПриложении",
		Новый ОписаниеТипов("Булево")));
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.user, Переименования);
	
КонецФункции

// Возвращает список пользователей, которым разрешен доступ к указанному приложению.
//
// Параметры:
//  Параметры - см. НовыйПараметрыЗапросаПользователейПриложенияАбонента.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// 
// Возвращаемое значение:
//  ТаблицаЗначений - пользователи, которым разрешен доступ:
//   * Логин - Строка - логин (имя) пользователя
//   * Право - ПеречислениеСсылка.ПраваПользователяПриложения - право пользователя на текущее приложение в менеджере сервиса.
//   * ТребуетсяНастройкаВПриложении - Булево - признак необходимости настройки прав пользователя в приложении.
//
Функция ПользователиПриложенияАбонента(Параметры,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/users/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", Параметры.КодПриложения);
	Если ЗначениеЗаполнено(Параметры.Логин) Тогда
		ДанныеЗапроса.Вставить("login", Параметры.Логин);
	КонецЕсли;
	
	ДобавитьАвторизациюАбонента(ДанныеЗапроса, Параметры.КодАбонента);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Переименования = Новый Соответствие;
	Переименования.Вставить("login", Служебный.ОписаниеКолонки("Логин", ОбщегоНазначения.ОписаниеТипаСтрока(50)));
	Переименования.Вставить("role", Служебный.ОписаниеКолонки("Право", 
		Новый ОписаниеТипов("ПеречислениеСсылка.ПраваПользователяПриложения")));
	Переименования.Вставить("setup_required", Служебный.ОписаниеКолонки("ТребуетсяНастройкаВПриложении",
		Новый ОписаниеТипов("Булево")));
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.user, Переименования);
	
КонецФункции

// Устанавливает пользователю с указанным логином доступ к указанному приложению 
// и назначает указанную роль для работы в приложении.
//
// Параметры:
//  ПараметрыДобавления - см. НовыйПараметрыДобавленияПользователяВПриложение
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - результат установки права доступа к приложению: Истина - право установлено, Ложь - произошла ошибка.
//
Функция ДобавитьПользователяВПриложение(ПараметрыДобавления,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/users/add";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", ПараметрыДобавления.КодПриложения);
	ДанныеЗапроса.Вставить("login", ПараметрыДобавления.Логин);
	ДанныеЗапроса.Вставить("role", Перечисления.ПраваПользователяПриложения.ИмяПоЗначению(ПараметрыДобавления.Право));
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Отменяет пользователю с указанным логином доступ к указанному приложению.
//
// Параметры:
//  Логин - Строка - Логин (Имя) пользователя.
//  КодПриложения - Число - код приложения (номер области)
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - результат отмены права доступа к приложению: Истина - право отменено, Ложь - произошла ошибка.
//
Функция УдалитьПользователяИзПриложения(Логин, КодПриложения,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/users/delete";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", КодПриложения);
	ДанныеЗапроса.Вставить("login", Логин);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Создает новое приложение с указанной прикладной конфигурацией.
//
// Параметры:
//  ПараметрыСоздания - см. НовыйПараметрыСозданияПриложения
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Структура - результат создания приложения:
//  * Код - Число - код созданного приложения (номер области)
//  * СостояниеПриложения - ПеречислениеСсылка.СостоянияПриложений - состояние приложения после создания.
//  * АдресПриложения - Строка - адрес созданного приложения.
//
Функция СоздатьПриложение(ПараметрыСоздания,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/create";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("application", ПараметрыСоздания.КодКонфигурации);
	ДанныеЗапроса.Вставить("name", ПараметрыСоздания.Наименование);
	ДанныеЗапроса.Вставить("timezone", ПараметрыСоздания.ЧасовойПояс);
	
	Если ПараметрыСоздания.УстановитьПраваПриложений Тогда
		
		ПраваНаПриложения = Новый Массив;
		Для каждого Строка Из ПараметрыСоздания.ПраваНаПриложения Цикл
			
			ПравоНаПриложение = Новый Структура;
			ПравоНаПриложение.Вставить("login", Строка.Логин);
			ПравоНаПриложение.Вставить("role", Перечисления.ПраваПользователяПриложения.ИмяПоЗначению(Строка.Право));
			
			ПраваНаПриложения.Добавить(ПравоНаПриложение);
			
		КонецЦикла;
		
		ДанныеЗапроса.Вставить("users", ПраваНаПриложения);
		
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);

	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Новый Соответствие;
	Переименования.Вставить("id", Служебный.ОписаниеКолонки("Код", ОбщегоНазначения.ОписаниеТипаЧисло(7, 0, ДопустимыйЗнак.Неотрицательный)));
	Переименования.Вставить("status", Служебный.ОписаниеКолонки("СостояниеПриложения", Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияПриложений")));
	Переименования.Вставить("url", Служебный.ОписаниеКолонки("АдресПриложения", ОбщегоНазначения.ОписаниеТипаСтрока(500)));
	
	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Или ОсновныеСвойстваОтвета.КодОтвета = 10202 Тогда
		Результат = ОбщегоНазначения.СкопироватьРекурсивно(ДанныеОтвета.tenant);
		Результат = Служебный.ПереименоватьСвойства(Результат, Переименования);
	Иначе
		Результат = Новый Структура;
		Результат.Вставить("Код", 0);
		Результат.Вставить("СостояниеПриложения", Перечисления.СостоянияПриложений.ПустаяСсылка());
		Результат.Вставить("АдресПриложения", "");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Обновляет свойства приложения.
// Реализует метод внешнего программного интерфейса - tenant/update.
//
// Параметры:
//  Параметры - см. НовыйПараметрыОбновленияПриложения
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Структура - результат создания приложения:
//  * Код - Число - код созданного приложения (номер области)
//  * СостояниеПриложения - ПеречислениеСсылка.СостоянияПриложений - состояние приложения после создания.
//  * АдресПриложения - Строка - адрес созданного приложения.
//
Функция ОбновитьПриложение(Параметры,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/update";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", Параметры.КодПриложения);
	Если Не ПустаяСтрока(Параметры.Наименование) Тогда
		ДанныеЗапроса.Вставить("name", Параметры.Наименование);
	КонецЕсли;
	
	Если Не ПустаяСтрока(Параметры.ЧасовойПояс) Тогда
		ДанныеЗапроса.Вставить("timezone", Параметры.ЧасовойПояс);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Пользователи) Тогда
		ПользователиПриложения = Новый Массив;
		Для каждого Строка Из Параметры.Пользователи Цикл
			
			ПользовательПриложения = Новый Структура;
			ПользовательПриложения.Вставить("login", Строка.Логин);
			ПользовательПриложения.Вставить("role", Перечисления.ПраваПользователяПриложения.ИмяПоЗначению(Строка.Право));
			
			ПользователиПриложения.Добавить(ПользовательПриложения);
			
		КонецЦикла;
		
		ДанныеЗапроса.Вставить("users", ПользователиПриложения);
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);
	
	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Создает новое приложение из указанного файла выгрузки.
//
// Параметры:
//  ПараметрыСоздания - см. НовыйПараметрыСозданияПриложенияИзФайлаВыгрузки
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Структура - результат создания приложения:
//  * Код - Число - код созданного приложения (номер области)
//  * СостояниеПриложения - ПеречислениеСсылка.СостоянияПриложений - состояние приложения после создания.
//  * АдресПриложения - Строка - адрес созданного приложения.
//
Функция СоздатьПриложениеИзФайлаВыгрузки(ПараметрыСоздания,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/create_from_data_dump";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("file_id", ПараметрыСоздания.ИдентификаторФайла);
	ДанныеЗапроса.Вставить("name", ПараметрыСоздания.Наименование);
	ДанныеЗапроса.Вставить("timezone", ПараметрыСоздания.ЧасовойПояс);
	
	СписокПользователей = Новый Массив;
	Для каждого Строка Из ПараметрыСоздания.Пользователи Цикл
		
		Пользователь = Новый Структура;
		Пользователь.Вставить("login", Строка.Логин);
		Пользователь.Вставить("role", Перечисления.ПраваПользователяПриложения.ИмяПоЗначению(Строка.Право));
		Пользователь.Вставить("user_id", Строка.ИдентификаторПользователя);
		
		СписокПользователей.Добавить(Пользователь);
		
	КонецЦикла;
	ДанныеЗапроса.Вставить("users", СписокПользователей);
	
	СписокРасширений = Новый Массив;
	Для каждого Строка Из ПараметрыСоздания.Расширения Цикл
		
		Расширение = Новый Структура;
		Расширение.Вставить("version_uuid", Строка.УникальныйИдентификатор);
		Расширение.Вставить("id", Строка.Имя);
		Расширение.Вставить("restore_frame", Строка.ВосстанавливатьКаркаснуюВерсию);
		
		СписокРасширений.Добавить(Расширение);
		
	КонецЦикла;
	ДанныеЗапроса.Вставить("extensions", СписокРасширений);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);

	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Новый Соответствие;
	Переименования.Вставить("id", Служебный.ОписаниеКолонки("Код", ОбщегоНазначения.ОписаниеТипаЧисло(7, 0, ДопустимыйЗнак.Неотрицательный)));
	Переименования.Вставить("status", Служебный.ОписаниеКолонки("СостояниеПриложения", Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияПриложений")));
	Переименования.Вставить("url", Служебный.ОписаниеКолонки("АдресПриложения", ОбщегоНазначения.ОписаниеТипаСтрока(500)));
	
	Если (ОсновныеСвойстваОтвета.КодОтвета = 10200 Или ОсновныеСвойстваОтвета.КодОтвета = 10202)
		И ДанныеОтвета.Свойство("tenant") Тогда
		
		Результат = ОбщегоНазначения.СкопироватьРекурсивно(ДанныеОтвета.tenant);
		Результат = Служебный.ПереименоватьСвойства(Результат, Переименования);
	Иначе
		Результат = Новый Структура;
		Результат.Вставить("Код", 0);
		Результат.Вставить("СостояниеПриложения", Перечисления.СостоянияПриложений.ПустаяСсылка());
		Результат.Вставить("АдресПриложения", "");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Получает наименование для нового приложения.
//
// Параметры:
//  КодВидаПриложения - Строка - код вида приложения нового приложения.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Строка - наименование для установки новому приложению.
//
Функция НаименованиеНовогоПриложения(КодВидаПриложения,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/new_name";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("apptype_id", КодВидаПриложения);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеОтвета.name;
	
КонецФункции

// Устанавливает новое наименование приложению.
//
// Параметры:
//  ПараметрыИзменения - см. НовыйПараметрыУстановкиНаименованияПриложение
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - результат установки наименования приложению: Истина - наименование установлено, Ложь - произошла ошибка.
//
Функция УстановитьНаименованиеПриложения(ПараметрыИзменения,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/set_name";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", ПараметрыИзменения.КодПриложения);
	ДанныеЗапроса.Вставить("name", ПараметрыИзменения.НазваниеПриложения);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);
	
	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Устанавливает новый часовой пояс приложению.
//
// Параметры:
//  ПараметрыИзменения - см. НовыйПараметрыУстановкиЧасовогоПоясаПриложения
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - результат установки часового пояса приложению: Истина - установлено, Ложь - произошла ошибка.
//
Функция УстановитьЧасовойПоясПриложения(ПараметрыИзменения,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/set_timezone";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", ПараметрыИзменения.КодПриложения);
	ДанныеЗапроса.Вставить("timezone", ПараметрыИзменения.ЧасовойПояс);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, ОсновныеСвойстваОтвета.КодОтвета, ОсновныеСвойстваОтвета.Сообщение);
	
	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Возвращает список резервных копий приложения.
// Реализует метод внешнего программного интерфейса - tenant/backup/list.
//
// Возвращаемое значение:
//  ТаблицаЗначений - список резервных копий:
//   * ИдентификаторРК - Строка - идентификатор резервной копии, длина - 36
//   * МоментСозданияРК - Дата - момент создания резервной копии, ДатаВремя
//   * ДляТехПоддержки - Булево - признак создания резервной копии для тех. поддержки
//
Функция СписокРезервныхКопийПриложения() Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/backup/list";
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", РаботаВМоделиСервиса.ЗначениеРазделителяСеанса());
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат);

	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияРезервныеКопийПриложения();
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.backup, Переименования);
	
КонецФункции

// Возвращает список резервных копий приложения.
// Реализует метод внешнего программного интерфейса - tenant/backup/list.
//
// Параметры:
//  Параметры - см. НовыйПараметрыПолученияСпискаРезервныхКопийПриложенияАбонента
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  ТаблицаЗначений - список резервных копий:
//   * ИдентификаторРК - Строка - идентификатор резервной копии, длина - 36
//   * НомерОбласти - Число
//   * НаименованиеОбласти - Строка
//   * Размер - Число - размер файла
//   * ВерсияКонфигурации - Строка
//   * ИмяВидаПриложения - Строка
//   * Исходная - Булево
//   * ДатаСозданияUTC - Дата - дата создания в универсальном времени
//   * ПоТребованию - Булево
//   * Ежегодная - Булево
//   * Ежемесячная - Булево
//   * Ежедневная - Булево
//   * Комментарий - Строка
//   * ЧасовойПояс - Строка
//
Функция СписокРезервныхКопийПриложенияАбонента(Параметры,
		ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tenant/backup/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДобавитьАвторизациюАбонента(ДанныеЗапроса, Параметры.КодАбонента);
	ДанныеЗапроса.Вставить("id", Параметры.КодПриложения);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияРезервныеКопии();
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.backup, Переименования);
	
КонецФункции

// Возвращает шаблон параметров создания приложения для метода ПрограммныйИнтерфейсСервиса.СоздатьПриложение.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров создания приложения:
//   * Наименование - Строка - наименование создаваемого приложения.
//   * КодКонфигурации - Строка - код конфигурации (вид приложения)
//   * ЧасовойПояс - Строка - рабочий часовой пояс приложения
//   * УстановитьПраваПриложений - Булево - необходимость установки прав на приложения.
//   * ПраваНаПриложения - ТаблицаЗначений - устанавливаемые права на приложения:
//     ** Логин - Строка - логин (имя пользователя)
//     ** Право - ПеречислениеСсылка.ПраваПользователяПриложения - устанавливаемая роль пользователя.
Функция НовыйПараметрыСозданияПриложения() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("Наименование", "");
	Параметры.Вставить("КодКонфигурации", "");
	Параметры.Вставить("ЧасовойПояс", "");
	
	Параметры.Вставить("УстановитьПраваПриложений", Ложь);
	ПраваНаПриложения = Новый ТаблицаЗначений;
	ПраваНаПриложения.Колонки.Добавить("Логин", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ПраваНаПриложения.Колонки.Добавить("Право", Новый ОписаниеТипов("ПеречислениеСсылка.ПраваПользователяПриложения"));
	Параметры.Вставить("ПраваНаПриложения", ПраваНаПриложения);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает шаблон параметров обновления приложения для метода ПрограммныйИнтерфейсСервиса.ОбновитьПриложение.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров создания приложения:
//   * КодПриложения - Число - код обновляемого приложения.
//   * Наименование - Строка - наименование приложения.
//   * ЧасовойПояс - Строка - рабочий часовой пояс приложения
//   * Пользователи - ТаблицаЗначений - устанавливаемые права на приложения:
//     ** Логин - Строка - логин (имя пользователя)
//     ** Право - ПеречислениеСсылка.ПраваПользователяПриложения - устанавливаемая роль пользователя.
Функция НовыйПараметрыОбновленияПриложения() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("КодПриложения", 0);
	Параметры.Вставить("Наименование", "");
	Параметры.Вставить("ЧасовойПояс", "");
	ПользователиПриложения = Новый ТаблицаЗначений;
	ПользователиПриложения.Колонки.Добавить("Логин", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ПользователиПриложения.Колонки.Добавить("Право", Новый ОписаниеТипов("ПеречислениеСсылка.ПраваПользователяПриложения"));
	Параметры.Вставить("Пользователи", ПользователиПриложения);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает шаблон параметров создания приложения для метода ПрограммныйИнтерфейсСервиса.СоздатьПриложениеИзФайлаВыгрузки.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров создания приложения:
//   * ИдентификаторФайла - Строка - код конфигурации (вид приложения)
//   * Наименование - Строка - наименование создаваемого приложения.
//   * ЧасовойПояс - Строка - рабочий часовой пояс приложения
//   * Пользователи - ТаблицаЗначений - пользователи для сопоставления:
//     ** Логин - Строка - логин (имя пользователя).
//     ** Право - ПеречислениеСсылка.ПраваПользователяПриложения - устанавливаемая роль пользователя.
//     ** ИдентификаторПользователя - УникальныйИдентификатор - идентификатор пользователя из файла данных.
Функция НовыйПараметрыСозданияПриложенияИзФайлаВыгрузки() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИдентификаторФайла", "");
	Параметры.Вставить("Наименование", "");
	Параметры.Вставить("ЧасовойПояс", "");
	
	СписокПользователей = Новый ТаблицаЗначений;
	СписокПользователей.Колонки.Добавить("Логин", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	СписокПользователей.Колонки.Добавить("Право", Новый ОписаниеТипов("ПеречислениеСсылка.ПраваПользователяПриложения"));
	СписокПользователей.Колонки.Добавить("ИдентификаторПользователя",
		Новый ОписаниеТипов("УникальныйИдентификатор"));
	Параметры.Вставить("Пользователи", СписокПользователей);
	
	СписокРасширений = Новый ТаблицаЗначений;
	СписокРасширений.Колонки.Добавить("УникальныйИдентификатор", Новый ОписаниеТипов("УникальныйИдентификатор"));
	СписокРасширений.Колонки.Добавить("Имя", ОбщегоНазначения.ОписаниеТипаСтрока(0));
	СписокРасширений.Колонки.Добавить("ВосстанавливатьКаркаснуюВерсию", Новый ОписаниеТипов("Булево"));
	Параметры.Вставить("Расширения", СписокРасширений);
	
	Возврат Параметры;
	
КонецФункции

// Возвращает шаблон параметров добавления пользователя в приложение для метода
// ПрограммныйИнтерфейсСервиса.ДобавитьПользователяВПриложение.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров добавления пользователя в приложение:
//	 * КодПриложения - Число - номер приложения.
//	 * Логин - Строка - логин (имя пользователя) 
//   * Право - ПеречислениеСсылка.ПраваПользователяПриложения - право пользователя на приложение в менеджере сервиса 
//
Функция НовыйПараметрыДобавленияПользователяВПриложение() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("КодПриложения", 0);
	Параметры.Вставить("Логин", "");
	Параметры.Вставить("Право", Перечисления.ПраваПользователяПриложения.ПустаяСсылка());
	
	Возврат Параметры;
	
КонецФункции

// Возвращает шаблон параметров изменения наименования приложения для метода
// ПрограммныйИнтерфейсСервиса.УстановитьНаименованиеПриложения.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров добавления пользователя в приложение:
//   * КодПриложения - Число - код приложения (номер области)
//   * НазваниеПриложения - Строка - новое наименование приложения
//
Функция НовыйПараметрыУстановкиНаименованияПриложение() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("КодПриложения", 0);
	Параметры.Вставить("НазваниеПриложения", "");
	
	Возврат Параметры;
	
КонецФункции

// Возвращает шаблон параметров изменения наименования приложения для метода
// ПрограммныйИнтерфейсСервиса.УстановитьЧасовойПоясПриложения.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров добавления пользователя в приложение:
//   * КодПриложения - Число - код приложения (номер области)
//   * ЧасовойПояс - Строка - устанавливаемый часовой пояс
//
Функция НовыйПараметрыУстановкиЧасовогоПоясаПриложения() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("КодПриложения", 0);
	Параметры.Вставить("ЧасовойПояс", "");
	
	Возврат Параметры;
	
КонецФункции

// Возвращает шаблон параметров для метода
// ПрограммныйИнтерфейсСервиса.ПользователиПриложенияАбонента.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров добавления пользователя в приложение:
//   * КодАбонента - Число - код абонента
//   * КодПриложения - Число - код приложения (номер области)
//   * Логин - Строка - фильтр по логину пользователя
//
Функция НовыйПараметрыЗапросаПользователейПриложенияАбонента() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("КодАбонента", 0);
	Параметры.Вставить("КодПриложения", 0);
	Параметры.Вставить("Логин", "");
	
	Возврат Параметры;
	
КонецФункции

// Возвращает шаблон параметров для метода
// ПрограммныйИнтерфейсСервиса.СписокРезервныхКопийПриложенияАбонента.
// 
// Возвращаемое значение:
//  Структура - шаблон параметров создания приложения:
//   * КодАбонента - Число - код абонента
//   * КодПриложения - Число - код приложения (номер области)
Функция НовыйПараметрыПолученияСпискаРезервныхКопийПриложенияАбонента() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("КодАбонента", 0);
	Параметры.Вставить("КодПриложения", 0);
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область Tariff

// Возвращает информацию о тарифе сервиса по коду тарифа.
//
// Параметры:
//  КодТарифа - Строка - код тарифа.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  Структура - информация о тарифе:
//   * Код - Строка - код тарифа
//   * Наименование - Строка - наименование тарифа
//   * ОписаниеДляОбслуживающихОрганизаций - Строка - описание тарифа для обслуживающей организации.
//   * ОписаниеДляАбонентов - ФорматированныйДокумент - описание тарифа для абонентов.
//   * ДатаНачалаДействия - Дата - дата начала действия тарифа.
//   * ДатаОкончанияДействия - Дата - дата окончания действия тарифа.
//   * ПериодДействияПродлевающейПодписки - Число - период (в днях), в течение которого действует продлевающая подписка (если разрешено продление).
//   * ПериодДействияРасширяющейПодписки - Число - период (в днях), в течение которого действует расширяющая подписка.
//   * ПериодДобавленияПродлевающейПодписки - Число - период (в днях) после завершения действия подписки, в течение которого можно создать продлевающую подписку.
//   * РасширениеТарифа - Булево - признак, что тариф является расширением
//   * ТипТарифа - ПеречислениеСсылка.ТипыТарифов - тип тарифа
//   * Платный - Булево - признак, что тариф является платным (содержит платные периоды действия)
//   * Тестовый - Булево - признак, что тариф является тестовым
//   * ЕстьУсловие - Булево - признак, что для использования тарифа требуется соблюдение условия использования.
//   * ПериодическаяОплата - Булево - признак, что тариф использует периодическую оплату
//   * ПериодичностьОплаты - Строка - код периода периодичности оплаты
//   * Услуги - ТаблицаЗначений - услуги тарифа:
//     ** Код - Строка - код услуги
//     ** Наименование - Строка - Наименование услуги
//     ** ТипУслуги - ПеречислениеСсылка.ТипыУслуг - тип услуги
//     ** Описание - Строка - Описание услуги
//     ** КоличествоЛицензий - Число - количество лицензий на услугу, включенное в тариф
//     ** КоличествоДопЛицензийРасширяющейПодписки - Число - количество лицензий на услугу, которое может быть предоставлено расширяющей подпиской
//     ** ИдентификаторПоставщика - Строка - идентификатор поставщика услуги
//     ** НаименованиеПоставщика - Строка - наименование поставщика услуги
//   * Расширения - ТаблицаЗначений - расширения тарифа:
//     ** Код - Строка - код тарифа-расширения
//     ** Наименование - Строка - наименование тарифа-расширения
//   * Конфигурации - ТаблицаЗначений - конфигурации тарифа:
//     ** Код - Строка - код конфигурации
//     ** Наименование - Строка - имя конфигурации
//     ** Описание - Строка - описание конфигурации
//   * ПериодыДействия - ТаблицаЗначений - периоды действия тарифа:
//     ** Код - Строка - код периода действия
//     ** Наименование - Строка - наименование периода действия
//     ** Сумма - Число - стоимость
//     ** Комментарий - Строка - комментарий к периоду действия
//   * ПериодыОповещенийОбОкончанииПодписки - ТаблицаЗначений - периоды оповещений об окончании подписки на тариф:
//     ** КоличествоДней - Число - количество дней, когда оповещать пользователя.
//
Функция ТарифСервиса(КодТарифа, ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ОсновныеСвойстваОтвета.КодСостояния = 200;
	
	Попытка
		Результат = ПрограммныйИнтерфейсСервисаПовтИсп.ТарифСервиса(КодТарифа);
		ОсновныеСвойстваОтвета.КодОтвета = 10200;
	Исключение
		Если ВызыватьИсключениеПриОшибке Тогда
			ВызватьИсключение;
		Иначе
			Результат = Неопределено;
			ОсновныеСвойстваОтвета.КодОтвета = 10404;
			ОсновныеСвойстваОтвета.Сообщение = ТехнологияСервиса.КраткийТекстОшибки(ИнформацияОбОшибке());
		КонецЕсли; 
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Возвращает список тарифов сервиса, доступных абоненту этого приложения.
// Параметры:
//  Отбор - см. НовыйОтборСпискаТарифов
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  ТаблицаЗначений - список тарифов сервиса:
//   * Код - Строка - код тарифа
//   * Наименование - Строка - наименование тарифа
//   * ОписаниеДляОбслуживающихОрганизаций - Строка - описание тарифа для обслуживающей организации.
//   * ОписаниеДляАбонентов - ФорматированныйДокумент - описание тарифа для абонентов.
//   * ДатаНачалаДействия - Дата - дата начала действия тарифа.
//   * ДатаОкончанияДействия - Дата - дата окончания действия тарифа.
//   * ПериодДействияПродлевающейПодписки - Число - период (в днях), в течение которого действует продлевающая подписка (если разрешено продление).
//   * ПериодДействияРасширяющейПодписки - Число - период (в днях), в течение которого действует расширяющая подписка.
//   * ПериодДобавленияПродлевающейПодписки - Число - период (в днях) после завершения действия подписки, в течение которого можно создать продлевающую подписку.
//   * РасширениеТарифа - Булево - признак, что тариф является расширением
//   * ТипТарифа - ПеречислениеСсылка.ТипыТарифов - тип тарифа
//   * Платный - Булево - признак, что тариф является платным (содержит платные периоды действия)
//   * Тестовый - Булево - признак, что тариф является тестовым
//   * ЕстьУсловие - Булево - признак, что для использования тарифа требуется соблюдение условия использования.
//   * ПериодическаяОплата - Булево - признак, что тариф использует периодическую оплату
//   * ПериодичностьОплаты - Строка - код периода периодичности оплаты
//   * ПериодыДействия - ТаблицаЗначений - периоды действия. Заполняется если передан параметр получения ПараметрПолученияПериодыДействия:
//     ** Код - Строка - код периода действия
//     ** Наименование - Строка - наименование периода действия
//     ** Сумма - Число - стоимость
//     ** Комментарий - Строка - комментарий к периоду действия
//
Функция ТарифыСервиса(Отбор = Неопределено, 
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "tariff/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Если Отбор = Неопределено Тогда
		Отбор = НовыйОтборСпискаТарифов();
	КонецЕсли; 
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ВерсияИнтерфейса = СвойстваВерсииИнтерфейса().Версия;
	
	ПолеПериодыДействия = "validity_periods";
	ПолеТариф = "tariff";
		
	Если Не ПустаяСтрока(Отбор.Наименование) Тогда
		ДанныеЗапроса.Вставить("name", Отбор.Наименование);
	КонецЕсли;
	Если ВерсияИнтерфейса >= 23 И Отбор.ДоступныеТарифы.Количество() > 0 Тогда
		ДанныеЗапроса.Вставить("available_tariffs", Отбор.ДоступныеТарифы);
	КонецЕсли;
		
	Если ВерсияИнтерфейса >= 19 Тогда
		Если Отбор.ПараметрыПолучения.Количество() > 0 Тогда
			ДанныеЗапроса.Вставить("scope", Новый Массив);
			Для Каждого Строка Из Отбор.ПараметрыПолучения Цикл
				Если Строка = ПараметрПолученияТолькоРасширения() И ВерсияИнтерфейса < 23 Тогда
					Продолжить;
				Иначе
					ДанныеЗапроса["scope"].Добавить(Строка);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке,
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
		
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ВерсияИнтерфейса >= 19 И Не Отбор.ПараметрыПолучения.Найти(ПараметрПолученияПериодыДействия()) = Неопределено Тогда
		ПереименованияПериодыДействия = Служебный.ПереименованияТарифПериодыДействия();
		Для Каждого Элемент Из ДанныеОтвета[ПолеТариф] Цикл
			Элемент[ПолеПериодыДействия] = Служебный.МассивСтруктурВТаблицуЗначений(
				Элемент[ПолеПериодыДействия], ПереименованияПериодыДействия);
		КонецЦикла; 
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияТариф();
	Результат = Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета[ПолеТариф], Переименования);
	
	Возврат Результат;
	
КонецФункции

// Возвращает шаблон отбора списка тарифов провайдера.
// 
// Возвращаемое значение:
//  Структура:
//    * Наименование - Строка - отбор по части наименования
//    * ДоступныеТарифы - Массив из Строка - отбор тарифов по кодам доступных тарифов
//    * ПараметрыПолучения - Массив из Строка - может содержать параметры получения данных, возвращаемые методами
//     - см. ПараметрПолученияПериодыДействия
//     - см. ПараметрПолученияТолькоПлатные
//     - см. ПараметрПолученияТолькоРасширения
Функция НовыйОтборСпискаТарифов() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("Наименование", "");
	Отбор.Вставить("ПараметрыПолучения", Новый Массив);
	Отбор.Вставить("ДоступныеТарифы", Новый Массив);
	
	Возврат Отбор;
	
КонецФункции

// Возвращает шаблон отбора списка тарифов обслуживающей организации.
// 
// Возвращаемое значение:
//  Структура:
//    * КодОбслуживающейОрганизации - Строка - код обслуживающей организации
//    * ДоступныеТарифы - Массив из Строка - отбор тарифов по кодам доступных тарифов
//    * ПараметрыПолучения - Массив из Строка - может содержать параметры получения данных, возвращаемые методами
//     - см. ПараметрПолученияПериодыДействия
//     - см. ПараметрПолученияТолькоРасширения
Функция НовыйОтборСпискаТарифовОбслуживающейОрганизации() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("КодОбслуживающейОрганизации", "");
	Отбор.Вставить("ПараметрыПолучения", Новый Массив);
	Отбор.Вставить("ДоступныеТарифы", Новый Массив);
	
	Возврат Отбор;
	
КонецФункции
	
// Возвращает параметр получения тарифов "Периоды действия"
// 
// Возвращаемое значение:
//  Строка 
//
Функция ПараметрПолученияПериодыДействия() Экспорт
	
	Возврат "validity_periods";
	
КонецФункции

// Возвращает параметр получения тарифов "Только платные"
// 
// Возвращаемое значение:
//  Строка 
Функция ПараметрПолученияТолькоПлатные() Экспорт
	
	Возврат "is_payable";
	
КонецФункции

// Возвращает параметр получения тарифов "Только расширения"
// 
// Возвращаемое значение:
//  Строка 
Функция ПараметрПолученияТолькоРасширения() Экспорт
	
	Возврат "is_extension";
	
КонецФункции

#КонецОбласти

#Область Subscription

// Возвращает список существующих подписок абонента текущего приложения.
//
// Параметры:
//  Отбор - см. НовыйОтборПодписокНаТарифы
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  ТаблицаЗначений - существующие подписки абонента:
//   * КодАбонента - Число - код (номер) абонента
//   * Номер - Строка - номер подписки
//   * Дата - Дата - дата регистрации подписки
//   * ТипПодписки - ПеречислениеСсылка.ТипыПодписокСервиса - тип подписки
//   * КодОбслуживающейОрганизации - Число - код (номер) абонента обслуживающей организации
//   * ДатаПодключения - Дата - дата подключения тарифа по подписке
//   * ДатаОтключения - Дата - дата отключения тарифа
//   * КодТарифа - Строка - код тарифа в подписке
//   * КодТарифаОбслуживающейОрганизации - Строка - код тарифа обслуживающей организации
//   * КодПериодаДействия - Строка - код периода действия
//   * Количество - Число - количество тарифов в подписке
//   * НомерОсновнойПодписки - Строка - номер основной подписки, если текущая подписка на тариф-расширение.
//   * НомерСчетаНаОплату - Строка - номер счета на оплату
//   * ИдентификаторСчетаНаОплату - УникальныйИдентификатор - идентификатор счета на оплату
//
Функция ПодпискиАбонента(Отбор = Неопределено,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "subscription/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	КодАбонента = КодАбонента();
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("account", КодАбонента);
	
	Если ТипЗнч(Отбор) = Тип("Структура") Тогда
		ПоляОтбора = Новый Соответствие;
		ПоляОтбора.Вставить("ТолькоАктивные", "active");
		ПоляОтбора.Вставить("ТолькоОсновные", "basic");
		ПоляОтбора.Вставить("НачалоПериода", "start_date");
		ПоляОтбора.Вставить("КонецПериода", "end_date");
		Для Каждого Элемент Из ПоляОтбора Цикл
			Если Отбор.Свойство(Элемент.Ключ) И ЗначениеЗаполнено(Отбор[Элемент.Ключ]) Тогда
				ДанныеЗапроса.Вставить(Элемент.Значение, Отбор[Элемент.Ключ]);
			КонецЕсли; 
		КонецЦикла;
	Иначе
		Отбор = НовыйОтборПодписокНаТарифы();
	КонецЕсли; 
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке,
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияПодпискаНаТариф(Ложь);
	Результат = Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.subscription, Переименования);
	
	ВерсияИнтерфейса = СвойстваВерсииИнтерфейса().Версия;
	
	УстановленыОтборы = (Отбор.ТолькоОсновные Или Отбор.ТолькоАктивные 
						Или ЗначениеЗаполнено(Отбор.НачалоПериода) Или ЗначениеЗаполнено(Отбор.КонецПериода));
						 
	ДатаСеанса = ТекущаяДатаСеанса();
	Для Индекс = 1 По Результат.Количество() Цикл
		Строка = Результат[Результат.Количество() - Индекс];
		Если ВерсияИнтерфейса < 14 И УстановленыОтборы Тогда // Обратная совместимость
			Если (Отбор.ТолькоОсновные И Не ПустаяСтрока(Строка.НомерОсновнойПодписки))
			 Или (Отбор.ТолькоАктивные И Строка.ДатаОтключения < ДатаСеанса) 
			 Или (ЗначениеЗаполнено(Отбор.НачалоПериода) И Строка.Дата < Отбор.НачалоПериода) 
			 Или (ЗначениеЗаполнено(Отбор.КонецПериода) И Строка.Дата > Отбор.КонецПериода) Тогда
				Результат.Удалить(Строка);
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		Если Строка.Количество = 0 Тогда
			Строка.Количество = 1;
		КонецЕсли; 
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Расширяет подписку абонента текущего приложения.
//
// Параметры:
//  НомерПодписки - Строка - номер основной подписки.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  Структура - свойства расширяющей подписки абонента:
//   * Номер - Строка - номер подписки
//
Функция РасширитьПодпискуАбонента(НомерПодписки,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "subscription/extend";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	КодАбонента = КодАбонента();
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("account", КодАбонента);
	ДанныеЗапроса.Вставить("id", НомерПодписки);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке,
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Новый Соответствие;
	Переименования.Вставить("id", Служебный.ОписаниеКолонки(
		"Номер", ОбщегоНазначения.ОписаниеТипаСтрока(9)));
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета, Переименования);
	
	Возврат Результат;
	
КонецФункции

// Продлевает подписку абонента текущего приложения.
//
// Параметры:
//  НомерПодписки - Строка - номер основной подписки.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  Структура - свойства продлевающей подписки абонента:
//   * Номер - Строка - номер подписки
//
Функция ПродлитьПодпискуАбонента(НомерПодписки,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "subscription/prolong";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	КодАбонента = КодАбонента();
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("account", КодАбонента);
	ДанныеЗапроса.Вставить("id", НомерПодписки);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке,
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Новый Соответствие;
	Переименования.Вставить("id", Служебный.ОписаниеКолонки(
		"Номер", ОбщегоНазначения.ОписаниеТипаСтрока(9)));
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета, Переименования);
	
	Возврат Результат;
	
КонецФункции

// Возвращает шаблон отбора подписок на тарифы обслуживаемых абонентов.
// 
// Возвращаемое значение:
//  Структура:
//   * КодОбслуживаемогоАбонента - Число - код (номер) обслуживаемого абонента 
//										   (не используется, если вызывается для метода ПодпискиАбонента)
//   * ТолькоАктивные - Булево - получение только активных подписок
//   * ТолькоОсновные - Булево - получение только основных подписок
//   * НачалоПериода - Дата - дата начала периода
//   * КонецПериода - Дата - дата конца периода
Функция НовыйОтборПодписокНаТарифы() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("КодОбслуживаемогоАбонента", 0);
	Отбор.Вставить("ТолькоАктивные", Ложь);
	Отбор.Вставить("ТолькоОсновные", Ложь);
	Отбор.Вставить("НачалоПериода", '00010101');
	Отбор.Вставить("КонецПериода", '00010101');
	
	Возврат Отбор;
	
КонецФункции

#КонецОбласти

#Область PromoCode

// Выполняет активацию указанного промо-кода для абонента текущего приложения.
//
// Параметры:
//  Промокод - Строка - используемый промокод.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - результат использования: Истина - промокод активирован, Ложь - произошла ошибка.
//
Функция ИспользоватьПромокод(Промокод,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "promo_code/activate";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	КодАбонента = КодАбонента();
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("account", КодАбонента);
	ДанныеЗапроса.Вставить("code", ПромоКод);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Выполняет активацию указанного промо-кода для абонента текущего приложения.
//
// Параметры:
//  Промокод - Строка - используемый промокод.
//  Метка - Строка - дополнительная информация о промо-коде
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - результат использования: Истина - промокод активирован, Ложь - произошла ошибка.
//
Функция ИспользоватьПромокодСМеткой(Промокод, Метка, 
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "promo_code/activate";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	КодАбонента = КодАбонента();
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("account", КодАбонента);
	ДанныеЗапроса.Вставить("code", ПромоКод);
	ДанныеЗапроса.Вставить("subid", Метка);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

#КонецОбласти

#Область Sessions

// Отправляет запрос на подготовку списка сеансов пользователей в приложениях.
//
// Параметры:
//  ИдентификаторЗапроса - Строка - уникальный идентификатор запроса на получение списка сеансов (UUID).
//  КодыПриложений - Массив из Число - коды приложений для отбора.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  Булево - Истина - признак успешного выполнения, Ложь - произошла ошибка.
Функция ПодготовитьСписокСеансов(ИдентификаторЗапроса, КодыПриложений = Неопределено,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "session/prepare_list";
	ВерсияИнтерфейса = СвойстваВерсииИнтерфейса().Версия;
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("account", КодАбонента());
	
	Если ВерсияИнтерфейса >= 27 Тогда
		ДанныеЗапроса.Вставить("request_id", ИдентификаторЗапроса);
	КонецЕсли;
	
	Если ВерсияИнтерфейса >= 27 И КодыПриложений <> Неопределено Тогда
		ДанныеЗапроса.Вставить("tenants", КодыПриложений);
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);

	Если ОсновныеСвойстваОтвета.КодОтвета = 10202 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Проверяет готовность списка сеансов пользователей в приложениях, запрошенных ранее.
//
// Параметры:
//  ИдентификаторЗапроса - Строка - уникальный идентификатор запроса на получение списка сеансов (UUID)
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  Булево - Истина - список подготовлен, Ложь - список готовится или произошла ошибка.
Функция ПроверитьСписокСеансов(ИдентификаторЗапроса,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "session/check_list";
	ВерсияИнтерфейса = СвойстваВерсииИнтерфейса().Версия;
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("account", КодАбонента());
	
	Если ВерсияИнтерфейса >= 27 Тогда
		ДанныеЗапроса.Вставить("request_id", ИдентификаторЗапроса);
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);

	Возврат ДанныеОтвета.ready;
	
КонецФункции

// Получает ранее запрошенный список сеансов пользователей в приложениях.
//
// Параметры:
//  ИдентификаторЗапроса - Строка - уникальный идентификатор запроса на получение списка сеансов (UUID)
//  КодыПриложений - Массив из Число - коды приложений для отбора.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  ТаблицаЗначений - список сеансов:
//   * НомерОбласти - Число
//   * НаименованиеОбласти - Строка
//   * ИмяПользователя - Строка
//   * НачалоСеанса - Дата
//   * НомерСеанса - Дата
//   * ИмяПриложения - Строка
//   * Спящий - Булево
//   * IPАдрес - Строка
Функция ПрочитатьСеансы(ИдентификаторЗапроса, КодыПриложений = Неопределено, ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "session/list";
	ВерсияИнтерфейса = СвойстваВерсииИнтерфейса().Версия;
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("account", КодАбонента());
	
	Если ВерсияИнтерфейса >= 27 Тогда
		ДанныеЗапроса.Вставить("request_id", ИдентификаторЗапроса);
	КонецЕсли;
	
	Если ВерсияИнтерфейса >= 27 И КодыПриложений <> Неопределено Тогда
		ДанныеЗапроса.Вставить("tenants", КодыПриложений);
	КонецЕсли;
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено ИЛИ ОсновныеСвойстваОтвета.КодОтвета = 10202 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияСеанс();
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.session, Переименования);
	
КонецФункции

// Выполняет чтение сеансов в фоне, используя методы ПодготовитьСписокСеансов, ПроверитьСписокСеансов и ПрочитатьСеансы.
// Метод нужно вызывать в фоновом задании.
//
// Параметры:
//  ИдентификаторЗапроса - Строка - уникальный идентификатор запроса на получение списка сеансов (UUID)
//  КодыПриложений - Массив из Число - коды приложений для отбора.
//  Таймаут - Число - таймаут ожидания подготовки списка сеансов.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  ТаблицаЗначений - список сеансов:
//   * НомерОбласти - Число
//   * НаименованиеОбласти - Строка
//   * ИмяПользователя - Строка
//   * НачалоСеанса - Дата
//   * НомерСеанса - Дата
//   * ИмяПриложения - Строка
//   * Спящий - Булево
//   * IPАдрес - Строка
Функция ПрочитатьСеансыВФоне(ИдентификаторЗапроса, КодыПриложений = Неопределено, Таймаут = 120) Экспорт
	
	Если ПолучитьТекущийСеансИнформационнойБазы().ПолучитьФоновоеЗадание() = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Метода можно вызывать только в фоновом задании.';
								|en = 'You can call the method only in a background job.'");
	КонецЕсли;
	
	ПодготовитьСписокСеансов(ИдентификаторЗапроса, КодыПриложений);
	
	ДатаЗавершения = ТекущаяДатаСеанса() + Таймаут;
	Пока Истина Цикл
		
		Если ПроверитьСписокСеансов(ИдентификаторЗапроса) Тогда
			Возврат ПрочитатьСеансы(ИдентификаторЗапроса);
		ИначеЕсли ТекущаяДатаСеанса() > ДатаЗавершения Тогда
			Возврат Неопределено;
		Иначе
			ОбщегоНазначенияБТС.Пауза(1);
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

// Выполняет завершение сеансов пользователей.
// 
// Параметры:
// 	КодПриложения -  Число - приложение, в котором нужно завершить сеансы
// 	НомераСеансов - Массив Из Число - номера сеансов, которые требуется завершить.
// 	Пользователь - СправочникСсылка.Пользователи - пользователь абонента,
// 	от имени которого выполняется операция или идентификатор администратора ИБ.
// 	Токен - Строка - секретный ключ, использующийся для выполнения операции от имени администратора ИБ
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
Процедура ЗавершитьСеансы(Знач КодПриложения, Знач НомераСеансов,
	Знач Пользователь = Неопределено, Знач Токен = Неопределено,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "session/terminate";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	Адрес = Служебный.АдресИсполненияВнешнегоПрограммногоИнтерфейса(Метод);
	
	Если Не ЗначениеЗаполнено(КодПриложения) Тогда
		КодПриложения = РаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("account", КодАбонента());
	ДанныеЗапроса.Вставить("tenant", КодПриложения);
	ДанныеЗапроса.Вставить("user", "ФиктивныйПользователь");
	ДанныеЗапроса.Вставить("id", НомераСеансов);
	ДанныеЗапроса.Вставить("auth", Служебный.СвойстваАвторизации(КодАбонента(), Пользователь, Токен));
	
	Если РаботаВМоделиСервиса.АутентификацияТокеномДоступаВнутреннихВызововПрограммногоИнтерфейса()
		И Токен = Неопределено Тогда
		ТокенДоступа = ДействующийТокенДоступа(Пользователь);
		Результат = ПрограммныйИнтерфейсСервисаСлужебный.ОтправитьЗапросВМенеджерСервисаПоТокенуДоступа(
			"POST", Адрес, ТокенДоступа, ДанныеЗапроса);
	Иначе
		Результат = РаботаВМоделиСервисаБТС.ОтправитьЗапросВМенеджерСервиса("POST", Адрес, ДанныеЗапроса);
	КонецЕсли;
	
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке,
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
КонецПроцедуры

#КонецОбласти

#Область Task

// Возвращает список активных задач пользователя из МС, относящихся к текущей области пользователя
// 
// Параметры:
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//   
// Возвращаемое значение:
//  ТаблицаЗначений:
//	* НомерЗадачи - Строка - номер задачи
//	* НаименованиеЗадачи - Строка - номер задачи
Функция Задачи(ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "task/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке,
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияЗадачаПользователя();
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.task, Переименования);
	
КонецФункции

// Возвращает описание задачи пользователя.
// 
// Параметры:
//  НомерЗадачи - Строка - номер задачи, для которой требуется получить данные.
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Структура:
//   * type - Строка - тип задачи
//   * author - Строка - автор задачи
//   * description - Строка - описание задачи
//   * tenant - Строка - Наименование приложения
//   * subscriber - Строка - Наименование ведущего абонента (Абонент ОО или ЛК)
//   * backup_type - Строка - вид запрашиваемой резервной копии (Для тех.поддержки или нет)
Функция СвойстваЗадачи(НомерЗадачи,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "task/info";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("id", НомерЗадачи);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке,
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеОтвета.task;

КонецФункции

// Выполнение задачи пользователя, отказ или согласование.
//
// Параметры:
//  ПараметрыЗапроса - см. НовыйПараметрыПодтвержденияЗадачи
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Булево - результат выполнения задачи: Истина - задача выполнена, Ложь - произошла ошибка.
Функция ВыполнитьЗадачу(ПараметрыЗапроса,
		ВызыватьИсключениеПриОшибке = Истина,  ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "task/execute";

	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;

	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	ДанныеЗапроса.Вставить("result", ПараметрыЗапроса.ПолученоСогласие);
	ДанныеЗапроса.Вставить("id", ПараметрыЗапроса.НомерЗадачи);
	ДанныеЗапроса.Вставить("date_access", ПараметрыЗапроса.ДатаИстеченияДоступа);
	ДанныеЗапроса.Вставить("backup_id", ПараметрыЗапроса.ИдентификаторКопии);
	ДанныеЗапроса.Вставить("backup_existing", ПараметрыЗапроса.СуществующаяКопия);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния, 
		ОсновныеСвойстваОтвета.КодОтвета, 
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ОсновныеСвойстваОтвета.КодОтвета = 10200 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Возвращает шаблон параметров подтверждения задачи.
//
// Возвращаемое значение:
//  Структура:
//   * ПолученоСогласие - Булево - признак получения согласия на предоставление доступа.
//   * НомерЗадачи - Строка - номер подтверждаемой задачи.
//   * ДатаИстеченияДоступа - Дата - дата прекращения доступа к копии.
//   * ИдентификаторКопии - Строка - идентификатор существующей копии
//   * СуществующаяКопия - Булево - признак использования существующей копии.
Функция НовыйПараметрыПодтвержденияЗадачи() Экспорт

	Параметры = Новый Структура;
	Параметры.Вставить("ПолученоСогласие", Ложь);
	Параметры.Вставить("НомерЗадачи", "");
	Параметры.Вставить("ДатаИстеченияДоступа");
	Параметры.Вставить("ИдентификаторКопии");
	Параметры.Вставить("СуществующаяКопия");

	Возврат Параметры;

КонецФункции

#КонецОбласти

#Область Extension

// Получает список расширений для сопоставления.
//
// Параметры:
//  ПараметрыЗапроса - см. НовыйПараметрыЗапросаРасширенийДляСопоставления
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//
// Возвращаемое значение:
//  ТаблицаЗначений:
//   * Имя - Строка - Имя расширения
//   * ВнешнийИдентификатор - Строка - Идентификатор во внешней системе
//   * Владелец - Число - Код абонента владельца
//   * Наименование - Строка - Наименование расширения
//   * Версия - Строка - Версия расширения
//   * ИзменяетСтруктуруДанных - Булево - Расширение расширяет данные
//
Функция РасширенияДляСопоставления(ПараметрыЗапроса,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "extension/list_for_compare";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	Если Не ПустаяСтрока(ПараметрыЗапроса.ИмяКонфигурации) Тогда
		ДанныеЗапроса.Вставить("sysname", ПараметрыЗапроса.ИмяКонфигурации);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ПараметрыЗапроса.КодВидаПриложения) Тогда
		ДанныеЗапроса.Вставить("apptype_id", ПараметрыЗапроса.КодВидаПриложения);
	КонецЕсли;
	
	ДанныеЗапроса.Вставить("sysversion", ПараметрыЗапроса.ВерсияКонфигурации);
	ДанныеЗапроса.Вставить("account", КодАбонента());
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);

	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Новый Соответствие;
	Переименования.Вставить("id", Служебный.ОписаниеКолонки("Имя", ОбщегоНазначения.ОписаниеТипаСтрока(260)));
	Переименования.Вставить("ext_system_id", Служебный.ОписаниеКолонки("ВнешнийИдентификатор", ОбщегоНазначения.ОписаниеТипаСтрока(150)));
	Переименования.Вставить("owner", Служебный.ОписаниеКолонки("Владелец", ОбщегоНазначения.ОписаниеТипаЧисло(12, 0, ДопустимыйЗнак.Неотрицательный)));
	Переименования.Вставить("description", Служебный.ОписаниеКолонки("Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(100)));
	Переименования.Вставить("version", Служебный.ОписаниеКолонки("Версия", ОбщегоНазначения.ОписаниеТипаСтрока(18)));
	Переименования.Вставить("changes_data_structure", Служебный.ОписаниеКолонки("ИзменяетСтруктуруДанных", Новый ОписаниеТипов("Булево")));
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.extension, Переименования);
	
КонецФункции

// Возвращает шаблон параметров для метода ПрограммныйИнтерфейсСервиса.РасширенияДляСопоставления.
// 
// Возвращаемое значение:
//  Структура:
//   * ИмяКонфигурации - Строка - Имя конфигурации, как оно задано в конфигураторе
//   * КодВидаПриложения - Строка - Код вида приложения
//   * ВерсияКонфигурации - Строка - Версия конфигурации
//
Функция НовыйПараметрыЗапросаРасширенийДляСопоставления() Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИмяКонфигурации", "");
	Параметры.Вставить("КодВидаПриложения", "");
	Параметры.Вставить("ВерсияКонфигурации", "");
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область Srv

#Область Files

// Получает параметры для составной загрузки.
//
// Параметры:
//  ИмяФайла - Строка 
//  РазмерФайла - Число
//  ТипФайла - Строка
//  Владелец - Произвольный
// 
// Возвращаемое значение:
//  Структура:
//    * ИдентификаторФайла - Строка
//    * Тип - Строка
//    * Адрес - Строка
//    * Заголовки - Соответствие из КлючИЗначение:
//      ** Ключ - Строка
//      ** Значение - Строка
Функция НачатьСоставнуюЗагрузку(ИмяФайла, РазмерФайла, ТипФайла, Владелец) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "files/new_multipart";
	ТипИнтерфейса = "srv";

	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод, ТипИнтерфейса);
	ДанныеЗапроса.Вставить("name", ИмяФайла);
	ДанныеЗапроса.Вставить("size", РазмерФайла);
	Если ТипФайла = "РезервнаяКопияОбласти" Тогда
		ДанныеЗапроса.Вставить("type", "tenant_backup");
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестный тип файла';
								|en = 'Unknown file type'");
	КонецЕсли;
	ДанныеЗапроса.Вставить("owner", Владелец);
	Адрес = Служебный.АдресИсполненияВнешнегоПрограммногоИнтерфейса(Метод, ТипИнтерфейса);
	Результат = РаботаВМоделиСервисаБТС.ОтправитьЗапросВМенеджерСервиса("POST", Адрес, ДанныеЗапроса);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат);
	
	Заголовки = Новый Соответствие;
	Если ДанныеОтвета.Свойство("headers") Тогда
		Для Каждого Заголовок Из ДанныеОтвета.headers Цикл
			Разделитель = СтрНайти(Заголовок, ":");
			Заголовки.Вставить(Лев(Заголовок, Разделитель - 1), Сред(Заголовок, Разделитель + 1));
		КонецЦикла;
	КонецЕсли;
	
	Ответ = Новый Структура;
	Ответ.Вставить("Тип", ДанныеОтвета.type);
	Ответ.Вставить("ИдентификаторФайла", ДанныеОтвета.file_id);
	Ответ.Вставить("Адрес", Строка(ДанныеОтвета.url));
	Ответ.Вставить("Заголовки", Заголовки);
	
	Возврат Ответ;
	
КонецФункции

// Получает параметры для новой части
//
// Параметры:
//  ИдентификаторФайла - Строка
//  НомерЧасти - Строка
// 
// Возвращаемое значение:
//  Структура:
//    * Тип - Строка
//    * Адрес - Строка
//    * Заголовки - Соответствие из КлючИЗначение:
//      ** Ключ - Строка
//      ** Значение - Строка
Функция НоваяЧасть(ИдентификаторФайла, НомерЧасти) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "files/new_part";
	ТипИнтерфейса = "srv";
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод, ТипИнтерфейса);
	ДанныеЗапроса.Вставить("file_id", ИдентификаторФайла);
	ДанныеЗапроса.Вставить("part_number", НомерЧасти);
	
	Адрес = Служебный.АдресИсполненияВнешнегоПрограммногоИнтерфейса(Метод, ТипИнтерфейса);
	Результат = РаботаВМоделиСервисаБТС.ОтправитьЗапросВМенеджерСервиса("POST", Адрес, ДанныеЗапроса);
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат);
	
	Заголовки = Новый Соответствие;
	Для Каждого Заголовок Из ДанныеОтвета.headers Цикл
		Разделитель = СтрНайти(Заголовок, ":");
		Заголовки.Вставить(Лев(Заголовок, Разделитель - 1), Сред(Заголовок, Разделитель + 1));
	КонецЦикла;
	
	Ответ = Новый Структура;
	Ответ.Вставить("Тип", ДанныеОтвета.type);
	Ответ.Вставить("Адрес", ДанныеОтвета.url);
	Ответ.Вставить("Заголовки", Заголовки);
	
	Возврат Ответ;
	
КонецФункции

// Завершает составную загрузку
//
// Параметры:
//  ИдентификаторФайла - Строка
//  Части - Массив из Строка
Процедура ЗавершитьСоставнуюЗагрузку(ИдентификаторФайла, Части) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "files/complete_multipart"; 
	ТипИнтерфейса = "srv";
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод, ТипИнтерфейса);
	ДанныеЗапроса.Вставить("file_id", ИдентификаторФайла);
	ДанныеЗапроса.Вставить("parts", Части);
	
	Адрес = Служебный.АдресИсполненияВнешнегоПрограммногоИнтерфейса(Метод, ТипИнтерфейса);
	РаботаВМоделиСервисаБТС.ОтправитьЗапросВМенеджерСервиса("POST", Адрес, ДанныеЗапроса, , Части.Количество() * 60);
	
КонецПроцедуры

// Отменяет составную загрузку
//
// Параметры:
//  ИдентификаторФайла - Строка
Процедура ОтменитьСоставнуюЗагрузку(ИдентификаторФайла) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "files/abort_multipart";
	ТипИнтерфейса = "srv";
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод, ТипИнтерфейса);
	ДанныеЗапроса.Вставить("file_id", ИдентификаторФайла);
	
	Адрес = Служебный.АдресИсполненияВнешнегоПрограммногоИнтерфейса(Метод, ТипИнтерфейса);
	РаботаВМоделиСервисаБТС.ОтправитьЗапросВМенеджерСервиса("POST", Адрес, ДанныеЗапроса);
	
КонецПроцедуры

// Читает свойства загруженного файла.
//
// Параметры:
//  ПараметрыЧтения - см. НовыйПараметрыЧтенияСвойствФайла
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
// 
// Возвращаемое значение:
//  Структура:
//   * ИмяФайла - Строка
//   * Размер - Число
//   * ИмяКонфигурации - Строка - Имя конфигурации, как оно задано в конфигураторе
//   * КодВидаПриложения - Строка
//   * НаименованиеВидаПриложения - Строка
//   * ВерсияКонфигурации - Строка
//   * Пользователи - ТаблицаЗначений:
//    ** Логин - Строка
//    ** ПолноеИмя - Строка - полное имя пользователя
//    ** ИдентификаторПользователя - Строка
//   * Расширения - ТаблицаЗначений:
//    ** Имя - Строка
//    ** Наименование - Строка
//    ** Версия - Строка
//    ** ИзменяетСтруктуруДанных - Булево
Функция СвойстваФайла(ПараметрыЧтения,
	ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "files/info";
	ТипИнтерфейса = "srv";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод, ТипИнтерфейса);
	ДанныеЗапроса.Вставить("file_id", ПараметрыЧтения.ИдентификаторФайла);
	ПараметрыПолучения = Новый Массив;
	Если ПараметрыЧтения.ПрочитатьДанныеИзФайла Тогда
		ПараметрыПолучения.Добавить("data_dump_info");
	КонецЕсли;
	ДанныеЗапроса.Вставить("scope", ПараметрыПолучения);
	
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод, ТипИнтерфейса, Ложь);
	
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке,
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Служебный.ПереименованияСвойстваФайла();
	Результат = Служебный.ПереименоватьСвойства(ДанныеОтвета.file, Переименования);
	
	Если Результат.Свойство("data_dump_info") Тогда
		
		Переименования = Служебный.ПереименованияДанныеАрхивнойКопии();
		ДанныеАрхивнойКопии = Служебный.ПереименоватьСвойства(Результат.data_dump_info, Переименования);
		Результат.Удалить("data_dump_info");
		Результат.Вставить("ДанныеАрхивнойКопии", ДанныеАрхивнойКопии);
		
		Если ДанныеАрхивнойКопии.Свойство("users") Тогда
			Переименования = Служебный.ПереименованияПользователиИзАрхива();
			ПользователиИзАрхива = Служебный.МассивСтруктурВТаблицуЗначений(ДанныеАрхивнойКопии.users, Переименования);
			ДанныеАрхивнойКопии.Удалить("users");
			ДанныеАрхивнойКопии.Вставить("Пользователи", ПользователиИзАрхива);
		КонецЕсли;
		
		Если ДанныеАрхивнойКопии.Свойство("extensions") Тогда
			Переименования = Служебный.ПереименованияРасширенияИзАрхива();
			РасширенияИзАрхива = Служебный.МассивСтруктурВТаблицуЗначений(ДанныеАрхивнойКопии.extensions, Переименования);
			ДанныеАрхивнойКопии.Удалить("extensions");
			ДанныеАрхивнойКопии.Вставить("Расширения", РасширенияИзАрхива);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает шаблон параметров чтения свойств файла для метода
// ПрограммныйИнтерфейсСервиса.СвойстваФайла
// 
// Возвращаемое значение:
//  Структура - параметры чтения свойств файла:
//   * ИдентификаторФайла - Строка
//   * ПрочитатьДанныеИзФайла - Булево
//
Функция НовыйПараметрыЧтенияСвойствФайла() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторФайла", "");
	Результат.Вставить("ПрочитатьДанныеИзФайла", Ложь);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область FAQ

// Возвращает список часто задаваемых вопросов.
// Реализует метод внешнего программного интерфейса - faq/list.
// 
// Параметры:
//  ВызыватьИсключениеПриОшибке - Булево - признак вызова исключения при ошибке получения данных.
//  ОсновныеСвойстваОтвета - Структура - возвращаемый параметр:
//   * КодСостояния - Число - код состояния ответа HTTP-сервиса.
//   * КодОтвета - Число - заполняется из ответа значением свойства "general.response".
//   * Сообщение - Строка - заполняется из ответа значением свойства "general.message".
//  
// Возвращаемое значение:
//  ТаблицаЗначений - часто задаваемые вопросы:
//   * Описание - Строка - описание вопроса.
//   * Гиперссылка - Строка - Ссылка на ответ на вопрос.
//
Функция ЧастоЗадаваемыеВопросы(ВызыватьИсключениеПриОшибке = Истина, ОсновныеСвойстваОтвета = Неопределено) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	Метод = "faq/list";
	
	Если ОсновныеСвойстваОтвета = Неопределено Тогда
		ОсновныеСвойстваОтвета = НовыйОсновныеСвойстваОтвета();
	КонецЕсли;
	
	ДанныеЗапроса = Служебный.ШаблонЗапроса(Метод);
	Результат = Служебный.ОтправитьДанныеВМенеджерСервиса(ДанныеЗапроса, Метод);
	
	ДанныеОтвета = Служебный.РезультатВыполнения(Результат, ВызыватьИсключениеПриОшибке, 
		ОсновныеСвойстваОтвета.КодСостояния,
		ОсновныеСвойстваОтвета.КодОтвета,
		ОсновныеСвойстваОтвета.Сообщение);
	
	Если ДанныеОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Переименования = Новый Соответствие;
	Переименования.Вставить("description", Служебный.ОписаниеКолонки("Описание", ОбщегоНазначения.ОписаниеТипаСтрока(100)));
	Переименования.Вставить("link", Служебный.ОписаниеКолонки("Гиперссылка", ОбщегоНазначения.ОписаниеТипаСтрока(0)));
	
	Возврат Служебный.МассивСтруктурВТаблицуЗначений(ДанныеОтвета.faq, Переименования);
	
КонецФункции

#КонецОбласти

#Область Timezone

// Возвращает список часовых поясов.
// Реализует метод внешнего программного интерфейса - timezones/list.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - часто задаваемые вопросы:
//   * Идентификатор - Строка - идентификатор часового пояса.
//   * Представление - Строка - представление часового пояса.
//   * Смещение - Число - смещение в секундах относительно универсального времени.
//   * Основной - Булево - часовой пояс задан как основной в настройках сервиса.
//
Функция ЧасовыеПояса() Экспорт
	
	Возврат ПрограммныйИнтерфейсСервисаПовтИсп.ЧасовыеПояса();
	
КонецФункции

#КонецОбласти

// Возвращает шаблон основных свойств ответа на запрос программного интерфейса.
// 
// Возвращаемое значение:
// 	Структура:
// * КодСостояния - Число - код состояния ответа вызова HTTP-сервиса.
// * КодОтвета - Число - код ответа программного интерфейса. 
// * Сообщение - Строка - сообщение программного интерфейса.
Функция НовыйОсновныеСвойстваОтвета() Экспорт
	
	Свойства = Новый Структура;
	Свойства.Вставить("КодСостояния", 0);
	Свойства.Вставить("КодОтвета", 0);
	Свойства.Вставить("Сообщение", "");

	Возврат Свойства;

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание типа значения дополнительного свойства по имени.
//
// Параметры:
//  ИмяТипа - Строка - имя типа значения дополнительного свойства.
// 
// Возвращаемое значение:
//  ОписаниеТипов - описание типа значения.
Функция ТипЗначенияДополнительногоСвойстваПоИмени(ИмяТипа) Экспорт
	
	Если ИмяТипа = "string" Тогда
		Возврат ОбщегоНазначения.ОписаниеТипаСтрока(1024);
	ИначеЕсли ИмяТипа = "decimal" Тогда
		Возврат ОбщегоНазначения.ОписаниеТипаЧисло(10);
	ИначеЕсли ИмяТипа = "date" Тогда
		Возврат ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя);
	ИначеЕсли ИмяТипа = "boolean" Тогда
		Возврат Новый ОписаниеТипов("Булево");
	ИначеЕсли ИмяТипа = "subscriber" Тогда
		Возврат ОбщегоНазначения.ОписаниеТипаЧисло(12, 0, ДопустимыйЗнак.Неотрицательный);
	ИначеЕсли ИмяТипа = "service" Тогда
		Возврат ОбщегоНазначения.ОписаниеТипаСтрока(9);
	ИначеЕсли ИмяТипа = "additional_value" Тогда
		Возврат ОбщегоНазначения.ОписаниеТипаСтрока(255);
	ИначеЕсли ИмяТипа = "additional_value_group" Тогда
		Возврат ОбщегоНазначения.ОписаниеТипаСтрока(255);
	ИначеЕсли ИмяТипа = "tariff" Тогда
		Возврат ОбщегоНазначения.ОписаниеТипаСтрока(9);
	ИначеЕсли ИмяТипа = "service_provider_tariff" Тогда
		Возврат ОбщегоНазначения.ОписаниеТипаСтрока(9);
	ИначеЕсли ИмяТипа = "user" Тогда
		Возврат ОбщегоНазначения.ОписаниеТипаСтрока(32);
	ИначеЕсли ИмяТипа = "tariff_period" Тогда
		Возврат ОбщегоНазначения.ОписаниеТипаСтрока(10);
	ИначеЕсли ИмяТипа = "subscription" Тогда
		Возврат ОбщегоНазначения.ОписаниеТипаСтрока(9);
	Иначе
		Возврат Тип("Неопределено");
	КонецЕсли;
	
КонецФункции

// Заполняет список выбора типов значений дополнительных свойств.
//
// Параметры:
//  СписокВыбора - СписокЗначений из Строка - список выбора на форме.
Процедура ЗаполнитьСписокВыбораТиповЗначенийДополнительныхСвойств(СписокВыбора) Экспорт

	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	ПредставленияТипов = Служебный.ПредставленияТиповЗначенийДополнительныхСведений();
	Для Каждого Строка Из ПредставленияТипов Цикл
		НоваяСтрока = СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
	КонецЦикла; 

КонецПроцедуры

// Заполняет условное оформление поля типа дополнительного свойства по значению.
//
// Параметры:
//  УсловноеОформление - УсловноеОформлениеКомпоновкиДанных - условное оформление формы.
//  ИмяРеквизита - Строка - имя реквизита формы
//  ИмяПоля - Строка - имя поля формы
//
Процедура УстановитьОформлениеПоляТипаДополнительногоСвойства(УсловноеОформление, ИмяРеквизита, ИмяПоля) Экспорт
	
	Служебный = ПрограммныйИнтерфейсСервисаСлужебный;
	ПредставленияТипов = Служебный.ПредставленияТиповЗначенийДополнительныхСведений();
	
	Для Каждого Элемент Из ПредставленияТипов Цикл
		ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
		
		ЭлементТекст = ЭлементУсловногоОформления.Оформление.Элементы.Найти("Text");
		ЭлементТекст.Значение = Элемент.Представление;
		ЭлементТекст.Использование = Истина;
		
		ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораДанных.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ИмяРеквизита);
		ЭлементОтбораДанных.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораДанных.ПравоеЗначение = Элемент.Значение;
		ЭлементОтбораДанных.Использование  = Истина;
		
		ЭлементОформляемогоПоля = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ЭлементОформляемогоПоля.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
		ЭлементОформляемогоПоля.Использование = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДобавитьАвторизациюАбонента(ДанныеЗапроса, КодАбонента)
	
	АвторизацияАбонента = Новый Структура("account", КодАбонента);
	ДанныеЗапроса.Вставить("auth", АвторизацияАбонента);
	
	Возврат ДанныеЗапроса;

КонецФункции

Функция КодАбонента() Экспорт
	
	КодАбонента = ПрограммныйИнтерфейсСервисаПовтИсп.КодАбонента();
	Если КодАбонента = 0 Тогда
		КодАбонента = АбонентЭтогоПриложения().Код;
	КонецЕсли;
	
	Возврат КодАбонента;
	
КонецФункции

#КонецОбласти
