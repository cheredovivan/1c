#Область ПрограммныйИнтерфейс

Функция АдресДанныхПроверкиМаркируемойПродукцииЧекККМ(ПараметрыСканирования, Знач Объект, УникальныйИдентификатор, ВидМаркируемойПродукции) Экспорт
	
	Возврат ПроверкаИПодборПродукцииИСМПУТ.АдресДанныхПроверкиМаркируемойПродукцииЧекККМ(
		ПараметрыСканирования,
		Объект,
		УникальныйИдентификатор,
		ВидМаркируемойПродукции);
	
КонецФункции

// Использовать ордерную схему при поступлении.
// 
// Параметры:
//  Склад - СправочникСсылка.Склады -
//  Дата - Неопределено, Дата -
//  МожетБытьГруппа - Булево - Может быть группа
// 
// Возвращаемое значение:
//  Булево, Произвольный - Использовать ордерную схему при поступлении
Функция ИспользоватьОрдернуюСхемуПриПоступлении(Склад, Дата = Неопределено, МожетБытьГруппа = Ложь) Экспорт
	
	Возврат СкладыСервер.ИспользоватьОрдернуюСхемуПриПоступлении(Склад, Дата, МожетБытьГруппа);
	
КонецФункции

// Использовать ордерную схему при отгрузке.
// 
// Параметры:
//  Склад - СправочникСсылка.Склады - 
//  Дата - Неопределено, Дата - 
//  МожетБытьГруппа - Булево - Может быть группа складов
// 
// Возвращаемое значение:
//  Булево, Произвольный - Использовать ордерную схему при отгрузке
Функция ИспользоватьОрдернуюСхемуПриОтгрузке(Склад, Дата = Неопределено, МожетБытьГруппа = Ложь) Экспорт
	
	Возврат СкладыСервер.ИспользоватьОрдернуюСхемуПриОтгрузке(Склад, Дата);
	
КонецФункции

// Получить данные по фактически обработанным маркам
// 
// Параметры:
// ПроверяемыйДокумент - ДокументСсылка - распоряжение ордеров.
// ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - вид продукции формы проверки
// Возвращаемое значение:
// Структура:
//	*Успешно - Булево
//	*Штрихкоды - см. ЗаполнитьТаблицуШтрихКодовДляЗагрузки.Штрихкоды
//	*ТекстОшибки - Строка
Функция КодыМаркировкиПоОрдерам(ПроверяемыйДокумент, ВидПродукции) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("Успешно",      Ложь);
	ВозвращаемоеЗначение.Вставить("Штрихкоды",    Новый Массив);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",  "");
	
	Штрихкоды = Новый Массив();
	ТекстЗапроса = "";

	Если ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
		Или ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПриемкаТоваровНаХранение")
		Или ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПоступлениеТоваровОтКлиента")
		Или ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПоступлениеТоваровОтХранителя") Тогда
		
		ТекстЗапросаОрдераПоРаспоряжению(ТекстЗапроса, Истина);
		
	ИначеЕсли ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПриемкаТоваровИСМП") Тогда 
	
		ДокументОснование = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(ПроверяемыйДокумент, "ДокументОснование");
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")
			Или ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПриемкаТоваровНаХранение")
			Или ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПоступлениеТоваровОтКлиента")
			Или ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПоступлениеТоваровОтХранителя") Тогда
			
			ПроверяемыйДокумент = ДокументОснование;
			ТекстЗапросаОрдераПоРаспоряжению(ТекстЗапроса, Истина);
			
		КонецЕсли;
			
	ИначеЕсли ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ОтгрузкаТоваровСХранения")
		Или ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту")
		Или ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПередачаТоваровХранителю")
		Или ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПеремещениеТоваров")
	Тогда
		ТекстЗапросаОрдераПоРаспоряжению(ТекстЗапроса);
	Иначе
		
		ВозвращаемоеЗначение.Успешно = Ложь;
		ВозвращаемоеЗначение.ТекстОшибки = СтрШаблон(НСтр("ru = 'На основании документа %1 
															|невозможно подобрать фактически обработанные марки';
															|en = 'Cannot select actually processed stamps from the %1 
															| document'"),
				ПроверяемыйДокумент);
				
		Возврат ВозвращаемоеЗначение;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		
		ВозвращаемоеЗначение.Успешно = Ложь;
		ВозвращаемоеЗначение.ТекстОшибки = СтрШаблон(НСтр("ru = 'На основании документа %1 
															|невозможно подобрать фактически обработанные марки';
															|en = 'Cannot select actually processed stamps from the %1 
															| document'"),
				ПроверяемыйДокумент);
				
		Возврат ВозвращаемоеЗначение;
		
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Если ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Запрос.УстановитьПараметр("Распоряжения",
			РеализацияТоваровУслугЛокализация.ПолучитьРаспоряженияПоДокументу(ПроверяемыйДокумент));
	ИначеЕсли ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПередачаТоваровХранителю") Тогда
		Запрос.УстановитьПараметр("Распоряжения",
			ПередачаТоваровХранителюЛокализация.ПолучитьРаспоряженияПоДокументу(ПроверяемыйДокумент));
	ИначеЕсли ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ОтгрузкаТоваровСХранения") Тогда
		Запрос.УстановитьПараметр("Распоряжения",
			ОтгрузкаТоваровСХраненияЛокализация.ПолучитьРаспоряженияПоДокументу(ПроверяемыйДокумент));
	Иначе
		Запрос.УстановитьПараметр("Распоряжения", ПолучитьРаспоряженияПоДокументу(ПроверяемыйДокумент));
	КонецЕсли;
	
	ТаблицаОрдеровПоРаспоряжению = Запрос.Выполнить().Выгрузить();
	Если ТаблицаОрдеровПоРаспоряжению.Количество() = 0 Тогда
		
		ВозвращаемоеЗначение.Успешно = Ложь;
		ВозвращаемоеЗначение.ТекстОшибки = СтрШаблон(НСтр("ru = 'На основании документа %1 
															|не найдены фактически обработанные марки';
															|en = 'Cannot find actually processed stamps from the %1 
															| document'"),
				ПроверяемыйДокумент);
				
		Возврат ВозвращаемоеЗначение;
		
	КонецЕсли;
	
	// Заполнение подобранной проверенной продукции. 
	// Исключить штрихкоды упаковок, отгруженные в других документах по распоряжению
	ТаблицаОтгрузокПоРаспоряжению = Новый Массив;
	Если ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПередачаТоваровХранителю")
		Или ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ОтгрузкаТоваровСХранения")
		Или ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту")
	Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТаблицаТовары.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
		|ГДЕ
		|	ТаблицаТовары.ЗаказКлиента В (&Распоряжения)
		|	И ТаблицаТовары.Ссылка <> &Ссылка";
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "РеализацияТоваровУслуг", ПроверяемыйДокумент.Метаданные().Имя);
		Запрос.УстановитьПараметр("Ссылка", ПроверяемыйДокумент);
		ТаблицаОтгрузокПоРаспоряжению = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ИначеЕсли ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТаблицаТовары.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПеремещениеТоваров.Товары КАК ТаблицаТовары
		|ГДЕ
		|	ТаблицаТовары.ЗаказНаПеремещение В (&Распоряжения)
		|	И ТаблицаТовары.Ссылка <> &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", ПроверяемыйДокумент);
		ТаблицаОтгрузокПоРаспоряжению = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	ЕстьОшибки = Ложь;
	
	Пропускаем = Новый Соответствие;
	Для Каждого Строка Из ТаблицаОтгрузокПоРаспоряжению Цикл
		
		ШтрихкодыУпаковокПоДокументу = ШтрихкодированиеИС.ВложенныеШтрихкодыУпаковокПоДокументу(
			Строка, ВидПродукции,, Истина);
		ОтметитьИспользованныеШтрихкоды(Пропускаем, ШтрихкодыУпаковокПоДокументу.ВложенныеШтрихкоды.ДеревоУпаковок.Строки);
	
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаОрдеровПоРаспоряжению Цикл
		
		ШтрихкодыУпаковокПоДокументу = ШтрихкодированиеИС.ВложенныеШтрихкодыУпаковокПоДокументу(
			Строка.Ордер, ВидПродукции,, Истина);
		
		Если ШтрихкодыУпаковокПоДокументу.ЕстьОшибки Тогда
			ВозвращаемоеЗначение.Успешно = Ложь;
			ВозвращаемоеЗначение.ТекстОшибки = ШтрихкодыУпаковокПоДокументу.ТекстОшибки;
			ЕстьОшибки = Истина;
			Прервать;
		КонецЕсли; 
		
		ЗаполнитьТаблицуШтрихКодовДляЗагрузки(ВидПродукции, Пропускаем, Штрихкоды, ШтрихкодыУпаковокПоДокументу.ВложенныеШтрихкоды.ДеревоУпаковок.Строки, "");
	
	КонецЦикла;
	
	Если Штрихкоды.Количество() = 0 Тогда
		ВозвращаемоеЗначение.Успешно = Ложь;
		ВозвращаемоеЗначение.ТекстОшибки = СтрШаблон(НСтр("ru = 'На основании документа %1 
															|не найдены фактически обработанные марки по виду продукции %2';
															|en = 'Cannot find actually processed stamps by the %2 item kind from the %1 
															|document'"),
				ПроверяемыйДокумент, ВидПродукции);
				
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Если Не ЕстьОшибки Тогда
		ВозвращаемоеЗначение.Успешно   = Истина;
		ВозвращаемоеЗначение.Штрихкоды = Штрихкоды;
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Склад группы с общей политикой учета серий.
// 
// Параметры:
//  ГруппаСкладов - СправочникСсылка.Склады - Группа складов
//  ВидМаркируемойПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид маркируемой продукции
// 
// Возвращаемое значение:
//  СправочникСсылка.Склады - склад из указанной группы складов, если по складам группы 
//   по маркируемой продукции указанного вида нет особенностей настроек серий.
Функция СкладГруппыСОбщейПолитикойУчетаСерий(ГруппаСкладов, ВидМаркируемойПродукции) Экспорт
	
	Если Не ЗначениеЗаполнено(ВидМаркируемойПродукции) Тогда
		Возврат ГруппаСкладов;
	КонецЕсли;
	ОсобенностьУчета = ИнтеграцияИСУТКлиентСервер.ОсобенностьУчетаПоВидуПродукции(ВидМаркируемойПродукции);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОсобенностьУчета", ОсобенностьУчета);
	Запрос.УстановитьПараметр("ГруппаСкладов", ГруппаСкладов);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыНоменклатуры.ВладелецСерий
	|ПОМЕСТИТЬ ВладельцыСерий
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|ГДЕ
	|	ВидыНоменклатуры.ОсобенностьУчета = &ОсобенностьУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад
	|ИЗ
	|	Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ВидыНоменклатурыПолитикиУчетаСерий
	|ГДЕ
	|	ВидыНоменклатурыПолитикиУчетаСерий.Ссылка В (ВЫБРАТЬ ВладелецСерий ИЗ ВладельцыСерий)
	|	И ВидыНоменклатурыПолитикиУчетаСерий.Склад В ИЕРАРХИИ (&ГруппаСкладов)
	|	И ВидыНоменклатурыПолитикиУчетаСерий.Склад <> (&ГруппаСкладов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Склады.Ссылка
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.Ссылка В ИЕРАРХИИ (&ГруппаСкладов)
	|	И Не Склады.ЭтоГруппа
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Склад;
	КонецЕсли;
	Возврат Справочники.Склады.ПустаяСсылка();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
// ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - вид продукции формы проверки
// ИсключаяШтрихкоды - Соответствие из Строка
// Штрихкоды - Массив из Структура:
//	*Штрихкод - Строка
//	*Количество - Строка
//	*ШтрихкодМаркиАлкогольнойПродукции - Строка
//	*ШтрихкодУпаковки - Строка
// СтрокиШК - Массив из СтрокаТаблицыЗначений
// УпаковкаВерхнегоУровня - Строка
Процедура ЗаполнитьТаблицуШтрихКодовДляЗагрузки(ВидПродукции, ИсключаяШтрихкоды, Штрихкоды, СтрокиШК, Знач УпаковкаВерхнегоУровня)
	
	Для Каждого СтрокаШК Из СтрокиШК Цикл
		
		Если ИсключаяШтрихкоды.Получить(СтрокаШК.Штрихкод) = Истина Тогда
			Продолжить;
		ИначеЕсли УпаковкаВерхнегоУровня = ""
			И ЗначениеЗаполнено(СтрокаШК.ВидПродукции)
			И СтрокаШК.ВидПродукции <> ВидПродукции
		Тогда
			Продолжить;
		КонецЕсли;
		
		ПоляСтроки = Новый Структура;
		ПоляСтроки.Вставить("Штрихкод",                          СтрокаШК.Штрихкод);
		ПоляСтроки.Вставить("Количество",                        СтрокаШК.Количество);
		ПоляСтроки.Вставить("ШтрихкодМаркиАлкогольнойПродукции", "");
		ПоляСтроки.Вставить("ШтрихкодУпаковки",                  УпаковкаВерхнегоУровня);
		Штрихкоды.Добавить(ПоляСтроки);
		
		Если СтрокаШК.Строки.Количество() > 0
			И (Не ЗначениеЗаполнено(СтрокаШК.Номенклатура)
				Или СтрокаШК.Номенклатура = СтрокаШК.Строки[0].Номенклатура)
		Тогда
			ЗаполнитьТаблицуШтрихКодовДляЗагрузки(ВидПродукции, ИсключаяШтрихкоды, Штрихкоды, СтрокаШК.Строки, СтрокаШК.Штрихкод);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
// Штрихкоды - Соответствие Из Строка
// СтрокиШК - Массив из СтрокаТаблицыЗначений
Процедура ОтметитьИспользованныеШтрихкоды(Штрихкоды, СтрокиШК)
	
	Для Каждого СтрокаШК Из СтрокиШК Цикл
		
		Штрихкоды.Вставить(СтрокаШК.Штрихкод, Истина);
		Если СтрокаШК.Строки.Количество() > 0 Тогда
			ОтметитьИспользованныеШтрихкоды(Штрихкоды, СтрокаШК.Строки);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает текст запроса остатка к оформлению.
// 
// Параметры:
//  ТекстЗапроса - Строка - текст запроса.
//  Приход - Булево - для документа поступления
//
Процедура ТекстЗапросаОрдераПоРаспоряжению(ТекстЗапроса, Приход = Ложь)
	
	Если Приход Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	Таблица.Регистратор КАК Ордер
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.Обороты(,, Регистратор, ДокументПоступления В (&Распоряжения)) КАК Таблица
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(Таблица.Регистратор) = ТИП(Документ.ПриходныйОрдерНаТовары)
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Регистратор
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.КОформлениюПоступленийПоОрдерамПриход) > 0";
		
	Иначе
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	Таблица.Регистратор КАК Ордер
		|ИЗ
		|	РегистрНакопления.ТоварыКОтгрузке.Обороты(,, Регистратор, ДокументОтгрузки В (&Распоряжения)) КАК Таблица
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(Таблица.Регистратор) = ТИП(Документ.РасходныйОрдерНаТовары)
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Регистратор
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.КОтгрузкеРасход) > 0";
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьРаспоряженияПоДокументу(ПроверяемыйДокумент)
	
	Результат = Новый Массив;
	Результат.Добавить(ПроверяемыйДокумент);
	
	Текст = "";
	Если ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ЗаказПоставщику КАК Основание ИЗ Документ.ПриобретениеТоваровУслуг.Товары КАК Товары ГДЕ Ссылка = &Ссылка
		|ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ЗаказПоставщику ИЗ Документ.ПриобретениеТоваровУслуг КАК ПриобретениеТоваровУслуг ГДЕ Ссылка = &Ссылка";
	ИначеЕсли ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		Текст = "ВЫБРАТЬ ЗаявкаНаВозвратТоваровОтКлиента КАК Основание ИЗ Документ.ВозвратТоваровОтКлиента КАК Товары ГДЕ Ссылка = &Ссылка
		|ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ЗаявкаНаВозвратТоваровОтКлиента ИЗ Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваровОтКлиента ГДЕ Ссылка = &Ссылка";
	ИначеЕсли ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПоступлениеТоваровОтХранителя") Тогда
		Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Распоряжение КАК Основание ИЗ Документ.ПоступлениеТоваровОтХранителя.Товары КАК Товары ГДЕ Ссылка = &Ссылка";
	ИначеЕсли ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПоступлениеТоваровОтКлиента") Тогда
		Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ Распоряжение КАК Основание ИЗ Документ.ПоступлениеТоваровОтКлиента.Товары КАК Товары ГДЕ Ссылка = &Ссылка
		|ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ Распоряжение ИЗ Документ.ПоступлениеТоваровОтКлиента КАК ПоступлениеТоваровОтКлиента ГДЕ Ссылка = &Ссылка";
	ИначеЕсли ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПриемкаТоваровНаХранение") Тогда
		Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ЗаказПоставщику КАК Основание ИЗ Документ.ПриемкаТоваровНаХранение.Товары КАК Товары ГДЕ Ссылка = &Ссылка
		|ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ЗаказПоставщику ИЗ Документ.ПриемкаТоваровНаХранение КАК ПриемкаТоваровНаХранение ГДЕ Ссылка = &Ссылка";
	ИначеЕсли ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ЗаказНаПеремещение КАК Основание ИЗ Документ.ПеремещениеТоваров.Товары КАК Товары ГДЕ Ссылка = &Ссылка
		|ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ЗаказНаПеремещение ИЗ Документ.ПеремещениеТоваров КАК ПеремещениеТоваров ГДЕ Ссылка = &Ссылка";
	ИначеЕсли ТипЗнч(ПроверяемыйДокумент) = Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту") Тогда
		Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ ЗаказКлиента КАК Основание ИЗ Документ.ОтгрузкаТоваровКлиенту.Товары КАК Товары ГДЕ Ссылка = &Ссылка
		|ОБЪЕДИНИТЬ ВСЕ ВЫБРАТЬ ЗаказКлиента ИЗ Документ.ОтгрузкаТоваровКлиенту КАК ОтгрузкаТоваровКлиенту ГДЕ Ссылка = &Ссылка";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Текст) Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ПроверяемыйДокумент);
		Запрос.Текст = Текст;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.Основание) Тогда
				Результат.Добавить(Выборка.Основание);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
#КонецОбласти

