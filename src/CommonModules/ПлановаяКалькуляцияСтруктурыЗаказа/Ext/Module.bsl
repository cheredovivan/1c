
#Область ПрограммныйИнтерфейс

// Обработать продукцию по заказу.
// Основная процедура разузловки продукции по динамической структуре заказов на производство.
// 
// Параметры:
//	ПараметрыРасчета - Структура - Структура параметров, содержит в том числе:
//		* ВидЦеныПлановойСтоимостиТМЦ - СправочникСсылка.ВидыЦен - Вид цены Плановой стоимости ТМЦ.
//		* ВалютаЦеныПлановойСтоимостиТМЦ - СправочникСсылка.Валюты - Валюта Плановой стоимости ТМЦ.
//		* БазоваяВалюта - СправочникСсылка.Валюты - Базовая валюта в базе.
//		* РеквизитыПК - Структура - Содержит в том числе:
//			** Дата - Дата - Дата плановой калькуляции.
//			** НачалоМесяцаДатыКалькуляции - Дата - Начало месяца плановой калькуляции.
//			** ПодразделениеДиспетчер - СправочникСсылка.СтруктураПредприятия.
//			** ВидЦены - СправочникСсылка.ВидыЦен - Вид цены плановой калькуляции.
//			** Организация - СправочникСсылка.Организации - Организация в плановой калькуляции.
//		* СтруктураДанных - Структура - Содержит ключи:
//			** Множители - ТаблицаЗначений - Множители.
//			** Движения - КоллекцияДвижений - Движения документа Плановая калькуляция.
//			** МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер передаваемых временных таблиц для запросов.
Процедура ОбработатьПродукциюПоЗаказу(ПараметрыРасчета) Экспорт
	
	Если РегистрыСведений.МножителиПартийИПолуфабрикатов.БылаОтменаПроведения(ПараметрыРасчета.Калькуляция) Тогда
		Возврат;
	КонецЕсли;
		
	СтруктураДанных = ПараметрыРасчета.СтруктураДанных;
	
	СтрокиДляРазузлования = Новый Массив();
	Для Каждого Множитель Из СтруктураДанных.Множители Цикл
		Если Множитель.РасчетЗавершен Тогда 
			Продолжить 
		КонецЕсли;
		
		Если (ТипЗнч(Множитель.ПартияСпецификацияПродукции) = Тип("ДокументСсылка.ЭтапПроизводства2_2")
				И Не ЗначениеЗаполнено(Множитель.ПартияСпецификацияПолуфабриката))
			Или (ТипЗнч(Множитель.ПартияСпецификацияПродукции) = Тип("ДокументСсылка.ЭтапПроизводства2_2")
				И ТипЗнч(Множитель.ПартияСпецификацияПолуфабриката) = Тип("ДокументСсылка.ЭтапПроизводства2_2"))
				
			// Признак дин. структуры Порядок = -1024 см. ПлановаяКалькуляция2_2.ТекстЗапросаТаблицаМножителиПартийИПолуфабрикатов
			Или Множитель.Порядок = -1024 Тогда
				СтрокиДляРазузлования.Добавить(Множитель);
				Множитель.РасчетЗавершен = Истина;
		КонецЕсли
	КонецЦикла;
	
	ПостоянныеВременныеТаблицы = Новый Массив();
	Для Каждого ВременнаяТаблица Из СтруктураДанных.МенеджерВременныхТаблиц.Таблицы Цикл
		ПостоянныеВременныеТаблицы.Добавить(ВременнаяТаблица.ПолноеИмя);
	КонецЦикла;

	НаборВалют = Константы.СоздатьНабор("ВалютаРегламентированногоУчета, ВалютаУправленческогоУчета");
	НаборВалют.Прочитать();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДанных.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Калькуляция", ПараметрыРасчета.Калькуляция);
	Запрос.МенеджерВременныхТаблиц = СтруктураДанных.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Калькуляция", ПараметрыРасчета.Калькуляция);
	Запрос.УстановитьПараметр("ПустойУИД", ПараметрыРасчета.ПустойУИД);
	Запрос.УстановитьПараметр("ПустойУИДСтрока", ПараметрыРасчета.ПустойУИДСтрока);
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.РеквизитыПК.Организация);
	Запрос.УстановитьПараметр("ВидЦеныПлановойСтоимостиТМЦ", ПараметрыРасчета.ВидЦеныПлановойСтоимостиТМЦ);
	Запрос.УстановитьПараметр("ДатаКалькуляции", ПараметрыРасчета.РеквизитыПК.ДатаКалькуляции);
	Запрос.УстановитьПараметр("Дата", ПараметрыРасчета.РеквизитыПК.Дата);
	Запрос.УстановитьПараметр("Валюта", ПараметрыРасчета.ВалютаЦеныПлановойСтоимостиТМЦ);
	Запрос.УстановитьПараметр("БазоваяВалюта", ПараметрыРасчета.БазоваяВалюта);
	Запрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоНазначениям",
		ПараметрыРасчета.УчитыватьСебестоимостьТоваровПоНазначениям);
	Запрос.УстановитьПараметр("НеРассчитанныйПериодПоОрганизации", ПараметрыРасчета.НеРассчитанныйПериодПоОрганизации);
	
	ИмяКолонкиНомеровСтрок = "К";
	ОбщегоНазначенияУТ.ПронумероватьТаблицуЗначений(СтруктураДанных.Множители, ИмяКолонкиНомеровСтрок);
	СтруктураДанных.Множители.Индексы.Добавить(ИмяКолонкиНомеровСтрок);
	ИменаКолонокТаблицыМножители = СтрШаблон("%1,%2,%3",
		ПараметрыРасчета.Множители_КолонкиГруппировки,
		ПараметрыРасчета.Множители_КолонкиСуммирования,
		ИмяКолонкиНомеровСтрок);
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(Запрос.МенеджерВременныхТаблиц,
		"Множители", СтруктураДанных.Множители.Скопировать(СтрокиДляРазузлования),
		ИменаКолонокТаблицыМножители,
		"РасчетЗавершен,ПартияВыпущена,КлючПартия");
	// В множителях есть строки с порядком -1, это СписаниеНаРасходы, 
	// после помещения таблицы во временные удалим данные строки, в регистр они не будут писаться.
	ОтборСтрок = Новый Структура("Порядок", -1);
	СтрокиКУдалению = СтруктураДанных.Множители.НайтиСтроки(ОтборСтрок);
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		СтруктураДанных.Множители.Удалить(СтрокаКУдалению);
	КонецЦикла;
	
	#Область ТекстЗапроса
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеквизитыЗаказаНаПроизводство.Ссылка КАК ЗаказНаПроизводство,
	|	РеквизитыЗаказаНаПроизводство.НачатьНеРанее КАК НачатьНеРанее,
	|	РеквизитыЗаказаНаПроизводство.Статус КАК Статус
	|ПОМЕСТИТЬ ВТЗаказыНаПроизводство
	|ИЗ
	|	Документ.ПлановаяКалькуляция2_2.ОбъектыКалькуляции КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2 КАК РеквизитыЗаказаНаПроизводство
	|		ПО ДД.Объект = РеквизитыЗаказаНаПроизводство.Ссылка
	|ГДЕ
	|	ДД.Ссылка = &Калькуляция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////	
	|ВЫБРАТЬ
	|	ЗаказыНаПроизводство.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ДД.Ссылка КАК ПартияПроизводства,
	|	ДД.Организация КАК Организация
	|ПОМЕСТИТЬ ПартииПроизводства
	|ИЗ
	|	ВТЗаказыНаПроизводство КАК ЗаказыНаПроизводство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК ДД
	|		ПО ЗаказыНаПроизводство.ЗаказНаПроизводство = ДД.Документ
	|			И ДД.Организация = &Организация
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|	
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПартииПроизводства.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ДД.Ссылка						КАК ЭтапСсылка,
	|	ДД.ПартияПроизводства			КАК ПартияПроизводства,
	|	ПартииПроизводства.Организация	КАК Организация,
	|	ДД.ПроизводствоНаСтороне ИЛИ ДД.ПроизводствоНаСтороне2_5 ИЛИ ДД.ВнутренняяПереработка КАК ПроизводствоНаСтороне,
	|	ДД.НомерСледующегоЭтапа			КАК НомерСледующегоЭтапа
	|ПОМЕСТИТЬ ВсеЭтапыПартийПроизводства
	|ИЗ
	|	ПартииПроизводства КАК ПартииПроизводства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ДД
	|		ПО ПартииПроизводства.ПартияПроизводства = ДД.ПартияПроизводства
	|			И ДД.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеЭтапыПартийПроизводства.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Назначения.Ссылка КАК НазначениеСсылка,
	|	ВсеЭтапыПартийПроизводства.ПартияПроизводства КАК ПартияПроизводства
	|
	|ПОМЕСТИТЬ НазначенияПодЭтапыЗаказаНаПроизводство
	|ИЗ Справочник.Назначения КАК Назначения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеЭтапыПартийПроизводства КАК ВсеЭтапыПартийПроизводства
	|	ПО Назначения.Заказ = ВсеЭтапыПартийПроизводства.ЭтапСсылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказНаПроизводство,
	|	НазначениеСсылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	
	|ВЫБРАТЬ
	|	ДД.ЭтапСсылка					КАК ВыпускающийЭтап,
	|	ДД.ПартияПроизводства			КАК ПартияПроизводства,
	|	ДД.Организация					КАК Организация,
	|	ДД.ПроизводствоНаСтороне		КАК ПроизводствоНаСтороне
	|ПОМЕСТИТЬ ВыпускающиеЭтапы
	|ИЗ
	|	ВсеЭтапыПартийПроизводства КАК ДД
	|ГДЕ
	|	ДД.НомерСледующегоЭтапа = 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Собираем реквизиты всех этапов, участвующих в партиях производства
	|ВЫБРАТЬ
	// Комментарий 211. Выпускающий этап это финальный этап в цепочке, который выпускает продукцию или полуфабрикат.
	|	ВыпускающиеЭтапы.ВыпускающийЭтап		КАК ВыпускающийЭтап,
	|	ВыпускающиеЭтапы.ПроизводствоНаСтороне	КАК ПроизводствоНаСтороне,
	|	РеквизитыЭП.Распоряжение		КАК ЗаказНаПроизводство,
	|	РеквизитыЭП.ПартияПроизводства	КАК ПартияПроизводства,
	|	РеквизитыЭП.Назначение			КАК Назначение,
	|	РеквизитыЭП.Этап				КАК ЭтапРесурснойСпецификации,
	// Комментарий 218. Этап это текущий этап, по которому мы можем например получать использованные ПФ или Материалы.
	|	РеквизитыЭП.Ссылка				КАК Этап,
	|	РеквизитыЭП.Дата				КАК Дата,
	|	РеквизитыЭП.Валюта				КАК Валюта,
	|	РеквизитыЭП.Организация			КАК Организация,
	|	РеквизитыЭП.Подразделение		КАК Подразделение,
	|	РеквизитыЭП.НеОтгружатьЧастями	КАК ОтгружатьОднойДатой,
	|	ВЫБОР
	|		КОГДА РеквизитыЭП.ДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РеквизитыЭП.Дата
	|		ИНАЧЕ РеквизитыЭП.ДатаОтгрузки
	|	КОНЕЦ									КАК ДатаОтгрузки,
	|	РеквизитыЭП.ВыполнениеРаботОднойДатой	КАК ВыполнениеРаботОднойДатой,
	|	РеквизитыЭП.ДатаВыполненияРабот			КАК ДатаВыполненияРабот,
	|	РеквизитыЭП.ПроизводствоОднойДатой		КАК ПроизводствоОднойДатой,
	|	ВЫБОР
	|		КОГДА РеквизитыЭП.ДатаПроизводства = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РеквизитыЭП.Дата
	|		ИНАЧЕ РеквизитыЭП.ДатаПроизводства
	|	КОНЕЦ									КАК ДатаПроизводства,
	|	РеквизитыЭП.Спецификация				КАК Спецификация,
	|	РеквизитыЭП.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен) КАК ЭтапЗавершен,
	|	РеквизитыЭП.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Формируется) КАК ЭтапФормируется,
	|	РеквизитыЭП.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
	|		И ВЫБОР КОГДА РеквизитыЭП.ПроизводствоОднойДатой
	|			ТОГДА РеквизитыЭП.ДатаПроизводства <= &ДатаКалькуляции
	|				И РеквизитыЭП.ДатаПроизводства < &НеРассчитанныйПериодПоОрганизации
	|		ИНАЧЕ
	|			РеквизитыЭП.ФактическоеОкончаниеЭтапа <= &ДатаКалькуляции
	|			И РеквизитыЭП.ФактическоеОкончаниеЭтапа < &НеРассчитанныйПериодПоОрганизации
	|	КОНЕЦ									КАК ПартияВыпущена,
	|	РеквизитыЭП.НомерСледующегоЭтапа = 0	КАК ЭтоВыпускающийЭтап,
	|	РеквизитыЭП.ФактическоеОкончаниеЭтапа	КАК ФактическоеОкончаниеЭтапа
	|	
	|ПОМЕСТИТЬ РеквизитыЭтаповПартий
	|ИЗ
	|	ВыпускающиеЭтапы КАК ВыпускающиеЭтапы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК РеквизитыЭП
	|		ПО ВыпускающиеЭтапы.ПартияПроизводства = РеквизитыЭП.ПартияПроизводства
	|			И РеквизитыЭП.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.ДатаПроизводства КАК Период,
	|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ДД.Подразделение КАК Подразделение,
	|	ДД.ВыпускающийЭтап КАК ВыпускающийЭтап,
	|	Этапы.Ссылка КАК ЭтапСсылка,
	|	ДД.Назначение КАК Назначение,
	|	ТЧУслугиПереработчика.Номенклатура КАК Номенклатура,
	|	ТЧУслугиПереработчика.Характеристика КАК Характеристика,
	|	ТЧУслугиПереработчика.Количество КАК КоличествоНаЕдиницуПартииВыпуска,
	|	ТЧУслугиПереработчика.СтатьяКалькуляции КАК СтатьяКалькуляции
	|
	|ПОМЕСТИТЬ ПроизводствоНаСторонеВПромежуточныхЭтапах
	|ИЗ
	|	РеквизитыЭтаповПартий КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
	|		ПО (ДД.ЭтапРесурснойСпецификации = Этапы.Ссылка)
	|			И (НЕ Этапы.ПометкаУдаления)
	|			И (Этапы.ПроизводствоНаСтороне)
	|			И (НЕ ДД.ПроизводствоНаСтороне)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства.УслугиПереработчика КАК ТЧУслугиПереработчика
	|		ПО (Этапы.Ссылка = ТЧУслугиПереработчика.Ссылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВыпускающийЭтап
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ИЗ
	|	ВТЗаказыНаПроизводство КАК ДД
	|";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ИндексПоследнегоЗапроса = МассивРезультатов.Количество() - 1;
	ВыборкаЗаказовНаПроизводство = МассивРезультатов[ИндексПоследнегоЗапроса].Выбрать();
	ЗаказыНаПроизводство = Новый Массив();
	Пока ВыборкаЗаказовНаПроизводство.Следующий() Цикл
		ЗаказыНаПроизводство.Добавить(ВыборкаЗаказовНаПроизводство.ЗаказНаПроизводство);
	КонецЦикла;
	СтруктураДанных.Вставить("ЗаказыНаПроизводство", ЗаказыНаПроизводство);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ДД.ЗаказНаПроизводство		КАК ЗаказНаПроизводство,
	|	ДД.ПроизводствоНаСтороне	КАК ПроизводствоНаСтороне,
	|	ДД.Подразделение 		КАК Подразделение,
	|	Изделия.Номенклатура	КАК Номенклатура,
	|	Изделия.Характеристика	КАК Характеристика,
	|	Изделия.Назначение		КАК Назначение,
	|	Изделия.Назначение.Заказ КАК ЗаказИзНазначения,
	|	Изделия.Серия			КАК Серия,
	|	ДД.Спецификация			КАК Спецификация,
	|	ВЫБОР
	|		КОГДА ДД.ПроизводствоОднойДатой
	|				ИЛИ Изделия.ДатаПроизводства = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДД.ДатаПроизводства
	|		ИНАЧЕ Изделия.ДатаПроизводства
	|	КОНЕЦ					КАК ДатаПроизводства,
	|	Изделия.Ссылка			КАК ВыпускающийЭтап,
	|	ДД.ПартияПроизводства	КАК ПартияПроизводства,
	|	Изделия.Произведено		КАК ИзделиеПроизведено,
	|	Изделия.Количество		КАК КоличествоВыпуск,
	|	ВЫБОР
	|		КОГДА Изделия.ДоляСтоимости = 0
	|			ТОГДА Изделия.Количество
	|		ИНАЧЕ Изделия.ДоляСтоимости
	|	КОНЕЦ						КАК ДоляСтоимости,
	|	Изделия.СписатьНаРасходы	КАК СписатьНаРасходы,
	|	ДД.ПартияВыпущена
	|		ИЛИ Изделия.Произведено
	|			И ВЫБОР КОГДА ДД.ПроизводствоОднойДатой
	|					ИЛИ Изделия.ДатаПроизводства = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ДД.ДатаПроизводства <= &ДатаКалькуляции
	|					И ДД.ДатаПроизводства < &НеРассчитанныйПериодПоОрганизации
	|				ИНАЧЕ Изделия.ДатаПроизводства <= &ДатаКалькуляции
	|					И Изделия.ДатаПроизводства < &НеРассчитанныйПериодПоОрганизации
	|			КОНЕЦ				КАК ПартияВыпущена
	|
	|ПОМЕСТИТЬ ВыходныеИзделияПредварительно
	|ИЗ
	|	РеквизитыЭтаповПартий КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК Изделия
	|		ПО ДД.Этап = Изделия.Ссылка
	|			И НЕ Изделия.Отменено
	//			И НЕ Изделия.СписатьНаРасходы для верного расчета всей структуры заказа на производство, 
	//			списанные изделия должны проходить по всей цепочке и исключаться только перед финальной записью множителей.
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВыпускающийЭтап,
	|	СписатьНаРасходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.ВыпускающийЭтап					КАК ВыпускающийЭтап,
	// Пока партия не выпущена затраты считаются по ТЧ обеспечение,
	// поэтому считаем знаменатель как общую долю стоимости и выпущенных и не выпущенных партий.
	|	СУММА(ДД.ДоляСтоимости)				КАК ОбщийЗнаменатель,
	|	СУММА(ВЫБОР КОГДА ДД.ПартияВыпущена
	|		ТОГДА ДД.ДоляСтоимости
	|		ИНАЧЕ 0
	|	КОНЕЦ)								КАК ЗнаменательПартияВыпущена
	|ПОМЕСТИТЬ ВыходныеИзделияИтогиДолейСтоимости
	|ИЗ
	|	ВыходныеИзделияПредварительно КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.ВыпускающийЭтап
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АВТОНОМЕРЗАПИСИ()			КАК ИдентификаторСтроки,
	|	ДД.СписатьНаРасходы			КАК СписатьНаРасходы,
	|	ДД.ЗаказНаПроизводство		КАК ЗаказНаПроизводство,
	|	ДД.ПроизводствоНаСтороне	КАК ПроизводствоНаСтороне,
	|	ДД.Подразделение			КАК Подразделение,
	|	ДД.Номенклатура				КАК Номенклатура,
	|	ДД.Характеристика			КАК Характеристика,
	|	ДД.Назначение				КАК Назначение,
	|	ДД.Серия					КАК Серия,
	|	ДД.ЗаказИзНазначения = ДД.ЗаказНаПроизводство
	|		ИЛИ ДД.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК НазначениеПустое,
	|	ДД.ЗаказИзНазначения		КАК ЗаказИзНазначения,
	|	ДД.Спецификация			КАК Спецификация,
	|	ДД.ДатаПроизводства		КАК ДатаПроизводства,
	|	ДД.ВыпускающийЭтап		КАК ВыпускающийЭтап,
	|	ДД.ПартияПроизводства	КАК ПартияПроизводства,
	|	ДД.ИзделиеПроизведено	КАК ИзделиеПроизведено,
	|	ДД.КоличествоВыпуск		КАК КоличествоВыпуск,
	|
	|	ВЫБОР КОГДА ДД.ПартияВыпущена
	|		ТОГДА 
	|			ВЫБОР КОГДА ИДС.ЗнаменательПартияВыпущена = ДД.ДоляСтоимости
	|				ТОГДА 1
	|				ИНАЧЕ ИДС.ЗнаменательПартияВыпущена
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ИДС.ОбщийЗнаменатель = ДД.ДоляСтоимости
	|				ТОГДА 1
	|				ИНАЧЕ ИДС.ОбщийЗнаменатель
	|			КОНЕЦ
	|	КОНЕЦ КАК Знаменатель,
	|
	|	ВЫБОР КОГДА ДД.ПартияВыпущена
	|		ТОГДА
	|			ВЫБОР КОГДА ДД.ДоляСтоимости = ИДС.ЗнаменательПартияВыпущена
	|				ТОГДА 1
	|				ИНАЧЕ ДД.ДоляСтоимости
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР КОГДА ДД.ДоляСтоимости = ИДС.ОбщийЗнаменатель
	|				ТОГДА 1
	|				ИНАЧЕ ДД.ДоляСтоимости
	|			КОНЕЦ
	|	КОНЕЦ КАК Числитель,
	|
	|	ДД.ПартияВыпущена КАК ПартияВыпущена
	|
	|ПОМЕСТИТЬ ВыходныеИзделия
	|ИЗ ВыходныеИзделияПредварительно КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыходныеИзделияИтогиДолейСтоимости КАК ИДС
	|		ПО ДД.ВыпускающийЭтап = ИДС.ВыпускающийЭтап
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказНаПроизводство,
	|	ВыпускающийЭтап,
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// В плановых, трудозатратах, материальных и прочих затратах нет разреза Заказ на производство,
	// поэтому в данной ВТ нет этой колонки.
	|
	|ВЫБРАТЬ
	|	ДД.Номенклатура КАК Номенклатура,
	|	ДД.Характеристика КАК Характеристика,
	|	ДД.Спецификация КАК Спецификация,
	|	ДД.Запланировать
	|		И ДД.ТипОбеспечения = ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Производство)
	// Побочный выход не должен иметь флаг Производится = ИСТИНА
	|		И ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА
	|					ИЗ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК РСВ
	|					ГДЕ РСВ.Ссылка = ДД.Спецификация
	|						И РСВ.Номенклатура = ДД.Номенклатура
	|						И (РСВ.Характеристика = ДД.Характеристика
	|							ИЛИ РСВ.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|					)
	|		КАК Производится
	|ПОМЕСТИТЬ ВТКэшНСИСтруктурыЗаказаПредварительная
	|ИЗ ВТЗаказыНаПроизводство КАК ВТЗаказыНаПроизводство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КэшНСИСтруктурыЗаказа КАК ДД
	|		ПО ВТЗаказыНаПроизводство.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.Номенклатура КАК Номенклатура,
	|	ДД.Характеристика КАК Характеристика,
	|	МАКСИМУМ(ДД.Спецификация) КАК Спецификация,
	|	МАКСИМУМ(ДД.Производится) КАК Производится
	|ПОМЕСТИТЬ ВТКэшНСИСтруктурыЗаказа
	|ИЗ ВТКэшНСИСтруктурыЗаказаПредварительная КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Номенклатура,
	|	ДД.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТКэшНСИСтруктурыЗаказаПредварительная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЗаказыНаПроизводство.НачатьНеРанее КАК Период,
	|	ДД.ВидРабот КАК ВидРабот,
	|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ДД.КлючПартия КАК КлючПартияУИД,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(36)) КАК КлючПартия,
	|	ДД.Этап КАК Этап,
	|	ДД.Спецификация КАК Спецификация,
	|	ДД.Подразделение КАК Подразделение,
	|	ДД.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ДД.Количество КАК Количество
	|
	|ИЗ ВТЗаказыНаПроизводство КАК ВТЗаказыНаПроизводство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТрудозатратыСтруктурыЗаказа КАК ДД
	|		ПО ВТЗаказыНаПроизводство.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Реквизиты.Подразделение КАК Подразделение,
	|	Множители.Период КАК Период,
	|	Реквизиты.ПроизводствоНаСтороне КАК ПроизводствоНаСтороне,
	|	Множители.Продукция КАК Продукция,
	|	Множители.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Множители.ХарактеристикаПродукции) КАК ХарактеристикаПродукцииУид,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(36)) КАК ХарактеристикаПродукцииУидСтрока,
	|	Множители.Назначение КАК НазначениеПродукции,
	|	Множители.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
	|	Реквизиты.Спецификация КАК Спецификация,
	|
	|	Множители.Полуфабрикат КАК Полуфабрикат,
	|	Множители.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
	|
	|	Множители.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
	|	Множители.Числитель КАК Числитель,
	|	Множители.Знаменатель КАК Знаменатель,
	|	Множители.Порядок = -1 КАК СписатьНаРасходы
	|
	|ИЗ Множители КАК Множители
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеквизитыЭтаповПартий КАК Реквизиты
	|	ПО Множители.ПартияСпецификацияПродукции = Реквизиты.Этап
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Множители.ПартияСпецификацияПродукции) = ТИП(Документ.ЭтапПроизводства2_2)
	|	И НЕ Множители.ПартияВыпущена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтруктураЗаказа.Склад				КАК Склад,
	|	СтруктураЗаказа.Номенклатура		КАК Номенклатура,
	|	СтруктураЗаказа.Характеристика		КАК Характеристика,
	|	ЕСТЬNULL(КэшНСИ.Производится, ЛОЖЬ)	КАК ЭтоПолуфабрикат,
	// Не обеспечиваемой работы в ВТКэшНСИСтруктурыЗаказа нет, получаем из справочника.
	|	ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА
	|				ИЗ Справочник.Номенклатура КАК СпрНоменклатура
	|				ГДЕ СпрНоменклатура.Ссылка = СтруктураЗаказа.Номенклатура
	|					И СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			) КАК ЭтоРабота,
	|	СтруктураЗаказа.Назначение			КАК Назначение,
	|	СтруктураЗаказа.ЗаказНаПроизводство	КАК ЗаказНаПроизводство,
	|	СтруктураЗаказа.КлючНоменклатура	КАК КлючНоменклатура,
	|	СтруктураЗаказа.КлючПартия			КАК КлючПартияУИД,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА (36))		КАК КлючПартия,
	|	СтруктураЗаказа.ВидСтроки			КАК ВидСтроки, 
	|	СтруктураЗаказа.Этап				КАК Этап,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(СтруктураЗаказа.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
	|			ТОГДА ИСТИНА В
	|					(ВЫБРАТЬ ПЕРВЫЕ 1
	|						ИСТИНА
	|					ИЗ
	|						Документ.ЭтапПроизводства2_2 КАК ЭП
	|					ГДЕ
	|						ЭП.Ссылка = СтруктураЗаказа.Этап
	|						И ЭП.ПроизводствоНаСтороне)
	|		ИНАЧЕ ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					Справочник.ЭтапыПроизводства КАК ЭП
	|				ГДЕ
	|					ЭП.Ссылка = СтруктураЗаказа.Этап
	|					И ЭП.ПроизводствоНаСтороне)
	|	КОНЕЦ								КАК ПроизводствоНаСтороне,
	|	ВЫБОР
	|		КОГДА СтруктураЗаказа.Спецификация = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|				И СтруктураЗаказа.ВидСтроки В
	|									(ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Зарезервировано),
	|									ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПолуфабриката))
	|			ТОГДА ЕСТЬNULL(ЭтапПроизводстваКакЗаказНаПоступление.Спецификация, 
	|					ЕСТЬNULL(КэшНСИ.Спецификация, ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)))
	|		ИНАЧЕ СтруктураЗаказа.Спецификация
	|	КОНЕЦ КАК Спецификация,
	|	СтруктураЗаказа.Подразделение		КАК Подразделение,
	|	СтруктураЗаказа.СтатьяКалькуляции	КАК СтатьяКалькуляции,
	|	ВЫБОР КОГДА СтруктураЗаказа.ДатаПоступления = ДАТАВРЕМЯ(1,1,1)
	|		ТОГДА ВТЗаказыНаПроизводство.НачатьНеРанее
	|		ИНАЧЕ СтруктураЗаказа.ДатаПоступления
	|	КОНЕЦ										КАК Период,
	|	СтруктураЗаказа.НеОбеспечивать				КАК НеОбеспечивать,
	|
	|	ВЫБОР
	|		КОГДА СтруктураЗаказа.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Зарезервировано)
	|				И СтруктураЗаказа.СпецификацияПолуфабриката = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(ЭтапПроизводстваКакЗаказНаПоступление.Спецификация,
	|					ЕСТЬNULL(КэшНСИ.Спецификация, ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)))
	|		ИНАЧЕ СтруктураЗаказа.СпецификацияПолуфабриката
	|	КОНЕЦ КАК СпецификацияПолуфабриката,
	|
	|	СтруктураЗаказа.Требуется							КАК Требуется,
	|	СтруктураЗаказа.Запланировано						КАК Запланировано,
	|	СтруктураЗаказа.РаспределеноИзЗапасовОбособленно	КАК РаспределеноИзЗапасовОбособленно,
	|	СтруктураЗаказа.РаспределеноИзЗапасов				КАК РаспределеноИзЗапасов,
	|	СтруктураЗаказа.РаспределеноИзПартийОбособленно		КАК РаспределеноИзПартийОбособленно,
	|	СтруктураЗаказа.РаспределеноИзПартий				КАК РаспределеноИзПартий,
	|	СтруктураЗаказа.Запущено							КАК Запущено,
	|	СтруктураЗаказа.Готово								КАК Готово
	|
	|
	|ИЗ
	|	ВТЗаказыНаПроизводство КАК ВТЗаказыНаПроизводство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтруктураЗаказа КАК СтруктураЗаказа
	|		ПО ВТЗаказыНаПроизводство.ЗаказНаПроизводство = СтруктураЗаказа.ЗаказНаПроизводство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводстваКакЗаказНаПоступление
	|		ПО (СтруктураЗаказа.ЗаказНаПоступление = ЭтапПроизводстваКакЗаказНаПоступление.Ссылка)
	|			И СтруктураЗаказа.ВидСтроки В
	|									(ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Зарезервировано),
	|									ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПолуфабриката))
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКэшНСИСтруктурыЗаказа КАК КэшНСИ
	|		ПО КэшНСИ.Номенклатура = СтруктураЗаказа.Номенклатура
	|			И КэшНСИ.Характеристика = СтруктураЗаказа.Характеристика
	|";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ИндексПоследнегоЗапроса = МассивРезультатов.ВГраница();
	
	#Область КонвертацияКлючПартияВСтроку
	
	// ВТКэшСтруктураЗаказаПредварительная
	ВТКэшСтруктураЗаказаПредварительная = МассивРезультатов[ИндексПоследнегоЗапроса].Выгрузить();
	Для Каждого ТекущаяСтрока Из ВТКэшСтруктураЗаказаПредварительная Цикл
		ТекущаяСтрока.КлючПартия = Строка(ТекущаяСтрока.КлючПартияУИД);
	КонецЦикла;
	
	КолонкиВременнойТаблицы = "Склад, Номенклатура, Характеристика, ЭтоПолуфабрикат, ЭтоРабота, Назначение, ЗаказНаПроизводство,
		| КлючНоменклатура, КлючПартия, ВидСтроки, Этап, ПроизводствоНаСтороне, Спецификация, Подразделение,
		| СтатьяКалькуляции, Период, НеОбеспечивать, СпецификацияПолуфабриката, Требуется, Запланировано,
		| РаспределеноИзЗапасовОбособленно, РаспределеноИзЗапасов, РаспределеноИзПартийОбособленно, РаспределеноИзПартий,
		| Запущено, Готово"; //@Query-part
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(Запрос.МенеджерВременныхТаблиц,
		"ВТКэшСтруктураЗаказаПредварительная", ВТКэшСтруктураЗаказаПредварительная, КолонкиВременнойТаблицы); //@Query-part
	
	// ПродукцияВЭтапахПартияНеВыпущена
	ПродукцияВЭтапахПартияНеВыпущена = МассивРезультатов[ИндексПоследнегоЗапроса - 1].Выгрузить();
	Для Каждого ТекущаяСтрока Из ПродукцияВЭтапахПартияНеВыпущена Цикл
		ТекущаяСтрока.ХарактеристикаПродукцииУидСтрока = Строка(ТекущаяСтрока.ХарактеристикаПродукцииУид);
	КонецЦикла;
	КолонкиВременнойТаблицы = "ЗаказНаПроизводство, Подразделение, Период, ПроизводствоНаСтороне, Продукция,
		| ХарактеристикаПродукции, ХарактеристикаПродукцииУидСтрока, НазначениеПродукции,
		| ПартияСпецификацияПродукции, Спецификация, Полуфабрикат, ХарактеристикаПолуфабриката,
		| КоличествоПолуфабриката, Числитель, Знаменатель, СписатьНаРасходы"; //@Query-part
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(Запрос.МенеджерВременныхТаблиц,
		"ПродукцияВЭтапахПартияНеВыпущена", ПродукцияВЭтапахПартияНеВыпущена, КолонкиВременнойТаблицы); //@Query-part
	
	// ВТКэшТрудозатратыСтруктурыЗаказа
	ВТКэшТрудозатратыСтруктурыЗаказа = МассивРезультатов[ИндексПоследнегоЗапроса - 2].Выгрузить();
	Для Каждого ТекущаяСтрока Из ВТКэшТрудозатратыСтруктурыЗаказа Цикл
		ТекущаяСтрока.КлючПартия = Строка(ТекущаяСтрока.КлючПартияУИД);
	КонецЦикла;
	КолонкиВременнойТаблицы = "Период, ВидРабот, ЗаказНаПроизводство, КлючПартия, Этап, Спецификация, Подразделение,
		| СтатьяКалькуляции, Количество"; //@Query-part
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(Запрос.МенеджерВременныхТаблиц,
		"ВТКэшТрудозатратыСтруктурыЗаказа", ВТКэшТрудозатратыСтруктурыЗаказа, КолонкиВременнойТаблицы); //@Query-part
	
	#КонецОбласти
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	СтруктураЗаказа.Склад				КАК Склад,
	|	СтруктураЗаказа.Номенклатура		КАК Номенклатура,
	|	СтруктураЗаказа.Характеристика		КАК Характеристика,
	|	МАКСИМУМ(СтруктураЗаказа.ЭтоПолуфабрикат) КАК ЭтоПолуфабрикат,
	|	МАКСИМУМ(СтруктураЗаказа.ЭтоРабота) КАК ЭтоРабота,
	|	СтруктураЗаказа.Назначение			КАК Назначение,
	|	СтруктураЗаказа.ЗаказНаПроизводство	КАК ЗаказНаПроизводство,
	|	СтруктураЗаказа.КлючНоменклатура	КАК КлючНоменклатура,
	|	СтруктураЗаказа.КлючПартия			КАК КлючПартия,
	|	СтруктураЗаказа.ВидСтроки			КАК ВидСтроки, 
	|	СтруктураЗаказа.Этап				КАК Этап,
	|	СтруктураЗаказа.ПроизводствоНаСтороне	КАК ПроизводствоНаСтороне,
	|	СтруктураЗаказа.Спецификация			КАК Спецификация,
	|	СтруктураЗаказа.Подразделение			КАК Подразделение,
	|	СтруктураЗаказа.СтатьяКалькуляции		КАК СтатьяКалькуляции,
	|	СтруктураЗаказа.Период					КАК Период,
	|	СтруктураЗаказа.НеОбеспечивать				КАК НеОбеспечивать,
	|	СтруктураЗаказа.СпецификацияПолуфабриката	КАК СпецификацияПолуфабриката,
	|
	|	СУММА(СтруктураЗаказа.Требуется)							КАК Требуется,
	|	СУММА(СтруктураЗаказа.Запланировано)						КАК Запланировано,
	|	СУММА(СтруктураЗаказа.РаспределеноИзЗапасовОбособленно)		КАК РаспределеноИзЗапасовОбособленно,
	|	СУММА(СтруктураЗаказа.РаспределеноИзЗапасов)				КАК РаспределеноИзЗапасов,
	|	СУММА(СтруктураЗаказа.РаспределеноИзПартийОбособленно)		КАК РаспределеноИзПартийОбособленно,
	|	СУММА(СтруктураЗаказа.РаспределеноИзПартий)					КАК РаспределеноИзПартий,
	|	СУММА(СтруктураЗаказа.Запущено)								КАК Запущено,
	|	СУММА(СтруктураЗаказа.Готово)								КАК Готово
	|
	|ПОМЕСТИТЬ ВТКэшСтруктураЗаказа
	|ИЗ ВТКэшСтруктураЗаказаПредварительная КАК СтруктураЗаказа
	// В РС СтруктураЗаказа по идентичным измерениям в ресурсах могут быть положительные и отрицательные числа,
	// Группируем, расчет работает только с положительными числами.
	|
	|СГРУППИРОВАТЬ ПО
	|	СтруктураЗаказа.Склад,
	|	СтруктураЗаказа.Номенклатура,
	|	СтруктураЗаказа.Характеристика,
	|	СтруктураЗаказа.Назначение,
	|	СтруктураЗаказа.ЗаказНаПроизводство,
	|	СтруктураЗаказа.КлючНоменклатура,
	|	СтруктураЗаказа.КлючПартия,
	|	СтруктураЗаказа.ВидСтроки,
	|	СтруктураЗаказа.Этап,
	|	СтруктураЗаказа.ПроизводствоНаСтороне,
	|	СтруктураЗаказа.Спецификация,
	|	СтруктураЗаказа.Подразделение,
	|	СтруктураЗаказа.СтатьяКалькуляции,
	|	СтруктураЗаказа.Период,
	|	СтруктураЗаказа.НеОбеспечивать,
	|	СтруктураЗаказа.СпецификацияПолуфабриката
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидСтроки,
	|	КлючНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТКэшСтруктураЗаказаПредварительная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Комментарий 636.
	// Для ключа партии может быть запланировано превышение количества выпуска ПФ, над необходимым количеством.
	// Например, если в спецификации указано минимальное количество запуска больше чем необходимо в данный момент.
	// Для того, чтобы далее отображать корректно количество необходимого материала на выпуск необходимого кол-ва ПФ,
	// создадим временную таблицу с этими случаями.
	|ВЫБРАТЬ
	|	ВЗ.КлючПартия КАК КлючПартия,
	|	ВЗ.Номенклатура КАК Номенклатура,
	|	ВЗ.Характеристика КАК Характеристика,
	|	СУММА(ВЗ.Запланировано + ВЗ.Превышение) КАК ИтогоКоличество,
	|	СУММА(ВЗ.Запланировано) КАК Запланировано,
	|	СУММА(ВЗ.Превышение) КАК Превышение
	|ПОМЕСТИТЬ ПобочныйВыпускПФ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДД.КлючПартия КАК КлючПартия,
	|		ДД.Номенклатура КАК Номенклатура,
	|		ДД.Характеристика КАК Характеристика,
	|		ДД.Запланировано КАК Запланировано,
	|		0 КАК Превышение
	|	ИЗ
	|		ВТКэшСтруктураЗаказа КАК ДД
	|	ГДЕ
	|		ДД.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПолуфабриката)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДД.КлючПартия,
	|		ДД.Номенклатура КАК Номенклатура,
	|		ДД.Характеристика КАК Характеристика,
	|		0,
	|		ДД.Запланировано
	|	ИЗ
	|		ВТКэшСтруктураЗаказа КАК ДД
	|	ГДЕ
	|		ДД.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПобочныйВыпуск)) КАК ВЗ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЗ.КлючПартия,
	|	ВЗ.Номенклатура,
	|	ВЗ.Характеристика
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЗ.Запланировано) <> 0 И
	|	СУММА(ВЗ.Превышение) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючПартия,
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.КлючПартия КАК КлючПартия,
	|	СУММА(ДД.Запущено) КАК Запущено
	|ПОМЕСТИТЬ ВТКэшСтруктураЗаказа_ЗапущеноПоКлючамПартий
	|ИЗ ВТКэшСтруктураЗаказа КАК ДД
	|ГДЕ
	|	ДД.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Запущено)
	|СГРУППИРОВАТЬ ПО
	|	ДД.КлючПартия
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючПартия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.КлючПартия КАК КлючПартия,
	|	СУММА(ДД.Запланировано) КАК Запланировано
	|ПОМЕСТИТЬ ЗапланированоПоКлючамПартиям
	|ИЗ
	|	ВТКэшСтруктураЗаказа КАК ДД
	|ГДЕ
	|	ДД.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПолуфабриката)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.КлючПартия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючПартия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОчередьРасчета.ПартияСпецификацияПродукции	КАК ПартияСпецификацияПродукции,
	|	ДД.Номенклатура								КАК Номенклатура,
	|	ДД.Характеристика							КАК Характеристика,
	|	ДД.Назначение								КАК Назначение,
	|	ДД.ЗаказНаПроизводство						КАК ЗаказНаПроизводство,
	|	ДД.КлючНоменклатура							КАК КлючНоменклатура,
	|	ДД.КлючПартия								КАК КлючПартия
	|ПОМЕСТИТЬ ПродукцияЗаказа
	|ИЗ
	|	ВТКэшСтруктураЗаказа КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОчередьРасчета КАК ОчередьРасчета
	|		ПО ДД.Номенклатура = ОчередьРасчета.Продукция
	|			И ДД.Характеристика = ОчередьРасчета.ХарактеристикаПродукции
	|			И ДД.Назначение = ОчередьРасчета.Назначение
	|ГДЕ
	|	ДД.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Продукция)
	// Если Партия спецификация или Этап это ссылка на документ ЭтапПроизводства2_2 обрабатываем по сценарию стат. заказа на производство
	|	И ДД.Этап Ссылка Справочник.ЭтапыПроизводства
	|	И ОчередьРасчета.ПартияСпецификацияПродукции ССЫЛКА Справочник.РесурсныеСпецификации
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Спецификация КАК Спецификация
	|ПОМЕСТИТЬ ИспользуемыеСпецификации
	|ИЗ
	|	ВТКэшСтруктураЗаказа КАК ДД
	|ГДЕ
	|	ДД.ВидСтроки В (ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПродукции),
	|					ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПолуфабриката))
	|	ИЛИ (ДД.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПобочныйВыпуск)
	|		И ДД.Запланировано > 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Спецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпецификацияВозвратныеОтходы.Ссылка КАК Спецификация,
	|	СпецификацияВозвратныеОтходы.Номенклатура КАК Номенклатура,
	|	СпецификацияВозвратныеОтходы.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ДинСтруктураПобочныйВыходСпецификаций
	|ИЗ ИспользуемыеСпецификации КАК ИспользуемыеСпецификации
	// В ресурсной спецификации по Номенклатура, Характеристика запрещено дублирование строк.
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВозвратныеОтходы КАК СпецификацияВозвратныеОтходы
	|		ПО ИспользуемыеСпецификации.Спецификация = СпецификацияВозвратныеОтходы.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Спецификация,
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпецификацияВыходныеИзделия.Ссылка КАК Спецификация,
	|	СпецификацияВыходныеИзделия.Номенклатура КАК Номенклатура,
	|	СпецификацияВыходныеИзделия.Характеристика КАК Характеристика,
	|	ВЫБОР КОГДА СпецификацияВыходныеИзделия.ДоляСтоимости = 0
	|		ТОГДА СпецификацияВыходныеИзделия.КоличествоУпаковок
	|		ИНАЧЕ СпецификацияВыходныеИзделия.ДоляСтоимости
	|	КОНЕЦ КАК ДоляСтоимости
	|ПОМЕСТИТЬ ДолиСтоимостиПоСпецификациям
	|ИЗ
	|	ИспользуемыеСпецификации КАК ИспользуемыеСпецификации
	// В ресурсной спецификации по Номенклатура, Характеристика запрещено дублирование строк.
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ПО ИспользуемыеСпецификации.Спецификация = СпецификацияВыходныеИзделия.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Спецификация,
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.Спецификация КАК Спецификация,
	|	СУММА(ДД.ДоляСтоимости) КАК ИтогоДоляСтоимости
	|ПОМЕСТИТЬ ОбщиеДолиСтоимостиПоСпецификациям
	|ИЗ
	|	ДолиСтоимостиПоСпецификациям КАК ДД
	|СГРУППИРОВАТЬ ПО
	|	ДД.Спецификация
	|ИНДЕКСИРОВАТЬ ПО
	|	Спецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Комментарий 680.
	// В заказе может быть побочный выпуск, если в спецификации разрешен выбор спецификации для изделий побочного выхода,
	// при этом по этой же спецификации и ключу партии может производится и основная продукция.
	// Для того, чтобы не было ложной разузловки удаляем побочный выпуск из множителей.
	// Побочный выпуск должен иметь фиксированную цену, и не должен разузловываться.
	// Добавим побочный выход с отрицательным количеством как материал.
	|ВЫБРАТЬ
	|	ДД.ЗаказНаПроизводство			КАК ЗаказНаПроизводство,
	|	ДД.Этап							КАК Этап,
	|	ДД.Период						КАК Период,
	|	ДД.Подразделение				КАК Подразделение,
	|	ДД.СтатьяКалькуляции			КАК СтатьяКалькуляции,
	|	ДД.Спецификация					КАК Спецификация,
	|	ПродукцияЗаказа.Номенклатура	КАК Номенклатура,
	|	ПродукцияЗаказа.Характеристика	КАК Характеристика,
	|	ПродукцияЗаказа.Назначение		КАК Назначение,
	|	ДД.КлючПартия					КАК КлючПартия,
	|	Множители.К						КАК К,
	|	Множители.ПартияСпецификацияПродукции	КАК ПартияСпецификацияПродукции,
	|	- Множители.КоличествоПолуфабриката		КАК Требуется,
	|	ЛОЖЬ									КАК ЭтоПолуфабрикат
	|
	|ПОМЕСТИТЬ ПобочныйВыходВЗаказеНаПроизводство
	|ИЗ
	|	ПродукцияЗаказа КАК ПродукцияЗаказа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКэшСтруктураЗаказа КАК ДД
	|		ПО (ДД.КлючНоменклатура = ПродукцияЗаказа.КлючНоменклатура)
	|			И (ДД.Спецификация = ПродукцияЗаказа.ПартияСпецификацияПродукции)
	|			И (ДД.Этап ССЫЛКА Справочник.ЭтапыПроизводства)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Множители КАК Множители
	|		ПО (ДД.КлючПартия = Множители.КлючПартия)
	|			И (Множители.РасчетЗавершен)
	|			И (НЕ Множители.ПартияВыпущена)
	|			И (ТИПЗНАЧЕНИЯ(Множители.ПартияСпецификацияПродукции) = ТИП(Справочник.РесурсныеСпецификации))
	|			И (Множители.ПартияСпецификацияПолуфабриката = НЕОПРЕДЕЛЕНО)
	|			И ПродукцияЗаказа.Номенклатура = Множители.Продукция
	|			И ПродукцияЗаказа.Характеристика = Множители.ХарактеристикаПродукции
	|			И ПродукцияЗаказа.Назначение = Множители.Назначение
	|
	|ГДЕ
	|	ИСТИНА В (
	|				ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ ДинСтруктураПобочныйВыходСпецификаций КАК ДинСтруктураПобочныйВыходСпецификаций
	|				ГДЕ ДД.Спецификация = ДинСтруктураПобочныйВыходСпецификаций.Спецификация
	|					И ДД.Номенклатура = ДинСтруктураПобочныйВыходСпецификаций.Номенклатура
	|					И ДД.Характеристика = ДинСтруктураПобочныйВыходСпецификаций.Характеристика
	|			)
	|
	|ИНДЕКСИРОВАТЬ ПО КлючПартия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.Период					КАК Период,
	|	ДД.Спецификация				КАК ПартияСпецификацияПродукции,
	|	ДД.Этап						КАК ПартияСпецификацияПолуфабриката,
	|	ДД.ПроизводствоНаСтороне	КАК ПроизводствоНаСтороне,
	|	ДД.Номенклатура			КАК Продукция,
	|	ДД.Характеристика		КАК ХарактеристикаПродукции,
	|	ДД.Назначение			КАК НазначениеПродукции,
	|	ДД.ЗаказНаПроизводство	КАК ЗаказНаПроизводство,
	|	ДД.КлючНоменклатура		КАК КлючНоменклатура,
	|	ДД.КлючПартия			КАК КлючПартия,
	|	1						КАК КоличествоЗапущено,
	|	1						КАК КоличествоТребуется,
	|	1						КАК КоличествоПоПартииСпецификации,
	|	ДД.Запланировано		КАК Запланировано,
	|
	|	ДД.Требуется - ДД.РаспределеноИзЗапасовОбособленно - ДД.РаспределеноИзЗапасов
	|		- ДД.РаспределеноИзПартийОбособленно - ДД.РаспределеноИзПартий		КАК Остаток,
	|
	|	ДолиСтоимостиПоСпецификациям.ДоляСтоимости
	|		/ ОбщиеДолиСтоимостиПоСпецификациям.ИтогоДоляСтоимости	КАК КоэффициентДолиСтоимости,
	|	ДолиСтоимостиПоСпецификациям.ДоляСтоимости					КАК Числитель,
	|	ОбщиеДолиСтоимостиПоСпецификациям.ИтогоДоляСтоимости		КАК Знаменатель
	|
	|ПОМЕСТИТЬ ПроизводствоПродукции
	|ИЗ
	|	ВТКэшСтруктураЗаказа КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПродукцияЗаказа КАК ПродукцияЗаказа
	|		ПО ДД.КлючНоменклатура = ПродукцияЗаказа.КлючНоменклатура
	|			И ДД.Спецификация = ПродукцияЗаказа.ПартияСпецификацияПродукции
	|			И ДД.Этап Ссылка Справочник.ЭтапыПроизводства
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбщиеДолиСтоимостиПоСпецификациям КАК ОбщиеДолиСтоимостиПоСпецификациям
	|		ПО ДД.Спецификация = ОбщиеДолиСтоимостиПоСпецификациям.Спецификация
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДолиСтоимостиПоСпецификациям КАК ДолиСтоимостиПоСпецификациям
	|		ПО ДД.Спецификация = ДолиСтоимостиПоСпецификациям.Спецификация
	|			И ДД.Номенклатура = ДолиСтоимостиПоСпецификациям.Номенклатура
	|			И (ДД.Характеристика = ДолиСтоимостиПоСпецификациям.Характеристика
	|				ИЛИ ДолиСтоимостиПоСпецификациям.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|	
	|ГДЕ
	|	ДД.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПродукции)
	// Комментарий 695.
	// В случае если, в спецификации, в табличной части ВыходныеИзделия указано несколько позиций продукции,
	// а в заказе на производство только часть из них,
	// не указанные в заказе позиции номенклатуры идут как ПобочныйВыпуск и будут добавлены в табличную часть
	// создаваемых этапов производства, в табличную часть ВыходныеИзделия.
	|	ИЛИ (ДД.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПобочныйВыпуск)
	|		И ДД.Запланировано > 0)
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Продукция,
	|	ХарактеристикаПродукции,
	|	НазначениеПродукции,
	|	КлючПартия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроизводствоПродукции.Период КАК Период,
	|	ПроизводствоПродукции.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Этапы.Ссылка КАК ЭтапСсылка,
	|	ПроизводствоПродукции.НазначениеПродукции КАК Назначение,
	|	ПроизводствоПродукции.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
	|	ПроизводствоПродукции.ПартияСпецификацияПродукции КАК Спецификация,
	|	Этапы.Подразделение КАК Подразделение,
	|	ПроизводствоПродукции.Продукция КАК Продукция,
	|	ПроизводствоПродукции.КлючПартия КАК КлючПартия,
	|	ТЧУслугиПереработчика.Номенклатура КАК Номенклатура,
	|	ТЧУслугиПереработчика.Характеристика КАК Характеристика,
	|	ТЧУслугиПереработчика.Количество * ПроизводствоПродукции.Запланировано КАК Количество,
	|	ТЧУслугиПереработчика.Количество КАК КоличествоНаЕдиницуПартииВыпуска,
	|	ТЧУслугиПереработчика.СтатьяКалькуляции КАК СтатьяКалькуляции
	|
	|ПОМЕСТИТЬ ДинСтруктураПроизводствоНаСторонеВПромежуточныхЭтапах
	|ИЗ
	|	ПроизводствоПродукции КАК ПроизводствоПродукции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
	|		ПО (ПроизводствоПродукции.ПартияСпецификацияПродукции = Этапы.Владелец)
	|			И (НЕ Этапы.ПометкаУдаления)
	|			И (Этапы.ПроизводствоНаСтороне)
	|			И (НЕ ПроизводствоПродукции.ПроизводствоНаСтороне)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства.УслугиПереработчика КАК ТЧУслугиПереработчика
	|		ПО (Этапы.Ссылка = ТЧУслугиПереработчика.Ссылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючПартия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ИспользуемыеСпецификации
	|";
	
	ТекстыЗапросов = Новый Массив();
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	ТекстыЗапросов.Добавить(ТекстЗапроса_ПотреблениеМатериаловИПолуфабрикатов("ПроизводствоПродукции")); //@Query-part
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДД.ВыпускающийЭтап			КАК ВыпускающийЭтап,
	|	ДД.ЗаказНаПроизводство		КАК ЗаказНаПроизводство,
	|	ДД.ПроизводствоНаСтороне	КАК ПроизводствоНаСтороне,
	|	ДД.Номенклатура				КАК Номенклатура,
	|	ДД.Характеристика			КАК Характеристика,
	|	ДД.Назначение				КАК Назначение,
	|	ДД.Спецификация				КАК Спецификация,
	|
	|	СУММА(ДД.КоличествоВыпуск)	КАК КоличествоВыпуск,
	|	СУММА(ДД.Числитель)			КАК Числитель
	|
	|ПОМЕСТИТЬ ВыходныеИзделияСводныеДанные
	|ИЗ ВыходныеИзделия КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.ВыпускающийЭтап,
	|	ДД.ЗаказНаПроизводство,
	|	ДД.ПроизводствоНаСтороне,
	|	ДД.Номенклатура,
	|	ДД.Характеристика,
	|	ДД.Назначение,
	|	ДД.Спецификация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВыпускающийЭтап,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.ПартияСпецификацияПродукции КАК Спецификация,
	|	ДД.КлючПартия КАК КлючПартия,
	|	СУММА(ДД.Числитель) КАК Знаменатель
	|ПОМЕСТИТЬ ЗнаменательИспользуемыхИзделийСпецификаций
	|ИЗ ПроизводствоПродукции КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.ПартияСпецификацияПродукции,
	|	ДД.КлючПартия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючПартия,
	|	Спецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ДД.ВыпускающийЭтап КАК ВыпускающийЭтап,
	|	ДД.ДатаПроизводства КАК ДатаПроизводства,
	|	ДД.Спецификация	КАК Спецификация,
	|	ДД.Номенклатура КАК Номенклатура,
	|	ДД.Характеристика КАК Характеристика,
	|	ДД.Назначение КАК Назначение,
	|	ДД.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ДД.Подразделение КАК Подразделение,
	|	ДД.ПроизводствоНаСтороне КАК ПроизводствоНаСтороне,
	|	ДД.ПартияВыпущена КАК ПартияВыпущена,
	|	ДД.НазначениеПустое КАК НазначениеПустое,
	|	ДД.КоличествоВыпуск КАК КоличествоВыпуск,
	|	ДД.КоличествоВыпуск КАК КоличествоВыпускОстаток,
	|	ДД.Знаменатель КАК Знаменатель,
	|	ДД.Числитель КАК Числитель,
	|	ВИСД.КоличествоВыпуск КАК КоличествоИтогоВРамкахЭтапа,
	|	ВИСД.Числитель КАК ЧислительИтогоВРамкахЭтапа
	|ИЗ
	|	ВыходныеИзделия КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыходныеИзделияСводныеДанные КАК ВИСД
	|		ПО ДД.ВыпускающийЭтап = ВИСД.ВыпускающийЭтап
	|			И ДД.Номенклатура = ВИСД.Номенклатура
	|			И ДД.Характеристика = ВИСД.Характеристика
	|			И ДД.Назначение = ВИСД.Назначение
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДД.ДатаПроизводства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Продукция заказа производится на стороне, запрос для смены принципа разузловки через спецификацию, см. 1391
	|ВЫБРАТЬ
	|	Множители.К КАК К
	|ИЗ
	|	ПроизводствоПродукции КАК ПроизводствоПродукции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Множители КАК Множители
	|		ПО (ПроизводствоПродукции.КлючПартия = Множители.КлючПартия)
	|			И (ПроизводствоПродукции.ПроизводствоНаСтороне)
	|			И (Множители.РасчетЗавершен)
	|			И (НЕ Множители.ПартияВыпущена)
	|			И (ТИПЗНАЧЕНИЯ(Множители.ПартияСпецификацияПродукции) = ТИП(Справочник.РесурсныеСпецификации))
	|			И (Множители.ПартияСпецификацияПолуфабриката = НЕОПРЕДЕЛЕНО)
	|
	|			И ПроизводствоПродукции.Продукция = Множители.Продукция
	|			И ПроизводствоПродукции.ХарактеристикаПродукции = Множители.ХарактеристикаПродукции
	|			И ПроизводствоПродукции.НазначениеПродукции = Множители.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Комментарий 781
	// В рамках одной спецификации может быть выпуск произвольного количества продукции,
	// Необходимо установить числитель и знаменатель исходя из установленных в спецификации долей стоимости.
	|ВЫБРАТЬ
	|	Множители.К КАК К,
	|	ПроизводствоПродукции.Числитель КАК Числитель,
	|	ЕСТЬNULL(ЗнаменательИспользуемыхИзделийСпецификаций.Знаменатель, ПроизводствоПродукции.Знаменатель) КАК Знаменатель
	|
	|ИЗ ПроизводствоПродукции КАК ПроизводствоПродукции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Множители КАК Множители
	|		ПО (ПроизводствоПродукции.КлючПартия = Множители.КлючПартия)
	|			И (ПроизводствоПродукции.КоэффициентДолиСтоимости <> 1)
	|			И (Множители.РасчетЗавершен)
	|			И (НЕ Множители.ПартияВыпущена)
	|			И (ТИПЗНАЧЕНИЯ(Множители.ПартияСпецификацияПродукции) = ТИП(Справочник.РесурсныеСпецификации))
	|			И (Множители.ПартияСпецификацияПолуфабриката = НЕОПРЕДЕЛЕНО)
	|
	|			И ПроизводствоПродукции.Продукция = Множители.Продукция
	|			И ПроизводствоПродукции.ХарактеристикаПродукции = Множители.ХарактеристикаПродукции
	|			И ПроизводствоПродукции.НазначениеПродукции = Множители.Назначение
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗнаменательИспользуемыхИзделийСпецификаций КАК ЗнаменательИспользуемыхИзделийСпецификаций
	|		ПО ПроизводствоПродукции.КлючПартия = ЗнаменательИспользуемыхИзделийСпецификаций.КлючПартия
	|			И Множители.ПартияСпецификацияПродукции = ЗнаменательИспользуемыхИзделийСпецификаций.Спецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// ЗнаменательИспользуемыхИзделийСпецификаций
	|ВЫБРАТЬ
	|	ДД.Спецификация КАК Спецификация,
	|	ДД.КлючПартия КАК КлючПартия,
	|	ДД.Знаменатель КАК Знаменатель
	|ИЗ ЗнаменательИспользуемыхИзделийСпецификаций КАК ДД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// См. 680.
	// УдалениеПобочногоВыходаИзМножителей
	|ВЫБРАТЬ
	|	ДД.К КАК К
	|
	|ИЗ ПобочныйВыходВЗаказеНаПроизводство КАК ДД
	|";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);	
	Запрос.Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	#КонецОбласти
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ИндексПоследнегоЗапроса = МассивРезультатов.ВГраница();
	
	// См. 680.
	УдалениеПобочногоВыходаИзМножителей =  МассивРезультатов[ИндексПоследнегоЗапроса].Выбрать();
	Пока УдалениеПобочногоВыходаИзМножителей.Следующий() Цикл
		СтрокаЗаменыКлючПартия = СтруктураДанных.Множители.Найти(УдалениеПобочногоВыходаИзМножителей.К, "К");
		СтруктураДанных.Множители.Удалить(СтрокаЗаменыКлючПартия);
	КонецЦикла;
	
	СтруктураДанных.Вставить("ЗнаменательИспользуемыхИзделийСпецификаций",
		МассивРезультатов[ИндексПоследнегоЗапроса - 1].Выгрузить());
		
	// См. Комментарий 781.
	СменаЧислителяИЗнаменателя = МассивРезультатов[ИндексПоследнегоЗапроса - 2].Выбрать();
	Пока СменаЧислителяИЗнаменателя.Следующий() Цикл
		СтрокаСменыЧислителяЗнаменателя = СтруктураДанных.Множители.Найти(СменаЧислителяИЗнаменателя.К, "К");
		СтрокаСменыЧислителяЗнаменателя.Числитель = СменаЧислителяИЗнаменателя.Числитель;
		СтрокаСменыЧислителяЗнаменателя.Знаменатель = СменаЧислителяИЗнаменателя.Знаменатель;
	КонецЦикла;
	
	//	См. Комментарий 1391.
	// Если продукция производится на стороне, снимаем галку РасчетЗавершен,
	// разузловка должна произойти по ресурсной спецификации.
	ПроизводствоПродукцииНаСтороне = МассивРезультатов[ИндексПоследнегоЗапроса - 3].Выбрать();
	Пока ПроизводствоПродукцииНаСтороне.Следующий() Цикл
		СтрокаПроизводстваНаСтороне = СтруктураДанных.Множители.Найти(ПроизводствоПродукцииНаСтороне.К, "К");
		СтрокаПроизводстваНаСтороне.РасчетЗавершен = Ложь;
	КонецЦикла;
	
	СтруктураДанных.Вставить("ВыходныеИзделия", МассивРезультатов[ИндексПоследнегоЗапроса - 4].Выгрузить());
	СтруктураДанных.ВыходныеИзделия.Индексы.Добавить("ИдентификаторСтроки");
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(Запрос.МенеджерВременныхТаблиц,
		"ПотреблениеМатериаловИПолуфабрикатов") > 0 Тогда
		РаспределитьПотреблениеПФПоВозможнымВыпускам(Запрос, ПараметрыРасчета, Ложь);
	КонецЕсли;
	
	РаспределитьПотреблениеПФПоВозможнымВыпускам(Запрос, ПараметрыРасчета, Истина);
	
	ПоместитьИспользуемыеПолуфабрикатыВВТ(Запрос.МенеджерВременныхТаблиц,
		ПараметрыРасчета.СтруктураДанных.ИспользуемыеПолуфабрикаты);
	
	ПараметрыРасчета.Вставить("ТекстЗапросаПолученияДанныхПоТекУровню", ТекстЗапроса_ПлановыеМатериальныеЗатраты());
		
	ТекстыЗапросов = Новый Массив();
	ТекстыЗапросов.Добавить(ТекстЗапроса_Полуфабрикаты(ПараметрыРасчета));
	ТекстыЗапросов.Добавить(ПараметрыРасчета.ТекстЗапросаПолученияДанныхПоТекУровню);
	// 14 ПлановыеТрудозатраты
	ТекстыЗапросов.Добавить(ТекстЗапроса_ПлановыеТрудозатраты());
	Запрос.Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц, 
		"ЗнаменательИспользуемыхИзделийСпецификаций"); //@Query-part
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(
		Запрос.МенеджерВременныхТаблиц,
		"ЗнаменательИспользуемыхИзделийСпецификаций",
		ПараметрыРасчета.СтруктураДанных.ЗнаменательИспользуемыхИзделийСпецификаций,
		"Спецификация,КлючПартия,Знаменатель",
		"КлючПартия,Спецификация");
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ИндексПоследнегоЗапроса = МассивРезультатов.ВГраница();
	
	// 10 Множители по полуфабрикатам производимым процессе.
	ВыборкаМножителей = МассивРезультатов[ИндексПоследнегоЗапроса - 2].Выбрать();
	Пока ВыборкаМножителей.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СтруктураДанных.Множители.Добавить(), ВыборкаМножителей);
	КонецЦикла;
	
	СтруктураДанных.Вставить("Порядок", 1);
	
	// 15 ВыпускиПФВРамкахЗаказа
	СтруктураДанных.Вставить("ВыпускиПФВРамкахЗаказа");
	ПеренестиДанныеИзРезультатаЗапросаВТаблицуЗначений(СтруктураДанных.ВыпускиПФВРамкахЗаказа,
		МассивРезультатов[ИндексПоследнегоЗапроса - 3], Ложь);
	
	ПеренестиДанныеИзРезультатовЗапросов(МассивРезультатов, ПараметрыРасчета);
	
	ПараметрыРасчета.Вставить("ТекстЗапроса_РазузловкаПолуфабрикатовДинСтруктура",
		ТекстЗапроса_РазузловкаПолуфабрикатовДинСтруктура(ПараметрыРасчета));

	ПараметрыРасчета.Вставить("ТекстЗапроса_Полуфабрикаты", ТекстЗапроса_Полуфабрикаты(ПараметрыРасчета));
	РазузловатьПФПроизводимыеВПроцессе(Запрос, ПараметрыРасчета);

	Если СтруктураДанных.ВыпускиПФВРамкахЗаказа.Количество() > 0 Тогда
		СтруктураДанных.ВыпускиПФВРамкахЗаказа.Свернуть("СписатьНаРасходы,ГПВЭтапе,ВыходныеИзделия_ИД,
			|ВыпускающийЭтап,ПотребляющийЭтап,ЗаказНаПроизводство,КлючПартия,
			|Период,Продукция,ХарактеристикаПродукции,НазначениеПродукции,ПартияСпецификацияПродукции,Полуфабрикат,ХарактеристикаПолуфабриката,
			|ПартияСпецификацияПолуфабриката,СпецификацияПолуфабриката,ПартияВыпущена", "КоличествоТребуется");
		ПоместитьВыпускиПФВРамкахЗаказаВВТ(Запрос, СтруктураДанных.ВыпускиПФВРамкахЗаказа);
		РазузлованиеЭтаповНаПроизводство(Запрос, ПараметрыРасчета);
	КонецЕсли;
	
	СтруктураДанных.Множители.ЗаполнитьЗначения(ПараметрыРасчета.Калькуляция, "Калькуляция");
	
	// Сворачивание по идентичным знаменателям
	// Приведение к общему знаменателю произойдет тут МножителиПартийИПолуфабрикатов.ПривестиМножителиКОбщемуЗнаменателю
	РегистрыСведений.МножителиПартийИПолуфабрикатов.СвернутьМножителиВРамкахТЗ(СтруктураДанных.Множители,
		ПараметрыРасчета);
	
	СтруктураДанных.Множители.Колонки.Добавить("Подразделение",
		Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	СтруктураДанных.Множители.ЗаполнитьЗначения(ПараметрыРасчета.РеквизитыПК.ПодразделениеДиспетчер, "Подразделение");
	
	// Удаляем данные необходимые только для этого уровня расчета
	СтруктураДанных.Удалить("ВыпускиПФВРамкахЗаказа");
	СтруктураДанных.Удалить("ВыпускающиеЭтапыПодКлючиПартий");
	СтруктураДанных.Удалить("ИспользуемыеПолуфабрикаты");
	
	ПараметрыРасчета.Удалить("ТекстЗапросаПолученияДанныхПоТекУровню");
	ПараметрыРасчета.Удалить("ТекстЗапроса_РазузловкаПолуфабрикатовДинСтруктура");
	ПараметрыРасчета.Удалить("ТекстЗапроса_РазузлованиеЭтапов");
	ПараметрыРасчета.Удалить("ТекстЗапроса_Полуфабрикаты");
	
	// Уничтожим все временные таблицы, которых нет в массиве ПостоянныеВременныеТаблицы
	ТекстыУничтоженияВременныхТаблиц = Новый Массив();
	Для Каждого ВременнаяТаблица Из СтруктураДанных.МенеджерВременныхТаблиц.Таблицы Цикл
		Если ПостоянныеВременныеТаблицы.Найти(ВременнаяТаблица.ПолноеИмя) = Неопределено Тогда
			ТекстыУничтоженияВременныхТаблиц.Добавить("УНИЧТОЖИТЬ " + ВременнаяТаблица.ПолноеИмя); //@Query-part
		КонецЕсли;
		ПостоянныеВременныеТаблицы.Добавить();
	КонецЦикла;
	
	Если ТекстыУничтоженияВременныхТаблиц.Количество() > 0 Тогда
		Запрос.Текст = СтрСоединить(ТекстыУничтоженияВременныхТаблиц,
			ОбщегоНазначения.РазделительПакетаЗапросов());
		Запрос.Выполнить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Перенести данные из результата запроса в таблицу значений.
// 
// Параметры:
//  ТаблицаЗначенийПриемник - ТаблицаЗначений - Таблица значений приемник
//  РезультатЗапросаИсточник - РезультатЗапроса - Результат запроса источник
//  ВозвращатьМассивДобавленныхСтрок - Булево - Необходимо передать истина если необходима работа со строками, после добавления.
Функция ПеренестиДанныеИзРезультатаЗапросаВТаблицуЗначений(ТаблицаЗначенийПриемник, РезультатЗапросаИсточник,
	ВозвращатьМассивДобавленныхСтрок = Ложь)
	
	МассивДобавленныхСтрок = Новый Массив();
	Если ТаблицаЗначенийПриемник = Неопределено Тогда
		ТаблицаЗначенийПриемник = РезультатЗапросаИсточник.Выгрузить();
		Если ВозвращатьМассивДобавленныхСтрок Тогда
			Для Каждого Стр Из ТаблицаЗначенийПриемник Цикл
				МассивДобавленныхСтрок.Добавить(Стр);
			КонецЦикла;
		КонецЕсли;
	Иначе
		РезЗапроса = РезультатЗапросаИсточник.Выбрать();
		Пока РезЗапроса.Следующий() Цикл
			НоваяСтрока = ТаблицаЗначенийПриемник.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, РезЗапроса);
			Если ВозвращатьМассивДобавленныхСтрок Тогда
				МассивДобавленныхСтрок.Добавить(НоваяСтрока);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ?(ВозвращатьМассивДобавленныхСтрок, МассивДобавленныхСтрок, Неопределено);
КонецФункции

// Перенести данные из результатов запросов.
// 
// Параметры:
//	МассивРезультатов - Массив из РезультатЗапроса - Массив результатов запросов.
//	ПараметрыРасчета - Структура - Содержит ключи:
//		* ВидЦеныПлановойСтоимостиТМЦ - СправочникСсылка.ВидыЦен - Вид цены Плановой стоимости ТМЦ.
//		* ВалютаЦеныПлановойСтоимостиТМЦ - СправочникСсылка.Валюты - Валюта Плановой стоимости ТМЦ.
//		* БазоваяВалюта - СправочникСсылка.Валюты - Базовая валюта в базе.
//		* РеквизитыПК - Структура - Содержит ключи:
//			* Дата - Дата - Дата плановой калькуляции.
//			* НачалоМесяцаДатыКалькуляции - Дата - Начало месяца плановой калькуляции.
//			* ПодразделениеДиспетчер - СправочникСсылка.СтруктураПредприятия.
//			* ВидЦены - СправочникСсылка.ВидыЦен - Вид цены плановой калькуляции.
//			* Организация - СправочникСсылка.Организации - Организация в плановой калькуляции.
//		* СтруктураДанных - Структура - Содержит ключи:
//			* Движения - КоллекцияДвижений - Движения документа Плановая калькуляция.
Процедура ПеренестиДанныеИзРезультатовЗапросов(МассивРезультатов, ПараметрыРасчета)
	СтруктураДанных = ПараметрыРасчета.СтруктураДанных;
	
	ИндексПоследнегоЗапроса = МассивРезультатов.Количество() - 1;

	// 13 ПлановыеМатериальныеЗатраты
	ПлановыеМатериальныеЗатраты = МассивРезультатов[ИндексПоследнегоЗапроса - 1].Выбрать();
	Пока ПлановыеМатериальныеЗатраты.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СтруктураДанных.Движения.ПлановыеМатериальныеЗатраты.Добавить(),
			ПлановыеМатериальныеЗатраты);
	КонецЦикла;
	
	// 14 ПлановыеТрудозатраты
	ПлановыеТрудозатраты = МассивРезультатов[ИндексПоследнегоЗапроса].Выбрать();
	Пока ПлановыеТрудозатраты.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СтруктураДанных.Движения.ПлановыеТрудозатраты.Добавить(), ПлановыеТрудозатраты);
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапроса_Полуфабрикаты(ПараметрыРасчета)
	ТекстыЗапросов = Новый Массив();
	ТекстЗапроса = "ВЫБРАТЬ
	|	ДД.ГПВЭтапе								КАК ГПВЭтапе,
	|	ДД.ЗаказНаПроизводство					КАК ЗаказНаПроизводство,
	|	ДД.Период								КАК Период,
	|	ДД.Продукция							КАК Продукция,
	|	ДД.ХарактеристикаПродукции				КАК ХарактеристикаПродукции,
	|	ДД.НазначениеПродукции					КАК НазначениеПродукции,
	|	ДД.ПартияСпецификацияПродукции			КАК ПартияСпецификацияПродукции,
	|	ДД.ПроизводствоНаСтороне				КАК ПроизводствоНаСтороне,
	|	ДД.Полуфабрикат							КАК Полуфабрикат,
	|	ДД.ХарактеристикаПолуфабриката			КАК ХарактеристикаПолуфабриката,
	|	ДД.СпецификацияПолуфабриката			КАК СпецификацияПолуфабриката,
	|	ДД.СпецификацияПолуфабриката			КАК ПартияСпецификацияПолуфабриката,
	|	ДД.КлючПартия							КАК КлючПартия,
	// Для разузловки по Дин структуре, берем только запланированное количество
	|	СУММА(ДД.Запланировано)					КАК КоличествоТребуется,
	|	СУММА(ДД.КоличествоЗапущено)			КАК КоличествоЗапущено,
	|	СУММА(ДД.КоличествоИзСпецификации)		КАК КоличествоПоПартииСпецификации,
	|	СУММА(ДД.РаспределеноИзЗапасов)			КАК РаспределеноИзЗапасов,
	|	МАКСИМУМ(ДД.КоэффициентДолиСтоимости)	КАК КоэффициентДолиСтоимости,
	|	СУММА(ДД.Числитель)						КАК Числитель,
	|	ДД.Знаменатель							КАК Знаменатель
	|
	|ПОМЕСТИТЬ ПФРазузловкаПоДинСтруктуре
	|ИЗ
	|	ИспользуемыеПолуфабрикаты КАК ДД
	|ГДЕ
	|	(ДД.КоличествоИзСпецификации > 0
	// см. комментарий 1155.
	|		ИЛИ ДД.РаспределеноИзЗапасов > 0)
	|	И НЕ ДД.ГПВЭтапе
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.ГПВЭтапе,
	|	ДД.ЗаказНаПроизводство,
	|	ДД.Период,
	|	ДД.Продукция,
	|	ДД.ХарактеристикаПродукции,
	|	ДД.НазначениеПродукции,
	|	ДД.ПартияСпецификацияПродукции,
	|	ДД.ПроизводствоНаСтороне,
	|	ДД.Полуфабрикат,
	|	ДД.ХарактеристикаПолуфабриката,
	|	ДД.СпецификацияПолуфабриката,
	|	ДД.ПартияСпецификацияПолуфабриката,
	|	ДД.КлючПартия,
	|	ДД.Знаменатель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПроизводствоНаСтороне
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 15 ВыпускиПФВРамкахЗаказа
	// В данной таблице собираем все ожидаемые выпуски ПФ в рамках заказа, по которым в дин. структуре нет разузловки.
	|ВЫБРАТЬ
	|	ДД.СписатьНаРасходы					КАК СписатьНаРасходы,
	|	ДД.ГПВЭтапе							КАК ГПВЭтапе,
	|	ДД.Источник_ИД						КАК ВыходныеИзделия_ИД,
	|	ДД.ПартияСпецификацияПолуфабриката	КАК ВыпускающийЭтап,
	|	ДД.ПотребляющийЭтап					КАК ПотребляющийЭтап,
	|	ДД.ЗаказНаПроизводство				КАК ЗаказНаПроизводство,
	|	ДД.КлючПартия						КАК КлючПартия,
	|	ДД.Период							КАК Период,
	|	
	|	ДД.Продукция						КАК Продукция,
	|	ДД.ХарактеристикаПродукции			КАК ХарактеристикаПродукции,
	|	ДД.НазначениеПродукции				КАК НазначениеПродукции,
	|	ДД.ПартияСпецификацияПродукции		КАК ПартияСпецификацияПродукции,
	|
	|	ДД.Полуфабрикат						КАК Полуфабрикат,
	|	ДД.ХарактеристикаПолуфабриката		КАК ХарактеристикаПолуфабриката,
	|
	|	ДД.КоличествоИзЭтапа				КАК КоличествоТребуется,
	|	ДД.ПартияСпецификацияПолуфабриката	КАК ПартияСпецификацияПолуфабриката,
	|	ДД.СпецификацияПолуфабриката		КАК СпецификацияПолуфабриката,
	|	ДД.ПартияВыпущена					КАК ПартияВыпущена
	|
	|ИЗ ИспользуемыеПолуфабрикаты КАК ДД
	|
	|ГДЕ
	|	ДД.КоличествоИзЭтапа > 0
	//	см. комментарий 1391
	|	И НЕ ДД.ПроизводствоНаСтороне
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 10 Множители по полуфабрикатам производимым процессе.
	|ВЫБРАТЬ
	|	ДД.ЗаказНаПроизводство				КАК ЗаказНаПроизводство,
	|	ДД.Период							КАК Период,
	|	ДД.Продукция 						КАК Продукция,
	|	ДД.ХарактеристикаПродукции			КАК ХарактеристикаПродукции,
	|	ДД.НазначениеПродукции				КАК Назначение,
	|	ДД.ПартияСпецификацияПродукции		КАК ПартияСпецификацияПродукции,
	|	ДД.ПроизводствоНаСтороне			КАК ПроизводствоНаСтороне,
	|	ДД.Полуфабрикат 					КАК Полуфабрикат,
	|	ДД.ХарактеристикаПолуфабриката		КАК ХарактеристикаПолуфабриката,
	|	ВЫБОР
	|		КОГДА ДД.ПроизводствоНаСтороне
	//			см. комментарий 1391
	|			ТОГДА ДД.СпецификацияПолуфабриката
	|		ИНАЧЕ ДД.ПартияСпецификацияПолуфабриката
	|	КОНЕЦ								КАК ПартияСпецификацияПолуфабриката,
	|	ДД.КоличествоПоПартииСпецификации	КАК КоличествоПолуфабриката,
	//	см. комментарий 1391
	|	НЕ ДД.ПроизводствоНаСтороне				КАК РасчетЗавершен,
	|	ЛОЖЬ 									КАК ПартияВыпущена,
	|	1										КАК Порядок,
	|	ДД.Числитель							КАК Числитель,
	|	ЕСТЬNULL(ЗнаменательИспользуемыхИзделийСпецификаций.Знаменатель, 1) КАК Знаменатель,
	|	""МножителиПФПроизводимыхВПроцессе"" КАК Комментарий,
	|
	|	&ПустойУИД КАК КлючНоменклатура,
	|	ДД.КлючПартия КАК КлючПартия
	|ИЗ
	|	ПФРазузловкаПоДинСтруктуре КАК ДД
	|	ЛЕВОЕ СОЕДИНЕНИЕ ЗнаменательИспользуемыхИзделийСпецификаций КАК ЗнаменательИспользуемыхИзделийСпецификаций
	|	ПО ДД.КлючПартия = ЗнаменательИспользуемыхИзделийСпецификаций.КлючПартия
	|		И &УсловиеСоединения
	|ГДЕ
	|	ДД.КоличествоПоПартииСпецификации > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.ЗаказНаПроизводство				КАК ЗаказНаПроизводство,
	|	ДД.Период							КАК Период,
	|	ДД.Продукция 						КАК Продукция,
	|	ДД.ХарактеристикаПродукции			КАК ХарактеристикаПродукции,
	|	ДД.НазначениеПродукции				КАК Назначение,
	|	ДД.ПартияСпецификацияПродукции		КАК ПартияСпецификацияПродукции,
	|	ДД.ПроизводствоНаСтороне			КАК ПроизводствоНаСтороне,
	|	ДД.Полуфабрикат 					КАК Полуфабрикат,
	|	ДД.ХарактеристикаПолуфабриката		КАК ХарактеристикаПолуфабриката,
	|	ВЫБОР
	|		КОГДА ДД.ПроизводствоНаСтороне
	//			см. комментарий 1391
	|			ТОГДА ДД.СпецификацияПолуфабриката
	|		ИНАЧЕ ДД.ПартияСпецификацияПолуфабриката
	|	КОНЕЦ								КАК ПартияСпецификацияПолуфабриката,
	|	ДД.РаспределеноИзЗапасов			КАК КоличествоПолуфабриката,
	|	ЛОЖЬ								КАК РасчетЗавершен,
	|	ЛОЖЬ 									КАК ПартияВыпущена,
	|	1										КАК Порядок,
	|	ДД.Числитель							КАК Числитель,
	|	ЕСТЬNULL(ЗнаменательИспользуемыхИзделийСпецификаций.Знаменатель, 1) КАК Знаменатель,
	|	""МножителиПФПроизводимыхВПроцессе""	КАК Комментарий,
	|
	|	&ПустойУИД КАК КлючНоменклатура,
	|	ДД.КлючПартия КАК КлючПартия
	|ИЗ
	|	ПФРазузловкаПоДинСтруктуре КАК ДД
	|	ЛЕВОЕ СОЕДИНЕНИЕ ЗнаменательИспользуемыхИзделийСпецификаций КАК ЗнаменательИспользуемыхИзделийСпецификаций
	|	ПО ДД.КлючПартия = ЗнаменательИспользуемыхИзделийСпецификаций.КлючПартия
	|		И &УсловиеСоединения
	|ГДЕ
	// Комментарий 1155.
	// Разузловка полуфабриката, при распределении из запасов, происходит по спецификации.
	|	ДД.РаспределеноИзЗапасов > 0
	|";
	
	УсловиеСоединения = "(
	|			ВЫБОР
	|				КОГДА ДД.ПроизводствоНаСтороне
	//					см. комментарий 1391
	|					ТОГДА ДД.СпецификацияПолуфабриката
	|			ИНАЧЕ ДД.ПартияСпецификацияПолуфабриката
	|			КОНЕЦ = ЗнаменательИспользуемыхИзделийСпецификаций.Спецификация
	|)"; //@Query-part
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСоединения", УсловиеСоединения); //@Query-part
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	Возврат СтрСоединить(ТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
КонецФункции

Функция ТекстЗапроса_ПлановыеМатериальныеЗатраты()
	ТекстЗапроса = "
	|
	// 13 ПлановыеМатериальныеЗатраты
	|ВЫБРАТЬ
	|	ДД.ЗаказНаПроизводство	КАК ЗаказНаПроизводство,
	|
	|	ДД.Период				КАК Период,
	|	&Калькуляция 			КАК Калькуляция,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДД.ПартияСпецификацияПолуфабриката) = ТИП(Документ.ЭтапПроизводства2_2)
	|			ТОГДА ДД.ПартияСпецификацияПолуфабриката
	|		ИНАЧЕ ДД.Спецификация
	|	КОНЕЦ					КАК ПартияСпецификация,
	|	ДД.КлючПартия			КАК КлючПартия,
	|	ДД.Подразделение 		КАК Подразделение,
	|	ДД.СтатьяКалькуляции	КАК СтатьяКалькуляции,
	|	ДД.Номенклатура 		КАК Номенклатура,
	|	ДД.Характеристика 		КАК Характеристика,
	|
	|	ДД.Требуется							КАК Количество,
	|	0										КАК Стоимость,
	|	0										КАК СтоимостьБезНДС,
	|	0										КАК СтоимостьРегл,
	|	0										КАК СтоимостьЗабалансовая,
	|	0										КАК СтоимостьЗабалансоваяРегл,
	|	
	|	""ПлановыеМатериальныеЗатраты""		КАК Комментарий,
	|	НАЧАЛОПЕРИОДА(ДД.Период, МЕСЯЦ)		КАК ПериодЗатрат,
	|	ДД.ЭтоПолуфабрикат					КАК ЭтоПолуфабрикат
	|ИЗ
	|	ПотреблениеМатериаловИПолуфабрикатов КАК ДД
	|";
	
	Возврат ТекстЗапроса;
КонецФункции

#Область РазузлованиеПФПоДинСтруктуре

Функция ТекстЗапроса_РазузловкаПолуфабрикатовДинСтруктура(ПараметрыРасчета)
	ТекстыЗапросов = Новый Массив();
	ТекстыЗапросов.Добавить("УНИЧТОЖИТЬ ПродукцияПФПредварительная"); //@Query-part
	ТекстыЗапросов.Добавить("УНИЧТОЖИТЬ ПотреблениеМатериаловИПолуфабрикатов"); //@Query-part
	ТекстыЗапросов.Добавить(ТекстЗапроса_ПотреблениеМатериаловИПолуфабрикатов("ПФРазузловкаПоДинСтруктуре")); //@Query-part
	ТекстыЗапросов.Добавить(ПараметрыРасчета.ТекстЗапросаПолученияДанныхПоТекУровню);
	// 14 ПлановыеТрудозатраты
	ТекстыЗапросов.Добавить(ТекстЗапроса_ПлановыеТрудозатраты());
	Возврат СтрСоединить(ТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
КонецФункции

Процедура РазузловатьПФПроизводимыеВПроцессе(Запрос, ПараметрыРасчета)
	СтруктураДанных = ПараметрыРасчета.СтруктураДанных;
	СтруктураДанных.Порядок = СтруктураДанных.Порядок + 1;
	
	Запрос.Текст = ПараметрыРасчета.ТекстЗапроса_РазузловкаПолуфабрикатовДинСтруктура;
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ПеренестиДанныеИзРезультатовЗапросов(МассивРезультатов, ПараметрыРасчета);
	
	ИменаВременныхТаблиц = "ПФРазузловкаПоДинСтруктуре, ИспользуемыеПолуфабрикаты, ЗнаменательИспользуемыхИзделийСпецификаций"; //@Query-part
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц, ИменаВременныхТаблиц);
	
	РаспределитьПотреблениеПФПоВозможнымВыпускам(Запрос, ПараметрыРасчета, Ложь);
	ПоместитьИспользуемыеПолуфабрикатыВВТ(Запрос.МенеджерВременныхТаблиц,
		ПараметрыРасчета.СтруктураДанных.ИспользуемыеПолуфабрикаты);
		
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(
		Запрос.МенеджерВременныхТаблиц,
		"ЗнаменательИспользуемыхИзделийСпецификаций",
		ПараметрыРасчета.СтруктураДанных.ЗнаменательИспользуемыхИзделийСпецификаций,
		"Спецификация,КлючПартия,Знаменатель",
		"КлючПартия,Спецификация");
		
	Запрос.Текст = ПараметрыРасчета.ТекстЗапроса_Полуфабрикаты;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ИндексПоследнегоЗапроса = МассивРезультатов.Количество() - 1;
	
	// 15 ВыпускиПФВРамкахЗаказа
	ПеренестиДанныеИзРезультатаЗапросаВТаблицуЗначений(СтруктураДанных.ВыпускиПФВРамкахЗаказа,
		МассивРезультатов[ИндексПоследнегоЗапроса - 1], Ложь);

	Если МассивРезультатов[ИндексПоследнегоЗапроса].Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	// 10 Множители по полуфабрикатам производимым процессе.
	ВыборкаМножителей = МассивРезультатов[ИндексПоследнегоЗапроса].Выбрать();
	Пока ВыборкаМножителей.Следующий() Цикл
		НовыйМножитель = СтруктураДанных.Множители.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйМножитель, ВыборкаМножителей);
		НовыйМножитель.Порядок = СтруктураДанных.Порядок;
	КонецЦикла;
	
	РазузловатьПФПроизводимыеВПроцессе(Запрос, ПараметрыРасчета);
	
КонецПроцедуры

// В данной процедуре определяется как будет разузловыватся ПФ.
// В случае наличия запущенного, в т.ч. завершенного производства ПФ, разузловка будет происходить через этапы.
// Количество которое будет разузловано через этапы распределяется по остаточному принципу.
// Количество сверх этапов разузловывается через спецификации.
// Необходимо учитывать что один ПФ, требующийся на разных уровнях может быть запущен частично в разных этапах.
Процедура РаспределитьПотреблениеПФПоВозможнымВыпускам(Запрос, ПараметрыРасчета, РаспределитьГПВЭтапахПартияНеВыпущена)
	
	Если РаспределитьГПВЭтапахПартияНеВыпущена Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ПЭПНВ.СписатьНаРасходы										КАК СписатьНаРасходы,
		|	ИСТИНА														КАК ВСтруктуреНетРазузловки,
		|	АВТОНОМЕРЗАПИСИ()											КАК ИдентификаторСтроки,
		|	ИСТИНА														КАК ГПВЭтапе,
		|	ПЭПНВ.Подразделение											КАК Подразделение,
		|	ПЭПНВ.ЗаказНаПроизводство									КАК ЗаказНаПроизводство,
		|	ПЭПНВ.Период												КАК Период,
		|	ПЭПНВ.Продукция												КАК Продукция,
		|	ПЭПНВ.ХарактеристикаПродукции								КАК ХарактеристикаПродукции,
		|	ПЭПНВ.НазначениеПродукции									КАК НазначениеПродукции,
		|	ПЭПНВ.ПартияСпецификацияПродукции							КАК ПартияСпецификацияПродукции,
		|	ПЭПНВ.ПроизводствоНаСтороне									КАК ПроизводствоНаСтороне,
		|
		|	ПЭПНВ.Полуфабрикат											КАК Полуфабрикат,
		|	ПЭПНВ.ХарактеристикаПолуфабриката							КАК ХарактеристикаПолуфабриката,
		|
		|	ПЭПНВ.Спецификация											КАК СпецификацияПолуфабриката,
		|	ПЭПНВ.ПартияСпецификацияПродукции							КАК ПартияСпецификацияПолуфабриката,
		|
		|	ИСТИНА														КАК ЕстьСозданныйЭтапВыпускаПФ,
		|	ПЭПНВ.ХарактеристикаПродукцииУидСтрока						КАК КлючПартия,
		|	0								КАК КоличествоЗапущено,
		|	0								КАК КоличествоТребуется,
		|	0								КАК Остаток,
		|	0								КАК Запланировано,
		|	ПЭПНВ.КоличествоПолуфабриката	КАК РаспределеноИзПартийОбособленно,
		|	0								КАК РаспределеноИзЗапасовОбособленно,
		|	0								КАК РаспределеноИзЗапасов,
		|	
		|	ВЫРАЗИТЬ(0.0 КАК ЧИСЛО(15, 3))	КАК КоличествоИзЭтапа,
		|	ВЫРАЗИТЬ(0.0 КАК ЧИСЛО(15, 3))	КАК КоличествоИзСпецификации,
		|	1 КАК Числитель,
		|	1 КАК Знаменатель
		|
		|ПОМЕСТИТЬ ИспользуемыеПолуфабрикатыПредварительная_1
		|ИЗ
		|	ПродукцияВЭтапахПартияНеВыпущена КАК ПЭПНВ
		|;
		|";
	Иначе
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ЛОЖЬ																КАК СписатьНаРасходы,
		|	АВТОНОМЕРЗАПИСИ()													КАК ИдентификаторСтроки,
		|	ЛОЖЬ																КАК ГПВЭтапе,
		|	ДД.ЗаказНаПроизводство												КАК ЗаказНаПроизводство,
		|	ДД.Подразделение													КАК Подразделение,
		|	ДД.Период															КАК Период,
		|	ДД.Продукция														КАК Продукция,
		|	ДД.ХарактеристикаПродукции											КАК ХарактеристикаПродукции,
		|	ДД.НазначениеПродукции												КАК НазначениеПродукции,
		|	ДД.ПартияСпецификацияПродукции										КАК ПартияСпецификацияПродукции,
		|	ЕСТЬNULL(ПроизводствоПолуфабриката.ПроизводствоНаСтороне, ЛОЖЬ)		КАК ПроизводствоНаСтороне,
		|	ДД.Номенклатура														КАК Полуфабрикат,
		|	ДД.Характеристика													КАК ХарактеристикаПолуфабриката,
		|	НЕ ПроизводствоПолуфабриката.Этап IS NULL
		|		И ТИПЗНАЧЕНИЯ(ПроизводствоПолуфабриката.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|																		КАК ЕстьСозданныйЭтапВыпускаПФ,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПроизводствоПолуфабриката.Спецификация, ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
		|			ТОГДА ПроизводствоПолуфабриката.Спецификация
		|		КОГДА ЕСТЬNULL(ПотреблениеПолуфабриката.СпецификацияПолуфабриката, ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
		|			ТОГДА ПотреблениеПолуфабриката.СпецификацияПолуфабриката
		|		ИНАЧЕ ЕСТЬNULL(КэшЗарезервировано.СпецификацияПолуфабриката, ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка))
		|	КОНЕЦ																КАК СпецификацияПолуфабриката,
		|
		// Если в структуре нет производства ПФ, то спецификацию берем из строки потребления
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ПроизводствоПолуфабриката.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|			ТОГДА ПроизводствоПолуфабриката.Этап
		|		КОГДА ЕСТЬNULL(ПроизводствоПолуфабриката.Спецификация, ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
		|			ТОГДА ПроизводствоПолуфабриката.Спецификация
		|		КОГДА ЕСТЬNULL(ПотреблениеПолуфабриката.СпецификацияПолуфабриката, ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
		|			ТОГДА ПотреблениеПолуфабриката.СпецификацияПолуфабриката
		|		ИНАЧЕ ЕСТЬNULL(КэшЗарезервировано.СпецификацияПолуфабриката, ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка))
		|	КОНЕЦ																	КАК ПартияСпецификацияПолуфабриката,
		|
		|	ПроизводствоПолуфабриката.КлючНоменклатура								КАК КлючНоменклатура,
		|	ЕСТЬNULL(ПроизводствоПолуфабриката.КлючПартия, &ПустойУИДСтрока)		КАК КлючПартия,
		|
		|	ДД.Требуется - ДД.РаспределеноИзЗапасовОбособленно - ДД.РаспределеноИзЗапасов 
		|		- ДД.РаспределеноИзПартийОбособленно - ДД.РаспределеноИзПартий
		|		- ЕСТЬNULL(ПроизводствоПолуфабриката.РаспределеноИзПартийОбособленно, 0) КАК Остаток,
		|	ПотреблениеПолуфабриката.Требуется										КАК КоличествоТребуется,
		|	ЕСТЬNULL(ЗапущеноПроизводствоПолуфабриката.Запущено, 0)					КАК КоличествоЗапущено,
		|	ПроизводствоПолуфабриката.КлючНоменклатура ЕСТЬ NULL
		|		ИЛИ ТИПЗНАЧЕНИЯ(ПроизводствоПолуфабриката.Этап) = ТИП(Документ.ЭтапПроизводства2_2) КАК ВСтруктуреНетРазузловки,
		// Производство одного ПФ(одного ключа номенклатуры), может быть частично запущено.
		// РаспределеноИзЗапасовОбособленно, для случаев когда ПроизводствоПолуфабриката.Этап ссылка на документ это явная отсылка из какого этапа ожидается ПФ,
		// но если ПФ выпущен, то ссылки на Этап уже не будет и количество РаспределеноИзЗапасовОбособленно остается, но оно не дает 100% гарантии что ПФ выпускается в рамках этого заказа,
		// для таких случаев необходимо поискать в подчиненных этапах, есть ли выпуск ПФ, если есть то разузловывать через эти выпуски, если нет тогда разузловывать через спецификацию.
		// Так же необходимо учитывать что 
		// Исходя из описания выше поэтапное распределение будет реализовано по остаточному принципу.
		|	ЕСТЬNULL(ПроизводствоПолуфабриката.Запланировано, 0) 					* ДД.Числитель / ДД.Знаменатель	КАК Запланировано,
		|	ЕСТЬNULL(ПроизводствоПолуфабриката.РаспределеноИзПартийОбособленно, 0)	* ДД.Числитель / ДД.Знаменатель	КАК РаспределеноИзПартийОбособленно,
		|	ЕСТЬNULL(КэшЗарезервировано.РаспределеноИзЗапасовОбособленно, 0) 		* ДД.Числитель / ДД.Знаменатель	КАК РаспределеноИзЗапасовОбособленно,
		|	ЕСТЬNULL(КэшЗарезервировано.РаспределеноИзЗапасов, 0) 					* ДД.Числитель / ДД.Знаменатель	КАК РаспределеноИзЗапасов,
		|
		|	ДД.Числитель
		|		* ЕСТЬNULL(ПобочныйВыпускПФ.Запланировано, 1) / ЕСТЬNULL(ПобочныйВыпускПФ.ИтогоКоличество, 1)
		|		* ВЫБОР КОГДА ЕСТЬNULL(ЗапланированоПоКлючамПартиям.Запланировано, 1) = 0  
		|			ТОГДА 1
		|			ИНАЧЕ ЕСТЬNULL(ПроизводствоПолуфабриката.Запланировано, 1) / ЕСТЬNULL(ЗапланированоПоКлючамПартиям.Запланировано, 1)
		|		КОНЕЦ КАК Числитель,
		|	ДД.Знаменатель КАК Знаменатель
		|
		|ПОМЕСТИТЬ ИспользуемыеПолуфабрикатыПредварительная_1
		|ИЗ
		|	ПотреблениеМатериаловИПолуфабрикатов КАК ДД
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКэшСтруктураЗаказа КАК ПроизводствоПолуфабриката
		|		ПО (ДД.КлючНоменклатура = ПроизводствоПолуфабриката.КлючНоменклатура)
		|			И (ПроизводствоПолуфабриката.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПолуфабриката))
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКэшСтруктураЗаказа КАК ПотреблениеПолуфабриката
		|		ПО (ДД.КлючНоменклатура = ПотреблениеПолуфабриката.КлючНоменклатура)
		|			И (ПотреблениеПолуфабриката.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Потребление))
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКэшСтруктураЗаказа_ЗапущеноПоКлючамПартий КАК ЗапущеноПроизводствоПолуфабриката
		|		ПО (ПроизводствоПолуфабриката.КлючПартия = ЗапущеноПроизводствоПолуфабриката.КлючПартия)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКэшСтруктураЗаказа КАК КэшЗарезервировано
		|		ПО ДД.КлючНоменклатура = КэшЗарезервировано.КлючНоменклатура
		|			И ДД.КлючПартия = КэшЗарезервировано.КлючПартия
		|			И КэшЗарезервировано.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Зарезервировано)
		|
		// См. Комментарий 636.
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПобочныйВыпускПФ КАК ПобочныйВыпускПФ
		|		ПО ПроизводствоПолуфабриката.КлючПартия = ПобочныйВыпускПФ.КлючПартия
		|			И ДД.Номенклатура = ПобочныйВыпускПФ.Номенклатура
		|			И ДД.Характеристика = ПобочныйВыпускПФ.Характеристика
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапланированоПоКлючамПартиям КАК ЗапланированоПоКлючамПартиям
		|		ПО ЗапланированоПоКлючамПартиям.КлючПартия = ПроизводствоПолуфабриката.КлючПартия
		|
		|ГДЕ
		|	ДД.ЭтоПолуфабрикат
		|	И (ЕСТЬNULL(ПроизводствоПолуфабриката.Запланировано, 0) <> 0
		|		ИЛИ ЕСТЬNULL(ПроизводствоПолуфабриката.РаспределеноИзПартийОбособленно, 0) <> 0
		|		ИЛИ ЕСТЬNULL(КэшЗарезервировано.РаспределеноИзЗапасовОбособленно, 0) <> 0
		|		ИЛИ ЕСТЬNULL(КэшЗарезервировано.РаспределеноИзЗапасов, 0) <> 0
		|	)
		|;
		|";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|ВЫБРАТЬ
	|	ДД.СписатьНаРасходы						КАК СписатьНаРасходы,
	|	-1										КАК Источник_ИД,
	|	ДД.ИдентификаторСтроки					КАК ИдентификаторСтроки,
	|	ДД.ГПВЭтапе								КАК ГПВЭтапе,
	|	ДАТАВРЕМЯ(1, 1, 1)						КАК ДатаПроизводства,
	|	ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка) КАК ВыпускающийЭтап,
	|	ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка) КАК ПотребляющийЭтап,
	|	ЛОЖЬ									КАК ПартияВыпущена,
	|	ДД.ВСтруктуреНетРазузловки				КАК ВСтруктуреНетРазузловки,
	|	ДД.Подразделение						КАК Подразделение,
	|	ДД.ЗаказНаПроизводство					КАК ЗаказНаПроизводство,
	|	ДД.Период								КАК Период,
	|	ДД.Продукция							КАК Продукция,
	|	ДД.ХарактеристикаПродукции				КАК ХарактеристикаПродукции,
	|	ДД.НазначениеПродукции					КАК НазначениеПродукции,
	|	ДД.ПартияСпецификацияПродукции			КАК ПартияСпецификацияПродукции,
	|	ДД.ПроизводствоНаСтороне				КАК ПроизводствоНаСтороне,
	|	ДД.Полуфабрикат							КАК Полуфабрикат,
	|	ДД.ХарактеристикаПолуфабриката			КАК ХарактеристикаПолуфабриката,
	|	ДД.СпецификацияПолуфабриката			КАК СпецификацияПолуфабриката,
	|	ДД.ЕстьСозданныйЭтапВыпускаПФ			КАК ЕстьСозданныйЭтапВыпускаПФ,
	|	ДД.ПартияСпецификацияПолуфабриката		КАК ПартияСпецификацияПолуфабриката,
	|	ДД.КлючПартия							КАК КлючПартия,
	|
	|	ДД.КоличествоЗапущено					КАК КоличествоЗапущено,
	|	ДД.КоличествоТребуется					КАК КоличествоТребуется,
	|	ДД.Остаток								КАК Остаток,
	|
	|	ДД.Запланировано						КАК Запланировано,
	|	ДД.РаспределеноИзПартийОбособленно		КАК РаспределеноИзПартийОбособленно,
	|	ДД.РаспределеноИзЗапасовОбособленно		КАК РаспределеноИзЗапасовОбособленно,
	|	ДД.РаспределеноИзЗапасов				КАК РаспределеноИзЗапасов,
	|	ВЫРАЗИТЬ(0.0 КАК ЧИСЛО(15, 3))			КАК КоличествоИзЭтапа,
	|	ВЫРАЗИТЬ(0.0 КАК ЧИСЛО(15, 3))			КАК КоличествоИзСпецификации,
	|	
	|	ЕСТЬNULL(ДолиСтоимостиПоСпецификациям.ДоляСтоимости, 1) * ДД.Числитель / ДД.Знаменатель
	|		/ ЕСТЬNULL(ОбщиеДолиСтоимостиПоСпецификациям.ИтогоДоляСтоимости, 1)					КАК КоэффициентДолиСтоимости,
	|	ЕСТЬNULL(ДолиСтоимостиПоСпецификациям.ДоляСтоимости, 1) * ДД.Числитель / ДД.Знаменатель	КАК Числитель,
	|	ЕСТЬNULL(ОбщиеДолиСтоимостиПоСпецификациям.ИтогоДоляСтоимости, 1)						КАК Знаменатель
	|ПОМЕСТИТЬ ИспользуемыеПолуфабрикатыПредварительная_2
	|ИЗ
	|	ИспользуемыеПолуфабрикатыПредварительная_1 КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОбщиеДолиСтоимостиПоСпецификациям КАК ОбщиеДолиСтоимостиПоСпецификациям
	|		ПО ДД.СпецификацияПолуфабриката = ОбщиеДолиСтоимостиПоСпецификациям.Спецификация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДолиСтоимостиПоСпецификациям КАК ДолиСтоимостиПоСпецификациям
	|		ПО ДД.СпецификацияПолуфабриката = ДолиСтоимостиПоСпецификациям.Спецификация
	|			И ДД.Полуфабрикат = ДолиСтоимостиПоСпецификациям.Номенклатура
	|			И (ДД.ХарактеристикаПолуфабриката = ДолиСтоимостиПоСпецификациям.Характеристика
	|				ИЛИ ДолиСтоимостиПоСпецификациям.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// ЗнаменательИспользуемыхИзделийСпецификаций
	|ВЫБРАТЬ
	|	ДД.СпецификацияПолуфабриката КАК Спецификация,
	|	ДД.КлючПартия КАК КлючПартия,
	|	ЛОЖЬ КАК ЭтоПродукция,
	|	СУММА(ДД.Числитель) КАК Знаменатель
	|
	|ИЗ ИспользуемыеПолуфабрикатыПредварительная_2 КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.СпецификацияПолуфабриката,
	|	ДД.КлючПартия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВИ.ИдентификаторСтроки КАК Источник_ИД,
	|	ДД.ИдентификаторСтроки КАК Приемник_ИД
	|
	|ИЗ
	|	ИспользуемыеПолуфабрикатыПредварительная_1 КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыходныеИзделия КАК ВИ
	|		ПО
	|			ВЫБОР КОГДА ДД.ЕстьСозданныйЭтапВыпускаПФ
	|				ТОГДА ДД.ПартияСпецификацияПолуфабриката = ВИ.ВыпускающийЭтап
	|			ИНАЧЕ
	|				ДД.СпецификацияПолуфабриката = ВИ.Спецификация
	|			КОНЕЦ
	|
	|			И ДД.ЗаказНаПроизводство = ВИ.ЗаказНаПроизводство
	|			И ДД.Подразделение = ВИ.Подразделение
	|
	|			И ВЫБОР КОГДА ДД.ГПВЭтапе ТОГДА
	|				ДД.Продукция = ВИ.Номенклатура
	|				И ДД.ХарактеристикаПродукции = ВИ.Характеристика
	|				И НЕ ВИ.ПартияВыпущена
	|			ИНАЧЕ
	|				ДД.Полуфабрикат = ВИ.Номенклатура
	|				И ДД.ХарактеристикаПолуфабриката = ВИ.Характеристика
	|			КОНЕЦ
	|
	|			И ((ДД.НазначениеПродукции = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|					И ВИ.НазначениеПустое)
	|				ИЛИ (ДД.НазначениеПродукции = ВИ.Назначение)
	|			)
	|			И ДД.ПроизводствоНаСтороне = ВИ.ПроизводствоНаСтороне
	|			И ДД.СписатьНаРасходы = ВИ.СписатьНаРасходы
	|
	|ГДЕ
	//	см. комментарий 1391
	|	НЕ ДД.ПроизводствоНаСтороне
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.СписатьНаРасходы						КАК СписатьНаРасходы,
	|	ДД.Источник_ИД 							КАК Источник_ИД,
	|	ДД.ИдентификаторСтроки					КАК ИдентификаторСтроки,
	|	ДД.ГПВЭтапе								КАК ГПВЭтапе,
	|	ДД.ДатаПроизводства						КАК ДатаПроизводства,
	|	ДД.ВыпускающийЭтап						КАК ВыпускающийЭтап,
	|	ДД.ПотребляющийЭтап						КАК ПотребляющийЭтап,
	|	ДД.ПартияВыпущена						КАК ПартияВыпущена,
	|	ДД.ВСтруктуреНетРазузловки				КАК ВСтруктуреНетРазузловки,
	|	ДД.Подразделение						КАК Подразделение,
	|	ДД.ЗаказНаПроизводство					КАК ЗаказНаПроизводство,
	|	ДД.Период								КАК Период,
	|	ДД.Продукция							КАК Продукция,
	|	ДД.ХарактеристикаПродукции				КАК ХарактеристикаПродукции,
	|	ДД.НазначениеПродукции					КАК НазначениеПродукции,
	|	ДД.ПартияСпецификацияПродукции			КАК ПартияСпецификацияПродукции,
	|	ДД.ПроизводствоНаСтороне				КАК ПроизводствоНаСтороне,
	|	ДД.Полуфабрикат							КАК Полуфабрикат,
	|	ДД.ХарактеристикаПолуфабриката			КАК ХарактеристикаПолуфабриката,
	|	ДД.СпецификацияПолуфабриката			КАК СпецификацияПолуфабриката,
	|	ДД.ЕстьСозданныйЭтапВыпускаПФ			КАК ЕстьСозданныйЭтапВыпускаПФ,
	|	ДД.ПартияСпецификацияПолуфабриката		КАК ПартияСпецификацияПолуфабриката,
	|	ДД.КлючПартия							КАК КлючПартия,
	|
	|	ДД.КоличествоЗапущено					КАК КоличествоЗапущено,
	|	ДД.КоличествоТребуется					КАК КоличествоТребуется,
	|	ДД.Остаток								КАК Остаток,
	|
	|	ДД.Запланировано						КАК Запланировано,
	|	ДД.РаспределеноИзПартийОбособленно		КАК РаспределеноИзПартийОбособленно,
	|	ДД.РаспределеноИзЗапасовОбособленно		КАК РаспределеноИзЗапасовОбособленно,
	|	ДД.РаспределеноИзЗапасов				КАК РаспределеноИзЗапасов,
	|	ДД.КоличествоИзЭтапа					КАК КоличествоИзЭтапа,
	|	ДД.КоличествоИзСпецификации				КАК КоличествоИзСпецификации,
	|	
	|	ДД.КоэффициентДолиСтоимости				КАК КоэффициентДолиСтоимости,
	|	ДД.Числитель							КАК Числитель,
	|	ДД.Знаменатель							КАК Знаменатель
	|ИЗ
	|	ИспользуемыеПолуфабрикатыПредварительная_2 КАК ДД
	|
	|УПОРЯДОЧИТЬ ПО
	// Сначала будем забирать количество изготовленного ПФ из явно указанных этапов.
	|	ДД.ЕстьСозданныйЭтапВыпускаПФ Убыв,
	|	ДД.Период
	|";
	
	Запрос.Текст = ТекстЗапроса;
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ИндексПоследнегоЗапроса = РезультатыЗапроса.Количество() - 1;
	ИспользуемыеПолуфабрикатыПредварительная = РезультатыЗапроса[ИндексПоследнегоЗапроса].Выгрузить();
	ИсточникиПриемники = РезультатыЗапроса[ИндексПоследнегоЗапроса - 1].Выгрузить();
	ИсточникиПриемники.Индексы.Добавить("Приемник_ИД");
	
	ЗнаменательИспользуемыхИзделийСпецификаций = РезультатыЗапроса[ИндексПоследнегоЗапроса - 2].Выбрать();
	Пока ЗнаменательИспользуемыхИзделийСпецификаций.Следующий() Цикл
		НоваяСтрока = ПараметрыРасчета.СтруктураДанных.ЗнаменательИспользуемыхИзделийСпецификаций.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗнаменательИспользуемыхИзделийСпецификаций);
	КонецЦикла;
	
	Если ПараметрыРасчета.СтруктураДанных.Свойство("ИспользуемыеПолуфабрикаты") Тогда
		ИспользуемыеПолуфабрикаты = ПараметрыРасчета.СтруктураДанных.ИспользуемыеПолуфабрикаты;
	Иначе
		ИспользуемыеПолуфабрикаты = ИспользуемыеПолуфабрикатыПредварительная.СкопироватьКолонки("СписатьНаРасходы,
			|Источник_ИД,
			|ИдентификаторСтроки,ГПВЭтапе,
			|ДатаПроизводства,ВыпускающийЭтап,ПартияВыпущена,ВСтруктуреНетРазузловки,Подразделение,
			|ЗаказНаПроизводство,Период,ПроизводствоНаСтороне,ЕстьСозданныйЭтапВыпускаПФ,КлючПартия,
			|КоличествоЗапущено,КоличествоТребуется,Остаток,Запланировано,РаспределеноИзПартийОбособленно,
			|РаспределеноИзЗапасовОбособленно,РаспределеноИзЗапасов,
			|КоличествоИзЭтапа,КоличествоИзСпецификации,
			|КоэффициентДолиСтоимости,Числитель,Знаменатель");
	
		// При первом вызове колонки могут иметь не подходящие или не все необходимые типы,
		// поэтому часть колонок определяем явно.
		МассивТиповПартияСпецификация = Новый Массив();
		МассивТиповПартияСпецификация.Добавить(Тип("СправочникСсылка.РесурсныеСпецификации"));
		МассивТиповПартияСпецификация.Добавить(Тип("ДокументСсылка.ЭтапПроизводства2_2"));
		ОписаниеТипаПартияСпецификация = Новый ОписаниеТипов(МассивТиповПартияСпецификация);
		
		ИспользуемыеПолуфабрикаты.Колонки.Добавить("Продукция", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ИспользуемыеПолуфабрикаты.Колонки.Добавить("ХарактеристикаПродукции",
			Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ИспользуемыеПолуфабрикаты.Колонки.Добавить("НазначениеПродукции",
			Новый ОписаниеТипов("СправочникСсылка.Назначения"));
		ИспользуемыеПолуфабрикаты.Колонки.Добавить("ПартияСпецификацияПродукции", ОписаниеТипаПартияСпецификация);
		ИспользуемыеПолуфабрикаты.Колонки.Добавить("Полуфабрикат", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ИспользуемыеПолуфабрикаты.Колонки.Добавить("ХарактеристикаПолуфабриката",
			Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ИспользуемыеПолуфабрикаты.Колонки.Добавить("СпецификацияПолуфабриката",
			Новый ОписаниеТипов("СправочникСсылка.РесурсныеСпецификации"));
		ИспользуемыеПолуфабрикаты.Колонки.Добавить("ПартияСпецификацияПолуфабриката", ОписаниеТипаПартияСпецификация);
		ИспользуемыеПолуфабрикаты.Колонки.Добавить("ПотребляющийЭтап",
			Новый ОписаниеТипов("ДокументСсылка.ЭтапПроизводства2_2"));
		
		ПараметрыРасчета.СтруктураДанных.Вставить("ИспользуемыеПолуфабрикаты", ИспользуемыеПолуфабрикаты);
	КонецЕсли;

	Для Каждого Стр Из ИспользуемыеПолуфабрикатыПредварительная Цикл
		
		КоличествоНеобходимо = Стр.Запланировано + Стр.РаспределеноИзПартийОбособленно + Стр.РаспределеноИзЗапасовОбособленно;
		// Верхний уровень может из этапа забрать количество для нижнего уровня,
		// тогда как в верхнем уровне может быть разузузловка по дин структуре, а нижний уровень разузловывается только по этапам, 
		// в этом случае разузловка верхнего уровня теряется т.к. у нижнего уровня другая ключ партия и происходит потеря затрат материалов(в т.ч. ПФ) и трудозатрат.
		КоличествоНеобходимоИзЭтапа = КоличествоНеобходимо - Стр.Запланировано;
		
		ПодходящиеИсточники = ИсточникиПриемники.НайтиСтроки(Новый Структура("Приемник_ИД", Стр.ИдентификаторСтроки));
		
		Если ПодходящиеИсточники.Количество() = 0 Тогда
			// Нет подходящего выпуска ПФ, разузловываем через спецификацию.
			СтрокаСпецификации = ИспользуемыеПолуфабрикаты.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаСпецификации, Стр);
			СтрокаСпецификации.КоличествоИзСпецификации = КоличествоНеобходимо;
			Если СтрокаСпецификации.ЕстьСозданныйЭтапВыпускаПФ Тогда
				СтрокаСпецификации.ЕстьСозданныйЭтапВыпускаПФ = Ложь;
				СтрокаСпецификации.ПартияСпецификацияПолуфабриката = СтрокаСпецификации.СпецификацияПолуфабриката;
			КонецЕсли;
			
			Продолжить;
		КонецЕсли;
		
		Для Каждого Источник Из ПодходящиеИсточники Цикл
			СтрокаОстатка = ПараметрыРасчета.СтруктураДанных.ВыходныеИзделия.Найти(Источник.Источник_ИД, "ИдентификаторСтроки");
			КоличествоИзЭтапа = Мин(СтрокаОстатка.КоличествоВыпускОстаток, КоличествоНеобходимоИзЭтапа);
			
			Если КоличествоИзЭтапа = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаЭтапа = ИспользуемыеПолуфабрикаты.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаЭтапа, Стр);
			СтрокаЭтапа.КоличествоИзЭтапа = КоличествоИзЭтапа;
			СтрокаЭтапа.Источник_ИД = Источник.Источник_ИД;
			
			СтрокаОстатка.КоличествоВыпускОстаток = СтрокаОстатка.КоличествоВыпускОстаток - СтрокаЭтапа.КоличествоИзЭтапа;
			КоличествоНеобходимо = КоличествоНеобходимо - СтрокаЭтапа.КоличествоИзЭтапа;
			КоличествоНеобходимоИзЭтапа = КоличествоНеобходимоИзЭтапа - СтрокаЭтапа.КоличествоИзЭтапа;
			
			Если СтрокаЭтапа.КоличествоИзЭтапа > 0 Тогда
				СтрокаЭтапа.ПартияВыпущена = СтрокаОстатка.ПартияВыпущена;
				СтрокаЭтапа.ДатаПроизводства = СтрокаОстатка.ДатаПроизводства;
				СтрокаЭтапа.ВыпускающийЭтап = СтрокаОстатка.ВыпускающийЭтап;
				Если Не Стр.ЕстьСозданныйЭтапВыпускаПФ Тогда
					// Найден подходящий этап выпуска необходимой ПФ
					СтрокаЭтапа.ЕстьСозданныйЭтапВыпускаПФ = Истина;
					СтрокаЭтапа.ПартияСпецификацияПолуфабриката = СтрокаЭтапа.ВыпускающийЭтап;
				КонецЕсли;
			КонецЕсли;
			
			Если КоличествоНеобходимо = 0 Или КоличествоНеобходимоИзЭтапа = 0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если КоличествоНеобходимо > 0 Тогда
			СтрокаСпецификации = ИспользуемыеПолуфабрикаты.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаСпецификации, Стр);
			СтрокаСпецификации.КоличествоИзСпецификации = КоличествоНеобходимо;
			СтрокаСпецификации.ЕстьСозданныйЭтапВыпускаПФ = Ложь;
			СтрокаСпецификации.ПартияСпецификацияПолуфабриката = Стр.СпецификацияПолуфабриката;
		КонецЕсли;
	КонецЦикла;
	
	Запрос.Текст = "УНИЧТОЖИТЬ ИспользуемыеПолуфабрикатыПредварительная_1;
	|УНИЧТОЖИТЬ ИспользуемыеПолуфабрикатыПредварительная_2";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПоместитьИспользуемыеПолуфабрикатыВВТ(МенеджерВременныхТаблиц, ИспользуемыеПолуфабрикаты)
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(МенеджерВременныхТаблиц,
		"ИспользуемыеПолуфабрикаты", ИспользуемыеПолуфабрикаты,
		"СписатьНаРасходы,Источник_ИД,ГПВЭтапе,ДатаПроизводства,ВыпускающийЭтап,ПотребляющийЭтап,
		|ПартияВыпущена,ВСтруктуреНетРазузловки,Подразделение,
		|ЗаказНаПроизводство,Период,Продукция,ХарактеристикаПродукции,НазначениеПродукции,ПартияСпецификацияПродукции,
		|ПроизводствоНаСтороне,Полуфабрикат,ХарактеристикаПолуфабриката,СпецификацияПолуфабриката,
		|ЕстьСозданныйЭтапВыпускаПФ,ПартияСпецификацияПолуфабриката,КлючПартия,КоличествоЗапущено,КоличествоТребуется,
		|Остаток,Запланировано,РаспределеноИзПартийОбособленно,РаспределеноИзЗапасовОбособленно,РаспределеноИзЗапасов,
		|КоличествоИзЭтапа,КоличествоИзСпецификации,КоэффициентДолиСтоимости,Числитель,Знаменатель");
	ИспользуемыеПолуфабрикаты.Очистить();

КонецПроцедуры

Функция ТекстЗапроса_ПлановыеТрудозатраты()
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ДД.ЗаказНаПроизводство			КАК ЗаказНаПроизводство,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ПП.ПартияСпецификацияПолуфабриката) = ТИП(Документ.ЭтапПроизводства2_2)
	|			ТОГДА ПП.ПартияСпецификацияПолуфабриката
	|		ИНАЧЕ ДД.Спецификация
	|	КОНЕЦ							КАК ПартияСпецификация,
	|	ДД.КлючПартия					КАК КлючПартия,
	|	ДД.Период						КАК Период,
	|	ДД.Подразделение				КАК Подразделение,
	|	ДД.СтатьяКалькуляции			КАК СтатьяКалькуляции,
	|	ДД.ВидРабот						КАК ВидРабот,
	// ПФ могут выпускаться для 2х или более уровней в одном этапе.
	// Для верного расчета количества трудозатрат необходимых для определенного уровня,
	// необходимо их рассчитывать как отношение количества ПФ для тек. уровня, к количеству ПФ для всех уровней.
	|	ВЫБОР
	|		КОГДА ПП.КоличествоЗапущено = 0
	|			ТОГДА ДД.Количество
	|		ИНАЧЕ ДД.Количество * ПП.КоличествоПоПартииСпецификации / ПП.КоличествоЗапущено 
	|	КОНЕЦ * ПП.Числитель / ПП.Знаменатель	КАК Количество,
	|	НАЧАЛОПЕРИОДА(ДД.Период, МЕСЯЦ)			КАК ПериодЗатрат,
	|	""Трудозатраты""						КАК Комментарий
	|ИЗ
	|	ПродукцияПФПредварительная КАК ПП
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКэшТрудозатратыСтруктурыЗаказа КАК ДД
	|		ПО ПП.КлючПартия = ДД.КлючПартия
	|";
	
	Возврат ТекстЗапроса;
КонецФункции

// Формирует временные таблицы для подготовки потребляемых материалов и ПФ при разузловании дин. структуры, данные готовятся для каждого уровня разузлования.
// Параметры:
//  ИмяВТПродукцииПФ - Строка - Строка определяющая источник данных,
//    на первом уровне источник данных ВТ "ПроизводствоПродукции",
//    на следующих уровнях "ПФРазузловкаПоДинСтруктуре".
Функция ТекстЗапроса_ПотреблениеМатериаловИПолуфабрикатов(ИмяВТПродукцииПФ)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ПродукцияПФ.ПартияСпецификацияПолуфабриката 		КАК ПартияСпецификацияПолуфабриката,
	|	ПродукцияПФ.ПартияСпецификацияПродукции				КАК ПартияСпецификацияПродукции,
	|	ПродукцияПФ.Продукция								КАК Продукция,
	|	ПродукцияПФ.ХарактеристикаПродукции					КАК ХарактеристикаПродукции,
	|	ПродукцияПФ.НазначениеПродукции						КАК НазначениеПродукции,
	|	ПродукцияПФ.КлючПартия								КАК КлючПартия,
	|	ПродукцияПФ.ПроизводствоНаСтороне					КАК ПроизводствоНаСтороне,
	|	СУММА(ПродукцияПФ.КоличествоЗапущено)				КАК КоличествоЗапущено,
	|	СУММА(ПродукцияПФ.КоличествоПоПартииСпецификации)	КАК КоличествоПоПартииСпецификации,
	|	МАКСИМУМ(ПродукцияПФ.КоэффициентДолиСтоимости)		КАК КоэффициентДолиСтоимости,
	|	СУММА(ПродукцияПФ.Числитель)						КАК Числитель,
	|	ПродукцияПФ.Знаменатель								КАК Знаменатель
	|ПОМЕСТИТЬ ПродукцияПФПредварительная
	|ИЗ
	|	ПроизводствоПродукции КАК ПродукцияПФ
	|
	|ГДЕ
	//	см. комментарий 1391
	|	НЕ ПродукцияПФ.ПроизводствоНаСтороне
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродукцияПФ.ПартияСпецификацияПолуфабриката,
	|	ПродукцияПФ.ПартияСпецификацияПродукции,
	|	ПродукцияПФ.Продукция,
	|	ПродукцияПФ.ХарактеристикаПродукции,
	|	ПродукцияПФ.НазначениеПродукции,
	|	ПродукцияПФ.КлючПартия,
	|	ПродукцияПФ.ПроизводствоНаСтороне,
	|	ПродукцияПФ.Знаменатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.Склад									КАК Склад,
	|	ДД.Период									КАК Период,
	|	ДД.Этап										КАК Этап,
	|	ПродукцияПФ.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
	|	ДД.Спецификация								КАК Спецификация,
	|	ДД.Подразделение							КАК Подразделение,
	|	ПродукцияПФ.ПартияСпецификацияПродукции		КАК ПартияСпецификацияПродукции,
	|	ПродукцияПФ.Продукция						КАК Продукция,
	|	ПродукцияПФ.ХарактеристикаПродукции			КАК ХарактеристикаПродукции,
	|	ПродукцияПФ.НазначениеПродукции				КАК НазначениеПродукции,
	|	ДД.ЭтоПолуфабрикат		КАК ЭтоПолуфабрикат,
	|	ДД.СтатьяКалькуляции	КАК СтатьяКалькуляции,
	|	ДД.Номенклатура			КАК Номенклатура,
	|	ДД.Характеристика		КАК Характеристика,
	|	ДД.Назначение			КАК Назначение,
	|	ДД.ЗаказНаПроизводство	КАК ЗаказНаПроизводство,
	|	ДД.КлючНоменклатура		КАК КлючНоменклатура,
	|	ДД.КлючПартия			КАК КлючПартия,
	|	ВЫБОР КОГДА ДД.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПобочныйВыпуск)
	|		ТОГДА - ДД.Запланировано
	|		ИНАЧЕ (ДД.Требуется + ДД.Готово)
	// Есть 2 типа работы:
	//		* Обособленные, тогда обеспечиваются под заказ.
	//		* Не обособленные работы, не обеспечиваются, количество в НеОбеспечивать.
	|			+ ВЫБОР КОГДА ДД.ЭтоРабота И НЕ ДД.ЭтоПолуфабрикат
	|					ТОГДА ДД.НеОбеспечивать
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ
	// Комментарий 1877.
	// ПФ могут выпускаться для 2х или более уровней в одном этапе.
	// Для верного расчета количества материалов необходимых для определенного уровня,
	// необходимо их рассчитывать как отношение количества ПФ для тек. уровня, к количеству ПФ для всех уровней.
	|		* ВЫБОР
	|			КОГДА ПродукцияПФ.КоличествоЗапущено = 0
	|				ТОГДА 1
	|			ИНАЧЕ ПродукцияПФ.КоличествоПоПартииСпецификации / ПродукцияПФ.КоличествоЗапущено
	|		КОНЕЦ								
	|		* ПродукцияПФ.Числитель / ПродукцияПФ.Знаменатель	КАК Требуется,
	|	ДД.РаспределеноИзЗапасовОбособленно						КАК РаспределеноИзЗапасовОбособленно,
	|	ДД.РаспределеноИзЗапасов								КАК РаспределеноИзЗапасов,
	|	ДД.РаспределеноИзПартийОбособленно						КАК РаспределеноИзПартийОбособленно,
	|	ДД.РаспределеноИзПартий									КАК РаспределеноИзПартий,
	|	ПродукцияПФ.КоэффициентДолиСтоимости					КАК КоэффициентДолиСтоимости,
	|	ПродукцияПФ.Числитель									КАК Числитель,
	|	ПродукцияПФ.Знаменатель									КАК Знаменатель
	|
	|ПОМЕСТИТЬ ПотреблениеМатериаловИПолуфабрикатов
	|ИЗ
	|	ПродукцияПФПредварительная КАК ПродукцияПФ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКэшСтруктураЗаказа КАК ДД
	|		ПО ДД.КлючПартия = ПродукцияПФ.КлючПартия
	|		
	|ГДЕ
	|	ДД.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Потребление)
	|	ИЛИ (ДД.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПобочныйВыпуск)
	|			И ДД.Запланировано > 0
				// Дополнение к комментарию 695:
				//  Позиции, которые находятся в ТЧ спецификации - ВыходныеИзделия, но отсутвующие в заказе на производство,
				//  в ВТКэшСтруктураЗаказа имеют ВидСтроки = ПобочныйВыпуск.
				//  Для того чтобы указанные строки не воспринимать как побочный выход текущей КлючПартия,
				//  проверяем что в рамках текущей спецификации это действительно побочный выход.
	|			И ИСТИНА В (
	|						ВЫБРАТЬ ПЕРВЫЕ 1
	|							ИСТИНА
	|						ИЗ ДинСтруктураПобочныйВыходСпецификаций КАК ДинСтруктураПобочныйВыходСпецификаций
	|						ГДЕ ДД.Спецификация = ДинСтруктураПобочныйВыходСпецификаций.Спецификация
	|							И ДД.Номенклатура = ДинСтруктураПобочныйВыходСпецификаций.Номенклатура
	|							И ДД.Характеристика = ДинСтруктураПобочныйВыходСпецификаций.Характеристика
	|						)
	|		)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)	КАК Склад,
	|	ДД.Период									КАК Период,
	|	ДД.ЭтапСсылка								КАК Этап,
	|	ПродукцияПФ.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
	|	ДД.Спецификация								КАК Спецификация,
	|	ДД.Подразделение							КАК Подразделение,
	|	ПродукцияПФ.ПартияСпецификацияПродукции		КАК ПартияСпецификацияПродукции,
	|	ПродукцияПФ.Продукция						КАК Продукция,
	|	ПродукцияПФ.ХарактеристикаПродукции			КАК ХарактеристикаПродукции,
	|	ПродукцияПФ.НазначениеПродукции				КАК НазначениеПродукции,
	|	ЛОЖЬ					КАК ЭтоПолуфабрикат,
	|	ДД.СтатьяКалькуляции	КАК СтатьяКалькуляции,
	|	ДД.Номенклатура			КАК Номенклатура,
	|	ДД.Характеристика		КАК Характеристика,
	|	ДД.Назначение			КАК Назначение,
	|	ДД.ЗаказНаПроизводство	КАК ЗаказНаПроизводство,
	|	&ПустойУИД				КАК КлючНоменклатура,
	|	ДД.КлючПартия			КАК КлючПартия,
	|
	|	ДД.Количество
	// см. 1877.
	|		* ВЫБОР
	|			КОГДА ПродукцияПФ.КоличествоЗапущено = 0
	|				ТОГДА 1
	|			ИНАЧЕ ПродукцияПФ.КоличествоПоПартииСпецификации / ПродукцияПФ.КоличествоЗапущено
	|		КОНЕЦ								
	|		* ПродукцияПФ.Числитель / ПродукцияПФ.Знаменатель КАК Требуется,
	|
	|	0 КАК РаспределеноИзЗапасовОбособленно,
	|	0 КАК РаспределеноИзЗапасов,
	|	0 КАК РаспределеноИзПартийОбособленно,
	|	0 КАК РаспределеноИзПартий,
	|	ПродукцияПФ.КоэффициентДолиСтоимости	КАК КоэффициентДолиСтоимости,
	|	ПродукцияПФ.Числитель					КАК Числитель,
	|	ПродукцияПФ.Знаменатель					КАК Знаменатель
	|
	|ИЗ
	|	ПродукцияПФПредварительная КАК ПродукцияПФ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДинСтруктураПроизводствоНаСторонеВПромежуточныхЭтапах КАК ДД
	|		ПО ДД.КлючПартия = ПродукцияПФ.КлючПартия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)	КАК Склад,
	|	ДД.Период									КАК Период,
	|	ДД.Этап										КАК Этап,
	|	ПродукцияПФ.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
	|	ДД.Спецификация								КАК Спецификация,
	|	ДД.Подразделение							КАК Подразделение,
	|	ПродукцияПФ.ПартияСпецификацияПродукции		КАК ПартияСпецификацияПродукции,
	|	ПродукцияПФ.Продукция						КАК Продукция,
	|	ПродукцияПФ.ХарактеристикаПродукции			КАК ХарактеристикаПродукции,
	|	ПродукцияПФ.НазначениеПродукции				КАК НазначениеПродукции,
	|	ДД.ЭтоПолуфабрикат		КАК ЭтоПолуфабрикат,
	|	ДД.СтатьяКалькуляции	КАК СтатьяКалькуляции,
	|	ДД.Номенклатура			КАК Номенклатура,
	|	ДД.Характеристика		КАК Характеристика,
	|	ДД.Назначение			КАК Назначение,
	|	ДД.ЗаказНаПроизводство	КАК ЗаказНаПроизводство,
	|	&ПустойУИД				КАК КлючНоменклатура,
	|	ДД.КлючПартия			КАК КлючПартия,
	|
	|	ДД.Требуется
	// см. 1877.
	|		* ВЫБОР
	|			КОГДА ПродукцияПФ.КоличествоЗапущено = 0
	|				ТОГДА 1
	|			ИНАЧЕ ПродукцияПФ.КоличествоПоПартииСпецификации / ПродукцияПФ.КоличествоЗапущено
	|		КОНЕЦ								
	|		* ПродукцияПФ.Числитель / ПродукцияПФ.Знаменатель КАК Требуется,
	|
	|	0 КАК РаспределеноИзЗапасовОбособленно,
	|	0 КАК РаспределеноИзЗапасов,
	|	0 КАК РаспределеноИзПартийОбособленно,
	|	0 КАК РаспределеноИзПартий,
	|	ПродукцияПФ.КоэффициентДолиСтоимости	КАК КоэффициентДолиСтоимости,
	|	ПродукцияПФ.Числитель					КАК Числитель,
	|	ПродукцияПФ.Знаменатель					КАК Знаменатель
	|
	|ИЗ
	|	ПродукцияПФПредварительная КАК ПродукцияПФ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПобочныйВыходВЗаказеНаПроизводство КАК ДД
	|		ПО ДД.КлючПартия = ПродукцияПФ.КлючПартия
	|
	|";
	
	Если ИмяВТПродукцииПФ <> "ПроизводствоПродукции" Тогда //@Query-part
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПроизводствоПродукции", ИмяВТПродукцииПФ); //@Query-part
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область РазузлованиеЭтаповНаПроизводство

Процедура РазузлованиеЭтаповНаПроизводство(Запрос, ПараметрыРасчета)
	
	Запрос.Текст = ТекстЗапроса_ПервичныеДанныеДляРазузлованияЭтапов();
	
	Запрос.Выполнить();
	ПараметрыРасчета.Вставить("ТекстЗапроса_РазузлованиеЭтапов", ТекстЗапроса_РазузлованиеЭтапов());
	
	ВыпускающиеЭтапыПодКлючиПартий = Новый ТаблицаЗначений();
	ВыпускающиеЭтапыПодКлючиПартий.Колонки.Добавить("КлючПартия",
		Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(36)));
	ВыпускающиеЭтапыПодКлючиПартий.Колонки.Добавить("ВыпускающийЭтап",
		Новый ОписаниеТипов("ДокументСсылка.ЭтапПроизводства2_2"));
	ВыпускающиеЭтапыПодКлючиПартий.Колонки.Добавить("Числитель",
		ОбщегоНазначения.ОписаниеТипаЧисло(31, 6, ДопустимыйЗнак.Неотрицательный));
	ВыпускающиеЭтапыПодКлючиПартий.Колонки.Добавить("Знаменатель",
		ОбщегоНазначения.ОписаниеТипаЧисло(31, 6, ДопустимыйЗнак.Неотрицательный));
	ВыпускающиеЭтапыПодКлючиПартий.Колонки.Добавить("КоличествоПолуфабриката",
		ОбщегоНазначения.ОписаниеТипаЧисло(22, 7, ДопустимыйЗнак.Неотрицательный));
	ПараметрыРасчета.СтруктураДанных.Вставить("ВыпускающиеЭтапыПодКлючиПартий", ВыпускающиеЭтапыПодКлючиПартий);
	РазузловатьЭтапыНаПроизводство(Запрос, ПараметрыРасчета, Истина);
	
	ВыпускающиеЭтапыПодКлючиПартий.Свернуть("КлючПартия, ВыпускающийЭтап, Знаменатель", "Числитель, КоличествоПолуфабриката");
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(Запрос.МенеджерВременныхТаблиц,
		"ВыпускающиеЭтапыПодКлючиПартий", ВыпускающиеЭтапыПодКлючиПартий,
		"КлючПартия, ВыпускающийЭтап, Числитель, Знаменатель, КоличествоПолуфабриката", "ВыпускающийЭтап");
	
	// Заполняем данными по расходам из всех выпускающих этапов
	Запрос.Текст = "
	// 13 ПлановыеМатериальныеЗатраты
	|ВЫБРАТЬ
	|	ДД.ПериодЗатрат				КАК Период,
	|	ДД.ПериодЗатрат				КАК ПериодЗатрат,
	|	&Калькуляция				КАК Калькуляция,
	|	ДД.ВыпускающийЭтап			КАК ПартияСпецификация,
	|	ДД.Подразделение			КАК Подразделение,
	|	ДД.СтатьяКалькуляции		КАК СтатьяКалькуляции,
	|	ВЭКП.КлючПартия				КАК КлючПартия,
	|	ДД.Номенклатура				КАК Номенклатура,
	|	ДД.Характеристика			КАК Характеристика,
	|	ДД.ПроизводитсяВПроцессе И НЕ ДД.ПобочноеИзделие КАК ЭтоПолуфабрикат,
	|	ДД.План
	// Комментарий 1574.
	// Обрабатываем частичный выпуск, партия выпущена, в рамках одного этапа.
	// Все затраты материалов и трудозатрат в таком случае будут собираться тут МножителиПартийИПолуфабрикатов.РассчитатьМножителиПоФактическимДанным
	// Чтобы исключить ошибку по материалам, по не выпущенным партиям,
	// берем материалы по коэффициенту - Количество общего выпуска / Количество партия НЕ выпущена.
	|		* ВЭКП.Числитель / ВЭКП.Знаменатель КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	0 КАК СтоимостьРегл,
	|	0 КАК СтоимостьЗабалансовая,
	|	0 КАК СтоимостьЗабалансоваяРегл,
	|	
	|	""ПлановыеМатериальныеЗатраты"" КАК Комментарий
	|ИЗ
	|	ВыпускающиеЭтапыПодКлючиПартий КАК ВЭКП
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланМатериалов КАК ДД
	|			ПО ДД.ВыпускающийЭтап = ВЭКП.ВыпускающийЭтап
	|ГДЕ
	// См. комментарий 3145.
	|	НЕ ДД.ПромежуточныйВыход
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Период					КАК Период,
	|	ДД.Период					КАК ПериодЗатрат,
	|	&Калькуляция				КАК Калькуляция,
	|	ДД.ВыпускающийЭтап			КАК ПартияСпецификация,
	|	ДД.Подразделение			КАК Подразделение,
	|	ДД.СтатьяКалькуляции		КАК СтатьяКалькуляции,
	|	ВЭКП.КлючПартия				КАК КлючПартия,
	|	ДД.Номенклатура				КАК Номенклатура,
	|	ДД.Характеристика			КАК Характеристика,
	|	ЛОЖЬ						КАК ЭтоПолуфабрикат,
	|	ВЭКП.КоличествоПолуфабриката * ДД.КоличествоНаЕдиницуПартииВыпуска
		// см. 1574.
	|		* ВЭКП.Числитель / ВЭКП.Знаменатель КАК Количество,
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	0 КАК СтоимостьРегл,
	|	0 КАК СтоимостьЗабалансовая,
	|	0 КАК СтоимостьЗабалансоваяРегл,
	|	
	|	""ПлановыеМатериальныеЗатраты"" КАК Комментарий
	|
	|ИЗ ВыпускающиеЭтапыПодКлючиПартий КАК ВЭКП
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПроизводствоНаСторонеВПромежуточныхЭтапах КАК ДД
	|			ПО ДД.ВыпускающийЭтап = ВЭКП.ВыпускающийЭтап
	|;
	|////////////////////////////////////////////////////////////////////////////////
	// 14 ПлановыеТрудозатраты
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НЕ Трудозатраты.Выполнено
	|			ТОГДА ДокументЭтап.Дата
	|		КОГДА ДокументЭтап.ВыполнениеРаботОднойДатой
	|			ТОГДА ДокументЭтап.ДатаВыполненияРабот
	|		ИНАЧЕ Трудозатраты.ДатаВыполнения
	|	КОНЕЦ								КАК Период,
	|	ДокументЭтап.ВыпускающийЭтап		КАК ПартияСпецификация,
	|	Трудозатраты.Подразделение			КАК Подразделение,
	|	Трудозатраты.СтатьяКалькуляции		КАК СтатьяКалькуляции,
	|	ВЭКП.КлючПартия						КАК КлючПартия,
	|	Трудозатраты.ВидРабот				КАК ВидРабот,
	// План
	|	Трудозатраты.Количество * ВЭКП.Числитель / ВЭКП.Знаменатель КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ Трудозатраты.Выполнено
	|			ТОГДА ДокументЭтап.Дата
	|		КОГДА ДокументЭтап.ВыполнениеРаботОднойДатой
	|			ТОГДА ДокументЭтап.ДатаВыполненияРабот
	|		ИНАЧЕ Трудозатраты.ДатаВыполнения
	|	КОНЕЦ								КАК ПериодЗатрат,
	|	""Трудозатраты""					КАК Комментарий
	|
	|ИЗ
	|	ВыпускающиеЭтапыПодКлючиПартий КАК ВЭКП
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеквизитыЭтаповПартий КАК ДокументЭтап
	|		ПО ДокументЭтап.ВыпускающийЭтап = ВЭКП.ВыпускающийЭтап
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.Трудозатраты КАК Трудозатраты
	|		ПО (ДокументЭтап.Этап = Трудозатраты.Ссылка)
	|			И НЕ Трудозатраты.Отменено
	|			И НЕ ДокументЭтап.ПартияВыпущена
	|		
	|";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ПеренестиДанныеИзРезультатовЗапросов(МассивРезультатов, ПараметрыРасчета);
	ПереРассчитатьЗнаменателиПоНеВыпущеннымПартиям(ПараметрыРасчета);
	ДобавитьМножителиПоВозможнымПолуфабрикатам(ПараметрыРасчета);
	
КонецПроцедуры

// Для верного расчета стоимостей по не выпущенным партиям, необходимо пересчитать знаменатели.
Процедура ПереРассчитатьЗнаменателиПоНеВыпущеннымПартиям(ПараметрыРасчета)
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(Запрос.МенеджерВременныхТаблиц,
		"Множители", ПараметрыРасчета.СтруктураДанных.Множители,
		ПараметрыРасчета.Множители_КолонкиГруппировки + ", "+ ПараметрыРасчета.Множители_КолонкиСуммирования,
		"РасчетЗавершен,ПартияВыпущена"
	);
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	ДД.ПартияВыпущена КАК ПартияВыпущена,
		|	ВЫБОР КОГДА ДД.ПартияСпецификацияПолуфабриката = НЕОПРЕДЕЛЕНО
		|		ТОГДА ДД.ПартияСпецификацияПродукции
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ПартияСпецификацияПродукции,
		|	ДД.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	СУММА(ДД.Числитель) КАК Знаменатель,
		|	МАКСИМУМ(ДД.Порядок) КАК Порядок,
		|	ДД.КлючПартия
		|ПОМЕСТИТЬ ОбщиеЗнаменатели
		|ИЗ
		|	Множители КАК ДД
		|ГДЕ
		|	НЕ ДД.ПартияВыпущена
		|	И ВЫБОР
		|		КОГДА ДД.ПартияСпецификацияПолуфабриката = НЕОПРЕДЕЛЕНО
		|			ТОГДА ТИПЗНАЧЕНИЯ(ДД.ПартияСпецификацияПродукции) <> ТИП(Справочник.РесурсныеСпецификации)
		|		ИНАЧЕ
		|			ТИПЗНАЧЕНИЯ(ДД.ПартияСпецификацияПолуфабриката) <> ТИП(Справочник.РесурсныеСпецификации)
		|		КОНЕЦ
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.ПартияВыпущена,
		|	ВЫБОР КОГДА ДД.ПартияСпецификацияПолуфабриката = НЕОПРЕДЕЛЕНО
		|		ТОГДА ДД.ПартияСпецификацияПродукции
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ,
		|	ДД.ПартияСпецификацияПолуфабриката,
		|	ДД.КлючПартия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПартияВыпущена,
		|	ПартияСпецификацияПродукции,
		|	ПартияСпецификацияПолуфабриката,
		|	КлючПартия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Множители.Период КАК Период,
		|	Множители.Калькуляция КАК Калькуляция,
		|	Множители.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	Множители.Продукция КАК Продукция,
		|	Множители.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	Множители.Назначение КАК Назначение,
		|	Множители.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	Множители.КлючПартия КАК КлючПартия,
		|	Множители.Полуфабрикат КАК Полуфабрикат,
		|	Множители.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ЕСТЬNULL(ОбщиеЗнаменатели.Порядок, Множители.Порядок) КАК Порядок,
		|	ЕСТЬNULL(ОбщиеЗнаменатели.Знаменатель, Множители.Знаменатель) КАК Знаменатель,
		|	Множители.РасчетЗавершен КАК РасчетЗавершен,
		|	Множители.ПартияВыпущена КАК ПартияВыпущена,
		|	Множители.Числитель КАК Числитель,
		|	Множители.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
		|	Множители.Стоимость КАК Стоимость,
		|	Множители.СтоимостьБезНДС КАК СтоимостьБезНДС,
		|	Множители.СтоимостьРегл КАК СтоимостьРегл,
		|	Множители.Трудозатраты КАК Трудозатраты,
		|	Множители.ТрудозатратыРегл КАК ТрудозатратыРегл,
		|	Множители.ПостатейныеПостоянные КАК ПостатейныеПостоянные,
		|	Множители.ПостатейныеПостоянныеБезНДС КАК ПостатейныеПостоянныеБезНДС,
		|	Множители.ПостатейныеПостоянныеРегл КАК ПостатейныеПостоянныеРегл,
		|	Множители.ПостатейныеПеременные КАК ПостатейныеПеременные,
		|	Множители.ПостатейныеПеременныеБезНДС КАК ПостатейныеПеременныеБезНДС,
		|	Множители.ПостатейныеПеременныеРегл КАК ПостатейныеПеременныеРегл,
		|	Множители.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
		|	Множители.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл
		|ИЗ
		|	Множители КАК Множители
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОбщиеЗнаменатели КАК ОбщиеЗнаменатели
		|		ПО Множители.ПартияВыпущена = ОбщиеЗнаменатели.ПартияВыпущена
		|			И Множители.ПартияСпецификацияПолуфабриката = ОбщиеЗнаменатели.ПартияСпецификацияПолуфабриката
		|			И Множители.КлючПартия = ОбщиеЗнаменатели.КлючПартия
		|			И (ОбщиеЗнаменатели.ПартияСпецификацияПродукции = НЕОПРЕДЕЛЕНО
		|				ИЛИ Множители.ПартияСпецификацияПродукции = ОбщиеЗнаменатели.ПартияСпецификацияПродукции)";
	
	ПараметрыРасчета.СтруктураДанных.Множители = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура ДобавитьМножителиПоВозможнымПолуфабрикатам(ПараметрыРасчета)
	
	СтруктураДанных = ПараметрыРасчета.СтруктураДанных;
	
	Если СтруктураДанных.Движения.ПлановыеМатериальныеЗатраты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	ПлановыеМатериальныеЗатраты = СтруктураДанных.Движения.ПлановыеМатериальныеЗатраты;
	ПлановыеМатериальныеЗатраты.Колонки.Добавить("ИндексДанных", Новый ОписаниеТипов("Число"));
	ОбщегоНазначенияУТ.ПронумероватьТаблицуЗначений(ПлановыеМатериальныеЗатраты, "ИндексДанных");

	ВозможныеПФ = СтруктураДанных.Движения.ПлановыеМатериальныеЗатраты.НайтиСтроки(
		Новый Структура("ЭтоПолуфабрикат", Ложь));
	Материалы = СтруктураДанных.Движения.ПлановыеМатериальныеЗатраты.Скопировать(ВозможныеПФ);
	Материалы.Свернуть("Номенклатура, Характеристика");
	Материалы.Колонки.Добавить("ПроизводствоНаСтороне", Новый ОписаниеТипов("Булево"));
	Материалы.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	Материалы.ЗаполнитьЗначения(ПараметрыРасчета.РеквизитыПК.ПодразделениеДиспетчер, "Подразделение");

	ДанныеЗаказа = Новый Структура("ПодразделениеДиспетчер", ПараметрыРасчета.РеквизитыПК.ПодразделениеДиспетчер);
	СпособыПолученияМатериалов = ОбеспечениеПроизводства.СпособыПолученияМатериаловПоУмолчанию(
			ДанныеЗаказа, Материалы, , Перечисления.ВариантыЗаполненияОбеспеченияПроизводства.ПоСхемамОбеспечения);
	СпособыПолученияМатериалов.Колонки.Добавить("НачалоПроизводства", Новый ОписаниеТипов("Дата"));
	СпособыПолученияМатериалов.ЗаполнитьЗначения(ПараметрыРасчета.РеквизитыПК.Дата, "НачалоПроизводства");
	СпособыПолученияМатериалов.Колонки.Добавить("ИндексДанных", Новый ОписаниеТипов("Число"));
	ОбщегоНазначенияУТ.ПронумероватьТаблицуЗначений(СпособыПолученияМатериалов, "ИндексДанных");

	ВсеСпецификацииПФ = РегистрыСведений.МножителиПартийИПолуфабрикатов.ПолучитьСпецификацииНоменклатуры(
			СпособыПолученияМатериалов.Скопировать(Новый Структура("Запланировать", Истина)));
			
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(Запрос.МенеджерВременныхТаблиц,
		"СпособыПолученияМатериалов", СпособыПолученияМатериалов,
		"ИндексДанных,Номенклатура,Характеристика,Запланировать",
		"Запланировать");
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(Запрос.МенеджерВременныхТаблиц,
		"ПлановыеМатериальныеЗатраты", ПлановыеМатериальныеЗатраты,
		"ИндексДанных,Номенклатура,Характеристика,ПартияСпецификация,Подразделение,ЭтоПолуфабрикат,Количество",
		"Номенклатура,Характеристика");
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(Запрос.МенеджерВременныхТаблиц,
		"ВсеСпецификацииПФ", ВсеСпецификацииПФ,
		"ИндексДанных,Спецификация",
		"ИндексДанных");
	Множители_КолонкиГруппировки = ?(СтруктураДанных.Множители.Колонки.Найти("Приоритет") = Неопределено,
		ПараметрыРасчета.Множители_КолонкиГруппировкиБезПриоритета,
		ПараметрыРасчета.Множители_КолонкиГруппировки);
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(Запрос.МенеджерВременныхТаблиц,
		"Множители", СтруктураДанных.Множители,
		Множители_КолонкиГруппировки + ",Числитель",
		"ПартияСпецификацияПродукции");
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПлановыеМатериальныеЗатраты.Подразделение КАК Подразделение,
	|	ПлановыеМатериальныеЗатраты.ИндексДанных КАК ИД_ПлановыеМатериалы,
	|	ДД.Номенклатура КАК Полуфабрикат,
	|	ДД.Характеристика КАК ХарактеристикаПолуфабриката,
	|	ВсеСпецификацииПФ.Спецификация КАК СпецификацияПолуфабриката,
	|	ПлановыеМатериальныеЗатраты.ПартияСпецификация КАК ПартияСпецификация,
	|	ПлановыеМатериальныеЗатраты.Количество КАК КоличествоПолуфабриката
	|ПОМЕСТИТЬ МножителиПолуфабрикатов
	|ИЗ
	|	СпособыПолученияМатериалов КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеСпецификацииПФ КАК ВсеСпецификацииПФ
	|		ПО (ДД.Запланировать)
	|			И (ДД.ИндексДанных = ВсеСпецификацииПФ.ИндексДанных)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлановыеМатериальныеЗатраты КАК ПлановыеМатериальныеЗатраты
	|		ПО (НЕ ПлановыеМатериальныеЗатраты.ЭтоПолуфабрикат)
	|			И (ПлановыеМатериальныеЗатраты.Номенклатура = ДД.Номенклатура)
	|			И (ПлановыеМатериальныеЗатраты.Характеристика = ДД.Характеристика)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МножителиПолуфабрикатов.ИД_ПлановыеМатериалы КАК ИД_ПлановыеМатериалы,
	// Конвертация типа в строку будет выполнена автоматически при обработке выборки.
	|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(МножителиПолуфабрикатов.ХарактеристикаПолуфабриката) КАК КлючПартия
	|ИЗ
	|	МножителиПолуфабрикатов КАК МножителиПолуфабрикатов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Множители.Период КАК Период,
	|	Множители.Калькуляция КАК Калькуляция,
	|	МножителиПолуфабрикатов.Подразделение КАК Подразделение,
	|	Множители.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
	|	Множители.Продукция КАК Продукция,
	|	Множители.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	Множители.Назначение КАК Назначение,
	|	Множители.Порядок + 1 КАК Порядок,
	|	МножителиПолуфабрикатов.Полуфабрикат КАК Полуфабрикат,
	|	МножителиПолуфабрикатов.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
	// Конвертация типа в строку будет выполнена автоматически при обработке выборки.
	|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(МножителиПолуфабрикатов.ХарактеристикаПолуфабриката) КАК КлючПартия,
	|	МножителиПолуфабрикатов.СпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
	|	МножителиПолуфабрикатов.КоличествоПолуфабриката * Множители.Числитель / Множители.Знаменатель КАК КоличествоПолуфабриката,
	|	1 КАК Числитель,
	|	1 КАК Знаменатель,
	|	0 КАК Приоритет,
	|	ЛОЖЬ КАК ПартияВыпущена,
	|	ЛОЖЬ РасчетЗавершен
	|ИЗ
	|	МножителиПолуфабрикатов КАК МножителиПолуфабрикатов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Множители КАК Множители
	|		ПО МножителиПолуфабрикатов.ПартияСпецификация = Множители.ПартияСпецификацияПродукции
	|			И НЕ Множители.ПартияВыпущена
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Множители.Период КАК Период,
	|	Множители.Калькуляция КАК Калькуляция,
	|	МножителиПолуфабрикатов.Подразделение КАК Подразделение,
	|	Множители.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
	|	Множители.Продукция КАК Продукция,
	|	Множители.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	Множители.Назначение КАК Назначение,
	|	Множители.Порядок + 1 КАК Порядок,
	|	МножителиПолуфабрикатов.Полуфабрикат КАК Полуфабрикат,
	|	МножителиПолуфабрикатов.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
	// Конвертация типа в строку будет выполнена автоматически при обработке выборки.
	|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(МножителиПолуфабрикатов.ХарактеристикаПолуфабриката) КАК КлючПартия,
	|	МножителиПолуфабрикатов.СпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
	|	МножителиПолуфабрикатов.КоличествоПолуфабриката * Множители.Числитель / Множители.Знаменатель КАК КоличествоПолуфабриката,
	|	1 КАК Числитель,
	|	1 КАК Знаменатель,
	|	0 КАК Приоритет,
	|	ЛОЖЬ КАК ПартияВыпущена,
	|	ЛОЖЬ РасчетЗавершен
	|ИЗ
	|	МножителиПолуфабрикатов КАК МножителиПолуфабрикатов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Множители КАК Множители
	|		ПО МножителиПолуфабрикатов.ПартияСпецификация = Множители.ПартияСпецификацияПолуфабриката
	|			И НЕ Множители.ПартияВыпущена
	|";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	МатериалыПолуфабрикаты = РезультатыЗапроса[1].Выбрать();
	НовыеМножителиПолуфабрикатов = РезультатыЗапроса[2].Выбрать();
	
	Пока МатериалыПолуфабрикаты.Следующий() Цикл
		ПлановыеМатериальныеЗатраты[МатериалыПолуфабрикаты.ИД_ПлановыеМатериалы].ЭтоПолуфабрикат = Истина;
	КонецЦикла;
	
	Пока НовыеМножителиПолуфабрикатов.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(СтруктураДанных.Множители.Добавить(), НовыеМножителиПолуфабрикатов);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПодготовитьДанныеПодРазузлованиеЭтапов(Запрос, ПараметрыРасчета, ПервыйШаг)
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ДД.ПотребляющийЭтап				КАК ПотребляющийЭтап,
	|	ДД.СписатьНаРасходы				КАК СписатьНаРасходы,
	|	ДД.ВыходныеИзделия_ИД			КАК ВыходныеИзделия_ИД,
	|	АВТОНОМЕРЗАПИСИ()				КАК ИдентификаторСтроки,
	|	ДД.ГПВЭтапе						КАК ГПВЭтапе,
	|	ДД.ВыходныеИзделия_ИД			КАК ИД,
	|	ДД.СпецификацияПолуфабриката	КАК Спецификация,
	|	ДД.ЗаказНаПроизводство			КАК ЗаказНаПроизводство,
	|	ДД.Период						КАК Период,
	|	ДД.Продукция					КАК Продукция,
	|	ДД.ХарактеристикаПродукции		КАК ХарактеристикаПродукции,
	|	ДД.НазначениеПродукции			КАК НазначениеПродукции,
	|	ДД.ПартияСпецификацияПродукции	КАК ПартияСпецификацияПродукции,
	|	ДД.Полуфабрикат					КАК Полуфабрикат,
	|	ДД.ХарактеристикаПолуфабриката	КАК ХарактеристикаПолуфабриката,
	|	ДД.ПартияСпецификацияПолуфабриката	КАК ПартияСпецификацияПолуфабриката,
	|	ДД.КоличествоТребуется			КАК КоличествоПолуфабриката,
	|	ДД.КлючПартия					КАК КлючПартия
	|
	|ПОМЕСТИТЬ МножителиПФПроизводимыхВПроцессеПредварительная
	|ИЗ
	|	ВыпускиПФВРамкахЗаказа КАК ДД
	|
	|ГДЕ
	|	ДД.КоличествоТребуется > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыходныеИзделия.ИдентификаторСтроки	КАК Источник_ИД,
	|	ДД.ИдентификаторСтроки				КАК Приемник_ИД
	|ИЗ
	|	МножителиПФПроизводимыхВПроцессеПредварительная КАК ДД
	//		В таблице выходных изделий пытаемся найти выпуск ПФ который добавили к разузлованию на предыдущем шаге рекурсии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВыходныеИзделия КАК ВыходныеИзделия
	|		ПО
	|			ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(ДД.ПартияСпецификацияПолуфабриката) = ТИП(Документ.ЭтапПроизводства2_2)
	|					ТОГДА ДД.ПартияСпецификацияПолуфабриката = ВыходныеИзделия.ВыпускающийЭтап
	|					ИНАЧЕ ДД.Спецификация = ВыходныеИзделия.Спецификация
	|							И ДД.ЗаказНаПроизводство = ВыходныеИзделия.ЗаказНаПроизводство
	|			КОНЕЦ
	|
	|			И ВЫБОР КОГДА ДД.ГПВЭтапе ТОГДА
	|				(ДД.Продукция = ВыходныеИзделия.Номенклатура)
	|				И (ДД.ХарактеристикаПродукции = ВыходныеИзделия.Характеристика)
	|				И НЕ ВыходныеИзделия.ПартияВыпущена
	|			ИНАЧЕ
	|				(ДД.Полуфабрикат = ВыходныеИзделия.Номенклатура)
	|				И (ДД.ХарактеристикаПолуфабриката = ВыходныеИзделия.Характеристика)
	|			КОНЕЦ
	|
	|			И ((ДД.НазначениеПродукции = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|					И ВыходныеИзделия.НазначениеПустое)
	|				ИЛИ (ДД.НазначениеПродукции = ВыходныеИзделия.Назначение)
	|				ИЛИ (ДД.ПотребляющийЭтап = ВыходныеИзделия.ЗаказИзНазначения)
	|				)
	|			И ДД.СписатьНаРасходы = ВыходныеИзделия.СписатьНаРасходы
	|ГДЕ
	|	ДД.ВыходныеИзделия_ИД = -1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.СписатьНаРасходы				КАК СписатьНаРасходы,
	|	ДД.ВыходныеИзделия_ИД			КАК ВыходныеИзделия_ИД,
	|	ДД.ИдентификаторСтроки			КАК ИдентификаторСтроки,
	|	ДД.ГПВЭтапе						КАК ГПВЭтапе,
	|	ДД.ИД							КАК ИД,
	|	ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка) КАК ВыпускающийЭтап,
	|	ЛОЖЬ							КАК ПроизводствоНаСтороне,
	|	ДД.Спецификация					КАК Спецификация,
	|	ДД.ЗаказНаПроизводство			КАК ЗаказНаПроизводство,
	|	ДД.Период						КАК Период,
	|	ДД.Продукция					КАК Продукция,
	|	ДД.ХарактеристикаПродукции		КАК ХарактеристикаПродукции,
	|	ДД.НазначениеПродукции			КАК НазначениеПродукции,
	|	ДД.ПартияСпецификацияПродукции	КАК ПартияСпецификацияПродукции,
	|	ДД.Полуфабрикат					КАК Полуфабрикат,
	|	ДД.ХарактеристикаПолуфабриката	КАК ХарактеристикаПолуфабриката,
	|	ДД.ПартияСпецификацияПолуфабриката	КАК ПартияСпецификацияПолуфабриката,
	|	ДД.КоличествоПолуфабриката			КАК КоличествоПолуфабриката,
	|	1								КАК Числитель,
	|	1								КАК Знаменатель,
	|	ЛОЖЬ							КАК ПартияВыпущена,
	// Во время разузловки этапов можно встретить ПФ, для которого нет выпускающего этапа в тек. структуре заказа на производство
	// В таком случае, пока, разузловка через спецификацию.
	|	ЛОЖЬ							КАК ЕстьВыпускающийЭтап,
	|	ДД.КлючПартия					КАК КлючПартия
	|ИЗ
	|	МножителиПФПроизводимыхВПроцессеПредварительная КАК ДД
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДД.Период
	|";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ИндексПоследнегоЗапроса = РезультатыЗапроса.Количество() - 1;
	МножителиПФПроизводимыхВПроцессеПредварительная = РезультатыЗапроса[ИндексПоследнегоЗапроса].Выгрузить();
	ИсточникиПриемники = РезультатыЗапроса[ИндексПоследнегоЗапроса - 1].Выгрузить();
	ИсточникиПриемники.Индексы.Добавить("Приемник_ИД");
	
	МножителиПФПроизводимыхВПроцессе = МножителиПФПроизводимыхВПроцессеПредварительная.СкопироватьКолонки();
	Для Каждого Стр Из МножителиПФПроизводимыхВПроцессеПредварительная Цикл
		
		Если Стр.ВыходныеИзделия_ИД = -1 Тогда
			КоличествоНеобходимо = Стр.КоличествоПолуфабриката;
			ПодходящиеИсточники = ИсточникиПриемники.НайтиСтроки(Новый Структура("Приемник_ИД", Стр.ИдентификаторСтроки));
			Если ПодходящиеИсточники.Количество() = 0 Тогда
				// Нет подходящего выпуска ПФ, разузловываем через спецификацию.
				СтрокаСпецификации = МножителиПФПроизводимыхВПроцессе.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаСпецификации, Стр);
				СтрокаСпецификации.КоличествоПолуфабриката = КоличествоНеобходимо;
				СтрокаСпецификации.ПартияСпецификацияПолуфабриката = Стр.Спецификация;
				
				Продолжить;
			КонецЕсли;
			
			Для Каждого Источник Из ПодходящиеИсточники Цикл
				СтрокаОстатка = ПараметрыРасчета.СтруктураДанных.ВыходныеИзделия.Найти(Источник.Источник_ИД,
					"ИдентификаторСтроки");
				
				КоличествоИзЭтапа = Мин(СтрокаОстатка.КоличествоВыпускОстаток, КоличествоНеобходимо);
				СтрокаОстатка.КоличествоВыпускОстаток = СтрокаОстатка.КоличествоВыпускОстаток - КоличествоИзЭтапа;
				КоличествоНеобходимо = КоличествоНеобходимо - КоличествоИзЭтапа;
				
				Если КоличествоИзЭтапа > 0 Тогда
					СтрокаДляЭтапа = МножителиПФПроизводимыхВПроцессе.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаДляЭтапа, Стр);
					
					СтрокаДляЭтапа.КоличествоПолуфабриката = КоличествоИзЭтапа;
					СтрокаДляЭтапа.Знаменатель = СтрокаОстатка.Знаменатель;
					СтрокаДляЭтапа.Числитель = КоличествоИзЭтапа * СтрокаОстатка.Числитель / СтрокаОстатка.КоличествоВыпуск;
					
					// В Стр все значения под вариант когда этапа нет, поэтому меняем на значения строки остатков партии.
					СтрокаДляЭтапа.ПартияВыпущена = СтрокаОстатка.ПартияВыпущена;
					СтрокаДляЭтапа.ЕстьВыпускающийЭтап = Истина;
					СтрокаДляЭтапа.ПартияСпецификацияПолуфабриката = СтрокаОстатка.ВыпускающийЭтап;
					СтрокаДляЭтапа.ПроизводствоНаСтороне = СтрокаОстатка.ПроизводствоНаСтороне;
					СтрокаДляЭтапа.Спецификация = СтрокаОстатка.Спецификация;
				КонецЕсли;
				
				Если КоличествоНеобходимо = 0 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			// ВыпускиПФВРамкахЗаказа первично формируется при разузловке Дин. структуры.
			// ВыходныеИзделия_ИД определен, значит Партия уже подобрана.
			// Расходуемое количество уже списано из остатка партии.
			СтрокаОстатка = ПараметрыРасчета.СтруктураДанных.ВыходныеИзделия.Найти(Стр.ВыходныеИзделия_ИД,
					"ИдентификаторСтроки");
			
			КоличествоИзЭтапа = Стр.КоличествоПолуфабриката;
			КоличествоНеобходимо = 0;
			
			СтрокаДляЭтапа = МножителиПФПроизводимыхВПроцессе.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДляЭтапа, Стр);
					
			СтрокаДляЭтапа.КоличествоПолуфабриката = КоличествоИзЭтапа;
			СтрокаДляЭтапа.Знаменатель = СтрокаОстатка.Знаменатель;
			СтрокаДляЭтапа.Числитель = КоличествоИзЭтапа * СтрокаОстатка.Числитель / СтрокаОстатка.КоличествоВыпуск;
			
			СтрокаДляЭтапа.ПартияВыпущена = СтрокаОстатка.ПартияВыпущена;
			СтрокаДляЭтапа.ЕстьВыпускающийЭтап = Истина;
			СтрокаДляЭтапа.ПартияСпецификацияПолуфабриката = СтрокаОстатка.ВыпускающийЭтап;
			СтрокаДляЭтапа.ПроизводствоНаСтороне = СтрокаОстатка.ПроизводствоНаСтороне;
			СтрокаДляЭтапа.Спецификация = СтрокаОстатка.Спецификация;
		КонецЕсли;
		
		Если КоличествоНеобходимо > 0 Тогда
			// Количество в источниках не хватило, разузловка остатка через спецификацию.
			СтрокаСпецификации = МножителиПФПроизводимыхВПроцессе.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаСпецификации, Стр);
			СтрокаСпецификации.КоличествоПолуфабриката = КоличествоНеобходимо;
			СтрокаСпецификации.ПартияСпецификацияПолуфабриката = Стр.Спецификация;
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("МножителиПФПроизводимыхВПроцессе", МножителиПФПроизводимыхВПроцессе);
	Запрос.Текст = "
	|УНИЧТОЖИТЬ МножителиПФПроизводимыхВПроцессеПредварительная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.СписатьНаРасходы				КАК СписатьНаРасходы,
	|	ДД.ВыпускающийЭтап				КАК ВыпускающийЭтап,
	|	ДД.ГПВЭтапе						КАК ГПВЭтапе,
	|	ДД.ПроизводствоНаСтороне		КАК ПроизводствоНаСтороне,
	|	ДД.Спецификация					КАК Спецификация,
	|	ДД.ЗаказНаПроизводство			КАК ЗаказНаПроизводство,
	|	ДД.Период						КАК Период,
	|	ДД.Продукция					КАК Продукция,
	|	ДД.ХарактеристикаПродукции		КАК ХарактеристикаПродукции,
	|	ДД.НазначениеПродукции			КАК НазначениеПродукции,
	|	ДД.ПартияСпецификацияПродукции	КАК ПартияСпецификацияПродукции,
	|	ДД.Полуфабрикат					КАК Полуфабрикат,
	|	ДД.ХарактеристикаПолуфабриката	КАК ХарактеристикаПолуфабриката,
	|	ДД.ПартияСпецификацияПолуфабриката	КАК ПартияСпецификацияПолуфабриката,
	|	ДД.КоличествоПолуфабриката			КАК КоличествоПолуфабриката,
	|	ДД.Числитель						КАК Числитель,
	|	ДД.Знаменатель						КАК Знаменатель,
	|	ДД.ПартияВыпущена					КАК ПартияВыпущена,
	|	ДД.ЕстьВыпускающийЭтап				КАК ЕстьВыпускающийЭтап,
	|	ДД.КлючПартия						КАК КлючПартия
	|
	|ПОМЕСТИТЬ МножителиПФПроизводимыхВПроцессе
	|ИЗ &МножителиПФПроизводимыхВПроцессе КАК ДД
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура РазузловатьЭтапыНаПроизводство(Запрос, ПараметрыРасчета, ПервыйШаг)
	
	СтруктураДанных = ПараметрыРасчета.СтруктураДанных;
	СтруктураДанных.Порядок = СтруктураДанных.Порядок + 1;
	
	ПодготовитьДанныеПодРазузлованиеЭтапов(Запрос, ПараметрыРасчета, ПервыйШаг);
	Запрос.Текст = ПараметрыРасчета.ТекстЗапроса_РазузлованиеЭтапов;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ИндексПоследнегоЗапроса = МассивРезультатов.Количество() - 1;
	
	// 10 Множители по полуфабрикатам производимым процессе.
	ВыборкаМножителей = МассивРезультатов[ИндексПоследнегоЗапроса].Выбрать();
	Пока ВыборкаМножителей.Следующий() Цикл

		// Если продукция заказа уже в созданном этапе, строка есть в ТЗ множители, добавлять еще одну не нужно.
		Если Не ВыборкаМножителей.ГПВЭтапе Тогда
			НовыйМножитель = СтруктураДанных.Множители.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйМножитель, ВыборкаМножителей);
			НовыйМножитель.Порядок = СтруктураДанных.Порядок;
		КонецЕсли;
		
		Если ВыборкаМножителей.ПроизводствоНаСтороне И Не ВыборкаМножителей.ПартияВыпущена Тогда
			// 1391 Если есть выпуск ПФ через переработку, разузловываем через спецификацию,
			// т.к. в этапе только материалы которые переданы переработчику и нет услуг переработки,
			// цена которых должна включать возможные материалы переработчика.
			// Если нет спецификации на данный ПФ, тогда должны быть введены цены номенклатуры.
			НовыйМножитель.РасчетЗавершен = Ложь;
			НовыйМножитель.ПартияСпецификацияПолуфабриката = ВыборкаМножителей.СпецификацияПолуфабриката;
		Иначе
			Если ВыборкаМножителей.ПартияВыпущена Тогда
				НовыйМножитель.Порядок = -100;
			Иначе
				// Для получения первичных затрат(не ПФ) добавляем этап в таблицу выпускающих этапов.
				НовыйВЭКП = СтруктураДанных.ВыпускающиеЭтапыПодКлючиПартий.Добавить();
				НовыйВЭКП.ВыпускающийЭтап = ВыборкаМножителей.ПартияСпецификацияПолуфабриката;
				НовыйВЭКП.КлючПартия = ВыборкаМножителей.КлючПартия;
				НовыйВЭКП.Числитель = ВыборкаМножителей.Числитель;
				НовыйВЭКП.Знаменатель = ВыборкаМножителей.Знаменатель;
				НовыйВЭКП.КоличествоПолуфабриката = ВыборкаМножителей.КоличествоПолуфабриката;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураДанных = ПараметрыРасчета.СтруктураДанных;
	СтруктураДанных.ВыпускиПФВРамкахЗаказа.Очистить();
	// 15 ВыпускиПФВРамкахЗаказа Использование ПФ В Этапах производства
	ПФВОбеспеченииЭтапов = МассивРезультатов[ИндексПоследнегоЗапроса - 1].Выбрать();
	Пока ПФВОбеспеченииЭтапов.Следующий() Цикл
		Если ЗначениеЗаполнено(ПФВОбеспеченииЭтапов.ВыпускающийЭтап) Тогда
			ЗаполнитьЗначенияСвойств(СтруктураДанных.ВыпускиПФВРамкахЗаказа.Добавить(), ПФВОбеспеченииЭтапов);
		Иначе
			// Если ПФ не выпускается в рамках заказа
			// тогда разузловка через спецификацию тут - МножителиПартийИПолуфабрикатов.РазузловатьСпецификациюИзделий
			НовыйМножитель = СтруктураДанных.Множители.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйМножитель, ПФВОбеспеченииЭтапов);
			НовыйМножитель.РасчетЗавершен = Ложь;
			НовыйМножитель.Числитель = 1;
			НовыйМножитель.Знаменатель = 1;
			НовыйМножитель.КоличествоПолуфабриката = ПФВОбеспеченииЭтапов.КоличествоТребуется;
		КонецЕсли;
	КонецЦикла;
	
	Если СтруктураДанных.ВыпускиПФВРамкахЗаказа.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = "УНИЧТОЖИТЬ ВыпускиПФВРамкахЗаказа";
	Запрос.Выполнить();
	
	ПоместитьВыпускиПФВРамкахЗаказаВВТ(Запрос, СтруктураДанных.ВыпускиПФВРамкахЗаказа);
	
	Запрос.Текст = "УНИЧТОЖИТЬ МножителиПФПроизводимыхВПроцессе";
	Запрос.Выполнить();
	
	РазузловатьЭтапыНаПроизводство(Запрос, ПараметрыРасчета, Ложь);
	
КонецПроцедуры

Процедура ПоместитьВыпускиПФВРамкахЗаказаВВТ(Запрос, ВыпускиПФВРамкахЗаказа)
	
	Запрос.УстановитьПараметр("Таблица", ВыпускиПФВРамкахЗаказа);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Т.СписатьНаРасходы					КАК СписатьНаРасходы,
	|	Т.ГПВЭтапе							КАК ГПВЭтапе,
	|	Т.ВыходныеИзделия_ИД				КАК ВыходныеИзделия_ИД,
	|	Т.ЗаказНаПроизводство				КАК ЗаказНаПроизводство,
	|	Т.ВыпускающийЭтап					КАК ВыпускающийЭтап,
	|	Т.ПотребляющийЭтап					КАК ПотребляющийЭтап,
	|	Т.Продукция							КАК Продукция,
	|	Т.ХарактеристикаПродукции			КАК ХарактеристикаПродукции,
	|	Т.НазначениеПродукции				КАК НазначениеПродукции,
	|	Т.ПартияСпецификацияПродукции		КАК ПартияСпецификацияПродукции,
	|	Т.Полуфабрикат						КАК Полуфабрикат,
	|	Т.ХарактеристикаПолуфабриката		КАК ХарактеристикаПолуфабриката,
	|	Т.ПартияСпецификацияПолуфабриката	КАК ПартияСпецификацияПолуфабриката,
	|	Т.СпецификацияПолуфабриката			КАК СпецификацияПолуфабриката,
	|	Т.КлючПартия						КАК КлючПартия,
	|	Т.ПартияВыпущена					КАК ПартияВыпущена,
	|
	|	Т.Период							КАК Период,
	|	Т.КоличествоТребуется				КАК КоличествоТребуется
	|ПОМЕСТИТЬ ВыпускиПФВРамкахЗаказа
	|ИЗ
	|	&Таблица КАК Т";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ТекстЗапроса_ПервичныеДанныеДляРазузлованияЭтапов()
	Возврат "
	// План материалов и отходов
	|ВЫБРАТЬ
	|	ДД.ПериодЗатрат				КАК ПериодЗатрат,
	|	ДД.ВыпускающийЭтап			КАК ВыпускающийЭтап,
	|	ДД.Этап						КАК ПотребляющийЭтап,
	|	МАКСИМУМ(ДД.ПроизводствоНаСтороне) КАК ПотребляющийЭтап_ПроизводствоНаСтороне,
	|	ДД.Организация				КАК Организация,
	|	ДД.ПартияПроизводства		КАК ПартияПроизводства,
	|	ДД.Номенклатура				КАК Номенклатура,
	|	ДД.ПроизводитсяВПроцессе	КАК ПроизводитсяВПроцессе,
	|	ДД.ПромежуточныйВыход		КАК ПромежуточныйВыход,
	|	ДД.ПобочноеИзделие			КАК ПобочноеИзделие,
	|	ДД.Спецификация				КАК Спецификация,
	|	ДД.Характеристика			КАК Характеристика,
	|	ДД.Подразделение			КАК Подразделение,
	|	ДД.Назначение				КАК Назначение,
	|	ДД.СтатьяКалькуляции		КАК СтатьяКалькуляции,
	|	СУММА(ДД.План * ДД.КоэффициентПриведения) КАК План
	|ПОМЕСТИТЬ ПредварительныйПланМатериалов
	|ИЗ
	|	(
	// план материалов этапов
	|	ВЫБРАТЬ
	|		ВЫБОР КОГДА ДД.ОтгружатьОднойДатой ТОГДА
	|			ДД.ДатаОтгрузки
	|		ИНАЧЕ
	|			Обеспечение.ДатаОтгрузки
	|		КОНЕЦ										КАК ПериодЗатрат,
	|		ДД.ВыпускающийЭтап							КАК ВыпускающийЭтап,
	|		ДД.Этап										КАК Этап,
	|		ДД.ПроизводствоНаСтороне					КАК ПроизводствоНаСтороне,
	|		ДД.Организация								КАК Организация,
	|		ДД.ПартияПроизводства						КАК ПартияПроизводства,
	|		Обеспечение.Номенклатура					КАК Номенклатура,
	|		Обеспечение.Характеристика					КАК Характеристика,
	|		Обеспечение.Подразделение					КАК Подразделение,
	|		ВЫБОР
	|			КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|				ТОГДА Обеспечение.Назначение
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		КОНЕЦ										КАК Назначение,
	|		Обеспечение.СтатьяКалькуляции				КАК СтатьяКалькуляции,
	|		Обеспечение.Количество						КАК План,
	|		1											КАК КоэффициентПриведения,
	|		Обеспечение.Спецификация <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка) КАК ПроизводитсяВПроцессе,
	|		ЛОЖЬ										КАК ПромежуточныйВыход,
	|		ЛОЖЬ										КАК ПобочноеИзделие,
	|		Обеспечение.Спецификация					КАК Спецификация
	|	ИЗ
	|		РеквизитыЭтаповПартий КАК ДД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК Обеспечение
	|			ПО ДД.Этап = Обеспечение.Ссылка
	|
	|	ГДЕ
	// Данные переработки выпускающего этапа на стороне исключаются, т.к. движения формируются отчетом без распределения.
	|		НЕ ДД.ПроизводствоНаСтороне
	|		И НЕ Обеспечение.Отменено
	|		И НЕ (ДД.ЭтоВыпускающийЭтап И ДД.ПартияВыпущена)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// план материалов этапов, выполняющихся на стороне (переработка 2.5 по этапам)
	|	ВЫБРАТЬ
	|		ВЫБОР КОГДА ДД.ОтгружатьОднойДатой ТОГДА
	|			ДД.ДатаОтгрузки
	|		ИНАЧЕ
	|			Обеспечение.ДатаОтгрузки
	|		КОНЕЦ													КАК ПериодЗатрат,
	|		ДД.ВыпускающийЭтап										КАК ВыпускающийЭтап,
	|		ДД.Этап													КАК Этап,
	|		ДД.ПроизводствоНаСтороне								КАК ПроизводствоНаСтороне,
	|		ДД.Организация											КАК Организация,
	|		ДД.ПартияПроизводства									КАК ПартияПроизводства,
	|		Обеспечение.Номенклатура								КАК Номенклатура,
	|		Обеспечение.Характеристика								КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)	КАК Подразделение,
	|		ВЫБОР
	|			КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|				ТОГДА Обеспечение.Назначение
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		КОНЕЦ													КАК Назначение,
	|		Обеспечение.СтатьяКалькуляции							КАК СтатьяКалькуляции,
	|		Обеспечение.Количество									КАК План,
	|		1														КАК КоэффициентПриведения,
	|		Обеспечение.Спецификация <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка) КАК ПроизводитсяВПроцессе,
	|		ЛОЖЬ													КАК ПромежуточныйВыход,
	|		ЛОЖЬ													КАК ПобочноеИзделие,
	|		Обеспечение.Спецификация								КАК Спецификация
	|	ИЗ
	|		РеквизитыЭтаповПартий КАК ДД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК Обеспечение
	|			ПО ДД.Этап = Обеспечение.Ссылка
	|
	|	ГДЕ
	|		Обеспечение.Ссылка.ПроизводствоНаСтороне2_5
	|		И НЕ Обеспечение.Отменено
	|		И НЕ (ДД.ЭтоВыпускающийЭтап И ДД.ПартияВыпущена)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|
	// план отходов этапов цепочки
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ДД.ПроизводствоОднойДатой
	|					ИЛИ ТаблицаТовары.ДатаПроизводства = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ДД.ДатаПроизводства
	|			ИНАЧЕ ТаблицаТовары.ДатаПроизводства
	|		КОНЕЦ							КАК ПериодЗатрат,
	|		ДД.ВыпускающийЭтап				КАК ВыпускающийЭтап,
	|		ДД.Этап							КАК Этап,
	|		ДД.ПроизводствоНаСтороне		КАК ПроизводствоНаСтороне,
	|		ДД.Организация					КАК Организация,
	|		ДД.ПартияПроизводства			КАК ПартияПроизводства,
	|		ТаблицаТовары.Номенклатура		КАК Номенклатура,
	|		ТаблицаТовары.Характеристика	КАК Характеристика,
	|		ТаблицаТовары.Подразделение		КАК Подразделение,
	|		ВЫБОР
	|			КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|				ТОГДА ТаблицаТовары.Назначение
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		КОНЕЦ							КАК Назначение,
	|		ТаблицаТовары.СтатьяКалькуляции	КАК СтатьяКалькуляции,
	|		ТаблицаТовары.Количество		КАК План,
	|		-1								КАК КоэффициентПриведения,
	|		ИСТИНА							КАК ПроизводитсяВПроцессе,
	|		&УчитыватьСебестоимостьТоваровПоНазначениям
	|			И ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА
	|								ИЗ НазначенияПодЭтапыЗаказаНаПроизводство КАК НЭЗП
	|								ГДЕ НЭЗП.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
	|									И НЭЗП.НазначениеСсылка = ТаблицаТовары.Назначение) КАК ПромежуточныйВыход,
	|		ИСТИНА							КАК ПобочноеИзделие,
	|			
	|		ТаблицаТовары.Спецификация		КАК Спецификация
	|	ИЗ
	|		РеквизитыЭтаповПартий КАК ДД
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ТаблицаТовары
	|			ПО ДД.Этап = ТаблицаТовары.Ссылка
	|
	|	ГДЕ
	// Данные переработки выпускающего этапа на стороне исключаются, т.к. движения формируются отчетом без распределения.
	|		НЕ ДД.ПроизводствоНаСтороне
	|		И НЕ ТаблицаТовары.Отменено
	|		И НЕ (ДД.ЭтоВыпускающийЭтап И ДД.ПартияВыпущена)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// план отходов этапов других цепочек
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ДД.ПроизводствоОднойДатой
	|					ИЛИ ТаблицаТовары.ДатаПроизводства = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ДД.ДатаПроизводства
	|			ИНАЧЕ ТаблицаТовары.ДатаПроизводства
	|		КОНЕЦ							КАК ПериодЗатрат,
	|		ДД.ВыпускающийЭтап				КАК ВыпускающийЭтап,
	|		ДД.Этап							КАК Этап,
	|		ДД.ПроизводствоНаСтороне		КАК ПроизводствоНаСтороне,
	|		ДД.Организация					КАК Организация,
	|		ДД.ПартияПроизводства			КАК ПартияПроизводства,
	|		ТаблицаТовары.Номенклатура		КАК Номенклатура,
	|		ТаблицаТовары.Характеристика	КАК Характеристика,
	|		ТаблицаТовары.Подразделение		КАК Подразделение,
	|		ВЫБОР
	|			КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям
	|				ТОГДА ТаблицаТовары.Назначение
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		КОНЕЦ							КАК Назначение,
	|		ТаблицаТовары.СтатьяКалькуляции	КАК СтатьяКалькуляции,
	|		ТаблицаТовары.Количество		КАК План,
	|		-1								КАК КоэффициентПриведения,
	|		ИСТИНА							КАК ПроизводитсяВПроцессе,
	|		&УчитыватьСебестоимостьТоваровПоНазначениям
	|			И ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА
	|									ИЗ НазначенияПодЭтапыЗаказаНаПроизводство КАК НЭЗП
	|									ГДЕ НЭЗП.ЗаказНаПроизводство = ДД.ЗаказНаПроизводство
	|										И НЭЗП.НазначениеСсылка = ТаблицаТовары.Назначение) КАК ПромежуточныйВыход,
	|		ИСТИНА							КАК ПобочноеИзделие,
	|		ТаблицаТовары.Спецификация		КАК Спецификация
	|	ИЗ
	|		РеквизитыЭтаповПартий КАК ДД
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ТаблицаТовары
	|			ПО ДД.Этап = ТаблицаТовары.ЭтапПотребитель
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапыДругихЦепочек
	|			ПО ЭтапыДругихЦепочек.Ссылка = ТаблицаТовары.Ссылка
	|
	|	ГДЕ
	// Данные переработки выпускающего этапа на стороне исключаются, т.к. движения формируются отчетом без распределения.
	|		НЕ ДД.ПроизводствоНаСтороне
	|		И НЕ ТаблицаТовары.Отменено
	|		И НЕ ДД.ПартияПроизводства = ЭтапыДругихЦепочек.ПартияПроизводства
	|		И НЕ (ДД.ЭтоВыпускающийЭтап И ДД.ПартияВыпущена)
	|	
	|) КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.ПериодЗатрат,
	|	ДД.ВыпускающийЭтап,
	|	ДД.Этап,
	|	ДД.Организация,
	|	ДД.ПартияПроизводства,
	|	ДД.Номенклатура,
	|	ДД.ПроизводитсяВПроцессе,
	|	ДД.ПобочноеИзделие,
	|	ДД.ПромежуточныйВыход,
	|	ДД.Спецификация,
	|	ДД.Характеристика,
	|	ДД.Подразделение,
	|	ДД.Назначение,
	|	ДД.СтатьяКалькуляции
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВыпускающийЭтап,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	ПромежуточныйВыход
	|;
	|
	|ВЫБРАТЬ
	|	ДД.ПериодЗатрат			КАК ПериодЗатрат,
	// См. комментарий 211.
	|	ДД.ВыпускающийЭтап		КАК ВыпускающийЭтап,
	// См. комментарий 218.
	|	ДД.ПотребляющийЭтап		КАК ПотребляющийЭтап,
	|	ДД.ПотребляющийЭтап_ПроизводствоНаСтороне КАК ПотребляющийЭтап_ПроизводствоНаСтороне,
	|	ДД.Организация			КАК Организация,
	|	ДД.ПартияПроизводства	КАК ПартияПроизводства,
	|	ДД.Номенклатура			КАК Номенклатура,
	|	ДД.ПроизводитсяВПроцессе ИЛИ ЕСТЬNULL(КэшНСИ.Производится, ЛОЖЬ) КАК ПроизводитсяВПроцессе,
	|	ДД.ПобочноеИзделие		КАК ПобочноеИзделие,
	|	ДД.ПромежуточныйВыход
	// Комментарий 3145.
	// Для определения и исключения из материалов, промежуточного выхода в ТЧ ОбеспечениеМатериаламиИРаботами,
	// сделаем поиск номенклатуры, характеристики, с определенным фактом промежуточного выхода и тем же назначением.
	// При этом чтобы исключить ПФ производимый в процессе из промежуточного выхода, добавим фильтр НЕ ПроизводитсяВПроцессе.
	|		ИЛИ (НЕ (ДД.ПроизводитсяВПроцессе ИЛИ ЕСТЬNULL(КэшНСИ.Производится, ЛОЖЬ))
	|				И ИСТИНА В
	|					(ВЫБРАТЬ ПЕРВЫЕ 1
	|						ИСТИНА
	|					ИЗ
	|						ПредварительныйПланМатериалов КАК ППМ
	|					ГДЕ
	|						ППМ.ВыпускающийЭтап = ДД.ВыпускающийЭтап
	|						И ППМ.Номенклатура = ДД.Номенклатура
	|						И ППМ.Характеристика = ДД.Характеристика
	|						И ППМ.Назначение = ДД.Назначение
	|						И ППМ.ПромежуточныйВыход)
	|			) КАК ПромежуточныйВыход,
	|
	// Спецификация указанная в этапах производства более приоритетна
	|	ВЫБОР КОГДА
	|		ДД.Спецификация = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|		ТОГДА ЕСТЬNULL(КэшНСИ.Спецификация, ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка))
	|		ИНАЧЕ ДД.Спецификация
	|	КОНЕЦ								КАК Спецификация,
	|	ДД.Характеристика					КАК Характеристика,
	|	ДД.Подразделение					КАК Подразделение,
	|	ДД.Назначение						КАК Назначение,
	|	ДД.СтатьяКалькуляции				КАК СтатьяКалькуляции,
	|	ДД.План								КАК План,
	|	&ПустойУИДСтрока					КАК КлючПартия
	|
	|ПОМЕСТИТЬ ПланМатериалов
	|ИЗ ПредварительныйПланМатериалов КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКэшНСИСтруктурыЗаказа КАК КэшНСИ
	|		ПО ДД.Номенклатура = КэшНСИ.Номенклатура
	|			И ДД.Характеристика = КэшНСИ.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВыпускающийЭтап,
	|	ПроизводитсяВПроцессе,
	|	ПотребляющийЭтап_ПроизводствоНаСтороне
	|;";
КонецФункции

Функция ТекстЗапроса_РазузлованиеЭтапов()
	Возврат "
	// 15 ВыпускиПФВРамкахЗаказа Использование ПФ В Этапах производства
	// Собираем ПФ которые необходимы для выпуска текущих ПФ и ГП.
	|ВЫБРАТЬ
	|	МПФ.СписатьНаРасходы									КАК СписатьНаРасходы,
	|	-1														КАК ВыходныеИзделия_ИД,
	|	ЛОЖЬ													ГПВЭтапе,
	|	ПланМатериалов.ВыпускающийЭтап							КАК ВыпускающийЭтап,
	|	ПланМатериалов.ПотребляющийЭтап							КАК ПотребляющийЭтап,
	|	МПФ.ПроизводствоНаСтороне								КАК ПроизводствоНаСтороне,
	|	МПФ.ЗаказНаПроизводство									КАК ЗаказНаПроизводство,
	|	ПланМатериалов.ПериодЗатрат								КАК Период,
	|	МПФ.Продукция											КАК Продукция,
	|	МПФ.ХарактеристикаПродукции								КАК ХарактеристикаПродукции,
	|	МПФ.НазначениеПродукции									КАК НазначениеПродукции,
	|	МПФ.ПартияСпецификацияПродукции							КАК ПартияСпецификацияПродукции,
	|	ПланМатериалов.Номенклатура								КАК Полуфабрикат,
	|	ПланМатериалов.Характеристика							КАК ХарактеристикаПолуфабриката,
	|	ПланМатериалов.План * МПФ.Числитель / МПФ.Знаменатель	КАК КоличествоТребуется,
	|	ПланМатериалов.СтатьяКалькуляции						КАК СтатьяКалькуляции,
	|	ПланМатериалов.Спецификация								КАК СпецификацияПолуфабриката,
	// Конвертация типа в строку будет выполнена автоматически при обработке выборки.
	|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ПланМатериалов.Характеристика)	КАК КлючПартия,
	// Значение колонки ПартияВыпущена определяется при подборе списываемой партии.
	|	МПФ.ПартияВыпущена										КАК ПартияВыпущена
	|ИЗ
	|	ПланМатериалов КАК ПланМатериалов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МножителиПФПроизводимыхВПроцессе КАК МПФ
	// МПФ.ПартияСпецификацияПолуфабриката в данном условии это этап который потребляет ПФ, для выпуска другого ПФ.
	|		ПО ПланМатериалов.ВыпускающийЭтап = МПФ.ПартияСпецификацияПолуфабриката
	|			И (МПФ.ЕстьВыпускающийЭтап)
	|			И (ПланМатериалов.ПроизводитсяВПроцессе)
	//			См. Комментарий 1391.
	|			И (НЕ ПланМатериалов.ПотребляющийЭтап_ПроизводствоНаСтороне)
	//			Затраты по выпущенным партиям собираются при разузловке по дереву себестоимости
	|			И НЕ МПФ.ПартияВыпущена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 10 Множители по полуфабрикатам производимым процессе.
	|ВЫБРАТЬ
	|	ДД.Период КАК Период,
	|	ДД.ГПВЭтапе КАК ГПВЭтапе,
	|	ДД.ПроизводствоНаСтороне КАК ПроизводствоНаСтороне,
	|	ДД.Продукция КАК Продукция,
	|	ДД.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	ДД.НазначениеПродукции КАК Назначение,
	|	ДД.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
	|	ДД.Полуфабрикат КАК Полуфабрикат,
	|	ДД.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
	|	ДД.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
	|	ДД.Спецификация КАК СпецификацияПолуфабриката,
	|	ДД.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
	|	ДД.ЕстьВыпускающийЭтап КАК РасчетЗавершен,
	|	ДД.ЕстьВыпускающийЭтап КАК ЕстьВыпускающийЭтап,
	|	ВЫБОР КОГДА ДД.ПартияВыпущена
	|		ТОГДА -100
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Порядок,
	|	ДД.Числитель КАК Числитель,
	|	ДД.Знаменатель КАК Знаменатель,
	|	""МножителиПФПроизводимыхВПроцессе"" КАК Комментарий,
	|	&ПустойУИД КАК КлючНоменклатура,
	|	ДД.КлючПартия КАК КлючПартия,
	|	ДД.ПартияВыпущена КАК ПартияВыпущена
	|ИЗ
	|	МножителиПФПроизводимыхВПроцессе КАК ДД
	|ГДЕ
	|	НЕ ДД.СписатьНаРасходы
	|";
КонецФункции

#КонецОбласти

#КонецОбласти
