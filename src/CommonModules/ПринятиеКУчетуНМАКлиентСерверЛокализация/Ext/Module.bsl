////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Принятие к учету НМА".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДополнитьПараметрыПринятияКУчетуНМА(Объект, ВспомогательныеРеквизиты, ПараметрыПринятияКУчетуНМА) Экспорт

	//++ Локализация
	
	// Определяет доступность параметров начисления амортизации
									
	ДоступныПараметрыАмортизацииНУ = 
		ВспомогательныеРеквизиты.ОтражатьВНУ
		И (Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию")
			ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислениеАмортизацииИлиСписаниеНаРасходы"))
		И (ВспомогательныеРеквизиты.ПлательщикНалогаНаПрибыль 
			ИЛИ ВспомогательныеРеквизиты.ПрименяетсяУСНДоходыМинусРасходы);
									
	// Определяет доступность реквизитов, которые связаны с отражением расходов (амортизации)
	ДоступноОтражениеРасходовНУ = 
		ВспомогательныеРеквизиты.ОтражатьВНУ
		И Объект.ИнвестиционныйНалоговыйВычет <> ПредопределенноеЗначение("Перечисление.ВариантыПримененияИНВ.Региональный")
		И (Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию")
			ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.СписыватьПриПринятииКУчету")
			ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы")
			ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислениеАмортизацииИлиСписаниеНаРасходы"));
			
	//
	СтатьяРасходовУУОбязательна = 
		ВспомогательныеРеквизиты.ОтражатьВУпрУчете 
		И (НЕ ПараметрыПринятияКУчетуНМА.ДоступноОтражениеРасходовБУ
				И НЕ ДоступноОтражениеРасходовНУ
			ИЛИ Объект.ПорядокУчетаБУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.СписыватьПриПринятииКУчету")
				И (Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы")
					ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы")
					ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислениеАмортизацииИлиСписаниеНаРасходы"))
			ИЛИ ЗначениеЗаполнено(Объект.СтатьяРасходовУУ)
			ИЛИ ЗначениеЗаполнено(Объект.АналитикаРасходовУУ)
			ИЛИ ЗначениеЗаполнено(Объект.ПодразделениеУУ)
			ИЛИ ЗначениеЗаполнено(Объект.НаправлениеДеятельностиУУ));
		
	СтатьяРасходовНУОбязательна = 
		ВспомогательныеРеквизиты.ОтражатьВНУ 
		И (НЕ ПараметрыПринятияКУчетуНМА.ДоступноОтражениеРасходовБУ
			ИЛИ Объект.ПорядокУчетаБУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы")
			ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.СписыватьПриПринятииКУчету")
			ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы")
			ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислениеАмортизацииИлиСписаниеНаРасходы")
			ИЛИ ЗначениеЗаполнено(Объект.СтатьяРасходовНУ)
			ИЛИ ЗначениеЗаполнено(Объект.АналитикаРасходовНУ)
			ИЛИ ЗначениеЗаполнено(Объект.ПодразделениеНУ)
			ИЛИ ЗначениеЗаполнено(Объект.НаправлениеДеятельностиНУ));
		
	АмортизацияНУДоступна = 
		ВспомогательныеРеквизиты.ОтражатьВНУ
		И (Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию")
			ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию")
			ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.ПустаяСсылка"));
			
	ПоИнвентаризации = (ВспомогательныеРеквизиты.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМАпоИнвентаризации"));
	ПараметрыПринятияКУчетуНМА.Вставить("ПоИнвентаризации", ПоИнвентаризации);
	
	ПараметрыПринятияКУчетуНМА.Вставить("ДоступныПараметрыАмортизацииНУ", ДоступныПараметрыАмортизацииНУ);
	ПараметрыПринятияКУчетуНМА.Вставить("ДоступноОтражениеРасходовНУ", ДоступноОтражениеРасходовНУ);
	ПараметрыПринятияКУчетуНМА.Вставить("СтатьяРасходовУУОбязательна", СтатьяРасходовУУОбязательна);
	ПараметрыПринятияКУчетуНМА.Вставить("СтатьяРасходовНУОбязательна", СтатьяРасходовНУОбязательна);
	ПараметрыПринятияКУчетуНМА.Вставить("АмортизацияНУДоступна", АмортизацияНУДоступна);

	//
	НаправлениеДеятельностиОбязательно = 
		(НЕ ВспомогательныеРеквизиты.Свойство("ХозяйственнаяОперация")
			ИЛИ ВспомогательныеРеквизиты.ХозяйственнаяОперация <> ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВводОстатковНМАиРасходовНаНИОКР"))
		И (Объект.ВариантПримененияЦелевогоФинансирования = ПредопределенноеЗначение("Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное")
			ИЛИ Объект.ВариантПримененияЦелевогоФинансирования = ПредопределенноеЗначение("Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное"));
			
	ПараметрыПринятияКУчетуНМА.Вставить("НаправлениеДеятельностиОбязательно", НаправлениеДеятельностиОбязательно);
	
	//-- Локализация
	
КонецПроцедуры

#Область ФормаДокумента

Процедура ДополнитьЗначенияРеквизитовДоИзменения(Форма, ЗначенияРеквизитовДоИзменения) Экспорт

	//++ Локализация
	
	Объект = Форма.Объект;
	
	ЗначенияРеквизитовДоИзменения.Вставить("ПервоначальнаяСтоимостьНУ", Объект.ПервоначальнаяСтоимостьНУ);
	
	//-- Локализация
	
КонецПроцедуры

Процедура ДополнитьЗначенияСвойствЗависимыхРеквизитов(Объект, ПараметрыПринятияКУчетуНМА, ВспомогательныеРеквизиты, СтруктураИзмененныхРеквизитов, ПараметрыРеквизитовОбъекта) Экспорт
	
	//++ Локализация
	
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	#Область Основное
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектаУчета") 
		Или ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			Объект.ВидОбъектаУчета = ПредопределенноеЗначение("Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив")
			И Объект.ОтражатьВРеглУчете;
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СпособПоступления", 
			"СпособПоступления", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете") 
		Или СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация") 
		Или СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаБУ") 
		Или СтруктураИзмененныхРеквизитов.Свойство("Дата") 
		Или ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			Объект.ОтражатьВРеглУчете 
			И НЕ ПараметрыПринятияКУчетуНМА.ПоИнвентаризации
			И Объект.ПорядокУчетаБУ <> ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы")
			И ВспомогательныеРеквизиты.ЕстьУчетСебестоимости
			И ВспомогательныеРеквизиты.ИспользоватьУчетПоНаправлениямДеятельности;
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ВариантПримененияЦелевогоФинансирования", 
			"ВариантПримененияЦелевогоФинансирования", 
			"ВидимостьЭлемента", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете") 
		Или СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВНУ") 
		Или СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация") 
		Или СтруктураИзмененныхРеквизитов.Свойство("Организация") 
		Или СтруктураИзмененныхРеквизитов.Свойство("Дата") 
		Или ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			Объект.ОтражатьВНУ 
			И (ПараметрыПринятияКУчетуНМА.ПоИнвентаризации 
					ИЛИ НЕ ВспомогательныеРеквизиты.ЕстьУчетСебестоимости)
			И ВспомогательныеРеквизиты.ПлательщикНалогаНаПрибыль;
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПервоначальнаяСтоимостьНУ", 
			"ПервоначальнаяСтоимостьНУ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		//
		ЗначениеСвойства = 
			Объект.ОтражатьВРеглУчете 
			И (ПараметрыПринятияКУчетуНМА.ПоИнвентаризации 
					ИЛИ НЕ ВспомогательныеРеквизиты.ЕстьУчетСебестоимости)
			И ВспомогательныеРеквизиты.ПлательщикНалогаНаПрибыль
			И ВспомогательныеРеквизиты.ВедетсяУчетПостоянныхИВременныхРазниц;
							
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПервоначальнаяСтоимостьПР", 
			"ПервоначальнаяСтоимостьПР", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПервоначальнаяСтоимостьВР", 
			"ПервоначальнаяСтоимостьВР", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Организация")
		Или СтруктураИзмененныхРеквизитов.Свойство("Дата")
		Или ОбновитьВсе Тогда
		
		ЗначениеСвойства = ВспомогательныеРеквизиты.РаздельныйУчетВНАПоНалогообложениюНДС;
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ВариантРаздельногоУчетаНДС", 
			"ВариантРаздельногоУчетаНДС", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"", 
			"ВариантРаздельногоУчетаНДСРаспределяется", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НалогообложениеНДС", 
			"НалогообложениеНДС", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"", 
			"ДекорацияРаздельныйУчет", 
			"Видимость",
			ЗначениеСвойства,
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ВариантРаздельногоУчетаНДС")
		Или СтруктураИзмененныхРеквизитов.Свойство("ВариантРаздельногоУчетаНДСРаспределяется")
		Или ОбновитьВсе Тогда
		
		ЗначениеСвойства = Объект.ВариантРаздельногоУчетаНДС =
			ПредопределенноеЗначение("Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента");
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НалогообложениеНДС", 
			"НалогообложениеНДС", 
			"ТолькоПросмотр", 
			Не ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		//
		ЗначениеСвойства = Объект.ВариантРаздельногоУчетаНДС =
			ПредопределенноеЗначение("Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента")
			И ВспомогательныеРеквизиты.РаздельныйУчетВНАПоНалогообложениюНДС;
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НалогообложениеНДС", 
			"НалогообложениеНДС", 
			"АвтоОтметкаНезаполненного", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ВариантРаздельногоУчетаНДС")
		Или СтруктураИзмененныхРеквизитов.Свойство("ВариантРаздельногоУчетаНДСРаспределяется")
		Или СтруктураИзмененныхРеквизитов.Свойство("НалогообложениеНДС")
		Или ОбновитьВсе Тогда
		
		ЗначениеСвойства = Объект.ВариантРаздельногоУчетаНДС =
			ПредопределенноеЗначение("Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента")
			И Не ЗначениеЗаполнено(Объект.НалогообложениеНДС);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НалогообложениеНДС", 
			"НалогообложениеНДС", 
			"ОтметкаНезаполненного", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация") 
		Или ОбновитьВсе Тогда
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ДокументНаОсновании", 
			"ДокументНаОсновании",
			"Видимость", 
			ПараметрыПринятияКУчетуНМА.ПоИнвентаризации, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ДокументОснование", 
			"ДокументОснование",
			"Видимость", 
			ПараметрыПринятияКУчетуНМА.ПоИнвентаризации, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"", 
			"ВидОбъектаУчета",
			"ВидимостьЭлемента", 
			НЕ ПараметрыПринятияКУчетуНМА.ПоИнвентаризации, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ПараметрыУчетаНУ
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВНУ")
		ИЛИ ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			Объект.ОтражатьВНУ
			И (ВспомогательныеРеквизиты.ПлательщикНалогаНаПрибыль
				ИЛИ ВспомогательныеРеквизиты.ПрименяетсяУСНДоходыМинусРасходы);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПорядокУчетаНУ", 
			"ПорядокУчетаНУ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СрокИспользованияНУ", 
			"СрокИспользованияНУ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"", 
			"СрокИспользованияНУРасшифровка", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Подразделение")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаБУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаУУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ИнвестиционныйНалоговыйВычет")
		ИЛИ ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			Объект.ОтражатьВНУ
			И (ВспомогательныеРеквизиты.ПлательщикНалогаНаПрибыль
				ИЛИ ВспомогательныеРеквизиты.ПрименяетсяУСНДоходыМинусРасходы)
			И Объект.ИнвестиционныйНалоговыйВычет = ПредопределенноеЗначение("Перечисление.ВариантыПримененияИНВ.Региональный");
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПорядокУчетаНУ", 
			"ПорядокУчетаНУ", 
			"ТолькоПросмотрБезОчистки", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;

	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Подразделение")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектаУчета")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ИнвестиционныйНалоговыйВычет")
		ИЛИ ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			Объект.ОтражатьВНУ 
			И Объект.ВидОбъектаУчета = ПредопределенноеЗначение("Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив")
			И (Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию")
				ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислениеАмортизацииИлиСписаниеНаРасходы")) 
			И (ВспомогательныеРеквизиты.ПлательщикНалогаНаПрибыль
				ИЛИ ВспомогательныеРеквизиты.ПрименяетсяУСНДоходыМинусРасходы)
			И Объект.ИнвестиционныйНалоговыйВычет <> ПредопределенноеЗначение("Перечисление.ВариантыПримененияИНВ.Региональный");
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НачислятьАмортизациюНУ", 
			"НачислятьАмортизациюНУ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		//
		ЗначениеСвойства = 
			Объект.ОтражатьВНУ
			И (Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию")
				ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислениеАмортизацииИлиСписаниеНаРасходы"));
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СрокИспользованияНУ", 
			"СрокИспользованияНУ", 
			"АвтоОтметкаНезаполненного", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Подразделение")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектаУчета")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("НачислятьАмортизациюНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ИнвестиционныйНалоговыйВычет")
		ИЛИ ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			Объект.ОтражатьВНУ
			И Объект.НачислятьАмортизациюНУ
			И (Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию")
				ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислениеАмортизацииИлиСписаниеНаРасходы"))
			И Объект.ВидОбъектаУчета = ПредопределенноеЗначение("Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив")
			И Объект.ИнвестиционныйНалоговыйВычет <> ПредопределенноеЗначение("Перечисление.ВариантыПримененияИНВ.Региональный");
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СпециальныйКоэффициентНУ", 
			"СпециальныйКоэффициентНУ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
			
	#КонецОбласти
	
	#Область ОтражениеРасходовНУ
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Подразделение")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ИнвестиционныйНалоговыйВычет") 
		ИЛИ ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			Объект.ОтражатьВНУ 
			И ПараметрыПринятияКУчетуНМА.ДоступноОтражениеРасходовНУ;
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СтатьяРасходовНУ", 
			"СтатьяРасходовНУ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"АналитикаРасходовНУ", 
			"АналитикаРасходовНУ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СпособОтраженияРасходовНУ", 
			"СпособОтраженияРасходовНУ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПодразделениеНУ", 
			"ПодразделениеНУ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НаправлениеДеятельностиНУ", 
			"НаправлениеДеятельностиНУ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Подразделение")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВУпрУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаБУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаУУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("СпособОтраженияРасходовНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("СтатьяРасходовНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("АналитикаРасходовНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПодразделениеНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("НаправлениеДеятельностиНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Дата") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ДоступныПараметрыУчета") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ИнвестиционныйНалоговыйВычет") 
		ИЛИ ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			Объект.ОтражатьВНУ
			И ПараметрыПринятияКУчетуНМА.СтатьяРасходовНУОбязательна;
							
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СтатьяРасходовНУ", 
			"СтатьяРасходовНУ", 
			"АвтоОтметкаНезаполненного", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПодразделениеНУ", 
			"ПодразделениеНУ", 
			"АвтоОтметкаНезаполненного", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		//
		ЗначениеСвойства = 
			ПараметрыПринятияКУчетуНМА.ДоступноОтражениеРасходовНУ
			И ПараметрыПринятияКУчетуНМА.СтатьяРасходовНУОбязательна;

		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчетаНУ", 
			"ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчетаНУ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
		
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Подразделение")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчетаНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("СпособОтраженияРасходовНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("СтатьяРасходовНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("АналитикаРасходовНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПодразделениеНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("НаправлениеДеятельностиНУ")
		ИЛИ ОбновитьВсе Тогда

		ЗначениеСвойства = 
			Объект.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчетаНУ;

		ИмяСвойства = ?( 
			НЕ ЗначениеСвойства
			И НЕ ПараметрыПринятияКУчетуНМА.СтатьяРасходовНУОбязательна,
			"ТолькоПросмотр",
			"ТолькоПросмотрБезОтметкиНезаполненного");
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПодразделениеНУ", 
			"ПодразделениеНУ", 
			ИмяСвойства, 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НаправлениеДеятельностиНУ", 
			"НаправлениеДеятельностиНУ", 
			"ТолькоПросмотр", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
	КонецЕсли;
		
	#КонецОбласти
	
	#Область ОтражениеРасходовПрочее
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ПередаватьРасходыВДругуюОрганизацию") 
		Или ОбновитьВсе Тогда
		
		ЗначениеСвойства = Объект.ПередаватьРасходыВДругуюОрганизацию;
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ОрганизацияПолучательРасходов", 
			"ОрганизацияПолучательРасходов", 
			"Доступность", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ОрганизацияПолучательРасходов", 
			"ОрганизацияПолучательРасходов", 
			"ТолькоПросмотр", 
			Не ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Организация") 
		Или ОбновитьВсе Тогда
		
		ЗначениеСвойства = ВспомогательныеРеквизиты.ЕстьСвязанныеОрганизации;
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПередаватьРасходыВДругуюОрганизацию", 
			"ПередаватьРасходыВДругуюОрганизацию",
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ОрганизацияПолучательРасходов", 
			"ОрганизацияПолучательРасходов",
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);

	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация") 
		Или ОбновитьВсе Тогда
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"", 
			"СтатьяДоходовПоИнвентаризации",
			"Видимость", 
			ПараметрыПринятияКУчетуНМА.ПоИнвентаризации, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"", 
			"АналитикаДоходовПоИнвентаризации",
			"Видимость", 
			ПараметрыПринятияКУчетуНМА.ПоИнвентаризации, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация") 
		Или ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			Объект.ВариантПримененияЦелевогоФинансирования = ПредопределенноеЗначение("Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное")
			ИЛИ Объект.ВариантПримененияЦелевогоФинансирования = ПредопределенноеЗначение("Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное")
			ИЛИ ПараметрыПринятияКУчетуНМА.ПоИнвентаризации;

		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"СтатьяДоходов", 
			"",
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"АналитикаДоходов", 
			"",
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ЦелевоеФинансирование
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВариантПримененияЦелевогоФинансирования")
		ИЛИ ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			Объект.ВариантПримененияЦелевогоФинансирования = ПредопределенноеЗначение("Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное")
			И НЕ ПараметрыПринятияКУчетуНМА.ПоИнвентаризации
			И Объект.ОтражатьВРеглУчете;
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ЦелевоеФинансирование.Сумма", 
			"ЦелевоеФинансированиеСумма", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ВариантПримененияЦелевогоФинансирования") 
		Или ОбновитьВсе Тогда
		
		ЗначениеСвойства = (Объект.ВариантПримененияЦелевогоФинансирования = ПредопределенноеЗначение("Перечисление.ВариантыПримененияЦелевогоФинансирования.Частичное")
							ИЛИ Объект.ВариантПримененияЦелевогоФинансирования = ПредопределенноеЗначение("Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное"));
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"", 
			"СтатьяДоходовЦФ", 
			"ВидимостьЭлемента", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"", 
			"АналитикаДоходовЦФ", 
			"ВидимостьЭлемента", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ЦелевоеФинансирование", 
			"ЦелевоеФинансирование", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область СтраницаДополнительно
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектаУчета")
		ИЛИ ОбновитьВсе Тогда
	
		ЗначениеСвойства =
			Объект.ОтражатьВНУ
			И ВспомогательныеРеквизиты.ДоступныеВариантыИНВ.Количество() <> 0
			И Объект.ХозяйственнаяОперация <> ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМАпоИнвентаризации")
			И Объект.ВидОбъектаУчета = ПредопределенноеЗначение("Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив");
							
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ИнвестиционныйНалоговыйВычет", 
			"ИнвестиционныйНалоговыйВычет", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаБУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаУУ")
		ИЛИ ОбновитьВсе Тогда
	
		ЗначениеСвойства =
			Объект.ОтражатьВНУ
			И Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.АмортизацияНачислена");
							
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ИнвестиционныйНалоговыйВычет", 
			"ИнвестиционныйНалоговыйВычет", 
			"ТолькоПросмотрБезОчистки", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	#КонецОбласти
	
	//-- Локализация
	
КонецПроцедуры

Процедура НастроитьЗависимыеЭлементыФормы(Форма, ВспомогательныеРеквизиты, СтруктураИзмененныхРеквизитов, ПараметрыПринятияКУчетуНМА, ПараметрыРеквизитовОбъекта) Экспорт

	//++ Локализация
	
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект; // ДокументОбъект.ПринятиеКУчетуНМА2_4
	
	ПараметрыПринятияКУчетуНМА = ВнеоборотныеАктивыКлиентСервер.ПараметрыПринятияКУчетуНМА(Объект, ВспомогательныеРеквизиты);
	
	ЭтоНМА = Объект.ВидОбъектаУчета = ПредопределенноеЗначение("Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив");
	
	#Область Основное
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВУпрУчете") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ ОбновитьВсе Тогда
		
		Если Объект.ОтражатьВРеглУчете Тогда
			
			Если ПараметрыПринятияКУчетуНМА.ПоИнвентаризации 
				ИЛИ НЕ ВспомогательныеРеквизиты.ЕстьУчетСебестоимости Тогда
				
				Элементы.ЗаполнитьПервоначальнуюСтоимостьРегл.Видимость = Ложь;
				
				Элементы.ПервоначальнаяСтоимостьБУ.Заголовок = НСтр("ru = 'Бухгалтерский учет';
																	|en = 'Accounting'");
				Элементы.ПервоначальнаяСтоимостьНУ.Заголовок = НСтр("ru = 'Налоговый учет';
																	|en = 'Tax accounting'");
				Элементы.ПервоначальнаяСтоимостьПР.Заголовок = НСтр("ru = 'Постоянная разница';
																	|en = 'Permanent difference'");
				Элементы.ПервоначальнаяСтоимостьВР.Заголовок = НСтр("ru = 'Временная разница';
																	|en = 'Temporary difference'");
				Элементы.ПервоначальнаяСтоимостьУУ.Заголовок = НСтр("ru = 'Управленческий учет';
																	|en = 'Management accounting'");
				
			КонецЕсли; 
			
		Иначе
			
			Элементы.ЗаполнитьПервоначальнуюСтоимостьРегл.Видимость = Ложь;
			
		КонецЕсли;
		
		Если Объект.ОтражатьВУпрУчете Тогда
			
			Если ПараметрыПринятияКУчетуНМА.ПоИнвентаризации
				ИЛИ НЕ ВспомогательныеРеквизиты.ЕстьУчетСебестоимости Тогда
				
				Элементы.ЗаполнитьПервоначальнуюСтоимостьУпр.Видимость = Ложь;
				
			КонецЕсли; 
			
		Иначе
			
			Элементы.ЗаполнитьПервоначальнуюСтоимостьУпр.Видимость = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	#КонецОбласти
									
	#Область ПараметрыУчетаНУ
	
	Если СтруктураИзмененныхРеквизитов.Свойство("СрокИспользованияНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ ОбновитьВсе Тогда
		
		Форма.СрокИспользованияНУРасшифровка = ВнеоборотныеАктивыКлиентСервер.ПредставлениеКоличестваМесяцевСтрокой(
			Объект.СрокИспользованияНУ);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация") 
		ИЛИ ОбновитьВсе Тогда
			
		ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
			Элементы.ПорядокУчетаНУ.СписокВыбора, 
			Объект.ХозяйственнаяОперация <> ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМАпоИнвентаризации"), 
			ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы"));
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектаУчета") 
		ИЛИ ОбновитьВсе Тогда
		
		Если ЭтоНМА Тогда
			
			Элементы.ПорядокУчетаНУ.Заголовок = НСтр("ru = 'Порядок включения стоимости в расходы';
													|en = 'Procedure of including cost in expenses'");
			Элементы.СрокИспользованияНУ.Заголовок = НСтр("ru = 'Срок использования';
															|en = 'Useful life'");
			
			ПорядокНеНачислятьАмортизациюПредставлениеНУ = НСтр("ru = 'Стоимость не включается в расходы';
																|en = 'Cost is not included in expenses'");
			ПорядокСписыватьПриПринятииКУчетуПредставлениеНУ = НСтр("ru = 'Включить в расходы при принятии к учету';
																	|en = 'Include in expenses on recognition'");
			
		Иначе
			
			Элементы.ПорядокУчетаНУ.Заголовок = НСтр("ru = 'Порядок списания на расходы';
													|en = 'Procedure of writing off as expenses'");
			Элементы.СрокИспользованияНУ.Заголовок = НСтр("ru = 'Срок списания';
															|en = 'Amortization period'");
			
			ПорядокНеНачислятьАмортизациюПредставлениеНУ = НСтр("ru = 'Не списывать';
																|en = 'Do not write off'");
			ПорядокСписыватьПриПринятииКУчетуПредставлениеНУ = НСтр("ru = 'При принятии к учету';
																	|en = 'While recognition'");
		
		КонецЕсли;
		
		ВнеоборотныеАктивыКлиентСервер.УстановитьПредставлениеЗначенияСпискаВыбора(
			Элементы.ПорядокУчетаНУ.СписокВыбора,
			ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеНачислятьАмортизацию"),
			ПорядокНеНачислятьАмортизациюПредставлениеНУ);
			
		ВнеоборотныеАктивыКлиентСервер.УстановитьПредставлениеЗначенияСпискаВыбора(
			Элементы.ПорядокУчетаНУ.СписокВыбора,
			ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.СписыватьПриПринятииКУчету"),
			ПорядокСписыватьПриПринятииКУчетуПредставлениеНУ);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектаУчета")
		ИЛИ ОбновитьВсе Тогда
	
		Если Форма.ПлательщикНалогаНаПрибыль Тогда
			Если Объект.ВидОбъектаУчета = ПредопределенноеЗначение("Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив") Тогда
				ПорядокНачислятьАмортизациюПредставлениеНУ = НСтр("ru = 'Начислять амортизацию';
																	|en = 'Accrue depreciation '");
			Иначе
				ПорядокНачислятьАмортизациюПредставлениеНУ = НСтр("ru = 'Равномерно';
																	|en = 'Evenly'");
			КонецЕсли;
		Иначе
			ПорядокНачислятьАмортизациюПредставлениеНУ = НСтр("ru = 'Включить в состав амортизируемого имущества';
																|en = 'Include in depreciable fixed assets'");
		КонецЕсли;
		
		ВнеоборотныеАктивыКлиентСервер.УстановитьПредставлениеЗначенияСпискаВыбора(
			Элементы.ПорядокУчетаНУ.СписокВыбора,
			ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию"),
			ПорядокНачислятьАмортизациюПредставлениеНУ);
			
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВариантПримененияЦелевогоФинансирования")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектаУчета")
		ИЛИ ОбновитьВсе Тогда
	
		Если Элементы.ПорядокУчетаНУ.Видимость Тогда
			
			СписокВыбораНУ = Форма.Элементы.ПорядокУчетаНУ.СписокВыбора;
	
			НеПолныйВариантПримененияЦФ = 
				Объект.ВариантПримененияЦелевогоФинансирования <> ПредопределенноеЗначение("Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное");
	
			//
			ВидимостьПорядкаУчета = 
				НеПолныйВариантПримененияЦФ
				И Объект.ХозяйственнаяОперация <> ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМАпоИнвентаризации");
					
			ПредставлениеЗначения = 
				?(ЭтоНМА, НСтр("ru = 'Начислять амортизацию или включить в расходы';
								|en = 'Accrue depreciation or include in expenses'"),
							НСтр("ru = 'Равномерно или при принятии к учету';
								|en = 'Evenly or when recognized'"));
				
			ВнеоборотныеАктивыКлиентСервер.УстановитьВидимостьЗначенияСпискаВыбора(
				СписокВыбораНУ, 
				ВидимостьПорядкаУчета, 
				ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислениеАмортизацииИлиСписаниеНаРасходы"),
				ПредставлениеЗначения, 
				2);
				
		КонецЕсли;
				
	КонецЕсли;
				
	Если СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("СрокИспользованияБУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПервоначальнаяСтоимостьБУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектаУчета")
		ИЛИ ОбновитьВсе Тогда
		
		Если Элементы.ПорядокУчетаНУ.Видимость Тогда
			
			СписокВыбораНУ = Форма.Элементы.ПорядокУчетаНУ.СписокВыбора;
			
			Причина = "";
			Форма.РекомендуемыйПорядокУчетаНУ = СписокВыбораНУ.Получить(0).Значение;
				
			Если ЗначениеЗаполнено(Форма.Объект.СрокИспользованияБУ) 
				И Форма.Объект.СрокИспользованияБУ <= 12 Тогда
				
				Причина = ?(ЭтоНМА,
					НСтр("ru = 'Срок полезного использования не превышает 12 месяцев, рекомендуется установить значение <a href = ""УстановкаПорядкаУчетаНУ"">%1</a>';
						|en = 'The useful life does not exceed 12 months. Set the <a href = ""УстановкаПорядкаУчетаНУ"">%1</a> value'"),
					НСтр("ru = 'Срок списания не превышает 12 месяцев, рекомендуется установить значение <a href = ""УстановкаПорядкаУчетаНУ"">%1</a>';
						|en = 'The write-off period does not exceed 12 months. Set the <a href = ""УстановкаПорядкаУчетаНУ"">%1</a> value'"));
				
				Если Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМАпоИнвентаризации") Тогда
					Форма.РекомендуемыйПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.СписыватьПриПринятииКУчету");
				Иначе
					Форма.РекомендуемыйПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы");
				КонецЕсли;
				
			ИначеЕсли ЗначениеЗаполнено(Форма.Объект.ПервоначальнаяСтоимостьБУ)
				И Форма.Объект.ПервоначальнаяСтоимостьБУ <= 100000 Тогда
				
				Причина = НСтр("ru = 'Первоначальная стоимость не превышает 100 000 руб, рекомендуется установить значение <a href = ""УстановкаПорядкаУчетаНУ"">%1</a>.';
								|en = 'The initial cost does not exceed 100,000 rubles. Set the <a href = ""УстановкаПорядкаУчетаНУ"">%1</a> value.'");
				
				Если Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМАпоИнвентаризации") Тогда
					Форма.РекомендуемыйПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.СписыватьПриПринятииКУчету");
				Иначе
					Форма.РекомендуемыйПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы");
				КонецЕсли;
				
			КонецЕсли;
			
			МассивСтрок = Новый Массив;
			Если ЗначениеЗаполнено(Причина) 
				И Форма.Объект.ПорядокУчетаНУ <> Форма.РекомендуемыйПорядокУчетаНУ Тогда
					
				МассивСтрок.Добавить(ВнеоборотныеАктивыКлиентСервер.ФорматированнаяСтрока(СтрШаблон(Причина, Строка(Форма.РекомендуемыйПорядокУчетаНУ))));

				Форма.Элементы.ГруппаУстановкаПараметраУчетаНУ.Видимость = Истина;
				Элементы.ДекорацияУстановкаПараметраУчетаНУ.Видимость = Истина;
				
			Иначе
				
				Форма.Элементы.ГруппаУстановкаПараметраУчетаНУ.Видимость = Ложь;
				Элементы.ДекорацияУстановкаПараметраУчетаНУ.Видимость = Ложь;
				
			КонецЕсли;
			
			Форма.УстановитьПорядокУчетаНУ = Новый ФорматированнаяСтрока(МассивСтрок);
			
		Иначе
			Элементы.ГруппаУстановкаПараметраУчетаНУ.Видимость = Ложь;
			Элементы.ДекорацияУстановкаПараметраУчетаНУ.Видимость = Ложь;
		КонецЕсли; 
		
	КонецЕсли;

	Если СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектаУчета")
		ИЛИ ОбновитьВсе Тогда
		
		Если Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислениеАмортизацииИлиСписаниеНаРасходы") Тогда
			ПодсказкаЭлемента = 
				?(ЭтоНМА,
					НСтр("ru = 'Если после закрытия месяца первоначальная стоимость не превысит 100 000 рублей, то она автоматически будет включена в расходы при принятии к учету, иначе - начисление амортизации';
						|en = 'If after month-end closing the initial cost does not exceed 100,000 rubles, it will automatically be included in expenses upon recognition, otherwise - depreciation accrual'"),
					НСтр("ru = 'Если после закрытия месяца расходы на НИОКР не превысят 100 000 рублей, то они автоматически будут списаны при принятии к учету, иначе - равномерно';
						|en = 'If, after the month-end closing, R&D expenses do not exceed 100,000 rubles, these will be automatically written off upon recognition, otherwise - evenly'"));
					
			Элементы.ПорядокУчетаНУ.ОтображениеПодсказки  = ОтображениеПодсказки.ОтображатьСнизу;
		Иначе
			ПодсказкаЭлемента = НСтр("ru = 'Порядок включения стоимости в состав расходов в налоговом учете';
									|en = 'Procedure of adding a cost to expenses in tax accounting'");
			Элементы.ПорядокУчетаНУ.ОтображениеПодсказки  = ОтображениеПодсказки.Авто;
		КонецЕсли;	
		
		Элементы.ПорядокУчетаНУ.Подсказка = ПодсказкаЭлемента;
		 
	КонецЕсли;

	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Подразделение")
		ИЛИ ОбновитьВсе Тогда
	
		Элементы.ИнвестиционныйНалоговыйВычет.СписокВыбора.Очистить();
		
		Если ВспомогательныеРеквизиты.ДоступныеВариантыИНВ.Количество() <> 0 Тогда
			
			Для Каждого Элемент Из ВспомогательныеРеквизиты.ДоступныеВариантыИНВ Цикл
				Элементы.ИнвестиционныйНалоговыйВычет.СписокВыбора.Добавить(Элемент);
			КонецЦикла;
			
			Элементы.ИнвестиционныйНалоговыйВычет.СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыПримененияИНВ.НеПрименяется"));
			
			Если НЕ ОбновитьВсе
				И Элементы.ИнвестиционныйНалоговыйВычет.СписокВыбора.НайтиПоЗначению(Объект.ИнвестиционныйНалоговыйВычет) = Неопределено Тогда
					
				Объект.ИнвестиционныйНалоговыйВычет = ПредопределенноеЗначение("Перечисление.ВариантыПримененияИНВ.ПустаяСсылка");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;

	#КонецОбласти
	
	#Область ОтражениеРасходов
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВУпрУчете") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("СтатьяРасходовУУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаБУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("СпособОтраженияРасходовУУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПодразделениеУУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("НаправлениеДеятельностиУУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчетаУУ") 
		ИЛИ ОбновитьВсе Тогда
		
		Если ПараметрыПринятияКУчетуНМА.СтатьяРасходовУУОбязательна
			ИЛИ ЗначениеЗаполнено(Объект.СтатьяРасходовУУ) Тогда
			
			Элементы.СпособОтраженияРасходовУУ.ПодсказкаВвода = "";
			Элементы.СтатьяРасходовУУ.ПодсказкаВвода = "";
			Элементы.АналитикаРасходовУУ.ПодсказкаВвода = "";
			Элементы.ПодразделениеУУ.ПодсказкаВвода = "";
			Элементы.НаправлениеДеятельностиУУ.ПодсказкаВвода = "";
			
		Иначе
			
			Если ПараметрыПринятияКУчетуНМА.ДоступныПараметрыАмортизацииБУ Тогда
				ПодсказкаВводаСтатьи = НСтр("ru = 'совпадает с бухгалтерским учетом';
											|en = 'matches accounting'");
			Иначе
				ПодсказкаВводаСтатьи = НСтр("ru = 'совпадает с налоговым учетом';
											|en = 'matches tax accounting'");
			КонецЕсли;
			
			Элементы.СпособОтраженияРасходовУУ.ПодсказкаВвода = ПодсказкаВводаСтатьи;
			Элементы.СтатьяРасходовУУ.ПодсказкаВвода = ПодсказкаВводаСтатьи;
			Элементы.АналитикаРасходовУУ.ПодсказкаВвода = ПодсказкаВводаСтатьи;
			Элементы.ПодразделениеУУ.ПодсказкаВвода = ПодсказкаВводаСтатьи;
			Элементы.НаправлениеДеятельностиУУ.ПодсказкаВвода = ПодсказкаВводаСтатьи;
			
		КонецЕсли; 
		
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("СтатьяРасходовНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаБУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("СпособОтраженияРасходовНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПодразделениеНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("НаправлениеДеятельностиНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчетаНУ") 
		ИЛИ ОбновитьВсе Тогда
		
		Если ПараметрыПринятияКУчетуНМА.СтатьяРасходовНУОбязательна 
			ИЛИ ЗначениеЗаполнено(Объект.СтатьяРасходовНУ) Тогда
				
			Элементы.СпособОтраженияРасходовНУ.ПодсказкаВвода = "";
			Элементы.СтатьяРасходовНУ.ПодсказкаВвода = "";
			Элементы.АналитикаРасходовНУ.ПодсказкаВвода = "";
			Элементы.ПодразделениеНУ.ПодсказкаВвода = "";
			Элементы.НаправлениеДеятельностиНУ.ПодсказкаВвода = "";
			
		Иначе
			
			ПодсказкаВводаСтатьи = НСтр("ru = 'совпадает с бухгалтерским учетом';
										|en = 'matches accounting'");
			
			Элементы.СпособОтраженияРасходовНУ.ПодсказкаВвода = ПодсказкаВводаСтатьи;
			Элементы.СтатьяРасходовНУ.ПодсказкаВвода = ПодсказкаВводаСтатьи;
			Элементы.АналитикаРасходовНУ.ПодсказкаВвода = ПодсказкаВводаСтатьи;
			Элементы.ПодразделениеНУ.ПодсказкаВвода = ПодсказкаВводаСтатьи;
			Элементы.НаправлениеДеятельностиНУ.ПодсказкаВвода = ПодсказкаВводаСтатьи;
			
		КонецЕсли; 
		
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("СпособОтраженияРасходовНУ") 
		ИЛИ ОбновитьВсе Тогда
			
		ВнеоборотныеАктивыКлиентСервер.НастроитьЭлементыПоСпособуОтраженияРасходов(
			Объект.СпособОтраженияРасходовНУ, 
			Форма,
			"СтатьяРасходовНУ,АналитикаРасходовНУ,ПодразделениеНУ,НаправлениеДеятельностиНУ,ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчетаНУ");
	
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектаУчета") 
		ИЛИ ОбновитьВсе Тогда
		
		Если Объект.ВидОбъектаУчета = ПредопределенноеЗначение("Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив") Тогда
			
			Элементы.СтатьяРасходовНУ.Подсказка = 
				?(Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.СписыватьПриПринятииКУчету")
						ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы"),
					НСтр("ru = 'Статья расходов, на которую списывается стоимость при принятии к учету';
						|en = 'Expense item to which the cost is charged to on initial recognition'"),
					НСтр("ru = 'Статья расходов, по которой отражаются амортизационные расходы';
						|en = 'Expense item recording depreciation expenses'"));
					
		Иначе
			
			Элементы.СтатьяРасходовНУ.Подсказка = 
				?(Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.СписыватьПриПринятииКУчету")
						ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы"),
					НСтр("ru = 'Статья расходов, на которую списываются расходы при принятии к учету';
						|en = 'Expense item to which the expenses are charged to on initial recognition'"),
					НСтр("ru = 'Статья расходов, по которой отражается списание расходов';
						|en = 'Expense item on which the expenses write-off is recognized'"));
					
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВУпрУчете") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектаУчета") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаБУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаНУ") 
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация") 
		ИЛИ ОбновитьВсе Тогда
		
		Если Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.СписыватьПриПринятииКУчету")
			ИЛИ Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы") Тогда
			
			Элементы.ГруппаРасходыНУ.Заголовок = НСтр("ru = 'Включение в расходы (налоговый учет)';
														|en = 'Inclusion in expenses (tax accounting)'");
					
		Иначе
			
			Если Объект.ВидОбъектаУчета = ПредопределенноеЗначение("Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив") Тогда
				
				Элементы.ГруппаРасходыНУ.Заголовок = 
					?(Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислениеАмортизацииИлиСписаниеНаРасходы"),
						НСтр("ru = 'Амортизация / Включение в расходы (налоговый учет)';
							|en = 'Depreciation / Inclusion in expenses (tax accounting)'"),
						НСтр("ru = 'Амортизация (налоговый учет)';
							|en = 'Depreciation (tax accounting)'"));
															
			Иначе

				Элементы.ГруппаРасходыНУ.Заголовок = НСтр("ru = 'Списание расходов (налоговый учет)';
															|en = 'Expense write-off (tax accounting)'");
															
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация") 
		ИЛИ ОбновитьВсе Тогда
		
		Элементы.СтраницаОтражениеРасходов.Заголовок = ?(
			ПараметрыПринятияКУчетуНМА.ПоИнвентаризации,
			НСтр("ru = 'Отражение доходов и расходов';
				|en = 'Record income and expenses'"),
			НСтр("ru = 'Отражение расходов';
				|en = 'Expense parameters'"));
		
	КонецЕсли;
	
	#КонецОбласти
		
	//-- Локализация
	
КонецПроцедуры

Процедура ДополнитьВспомогательныеРеквизиты(Форма, ВспомогательныеРеквизиты) Экспорт

	//++ Локализация
	
	ВспомогательныеРеквизиты.Вставить("ЕстьСвязанныеОрганизации", Форма.ЕстьСвязанныеОрганизации);
	ВспомогательныеРеквизиты.Вставить("ПлательщикНалогаНаПрибыль", Форма.ПлательщикНалогаНаПрибыль);
	ВспомогательныеРеквизиты.Вставить("ПрименяетсяУСНДоходыМинусРасходы", Форма.ПрименяетсяУСНДоходыМинусРасходы);
	ВспомогательныеРеквизиты.Вставить("РаздельныйУчетВНАПоНалогообложениюНДС", Форма.РаздельныйУчетВНАПоНалогообложениюНДС);
	ВспомогательныеРеквизиты.Вставить("ПоддержкаПБУ18", Форма.ПоддержкаПБУ18);
	ВспомогательныеРеквизиты.Вставить("ВедетсяУчетПостоянныхИВременныхРазниц", Форма.ВедетсяУчетПостоянныхИВременныхРазниц);
	ВспомогательныеРеквизиты.Вставить("ДоступныеВариантыИНВ", Форма.СлужебныеПараметрыФормы.ДоступныеВариантыИНВ);
	
	//-- Локализация
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыВЗависимостиОтСвойств(Форма, ПараметрыРеквизита, Действие, ИзмененныеРеквизиты) Экспорт

	//++ Локализация
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	#Область СтраницаОсновное
	
	Если ПараметрыРеквизита.ИмяРеквизита = "ВариантРаздельногоУчетаНДС" Тогда
		Если Действие = "Заполнить" Тогда
			Объект.ВариантРаздельногоУчетаНДС = ПредопределенноеЗначение("Перечисление.ВариантыРаздельногоУчетаНДС.ИзДокумента");
			Объект.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС");
			ИзмененныеРеквизиты.Добавить("ВариантРаздельногоУчетаНДС");
			ИзмененныеРеквизиты.Добавить("НалогообложениеНДС");
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыРеквизита.ИмяРеквизита = "НалогообложениеНДС" Тогда
		Если Действие = "Заполнить" Тогда
			Объект.НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС");
			ИзмененныеРеквизиты.Добавить("НалогообложениеНДС");
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыРеквизита.ИмяРеквизита = "ВариантПримененияЦелевогоФинансирования" Тогда
		Если Действие = "Заполнить" Тогда
			Объект.ВариантПримененияЦелевогоФинансирования = ПредопределенноеЗначение("Перечисление.ВариантыПримененияЦелевогоФинансирования.НеИспользуется");
			ИзмененныеРеквизиты.Добавить(ПараметрыРеквизита.ИмяРеквизита);
		КонецЕсли;
	КонецЕсли;
	
	#КонецОбласти
	
	#Область СтраницаУчет
	
	Если ПараметрыРеквизита.ИмяРеквизита = "ПорядокУчетаНУ" Тогда
		
		Если Действие = "Заполнить" Тогда
			Объект[ПараметрыРеквизита.ИмяРеквизита] = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию");
			ИзмененныеРеквизиты.Добавить(ПараметрыРеквизита.ИмяРеквизита);
		КонецЕсли;
		
	ИначеЕсли ПараметрыРеквизита.ИмяРеквизита = "НачислятьАмортизациюНУ" Тогда
		
		Если Действие = "Заполнить" 
			И Элементы.НачислятьАмортизациюНУ.Видимость
			И НЕ Элементы.НачислятьАмортизациюНУ.ТолькоПросмотр Тогда
			
			Объект[ПараметрыРеквизита.ИмяРеквизита] = Истина;
			ИзмененныеРеквизиты.Добавить(ПараметрыРеквизита.ИмяРеквизита);
		КонецЕсли;
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область СтраницаДополнительно
	
	Если ПараметрыРеквизита.ИмяРеквизита = "ИнвестиционныйНалоговыйВычет" Тогда
		Если Действие = "Заполнить" Тогда
			Объект.ИнвестиционныйНалоговыйВычет = ПредопределенноеЗначение("Перечисление.ВариантыПримененияИНВ.НеПрименяется");
			ИзмененныеРеквизиты.Добавить("ИнвестиционныйНалоговыйВычет");
		КонецЕсли; 
	КонецЕсли;

	#КонецОбласти
	
	//-- Локализация
	
КонецПроцедуры

Процедура ПриИзмененииРеквизитов(Форма, СтруктураИзмененныхРеквизитов) Экспорт
	
	//++ Локализация
	
	Объект = Форма.Объект;
	СлужебныеПараметрыФормы = Форма.СлужебныеПараметрыФормы;
	
	ЭтоИнвентаризация = Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМАпоИнвентаризации");
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация")
			И ЭтоИнвентаризация
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПорядокУчетаБУ")
			И Объект.ПорядокУчетаБУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы") Тогда
		
		Объект.ВариантПримененияЦелевогоФинансирования = ПредопределенноеЗначение("Перечисление.ВариантыПримененияЦелевогоФинансирования.НеИспользуется");
		СтруктураИзмененныхРеквизитов.Вставить("ВариантПримененияЦелевогоФинансирования");
	КонецЕсли;
		
	Если СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация")
		И ЭтоИнвентаризация Тогда
			
		Объект.ВидОбъектаУчета = ПредопределенноеЗначение("Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив");
		СтруктураИзмененныхРеквизитов.Вставить("ВидОбъектаУчета");
		
		Если Форма.ПлательщикНалогаНаПрибыль Тогда
			Объект.ПервоначальнаяСтоимостьНУ = Объект.ПервоначальнаяСтоимостьБУ;
		КонецЕсли;
		Объект.ПервоначальнаяСтоимостьПР = 0;
		Объект.ПервоначальнаяСтоимостьВР = 0;
		
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВариантПримененияЦелевогоФинансирования") Тогда
		
		Если Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислениеАмортизацииИлиСписаниеНаРасходы")
			И (ЭтоИнвентаризация
				ИЛИ Объект.ВариантПримененияЦелевогоФинансирования = ПредопределенноеЗначение("Перечисление.ВариантыПримененияЦелевогоФинансирования.Полное")) Тогда
					
			Объект.ПорядокУчетаНУ = ПредопределенноеЗначение("Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию");
			СтруктураИзмененныхРеквизитов.Вставить("ПорядокУчетаНУ");
		КонецЕсли;
	
	КонецЕсли;

	Если СтруктураИзмененныхРеквизитов.Свойство("Организация") Тогда
		Если Не Форма.ЕстьСвязанныеОрганизации Тогда
			Объект.ПередаватьРасходыВДругуюОрганизацию = Ложь;
			СтруктураИзмененныхРеквизитов.Вставить("ПередаватьРасходыВДругуюОрганизацию");
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ХозяйственнаяОперация") Тогда
			
		Если СлужебныеПараметрыФормы.ДоступныеВариантыИНВ.Количество() = 0
			ИЛИ Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМАпоИнвентаризации") Тогда
			
			Объект.ИнвестиционныйНалоговыйВычет = ПредопределенноеЗначение("Перечисление.ВариантыПримененияИНВ.ПустаяСсылка");
			СтруктураИзмененныхРеквизитов.Вставить("ИнвестиционныйНалоговыйВычет");
		КонецЕсли;

	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

//++ Локализация

Процедура ПересчитатьСуммуВР(Форма) Экспорт

	Если (Форма.Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПринятиеКУчетуНМАпоИнвентаризации")
			ИЛИ НЕ Форма.СлужебныеПараметрыФормы.ЕстьУчетСебестоимости)
		И Форма.ПоддержкаПБУ18 Тогда
		
		Форма.Объект.ПервоначальнаяСтоимостьВР = 
				Форма.Объект.ПервоначальнаяСтоимостьБУ 
				- Форма.Объект.ПервоначальнаяСтоимостьНУ 
				- Форма.Объект.ПервоначальнаяСтоимостьПР;
				
	КонецЕсли; 

КонецПроцедуры

//-- Локализация

#КонецОбласти

#КонецОбласти
