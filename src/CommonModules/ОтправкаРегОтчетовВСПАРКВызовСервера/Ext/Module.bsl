
#Область ПрограммныйИнтерфейс

Процедура ЗаписатьСтатистикуОтправкиБухОтчетностиВСПАРК(Показатель) Экспорт

	//СтатистикаБРО.БухОтчетностьВСПАРК.СогласилисьСразу + 1
	//СтатистикаБРО.БухОтчетностьВСПАРК.ОтказалисьСразу + 1
	//СтатистикаБРО.БухОтчетностьВСПАРК.Передумали + 1
	//СтатистикаБРО.БухОтчетностьВСПАРК.ПревышеноПопыток + 1
	//СтатистикаБРО.БухОтчетностьВСПАРК.ОтказалисьПовторитьОтправкуПослеПревышеноПопыток + 1
	//СтатистикаБРО.БухОтчетностьВСПАРК.СогласилисьПовторитьОтправкуПослеПревышеноПопыток + 1
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЦентрМониторинга") Тогда
		
		МодульЦентрМониторинга = ОбщегоНазначения.ОбщийМодуль("ЦентрМониторинга");
		
		ИмяОперации = "СтатистикаБРО.БухОтчетностьВСПАРК." + Показатель;
		МодульЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики(ИмяОперации, 1);
		
	КонецЕсли;
	
КонецПроцедуры

Функция КоличествоНеудачныхПопыток() Экспорт

	Возврат 20;

КонецФункции
 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОтчетыКоторыеНеУдаетсяОтправитьВСПАРК() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтчетыПереданныеВСПАРК.Отчет КАК Отчет
		|ИЗ
		|	РегистрСведений.ОтчетыПереданныеВСПАРК КАК ОтчетыПереданныеВСПАРК
		|ГДЕ
		|	ОтчетыПереданныеВСПАРК.СтатусОтправки = ЗНАЧЕНИЕ(Перечисление.СтатусыОтправкиВСПАРК.ВозниклаОшибка)
		|	И ОтчетыПереданныеВСПАРК.КоличествоНеудачныхПопыток >= &КоличествоНеудачныхПопыток";
	
	Запрос.УстановитьПараметр("КоличествоНеудачныхПопыток", ОтправкаРегОтчетовВСПАРКВызовСервера.КоличествоНеудачныхПопыток());
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса.ВыгрузитьКолонку("Отчет");
	
КонецФункции

Функция ОтчетКОтправкеВСПАРК() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтчетыПереданныеВСПАРК.Отчет КАК Отчет
		|ИЗ
		|	РегистрСведений.ОтчетыПереданныеВСПАРК КАК ОтчетыПереданныеВСПАРК
		|ГДЕ
		|	ОтчетыПереданныеВСПАРК.СтатусОтправки = ЗНАЧЕНИЕ(Перечисление.СтатусыОтправкиВСПАРК.ВозниклаОшибка)
		|	И ОтчетыПереданныеВСПАРК.КоличествоНеудачныхПопыток < &КоличествоНеудачныхПопыток";
	
	Запрос.УстановитьПараметр("КоличествоНеудачныхПопыток", ОтправкаРегОтчетовВСПАРКВызовСервера.КоличествоНеудачныхПопыток());
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса.ВыгрузитьКолонку("Отчет");
	
КонецФункции

Функция НачатьОтправкуОтчетовВСПАРКВФоне(ДопПараметры) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отправка отчетов в СПАРК';
															|en = 'Отправка отчетов в СПАРК'");
	ПараметрыВыполнения.ОжидатьЗавершение = Истина;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ОтправкаРегОтчетовВСПАРК.ОтправкаОтчетовВСПАРКВФоне", 
		ДопПараметры, 
		ПараметрыВыполнения);

КонецФункции
	
Процедура ОтправитьОтчетВСПАРКПриНеобходимостиПоТранспортномуСообщениюСервер(ТранспортноеСообщение) Экспорт
	
	НеобходимоОтправить = ОтправкаРегОтчетовВСПАРК.ПроверитьНеобходимостьОтправитьВСПАРКПоТранспортномуСообщению(ТранспортноеСообщение);
	
	Если НеобходимоОтправить Тогда
		Результат = ОтправкаРегОтчетовВСПАРК.ОтправитьОтчетВСПАРКиЗаписатьСтатус(ТранспортноеСообщение.ЦиклОбмена.Предмет);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьОтчетСПАРКВРегистр(
		СсылкаНаОтчет, 
		СтатусОтправки, 
		ОписаниеОшибки = "") Экспорт
		
	ОтправкаРегОтчетовВСПАРК.ЗаписатьОтчетСПАРКВРегистр(
		СсылкаНаОтчет, 
		СтатусОтправки, 
		ОписаниеОшибки);

КонецПроцедуры

Процедура ОбработатьОтчетыУКоторыхЭЦПФНСНеверна() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОтчетыСОшибкой = ОтчетыУКоторыхЭЦПФНСНеверна();
	
	Для каждого Отчет Из ОтчетыСОшибкой Цикл
	
		ЗаписатьОтчетСПАРКВРегистр(
			Отчет, 
			ПредопределенноеЗначение("Перечисление.СтатусыОтправкиВСПАРК.ОтправленУспешно"));
			
		// Исправляем статус отчетов в форме 1С-Отчетность
		Отказ = Ложь;
		РегламентированнаяОтчетность.ЗаписьОбъектовРегламентированнойОтчетности(Отчет, Отказ);
	
	КонецЦикла;
	
КонецПроцедуры

Функция ОтчетыУКоторыхЭЦПФНСНеверна() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтчетыПереданныеВСПАРК.Отчет КАК Отчет
		|ИЗ
		|	РегистрСведений.ОтчетыПереданныеВСПАРК КАК ОтчетыПереданныеВСПАРК
		|ГДЕ
		|	ОтчетыПереданныеВСПАРК.СтатусОтправки = ЗНАЧЕНИЕ(Перечисление.СтатусыОтправкиВСПАРК.ВозниклаОшибка)
		|	И ОтчетыПереданныеВСПАРК.ОписаниеОшибки ПОДОБНО ""%математически неверна%""
		|	И ГОД(ОтчетыПереданныеВСПАРК.ДатаДобавления) = 2024";
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса.ВыгрузитьКолонку("Отчет");
	
КонецФункции

Функция НерасшифрованныйРезультатПриемаДекларации(Отчет) Экспорт
	
	Результат = Неопределено;
	
	ТипСообщения = Перечисления.ТипыТранспортныхСообщений.РезультатПриемаДекларацияНО;
	Статус = Перечисления.СтатусыПисем.Полученное;
	ЦиклОбмена = ДокументооборотСКОВызовСервера.ПолучитьПоследнийЦиклОбмена(Отчет);
	
	НерасшифрованноеСообщение = НерасшифрованноеСообщениеПоТипуСообщений(ЦиклОбмена, ТипСообщения, Статус);	 
	Если ЗначениеЗаполнено(НерасшифрованноеСообщение) Тогда
		Результат = НерасшифрованноеСообщение;
		Возврат Результат;
	КонецЕсли;
	
	СообщениеБезПодписи = СообщениеБезПрикрепленнойПодписи(ЦиклОбмена, ТипСообщения, Статус);	 
	Если ЗначениеЗаполнено(СообщениеБезПодписи) Тогда
		Результат = СообщениеБезПодписи;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СообщениеБезПрикрепленнойПодписи(ЦиклОбмена, ТипСообщения, Статус)
	
	Результат = Неопределено;
	ТипПодписи = Перечисления.ТипыСодержимогоТранспортногоКонтейнера.ЭлектронноЦифроваяПодпись;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СодержимоеТранспортныхКонтейнеров.Идентификатор КАК Идентификатор,
	|	СодержимоеТранспортныхКонтейнеров.ТранспортноеСообщение КАК ТранспортноеСообщение,
	|	СодержимоеТранспортныхКонтейнеров.Тип КАК Тип,
	|	СодержимоеТранспортныхКонтейнеров.ИмяФайла КАК ИмяФайла,
	|	СодержимоеТранспортныхКонтейнеров.ЭЦПИмяПодписанногоФайла КАК ЭЦПИмяПодписанногоФайла
	|ИЗ
	|	РегистрСведений.СодержимоеТранспортныхКонтейнеров КАК СодержимоеТранспортныхКонтейнеров
	|ГДЕ
	|	СодержимоеТранспортныхКонтейнеров.ТранспортноеСообщение.ЦиклОбмена = &ЦиклОбмена
	|	И СодержимоеТранспортныхКонтейнеров.ТранспортноеСообщение.ПометкаУдаления = ЛОЖЬ
	|	И СодержимоеТранспортныхКонтейнеров.Тип = &ТипПодписи
	|	И СодержимоеТранспортныхКонтейнеров.ТранспортноеСообщение.Тип = &ТипСообщения
	|	И СодержимоеТранспортныхКонтейнеров.ЭЦПЭтоПодписьАбонента = ЛОЖЬ
	|	И СодержимоеТранспортныхКонтейнеров.ЭЦПИмяПодписанногоФайла = &ИмяПодписанногоФайла";
	
	Запрос.УстановитьПараметр("ТипПодписи", ТипПодписи);
	Запрос.УстановитьПараметр("ТипСообщения", ТипСообщения);
	Запрос.УстановитьПараметр("ЦиклОбмена", ЦиклОбмена);
	Запрос.УстановитьПараметр("ИмяПодписанногоФайла", "");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Результат = ВыборкаДетальныеЗаписи.ТранспортноеСообщение;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции 

Функция НерасшифрованноеСообщениеПоТипуСообщений(ЦиклОбмена, ТипСообщения, Статус) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТранспортныеСообщения.Ссылка КАК ТранспортноеСообщение
	|ПОМЕСТИТЬ ВТ_ТранспортныеКонтейнеры
	|ИЗ
	|	Документ.ТранспортноеСообщение КАК ТранспортныеСообщения
	|ГДЕ
	|	ТранспортныеСообщения.ЦиклОбмена = &ЦиклОбмена
	|	И ТранспортныеСообщения.Тип = &Тип
	|	И ТранспортныеСообщения.ПометкаУдаления = ЛОЖЬ
	|	И ТранспортныеСообщения.Статус = &Статус
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СодержимоеТранспортныхКонтейнеров.ТранспортноеСообщение КАК ТранспортноеСообщение,
	|	КОЛИЧЕСТВО(ИСТИНА) КАК Количество
	|ПОМЕСТИТЬ ВТ_РасшифрованныеВложения
	|ИЗ
	|	РегистрСведений.СодержимоеТранспортныхКонтейнеров КАК СодержимоеТранспортныхКонтейнеров
	|ГДЕ
	|	СодержимоеТранспортныхКонтейнеров.ТранспортноеСообщение В
	|			(ВЫБРАТЬ
	|				ВТ_ТранспортныеКонтейнеры.ТранспортноеСообщение КАК ТранспортноеСообщение
	|			ИЗ
	|				ВТ_ТранспортныеКонтейнеры КАК ВТ_ТранспортныеКонтейнеры)
	|
	|СГРУППИРОВАТЬ ПО
	|	СодержимоеТранспортныхКонтейнеров.ТранспортноеСообщение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЕСТЬNULL(ВТ_РасшифрованныеВложения.Количество, 0) КАК КоличествоРасшифрованных,
	|	ВТ_ТранспортныеКонтейнеры.ТранспортноеСообщение КАК ТранспортноеСообщение
	|ИЗ
	|	ВТ_ТранспортныеКонтейнеры КАК ВТ_ТранспортныеКонтейнеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасшифрованныеВложения КАК ВТ_РасшифрованныеВложения
	|		ПО ВТ_ТранспортныеКонтейнеры.ТранспортноеСообщение = ВТ_РасшифрованныеВложения.ТранспортноеСообщение
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_ТранспортныеКонтейнеры.ТранспортноеСообщение УБЫВ";
	
	Запрос.УстановитьПараметр("ЦиклОбмена", ЦиклОбмена);
	Запрос.УстановитьПараметр("Тип", ТипСообщения);
	Запрос.УстановитьПараметр("Статус", Статус);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.КоличествоРасшифрованных = 0 Тогда
			Результат = ВыборкаДетальныеЗаписи.ТранспортноеСообщение;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции   

#КонецОбласти