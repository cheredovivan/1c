///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП".
// ОбщийМодуль.СистемаБыстрыхПлатежей.
//
// Серверные процедуры обмена данными с Системой быстрых платежей:
//  - предоставление учетных данных мерчанта;
//  - загрузка и обработка настроек.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область НастройкиПодключения

// Определяет доступность использования функциональности подключения
// к СБП на основании прав доступа пользователя.
//
// Возвращаемое значение:
//  Булево - если Истина, настройка подключения к Системе быстрых платежей доступна.
//
Функция НастройкаПодключенияДоступна() Экспорт
	
	Возврат ПравоДоступа("Изменение", Метаданные.Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей)
		И ПравоДоступа("Добавление", Метаданные.Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей)
		И ПравоДоступа("Просмотр", Метаданные.Обработки.ПодключениеКСБП)
		И ПравоДоступа("Использование", Метаданные.Обработки.ПодключениеКСБП);
	
КонецФункции

// Определяет возможность настройки подключения к Системе быстрых платежей.
//
// Параметры:
//  БИК - Строка - идентификатор банка;
//
// Возвращаемое значение:
//  Структура - настройки подключения:
//    * ИнтеграцияДоступнаСБПc2b - Булево - если Истина, участник СБП интегрирован;
//    * ПараметрыУчастникаСБП - Структура, Неопределено - информация об участие СБП:
//      ** Наименование - Строка - имя банка участника СБП;
//      ** НастройкаПодключения  - Массив из СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - список
//        настроек подключения;
//      ** Идентификатор  - Строка - идентификатор участника СБП;
//
Функция ПараметрыПодключенияПоБИК(БИК) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИнтеграцияДоступнаСБПc2b", Ложь);
	Результат.Вставить("ПараметрыУчастникаСБП ", Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НастройкиУчастниковСБП.Идентификатор КАК Идентификатор,
		|	НастройкиУчастниковСБП.Наименование КАК Наименование
		|ИЗ
		|	РегистрСведений.БИКУчастниковСБП КАК БИКУчастниковСБП
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчастниковСБП КАК НастройкиУчастниковСБП
		|		ПО БИКУчастниковСБП.Идентификатор = НастройкиУчастниковСБП.Идентификатор
		|ГДЕ
		|	БИКУчастниковСБП.БИК = &БИК";
	
	Запрос.УстановитьПараметр("БИК", БИК);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	ПараметрыУчастникаСБП = Новый Структура;
	ПараметрыУчастникаСБП.Вставить("Наименование", ВыборкаДетальныеЗаписи.Наименование);
	ПараметрыУчастникаСБП.Вставить("НастройкаПодключения", Новый Массив);
	ПараметрыУчастникаСБП.Вставить("Идентификатор", ВыборкаДетальныеЗаписи.Идентификатор);
	
	Результат.ИнтеграцияДоступнаСБПc2b = Истина;
	Результат.ПараметрыУчастникаСБП = ПараметрыУчастникаСБП;
	ПараметрыУчастникаСБП.НастройкаПодключения = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.НастройкиПодключенияУчастникаСБП(
		ВыборкаДетальныеЗаписи.Идентификатор);
	
	Возврат Результат;
	
КонецФункции

// Определяет доступность операций для настройки подключения к СБП. В сценариях оплаты
// различных участников СБП существуют отличия, поэтому ряд операций могут
// быть запрещены для выполнения. Метод следует использовать для настройки
// элементов форм оплаты и возвратов и получение общих настроек подключения.
//
// Параметры:
//  Настройка - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//   настройка выполнения операции СБП.
//
// Возвращаемое значение:
//  Структура - общие настройки и информация о доступных операциях:
//    * Идентификатор - Строка - строковый идентификатор или участника СБП;
//    * СинонимСистемы - Строка - синоним для печати;
//    * Используется - Булево - признак активности настройки;
//    * ИНН - Строка - ИНН участника СБП;
//    * БИК - Массив из Строка - список БИК участника СБП;
//    * Наименование - Строка - наименование участника СБП;
//    * НастройкиСБПc2b - Структура - настройки выполнения переводов c2b:
//       ** Переводыc2b - Булево - признак доступности переводов СБП c2b;
//       ** КассовыеСсылки - Булево - признак доступности использования кассовых ссылок;
//       ** ПлатежныйАгрегатор - Булево - настроена интеграция с платежным агрегатором;
//       ** УчастникСБПВозврата - Булево - Устарело. Следует использовать ПереводыСБПc2b.НастройкиВозвратаОплаты
//          признак доступности возврата оплаты на счет отличный отчета оплаты. Для получения списка
//          доступных участников следует использовать метод ПереводыСБПc2b.УчастникиСБПДляВозврата;
//       ** ПодключениеКассовойСсылки - Булево - определяет доступность подключения кассовой ссылки;
//       ** СверкаВзаиморасчетов - Булево - определяет доступность использования сверки;
//    * НастройкиСБПb2b - Структура - настройки выполнения переводов c2b:
//       ** Переводыb2b - Булево - признак доступности переводов СБП b2b;
//       ** ВидПоступления - Строка, Неопределено - определяет вид зачисления денежных средств на расчетный счет продавца.
//          Возможные значения:
//           - ACQUIRING - зачисление по схеме эквайринга;
//           - PAYMENT_FROM_THE_BUYER - перевод от юридического лица;
//           - Неопределено - вид поступления не определен.
//
Функция НастройкиПодключения(Настройка) Экспорт
	
	// Проверка входных параметров
	ИмяФункции = "СистемаБыстрыхПлатежей.НастройкиПодключения";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"Настройка",
		Настройка,
		Тип("СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей"));
	
	Если Не ЗначениеЗаполнено(Настройка) Тогда
		ВызватьИсключение НСтр("ru = 'Параметр ""НастройкаПодключения"" не заполнен.';
								|en = 'The ""НастройкаПодключения"" parameter is not filled.'");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	РеквизитыНастройки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Настройка,
		"Используется, ИдентификаторУчастника, ВариантНастройки");
	
	НастройкиУчастникаСБП = СистемаБыстрыхПлатежейСлужебный.НастройкиУчастникаСБП(
		РеквизитыНастройки.ИдентификаторУчастника);
	
	Если НастройкиУчастникаСБП = Неопределено Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Неопределенны настройки участника СБП по идентификатору %1.
				|Проверьте данные регистра сведений НастройкиУчастниковСБП.';
				|en = 'FPS party settings by ID %1 are not  defined.
				|Check the НастройкиУчастниковСБП information register data.'"),
			РеквизитыНастройки.ИдентификаторУчастника);
	КонецЕсли;
	
	ОбщиеНастройки = Новый Структура;
	ОбщиеНастройки.Вставить("Используется", РеквизитыНастройки.Используется);
	ОбщиеНастройки.Вставить("Идентификатор", РеквизитыНастройки.ИдентификаторУчастника);
	ОбщиеНастройки.Вставить("СинонимСистемы", НСтр("ru = 'Система быстрых платежей';
													|en = 'Faster Payments System'"));
	ОбщиеНастройки.Вставить("БИК", Новый Массив);
	ОбщиеНастройки.Вставить("Наименование", НастройкиУчастникаСБП.Наименование);
	ОбщиеНастройки.Вставить("ИНН", НастройкиУчастникаСБП.ИНН);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	БИКУчастниковСБП.БИК КАК БИК
		|ИЗ
		|	РегистрСведений.БИКУчастниковСБП КАК БИКУчастниковСБП
		|ГДЕ
		|	БИКУчастниковСБП.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр(
		"Идентификатор",
		РеквизитыНастройки.ИдентификаторУчастника);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ОбщиеНастройки.БИК.Добавить(ВыборкаДетальныеЗаписи.БИК);
	КонецЦикла;
	
	СистемаБыстрыхПлатежейСобытия.ПриОпределенииНастроекПоНастройкиПодключения(
		РеквизитыНастройки.ВариантНастройки,
		НастройкиУчастникаСБП,
		ОбщиеНастройки);
	
	Возврат ОбщиеНастройки;
	
КонецФункции

#КонецОбласти

#Область ДополнительныеОперации

// Выполняет загрузку статусов операций СБП, по которым было
// отложено получение результата.
//
// Возвращаемое значение:
//  Массив из ОпределяемыйТип.ДокументОперацииСБП - данные обработанных документов.
//
Функция СтатусыОпераций() Экспорт
	
	Возврат СистемаБыстрыхПлатежейСлужебный.СтатусыОпераций();
	
КонецФункции

// Формирование изображения идентификатора оплаты (QR-кода).
//
// Параметры: 
//  ФункциональнаяСсылка - Строка - идентификатор оплаты;
//  Размер - Число - размер QR-кода в пикселях;
//  ТипПечати - Число - вариант печати QR-кода. В зависимости от выбранного варианта
//    печати подбирается картина с логотипом Системы быстрых платежей. Возможные варианты:
//      0 - печать на цветном принтере;
//      1 - монохромная печать;
//      2 - черно-белая печать;
//      3 - без логотипа.
//
// Возвращаемое значение:
//  ДвоичныеДанные - данные изображения QR-кода.
//
Функция ИзображениеQRКодаСБП(ФункциональнаяСсылка, Размер, ТипПечати = 0) Экспорт
	
	// Проверка входных параметров
	ИмяФункции = "СистемаБыстрыхПлатежей.ИзображениеQRКодаСБП";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"ФункциональнаяСсылка",
		ФункциональнаяСсылка,
		Тип("Строка"));
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"Размер",
		Размер,
		Тип("Число"));
	
	Логотип = Неопределено;
	Если ТипПечати = 0 Тогда
		Логотип = БиблиотекаКартинок.ЛоготипСБПЦветной;
	ИначеЕсли ТипПечати = 1 Тогда
		Логотип = БиблиотекаКартинок.ЛоготипСБПМонохромный;
	ИначеЕсли ТипПечати = 2 Тогда
		Логотип = БиблиотекаКартинок.ЛоготипСБПЧерный;
	КонецЕсли;
	
	ПараметрыГенерации = ГенерацияШтрихкода.ПараметрыГенерацииШтрихкода();
	ПараметрыГенерации.Ширина = Размер;
	ПараметрыГенерации.Высота = Размер;
	ПараметрыГенерации.ТипКода = 16;
	ПараметрыГенерации.Штрихкод = ФункциональнаяСсылка;
	ПараметрыГенерации.УровеньКоррекцииQR = 3;
	
	ПараметрыГенерации.ЛоготипКартинка = ?(Логотип = Неопределено,
		"",
		Base64Строка(Логотип.ПолучитьДвоичныеДанные()));
	ПараметрыГенерации.ЛоготипРазмерПроцентОтШК = ?(Логотип = Неопределено,
		0,
		33);
	
	Результат = ГенерацияШтрихкода.ИзображениеШтрихкода(
		ПараметрыГенерации);
	
	Возврат Результат.ДвоичныеДанные;
	
КонецФункции

#КонецОбласти

#Область СохранениеНастроек

// Возвращает список участников СБП.
// Метод следует использовать только при реализации программного создания настроек.
//
// Возвращаемое значение:
//  Структура - результат запроса:
//   * ДанныеУчастников - Соответствие - Содержит описание данных участников СБП
//      ** Ключ - Строка - идентификатор участника СБП.
//      ** Значение - Структура - Описание параметров участника СБП.
//          *** Представление - Строка - Представление участника СБП для пользователя.
//          *** АдресСтраницыПодключения - Строка - Адрес страницы подключения к участнику СБП.
//          *** ТекстПредупреждения - Строка - Представление участника СБП для пользователя.
//          *** ИНН - Строка - ИНН участника СБП.
//          *** БИК - Массив Из Строк - Массив БИК участника СБП;
//          *** АдресСтраницыЗаявки - Строка - адресе страницы отправки заявки;
//          *** ПартнерАгентаСБП - Булево - признак подключения банка партнера Агента ТСП.
//          *** ПорядокОтображения - Число - порядок отображения участника в списке.
//   * Ошибка - Булево, Истина - если задание завершено с ошибкой.
//   * СообщениеОбОшибке - Строка, Форматированная строка - сообщение об ошибке для пользователя.
//   * ИнформацияОбОшибке - Строка, Форматированная строка - сообщение об ошибке для администратора.
//
Функция ПолучитьУчастниковСБП() Экспорт
	
	РезультатОперации = Новый Структура;
	РезультатОперации.Вставить("Ошибка", Ложь);
	РезультатОперации.Вставить("СообщениеОбОшибке", Неопределено);
	РезультатОперации.Вставить("ИнформацияОбОшибке", Неопределено);
	РезультатОперации.Вставить("ДанныеУчастников", Новый Соответствие);
	
	РезультатПолучения = СистемаБыстрыхПлатежейСлужебный.ПолучитьУчастниковСБП();
	
	РезультатОперации.Ошибка = ?(ЗначениеЗаполнено(РезультатПолучения.КодОшибки),
		Истина,
		Ложь);
	ЗаполнитьЗначенияСвойств(
		РезультатОперации,
		РезультатПолучения,
		"СообщениеОбОшибке, ИнформацияОбОшибке");
	
	Если РезультатОперации.Ошибка Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из РезультатПолучения.ДанныеУчастников Цикл
		Если КлючЗначение.Значение.c2b Тогда
			НастройкиУчастника = Новый Структура;
			НастройкиУчастника.Вставить("Представление", КлючЗначение.Значение.Представление);
			НастройкиУчастника.Вставить("АдресСтраницыПодключения", КлючЗначение.Значение.АдресСтраницыПодключенияc2b);
			НастройкиУчастника.Вставить("ТекстПредупреждения", КлючЗначение.Значение.ТекстПредупрежденияc2b);
			НастройкиУчастника.Вставить("БИК", КлючЗначение.Значение.БИК);
			НастройкиУчастника.Вставить("АдресСтраницыЗаявки", КлючЗначение.Значение.АдресСтраницыЗаявкиc2b);
			НастройкиУчастника.Вставить("ПартнерАгентаСБП", КлючЗначение.Значение.ПартнерАгентаСБП);
			НастройкиУчастника.Вставить("ИНН", КлючЗначение.Значение.ИНН);
			НастройкиУчастника.Вставить("ПорядокОтображения", КлючЗначение.Значение.ПорядокОтображенияc2b);
			РезультатОперации.ДанныеУчастников.Вставить(
				КлючЗначение.Ключ,
				НастройкиУчастника);
		КонецЕсли;
	КонецЦикла;
	
	Возврат РезультатОперации;
	
КонецФункции

// Возвращает параметры подключения, оплаты и аутентификации для участника СБП.
// Метод следует использовать только при реализации программного создания настроек.
//
// Параметры:
//  УчастникСБП - Строка - идентификатор участника СБП для которого получается настройка.
//
// Возвращаемое значение:
//  Структура - результат выполнения:
//   * ПараметрыНастройкиПодключения - Массив из Структура - содержит описание и значения полей реквизитов справочника
//       "НастройкиПодключенияКСистемеБыстрыхПлатежей".
//   * ПараметрыОплаты - Структура - настройки приема оплат:
//      ** c2b - Массив из Структура, Неопределено - содержит описание и значения полей регистра сведений,
//         хранящего данные о настройках оплат.
//   * ПараметрыАутентификации - Массив из Структура - описание и значения параметров аутентификации.
//
Функция ПараметрыПодключенияПоУчастникуСБП(УчастникСБП) Экспорт
	
	// Проверка входных параметров
	ИмяФункции = "СистемаБыстрыхПлатежей.ПараметрыПодключенияПоУчастникуСБП";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"УчастникСБП",
		УчастникСБП,
		Тип("Строка"));
	
	Возврат СистемаБыстрыхПлатежейСлужебный.ПараметрыПодключенияПоУчастникуСБП(
		УчастникСБП,
		Перечисления.ВариантыНастройкиСБП.c2b);
	
КонецФункции

// Возвращает параметры подключения, оплаты и аутентификации для переданного по ссылке элемента справочника
// "НастройкиПодключенияКСистемеБыстрыхПлатежей".
// Метод следует использовать только при реализации программного создания настроек.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - ссылка на элемент справочника 
//    для которого нужно получить параметры подключения.
//
// Возвращаемое значение:
//  Структура, Неопределено - результат выполнения:
//   * ПараметрыНастройкиПодключения - Массив из Структура - содержит описание и значения полей реквизитов элемента справочника
//       "НастройкиПодключенияКСистемеБыстрыхПлатежей";
//   * ПараметрыОплаты - Структура - настройки приема оплат:
//      ** c2b - Массив из Структура, Неопределено - содержит описание и значения полей регистра сведений,
//         хранящего данные о настройках оплат.
//   * ПараметрыАутентификации - Массив из Структура - описание и значения параметров аутентификации.
//
Функция ПараметрыПодключенияПоНастройке(НастройкаПодключения) Экспорт
	
	// Проверка входных параметров
	ИмяФункции = "СистемаБыстрыхПлатежей.ПараметрыПодключенияПоНастройке";
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"НастройкаПодключения",
		НастройкаПодключения,
		Тип("СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей"));
	
	ДанныеНастройкиПодключения = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ДанныеНастройкиПодключения(
		НастройкаПодключения);
	Если ДанныеНастройкиПодключения.ВариантНастройки <> Перечисления.ВариантыНастройкиСБП.c2b Тогда
		ВызватьИсключение НСтр("ru = 'Получение параметров настройки поддерживается только для варианта работы c2b.';
								|en = 'Receiving setting parameters is supported only for c2b.'");
	КонецЕсли;
	
	Возврат СистемаБыстрыхПлатежейСлужебный.ПараметрыПодключенияПоНастройке(
		НастройкаПодключения);
	
КонецФункции

// Возвращает результат проверки подключения по переданным параметрам.
// Метод следует использовать только при реализации программного создания настроек.
//
// Параметры:
//  ИдентификаторУчастника - Строка - идентификатор участника СБП для которого производится настройка.
//  ПараметрыАутентификации - Массив из Структура - описание и значения параметров аутентификации.
//
// Возвращаемое значение:
//  Структура - результат проверки параметров подключения:
//   * ПодключениеУстановлено - Булево - Истина - если подключение выполнено успешно;
//   * СообщениеОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//
Функция ПроверитьПараметрыПодключения(
		ИдентификаторУчастника,
		ПараметрыАутентификации) Экспорт
	
	ПараметрыАутентификацииСоответствием = Новый Соответствие;
	Для Каждого ОписаниеКлюча Из ПараметрыАутентификации Цикл
		Если ОписаниеКлюча.Значение <> НастройкиПлатежныхСистемКлиентСервер.УдалитьНечитаемыеСимволы(ОписаниеКлюча.Значение) Тогда
			ОписаниеКлюча.Значение = НастройкиПлатежныхСистемКлиентСервер.УдалитьНечитаемыеСимволы(
				ОписаниеКлюча.Значение);
		КонецЕсли;
		ПараметрыАутентификацииСоответствием.Вставить(
			ОписаниеКлюча.Имя,
			ОписаниеКлюча.Значение);
	КонецЦикла;
	
	РезультатПроверки = СистемаБыстрыхПлатежейСлужебный.ПроверитьПараметрыПодключения(
		ИдентификаторУчастника,
		ПараметрыАутентификацииСоответствием,
		Перечисления.ВариантыНастройкиСБП.c2b);
	РезультатПроверки.Вставить(
		"ПодключениеУстановлено",
		?(ЗначениеЗаполнено(РезультатПроверки.КодОшибки),
		Ложь,
		Истина));
	РезультатПроверки.Удалить("КодОшибки");
	
	Возврат РезультатПроверки;
	
КонецФункции

// Сохраняет настройки подключения к СБП в информационную базу.
// Метод следует использовать только при реализации программного создания настроек.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - ссылка на элемент справочника,
//    данные которого нужно записать. Пустая, если нужно записать новый элемент.
//  НастройкиЗаполнения - Структура - описание сохраняемых данных:
//   * ПараметрыНастройкиПодключения - Массив из Структура - содержит описание и значения полей реквизитов элемента справочника
//       "НастройкиПодключенияКСистемеБыстрыхПлатежей";
//   * ПараметрыОплаты - Структура - настройки приема оплат:
//      ** c2b - Массив из Структура, Неопределено - содержит описание и значения полей регистра сведений,
//         хранящего данные о настройках оплат.
//   * ПараметрыАутентификации - Массив из Структура - описание и значения параметров аутентификации.
//  ИдентификаторУчастника - Строка - идентификатор участника СБП для которого производится настройка.
//
// Возвращаемое значение:
//  Структура - результат создания торговой точки:
//   * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - ссылка на элемент справочника;
//   * Ошибка - Булево - признак ошибки сохранения;
//   * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//   * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ЗаписатьПараметрыПодключения(
		НастройкаПодключения,
		НастройкиЗаполнения,
		ИдентификаторУчастника) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НастройкаПодключения", НастройкаПодключения);
	Результат.Вставить("Ошибка", Ложь);
	Результат.Вставить("СообщениеОбОшибке", "");
	Результат.Вставить("ИнформацияОбОшибке", "");
	
	Если ЗначениеЗаполнено(НастройкаПодключения) Тогда
		
		НовоеЗначениеРеквизитаИспользуется = Неопределено;
		
		Для Каждого Реквизит Из НастройкиЗаполнения.ПараметрыНастройкиПодключения Цикл
			Если Реквизит.Имя = "Используется" Тогда
				НовоеЗначениеРеквизитаИспользуется = Реквизит.Значение;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ДанныеНастройки = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ДанныеНастройкиПодключения(НастройкаПодключения);
		
		Если НовоеЗначениеРеквизитаИспользуется <> Неопределено
			И ДанныеНастройки.Используется <> НовоеЗначениеРеквизитаИспользуется Тогда
			СистемаБыстрыхПлатежейСобытия.ПриИзмененииИспользованияНастройки(
				НастройкаПодключения,
				НовоеЗначениеРеквизитаИспользуется,
				ДанныеНастройки.ВариантНастройки,
				Истина,
				Результат);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Результат.Ошибка Тогда
		Возврат Результат;
	КонецЕсли;
	
	НастройкиУчастника = СистемаБыстрыхПлатежейСлужебный.НастройкиАвторизацииУчастникаСБП(
		ИдентификаторУчастника,
		Перечисления.ВариантыНастройкиСБП.c2b);
	
	Родитель = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ГруппаНастройкиБанк(
		ИдентификаторУчастника,
		Перечисления.ВариантыНастройкиСБП.c2b);
	
	НачатьТранзакцию();
	
	Попытка
		
		Для Каждого ПараметрАутентификации Из НастройкиЗаполнения.ПараметрыАутентификации Цикл 
			Если ПараметрАутентификации.Значение <> НастройкиПлатежныхСистемКлиентСервер.УдалитьНечитаемыеСимволы(ПараметрАутентификации.Значение) Тогда
				ПараметрАутентификации.Значение = НастройкиПлатежныхСистемКлиентСервер.УдалитьНечитаемыеСимволы(
						ПараметрАутентификации.Значение);
			КонецЕсли;
		КонецЦикла;
		
		Если ЗначениеЗаполнено(НастройкаПодключения) Тогда
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", НастройкаПодключения);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			НастройкаПодключенияОбъект = НастройкаПодключения.ПолучитьОбъект();
			НастройкаПодключенияОбъект.Заблокировать();
			
			Если НастройкаПодключенияОбъект.ЭтоГруппа Тогда
				
				ОтменитьТранзакцию();
				
				Результат.Ошибка = Истина;
				Результат.СообщениеОбОшибке = НСтр("ru = 'Ошибка записи настройки подключения.
					|В качестве значения параметра запрещено передавать группу справочника.';
					|en = 'An error occurred while saving the connection setting.
					|You cannot pass a catalog group as a parameter value.'");
				Результат.ИнформацияОбОшибке = Результат.СообщениеОбОшибке;
				
				Возврат Результат;
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(НастройкаПодключенияОбъект.Родитель)
				И НастройкаПодключенияОбъект.Родитель <> Родитель Тогда
			
				ОтменитьТранзакцию();
					
				Результат.Ошибка = Истина;
				Результат.СообщениеОбОшибке = НСтр("ru = 'Ошибка записи настройки подключения.
					|Запрещено менять платежную систему у существующей настройки.';
					|en = 'An error occurred when saving the connection setting.
					|You cannot change the payment system of an existing setting.'");
				Результат.ИнформацияОбОшибке = Результат.СообщениеОбОшибке;
				
				Возврат Результат;
				
			КонецЕсли;
			
		Иначе
			НастройкаПодключенияОбъект = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.СоздатьЭлемент();
			НастройкаПодключенияОбъект.Заполнить(Неопределено);
		КонецЕсли;
		
		НастройкаПодключенияОбъект.Родитель = Родитель;
		НастройкаПодключенияОбъект.ИдентификаторУчастника = ИдентификаторУчастника;
		НастройкаПодключенияОбъект.ВариантНастройки = Перечисления.ВариантыНастройкиСБП.c2b;
		
		Для Каждого Реквизит Из НастройкиЗаполнения.ПараметрыНастройкиПодключения Цикл
			
			Если Реквизит.Свойство("ДляГруппы") И Реквизит.ДляГруппы Тогда // АПК:1416 Универсальный механизм обработки настроек.
				Продолжить;
			КонецЕсли;
			
			Попытка
				НастройкаПодключенияОбъект[Реквизит.Имя] = Реквизит.Значение;
			Исключение
				
				ОтменитьТранзакцию();
				
				Результат.Ошибка = Истина;
				Результат.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось присвоить значение реквизиту %1 по причине:
					|%2';
					|en = 'Cannot set the value for the %1 attribute due to:
					|%2'"),
					Реквизит.Ключ,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				Результат.ИнформацияОбОшибке = Результат.СообщениеОбОшибке;
				
				Возврат Результат;
				
			КонецПопытки;
			
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(НастройкаПодключенияОбъект.ИдентификаторМерчанта) Тогда
			ПараметрыАутентификацииСоответствием = Новый Соответствие;
			Для Каждого ОписаниеКлюча Из НастройкиЗаполнения.ПараметрыАутентификации Цикл
				ПараметрыАутентификацииСоответствием.Вставить(ОписаниеКлюча.Имя, ОписаниеКлюча.Значение);
			КонецЦикла;
			НастройкаПодключенияОбъект.ИдентификаторМерчанта = СистемаБыстрыхПлатежейСлужебный.ИдентификаторМерчантаПоДаннымАутентификации(
				ПараметрыАутентификацииСоответствием);
		КонецЕсли;
		
		НастройкаПодключенияОбъект.Записать();
		
		ПараметрыОплаты = Новый Соответствие;
		Для Каждого ПараметрОплаты Из НастройкиЗаполнения.ПараметрыОплаты.c2b Цикл
			ПараметрыОплаты.Вставить(ПараметрОплаты.Имя, ПараметрОплаты.Значение);
		КонецЦикла;
		
		РезультатЗаписиНастроек = СистемаБыстрыхПлатежейСлужебный.ЗаписатьНастройкиОплаты(
			ПараметрыОплаты,
			НастройкаПодключенияОбъект.Ссылка,
			Перечисления.ВариантыНастройкиСБП.c2b);
		
		Если РезультатЗаписиНастроек.Отказ Тогда
			Результат.Ошибка = Истина;
			Результат.СообщениеОбОшибке = РезультатЗаписиНастроек.СообщениеОбОшибке;
			ОтменитьТранзакцию();
			Возврат Результат;
		КонецЕсли;
		
		ПараметрыАутентификации = Новый Соответствие;
		Для Каждого ПараметрАутентификации Из НастройкиЗаполнения.ПараметрыАутентификации Цикл
			ПараметрыАутентификации.Вставить(
				ПараметрАутентификации.Имя,
				ПараметрАутентификации.Значение);
		КонецЦикла;
		
		СистемаБыстрыхПлатежейСлужебный.СохранитьНастройкиАутентификации(
			НастройкаПодключенияОбъект.Ссылка,
			ПараметрыАутентификации,
			ИдентификаторУчастника,
			НастройкиУчастника.ТипАутентификации);
			
		Результат.НастройкаПодключения = НастройкаПодключенияОбъект.Ссылка;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		Если ТранзакцияАктивна() Тогда
			Пока ТранзакцияАктивна() Цикл
				ОтменитьТранзакцию(); // АПК:325 Отмена незакрытых транзакций.
			КонецЦикла;
		КонецЕсли;
		
		// Вызов во вложенной транзакции не предполагается,
		// вместо вызова исключения обработка ошибки делегируется
		// вызывающему методу.
		Результат.Ошибка = Истина;
		Результат.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось записать торговую точку мерчанта по причине:
				|%1';
				|en = 'Cannot save the merchant sales outlet due to:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Результат.ИнформацияОбОшибке = Результат.СообщениеОбОшибке;
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ШаблоныСообщений

// Возвращает данные предопределенных шаблонов сообщений подсистемы,
// структурированный по типам назначения.
//
// Возвращаемое значение:
//  Структура - данные предопределенных шаблонов сообщений подсистемы,
//   структурированный по сценариям перевода.
//   * c2b - Структура - перечень предопределенных шаблонов сообщений c2b сценария,
//       структурированный по типам назначения.
//     ** Ключ - Строка - тип назначения шаблона, может принимать значения - Письмо, SMS.
//     ** Значение - Массив из СправочникСсылка.ШаблоныСообщений - данные предопределенных
//       шаблонов по типу.
//   * b2b - Структура - шаблоны сообщений для переводов юридическим лица:
//     ** Ключ - Строка - тип назначения шаблона, может принимать значения - Письмо, SMS.
//     ** Значение - Массив из СправочникСсылка.ШаблоныСообщений - данные предопределенных
//         шаблонов подсистемы по типу.
//
Функция ШаблоныСообщенийПоТипам() Экспорт
	
	Возврат СистемаБыстрыхПлатежейСлужебный.ШаблоныСообщенийПоТипам();
	
КонецФункции

#КонецОбласти

#КонецОбласти