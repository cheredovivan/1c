////////////////////////////////////////////////////////////////////////////////
// Модуль содержит общие серверные процедуры и функции для модификации
// и заполнения данными файлов или шаблонов в форматах XLSХ, XLSM.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьЗначениеВЯчейкеЛиста(СтрокаАдреса, ЗначениеЯчейки,
			ТЗОбработки, ПараметрыВыгрузки, ДопПараметры = Неопределено) Экспорт
	
	Если ТипЗнч(СтрокаАдреса) = Тип("Структура") Тогда
		ИмяЛистаИСсылкаРазд = СтрокаАдреса;
	Иначе
		ИмяЛистаИСсылкаРазд = ИмяЛистаИСсылка(СтрокаАдреса);
	КонецЕсли;
	
	ФайлыЛистаСтроки = ТЗОбработки.НайтиСтроки(Новый Структура("ЭтоЛист,ИмяЛиста", Истина, ИмяЛистаИСсылкаРазд.ИмяЛиста));
	Если ФайлыЛистаСтроки.Количество() > 0 Тогда
		
		ФайлЛистаСтрока = ФайлыЛистаСтроки[0];
		Если ФайлЛистаСтрока.Статус = 0 Тогда
			ЗаполнитьКэшЛиста(ФайлЛистаСтрока);
			ФайлЛистаСтрока.Статус = 1;
		КонецЕсли;
		
		УзелЯчейки = УзелЯчейкиЛиста(ФайлЛистаСтрока, ИмяЛистаИСсылкаРазд.Ссылка);
		
		УзлыЗначениеЯчейки = УзелЯчейки.ПолучитьЭлементыПоИмени("v");
		Если УзлыЗначениеЯчейки.Количество() > 0 Тогда
			УзелЗначениеЯчейки = УзлыЗначениеЯчейки[0]; 
		Иначе
			Если УзелЯчейки.ЕстьАтрибут("t") И УзелЯчейки.ПолучитьАтрибут("t") = "inlineStr" Тогда
				УзлыЛокальныеСтроки = УзелЯчейки.ПолучитьЭлементыПоИмени("is");
				Если УзлыЛокальныеСтроки.Количество() > 0 Тогда
					УзелЛокальнаяСтрока = УзлыЛокальныеСтроки[0];
					УзлыЗначениеЯчейки = УзелЛокальнаяСтрока.ПолучитьЭлементыПоИмени("t");
					Если УзлыЗначениеЯчейки.Количество() > 0 Тогда
						УзелЗначениеЯчейки = УзлыЗначениеЯчейки[0];
					Иначе
						УзелЗначениеЯчейки = УзелЛокальнаяСтрока.ДобавитьДочерний(
							УзелЛокальнаяСтрока.ДокументВладелец.СоздатьЭлемент(УзелЯчейки.URIПространстваИмен, "t"));
						УзелЗначениеЯчейки.ОтменитьСоответствиеПространстваИмен(УзелЯчейки.URIПространстваИмен);
					КонецЕсли;
				Иначе
					УзелЛокальнаяСтрока = УзелЯчейки.ДобавитьДочерний(
						УзелЯчейки.ДокументВладелец.СоздатьЭлемент(УзелЯчейки.URIПространстваИмен, "is"));
					УзелЗначениеЯчейки = УзелЛокальнаяСтрока.ДобавитьДочерний(
						УзелЛокальнаяСтрока.ДокументВладелец.СоздатьЭлемент(УзелЯчейки.URIПространстваИмен, "t"));
					УзелЛокальнаяСтрока.ОтменитьСоответствиеПространстваИмен(УзелЯчейки.URIПространстваИмен);
					УзелЗначениеЯчейки.ОтменитьСоответствиеПространстваИмен(УзелЯчейки.URIПространстваИмен);
				КонецЕсли;
			Иначе
				УзелЗначениеЯчейки = УзелЯчейки.ДобавитьДочерний(
					УзелЯчейки.ДокументВладелец.СоздатьЭлемент(УзелЯчейки.URIПространстваИмен, "v"));
				УзелЗначениеЯчейки.ОтменитьСоответствиеПространстваИмен(УзелЯчейки.URIПространстваИмен);
			КонецЕсли;
		КонецЕсли;
		
		Если ТипЗнч(ЗначениеЯчейки) = Тип("Число") И НЕ УзелЯчейки.ЕстьАтрибут("t") Тогда
			УзелЗначениеЯчейки.ТекстовоеСодержимое = ФорматированноеЗначение(ЗначениеЯчейки);
		ИначеЕсли УзелЯчейки.ЕстьАтрибут("t") И УзелЯчейки.ПолучитьАтрибут("t") = "inlineStr" Тогда
			УзелЗначениеЯчейки.ТекстовоеСодержимое = ФорматированноеЗначение(ЗначениеЯчейки);
		Иначе
			ФорматированноеЗначениеЯчейки = ФорматированноеЗначение(ЗначениеЯчейки);
			Если ФорматированноеЗначениеЯчейки <> "" ИЛИ УзелЯчейки.ЕстьАтрибут("t") Тогда
				Если УзелЯчейки.ПолучитьЭлементыПоИмени("f").Количество() > 0
				   И УзелЯчейки.ЕстьАтрибут("t") И УзелЯчейки.ПолучитьАтрибут("t") = "str" Тогда
					УзелЗначениеЯчейки.ТекстовоеСодержимое = ФорматированноеЗначениеЯчейки;
				Иначе
					УзелЯчейки.УстановитьАтрибут("t", "s");
					УзелЗначениеЯчейки.ТекстовоеСодержимое = СтрИндексОбщейСтроки(ФорматированноеЗначениеЯчейки, ПараметрыВыгрузки);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
			
			ПримечаниеЯчейки = Неопределено;
			Если ДопПараметры.Свойство("Примечание", ПримечаниеЯчейки) Тогда
				УстановитьПримечаниеВЯчейкеЛиста(ФайлЛистаСтрока, ИмяЛистаИСсылкаРазд.Ссылка, ПримечаниеЯчейки);
			КонецЕсли;
			
		КонецЕсли;
		
		// Установим новые размеры листа.
		УзелРазмерыЛиста = Неопределено;
		Если ФайлЛистаСтрока.КэшЭлем.Свойство("dimension", УзелРазмерыЛиста) Тогда
			УзелАтрибута = УзелРазмерыЛиста.Док.ПолучитьУзелАтрибута("ref");
			Если УзелАтрибута <> Неопределено Тогда
				
				КоординатыЛиста  = КоординатыОбласти(УзелАтрибута.Значение);
				КоординатыЯчейки = КоординатыОбласти(ИмяЛистаИСсылкаРазд.Ссылка);
				
				Если КоординатыЯчейки.Строка1 > КоординатыЛиста.Строка2 ИЛИ КоординатыЯчейки.Столбец1 > КоординатыЛиста.Столбец2 Тогда
					НовоеЗначениеАтрибута
						= СимвольныйКодИзПорядковогоНомера(КоординатыЛиста.Столбец1) + Формат(КоординатыЛиста.Строка1, "ЧГ=") + ":"
						+ СимвольныйКодИзПорядковогоНомера(Макс(КоординатыЯчейки.Столбец1, КоординатыЛиста.Столбец2))
						+ Формат(Макс(КоординатыЯчейки.Строка1, КоординатыЛиста.Строка2), "ЧГ=");
					УзелАтрибута.Значение = НовоеЗначениеАтрибута;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьДокументDOMВФайлXML(ДокументDOM, ИмяФайлаXML, СвойстваФайлаXML, ТипФайла) Экспорт
	
	Кодировка = ДокументDOM.КодировкаИсточника;
	Если ПустаяСтрока(Кодировка) Тогда
		Кодировка = "UTF-8";
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.Отступ = Ложь;
	ЗаписьXML.УстановитьСтроку(Кодировка);
	
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьDOM.Записать(ДокументDOM, ЗаписьXML);
	
	ТекстовоеСодержимоеФайлаXML = ЗаписьXML.Закрыть();
	
	Если ТипЗнч(СвойстваФайлаXML) = Тип("Структура") Тогда
		РазрешенаЗаменаАтрибутов = Истина;
		
		НайденныеЭлементы = ДокументDOM.ПолучитьЭлементыПоИмени(СвойстваФайлаXML.ИмяЭлемента);
		Если НайденныеЭлементы.Количество() > 0 И НайденныеЭлементы[0].ЕстьАтрибуты() Тогда
			РазрешенаЗаменаАтрибутов = АтрибутыСовпадают(НайденныеЭлементы[0].Атрибуты, СвойстваФайлаXML.АтрибутыЭлемента);
		Иначе
			РазрешенаЗаменаАтрибутов = Ложь;
		КонецЕсли;
		
		ПозицияНачалаЭлемента = СтрНайти(ТекстовоеСодержимоеФайлаXML, "<" + СвойстваФайлаXML.ИмяЭлемента);
		Если ПозицияНачалаЭлемента > 0 Тогда
			ТекстXMLБезОбъявления = Сред(ТекстовоеСодержимоеФайлаXML, ПозицияНачалаЭлемента);
			Если РазрешенаЗаменаАтрибутов
			   И ЗначениеЗаполнено(СвойстваФайлаXML.АтрибутыЭлемента)
			   И ЗначениеЗаполнено(СвойстваФайлаXML.СтрАтрибутыЭлемента)
			   И (СвойстваФайлаXML.АтрибутыЗаменить ИЛИ ТипФайла = "drawing") Тогда
				НачалоЭлемента = "<" + СвойстваФайлаXML.ИмяЭлемента + СвойстваФайлаXML.СтрАтрибутыЭлемента;
				ТекстXMLБезОбъявления = НачалоЭлемента + Сред(ТекстXMLБезОбъявления, СтрДлина(НачалоЭлемента) + 1);
			КонецЕсли;
			
			Разделитель = ?(СвойстваФайлаXML.ЕстьРазделитель, Символы.ВК + Символы.ПС, "");
			ТекстовоеСодержимоеФайлаXML = СвойстваФайлаXML.ОбъявлениеXML + Разделитель + ТекстXMLБезОбъявления;
		КонецЕсли;
	КонецЕсли;
	
	ПотокФайлаXML = Новый ФайловыйПоток(ИмяФайлаXML, РежимОткрытияФайла.Создать);
	
	ЗаписьТекста = Новый ЗаписьТекста(ПотокФайлаXML, Кодировка, Символы.ВК + Символы.ПС, Символы.ПС);
	ЗаписьТекста.Записать(ТекстовоеСодержимоеФайлаXML);
	ЗаписьТекста.Закрыть();
	
	ПотокФайлаXML.Закрыть(); // записывается без BOM
	
	Файл = Новый Файл(ИмяФайлаXML);
	Файл.УстановитьВремяИзменения('19800101000000'); // установим минимальную дату изменения файла
	
КонецПроцедуры

Функция ДокументDOMИзФайлаXML(ИмяФайлаXML, СвойстваФайлаXML) Экспорт
	
	Перем ДокументDOM;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяФайлаXML);
	
	ПостроительDOM = Новый ПостроительDOM;
	ДокументDOM = ПостроительDOM.Прочитать(ЧтениеXML);
	
	ЧтениеXML.Закрыть();
	
	СвойстваФайлаXML = СвойстваИзФайлаXML(ИмяФайлаXML);
	
	Возврат ДокументDOM;
	
КонецФункции

Функция ИндексыОбщихСтрок(ДокументDOM) Экспорт
	
	УникальныеСтроки = Новый Соответствие;
	
	ИндексСтроки = 0;
	
	УзелВсеСтроки = ДокументDOM.ЭлементДокумента;
	Если УзелВсеСтроки.ЛокальноеИмя = "sst" Тогда
		
		УзелСтрока = УзелВсеСтроки.ПервыйДочерний;
		Пока УзелСтрока <> Неопределено Цикл
			Если УзелСтрока.ЛокальноеИмя = "si" Тогда
				УзелСодержимое = УзелСтрока.ПервыйДочерний;
				ЗначениеУзла = ?(УзелСодержимое = Неопределено, "", УзелСодержимое.ТекстовоеСодержимое);
				УникальныеСтроки.Вставить(ЗначениеУзла, Формат(ИндексСтроки, "ЧН=; ЧГ="));
				ИндексСтроки = ИндексСтроки + 1;
			КонецЕсли;
			
			УзелСтрока = УзелСтрока.СледующийСоседний;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат УникальныеСтроки;
	
КонецФункции

Функция СвойстваИзФайлаXML(ИмяФайлаXML)
	
	СвойстваИзФайла = Новый Структура;
	СвойстваИзФайла.Вставить("ОбъявлениеXML",       "");
	СвойстваИзФайла.Вставить("ИмяЭлемента",         "");
	СвойстваИзФайла.Вставить("СтрАтрибутыЭлемента", "");
	СвойстваИзФайла.Вставить("АтрибутыЭлемента", Новый СписокЗначений);
	СвойстваИзФайла.Вставить("АтрибутыЗаменить", Ложь);
	СвойстваИзФайла.Вставить("ЕстьРазделитель",  Ложь);
	
	КодировкаXML = "UTF-8";
	
	ПараметрыЧтенияXML = Новый ПараметрыЧтенияXML( , , , , Ложь, Истина, Истина, Истина, Ложь);
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяФайлаXML, ПараметрыЧтенияXML);
	ЧтениеXML.ИгнорироватьПробелы = Ложь;
	
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.ОбъявлениеXML Тогда
			Если НЕ ПустаяСтрока(ЧтениеXML.КодировкаXML) Тогда
				КодировкаXML = ЧтениеXML.КодировкаXML;
			КонецЕсли;
			СтрАвтономный = ?(ЧтениеXML.Автономный, " standalone=""yes""", ""); 
			СвойстваИзФайла.ОбъявлениеXML = "<?xml"
				+ " version=""" + ЧтениеXML.ВерсияXML + """"
				+ " encoding=""" + КодировкаXML + """"
				+ СтрАвтономный + "?>";
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			СвойстваИзФайла.ИмяЭлемента = ЧтениеXML.Имя;
			Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
				СвойстваИзФайла.АтрибутыЭлемента.Добавить(ЧтениеXML.Имя, ЧтениеXML.Значение);
			КонецЦикла;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	
	ЧтениеТекста = Новый ЧтениеТекста(ИмяФайлаXML, КодировкаXML, , , Ложь);
	
	СтрОбъявлениеXML = ЧтениеТекста.ПрочитатьСтроку("<" + СвойстваИзФайла.ИмяЭлемента);
	Если СтрОбъявлениеXML <> Неопределено Тогда
		Если СтрДлина(СтрОбъявлениеXML) > 5
		 И  Лев(СтрОбъявлениеXML, 5) = "<?xml"
		 И Прав(СтрОбъявлениеXML, 1) = Символы.ПС Тогда
			СвойстваИзФайла.ЕстьРазделитель = Истина;
			СтрОбъявлениеXML = СокрП(СтрОбъявлениеXML);
		КонецЕсли;
		СвойстваИзФайла.ОбъявлениеXML = СтрОбъявлениеXML;
		
		СтрАтрибутыЭлемента = ЧтениеТекста.ПрочитатьСтроку(">");
		Если СтрАтрибутыЭлемента <> Неопределено Тогда
			Если СтрДлина(СтрАтрибутыЭлемента) > 0
			 И Прав(СтрАтрибутыЭлемента, 1) = "/" Тогда
				СтрАтрибутыЭлемента = Лев(СтрАтрибутыЭлемента, СтрДлина(СтрАтрибутыЭлемента) - 1);
			КонецЕсли;
			СвойстваИзФайла.СтрАтрибутыЭлемента = СтрАтрибутыЭлемента;
		КонецЕсли;
	КонецЕсли;
	
	ЧтениеТекста.Закрыть();
	
	СтрАтрибутыЭлементаВПорядкеСчитывания = "";
	Для Каждого АтрибутЭлемента Из СвойстваИзФайла.АтрибутыЭлемента Цикл
		СтрАтрибутыЭлементаВПорядкеСчитывания = СтрАтрибутыЭлементаВПорядкеСчитывания
			+ " " + АтрибутЭлемента.Значение + "=""" + АтрибутЭлемента.Представление + """";
	КонецЦикла;
	Если СвойстваИзФайла.СтрАтрибутыЭлемента <> СтрАтрибутыЭлементаВПорядкеСчитывания Тогда
		СвойстваИзФайла.АтрибутыЗаменить = Истина;
	КонецЕсли;
	
	Возврат СвойстваИзФайла;
	
КонецФункции

Функция АтрибутыСовпадают(АтрибутыЭлементаDOM, АтрибутыЭлемента)
	
	АтрибутыЭлементаDOMСписок = Новый СписокЗначений;
	
	Для Каждого АтрибутЭлементаDOM Из АтрибутыЭлементаDOM Цикл
		АтрибутЭлемента = АтрибутыЭлемента.НайтиПоЗначению(АтрибутЭлементаDOM.Имя);
		Если АтрибутЭлемента = Неопределено
		 ИЛИ АтрибутЭлемента.Представление <> АтрибутЭлементаDOM.ТекстовоеСодержимое Тогда
			Возврат Ложь;
		КонецЕсли;
		АтрибутыЭлементаDOMСписок.Добавить(АтрибутЭлементаDOM.Имя, АтрибутЭлементаDOM.ТекстовоеСодержимое);
	КонецЦикла;
	
	Для Каждого АтрибутЭлемента Из АтрибутыЭлемента Цикл
		АтрибутЭлементаDOM = АтрибутыЭлементаDOMСписок.НайтиПоЗначению(АтрибутЭлемента.Значение);
		Если АтрибутЭлементаDOM = Неопределено
		 ИЛИ АтрибутЭлементаDOM.Представление <> АтрибутЭлемента.Представление Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#Область ЗаполнениеФайловШаблонаДанными

Функция СтрИндексОбщейСтроки(ДобавляемаяСтрока, ПараметрыВыгрузки)
	
	ДокументDOM = ПараметрыВыгрузки.ДопПараметрыШаблона.ДокументОбщиеСтроки;
	ИндОбщихСтрок = ПараметрыВыгрузки.ДопПараметрыШаблона.ИндексыОбщихСтрок;
	
	СтрИндОбщейСтроки = Неопределено;
	
	УзелВсеСтроки = ДокументDOM.ЭлементДокумента;
	Если УзелВсеСтроки.ЛокальноеИмя = "sst" Тогда
		
		СтрИндОбщейСтроки = ИндОбщихСтрок[ДобавляемаяСтрока];
		
		Если СтрИндОбщейСтроки = Неопределено Тогда
			
			УзелСтрока = УзелВсеСтроки.ПоследнийДочерний;
			Пока УзелСтрока <> Неопределено И УзелСтрока.ЛокальноеИмя <> "si" Цикл
				УзелСтрока = УзелСтрока.ПредыдущийСоседний;
			КонецЦикла;
			
			КоличествоУникальныхСтрокСтр = УзелВсеСтроки.ПолучитьАтрибут("uniqueCount");
			НовКоличествоУникальныхСтрок = Число(" " + КоличествоУникальныхСтрокСтр) + 1;
			НовКоличествоУникальныхСтрокСтр = Формат(НовКоличествоУникальныхСтрок, "ЧН=; ЧГ=");
			УзелВсеСтроки.УстановитьАтрибут("uniqueCount", НовКоличествоУникальныхСтрокСтр);
			
			ИндОбщихСтрок.Вставить(ДобавляемаяСтрока, КоличествоУникальныхСтрокСтр);
			
			СтрИндОбщейСтроки = КоличествоУникальныхСтрокСтр;
			
			НовУзелТекст = ДокументDOM.СоздатьЭлемент(УзелВсеСтроки.URIПространстваИмен, "t");
			НовУзелТекст.ТекстовоеСодержимое = ДобавляемаяСтрока;
			НовУзелСтрока = ДокументDOM.СоздатьЭлемент(УзелВсеСтроки.URIПространстваИмен, "si");
			НовУзелСтрока.ДобавитьДочерний(НовУзелТекст);
			Если УзелСтрока <> Неопределено И УзелСтрока.СледующийСоседний <> Неопределено Тогда
				УзелВсеСтроки.ВставитьПеред(НовУзелСтрока, УзелСтрока.СледующийСоседний);
			Иначе
				УзелВсеСтроки.ДобавитьДочерний(НовУзелСтрока);
			КонецЕсли;
			НовУзелТекст.ОтменитьСоответствиеПространстваИмен(УзелВсеСтроки.URIПространстваИмен);
			НовУзелСтрока.ОтменитьСоответствиеПространстваИмен(УзелВсеСтроки.URIПространстваИмен);
			
		КонецЕсли;
		
		НовКоличествоСтрок = Число(" " + УзелВсеСтроки.ПолучитьАтрибут("count")) + 1;
		УзелВсеСтроки.УстановитьАтрибут("count", Формат(НовКоличествоСтрок, "ЧН=; ЧГ="));
		
	КонецЕсли;
	
	Возврат СтрИндОбщейСтроки;
	
КонецФункции

Функция ИмяЛистаИСсылка(ПолнаяСсылка) Экспорт
	
	ЭлементыПолнойСсылки = СтрРазделить(ПолнаяСсылка, "!");
	
	СтруктураПолнойСсылки = Новый Структура("ИмяЛиста,Ссылка",
		СтрЗаменить(ЭлементыПолнойСсылки[0], "'", ""), ?(ЭлементыПолнойСсылки.ВГраница() > 0, ЭлементыПолнойСсылки[1], ""));
	
	Возврат СтруктураПолнойСсылки;
	
КонецФункции

Функция КоординатыОбласти(СсылкаНаОбласть) Экспорт
	
	Координаты = Новый Структура("Строка1,Столбец1,Строка2,Столбец2", 0, 0, 0, 0);
	
	ЧастиСтрСсылки = СтрРазделить(СтрЗаменить(СсылкаНаОбласть, "$", ""), ":");
	
	СтрЦифры = "0123456789";
	
	Для Ном = 1 По Мин(ЧастиСтрСсылки.Количество(), 2) Цикл
		
		СтрСтрока = "";
		СтрСтолбец = "";
		
		ЧастьСтрСсылки = СокрЛП(ЧастиСтрСсылки[Ном - 1]);
		Для НомСим = 1 По СтрДлина(ЧастьСтрСсылки) Цикл
			Сим = Сред(ЧастьСтрСсылки, НомСим, 1);
			Если СтрНайти(СтрЦифры, Сим) > 0 Тогда
				СтрСтрока = СтрСтрока + Сим;
			Иначе
				СтрСтолбец = СтрСтолбец + Сим;
			КонецЕсли;
		КонецЦикла;
		
		ИмяКлючаСтрока  = "Строка"  + СокрЛ(Ном);
		ИмяКлючаСтолбец = "Столбец" + СокрЛ(Ном);
		
		Координаты[ИмяКлючаСтрока]  = Число(" " + СтрСтрока);
		Координаты[ИмяКлючаСтолбец] = ПорядковыйНомерИзСимвольногоКода(СтрСтолбец);
		
	КонецЦикла;
	
	Возврат Координаты;
	
КонецФункции

Функция СимвольныйКодИзПорядковогоНомера(Знач ПорядковыйНомер) Экспорт
	
	НаборСимволов = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
	
	ДлинаНабора = СтрДлина(НаборСимволов);
	
	СимвольныйКод = "";
	
	Пока ПорядковыйНомер > 0 Цикл
		Остаток = ПорядковыйНомер % ДлинаНабора;
		Остаток = ?(Остаток > 0, Остаток, ДлинаНабора);
		ПорядковыйНомер = Окр((ПорядковыйНомер - Остаток) / ДлинаНабора);
		СимвольныйКод = Сред(НаборСимволов, Остаток, 1) + СимвольныйКод;
	КонецЦикла;
	
	Возврат СимвольныйКод;
	
КонецФункции

Функция ПорядковыйНомерИзСимвольногоКода(Знач СимвольныйКод)
	
	ПорядковыйНомер = 0;
	
	НаборСимволов = "ABCDEFGHIJKLMNOPQRSTUVWXYZ";
	
	ДлинаКода   = СтрДлина(СимвольныйКод);
	ДлинаНабора = СтрДлина(НаборСимволов);
	
	ВесРазряда = 1;
	Для Инд = 0 По ДлинаКода - 1 Цикл
		Сим = ВРег(Сред(СимвольныйКод, ДлинаКода - Инд, 1));
		ПорядковыйНомер = ПорядковыйНомер + ВесРазряда * СтрНайти(НаборСимволов, Сим);
		ВесРазряда = ВесРазряда * ДлинаНабора;
	КонецЦикла;
	
	Возврат ПорядковыйНомер;
	
КонецФункции

Функция НоваяТаблицаКэшированныхУзлов()
	
	ТаблицаКэшированныхУзлов = Новый ТаблицаЗначений;
	
	ТаблицаКэшированныхУзлов.Колонки.Добавить("Ном", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(7,0)));
	ТаблицаКэшированныхУзлов.Колонки.Добавить("Док");
	ТаблицаКэшированныхУзлов.Колонки.Добавить("Таб");
	
	ТаблицаКэшированныхУзлов.Индексы.Добавить("Ном");
	
	Возврат ТаблицаКэшированныхУзлов;
	
КонецФункции

Функция СимвольнаяИЧисловаяЧастиИД(СтрокаИД)
	
	СоставнойИД = Новый Структура("СимвольнаяЧасть,ЧисловаяЧасть", "", 0);
	
	СтрЦифры = "0123456789";
	
	СтрЧисловаяЧасть = "";
	СтрСимвольнаяЧасть = "";
	
	Для НомСим = 1 По СтрДлина(СтрокаИД) Цикл
		Сим = Сред(СтрокаИД, НомСим, 1);
		Если СтрНайти(СтрЦифры, Сим) > 0 Тогда
			СтрЧисловаяЧасть = СтрЧисловаяЧасть + Сим;
		Иначе
			СтрСимвольнаяЧасть = СтрСимвольнаяЧасть + Сим;
		КонецЕсли;
	КонецЦикла;
	
	СоставнойИД.СимвольнаяЧасть = СтрСимвольнаяЧасть;
	СоставнойИД.ЧисловаяЧасть = Число(" " + СтрЧисловаяЧасть);
	
	Возврат СоставнойИД;
	
КонецФункции

Функция УзелЯчейкиЛиста(ФайлЛистаСтрока, СсылкаСтрока)
	
	КоординатыЯчейки = КоординатыОбласти(СсылкаСтрока);
	
	ДанныеЛиста = ФайлЛистаСтрока.КэшЭлем.sheetData;
	
	НомСтроки = КоординатыЯчейки.Строка1;
	СтрокаЛиста = ДанныеЛиста.Таб.Найти(НомСтроки, "Ном");
	Если СтрокаЛиста = Неопределено Тогда // добавим новый узел строки листа
		ПредыдущаяСтрокаЛиста = Неопределено;
		СледующаяСтрокаЛиста  = Неопределено;
		Если ДанныеЛиста.Таб.Количество() > 0 Тогда
			ПоследняяСтрокаЛиста = ДанныеЛиста.Таб[ДанныеЛиста.Таб.Количество() - 1];
			Если НомСтроки > ПоследняяСтрокаЛиста.Ном Тогда
				ПредыдущаяСтрокаЛиста = ПоследняяСтрокаЛиста;
				СледующаяСтрокаЛиста  = ПоследняяСтрокаЛиста;
			Иначе
				Для Каждого СледующаяСтрокаЛиста Из ДанныеЛиста.Таб Цикл
					Если НомСтроки < СледующаяСтрокаЛиста.Ном Тогда
						Прервать;
					КонецЕсли;
					ПредыдущаяСтрокаЛиста = СледующаяСтрокаЛиста;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		НовУзелСтрока = ДанныеЛиста.Док.ДокументВладелец.СоздатьЭлемент(ДанныеЛиста.Док.URIПространстваИмен, "row");
		Если ПредыдущаяСтрокаЛиста = СледующаяСтрокаЛиста Тогда
			СтрокаЛиста = ДанныеЛиста.Таб.Добавить();
			СтрокаЛиста.Док = ДанныеЛиста.Док.ДобавитьДочерний(НовУзелСтрока);
		Иначе
			СтрокаЛиста = ДанныеЛиста.Таб.Вставить(ДанныеЛиста.Таб.Индекс(СледующаяСтрокаЛиста));
			СтрокаЛиста.Док = ДанныеЛиста.Док.ВставитьПеред(НовУзелСтрока, СледующаяСтрокаЛиста.Док);
		КонецЕсли;
		Если СледующаяСтрокаЛиста <> Неопределено Тогда
			Для Каждого УзелАтрибут Из СледующаяСтрокаЛиста.Док.Атрибуты Цикл
				Если СтрНайти("'r','spans','x14ac:dyDescent'", "'" + УзелАтрибут.Имя + "'") = 0 Тогда
					Продолжить;
				КонецЕсли;
				СтрокаЛиста.Док.УстановитьАтрибут(УзелАтрибут.URIПространстваИмен, УзелАтрибут.Имя, УзелАтрибут.Значение);
			КонецЦикла;
		КонецЕсли;
		СтрокаЛиста.Док.ОтменитьСоответствиеПространстваИмен(ДанныеЛиста.Док.URIПространстваИмен);
		
		СтрокаЛиста.Ном = НомСтроки;
		СтрокаЛиста.Таб = НоваяТаблицаКэшированныхУзлов();
		СтрокаЛиста.Док.УстановитьАтрибут("r", Формат(НомСтроки, "ЧГ="));
		УзелФорматЛиста = Неопределено;
		Если ФайлЛистаСтрока.КэшЭлем.Свойство("sheetFormatPr", УзелФорматЛиста) Тогда
			URIПоПрефиксу = УзелФорматЛиста.Док.НайтиURIПространстваИмен("x14ac");
			УзелАтрибута = УзелФорматЛиста.Док.ПолучитьУзелАтрибута(URIПоПрефиксу, "dyDescent");
			Если УзелАтрибута <> Неопределено Тогда
				СтрокаЛиста.Док.УстановитьАтрибут(УзелАтрибута.URIПространстваИмен, УзелАтрибута.Имя, УзелАтрибута.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	НомСтолбца = КоординатыЯчейки.Столбец1;
	ЯчейкаЛиста = СтрокаЛиста.Таб.Найти(НомСтолбца, "Ном");
	Если ЯчейкаЛиста = Неопределено Тогда // добавим новый узел ячейки листа
		ПредыдущаяЯчейкаЛиста = Неопределено;
		СледующаяЯчейкаЛиста  = Неопределено;
		Если СтрокаЛиста.Таб.Количество() > 0 Тогда
			ПоследняяЯчейкаСтроки = СтрокаЛиста.Таб[СтрокаЛиста.Таб.Количество() - 1];
			Если НомСтолбца > ПоследняяЯчейкаСтроки.Ном Тогда
				ПредыдущаяЯчейкаЛиста = ПоследняяЯчейкаСтроки;
				СледующаяЯчейкаЛиста  = ПоследняяЯчейкаСтроки;
			Иначе
				Для Каждого СледующаяЯчейкаЛиста Из СтрокаЛиста.Таб Цикл
					Если НомСтолбца < СледующаяЯчейкаЛиста.Ном Тогда
						Прервать;
					КонецЕсли;
					ПредыдущаяЯчейкаЛиста = СледующаяЯчейкаЛиста;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		НовУзелСтолбец = СтрокаЛиста.Док.ДокументВладелец.СоздатьЭлемент(СтрокаЛиста.Док.URIПространстваИмен, "c");
		Если ПредыдущаяЯчейкаЛиста = СледующаяЯчейкаЛиста Тогда
			ЯчейкаЛиста = СтрокаЛиста.Таб.Добавить();
			ЯчейкаЛиста.Док = СтрокаЛиста.Док.ДобавитьДочерний(НовУзелСтолбец);
		Иначе
			ЯчейкаЛиста = СтрокаЛиста.Таб.Вставить(СтрокаЛиста.Таб.Индекс(СледующаяЯчейкаЛиста));
			ЯчейкаЛиста.Док = СтрокаЛиста.Док.ВставитьПеред(НовУзелСтолбец, СледующаяЯчейкаЛиста.Док);
		КонецЕсли;
		Если СледующаяЯчейкаЛиста <> Неопределено Тогда
			Для Каждого УзелАтрибут Из СледующаяЯчейкаЛиста.Док.Атрибуты Цикл
				Если СтрНайти("'r','s'", "'" + УзелАтрибут.Имя + "'") = 0 Тогда
					Продолжить;
				КонецЕсли;
				ЯчейкаЛиста.Док.УстановитьАтрибут(УзелАтрибут.URIПространстваИмен, УзелАтрибут.Имя, УзелАтрибут.Значение);
			КонецЦикла;
		КонецЕсли;
		ЯчейкаЛиста.Док.ОтменитьСоответствиеПространстваИмен(СтрокаЛиста.Док.URIПространстваИмен);
		
		ЯчейкаЛиста.Ном = НомСтолбца;
		ЯчейкаЛиста.Док.УстановитьАтрибут("r", СимвольныйКодИзПорядковогоНомера(НомСтолбца) + Формат(НомСтроки, "ЧГ="));
		ФорматСтолбцов = Неопределено;
		Если ФайлЛистаСтрока.КэшЭлем.Свойство("cols", ФорматСтолбцов) Тогда
			ЗначениеАтрибутаСтиль = Неопределено;
			
			НайденныйСтолбец = ФорматСтолбцов.Таб.Найти(НомСтолбца, "Ном");
			Если НайденныйСтолбец <> Неопределено Тогда
				ЗначениеАтрибутаСтиль = НайденныйСтолбец.Док.ПолучитьАтрибут("style");
			Иначе
				Для Каждого НайденныйСтолбец Из ФорматСтолбцов.Таб Цикл
					Если НомСтолбца >= НайденныйСтолбец.Ном
					   И НомСтолбца <= Число(" " + НайденныйСтолбец.Док.ПолучитьАтрибут("max")) Тогда
						ЗначениеАтрибутаСтиль = НайденныйСтолбец.Док.ПолучитьАтрибут("style");
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если ЗначениеАтрибутаСтиль <> Неопределено  Тогда
				ЯчейкаЛиста.Док.УстановитьАтрибут("s", ЗначениеАтрибутаСтиль);
			КонецЕсли;
		КонецЕсли;
		
		ЧастиДиапазона = СтрРазделить(Строка(СтрокаЛиста.Док.ПолучитьАтрибут("spans")), ":");
		Если ЧастиДиапазона.Количество() = 2 Тогда
			НомНач = Число(" " + ЧастиДиапазона[0]);
			НомКон = Число(" " + ЧастиДиапазона[1]);
			ОбновитьДиапазон = Ложь;
			Если НомСтолбца < НомНач Тогда
				ЧастиДиапазона[0] = Формат(НомСтолбца, "ЧГ=");
				ОбновитьДиапазон = Истина;
			КонецЕсли;
			Если НомСтолбца > НомКон Тогда
				ЧастиДиапазона[1] = Формат(НомСтолбца, "ЧГ=");
				ОбновитьДиапазон = Истина;
			КонецЕсли;
			Если ОбновитьДиапазон Тогда
				СтрокаЛиста.Док.УстановитьАтрибут("spans", СтрСоединить(ЧастиДиапазона, ":"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЯчейкаЛиста.Док;
	
КонецФункции

Функция ФорматированноеЗначение(Знач Значение)
	
	Если ТипЗнч(Значение) = Тип("Число") Тогда
		Значение = Формат(Значение, "ЧРД=.; ЧН=; ЧГ=");
	ИначеЕсли ТипЗнч(Значение) = Тип("Дата") Тогда
		Значение = Формат(Значение, "ДФ=dd.MM.yyyy");
	Иначе
		Значение = Строка(Значение);
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

Функция ПримечанияЛистаДокументDOM()
	
	URIПространстваИмен = "http://schemas.openxmlformats.org/spreadsheetml/2006/main";
	
	ДокументПримечанияЛиста = Новый ДокументDOM(URIПространстваИмен, "comments");
	ДокументПримечанияЛиста.ВерсияXML = "1.0";
	ДокументПримечанияЛиста.Автономный = Истина;
	
	КорневойЭлементДокумента = ДокументПримечанияЛиста.ЭлементДокумента;
	
	УзелАвторы = КорневойЭлементДокумента.ДобавитьДочерний(
		ДокументПримечанияЛиста.СоздатьЭлемент(URIПространстваИмен, "authors"));
	УзелАвтор = УзелАвторы.ДобавитьДочерний(
		ДокументПримечанияЛиста.СоздатьЭлемент(URIПространстваИмен, "author"));
	УзелАвтор.ТекстовоеСодержимое = СокрЛП(ПолноеИмяПользователя());
	УзелАвторы.ОтменитьСоответствиеПространстваИмен(URIПространстваИмен);
	
	УзелКомментарии = КорневойЭлементДокумента.ДобавитьДочерний(
		ДокументПримечанияЛиста.СоздатьЭлемент(URIПространстваИмен, "commentList"));
	УзелКомментарии.ОтменитьСоответствиеПространстваИмен(URIПространстваИмен);
	
	Возврат ДокументПримечанияЛиста;
	
КонецФункции

Процедура ДобавитьПримечаниеВКомпонентПримечаний(ФайлПримечанийЛистаСтрока, СсылкаСтрока, ПримечаниеСтрока)
	
	УзелСписокПримечаний = ФайлПримечанийЛистаСтрока.Документ.ПолучитьЭлементыПоИмени("commentList")[0];
	
	УзелПримечание = УзелСписокПримечаний.ДобавитьДочерний(УзелСписокПримечаний.ДокументВладелец.СоздатьЭлемент(
		УзелСписокПримечаний.URIПространстваИмен, "comment"));
	УзелПримечание.УстановитьАтрибут(УзелСписокПримечаний.URIПространстваИмен,
		"ref", СтрЗаменить(СсылкаСтрока, "$", ""));
	УзелПримечание.УстановитьАтрибут(УзелСписокПримечаний.URIПространстваИмен,
		"authorId", "0");
	
	УзелПримечаниеТекст = УзелПримечание.ДобавитьДочерний(УзелСписокПримечаний.ДокументВладелец.СоздатьЭлемент(
		УзелСписокПримечаний.URIПространстваИмен, "text"));
	УзелПримечаниеФрмТекст = УзелПримечаниеТекст.ДобавитьДочерний(УзелСписокПримечаний.ДокументВладелец.СоздатьЭлемент(
		УзелСписокПримечаний.URIПространстваИмен, "r"));
	
	//УзелПримечаниеСвойстваТекста = УзелПримечаниеФрмТекст.ДобавитьДочерний(
	//	УзелСписокПримечаний.ДокументВладелец.СоздатьЭлемент(УзелСписокПримечаний.URIПространстваИмен, "rPr"));
	УзелПримечаниеТекстСодержимое = УзелПримечаниеФрмТекст.ДобавитьДочерний(
		УзелСписокПримечаний.ДокументВладелец.СоздатьЭлемент(УзелСписокПримечаний.URIПространстваИмен, "t"));
	
	УзелПримечаниеТекстСодержимое.ТекстовоеСодержимое = СокрЛП(ПримечаниеСтрока);
	
	УзелПримечание.ОтменитьСоответствиеПространстваИмен(УзелСписокПримечаний.URIПространстваИмен);
	
КонецПроцедуры

Процедура УстановитьПримечаниеВЯчейкеЛиста(ФайлЛистаСтрока, СсылкаСтрока, ПримечаниеЯчейки)
	
	ФайлыСвязиЛистаСтроки = ФайлЛистаСтрока.Владелец().НайтиСтроки(
		Новый Структура("ТипФайла,ИмяФайла", "Relationships", ФайлЛистаСтрока.ИмяФайла + ".rels"));
	
	Если НЕ ЗначениеЗаполнено(ФайлыСвязиЛистаСтроки) Тогда
		Возврат;
	КонецЕсли;
	
	ФайлСвязиЛистаСтрока = ФайлыСвязиЛистаСтроки[0];
	
	КорневойСвязиИзДокумента = ФайлСвязиЛистаСтрока.Документ.ЭлементДокумента;
	СвязиИзДокумента = КорневойСвязиИзДокумента.ПолучитьЭлементыПоИмени("Relationship");
	
	МаксИндексИД = 0;
	СимвольнаяЧастьИД = "rId";
	
	ФайлПримечанийЛистаСтрока = Неопределено;
	
	ТипПримечаниеXML = "http://schemas.openxmlformats.org/officeDocument/2006/relationships/comments";
	
	Для Каждого СвязьИзДокумента Из СвязиИзДокумента Цикл
		
		ИД = СвязьИзДокумента.ПолучитьАтрибут("Id");
		ТипФайлаXML = СвязьИзДокумента.ПолучитьАтрибут("Type");
		ОтносительныйПутьКФайлу = СвязьИзДокумента.ПолучитьАтрибут("Target");
		
		СоставнойИД = СимвольнаяИЧисловаяЧастиИД(ИД);
		МаксИндексИД = Макс(МаксИндексИД, СоставнойИД.ЧисловаяЧасть);
		СимвольнаяЧастьИД = СоставнойИД.СимвольнаяЧасть;
		
		Если ВРег(ТипФайлаXML) = ВРег(ТипПримечаниеXML) Тогда
			Файл = Новый Файл(ФайлСвязиЛистаСтрока.Каталог + ".." + ПолучитьРазделительПути()
				+ СокрЛП(СтрЗаменить(ОтносительныйПутьКФайлу, "/", ПолучитьРазделительПути())));
			ФайлыПоСсылкеСтроки = ФайлСвязиЛистаСтрока.Владелец().НайтиСтроки(Новый Структура("Каталог,ИмяФайла", Файл.Путь, Файл.Имя));
			Если ФайлыПоСсылкеСтроки.Количество() > 0 Тогда
				 ФайлПримечанийЛистаСтрока = ФайлыПоСсылкеСтроки[0];
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ФайлПримечанийЛистаСтрока = Неопределено Тогда
		
		ФайлыТиповСтроки = ФайлСвязиЛистаСтрока.Владелец().НайтиСтроки(Новый Структура("ТипФайла", "Types"));
		
		ЭлементТипов = ФайлыТиповСтроки[0].Документ.ЭлементДокумента;
		
		ИмяЭлементаПереопределение = "Override";
		УзлыТиповПереопределение = ЭлементТипов.ПолучитьЭлементыПоИмени(ИмяЭлементаПереопределение);
		
		ТипРабочаяКнигаXML = "application/vnd.ms-excel.sheet.macroEnabled.main+xml";
		ТипыПримечанийXML = "application/vnd.openxmlformats-officedocument.spreadsheetml.comments+xml";
		
		ИндексФайлаПримечаний = 0;
		ИмяФайлаПримечанийБР = "comments";
		ОтносительныйКаталогФайлаПримечаний = "/xl/";
		Для Каждого УзелТипаКонтекста Из УзлыТиповПереопределение Цикл
			ТипКонтекстаXML = УзелТипаКонтекста.ПолучитьАтрибут("ContentType");
			ОтносительныйПутьКФайлуКонтекста = УзелТипаКонтекста.ПолучитьАтрибут("PartName");
			Если ВРег(ТипКонтекстаXML) = ВРег(ТипыПримечанийXML) Тогда
				ЧастиПутиКФайлуПримечаний = СтрРазделить(ОтносительныйПутьКФайлуКонтекста, "/");
				ИмяФайлаПримечаний = ЧастиПутиКФайлуПримечаний[ЧастиПутиКФайлуПримечаний.ВГраница()];
				
				ЧастиИмениФайлаПримечаний = СимвольнаяИЧисловаяЧастиИД(СтрЗаменить(ИмяФайлаПримечаний, ".xml", ""));
				ИндексФайлаПримечаний = Макс(ИндексФайлаПримечаний, ЧастиИмениФайлаПримечаний.ЧисловаяЧасть);
				ИмяФайлаПримечанийБР = ЧастиИмениФайлаПримечаний.СимвольнаяЧасть;
				ОтносительныйКаталогФайлаПримечаний = СтрЗаменить(ОтносительныйПутьКФайлуКонтекста, ИмяФайлаПримечаний, "");
			ИначеЕсли ВРег(ТипКонтекстаXML)= ВРег(ТипРабочаяКнигаXML) Тогда
				ОтносительныйКаталогФайлаПримечаний = СтрЗаменить(ОтносительныйПутьКФайлуКонтекста, "workbook.xml", "");
			КонецЕсли;
		КонецЦикла;
		
		НовоеИмяФайлаПримечаний = ИмяФайлаПримечанийБР + Формат(ИндексФайлаПримечаний + 1, "ЧГ=") + ".xml";
		ОтносительныйПутьКФайлуПримечаний = ОтносительныйКаталогФайлаПримечаний + НовоеИмяФайлаПримечаний;
		
		// Добавление нового типа в словарь.
		НовыйУзелТипаПереопределение = ЭлементТипов.ДобавитьДочерний(
			ЭлементТипов.ДокументВладелец.СоздатьЭлемент(ЭлементТипов.URIПространстваИмен, ИмяЭлементаПереопределение));
		НовыйУзелТипаПереопределение.УстановитьАтрибут(ЭлементТипов.URIПространстваИмен,
			"ContentType", ТипыПримечанийXML);
		НовыйУзелТипаПереопределение.УстановитьАтрибут(ЭлементТипов.URIПространстваИмен,
			"PartName", ОтносительныйПутьКФайлуПримечаний);
		НовыйУзелТипаПереопределение.ОтменитьСоответствиеПространстваИмен(ЭлементТипов.URIПространстваИмен);
		
		ФайлыТиповСтроки[0].Статус = 1;
		
		// Добавление связи в компонент связей.
		ИДКомпонентаПримечаний = СимвольнаяЧастьИД + Формат(МаксИндексИД + 1, "ЧГ=");
		НовыйУзелСвязи = КорневойСвязиИзДокумента.ДобавитьДочерний(КорневойСвязиИзДокумента.ДокументВладелец.СоздатьЭлемент(
			КорневойСвязиИзДокумента.URIПространстваИмен, "Relationship"));
		НовыйУзелСвязи.УстановитьАтрибут(КорневойСвязиИзДокумента.URIПространстваИмен,
			"Id", ИДКомпонентаПримечаний);
		НовыйУзелСвязи.УстановитьАтрибут(КорневойСвязиИзДокумента.URIПространстваИмен,
			"Target", "../" + НовоеИмяФайлаПримечаний);
		НовыйУзелСвязи.УстановитьАтрибут(КорневойСвязиИзДокумента.URIПространстваИмен,
			"Type", ТипПримечаниеXML);
		НовыйУзелСвязи.ОтменитьСоответствиеПространстваИмен(КорневойСвязиИзДокумента.URIПространстваИмен);
		
		ФайлСвязиЛистаСтрока.Статус = 1;
		
		// Добавление записи в таблицу файлов для последующего создания файла примечаний.
		Файл = Новый Файл(ФайлСвязиЛистаСтрока.Каталог + ".." + ПолучитьРазделительПути()
			+  ".." + ПолучитьРазделительПути() + НовоеИмяФайлаПримечаний);
			
		ФайлПримечанийЛистаСтрока = ФайлСвязиЛистаСтрока.Владелец().Добавить();
		
		ФайлПримечанийЛистаСтрока.ИД          = ИДКомпонентаПримечаний;
		ФайлПримечанийЛистаСтрока.Каталог     = Файл.Путь;
		ФайлПримечанийЛистаСтрока.ИмяФайла    = Файл.Имя;
		ФайлПримечанийЛистаСтрока.ТипФайла    = "comments";
		ФайлПримечанийЛистаСтрока.ТипФайлаXML = ТипПримечаниеXML;
		ФайлПримечанийЛистаСтрока.СвязьСтрока = ФайлСвязиЛистаСтрока;
		
		ФайлПримечанийЛистаСтрока.Документ    = ПримечанияЛистаДокументDOM();
		
	КонецЕсли;
	
	ДобавитьПримечаниеВКомпонентПримечаний(ФайлПримечанийЛистаСтрока, СсылкаСтрока, ПримечаниеЯчейки);
	
	ФайлПримечанийЛистаСтрока.Статус = 1;
	
КонецПроцедуры

Процедура ЗаполнитьКэшЛиста(ФайлЛистаСтрока)
	
	ТекущийКорневойУзел = ФайлЛистаСтрока.Документ.ПервыйДочерний;
	
	Пока ТекущийКорневойУзел <> Неопределено Цикл
		Если ТекущийКорневойУзел.ЛокальноеИмя = "worksheet" Тогда
			УзелДанныеЛиста = ТекущийКорневойУзел.ПервыйДочерний;
			
			Пока УзелДанныеЛиста <> Неопределено Цикл
				
				Если УзелДанныеЛиста.ЛокальноеИмя = "dimension" Тогда
					
					ДанныеУзла = Новый Структура("Док", УзелДанныеЛиста);
					ФайлЛистаСтрока.КэшЭлем.Вставить(УзелДанныеЛиста.ЛокальноеИмя, ДанныеУзла);
					
				ИначеЕсли УзелДанныеЛиста.ЛокальноеИмя = "sheetFormatPr" Тогда
					
					ДанныеУзла = Новый Структура("Док", УзелДанныеЛиста);
					ФайлЛистаСтрока.КэшЭлем.Вставить(УзелДанныеЛиста.ЛокальноеИмя, ДанныеУзла);
					
				ИначеЕсли УзелДанныеЛиста.ЛокальноеИмя = "cols" Тогда
					
					ДанныеУзла = Новый Структура("Док,Таб", УзелДанныеЛиста, НоваяТаблицаКэшированныхУзлов());
					ФайлЛистаСтрока.КэшЭлем.Вставить(УзелДанныеЛиста.ЛокальноеИмя, ДанныеУзла);
					
					НомУзла = 1;
					УзелСтолбец = УзелДанныеЛиста.ПервыйДочерний;
					Пока УзелСтолбец <> Неопределено Цикл
						Если УзелСтолбец.ЛокальноеИмя = "col" Тогда
							НовСтрока = ДанныеУзла.Таб.Добавить();
							НовСтрока.Док = УзелСтолбец;
							НомСтроки = Число(" " + УзелСтолбец.ПолучитьАтрибут("min"));
							Если НомСтроки > 0 Тогда
								НомУзла = НомСтроки;
							КонецЕсли;
							НовСтрока.Ном = НомУзла;
							НомУзла = НомУзла + 1;
						КонецЕсли;
						УзелСтолбец = УзелСтолбец.СледующийСоседний;
					КонецЦикла;
					
				ИначеЕсли УзелДанныеЛиста.ЛокальноеИмя = "sheetData" Тогда
					
					ДанныеУзла = Новый Структура("Док,Таб", УзелДанныеЛиста, НоваяТаблицаКэшированныхУзлов());
					ФайлЛистаСтрока.КэшЭлем.Вставить(УзелДанныеЛиста.ЛокальноеИмя, ДанныеУзла);
					
					НомУзла = 1;
					УзелСтрока = УзелДанныеЛиста.ПервыйДочерний;
					Пока УзелСтрока <> Неопределено Цикл
						Если УзелСтрока.ЛокальноеИмя = "row" Тогда
							НовСтрока = ДанныеУзла.Таб.Добавить();
							НовСтрока.Док = УзелСтрока;
							НовСтрока.Таб = НоваяТаблицаКэшированныхУзлов();
							НомСтроки = Число(" " + УзелСтрока.ПолучитьАтрибут("r"));
							Если НомСтроки > 0 Тогда
								НомУзла = НомСтроки;
							КонецЕсли;
							НовСтрока.Ном = НомУзла;
							
							НомУзлаСтолбец = 1;
							УзелСтолбец = УзелСтрока.ПервыйДочерний;
							Пока УзелСтолбец <> Неопределено Цикл
								Если УзелСтолбец.ЛокальноеИмя = "c" Тогда
									НовСтолбец = НовСтрока.Таб.Добавить();
									НовСтолбец.Док = УзелСтолбец;
									НомСтолбца = КоординатыОбласти(УзелСтолбец.ПолучитьАтрибут("r")).Столбец1;
									Если НомСтолбца > 0 Тогда
										НомУзлаСтолбец = НомСтолбца;
									КонецЕсли;
									НовСтолбец.Ном = НомУзлаСтолбец;
									НомУзлаСтолбец = НомУзлаСтолбец + 1;
								КонецЕсли;
								УзелСтолбец = УзелСтолбец.СледующийСоседний;
							КонецЦикла;
							
							НомУзла = НомУзла + 1;
						КонецЕсли;
						УзелСтрока = УзелСтрока.СледующийСоседний;
					КонецЦикла;
					
				ИначеЕсли УзелДанныеЛиста.ЛокальноеИмя = "sheetProtection" Тогда
					
					ДанныеУзла = Новый Структура("Док", УзелДанныеЛиста);
					ФайлЛистаСтрока.КэшЭлем.Вставить(УзелДанныеЛиста.ЛокальноеИмя, ДанныеУзла);
					
				ИначеЕсли УзелДанныеЛиста.ЛокальноеИмя = "dataValidations" Тогда
					
					ДанныеУзла = Новый Структура("Док,Таб", УзелДанныеЛиста, НоваяТаблицаКэшированныхУзлов());
					ФайлЛистаСтрока.КэшЭлем.Вставить(УзелДанныеЛиста.ЛокальноеИмя, ДанныеУзла);
					
					НомУзла = 1;
					УзелКонтроль = УзелДанныеЛиста.ПервыйДочерний;
					Пока УзелКонтроль <> Неопределено Цикл
						Если УзелКонтроль.ЛокальноеИмя = "dataValidation" Тогда
							НовСтрока = ДанныеУзла.Таб.Добавить();
							НовСтрока.Док = УзелКонтроль;
							НовСтрока.Ном = НомУзла;
							НомУзла = НомУзла + 1;
						КонецЕсли;
						УзелКонтроль = УзелКонтроль.СледующийСоседний;
					КонецЦикла;
					
				ИначеЕсли УзелДанныеЛиста.ЛокальноеИмя = "extLst" Тогда
					
					URIПоПрефиксу = Неопределено;
					УзелДочерний = УзелДанныеЛиста.ПервыйДочерний;
					Если УзелДочерний <> Неопределено Тогда
						URIПоПрефиксу = УзелДочерний.НайтиURIПространстваИмен("x14");
					КонецЕсли;
					Если URIПоПрефиксу <> Неопределено Тогда
						УзлыКонтроль = УзелДанныеЛиста.ПолучитьЭлементыПоИмени(URIПоПрефиксу, "dataValidations");
						Если УзлыКонтроль.Количество() > 0 Тогда
							ДанныеУзла = Новый Структура("Док,Таб", УзлыКонтроль[0], НоваяТаблицаКэшированныхУзлов());
							ФайлЛистаСтрока.КэшЭлем.Вставить("x14_dataValidations", ДанныеУзла);
							
							НомУзла = 1;
							УзелКонтроль = УзлыКонтроль[0].ПервыйДочерний;
							Пока УзелКонтроль <> Неопределено Цикл
								Если УзелКонтроль.ИмяЭлемента = "x14:dataValidation" Тогда
									НовСтрока = ДанныеУзла.Таб.Добавить();
									НовСтрока.Док = УзелКонтроль;
									НовСтрока.Ном = НомУзла;
									НомУзла = НомУзла + 1;
								КонецЕсли;
								УзелКонтроль = УзелКонтроль.СледующийСоседний;
							КонецЦикла;
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
				
				УзелДанныеЛиста = УзелДанныеЛиста.СледующийСоседний;
			КонецЦикла;
			
			Прервать;
		КонецЕсли;
		
		ТекущийКорневойУзел = ТекущийКорневойУзел.СледующийСоседний;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
