////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Корректировка стоимости и амортизации НМА".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьВспомогательныеРеквизиты(Форма, ВспомогательныеРеквизиты) Экспорт

	//++ Локализация
	
	ВспомогательныеРеквизиты.Вставить("ПлательщикНалогаНаПрибыль", Форма.СлужебныеПараметрыФормы.ПлательщикНалогаНаПрибыль);
	ВспомогательныеРеквизиты.Вставить("ВедетсяУчетПостоянныхИВременныхРазниц", Форма.СлужебныеПараметрыФормы.ВедетсяУчетПостоянныхИВременныхРазниц);
	
	//-- Локализация
	
КонецПроцедуры

Процедура ДополнитьЗначенияСвойствЗависимыхРеквизитов(Объект, ВспомогательныеРеквизиты, СтруктураИзмененныхРеквизитов, ПараметрыРеквизитовОбъекта) Экспорт

	//++ Локализация
	
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	ЭтоНМА = (Объект.ВидОбъектовУчета = ПредопределенноеЗначение("Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив"));
	
	ЭтоКорректировкаНаНачалоГода = (НачалоДня(Объект.Дата) = НачалоГода(Объект.Дата));
		
	#Область СтраницаНМА
		
	#Область НУ
	
	Если СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВУпрУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектовУчета")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("КорректировкаСтоимости")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("КорректировкаПервоначальнойСтоимости")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("КорректировкаАмортизации")
		ИЛИ ОбновитьВсе Тогда

		ЗначениеСвойства = 
			Объект.ОтражатьВРеглУчете
			И Объект.КорректировкаСтоимости
			И ВспомогательныеРеквизиты.ПлательщикНалогаНаПрибыль;
					
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НМА.СтоимостьНУ", 
			"НМАСтоимостьНУ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);

		//
		ЗначениеСвойства = 
			Объект.ОтражатьВРеглУчете
			И Объект.КорректировкаПервоначальнойСтоимости
			И ВспомогательныеРеквизиты.ПлательщикНалогаНаПрибыль;
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НМА.ПервоначальнаяСтоимостьНУ", 
			"НМАПервоначальнаяСтоимостьНУ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		//			
		ЗначениеСвойства = 
			Объект.ОтражатьВРеглУчете
			И Объект.КорректировкаАмортизации
			И ЭтоНМА
			И ВспомогательныеРеквизиты.ПлательщикНалогаНаПрибыль;
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НМА.АмортизацияНУ", 
			"НМААмортизацияНУ", 
			"Видимость", 
			ЗначениеСвойства И (НЕ Объект.РаздельнаяКорректировкаТекущегоИПрошлогоПериодов ИЛИ НЕ ЭтоКорректировкаНаНачалоГода), 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НМА.АмортизацияНаНачалоГодаНУ", 
			"НМААмортизацияНаНачалоГодаНУ", 
			"Видимость", 
			ЗначениеСвойства И Объект.РаздельнаяКорректировкаТекущегоИПрошлогоПериодов, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ПР
	Если СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВУпрУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Организация")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектовУчета")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("КорректировкаСтоимости")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("КорректировкаПервоначальнойСтоимости")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("КорректировкаАмортизации")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("РаздельнаяКорректировкаТекущегоИПрошлогоПериодов")
		ИЛИ ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			Объект.ОтражатьВРеглУчете 
			И Объект.КорректировкаСтоимости
			И ВспомогательныеРеквизиты.ПлательщикНалогаНаПрибыль
			И ВспомогательныеРеквизиты.ВедетсяУчетПостоянныхИВременныхРазниц;
							
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НМА.СтоимостьПР", 
			"НМАСтоимостьПР", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		//
		ЗначениеСвойства = 
			Объект.ОтражатьВРеглУчете
			И Объект.КорректировкаАмортизации
			И ЭтоНМА 
			И ВспомогательныеРеквизиты.ПлательщикНалогаНаПрибыль
			И ВспомогательныеРеквизиты.ВедетсяУчетПостоянныхИВременныхРазниц;
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НМА.АмортизацияПР", 
			"НМААмортизацияПР", 
			"Видимость", 
			ЗначениеСвойства И (НЕ Объект.РаздельнаяКорректировкаТекущегоИПрошлогоПериодов ИЛИ НЕ ЭтоКорректировкаНаНачалоГода), 
			ПараметрыРеквизитовОбъекта);
		
		//
		ЗначениеСвойства = 
			Объект.ОтражатьВРеглУчете
			И Объект.КорректировкаАмортизации
			И Объект.РаздельнаяКорректировкаТекущегоИПрошлогоПериодов
			И ЭтоНМА 
			И ВспомогательныеРеквизиты.ПлательщикНалогаНаПрибыль
			И ВспомогательныеРеквизиты.ВедетсяУчетПостоянныхИВременныхРазниц;
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НМА.АмортизацияНаНачалоГодаПР", 
			"НМААмортизацияНаНачалоГодаПР", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ЦФ
	
	Если СтруктураИзмененныхРеквизитов.Свойство("Дата")
		ИЛИ  СтруктураИзмененныхРеквизитов.Свойство("ОтражатьВРеглУчете")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ВидОбъектовУчета")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПрименениеЦелевогоФинансирования")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("КорректировкаСтоимости")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("КорректировкаАмортизации")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("РаздельнаяКорректировкаТекущегоИПрошлогоПериодов")
		ИЛИ ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			Объект.ОтражатьВРеглУчете
			И Объект.ПрименениеЦелевогоФинансирования
			И Объект.КорректировкаСтоимости
			И ВспомогательныеРеквизиты.ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности;
							
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НМА.СтоимостьЦФ", 
			"НМАСтоимостьЦФ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		//
		ЗначениеСвойства = 
			Объект.ОтражатьВРеглУчете
			И Объект.ПрименениеЦелевогоФинансирования
			И Объект.КорректировкаАмортизации
			И ЭтоНМА
			И ВспомогательныеРеквизиты.ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности;
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НМА.АмортизацияЦФ", 
			"НМААмортизацияЦФ", 
			"Видимость", 
			ЗначениеСвойства И (НЕ Объект.РаздельнаяКорректировкаТекущегоИПрошлогоПериодов ИЛИ НЕ ЭтоКорректировкаНаНачалоГода), 
			ПараметрыРеквизитовОбъекта);
			
		//
		ЗначениеСвойства = 
			Объект.ОтражатьВРеглУчете
			И Объект.КорректировкаАмортизации
			И Объект.ПрименениеЦелевогоФинансирования
			И Объект.РаздельнаяКорректировкаТекущегоИПрошлогоПериодов
			И ЭтоНМА
			И ВспомогательныеРеквизиты.ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности;
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НМА.АмортизацияНаНачалоГодаЦФ", 
			"НМААмортизацияНаНачалоГодаЦФ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;
	
	#КонецОбласти
	
	#КонецОбласти
	
	#Область СтраницаРасходыИДоходы
	
	Если СтруктураИзмененныхРеквизитов.Свойство("КорректировкаСтоимости")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражениеУвеличенияСтоимости")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПрименениеЦелевогоФинансирования")
		ИЛИ ОбновитьВсе Тогда

		ЗначениеСвойства =
			Объект.КорректировкаСтоимости
			И Объект.ОтражениеУвеличенияСтоимости
			И Объект.ПрименениеЦелевогоФинансирования;

		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПодразделениеУвеличениеДоходовЦФ", 
			"ПодразделениеУвеличениеДоходовЦФ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НаправлениеДеятельностиУвеличениеДоходовЦФ", 
			"НаправлениеДеятельностиУвеличениеДоходовЦФ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПараметрыРасходовСовпадаютСДаннымиУчетаУвеличениеДоходовЦФ", 
			"ПараметрыРасходовСовпадаютСДаннымиУчетаУвеличениеДоходовЦФ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"", 
			"ДекорацияУвеличениеДоходовЦФ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;

	Если СтруктураИзмененныхРеквизитов.Свойство("КорректировкаСтоимости")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражениеУменьшенияСтоимости")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПрименениеЦелевогоФинансирования")
		ИЛИ ОбновитьВсе Тогда

		ЗначениеСвойства =
			Объект.КорректировкаСтоимости
			И Объект.ОтражениеУменьшенияСтоимости
			И Объект.ПрименениеЦелевогоФинансирования;

		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПодразделениеУменьшениеДоходовЦФ", 
			"ПодразделениеУменьшениеДоходовЦФ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НаправлениеДеятельностиУменьшениеДоходовЦФ", 
			"НаправлениеДеятельностиУменьшениеДоходовЦФ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПараметрыРасходовСовпадаютСДаннымиУчетаУменьшениеДоходовЦФ", 
			"ПараметрыРасходовСовпадаютСДаннымиУчетаУменьшениеДоходовЦФ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"", 
			"ДекорацияУменьшениеДоходовЦФ", 
			"Видимость", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
			
	КонецЕсли;

	Если СтруктураИзмененныхРеквизитов.Свойство("КорректировкаСтоимости")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражениеУвеличенияСтоимости")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПараметрыРасходовСовпадаютСДаннымиУчетаУвеличениеДоходовЦФ")
		ИЛИ ОбновитьВсе Тогда

		ЗначениеСвойства = 
			Объект.КорректировкаСтоимости
			И Объект.ОтражениеУвеличенияСтоимости
			И Объект.ПараметрыРасходовСовпадаютСДаннымиУчетаУвеличениеДоходовЦФ;

		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПодразделениеУвеличениеДоходовЦФ", 
			"ПодразделениеУвеличениеДоходовЦФ", 
			"ТолькоПросмотрБезОтметкиНезаполненного", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НаправлениеДеятельностиУвеличениеДоходовЦФ", 
			"НаправлениеДеятельностиУвеличениеДоходовЦФ", 
			"ТолькоПросмотр", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
	КонецЕсли;

	Если СтруктураИзмененныхРеквизитов.Свойство("КорректировкаСтоимости")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ОтражениеУменьшенияСтоимости")
		ИЛИ СтруктураИзмененныхРеквизитов.Свойство("ПараметрыРасходовСовпадаютСДаннымиУчетаУменьшениеДоходовЦФ")
		ИЛИ ОбновитьВсе Тогда

		ЗначениеСвойства = 
			Объект.КорректировкаСтоимости
			И Объект.ОтражениеУменьшенияСтоимости
			И Объект.ПараметрыРасходовСовпадаютСДаннымиУчетаУменьшениеДоходовЦФ;

		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"ПодразделениеУменьшениеДоходовЦФ", 
			"ПодразделениеУменьшениеДоходовЦФ", 
			"ТолькоПросмотрБезОтметкиНезаполненного", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
		ОбщегоНазначенияУТКлиентСервер.ДобавитьПараметрыРеквизитаОбъекта(
			"НаправлениеДеятельностиУменьшениеДоходовЦФ", 
			"НаправлениеДеятельностиУменьшениеДоходовЦФ", 
			"ТолькоПросмотр", 
			ЗначениеСвойства, 
			ПараметрыРеквизитовОбъекта);
		
	КонецЕсли;
	
	#КонецОбласти

	//-- Локализация
	
КонецПроцедуры
 
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СтруктураИзмененныхРеквизитов) Экспорт

	//++ Локализация
	
	Элементы = Форма.Элементы;
	ОбновитьВсе = СтруктураИзмененныхРеквизитов.Количество() = 0;
	
	Если ОбновитьВсе Тогда
		
		ЗначениеСвойства = 
			?(Форма.СлужебныеПараметрыФормы.ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности,
				НСтр("ru = 'Стоимость (собственные средства)';
					|en = 'Cost (own funds)'"),
				НСтр("ru = 'Стоимость';
					|en = 'Profit'"));
				
		Элементы.НМАГруппаСтоимость.Заголовок = ЗначениеСвойства;
		
		//
		ЗначениеСвойства = 
			?(Форма.СлужебныеПараметрыФормы.ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности,
				НСтр("ru = 'Амортизация (собственные средства)';
					|en = 'Depreciation (own funds)'"),
				НСтр("ru = 'Амортизация';
					|en = 'Depreciation'"));
				
		Элементы.НМАГруппаАмортизация.Заголовок = ЗначениеСвойства;
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыВЗависимостиОтСвойств(Объект, ПараметрыРеквизита, Действие, ИзмененныеРеквизиты) Экспорт

	//++ Локализация
	
	Если ПараметрыРеквизита.ИмяРеквизита = "ВидИмущества" Тогда
		Если Действие = "Заполнить" Тогда
			Объект[ПараметрыРеквизита.ИмяРеквизита] = ПредопределенноеЗначение("Перечисление.ВидыИмущества.Собственное");
			ИзмененныеРеквизиты.Добавить(ПараметрыРеквизита.ИмяРеквизита);
		КонецЕсли; 
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры
 
#КонецОбласти
