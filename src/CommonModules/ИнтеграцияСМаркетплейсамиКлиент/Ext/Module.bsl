///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обрабатывает вызов команды заполнения.
//
// Параметры:
//  Форма      - ФормаКлиентскогоПриложения  - форма, в которой требуется обработать событие.
//  ИмяКоманды - Строка - имя обрабатываемой команды.
//  ЗначенияПараметров - Структура из КлючИЗначение - произвольный список параметров.
//
Процедура ВыполнитьКоманду(Форма, ИмяКоманды, ЗначенияПараметров) Экспорт

	Если ИмяКоманды = "ЗаполнитьСклады" Тогда
		ЗаполнитьСкладВВыделенныхСтроках(Форма, ИмяКоманды, ЗначенияПараметров);
	КонецЕсли;

КонецПроцедуры

// Выполняет обработку события начала выбора для элемента.
//
// Параметры:
//   Форма                - ФормаКлиентскогоПриложения  - форма, в которой требуется обработать событие.
//   Элемент              - ПолеФормы  - элемент, для которого требуется обработать событие.
//   ДанныеВыбора         - Неопределено,
//                        - СписокЗначений Из Произвольный - для заполнения списка доступных к выбору значений.
//   СтандартнаяОбработка - Булево - признак стандартной обработки.
//   Параметры            - Неопределено
//                        - Структура из КлючИЗначение - любые переданные параметры.
//
Процедура ОбработатьСобытиеНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка,
		Параметры = Неопределено) Экспорт

	Если Элемент.Имя = "ТаблицаСопоставленияДанных_Склад"
	  Или Элемент.Имя = "ТаблицаСопоставленияДанных_Серия" Тогда
		ОткрытьПодборСкладовИСерий(Форма, СтандартнаяОбработка, Параметры);
	КонецЕсли;

КонецПроцедуры

// В зависимости вида формы и от ключа назначения формы дополняются параметры открытия формы.
//
// Параметры:
//   ПараметрыФормы             - Структура - параметры формы, где ключ структуры - имя параметра, а значение - значение
//                                  параметра формы.
//   ИсточникПараметров         - Структура - параметры, на основании которых выполняется анализ необходимости дополнения параметров формы:
//     * КлючНазначенияФормы      - Строка - ключ назначения формы.
//     * ДанныеТорговойПлощадки   - Структура Из КлючИЗначение - данные торговой площадки.
//   ИмяОткрываемойФормы        - Строка - имя формы, параметры для которой нужно дополнить.
//
Процедура ДополнитьПараметрыФормы(ПараметрыФормы, ИсточникПараметров, ИмяОткрываемойФормы) Экспорт

	Если Не ПараметрыФормы.Свойство("УникальныйИдентификаторФормы") Тогда
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы", Неопределено);
	КонецЕсли;
	
	КлючНазначенияИзПараметровФормы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыФормы, "КлючНазначенияФормы", "");
	КлючНазначенияФормы             = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИсточникПараметров, "КлючНазначенияФормы", "");
	ДанныеТорговойПлощадки          = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ИсточникПараметров, "ДанныеТорговойПлощадки", ДанныеТорговойПлощадкиПустые());
	
	КлючНазначенияОткрываемойФормы  = "";
	ЗаголовокФормы                  = "";
	ИмяДинамическогоСписка          = "";
	БыстрыйОтбор                    = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыФормы, "СтруктураБыстрогоОтбора", Новый Структура);
	Отбор                           = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыФормы, "Отбор", Новый Структура);
	ФиксированныеНастройки          = Неопределено;
	
	ЭтоЯМ   = Ложь;
	
	ХозОперацииПоставок          = Неопределено;
	ХозОперацииПродаж            = Неопределено;
	ХозОперацииПередачиВДоставку = Неопределено;
	
	ДоговорПродажиЧерезСкладыТорговойПлощадки = ДанныеТорговойПлощадки.ДоговорПродажиЧерезСкладыТорговойПлощадки;
	ДоговорПродажиЧерезСкладыСобственные      = ДанныеТорговойПлощадки.ДоговорПродажиЧерезСкладыСобственные;
	
	СкладОтгрузки = ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка");
	
	ЭтоРеализацияДляFBS = Ложь;
	
	ИспользуемыеХозОперации = Новый Структура;
	
	Если ДанныеТорговойПлощадки.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		ИспользуемыеХозОперации = ИнтеграцияСМаркетплейсомOzonВызовСервера.ИспользуемыеХозяйственныеОперации(
			ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);
		
		Если ДанныеТорговойПлощадки.СпособОтраженияПродажFBO = "Комиссия" Тогда
			ХозОперацииПоставок = ИспользуемыеХозОперации.ПоставкиКомиссия;
		КонецЕсли;
		
		Если ДанныеТорговойПлощадки.СпособОтраженияПродажFBS = "Комиссия" Тогда
			ХозОперацииПродаж = ИспользуемыеХозОперации.ПродажиКомиссия;
			ХозОперацииПередачиВДоставку = ИспользуемыеХозОперации.ПродажиКомиссияПередачаВДоставку;
		ИначеЕсли ДанныеТорговойПлощадки.СпособОтраженияПродажFBS = "НеИспользуются"
					И ДанныеТорговойПлощадки.СпособОтраженияПродажFBO = "Комиссия" Тогда
			ХозОперацииПродаж = ИспользуемыеХозОперации.ПродажиКомиссия;
		ИначеЕсли ДанныеТорговойПлощадки.СпособОтраженияПродажFBS = "РеализацияВПути"
				И ДанныеТорговойПлощадки.СпособОтраженияПродажFBO = "Комиссия" Тогда
			ХозОперацииПродаж = ИспользуемыеХозОперации.ПродажиКомиссияРеализацияВПути;
		ИначеЕсли ДанныеТорговойПлощадки.СпособОтраженияПродажFBS = "РеализацияВПути" Тогда
			ХозОперацииПродаж = ИспользуемыеХозОперации.ПродажиРеализацияВПути;
		КонецЕсли;
		
		Если ДанныеТорговойПлощадки.СпособОтраженияПродажFBS = "РеализацияВПути" Тогда
			ЭтоРеализацияДляFBS = Истина;
		КонецЕсли;
		
	ИначеЕсли ДанныеТорговойПлощадки.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет") Тогда
		ЭтоЯМ = Истина;
		
		ИспользуемыеХозОперации = ИнтеграцияСЯндексМаркетВызовСервера.ИспользуемыеХозяйственныеОперации(
			ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);
		
		КонстантыСервиса = ИнтеграцияСЯндексМаркетКлиентСервер.КонстантыСервиса();
	
		Если ДанныеТорговойПлощадки.СпособОтраженияПродаж = КонстантыСервиса.СпособОтраженияПродажРеализация Тогда
			ХозОперацииПоставок = ИспользуемыеХозОперации.ПоставкиРеализация;
			ХозОперацииПродаж   = ИспользуемыеХозОперации.ПродажиРеализация;
			ДоговорПродажиЧерезСкладыТорговойПлощадки = ДанныеТорговойПлощадки.ДоговорРеализация;
		ИначеЕсли ДанныеТорговойПлощадки.СпособОтраженияПродаж = КонстантыСервиса.СпособОтраженияПродажКомиссия Тогда
			ХозОперацииПоставок = ИспользуемыеХозОперации.ПоставкиКомиссия;
			ХозОперацииПродаж   = ИспользуемыеХозОперации.ПродажиКомиссия;
			ДоговорПродажиЧерезСкладыТорговойПлощадки = ДанныеТорговойПлощадки.ДоговорКомиссия;
		КонецЕсли;
		
		СкладОтгрузки = ДанныеТорговойПлощадки.СкладОтгрузки;
		
	Иначе
		Возврат;
	КонецЕсли;
	
	ДанныеТорговойПлощадки.Вставить("ХозОперацииПоставок", ХозОперацииПоставок);
	ДанныеТорговойПлощадки.Вставить("ХозОперацииПродаж", ХозОперацииПродаж);
	ДанныеТорговойПлощадки.Вставить("ХозОперацииПередачиВДоставку", ХозОперацииПередачиВДоставку);
	
	БыстрыйОтборВДинамическийСписок = Ложь;
	
	Если ИмяОткрываемойФормы = "Обработка.ЖурналДокументовПродажи.Форма.СписокДокументов" Тогда
		
		ИмяДинамическогоСписка = "СписокДокументыПродажи";
		
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документы поставки <%1> (все)';
					|en = 'Shipment documents <%1> (all)'"),
				ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);
			
			Если КлючНазначенияИзПараметровФормы = "ВозвратТоваровОтКлиента" Тогда
				ПараметрыФормы.Вставить("КлючНазначенияФормы", "ПоступлениеТоваровОтХранителя");
			КонецЕсли;
		ИначеЕсли КлючНазначенияФормы = "РаботаСПродажамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документы продажи <%1> (все)';
					|en = 'Sales documents <%1> (all)'"),
				ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);
		ИначеЕсли КлючНазначенияФормы = "РаботаСПродажамиПередачаВДоставкуМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПродажамиПередачаВДоставкуМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документы передачи в доставку <%1> (все)';
					|en = '<%1> documents of transfer to delivery service (all)'"),
				ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		ПереопределениеОтбора = Новый Структура;
		ПереопределениеОтбора.Вставить(
			"ПокупательПартнер", ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка"));
		ПереопределениеОтбора.Вставить(
			"ПокупательКонтрагент", ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
		ПереопределениеОтбора.Вставить(
			"РозничныйПартнер", ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка"));
		ПереопределениеОтбора.Вставить(
			"РозничныйКонтрагент", ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
		
		ЗаполнитьЗначенияСвойств(ПереопределениеОтбора, ДанныеТорговойПлощадки);
		
		// Отбор по партнеру.
		Если ЭтоЯМ И ЗначениеЗаполнено(СкладОтгрузки) Тогда
			// Пропустить отбор.
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП"
					И ЭтоРеализацияДляFBS Тогда
			// Пропустить отбор.
		ИначеЕсли ЭтоЯМ И ЗначениеЗаполнено(ПереопределениеОтбора.ПокупательПартнер) Тогда
			Отбор.Вставить("Партнер", ПереопределениеОтбора.ПокупательПартнер);
		ИначеЕсли ЭтоЯМ И ЗначениеЗаполнено(ПереопределениеОтбора.РозничныйПартнер) Тогда
			Отбор.Вставить("Партнер", ПереопределениеОтбора.РозничныйПартнер);
		ИначеЕсли ЗначениеЗаполнено(ДанныеТорговойПлощадки.Партнер) Тогда
			Отбор.Вставить("Партнер", ДанныеТорговойПлощадки.Партнер);
		КонецЕсли;
		
		// Отбор по контрагенту.
		Если ЭтоЯМ И ЗначениеЗаполнено(СкладОтгрузки) Тогда
			// Пропустить отбор.
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП"
					И ЭтоРеализацияДляFBS Тогда
			// Пропустить отбор.
		ИначеЕсли ЭтоЯМ И ЗначениеЗаполнено(ПереопределениеОтбора.ПокупательКонтрагент) Тогда
			Отбор.Вставить("Контрагент", ПереопределениеОтбора.ПокупательКонтрагент);
		ИначеЕсли ЭтоЯМ И ЗначениеЗаполнено(ПереопределениеОтбора.РозничныйКонтрагент) Тогда
			Отбор.Вставить("Контрагент", ПереопределениеОтбора.РозничныйКонтрагент);
		ИначеЕсли ЗначениеЗаполнено(ДанныеТорговойПлощадки.Контрагент) Тогда
			Отбор.Вставить("Контрагент", ДанныеТорговойПлощадки.Контрагент);
		КонецЕсли;
		
		// Отбор по складу.
		Если ЭтоЯМ И ЗначениеЗаполнено(СкладОтгрузки) Тогда
			БыстрыйОтбор.Вставить("Склад", СкладОтгрузки);
		КонецЕсли;
		
		// Отбор по договорам.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП"
				И ЗначениеЗаполнено(ДоговорПродажиЧерезСкладыТорговойПлощадки) Тогда
			Отбор.Вставить("Договор", ДоговорПродажиЧерезСкладыТорговойПлощадки);
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП" Тогда
			Если ЭтоРеализацияДляFBS Тогда
				// Пропустить отбор.
			ИначеЕсли ЭтоЯМ Тогда
				// Пропустить отбор.
			Иначе
				ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(
					ФиксированныеНастройки,
					ДанныеТорговойПлощадки,
					"ВсеДоговоры");
			КонецЕсли;
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиПередачаВДоставкуМП"
					И ЗначениеЗаполнено(ДоговорПродажиЧерезСкладыСобственные) Тогда
			Отбор.Вставить("Договор", ДоговорПродажиЧерезСкладыСобственные);
		КонецЕсли;
		
		// Используемые хоз. операции.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП"
				И ХозОперацииПоставок <> Неопределено Тогда
			ПараметрыФормы.Вставить("ИспользуемыеХозОперации", ХозОперацииПоставок);
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП"
					И ХозОперацииПродаж <> Неопределено Тогда
			ПараметрыФормы.Вставить("ИспользуемыеХозОперации", ХозОперацииПродаж);
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиПередачаВДоставкуМП"
				И ХозОперацииПередачиВДоставку <> Неопределено Тогда
			ПараметрыФормы.Вставить("ИспользуемыеХозОперации", ХозОперацииПередачиВДоставку);
		Иначе
			ПараметрыФормы.Вставить("ИспользоватьЗагрузкуСохраненныхНастроек", Истина);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("ИспользоватьЗагрузкуНастроек", Истина);
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы",
			Строка(ДанныеТорговойПлощадки.УникальныйИдентификатор) + КлючНазначенияОткрываемойФормы
			+ ?(ПустаяСтрока(КлючНазначенияИзПараметровФормы) Или КлючНазначенияОткрываемойФормы = КлючНазначенияФормы, "", КлючНазначенияИзПараметровФормы));
		
	ИначеЕсли ИмяОткрываемойФормы = "Документ.ЗаказКлиента.Форма.ФормаСпискаДокументов" Тогда
		
		ИмяДинамическогоСписка = "Список";
		
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Заявки на поставку <%1>';
					|en = 'Shipment requests <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы + ", %1");
		ИначеЕсли КлючНазначенияФормы = "РаботаСПродажамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Заказы клиентов <%1>';
					|en = 'Sales orders <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы + ", %1");
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если БыстрыйОтбор.Свойство("Организация") И ЗначениеЗаполнено(БыстрыйОтбор.Организация) Тогда
			Отбор.Вставить("Организация", БыстрыйОтбор.Организация);
		Иначе
			Отбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ЗаголовокФормы, Отбор.Организация);
		
		// Отбор по партнеру.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП"
				И ЭтоРеализацияДляFBS Тогда
			// Пропустить отбор.
		ИначеЕсли ЗначениеЗаполнено(ДанныеТорговойПлощадки.Партнер) Тогда
			Отбор.Вставить("Партнер", ДанныеТорговойПлощадки.Партнер);
		КонецЕсли;
		
		// Отбор по контрагенту.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП"
				И ЭтоРеализацияДляFBS Тогда
			// Пропустить отбор.
		ИначеЕсли ЗначениеЗаполнено(ДанныеТорговойПлощадки.Контрагент) Тогда
			Отбор.Вставить("Контрагент", ДанныеТорговойПлощадки.Контрагент);
		КонецЕсли;
		
		// Отбор по договорам.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП"
				И ЗначениеЗаполнено(ДоговорПродажиЧерезСкладыТорговойПлощадки) Тогда
			Отбор.Вставить("Договор", ДоговорПродажиЧерезСкладыТорговойПлощадки);
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП" Тогда
			Если ЭтоРеализацияДляFBS Тогда
				// Пропустить отбор.
			ИначеЕсли ЗначениеЗаполнено(ДоговорПродажиЧерезСкладыСобственные) Тогда
				Отбор.Вставить("Договор", ДоговорПродажиЧерезСкладыСобственные);
			КонецЕсли;
		КонецЕсли;
		
		// Используемые хоз. операции.
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки, ДанныеТорговойПлощадки, "ХозОперацииПоставок");
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП" Тогда
			ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки, ДанныеТорговойПлощадки, "ХозОперацииПродаж");
		КонецЕсли;
		
		ПараметрыФормы.Вставить("ИспользоватьЗагрузкуНастроек", Истина);
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы",
			Строка(ДанныеТорговойПлощадки.УникальныйИдентификатор) + КлючНазначенияОткрываемойФормы);
		
	ИначеЕсли ИмяОткрываемойФормы = "Обработка.ЖурналДокументовПродажи.Форма.КОформлениюНакладных" Тогда
		
		ИмяДинамическогоСписка = "СписокРаспоряженияНаОформление";
		
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документы поставки (к оформлению) <%1>';
					|en = 'Shipment documents (to register) <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);
		ИначеЕсли КлючНазначенияФормы = "РаботаСПродажамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документы продажи (к оформлению) <%1>';
					|en = 'Sales documents (to register) <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		// Отбор по партнеру.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП"
				И ЭтоРеализацияДляFBS Тогда
			// Пропустить отбор.
		ИначеЕсли ЗначениеЗаполнено(ДанныеТорговойПлощадки.Партнер) Тогда
			Отбор.Вставить("Партнер", ДанныеТорговойПлощадки.Партнер);
		КонецЕсли;
		
		// Отбор по контрагенту.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП"
				И ЭтоРеализацияДляFBS Тогда
			// Пропустить отбор.
		ИначеЕсли ЗначениеЗаполнено(ДанныеТорговойПлощадки.Контрагент) Тогда
			Отбор.Вставить("Контрагент", ДанныеТорговойПлощадки.Контрагент);
		КонецЕсли;
		
		// Отбор по договорам.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП"
				И ЗначениеЗаполнено(ДоговорПродажиЧерезСкладыТорговойПлощадки) Тогда
			Отбор.Вставить("Договор", ДоговорПродажиЧерезСкладыТорговойПлощадки);
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП" Тогда
			Если ЭтоРеализацияДляFBS Тогда
				// Пропустить отбор.
			ИначеЕсли ЗначениеЗаполнено(ДоговорПродажиЧерезСкладыСобственные) Тогда
				Отбор.Вставить("Договор", ДоговорПродажиЧерезСкладыСобственные);
			КонецЕсли;
		КонецЕсли;
		
		// Используемые хоз. операции.
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки, ДанныеТорговойПлощадки, "ХозОперацииПоставок");
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП" Тогда
			ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки, ДанныеТорговойПлощадки, "ХозОперацииПродаж");
			ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки, ДанныеТорговойПлощадки, "ТипыРаспоряженийПродаж");
		КонецЕсли;
		
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы",
			Строка(ДанныеТорговойПлощадки.УникальныйИдентификатор) + КлючНазначенияОткрываемойФормы);
		
	ИначеЕсли ИмяОткрываемойФормы = "Обработка.ЖурналДокументовОтчетыКомиссионеров.Форма.КОформлениюОтчетовКомиссионеров" Тогда
		
		ИмяДинамическогоСписка = "СписокНаОформление";
		
		Если КлючНазначенияФормы = "РаботаСПродажамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Отчеты комиссионеров о продажах/Реализации через комиссионеров (к оформлению) <%1>';
					|en = 'Consignment notifications and invoices via consignee (to register) <%1>'"), 
				ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		// Отбор по партнеру.
		Если Не БыстрыйОтбор.Свойство("Комиссионер") И ЗначениеЗаполнено(ДанныеТорговойПлощадки.Партнер) Тогда
			БыстрыйОтбор.Вставить("Комиссионер", ДанныеТорговойПлощадки.Партнер);
		КонецЕсли;
		
		// Отбор по контрагенту.
		Если ЗначениеЗаполнено(ДанныеТорговойПлощадки.Контрагент) И ЗначениеЗаполнено(ДанныеТорговойПлощадки.Контрагент) Тогда
			Отбор.Вставить("Контрагент", ДанныеТорговойПлощадки.Контрагент);
		КонецЕсли;
		
		// Отбор по договорам.
		ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки, ДанныеТорговойПлощадки, "ВсеДоговоры");
		
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы", ДанныеТорговойПлощадки.УникальныйИдентификатор);
		
	ИначеЕсли ИмяОткрываемойФормы = "Документ.АктОРасхожденияхПослеОтгрузки.Форма.ФормаСписка" Тогда
		
		ИмяДинамическогоСписка = "Список";
		
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Акты о расхождениях после передачи товаров <%1>';
					|en = 'Discrepancy reports after goods transfer <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы + ", %1");
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если БыстрыйОтбор.Свойство("Организация") И ЗначениеЗаполнено(БыстрыйОтбор.Организация) Тогда
			Отбор.Вставить("Организация", БыстрыйОтбор.Организация);
		Иначе
			Отбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ЗаголовокФормы, Отбор.Организация);
		
		// Отбор по партнеру.
		Если ЗначениеЗаполнено(ДанныеТорговойПлощадки.Партнер) Тогда
			Отбор.Вставить("Партнер", ДанныеТорговойПлощадки.Партнер);
		КонецЕсли;
		
		// Отбор по контрагенту.
		Если ЗначениеЗаполнено(ДанныеТорговойПлощадки.Контрагент) Тогда
			Отбор.Вставить("Контрагент", ДанныеТорговойПлощадки.Контрагент);
		КонецЕсли;
		
		// Отбор по договорам.
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП"
				И ЗначениеЗаполнено(ДоговорПродажиЧерезСкладыТорговойПлощадки) Тогда
			Отбор.Вставить("Договор", ДоговорПродажиЧерезСкладыТорговойПлощадки);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("ТипОснованияАктаОРасхождении", ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаНаКомиссию"));
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы", ДанныеТорговойПлощадки.УникальныйИдентификатор);
		
	ИначеЕсли ИмяОткрываемойФормы = "Документ.ЗаявкаНаВозвратТоваровОтКлиента.Форма.ФормаСпискаДокументов" Тогда
		
		ИмяДинамическогоСписка = "Список";
		
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Заявки на возврат товаров поставки <%1>';
					|en = 'Shipment return requests <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы + ", %1");
		ИначеЕсли КлючНазначенияФормы = "РаботаСПродажамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Заявки на возврат товаров от клиента <%1>';
					|en = 'Sales return requests <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы + ", %1");
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если БыстрыйОтбор.Свойство("Организация") И ЗначениеЗаполнено(БыстрыйОтбор.Организация) Тогда
			Отбор.Вставить("Организация", БыстрыйОтбор.Организация);
		Иначе
			Отбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ЗаголовокФормы, Отбор.Организация);
		
		// Отбор по партнеру.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП"
				И ЭтоРеализацияДляFBS Тогда
			// Пропустить отбор.
		ИначеЕсли ЗначениеЗаполнено(ДанныеТорговойПлощадки.Партнер) Тогда
			Отбор.Вставить("Партнер", ДанныеТорговойПлощадки.Партнер);
		КонецЕсли;
		
		// Отбор по контрагенту.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП"
				И ЭтоРеализацияДляFBS Тогда
			// Пропустить отбор.
		ИначеЕсли ЗначениеЗаполнено(ДанныеТорговойПлощадки.Контрагент) Тогда
			Отбор.Вставить("Контрагент", ДанныеТорговойПлощадки.Контрагент);
		КонецЕсли;
		
		// Отбор по договорам.
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП"
				И ЗначениеЗаполнено(ДоговорПродажиЧерезСкладыТорговойПлощадки) Тогда
			Отбор.Вставить("Договор", ДоговорПродажиЧерезСкладыТорговойПлощадки);
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП" Тогда
			Если ЭтоРеализацияДляFBS Тогда
				// Пропустить отбор.
			ИначеЕсли ЗначениеЗаполнено(ДоговорПродажиЧерезСкладыСобственные) Тогда
				Отбор.Вставить("Договор", ДоговорПродажиЧерезСкладыСобственные);
			КонецЕсли;
		КонецЕсли;
		
		// Используемые хоз. операции.
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки, ДанныеТорговойПлощадки, "ХозОперацииПоставок");
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП" Тогда
			ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки, ДанныеТорговойПлощадки, "ХозОперацииПродаж");
		КонецЕсли;
		
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы",
			Строка(ДанныеТорговойПлощадки.УникальныйИдентификатор) + КлючНазначенияОткрываемойФормы);
		
	ИначеЕсли ИмяОткрываемойФормы = "Обработка.ЖурналДокументовПродажи.Форма.КОформлениюНакладныхВозвратов" Тогда
		
		ИмяДинамическогоСписка = "СписокРаспоряженияНаОформление";
		
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документы возвратов поставки (к оформлению) <%1>';
					|en = 'Shipment return documents (to register) <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);
		ИначеЕсли КлючНазначенияФормы = "РаботаСПродажамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документы возвратов продажи (к оформлению) <%1>';
					|en = 'Sales return documents (to register) <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		// Отбор по партнеру.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП"
				И ЭтоРеализацияДляFBS Тогда
			// Пропустить отбор.
		ИначеЕсли ЗначениеЗаполнено(ДанныеТорговойПлощадки.Партнер) Тогда
			Отбор.Вставить("Партнер", ДанныеТорговойПлощадки.Партнер);
		КонецЕсли;
		
		// Отбор по контрагенту.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП"
				И ЭтоРеализацияДляFBS Тогда
			// Пропустить отбор.
		ИначеЕсли ЗначениеЗаполнено(ДанныеТорговойПлощадки.Контрагент) Тогда
			Отбор.Вставить("Контрагент", ДанныеТорговойПлощадки.Контрагент);
		КонецЕсли;
		
		// Отбор по договорам.
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП"
				И ЗначениеЗаполнено(ДоговорПродажиЧерезСкладыТорговойПлощадки) Тогда
			Отбор.Вставить("Договор", ДоговорПродажиЧерезСкладыТорговойПлощадки);
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП" Тогда
			Если ЭтоРеализацияДляFBS Тогда
				// Пропустить отбор.
			ИначеЕсли ЗначениеЗаполнено(ДоговорПродажиЧерезСкладыСобственные) Тогда
				Отбор.Вставить("Договор", ДоговорПродажиЧерезСкладыСобственные);
			КонецЕсли;
		КонецЕсли;
		
		// Используемые хоз. операции.
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки, ДанныеТорговойПлощадки, "ХозОперацииПоставок");
		ИначеЕсли КлючНазначенияОткрываемойФормы = "РаботаСПродажамиМП" Тогда
			ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки, ДанныеТорговойПлощадки, "ХозОперацииПродаж");
			ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки, ДанныеТорговойПлощадки, "ТипыРаспоряженийПродаж");
		КонецЕсли;
		
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы",
			Строка(ДанныеТорговойПлощадки.УникальныйИдентификатор) + КлючНазначенияОткрываемойФормы);
		
	ИначеЕсли ИмяОткрываемойФормы = "Документ.ТранспортнаяНакладная.Форма.ФормаСписка" Тогда
		
		ИмяДинамическогоСписка = "Список";
		
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП"
				Или КлючНазначенияФормы = "РаботаСПродажамиМП" Тогда
			ЗаголовокФормы = НСтр("ru = 'Транспортные накладные <%1>';
									|en = 'Consignment notes <%1>'");
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		// Иные отборы.
		ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки, ДанныеТорговойПлощадки, "Организация");
		ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки, ДанныеТорговойПлощадки, "НастройкиДляТТН");
		
		ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ЗаголовокФормы, Строка(ДанныеТорговойПлощадки.Контрагент) + ", " + Строка(ДанныеТорговойПлощадки.Организация));
		
		ПараметрыФормы.Вставить("ВладелецОткрываемойФормы",     Неопределено);
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы", ДанныеТорговойПлощадки.Организация.УникальныйИдентификатор());
		
	ИначеЕсли ИмяОткрываемойФормы = "Обработка.ЖурналДокументовНДС.Форма.СписокДокументов" Тогда
		
		ИмяДинамическогоСписка = "СписокДокументыНДС";
		
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП"
				Или КлючНазначенияФормы = "РаботаСПродажамиМП" Тогда
			// Устанавливаются параметры формы.
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		// Отбор по контрагенту.
		Если КлючНазначенияФормы = "РаботаСПродажамиМП"
				И ЭтоРеализацияДляFBS Тогда
			// Пропустить отбор.
		ИначеЕсли ЗначениеЗаполнено(ДанныеТорговойПлощадки.Контрагент) Тогда
			Отбор.Вставить("Контрагент", ДанныеТорговойПлощадки.Контрагент);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("ВладелецОткрываемойФормы",     Неопределено);
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы", ДанныеТорговойПлощадки.УникальныйИдентификатор);
		
	ИначеЕсли ИмяОткрываемойФормы = "Обработка.ЖурналДокументовВнутреннегоТовародвижения.Форма.СписокДокументов" Тогда
		
		ИмяДинамическогоСписка = "СписокДокументыВнутреннегоТовародвижения";
		
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документы поставки <%1> (все)';
					|en = 'Shipment documents <%1> (all)'"),
				ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		// Отбор по складу.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП"
				И ЗначениеЗаполнено(СкладОтгрузки) Тогда
			БыстрыйОтбор.Вставить("Склад", СкладОтгрузки);
		КонецЕсли;
		
		// Отбор по хоз.операциям.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП" 
				И ХозОперацииПоставок <> Неопределено Тогда
			ПараметрыФормы.Вставить("ИспользуемыеХозОперации", ХозОперацииПоставок);
		Иначе
			ПараметрыФормы.Вставить("ИспользоватьЗагрузкуСохраненныхНастроек", Истина);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("ИспользоватьЗагрузкуНастроек", Истина);
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы",
			Строка(ДанныеТорговойПлощадки.УникальныйИдентификатор) + КлючНазначенияОткрываемойФормы);
		
	ИначеЕсли ИмяОткрываемойФормы = "Обработка.ЖурналДокументовВнутреннегоТовародвижения.Форма.СписокДокументовКОформлению" Тогда
		
		ИмяДинамическогоСписка = "СписокРаспоряженияНаОформление";
		
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "ВнутреннееТовародвижениеМП";
			ПараметрыФормы.Вставить("КлючНазначенияФормы", "ВнутреннееТовародвижение");
			
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Документы поставки (к оформлению) <%1>';
					|en = 'Shipment documents (to register) <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("ИспользоватьЗагрузкуСохраненныхНастроек", Истина);
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы",
			Строка(ДанныеТорговойПлощадки.УникальныйИдентификатор) + КлючНазначенияОткрываемойФормы);
		
	ИначеЕсли ИмяОткрываемойФормы = "Документ.ЗаказНаПеремещение.Форма.ФормаСписка" Тогда
		
		ИмяДинамическогоСписка = "Список";
		
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Заказы на перемещение товаров <%1>';
					|en = 'Goods transfer orders <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы + ", %1");
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если БыстрыйОтбор.Свойство("Организация") И ЗначениеЗаполнено(БыстрыйОтбор.Организация) Тогда
			Отбор.Вставить("Организация", БыстрыйОтбор.Организация);
		Иначе
			Отбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ЗаголовокФормы, Отбор.Организация);
			
		// Отбор по складу.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП"
			 И ЗначениеЗаполнено(СкладОтгрузки) Тогда
			Отбор.Вставить("СкладПолучатель", СкладОтгрузки);
		КонецЕсли;
		
		БыстрыйОтборВДинамическийСписок = Истина;
		
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы", ДанныеТорговойПлощадки.УникальныйИдентификатор);  
		
	ИначеЕсли ИмяОткрываемойФормы = "Документ.АктОРасхожденияхПослеПеремещения.Форма.ФормаСписка" Тогда
		
		ИмяДинамическогоСписка = "Список";
		
		Если КлючНазначенияФормы = "РаботаСПоставкамиМП" Тогда
			КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Акты о расхождениях после перемещения на склад  <%1>';
					|en = 'Stock transfer discrepancy report <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы + ", %1");
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если БыстрыйОтбор.Свойство("Организация") И ЗначениеЗаполнено(БыстрыйОтбор.Организация) Тогда
			Отбор.Вставить("Организация", БыстрыйОтбор.Организация);
		Иначе
			Отбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ЗаголовокФормы, Отбор.Организация);
			
		// Отбор по складу.
		Если КлючНазначенияОткрываемойФормы = "РаботаСПоставкамиМП"
				И ЗначениеЗаполнено(СкладОтгрузки) Тогда
			Отбор.Вставить("СкладПолучатель", СкладОтгрузки);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы", ДанныеТорговойПлощадки.УникальныйИдентификатор);
		
	ИначеЕсли ИмяОткрываемойФормы = "Документ.ПриобретениеУслугПрочихАктивов.ФормаСписка" Тогда
		
		ИмяДинамическогоСписка = "Список";
		
		Если КлючНазначенияФормы = "ВзаиморасчетыМП" Тогда
			КлючНазначенияОткрываемойФормы = "ВзаиморасчетыМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Приобретение услуг и прочих активов <%1>';
					|en = 'Vendor invoice — Services and Assets <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы + ", %1");
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если БыстрыйОтбор.Свойство("Организация") И ЗначениеЗаполнено(БыстрыйОтбор.Организация) Тогда
			Отбор.Вставить("Организация", БыстрыйОтбор.Организация);
		Иначе
			Отбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ЗаголовокФормы, Отбор.Организация);
		
		// Отбор по партнеру.
		Если ЗначениеЗаполнено(ДанныеТорговойПлощадки.Партнер) Тогда
			Отбор.Вставить("Партнер", ДанныеТорговойПлощадки.Партнер);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы", ДанныеТорговойПлощадки.УникальныйИдентификатор);
		
	ИначеЕсли ИмяОткрываемойФормы = "Документ.ВзаимозачетЗадолженности.ФормаСписка" Тогда
		
		ИмяДинамическогоСписка = "Список";
		
		Если КлючНазначенияФормы = "ВзаиморасчетыМП" Тогда
			КлючНазначенияОткрываемойФормы = "ВзаиморасчетыМП";
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Взаимозачеты задолженности <%1>';
					|en = 'AR/AP offsets <%1>'"), ДанныеТорговойПлощадки.Контрагент);
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		БыстрыйОтборВДинамическийСписок = Истина;
		
		// Отбор по контрагенту.
		Если ЗначениеЗаполнено(ДанныеТорговойПлощадки.Контрагент) Тогда
			ФиксированныеНастройки = ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(ФиксированныеНастройки,
				ДанныеТорговойПлощадки, "Взаиморасчеты");
		КонецЕсли;
		
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы", ДанныеТорговойПлощадки.Контрагент.УникальныйИдентификатор());
		
	ИначеЕсли ИмяОткрываемойФормы = "Обработка.ЖурналДокументовБезналичныеПлатежи.Форма.ФормаСписка" Тогда
		
		ИмяДинамическогоСписка = "СписокПлатежей";
		
		Если КлючНазначенияФормы = "ВзаиморасчетыМП" Тогда
			КлючНазначенияОткрываемойФормы = "ВзаиморасчетыМП";
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		// Отбор по банковскому счету.
		Если Не БыстрыйОтбор.Свойство("БанковскийСчетОтбор") Тогда
			БыстрыйОтбор.Вставить("БанковскийСчетОтбор", ПредопределенноеЗначение("Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка"));
		КонецЕсли;
		
		// Отбор по контрагенту.
		БыстрыйОтбор.Вставить("КонтрагентПредставление", ДанныеТорговойПлощадки.Контрагент);
		
		БыстрыйОтборВДинамическийСписок = Истина;
		
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы", ДанныеТорговойПлощадки.УникальныйИдентификатор);
		
	ИначеЕсли ИмяОткрываемойФормы = "Обработка.ЖурналДокументовБезналичныеПлатежи.КПоступлению" Тогда
		
		ИмяДинамическогоСписка = "ЗаказыКПоступлению";
		
		Если КлючНазначенияФормы = "ВзаиморасчетыМП" Тогда
			// Устанавливаются параметры формы.
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		// Отбор по банковскому счету.
		Если Не БыстрыйОтбор.Свойство("БанковскийСчетОтбор") Тогда
			БыстрыйОтбор.Вставить("БанковскийСчетОтбор", ПредопределенноеЗначение("Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка"));
		КонецЕсли;
		
		// Отбор по контрагенту.
		БыстрыйОтбор.Вставить("КонтрагентПредставление", ДанныеТорговойПлощадки.Контрагент);
		
	ИначеЕсли ИмяОткрываемойФормы = "Обработка.ЖурналДокументовБезналичныеПлатежи.ЗаявкиКОплате" Тогда
		
		ИмяДинамическогоСписка = "ЗаявкиКОплате";
		
		Если КлючНазначенияФормы = "ВзаиморасчетыМП" Тогда
			// Устанавливаются параметры формы.
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		// Отбор по банковскому счету.
		Если Не БыстрыйОтбор.Свойство("БанковскийСчетОтбор") Тогда
			БыстрыйОтбор.Вставить("БанковскийСчетОтбор", ПредопределенноеЗначение("Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка"));
		КонецЕсли;
		
		// Отбор по операциям.
		Если Не БыстрыйОтбор.Свойство("СписокОперацийОплаты") Тогда
			БыстрыйОтбор.Вставить("СписокОперацийОплаты", Неопределено);
		КонецЕсли;
		
		// Отбор по контрагенту.
		БыстрыйОтбор.Вставить("КонтрагентПредставление", ДанныеТорговойПлощадки.Контрагент);
		
	ИначеЕсли ИмяОткрываемойФормы = "Обработка.ЖурналДокументовБезналичныеПлатежи.ЗаказыКОплате" Тогда
		
		ИмяДинамическогоСписка = "ЗаказыКОплате";
		
		Если КлючНазначенияФормы = "ВзаиморасчетыМП" Тогда
			// Устанавливаются параметры формы.
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		// Отбор по банковскому счету.
		Если Не БыстрыйОтбор.Свойство("БанковскийСчетОтбор") Тогда
			БыстрыйОтбор.Вставить("БанковскийСчетОтбор", ПредопределенноеЗначение("Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка"));
		КонецЕсли;
		
		// Отбор по контрагенту.
		БыстрыйОтбор.Вставить("КонтрагентПредставление", ДанныеТорговойПлощадки.Контрагент);
		
	ИначеЕсли ИмяОткрываемойФормы = "Обработка.ЖурналДокументовБезналичныеПлатежи.ВалютныйКонтроль" Тогда
		
		ИмяДинамическогоСписка = "ДокументыВалютногоКонтроля";
		
		Если КлючНазначенияФормы = "ВзаиморасчетыМП" Тогда
			// Устанавливаются параметры формы.
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если Не БыстрыйОтбор.Свойство("Организация") Тогда
			БыстрыйОтбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		// Отбор по банковскому счету.
		Если Не БыстрыйОтбор.Свойство("БанковскийСчетОтбор") Тогда
			БыстрыйОтбор.Вставить("БанковскийСчетОтбор", ПредопределенноеЗначение("Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка"));
		КонецЕсли;
		
		// Отбор по контрагенту.
		БыстрыйОтбор.Вставить("КонтрагентПредставление", ДанныеТорговойПлощадки.Контрагент);

	ИначеЕсли ИмяОткрываемойФормы = "Документ.СверкаВзаиморасчетов2_5_11.ФормаСписка" Тогда

		ИмяДинамическогоСписка = "Список";
		
		Если КлючНазначенияФормы = "ВзаиморасчетыМП" Тогда
			ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Сверки взаиморасчетов <%1>';
					|en = 'AR/AP reconciliations <%1>'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы + ", %1");
		Иначе
			Возврат;
		КонецЕсли;
		
		// Отбор по организации.
		Если БыстрыйОтбор.Свойство("Организация") И ЗначениеЗаполнено(БыстрыйОтбор.Организация) Тогда
			Отбор.Вставить("Организация", БыстрыйОтбор.Организация);
		Иначе
			Отбор.Вставить("Организация", ДанныеТорговойПлощадки.Организация);
		КонецЕсли;
		
		ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ЗаголовокФормы, Отбор.Организация);
		
		// Отбор по контрагенту.
		Если ЗначениеЗаполнено(ДанныеТорговойПлощадки.Контрагент) Тогда
			Отбор.Вставить("Контрагент", ДанныеТорговойПлощадки.Контрагент);
		КонецЕсли;
		
		ПараметрыФормы.Вставить("УникальныйИдентификаторФормы", ДанныеТорговойПлощадки.УникальныйИдентификатор);
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
	
	Если Не ПараметрыФормы.Свойство("КлючНазначенияФормы") Или ПустаяСтрока(ПараметрыФормы.КлючНазначенияФормы) Тогда
		ПараметрыФормы.Вставить("КлючНазначенияФормы", КлючНазначенияОткрываемойФормы
			+ ДанныеТорговойПлощадки.ИдентификаторКлиента);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ЗаголовокФормы) Тогда
		ПараметрыФормы.Вставить("ЗаголовокФормы", ЗаголовокФормы);
	КонецЕсли;
	
	Если БыстрыйОтбор.Количество() > 0 Тогда
		ПараметрыФормы.Вставить("СтруктураБыстрогоОтбора", БыстрыйОтбор);
	КонецЕсли;
	
	Если Отбор.Количество() > 0 Тогда
		ПараметрыФормы.Вставить("Отбор", Отбор);
	КонецЕсли;
	
	Если ФиксированныеНастройки <> Неопределено Тогда
		ПараметрыФормы.Вставить("ФиксированныеНастройки", ФиксированныеНастройки);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ИмяДинамическогоСписка) Тогда
		ДополнительныеСвойства = Новый Структура;
		ДополнительныеСвойства.Вставить("КлючНазначенияФормы",    КлючНазначенияОткрываемойФормы);
		ДополнительныеСвойства.Вставить("ДанныеТорговойПлощадки", ДанныеТорговойПлощадки);
	
		Если БыстрыйОтборВДинамическийСписок И БыстрыйОтбор.Количество() > 0 Тогда
			ДополнительныеСвойства.Вставить("СтруктураБыстрогоОтбора", БыстрыйОтбор);
		КонецЕсли;
	
		ДополнительныеСвойстваДинСписков = Новый Структура;
		ДополнительныеСвойстваДинСписков.Вставить(ИмяДинамическогоСписка, ДополнительныеСвойства);
	
		ПараметрыФормы.Вставить("ДополнительныеСвойстваДинамическихСписков", ДополнительныеСвойстваДинСписков);
	КонецЕсли;

КонецПроцедуры

// Показывает состояние выполнения действия.
//
// Параметры:
//   Результат                - Структура - любая структура, ожидаются ключи "КодОшибки", "ОписаниеОшибки".
//   ДополнительныеПараметры  - Неопределено - 
//                            - см. НовоеОписаниеПараметровДействия.
//   ВывестиСообщениеОбОшибке - Булево - признак вывода сообщения.
//   Логотип                  - БиблиотекаКартинок - картинка логотипа.
//
Процедура ВывестиСостояние(Результат, ДополнительныеПараметры = Неопределено,
			ВывестиСообщениеОбОшибке = Ложь, Знач Логотип = Неопределено) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Логотип = Неопределено Тогда
		Логотип = БиблиотекаКартинок.Пустая;
	КонецЕсли;
	
	ОжидаемыйРезультат = Новый Структура;
	ОжидаемыйРезультат.Вставить("КодОшибки",      "");
	ОжидаемыйРезультат.Вставить("ОписаниеОшибки", "");
	ОжидаемыйРезультат.Вставить("Детализация",    Неопределено);
	
	ЗаполнитьЗначенияСвойств(ОжидаемыйРезультат, Результат);
	
	Если Не ПустаяСтрока(ОжидаемыйРезультат.КодОшибки) Тогда
		Если ВывестиСообщениеОбОшибке И Не ПустаяСтрока(ОжидаемыйРезультат.ОписаниеОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ОжидаемыйРезультат.ОписаниеОшибки);
		КонецЕсли;
		
		Если ВывестиСообщениеОбОшибке
				И Результат.Детализация <> Неопределено Тогда
			Если Не ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
				Префикс = Символы.Таб;
			Иначе
				Префикс = "";
			КонецЕсли;
			
			СообщенияДляВывода = ИнтеграцияСМаркетплейсамиКлиентСервер.ЭлементыМассиваДляВыводаСообщений(
				Результат.Детализация,
				НСтр("ru = ';%1 аналогичное сообщение;;%1 аналогичных сообщения;%1 аналогичных сообщений;';
					|en = ';%1 similar message;;;;%1 similar messages'"));
				
			Для Каждого ТекстСообщения Из СообщенияДляВывода Цикл
				ОбщегоНазначенияКлиент.СообщитьПользователю(Префикс + ТекстСообщения);
			КонецЦикла;
		КонецЕсли;
		
		Если ДополнительныеПараметры <> Неопределено Тогда
			Состояние(ДополнительныеПараметры.ОписаниеДействия,, ДополнительныеПараметры.РезультатСОшибками, Логотип);
		КонецЕсли;
		
	ИначеЕсли ДополнительныеПараметры <> Неопределено Тогда
		Состояние(ДополнительныеПараметры.ОписаниеДействия,, ДополнительныеПараметры.УспешныйРезультат, Логотип);
	КонецЕсли;

КонецПроцедуры

// Возвращает новую структуру с описанием параметров действия.
//
// Возвращаемое значение:
//   Структура - структура с описанием параметров действия:
//     * Действие           - Строка - имя действия.
//     * ОписаниеДействия   - Строка - описание действия.
//     * УспешныйРезультат  - Строка - описание успешного результата.
//     * РезультатСОшибками - Строка - описание результата с ошибками.
//
Функция НовоеОписаниеПараметровДействия() Экспорт

	ПараметрыДействия = Новый Структура;
	ПараметрыДействия.Вставить("Действие", "");
	ПараметрыДействия.Вставить("ОписаниеДействия", "");
	ПараметрыДействия.Вставить("УспешныйРезультат", "");
	ПараметрыДействия.Вставить("РезультатСОшибками", "");
	
	Возврат ПараметрыДействия;

КонецФункции

// Открывает форму печати этикеток торговой площадки.
//
// Параметры:
//  ПараметрыПечати - Структура - сведения о печатной форме:
//   * ОбъектыПечати - Массив из ДокументСсылка - массив ссылок выбранных объектов.
//   * Форма - ФормаКлиентскогоПриложения - форма, из которой вызвана команда печати.
//   * ДополнительныеПараметры - Структура - дополнительные параметры печати.
//       Прочие ключи структуры соответствуют колонкам таблицы КомандыПечати,
//       подробнее см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
// 
// Возвращаемое значение:
//   Неопределено
//
Функция ПечатьЭтикеток(ПараметрыПечати) Экспорт
	
	ЭтоFBO = Не ИнтеграцияСМаркетплейсомOzonВызовСервера.ЕстьЗаказыFBSВМассиве(ПараметрыПечати.ОбъектыПечати);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Объекты", ПараметрыПечати.ОбъектыПечати);
	ПараметрыОткрытия.Вставить("ЭтоFBO",  ЭтоFBO);
	
	ОткрытьФорму(
		"РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПечатьЭтикеток",
		ПараметрыОткрытия);
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ЭлектронноеВзаимодействие

// Обработчик команды сверки документа с получателем.
//
// Параметры:
//   ПараметрКоманды            - ДокументСсылка - ссылка на объект информационной базы.
//   ПараметрыВыполненияКоманды - Структура из КлючИЗначение - дополнительные параметры команды.
//
Процедура СверитьДокументСТорговойПлощадкой(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	АдресВХранилище         = Неопределено;
	УникальныйИдентификатор = Новый УникальныйИдентификатор;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СсылкаНаДокумент", ПараметрКоманды);
	ДополнительныеПараметры.Вставить("Владелец",         ПараметрыВыполненияКоманды.Источник);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("СверитьДокументСТорговойПлощадкойОбработатьРезультатПомещенияФайла", 
		ЭтотОбъект, 
		ДополнительныеПараметры);
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.Заголовок          = НСтр("ru = 'Выберите файл';
												|en = 'Select file'");
	ПараметрыДиалога.МножественныйВыбор = Ложь;
	ПараметрыДиалога.Фильтр             = НСтр("ru = 'XML файл';
												|en = 'XML file'") + " (*.xml)|*.xml|" 
											+ НСтр("ru = 'Книга Excel 97';
													|en = 'Excel 97 workbook'") + " (*.xls)|*.xls|"
											+ НСтр("ru = 'Книга Excel 2007';
													|en = 'Excel 2007 workbook'") + " (*.xlsx)|*.xlsx|"
											+ СтрШаблон(НСтр("ru = 'Электронная таблица %1';
															|en = '%1 spreadsheet'"), "OpenDocument") + " (*.ods)|*.ods|"
											+ НСтр("ru = 'Все файлы';
													|en = 'All files'") + " (*.*)|*.*";
	
	НачатьПомещениеФайлаНаСервер(ОповещениеОЗавершении,,, АдресВХранилище, ПараметрыДиалога, УникальныйИдентификатор);
	
КонецПроцедуры

// См. СопоставлениеНоменклатурыКонтрагентовКлиентПереопределяемый.ПриСозданииНоменклатурыПоДаннымКонтрагента
Процедура ПриСозданииНоменклатурыПоДаннымКонтрагента(Знач НаборНоменклатурыКонтрагентов, Знач ОповещениеОЗавершении,
			Знач Контекст, СтандартнаяОбработка = Истина) Экспорт
	
	Если Не СтандартнаяОбработка Тогда
		Возврат;
		
	ИначеЕсли ТипЗнч(Контекст.ДополнительныеПараметрыПоиска) <> Тип("Структура") Тогда
		Возврат;
		
	ИначеЕсли НаборНоменклатурыКонтрагентов.Количество() = 0 Тогда
		Возврат;
		
	ИначеЕсли НаборНоменклатурыКонтрагентов.Количество() = 1 Тогда
		ОткрытьФормуНоменклатуры(
			НаборНоменклатурыКонтрагентов,
			ОповещениеОЗавершении,
			Контекст,
			СтандартнаяОбработка);
		
	Иначе
		ОткрытьФормуЗаполненияРеквизитовНовойНоменклатуры(
			НаборНоменклатурыКонтрагентов,
			ОповещениеОЗавершении,
			Контекст,
			СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СопоставлениеНоменклатуры

// Выполняет действие команды формы подсистемы "Электронное взаимодействие".
//
// Параметры:
//   Контекст - Структура из КлючИЗначение - контекст выполнения метода.
//   Команда  - КомандаФормы - см. описание параметра действия команды формы.
//
Процедура ВыполнитьКомандуФормыСопоставленияНоменклатуры(Контекст, Команда) Экспорт
	
	Если Контекст.Назначение = "СопоставлениеНоменклатуры" 
			И ТипЗнч(Контекст.Форма.ДополнительныеПараметрыПоиска) = Тип("Структура")
			И Контекст.Форма.ДополнительныеПараметрыПоиска.Свойство("СпособОбработки")
			И Контекст.Форма.ДополнительныеПараметрыПоиска.СпособОбработки = "СверкаДокументаСТорговойПлощадкой" Тогда
		Префикс = Контекст.Форма.ДополнительныеПараметрыПоиска.СпособОбработки + "_";
		
		Если Команда.Имя = Префикс + "Назад" Тогда
			Контекст.Форма.ТолькоПросмотр = Истина;
			Контекст.Форма.Закрыть("Назад");
			
		ИначеЕсли Команда.Имя = Префикс + "Далее" Тогда
			ОтборНеСопоставленных  = Новый Структура("Сопоставлено", Ложь);
			НеСопоставленныеСтроки = Контекст.Форма.Объект.Сопоставление.НайтиСтроки(ОтборНеСопоставленных);
			
			Если ЗначениеЗаполнено(НеСопоставленныеСтроки) Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Переход к следующему шагу помощника возможен после сопоставления всех позиций номенклатуры.';
												|en = 'You can go to the next step of the wizard after mapping all the items.'"));
				
			Иначе
				ОповещениеОЗавершении = Новый ОписаниеОповещения("СохранитьСопоставлениеНоменклатуры", 
					ЭтотОбъект, Контекст);
				
				Отбор = Новый Структура;
				Отбор.Вставить("Сопоставлено", Истина);
				Отбор.Вставить("Сохранено"   , Ложь);
				Отбор.Вставить("ВариантУказанияНоменклатуры", 
					СопоставлениеНоменклатурыКонтрагентовКлиентСервер.ВариантУказанияНоменклатураКонтрагента());
				
				НайденныеСтроки = Контекст.Форма.Объект.Сопоставление.НайтиСтроки(Отбор);
				Если НайденныеСтроки.Количество() > 0 Тогда
					ТекстВопроса = НСтр("ru = 'Имеются данные, требующие сохранения. Сохранить изменения и перейти к следующему шагу помощника?';
										|en = 'There is data to save. Save the changes and go to the next step of the wizard?'");
					ПоказатьВопрос(ОповещениеОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
				Иначе
					ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, КодВозвратаДиалога.Да);
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли Команда.Имя = Префикс + "Закрыть" Тогда
			Оповестить(Префикс + "Закрыть");
			
		ИначеЕсли Команда.Имя = Префикс + "ПоказатьПодсказку" Тогда
			ВидимостьПодсказки = Истина;
			Контекст.Форма.Элементы[Префикс + "ПоказатьПодсказку"].Видимость = Не ВидимостьПодсказки;
			Контекст.Форма.Элементы[Префикс + "ГруппаИнформация"].Видимость  = ВидимостьПодсказки;
			
			Оповестить(Префикс + "ПоказатьСкрытьПодсказку", ВидимостьПодсказки);
			
		ИначеЕсли Команда.Имя = Префикс + "СкрытьПодсказку" Тогда
			ВидимостьПодсказки = Ложь;
			Контекст.Форма.Элементы[Префикс + "ПоказатьПодсказку"].Видимость = Не ВидимостьПодсказки;
			Контекст.Форма.Элементы[Префикс + "ГруппаИнформация"].Видимость  = ВидимостьПодсказки;
			
			Оповестить(Префикс + "ПоказатьСкрытьПодсказку", ВидимостьПодсказки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура СохранитьСопоставлениеНоменклатуры(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ДополнительныеПараметры.Форма.ОбработатьВопросОСохраненииПередЗакрытием(КодВозвратаДиалога.Да, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

// Инициирует смену владельца товарного каталога.
//
// Параметры:
//   Форма                - Неопределено, ФормаКлиентскогоПриложения - форма, из которой вызывается длительная операция.
//   Параметры            - см. ИнтеграцияСМаркетплейсамиКлиентСервер.ПараметрыИзмененияВладельцаТоварногоКаталога.
//   БлокируемыеПоляФормы - Неопределено, Массив Из ПолеФормы - блокируемые поля формы.
//
Процедура ИзменитьВладельцаТоварногоКаталога(Форма, Параметры, БлокируемыеПоляФормы = Неопределено) Экспорт

	ОповеститьФормы = Не ПустаяСтрока(Параметры.ИмяСобытияОповещения);
	
	Если ЗначениеЗаполнено(Параметры.НовыйВладелец)
			И Параметры.ТекущийВладелец <> Параметры.НовыйВладелец Тогда
		ЕстьНоменклатураДляПереноса =
			ИнтеграцияСМаркетплейсамиВызовСервера.ПроверкаНаличияНоменклатурыПартнера(Параметры);
		
		Если ЕстьНоменклатураДляПереноса Тогда
			ОповеститьФормы = Ложь;
			
			Если БлокируемыеПоляФормы = Неопределено Тогда
				БлокируемыеПоляФормы = Новый Массив;
			КонецЕсли;
			
			Для Каждого ПолеФормы Из БлокируемыеПоляФормы Цикл
				ПолеФормы.ТолькоПросмотр = Истина;
			КонецЦикла;
			
			ДлительнаяОперация =
				ИнтеграцияСМаркетплейсамиВызовСервера.ПеренестиНоменклатуруПартнера(Параметры);
			
			ПараметрыОповещенияОЗавершении = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Параметры);
			ПараметрыОповещенияОЗавершении.Вставить("БлокируемыеПоляФормы", БлокируемыеПоляФормы);
			
			ОповещениеОЗавершении = Новый ОписаниеОповещения(
				"ИзменитьВладельцаТоварногоКаталогаЗавершениеФоновогоЗадания",
				ЭтотОбъект,
				ПараметрыОповещенияОЗавершении);
			
			Если ДлительнаяОперация.Статус = "Выполнено" Тогда
				ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, ДлительнаяОперация);
			Иначе
				ТекстОповещенияПользователя = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1. Обновление данных';
						|en = '%1. Data update'"),
					ИнтеграцияСМаркетплейсамиКлиентСервер.ПредставлениеВидаТорговойПлощадки(Параметры.ВидТорговойПлощадки));
				
				Если Не Параметры.ВыводитьОкноОжидания Тогда
					ПоказатьОповещениеПользователя(
						ТекстОповещенияПользователя,
						,
						НСтр("ru = 'Запущено обновление данных позиций товарного каталога.';
							|en = 'Updating data on the goods directory items is started.'"),
						Параметры.Логотип);
				КонецЕсли;
				
				ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
				ПараметрыОжидания.ВыводитьОкноОжидания              = Параметры.ВыводитьОкноОжидания;
				ПараметрыОжидания.ТекстСообщения                    = НСтр("ru = 'Обновление данных позиций товарного каталога.';
																			|en = 'Update data on the goods directory items.'");
				ПараметрыОжидания.ОповещениеПользователя.Показать   = Истина;
				ПараметрыОжидания.ОповещениеПользователя.Текст      = ТекстОповещенияПользователя;
				ПараметрыОжидания.ОповещениеПользователя.Пояснение  = НСтр("ru = 'Завершено обновление данных позиций товарного каталога.';
																			|en = 'Data on the goods directory items is updated.'");
				ПараметрыОжидания.ОповещениеПользователя.Картинка   = Параметры.Логотип;
				ПараметрыОжидания.ОтменятьПриЗакрытииФормыВладельца = Ложь;
				
				ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
			КонецЕсли;
		ИначеЕсли Не ПустаяСтрока(Параметры.ИмяСобытияОповещения) Тогда
			Оповестить(Параметры.ИмяСобытияОповещения, Параметры.УчетнаяЗапись);
		КонецЕсли;
	КонецЕсли;
	
	Если ОповеститьФормы Тогда
		Оповестить(Параметры.ИмяСобытияОповещения, Параметры.УчетнаяЗапись);
	КонецЕсли;

КонецПроцедуры

// Открывает форму сопоставления номенклатуры контрагента.
//
// Параметры:
//   Форма     - ФормаКлиентскогоПриложения - форма, из которой вызывается сопоставление.
//   ИмяСписка - Строка - имя элемента динамического списка.
//
Процедура ОткрытьСопоставлениеНоменклатуры(Форма, ИмяСписка = "Список") Экспорт

	Ссылки = Новый Массив;
	Для Каждого СтрокаСписка Из Форма.Элементы[ИмяСписка].ВыделенныеСтроки Цикл
		ДанныеСтроки = Форма.Элементы[ИмяСписка].ДанныеСтроки(СтрокаСписка);
		Ссылки.Добавить(ДанныеСтроки.НоменклатураПартнера);
	КонецЦикла;
	
	НоменклатураДляСопоставления =
		ИнтеграцияСМаркетплейсамиВызовСервера.НоменклатураКонтрагентовПоСсылкам(Ссылки);
	
	Если Не ЗначениеЗаполнено(НоменклатураДляСопоставления) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Сопоставление номенклатуры не требуется.';
										|en = 'Products mapping is not required.'"));
		Возврат;
	КонецЕсли;
	
	Настройки = Новый Структура;
	Настройки.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Настройки.Вставить("ВладелецФормы",     Форма);
	
	ДополнительныеПараметры = НовыеПараметрыПоискаНоменклатурыПартнера();
	ДополнительныеПараметры.СпособОбработки =
		ИнтеграцияСМаркетплейсамиКлиентСервер.СпособОбработкиСопоставленияНоменклатурыПоискВариантов();
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "УчетнаяЗаписьМаркетплейса") Тогда
		УчетнаяЗапись = Форма.УчетнаяЗаписьМаркетплейса;
		
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "УчетнаяЗапись") Тогда
		УчетнаяЗапись = Форма.УчетнаяЗапись;
		
	Иначе
		УчетнаяЗапись = ПредопределенноеЗначение("Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка");
	КонецЕсли;
	
	ДополнительныеПараметры.УчетнаяЗапись = УчетнаяЗапись;
	
	Настройки.Вставить("ДополнительныеПараметрыПоиска", ДополнительныеПараметры);
	
	ИмяПроцедурыОповещения = "ОбработатьСопоставлениеНоменклатуры";
	ОписаниеОповещения = Новый ОписаниеОповещения(ИмяПроцедурыОповещения, Форма);
	
	СопоставлениеНоменклатурыКонтрагентовКлиент.ОткрытьСопоставлениеНоменклатуры(
		НоменклатураДляСопоставления,
		Настройки,
		ОписаниеОповещения);

КонецПроцедуры

#КонецОбласти

#Область ИмпортТоваровИзСервиса

// Инициирует получение данных товарного каталога из сервиса.
//
// Параметры:
//   Форма     - Неопределено, ФормаКлиентскогоПриложения - форма, из которой вызывается длительная операция.
//   Параметры - см. ИнтеграцияСМаркетплейсамиКлиентСервер.ПараметрыПолученияТоваровИзСервиса.
//   ПроверитьЗапущенноеЗадание - Булево - признак необходимости проверки наличия запущенного задания по
//                                импорту товарного каталога.
//
Процедура ВыполнитьПолучениеТоваровИзСервиса(Форма, Параметры, ПроверитьЗапущенноеЗадание = Ложь) Экспорт
	
	ДлительнаяОперация = ИнтеграцияСМаркетплейсамиВызовСервера.ПолучитьТоварыИзСервиса(
		Параметры, Истина, ПроверитьЗапущенноеЗадание);
	
	ПараметрыОповещения = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Параметры.ПараметрыМетода);
	ПараметрыОповещения.Вставить("КлючЗадания", Параметры.КлючЗадания);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ПолучениеТоваровИзСервисаЗавершениеФоновогоЗадания",
		ЭтотОбъект,
		ПараметрыОповещения);
		
	Оповестить(
		ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяСобытияНачалаИмпортаТоварногоКаталога(),
		Параметры.ПараметрыМетода.УчетнаяЗапись,
		Параметры.КлючЗадания);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания             = Параметры.ВыводитьОкноОжидания;
	ПараметрыОжидания.ТекстСообщения                   =
		НСтр("ru = 'Загрузка данных товарного каталога с торговой площадки.';
			|en = 'Import goods directory data from the trading platform.'");
	ПараметрыОжидания.ОповещениеПользователя.Показать  = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст     =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Загрузка данных товарного каталога';
				|en = '%1. Import goods directory data'"),
			ИнтеграцияСМаркетплейсамиКлиентСервер.ПредставлениеВидаТорговойПлощадки(Параметры.ПараметрыМетода.ВидТорговойПлощадки));
	ПараметрыОжидания.ОповещениеПользователя.Пояснение =
		НСтр("ru = 'Завершена загрузка данных товарного каталога с торговой площадки.';
			|en = 'Goods directory data is imported from the trading platform.'");
	ПараметрыОжидания.ОповещениеПользователя.Картинка  = Параметры.ПараметрыМетода.Логотип;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФорм

// Переопределяемая процедура, вызываемая из одноименного обработчика события элемента.
//
// Параметры:
//   Форма                   - ФормаКлиентскогоПриложения - форма, из которой происходит вызов процедуры.
//   Элемент                 - ГруппаФормы, ГруппаФормы, ДекорацияФормы, КнопкаФормы, ПолеФормы, ТаблицаФормы -
//                               источник события "При изменении".
//   ДополнительныеПараметры - Структура        - значения дополнительных параметров влияющих на обработку.
//
Процедура ОбработкаИзмененияПолей(Форма, Элемент = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт

	Если Форма.ИмяФормы = "Справочник.Номенклатура.Форма.ФормаЭлемента" Тогда
		ВходящиеПараметры = Форма.ПараметрыСоздания;
		
		Если ТипЗнч(ВходящиеПараметры) <> Тип("ФиксированнаяСтруктура")
				Или Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ВходящиеПараметры, "УчетнаяЗапись") Тогда
			Возврат;
		КонецЕсли;
		
		ИмяЭлемента = Элемент.Имя;
		
		Если ИмяЭлемента = "ВидНоменклатуры"
				Или ИмяЭлемента = "ВидНоменклатурыПереключатель"
				Или ИмяЭлемента = "ВидНоменклатурыОбязательныеПоля"
				Или ИмяЭлемента = "ВидНоменклатурыПереключательОбязательныеПоля"
				Или ИмяЭлемента = "Родитель"
				Или ИмяЭлемента = "ТоварнаяКатегория" Тогда
			
			НоменклатураКонтрагента = ПредопределенноеЗначение("Справочник.НоменклатураКонтрагентов.ПустаяСсылка");
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ВходящиеПараметры, "СопоставлениеНоменклатурыКонтрагентов")
					И ЗначениеЗаполнено(ВходящиеПараметры.СопоставлениеНоменклатурыКонтрагентов) Тогда
				НоменклатураКонтрагента = ВходящиеПараметры.СопоставлениеНоменклатурыКонтрагентов.НоменклатураКонтрагента;
			КонецЕсли;
			
			ИнтеграцияСМаркетплейсамиКлиентСервер.ЗаполнитьРеквизитыНоменклатурыПоКатегорииСервиса(
				ВходящиеПараметры.УчетнаяЗапись,
				Форма,
				НоменклатураКонтрагента,
				Истина);
			
			УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(Форма);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

// Копирует выделенный текст в буфер обмена.
//
// Параметры:
//   Текст - Строка - выделенный текст.
//
// Возвращаемое значение:
//   Булево - результат выполнения обещания.
//
Асинх Функция СкопироватьВБуферОбмена(Текст) Экспорт

	Если СредстваБуфераОбмена.ИспользованиеДоступно() Тогда
		ФорматДанных = СтандартныйФорматДанныхБуфераОбмена.Текст;
		
		Если Ждать СредстваБуфераОбмена.ПоддерживаетсяФорматДанных(ФорматДанных) Тогда
			ПомещаемыеДанные = Новый ЭлементБуфераОбмена(ФорматДанных, Текст);
			Возврат Ждать СредстваБуфераОбмена.ПоместитьДанныеАсинх(ПомещаемыеДанные);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

// Открывает форму для заполнения данных экземпляров товаров заказа.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//   * ДокументЗаказа - ДокументСсылка.ЗаказКлиента
//   * НомерЗаказа - Строка
//   * ТаблицаЗаказов - ДанныеФормыКоллекция
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//
Процедура ОткрытьФормуЗаполненияДанныхЭкземпляров(Форма, УчетнаяЗапись) Экспорт
	
	Если Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПросмотрИЗагрузкаЗаказов" Тогда
		ИдентификаторыВыделенныхСтрок = Форма.Элементы.ТаблицаЗаказов.ВыделенныеСтроки;
		ДанныеЗаказа = ДанныеЗаказаИзВыделенныхСтрок(ИдентификаторыВыделенныхСтрок, Форма.ТаблицаЗаказов);
		
		Заказ = ДанныеЗаказа.Заказ;
		НомерЗаказа = ДанныеЗаказа.НомерЗаказа;
		
	ИначеЕсли Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.СоставЗаказа" Тогда
		Заказ = Форма.ДокументЗаказа;
		НомерЗаказа = Форма.НомерЗаказа;
		
	Иначе
		КлючиВыделенныхСтрок = Форма.Элементы.Список.ВыделенныеСтроки;
		ДанныеЗаказа = ДанныеЗаказаИзВыделенныхСтрок(КлючиВыделенныхСтрок);
		
		Заказ = ДанныеЗаказа.Заказ;
		НомерЗаказа = ДанныеЗаказа.НомерЗаказа;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Заказ) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	ПараметрыФормы.Вставить("Заказ",         Заказ);
	ПараметрыФормы.Вставить("НомерЗаказа",   НомерЗаказа);
	
	ОткрытьФорму(
		"РегистрСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.Форма.ЗаполнениеДанныхЭкземпляров",
		ПараметрыФормы,
		ЭтотОбъект,
		Заказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// См. СтандартныеПодсистемыКлиент.ПриПолученииСерверногоОповещения
Процедура ПриПолученииСерверногоОповещения(ИмяОповещения, Результат) Экспорт

	Если ИмяОповещения = "ИнтеграцияСЯндексМаркетСервер.ОбновитьДанные" Тогда
		Оповестить(ИмяОповещения, Результат, "ОбщийМодуль.ИнтеграцияСМаркетплейсамиКлиент.ПриПолученииСерверногоОповещения");
	
	ИначеЕсли ИмяОповещения = "ИнтеграцияСМаркетплейсомOzonСервер.ОбновитьДанные" Тогда
		Оповестить(ИмяОповещения, Результат, "ОбщийМодуль.ИнтеграцияСМаркетплейсамиКлиент.ПриПолученииСерверногоОповещения");
	КонецЕсли;
	
КонецПроцедуры

// Конструктор параметров формы Справочник.УчетныеЗаписиМаркетплейсов.Формы.ВыгрузкаЗагрузкаДанных.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//
// Возвращаемое значение:
//   Структура - параметры для открытия формы выгрузки/загрузки данных:
//     * УчетнаяЗапись                              - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись 
//                                                      торговой площадки.
//     * ЗаголовокФормы                             - Строка - заголовок формы при открытии.
//     * ИмяОбъектаМетаданных                       - Строка - полное имя объекта метаданных. 
//                                                      Например: "Обработка.УправлениеПродажамиНаOzon".
//     * ИмяТабличнойЧасти                          - Строка - имя табличной части объекта метаданных.
//     * ИмяМакетаСШаблоном                         - Строка - имя макета объекта метаданных.
//     * ЛистыШаблона                               - Неопределено - используется одностраничный шаблон.
//                                                  - Структура - описание многостраничного шаблона:
//       ** ИмяЛиста                                  - Строка - имя листа.
//       ** ИмяМакетаСШаблоном                        - Строка - имя макета объекта метаданных.
//       ** Служебный                                 - Произвольный - дополнительная служебная информация.
//     * ЭтоЗагрузкаДанных                          - Булево - признак действия: загрузка - значение Истина, выгрузка - 
//                                                      значение Ложь.
//     * ВариантВыгрузкиЗагрузки                    - Строка - вариант загрузки по умолчанию из доступных значений:
//                                                      "ЗаполнениеТаблицы", "ВнешнийФайл", "ИнтеграционнымиМетодами".
//     * ДоступныИнтеграционныеМетоды               - Булево - признак доступности выгрузки/загрузки интеграционными методами.
//     * ПроверитьДоступностьИнтеграционныхМетодов  - Булево - признак необходимости проверки доступности
//                                                      выгрузки/загрузки интеграционными методами, если признак их
//                                                      доступности не передан.
//     * ЗагружатьНезаполненныеСтроки               - Булево - признак доступности загрузки незаполненных строк.
//     * СсылкаНаОбъект                             - Неопределено, ЛюбаяСсылка - значение, определяющее тип реквизита 
//                                                      выбора объекта выгрузки/загрузки.
//     * СсылкаНаОбъектЗаголовок                    - Строка - заголовок реквизита выбора объекта выгрузки/загрузки.
//     * СсылкаНаОбъектПараметрыВыбора              - Массив из Структура - связи параметров выбора для реквизита формы выгрузки/загрузки:
//       ** Имя                                       - Строка - имя элемента формы.
//       ** ПутьКДанным                               - Строка - путь к реквизиту формы.
//     * СсылкаНаОбъектЗаписыватьЗагружаемыйФайл    - Булево - признак записи файла в присоединенные файлы ссылки на объект.
//     * ПояснениеВариантаЗагрузкиИзМакета          - Строка - текст в форме выгрузки/загрузки при загрузке из макета.
//     * ПодсказкаВариантаЗагрузкиИзМакета          - Строка - текст в подсказке (знак вопроса) в форме выгрузки/загрузки при 
//                                                      загрузке из макета.
//     * ПояснениеВариантаВыгрузкиЗагрузкиЧерезФайл - Строка - текст в форме выгрузки/загрузки при загрузке из файла.
//     * ПодсказкаВариантаВыгрузкиЗагрузкиЧерезФайл - Строка - текст в подсказке (знак вопроса) в форме выгрузки/загрузки 
//                                                      при загрузке из файла.
//     * ПояснениеВариантаВыгрузкиЗагрузкиЧерезAPI  - Строка - текст в форме выгрузки/загрузки при загрузке интеграционными 
//                                                      методами.
//     * ПодсказкаВариантаВыгрузкиЗагрузкиЧерезAPI  - Строка - текст  в подсказке (знак вопроса) в форме выгрузки/загрузки 
//                                                      при загрузке интеграционными методами.
//     * ВидПериода                                 - ПеречислениеСсылка.ДоступныеПериодыОтчета - вид выбираемого периода 
//                                                      выгрузки/загрузки.
//     * ДатаВПериоде                               - Дата - дата, для которой нужно установить период.
//     * ИмяФормыДополнительныхНастроек             - Строка - имя формы, открываемой для заполнения дополнительных настроек.
//     * ПараметрыФормыДополнительныхНастроек       - см. ПараметрыФормыДополнительныхНастроек.
//     * ИмяФормыСопоставленияДанных                - Строка - имя формы, открываемой для сопоставления загружаемых данных.
//     * ПодсказкаОткрытияФормыСопоставленияДанных  - Строка - заголовок команды для формы, открываемой для сопоставления 
//                                                      загружаемых данных.
//     * ИмяФормыПослеЗагрузкиДанных                - Строка - имя формы, открываемой после загрузки данных.
//     * ПодсказкаОткрытияФормыПослеЗагрузкиДанных  - Строка - заголовок команды для формы, открываемой после загрузки данных.
//     * ДополнительныеПараметрыФормы               - Произвольный, Неопределено - любые дополнительные параметры, 
//                                                      которые будут переданы в форму выгрузки/загрузки.
//
Функция ПараметрыФормыВыгрузкиЗагрузки(УчетнаяЗапись) Экспорт

	ПараметрыФормы = Новый Структура;

	ПараметрыФормы.Вставить("УчетнаяЗапись", УчетнаяЗапись);

	ПараметрыФормы.Вставить("ЗаголовокФормы", "");

	ПараметрыФормы.Вставить("ИмяОбъектаМетаданных", "");
	ПараметрыФормы.Вставить("ИмяТабличнойЧасти", "");
	ПараметрыФормы.Вставить("ИмяМакетаСШаблоном", "");
	ПараметрыФормы.Вставить("ЛистыШаблона", Неопределено);

	ПараметрыФормы.Вставить("ЭтоЗагрузкаДанных", Истина);
	ПараметрыФормы.Вставить("ВариантВыгрузкиЗагрузки", "ВнешнийФайл");
	ПараметрыФормы.Вставить("ДоступныИнтеграционныеМетоды", Ложь);
	ПараметрыФормы.Вставить("ПроверитьДоступностьИнтеграционныхМетодов", Ложь);
	ПараметрыФормы.Вставить("ЗагружатьНезаполненныеСтроки", Ложь);

	ПараметрыФормы.Вставить("СсылкаНаОбъект", Неопределено);
	ПараметрыФормы.Вставить("СсылкаНаОбъектЗаголовок", НСтр("ru = 'Ссылка на объект для выгрузки / загрузки данных';
															|en = 'Link to object for data export/import'"));
	ПараметрыФормы.Вставить("СсылкаНаОбъектПараметрыВыбора", Новый Массив);		// Для хранения массива структур с ключами "Имя, ПутьКДанным".
	ПараметрыФормы.Вставить("СсылкаНаОбъектЗаписыватьЗагружаемыйФайл", Ложь);	// Устанавливать можно только объектов метаданных,
																				// для которых есть справочник присоединенных файлов.

	ПараметрыФормы.Вставить("ПояснениеВариантаЗагрузкиИзМакета",          "");
	ПараметрыФормы.Вставить("ПодсказкаВариантаЗагрузкиИзМакета",          "");
	ПараметрыФормы.Вставить("ПояснениеВариантаВыгрузкиЗагрузкиЧерезФайл", "");
	ПараметрыФормы.Вставить("ПодсказкаВариантаВыгрузкиЗагрузкиЧерезФайл", "");
	ПараметрыФормы.Вставить("ПояснениеВариантаВыгрузкиЗагрузкиЧерезAPI",  "");
	ПараметрыФормы.Вставить("ПодсказкаВариантаВыгрузкиЗагрузкиЧерезAPI",  "");

	ПараметрыФормы.Вставить("ВидПериода",   ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.ПустаяСсылка"));
	ПараметрыФормы.Вставить("ДатаВПериоде", Дата(1,1,1));

	ПараметрыФормы.Вставить("ИмяФормыСопоставленияДанных", "");
	ПараметрыФормы.Вставить("ПодсказкаОткрытияФормыСопоставленияДанных", "");

	ПараметрыФормы.Вставить("ИмяФормыДополнительныхНастроек", "");
	ПараметрыФормы.Вставить("ПараметрыФормыДополнительныхНастроек", ПараметрыФормыДополнительныхНастроек());

	ПараметрыФормы.Вставить("ИмяФормыПослеЗагрузкиДанных", "");
	ПараметрыФормы.Вставить("ПодсказкаОткрытияФормыПослеЗагрузкиДанных", "");

	ПараметрыФормы.Вставить("ДополнительныеПараметрыФормы", Неопределено);

	Возврат ПараметрыФормы;

КонецФункции

Функция ДанныеТорговойПлощадкиПустые()

	ДанныеТорговойПлощадкиПустые = Новый Структура;
	ДанныеТорговойПлощадкиПустые.Вставить("ВидМаркетплейса",                           ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.ПустаяСсылка"));
	ДанныеТорговойПлощадкиПустые.Вставить("Организация",                               ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
	ДанныеТорговойПлощадкиПустые.Вставить("Партнер",                                   ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка"));
	ДанныеТорговойПлощадкиПустые.Вставить("Контрагент",                                ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
	ДанныеТорговойПлощадкиПустые.Вставить("ПостфиксЗаголовкаФормы",                    "");
	ДанныеТорговойПлощадкиПустые.Вставить("ИдентификаторКлиента",                      "");
	ДанныеТорговойПлощадкиПустые.Вставить("УникальныйИдентификатор",                   Новый УникальныйИдентификатор());
	ДанныеТорговойПлощадкиПустые.Вставить("ДоговорПродажиЧерезСкладыТорговойПлощадки", ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка"));
	ДанныеТорговойПлощадкиПустые.Вставить("ДоговорПродажиЧерезСкладыСобственные",      ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка"));
	
	Возврат ДанныеТорговойПлощадкиПустые;

КонецФункции

Функция ПараметрыФормыДополнительныхНастроек()

	ПараметрыФормы = Новый Структура;
	// Основные.
	ПараметрыФормы.Вставить("УчетнаяЗапись",   ПредопределенноеЗначение("Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка"));
	ПараметрыФормы.Вставить("ЗаголовокФормы",  "");
	ПараметрыФормы.Вставить("ВариантНастроек", "");

	// Заполняемые в Справочник.УчетныеЗаписиМаркетплейсов.Формы.ВыгрузкаЗагрузкаДанных.
	ПараметрыФормы.Вставить("СсылкаНаОбъект",                   Неопределено);
	ПараметрыФормы.Вставить("ИмяОбъектаМетаданных",             "");
	ПараметрыФормы.Вставить("ИмяТабличнойЧасти",                "");
	ПараметрыФормы.Вставить("ИнформацияПоКолонкам",             Неопределено);
	ПараметрыФормы.Вставить("ЗначенияПараметровСсылкиНаОбъект", Новый Структура);
	
	Возврат ПараметрыФормы;

КонецФункции

Функция НовыйИсточникОповещенияВыгрузкаЗагрузкаДанных()

	ИсточникОповещения = Новый Структура;
	ИсточникОповещения.Вставить("ИдентификаторФормыОповещения", Неопределено);
	ИсточникОповещения.Вставить("ИмяМетода",                    "");

	Возврат ИсточникОповещения;

КонецФункции

// Открывает форму выгрузки/загрузки данных.
//
// Параметры:
//   ПараметрыФормы - Структура - параметры открытия формы, см. ПараметрыФормыВыгрузкиЗагрузки.
//   Уникальность   - Неопределено, Произвольный - ключ, значение которого будет использоваться для поиска уже открытых форм.
//
Процедура ОткрытьФормуВыгрузкиЗагрузкиДанных(ПараметрыФормы, Уникальность = Неопределено) Экспорт

	ОчиститьСообщения();

	ОткрытьФорму("Справочник.УчетныеЗаписиМаркетплейсов.Форма.ВыгрузкаЗагрузкаДанных",
		ПараметрыФормы,
		,
		?(Уникальность = Неопределено, Новый УникальныйИдентификатор(), Уникальность));

КонецПроцедуры

// Подготавливает параметры открытия формы сопоставления в зависимости от объекта выгрузки/загрузки данных.
//
// Параметры:
//   ТаблицаСопоставленияДанных - ТаблицаЗначений - собирается динамически в зависимости от макета выгрузки/загрузки,
//                                см. Справочник.УчетныеЗаписиМаркетплейсов.Форма.ВыгрузкаЗагрузкаДанных.ТаблицаСопоставленияДанных.
//   ВыделенныеСтроки           - Массив Из Число - идентификаторы выделенных строк ТаблицаСопоставленияДанных.
//   Параметры                  - Структура из Строка -описана в Справочник.УчетныеЗаписиМаркетплейсов.Форма.ВыгрузкаЗагрузкаДанных.ПараметрыПодготовкиКСопоставлениюДанных.
//
// Возвращаемое значение:
//   Структура - список параметров в зависимости от объекта выгрузки/загрузки данных:
//     * УчетнаяЗапись                      - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//     * ОтборПоИдентификаторуПубликации    - СписокЗначений из Строка - отбор по идентификаторам публикации.
//     * ОтборПоИдентификаторуМаркетплейса  - СписокЗначений из Строка - отбор по идентификаторам маркетплейса.
//     * ОтборПоSKU                         - СписокЗначений из Строка - отбор по идентификаторам SKU.
//     * ВРежимеИмпортаДанных               - Булево - открытие формы в режиме импорта данных.
//     * ПолеОбработчикаСобытияПриИзменении - Строка - имя поля обработчика события.
//
Функция ПодготовитьПараметрыФормыСопоставленияИзВыгрузкиЗагрузкиДанных(Знач ТаблицаСопоставленияДанных,
			Знач ВыделенныеСтроки, Параметры) Экспорт

	УчетнаяЗапись = Параметры.УчетнаяЗапись; // СправочникСсылка.УчетныеЗаписиМаркетплейсов
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗапись",                      УчетнаяЗапись);
	ПараметрыФормы.Вставить("ОтборПоИдентификаторуПубликации",    Новый СписокЗначений);
	ПараметрыФормы.Вставить("ОтборПоИдентификаторуМаркетплейса",  Новый СписокЗначений);
	ПараметрыФормы.Вставить("ОтборПоSKU",                         Новый СписокЗначений);
	ПараметрыФормы.Вставить("ВРежимеИмпортаДанных",               Истина);
	ПараметрыФормы.Вставить("ПолеОбработчикаСобытияПриИзменении", "");
	
	ДоступныеВариантыОтбора = ДоступныеВариантыОтбора(Параметры.ВидМаркетплейса);
	Если Не ЗначениеЗаполнено(ДоступныеВариантыОтбора) Тогда
		Возврат ПараметрыФормы;
	КонецЕсли;
	
	ВариантыОтбора = Новый Массив;
	
	Если Параметры.ИмяОбъектаМетаданных = "Обработка.УправлениеПродажамиНаOzon" Тогда
		ПараметрыФормы.ПолеОбработчикаСобытияПриИзменении = "ТаблицаСопоставленияДанных_" + "Номенклатура";
		
		Если Параметры.ИмяТабличнойЧасти = "ДанныеПоставок"
			Или Параметры.ИмяТабличнойЧасти = "ДанныеЗаявокНаВозврат"
			Или Параметры.ИмяТабличнойЧасти = "ДанныеОРеализованныхТоварах" Тогда
			
			Если Параметры.ИмяТабличнойЧасти = "ДанныеПоставок" Тогда
				ВариантыОтбора.Добавить(ДоступныеВариантыОтбора["ОтборПоИдентификаторуПубликации"]);
			ИначеЕсли Параметры.ИмяТабличнойЧасти = "ДанныеОРеализованныхТоварах" Тогда
				ВариантыОтбора.Добавить(ДоступныеВариантыОтбора["ОтборПоSKU"]);
				ВариантыОтбора.Добавить(ДоступныеВариантыОтбора["ОтборПоИдентификаторуПубликации"]);
			Иначе
				ВариантыОтбора.Добавить(ДоступныеВариантыОтбора["ОтборПоSKU"]);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ВариантыОтбора.Количество() = 0 Тогда
		Возврат ПараметрыФормы;
	КонецЕсли;
	
	ЕстьИдентификаторыПоиска = Ложь;
	
	Для Каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		СтрокаДанных = ТаблицаСопоставленияДанных.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если СтрокаДанных <> Неопределено Тогда
			Если СтрокаДанных.РезультатСопоставленияСтроки <> "СтрокаНеСопоставлена" Тогда
				Продолжить;
			КонецЕсли;
			
			Для Каждого ВариантОтбора Из ВариантыОтбора Цикл
				ЗначениеИдентификатора = СтрокаДанных[ВариантОтбора.ИмяИдентификатора];
				Идентификаторы = ПараметрыФормы[ВариантОтбора.ИмяОтбора];
				
				Если ПустаяСтрока(ЗначениеИдентификатора)
						Или ЗначениеИдентификатора = "<...>" Тогда
					Продолжить;
				КонецЕсли;
				
				Если Идентификаторы.НайтиПоЗначению(ЗначениеИдентификатора) <> Неопределено Тогда
					Прервать;
				КонецЕсли;
				
				Идентификаторы.Добавить(ЗначениеИдентификатора);
				ЕстьИдентификаторыПоиска = Истина;
				Прервать;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЕстьИдентификаторыПоиска Тогда
		Параметры.СообщениеПользователю = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для выбранных позиций уже выполнено сопоставление номенклатуре учетной системы 1С или не заполнено значение в колонке ""%1"".';
				|en = 'The selected items are already mapped to 1C items or the value in the ""%1"" column is not filled.'"),
			ВариантыОтбора[0].ПредставлениеИдентификатора);
	КонецЕсли;
	
	Возврат ПараметрыФормы;

КонецФункции

Функция ДоступныеВариантыОтбора(ВидТорговойПлощадки)
	
	ВариантыОтбора = Новый Соответствие;
	
	ПредставлениеКолонки = ИнтеграцияСМаркетплейсамиКлиентСервер.ПредставленияИдентификаторов(ВидТорговойПлощадки);
	
	Если ВидТорговойПлощадки = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		//
		ВариантОтбора = Новый Структура;
		ВариантОтбора.Вставить("ИмяОтбора",                   "ОтборПоИдентификаторуПубликации");
		ВариантОтбора.Вставить("ИмяИдентификатора",           "ИдентификаторПубликации");
		ВариантОтбора.Вставить("ПредставлениеИдентификатора", ПредставлениеКолонки[ВариантОтбора.ИмяИдентификатора]);
		
		ВариантыОтбора.Вставить("ОтборПоИдентификаторуПубликации", ВариантОтбора);
		
		//
		ВариантОтбора = Новый Структура;
		ВариантОтбора.Вставить("ИмяОтбора",                   "ОтборПоИдентификаторуМаркетплейса");
		ВариантОтбора.Вставить("ИмяИдентификатора",           "ИдентификаторОбъектаМаркетплейса");
		ВариантОтбора.Вставить("ПредставлениеИдентификатора", ПредставлениеКолонки[ВариантОтбора.ИмяИдентификатора]);
		
		ВариантыОтбора.Вставить("ОтборПоИдентификаторуМаркетплейса", ВариантОтбора);
		
		//
		ВариантОтбора = Новый Структура;
		ВариантОтбора.Вставить("ИмяОтбора",                   "ОтборПоSKU");
		ВариантОтбора.Вставить("ИмяИдентификатора",           "ИдентификаторSKU");
		ВариантОтбора.Вставить("ПредставлениеИдентификатора", ПредставлениеКолонки[ВариантОтбора.ИмяИдентификатора]);
		
		ВариантыОтбора.Вставить("ОтборПоSKU", ВариантОтбора);
	КонецЕсли;
	
	Возврат ВариантыОтбора;
	
КонецФункции

Функция ПолучитьФиксированныеНастройкиОткрытияСпискаДокументов(Знач НастройкиКомпоновкиДанных, ДанныеТорговойПлощадки, ВидНастройки)

	Если ЗначениеЗаполнено(ВидНастройки) Тогда
		Если НастройкиКомпоновкиДанных = Неопределено Тогда
			НастройкиКомпоновкиДанных = Новый НастройкиКомпоновкиДанных;
		КонецЕсли;

		Если ВидНастройки = "ВсеДоговоры" Тогда
			СписокЗначений = Новый СписокЗначений;
			СписокЗначений.Добавить(ДанныеТорговойПлощадки.ДоговорПродажиЧерезСкладыТорговойПлощадки);
			Если ДанныеТорговойПлощадки.Свойство("ДоговорПродажиЧерезСкладыСобственные") Тогда
				СписокЗначений.Добавить(ДанныеТорговойПлощадки.ДоговорПродажиЧерезСкладыСобственные);
			КонецЕсли;
			Если ДанныеТорговойПлощадки.Свойство("ДоговорКомиссия") Тогда
				СписокЗначений.Добавить(ДанныеТорговойПлощадки.ДоговорКомиссия);
			КонецЕсли;
			Если ДанныеТорговойПлощадки.Свойство("ДоговорРеализация") Тогда
				СписокЗначений.Добавить(ДанныеТорговойПлощадки.ДоговорРеализация);
			КонецЕсли;

			ЭлементОтбора = НастройкиКомпоновкиДанных.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.Использование  = Истина;
			ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Договор");
			ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
			ЭлементОтбора.ПравоеЗначение = СписокЗначений;
		КонецЕсли;

		Если ВидНастройки = "НастройкиДляТТН" Тогда
			ГруппаОтбора = НастройкиКомпоновкиДанных.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

			ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.Использование  = Истина;
			ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Грузополучатель");
			ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбора.ПравоеЗначение = ДанныеТорговойПлощадки.Контрагент;

			ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.Использование  = Истина;
			ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Грузоотправитель");
			ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбора.ПравоеЗначение = ДанныеТорговойПлощадки.Контрагент;
		КонецЕсли;

		Если ВидНастройки = "Организация" Тогда
			СписокЗначений = Новый СписокЗначений;
			СписокЗначений.Добавить(ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
			СписокЗначений.Добавить(ДанныеТорговойПлощадки.Организация);

			ЭлементОтбора = НастройкиКомпоновкиДанных.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.Использование  = Истина;
			ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Организация");
			ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
			ЭлементОтбора.ПравоеЗначение = СписокЗначений;
		КонецЕсли;

		Если ВидНастройки = "Взаиморасчеты" Тогда
			ГруппаОтбораИли = НастройкиКомпоновкиДанных.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаОтбораИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

			ЭлементОтбора = ГруппаОтбораИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.Использование  = Истина;
			ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("КонтрагентДебитор");
			ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбора.ПравоеЗначение = ДанныеТорговойПлощадки.Контрагент;

			ЭлементОтбора = ГруппаОтбораИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.Использование  = Истина;
			ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("КонтрагентКредитор");
			ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбора.ПравоеЗначение = ДанныеТорговойПлощадки.Контрагент;
		КонецЕсли;

		Если ВидНастройки = "ХозОперацииПоставок" И ЗначениеЗаполнено(ДанныеТорговойПлощадки.ХозОперацииПоставок) Тогда
			ХозОперацииПоставок =
				ИнтеграцияСМаркетплейсомOzonВызовСервера.ХозОперацииРаздела(ДанныеТорговойПлощадки.ХозОперацииПоставок);
			СписокЗначений = Новый СписокЗначений;
			СписокЗначений.ЗагрузитьЗначения(ХозОперацииПоставок);

			ЭлементОтбора = НастройкиКомпоновкиДанных.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.Использование  = Истина;
			ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ХозяйственнаяОперация");
			ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
			ЭлементОтбора.ПравоеЗначение = СписокЗначений;
		КонецЕсли;

		Если ВидНастройки = "ХозОперацииПродаж" И ЗначениеЗаполнено(ДанныеТорговойПлощадки.ХозОперацииПродаж) Тогда
			ХозОперацииПродаж =
				ИнтеграцияСМаркетплейсомOzonВызовСервера.ХозОперацииРаздела(ДанныеТорговойПлощадки.ХозОперацииПродаж);
			СписокЗначений = Новый СписокЗначений;
			СписокЗначений.ЗагрузитьЗначения(ХозОперацииПродаж);

			ЭлементОтбора = НастройкиКомпоновкиДанных.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.Использование  = Истина;
			ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ХозяйственнаяОперация");
			ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
			ЭлементОтбора.ПравоеЗначение = СписокЗначений;
		КонецЕсли;

		Если ВидНастройки = "ТипыРаспоряженийПродаж" Тогда
			Типы = Новый СписокЗначений;
			Типы.Добавить(Тип("ДокументСсылка.ЗаказКлиента"));
			Типы.Добавить(Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента"));
			Типы.Добавить(Тип("ДокументСсылка.ВозвратТоваровОтКлиента"));

			ЭлементОтбора = НастройкиКомпоновкиДанных.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.Использование  = Истина;
			ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ТипРаспоряжения");
			ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
			ЭлементОтбора.ПравоеЗначение = Типы;
		КонецЕсли;
	Иначе
		НастройкиКомпоновкиДанных = Неопределено;
	КонецЕсли;

	Возврат НастройкиКомпоновкиДанных;

КонецФункции

// Возвращаемое значение:
//  Структура - Новые параметры поиска номенклатуры партнера:
// * СпособОбработки - Строка - 
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - 
//
Функция НовыеПараметрыПоискаНоменклатурыПартнера()
	
	Результат = Новый Структура;
	
	Результат.Вставить("СпособОбработки", "");
	Результат.Вставить(
		"УчетнаяЗапись", ПредопределенноеЗначение("Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка"));
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеЗаказаИзВыделенныхСтрок(ВыделенныеСтроки, Таблица = Неопределено)
	
	Результат = Новый Структура;
	Результат.Вставить("Заказ", ПредопределенноеЗначение("Документ.ЗаказКлиента.ПустаяСсылка"));
	Результат.Вставить("НомерЗаказа", "");
	
	ЗагруженныеЗаказы = Новый Соответствие;
	НезагруженныеЗаказы = Новый Соответствие;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл // Число, КлючСтрокиДинамическогоСписка, РегистрСведенийКлючЗаписи.ЗаказыТорговыхПлощадок
		
		ДанныеСтроки = ДанныеОтправленияИзВыделеннойСтрокиТаблицы(ВыделеннаяСтрока, Таблица);
		
		Если ДанныеСтроки = Неопределено Или ПустаяСтрока(ДанныеСтроки.НомерЗаказа) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеСтроки.Заказ) Тогда
			НезагруженныеЗаказы.Вставить(ДанныеСтроки.НомерЗаказа);
		Иначе
			ЗагруженныеЗаказы.Вставить(ДанныеСтроки.НомерЗаказа, ДанныеСтроки.Заказ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ЗагруженныйЗаказ Из ЗагруженныеЗаказы Цикл
		Если НезагруженныеЗаказы[ЗагруженныйЗаказ.Ключ] <> Неопределено Тогда
			НезагруженныеЗаказы.Удалить(ЗагруженныйЗаказ.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Если ЗагруженныеЗаказы.Количество() > 1 Тогда
		ТекстСообщения = НСтр("ru = 'Не предусмотрено заполнение сведений по товарам для нескольких заказов.';
								|en = 'Cannot fill goods information for several orders.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат Результат;
	КонецЕсли;
	
	ТекстСообщения = "";
	Если НезагруженныеЗаказы.Количество() > 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не предусмотрено заполнение сведений по товарам для незагруженного заказа.';
								|en = 'Cannot fill goods information for a non-imported order.'");
	ИначеЕсли ЗагруженныеЗаказы.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Нет заказов для заполнения сведений по товарам.';
								|en = 'There are no orders to fill in goods information.'");
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекстСообщения) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если ЗагруженныеЗаказы.Количество() = 1 Тогда
		Для Каждого КлючЗначение Из ЗагруженныеЗаказы Цикл
			Результат.Заказ = КлючЗначение.Значение;
			Результат.НомерЗаказа = КлючЗначение.Ключ;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#Область ВыгрузкаЗагрузкаДанныхСлужебныеПроцедурыИФункции

#Область ВыборСкладовИСерий

Процедура ЗаполнитьСкладВВыделенныхСтроках(Форма, ИмяКоманды, ЗначенияПараметров)

	ВыделенныеСтроки = Форма.Элементы.ТаблицаСопоставленияДанных.ВыделенныеСтроки;

	ЗаполнятьСклад = СкладыКлиент.ПроверитьВозможностьЗаполненияСкладовВТабличнойЧасти(
		Форма,
		Форма.ТаблицаСопоставленияДанных,
		НСтр("ru = 'Таблица загружаемых данных';
			|en = 'Table of data to import'"),
		ВыделенныеСтроки);
	Если ЗаполнятьСклад Тогда
		МассивОтбора = Новый Массив();
		МассивОтбора.Добавить(ПредопределенноеЗначение("Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах"));
		МассивОтбора.Добавить(ПредопределенноеЗначение("Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказахИНакладных"));

		СтруктураОтбора = Новый Структура("ВыборГруппыСкладов, ДокументПродажи, ЭтоГруппа", МассивОтбора, Истина, Ложь);
		СтруктураПараметров = Новый Структура("Отбор", СтруктураОтбора);

		ПустойСклад = ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка");
		ДанныеФормы = Новый Структура("Склад, СкладГруппа, ГруппаСкладов", ПустойСклад, Ложь, ПустойСклад);
		ЗаполнитьЗначенияСвойств(ДанныеФормы, Форма);

		Если ДанныеФормы.СкладГруппа Тогда
			СтруктураПараметров.Вставить("ГруппаСкладов", ДанныеФормы.Склад);
		ИначеЕсли ЗначениеЗаполнено(ДанныеФормы.ГруппаСкладов) Тогда
			СтруктураПараметров.Вставить("ГруппаСкладов", ДанныеФормы.ГруппаСкладов);
		Иначе
			СтруктураПараметров.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы);
		КонецЕсли;

		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ИдентификаторФормы", Форма.УникальныйИдентификатор);
		ПараметрыОповещения.Вставить("ВыделенныеСтроки",   ВыделенныеСтроки);
		Оповещение = Новый ОписаниеОповещения("ЗаполнитьСкладВВыделенныхСтрокахЗавершение",
			ЭтотОбъект,
			ПараметрыОповещения);

		ОткрытьФорму(
			"Справочник.Склады.ФормаВыбора",
			СтруктураПараметров,
			Форма,
			,
			,
			,
			Оповещение,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;

КонецПроцедуры

// Обработчик оповещения.
//
// Параметры:
//   Результат               - СправочникСсылка.Склады - выбранный склад.
//   ДополнительныеПараметры - Произвольный - дополнительные параметры обработки выбора.
//
Процедура ЗаполнитьСкладВВыделенныхСтрокахЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Параметры = Новый Структура;
	Параметры.Вставить("ОбновитьСтатистикуСопоставления", Истина);
	Параметры.Вставить("ВыделенныеСтроки", ДополнительныеПараметры.ВыделенныеСтроки);
	Параметры.Вставить("ВыбранныйСклад", Результат);

	ИсточникОповещения = НовыйИсточникОповещенияВыгрузкаЗагрузкаДанных();
	ИсточникОповещения.ИдентификаторФормыОповещения = ДополнительныеПараметры.ИдентификаторФормы;
	ИсточникОповещения.ИмяМетода                    = "ЗаполнитьСкладыВВыделенныхСтроках";

	Оповестить(
		"ВыгрузкаЗагрузкаДанных_ОбработатьОповещениеНаСервере",
		Параметры,
		ИсточникОповещения);

КонецПроцедуры

Процедура ОткрытьПодборСкладовИСерий(Форма, СтандартнаяОбработка, Знач Параметры)

	ПараметрыФормы = ПараметрыФормыЗапросаКоличестваИСерий(Форма, Параметры);
	Если ПараметрыФормы <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму(
			"Обработка.ЗапросКоличестваИСерий.Форма",
			ПараметрыФормы,
			Форма,
			,
			,
			,
			Новый ОписаниеОповещения("ОбработатьВыборСкладаИСерии", ЭтотОбъект, ПараметрыФормы));
	КонецЕсли;

КонецПроцедуры

// Подготавливает параметры открытия формы запроса количества и серий.
//
// Параметры:
//   Форма          - ФормаКлиентскогоПриложения  - форма, для которой подготавливаются параметры.
//   ПараметрыФормы - см. ОбеспечениеВДокументахКлиентСервер.ПараметрыФормыЗапросаКоличестваИСерий.
//
// Возвращаемое значение:
//   - Неопределено - если не пройдены проверки заполнения.
//   - Структура Из КлючИЗначение.
//
Функция ПараметрыФормыЗапросаКоличестваИСерий(Форма, Знач ПараметрыФормы = Неопределено)

	Если ТипЗнч(ПараметрыФормы) = Тип("Структура") Тогда
		Режим = ПараметрыФормы.Режим;
	Иначе
		Режим = ОбеспечениеВДокументахКлиентСервер.РежимПодборСкладов();
	КонецЕсли;

	ПараметрыПроверки = ОбеспечениеКлиентСервер.ИнициализироватьПараметрыПроверкиЗаполнения();

	Если Не ОбеспечениеКлиент.ПроверитьЗаполнение(
				Форма,
				Форма.ТаблицаСопоставленияДанных,
				Форма.Элементы.ТаблицаСопоставленияДанных.ТекущаяСтрока,
				ПараметрыПроверки,
				Форма.Склад,
				Режим) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Если ТипЗнч(ПараметрыФормы) <> Тип("Структура") Тогда
		ПараметрыФормы = Новый Структура;
	КонецЕсли;

	ПараметрыФормы.Вставить("ИдентификаторФормы", Форма.УникальныйИдентификатор);

	Возврат ПараметрыФормы;

КонецФункции

// Обработчик оповещения.
//
// Параметры:
//   ВыбранноеЗначение - Произвольный - выбранное значение.
//   ПараметрыФормы    - Произвольный - дополнительные параметры обработки выбора.
//
Процедура ОбработатьВыборСкладаИСерии(ВыбранноеЗначение, Знач ПараметрыФормы) Экспорт

	Если Не ОбеспечениеВДокументахКлиент.ЕстьПодобранныеТовары(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;

	ОбеспечениеВДокументахКлиент.ПоказатьВопросСнятьФлагОтгружатьОднойДатойПослеВыбораОбеспечения(
		ЭтотОбъект,
		ВыбранноеЗначение,
		ПараметрыФормы,
		"ОбработатьВыборСкладаИСерииПослеВопроса");

КонецПроцедуры

// Обработчик оповещения.
//
// Параметры:
//   Результат - Произвольный - выбранное значение.
//   Параметры - Произвольный - дополнительные параметры обработки выбора.
//
Процедура ОбработатьВыборСкладаИСерииПослеВопроса(Результат, Знач Параметры) Экспорт

	Параметры.Вставить("ОбновитьСтатистикуСопоставления", Истина);
	ПараметрыЗаполнения =
		ОбеспечениеВДокументахКлиент.ПараметрыОбработкиДатОтгрузкиПослеЗаполненияОбеспечения(Результат);
	Параметры.Вставить("ПараметрыЗаполнения", ПараметрыЗаполнения);

	ИсточникОповещения = НовыйИсточникОповещенияВыгрузкаЗагрузкаДанных();
	ИсточникОповещения.ИмяМетода                        = "ОбработатьВыборСкладаИСерии";
	Если Параметры.Свойство("ПараметрыФормы") И Параметры.ПараметрыФормы.Свойство("ИдентификаторФормы") Тогда
		ИсточникОповещения.ИдентификаторФормыОповещения = Параметры.ПараметрыФормы.ИдентификаторФормы;
	КонецЕсли;

	Оповестить(
		"ВыгрузкаЗагрузкаДанных_ОбработатьОповещениеНаСервере",
		Параметры,
		ИсточникОповещения);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Отправления

// Процедура заполнения сведений по товарам и распределение по отправлениям на основании документов.
//
// Параметры:
//   Результат - Произвольный -
//   ДополнительныеПараметры - Структура:
//     * МассивСсылок - Массив Из ДокументСсылка.ЗаказКлиента - основание.
//     * Форма        - ФормаКлиентскогоПриложения - форма-владелец.
//
Процедура ЗаполнитьИРаспределитьВызов(Результат, ДополнительныеПараметры) Экспорт
	
	ОчиститьСообщения();
	
	МассивСсылок = Новый Массив();
	Если Не ДополнительныеПараметры.Свойство("МассивСсылок", МассивСсылок) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не выбрано ни одного документа, для которого возможно заполнение сведений по товарам.';
				|en = 'No document for which it is possible to fill in goods information is selected.'"));
		Возврат;
	КонецЕсли; 
	
	Ссылка       = МассивСсылок[0];
	ДанныеЗаказа = Неопределено;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		ДанныеЗаказа = ИнтеграцияСМаркетплейсомOzonВызовСервера.ПолучитьДанныеЗаказа(Ссылка, );
	ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ПередачаТоваровХранителю") 
				Или ТипЗнч(Ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		ДанныеЗаказа = ИнтеграцияСМаркетплейсомOzonВызовСервера.ПолучитьДанныеЗаказа(, Ссылка);
	КонецЕсли;
	
	Если ДанныеЗаказа = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Нет заказов для заполнения сведений по товарам.';
				|en = 'There are no orders to fill in goods information.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("УчетнаяЗапись", ДанныеЗаказа.УчетнаяЗапись);
	ПараметрыОткрытия.Вставить("Заказ",         ДанныеЗаказа.Заказ);
	ПараметрыОткрытия.Вставить("НомерЗаказа",   ДанныеЗаказа.НомерЗаказа);
	
	ОткрытьФорму(
		"РегистрСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.Форма.ЗаполнениеДанныхЭкземпляров",
		ПараметрыОткрытия,
		ДополнительныеПараметры.Форма,
		ДанныеЗаказа.Заказ);
	
КонецПроцедуры

// Получает данные отправления по заказу из выделенной строк таблицы формы.
// 
// Параметры:
//  ВыделеннаяСтрока - РегистрСведенийКлючЗаписи.ЗаказыТорговыхПлощадок - ключ записи регистра из динамического списка.
//                   - Число - идентификатор строки таблицы формы.
//                   - Структура - данные строки в виде структуры.
//                   - КлючСтрокиДинамическогоСписка - ключ строки с полями:
//                      * Заказ - ДокументСсылка.ЗаказКлиента
//                      * НомерЗаказа - Строка
//                      * Статус - ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок
//  Таблица - Неопределено, ДанныеФормыКоллекция - таблица формы, выделенная строка которой передана. Используется, если
//                     в качестве выделенной строки передан идентификатор строки таблицы.
// 
// Возвращаемое значение:
//  - Неопределено - если переданы неподдерживаемые данные.
//  - см. ИнтеграцияСМаркетплейсамиКлиентСервер.НовыеДанныеОтправленияДляОбработки
//
Функция ДанныеОтправленияИзВыделеннойСтрокиТаблицы(ВыделеннаяСтрока, Таблица) Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиКлиентСервер.НовыеДанныеОтправленияДляОбработки();
	ТипСтроки = ТипЗнч(ВыделеннаяСтрока);
	
	Если ТипСтроки = Тип("РегистрСведенийКлючЗаписи.ЗаказыТорговыхПлощадок") Тогда
		Результат = ИнтеграцияСМаркетплейсамиВызовСервера.ДанныеЗаказаТорговойПлощадкиПоКлючу(ВыделеннаяСтрока);
		
	ИначеЕсли ТипСтроки = Тип("Число") И Таблица <> Неопределено Тогда
		ДанныеСтрокиТаблицы = Таблица.НайтиПоИдентификатору(ВыделеннаяСтрока);
		Если ДанныеСтрокиТаблицы <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(Результат, ДанныеСтрокиТаблицы);
		КонецЕсли;
		
	ИначеЕсли ТипСтроки = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Результат, ВыделеннаяСтрока);
		
	ИначеЕсли ТипСтроки = Тип("КлючСтрокиДинамическогоСписка") Тогда
		Результат.Заказ = ВыделеннаяСтрока.Заказ;
		Результат.ДокументЗаказа = ВыделеннаяСтрока.Заказ;
		Результат.НомерЗаказа = ВыделеннаяСтрока.НомерЗаказа;
		Результат.НомерОтправления = ВыделеннаяСтрока.НомерЗаказа;
		Результат.СтатусОтправления = ВыделеннаяСтрока.Статус;
		
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	Если Результат <> Неопределено Тогда
		ДозаполнитьЗначенияДанныхОтправления(Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ДозаполнитьЗначенияДанныхОтправления(Результат)
	
	Если ЗначениеЗаполнено(Результат.Заказ) И Не ЗначениеЗаполнено(Результат.ДокументЗаказа) Тогда
		Результат.ДокументЗаказа = Результат.Заказ;
	ИначеЕсли ЗначениеЗаполнено(Результат.ДокументЗаказа) И Не ЗначениеЗаполнено(Результат.Заказ) Тогда
		Результат.Заказ = Результат.ДокументЗаказа;
	КонецЕсли;
	
	Если ПустаяСтрока(Результат.НомерЗаказа) И Не ПустаяСтрока(Результат.НомерОтправления) Тогда
		Результат.НомерЗаказа = Результат.НомерОтправления;
	ИначеЕсли ПустаяСтрока(Результат.НомерОтправления) И Не ПустаяСтрока(Результат.НомерЗаказа) Тогда
		Результат.НомерОтправления = Результат.НомерЗаказа;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьФормуСпискаЗаказовИОтправлений(УчетнаяЗапись, ИдентификаторУчетнойЗаписи) Экспорт
	
	ОценкаПроизводительностиКлиент.ЗамерВремени(
		"РегистрСведений.ЗаказыТорговыхПлощадок.ПриОткрытии");
	
	ОчиститьСообщения();
	
	ИмяОткрываемойФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ФормаСпискаЗаказов";

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "FBS" + ИдентификаторУчетнойЗаписи);

	ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыФормы, , "FBS");
	
КонецПроцедуры

#КонецОбласти

#Область ЭлектронноеВзаимодействиеСлужебный

// Обработка результата помещения файла.
//
// Параметры:
//   Результат               - ОписаниеПомещенногоФайла, Неопределено - описание помещенного во временное хранилище файла.
//   ДополнительныеПараметры - Структура - дополнительные параметры:
//     * СсылкаНаДокумент        - ДокументСсылка - ссылка на объект информационной базы.
//     * УникальныйИдентификатор - УникальныйИдентификатор - сформированный идентификатор.
//
Процедура СверитьДокументСТорговойПлощадкойОбработатьРезультатПомещенияФайла(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
			Или Результат.ПомещениеФайлаОтменено Тогда
		Возврат;
	КонецЕсли;
	
	РасширениеФайла  = НРег(СтрЗаменить(Результат.СсылкаНаФайл.Расширение, ".", ""));
	СсылкаНаДокумент = ДополнительныеПараметры.СсылкаНаДокумент;
	Владелец         = ДополнительныеПараметры.Владелец;
	
	Если РасширениеФайла <> "xml" 
			И РасширениеФайла <> "xls" 
			И РасширениеФайла <> "xlsx" 
			И РасширениеФайла <> "ods" Тогда
		ТекстСообщения = НСтр("ru = 'Некорректный формат файла.
									|Выберите файл с расширением ""xml"" или ""xls"".';
									|en = 'Incorrect file format.
									|Select a file with ""xml"" or ""xls"" extension.'");
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("НаправлениеЭД",       ПредопределенноеЗначение("Перечисление.НаправленияЭДО.Входящий"));
	ПараметрыОткрытия.Вставить("ИмяФайла",            Результат.СсылкаНаФайл.Имя);
	ПараметрыОткрытия.Вставить("АдресХранилищаФайла", Результат.Адрес);
	ПараметрыОткрытия.Вставить("СсылкаНаДокумент",    СсылкаНаДокумент);
	
	ОткрытьФорму("Справочник.УчетныеЗаписиМаркетплейсов.Форма.ЗагрузкаПросмотрЭлектронногоДокумента",
		ПараметрыОткрытия,
		Владелец,
		Владелец.УникальныйИдентификатор);
	
КонецПроцедуры

// Параметры:
//   Результат - Строка - адрес набора номенклатуры контрагента.
//   ДополнительныеПараметры - Структура:
//     * ОповещениеОЗавершении - ОписаниеОповещения - 
//
Процедура ПослеВводаРеквизитовНовойНоменклатуры(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеОЗавершении, Результат);
	
КонецПроцедуры

// Открывает форму группового создания номенклатуры.
//
// Параметры:
//  НаборНоменклатурыКонтрагентов - Массив из См. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента -
//        номенклатура контрагентов, по которой нужно создать номенклатуру информационной базы.
//  ОповещениеОЗавершении - ОписаниеОповещения - 
//   оповещение, которое нужно выполнить после создания номенклатуры с результатом, представляющим массив структур со свойствами:
//   * НоменклатураКонтрагента - Структура - элемент из параметра НаборНоменклатурыКонтрагентов, для которого удалось создать номенклатуру.
//   * НоменклатураИБ - Структура - описание созданной номенклатуры. См. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы.
//  Контекст - см. СопоставлениеНоменклатурыКонтрагентовСлужебныйКлиент.НовыйКонтекстСозданияНоменклатурыПоДаннымКонтрагента
//  СтандартнаяОбработка - Булево - если метод реализован, то необходимо установить значение Ложь.
//
Процедура ОткрытьФормуЗаполненияРеквизитовНовойНоменклатуры(НаборНоменклатурыКонтрагентов,
			ОповещениеОЗавершении, Контекст, СтандартнаяОбработка = Истина)

	ПараметрыПоиска = НовыеПараметрыПоискаНоменклатурыПартнера();
	ЗаполнитьЗначенияСвойств(ПараметрыПоиска, Контекст.ДополнительныеПараметрыПоиска);
	
	СпособОбработкиПоискВариантов =
		ИнтеграцияСМаркетплейсамиКлиентСервер.СпособОбработкиСопоставленияНоменклатурыПоискВариантов();
	
	Если ПараметрыПоиска.СпособОбработки <> СпособОбработкиПоискВариантов Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	АдресНабораНоменклатурыКонтрагентов = ПоместитьВоВременноеХранилище(
		НаборНоменклатурыКонтрагентов, Новый УникальныйИдентификатор);
	
	ПараметрыОткрытия = Новый Структура;
	
	ПараметрыОткрытия.Вставить("УчетнаяЗапись",                       ПараметрыПоиска.УчетнаяЗапись);
	ПараметрыОткрытия.Вставить("АдресНабораНоменклатурыКонтрагентов", АдресНабораНоменклатурыКонтрагентов);
	
	ДополнительныеПараметры = Новый Структура;
	
	ДополнительныеПараметры.Вставить("ОповещениеОЗавершении", ОповещениеОЗавершении);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПослеВводаРеквизитовНовойНоменклатуры", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму(
		"Справочник.УчетныеЗаписиМаркетплейсов.Форма.ВводРеквизитовНовойНоменклатуры",
		ПараметрыОткрытия,
		,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

// Открывает форму новой номенклатуры.
//
// Параметры:
//  НаборНоменклатурыКонтрагентов - Массив из см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента -
//    номенклатура контрагентов, по которой нужно создать номенклатуру информационной базы.
//  ОповещениеОЗавершении - ОписаниеОповещения - 
//   оповещение, которое нужно выполнить после создания номенклатуры с результатом, представляющим массив структур со свойствами:
//   * НоменклатураКонтрагента - Структура - элемент из параметра НаборНоменклатурыКонтрагентов, для которого удалось создать номенклатуру.
//   * НоменклатураИБ - Структура - описание созданной номенклатуры. См. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураИнформационнойБазы.
//  Контекст - см. СопоставлениеНоменклатурыКонтрагентовСлужебныйКлиент.НовыйКонтекстСозданияНоменклатурыПоДаннымКонтрагента
//  СтандартнаяОбработка - Булево - если метод реализован, то необходимо установить значение Ложь.
//
Процедура ОткрытьФормуНоменклатуры(НаборНоменклатурыКонтрагентов, ОповещениеОЗавершении,
			Контекст, СтандартнаяОбработка = Истина)

	СтандартнаяОбработка = Ложь;
	
	ПараметрыЗавершения = Новый Структура;
	ПараметрыЗавершения.Вставить("ОповещениеОЗавершении", ОповещениеОЗавершении);
	ПараметрыЗавершения.Вставить("НоменклатураКонтрагента", НаборНоменклатурыКонтрагентов[0]);
	
	ОписаниеОповещенияОЗакрытииФормы = Новый ОписаниеОповещения(
		"ПриСозданииНоменклатурыПоДаннымКонтрагентаЗавершение",
		ЭлектронноеВзаимодействиеУТКлиент,
		ПараметрыЗавершения);
	
	ДополнительныеПараметры = ДополнительныеПараметрыФормыЭлементаНоменклатуры(
		Контекст.ДополнительныеПараметрыПоиска,
		НаборНоменклатурыКонтрагентов);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	
	ОткрытаяФорма = ОткрытьФорму(
		"Справочник.Номенклатура.Форма.ФормаЭлемента",
		ПараметрыФормы,
		,,,,
		ОписаниеОповещенияОЗакрытииФормы,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	Если ОткрытаяФорма <> Неопределено Тогда
		// Заполнение основного изображения.
		Если ДополнительныеПараметры.ФайлОсновногоИзображения = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ОткрытаяФорма.ДобавлениеФайлаНеПрисоединенноЗавершение(
			ДополнительныеПараметры.ФайлОсновногоИзображения,
			Неопределено);
	КонецЕсли;

КонецПроцедуры

Функция ДополнительныеПараметрыФормыЭлементаНоменклатуры(Параметры, НаборНоменклатурыКонтрагентов)

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("УчетнаяЗапись",
		ПредопределенноеЗначение("Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка"));
	ДополнительныеПараметры.Вставить("ВидНоменклатуры",
		ПредопределенноеЗначение("Справочник.ВидыНоменклатуры.ПустаяСсылка"));
	
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, Параметры);
	
	Если Не ЗначениеЗаполнено(ДополнительныеПараметры.ВидНоменклатуры) Тогда
		ДополнительныеПараметры.ВидНоменклатуры = ИнтеграцияСМаркетплейсамиВызовСервера.ВидНовойНоменклатуры(
			ДополнительныеПараметры.УчетнаяЗапись,
			НаборНоменклатурыКонтрагентов);
	КонецЕсли;
	
	СопоставлениеНоменклатурыКонтрагентов = Новый Структура;
	СопоставлениеНоменклатурыКонтрагентов.Вставить("НоменклатураКонтрагента", НаборНоменклатурыКонтрагентов[0]);
	
	ДополнительныеПараметры.Вставить("СопоставлениеНоменклатурыКонтрагентов", СопоставлениеНоменклатурыКонтрагентов);
	ДополнительныеПараметры.Вставить("ФайлОсновногоИзображения", Неопределено);
	
	Возврат ДополнительныеПараметры;

КонецФункции

#КонецОбласти

#Область СопоставлениеНоменклатурыСлужебный

// Обработчик оповещения завершения фонового задания.
//
// Параметры:
//   Результат - см. ДлительныеОперации.ВыполнитьФункцию.
//   Параметры - см. ИнтеграцияСМаркетплейсамиКлиентСервер.ПараметрыИзмененияВладельцаТоварногоКаталога.
//
Процедура ИзменитьВладельцаТоварногоКаталогаЗавершениеФоновогоЗадания(Результат, Параметры) Экспорт

	Для Каждого ПолеФормы Из Параметры.БлокируемыеПоляФормы Цикл
		ПолеФормы.ТолькоПросмотр = Ложь;
	КонецЦикла;
	
	Параметры.Удалить("БлокируемыеПоляФормы");
	
	ЗавершениеФоновогоЗадания("ИзменитьВладельцаТоварногоКаталога", Результат, Параметры);

КонецПроцедуры

#КонецОбласти

#Область ИмпортТоваровИзСервисаСлужебный

// Обработчик оповещения завершения фонового задания.
//
// Параметры:
//   Результат - см. ДлительныеОперации.ВыполнитьФункцию.
//   Параметры - см. ИнтеграцияСМаркетплейсамиКлиентСервер.ПараметрыПолученияТоваровИзСервиса.
//
Процедура ПолучениеТоваровИзСервисаЗавершениеФоновогоЗадания(Результат, Параметры) Экспорт

	ЗавершениеФоновогоЗадания("ПолучениеТоваровИзСервиса", Результат, Параметры);

КонецПроцедуры

#КонецОбласти

// Обработчик оповещения завершения фонового задания.
//
// Параметры:
//   ИмяЗадания - Строка - имя завершенного задания.
//   Результат  - см. ДлительныеОперации.ВыполнитьФункцию.
//   Параметры  - Структура из Строка - параметры, передаваемые из вызывающей процедуры.
//
Процедура ЗавершениеФоновогоЗадания(ИмяЗадания, Результат, Параметры)

	ОповеститьИсточник = Истина;
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(Результат.ИнформацияОбОшибке));
			
			ОповеститьИсточник = Ложь;
		ИначеЕсли Результат.Статус = "Выполнено" Тогда
			ОжидаемыеПараметры = Новый Структура;
			ОжидаемыеПараметры.Вставить("ИдентификаторНазначения", Неопределено);
			ЗаполнитьЗначенияСвойств(ОжидаемыеПараметры, Параметры);
			
			ВывестиСообщенияДлительнойОперации(
				Результат.Сообщения,
				ОжидаемыеПараметры.ИдентификаторНазначения);
			
			РезультатВыполнения = ИнтеграцияСМаркетплейсамиВызовСервера.РезультатВыполненияФоновогоЗадания(
				Результат.АдресРезультата, Параметры);
			
			Для Каждого Состояние Из РезультатВыполнения.Состояния Цикл
				ВывестиСостояние(Состояние);
			КонецЦикла;
			
			ВывестиСообщенияДлительнойОперации(
				РезультатВыполнения.Сообщения, ОжидаемыеПараметры.ИдентификаторНазначения);
			
		КонецЕсли;
	КонецЕсли;
	
	Если ОповеститьИсточник Тогда
		ОжидаемыеПараметры = Новый Структура;
		ОжидаемыеПараметры.Вставить("ИмяСобытияОповещения", "");
		ОжидаемыеПараметры.Вставить("УчетнаяЗапись", ПредопределенноеЗначение("Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка"));
		ОжидаемыеПараметры.Вставить("КлючЗадания", "");
		
		ЗаполнитьЗначенияСвойств(ОжидаемыеПараметры, Параметры);
		
		Если Не ПустаяСтрока(ОжидаемыеПараметры.ИмяСобытияОповещения) Тогда
			Оповестить(
				ОжидаемыеПараметры.ИмяСобытияОповещения,
				ОжидаемыеПараметры.УчетнаяЗапись,
				ОжидаемыеПараметры.КлючЗадания);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ВывестиСообщенияДлительнойОперации(Сообщения, ИдентификаторНазначения)

	Для Каждого Сообщение Из Сообщения Цикл
		Если ЗначениеЗаполнено(ИдентификаторНазначения) Тогда
			Сообщение.ИдентификаторНазначения = ИдентификаторНазначения;
		КонецЕсли;
		
		Сообщение.Сообщить();
	КонецЦикла;

КонецПроцедуры

#КонецОбласти
