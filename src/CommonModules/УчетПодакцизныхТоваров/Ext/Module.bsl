#Область ПрограммныйИнтерфейс

#Область НастройкиИПараметрыУчетаПодакцизныхТоваров

// Возвращает признак использования импорта подакцизных товаров.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация, для которой определяется признак учета
//  											подакцизных товаров.
//  Период - Дата - Дата, на начало месяца которой определяется признак учета подакцизных товаров.
//
// Возвращаемое значение:
//  Булево
//
Функция ИспользуетсяИмпортПодакцизныхТоваров(Организация, Период) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПодакцизныеТовары") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмпортируетсяПодакцизныйТовар = Ложь;
	
	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
		"УчетнаяПолитикаФинансовогоУчета",
		Организация,
		Период,
		Ложь);
	
	Если ПараметрыУчетнойПолитики <> Неопределено Тогда
		ИмпортируетсяПодакцизныйТовар = ПараметрыУчетнойПолитики.ИмпортируетПодакцизныйТовар;
	КонецЕсли;
	
	Возврат ИмпортируетсяПодакцизныйТовар;
	
КонецФункции

//++ НЕ УТ

// Возвращает признак использования производства подакцизных товаров.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация, для которой определяется признак учета
//  											подакцизных товаров.
//  Период - Дата - Дата, на начало месяца которой определяется признак учета подакцизных товаров.
//
// Возвращаемое значение:
//  Булево
//
Функция ИспользуетсяПроизводствоПодакцизныхТоваров(Организация, Период) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПодакцизныеТовары") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПроизводитсяПодакцизныйТовар = Ложь;
	
	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
		"УчетнаяПолитикаФинансовогоУчета",
		Организация,
		Период,
		Ложь);
	
	Если ПараметрыУчетнойПолитики <> Неопределено Тогда
		ПроизводитсяПодакцизныйТовар = ПараметрыУчетнойПолитики.ПроизводитПодакцизныйТовар;
	КонецЕсли;
	
	Возврат ПроизводитсяПодакцизныйТовар;
	
КонецФункции

// Создает временную таблицу 'ВТПроизводителиПодакцизныхТоваров', которая содержит сведения финансовых учетных политик
// организаций в части производства подакцизных товаров на дату документа.
//
// Параметры:
//	ДокументОбъект - ДокументОбъект - данные документа.
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц запроса, в который помещается таблица
//														'ВТПроизводителиПодакцизныхТоваров'.
//	ПоВсемОрганизациям - Булево - признак способа получения данных. ЛОЖЬ - по умолчанию сведения получаются по данным
//									документа.
//
Процедура ПроизводителиПодакцизныхТоваров(ДокументОбъект, МенеджерВременныхТаблиц, ПоВсемОрганизациям = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если МенеджерВременныхТаблиц.Таблицы.Найти("ВТПроизводителиПодакцизныхТоваров") <> Неопределено Тогда
		Запрос.Текст = "УНИЧТОЖИТЬ ВТПроизводителиПодакцизныхТоваров";
		
		Запрос.Выполнить();
	КонецЕсли;
	
	Если ПоВсемОрганизациям Тогда
		НастройкиНалоговУчетныхПолитик.ДополнитьМенеджерВременныхТаблицГоловнымиОрганизациями(МенеджерВременныхТаблиц);
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПолитикиУчета.Организация КАК Организация,
		|	ПолитикиУчета.ПроизводитПодакцизныйТовар КАК ПроизводитПодакцизныйТовар
		|ПОМЕСТИТЬ ВТПолитикиУчета
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&Период, Организация В
		|		(ВЫБРАТЬ
		|			ГоловныеОрганизации.Организация
		|		ИЗ
		|			ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК ПолитикиУчета
		|ГДЕ
		|	ПолитикиУчета.ПроизводитПодакцизныйТовар
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СправочникОрганизации.Ссылка	КАК Организация,
		|	ВЫБОР
		|		КОГДА СправочникОрганизации.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
		|			ТОГДА СправочникОрганизации.СтранаРегистрации
		|		ИНАЧЕ &ОсновнаяСтрана
		|	КОНЕЦ							КАК Страна,
		|	ЕСТЬNULL(ВТПолитикиУчета.ПроизводитПодакцизныйТовар, ЛОЖЬ) КАК ПроизводитПодакцизныйТовар
		|ПОМЕСТИТЬ ВТПроизводителиПодакцизныхТоваров
		|ИЗ
		|	ВтГоловныеОрганизации КАК ГоловныеОрганизации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПолитикиУчета КАК ВТПолитикиУчета
		|		ПО ГоловныеОрганизации.Организация = ВТПолитикиУчета.Организация
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникОрганизации
		|		ПО ГоловныеОрганизации.ОбособленноеПодразделение = СправочникОрганизации.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПолитикиУчета";
		
		Период = ?(ЗначениеЗаполнено(ДокументОбъект.Дата), ДокументОбъект.Дата, ТекущаяДатаСеанса());
		
		Запрос.УстановитьПараметр("Период",			Период);
		Запрос.УстановитьПараметр("ОсновнаяСтрана",	ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана());
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Организация	КАК Организация,
		|	&Страна			КАК Страна,
		|	&ИспользуетсяПроизводствоПодакцизныхТоваров КАК ПроизводитПодакцизныйТовар
		|ПОМЕСТИТЬ ВТПроизводителиПодакцизныхТоваров";
		
		СтранаРегистрации = ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(ДокументОбъект.Организация);
		ИспользуетсяПроизводствоПодакцизныхТоваров = ИспользуетсяПроизводствоПодакцизныхТоваров(
														ДокументОбъект.Организация,
														ДокументОбъект.Дата);
		
		Запрос.УстановитьПараметр("Организация",	ДокументОбъект.Организация);
		Запрос.УстановитьПараметр("Страна",			СтранаРегистрации);
		Запрос.УстановитьПараметр("ИспользуетсяПроизводствоПодакцизныхТоваров",
									ИспользуетсяПроизводствоПодакцизныхТоваров);
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Возвращает текст запроса временной таблицы 'ВТПроизводителиПодакцизныхТоваров', которая содержит сведения финансовых
// учетных политик организаций в части производства подакцизных товаров по периодам документа.
// Исполняемый запрос должен содержать временную таблицу с колонками 'Период' и 'Организация'.
// Так же должен быть задан параметр запроса 'ОсновнаяСтрана'.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц запроса, в который помещается таблица
//														'ВТПроизводителиПодакцизныхТоваров'.
//	ИмяТаблицыТоваров - Строка - имя временной таблицы товаров, по которой осуществляется отбор в регистре.
//									По умолчанию - 'ТаблицаТоваров'.
//
// Возвращаемое значение:
//	Строка - текст запроса временной таблицы.
//
Функция ТекстЗапросаПроизводителиПодакцизныхТоваров(МенеджерВременныхТаблиц, ИмяТаблицыТоваров = "ТаблицаТоваров") Экспорт
	
	НастройкиНалоговУчетныхПолитик.ДополнитьМенеджерВременныхТаблицГоловнымиОрганизациями(МенеджерВременныхТаблиц);
	
	Если МенеджерВременныхТаблиц.Таблицы.Найти("ВТПроизводителиПодакцизныхТоваров") <> Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = "УНИЧТОЖИТЬ ВТПроизводителиПодакцизныхТоваров";
		
		Запрос.Выполнить();
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Период			КАК Период,
	|	ПолитикиУчета.Организация		КАК Организация,
	|	МАКСИМУМ(ПолитикиУчета.Период)	КАК ПериодДействия
	|ПОМЕСТИТЬ ВТПолитикиУчета
	|ИЗ
	|	#ТаблицаТоваров КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК ПолитикиУчета
	|		ПО ТаблицаТоваров.Период >= ПолитикиУчета.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Период,
	|	ПолитикиУчета.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПериодДействия,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТПолитикиУчета.Период							КАК Период,
	|	ГоловныеОрганизации.ОбособленноеПодразделение	КАК Организация,
	|	ВЫБОР
	|		КОГДА СправочникОрганизации.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА СправочникОрганизации.СтранаРегистрации
	|		ИНАЧЕ &ОсновнаяСтрана
	|	КОНЕЦ											КАК Страна,
	|	ПолитикиУчета.ПроизводитПодакцизныйТовар		КАК ПроизводитПодакцизныйТовар
	|ПОМЕСТИТЬ ВТПроизводителиПодакцизныхТоваров
	|ИЗ
	|	ВТПолитикиУчета КАК ВТПолитикиУчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК ПолитикиУчета
	|		ПО ВТПолитикиУчета.ПериодДействия = ПолитикиУчета.Период
	|			И ВТПолитикиУчета.Организация = ПолитикиУчета.Организация
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтГоловныеОрганизации КАК ГоловныеОрганизации
	|		ПО ВТПолитикиУчета.Организация = ГоловныеОрганизации.Организация
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаТоваров КАК ТаблицаТоваров
	|		ПО ГоловныеОрганизации.ОбособленноеПодразделение = ТаблицаТоваров.Организация
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК СправочникОрганизации
	|		ПО ГоловныеОрганизации.ОбособленноеПодразделение = СправочникОрганизации.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПолитикиУчета";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаТоваров", ИмяТаблицыТоваров);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- НЕ УТ

// Определяет принадлежность номенклатуры к подакцизной категории товаров в стране регистрации организации.
//
// Параметры:
//	Номенклатура - СправочникСсылка.Номенклатура - Номенклатура, для которой необходимо определить
//		принадлежность к подакцизной категории товара.
//	Организация - СправочникСсылка.Организации - Организация, по которой определяется страна регистрации
//		для определения подакцизной категории товара.
//	Период - Дата, Неопределено - Дата, на которую определяется принадлежность к подакцизной категории товара.
//		Если значение не передано, принадлежность определяется на текущую дату.
//
// Возвращаемое значение:
//	Булево - Истина, если номенклатура относится к подакцизной категории товаров.
//
Функция ЭтоПодакцизныйТовар(Номенклатура, Организация = Неопределено, Знач Период = Неопределено) Экспорт
	
	ЭтоПодакцизныйТовар = Ложь;
	
	Если Период = Неопределено Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если Организация <> Неопределено
		Или Не ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет") Тогда
		
		Если Организация <> Неопределено Тогда
			Страна = ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(Организация);
		Иначе
			Страна = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
		КонецЕсли;
		
		Отбор = Новый Структура("Номенклатура, Страна", Номенклатура, Страна);
		ЭтоПодакцизныйТовар =
			РегистрыСведений.КатегорииПодакцизныхТоваров.ПолучитьПоследнее(Период, Отбор).ПодакцизныйТовар;
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ПодакцизныйТовар
		|ИЗ
		|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(
		|			&ТекущаяДата,
		|			Номенклатура = &Номенклатура) КАК АкцизыНоменклатурыСрезПоследних
		|ГДЕ
		|	АкцизыНоменклатурыСрезПоследних.ПодакцизныйТовар";
		
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		Запрос.УстановитьПараметр("ТекущаяДата", Период);
		
		Результат = Запрос.Выполнить().Выбрать();
		ЭтоПодакцизныйТовар = Результат.Следующий();
		
	КонецЕсли;
	
	Возврат ЭтоПодакцизныйТовар;
	
КонецФункции

// Получает таблицу подакцизных категорий по номенклатуре.
//
// Параметры:
//	Номенклатура - СправочникСсылка.Номенклатура - Номенклатура, для которой необходимо определить
//		принадлежность к подакцизной категории товара.
//	Организация - СправочникСсылка.Организации - Организация, по которой определяется страна регистрации
//		для определения подакцизной категории товара.
//	Период - Дата, Неопределено - Дата, на которую определяется принадлежность к подакцизной категории товара.
//		Если значение не передано, принадлежность определяется на текущую дату.
//
// Возвращаемое значение:
//	ТаблицаЗначений:
//		* КатегорияПодакцизногоТовара - СправочникСсылка.КатегорииПодакцизныхТоваров - подакцизная категория;
//		* ТипСтавки - ПеречислениеСсылка.ТипыСтавокПодакцизнойКатегории - тип ставки подакцизной категории;
//		* Валюта - СправочникСсылка.Валюты - валюта ставки;
//		* ЕдиницаИзмерения - СправочникСсылка.УпаковкиЕдиницыИзмерения - единица измерения подакцизной категории;
//		* Ставка - Число - ставка подакцизной категории.
//
Функция ПодакцизныеКатегорииТовара(Номенклатура, Организация = Неопределено, Знач Период = Неопределено) Экспорт
	
	Страна = Неопределено;
	
	Если Период = Неопределено Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если Организация <> Неопределено
		Или Не ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет") Тогда
		
		Если Организация <> Неопределено Тогда
			Страна = ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(Организация);
		Иначе
			Страна = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПодакцизныеКатегории.КатегорияПодакцизногоТовара КАК Категория
	|ПОМЕСТИТЬ ДанныеКатегорий
	|ИЗ
	|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(&ТекущаяДата, Номенклатура = &Номенклатура
	|	И (Страна = &Страна
	|	ИЛИ НЕ &ОтборПоСтране)) КАК ПодакцизныеКатегории
	|ГДЕ
	|	ПодакцизныеКатегории.ПодакцизныйТовар
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Категория
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиАкциза.КатегорияПодакцизногоТовара КАК Категория,
	|	СтавкиАкциза.Ставка КАК Ставка
	|ПОМЕСТИТЬ СтавкиАкциза
	|ИЗ
	|	РегистрСведений.СтавкиКатегорийПодакцизныхТоваров.СрезПоследних(&ТекущаяДата,
	|			КатегорияПодакцизногоТовара В
	|			(ВЫБРАТЬ
	|				ДанныеКатегорий.Категория
	|			ИЗ
	|				ДанныеКатегорий)) КАК СтавкиАкциза
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Категория
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеКатегорий.Категория КАК КатегорияПодакцизногоТовара,
	|	КатегорииПодакцизныхТоваров.ТипСтавки КАК ТипСтавки,
	|	КатегорииПодакцизныхТоваров.Валюта КАК Валюта,
	|	КатегорииПодакцизныхТоваров.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА КатегорииПодакцизныхТоваров.ТипСтавки ЕСТЬ NULL
	|			ТОГДА 0
	|		КОГДА КатегорииПодакцизныхТоваров.ТипСтавки = &ТвердаяСтавка
	|			ТОГДА СтавкиАкциза.Ставка
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Ставка
	|ИЗ
	|	ДанныеКатегорий КАК ДанныеКатегорий
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КатегорииПодакцизныхТоваров КАК КатегорииПодакцизныхТоваров
	|		ПО ДанныеКатегорий.Категория = КатегорииПодакцизныхТоваров.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиАкциза КАК СтавкиАкциза
	|		ПО СтавкиАкциза.Категория = ДанныеКатегорий.Категория";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Страна", Страна);
	Запрос.УстановитьПараметр("ОтборПоСтране", Страна <> Неопределено);
	Запрос.УстановитьПараметр("ТекущаяДата", Период);
	Запрос.УстановитьПараметр("ТвердаяСтавка", Перечисления.ТипыСтавокПодакцизнойКатегории.Твердая);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Получает таблицу подакцизных категорий по номенклатуре.
//
// Параметры:
//	Номенклатура - СправочникСсылка.Номенклатура - Номенклатура, для которой необходимо определить
//		список подакцизных категорий.
//	Страна - СправочникСсылка.СтраныМира, Неопределено - Страна, по которой определяется подакцизные категории товара.
//	Период - Дата, Неопределено - Дата, на которую определяется принадлежность к подакцизной категории товара.
//		Если значение не передано, принадлежность определяется на текущую дату.
//
// Возвращаемое значение:
//	ТаблицаЗначений:
//		* Период - Дата - начала периода действия подакцизной категории;
//		* Страна - СправочникСсылка.СтраныМира - страна действия подакцизной категории;
//		* ЕдиницаИзмеренияНоменклатуры - СправочникСсылка.УпаковкиЕдиницыИзмерения - единица измерения номенклатуры;
//		* ПодакцизныйТовар - Булево - признак, что товар подакцизный;
//		* КатегорияПодакцизногоТовара - СправочникСсылка.КатегорииПодакцизныхТоваров - подакцизная категория;
//		* ЕдиницаИзмеренияКатегории - СправочникСсылка.УпаковкиЕдиницыИзмерения - единица измерения подакцизной категории;
//		* ТипСтавки - ПеречислениеСсылка.ТипыСтавокПодакцизнойКатегории - тип ставки подакцизной категории;
//		* Валюта - СправочникСсылка.Валюты - валюта ставки;
//		* Ставка - Число - ставка подакцизной категории;
//		* КоэффициентПересчета - Число - коэффициент пересчета из единицы измерения номенклатуры в единицу измерения подакцизной категории;
//		* ЕдиницаИзмеренияНоменклатурыВКатегорию - Строка - наименование единицы измерения категории
//			и наименование единицы измерения номенклатуры.
//
Функция ПолучитьИсториюКатегорийНоменклатуры(Номенклатура, Знач Страна = Неопределено, Период = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Страна)
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет") Тогда
		
		Страна = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
		
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КатегорииПодакцизныхТоваров.Период КАК Период,
	|	КатегорииПодакцизныхТоваров.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара,
	|	КатегорииПодакцизныхТоваров.КоэффициентПересчета КАК КоэффициентПересчета,
	|	КатегорииПодакцизныхТоваров.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	КатегорииПодакцизныхТоваров.Страна КАК Страна
	|ПОМЕСТИТЬ СписокКатегорий
	|ИЗ
	|	РегистрСведений.КатегорииПодакцизныхТоваров КАК КатегорииПодакцизныхТоваров
	|ГДЕ
	|	КатегорииПодакцизныхТоваров.Номенклатура = &Номенклатура
	|	И (КатегорииПодакцизныхТоваров.Страна = &Страна
	|	ИЛИ НЕ &СтранаЗаполнена)
	|	И НЕ &ОтборПоПериоду
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КатегорииПодакцизныхТоваров.Период,
	|	КатегорииПодакцизныхТоваров.КатегорияПодакцизногоТовара,
	|	КатегорииПодакцизныхТоваров.КоэффициентПересчета,
	|	КатегорииПодакцизныхТоваров.ПодакцизныйТовар,
	|	КатегорииПодакцизныхТоваров.Страна
	|ИЗ
	|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(&Период, Номенклатура = &Номенклатура
	|	И (Страна = &Страна
	|	ИЛИ НЕ &СтранаЗаполнена)) КАК КатегорииПодакцизныхТоваров
	|ГДЕ
	|	&ОтборПоПериоду
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КатегорияПодакцизногоТовара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиКатегорийПодакцизныхТоваров.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара,
	|	СтавкиКатегорийПодакцизныхТоваров.Ставка КАК Ставка
	|ПОМЕСТИТЬ СтавкиКатегорийПодакцизныхТоваров
	|ИЗ
	|	РегистрСведений.СтавкиКатегорийПодакцизныхТоваров.СрезПоследних(&Период, &ОтборПоПериоду
	|		И КатегорияПодакцизногоТовара В
	|			(ВЫБРАТЬ
	|				СписокКатегорий.КатегорияПодакцизногоТовара
	|			ИЗ
	|				СписокКатегорий)) КАК СтавкиКатегорийПодакцизныхТоваров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КатегорияПодакцизногоТовара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписокКатегорий.Период КАК Период,
	|	СписокКатегорий.Страна КАК Страна,
	|	ДанныеНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмеренияНоменклатуры,
	|	СписокКатегорий.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	СписокКатегорий.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара,
	|	СписокКатегорий.КатегорияПодакцизногоТовара.ЕдиницаИзмерения КАК ЕдиницаИзмеренияКатегории,
	|	СписокКатегорий.КатегорияПодакцизногоТовара.ТипСтавки КАК ТипСтавки,
	|	СписокКатегорий.КатегорияПодакцизногоТовара.Валюта КАК Валюта,
	|	ЕСТЬNULL(СтавкиКатегорийПодакцизныхТоваров.Ставка, 0) КАК Ставка,
	|	СписокКатегорий.КоэффициентПересчета КАК КоэффициентПересчета,
	|	ВЫБОР
	|		КОГДА СписокКатегорий.КоэффициентПересчета > 0
	|			ТОГДА СписокКатегорий.КатегорияПодакцизногоТовара.ЕдиницаИзмерения.Наименование + ""/"" +
	|				ДанныеНоменклатуры.ЕдиницаИзмерения.Наименование
	|		КОГДА ЕСТЬNULL(СписокКатегорий.КатегорияПодакцизногоТовара,
	|			ЗНАЧЕНИЕ(Справочник.КатегорииПодакцизныхТоваров.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.КатегорииПодакцизныхТоваров.ПустаяСсылка)
	|			ТОГДА """"
	|	КОНЕЦ КАК ЕдиницаИзмеренияНоменклатурыВКатегорию
	|ИЗ
	|	СписокКатегорий КАК СписокКатегорий
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиКатегорийПодакцизныхТоваров КАК СтавкиКатегорийПодакцизныхТоваров
	|		ПО СписокКатегорий.КатегорияПодакцизногоТовара = СтавкиКатегорийПодакцизныхТоваров.КатегорияПодакцизногоТовара
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ДанныеНоменклатуры
	|		ПО (ДанныеНоменклатуры.Ссылка = &Номенклатура)";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("ОтборПоПериоду", ЗначениеЗаполнено(Период));
	Запрос.УстановитьПараметр("Страна", Страна);
	Запрос.УстановитьПараметр("СтранаЗаполнена", ЗначениеЗаполнено(Страна));

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

// Копирует подакцизные категории для номенклатуры.
//
// Параметры:
//	Номенклатура - СправочникСсылка.Номенклатура - Номенклатура, для которой необходимо скопировать категории подакцизных товаров;
//	НоменклатураИсточник - СправочникСсылка.Номенклатура - Источник для подакцизных категорий;
//	КопироватьКоэффициент - Булево - признак, что необходимо скопировать коэффициент пересчета для подакцизных категорий.
//
Процедура СкопироватьПодакцизныеКатегории(Номенклатура, НоменклатураИсточник, КопироватьКоэффициент) Экспорт
	
	Период = ТекущаяДатаСеанса();
	ДанныеПоПодакцизнымКатегориям = ПолучитьИсториюКатегорийНоменклатуры(НоменклатураИсточник,, Период);
	
	Для Каждого ИсходнаяСтрока Из ДанныеПоПодакцизнымКатегориям Цикл
		
		НоваяЗаписьПодакцизнойКатегории = РегистрыСведений.КатегорииПодакцизныхТоваров.СоздатьМенеджерЗаписи();
		НоваяЗаписьПодакцизнойКатегории.Номенклатура = Номенклатура;
		НоваяЗаписьПодакцизнойКатегории.Период = ИсходнаяСтрока.Период;
		НоваяЗаписьПодакцизнойКатегории.Страна = ИсходнаяСтрока.Страна;
		Если КопироватьКоэффициент Тогда
			НоваяЗаписьПодакцизнойКатегории.КоэффициентПересчета = ИсходнаяСтрока.КоэффициентПересчета;
		КонецЕсли;
		НоваяЗаписьПодакцизнойКатегории.КатегорияПодакцизногоТовара = ИсходнаяСтрока.КатегорияПодакцизногоТовара;
		НоваяЗаписьПодакцизнойКатегории.ПодакцизныйТовар = ИсходнаяСтрока.ПодакцизныйТовар;
		НоваяЗаписьПодакцизнойКатегории.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаТабличнойЧасти

// Добавляет запрос в пакет запросов для получения признака при пакетной обработке строк табличной части.
//
// Параметры:
//  СтруктураДействий - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ.СтруктураДействий
//  ОписаниеЗапроса - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОписаниеЗапроса
//  КэшированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения
//
Процедура ДополнитьТекстЗапросаЗаполнитьПризнакПодакцизныйТовар(
	СтруктураДействий, ОписаниеЗапроса, КэшированныеЗначения) Экспорт
	
	Перем СтруктураПараметровДействия;
	
	Если ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
			"ЗаполнитьПризнакПодакцизныйТовар",
			СтруктураДействий,
			КэшированныеЗначения,
			СтруктураПараметровДействия) Тогда
		
		ТекстЗапроса = ТекстЗапросаВтПодакцизныеТовары("ВтИсточникДанных");
		Если ОписаниеЗапроса.ТекстыЗапросов.НайтиПоЗначению(ТекстЗапроса) = Неопределено Тогда
			ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "ВтПодакцизныеТовары");
		КонецЕсли;
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Товары.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
		|	ЕСТЬNULL(ВтПодакцизныеТовары.ПодакцизныйТовар, ЛОЖЬ) КАК ПодакцизныйТовар
		|ИЗ
		|	ВтИсточникДанных КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПодакцизныеТовары КАК ВтПодакцизныеТовары
		|		ПО Товары.Номенклатура = ВтПодакцизныеТовары.Номенклатура";
		
		Дата = СтруктураПараметровДействия.Дата;
		Организация = СтруктураПараметровДействия.Организация;
		ОписаниеЗапроса.ПараметрыЗапроса.Вставить("Дата", ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));
		ОписаниеЗапроса.ПараметрыЗапроса.Вставить("Страна",
			ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(Организация));
		
		ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "ЗаполнитьПризнакПодакцизныйТовар");
		
	КонецЕсли;
	
КонецПроцедуры

// Добавляет запрос в пакет запросов для получения параметров пересчета суммы акциза при пакетной обработке
// строк табличной части.
//
// Параметры:
//  СтруктураДействий - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ.СтруктураДействий
//  ОписаниеЗапроса - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОписаниеЗапроса
//  КэшированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения
//
Процедура ДополнитьТекстЗапросаПересчитатьСуммуАкциза(СтруктураДействий, ОписаниеЗапроса, КэшированныеЗначения) Экспорт
	
	Перем СтруктураПараметровДействия;
	
	Если ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
			"ПересчитатьСуммуАкциза",
			СтруктураДействий,
			КэшированныеЗначения,
			СтруктураПараметровДействия) Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаКатегории.Номенклатура КАК Номенклатура,
		|	ТаблицаКатегории.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара,
		|	ТаблицаКатегории.КоэффициентПересчета КАК КоэффициентПересчета
		|ПОМЕСТИТЬ ВтКатегорииПодакцизныхТоваров
		|ИЗ
		|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(&Дата, (Номенклатура, Страна) В
		|		(ВЫБРАТЬ
		|			Т.Номенклатура КАК Номенклатура,
		|			&Страна КАК Страна
		|		ИЗ
		|			ВтИсточникДанных КАК Т)) КАК ТаблицаКатегории
		|ГДЕ
		|	ТаблицаКатегории.ПодакцизныйТовар
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КатегорияПодакцизногоТовара
		|";
		
		ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "ВтКатегорииПодакцизныхТоваров");
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаСтавки.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара,
		|	ТаблицаСтавки.Ставка КАК Ставка
		|ПОМЕСТИТЬ ВтСтавкиКатегорийПодакцизныхТоваров
		|ИЗ
		|	РегистрСведений.СтавкиКатегорийПодакцизныхТоваров.СрезПоследних(&Дата, КатегорияПодакцизногоТовара В
		|		(ВЫБРАТЬ
		|			Т.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара
		|		ИЗ
		|			ВтКатегорииПодакцизныхТоваров КАК Т)) КАК ТаблицаСтавки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КатегорияПодакцизногоТовара
		|";
		
		ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "ВтСтавкиКатегорийПодакцизныхТоваров");
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаКатегории.Номенклатура КАК Номенклатура,
		|	ЕСТЬNULL(ТаблицаСтавки.Ставка, 0) КАК Ставка,
		|	ЕСТЬNULL(ТаблицаСтавки.Ставка, 0) * ТаблицаКатегории.КоэффициентПересчета КАК СтавкаЗаЕдиницуХранения,
		|	ТаблицаКатегории.КоэффициентПересчета КАК КоэффициентПересчета,
		|	КатегорииПодакцизныхТоваров.Ссылка КАК Категория,
		|	ЕСТЬNULL(КатегорииПодакцизныхТоваров.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта,
		|	ЕСТЬNULL(КатегорииПодакцизныхТоваров.ТипСтавки,
		|		ЗНАЧЕНИЕ(Перечисление.ТипыСтавокПодакцизнойКатегории.ПустаяСсылка)) КАК ТипСтавки,
		|	ТаблицаСтавки.Ставка ЕСТЬ NULL КАК НеЗаданаСтавка,
		|	КатегорииПодакцизныхТоваров.Ссылка ЕСТЬ NULL КАК НеЗаданаКатегория
		|ПОМЕСТИТЬ ВтПараметрыРасчетаАкциза
		|ИЗ
		|	ВтКатегорииПодакцизныхТоваров КАК ТаблицаКатегории
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтавкиКатегорийПодакцизныхТоваров КАК ТаблицаСтавки
		|		ПО ТаблицаКатегории.КатегорияПодакцизногоТовара = ТаблицаСтавки.КатегорияПодакцизногоТовара
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КатегорииПодакцизныхТоваров КАК КатегорииПодакцизныхТоваров
		|		ПО ТаблицаКатегории.КатегорияПодакцизногоТовара = КатегорииПодакцизныхТоваров.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Валюта
		|";
		
		ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "ВтПараметрыРасчетаАкциза");
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	КурсВалюты.Валюта КАК Валюта,
		|	КурсВалюты.КурсЧислитель / КурсВалюты.КурсЗнаменатель КАК КоэффициентПересчета
		|ПОМЕСТИТЬ ВтКурсыВалютАкциза
		|ИЗ
		|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Дата, (Валюта, БазоваяВалюта) В
		|			(ВЫБРАТЬ
		|				Т.Валюта КАК Валюта,
		|				&БазоваяВалюта КАК БазоваяВалюта
		|			ИЗ
		|				ВтПараметрыРасчетаАкциза КАК Т)) КАК КурсВалюты
		|ГДЕ
		|	КурсВалюты.КурсЗнаменатель <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|";
		
		ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "ВтКурсыВалютАкциза");
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Товары.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
		|	ТаблицаДанных.Номенклатура ЕСТЬ НЕ NULL КАК ПодакцизныйТовар,
		|	ВЫРАЗИТЬ(ТаблицаДанных.Ставка * ЕСТЬNULL(КурсыВалют.КоэффициентПересчета, 1) КАК ЧИСЛО(31, 2)) КАК Ставка,
		|	ВЫРАЗИТЬ(ТаблицаДанных.СтавкаЗаЕдиницуХранения
		|		* ЕСТЬNULL(КурсыВалют.КоэффициентПересчета, 1) КАК ЧИСЛО(31, 2)) КАК СтавкаЗаЕдиницуХранения,
		|	ТаблицаДанных.КоэффициентПересчета КАК КоэффициентПересчетаЕдиницИзмерения,
		|	ПРЕДСТАВЛЕНИЕ(ТаблицаДанных.Номенклатура) КАК Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(ТаблицаДанных.Категория) КАК Категория,
		|	ПРЕДСТАВЛЕНИЕ(ТаблицаДанных.Валюта) КАК Валюта,
		|	ТаблицаДанных.НеЗаданаСтавка КАК НеЗаданаСтавка,
		|	ТаблицаДанных.НеЗаданаКатегория КАК НеЗаданаКатегория,
		|	ТаблицаДанных.КоэффициентПересчета = 0
		|		И ТаблицаДанных.ТипСтавки = &ТвердаяСтавка КАК НеЗаданКоэффициентПересчета,
		|	ТаблицаДанных.Валюта <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
		|		И ТаблицаДанных.Валюта <> &БазоваяВалюта
		|		И КурсыВалют.КоэффициентПересчета ЕСТЬ NULL КАК НеЗаданКурсПересчетаВВалютуРегл
		|ИЗ
		|	ВтИсточникДанных КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПараметрыРасчетаАкциза КАК ТаблицаДанных
		|		ПО Товары.Номенклатура = ТаблицаДанных.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсыВалютАкциза КАК КурсыВалют
		|		ПО ТаблицаДанных.Валюта = КурсыВалют.Валюта";
		
		ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "ПересчитатьСуммуАкциза");
		
		Дата = СтруктураПараметровДействия.Дата;
		ОписаниеЗапроса.ПараметрыЗапроса.Вставить("Дата", ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()));
		ОписаниеЗапроса.ПараметрыЗапроса.Вставить("Страна",
			ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(СтруктураПараметровДействия.Организация));
		ОписаниеЗапроса.ПараметрыЗапроса.Вставить("БазоваяВалюта",
			ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(СтруктураПараметровДействия.Организация));
		ОписаниеЗапроса.ПараметрыЗапроса.Вставить("ТвердаяСтавка", Перечисления.ТипыСтавокПодакцизнойКатегории.Твердая);
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет служебный реквизит "ПодакцизныйТовар" в  текущей обрабатываемой строке табличной части в механизмах
// пакетной обработки строк табличных частей.
//
// Параметры:
//  ТекущаяСтрока - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ
//  СтруктураДействий - см.  ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ.СтруктураДействий
//  КэшированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения
//
Процедура ЗаполнитьПризнакПодакцизныйТоварПакетно(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если Не СтруктураДействий.Свойство("ЗаполнитьПризнакПодакцизныйТовар") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляОбработки = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки(
		"ЗаполнитьПризнакПодакцизныйТовар", КэшированныеЗначения);
	
	Если ДанныеДляОбработки <> Неопределено Тогда
		ТекущаяСтрока.ПодакцизныйТовар = ДанныеДляОбработки[0].ПодакцизныйТовар;
	Иначе
		ТекущаяСтрока.ПодакцизныйТовар = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает сумму акциза для текущей обрабатываемой строки табличной части в механизмах пакетной обработки
// строк табличных частей.
//
// Параметры:
//  ТекущаяСтрока - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ
//  СтруктураДействий - см.  ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ.СтруктураДействий
//  КэшированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения
//
Процедура ПересчитатьСуммуАкцизаПакетно(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем СтруктураПараметровДействия;
	
	Если Не СтруктураДействий.Свойство("ПересчитатьСуммуАкциза", СтруктураПараметровДействия) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока.СуммаАкциза = 0;
	
	Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(ТекущаяСтрока, "ПодакцизныйТовар")
		И Не ТекущаяСтрока.ПодакцизныйТовар
		Или Не ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляОбработки =
		ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки("ПересчитатьСуммуАкциза", КэшированныеЗначения); 
	
	Если ДанныеДляОбработки <> Неопределено Тогда
		
		ПараметрыРасчета = ДанныеДляОбработки[0];
		
		Если Не ПараметрыРасчета.ПодакцизныйТовар Тогда
			Возврат;
		КонецЕсли;
		
		ИмяПоляКоличество = СтруктураПараметровДействия.ИмяПоляКоличество;
		Количество = ТекущаяСтрока[ИмяПоляКоличество];
		
		Если СтруктураПараметровДействия.СтавкаЗаЕдиницуХранения Тогда
			Ставка = ПараметрыРасчета.СтавкаЗаЕдиницуХранения;
		Иначе
			Ставка = ПараметрыРасчета.Ставка;
		КонецЕсли;
		
		ТекущаяСтрока.СуммаАкциза = Количество * Ставка;
		
		СообщитьОбОшибкахРасчетаАкциза(ПараметрыРасчета, СтруктураПараметровДействия.Дата);
		
	КонецЕсли;
	
КонецПроцедуры

//++ НЕ УТ

// Выполняет расчет суммы акциза в таблице ВидыЗапасов.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - Документ, в котором рассчитывается сумма акциза.
//  ПараметрыЗаполнения - См. ЗапасыСервер.ПараметрыЗаполненияВидовЗапасов.
//  ИмяКолонкиВидЗапасов - Строка - Имя колонки, в которой находятся виды запасов.
//  Отказ - Булево - Признак отказа от записи.
//
Процедура РассчитатьСуммуАкциза(ДокументОбъект, ПараметрыЗаполнения, ИмяКолонкиВидЗапасов = "", Отказ = Ложь) Экспорт
	
	Если Отказ
		Или Не ЗначениеЗаполнено(ПараметрыЗаполнения)
		Или Не ПараметрыЗаполнения.РассчитатьСуммуАкциза Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ПараметрыЗаполнения.ТаблицаВидыЗапасов <> Неопределено Тогда
		ТаблицаВидыЗапасов = ПараметрыЗаполнения.ТаблицаВидыЗапасов;
	Иначе
		ТаблицаВидыЗапасов = ДокументОбъект[ПараметрыЗаполнения.ИмяТЧВидыЗапасов];
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗаполнения.ПериодНачисленияАкциза) Тогда
		Период = ПараметрыЗаполнения.ПериодНачисленияАкциза;
	Иначе
		Период = ДокументОбъект.Дата;
	КонецЕсли;
	
	Организация = ДокументОбъект[ПараметрыЗаполнения.ИмяПоляОрганизация];
	БазоваяВалюта = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	СтранаРегистрации = ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(Организация);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТаблицаВидыЗапасов", ТаблицаВидыЗапасов);
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("БазоваяВалюта", БазоваяВалюта);
	Запрос.УстановитьПараметр("СтранаРегистрации", СтранаРегистрации);
	Запрос.УстановитьПараметр("ТвердаяСтавка", Перечисления.ТипыСтавокПодакцизнойКатегории.Твердая);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	&ТекстЗапросаВидЗапасов КАК ВидЗапасов
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	&ТаблицаВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.СуммаАкциза = 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	Аналитика.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
	|		ПО ТаблицаВидыЗапасов.ВидЗапасов = ВидыЗапасов.Ссылка
	|ГДЕ
	|	ВидыЗапасов.Организация = &Организация
	|	И ВидыЗапасов.ОблагаетсяАкцизом
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКатегории.Номенклатура КАК Номенклатура,
	|	ТаблицаКатегории.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара,
	|	ТаблицаКатегории.КоэффициентПересчета КАК КоэффициентПересчета
	|ПОМЕСТИТЬ ТаблицаКатегории
	|ИЗ
	|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(&Период, (Номенклатура, Страна) В
	|		(ВЫБРАТЬ
	|			Т.Номенклатура КАК Номенклатура,
	|			&СтранаРегистрации КАК Страна
	|		ИЗ
	|			ТаблицаВидыЗапасов КАК Т)) КАК ТаблицаКатегории
	|ГДЕ
	|	ТаблицаКатегории.ПодакцизныйТовар
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	КатегорияПодакцизногоТовара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСтавки.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара,
	|	ТаблицаСтавки.Ставка КАК Ставка
	|ПОМЕСТИТЬ ТаблицаСтавки
	|ИЗ
	|	РегистрСведений.СтавкиКатегорийПодакцизныхТоваров.СрезПоследних(&Период, КатегорияПодакцизногоТовара В
	|		(ВЫБРАТЬ
	|			Т.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара
	|		ИЗ
	|			ТаблицаКатегории КАК Т)) КАК ТаблицаСтавки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КатегорияПодакцизногоТовара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	КатегорииПодакцизныхТоваров.Ссылка КАК Категория,
	|	ЕСТЬNULL(ТаблицаСтавки.Ставка, 0) * ТаблицаКатегории.КоэффициентПересчета КАК Ставка,
	|	ЕСТЬNULL(КатегорииПодакцизныхТоваров.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта,
	|	ЕСТЬNULL(КатегорииПодакцизныхТоваров.ТипСтавки,
	|		ЗНАЧЕНИЕ(Перечисление.ТипыСтавокПодакцизнойКатегории.ПустаяСсылка)) КАК ТипСтавки,
	|	ТаблицаСтавки.Ставка ЕСТЬ NULL КАК НеЗаданаСтавка,
	|	КатегорииПодакцизныхТоваров.Ссылка ЕСТЬ NULL КАК НеЗаданаКатегория,
	|	ТаблицаКатегории.КоэффициентПересчета КАК КоэффициентПересчета
	|ПОМЕСТИТЬ ТаблицаДанных
	|ИЗ
	|	ТаблицаКатегории КАК ТаблицаКатегории
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ПО ТаблицаКатегории.Номенклатура = ТаблицаВидыЗапасов.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСтавки КАК ТаблицаСтавки
	|		ПО ТаблицаКатегории.КатегорияПодакцизногоТовара = ТаблицаСтавки.КатегорияПодакцизногоТовара
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КатегорииПодакцизныхТоваров КАК КатегорииПодакцизныхТоваров
	|		ПО ТаблицаКатегории.КатегорияПодакцизногоТовара = КатегорииПодакцизныхТоваров.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсВалюты.Валюта КАК Валюта,
	|	КурсВалюты.КурсЧислитель / КурсВалюты.КурсЗнаменатель КАК КоэффициентПересчета
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Период, (Валюта, БазоваяВалюта) В
	|			(ВЫБРАТЬ
	|				Т.Валюта КАК Валюта,
	|				&БазоваяВалюта КАК БазоваяВалюта
	|			ИЗ
	|				ТаблицаДанных КАК Т)) КАК КурсВалюты
	|ГДЕ
	|	КурсВалюты.КурсЗнаменатель <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанных.НомерСтроки КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДанных.Номенклатура) КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДанных.Категория) КАК Категория,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДанных.Валюта) КАК Валюта,
	|	ВЫРАЗИТЬ(ТаблицаДанных.Ставка * ЕСТЬNULL(КурсыВалют.КоэффициентПересчета, 1) КАК ЧИСЛО(31, 2)) КАК Ставка,
	|	ТаблицаДанных.НеЗаданаСтавка
	|		И ТаблицаДанных.ТипСтавки = &ТвердаяСтавка КАК НеЗаданаСтавка,
	|	ТаблицаДанных.НеЗаданаКатегория КАК НеЗаданаКатегория,
	|	ТаблицаДанных.КоэффициентПересчета = 0
	|		И ТаблицаДанных.ТипСтавки = &ТвердаяСтавка КАК НеЗаданКоэффициентПересчета,
	|	ТаблицаДанных.Валюта <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|		И ТаблицаДанных.Валюта <> &БазоваяВалюта
	|		И КурсыВалют.КоэффициентПересчета ЕСТЬ NULL КАК НеЗаданКурсПересчетаВВалютуРегл
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаДанных
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО ТаблицаДанных.Валюта = КурсыВалют.Валюта";
	
	Если ПустаяСтрока(ИмяКолонкиВидЗапасов) Тогда
		ИмяКолонкиВидЗапасов = "ВидЗапасов";
	КонецЕсли;
	ТекстЗапросаВидЗапасов = СтрШаблон("ВидыЗапасов.%1", ИмяКолонкиВидЗапасов);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаВидЗапасов", ТекстЗапросаВидЗапасов);
	
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СообщитьОбОшибкахРасчетаАкциза(Выборка, ДокументОбъект.Дата, Отказ);
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаВидЗапасов = ТаблицаВидыЗапасов[Выборка.НомерСтроки - 1];
		СтрокаВидЗапасов.СуммаАкциза = СтрокаВидЗапасов.Количество * Выборка.Ставка;
		
	КонецЦикла;
	
КонецПроцедуры
//-- НЕ УТ

#КонецОбласти

#Область ПроверкаЗаполненияПодакцизныхТоваров

// Формирует стандартную структуру параметров для контроля заполнения подакцизных товаров.
//
// Возвращаемое значение:
//  Структура - Параметры:
//  	* ШаблонТекстаОшибки - Строка - Шаблон текста ошибки, отображаемого пользователю
//  	* ИмяТабличнойЧасти - Строка - Имя табличной части, содержащей данные для контроля
//  	* ПредставлениеТабличнойЧасти - Строка - Представление табличной части для отображения в тексте ошибки
//  	* ИмяПоляСОшибкой - Строка - Имя поля, к которому будет привязано сообщение об ошибке
//  	* ИмяПоляОрганизация - Строка -
//  	* ИмяПоляСклад - Строка - Имя поля, содержащего склад для контроля
//  	* ПроверятьСклад - Булево -
//  	* СкладВТабличнойЧасти - Булево -
//  	* ПроверятьСуммуАкциза - Булево -
//  	* СписокСтрок - Неопределено, Массив Из СтрокаТабличнойЧасти - список проверяемых строк табличной части.
//
Функция ПараметрыКонтроляЗаполненияПодакцизныхТоваров() Экспорт
	
	ПараметрыКонтроля = Новый Структура;
	ПараметрыКонтроля.Вставить("ШаблонТекстаОшибки", "");
	ПараметрыКонтроля.Вставить("ИмяТабличнойЧасти", "Товары");
	ПараметрыКонтроля.Вставить("ПредставлениеТабличнойЧасти", НСтр("ru = 'Товары';
																	|en = 'Goods'"));
	ПараметрыКонтроля.Вставить("ИмяПоляСОшибкой", "Номенклатура");
	ПараметрыКонтроля.Вставить("ИмяПоляОрганизация", "Организация");
	ПараметрыКонтроля.Вставить("ИмяПоляСклад", "Склад");
	ПараметрыКонтроля.Вставить("ПроверятьСклад", Ложь);
	ПараметрыКонтроля.Вставить("СкладВТабличнойЧасти", Ложь);
	ПараметрыКонтроля.Вставить("ПроверятьСуммуАкциза", Ложь);
	ПараметрыКонтроля.Вставить("СписокСтрок", Неопределено);
	
	Возврат ПараметрыКонтроля;
	
КонецФункции

// Проверяет корректность заполнения подакцизных товаров в документе.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект - Объект, заполнение которого требуется проверить.
//  ПараметрыКонтроля - см. ПараметрыКонтроляЗаполненияПодакцизныхТоваров.
//  Отказ - Булево - Признак отказа от записи.
//
Процедура КонтрольЗаполненияПодакцизныхТоваров(ДокументОбъект, ПараметрыКонтроля, Отказ) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПодакцизныеТовары") Тогда
		Возврат;
	КонецЕсли;
	
	Организация = ДокументОбъект[ПараметрыКонтроля.ИмяПоляОрганизация];
	СтранаРегистрации = ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(Организация);
	
	Склад = Справочники.Склады.ПустаяСсылка();
	ТекстТаблицаТоварыСклад = "&Склад";
	
	Если ПараметрыКонтроля.ПроверятьСклад Тогда
		Если ПараметрыКонтроля.СкладВТабличнойЧасти Тогда
			ТекстТаблицаТоварыСклад = "ТаблицаТовары." + ПараметрыКонтроля.ИмяПоляСклад;
		Иначе
			Склад = ДокументОбъект[ПараметрыКонтроля.ИмяПоляСклад];
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Период", ДокументОбъект.Дата);
	Запрос.УстановитьПараметр("Страна", СтранаРегистрации);
	Запрос.УстановитьПараметр("ТаблицаТовары", ДокументОбъект[ПараметрыКонтроля.ИмяТабличнойЧасти]);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ТвердаяСтавка", Перечисления.ТипыСтавокПодакцизнойКатегории.Твердая);
	Запрос.УстановитьПараметр("УсловиеОтбораЗаписейТаблицаТовары", Истина);
	Запрос.УстановитьПараметр("УсловиеОтбораЗаписейТаблицаКатегории", Истина);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	&ТаблицаТоварыСклад КАК Склад
	|ПОМЕСТИТЬ ВременнаяТаблицаТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	&УсловиеОтбораЗаписейТаблицаТовары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКатегории.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ ТаблицаКатегории
	|ИЗ
	|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(&Период, (Номенклатура, Страна) В
	|		(ВЫБРАТЬ
	|			Т.Номенклатура,
	|			&Страна
	|		ИЗ
	|			ВременнаяТаблицаТовары КАК Т)) КАК ТаблицаКатегории
	|ГДЕ
	|	ТаблицаКатегории.ПодакцизныйТовар
	|	И &УсловиеОтбораЗаписейТаблицаКатегории
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаТовары.Номенклатура) КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаТовары.Склад) КАК Склад
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаКатегории КАК ТаблицаКатегории
	|		ПО ВременнаяТаблицаТовары.Номенклатура = ТаблицаКатегории.Номенклатура";
	ТекстЗапроса = ТекстЗапроса + ?(ПараметрыКонтроля.ПроверятьСклад, "
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ВременнаяТаблицаТовары.Склад = Склады.Ссылка
	|		И Склады.ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.РозничныйМагазин)", "");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаТоварыСклад", ТекстТаблицаТоварыСклад);
	Если ПараметрыКонтроля.ПроверятьСуммуАкциза Тогда
		ТекстУсловиеОтбораСуммаАкциза = "ТаблицаТовары.СуммаАкциза = 0";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораЗаписейТаблицаТовары", ТекстУсловиеОтбораСуммаАкциза);
		ТекстУсловиеОтбораТипСтавки = "ТаблицаКатегории.КатегорияПодакцизногоТовара.ТипСтавки = &ТвердаяСтавка";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораЗаписейТаблицаКатегории", ТекстУсловиеОтбораТипСтавки);
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ТекстОшибки = ПараметрыКонтроля.ШаблонТекстаОшибки;
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Номенклатура%", Выборка.Номенклатура);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НомерСтроки%", Выборка.НомерСтроки);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Организация%", Организация);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Склад%", Выборка.Склад);
		ТекстОшибки = СтрЗаменить(
			ТекстОшибки, "%ПредставлениеТабличнойЧасти%", ПараметрыКонтроля.ПредставлениеТабличнойЧасти);
		
		ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ПараметрыКонтроля.ИмяТабличнойЧасти, Выборка.НомерСтроки, ПараметрыКонтроля.ИмяПоляСОшибкой);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ДокументОбъект, ПутьКДанным, , Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

//++ НЕ УТ

// Проверяет соответствие категории приходуемого и списываемого подакцизного товара в документе.
//
// Параметры:
//  ДокументОбъект - ДокументОбъект.ПересортицаТоваров,
//  				 ДокументОбъект.ПорчаТоваров,
//  				 ДокументОбъект.ВозвратТоваровОтКлиента - Объект, заполнение которого требуется проверить.
//  ПараметрыКонтроля - см. ПараметрыКонтроляЗаполненияПодакцизныхТоваров.
//  Отказ - Булево - Признак отказа от записи.
//
Процедура КонтрольСоответствияКатегорииПодакцизныхТоваров(ДокументОбъект, ПараметрыКонтроля, Отказ) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПодакцизныеТовары") Тогда
		Возврат;
	КонецЕсли;
	
	Организация = ДокументОбъект[ПараметрыКонтроля.ИмяПоляОрганизация];
	СтранаРегистрации = ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(Организация);
	ВыгружаемыеКолонки = "НомерСтроки, Номенклатура, НоменклатураОприходование";
	ТаблицаТовары = ДокументОбъект[ПараметрыКонтроля.ИмяТабличнойЧасти].Выгрузить(ПараметрыКонтроля.СписокСтрок, ВыгружаемыеКолонки);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Период", ДокументОбъект.Дата);
	Запрос.УстановитьПараметр("Страна", СтранаРегистрации);
	Запрос.УстановитьПараметр("ТаблицаТовары", ТаблицаТовары);
	Запрос.УстановитьПараметр("ПустаяКатегория", Справочники.КатегорииПодакцизныхТоваров.ПустаяСсылка());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.НоменклатураОприходование КАК НоменклатураОприходование
	|ПОМЕСТИТЬ ВременнаяТаблицаТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	НоменклатураОприходование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКатегории.Номенклатура КАК Номенклатура,
	|	ТаблицаКатегории.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара
	|ПОМЕСТИТЬ ТаблицаКатегории
	|ИЗ
	|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(&Период, (Номенклатура, Страна) В
	|		(ВЫБРАТЬ
	|			Т.Номенклатура КАК Номенклатура,
	|			&Страна КАК Страна
	|		ИЗ
	|			ВременнаяТаблицаТовары КАК Т
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			Т.НоменклатураОприходование КАК Номенклатура,
	|			&Страна КАК Страна
	|		ИЗ
	|			ВременнаяТаблицаТовары КАК Т)) КАК ТаблицаКатегории
	|ГДЕ
	|	ТаблицаКатегории.ПодакцизныйТовар
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаТовары.Номенклатура) КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕ(ВременнаяТаблицаТовары.НоменклатураОприходование) КАК НоменклатураОприходование
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКатегории КАК Категории
	|		ПО ВременнаяТаблицаТовары.Номенклатура = Категории.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаКатегории КАК КатегорииОприходование
	|		ПО ВременнаяТаблицаТовары.НоменклатураОприходование = КатегорииОприходование.Номенклатура
	|ГДЕ
	|	ЕСТЬNULL(Категории.КатегорияПодакцизногоТовара,
	|		&ПустаяКатегория) <> ЕСТЬNULL(КатегорииОприходование.КатегорияПодакцизногоТовара, &ПустаяКатегория)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ТекстОшибки = ПараметрыКонтроля.ШаблонТекстаОшибки;
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Номенклатура%", Выборка.Номенклатура);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НоменклатураОприходование%", Выборка.НоменклатураОприходование);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%НомерСтроки%", Выборка.НомерСтроки);
		ТекстОшибки = СтрЗаменить(
			ТекстОшибки, "%ПредставлениеТабличнойЧасти%", ПараметрыКонтроля.ПредставлениеТабличнойЧасти);
		
		ПутьКДанным = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			ПараметрыКонтроля.ИмяТабличнойЧасти, Выборка.НомерСтроки, ПараметрыКонтроля.ИмяПоляСОшибкой);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ДокументОбъект, ПутьКДанным, , Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РасчетКоличестваПодакцизныхТоваров

// Формирует стандартную структуру параметров для расчета количества подакцизных товаров в документе.
//
// Возвращаемое значение:
//  Структура - Параметры:
//   * ИмяТабличнойЧасти - Строка - Имя табличной части, содержащей данные по номенклатуре.
//   * ИмяПоляОрганизация - Строка - Имя поля, содержащего организацию, для которой нужно получить признак ведения
//  						учета подакцизных товаров.
//   * ПараметрыОтбора - Структура, Неопределено - Ключ структуры - идентификатор колонки, а значение структуры -
//  						значение отбора. Если указан отбор, из табличной части будут выгружены строки со
//  						значениями, удовлетворяющими значению отбора.
//
Функция ПараметрыРасчетаКоличестваПодакцизныхТоваров() Экспорт
	
	ПараметрыРасчета = Новый Структура;
	ПараметрыРасчета.Вставить("ИмяТабличнойЧасти", "Товары");
	ПараметрыРасчета.Вставить("ИмяПоляОрганизация", "Организация");
	ПараметрыРасчета.Вставить("ПараметрыОтбора", Неопределено);
	
	Возврат ПараметрыРасчета;
	
КонецФункции

// Запускает фоновое задание по расчету количества подакцизных товаров в документе.
//
// Параметры:
//  ДокументОбъект - ДанныеФормыСтруктура, ДокументОбъект - Документ, для которого нужно выполнить расчет
//  ПараметрыРасчета - Структура, Неопределено - см. УчетПодакцизныхТоваров.ПараметрыРасчетаКоличестваПодакцизныхТоваров
//
// Возвращаемое значение:
//  см. ДлительныеОперации.ВыполнитьВФоне
//
Функция ЗапуститьРасчетКоличестваПодакцизныхТоваров(ДокументОбъект, ПараметрыРасчета = Неопределено) Экспорт
	
	РезультатФоновогоЗадания = Новый Структура;
	
	Если ПараметрыРасчета = Неопределено Тогда
		ПараметрыРасчета = ПараметрыРасчетаКоличестваПодакцизныхТоваров();
	КонецЕсли;
	
	ТабличнаяЧасть = ДокументОбъект[ПараметрыРасчета.ИмяТабличнойЧасти];
	ПараметрыОтбора = ПараметрыРасчета.ПараметрыОтбора;
	ИмяКолонки = "Номенклатура";
	Номенклатура = ТабличнаяЧасть.Выгрузить(ПараметрыОтбора, ИмяКолонки).ВыгрузитьКолонку(ИмяКолонки);
	Организация = ДокументОбъект[ПараметрыРасчета.ИмяПоляОрганизация];
	Страна = ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(Организация);
	Период = ДокументОбъект.Дата;
	
	ПроцедураРасчета = "УчетПодакцизныхТоваров.ВычислитьКоличествоПодакцизныхТоваровВВФоне";
		
	ПараметрыЗадания = Новый Массив;
	ПараметрыЗадания.Добавить(Номенклатура);
	ПараметрыЗадания.Добавить(Страна);
	ПараметрыЗадания.Добавить(Период);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор());
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	РезультатФоновогоЗадания =
		ДлительныеОперации.ВыполнитьВФоне(ПроцедураРасчета, ПараметрыЗадания, ПараметрыВыполнения);
	
	Возврат РезультатФоновогоЗадания;
	
КонецФункции

// Определяет по массиву номенклатуры для указанной страны количество подакцизных товаров и помещает
// значение во временное хранилище.
//
// Параметры:
//  ПараметрыЗадания - Массив из СправочникСсылка.Номенклатура - содержит параметры вызова
//  АдресХранилища - Строка - адрес временного хранилища, по которому будет помещен результат выполнения.
//
Процедура ВычислитьКоличествоПодакцизныхТоваровВВФоне(ПараметрыЗадания, АдресХранилища) Экспорт
	
	Номенклатура = ПараметрыЗадания[0];
	Страна = ПараметрыЗадания[1];
	Период = ПараметрыЗадания[2];
	
	КоличествоПодакцизныхТоваров =
		РегистрыСведений.КатегорииПодакцизныхТоваров.КоличествоПодакцизныхТоваров(Номенклатура, Страна, Период);
	
	ПоместитьВоВременноеХранилище(КоличествоПодакцизныхТоваров, АдресХранилища);
	
КонецПроцедуры

//-- НЕ УТ

#КонецОбласти

#Область ПользовательскийИнтерфейс

// Обновляет заголовок поля формы облагается акцизом после расчета количества подакцизных товаров в документе.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Настраиваемая форма.
//  ОбновлятьДанные - Булево - Если Истина, при необходимости очищаются реквизиты объекта ОблагаетсяАкцизом
//  	и ПодразделениеПроизводства.
//  Период - Дата - период получения настроек учета акцизов.
//
Процедура ОбновитьЭлементыУчетаПодакцизныхТоваров(Форма, ОбновлятьДанные = Истина, Период = Неопределено) Экспорт
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Элементы.ОблагаетсяАкцизом.Видимость = Форма.ИспользоватьПодакцизныеТовары;
	Элементы.ПодразделениеПроизводства.Видимость = Форма.ИспользоватьПодакцизныеТовары;
	//++ НЕ УТ
	Форма.ТребуетсяОбновитьЗаголовокОблагаетсяАкцизом = Форма.ИспользоватьПодакцизныеТовары;
	
	Если ТипЗнч(Объект.Ссылка) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		ИмяПоляОрганизация = "ОрганизацияПолучатель";
	Иначе
		ИмяПоляОрганизация = "Организация";
	КонецЕсли;
	
	Если Период = Неопределено Тогда
		Период = Объект.Дата;
	КонецЕсли;
	
	ИспользуетсяПроизводствоПодакцизныхТоваров = Форма.ИспользоватьПодакцизныеТовары
		И ИспользуетсяПроизводствоПодакцизныхТоваров(Объект[ИмяПоляОрганизация], Период);
	
	Если ОбновлятьДанные И Не ИспользуетсяПроизводствоПодакцизныхТоваров Тогда
		Объект.ОблагаетсяАкцизом = Ложь;
		Объект.ПодразделениеПроизводства = Неопределено;
	КонецЕсли;
	
	Элементы.ОблагаетсяАкцизом.ТолькоПросмотр = Не ИспользуетсяПроизводствоПодакцизныхТоваров;
	Элементы.ПодразделениеПроизводства.Доступность = Объект.ОблагаетсяАкцизом;
	Элементы.ПодразделениеПроизводства.АвтоОтметкаНезаполненного = ?(Объект.ОблагаетсяАкцизом, Неопределено, Ложь);
	//-- НЕ УТ
	
КонецПроцедуры

#КонецОбласти

#Область ПодборВидовЗапасов

// Возвращает параметры формирования текста запроса временной таблицы 'ВТПодакцизныеКатегорииТоваров'.
//
// Возвращаемое значение:
//	Структура - содержит следующие свойства:
//		* ОтборКатегорийПоДате - Булево - признак способа получения данных из регистра. ИСТИНА - по умолчанию означает,
//											что данные будут получены из виртуальной таблицы среза последних.
//		* ИмяТаблицыТоваров - Строка - имя временной таблицы товаров, по которой осуществляется отбор в регистре.
//										По умолчанию - 'ТаблицаТоваров'.
//		* СтранаВТЧ - Булево - признак источника сведений о стране. ЛОЖЬ - по умолчанию означает, что
//								отбор по стране устанавливается в параметрах запроса.
//		* ИмяПоляПериод - Строка - имя поля период во временной таблице товаров. По этому полю осуществляется отбор
//									сведений из регистра.
//
Функция ПараметрыТекстаЗапросаВТПодакцизныеКатегорииТоваров() Экспорт
	
	ПараметрыТекстаЗапроса = Новый Структура;
	ПараметрыТекстаЗапроса.Вставить("ОтборКатегорийПоДате",	Истина);
	ПараметрыТекстаЗапроса.Вставить("ИмяТаблицыТоваров",	"ТаблицаТоваров");
	ПараметрыТекстаЗапроса.Вставить("СтранаВТЧ",			Ложь);
	ПараметрыТекстаЗапроса.Вставить("ИмяПоляПериод",		"Период");
	
	Возврат ПараметрыТекстаЗапроса;
	
КонецФункции

// Возвращает текст запроса временной таблицы 'ВТПодакцизныеКатегорииТоваров', содержащей сведения о категориях
// подакцизных товаров.
// Исполняемый запрос должен содержать временную таблицу товаров, для которых необходимо получить сведения о категориях.
// Так же должны быть заданы параметры запроса 'ИспользоватьПодакцизныеТовары', 'Дата' - если в параметрах текста
// запроса установлен отбор по дате, и 'Страна' - если таблица товаров не содержит одноименную колонку.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц, Неопределено - менеджер временных таблиц.
//	ПараметрыТекстаЗапроса - см. ПараметрыТекстаЗапросаВТПодакцизныеКатегорииТоваров.
//
// Возвращаемое значение:
//	Строка - текст запроса временной таблицы категорий подакцизных товаров.
//
Функция ТекстЗапросаВТПодакцизныеКатегорииТоваров(МенеджерВременныхТаблиц = Неопределено,
													ПараметрыТекстаЗапроса = Неопределено) Экспорт
	
	Если МенеджерВременныхТаблиц <> Неопределено
		И МенеджерВременныхТаблиц.Таблицы.Найти("ВТПодакцизныеКатегорииТоваров") <> Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = "УНИЧТОЖИТЬ ВТПодакцизныеКатегорииТоваров";
		
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Если ПараметрыТекстаЗапроса = Неопределено Тогда
		ПараметрыТекстаЗапроса = ПараметрыТекстаЗапросаВТПодакцизныеКатегорииТоваров();
	КонецЕсли;
	
	ТекстПериод = ?(ПараметрыТекстаЗапроса.ОтборКатегорийПоДате,
					"ТаблицаТоваров.Период",
					"ТаблицаТоваров." + ПараметрыТекстаЗапроса.ИмяПоляПериод);
	ТекстСтрана = ?(ПараметрыТекстаЗапроса.СтранаВТЧ, "ТаблицаТоваров.Страна", "&Страна");
	
	ТекстЗапроса = ?(ПараметрыТекстаЗапроса.ОтборКатегорийПоДате,
					ТекстЗапросаВТПодакцизныеКатегорииТоваровПоДате(),
					ТекстЗапросаВТПодакцизныеКатегорииТоваровПоПериодам());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеПериод", ТекстПериод);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СтранаТЧ", ТекстСтрана);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаТоваров", ПараметрыТекстаЗапроса.ИмяТаблицыТоваров);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область НеоперативноеДопроведение

// Записывает суммы акциза в регистр "Движения по акцизам".
//
// Параметры:
//	ДокументСсылка - ДокументСсылка - ссылка на документ, по которому необходимо записать сумму акцизов.
//
Процедура ЗаписатьСуммыАкциза(ДокументСсылка) Экспорт
	
	НаборЗаписей = Неопределено;
	ТипДокумента = ТипЗнч(ДокументСсылка);
	
	Если ТипДокумента <> Тип("ДокументСсылка.Сторно") Тогда
		
		МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДокументСсылка);
		Результат = МенеджерДокумента.ПолучитьДанныеПоАкцизам(ДокументСсылка);
		
		НаборЗаписей = РегистрыНакопления.ДвиженияПоАкцизам.СоздатьНаборЗаписей();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОтложенноеПроведение");
		НаборЗаписей.ДополнительныеСвойства.Вставить("ТаблицыКонтроля");
		НаборЗаписей.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла;
		
		ДокументСторно = ИсправлениеДокументов.СторноДокумент(ДокументСсылка);
		
		Если ДокументСторно <> Неопределено Тогда
			ДобавитьЗаданиеВОчередьНеоперативногоПроведения(ДокументСторно);
		КонецЕсли;
		
	КонецЕсли;

	Если ТипДокумента = Тип("ДокументСсылка.СписаниеНедостачТоваров")
		Или ТипДокумента = Тип("ДокументСсылка.ВнутреннееПотребление")
		Или ТипДокумента = Тип("ДокументСсылка.ЗаявлениеОВвозеТоваров") Тогда
		
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Исправление, СторнируемыйДокумент, Дата");
		
		Если РеквизитыДокумента.Исправление Тогда
			
			МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(РеквизитыДокумента.СторнируемыйДокумент);
			Результат = МенеджерДокумента.ПолучитьДанныеПоАкцизам(РеквизитыДокумента.СторнируемыйДокумент);
			
			РесурсыРегистра = Метаданные.РегистрыНакопления.ДвиженияПоАкцизам.Ресурсы;
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				НоваяСтрока.Период = РеквизитыДокумента.Дата;
				НоваяСтрока.Сторно = Истина;
				Для Каждого РесурсРегистра Из РесурсыРегистра Цикл
					НоваяСтрока[РесурсРегистра.Имя] = -НоваяСтрока[РесурсРегистра.Имя];
				КонецЦикла;
			КонецЦикла;
			
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.Сторно") Тогда
		
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Дата, СторнируемыйДокумент");
		
		МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(РеквизитыДокумента.СторнируемыйДокумент);
		Результат = МенеджерДокумента.ПолучитьДанныеПоАкцизам(РеквизитыДокумента.СторнируемыйДокумент);
		
		НаборЗаписей = РегистрыНакопления.ДвиженияПоАкцизам.СоздатьНаборЗаписей();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОтложенноеПроведение");
		НаборЗаписей.ДополнительныеСвойства.Вставить("ТаблицыКонтроля");
		НаборЗаписей.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
		
		РесурсыРегистра = Метаданные.РегистрыНакопления.ДвиженияПоАкцизам.Ресурсы;
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Период = РеквизитыДокумента.Дата;
			НоваяСтрока.Сторно = Истина;
			Для Каждого РесурсРегистра Из РесурсыРегистра Цикл
				НоваяСтрока[РесурсРегистра.Имя] = -НоваяСтрока[РесурсРегистра.Имя];
			КонецЦикла;
		КонецЦикла;
			
	КонецЕсли;
	
	Если НаборЗаписей <> Неопределено Тогда
		НаборЗаписей.Записать();
		ПроверитьДатуЗапретаПоТаблицеИзменений(НаборЗаписей);
	КонецЕсли;
	
КонецПроцедуры

// Создает задание на неоперативную запись регистра "Движения по акцизам" по документу.
//
// Параметры:
//	Документ - ДокументОбъект - документ, по которому необходимо записать сумму акцизов.
//  Свойства - См. ПроведениеДокументов.СвойстваДокумента
//
Процедура СоздатьЗаданиеНаЗаписьСуммыАкциза(Документ, Свойства) Экспорт

	Если ЭтоДокументДелаетДвижениеПоИмпортируемымТоварам(Документ)
		// ++ НЕ УТ
		Или (ЭтоДокументДелаетДвижениеПоПроизведеннымПодакцизнымТоварам(Документ)
			И Документ.ДополнительныеСвойства.Свойство("ЗаписатьСуммуАкциза")
			И Документ.ДополнительныеСвойства.ЗаписатьСуммуАкциза)
		// -- НЕ УТ
		Или ЭтоДокументСторно(Документ) Тогда
		
			ДобавитьЗаданиеВОчередьНеоперативногоПроведения(Документ);
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ЗаявлениеОВвозеТоваров

// Подготавливает текст запроса с данными о подакцизных товарах для документа "Заявление о ввозе товаров из ЕАЭС".
//
// Параметры:
//  ИспользоватьПодакцизныеТовары - Булево - Признак использования импорта/производства подакцизных товаров Организацией
//  ИмяТаблицыДляОтбора - Строка - Имя временной таблицы для отбора, содержащей поле "Номенклатура"
//
// Возвращаемое значение:
//  Строка - текст запроса.
//
Функция ТекстЗапросаДанныеПодакцизныхТоваров(ИспользоватьПодакцизныеТовары, ИмяТаблицыДляОтбора) Экспорт
	
	Если ИспользоватьПодакцизныеТовары Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаКатегории.Номенклатура КАК Номенклатура,
		|	ТаблицаКатегории.ПодакцизныйТовар КАК ПодакцизныйТовар,
		|	ТаблицаКатегории.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара,
		|	ТаблицаКатегории.КоэффициентПересчета КАК КоэффициентПересчета
		|ПОМЕСТИТЬ ТаблицаКатегории
		|ИЗ
		|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(&Период, (Номенклатура, Страна) В
		|		(ВЫБРАТЬ
		|			Т.Номенклатура,
		|			&СтранаРегистрации
		|		ИЗ &ИмяТаблицы КАК Т)) КАК ТаблицаКатегории
		|ГДЕ
		|	ТаблицаКатегории.ПодакцизныйТовар
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КатегорияПодакцизногоТовара
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаСтавкиПоКатегориям.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара,
		|	ТаблицаСтавкиПоКатегориям.Ставка КАК Ставка
		|ПОМЕСТИТЬ ТаблицаСтавкиПоКатегориям
		|ИЗ
		|	РегистрСведений.СтавкиКатегорийПодакцизныхТоваров.СрезПоследних(&Период,
		|		КатегорияПодакцизногоТовара В
		|		(ВЫБРАТЬ
		|			Т.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара
		|		ИЗ
		|			ТаблицаКатегории КАК Т)) КАК ТаблицаСтавкиПоКатегориям
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КатегорияПодакцизногоТовара
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаКатегории.Номенклатура КАК Номенклатура,
		|	ТаблицаКатегории.ПодакцизныйТовар КАК ПодакцизныйТовар,
		|	ТаблицаСтавкиПоКатегориям.Ставка КАК Ставка,
		|	КатегорииПодакцизныхТоваров.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаКатегории.КоэффициентПересчета КАК КоэффициентПересчета,
		|	КатегорииПодакцизныхТоваров.Валюта КАК Валюта,
		|	КатегорииПодакцизныхТоваров.ТипСтавки КАК ТипСтавки
		|ПОМЕСТИТЬ ДанныеПодакцизныхТоваров
		|ИЗ
		|	ТаблицаКатегории КАК ТаблицаКатегории
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаСтавкиПоКатегориям КАК ТаблицаСтавкиПоКатегориям
		|		ПО ТаблицаКатегории.КатегорияПодакцизногоТовара = ТаблицаСтавкиПоКатегориям.КатегорияПодакцизногоТовара
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КатегорииПодакцизныхТоваров КАК КатегорииПодакцизныхТоваров
		|		ПО ТаблицаКатегории.КатегорияПодакцизногоТовара = КатегорииПодакцизныхТоваров.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КурсВалюты.Валюта КАК Валюта,
		|	КурсВалюты.КурсЧислитель * КурсВалюты.КурсЗнаменатель КАК КоэффициентПересчета
		|ПОМЕСТИТЬ КурсыВалют
		|ИЗ
		|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(
		|			&Период, (Валюта, БазоваяВалюта) В
		|			(ВЫБРАТЬ
		|				Т.Валюта КАК Валюта,
		|				&БазоваяВалюта КАК БазоваяВалюта
		|			ИЗ
		|				ДанныеПодакцизныхТоваров КАК Т)) КАК КурсВалюты
		|ГДЕ
		|	КурсВалюты.КурсЗнаменатель <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПодакцизныеТовары.Номенклатура КАК Номенклатура,
		|	ПодакцизныеТовары.ПодакцизныйТовар КАК ПодакцизныйТовар,
		|	ПодакцизныеТовары.Ставка КАК Ставка,
		|	ВЫРАЗИТЬ(ПодакцизныеТовары.Ставка * ЕСТЬNULL(КурсыВалют.КоэффициентПересчета, 1)
		|		КАК ЧИСЛО(31, 2)) КАК СтавкаРегл,
		|	ПодакцизныеТовары.ЕдиницаИзмерения КАК ЕдиницаИзмеренияАкциза,
		|	ПодакцизныеТовары.КоэффициентПересчета КАК КоэффициентПересчетаЕдиницИзмерения,
		|	ПодакцизныеТовары.Валюта КАК ВалютаАкциза,
		|	ПодакцизныеТовары.ТипСтавки КАК ТипСтавки
		|ПОМЕСТИТЬ ПодакцизныеТовары
		|ИЗ
		|	ДанныеПодакцизныхТоваров КАК ПодакцизныеТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
		|		ПО ПодакцизныеТовары.Валюта = КурсыВалют.Валюта
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицыДляОтбора);
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
		|	ЛОЖЬ КАК ПодакцизныйТовар,
		|	0 КАК Ставка,
		|	0 КАК СтавкаРегл,
		|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК ЕдиницаИзмеренияАкциза,
		|	1 КАК КоэффициентПересчетаЕдиницИзмерения,
		|	&БазоваяВалюта КАК ВалютаАкциза,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыСтавокПодакцизнойКатегории.ПустаяСсылка) КАК ТипСтавки
		|ПОМЕСТИТЬ ПодакцизныеТовары
		|ГДЕ
		|	ЛОЖЬ";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Подготавливает текст запроса с данными об ошибках при расчете акциза в документе "Заявление о ввозе товаров из ЕАЭС".
//
// Параметры:
//  ИспользоватьПодакцизныеТовары - Булево - Признак использования импорта/производства подакцизных товаров Организацией
//
// Возвращаемое значение:
//  Строка - текст запроса.
//
Функция ТекстЗапросаОшибкиРасчетаАкциза(ИспользоватьПодакцизныеТовары) Экспорт
	
	Если ИспользоватьПодакцизныеТовары Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	*
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаКатегории.Номенклатура КАК Номенклатура,
		|		ТаблицаКатегории.КатегорияПодакцизногоТовара КАК Категория,
		|		ЕСТЬNULL(ТаблицаПодакцизныеТовары.ВалютаАкциза, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта,
		|		ТаблицаКатегории.КатегорияПодакцизногоТовара =
		|			ЗНАЧЕНИЕ(Справочник.КатегорииПодакцизныхТоваров.ПустаяСсылка) КАК НеЗаданаКатегория,
		|		ТаблицаСтавки.Ставка ЕСТЬ NULL КАК НеЗаданаСтавка,
		|		ТаблицаПодакцизныеТовары.ТипСтавки ЕСТЬ NULL
		|			ИЛИ ТаблицаПодакцизныеТовары.КоэффициентПересчетаЕдиницИзмерения = 0
		|				И ТаблицаПодакцизныеТовары.ТипСтавки = &ТвердаяСтавка КАК НеЗаданКоэффициентПересчета,
		|		ТаблицаПодакцизныеТовары.ВалютаАкциза ЕСТЬ NULL
		|			ИЛИ ТаблицаПодакцизныеТовары.ВалютаАкциза <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
		|				И ТаблицаПодакцизныеТовары.ВалютаАкциза <> &БазоваяВалюта
		|				И КурсыВалют.КоэффициентПересчета ЕСТЬ NULL КАК НеЗаданКурсПересчетаВВалютуРегл
		|	ИЗ
		|		ТаблицаКатегории КАК ТаблицаКатегории
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПодакцизныеТовары КАК ТаблицаПодакцизныеТовары
		|			ПО ТаблицаКатегории.Номенклатура = ТаблицаПодакцизныеТовары.Номенклатура
		|			ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСтавкиПоКатегориям КАК ТаблицаСтавки
		|			ПО ТаблицаКатегории.КатегорияПодакцизногоТовара = ТаблицаСтавки.КатегорияПодакцизногоТовара
		|			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
		|			ПО ТаблицаПодакцизныеТовары.ВалютаАкциза = КурсыВалют.Валюта) КАК ВложенныйЗапрос
		|ГДЕ
		|	ВложенныйЗапрос.НеЗаданаКатегория
		|	ИЛИ ВложенныйЗапрос.НеЗаданаСтавка
		|	ИЛИ ВложенныйЗапрос.НеЗаданКоэффициентПересчета
		|	ИЛИ ВложенныйЗапрос.НеЗаданКурсПересчетаВВалютуРегл";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
		|	ЗНАЧЕНИЕ(Справочник.КатегорииПодакцизныхТоваров.ПустаяСсылка) КАК Категория,
		|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
		|	ЛОЖЬ КАК НеЗаданаКатегория,
		|	ЛОЖЬ КАК НеЗаданаСтавка,
		|	ЛОЖЬ КАК НеЗаданКоэффициентПересчета,
		|	ЛОЖЬ КАК НеЗаданКурсПересчетаВВалютуРегл
		|ГДЕ
		|	ЛОЖЬ";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Обрабатывает ошибки расчета акциза на основе результата запроса.
//
// Параметры:
//   РезультатЗапроса - РезультатЗапроса - Результат запроса, содержащий данные для обработки ошибок.
//   ДатаСтавки - Дата - Дата проверки ставки акциза, для вывода в текст сообщения.
//
Процедура ОбработатьОшибкиРасчетаАкциза(РезультатЗапроса, ДатаСтавки) Экспорт
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СообщитьОбОшибкахРасчетаАкциза(Выборка, ДатаСтавки);
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет дополнительные данные для расчета суммы акциза в таблице Товары.
//
// Параметры:
//  ДокументОбъект - ДанныеФормыКоллекция - Заполняемый документ
//
Процедура ЗаполнитьДополнительныеПризнакиТоваровВЗаявленииОВвозе(ДокументОбъект) Экспорт
	
	БазоваяВалюта = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДокументОбъект.Организация);
	СтранаРегистрации = ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(ДокументОбъект.Организация);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Номенклатура", ДокументОбъект.Товары.Выгрузить().ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("Период", ДокументОбъект.Дата);
	Запрос.УстановитьПараметр("Организация", ДокументОбъект.Организация);
	Запрос.УстановитьПараметр("БазоваяВалюта", БазоваяВалюта);
	Запрос.УстановитьПараметр("Страна", СтранаРегистрации);
	Запрос.УстановитьПараметр("ПустойТипСтавки", Перечисления.ТипыСтавокПодакцизнойКатегории.ПустаяСсылка());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаКатегории.Номенклатура КАК Номенклатура,
	|	ТаблицаКатегории.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	ЕСТЬNULL(ТаблицаКатегории.КатегорияПодакцизногоТовара.Валюта, &БазоваяВалюта) КАК ВалютаАкциза,
	|	ЕСТЬNULL(ТаблицаКатегории.КатегорияПодакцизногоТовара.ТипСтавки, &ПустойТипСтавки) КАК ТипСтавки
	|ИЗ
	|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(&Период, Номенклатура В (&Номенклатура)
	|	И Страна = &Страна) КАК ТаблицаКатегории
	|ГДЕ
	|	ТаблицаКатегории.ПодакцизныйТовар";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОтборСтрок = Новый Структура("Номенклатура", Выборка.Номенклатура);
		НайденныеСтроки = ДокументОбъект.Товары.НайтиСтроки(ОтборСтрок);
		Для Каждого СтрокаТаблицы Из НайденныеСтроки Цикл
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Возвращает текст запроса временной таблицы ВтПодакцизныеТовары. В запросе должен быть установлен параметр с
// типом Дата и параметр с типом СправочникСсылка.СтраныМира, имена параметров можно передать в метод.
// 
// Параметры:
//  ИмяТаблицыДляОтбора - Строка - Имя временной таблицы для отбора, содержащей поле "Номенклатура".
//  ИмяПараметраДата - Строка - Имя параметра, имеющего тип Дата. Имя по умолчанию - "Дата".
//  ИмяПараметраСтрана - Строка - Имя параметра, имеющего тип СправочникСсылка.СтраныМира. Имя по умолчанию - "Страна".
//
// Возвращаемое значение:
//  Строка - текст запроса.
//
Функция ТекстЗапросаВтПодакцизныеТовары(ИмяТаблицыДляОтбора, ИмяПараметраДата = "", ИмяПараметраСтрана = "") Экспорт
	
	ИспользоватьПодакцизныеТовары = ПолучитьФункциональнуюОпцию("ИспользоватьПодакцизныеТовары");
	
	Если ИспользоватьПодакцизныеТовары Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаКатегории.Номенклатура КАК Номенклатура,
		|	ТаблицаКатегории.ПодакцизныйТовар КАК ПодакцизныйТовар
		|ПОМЕСТИТЬ ВтПодакцизныеТовары
		|ИЗ
		|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(&Дата, (Номенклатура, Страна) В
		|		(ВЫБРАТЬ
		|			Т.Номенклатура КАК Номенклатура,
		|			&Страна КАК Страна
		|		ИЗ
		|			&ИмяТаблицы КАК Т)) КАК ТаблицаКатегории
		|ГДЕ
		|	ТаблицаКатегории.ПодакцизныйТовар
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицыДляОтбора);
		
		Если ЗначениеЗаполнено(ИмяПараметраДата) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Дата", СтрШаблон("&%1", ИмяПараметраДата));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИмяПараметраСтрана) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Страна", СтрШаблон("&%1", ИмяПараметраСтрана));
		КонецЕсли;
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
		|	ЛОЖЬ КАК ПодакцизныйТовар
		|ПОМЕСТИТЬ ВтПодакцизныеТовары
		|ГДЕ
		|	ЛОЖЬ";
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для получения номеров строк, по которым необходимо очистить сумму акциза.
//
// Возвращаемое значение:
//  Строка - текст запроса.
//
Функция ТекстЗапросаНомераСтрокДляОчисткиСуммыАкциза() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	&Страна КАК Страна
	|ПОМЕСТИТЬ ВтТовары
	|ИЗ
	|	&Товары КАК Товары
	|ГДЕ
	|	Товары.СуммаАкциза <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Страна
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКатегории.Номенклатура КАК Номенклатура,
	|	ТаблицаКатегории.ПодакцизныйТовар И &ИспользоватьПодакцизныеТовары КАК ПодакцизныйТовар
	|ПОМЕСТИТЬ ВтПодакцизныеТовары
	|ИЗ
	|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(&Дата, (Номенклатура, Страна) В
	|		(ВЫБРАТЬ
	|			Товары.Номенклатура КАК Номенклатура,
	|			Товары.Страна КАК Страна
	|		ИЗ
	|			ВтТовары КАК Товары)) КАК ТаблицаКатегории
	|ГДЕ
	|	ТаблицаКатегории.ПодакцизныйТовар
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВтТовары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПодакцизныеТовары КАК ПодакцизныеТовары
	|		ПО Товары.Номенклатура = ПодакцизныеТовары.Номенклатура
	|ГДЕ
	|	НЕ ЕСТЬNULL(ПодакцизныеТовары.ПодакцизныйТовар, ЛОЖЬ)";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПодборВидовЗапасов

Функция ТекстЗапросаВТПодакцизныеКатегорииТоваровПоДате()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КатегорииТоваров.Период				КАК Период,
	|	КатегорииТоваров.Номенклатура		КАК Номенклатура,
	|	КатегорииТоваров.Страна				КАК Страна,
	|	КатегорииТоваров.ПодакцизныйТовар	КАК ПодакцизныйТовар,
	|	КатегорииТоваров.КатегорияПодакцизногоТовара КАК КатегорияТовара
	|ПОМЕСТИТЬ ВТПодакцизныеКатегорииТоваров
	|ИЗ
	|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(
	|			&Дата,
	|			(Номенклатура, Страна) В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ТаблицаТоваров.Номенклатура	КАК Номенклатура,
	|					&СтранаТЧ					КАК Страна
	|				ИЗ
	|					&ТаблицаТоваров КАК ТаблицаТоваров)
	|			И &ИспользоватьПодакцизныеТовары
	|		) КАК КатегорииТоваров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТПодакцизныеКатегорииТоваровПоПериодам()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&ПолеПериод							КАК Период,
	|	ТаблицаТоваров.Номенклатура			КАК Номенклатура,
	|	КатегорииТоваров.Страна				КАК Страна,
	|	МАКСИМУМ(КатегорииТоваров.Период)	КАК ПериодДействия
	|ПОМЕСТИТЬ ВТПодакцизныеКатегорииПоПериодам
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КатегорииПодакцизныхТоваров КАК КатегорииТоваров
	|		ПО ТаблицаТоваров.Номенклатура = КатегорииТоваров.Номенклатура
	|			И &ПолеПериод >= КатегорииТоваров.Период
	|			И &СтранаТЧ = КатегорииТоваров.Страна
	|			И &ИспользоватьПодакцизныеТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	&ПолеПериод,
	|	ТаблицаТоваров.Номенклатура,
	|	КатегорииТоваров.Страна
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПериодДействия,
	|	Номенклатура,
	|	Страна
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Период				КАК Период,
	|	ТаблицаТоваров.Номенклатура			КАК Номенклатура,
	|	ТаблицаТоваров.Страна				КАК Страна,
	|	КатегорииТоваров.ПодакцизныйТовар	КАК ПодакцизныйТовар,
	|	КатегорииТоваров.КоэффициентПересчета КАК КоэффициентПересчета,
	|	КатегорииТоваров.КатегорияПодакцизногоТовара КАК КатегорияТовара
	|ПОМЕСТИТЬ ВТПодакцизныеКатегорииТоваров
	|ИЗ
	|	ВТПодакцизныеКатегорииПоПериодам КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КатегорииПодакцизныхТоваров КАК КатегорииТоваров
	|		ПО ТаблицаТоваров.Номенклатура = КатегорииТоваров.Номенклатура
	|			И ТаблицаТоваров.ПериодДействия = КатегорииТоваров.Период
	|			И ТаблицаТоваров.Страна = КатегорииТоваров.Страна
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПодакцизныеКатегорииПоПериодам";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

// Сообщает об ошибках расчета акциза.
//
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса - Выборка, содержащая поля НеЗаданаКатегория, НеЗаданКоэффициентПересчета,
//  	НеЗаданКурсПересчетаВВалютуРегл, НеЗаданаСтавка с типом Булево.
//  Период - Дата - Дата для подстановки в текст сообщения.
//  Отказ - Булево - Признак отказа от записи.
//
Процедура СообщитьОбОшибкахРасчетаАкциза(Выборка, Период, Отказ = Ложь)
	
	ШаблонСообщения = НСтр("ru = '%ТекстСообщения%. Сумма акциза не рассчитана.';
							|en = '%ТекстСообщения%. Excise amount is not calculated.'");
	
	Если Выборка.НеЗаданаКатегория Тогда
		ТекстСообщения = НСтр("ru = 'Не задана категория для подакцизного товара ""%Номенклатура%""';
								|en = 'Category for the ""%Номенклатура%"" excisable goods is not specified'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номенклатура%", Выборка.Номенклатура);
		ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%ТекстСообщения%", ТекстСообщения);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Выборка.Категория) Тогда
		Возврат;
	КонецЕсли;
	
	Если Выборка.НеЗаданКоэффициентПересчета Тогда
		ТекстСообщения = НСтр("ru = 'Не задан коэффициент пересчета для категории ""%Категория%"" подакцизного товара ""%Номенклатура%""';
								|en = 'Conversion ratio is not set for the ""%Категория%"" category of the ""%Номенклатура%"" excisable goods'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Категория%", Выборка.Категория);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номенклатура%", Выборка.Номенклатура);
		ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%ТекстСообщения%", ТекстСообщения);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
	Если Выборка.НеЗаданКурсПересчетаВВалютуРегл Тогда
		ТекстСообщения = НСтр("ru = 'Не задан курс пересчета в валюту регламентированного учета для валюты %Валюта% категории ""%Категория%"" подакцизного товара ""%Номенклатура%""';
								|en = 'Conversion rate to the local accounting currency is not set for the %Валюта% currency of the ""%Категория%"" category of the ""%Номенклатура%"" excisable goods'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Валюта%", Выборка.Валюта);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Категория%", Выборка.Категория);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номенклатура%", Выборка.Номенклатура);
		ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%ТекстСообщения%", ТекстСообщения);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
	Если Выборка.НеЗаданаСтавка Тогда
		ТекстСообщения = НСтр("ru = 'На %Дата% не задана ставка для категории ""%Категория%"" подакцизного товара ""%Номенклатура%""';
								|en = 'The rate for the ""%Категория%"" category of the ""%Номенклатура%"" excisable goods is not set as of %Дата%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Дата%", Формат(Период, "ДЛФ=D;"));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Категория%", Выборка.Категория);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номенклатура%", Выборка.Номенклатура);
		ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%ТекстСообщения%", ТекстСообщения);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЗаданиеВОчередьНеоперативногоПроведения(Документ)

	Запрос = Новый Запрос;

	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Документ)) Тогда
		ДокументСсылка = Документ;
	Иначе
		ДокументСсылка = Документ.Ссылка;
	КонецЕсли;

	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	Запрос.Текст = "ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Документ,
	|	ДанныеДокумента.Дата КАК ПериодЗадания,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоАкцизам) КАК ВидЗадания
	|ИЗ
	|	&Документ КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Организация", "ДанныеДокумента.ОрганизацияПолучатель"); // @query-part
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Организация", "ДанныеДокумента.Организация"); // @query-part
	КонецЕсли;
		 
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Документ", "Документ." + Документ.Метаданные().Имя); // @query-part
	
	ДанныеКРегистрацииЗаданий = Запрос.Выполнить().Выгрузить();
	
	МенеджерВременныхТаблиц = Неопределено;
	
	Источник = РегистрыНакопления.ДвиженияПоАкцизам.СоздатьНаборЗаписей();
	МетаданныеРегистра = Метаданные.РегистрыНакопления.ДвиженияПоАкцизам;
	МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеРегистра.ПолноеИмя());
	
	ВидыЗаданийКРегистрацииВОчереди = НеоперативнаяОчередьЗаданий.ВидыЗаданийКРегистрацииВОчереди(МетаданныеРегистра);
	
	РегистрОчереди = Метаданные.РегистрыСведений.Найти("ЗаданияНеоперативногоДопроведенияДокументов");
	ИндетификаторЗаданийНеоперативногДопроведения = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(РегистрОчереди.ПолноеИмя());
	
	Если ВидыЗаданийКРегистрацииВОчереди.Количество() > 0
		И ВидыЗаданийКРегистрацииВОчереди.Найти(ИндетификаторЗаданийНеоперативногДопроведения, "РегистрОчереди") = Неопределено Тогда
		
		МассивВидов = Новый Массив;
		МассивВидов.Добавить(Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоАкцизам);
		
		РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.СоздатьЗадания(МенеджерРегистра, МассивВидов, Источник,
			МенеджерВременныхТаблиц, ДанныеКРегистрацииЗаданий);
		
	КонецЕсли;
	
	Для Каждого ТекущиеДанные Из ВидыЗаданийКРегистрацииВОчереди Цикл
		
		МетаданныеРегистраЗаданий = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ТекущиеДанные.РегистрОчереди);
		МенеджерРегистраЗаданий = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеРегистраЗаданий.ПолноеИмя());
		
		МенеджерРегистраЗаданий.СоздатьЗадания(МенеджерРегистра, ТекущиеДанные.ВидыЗаданий, Источник,
			МенеджерВременныхТаблиц, ДанныеКРегистрацииЗаданий);
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает признак того, что документ может делать начисление по акцизам импортируемых товаров.
//
// Параметры:
//	Документ - ДокументОбъект, ДокументСсылка - ссылка на документ.
//
// Возвращаемое значение:
//	Булево - признак, что документ может делать начисление по акцизам.
//
Функция ЭтоДокументДелаетДвижениеПоИмпортируемымТоварам(Документ)
	
	ТипДокумента = ТипЗнч(Документ);
	Результат = Ложь;
	
	Если ТипДокумента = Тип("ДокументОбъект.ТаможеннаяДекларацияИмпорт")
		Или ТипДокумента = Тип("ДокументОбъект.ЗаявлениеОВвозеТоваров") Тогда
		
		Результат = ИспользуетсяИмпортПодакцизныхТоваров(Документ.Организация, Документ.Дата);
		
	ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт")
		Или ТипДокумента = Тип("ДокументСсылка.ЗаявлениеОВвозеТоваров") Тогда
		
		Результат = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает признак того, что документ сторно по документу,
// который может делать начисление по акцизам.
//
// Параметры:
//	Документ - ДокументОбъект, ДокументСсылка - ссылка на документ.
//
// Возвращаемое значение:
//	Булево - признак, что документ может делать начисление по акцизам.
//
Функция ЭтоДокументСторно(Документ)
	
	ТипДокумента = ТипЗнч(Документ);
	Результат = Ложь;
	
	Если ТипДокумента = Тип("ДокументОбъект.Сторно") Тогда
		
		Если ЭтоДокументДелаетДвижениеПоИмпортируемымТоварам(Документ.СторнируемыйДокумент) Тогда
			
			Результат = Истина;
		
		//++ НЕ УТ
		ИначеЕсли ЭтоДокументДелаетДвижениеПоПроизведеннымПодакцизнымТоварам(Документ.СторнируемыйДокумент) Тогда
		
			Результат = Истина;

		//-- НЕ УТ
		
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции
	
//++ НЕ УТ

// Возвращает признак того, что документ может делать начисление по акцизам
// для производимых подакцизных товаров.
//
// Параметры:
//	Документ - ДокументСсылка, ДокументОбъект - ссылка на документ.
//
// Возвращаемое значение:
//	Булево - признак, что документ может делать начисление по акцизам.
//
Функция ЭтоДокументДелаетДвижениеПоПроизведеннымПодакцизнымТоварам(Документ)
	
	ТипДокумента = ТипЗнч(Документ);
	Результат = Ложь;
	
	Если ТипДокумента = Тип("ДокументСсылка.РеализацияТоваровУслуг")
		Или ТипДокумента = Тип("ДокументСсылка.КорректировкаРеализации")
		Или ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
		Или ТипДокумента = Тип("ДокументСсылка.СписаниеНедостачТоваров")
		Или ТипДокумента = Тип("ДокументСсылка.ВнутреннееПотребление")
		Или ТипДокумента = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
		Или ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
		Или ТипДокумента = Тип("ДокументОбъект.РеализацияТоваровУслуг")
		Или ТипДокумента = Тип("ДокументОбъект.КорректировкаРеализации")
		Или ТипДокумента = Тип("ДокументОбъект.ВозвратТоваровОтКлиента")
		Или ТипДокумента = Тип("ДокументОбъект.СписаниеНедостачТоваров")
		Или ТипДокумента = Тип("ДокументОбъект.ВнутреннееПотребление")
		Или ТипДокумента = Тип("ДокументОбъект.ПередачаТоваровМеждуОрганизациями")
		Или ТипДокумента = Тип("ДокументОбъект.ВозвратТоваровМеждуОрганизациями") Тогда
		
		Результат = ПолучитьФункциональнуюОпцию("ИспользоватьПодакцизныеТовары");
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

//-- НЕ УТ

Процедура ПроверитьДатуЗапретаПоТаблицеИзменений(НаборЗаписей)

		Запрос = Новый Запрос();
		Запрос.МенеджерВременныхТаблиц = НаборЗаписей.ДополнительныеСвойства.МенеджерВременныхТаблиц;
		
		Отказ = Ложь;
		ТекстВложенногоЗапроса = "";
		ТекстЗапросаВременныхТаблиц = "";
		ИменаВременныхТаблиц = "";

		ШаблонЗапроса = "
		|ВЫБРАТЬ
		|	МИНИМУМ(Таблица.Период) КАК Период,
		|	Таблица.Организация КАК Организация,
		|	Таблица.ИмяРегистра КАК ИмяРегистра,
		|	Таблица.РазделДатыЗапрета КАК РазделДатыЗапрета
		|ИЗ
		|	&КоллекцияДанных КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Организация,
		|	Таблица.РазделДатыЗапрета,
		|	Таблица.ИмяРегистра
		|;
		|";
		
		Механизмы = Новый СписокЗначений;
		Механизм = "ПодакцизныеТовары";
		
		МодульМеханизма = ОбщегоНазначения.ОбщийМодуль(ПроведениеДокументов.УчетныеМеханизмыКонфигурации()[Механизм]);
		
		СоответствиеЗапросов = МодульМеханизма.ТекстыЗапросовКонтрольДатыЗапретаПоТаблицеИзменений(Запрос);
		
		Для Каждого ЭлементСоответствия Из СоответствиеЗапросов Цикл
			ЗакрытиеМесяцаСервер.ДополнитьТекстЗапросаЗаданий(ЭлементСоответствия.Ключ,
				ЭлементСоответствия.Значение,
				Запрос.МенеджерВременныхТаблиц.Таблицы,
				ТекстВложенногоЗапроса,
				ТекстЗапросаВременныхТаблиц,
				ИменаВременныхТаблиц);
		КонецЦикла;

		ТекстУничтожитьВт = ЗакрытиеМесяцаСервер.ТестЗапросаУничтоженияВременныхТаблиц(ИменаВременныхТаблиц);
		
		Если ЗначениеЗаполнено(ТекстВложенногоЗапроса) Тогда
			Запрос.Текст = ТекстЗапросаВременныхТаблиц
				+ СтрЗаменить(ШаблонЗапроса, "&КоллекцияДанных", "(" + ТекстВложенногоЗапроса + ")")
				+ ТекстУничтожитьВт;
				
			Выборка = Запрос.Выполнить().Выбрать();
			
			Пока Выборка.Следующий() Цикл
				ДанныеДляПроверки = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
				
				НоваяСтрока = ДанныеДляПроверки.Добавить();
				НоваяСтрока.Дата   = НачалоДня(Выборка.Период);
				НоваяСтрока.Объект = Выборка.Организация;
				НоваяСтрока.Раздел = Выборка.РазделДатыЗапрета;
				
				ОписаниеОшибки = НСтр("ru = 'Изменяемые данные находятся в закрытом периоде.';
										|en = 'The data being changed is in closed period.'");
				Если НаборЗаписей.ДополнительныеСвойства.Свойство("ПроверкаДатыЗапретаИзменения") 
						И НЕ НаборЗаписей.ДополнительныеСвойства.ПроверкаДатыЗапретаИзменения Тогда
					Продолжить
				КонецЕсли;
				Если НаборЗаписей.ДополнительныеСвойства.Свойство("ПропуститьПроверкуЗапретаИзменения") 
						И НаборЗаписей.ДополнительныеСвойства.ПропуститьПроверкуЗапретаИзменения Тогда
					Продолжить
				КонецЕсли;
				Если ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ДанныеДляПроверки, 
						Новый Структура("НоваяВерсия, Данные", Истина, НаборЗаписей),
						ОписаниеОшибки) Тогда
					Если Отказ Тогда
						ВызватьИсключение ОписаниеОшибки;
					КонецЕсли;
				КонецЕсли;

			КонецЦикла;
			
		КонецЕсли;
		
КонецПроцедуры
	
#КонецОбласти
