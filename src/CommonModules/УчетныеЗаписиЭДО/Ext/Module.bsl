
#Область СлужебныйПрограммныйИнтерфейс

#Область АвторизацияВСервисеЭДО

// Возвращаемое значение:
//  Структура:
//  * ЗашифрованныеМаркеры - см. КриптографияБЭДКлиентСервер.НовыйНаборДанныхДляРасшифровки
//  * РасшифрованныеМаркеры - см. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  * ИдентификаторыЭДО - Массив из Строка
//  * ПредставленияУчетныхЗаписейЭДО - Соответствие из КлючИЗначение:
//    ** Ключ - Строка - идентификатор ЭДО.
//    ** Значение - Строка - представление учетной записи ЭДО.
Функция НовыйРезультатАвторизацииВСервисеЭДО() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ЗашифрованныеМаркеры", Новый Соответствие);
	Результат.Вставить("РасшифрованныеМаркеры", Новый Соответствие);
	Результат.Вставить("КонтекстДиагностики", ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики());
	Результат.Вставить("ИдентификаторыЭДО", Новый Массив);
	Результат.Вставить("ПредставленияУчетныхЗаписейЭДО", Новый Соответствие);
	Возврат Результат;
	
КонецФункции

// Параметры:
//  РезультатыПолученияОтпечатковНаКлиенте - см. КриптографияБЭДКлиентСервер.НовыеРезультатыПолученияОтпечатков
//  ИдентификаторыЭДО - Массив из Строка
//  ВыбранныеСертификаты -  Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  
// Возвращаемое значение:
//  См. НовыйРезультатАвторизацииВСервисеЭДО
Функция АвторизоватьсяВСервисеЭДО(РезультатыПолученияОтпечатковНаКлиенте, ИдентификаторыЭДО = Неопределено,
	ВыбранныеСертификаты = Неопределено) Экспорт
	
	РезультатАвторизации = НовыйРезультатАвторизацииВСервисеЭДО();
	
	КонтекстДиагностики = РезультатАвторизации.КонтекстДиагностики;
	
	Если ИдентификаторыЭДО = Неопределено Тогда
		ИдентификаторыЭДО = ИдентификаторыУчетныхЗаписейСпособаОбмена(
			Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО);
	КонецЕсли;
	
	ИдентификаторыПоИнтеграциямЭДО = ИдентификаторыПоИнтеграциямЭДО(ИдентификаторыЭДО);
	Если ЗначениеЗаполнено(ИдентификаторыПоИнтеграциямЭДО.ИдентификаторыДляЛокальногоЭДО) Тогда
		
		ВидОперации = УчетныеЗаписиЭДОКлиентСервер.ВидОперацииАвторизацияВСервисеЭДО();
		РезультатыПолученияОтпечатков = КриптографияБЭД.ПолучитьОтпечаткиСертификатов(ВидОперации, КонтекстДиагностики,
			РезультатыПолученияОтпечатковНаКлиенте);
		
		ДанныеДляСинхронизации = СинхронизацияЭДО.ДанныеДляСинхронизации(РезультатыПолученияОтпечатков,
			КонтекстДиагностики, ИдентификаторыЭДО, ВыбранныеСертификаты);
		
		КлючиСинхронизации = ДанныеДляСинхронизации.КлючиСинхронизации;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
			РезультатАвторизации.ЗашифрованныеМаркеры,
			СинхронизацияЭДО.ЗашифрованныеМаркерыИзКлючейСинхронизации(КлючиСинхронизации));
			
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
			РезультатАвторизации.РасшифрованныеМаркеры,
			СинхронизацияЭДО.РасшифрованныеМаркерыИзКлючейСинхронизации(КлючиСинхронизации));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторыПоИнтеграциямЭДО.ИдентификаторыДляОблачногоЭДО)
		И НастройкиЭДО.ИспользоватьИнтеграциюОблачногоЭДО() Тогда
		ОбщийМодульУчетныеЗаписиЭДОИнтеграцияОблака = ОбщегоНазначения.ОбщийМодуль(
			"УчетныеЗаписиЭДОИнтеграцияОблака");
			
		РезультатАвторизацииВОблаке = ОбщийМодульУчетныеЗаписиЭДОИнтеграцияОблака.АвторизоватьсяВСервисеЭДО(
			ИдентификаторыПоИнтеграциямЭДО.ИдентификаторыДляОблачногоЭДО, 
			РезультатыПолученияОтпечатковНаКлиенте, ВыбранныеСертификаты);
		
		ОбработкаНеисправностейБЭДКлиентСервер.ДополнитьКонтекстДиагностики(
			КонтекстДиагностики, РезультатАвторизацииВОблаке.КонтекстДиагностики);
			
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
			РезультатАвторизации.ЗашифрованныеМаркеры,
			РезультатАвторизацииВОблаке.ЗашифрованныеМаркеры);
			
	КонецЕсли;
	
	РезультатАвторизации.ПредставленияУчетныхЗаписейЭДО = ПредставленияУчетныхЗаписейЭДО(ИдентификаторыЭДО);
	
	Возврат РезультатАвторизации;
	
КонецФункции // АвторизацияВСервисеЭДО

#КонецОбласти

// Возвращает результат запроса с идентификатором ЭДО всех учетных записией
//
// Возвращаемое значение:
//  РезультатЗапроса
Функция СписокВсехУчетныхЗаписей() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетныеЗаписиЭДО.ИдентификаторЭДО
	|ИЗ
	|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗапроса;
	
КонецФункции

// Возвращает описание запроса, в результате которого будут содержаться учетные записи.
// Запрос содержит следующие поля:
//  * ИдентификаторЭДО - Строка - идентификатор учетной записи
//  * НаименованиеУчетнойЗаписи - Строка - наименование учетной записи
//  * НазначениеУчетнойЗаписи - Строка - назначение учетной записи
//  * ПодробноеОписаниеУчетнойЗаписи - Строка - подробное описание учетной записи
//  * Организация - ОпределяемыйТип.Организация
//  * ОператорЭДО - Строка - идентификатор оператора электронного документооборота
//  * СпособОбменаЭД - ПеречислениеСсылка.СпособыОбменаЭД
//  * ПринятыУсловияИспользования - Булево - пользователь принял условия использования сервиса
//                                  электронного документооборота
//  * Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  * ВозможенОбменБезПодписи - Булево
//  * ИмяФайлаСоглашенияНаРоуминг - Строка
//  * ДанныеСоглашенияНаРоуминг - ДвоичныеДанные
//  * ДатаСоглашенияНаРоуминг - Дата
//  * КаталогОбмена - Строка
//  * ПутьFTP - Строка
//  * УчетнаяЗаписьЭлектроннойПочты - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты
//  * КодНалоговогоОргана - Строка.
// 
// Параметры:
// 	ИмяВременнойТаблицы - Строка - таблица, в которую будет помещен результат запроса
// 	Отбор - см. НовыйОтборУчетныхЗаписей
// Возвращаемое значение:
// 	см. ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса
Функция ЗапросУчетныхЗаписей(ИмяВременнойТаблицы, Отбор = Неопределено) Экспорт
	
	Если Отбор = Неопределено Тогда
		Отбор = НовыйОтборУчетныхЗаписей();
	КонецЕсли;
	
	ПоляУсловия = Новый Массив;
	Если Отбор.УчетныеЗаписи <> "" Тогда
		ПоляУсловия.Добавить(СтрШаблон("ИдентификаторЭДО В(%1)", Отбор.УчетныеЗаписи));
	КонецЕсли;
	Если Отбор.Организация <> "" Тогда
		ПоляУсловия.Добавить(СтрШаблон("Организация В (%1)", Отбор.Организация));
	КонецЕсли;
	Если Отбор.СпособОбмена <> "" Тогда
		ПоляУсловия.Добавить(СтрШаблон("СпособОбменаЭД В (%1)", Отбор.СпособОбмена));
	КонецЕсли;
	Если Отбор.Оператор <> "" Тогда
		ПоляУсловия.Добавить(СтрШаблон("ОператорЭДО В (%1)", Отбор.Оператор));
	КонецЕсли;
	Если Отбор.ЭтоОблачныйЭДО <> Неопределено Тогда
		ПоляУсловия.Добавить(?(Отбор.ЭтоОблачныйЭДО, "ЭтоОблачныйЭДО", "ЭтоОблачныйЭДО = ЛОЖЬ"));
	КонецЕсли;
	
	ОписаниеЗапроса = ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса();
	ПоляЗапроса = "ИдентификаторЭДО, НаименованиеУчетнойЗаписи, НазначениеУчетнойЗаписи, ПодробноеОписаниеУчетнойЗаписи,
		|Организация, ОператорЭДО, СпособОбменаЭД, ПринятыУсловияИспользования,
		|СпособОбменаЭД В (&СпособыПрямогоОбмена) КАК ВозможенОбменБезПодписи,
		|ИмяФайлаСоглашенияНаРоуминг, ДанныеСоглашенияНаРоуминг, ДатаСоглашенияНаРоуминг,
		|КаталогОбмена, ПутьFTP, УчетнаяЗаписьЭлектроннойПочты, КодНалоговогоОргана, ЭтоОблачныйЭДО";
	ОписаниеЗапроса.Текст = ШаблонЗапросаУчетныхЗаписей(ИмяВременнойТаблицы,
		ПоляЗапроса, ПоляУсловия);
	
	ОписаниеЗапроса.СлужебныеПараметры.Вставить("СпособыПрямогоОбмена", СинхронизацияЭДО.СпособыПрямогоОбмена());
	Возврат ОписаниеЗапроса;
	
КонецФункции

// Возвращает описание запроса, в результате которого будут содержаться сертификаты учетных записей.
// Запрос содержит следующие поля:
//  * ИдентификаторЭДО - Строка - идентификатор учетной записи
//  * Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования.
// 
// Параметры:
// 	ИмяВременнойТаблицы - Строка
// 	Отбор - см. НовыйОтборСертификатовУчетныхЗаписей
// Возвращаемое значение:
// 	См. ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса
Функция ЗапросСертификатовУчетныхЗаписей(ИмяВременнойТаблицы, Отбор = Неопределено) Экспорт
	
	Если Отбор = Неопределено Тогда
		Отбор = НовыйОтборСертификатовУчетныхЗаписей();
	КонецЕсли;
	
	ПоляУсловия = Новый Массив;
	Если ЗначениеЗаполнено(Отбор.УчетныеЗаписи) Тогда
		ПоляУсловия.Добавить(СтрШаблон("СертификатыУчетныхЗаписей.ИдентификаторЭДО В(%1)", Отбор.УчетныеЗаписи));
	КонецЕсли;
	Если Отбор.ТолькоДействительные Тогда
		ПоляУсловия.Добавить("РАЗНОСТЬДАТ(&ТекущаяДата, СертификатыУчетныхЗаписей.ДействителенДо, МИНУТА) > 0");
	КонецЕсли;
	Если ЗначениеЗаполнено(Отбор.Сертификат) Тогда
		ПоляУсловия.Добавить(СтрШаблон("СертификатыУчетныхЗаписей.Сертификат В(%1)", Отбор.Сертификат));
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	СертификатыУчетныхЗаписей.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	СертификатыУчетныхЗаписей.Сертификат КАК Сертификат,
	|	СертификатыУчетныхЗаписей.Доверенность КАК Доверенность
	|ПОМЕСТИТЬ ИмяВременнойТаблицы
	|ИЗ
	|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписей
	|ГДЕ
	|&ПоляУсловия";
	
	ТекстЗапроса = ОбщегоНазначенияБЭД.ТекстЗапросаИзШаблона(ТекстЗапроса, ИмяВременнойТаблицы, "", ПоляУсловия);
	
	ОписаниеЗапроса = ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса();
	ОписаниеЗапроса.Текст = ТекстЗапроса;
	ОписаниеЗапроса.СлужебныеПараметры.Вставить("ТекущаяДата", ТекущаяДатаСеанса());
	
	Возврат ОписаниеЗапроса;
	
КонецФункции

// Возвращает учетные записи с непринятыми условиями использования.
// 
// Параметры:
// 	ИдентификаторыУчетныхЗаписей - Массив из Строка
// Возвращаемое значение:
// 	Массив из Строка - идентификаторы учетных записей
Функция УчетныеЗаписиЭДОБезПринятыхУсловийСервиса(ИдентификаторыУчетныхЗаписей) Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО В(&ИдентификаторыОрганизаций)
		|	И НЕ УчетныеЗаписиЭДО.ПринятыУсловияИспользования
		|	И НЕ УчетныеЗаписиЭДО.СпособОбменаЭД В (&СпособОбменаЭД)";
	
	Запрос.УстановитьПараметр("ИдентификаторыОрганизаций", ИдентификаторыУчетныхЗаписей);
	Запрос.УстановитьПараметр("СпособОбменаЭД", СинхронизацияЭДО.СпособыПрямогоОбмена());
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.ИдентификаторЭДО);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает описание запроса, в результате которого будут содержаться настройки учетных записей.
// 
// Параметры:
// 	ИмяВременнойТаблицы - Строка - таблица, в которую будет помещен результат запроса
// 	Отбор - см. НовыйОтборУчетныхЗаписей
// Возвращаемое значение:
// 	см. ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса
Функция ЗапросНастроекУчетныхЗаписей(ИмяВременнойТаблицы, Отбор = Неопределено) Экспорт
	
	Если Отбор = Неопределено Тогда
		Отбор = НовыйОтборУчетныхЗаписей();
	КонецЕсли;
	
	ПоляУсловия = Новый Массив;
	Если Отбор.УчетныеЗаписи <> "" Тогда
		ПоляУсловия.Добавить(СтрШаблон("ИдентификаторЭДО В(&%1)", Отбор.УчетныеЗаписи));
	КонецЕсли;
	Если Отбор.Организация <> "" Тогда
		ПоляУсловия.Добавить(СтрШаблон("Организация = &%1", Отбор.Организация));
	КонецЕсли;
	Если Отбор.СпособОбмена <> "" Тогда
		ПоляУсловия.Добавить(СтрШаблон("СпособОбменаЭД = &%1", Отбор.СпособОбмена));
	КонецЕсли;
	
	ОписаниеЗапроса = ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса();
	ОписаниеЗапроса.Текст = ШаблонЗапросаУчетныхЗаписей(ИмяВременнойТаблицы,
		"ИдентификаторЭДО, Организация, ОператорЭДО, СпособОбменаЭД,
		|КаталогОбмена, ПассивноеСоединениеFTP, ПортFTP, ПутьFTP, УчетнаяЗаписьЭлектроннойПочты", ПоляУсловия);
	
	Возврат ОписаниеЗапроса;
	
КонецФункции

// Возвращает учетные записи без доступных сертификатов.
// 
// Параметры:
// 	ОтпечаткиДоступныхУчетныхЗаписей - Массив из Строка
// Возвращаемое значение:
// 	Массив из Строка - идентификаторы учетных записей
Функция УчетныеЗаписиБезДоступныхСертификатов(ОтпечаткиДоступныхУчетныхЗаписей) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СертификатыУчетныхЗаписей.ИдентификаторЭДО КАК ИдентификаторЭДО
	|ПОМЕСТИТЬ УчетныеЗаписиСДоступнымиСертификатами
	|ИЗ
	|	СертификатыУчетныхЗаписей КАК СертификатыУчетныхЗаписей
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СертификатыДействующие КАК СертификатыДействующие
	|		ПО (СертификатыДействующие.Ссылка = СертификатыУчетныхЗаписей.Сертификат)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	УчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО
	|ПОМЕСТИТЬ УчетныеЗаписиСНедоступнымиСертификатами
	|ИЗ
	|	УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СертификатыУчетныхЗаписей КАК СертификатыУчетныхЗаписей
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ СертификатыКлючейЭлектроннойПодписиИШифрования КАК СертификатыКлючейЭлектроннойПодписиИШифрования
	|			ПО СертификатыУчетныхЗаписей.Сертификат = СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка
	|		ПО СертификатыУчетныхЗаписей.ИдентификаторЭДО = УчетныеЗаписиЭДО.ИдентификаторЭДО,
	|	Организации КАК Организации
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УчетныеЗаписиЭДО.ИдентификаторЭДО
	|ИЗ
	|	УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ СертификатыУчетныхЗаписей КАК СертификатыУчетныхЗаписей
	|		ПО УчетныеЗаписиЭДО.ИдентификаторЭДО = СертификатыУчетныхЗаписей.ИдентификаторЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ НастройкиОтправкиЭлектронныхДокументовПоВидам КАК НастройкиОтправкиПоВидам
	|		ПО УчетныеЗаписиЭДО.ИдентификаторЭДО = НастройкиОтправкиПоВидам.ИдентификаторОтправителя
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Организации КАК Организации
	|		ПО УчетныеЗаписиЭДО.Организация = Организации.Ссылка
	|ГДЕ
	|	(НастройкиОтправкиПоВидам.ОбменБезПодписи = ЛОЖЬ
	|			ИЛИ НЕ УчетныеЗаписиЭДО.СпособОбменаЭД В (&СпособыПрямогоОбмена))
	|	И СертификатыУчетныхЗаписей.Сертификат ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетныеЗаписиСНедоступнымиСертификатами.ИдентификаторЭДО КАК ИдентификаторЭДО
	|ИЗ
	|	УчетныеЗаписиСНедоступнымиСертификатами КАК УчетныеЗаписиСНедоступнымиСертификатами
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетныеЗаписиСДоступнымиСертификатами КАК УчетныеЗаписиСДоступнымиСертификатами
	|		ПО (УчетныеЗаписиСДоступнымиСертификатами.ИдентификаторЭДО = УчетныеЗаписиСНедоступнымиСертификатами.ИдентификаторЭДО)
	|ГДЕ
	|	УчетныеЗаписиСДоступнымиСертификатами.ИдентификаторЭДО ЕСТЬ NULL";
	
	Отбор = КриптографияБЭД.НовыйОтборНедействующихИлиНедоступныхСертификатов();
	Отбор.ОпечаткиДоступныхСертификатов = "Отпечатки";
	Запросы = Новый Массив;
	Запросы.Добавить(КриптографияБЭД.ЗапросНеДействующихИлиНедоступныхСертификатов(
		"СертификатыКлючейЭлектроннойПодписиИШифрования", Отбор));
	Запросы.Добавить(ИнтеграцияЭДО.ЗапросДоступныхОрганизаций("Организации"));
	ОтборУчетныхЗаписей = НовыйОтборУчетныхЗаписей();
	Запросы.Добавить(ЗапросУчетныхЗаписей("УчетныеЗаписиЭДО", ОтборУчетныхЗаписей));
	Запросы.Добавить(НастройкиЭДО.ЗапросНастроекОтправкиУчетныхЗаписей("НастройкиОтправкиЭлектронныхДокументовПоВидам"));
	Запросы.Добавить(ЗапросСертификатовУчетныхЗаписей("СертификатыУчетныхЗаписей"));
	
	ОтборДействующихСертификатов = КриптографияБЭД.НовыйОтборСертификатов();
	ОтборДействующихСертификатов.Отпечатки = "&Отпечатки";
	Запросы.Добавить(КриптографияБЭД.ЗапросДействующихСертификатов("СертификатыДействующие", ОтборДействующихСертификатов));
	
	ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
	СпособыПрямогоОбмена = СинхронизацияЭДО.СпособыПрямогоОбмена();
	ИтоговыйЗапрос.УстановитьПараметр("СпособыПрямогоОбмена", СпособыПрямогоОбмена);
	ИтоговыйЗапрос.УстановитьПараметр("Отпечатки", ОтпечаткиДоступныхУчетныхЗаписей);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = ИтоговыйЗапрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	Выборка = РезультатЗапроса.Выбрать();
	
	УчетныеЗаписи = Новый Массив;
	Пока Выборка.Следующий() Цикл
		УчетныеЗаписи.Добавить(Выборка.ИдентификаторЭДО);
	КонецЦикла;
	
	Возврат УчетныеЗаписи;
	
КонецФункции

// Параметры:
//  Сертификаты - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  ИдентификаторыЭДО - Массив из Строка
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  * Значение - Массив из Строка
Функция УчетныеЗаписиПоСертификатам(Сертификаты, ИдентификаторыЭДО = Неопределено) Экспорт
	
	Возврат РегистрыСведений.СертификатыУчетныхЗаписейЭДО.УчетныеЗаписиПоСертификатам(Сертификаты, ИдентификаторыЭДО);
	
КонецФункции

// Возвращает структуру для получения запроса учетных записей. См. ЗапросУчетныхЗаписей.
// 
// Возвращаемое значение:
// 	Структура:
// * УчетныеЗаписи - Строка - параметр или выражение для отбора по идентификаторам учетных записей
// * Организация - Строка - параметр или выражение для отбора по организации
// * СпособОбмена - Строка - параметр или выражение для отбора по способу обмена
// * Оператор - Строка - параметр или выражение для отбора по оператору
// * ЭтоОблачныйЭДО - Неопределено,Булево - параметр или выражение для отбора по использованию обмена через облачный ЭДО.
Функция НовыйОтборУчетныхЗаписей() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("УчетныеЗаписи", "");
	Отбор.Вставить("Организация", "");
	Отбор.Вставить("СпособОбмена", "");
	Отбор.Вставить("Оператор", "");
	Отбор.Вставить("ЭтоОблачныйЭДО", Неопределено);
	
	Возврат Отбор;
	
КонецФункции

// Возвращает структуру для получения запроса сертификатов учетных записей. См. ЗапросСертификатовУчетныхЗаписей.
// 
// Возвращаемое значение:
// 	Структура - Описание:
// * УчетныеЗаписи - Строка - параметр или выражение для отбора по идентификаторам учетных записей
// * ТолькоДействительные - Булево - отбирать только действительные сертификаты
// * Сертификат - Строка - параметр или выражение для отбора по сертификатам
Функция НовыйОтборСертификатовУчетныхЗаписей() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("УчетныеЗаписи", "");
	Отбор.Вставить("ТолькоДействительные", Ложь);
	Отбор.Вставить("Сертификат", "");
	
	Возврат Отбор;
	
КонецФункции

// Изменяет свойство учетной записи "Версия конфигурации".
// 
// Параметры:
// 	ИдентификаторУчетнойЗаписи - Строка
// 	ВерсияКонфигурации - Строка
Процедура ИзменитьВерсиюКонфигурации(ИдентификаторУчетнойЗаписи, ВерсияКонфигурации) Экспорт
	
	МенеджерЗаписи = РегистрыСведений.УчетныеЗаписиЭДО.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ИдентификаторЭДО = ИдентификаторУчетнойЗаписи;
	МенеджерЗаписи.Прочитать();

	Если МенеджерЗаписи.Выбран() И МенеджерЗаписи.ВерсияКонфигурации <> ВерсияКонфигурации Тогда

		МенеджерЗаписи.ВерсияКонфигурации = ВерсияКонфигурации;
		МенеджерЗаписи.Записать();

	КонецЕсли;
		
КонецПроцедуры

// Возвращает параметры учетной записи для обмена по FTP.
// 
// Параметры:
// 	УчетнаяЗапись - РегистрСведенийЗапись.УчетныеЗаписиЭДО
// Возвращаемое значение:
// 	Структура:
//    * ПутьFTP - Строка
//    * ПортFTP - Число
//    * Идентификатор - Строка
//    * ПассивноеСоединениеFTP - Булево
Функция ПараметрыУчетнойЗаписиДляОбменаПоFTP(УчетнаяЗапись) Экспорт
	
	ДанныеУчетнойЗаписи = Новый Структура;
	ДанныеУчетнойЗаписи.Вставить("ПутьFTP", УчетнаяЗапись.ПутьFTP);
	ДанныеУчетнойЗаписи.Вставить("ПортFTP", УчетнаяЗапись.ПортFTP);
	ДанныеУчетнойЗаписи.Вставить("Идентификатор", УчетнаяЗапись.ИдентификаторЭДО);
	ДанныеУчетнойЗаписи.Вставить("ПассивноеСоединениеFTP", УчетнаяЗапись.ПассивноеСоединениеFTP);
	
	Возврат ДанныеУчетнойЗаписи;
	
КонецФункции

// Возвращает список идентификаторов и их представлений по организации и обособленным подразделениям для сервиса ЭДО.
//
// Параметры:
//   Организация - ОпределяемыйТип.Организация
//
// Возвращаемое значение:
//   СписокЗначений из Строка - список идентификаторов организации и их представлений.
//
Функция СписокИдентификаторовУчетныхЗаписейОрганизацииСервисаЭДО(Организация) Экспорт
	
	МассивЗначенийСвойств = ЗначенияСвойствУчетныхЗаписейОрганизацииСервисаЭДО(Организация,
		"ИдентификаторЭДО, НаименованиеУчетнойЗаписи");
	
	СписокИдентификаторов = Новый СписокЗначений;
	Для каждого ЗначенияСвойств Из МассивЗначенийСвойств Цикл
		СписокИдентификаторов.Добавить(ЗначенияСвойств.ИдентификаторЭДО, ЗначенияСвойств.НаименованиеУчетнойЗаписи);
	КонецЦикла; 
	
	Возврат СписокИдентификаторов;
	
КонецФункции

// Сохраняет данные соглашения на роуминг.
// 
// Параметры:
// 	ИдентификаторУчетнойЗаписи - Строка
// 	ДанныеСоглашенияНаРоуминг - ДвоичныеДанные
// 	ИмяФайла - Строка
// Возвращаемое значение:
// 	Структура:
// * Статус - Булево - Истина, если сохранение прошло успешно
// * ОписаниеОшибки - Строка
Функция ЗаписатьДанныеСоглашенияНаРоуминг(ИдентификаторУчетнойЗаписи, ДанныеСоглашенияНаРоуминг, ИмяФайла) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Статус", Истина);
	Результат.Вставить("ОписаниеОшибки");
	
	НачатьТранзакцию();
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		ОбщегоНазначенияБЭД.УстановитьУправляемуюБлокировку(
			"РегистрСведений.УчетныеЗаписиЭДО", Новый Структура("ИдентификаторЭДО", ИдентификаторУчетнойЗаписи));
		
		МенеджерЗаписи = РегистрыСведений.УчетныеЗаписиЭДО.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ИдентификаторЭДО = ИдентификаторУчетнойЗаписи;
		
		МенеджерЗаписи.Прочитать();
		Если Не МенеджерЗаписи.Выбран() Тогда
			Результат.Статус = Ложь;
			Результат.ОписаниеОшибки = НСтр("ru = 'Не найдена учетная запись организации';
											|en = 'Company account not found'");
			ЗафиксироватьТранзакцию();
			Возврат Результат;
		КонецЕсли;
		
		МенеджерЗаписи.ИмяФайлаСоглашенияНаРоуминг = ИмяФайла;
		МенеджерЗаписи.ДанныеСоглашенияНаРоуминг = Новый ХранилищеЗначения(
			ДанныеСоглашенияНаРоуминг, Новый СжатиеДанных(9));
		МенеджерЗаписи.ДатаСоглашенияНаРоуминг = ТекущаяУниверсальнаяДата() + СмещениеСтандартногоВремени("GMT+3");
		МенеджерЗаписи.Записать();
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		Результат.Статус = Ложь;
		Результат.ОписаниеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЭлектронноеВзаимодействие.ОбработатьОшибку(НСтр("ru = 'Отправка приглашений ЭДО';
														|en = 'EDI invitation dispatch'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Удаляет данные соглашения на роуминг.
// 
// Параметры:
// 	ИдентификаторУчетнойЗаписи - Строка
// Возвращаемое значение:
// 	Булево
Функция УдалитьДанныеСоглашенияНаРоуминг(ИдентификаторУчетнойЗаписи) Экспорт
	
	Результат = Истина;
	
	НачатьТранзакцию();
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		
		ОбщегоНазначенияБЭД.УстановитьУправляемуюБлокировку(
			"РегистрСведений.УчетныеЗаписиЭДО", Новый Структура("ИдентификаторЭДО", ИдентификаторУчетнойЗаписи));
		
		МенеджерЗаписи = РегистрыСведений.УчетныеЗаписиЭДО.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ИдентификаторЭДО = ИдентификаторУчетнойЗаписи;
		
		МенеджерЗаписи.Прочитать();
		Если Не МенеджерЗаписи.Выбран() Тогда
			Результат = Ложь;
			ЗафиксироватьТранзакцию();
			Возврат Результат;
		КонецЕсли;
		
		МенеджерЗаписи.ИмяФайлаСоглашенияНаРоуминг = "";
		МенеджерЗаписи.ДанныеСоглашенияНаРоуминг   = Новый ХранилищеЗначения(Неопределено);
		МенеджерЗаписи.ДатаСоглашенияНаРоуминг     = '00010101';
		МенеджерЗаписи.Записать();
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		Результат = Ложь;
		
		ЭлектронноеВзаимодействие.ОбработатьОшибку(НСтр("ru = 'Отправка приглашений ЭДО';
														|en = 'EDI invitation dispatch'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			НСтр("ru = 'Не удалось удалить данные соглашения на роуминг для учетной записи организации';
				|en = 'Cannot delete roaming agreement data for company account'"));
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Возвращает электронные адреса для уведомлений о статусе заявке на роуминг.
// 
// Параметры:
// 	Организация - ОпределяемыйТип.Организация
// Возвращаемое значение:
// 	Массив из Строка
Функция ЭлектронныеАдресаДляУведомленияОСтатусеЗаявкиНаРоуминг(Организация) Экспорт
	
	ДанныеУчетныхЗаписей = ЗначенияСвойствУчетныхЗаписейОрганизацииСервисаЭДО(Организация, "ЭлектроннаяПочтаДляУведомлений, EmailОрганизации");
	КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация),
		Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты,,
		ТекущаяДатаСеанса());
	
	МассивEmailОрганизации = Новый Массив;
	МассивЭлектроннаяПочтаДляУведомлений = Новый Массив;
	МассивКонтактнаяИнформация = Новый Массив;
	Для каждого ДанныеУчетнойЗаписи Из ДанныеУчетныхЗаписей Цикл
		Если ЗначениеЗаполнено(ДанныеУчетнойЗаписи.EmailОрганизации)
			И МассивEmailОрганизации.Найти(ДанныеУчетнойЗаписи.EmailОрганизации) = Неопределено Тогда
			МассивEmailОрганизации.Добавить(ДанныеУчетнойЗаписи.EmailОрганизации);
		КонецЕсли;
		Если ЗначениеЗаполнено(ДанныеУчетнойЗаписи.ЭлектроннаяПочтаДляУведомлений)
			И МассивЭлектроннаяПочтаДляУведомлений.Найти(ДанныеУчетнойЗаписи.ЭлектроннаяПочтаДляУведомлений) = Неопределено Тогда
			МассивЭлектроннаяПочтаДляУведомлений.Добавить(ДанныеУчетнойЗаписи.ЭлектроннаяПочтаДляУведомлений);
		КонецЕсли;
	КонецЦикла;
	Для каждого ЭлементКонтактнойИнформации Из КонтактнаяИнформация Цикл
		Если ЗначениеЗаполнено(ЭлементКонтактнойИнформации.Представление)
			И МассивКонтактнаяИнформация.Найти(ЭлементКонтактнойИнформации.Представление) = Неопределено Тогда
			МассивКонтактнаяИнформация.Добавить(ЭлементКонтактнойИнформации.Представление);
		КонецЕсли;
	КонецЦикла; 
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивEmailОрганизации, МассивЭлектроннаяПочтаДляУведомлений, Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивEmailОрганизации, МассивКонтактнаяИнформация, Истина);
	
	Возврат МассивEmailОрганизации;
	
КонецФункции

// Сохраняет Email организации.
// 
// Параметры:
// 	ИдентификаторУчетнойЗаписи - Строка
// 	EmailОрганизации - Строка
Процедура ЗаписатьEmailОрганизации(ИдентификаторУчетнойЗаписи, EmailОрганизации) Экспорт
	
	НачатьТранзакцию();
	Попытка
		ОбщегоНазначенияБЭД.УстановитьУправляемуюБлокировку(
			"РегистрСведений.УчетныеЗаписиЭДО", Новый Структура("ИдентификаторЭДО", ИдентификаторУчетнойЗаписи));
		МенеджерЗаписиУчетнаяЗапись = РегистрыСведений.УчетныеЗаписиЭДО.СоздатьМенеджерЗаписи();
		МенеджерЗаписиУчетнаяЗапись.ИдентификаторЭДО = ИдентификаторУчетнойЗаписи;
		МенеджерЗаписиУчетнаяЗапись.Прочитать();
		Если Не МенеджерЗаписиУчетнаяЗапись.Выбран() Тогда
			МенеджерЗаписиУчетнаяЗапись.ИдентификаторЭДО = ИдентификаторУчетнойЗаписи;
		КонецЕсли;
		МенеджерЗаписиУчетнаяЗапись.EmailОрганизации = EmailОрганизации;
		МенеджерЗаписиУчетнаяЗапись.Записать();
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ИмяСобытия = НСтр("ru = 'Запись email организации учетной записи электронного документооборота';
							|en = 'Write an email of the EDI account company'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

// Возвращает данные учетной записи.
// 
// Параметры:
// 	ИдентификаторУчетнойЗаписи - Строка
// Возвращаемое значение:
// 	Структура, Неопределено - данные учетной записи:
//  * НаименованиеУчетнойЗаписи - Строка - наименование учетной записи
//  * ИдентификаторОрганизации - Строка - идентификатор учетной записи
//  * Организация - ОпределяемыйТип.Организация
//  * ОператорЭДО - Строка - идентификатор оператора электронного документооборота
//  * СпособОбменаЭД - ПеречислениеСсылка.СпособыОбменаЭД
Функция ДанныеУчетнойЗаписи(ИдентификаторУчетнойЗаписи) Экспорт 
	
	Данные = Неопределено;
	
	Если ОбщегоНазначения.ПодсистемаСуществует(
		"ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС") Тогда
		МодульЭлектронноеАктированиеЕИС = ОбщегоНазначения.ОбщийМодуль("ЭлектронноеАктированиеЕИС");
		Если МодульЭлектронноеАктированиеЕИС.ИспользоватьЭлектронноеАктированиеВЕИС() Тогда
			ДанныеУчетнойЗаписиЕИС = МодульЭлектронноеАктированиеЕИС.ДанныеУчетнойЗаписи(ИдентификаторУчетнойЗаписи);
			Если ЗначениеЗаполнено(ДанныеУчетнойЗаписиЕИС) Тогда
				Возврат ДанныеУчетнойЗаписиЕИС;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	УчетныеЗаписиЭДО.НаименованиеУчетнойЗаписи КАК НаименованиеУчетнойЗаписи,
		|	УчетныеЗаписиЭДО.Организация КАК Организация,
		|	УчетныеЗаписиЭДО.ОператорЭДО КАК ОператорЭДО,
		|	УчетныеЗаписиЭДО.СпособОбменаЭД КАК СпособОбменаЭД,
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторОрганизации
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО = &ИдентификаторЭДО";
	
	Запрос.УстановитьПараметр("ИдентификаторЭДО", ИдентификаторУчетнойЗаписи);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ТаблицаРезультат = РезультатЗапроса.Выгрузить();
	
	Если ТаблицаРезультат.Количество() > 0 Тогда
		
		Данные = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРезультат[0]);
		
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

// Возвращает таблицу учетных записей без сертификатов.
// 
// Возвращаемое значение:
// 	ТаблицаЗначений:
// * ИдентификаторЭДО - Строка
// * СообщениеЭДО - ДокументСсылка.СообщениеЭДО 
Функция НоваяТаблицаУчетныхЗаписейБезСертификатов() Экспорт
	
	УчетныеЗаписиБезСертификатов = Новый ТаблицаЗначений;
	УчетныеЗаписиБезСертификатов.Колонки.Добавить("ИдентификаторЭДО",
		Метаданные.РегистрыСведений.УчетныеЗаписиЭДО.Измерения.ИдентификаторЭДО.Тип);
	УчетныеЗаписиБезСертификатов.Колонки.Добавить("Сообщение", Новый ОписаниеТипов("ДокументСсылка.СообщениеЭДО"));
	
	Возврат УчетныеЗаписиБезСертификатов;
	
КонецФункции

// Возвращает ключ записи регистра сведений УчетныеЗаписиЭДО.
// 
// Параметры:
// 	ИдентификаторУчетнойЗаписи - Строка
// Возвращаемое значение:
// 	РегистрСведенийКлючЗаписи.УчетныеЗаписиЭДО
Функция КлючУчетнойЗаписи(ИдентификаторУчетнойЗаписи) Экспорт
	
	ЗначенияКлюча = Новый Структура("ИдентификаторЭДО", ИдентификаторУчетнойЗаписи);
	Возврат РегистрыСведений.УчетныеЗаписиЭДО.СоздатьКлючЗаписи(ЗначенияКлюча);
	
КонецФункции

// Создает учетную запись для обмена электронными документами.
// 
// Параметры:
// 	ОписаниеУчетнойЗаписи - см. УчетныеЗаписиЭДОКлиентСервер.НовоеОписаниеУчетнойЗаписи
// 	АдресИзменен - Булево
// Возвращаемое значение:
// 	Булево - учетная запись создана
Функция СоздатьУчетнуюЗапись(ОписаниеУчетнойЗаписи, АдресИзменен = Истина) Экспорт
	
	Возврат УчетныеЗаписиЭДОСлужебный.СоздатьУчетнуюЗапись(ОписаниеУчетнойЗаписи, АдресИзменен);

КонецФункции

// Параметры:
//  ИдентификаторУчетнойЗаписи - Строка
// 
// Возвращаемое значение:
//  Булево
Функция УдалитьУчетнуюЗапись(ИдентификаторУчетнойЗаписи) Экспорт
	
	Результат = Ложь;
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
		|	УчетныеЗаписиЭДО.СпособОбменаЭД КАК СпособОбменаЭД,
		|	УчетныеЗаписиЭДО.ЭтоОблачныйЭДО КАК ЭтоОблачныйЭДО
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО = &ИдентификаторЭДО";
	
	Запрос.УстановитьПараметр("ИдентификаторЭДО", ИдентификаторУчетнойЗаписи);
	
	НачатьТранзакцию();
	Попытка
		Поля = Новый Структура;
		Поля.Вставить("ИдентификаторЭДО", ИдентификаторУчетнойЗаписи);
		ОбщегоНазначенияБЭД.УстановитьУправляемуюБлокировку("РегистрСведений.УчетныеЗаписиЭДО",
			Поля);
		// Удаляем все под текущими правами пользователя.
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаУчетныеЗаписи = РезультатЗапроса.Выбрать();
		Если ВыборкаУчетныеЗаписи.Следующий() Тогда
			НаборЗаписей = РегистрыСведений.УчетныеЗаписиЭДО.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ИдентификаторЭДО.Установить(ИдентификаторУчетнойЗаписи);
			НаборЗаписей.Записать();
			ПриУдаленииУчетнойЗаписи(ВыборкаУчетныеЗаписи.ИдентификаторЭДО,
				ВыборкаУчетныеЗаписи.СпособОбменаЭД,
				ВыборкаУчетныеЗаписи.ЭтоОблачныйЭДО);
		КонецЕсли;
		
		Результат = Истина;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Информация = ИнформацияОбОшибке();
		
		Результат = Ложь;
		
		ЭлектронноеВзаимодействие.ОбработатьОшибку(НСтр("ru = 'Удаление учетной записи электронного документооборота';
														|en = 'Delete EDI account'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(Информация));
			
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  НоваяУчетнаяЗапись - РегистрСведенийМенеджерЗаписи.УчетныеЗаписиЭДО,РегистрСведенийЗапись.УчетныеЗаписиЭДО
//  ОписаниеУчетнойЗаписи - см. УчетныеЗаписиЭДОКлиентСервер.НовоеОписаниеУчетнойЗаписи
Процедура ЗаполнитьДанныеУчетнойЗаписи(НоваяУчетнаяЗапись, ОписаниеУчетнойЗаписи) Экспорт
	
	УчетныеЗаписиЭДОСлужебный.ЗаполнитьДанныеУчетнойЗаписи(НоваяУчетнаяЗапись, ОписаниеУчетнойЗаписи);
	
КонецПроцедуры

// Параметры:
//  ИдентификаторУчетнойЗаписи - Строка
//  Сертификаты - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//              - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  Замещать - Булево
//  Доверенности - Неопределено,Соответствие из КлючИЗначение:
//  * Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  * Значение - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций,СправочникСсылка.МЧД003
Процедура ЗаписатьСертификатыУчетнойЗаписи(ИдентификаторУчетнойЗаписи, Знач Сертификаты, Замещать = Истина,
	Доверенности = Неопределено) Экспорт
	
	УчетныеЗаписиЭДОСлужебный.ЗаписатьСертификатыУчетнойЗаписи(ИдентификаторУчетнойЗаписи, Сертификаты, Замещать,
		Доверенности);
	
КонецПроцедуры

// Параметры:
//  Организация - ОпределяемыйТип.Организация
//  Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  НовыйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// 
// Возвращаемое значение:
//  Массив из Структура:
//  * ИдентификаторЭДО - Строка
//  * Организация - ОпределяемыйТип.Организация
//  * НаименованиеУчетнойЗаписи - Строка
//  * ВерсияКонфигурации - Строка
//  * ОператорЭДО - Строка
//  * СпособОбменаЭД - ПеречислениеСсылка.СпособыОбменаЭД
//  * ЭлектроннаяПочтаДляУведомлений - Строка
//  * ОжидатьИзвещениеОПолучении - Булево
//  * УведомлятьОНовыхПриглашениях - Булево
//  * УведомлятьОбОтветахНаПриглашения - Булево
//  * УведомлятьОНовыхДокументах - Булево
//  * УведомлятьОНеОбработанныхДокументах - Булево
//  * УведомлятьОбОкончанииСрокаДействияСертификата -Булево
//  * КодНалоговогоОргана - Строка
//  * АдресОрганизации - Строка
//  * АдресОрганизацииЗначение - Строка
//  * НазначениеУчетнойЗаписи - Строка
//  * ПодробноеОписаниеУчетнойЗаписи - Строка
//  * ОбновитьНастройкиУведомлений - Булево
//  * ПринятыУсловияИспользования - Булево
//  * ВсеСертификаты - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
Функция УчетныеЗаписиЭДОДляОбновленияСертификата(Знач Организация, Знач Сертификат, Знач НовыйСертификат) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	УчетныеЗаписиЭДО.Организация КАК Организация,
	|	УчетныеЗаписиЭДО.НаименованиеУчетнойЗаписи КАК НаименованиеУчетнойЗаписи,
	|	УчетныеЗаписиЭДО.ВерсияКонфигурации КАК ВерсияКонфигурации,
	|	УчетныеЗаписиЭДО.ОператорЭДО КАК ОператорЭДО,
	|	УчетныеЗаписиЭДО.СпособОбменаЭД КАК СпособОбменаЭД,
	|	УчетныеЗаписиЭДО.ЭлектроннаяПочтаДляУведомлений КАК ЭлектроннаяПочтаДляУведомлений,
	|	УчетныеЗаписиЭДО.ОжидатьИзвещениеОПолучении КАК ОжидатьИзвещениеОПолучении,
	|	УчетныеЗаписиЭДО.УведомлятьОНовыхПриглашениях КАК УведомлятьОНовыхПриглашениях,
	|	УчетныеЗаписиЭДО.УведомлятьОбОтветахНаПриглашения КАК УведомлятьОбОтветахНаПриглашения,
	|	УчетныеЗаписиЭДО.УведомлятьОНовыхДокументах КАК УведомлятьОНовыхДокументах,
	|	УчетныеЗаписиЭДО.УведомлятьОНеОбработанныхДокументах КАК УведомлятьОНеОбработанныхДокументах,
	|	УчетныеЗаписиЭДО.УведомлятьОбОкончанииСрокаДействияСертификата КАК УведомлятьОбОкончанииСрокаДействияСертификата,
	|	УчетныеЗаписиЭДО.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	УчетныеЗаписиЭДО.АдресОрганизации КАК АдресОрганизации,
	|	УчетныеЗаписиЭДО.АдресОрганизацииЗначение КАК АдресОрганизацииЗначение,
	|	УчетныеЗаписиЭДО.НазначениеУчетнойЗаписи КАК НазначениеУчетнойЗаписи,
	|	УчетныеЗаписиЭДО.ПодробноеОписаниеУчетнойЗаписи КАК ПодробноеОписаниеУчетнойЗаписи,
	|	УчетныеЗаписиЭДО.ОбновитьНастройкиУведомлений КАК ОбновитьНастройкиУведомлений,
	|	УчетныеЗаписиЭДО.ПринятыУсловияИспользования КАК ПринятыУсловияИспользования
	|ПОМЕСТИТЬ втУчетныеЗаписиЭДО
	|ИЗ
	|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК Сертификаты
	|		ПО УчетныеЗаписиЭДО.ИдентификаторЭДО = Сертификаты.ИдентификаторЭДО
	|			И (Сертификаты.Сертификат = &Сертификат)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыНовый
	|		ПО УчетныеЗаписиЭДО.ИдентификаторЭДО = СертификатыНовый.ИдентификаторЭДО
	|			И (СертификатыНовый.Сертификат = &НовыйСертификат)
	|ГДЕ
	|	СертификатыНовый.Сертификат ЕСТЬ NULL
	|	И УчетныеЗаписиЭДО.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втУчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	втУчетныеЗаписиЭДО.Организация КАК Организация,
	|	втУчетныеЗаписиЭДО.НаименованиеУчетнойЗаписи КАК НаименованиеУчетнойЗаписи,
	|	втУчетныеЗаписиЭДО.ВерсияКонфигурации КАК ВерсияКонфигурации,
	|	втУчетныеЗаписиЭДО.ОператорЭДО КАК ОператорЭДО,
	|	втУчетныеЗаписиЭДО.СпособОбменаЭД КАК СпособОбменаЭД,
	|	втУчетныеЗаписиЭДО.ЭлектроннаяПочтаДляУведомлений КАК ЭлектроннаяПочтаДляУведомлений,
	|	втУчетныеЗаписиЭДО.ОжидатьИзвещениеОПолучении КАК ОжидатьИзвещениеОПолучении,
	|	втУчетныеЗаписиЭДО.УведомлятьОНовыхПриглашениях КАК УведомлятьОНовыхПриглашениях,
	|	втУчетныеЗаписиЭДО.УведомлятьОбОтветахНаПриглашения КАК УведомлятьОбОтветахНаПриглашения,
	|	втУчетныеЗаписиЭДО.УведомлятьОНовыхДокументах КАК УведомлятьОНовыхДокументах,
	|	втУчетныеЗаписиЭДО.УведомлятьОНеОбработанныхДокументах КАК УведомлятьОНеОбработанныхДокументах,
	|	втУчетныеЗаписиЭДО.УведомлятьОбОкончанииСрокаДействияСертификата КАК УведомлятьОбОкончанииСрокаДействияСертификата,
	|	втУчетныеЗаписиЭДО.КодНалоговогоОргана КАК КодНалоговогоОргана,
	|	втУчетныеЗаписиЭДО.АдресОрганизации КАК АдресОрганизации,
	|	втУчетныеЗаписиЭДО.АдресОрганизацииЗначение КАК АдресОрганизацииЗначение,
	|	втУчетныеЗаписиЭДО.НазначениеУчетнойЗаписи КАК НазначениеУчетнойЗаписи,
	|	втУчетныеЗаписиЭДО.ПодробноеОписаниеУчетнойЗаписи КАК ПодробноеОписаниеУчетнойЗаписи,
	|	втУчетныеЗаписиЭДО.ОбновитьНастройкиУведомлений КАК ОбновитьНастройкиУведомлений,
	|	втУчетныеЗаписиЭДО.ПринятыУсловияИспользования КАК ПринятыУсловияИспользования
	|ИЗ
	|	втУчетныеЗаписиЭДО КАК втУчетныеЗаписиЭДО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	СертификатыУчетныхЗаписейЭДО.Сертификат КАК Сертификат
	|ИЗ
	|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втУчетныеЗаписиЭДО КАК втУчетныеЗаписиЭДО
	|		ПО СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО = втУчетныеЗаписиЭДО.ИдентификаторЭДО";
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Сертификат", Сертификат);
	Запрос.УстановитьПараметр("НовыйСертификат", НовыйСертификат);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	УчетныеЗаписи = Результаты[1].Выгрузить();
	УчетныеЗаписи.Колонки.Добавить("ВсеСертификаты");
	
	Сертификаты = Результаты[2].Выгрузить();
	Сертификаты.Индексы.Добавить("ИдентификаторЭДО");
	
	Для каждого УчетнаяЗапись Из УчетныеЗаписи Цикл
		
		ОтборПоУчетнойЗаписи = Новый Структура("ИдентификаторЭДО", УчетнаяЗапись.ИдентификаторЭДО);
		НайденныеСтроки = Сертификаты.НайтиСтроки(ОтборПоУчетнойЗаписи);
		
		УчетнаяЗапись.ВсеСертификаты = Новый Массив;
		УчетнаяЗапись.ВсеСертификаты.Добавить(НовыйСертификат);
		Для каждого СтрокаСертификата Из НайденныеСтроки Цикл
			УчетнаяЗапись.ВсеСертификаты.Добавить(СтрокаСертификата.Сертификат);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(УчетныеЗаписи);
	
КонецФункции

// Параметры:
//  ОтпечаткиПоКонтекстам - см. КриптографияБЭДКлиентСервер.НовыеРезультатыПолученияОтпечатков
//  ИдентификаторыУчетныхЗаписейЭДО - Массив из Строка
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка - идентификатор учетной записи ЭДО.
//  * Значение - Массив из Строка - отпечатки сертификатов
Функция ОтпечаткиДействующихСертификатовПоУчетнымЗаписям(ОтпечаткиПоКонтекстам, ИдентификаторыУчетныхЗаписейЭДО) Экспорт
	
	ОтпечаткиПоУчетнымЗаписям = Новый Соответствие; // См. ОтпечаткиДействующихСертификатовПоУчетнымЗаписям
	
	ОписанияЗапросов = Новый Массив; // Массив из См. ОбщегоНазначенияБЭД.НовоеОписаниеЗапроса
	
	Отбор = КриптографияБЭД.НовыйОтборСертификатов();
	Отбор.Отпечатки = "&ОтпечаткиСертификатов";
	Отбор.ДоступныеТекущемуПользователю = Истина;
	Если Не ОтпечаткиПоКонтекстам.Облако.Доступность Тогда
		Отбор.Облачные = Ложь;
	КонецЕсли;
	ОписанияЗапросов.Добавить(КриптографияБЭД.ЗапросДействующихСертификатов("ДоступныеСертификаты", Отбор));
	
	Отбор = НовыйОтборСертификатовУчетныхЗаписей();
	Отбор.УчетныеЗаписи = "&ИдентификаторыУчетныхЗаписейЭДО";
	ОписанияЗапросов.Добавить(ЗапросСертификатовУчетныхЗаписей("СертификатыУчетныхЗаписей", Отбор));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоступныеСертификаты.Отпечаток КАК Отпечаток,
		|	СертификатыУчетныхЗаписей.ИдентификаторЭДО КАК ИдентификаторЭДО
		|ИЗ
		|	СертификатыУчетныхЗаписей КАК СертификатыУчетныхЗаписей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДоступныеСертификаты КАК ДоступныеСертификаты
		|		ПО СертификатыУчетныхЗаписей.Сертификат = ДоступныеСертификаты.Ссылка";
	
	Запрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, ОписанияЗапросов);
	
	ВсеОтпечатки = КриптографияБЭД.ПолучитьВсеОтпечаткиСертификатов(ОтпечаткиПоКонтекстам);
	Запрос.УстановитьПараметр("ОтпечаткиСертификатов", ВсеОтпечатки);
	Запрос.УстановитьПараметр("ИдентификаторыУчетныхЗаписейЭДО", ИдентификаторыУчетныхЗаписейЭДО);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ОтпечаткиПоУчетнымЗаписям;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Отпечатки = ОтпечаткиПоУчетнымЗаписям[Выборка.ИдентификаторЭДО];
		Если Отпечатки = Неопределено Тогда
			Отпечатки = Новый Массив; // Массив из Строка
			ОтпечаткиПоУчетнымЗаписям.Вставить(Выборка.ИдентификаторЭДО, Отпечатки);
		КонецЕсли;
		Если Отпечатки.Найти(Выборка.Отпечаток) = Неопределено Тогда
			Отпечатки.Добавить(Выборка.Отпечаток);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОтпечаткиПоУчетнымЗаписям;
	
КонецФункции

// Параметры:
//  ИдентификаторыУчетныхЗаписейЭДО - Массив из Строка
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка - идентификатор учетной записи ЭДО.
//  * Значение - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
Функция ДействующиеСертификатыПоУчетнымЗаписям(ИдентификаторыУчетныхЗаписейЭДО) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
		|	СертификатыУчетныхЗаписейЭДО.Сертификат КАК Сертификат
		|ИЗ
		|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
		|ГДЕ
		|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО В (&ИдентификаторЭДО)
		|	И РАЗНОСТЬДАТ(&ТекущаяДата, СертификатыУчетныхЗаписейЭДО.ДействителенДо, МИНУТА) > 0";
	
	Запрос.УстановитьПараметр("ИдентификаторЭДО", ИдентификаторыУчетныхЗаписейЭДО);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	СертификатыПоУчетнымЗаписям = Новый Соответствие;
	Пока Выборка.Следующий() Цикл
		Сертификаты = СертификатыПоУчетнымЗаписям[Выборка.ИдентификаторЭДО];
		Если Сертификаты = Неопределено Тогда
			Сертификаты = Новый Массив; // Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
			СертификатыПоУчетнымЗаписям.Вставить(Выборка.ИдентификаторЭДО, Сертификаты);
		КонецЕсли;
		Сертификаты.Добавить(Выборка.Сертификат);
	КонецЦикла;
	
	Возврат СертификатыПоУчетнымЗаписям;
	
КонецФункции

// Параметры:
//  ИдентификаторыЭДО - Массив из Строка
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка - идентификатор учетной записи ЭДО.
//  * Значение - ОпределяемыйТип.Организация
Функция ОрганизацииУчетныхЗаписей(ИдентификаторыЭДО) Экспорт
	
	Результат = Новый Соответствие; // См. ОрганизацииУчетныхЗаписей
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО,
		|	УчетныеЗаписиЭДО.Организация
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО В (&ИдентификаторыЭДО)";
	
	Запрос.УстановитьПараметр("ИдентификаторыЭДО", ИдентификаторыЭДО);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.ИдентификаторЭДО, Выборка.Организация);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ИдентификаторЭДО - Строка
// 
// Возвращаемое значение:
//  Неопределено,ОпределяемыйТип.Организация
Функция ОрганизацияУчетнойЗаписи(ИдентификаторЭДО) Экспорт
	ИдентификаторыЭДО = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторЭДО);
	ОрганизацииУчетныхЗаписей = ОрганизацииУчетныхЗаписей(ИдентификаторыЭДО);
	Возврат ОрганизацииУчетныхЗаписей[ИдентификаторЭДО];
КонецФункции

// Параметры:
//  ИдентификаторыЭДО - Массив из Строка
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка - идентификатор учетной записи ЭДО.
//  * Значение - ПеречислениеСсылка.СпособыОбменаЭД
Функция СпособыОбменаУчетныхЗаписей(ИдентификаторыЭДО) Экспорт
	
	Результат = Новый Соответствие; // См. СпособыОбменаУчетныхЗаписей
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО,
		|	УчетныеЗаписиЭДО.СпособОбменаЭД
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО В (&ИдентификаторыЭДО)";
	
	Запрос.УстановитьПараметр("ИдентификаторыЭДО", ИдентификаторыЭДО);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.ИдентификаторЭДО, Выборка.СпособОбменаЭД);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * Организация - Неопределено,ОпределяемыйТип.Организация
//  * ЗаполнятьОператоровЭДО - Булево
//  * ЗаполнятьИдентификаторыОрганизации - Булево
//  * УчетнаяЗаписьОблачногоЭДО - Неопределено,СправочникСсылка.УчетныеЗаписиОблачногоЭДО
Функция НовыеПараметрыДанныхПоИдентификаторамДляРегистрацииВЭДО() Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", Неопределено);
	Параметры.Вставить("ЗаполнятьОператоровЭДО", Ложь);
	Параметры.Вставить("ЗаполнятьИдентификаторыОрганизации", Ложь);
	Параметры.Вставить("УчетнаяЗаписьОблачногоЭДО", Неопределено);
	Возврат Параметры;
КонецФункции

// Возвращаемое значение:
//  ТаблицаЗначений:
//  * ИНН - Строка
//  * КПП - Строка
//  * ОГРН - Строка
//  * Сертификат - Строка
//  * СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД
//  * Наименование - Строка
//  * Идентификатор - Строка
Функция НовыйСписокОператоровДляРегистрацииВЭДО() Экспорт
	СписокОператоров = Новый ТаблицаЗначений;
	СписокОператоров.Колонки.Добавить("ИНН", ОбщегоНазначения.ОписаниеТипаСтрока(12));
	СписокОператоров.Колонки.Добавить("КПП", ОбщегоНазначения.ОписаниеТипаСтрока(9));
	СписокОператоров.Колонки.Добавить("ОГРН", ОбщегоНазначения.ОписаниеТипаСтрока(15));
	СписокОператоров.Колонки.Добавить("Сертификат", ОбщегоНазначения.ОписаниеТипаСтрока(28));
	СписокОператоров.Колонки.Добавить("СпособОбмена", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыОбменаЭД"));
	СписокОператоров.Колонки.Добавить("Наименование", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	СписокОператоров.Колонки.Добавить("Идентификатор", ОбщегоНазначения.ОписаниеТипаСтрока(10));
	Возврат СписокОператоров;
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ИдентификаторыОрганизации - Неопределено,СписокЗначений из Строка
//  * ОператорыЭДО - Неопределено
//                 - см. НовыйСписокОператоровДляРегистрацииВЭДО
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
Функция НовыеДанныеПоИдентификаторамДляРегистрацииВЭДО() Экспорт
	Данные = Новый Структура;
	Данные.Вставить("ИдентификаторыОрганизации", Неопределено); // СписокЗначений из Строка
	Данные.Вставить("ОператорыЭДО", Неопределено);
	Данные.Вставить("КонтекстДиагностики", ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики());
	Возврат Данные;
КонецФункции

// Параметры:
//  ПараметрыДанных - см. НовыеПараметрыДанныхПоИдентификаторамДляРегистрацииВЭДО
// 
// Возвращаемое значение:
//  См. НовыеДанныеПоИдентификаторамДляРегистрацииВЭДО
Функция ДанныеПоИдентификаторамДляРегистрацииВЭДО(ПараметрыДанных) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыДанных.УчетнаяЗаписьОблачногоЭДО) Тогда
		ОбщийМодульУчетныеЗаписиЭДОВызовОблака = ОбщегоНазначения.ОбщийМодуль("УчетныеЗаписиЭДОИнтеграцияОблака");
		Возврат ОбщийМодульУчетныеЗаписиЭДОВызовОблака.ДанныеПоИдентификаторамДляРегистрацииВЭДО(ПараметрыДанных);
	Иначе
		Возврат УчетныеЗаписиЭДОСлужебный.ДанныеПоИдентификаторамДляРегистрацииВЭДО(ПараметрыДанных);
	КонецЕсли;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
// * УникальныйИдентификаторЗаявки1СЭДО - Строка
// * НаименованиеОрганизации - Строка
// * ИННОрганизации - Строка
// * КППОрганизации - Строка
// * ОГРНОрганизации - Строка
// * ИдентификаторОрганизации - Строка
// * ОператорЭДО - Строка
// * ОператорЭДОИд - Строка
// * Сертификат - Массив из Строка - набор сертификатов в формате base64.
// * ЭлектроннаяПочта - Строка
// * Фамилия - Строка
// * Имя - Строка
// * Отчество - Строка
Функция НовыеРеквизитыРегистрационногоПакета() Экспорт
	РеквизитыПакета = Новый Структура;
	РеквизитыПакета.Вставить("УникальныйИдентификаторЗаявки1СЭДО", "");
	РеквизитыПакета.Вставить("НаименованиеОрганизации", "");
	РеквизитыПакета.Вставить("ИННОрганизации", "");
	РеквизитыПакета.Вставить("КППОрганизации", "");
	РеквизитыПакета.Вставить("ОГРНОрганизации", "");
	РеквизитыПакета.Вставить("ИдентификаторОрганизации", "");
	РеквизитыПакета.Вставить("ОператорЭДО", "");
	РеквизитыПакета.Вставить("ОператорЭДОИд", "");
	РеквизитыПакета.Вставить("Сертификат", Новый Массив);
	РеквизитыПакета.Вставить("ЭлектроннаяПочта", "");
	РеквизитыПакета.Вставить("Фамилия", "");
	РеквизитыПакета.Вставить("Имя", "");
	РеквизитыПакета.Вставить("Отчество", "");
	Возврат РеквизитыПакета;
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * РеквизитыПакета - см. НовыеРеквизитыРегистрационногоПакета
//  * ДвоичныеДанныеДляОператораЭДО - Неопределено,ДвоичныеДанные
//  * ДвоичныеДанныеСоглашенияНаПодключениеЭДО - Неопределено,ДвоичныеДанные
//  * ОператорЭДОСертификат - Строка
//  * ДобавляемыйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  * ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО - Неопределено,ДвоичныеДанные
//  * ПодписанныеДвоичныеДанныеДляОператораЭДО - Неопределено,ДвоичныеДанные
//  * ЗашифрованныеДанныеОператораЭДО - Неопределено,ДвоичныеДанные
//  * ТекстОшибки - Строка
Функция НовыеДанныеДляРегистрационногоПакета1СЭДО() Экспорт
	ДанныеПакета = Новый Структура;
	ДанныеПакета.Вставить("РеквизитыПакета", Новый Структура);
	ДанныеПакета.Вставить("ДвоичныеДанныеДляОператораЭДО", Неопределено);
	ДанныеПакета.Вставить("ДвоичныеДанныеСоглашенияНаПодключениеЭДО", Неопределено);
	ДанныеПакета.Вставить("ОператорЭДОСертификат", "");
	ДанныеПакета.Вставить("ДобавляемыйСертификат", Справочники.СертификатыКлючейЭлектроннойПодписиИШифрования.ПустаяСсылка());
	ДанныеПакета.Вставить("ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО", Неопределено);
	ДанныеПакета.Вставить("ПодписанныеДвоичныеДанныеДляОператораЭДО", Неопределено);
	ДанныеПакета.Вставить("ЗашифрованныеДанныеОператораЭДО", Неопределено);
	ДанныеПакета.Вставить("ТекстОшибки", "");
	Возврат ДанныеПакета;
КонецФункции

// Параметры:
//  ИдентификаторЭДО - Строка
// 
// Возвращаемое значение:
//  - Неопределено
//  - Структура:
//  * ИдентификаторЭДО - Строка
//  * Наименование - Строка
//  * Назначение - Строка
//  * ПодробноеОписание - Строка
Функция ИнформацияОбУчетнойЗаписи(ИдентификаторЭДО) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УчетныеЗаписиЭДО.НазначениеУчетнойЗаписи КАК Назначение,
		|	УчетныеЗаписиЭДО.ПодробноеОписаниеУчетнойЗаписи КАК ПодробноеОписание,
		|	УчетныеЗаписиЭДО.НаименованиеУчетнойЗаписи КАК Наименование
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО = &ИдентификаторЭДО";
	
	Запрос.УстановитьПараметр("ИдентификаторЭДО", ИдентификаторЭДО);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИнформацияОбУчетнойЗаписиЭДО = Новый Структура;
	ИнформацияОбУчетнойЗаписиЭДО.Вставить("ИдентификаторЭДО", ИдентификаторЭДО);
	ИнформацияОбУчетнойЗаписиЭДО.Вставить("Наименование", "");
	ИнформацияОбУчетнойЗаписиЭДО.Вставить("Назначение", "");
	ИнформацияОбУчетнойЗаписиЭДО.Вставить("ПодробноеОписание", "");
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(ИнформацияОбУчетнойЗаписиЭДО, Выборка);
	
	Возврат ИнформацияОбУчетнойЗаписиЭДО;
	
КонецФункции

// Параметры:
//  ИдентификаторыЭДО - Массив из Строка
// 
// Возвращаемое значение:
//  Структура:
//  * ИдентификаторыДляЛокальногоЭДО - Массив из Строка
//  * ИдентификаторыДляОблачногоЭДО - Массив из Строка
Функция ИдентификаторыПоИнтеграциямЭДО(ИдентификаторыЭДО) Экспорт
	
	ИдентификаторыДляЛокальногоЭДО = Новый Массив; // Массив из Строка
	ИдентификаторыДляОблачногоЭДО = Новый Массив; // Массив из Строка
	
	ИдентификаторыПоИнтеграциямЭДО = Новый Структура;
	ИдентификаторыПоИнтеграциямЭДО.Вставить("ИдентификаторыДляЛокальногоЭДО", ИдентификаторыДляЛокальногоЭДО);
	ИдентификаторыПоИнтеграциямЭДО.Вставить("ИдентификаторыДляОблачногоЭДО", ИдентификаторыДляОблачногоЭДО);
	
	Выборка = РегистрыСведений.УчетныеЗаписиЭДО.ВыборкаПризнакаОбменаЧерезОблачныйЭДО(ИдентификаторыЭДО);
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоОблачныйЭДО Тогда
			ИдентификаторыДляОблачногоЭДО.Добавить(Выборка.ИдентификаторЭДО);
		Иначе
			ИдентификаторыДляЛокальногоЭДО.Добавить(Выборка.ИдентификаторЭДО);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИдентификаторыПоИнтеграциямЭДО;
	
КонецФункции

// Параметры:
//  ИдентификаторЭДО - Строка
// 
// Возвращаемое значение:
//  Булево
Функция ЭтоУчетнаяЗаписьОблачногоЭДО(ИдентификаторЭДО) Экспорт
	
	Выборка = РегистрыСведений.УчетныеЗаписиЭДО.ВыборкаПризнакаОбменаЧерезОблачныйЭДО(ИдентификаторЭДО);
	Если Выборка.Следующий() 
		И Выборка.ЭтоОблачныйЭДО Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Возвращаемое значение:
//  Булево
Функция ЕстьУчетныеЗаписиЧерезОблачныйЭДО() Экспорт
	Возврат РегистрыСведений.УчетныеЗаписиЭДО.ЕстьУчетныеЗаписиЧерезОблачныйЭДО();
КонецФункции

// Возвращаемое значение:
//  Булево
Функция ЕстьУчетныеЗаписиЧерезСервис1СЭДО() Экспорт
	Возврат РегистрыСведений.УчетныеЗаписиЭДО.ЕстьУчетныеЗаписиЧерезСервис1СЭДО();
КонецФункции

// Параметры:
//  Организация - ОпределяемыйТип.Организация
//  ЭтоОблачныйЭДО - Неопределено,Булево
// 
// Возвращаемое значение:
//  Массив из Строка
Функция ИдентификаторыЭДОПоОрганизации(Организация, ЭтоОблачныйЭДО = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Если ЭтоОблачныйЭДО <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И УчетныеЗаписиЭДО.ЭтоОблачныйЭДО = &ЭтоОблачныйЭДО";
		Запрос.УстановитьПараметр("ЭтоОблачныйЭДО", ЭтоОблачныйЭДО);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ИдентификаторЭДО");
	
КонецФункции

// Параметры:
//  СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД
//  ЭтоОблачныйЭДО - Неопределено,Булево
// 
// Возвращаемое значение:
//  Массив из Строка
Функция ИдентификаторыУчетныхЗаписейСпособаОбмена(СпособОбмена, ЭтоОблачныйЭДО = Неопределено) Экспорт
	
	УчетныеЗаписи = Новый Массив; // Массив из Строка
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.СпособОбменаЭД = &СпособОбменаЭД
		|	И &УсловиеЭтоОблачныйЭДО";
	
	Запрос.УстановитьПараметр("СпособОбменаЭД", СпособОбмена);
	Если ЭтоОблачныйЭДО = Неопределено Тогда
		УсловиеЭтоОблачныйЭДО = "ИСТИНА";
	Иначе
		УсловиеЭтоОблачныйЭДО = ?(ЭтоОблачныйЭДО, "", "НЕ ") + "УчетныеЗаписиЭДО.ЭтоОблачныйЭДО";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеЭтоОблачныйЭДО", УсловиеЭтоОблачныйЭДО);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		УчетныеЗаписи.Добавить(Выборка.ИдентификаторЭДО);
	КонецЦикла;
	
	Возврат УчетныеЗаписи;
	
КонецФункции

// Возвращаемое значение:
//  Строка
Функция ИдентификаторОшибкиНедействительныеСертификаты() Экспорт
	Возврат "НедействительныеСертификаты";
КонецФункции

// Параметры:
//  НаименованиеУчетнойЗаписи - Строка
//  ИдентификаторУчетнойЗаписи - Строка
//
// Возвращаемое значение:
//  см. ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки
Функция ВидОшибкиНедействительныеСертификаты(НаименованиеУчетнойЗаписи, ИдентификаторУчетнойЗаписи) Экспорт

	ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки();
	ВидОшибки.Идентификатор = ИдентификаторОшибкиНедействительныеСертификаты();
	ВидОшибки.ЗаголовокПроблемы = НСтр("ru = 'В учетной записи есть недействительные сертификаты';
										|en = 'The account contains invalid certificates'");
	ВидОшибки.ОписаниеПроблемы =
		ТекстСообщенияВУчетнойЗаписиНедействительныеСеритификаты(НаименованиеУчетнойЗаписи);
	ОписаниеРешения = НСтр("ru = '<a href = ""%1"">Удалите </a> недействительные сертификаты учетной записи';
							|en = '<a href = ""%1"">Delete </a> invalid account certificates'");
	ВидОшибки.ОписаниеРешения = СтрШаблон(ОписаниеРешения, "Удалите");
	ВидОшибки.ВыводитьСсылкуНаСписокОшибок = Ложь;
	ВидОшибки.ОбработчикиНажатия.Вставить("Удалите", "УчетныеЗаписиЭДОСлужебныйКлиент.ОткрытьФормуУчетнойЗаписи");
	ВидОшибки.ПараметрыОбработчиков.Вставить("Удалите", ИдентификаторУчетнойЗаписи);
	Возврат ВидОшибки;
	
КонецФункции

// Параметры:
//  НаименованиеУчетнойЗаписи - Строка
//
// Возвращаемое значение:
//  Строка
Функция ТекстСообщенияВУчетнойЗаписиНедействительныеСеритификаты(НаименованиеУчетнойЗаписи) Экспорт
	
	Возврат СтрШаблон(НСтр("ru = 'В учетной записи ""%1""
		|есть недействительные сертификаты.
		|Получение и отправка электронных документов по ней невозможны.';
		|en = 'The ""%1""
		|account contains invalid certificates.
		|Cannot receive and send electronic documents.'"), НаименованиеУчетнойЗаписи);
	
КонецФункции

// Параметры:
//  ДанныеДляСинхронизации - см. СинхронизацияЭДОСлужебный.ДанныеДляСинхронизации
//  ОбработанныеУчетныеЗаписи - Соответствие из КлючИЗначение:
//  * Ключ - Строка
//  * Значение - см. ДиагностикаЭДОКлиентСервер.НовоеОписаниеОбработаннойУчетнойЗаписи
Процедура ОбработатьОшибкиСинхронизации(ДанныеДляСинхронизации, ОбработанныеУчетныеЗаписи) Экспорт
	
	Для Каждого КлючИЗначение Из ДанныеДляСинхронизации.УчетныеЗаписиБезДоступаКСертификатам Цикл
		ОбработаннаяУчетнаяЗапись = ДиагностикаЭДОКлиентСервер.НовоеОписаниеОбработаннойУчетнойЗаписи();
		ОбработаннаяУчетнаяЗапись.Идентификатор = КлючИЗначение.Ключ;
		КоличествоСертификатов = КлючИЗначение.Значение.Количество();
		Если КоличествоСертификатов = 1 Тогда
			ШаблонОшибки = НСтр("ru = '<a href = ""%1"">Сертификат</a> доступен, но в поле ""Пользователь"" сертификата учетной записи указан пользователь, отличный от текущего';
								|en = '<a href = ""%1"">Certificate</a> is available, but the ""User"" field of the account contains the user, which is different from the current one'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, "Сертификаты");
		Иначе 
			ШаблонОшибки = НСтр("ru = '<a href = ""%1"">Сертификаты(%2)</a> доступны, но в поле ""Пользователь"" сертификата учетной записи указан пользователь, отличный от текущего';
								|en = '<a href = ""%1"">Certificates (%2)</a> are available, but the ""User"" field of the account contains the user, which is different from the current one'");
			ТекстОшибки = СтрШаблон(ШаблонОшибки, "Сертификаты", КоличествоСертификатов);
		КонецЕсли;
		ИнформацияОбОшибке = ОбработаннаяУчетнаяЗапись.ИнформацияОбОшибке;
		ИнформацияОбОшибке.ВидОшибки = ДиагностикаЭДОКлиентСервер.ВидОшибкиОтправкиИПолученияНетДоступаКСертификатам();
		ИнформацияОбОшибке.КраткоеПредставление = НСтр("ru = 'Нет доступа к сертификатам учетной записи';
														|en = 'No access to account certificates'");
		ИнформацияОбОшибке.ПодробноеПредставление.Текст = СтроковыеФункции.ФорматированнаяСтрока(ТекстОшибки);
		Обработчик = Новый Структура;
		Обработчик.Вставить("ИмяПроцедуры", "ДиагностикаЭДОКлиент.ОткрытьСписокСертификатов");
		Обработчик.Вставить("Параметр", КлючИЗначение.Значение);
		ИнформацияОбОшибке.ПодробноеПредставление.ОбработчикиНажатия.Вставить("Сертификаты", Обработчик);
		ОбработанныеУчетныеЗаписи.Вставить(КлючИЗначение.Ключ, ОбработаннаяУчетнаяЗапись);
	КонецЦикла;
	
	Для Каждого УчетнаяЗапись Из ДанныеДляСинхронизации.УчетныеЗаписиБезДоступныхСертификатов Цикл
		ОбработаннаяУчетнаяЗапись = ДиагностикаЭДОКлиентСервер.НовоеОписаниеОбработаннойУчетнойЗаписи();
		ОбработаннаяУчетнаяЗапись.Идентификатор = УчетнаяЗапись;
		ТекстОшибки = НСтр("ru = 'Недоступен ни один личный сертификат из привязанных к учетной записи';
							|en = 'None of the personal certificates associated with the account is available'");
		ИнформацияОбОшибке = ОбработаннаяУчетнаяЗапись.ИнформацияОбОшибке;
		ИнформацияОбОшибке.ВидОшибки = ДиагностикаЭДОКлиентСервер.ВидОшибкиОтправкиИПолученияНетДоступныхСертификатов();
		ИнформацияОбОшибке.КраткоеПредставление = НСтр("ru = 'Нет доступных сертификатов';
														|en = 'There are no available certificates'");
		ИнформацияОбОшибке.ПодробноеПредставление.Текст = ТекстОшибки;
		ОбработанныеУчетныеЗаписи.Вставить(УчетнаяЗапись, ОбработаннаяУчетнаяЗапись);
	КонецЦикла;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// См. СинхронизацияЭДО.ПриЗаполненииСписковСОграничениемДоступа
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт

	Списки.Вставить(Метаданные.РегистрыСведений.УчетныеЗаписиЭДО, Истина);
	
КонецПроцедуры

// См. СинхронизацияЭДО.ПриЗаполненииВидовОграниченийПравОбъектовМетаданных
Процедура ПриЗаполненииВидовОграниченийПравОбъектовМетаданных(Описание) Экспорт
	
	Описание = Описание + "
	|РегистрСведений.УчетныеЗаписиЭДО.Чтение.Организации
	|РегистрСведений.УчетныеЗаписиЭДО.Изменение.Организации
	|";
	
КонецПроцедуры

#КонецОбласти

// Выполняет проверку существования учетной записи ЭДО.
// 
// Параметры:
//  Отбор - Структура:
//  *НазначениеУчетнойЗаписи - Строка
//  *КодНалоговогоОргана - Строка
//  *СпособОбменаЭД - ПеречислениеСсылка.СпособыОбменаЭД
//  *Организация - ОпределяемыйТип.Организация
// 
// Возвращаемое значение:
//  Булево - Учетная запись существует
Функция УчетнаяЗаписьСуществует(Знач Отбор) Экспорт

	ЗапросУчетныхЗаписей = ЗапросУчетныхЗаписей("УчетныеЗаписиЭДО");
	
	Запросы = Новый Массив;
	Запросы.Добавить(ЗапросУчетныхЗаписей);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО
	|ИЗ
	|	УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
	|ГДЕ
	|	УчетныеЗаписиЭДО.НазначениеУчетнойЗаписи = &НазначениеУчетнойЗаписи
	|	И УчетныеЗаписиЭДО.КодНалоговогоОргана = &КодНалоговогоОргана
	|	И УчетныеЗаписиЭДО.СпособОбменаЭД = &СпособОбменаЭД
	|	И УчетныеЗаписиЭДО.Организация = &Организация";
	
	ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
	ИтоговыйЗапрос.УстановитьПараметр("НазначениеУчетнойЗаписи", Отбор.НазначениеУчетнойЗаписи);
    ИтоговыйЗапрос.УстановитьПараметр("КодНалоговогоОргана", Отбор.КодНалоговогоОргана);
	ИтоговыйЗапрос.УстановитьПараметр("СпособОбменаЭД", Отбор.СпособОбменаЭД);
	ИтоговыйЗапрос.УстановитьПараметр("Организация", Отбор.Организация);
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = ИтоговыйЗапрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	Возврат Не РезультатЗапроса.Пустой();
КонецФункции

// Параметры:
//  ИдентификаторУчетнойЗаписи - Строка
//  ПроизвестиПроверкуДействительныхСертификатовНаОтзыв - Булево
//
// Возвращаемое значение:
//  Булево
Функция ЕстьНедействительныеСертификаты(ИдентификаторУчетнойЗаписи,
	ПроизвестиПроверкуДействительныхСертификатовНаОтзыв = Ложь) Экспорт
	
	Если ПроизвестиПроверкуДействительныхСертификатовНаОтзыв Тогда
		ПроверкаНаОтзывДействительныхСертификатов(ИдентификаторУчетнойЗаписи);
	КонецЕсли;
	КоличествоПросроченныхСертификатов = КоличествоПросроченныхСертификатовУчетнойЗаписи(ИдентификаторУчетнойЗаписи);
	КоличествоОтозванныхСертификатов = КоличествоОтозванныхСертификатовУчетнойЗаписи(ИдентификаторУчетнойЗаписи);
	Возврат КоличествоПросроченныхСертификатов + КоличествоОтозванныхСертификатов > 0;
	
КонецФункции

// Возвращаемое значение:
//  Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
Функция ОтозванныеСертификаты() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СертификатыУчетныхЗаписейЭДО.Сертификат КАК Сертификат
		|ИЗ
		|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО";
	Сертификаты = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сертификат");
	Возврат ИнтеграцияБСПБЭД.ОтозванныеСертификаты(Сертификаты);
	
КонецФункции

// см. ОбменСКонтрагентами.ДанныеУчетныхЗаписейЭДООрганизации
//
Функция ДанныеУчетныхЗаписейЭДООрганизации(Организация) Экспорт
	
	УчетныеЗаписи = ЗначенияСвойствУчетныхЗаписейОрганизацииСервисаЭДО(Организация,
		"ИдентификаторЭДО, НаименованиеУчетнойЗаписи, НазначениеУчетнойЗаписи, СпособОбменаЭД");
	
	ИдентификаторыЭДО = ОбщегоНазначения.ВыгрузитьКолонку(УчетныеЗаписи, "ИдентификаторЭДО", Истина);
	ОператорыЭДО = СервисНастроекЭДО.НайтиОператоровЭДОПоИдентификаторам(ИдентификаторыЭДО);
	
	Для Каждого УчетнаяЗапись Из УчетныеЗаписи Цикл
		УчетнаяЗапись.Вставить("ИдентификаторОператора");
		УчетнаяЗапись.Вставить("ПредставлениеОператора");
		ОператорЭДО = ОператорыЭДО.Найти(УчетнаяЗапись.ИдентификаторЭДО, "ИдентификаторЭДО");
		Если ОператорЭДО <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(УчетнаяЗапись, ОператорЭДО);
		КонецЕсли;
	КонецЦикла;
	
	Возврат УчетныеЗаписи;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ЗаполнятьРеквизитыПоИНН - Булево
//
Функция ПараметрыПомощникаПодключенияЭДО() Экспорт
	
	ПараметрыПомощника = Новый Структура;
	ПараметрыПомощника.Вставить("ЗаполнятьРеквизитыПоИНН", Ложь);
	ОбменСКонтрагентамиПереопределяемый.ПриОпределенииПараметровПомощникаПодключенияЭДО(ПараметрыПомощника);
	Возврат ПараметрыПомощника;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область АвторизацияВСервисеЭДО

// Параметры:
//  ИдентификаторыЭДО - Массив из Строка
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка
//  * Значение - Строка
Функция ПредставленияУчетныхЗаписейЭДО(ИдентификаторыЭДО)
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
		|	УчетныеЗаписиЭДО.НаименованиеУчетнойЗаписи КАК НаименованиеУчетнойЗаписи
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО В (&ИдентификаторыЭДО)";
	
	Запрос.УстановитьПараметр("ИдентификаторыЭДО", ИдентификаторыЭДО);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.ИдентификаторЭДО, Выборка.НаименованиеУчетнойЗаписи);
	КонецЦикла;
	
	Возврат Результат
	
КонецФункции

#КонецОбласти

// Параметры:
//  ИмяВременнойТаблицы - Строка
//  ВыбираемыеПоля - Строка
//  ПоляУсловия - Строка
// 
// Возвращаемое значение:
//  Строка
Функция ШаблонЗапросаУчетныхЗаписей(ИмяВременнойТаблицы, ВыбираемыеПоля, ПоляУсловия = "") 
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&ВыбираемыеПоля
	|ПОМЕСТИТЬ ИмяВременнойТаблицы
	|ИЗ
	|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
	|ГДЕ
	|	&ПоляУсловия";
	
	Если ИмяВременнойТаблицы = "" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ИмяВременнойТаблицы", "");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяВременнойТаблицы", ИмяВременнойТаблицы);
	КонецЕсли;
	
	Возврат ОбщегоНазначенияБЭД.ТекстЗапросаИзШаблона(ТекстЗапроса, "УчетныеЗаписиЭДО", ВыбираемыеПоля, ПоляУсловия);
	
КонецФункции

// Параметры:
//  Организация - ОпределяемыйТип.Организация
//  Свойства - Строка
// 
// Возвращаемое значение:
//  Массив Из Структура
Функция ЗначенияСвойствУчетныхЗаписейОрганизацииСервисаЭДО(Организация, Свойства)
	
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОрганизацииПоИНН.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТ_Организации
		|ИЗ
		|	ИмяТаблицыОрганизации КАК Организации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИмяТаблицыОрганизации КАК ОрганизацииПоИНН
		|		ПО Организации.ИмяРеквизитаИНН = ОрганизацииПоИНН.ИмяРеквизитаИНН
		|ГДЕ
		|	Организации.Ссылка = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	&Свойства,
		|	УчетныеЗаписиЭДО.НаименованиеУчетнойЗаписи КАК Представление
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Организации КАК ВТ_Организации
		|		ПО УчетныеЗаписиЭДО.Организация = ВТ_Организации.Ссылка
		|ГДЕ
		|	УчетныеЗаписиЭДО.СпособОбменаЭД В (&СпособыОбменаЧерезОператора)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Свойства", Свойства);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяТаблицыОрганизации", ОбщегоНазначения.ИмяТаблицыПоСсылке(Организация));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяРеквизитаИНН",
		ИнтеграцияЭДО.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("СпособыОбменаЧерезОператора", СинхронизацияЭДО.СпособыОбменаЧерезОператора());
	
	МассивЗначенийСвойств = Новый Массив;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗначенияСвойств = Новый Структура(Свойства);
			ЗаполнитьЗначенияСвойств(ЗначенияСвойств, Выборка);
			МассивЗначенийСвойств.Добавить(ЗначенияСвойств);
		КонецЦикла;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует(
		"ЭлектронноеВзаимодействие.ЭлектронноеАктированиеЕИС") Тогда
		МодульЭлектронноеАктированиеЕИС = ОбщегоНазначения.ОбщийМодуль("ЭлектронноеАктированиеЕИС");
		Если МодульЭлектронноеАктированиеЕИС.ИспользоватьЭлектронноеАктированиеВЕИС() Тогда
			МодульЭлектронноеАктированиеЕИС.ДополнитьЗначенияСвойствУчетныхЗаписейОрганизацииСервисаЭДО(
				Организация,
				Свойства,
				МассивЗначенийСвойств);
		КонецЕсли;
	КонецЕсли;
		
	Возврат МассивЗначенийСвойств;
	
КонецФункции

// Параметры:
//  ИдентификаторУчетнойЗаписи - Строка
//
// Возвращаемое значение:
//  Массив из ХранилищеЗначения
Функция ДанныеНеотозванныхСертификатовУчетнойЗаписи(ИдентификаторУчетнойЗаписи)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СертификатыУчетныхЗаписейЭДО.Сертификат КАК Сертификат
		|ИЗ
		|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
		|ГДЕ
		|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО = &ИдентификаторЭДО
		|	И РАЗНОСТЬДАТ(&ТекущаяДата, СертификатыУчетныхЗаписейЭДО.ДействителенДо, МИНУТА) > 0";
	Запрос.УстановитьПараметр("ИдентификаторЭДО", ИдентификаторУчетнойЗаписи);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	ДействительныеСертификаты = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сертификат");
	Возврат ИнтеграцияБСПБЭД.ДанныеНеотозванныхСертификатов(ДействительныеСертификаты);
	
КонецФункции

// Параметры:
//  ИдентификаторУчетнойЗаписи - Строка
//
// Возвращаемое значение:
//  Число
Функция КоличествоОтозванныхСертификатовУчетнойЗаписи(ИдентификаторУчетнойЗаписи)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СертификатыУчетныхЗаписейЭДО.Сертификат КАК Сертификат
		|ИЗ
		|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
		|ГДЕ
		|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО = &ИдентификаторЭДО";
	Запрос.УстановитьПараметр("ИдентификаторЭДО", ИдентификаторУчетнойЗаписи);
	СертификатыУчетнойЗаписи = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сертификат");
	Возврат ИнтеграцияБСПБЭД.КоличествоОтозванныхСертификатов(СертификатыУчетнойЗаписи);
	
КонецФункции

// Параметры:
//  ИдентификаторУчетнойЗаписи - Строка
//
// Возвращаемое значение:
//  Число
Функция КоличествоПросроченныхСертификатовУчетнойЗаписи(ИдентификаторУчетнойЗаписи)
	
	Результат = 0;
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(СертификатыУчетныхЗаписейЭДО.Сертификат) КАК КоличествоСертификатов
		|ИЗ
		|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
		|ГДЕ
		|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО = &ИдентификаторЭДО
		|	И РАЗНОСТЬДАТ(&ТекущаяДата, СертификатыУчетныхЗаписейЭДО.ДействителенДо, МИНУТА) < 0";
	Запрос.УстановитьПараметр("ИдентификаторЭДО", ИдентификаторУчетнойЗаписи);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.КоличествоСертификатов;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

// Параметры:
// ИдентификаторУчетнойЗаписи - Строка
Процедура ПроверкаНаОтзывДействительныхСертификатов(ИдентификаторУчетнойЗаписи)
	
	МенеджерКриптографии = ЭлектроннаяПодпись.МенеджерКриптографии("", Ложь);
	ДанныеДействительныхСертификатов = ДанныеНеотозванныхСертификатовУчетнойЗаписи(ИдентификаторУчетнойЗаписи);
	Для Каждого ДанныеСертификата Из ДанныеДействительныхСертификатов Цикл
		ОписаниеОшибки = "";
		ЭлектроннаяПодпись.ПроверитьСертификат(МенеджерКриптографии, ДанныеСертификата.Получить(),
			ОписаниеОшибки);
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ИдентификаторЭДО - Строка
//  СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД
//  ЭтоОблачныйЭДО - Булево
Процедура ПриУдаленииУчетнойЗаписи(ИдентификаторЭДО, СпособОбмена, ЭтоОблачныйЭДО)
	
	НаборЗаписей = РегистрыСведений.СертификатыУчетныхЗаписейЭДО.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторЭДО.Установить(ИдентификаторЭДО);
	НаборЗаписей.Записать();
	
	Если СпособОбмена = Перечисления.СпособыОбменаЭД.ЧерезFTP Тогда
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.УдалитьДанныеИзБезопасногоХранилища(ИдентификаторЭДО);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	НастройкиЭДО.ПриУдаленииУчетнойЗаписи(ИдентификаторЭДО);
	ПриглашенияЭДО.ПриУдаленииУчетнойЗаписи(ИдентификаторЭДО);
	ОповещенияОСобытияхЭДО.ПриУдаленииУчетнойЗаписи(ИдентификаторЭДО);
	АвтоматическоеПолучениеЭДО.ПриУдаленииУчетнойЗаписи(ИдентификаторЭДО, СпособОбмена, ЭтоОблачныйЭДО);
	
	Если ЭтоОблачныйЭДО Тогда
		УчетныеЗаписиЭДОИнтеграцияОблака.ПриУдаленииУчетнойЗаписи(ИдентификаторЭДО);
	Иначе
		УчетныеЗаписиЭДОСлужебный.ПриУдаленииУчетнойЗаписи(ИдентификаторЭДО);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
