#Область СлужебныйПрограммныйИнтерфейс

// Доступные виды оповещений.
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//   * Ключ - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//   * Значение - Массив Из СправочникСсылка.ВидыОповещенийПользователей
Функция ДоступныеВидыОповещений() Экспорт
	
	ДоступныеВидыОповещенийПоТипам = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыОповещенийПользователейИсточникиДанных.ТипИсточника КАК ТипИсточника,
	|	ВидыОповещенийПользователейИсточникиДанных.РеквизитИсточника КАК РеквизитИсточника,
	|	ВидыОповещенийПользователейИсточникиДанных.Ссылка КАК ВидОповещения
	|ИЗ
	|	Справочник.ВидыОповещенийПользователей.ИсточникиДанных КАК ВидыОповещенийПользователейИсточникиДанных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыОповещенийПользователей КАК ВидыОповещенийПользователей
	|		ПО ВидыОповещенийПользователейИсточникиДанных.Ссылка = ВидыОповещенийПользователей.Ссылка
	|ГДЕ
	|	ВидыОповещенийПользователей.Активен = ИСТИНА
	|	И ВидыОповещенийПользователей.ЭтоГруппа = ЛОЖЬ
	|	И ВидыОповещенийПользователей.ПометкаУдаления = ЛОЖЬ
	|	И ВидыОповещенийПользователей.ТипОповещения = ЗНАЧЕНИЕ(Перечисление.ТипыОповещенийПользователей.Оперативное)
	|ИТОГИ ПО
	|	ТипИсточника";
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаТип = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаТип.Следующий() Цикл
		ВыборкаВиды = ВыборкаТип.Выбрать();
		МассивВидов = Новый Массив;
		Пока ВыборкаВиды.Следующий() Цикл
			РеквизитИсточника = ?(ВыборкаВиды.РеквизитИсточника = "", "Ссылка", ВыборкаВиды.РеквизитИсточника);
			
			СтруктураВидОповещения = Новый Структура();
			СтруктураВидОповещения.Вставить("ВидОповещения", ВыборкаВиды.ВидОповещения);
			СтруктураВидОповещения.Вставить("РеквизитИсточника", РеквизитИсточника);
			
			МассивВидов.Добавить(СтруктураВидОповещения);
		КонецЦикла;
		ДоступныеВидыОповещенийПоТипам.Вставить(ВыборкаТип.ТипИсточника, МассивВидов);
	КонецЦикла;
	
	Возврат ДоступныеВидыОповещенийПоТипам
	
КонецФункции

// Идентификатор пользователя автор оповещений.
// 
// Параметры:
//  Автор - СправочникСсылка.Пользователи, Неопределено - Автор оповещений
// 
// Возвращаемое значение:
//  УникальныйИдентификатор - Идентификатор пользователя автор оповещений
Функция ИдентификаторПользователяАвторОповещений(Автор = Неопределено) Экспорт
	Если НЕ ЗначениеЗаполнено(Автор) Тогда
		Автор = ОповещенияПользователейОСобытияхПовтИсп.АвторОповещенийПользователей();
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Автор, "ИдентификаторПользователяИБ");
	
КонецФункции

// Код языка пользователя.
// 
// Параметры:
//  ИдентификаторПользователяИБ - Строка - Идентификатор пользователя ИБ строкой.
// 
// Возвращаемое значение:
//  Строка - Код языка пользователя
Функция КодЯзыкаПользователя(Знач ИдентификаторПользователяИБ) Экспорт
	
	КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	Если ЗначениеЗаполнено(ИдентификаторПользователяИБ)
		И ТипЗнч(ИдентификаторПользователяИБ) <> Тип("УникальныйИдентификатор") Тогда
		
		ИдентификаторПользователяИБ = Новый УникальныйИдентификатор(ИдентификаторПользователяИБ);
	КонецЕсли;
	
	СвойстваПользователяИБ = Пользователи.СвойстваПользователяИБ(ИдентификаторПользователяИБ);
	Если СвойстваПользователяИБ <> Неопределено И ЗначениеЗаполнено(СвойстваПользователяИБ.Язык) Тогда
		Язык = Метаданные.Языки.Найти(СвойстваПользователяИБ.Язык);
		КодЯзыка = Язык.КодЯзыка;
	КонецЕсли;
	
	Возврат КодЯзыка
	
КонецФункции

// Источники оповещений исключения.
// 
// Возвращаемое значение:
//  Массив из Строка - Источники оповещений исключения
Функция ИсточникиОповещенийИсключения() Экспорт
	
	Возврат ОповещенияПользователейОСобытиях.ИсточникиОповещенийИсключения();
	
КонецФункции

// Автор оповещений пользователей.
// 
// Возвращаемое значение:
//  СправочникСсылка.Пользователи - Автор оповещений пользователей
Функция АвторОповещенийПользователей() Экспорт
	
	Автор = Константы.АвторОповещенийПользователей.Получить();
	ОповещенияПользователейОСобытияхПереопределяемый.ПриПолученииАвторОповещенийПользователей(Автор);
	
	Если Не ЗначениеЗаполнено(Автор) Тогда
		Автор = ОповещенияПользователейОСобытиях.СлужебныйПользователь();
		Константы.АвторОповещенийПользователей.Установить(Автор);
	КонецЕсли;
	
	Возврат Автор
КонецФункции

#КонецОбласти