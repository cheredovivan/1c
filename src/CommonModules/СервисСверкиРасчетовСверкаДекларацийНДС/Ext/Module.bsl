
#Область ПрограммныйИнтерфейс

// В процедуре выводидтся детальная информация по расхождениям в книге покупок/продаж, декларации по НДС
// 
//Параметры:
// Форма - ФормаКлиентскогоПриложения - форма отчета (книга покупок/продаж)
//			или форма с результатами сверки (для декларации по НДС) где необходимо вывести результаты сверки
// ДопЛист - Булево - для книг покупок/продаж могут выводиться данные для основного раздела или для доп листов
// НомерРаздела - Строка - для декларации по НДС в одной форме выводятся сразу все разделы 8, 8.1, 9, 9.1
// 
Процедура ОткрытьДокументыСРасхождениями(Форма, ДопЛист = Ложь, НомерРаздела = "") Экспорт
	
	ТаблицыСверки = ПолучитьИзВременногоХранилища(Форма.ДанныеДляСверкиДеклараций.АдресРезультатаСверки);
	Если Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "КнигаПокупок"
		Или НомерРаздела = "Раздел8"
		Или НомерРаздела = "Раздел8_Прил1" Тогда
		ИмяОтчета = "КнигаПокупок";
	Иначе
		ИмяОтчета = "КнигаПродаж";
	КонецЕсли;
	
	Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел = "ТаблицаРасхождения" + НомерРаздела;
	
	Если ДопЛист Тогда
		ТаблицаРасхождения = ТаблицыСверки.ТаблицаРасхожденияДопЛист.Выгрузить();
	Иначе
		ТаблицаРасхождения = ТаблицыСверки["ТаблицаРасхождения" + НомерРаздела].Выгрузить();
	КонецЕсли;
	
	СтрокаИтоги = Новый Структура("Всего, НДС, СуммаБезНДСКнига, НДСКнига, СуммаКонтрагента, НДСКонтрагента", 0, 0, 0, 0, 0, 0);
	ТаблицаРасхождения.Сортировать("ДатаСчетаФактуры");
	ТаблицаКонтрагентов = ТаблицаРасхождения.Скопировать();
	Если ИмяОтчета = "КнигаПокупок" Тогда
		СтрокиСуммирования = "Всего, НДС, СуммаБезНДСКнига, НДСКнига, СуммаКонтрагента, НДСКонтрагента";
	Иначе
		СтрокиСуммирования = "Всего, НДС, СуммаКонтрагента, НДСКонтрагента";
	КонецЕсли;
	ТаблицаКонтрагентов.Свернуть("Контрагент, КонтрагентНаименование, КонтрагентИНН, КонтрагентКПП", СтрокиСуммирования);
	ТаблицаКонтрагентов.Сортировать("Всего Убыв");
	
	Макет = ПолучитьОбщийМакет("РезультатыСверки" + ИмяОтчета);
	
	Результат = Новый ТабличныйДокумент();
	Результат.ОтображатьГруппировки = Истина;
	
	Если НомерРаздела <> "" Тогда
		Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокРасхождения" + НомерРаздела));
	Иначе
		Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокРасхождения"));
	КонецЕсли;
	Результат.Вывести(Макет.ПолучитьОбласть("Шапка"),1);
	
	СтрокаКонтрагент         = Макет.ПолучитьОбласть("Контрагент");
	СтрокаИтогиПоКонтрагенту = Макет.ПолучитьОбласть("ИтогоПоКонтрагенту");
	Итоги                    = Макет.ПолучитьОбласть("Итоги");
	Сч = 1;
	Для Каждого Контрагент Из ТаблицаКонтрагентов Цикл
		Если Не ЗначениеЗаполнено(Контрагент.КонтрагентНаименование) Тогда
			Продолжить;
		КонецЕсли;
		СтрокаКонтрагент.Параметры.Контрагент = Контрагент.Контрагент;
		Если Контрагент.КонтрагентИНН = "" И Контрагент.КонтрагентКПП = "" Тогда
			СтрокаКонтрагент.Параметры.КонтрагентНаименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1';
				|en = '%1'"),
			Контрагент.КонтрагентНаименование);
		ИначеЕсли Контрагент.КонтрагентКПП = "" Тогда
			СтрокаКонтрагент.Параметры.КонтрагентНаименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 (%2)';
				|en = '%1 (%2)'"),
			Контрагент.КонтрагентНаименование,
			Контрагент.КонтрагентИНН);
		Иначе
			СтрокаКонтрагент.Параметры.КонтрагентНаименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 (%2/%3)';
				|en = '%1 (%2/%3)'"),
			Контрагент.КонтрагентНаименование,
			Контрагент.КонтрагентИНН,
			Контрагент.КонтрагентКПП);
		КонецЕсли;
		
		Результат.Вывести(СтрокаКонтрагент, 1);
		ТаблицаРасхожденийПоДокументам = ТаблицаРасхождения.НайтиСтроки(Новый Структура ("Контрагент", Контрагент.Контрагент));
		Результат.НачатьГруппуСтрок();
		Для Каждого Строка Из ТаблицаРасхожденийПоДокументам Цикл
			СтрокаРасхождения = Макет.ПолучитьОбласть("Строка");
			СтрокаРасхождения.Параметры.пп = Сч;
			Сч = Сч + 1;
			СтрокаРасхождения.Параметры.КодВидаОперации = Строка.КодВидаОперации;
			СтрокаРасхождения.Параметры.Документ = Строка.СчетФактураДокумент;
			СтрокаРасхождения.Параметры.ДатаНомерСФОрганизация = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 от %2';
				|en = '%1 dated %2'"),
			Строка.НомерСчетаФактуры,
			Формат(Строка.ДатаСчетаФактуры,  "ДЛФ=Д"));
			Если НЕ (Строка.ДатаСчетФактурыК = Дата('00010101')
				И Строка.НомерСчетФактурыК = "") Тогда
				СтрокаРасхождения.Параметры.ДатаНомерСФКонтрагент = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 от %2';
					|en = '%1 dated %2'"),
				Строка.НомерСчетФактурыК,
				Формат(Строка.ДатаСчетФактурыК, "ДЛФ=Д"));
			КонецЕсли;
			Если НачалоДня(Строка.ДатаСчетФактурыК) <> НачалоДня(Строка.ДатаСчетаФактуры)
				Или Строка.НомерСчетФактурыК <> Строка.НомерСчетаФактуры Тогда
				СтрокаРасхождения.Области.СФКонтрагента.ЦветТекста = WebЦвета.Красный;
			Иначе
				СтрокаРасхождения.Области.СФКонтрагента.ЦветТекста = WebЦвета.Черный;
			КонецЕсли;
			СтрокаРасхождения.Параметры.СуммаКонтрагент  = Строка.СуммаКонтрагента;
			СтрокаРасхождения.Параметры.НДСКонтрагент    = Строка.НДСКонтрагента;
			Если Строка.Всего <> Строка.СуммаКонтрагента Тогда
				СтрокаРасхождения.Области.СуммаКонтрагента.ЦветТекста = WebЦвета.Красный;
			Иначе
				СтрокаРасхождения.Области.СуммаКонтрагента.ЦветТекста = WebЦвета.Черный;
			КонецЕсли;
			Если Строка.НДС <> Строка.НДСКонтрагента Тогда
				СтрокаРасхождения.Области.НДСКонтрагента.ЦветТекста = WebЦвета.Красный;
			Иначе
				СтрокаРасхождения.Области.НДСКонтрагента.ЦветТекста = WebЦвета.Черный;
			КонецЕсли;
			Если ИмяОтчета = "КнигаПокупок" Тогда
				СтрокаРасхождения.Параметры.СуммаДокумент    = Строка.Всего;
				СтрокаРасхождения.Параметры.НДСДокумент      = Строка.НДС;
				СтрокаРасхождения.Параметры.СуммаОрганизация = Строка.СуммаБезНДСКнига;
				СтрокаРасхождения.Параметры.НДСОрганизация   = Строка.НДСКнига;
			Иначе
				СтрокаРасхождения.Параметры.СуммаОрганизация = Строка.Всего;
				СтрокаРасхождения.Параметры.НДСОрганизация   = Строка.НДС;
			КонецЕсли;
			Результат.Вывести(СтрокаРасхождения, 2);
			
		КонецЦикла;
		Результат.ЗакончитьГруппуСтрок();
		
		СтрокаИтогиПоКонтрагенту.Параметры.СуммаКонтрагент  = Контрагент.СуммаКонтрагента;
		СтрокаИтогиПоКонтрагенту.Параметры.НДСКонтрагент    = Контрагент.НДСКонтрагента;
		СтрокаИтоги.СуммаКонтрагента = СтрокаИтоги.СуммаКонтрагента + Контрагент.СуммаКонтрагента;
		СтрокаИтоги.НДСКонтрагента   = СтрокаИтоги.НДСКонтрагента + Контрагент.НДСКонтрагента;
		
		Если ИмяОтчета = "КнигаПокупок" Тогда
			СтрокаИтогиПоКонтрагенту.Параметры.СуммаОрганизация = Контрагент.СуммаБезНДСКнига;
			СтрокаИтогиПоКонтрагенту.Параметры.НДСОрганизация   = Контрагент.НДСКнига;
			СтрокаИтогиПоКонтрагенту.Параметры.СуммаДокумент    = Контрагент.Всего;
			СтрокаИтогиПоКонтрагенту.Параметры.НДСДокумент      = Контрагент.НДС;
			
			СтрокаИтоги.Всего            = СтрокаИтоги.Всего + Контрагент.Всего;
			СтрокаИтоги.НДС              = СтрокаИтоги.НДС + Контрагент.НДС;
			СтрокаИтоги.СуммаБезНДСКнига = СтрокаИтоги.СуммаБезНДСКнига + Контрагент.СуммаБезНДСКнига;
			СтрокаИтоги.НДСКнига         = СтрокаИтоги.НДСКнига + Контрагент.НДСКнига;
		Иначе
			СтрокаИтогиПоКонтрагенту.Параметры.СуммаОрганизация = Контрагент.Всего;
			СтрокаИтогиПоКонтрагенту.Параметры.НДСОрганизация   = Контрагент.НДС;
			
			СтрокаИтоги.Всего            = СтрокаИтоги.Всего + Контрагент.Всего;
			СтрокаИтоги.НДС              = СтрокаИтоги.НДС + Контрагент.НДС;
		КонецЕсли;
		
		Результат.Вывести(СтрокаИтогиПоКонтрагенту, 2);
		
	КонецЦикла;
	
	Итоги.Параметры.СуммаОрганизация = СтрокаИтоги.Всего;
	Итоги.Параметры.НДСОрганизация   = СтрокаИтоги.НДС;
	Итоги.Параметры.СуммаКонтрагент  = СтрокаИтоги.СуммаКонтрагента;
	Итоги.Параметры.НДСКонтрагент    = СтрокаИтоги.НДСКонтрагента;
	Если ИмяОтчета = "КнигаПокупок" Тогда
		Итоги.Параметры.СуммаДокумент = СтрокаИтоги.Всего;
		Итоги.Параметры.НДСДокумент   = СтрокаИтоги.НДС;
		Итоги.Параметры.СуммаОрганизация = СтрокаИтоги.СуммаБезНДСКнига;
		Итоги.Параметры.НДСОрганизация   = СтрокаИтоги.НДСКнига;
	Иначе
		Итоги.Параметры.СуммаОрганизация = СтрокаИтоги.Всего;
		Итоги.Параметры.НДСОрганизация   = СтрокаИтоги.НДС;
		
	КонецЕсли;
	Результат.Вывести(Итоги, 1);
	
	Если Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "Декларация" Тогда
		СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер.СделатьВыделениеТекущегоРазделаДляДекларации(Форма, Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел, НомерРаздела);
	Иначе
		СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер.СделатьВыделениеТекущегоРаздела(Форма, Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел);
	КонецЕсли;
	
	Форма.Результат = Результат;
	
КонецПроцедуры

// В процедуре выводидтся детальная информация по выбранному разделу сверки в книге покупок/продаж, декларации по НДС
// 
//Параметры:
// Форма - ФормаКлиентскогоПриложения -форма отчета (книга покупок/продаж)
//		или форма с результатами сверки (для декларации по НДС) где необходимо вывести результаты сверки
// ИмяТаблицы - Строка - имя таблицы, соответствующей разделу,
//				для которого надо вывести детальную информацию (нет у контрагента, не сверились, без расхождений)
// ДопЛист - Булево - для книг покупок/продаж могут выводиться данные для основного раздела или для доп листов
// НомерРаздела - Строка - для декларации по НДС в одной форме выводятся сразу все разделы 8, 8.1, 9, 9.1
//
Процедура ОткрытьДокументыБезРасхождений(Форма, ИмяТаблицы, ДопЛист = Ложь, НомерРаздела = "") Экспорт
	
	Если ИмяТаблицы = "" Или ИмяТаблицы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел = ИмяТаблицы;
	Если Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "КнигаПокупок"
		Или НомерРаздела = "Раздел8"
		Или НомерРаздела = "Раздел8_Прил1" Тогда
		ИмяОтчета = "КнигаПокупок";
	Иначе
		ИмяОтчета = "КнигаПродаж";
	КонецЕсли;	
		
		СтрокаИтоги = Новый Структура("Всего, НДС, СуммаБезНДСКнига, НДСКнига", 0, 0, 0, 0);
		ТаблицыСверки = ПолучитьИзВременногоХранилища(Форма.ДанныеДляСверкиДеклараций.АдресРезультатаСверки);
		ТаблицаСверки = ТаблицыСверки[ИмяТаблицы].Выгрузить();
		ТаблицаСверки.Сортировать("ДатаСчетаФактуры");
		ТаблицаКонтрагентов = ТаблицаСверки.Скопировать();
		Если ИмяОтчета = "КнигаПокупок" Тогда
			СтрокиСуммирования = "Всего, НДС, СуммаБезНДСКнига, НДСКнига";
		Иначе
			СтрокиСуммирования = "Всего, НДС";
		КонецЕсли;
		ТаблицаКонтрагентов.Свернуть("Контрагент, КонтрагентНаименование, КонтрагентИНН, КонтрагентКПП", СтрокиСуммирования);
		ТаблицаКонтрагентов.Сортировать("Всего Убыв");
			
		Макет = ПолучитьОбщийМакет("РезультатыСверкиБезРасхождения" + ИмяОтчета);
		
		Результат = Новый ТабличныйДокумент();
		Результат.ОтображатьГруппировки = Истина;
		Если ИмяТаблицы = "ТаблицаНеСверили" Или ИмяТаблицы = "ТаблицаНеСверилиДопЛист" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокНеСверились"));
		ИначеЕсли ИмяТаблицы = "ТаблицаНетУКонтрагента" Или ИмяТаблицы = "ТаблицаНетУКонтрагентаДопЛист" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокНеОтражено"));
		ИначеЕсли ИмяТаблицы = "ТаблицаНетВОрганизации" Или ИмяТаблицы = "ТаблицаНетВОрганизацииДопЛист" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокНеОтраженоВОрганизации"));
		ИначеЕсли ИмяТаблицы = "ТаблицаСверилиБезОшибок" Или ИмяТаблицы = "ТаблицаСверилиБезОшибокДопЛист" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокБезРасхождений"));
		ИначеЕсли ИмяТаблицы = "ТаблицаНетУКонтрагентаРаздел9" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокНеОтраженоРаздел9"));
		ИначеЕсли ИмяТаблицы  = "ТаблицаНетУКонтрагентаРаздел9_Прил1" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокНеОтраженоРаздел9_Прил1"));
		ИначеЕсли ИмяТаблицы = "ТаблицаНеСверилиРаздел9" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокНеСверилисьРаздел9"));
		ИначеЕсли ИмяТаблицы  = "ТаблицаНеСверилиРаздел9_Прил1" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокНеСверилисьРаздел9_Прил1"));
		ИначеЕсли ИмяТаблицы = "ТаблицаСверилиБезОшибокРаздел9" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокБезРасхожденийРаздел9"));
		ИначеЕсли ИмяТаблицы  = "ТаблицаСверилиБезОшибокРаздел9_Прил1" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокБезРасхожденийРаздел9_Прил1"));
		ИначеЕсли ИмяТаблицы = "ТаблицаНетВОрганизацииРаздел9" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокНеОтраженоВОрганизацииРаздел9"));
		ИначеЕсли ИмяТаблицы = "ТаблицаНетУКонтрагентаРаздел8" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокНеОтраженоРаздел8"));
		ИначеЕсли ИмяТаблицы  = "ТаблицаНетУКонтрагентаРаздел8_Прил1" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокНеОтраженоРаздел8_Прил1"));
		ИначеЕсли ИмяТаблицы = "ТаблицаНеСверилиРаздел8" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокНеСверилисьРаздел8"));
		ИначеЕсли ИмяТаблицы  = "ТаблицаНеСверилиРаздел8_Прил1" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокНеСверилисьРаздел8_Прил1"));
		ИначеЕсли ИмяТаблицы = "ТаблицаСверилиБезОшибокРаздел8" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокБезРасхожденийРаздел8"));
		ИначеЕсли ИмяТаблицы  = "ТаблицаСверилиБезОшибокРаздел8_Прил1" Тогда
			Результат.Вывести(Макет.ПолучитьОбласть("ЗаголовокБезРасхожденийРаздел8_Прил1"));
		КонецЕсли;
		
		Результат.Вывести(Макет.ПолучитьОбласть("ШапкаБезРасхождений"),1);
		
		СтрокаКонтрагент         = Макет.ПолучитьОбласть("КонтрагентБезРасхождений");
		СтрокаРасхождения        = Макет.ПолучитьОбласть("СтрокаБезРасхождений");
		СтрокаИтогиПоКонтрагенту = Макет.ПолучитьОбласть("ИтогоПоКонтрагентуБезРасхождений");
		Итоги                    = Макет.ПолучитьОбласть("ИтогиБезРасхождений");
		Сч = 1;
		Для Каждого Контрагент Из ТаблицаКонтрагентов Цикл
			Если Не ЗначениеЗаполнено(Контрагент.КонтрагентНаименование)
				И Не ЗначениеЗаполнено(Контрагент.КонтрагентИНН)
				И Не ЗначениеЗаполнено(Контрагент.КонтрагентКПП) Тогда
				Продолжить;
			КонецЕсли;
			СтрокаКонтрагент.Параметры.Контрагент = Контрагент.Контрагент;
			Если Не ЗначениеЗаполнено(Контрагент.КонтрагентНаименование) И Контрагент.КонтрагентИНН <> ""  И ИмяОтчета = "КнигаПродаж" Тогда
				Если ЗначениеЗаполнено(Контрагент.КонтрагентКПП) Тогда
					СтрокаКонтрагент.Параметры.КонтрагентНаименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Контрагент не найден (%1/%2)';
						|en = 'The counterparty is not found (%1/%2)'"),
					Контрагент.КонтрагентИНН,
					Контрагент.КонтрагентКПП);
					СтрокаКонтрагент.Области.КонтрагентНаименование.ЦветТекста = WebЦвета.Красный;
				Иначе
					СтрокаКонтрагент.Параметры.КонтрагентНаименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Контрагент не найден ИНН (%1)';
						|en = 'The counterparty is not found by TIN (%1)'"),
					Контрагент.КонтрагентИНН,
					Контрагент.КонтрагентКПП);
					СтрокаКонтрагент.Области.КонтрагентНаименование.ЦветТекста = WebЦвета.Красный;
				КонецЕсли;
			ИначеЕсли Контрагент.КонтрагентИНН = "" И Контрагент.КонтрагентКПП = "" Тогда
				СтрокаКонтрагент.Параметры.КонтрагентНаименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1';
					|en = '%1'"),
				Контрагент.КонтрагентНаименование);
				СтрокаКонтрагент.Области.КонтрагентНаименование.ЦветТекста = WebЦвета.Черный;
			ИначеЕсли Контрагент.КонтрагентКПП = "" Тогда
				СтрокаКонтрагент.Параметры.КонтрагентНаименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 (%2)';
					|en = '%1 (%2)'"),
				Контрагент.КонтрагентНаименование,
				Контрагент.КонтрагентИНН);
				СтрокаКонтрагент.Области.КонтрагентНаименование.ЦветТекста = WebЦвета.Черный;
			Иначе
				СтрокаКонтрагент.Параметры.КонтрагентНаименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 (%2/%3)';
					|en = '%1 (%2/%3)'"),
				Контрагент.КонтрагентНаименование,
				Контрагент.КонтрагентИНН,
				Контрагент.КонтрагентКПП);
				СтрокаКонтрагент.Области.КонтрагентНаименование.ЦветТекста = WebЦвета.Черный;
			КонецЕсли;
				
			Результат.Вывести(СтрокаКонтрагент, 1);
			ТаблицаПоДокументам = ТаблицаСверки.НайтиСтроки(Новый Структура("Контрагент", Контрагент.Контрагент));
			
			Результат.НачатьГруппуСтрок();
			Для Каждого Строка Из ТаблицаПоДокументам Цикл
				СтрокаРасхождения.Параметры.пп = Сч;
				Сч = Сч + 1;
				СтрокаРасхождения.Параметры.КодВидаОперации = Строка.КодВидаОперации;
				СтрокаРасхождения.Параметры.ДатаНомерСФОрганизация = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 от %2';
					|en = '%1 from %2'"),
				Строка.НомерСчетаФактуры,
				Формат(Строка.ДатаСчетаФактуры,  "ДЛФ=Д"));
				СтрокаРасхождения.Параметры.Документ               = Строка.СчетФактураДокумент;
			
				Если ИмяОтчета = "КнигаПокупок" Тогда
					СтрокаРасхождения.Параметры.СуммаДокумент    = Строка.Всего;
					СтрокаРасхождения.Параметры.НДСДокумент      = Строка.НДС;
					СтрокаРасхождения.Параметры.СуммаОрганизация = Строка.СуммаБезНДСКнига;
					СтрокаРасхождения.Параметры.НДСОрганизация   = Строка.НДСКнига;
				Иначе
					СтрокаРасхождения.Параметры.СуммаОрганизация = Строка.Всего;
					СтрокаРасхождения.Параметры.НДСОрганизация   = Строка.НДС;
				КонецЕсли;
				
				Результат.Вывести(СтрокаРасхождения, 2);
				
			КонецЦикла;
			
			Результат.ЗакончитьГруппуСтрок();
			
			Если ИмяОтчета = "КнигаПокупок" Тогда
				СтрокаИтогиПоКонтрагенту.Параметры.СуммаДокумент    = Контрагент.Всего;
				СтрокаИтогиПоКонтрагенту.Параметры.НДСДокумент      = Контрагент.НДС;
				СтрокаИтогиПоКонтрагенту.Параметры.СуммаОрганизация = Контрагент.СуммаБезНДСКнига;
				СтрокаИтогиПоКонтрагенту.Параметры.НДСОрганизация   = Контрагент.НДСКнига;
				
				СтрокаИтоги.Всего            = СтрокаИтоги.Всего + Контрагент.Всего;
				СтрокаИтоги.НДС              = СтрокаИтоги.НДС + Контрагент.НДС;
				СтрокаИтоги.СуммаБезНДСКнига = СтрокаИтоги.СуммаБезНДСКнига + Контрагент.СуммаБезНДСКнига;
				СтрокаИтоги.НДСКнига         = СтрокаИтоги.НДСКнига + Контрагент.НДСКнига;
			Иначе
				СтрокаИтогиПоКонтрагенту.Параметры.СуммаОрганизация = Контрагент.Всего;
				СтрокаИтогиПоКонтрагенту.Параметры.НДСОрганизация   = Контрагент.НДС;
				
				СтрокаИтоги.Всего            = СтрокаИтоги.Всего + Контрагент.Всего;
				СтрокаИтоги.НДС              = СтрокаИтоги.НДС + Контрагент.НДС;
				
			КонецЕсли;
			
			Результат.Вывести(СтрокаИтогиПоКонтрагенту, 2);
			
		КонецЦикла;
		
		Если ИмяОтчета = "КнигаПокупок" Тогда
			Итоги.Параметры.СуммаДокумент    = СтрокаИтоги.Всего;
			Итоги.Параметры.НДСДокумент      = СтрокаИтоги.НДС;
			Итоги.Параметры.СуммаОрганизация = СтрокаИтоги.СуммаБезНДСКнига;
			Итоги.Параметры.НДСОрганизация   = СтрокаИтоги.НДСКнига;
		Иначе
			Итоги.Параметры.СуммаОрганизация = СтрокаИтоги.Всего;
			Итоги.Параметры.НДСОрганизация   = СтрокаИтоги.НДС;
			
		КонецЕсли;
		
		Результат.Вывести(Итоги, 1);
		
	Если Форма.ДанныеДляСверкиДеклараций.ИмяОтчета = "Декларация" Тогда
		СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер.СделатьВыделениеТекущегоРазделаДляДекларации(Форма, Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел, НомерРаздела);
	Иначе
		СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер.СделатьВыделениеТекущегоРаздела(Форма, Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел);
	КонецЕсли;
	
	Форма.Результат = Результат;

КонецПроцедуры

#Область КнигиПокупокПродаж

// Добавление в параметры формирования отчета данных, необходимых для сверки счетов-фактур в отчете.
//
// Параметры:
//  Форма						 - ФормаКлиентскогоПриложения - Форма отчета, в котором выполняется сверка счетов-фактур.
//  ПараметрыОтчета				 - Структура - Параметры формирования отчета.
//  ЭтоПервоеФормированиеОтчета	 - Булево - Истина, если это формирование отчета перед проверкой контрагентов.
//		Ложь, если это формирование отчета при проверке контрагентов - при проверке контрагентов некоторые процедуры вызываются повторно в отчетах.
// 
Процедура ДобавитьПараметрыДляСверкиДеклараций(Форма, ПараметрыОтчета, ЭтоПервоеФормированиеОтчета = Истина) Экспорт
	
	ПараметрыОтчета.Вставить("ИспользуетсяСервисСверкиРасчетов", Форма.ИспользуетсяСервисСверкиРасчетов);
	
	Если Форма.ИспользуетсяСервисСверкиРасчетов Тогда
		Если ЭтоПервоеФормированиеОтчета Тогда
			Форма.ДанныеДляСверкиДеклараций.АдресДанныхОтчета   = ПоместитьВоВременноеХранилище(Неопределено, Форма.УникальныйИдентификатор);
			Форма.ДанныеДляСверкиДеклараций.АдресДанныхДопЛиста = ПоместитьВоВременноеХранилище(Неопределено, Форма.УникальныйИдентификатор);
		КонецЕсли;
		
		ПараметрыОтчета.Вставить("ДанныеДляСверкиДеклараций", Форма.ДанныеДляСверкиДеклараций);

	КонецЕсли;

КонецПроцедуры

// Запоминает данные отчета полученные при первоначальном формировании отчета
// и используемые в дальнейшем для сверки счетов-фактур с контрагентами.
//
// Параметры:
//  СтруктураПараметров - Структура - Параметры формирования отчета. 
//      Для сверки в в отчете важно наличие ключа "ДанныеДляСверкиДеклараций".
//      Данный ключ формируется в процедуре ДобавитьПараметрыДляСверкиДеклараций.
//  Результат - РезультатЗапроса - Данные отчета полученные при первоначальном формировании отчета
//      и используемые в дальнейшем для сверки счетов-фактур.
Процедура ЗапомнитьДанныеОтчета(СтруктураПараметров, Результат) Экспорт
	
	Если СтруктураПараметров.Свойство("ИспользуетсяСервисСверкиРасчетов") = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СтруктураПараметров.ЗаполнениеДекларации И СтруктураПараметров.ИспользуетсяСервисСверкиРасчетов Тогда
		Если Результат = Неопределено Тогда
			ДанныеОтчета = Неопределено;
		Иначе
			ДанныеОтчета = Результат.Выгрузить();
			Отбор = Новый Структура ("СчетФактура", NULL);
			ПустыеСтроки = ДанныеОтчета.НайтиСтроки(Отбор);
			Для Каждого Строка Из ПустыеСтроки Цикл
				ДанныеОтчета.Удалить(Строка);
			КонецЦикла;
		КонецЕсли;
		Если СтруктураПараметров.ДанныеДляСверкиДеклараций.ИмяОтчета = "ПомощникПоНДС" Тогда
			СтруктураПараметров.ДанныеДляСверкиДеклараций.Вставить("ДанныеОтчета", ДанныеОтчета);
		Иначе
			СтруктураПараметров.ДанныеДляСверкиДеклараций.АдресДанныхОтчета =
			ПоместитьВоВременноеХранилище(ДанныеОтчета, СтруктураПараметров.ДанныеДляСверкиДеклараций.АдресДанныхОтчета);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Запоминает данные доп листов отчета полученные при первоначальном формировании отчета
// и используемые в дальнейшем для сверки счетов-фактур с контрагентами.
//
// Параметры:
//  СтруктураПараметров - Структура - Параметры формирования отчета. 
//      Для сверки в в отчете важно наличие ключа "ДанныеДляСверкиДеклараций".
//      Данный ключ формируется в процедуре ДобавитьПараметрыДляСверкиДеклараций.
//  Результат - РезультатЗапроса - Данные доп листов отчета полученные при первоначальном формировании отчета
//      и используемые в дальнейшем для сверки счетов-фактур.
Процедура ЗапомнитьДанныеДопЛистов(СтруктураПараметров, Результат) Экспорт
	
	Если СтруктураПараметров.Свойство("ИспользуетсяСервисСверкиРасчетов") = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ СтруктураПараметров.ЗаполнениеДекларации И СтруктураПараметров.ИспользуетсяСервисСверкиРасчетов Тогда
		Если Результат = Неопределено Тогда
			ДанныеОтчета = Неопределено;
		Иначе
			ДанныеОтчета = Результат.Выгрузить();
			Отбор = Новый Структура ("СчетФактура", NULL);
			ПустыеСтроки = ДанныеОтчета.НайтиСтроки(Отбор);
			Для Каждого Строка Из ПустыеСтроки Цикл
				ДанныеОтчета.Удалить(Строка);
			КонецЦикла;
		КонецЕсли;
		
		Если СтруктураПараметров.ДанныеДляСверкиДеклараций.ИмяОтчета = "ПомощникПоНДС" Тогда
			СтруктураПараметров.ДанныеДляСверкиДеклараций.Вставить("ДанныеДопЛистов", ДанныеОтчета);
		Иначе
			СтруктураПараметров.ДанныеДляСверкиДеклараций.АдресДанныхДопЛиста =
				ПоместитьВоВременноеХранилище(ДанныеОтчета, СтруктураПараметров.ДанныеДляСверкиДеклараций.АдресДанныхДопЛиста);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Функция СверитьОтчетВФоне(СтруктураПараметров) Экспорт
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("НачальнаяДатаПолученияРасхождений", СтруктураПараметров.НачалоПериода);
	ПараметрыПроцедуры.Вставить("ЭтоВызовИзОтчета",                  Истина);

	СервисСверкиРасчетов.ОбменятьсяССервисом(ПараметрыПроцедуры);
	Возврат ВыполнитьЗапросСверки(СтруктураПараметров);
	
КонецФункции

Процедура ОтобразитьВсюКнигу(Форма) Экспорт
	
	ДопЛист = Форма.НомераРазделовДопЛистов.НайтиПоЗначению(Форма.ТекущийНомерРаздела) <> Неопределено;
	Если ДопЛист Тогда 
		Если Форма.ДанныеДляСверкиДеклараций.Свойство("ДопЛисты") Тогда
			Форма.Результат = ПолучитьИзВременногоХранилища(Форма.ДанныеДляСверкиДеклараций.ДопЛисты);
		КонецЕсли;
		
		Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел = "ДопЛисты";
	Иначе
		Если Форма.ПроверкаКонтрагентовПереключательРежимаОтображения = "Все" Или Форма.ПроверкаКонтрагентовПереключательРежимаОтображения = "" Тогда
			Если Форма.ДанныеДляСверкиДеклараций.Свойство("ОсновнойРаздел") Тогда
				Форма.Результат = ПолучитьИзВременногоХранилища(Форма.ДанныеДляСверкиДеклараций.ОсновнойРаздел);
			КонецЕсли;
		ИначеЕсли Форма.ПроверкаКонтрагентовПереключательРежимаОтображения = "Недействующие" Тогда
			Если Форма.ДанныеДляСверкиДеклараций.Свойство("НедействующиеКонтрагенты") Тогда
				Форма.Результат = ПолучитьИзВременногоХранилища(Форма.ДанныеДляСверкиДеклараций.НедействующиеКонтрагенты);
			КонецЕсли
		КонецЕсли;
		Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел = "СамОтчет";
	КонецЕсли;
	СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер.СделатьВыделениеТекущегоРаздела(Форма, Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел);
	
КонецПроцедуры

#КонецОбласти

#Область ДекларацияПоНДС

Функция СверитьДекларациюВФоне(СтруктураПараметров)Экспорт
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("НачальнаяДатаПолученияРасхождений", СтруктураПараметров.НачалоПериода);
	ПараметрыПроцедуры.Вставить("ЭтоВызовИзОтчета",                  Истина);

	СервисСверкиРасчетов.ОбменятьсяССервисом(ПараметрыПроцедуры);
	
	Разделы = ПроверяемыеРазделыДекларацииПоНДС();
	СтраницыРазделов = СтруктураПараметров.СтруктураСтраницРазделов;
	ТаблицыСверкиДекларацииПоНДС = ПолучитьСтруктуруТаблицСверкиДекларации();
	
	Для Каждого НомерРаздела Из Разделы Цикл
		СтраницыРаздела = СтраницыРазделов["СтраницыРаздел" + НомерРаздела.Значение];
		СтраницыТаблица = Новый ТаблицаЗначений();
		СтраницыТаблица = МассивВТаблицуЗначений(СтраницыРаздела, СтраницыТаблица);
		ТаблицаРаздела = Неопределено;
		
		Для Каждого СтраницаРаздела Из СтраницыТаблица Цикл
			
			ТаблицаСтраницы = СервисСверкиРасчетовПереопределяемый.СегментДанныхРазделаДекларацииНДС(
				СтруктураПараметров.ДекларацияПоНДС,
				"Раздел" + Строка(НомерРаздела.Значение),
				СтраницаРаздела.НомерПервойСтроки);
				Если ТаблицаРаздела = Неопределено Тогда 
					ТаблицаРаздела = ТаблицаСтраницы;
				Иначе
					ДополнитьТаблицу(ТаблицаСтраницы, ТаблицаРаздела);
				КонецЕсли;
		КонецЦикла;
		
		Если ТаблицаРаздела <> Неопределено Тогда
			ТаблицаРаздела = ПодготовитьТаблицуРазделаКСверке(ТаблицаРаздела, НомерРаздела.Значение, СтруктураПараметров.Организация);
			СтруктураПараметров.ДанныеСверки = ТаблицаРаздела;
			Если НомерРаздела.Значение = "8" Или НомерРаздела.Значение = "8_Прил1" Тогда
				СтруктураПараметров.КодВидаОперации = НеСверяемыеКодыОпераций("КнигаПокупок");
				Результат = СервисСверкиРасчетовПереопределяемый.ПолучитьДанныеСверкиДекларации(СтруктураПараметров, ,Истина);
			Иначе
				СтруктураПараметров.КодВидаОперации = НеСверяемыеКодыОпераций("КнигаПродаж");
				СтруктураПараметров.ОперацияНетВОрганизации = ОперацииПоКоторымВыводимСФОтсутствующиеВОрганизации();
				Результат = СервисСверкиРасчетовПереопределяемый.ПолучитьДанныеСверкиДекларации(СтруктураПараметров, Ложь, Истина, НомерРаздела.Значение = "9_Прил1");
			КонецЕсли;

			ТаблицыСверкиДекларацииПоНДС["ТаблицаНеСверилиРаздел"        + НомерРаздела.Значение] = Результат[3];
			ТаблицыСверкиДекларацииПоНДС["ТаблицаРасхожденияРаздел"      + НомерРаздела.Значение] = Результат[4];
			ТаблицыСверкиДекларацииПоНДС["ТаблицаСверилиБезОшибокРаздел" + НомерРаздела.Значение] = Результат[5];
			ТаблицыСверкиДекларацииПоНДС["ТаблицаНетУКонтрагентаРаздел"  + НомерРаздела.Значение] = Результат[6];
			
			Если НомерРаздела.Значение = "9" Тогда
				ТаблицыСверкиДекларацииПоНДС.ТаблицаНетВОрганизацииРаздел9 = Результат[7];
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураПараметров.ДанныеСверки = Неопределено;
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицыСверкиДекларацииПоНДС, СтруктураПараметров.ДанныеДляСверкиДеклараций.АдресРезультатаСверки);

КонецФункции

Процедура ПриСозданииНаСервереДекларации(Форма) Экспорт
	
	СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер.ИнициализацияРеквизитовСверкиДекларации(Форма, "Декларация");
	ПолучитьСтраницыРазделовДекларации(Форма);
	
КонецПроцедуры

Процедура ПолучитьСтраницыРазделовДекларации(Форма) Экспорт
	
	СтраницыРазделов = Новый Структура;
	Разделы = ПроверяемыеРазделыДекларацииПоНДС();
	
	Для Каждого НомерРаздела Из Разделы Цикл
	
		Ключ = "СтраницыРаздел" + НомерРаздела.Значение;
		СтраницыРазделов.Вставить(Ключ, ДанныеФормыВЗначение(Форма[Ключ], Тип("ТаблицаЗначений")));

	КонецЦикла;
	
	Форма.ДанныеДляСверкиДеклараций.Вставить("СтраницыРазделов",
		ПоместитьВоВременноеХранилище(СтраницыРазделов, Форма.УникальныйИдентификатор));
			
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НеСверяемыеКодыОпераций(ИмяОтчета) Экспорт
	
	Если ИмяОтчета = "КнигаПокупок" Тогда
		МассивКодовОпераций = Новый Массив();
		МассивКодовОпераций.Добавить("06");
		МассивКодовОпераций.Добавить("21");
		МассивКодовОпераций.Добавить("26");
		МассивКодовОпераций.Добавить("27");
		МассивКодовОпераций.Добавить("28");
		МассивКодовОпераций.Добавить("29");
		МассивКодовОпераций.Добавить("30");
		МассивКодовОпераций.Добавить("31");
		МассивКодовОпераций.Добавить("35");
	Иначе
		МассивКодовОпераций = Новый Массив();
		МассивКодовОпераций.Добавить("06");
		МассивКодовОпераций.Добавить("10");
		МассивКодовОпераций.Добавить("17");
		МассивКодовОпераций.Добавить("19");
		МассивКодовОпераций.Добавить("20");
		МассивКодовОпераций.Добавить("23");
		МассивКодовОпераций.Добавить("24");
		МассивКодовОпераций.Добавить("25");
		МассивКодовОпераций.Добавить("26");
		МассивКодовОпераций.Добавить("27");
		МассивКодовОпераций.Добавить("28");
		МассивКодовОпераций.Добавить("32");
		МассивКодовОпераций.Добавить("36");
		МассивКодовОпераций.Добавить("45");
		МассивКодовОпераций.Добавить("46");
	КонецЕсли;
	
	Возврат МассивКодовОпераций;
	
КонецФункции

Функция ОперацииПоКоторымВыводимСФОтсутствующиеВОрганизации() Экспорт
	
	Массив = Новый Массив();
	Массив.Добавить(Перечисления.СервисСверкиРасчетовВидыОпераций.Поступление);
	Массив.Добавить(Перечисления.СервисСверкиРасчетовВидыОпераций.КорректировкаПоступления);
	
	Возврат Массив;
	
КонецФункции

#Область ДекларацияПоНДС

Процедура НастроитьОтображениеРазделов(Форма) Экспорт
	
	СтраницыРазделов = ПолучитьИзВременногоХранилища(Форма.АдресСтраницРазделов);
	// Сначала определяем, какие вообще разделы заполнены в декларации - те и отображаем в результатах сверки
	Разделы = ПроверяемыеРазделыДекларацииПоНДС();
	ПервыйРаздел = "";
	Для Каждого НомерРаздела Из Разделы Цикл
		СтраницыРаздела = СтраницыРазделов["СтраницыРаздел" + НомерРаздела.Значение];
		Если СтраницыРаздела = Неопределено Или СтраницыРаздела.Количество() = 0 Тогда
			Форма.Элементы["ГруппаИтогиСверкиРаздел" +НомерРаздела.Значение].Видимость = Ложь;
		Иначе
			Форма.Элементы["ГруппаИтогиСверкиРаздел" +НомерРаздела.Значение].Видимость = Истина;
			Если ПервыйРаздел = "" Тогда
				ПервыйРаздел = НомерРаздела.Значение;
			КонецЕсли;
			СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер.ОтобразитьСверкуНаФорме(Форма, Ложь,
				"Раздел" + НомерРаздела.Значение, ПервыйРаздел = НомерРаздела.Значение);
		КонецЕсли;
	КонецЦикла;
	
	СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер.СделатьВыделениеТекущегоРазделаДляДекларации(Форма, Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел, "Раздел" + ПервыйРаздел);
	Если Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел = "ТаблицаРасхожденияРаздел" + ПервыйРаздел Тогда
		ОткрытьДокументыСРасхождениями(Форма, , "Раздел" + ПервыйРаздел);
	Иначе
		ОткрытьДокументыБезРасхождений(Форма, Форма.ДанныеДляСверкиДеклараций.ТекущийРаздел, , "Раздел" + ПервыйРаздел);
	КонецЕсли;

КонецПроцедуры

Функция ПроверяемыеРазделыДекларацииПоНДС() Экспорт
	
	Разделы = Новый Структура;
	Разделы.Вставить("Раздел8",   "8");
	Разделы.Вставить("Раздел8_1", "8_Прил1");
	Разделы.Вставить("Раздел9",   "9");
	Разделы.Вставить("Раздел9_1", "9_Прил1");
	
	Возврат Разделы;
	
КонецФункции

#КонецОбласти

#Область КнигиПокупокПродаж

Процедура ЗапомнитьРезультатФормированияОтчета(Форма, РезультатВыполнения) Экспорт
	
	Если Форма.ИспользуетсяСервисСверкиРасчетов Тогда
		Форма.ДанныеДляСверкиДеклараций = РезультатВыполнения.ДанныеДляСверкиДеклараций;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Преобразует Массив из структур в ТаблицуЗначений.
Функция МассивВТаблицуЗначений(Данные, Таблица)
	Для Каждого ЭлементМассива Из Данные Цикл
		// создаем колонки для таблицы
		Если Таблица.Колонки.Количество() = 0 Тогда
			Для Каждого ЗначениеСтруктуры Из ЭлементМассива Цикл
				Таблица.Колонки.Добавить(ЗначениеСтруктуры.Ключ);
			КонецЦикла;
		КонецЕсли;
		
		// Добавляем данные в таблицу
		НоваяСтрока = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементМассива);
	КонецЦикла;
	
	Возврат Таблица;
	
КонецФункции

Процедура ДополнитьТаблицу(ТаблицаИсточник, ТаблицаПриемник)
	
	Для Каждого СтрокаТаблицыИсточник Из ТаблицаИсточник Цикл
		
		ЗаполнитьЗначенияСвойств(ТаблицаПриемник.Добавить(), СтрокаТаблицыИсточник);
		
	КонецЦикла;
	
КонецПроцедуры

Функция СоздатьПустуюТаблицуДанных(ИмяОтчета)
	
	МассивТипов = Новый Массив();
	МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
	МассивТипов.Добавить(Тип("СправочникСсылка.Организации"));
	ОписаниеТиповКонтрагент = Новый ОписаниеТипов(МассивТипов);
	Если ИмяОтчета = "КнигаПродаж" Тогда
		ДанныеОтчета = Новый ТаблицаЗначений();
		ДанныеОтчета.Колонки.Добавить("СчетФактура",         Документы.ТипВсеСсылки());
		ДанныеОтчета.Колонки.Добавить("Организация",         Новый ОписаниеТипов("СправочникСсылка.Организации"));
		ДанныеОтчета.Колонки.Добавить("ДатаСчетаФактуры",    Новый ОписаниеТипов("Дата"));
		ДанныеОтчета.Колонки.Добавить("СчетФактураДокумент", Документы.ТипВсеСсылки());
		ДанныеОтчета.Колонки.Добавить("КодВидаОперации",     ОбщегоНазначения.ОписаниеТипаСтрока(10));
		ДанныеОтчета.Колонки.Добавить("Контрагент",          ОписаниеТиповКонтрагент);
		ДанныеОтчета.Колонки.Добавить("Покупатель",          ОбщегоНазначения.ОписаниеТипаСтрока(550));
		ДанныеОтчета.Колонки.Добавить("ПокупательИНН",       ОбщегоНазначения.ОписаниеТипаСтрока(50));
		ДанныеОтчета.Колонки.Добавить("ПокупательКПП",       ОбщегоНазначения.ОписаниеТипаСтрока(9));
		ДанныеОтчета.Колонки.Добавить("НДС20",               Новый ОписаниеТипов("Число"));
		ДанныеОтчета.Колонки.Добавить("НДС18",               Новый ОписаниеТипов("Число"));
		ДанныеОтчета.Колонки.Добавить("НДС10",               Новый ОписаниеТипов("Число"));
		ДанныеОтчета.Колонки.Добавить("НДС7",                Новый ОписаниеТипов("Число"));
		ДанныеОтчета.Колонки.Добавить("НДС5",                Новый ОписаниеТипов("Число"));
		ДанныеОтчета.Колонки.Добавить("НДС0",                Новый ОписаниеТипов("Число"));
		ДанныеОтчета.Колонки.Добавить("ВсегоПродаж",         Новый ОписаниеТипов("Число"));
		ДанныеОтчета.Колонки.Добавить("НомерСчетаФактуры",   ОбщегоНазначения.ОписаниеТипаСтрока(50));
		ДанныеОтчета.Колонки.Добавить("ДатаКорректировки",   Новый ОписаниеТипов("Дата"));
		ДанныеОтчета.Колонки.Добавить("НомерКорректировки",  ОбщегоНазначения.ОписаниеТипаСтрока(50));
	Иначе
		ДанныеОтчета = Новый ТаблицаЗначений();
		ДанныеОтчета.Колонки.Добавить("СчетФактура",         Документы.ТипВсеСсылки());
		ДанныеОтчета.Колонки.Добавить("Организация",         Новый ОписаниеТипов("СправочникСсылка.Организации"));
		ДанныеОтчета.Колонки.Добавить("ДатаСчетаФактуры",    Новый ОписаниеТипов("Дата"));
		ДанныеОтчета.Колонки.Добавить("СчетФактураДокумент", Документы.ТипВсеСсылки());
		ДанныеОтчета.Колонки.Добавить("КодВидаОперации",     ОбщегоНазначения.ОписаниеТипаСтрока(10));
		ДанныеОтчета.Колонки.Добавить("Контрагент",          ОписаниеТиповКонтрагент);
		ДанныеОтчета.Колонки.Добавить("Продавец",            ОбщегоНазначения.ОписаниеТипаСтрока(550));
		ДанныеОтчета.Колонки.Добавить("ПродавецИНН",         ОбщегоНазначения.ОписаниеТипаСтрока(50));
		ДанныеОтчета.Колонки.Добавить("ПродавецКПП",         ОбщегоНазначения.ОписаниеТипаСтрока(9));
		ДанныеОтчета.Колонки.Добавить("НДС",                 Новый ОписаниеТипов("Число"));
		ДанныеОтчета.Колонки.Добавить("ВсегоПокупок",        Новый ОписаниеТипов("Число"));
		ДанныеОтчета.Колонки.Добавить("НомерСчетаФактуры",   ОбщегоНазначения.ОписаниеТипаСтрока(50));
		ДанныеОтчета.Колонки.Добавить("ДатаКорректировки",   Новый ОписаниеТипов("Дата"));
		ДанныеОтчета.Колонки.Добавить("НомерКорректировки",  ОбщегоНазначения.ОписаниеТипаСтрока(50));
	КонецЕсли;
	
	Возврат ДанныеОтчета;
	
КонецФункции


#Область КнигиПокупокПродаж

Функция ВыполнитьЗапросСверки(СтруктураПараметров)
	
	СтруктураРезультата = Новый Структура ("ТаблицаНеСверили, ТаблицаРасхождения, ТаблицаСверилиБезОшибок, ТаблицаНетУКонтрагента,
		|ТаблицаНетВОрганизации, ТаблицаНеСверилиДопЛист, ТаблицаРасхожденияДопЛист, ТаблицаСверилиБезОшибокДопЛист, ТаблицаНетУКонтрагентаДопЛист");
	ИмяОтчета = СтруктураПараметров.ДанныеДляСверкиДеклараций.ИмяОтчета;
	Если Не СтруктураПараметров.ВыводитьТолькоДопЛисты Тогда
		ДанныеОтчета = СоздатьПустуюТаблицуДанных(СтруктураПараметров.ДанныеДляСверкиДеклараций.ИмяОтчета);
		ДанныеОтчета = МассивВТаблицуЗначений(СтруктураПараметров.ДанныеОтчета, ДанныеОтчета);
		
		Если Не ДанныеОтчета = Неопределено Тогда
			
			СтруктураПараметров.ДанныеСверки            = ДанныеОтчета;
			СтруктураПараметров.КодВидаОперации         = НеСверяемыеКодыОпераций(ИмяОтчета);
			СтруктураПараметров.ОперацияНетВОрганизации = ОперацииПоКоторымВыводимСФОтсутствующиеВОрганизации();
			Результат = СервисСверкиРасчетовПереопределяемый.ПолучитьДанныеСверкиДекларации(СтруктураПараметров, ИмяОтчета = "КнигаПокупок");
			
			СтруктураРезультата.ТаблицаНеСверили        = Результат[3];
			СтруктураРезультата.ТаблицаРасхождения      = Результат[4];
			СтруктураРезультата.ТаблицаСверилиБезОшибок = Результат[5];
			СтруктураРезультата.ТаблицаНетУКонтрагента  = Результат[6];
			СтруктураПараметров.ДанныеСверки            = Неопределено;
			Если ИмяОтчета = "КнигаПродаж" Тогда
				СтруктураРезультата.ТаблицаНетВОрганизации = Результат[7];
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если СтруктураПараметров.ФормироватьДополнительныеЛисты Тогда
		ДанныеДопЛистов = СоздатьПустуюТаблицуДанных(СтруктураПараметров.ДанныеДляСверкиДеклараций.ИмяОтчета);
		ДанныеДопЛистов = МассивВТаблицуЗначений(СтруктураПараметров.ДанныеДопЛистов, ДанныеДопЛистов);
		
		Если Не ДанныеДопЛистов = Неопределено Тогда
			
			СтруктураПараметров.ДанныеСверки            = ДанныеДопЛистов;
			СтруктураПараметров.КодВидаОперации         = НеСверяемыеКодыОпераций(ИмяОтчета);
			СтруктураПараметров.ОперацияНетВОрганизации = ОперацииПоКоторымВыводимСФОтсутствующиеВОрганизации();
			Результат = СервисСверкиРасчетовПереопределяемый.ПолучитьДанныеСверкиДекларации(СтруктураПараметров, ИмяОтчета = "КнигаПокупок", Ложь, Истина);
			СтруктураРезультата.ТаблицаНеСверилиДопЛист        = Результат[3];
			СтруктураРезультата.ТаблицаРасхожденияДопЛист      = Результат[4];
			СтруктураРезультата.ТаблицаСверилиБезОшибокДопЛист = Результат[5];
			СтруктураРезультата.ТаблицаНетУКонтрагентаДопЛист  = Результат[6];
			СтруктураПараметров.ДанныеСверки                   = Неопределено;
		КонецЕсли;
	Конецесли;
	
	Возврат ПоместитьВоВременноеХранилище(СтруктураРезультата, СтруктураПараметров.ДанныеДляСверкиДеклараций.АдресРезультатаСверки);
	
КонецФункции

#КонецОбласти

#Область ДекларацияПоНДС

Функция ПолучитьСтруктуруТаблицСверкиДекларации()
	
	ТаблицыСверки = Новый Структура;
	// Книга покупок
	ТаблицыСверки.Вставить("ТаблицаРасхожденияРаздел8");
	ТаблицыСверки.Вставить("ТаблицаНетУКонтрагентаРаздел8");
	ТаблицыСверки.Вставить("ТаблицаНеСверилиРаздел8");
	ТаблицыСверки.Вставить("ТаблицаСверилиБезОшибокРаздел8");
	// Книга покупок, доп листы
	ТаблицыСверки.Вставить("ТаблицаРасхожденияРаздел8_Прил1");
	ТаблицыСверки.Вставить("ТаблицаНетУКонтрагентаРаздел8_Прил1");
	ТаблицыСверки.Вставить("ТаблицаНеСверилиРаздел8_Прил1");
	ТаблицыСверки.Вставить("ТаблицаСверилиБезОшибокРаздел8_Прил1");
	// Книга продаж
	ТаблицыСверки.Вставить("ТаблицаРасхожденияРаздел9");
	ТаблицыСверки.Вставить("ТаблицаНетУКонтрагентаРаздел9");
	ТаблицыСверки.Вставить("ТаблицаНеСверилиРаздел9");
	ТаблицыСверки.Вставить("ТаблицаСверилиБезОшибокРаздел9");
	ТаблицыСверки.Вставить("ТаблицаНетВОрганизацииРаздел9");
	// Книга продаж, доп листы
	ТаблицыСверки.Вставить("ТаблицаРасхожденияРаздел9_Прил1");
	ТаблицыСверки.Вставить("ТаблицаНетУКонтрагентаРаздел9_Прил1");
	ТаблицыСверки.Вставить("ТаблицаНеСверилиРаздел9_Прил1");
	ТаблицыСверки.Вставить("ТаблицаСверилиБезОшибокРаздел9_Прил1");

	Возврат ТаблицыСверки;
	
КонецФункции

Функция ПодготовитьТаблицуРазделаКСверке(ТаблицаРаздела, НомерРаздела, Организация)
	Если НомерРаздела = "8" Или НомерРаздела = "8_Прил1" Тогда
		ИмяОтчета = "КнигаПокупок";
	Иначе
		ИмяОтчета = "КнигаПродаж";
	КонецЕсли;
	ТаблицаСверки = СоздатьПустуюТаблицуДанных(ИмяОтчета);
	
	Если ИмяОтчета = "КнигаПродаж" Тогда
		Для Каждого Строка Из ТаблицаРаздела Цикл
			НоваяСтрока = ТаблицаСверки.Добавить();
			НоваяСтрока.Организация         = Организация;
			НоваяСтрока.СчетФактура         = Строка.ДополнительныеСведения.Документ;
			НоваяСтрока.СчетФактураДокумент = Строка.ДополнительныеСведения.Документ;
			НоваяСтрока.ДатаСчетаФактуры     = ОбщегоНазначенияКлиентСервер.СтрокаВДату(Строка.ДатаСчФПрод);
			НоваяСтрока.НомерСчетаФактуры   = Строка.НомСчФПрод;
			НоваяСтрока.Контрагент          = Строка.ДополнительныеСведения.Контрагент;
			НоваяСтрока.КодВидаОперации     = ?(Строка.КодВидОпер.Количество() > 0, Строка.КодВидОпер[0], "");
			НоваяСтрока.НДС20               = Строка.СумНДССФ20;
			НоваяСтрока.НДС18               = Строка.СумНДССФ18;
			НоваяСтрока.НДС10               = Строка.СумНДССФ10;
			НоваяСтрока.НДС7                = Строка.СумНДССФ7;
			НоваяСтрока.НДС5                = Строка.СумНДССФ5;
			НоваяСтрока.ВсегоПродаж         = Строка.СтоимПродСф;
			НоваяСтрока.ДатаКорректировки   = ОбщегоНазначенияКлиентСервер.СтрокаВДату(Строка.ДатаКСчФПрод);
			НоваяСтрока.НомерКорректировки  = Строка.НомКСчФПрод;
		КонецЦикла;
	Иначе
		Для Каждого Строка Из ТаблицаРаздела Цикл
			НоваяСтрока = ТаблицаСверки.Добавить();
			НоваяСтрока.Организация         = Организация;
			НоваяСтрока.СчетФактура         = Строка.ДополнительныеСведения.Документ;
			НоваяСтрока.СчетФактураДокумент = Строка.ДополнительныеСведения.Документ;
			НоваяСтрока.ДатаСчетаФактуры    = ОбщегоНазначенияКлиентСервер.СтрокаВДату(Строка.ДатаСчФПрод);
			НоваяСтрока.НомерСчетаФактуры   = Строка.НомСчФПрод;
			НоваяСтрока.Контрагент          = Строка.ДополнительныеСведения.Контрагент;
			НоваяСтрока.КодВидаОперации     = ?(Строка.КодВидОпер.Количество() > 0, Строка.КодВидОпер[0], "");
			НоваяСтрока.НДС                 = Строка.СумНДСВыч;
			НоваяСтрока.ВсегоПокупок        = Строка.СтоимПокупВ;
			НоваяСтрока.ДатаКорректировки   = ОбщегоНазначенияКлиентСервер.СтрокаВДату(Строка.ДатаКСчФПрод);
			НоваяСтрока.НомерКорректировки  = Строка.НомКСчФПрод;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТаблицаСверки;
	
КонецФункции

#КонецОбласти

#КонецОбласти
