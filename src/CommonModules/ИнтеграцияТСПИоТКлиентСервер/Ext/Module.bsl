#Область ПрограммныйИнтерфейс

// Конструктор настройки экземляра ТСПИоТ.
// 
// Возвращаемое значение:
//  Структура:
// * НомерФискальногоНакопителя    - Строка - номер фискального накопителя, к которому подключен экземпляр ТС ПИоТ
// * ИдентификаторТСПИоТ           - Строка - идентификатор экземпляра ТС ПИоТ
// * ЗаводскойНомерККТ             - Строка - заводской номер ККТ
// * ИНН                           - Строка - ИНН владельца кассы
// * ТаймаутПроверкиКодаМаркировки - Число - Таймаут проверки кода маркировки в ТС ПИоТ в милисекундах.
Функция КонструкторНастройкиЭкземляраТСПИоТ() Экспорт
	
	СтруктураВозврата = Новый Структура();
	
	СтруктураВозврата.Вставить("НомерФискальногоНакопителя",    "");
	СтруктураВозврата.Вставить("ИдентификаторТСПИоТ",           "");
	СтруктураВозврата.Вставить("ЗаводскойНомерККТ",             "");
	СтруктураВозврата.Вставить("ИНН",                           "");
	СтруктураВозврата.Вставить("ТаймаутПроверкиКодаМаркировки", 0);
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ЗапросыВТСПИоТ

// Запрос данных о подключенной ККТ к экземпляру ТС ПИоТ по адресу подключения.
// 
// Параметры:
//  ПараметрыПодключения - см. ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияТСПИоТ
// 
// Возвращаемое значение:
//  Структура - Настройки экземпляра ТС ПИоТ:
// * РезультатОтправкиЗапроса - (См. ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
// * ТекстОшибки - Строка - текст ошибки, полученной от ТС ПИоТ
// * НастройкиЭкземпляраТСПИоТ - см. ИнтеграцияТСПиоТКлиентСервер.КонструкторНастройкиЭкземляраТСПИоТ
Функция ПолучитьНастройкиЭкземпляраТСПИоТ(ПараметрыПодключения) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",  Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                "");
	ВозвращаемоеЗначение.Вставить("НастройкиЭкземпляраТСПИоТ", Неопределено);
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("Content-Type",   "application/json");
	
	URLЗапроса = "api/v1/info";
	
	ПараметрыОтправкиHTTPЗапросов = Новый Структура;
	ПараметрыОтправкиHTTPЗапросов.Вставить("ПредставлениеСервиса",                  НСтр("ru = 'ТС ПИоТ';
																						|en = 'ТС ПИоТ'"));
	ПараметрыОтправкиHTTPЗапросов.Вставить("Сервер",                                ПараметрыПодключения.Сервер);
	ПараметрыОтправкиHTTPЗапросов.Вставить("Порт",                                  ПараметрыПодключения.Порт);
	ПараметрыОтправкиHTTPЗапросов.Вставить("Таймаут",                               60);
	ПараметрыОтправкиHTTPЗапросов.Вставить("ИспользоватьЗащищенноеСоединение",      ПараметрыПодключения.ЗащищенноеСоединение);
	ПараметрыОтправкиHTTPЗапросов.Вставить("ИсключитьПроверкуКорневыхСертификатов", Истина);
	
	РезультатЗапроса = ОбменДаннымиИСМПКлиентСервер.ОтправитьДанныеВСервис(
		URLЗапроса,
		,,
		"POST",
		ПараметрыОтправкиHTTPЗапросов,
		ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса, Истина);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если Не РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
				
		Иначе
			
			ДанныеОбработки = ОбменДаннымиИСМПКлиентСервер.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			
			НастройкиЭкземпляраТСПИоТ = КонструкторНастройкиЭкземляраТСПИоТ();
			
			НастройкиЭкземпляраТСПИоТ.НомерФискальногоНакопителя    = ДанныеОбработки.Получить("fnSerial");
			НастройкиЭкземпляраТСПИоТ.ИдентификаторТСПИоТ           = ДанныеОбработки.Получить("tspiotId");
			НастройкиЭкземпляраТСПИоТ.ЗаводскойНомерККТ             = ДанныеОбработки.Получить("kktSerial");
			НастройкиЭкземпляраТСПИоТ.ИНН                           = ДанныеОбработки.Получить("kktInn");
			НастройкиЭкземпляраТСПИоТ.ТаймаутПроверкиКодаМаркировки = ДанныеОбработки.Получить("codesCheckTimeout");
			
			ВозвращаемоеЗначение.НастройкиЭкземпляраТСПИоТ = НастройкиЭкземпляраТСПИоТ;
			
		КонецЕсли;
		
	Иначе
		
		// не получен ответ при инициализации, таймаут или иные проблемы
		ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	//@skip-check constructor-function-return-section
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Запрос данных на проверку марки разрешительным режимом к экземпляру ТС ПИоТ по адресу подключения.
// 
// Параметры:
//  ПараметрыПодключения           - см. ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияТСПИоТ
//  ДанныеПроверки                 - Массив Из СтрокаТабличнойЧасти: см. ШтрихкодированиеОбщегоНазначенияИС.ИнициализацияТаблицыДанныхКодовМаркировки - Коллекция строк таблицы
//  ПараметрыСканирования          - см. ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования
//  ДополнительныеПараметрыЗапроса - см. ИнтеграцияТСПИоТ.ДополнительныеДанныеЗапросаТСПИоТ
// 
// Возвращаемое значение:
//  Структура - Получить информацию по КМ от ККТ:
// * РезультатОтправкиЗапроса - (См. ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
// * ТекстОшибки - Строка - текст ошибки, полученной от ТС ПИоТ
// * АварийныйРежим - Булево - Истина, если действует аварийный режим или в ходе проверки он был объявлен
// * СтатусыКодовМаркировкиГИСМТ - Соответствие Из КлючИЗначение - Статусы кодов маркировки:
//		** Ключ - СтрокаТабличнойЧасти: см. ШтрихкодированиеОбщегоНазначенияИС.ИнициализацияТаблицыДанныхКодовМаркировки - Код маркировки.
//		** Значение - см. ПараметрыКодаМаркировкиПоДаннымЛокальногоМодуля
Функция ДанныеПоКодуМаркировкиПриРозничнойПродаже(ПараметрыПодключения, ДанныеПроверки, ПараметрыСканирования, ДополнительныеПараметрыЗапроса) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",    Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                 "");
	ВозвращаемоеЗначение.Вставить("СтатусыКодовМаркировкиГИСМТ", Новый Соответствие());
	ВозвращаемоеЗначение.Вставить("АварийныйРежим",              Ложь);
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("Content-Type",   "application/json");
	
	URLЗапроса = "api/v1/codes/check";
	
	ПараметрыОтправкиHTTPЗапросов = Новый Структура;
	ПараметрыОтправкиHTTPЗапросов.Вставить("ПредставлениеСервиса",                  НСтр("ru = 'ТС ПИоТ';
																						|en = 'ТС ПИоТ'"));
	ПараметрыОтправкиHTTPЗапросов.Вставить("Сервер",                                ПараметрыПодключения.Сервер);
	ПараметрыОтправкиHTTPЗапросов.Вставить("Порт",                                  ПараметрыПодключения.Порт);
	ПараметрыОтправкиHTTPЗапросов.Вставить("Таймаут",                               60);
	ПараметрыОтправкиHTTPЗапросов.Вставить("ИспользоватьЗащищенноеСоединение",      ПараметрыПодключения.ЗащищенноеСоединение);
	ПараметрыОтправкиHTTPЗапросов.Вставить("ИсключитьПроверкуКорневыхСертификатов", Истина);
	
	МассивКодовИдентификацииДляОтправки = Новый Массив;
	СоответствиеКИЗСтрокам              = Новый Соответствие;
	
	Для Каждого СтрокаДанныхПроверки Из ДанныеПроверки Цикл
		
		МассивКодовИдентификацииДляОтправки.Добавить(СтрокаДанныхПроверки.ПолныйКодМаркировки);
		СоответствиеКИЗСтрокам.Вставить(ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.Base64ВШтрихкод(СтрокаДанныхПроверки.ПолныйКодМаркировки), СтрокаДанныхПроверки);
		СоответствиеКИЗСтрокам.Вставить(СтрокаДанныхПроверки.КодИдентификацииДляОтправкиВЛокальныйМодуль, СтрокаДанныхПроверки);
		
	КонецЦикла;
	
	ДанныеОКлиенте = Новый Структура();
	ДанныеОКлиенте.Вставить("name",    ДополнительныеПараметрыЗапроса.ДанныеКлиента.НаименованиеКассовогоПО);
	ДанныеОКлиенте.Вставить("version", ДополнительныеПараметрыЗапроса.ДанныеКлиента.ВерсияКассовогоПО);
	ДанныеОКлиенте.Вставить("id",      ДополнительныеПараметрыЗапроса.ДанныеКлиента.ИдентификаторКассовогоПО);
	ДанныеОКлиенте.Вставить("token",   ДополнительныеПараметрыЗапроса.ДанныеКлиента.КонтрольнаяСумма);
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("codes",       МассивКодовИдентификацииДляОтправки);
	ПараметрыЗапроса.Вставить("client_info", ДанныеОКлиенте);
	
	ТелоЗапросаJSON = ОбменДаннымиИСМПКлиентСервер.ОбъектВТекстJSON(ПараметрыЗапроса, Истина);
	
	РезультатЗапроса = ОбменДаннымиИСМПКлиентСервер.ОтправитьДанныеВСервис(
		URLЗапроса,
		ТелоЗапросаJSON,,
		"POST",
		ПараметрыОтправкиHTTPЗапросов,
		ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса, Истина);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = ОбщегоНазначенияИСМПКлиентСерверПовтИсп.АварийныйРежимКодОтветаСервисаГИСМТ() Тогда
			
			// если аварийный режим, остальные коды можно не проверять
			ВозвращаемоеЗначение.АварийныйРежим = Истина;
		
		ИначеЕсли Не РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
				
		Иначе
			
			ДанныеОбработки = ОбменДаннымиИСМПКлиентСервер.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			ДанныеОКодах    = ДанныеОбработки.Получить("codesResponse");
			
			Если ДанныеОКодах <> Неопределено И ТипЗнч(ДанныеОКодах) = Тип("Соответствие") Тогда
				
				ДанныеОКодах = ДанныеОКодах.Получить("codesResponse");
				Если ДанныеОКодах <> Неопределено И ТипЗнч(ДанныеОКодах) = Тип("Массив") Тогда
					
					Для Каждого СтрокаДанныхОКодах Из ДанныеОКодах Цикл
					
						КодОшибки                                    = СтрокаДанныхОКодах.Получить("code");
						СообщениеОбОшибке                            = СтрокаДанныхОКодах.Получить("description");
						РезультатПроверки                            = СтрокаДанныхОКодах.Получить("codes");
						
						РазрешительныйРежимДатаЗапросаГИСМТ          = СтрокаДанныхОКодах.Получить("reqTimestamp");
						РазрешительныйРежимИдентификаторЗапросаГИСМТ = СтрокаДанныхОКодах.Получить("reqId");
						РазрешительныйРежимИдентификаторЭкземпляраПО = СтрокаДанныхОКодах.Получить("inst");
						РазрешительныйРежимВерсияБазы                = СтрокаДанныхОКодах.Получить("version");
						
						ПродажаПоДаннымОффлайнРежима                 = СтрокаДанныхОКодах.Получить("isCheckedOffline");
						
						Если ЗначениеЗаполнено(КодОшибки) Тогда
							
							ТекстОшибки = ТекстОшибкиРазрешительногоРежимаПоКодуОшибкиТСПИоТ(КодОшибки, СообщениеОбОшибке);
							
							Если ЗначениеЗаполнено(ТекстОшибки) Тогда
								
								Для Каждого ДанныеШтрихкода Из ДанныеПроверки Цикл
									
									ДанныеШтрихкода.ТекстОшибки               = ТекстОшибки;
									ДанныеШтрихкода.КонтролиРазрешительногоРежима.Добавить(ТекстОшибки);
									
								КонецЦикла;
								
							КонецЕсли;
							
						Иначе
							
							Если РезультатПроверки = Неопределено Тогда
								РезультатПроверки = СтрокаДанныхОКодах.Получить("detials");
							КонецЕсли;
							
							Для Каждого СтрокаРезультатаПроверкиПоКодам Из РезультатПроверки Цикл
								
								КодОшибки                                    = СтрокаРезультатаПроверкиПоКодам.Получить("code");
								СообщениеОбОшибке                            = СтрокаРезультатаПроверкиПоКодам.Получить("description");
								
								Если ЗначениеЗаполнено(КодОшибки) Тогда
									
									ТекстОшибки = ТекстОшибкиРазрешительногоРежимаПоКодуОшибкиТСПИоТ(КодОшибки, СообщениеОбОшибке);
									
									Если ЗначениеЗаполнено(ТекстОшибки) Тогда
										
										Для Каждого ДанныеШтрихкода Из ДанныеПроверки Цикл
											
											ДанныеШтрихкода.ТекстОшибки               = ТекстОшибки;
											ДанныеШтрихкода.КонтролиРазрешительногоРежима.Добавить(ТекстОшибки);
											
										КонецЦикла;
										
										Продолжить;
										
									КонецЕсли;
									
								КонецЕсли;
								
								КИЗСтроки       = СтрокаРезультатаПроверкиПоКодам.Получить("cis");
								ДанныеШтрихкода = Неопределено;
								
								Если Не КИЗСтроки = Неопределено Тогда
									ДанныеШтрихкода = СоответствиеКИЗСтрокам.Получить(КИЗСтроки);
								КонецЕсли;
								
								Если ДанныеШтрихкода = Неопределено Тогда
									Продолжить;
								КонецЕсли;
								
								КодОшибки = СтрокаРезультатаПроверкиПоКодам.Получить("errorCode");
								
								Если ЗначениеЗаполнено(КодОшибки) Тогда
									
									ТекстОшибки = ИнтерфейсИСМПОбщегоНазначенияКлиентСервер.ТекстОшибкиРазрешительногоРежимаПоКодуОшибкиГИСМТ(КодОшибки, СообщениеОбОшибке);
									
									Если ЗначениеЗаполнено(ТекстОшибки) Тогда
										
										ДанныеШтрихкода.ТекстОшибки               = ТекстОшибки;
										ДанныеШтрихкода.КонтролиРазрешительногоРежима.Добавить(ТекстОшибки);
										
									КонецЕсли;
									
									Продолжить;
									
								КонецЕсли;
								
								ПараметрыКодаМаркировки = ИнтерфейсИСМПОбщегоНазначенияКлиентСервер.ПараметрыКодаМаркировкиПоДаннымРазрешительногоРежима(СтрокаРезультатаПроверкиПоКодам, ДанныеШтрихкода.ВидПродукции, ДополнительныеПараметрыЗапроса);
								
								ПараметрыКодаМаркировки.РазрешительныйРежимИдентификаторЗапросаГИСМТ = РазрешительныйРежимИдентификаторЗапросаГИСМТ;
								ПараметрыКодаМаркировки.РазрешительныйРежимДатаЗапросаГИСМТ          = Формат(РазрешительныйРежимДатаЗапросаГИСМТ, "ЧГ=;");
								ПараметрыКодаМаркировки.РазрешительныйРежимИдентификаторЭкземпляраПО = РазрешительныйРежимИдентификаторЭкземпляраПО;
								ПараметрыКодаМаркировки.РазрешительныйРежимВерсияБазы                = РазрешительныйРежимВерсияБазы;
								
								ПараметрыКодаМаркировки.РазрешительныйРежимАдресСервера              = ПараметрыПодключения.АдресПодключения;
								ПараметрыКодаМаркировки.РазрешительныйРежимТелоЗапросаJSON           = URLЗапроса;
								ПараметрыКодаМаркировки.РазрешительныйРежимТелоОтветаJSON            = РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON;
								ПараметрыКодаМаркировки.РазрешительныйРежимКодОтвета                 = Формат(РезультатЗапроса.HTTPОтвет.КодСостояния, "ЧГ=0;");
								
								ПараметрыКодаМаркировки.ПродажаПоДаннымОффлайнРежима                 = ПродажаПоДаннымОффлайнРежима;
								
								Если Не ЗначениеЗаполнено(ДанныеШтрихкода.ВидПродукции)
									И ЗначениеЗаполнено(ПараметрыКодаМаркировки.ВидПродукции) Тогда
									ДанныеШтрихкода.ВидПродукции = ПараметрыКодаМаркировки.ВидПродукции;
								КонецЕсли;
								
								ВозвращаемоеЗначение.СтатусыКодовМаркировкиГИСМТ.Вставить(ДанныеШтрихкода, ПараметрыКодаМаркировки);
								
							КонецЦикла;
							
						КонецЕсли;
						
					КонецЦикла;
					
				Иначе
					
					// неклассифицированный формат ответа ТС ПИоТ
					ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
						URLЗапроса,
						РезультатОтправкиЗапроса);
					
				КонецЕсли;
				
			Иначе
				
				// неклассифицированный формат ответа ТС ПИоТ
				ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		// не получен ответ при инициализации, таймаут или иные проблемы
		ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
	//@skip-check constructor-function-return-section
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

#Область ТекстыОшибокТСПИоТ

Функция ТекстОшибкиРазрешительногоРежимаПоКодуОшибкиТСПиоТ(КодОшибки, ОписаниеОшибкиТСПИоТ = Неопределено) Экспорт
	
	ТекстОшибки = "";
	
	Если КодОшибки = 13 Тогда
		ТекстОшибки = НСтр("ru = 'Отсутствует связь с ЛМ ЧЗ. Проверьте состояние ТС ПИоТ.';
							|en = 'Отсутствует связь с ЛМ ЧЗ. Проверьте состояние ТС ПИоТ.'");
		// todo дополнить после получения кодов ошибок от ТС ПИоТ
	Иначе
		
		ШаблонПодсказки   = НСтр("ru = 'Неклассифицированная ошибка отправки запроса по КМ в ТС ПИоТ.
			|Описание из сервиса: %1.';
			|en = 'Неклассифицированная ошибка отправки запроса по КМ в ТС ПИоТ.
			|Описание из сервиса: %1.'");
			
		Если ОписаниеОшибкиТСПИоТ <> Неопределено Тогда
			ТекстИзГИСМТ = СтрЗаменить(ОписаниеОшибкиТСПИоТ, Символ(29), "");
		Иначе
			ТекстИзГИСМТ = "отсутствует";
		КонецЕсли;
		
		ТекстОшибки = СтрШаблон(ШаблонПодсказки, ТекстИзГИСМТ);
		
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции

#КонецОбласти

#Область КонвертацияДанных

// Преобразует исходную строку в дату. 
// Если дату не удалось распознать, то возвращается пустая дата (01.01.01 00:00:00).
//
// Параметры:
//  Значение - Строка - строка, которую необходимо привести к дате.
// 
// Возвращаемое значение:
//  Дата
//
Функция ДатаИзСтроки(Значение) Экспорт
	
	Если СтрНайти(Значение, "T") > 0 Тогда
		#Если ВебКлиент Тогда
			Возврат ДатаИзСтрокиJSONФорматаISO(Значение);
		#Иначе
			Возврат ПрочитатьДатуJSON(Значение, ФорматДатыJSON.ISO);
		#КонецЕсли
	Иначе
		Возврат СтроковыеФункцииКлиентСервер.СтрокаВДату(Значение, ЧастиДаты.ДатаВремя);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДатаИзСтрокиJSONФорматаISO(Значение)
	
	МассивПодчастейСтроки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Значение, ".", Истина, Истина);
	
	Если МассивПодчастейСтроки.Количество() = 2 Тогда
		
		ЧастьДата = МассивПодчастейСтроки[0];
		
		Если СтрНайти(ЧастьДата, "T") > 0 Тогда
			ЧастьДата = СтрЗаменить(ЧастьДата, "T", " ");
		КонецЕсли;
		
		Возврат СтроковыеФункцииКлиентСервер.СтрокаВДату(ЧастьДата, ЧастиДаты.ДатаВремя);
		
	Иначе
		Возврат Дата(1, 1, 1);
	КонецЕсли;
	
КонецФункции

#КонецОбласти
