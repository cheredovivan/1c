// @strict-types


#Область СлужебныйПрограммныйИнтерфейс

// Отправляет приглашения через сервис облачного ЭДО.
// 
// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения
//  Приглашения - Массив из см. ПриглашенияЭДОКлиентСервер.НовоеИсходящееПриглашение
Процедура ОтправитьПриглашения(ОповещениеОЗавершении, Приглашения) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ОтправитьПриглашенияПослеАвторизацииВСервисеЭДО", ЭтотОбъект,
		ПараметрыДействияСПриглашениями(ОповещениеОЗавершении, Приглашения));
	
	АвторизоватьсяВСервисеЭДОПоПриглашениям(Оповещение, Приглашения);
	
КонецПроцедуры

// Принимает приглашения через сервис облачного ЭДО.
// 
// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения
//  Приглашения - Массив из см. ПриглашенияЭДОКлиент.НовоеВходящееПриглашение
Процедура ПринятьПриглашения(ОповещениеОЗавершении, Приглашения) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПринятьПриглашенияПослеАвторизацииВСервисеЭДО", ЭтотОбъект,
		ПараметрыДействияСПриглашениями(ОповещениеОЗавершении, Приглашения));
	
	АвторизоватьсяВСервисеЭДОПоПриглашениям(Оповещение, Приглашения);
	
КонецПроцедуры

// Отклоняет приглашения через сервис облачного ЭДО.
// 
// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения
//  Приглашения - Массив из см. ПриглашенияЭДОКлиент.НовоеВходящееПриглашение
Процедура ОтклонитьПриглашения(ОповещениеОЗавершении, Приглашения) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ОтклонитьПриглашенияПослеАвторизацииВСервисеЭДО", ЭтотОбъект,
		ПараметрыДействияСПриглашениями(ОповещениеОЗавершении, Приглашения));
	
	АвторизоватьсяВСервисеЭДОПоПриглашениям(Оповещение, Приглашения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращаемое значение:
//  Структура:
//  * ОповещениеОЗавершении - ОписаниеОповещения
//  * Приглашения - Массив из см. ПриглашенияЭДОКлиентСервер.НовоеИсходящееПриглашение
//                - Массив из см. ПриглашенияЭДОКлиентСервер.НовоеВходящееПриглашение
Функция НовыеПараметрыДействияСПриглашениями()
	Параметры = Новый Структура;
	Параметры.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения);
	Параметры.Вставить("Приглашения", Новый Массив);
	Возврат Параметры;
КонецФункции

// Возвращаемое значение:
//  См. НовыеПараметрыДействияСПриглашениями
Функция ПараметрыДействияСПриглашениями(Оповещение, Приглашения)
	Параметры = НовыеПараметрыДействияСПриглашениями();
	Параметры.ОповещениеОЗавершении = Оповещение;
	Параметры.Приглашения = Приглашения;
	Возврат Параметры;
КонецФункции

// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения
//  Приглашения - Массив из см. ПриглашенияЭДОКлиентСервер.НовоеИсходящееПриглашение
//                - Массив из см. ПриглашенияЭДОКлиентСервер.НовоеВходящееПриглашение
Процедура АвторизоватьсяВСервисеЭДОПоПриглашениям(ОповещениеОЗавершении, Приглашения)
	
	ИдентификаторыУчетныхЗаписейЭДО = ОбщегоНазначенияБЭДКлиент.ВыгрузитьКолонку(Приглашения,
		 "ИдентификаторОрганизации", Истина); // см. АвторизоватьсяВСервисеЭДО.ИдентификаторыУчетныхЗаписейЭДО
	
	ПараметрыАвторизации = УчетныеЗаписиЭДОКлиент.НовыеПараметрыАвторизацииВСервисеЭДО();
	ПараметрыАвторизации.ИдентификаторыЭДО = ИдентификаторыУчетныхЗаписейЭДО;
	
	УчетныеЗаписиЭДОИнтеграцияОблакаКлиент.АвторизоватьсяВСервисеЭДО(ОповещениеОЗавершении, ПараметрыАвторизации);
	
КонецПроцедуры

// Параметры:
//  РезультатАвторизации - см. УчетныеЗаписиЭДОКлиент.НовыйРезультатАвторизацииВСервисеЭДО
//  ПараметрыДействия - см. НовыеПараметрыДействияСПриглашениями
Процедура ОтправитьПриглашенияПослеАвторизацииВСервисеЭДО(РезультатАвторизации, ПараметрыДействия) Экспорт
	
	ДлительнаяОперация = ПриглашенияЭДОИнтеграцияОблакаВызовСервера.ОтправитьПриглашенияВФоне(
		ПараметрыДействия.Приглашения, РезультатАвторизации.РасшифрованныеМаркеры);
	
	ОжидатьЗавершениеДействияСПриглашениями(ДлительнаяОперация, ПараметрыДействия);
	
КонецПроцедуры

// Параметры:
//  ИтогРасшифровки - см. КриптографияБЭДКлиент.НовыйИтогРасшифровкиНабораДанных
//  ПараметрыДействия - см. НовыеПараметрыДействияСПриглашениями
Процедура ПринятьПриглашенияПослеАвторизацииВСервисеЭДО(ИтогРасшифровки, ПараметрыДействия) Экспорт
	
	ДлительнаяОперация = ПриглашенияЭДОИнтеграцияОблакаВызовСервера.ПринятьПриглашенияВФоне(
		ПараметрыДействия.Приглашения, ИтогРасшифровки.НаборРасшифрованныхДанных);
	
	ОжидатьЗавершениеДействияСПриглашениями(ДлительнаяОперация, ПараметрыДействия);
	
КонецПроцедуры

// Параметры:
//  ИтогРасшифровки - см. КриптографияБЭДКлиент.НовыйИтогРасшифровкиНабораДанных
//  ПараметрыДействия - см. НовыеПараметрыДействияСПриглашениями
Процедура ОтклонитьПриглашенияПослеАвторизацииВСервисеЭДО(ИтогРасшифровки, ПараметрыДействия) Экспорт
	
	ДлительнаяОперация = ПриглашенияЭДОИнтеграцияОблакаВызовСервера.ОтклонитьПриглашенияВФоне(
		ПараметрыДействия.Приглашения, ИтогРасшифровки.НаборРасшифрованныхДанных);
	
	ОжидатьЗавершениеДействияСПриглашениями(ДлительнаяОперация, ПараметрыДействия);
	
КонецПроцедуры

// Параметры:
//  ДлительнаяОперация - см. ДлительныеОперации.ВыполнитьФункцию
//  ПараметрыДействия - см. НовыеПараметрыДействияСПриглашениями
Процедура ОжидатьЗавершениеДействияСПриглашениями(ДлительнаяОперация, ПараметрыДействия)
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатДействияСПриглашениями", ЭтотОбъект, ПараметрыДействия);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Ложь;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

// Параметры:
//  ДлительнаяОперация - см. ДлительныеОперации.ВыполнитьФункцию
//  ПараметрыДействия - см. НовыеПараметрыДействияСПриглашениями
Процедура ОбработатьРезультатДействияСПриглашениями(ДлительнаяОперация, ПараметрыДействия) Экспорт
	
	Результат = ПриглашенияЭДОИнтеграцияОблакаВызовСервера.РезультатДействияСПриглашениямиВФоне(ДлительнаяОперация);
	
	ВыполнитьОбработкуОповещения(ПараметрыДействия.ОповещениеОЗавершении, Результат);
	
КонецПроцедуры

#КонецОбласти
