#Область ПрограммныйИнтерфейс

Процедура ПриОткрытииОтчета(Форма, ИмяОтчета) Экспорт
	
	ПроверкаИспользованияСверки(Форма, ИмяОтчета);
	Форма.Элементы.КартинкаБаннера.Картинка          = БиблиотекаКартинок[СервисСверкиРасчетовВызовСервера.КартинкаБаннераСервисаСверкиРасчетов()];
	Форма.Элементы.КартинкаЗакрытьБаннер.Картинка    = БиблиотекаКартинок.ОчиститьЗначениеСверка;
	Форма.Элементы.ГруппаНеПодключенаСверка.ЦветФона = СервисСверкиРасчетовВызовСервера.ЦветФонаБаннеровСервисаСверкиРасчетов();
	
	Если ИмяОтчета = "КнигаПокупок" Тогда
		Форма.Элементы.НеПодключенаСверкаТекст.Заголовок = СервисСверкиРасчетовВызовСервера.ТекстБаннераСервисСверкиРасчетовКнигаПокупок();
	ИначеЕсли ИмяОтчета = "КнигаПродаж" Тогда
		Форма.Элементы.НеПодключенаСверкаТекст.Заголовок = СервисСверкиРасчетовВызовСервера.ТекстБаннераСервисСверкиРасчетовКнигаПродаж();
	ИначеЕсли ИмяОтчета = "Декларация" Тогда
		Форма.Элементы.НеПодключенаСверкаТекст.Заголовок = СервисСверкиРасчетовВызовСервера.ТекстБаннераСервисСверкиРасчетовДекларацияПоНДС();
	ИначеЕсли ИмяОтчета = "ПомощникПоНДС" Тогда
		Форма.Элементы.НеПодключенаСверкаТекст.Заголовок = СервисСверкиРасчетовВызовСервера.ТекстБаннераСервисСверкиРасчетовПомощникРасчетаНДС();
	КонецЕсли;
	
	Если Не (ИмяОтчета = "Декларация" Или ИмяОтчета = "ПомощникПоНДС") Тогда
		Форма.РазворачиватьРезультатыСверкиДекларацийСразу = СервисСверкиРасчетовСверкаДекларацийНДСВызовСервера.ОтображатьСверкуПриФормировании(ИмяОтчета);
	КонецЕсли;
	
	Если Не ИмяОтчета = "Декларация" Тогда
		СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер.ИнициализацияРеквизитовСверкиДекларации(Форма, ИмяОтчета);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверкаИспользованияСверки(Форма, ИмяОтчета) Экспорт
	
	Если ИмяОтчета = "Декларация" Тогда
		Форма.ИспользуетсяСервисСверкиРасчетов = СервисСверкиРасчетовВызовСервера.ОрганизацияПодключена(Форма.СтруктураРеквизитовФормы.Организация,
			Форма.СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
		Форма.Элементы.ГруппаНеПодключенаСверка.Видимость = Не Форма.ИспользуетсяСервисСверкиРасчетов;
	ИначеЕсли ИмяОтчета = "ПомощникПоНДС" Тогда
		Если ЗначениеЗаполнено(Форма.Объект.Организация) Тогда
			ПодключенаСверка = СервисСверкиРасчетовВызовСервера.ОрганизацияПодключена(Форма.Объект.Организация, НачалоКвартала(Форма.Объект.Период));
			Форма.Элементы.ГруппаНеПодключенаСверка.Видимость = Не ПодключенаСверка;
			Форма.ИспользуетсяСервисСверкиРасчетов = ПодключенаСверка И НачалоКвартала(Форма.Объект.Период) >= '20250101';
		Иначе
			Форма.ИспользуетсяСервисСверкиРасчетов = Ложь;
			Форма.Элементы.ГруппаНеПодключенаСверка.Видимость = Ложь;
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Форма.Отчет.Организация) Тогда
			ПодключенаСверка = СервисСверкиРасчетовВызовСервера.ОрганизацияПодключена(Форма.Отчет.Организация, Форма.Отчет.НачалоПериода);
			Форма.ИспользуетсяСервисСверкиРасчетов = ПодключенаСверка И Форма.Отчет.НачалоПериода >= '20240101';
			Форма.Элементы.ГруппаРезультатСверки.Видимость = Ложь;
			Форма.Элементы.ГруппаНеПодключенаСверка.Видимость = Не ПодключенаСверка;
		Иначе
			Форма.ИспользуетсяСервисСверкиРасчетов = Ложь;
			Форма.Элементы.ГруппаНеПодключенаСверка.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ИзменитьВидимостьСверки(Форма) Экспорт
	
	Если Форма.ОтображатьСверку Тогда
		Форма.Элементы.ИзменитьВидимостьСверки.Картинка = БиблиотекаКартинок.СтрелкаВлево;
		Форма.ОтображатьСверку = Ложь;
		Форма.Элементы.ГруппаИтогиСверки.Видимость = Ложь;
	Иначе
		Форма.Элементы.ИзменитьВидимостьСверки.Картинка = БиблиотекаКартинок.СтрелкаВправо;
		Форма.ОтображатьСверку = Истина;
		Форма.Элементы.ГруппаИтогиСверки.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗакрытьПанельСверки(Форма) Экспорт
	
	Форма.Элементы.ИзменитьВидимостьСверки.Картинка = БиблиотекаКартинок.СтрелкаВлево;
	Форма.ОтображатьСверку = Ложь;
	Форма.Элементы.ГруппаИтогиСверки.Видимость = Ложь;
	
КонецПроцедуры

Процедура ОтменитьВыполнениеСверки(Форма) Экспорт
	
	СервисСверкиРасчетовСверкаДекларацийНДСВызовСервера.ОтменитьВыполнениеСверки(Форма.ИдентификаторЗаданияСверки);
	Форма.ИдентификаторЗаданияСверки = Неопределено;
	Форма.Элементы.ГруппаРезультатСверки.Видимость = Ложь;
	
КонецПроцедуры

#Область КнигиПокупокПродаж

Процедура СверитьОтчет(Форма) Экспорт
	
	Если Не Форма.ИспользуетсяСервисСверкиРасчетов Тогда
		Возврат;
	КонецЕсли;
	
	Форма.ДанныеДляСверкиДеклараций.АдресРезультатаСверки = ПоместитьВоВременноеХранилище(Неопределено, Форма.УникальныйИдентификатор);
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("НачалоПериода",                  Форма.Отчет.НачалоПериода);
	СтруктураПараметров.Вставить("КонецПериода",                   Форма.Отчет.КонецПериода);
	СтруктураПараметров.Вставить("ДанныеДляСверкиДеклараций",      Форма.ДанныеДляСверкиДеклараций);
	СтруктураПараметров.Вставить("Организация",                    Форма.Отчет.Организация);
	СтруктураПараметров.Вставить("УникальныйИдентификатор",        Форма.УникальныйИдентификатор);
	СтруктураПараметров.Вставить("Контрагент",                     Форма.Отчет.КонтрагентДляОтбора);
	СтруктураПараметров.Вставить("ВыводитьТолькоДопЛисты",         Форма.Отчет.ВыводитьТолькоДопЛисты);
	СтруктураПараметров.Вставить("ФормироватьДополнительныеЛисты", Форма.Отчет.ФормироватьДополнительныеЛисты);
	СтруктураПараметров.Вставить("ДанныеСверки",                   Неопределено);
	СтруктураПараметров.Вставить("КодВидаОперации",                Неопределено);
	СтруктураПараметров.Вставить("ОперацияНетВОрганизации",        Неопределено);
	
	ВыполнениеСверки = СервисСверкиРасчетовСверкаДекларацийНДСВызовСервера.СверитьОтчет(СтруктураПараметров);

	Если ВыполнениеСверки.Статус = "Выполнено" Тогда
		
		СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер.ОтобразитьСверкуНаФорме(Форма);

	ИначеЕсли ВыполнениеСверки.Статус = "Ошибка" Тогда
		
		Форма.Элементы.ГруппаНеПолучилиДанныеИзСервиса.Видимость = Истина;
		Форма.Элементы.ТекстНеПолучилиДанныеИзСервиса.Заголовок = НСтр("ru = 'Не удалось получить данные из сервиса ""1С:Сверка 2.0"". ';
																		|en = 'Cannot get data from the 1C:Reconciliation 2.0 service or perform reconciliation.·'");
	
	ИначеЕсли ВыполнениеСверки.Статус = "Выполняется" Тогда
		ИзменитьВидимостьСверки(Форма);
		
		Форма.Элементы.ГруппаРезультатСверки.Видимость           = Истина;
		Форма.Элементы.ГруппаОжиданиеСверки.Видимость            = Истина;
		Форма.Элементы.ГруппаСверкаСРасхождениями.Видимость      = Ложь;
		Форма.Элементы.ГруппаСверкаБезОшибок.Видимость           = Ложь;
		Форма.Элементы.ГруппаНеПолучилиДанныеИзСервиса.Видимость = Ложь;
		Форма.ИдентификаторЗаданияСверки = ВыполнениеСверки.ИдентификаторЗадания;
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("Форма", Форма);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОтобразитьРезультатыСверки", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ВыполнениеСверки, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;

КонецПроцедуры

Процедура СохранитьРезультатыОтчета(Форма) Экспорт
	
	Если Форма.ПроверкаКонтрагентовПереключательРежимаОтображения = "Недействующие" Тогда
		Форма.ДанныеДляСверкиДеклараций.НедействующиеКонтрагенты = ПоместитьВоВременноеХранилище(Форма.Результат, Форма.УникальныйИдентификатор);
	Иначе
		Форма.ДанныеДляСверкиДеклараций.ОсновнойРаздел = ПоместитьВоВременноеХранилище(Форма.Результат, Форма.УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтобразитьРезультатыСверки(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	Форма.ИдентификаторЗаданияСверки = Неопределено;
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		Форма.Элементы.ГруппаНеПолучилиДанныеИзСервиса.Видимость = Истина;
		Форма.Элементы.ТекстНеПолучилиДанныеИзСервиса.Заголовок = НСтр("ru = 'Не удалось получить данные из сервиса ""1С:Сверка 2.0"" или произвести сверку. ';
																		|en = 'Cannot get data from the 1C:Reconciliation 2.0 service or perform reconciliation.·'");
		Форма.Элементы.ГруппаОжиданиеСверки.Видимость = Ложь;
	ИначеЕсли Форма.Открыта() Тогда
		СервисСверкиРасчетовСверкаДекларацийНДСКлиентСервер.ОтобразитьСверкуНаФорме(ДополнительныеПараметры.Форма, Форма.Отчет.ВыводитьТолькоДопЛисты);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДекларацияПоНДС

Процедура СверитьДекларациюПоНДС(Форма) Экспорт

	Если Не Форма.ИспользуетсяСервисСверкиРасчетов Тогда
		Возврат;
	КонецЕсли;
		
	Форма.ДанныеДляСверкиДеклараций.АдресРезультатаСверки = ПоместитьВоВременноеХранилище(Неопределено, Форма.УникальныйИдентификатор);
	
	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("НачалоПериода",                  Форма.СтруктураРеквизитовФормы.мДатаНачалаПериодаОтчета);
	СтруктураПараметров.Вставить("КонецПериода",                   Форма.СтруктураРеквизитовФормы.мДатаКонцаПериодаОтчета);
	СтруктураПараметров.Вставить("ДанныеДляСверкиДеклараций",      Форма.ДанныеДляСверкиДеклараций);
	СтруктураПараметров.Вставить("Организация",                    Форма.СтруктураРеквизитовФормы.Организация);
	СтруктураПараметров.Вставить("УникальныйИдентификатор",        Форма.УникальныйИдентификатор);
	СтруктураПараметров.Вставить("Контрагент",                     Неопределено);
	СтруктураПараметров.Вставить("ВыводитьТолькоДопЛисты",         Ложь);
	СтруктураПараметров.Вставить("ФормироватьДополнительныеЛисты", Истина);
	СтруктураПараметров.Вставить("ДекларацияПоНДС",                Форма.СтруктураРеквизитовФормы.мСохраненныйДок);
	СтруктураПараметров.Вставить("ДанныеСверки",                   Неопределено);
	СтруктураПараметров.Вставить("КодВидаОперации",                Неопределено);
	СтруктураПараметров.Вставить("ОперацияНетВОрганизации",        Неопределено);

	ВыполнениеСверки = СервисСверкиРасчетовСверкаДекларацийНДСВызовСервера.СверитьДекларацию(СтруктураПараметров);
	
	Если ВыполнениеСверки.Статус = "Выполнено" Тогда
		РезультатыСверки = СервисСверкиРасчетовСверкаДекларацийНДСВызовСервера.ПроверитьНаличиеРасхождений(Форма.ДанныеДляСверкиДеклараций.АдресРезультатаСверки);
		Форма.ДанныеДляСверкиДеклараций.ЕстьРасхождения = РезультатыСверки.ЕстьРасхождения;
		Форма.ДанныеДляСверкиДеклараций.ЕстьНеСверенные = РезультатыСверки.ЕстьНеСверенные;
		Форма.ДанныеДляСверкиДеклараций.ПроверкаВыполнена = Истина;
		Форма.ВывестиРезультатПроверки();
	ИначеЕсли ВыполнениеСверки.Статус = "Ошибка" Тогда
		Форма.ДанныеДляСверкиДекларации.ПроверкаВыполнена = Истина;
		Форма.ДанныеДляСверкиДеклараций.НеУдалосьВыполнитьСверку = Истина;
		Форма.ВывестиРезультатПроверки();

	ИначеЕсли ВыполнениеСверки.Статус = "Выполняется" Тогда
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("Форма", Форма);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОтобразитьРезультатыСверкиДекларации", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ВыполнениеСверки, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;

КонецПроцедуры

Процедура ОтобразитьРезультатыСверкиДекларации(Результат, ДополнительныеПараметры = Неопределено) Экспорт

	Форма = ДополнительныеПараметры.Форма;
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		Форма.ДанныеДляСверкиДеклараций.НеУдалосьВыполнитьСверку = Истина;
		Форма.ДанныеДляСверкиДеклараций.ПроверкаВыполнена = Истина;
		Форма.ВывестиРезультатПроверки();
	ИначеЕсли Форма.Открыта() Тогда
		Форма.ДанныеДляСверкиДеклараций.ПроверкаВыполнена = Истина;
		РезультатыСверки = СервисСверкиРасчетовСверкаДекларацийНДСВызовСервера.ПроверитьНаличиеРасхождений(Форма.ДанныеДляСверкиДеклараций.АдресРезультатаСверки);
		Форма.ДанныеДляСверкиДеклараций.ЕстьРасхождения          = РезультатыСверки.ЕстьРасхождения;
		Форма.ДанныеДляСверкиДеклараций.ЕстьНеСверенные          = РезультатыСверки.ЕстьНеСверенные;
		Форма.ДанныеДляСверкиДеклараций.НеУдалосьВыполнитьСверку = РезультатыСверки.НеУдалосьВыполнитьСверку;
		Форма.ВывестиРезультатПроверки();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьОтчетПоРасхождениямВСверке(Форма) Экспорт
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("АдресРезультатаСверки",     Форма.ДанныеДляСверкиДеклараций.АдресРезультатаСверки);
	СтруктураПараметров.Вставить("ПредставлениеОтчета",       Форма.Заголовок);
	СтруктураПараметров.Вставить("СтраницыРаздела",           Форма.ДанныеДляСверкиДеклараций.СтраницыРазделов);
	СтруктураПараметров.Вставить("ДанныеДляСверкиДеклараций", Форма.ДанныеДляСверкиДеклараций);
	
	ОткрытьФорму("ОбщаяФорма.РезультатыСверкиДекларацииНДС", СтруктураПараметров,,Новый УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
