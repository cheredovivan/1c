
#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// КлиентЭДО

// Отменяет выполнение длительной операции.
// 
// Параметры:
// 	ИдентификаторЗадания - УникальныйИдентификатор, Строка - идентификатор длительной операции.
//
Процедура ОтменитьВыполнениеЗадания(Знач ИдентификаторЗадания) Экспорт
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

// Конец КлиентЭДО

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция НайтиПрограммыЭлектроннойПодписиИШифрования(ОписанияПрограмм) Экспорт
	
	ИменаПрограмм = Новый Массив;
	Для каждого Описание Из ОписанияПрограмм Цикл
		Если Описание.Установлена Тогда
			ИменаПрограмм.Добавить(Описание.ИмяПрограммы);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Программы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПрограммыЭлектроннойПодписиИШифрования КАК Программы
	|ГДЕ
	|	Программы.ИмяПрограммы В(&ИменаПрограмм)";
	Запрос.УстановитьПараметр("ИменаПрограмм", ИменаПрограмм);
	
	НайденныеПрограммы = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НайденныеПрограммы.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат НайденныеПрограммы;
	
КонецФункции

// Находит существующий или создает новый элемент справочника СертификатыКлючейЭлектроннойПодписиИШифрования.
//
// Параметры:
//  ДвоичныеДанныеСертификата - ДвоичныеДанные - содержимое сертификата
//  Организация - СправочникСсылка.Организации
//  ИнформацияОПрограммеКриптографии - Строка - название криптосредства
//                                - СправочникСсылка.ПрограммыЭлектроннойПодписиИШифрования - ссылка на программу криптографии.
//
// Возвращаемое значение:
//  СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - ссылка на новый сертификат.
//
Функция НайтиСоздатьСертификатЭП(ДвоичныеДанныеСертификата, Организация, ИнформацияОПрограммеКриптографии = Неопределено) Экспорт
	
	Если ТипЗнч(ИнформацияОПрограммеКриптографии) = Тип("Строка")  Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	ПрограммыЭлектроннойПодписиИШифрования.Ссылка
		               |ИЗ
		               |	Справочник.ПрограммыЭлектроннойПодписиИШифрования КАК ПрограммыЭлектроннойПодписиИШифрования
		               |ГДЕ
		               |	ПрограммыЭлектроннойПодписиИШифрования.ИмяПрограммы = &НазваниеПрограммыКриптографии";
		Запрос.УстановитьПараметр("НазваниеПрограммыКриптографии", ИнформацияОПрограммеКриптографии);
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		Если Выборка.Следующий() Тогда
			Программа = Выборка.Ссылка;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ИнформацияОПрограммеКриптографии) = Тип("СправочникСсылка.ПрограммыЭлектроннойПодписиИШифрования") Тогда
		Программа = ИнформацияОПрограммеКриптографии;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Организация", Организация);
	
	// Если в ИБ уже есть такой сертификат, и в нем заполнена программа криптографии, то не меняем программу,
	// т.к. он могла быть указана правильно вручную.
	СсылкаНаСертификат = ЭлектроннаяПодпись.СсылкаНаСертификат(ДвоичныеДанныеСертификата);
	Если НЕ ЗначениеЗаполнено(СсылкаНаСертификат)
		ИЛИ (ЗначениеЗаполнено(СсылкаНаСертификат)
				И НЕ ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаСертификат, "Программа"))) Тогда
		ДополнительныеПараметры.Вставить("Программа", Программа);
	КонецЕсли;
	
	Возврат ЭлектроннаяПодпись.ЗаписатьСертификатВСправочник(ДвоичныеДанныеСертификата, ДополнительныеПараметры);

КонецФункции

// Возвращает Свойства сертификатов организаций по отпечаткам
// Параметры:
//  Отпечатки - Массив из Строка - Массив отпечатков на токене
//  ОтборОрганизаций - Массив из СправочникСсылка.Организации
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//   * Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//   * Значение - Структура:
//    ** Ссылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// 	  ** Отозван - Булево
//    ** Отпечаток - Строка
//    ** ДействителенДо - Дата - дата окончания действия сертификата
//    ** КемВыдан - Строка - удостоверяющий центр, который выдал сертификат
//    ** Наименование - Строка - пользовательское представление сертификата
//    ** Программа - Строка - Программа, требуемая для подписания и расшифровки документов
//    ** ДанныеСертификата - ДвоичныеДанные - данные файла сертификата
//    ** ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - в ключ помещается 
//       ссылка на переданный сертификат.
//    ** ПарольПолучен - Булево - содержит Ложь
//    ** ПарольПользователя - Неопределено
//    ** Комментарий - Строка - содержит пустую строку
//    ** СрокДействияВДнях - Число - количество дней, оставшееся до истечения срока действия сертификата
//    ** Организация - ОпределяемыйТип.Организация
//    ** Фамилия - Строка - фамилия владельца сертификата
//    ** Имя - Строка - имя владельца сертификата
//    ** Отчество - Строка - отчество владельца сертификата
//    ** Должность - Строка - должность владельца сертификата
//	  ** Пользователь - ОпределяемыйТип.Пользователь
//    ** КомуВыдан - Строка
//    ** Фирма - Строка
//
Функция СвойстваСертификатовОрганизацийПоОтпечаткам(Отпечатки, ОтборОрганизаций) Экспорт

	НайденныеСертификаты = Новый Массив;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка КАК Ссылка,
		|	СертификатыУчетныхЗаписейЭДО.Сертификат КАК Сертификат
		|ИЗ
		|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК СертификатыКлючейЭлектроннойПодписиИШифрования
		|		ПО СертификатыУчетныхЗаписейЭДО.Сертификат = СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка
		|ГДЕ
		|	СертификатыКлючейЭлектроннойПодписиИШифрования.Отпечаток В(&ОтпечаткиВКонтейнере)
		|	И СертификатыКлючейЭлектроннойПодписиИШифрования.ДействителенДо >= &ТекущаяДата
		|	И НЕ СертификатыКлючейЭлектроннойПодписиИШифрования.ПометкаУдаления
		|	И СертификатыКлючейЭлектроннойПодписиИШифрования.Организация В(&ОтборОрганизаций)";
	
	Запрос.УстановитьПараметр("ОтпечаткиВКонтейнере", Отпечатки);
	Запрос.УстановитьПараметр("ОтборОрганизаций", ОтборОрганизаций);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Если Отпечатки.Количество() > 0 Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Открытой части ключа сертификата не найдено на сервере. Необходимо добавить.';
														|en = 'The public part of the certificate key is not found on the server. It must be added.'"));
		Иначе
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не найдено доступных сертификатов на токене';
														|en = 'No available certificates for the token are found'"));
		КонецЕсли;
	Иначе
		НайденныеСертификаты = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	Возврат КриптографияБЭД.СвойстваСертификатов(НайденныеСертификаты);
	
КонецФункции

#Область СервисМояПодпись
 
// Возвращает параметры подписанта
// Параметры:
//  Подписант - СправочникСсылка.ПодписантыСервисаМобильнойПодписи
//	Возвращаемое значение:
//  Структура:
//    * ИНН - Строка - не заполнен для ИП
//    * ИННФЛ - Строка - ИНН физического лица (индивидуального предпринимателя)
//    * НомерТелефона - Строка - номер мобильного телефона, начинается с +7.
//    * Имя - Строка - (необязательный)
//    * Отчество - Строка - (необязательный)
//    * Фамилия - Строка - (необязательный)
//    * Должность - Строка - (необязательный)
//    * Организация - ОпределяемыйТип.Организация
//    * ОГРН - Строка - (необязательный)
//    * НаименованиеОрганизации - Строка - (необязательный) если не заполнена Организация
//    * ФизическоеЛицо - ОпределяемыйТип.ФизическоеЛицо - (необязательный)
//    * Наименование - Строка
//
Функция ПараметрыПодписантаМобильнойПодписи(Подписант) Экспорт
	
	Если Не ИнтеграцияБСПБЭДПовтИсп.ДоступноИспользованиеСервисаМояПодпись() Тогда
		ВызватьИсключение НСтр("ru = 'Сервис мобильной подписи не используется или не доступен';
								|en = 'The mobile signature service is not in use or is not available'");
	КонецЕсли;
	
	МодульСервисМобильнойПодписиКлиентСервер =
		ОбщегоНазначения.ОбщийМодуль("СервисМобильнойПодписиКлиентСервер");
	
	Если МодульСервисМобильнойПодписиКлиентСервер <> Неопределено Тогда
	
		ПараметрыПодписанта = МодульСервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
	
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Подписант,
			"ИНН, НомерТелефона, ИННФЛ, ОГРН, Имя, Отчество, Фамилия, Должность, Организация, НаименованиеОрганизации, ФизическоеЛицо, Наименование");
		
		ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, Реквизиты);
	
		Возврат ПараметрыПодписанта;
	
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти