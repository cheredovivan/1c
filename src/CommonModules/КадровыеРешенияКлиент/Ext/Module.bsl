// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

#Область КадровыеДокументы

Процедура ОбработкаНавигационнойСсылкиРешения(Форма, НавигационнаяСсылка, СтандартнаяОбработка) Экспорт 
	
	Объект = Форма.Объект;
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылка = "ДокументРешение" Тогда
		
		ПоказатьЗначение(, Объект.Решение);
		
	Иначе
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Организация", Объект.Организация);
		ПараметрыОтбора.Вставить("Проведен", Истина);
		
		Если НавигационнаяСсылка = "РешениеОПриемеНаРаботу" Тогда
			Если ОбщегоНазначенияКлиент.ПодсистемаСуществует(
				"ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
					
				МодульПодборПерсоналаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодборПерсоналаКлиент");
				МодульПодборПерсоналаКлиент.ДополнитьПараметрыОтбораСпискаРешений(НавигационнаяСсылка, ПараметрыОтбора,
					Объект);
					
			КонецЕсли;
		Иначе
			СоответствиеРеквизитов = КадровыеРешенияВызовСервера.СоответствиеРеквизитовОтбораРешенияРеквизитамПриказа(
				НавигационнаяСсылка);
			Для каждого РеквизитОтбора Из СоответствиеРеквизитов Цикл
				ПараметрыОтбора.Вставить(РеквизитОтбора.Ключ, Объект[РеквизитОтбора.Значение]);
			КонецЦикла;
		КонецЕсли;
		
		ПараметрыОткрытияФормыВыбора = Новый Структура;
		ПараметрыОткрытияФормыВыбора.Вставить("РежимВыбора", Истина);
		ПараметрыОткрытияФормыВыбора.Вставить("Отбор", ПараметрыОтбора);
		
		Оповещение = Новый ОписаниеОповещения("Подключаемый_ОбработкаНавигационнойСсылкиРешенияЗавершение", Форма);
		
		ИмяОткрываемойФормы = СтрШаблон("Документ.%1.ФормаВыбора", НавигационнаяСсылка);
		ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыОткрытияФормыВыбора, Форма, Истина, , , Оповещение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти