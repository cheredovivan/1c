
#Область ПрограммныйИнтерфейс

//++ НЕ УТКА

// Выгрузить модель МФУПолностью.
// 
// Параметры:
//  ИмяФайлаНаСервере - Строка - Имя файла на сервере
Процедура ВыгрузитьМодельМФУПолностью(ИмяФайлаНаСервере) Экспорт

	ДанныеВыгрузки = ТаблицаДанныеВыгрузки();
	
	Выборка = Справочники.ПланыСчетовМеждународногоУчета.Выбрать();
	ПланыСчетовМеждународногоУчетаКВыгрузке = Новый Массив();
	Пока Выборка.Следующий() Цикл
		ПланыСчетовМеждународногоУчетаКВыгрузке.Добавить(Выборка.Ссылка);
	КонецЦикла;
	Если ПланыСчетовМеждународногоУчетаКВыгрузке.Количество() Тогда
		ОбъектМД = Метаданные.Справочники.ПланыСчетовМеждународногоУчета;
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД, Неопределено, ПланыСчетовМеждународногоУчетаКВыгрузке);
		
		ОбъектМД = Метаданные.ПланыСчетов.Международный;
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки,
									ОбъектМД,
									Неопределено,
									ПланыСчетовМеждународногоУчетаКВыгрузке,
									Метаданные.ПланыСчетов.Международный.Реквизиты.ПланСчетов.Имя);
		
		Если ДанныеВыгрузки.Найти(ОбъектМД.ПолноеИмя(), "ИмяМД") <> Неопределено Тогда
			ОбъектМД = Метаданные.ПланыВидовХарактеристик.ВидыСубконтоМеждународные;
			ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД);
		КонецЕсли;
	КонецЕсли;
	
	Выборка = Справочники.ВидыФинансовыхОтчетов.Выбрать();
	ВидыФинансовыхОтчетовКВыгрузке = Новый Массив();
	Пока Выборка.Следующий() Цикл
		ВидыФинансовыхОтчетовКВыгрузке.Добавить(Выборка.Ссылка);
	КонецЦикла;
	Если ВидыФинансовыхОтчетовКВыгрузке.Количество() Тогда
		ОбъектМД = Метаданные.Справочники.ВидыФинансовыхОтчетов;
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД, Неопределено, ВидыФинансовыхОтчетовКВыгрузке);
		
		ОбъектМД = Метаданные.Справочники.ЭлементыФинансовыхОтчетов;
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД, Неопределено, ВидыФинансовыхОтчетовКВыгрузке, "Владелец");
	КонецЕсли;
	
	Выборка = Справочники.КомплектыФинансовыхОтчетов.Выбрать();
	КомплектыФинансовыхОтчетовКВыгрузке = Новый Массив();
	Пока Выборка.Следующий() Цикл
		КомплектыФинансовыхОтчетовКВыгрузке.Добавить(Выборка.Ссылка);
	КонецЦикла;
	Если КомплектыФинансовыхОтчетовКВыгрузке.Количество() Тогда
		ОбъектМД = Метаданные.Справочники.КомплектыФинансовыхОтчетов;
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД, Неопределено, КомплектыФинансовыхОтчетовКВыгрузке);
	КонецЕсли;
	
	Выборка = ПланыВидовХарактеристик.СтатьиАктивовПассивов.Выбрать();
	СтатьиАктивовПассивовКВыгрузке = Новый Массив();
	Пока Выборка.Следующий() Цикл
		СтатьиАктивовПассивовКВыгрузке.Добавить(Выборка.Ссылка);
	КонецЦикла;
	Если СтатьиАктивовПассивовКВыгрузке.Количество() Тогда
		ОбъектМД = Метаданные.ПланыВидовХарактеристик.СтатьиАктивовПассивов;
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД, Неопределено, СтатьиАктивовПассивовКВыгрузке);
	КонецЕсли;
	
	Выборка = Справочники.НастройкиФормированияПроводокМеждународногоУчета.Выбрать();
	НастройкиФормированияПроводокКВыгрузке = Новый Массив();
	Пока Выборка.Следующий() Цикл
		НастройкиФормированияПроводокКВыгрузке.Добавить(Выборка.Ссылка);
	КонецЦикла;
	Если НастройкиФормированияПроводокКВыгрузке.Количество() Тогда
		ОбъектМДОтбора = Метаданные.Справочники.НастройкиФормированияПроводокМеждународногоУчета;
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМДОтбора, Неопределено, НастройкиФормированияПроводокКВыгрузке);
		
		ОбъектМД = Метаданные.РегистрыСведений.ПравилаОтраженияВМеждународномУчете;
		ИмяИзмеренияДляОтбора = ИмяИзмеренияДляОтбора(ОбъектМДОтбора, ОбъектМД);
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД, Неопределено, НастройкиФормированияПроводокКВыгрузке, ИмяИзмеренияДляОтбора);
		
		ОбъектМД = Метаданные.РегистрыСведений.ХозяйственныеОперацииНеОтражаемыеВМеждународномУчете;
		ИмяИзмеренияДляОтбора = ИмяИзмеренияДляОтбора(ОбъектМДОтбора, ОбъектМД);
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД, Неопределено, НастройкиФормированияПроводокКВыгрузке, ИмяИзмеренияДляОтбора);
		
		ОбъектМД = Метаданные.РегистрыСведений.НастройкиОтраженияКорреспонденцийВМеждународномУчете;
		ИмяИзмеренияДляОтбора = ИмяИзмеренияДляОтбора(ОбъектМДОтбора, ОбъектМД);
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД, Неопределено, НастройкиФормированияПроводокКВыгрузке, ИмяИзмеренияДляОтбора);
		
		ОбъектМД = Метаданные.РегистрыСведений.НастройкиСчетовМеждународногоУчетаПоОбъектам;
		ИмяИзмеренияДляОтбора = ИмяИзмеренияДляОтбора(ОбъектМДОтбора, ОбъектМД);
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД, Неопределено, НастройкиФормированияПроводокКВыгрузке, ИмяИзмеренияДляОтбора);
		
		ОбъектМД = Метаданные.РегистрыСведений.НастройкиЗаполненияСубконтоНаСчетахМеждународногоУчета;
		ИмяИзмеренияДляОтбора = ИмяИзмеренияДляОтбора(ОбъектМДОтбора, ОбъектМД);
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД, Неопределено, НастройкиФормированияПроводокКВыгрузке, ИмяИзмеренияДляОтбора);
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПравилаОтраженияВМеждународномУчете.ШаблонПроводки КАК ШаблонПроводки
			|ИЗ
			|	РегистрСведений.ПравилаОтраженияВМеждународномУчете КАК ПравилаОтраженияВМеждународномУчете
			|ГДЕ
			|	ПравилаОтраженияВМеждународномУчете.НастройкаФормированияПроводок В (&НастройкаФормированияПроводок)";
		Запрос.УстановитьПараметр("НастройкаФормированияПроводок", НастройкиФормированияПроводокКВыгрузке);
		ШаблоныПроводокКВыгрузке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ШаблонПроводки");
		
		ОбъектМДОтбора = Метаданные.Справочники.ШаблоныПроводокДляМеждународногоУчета;
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМДОтбора, Неопределено, ШаблоныПроводокКВыгрузке);
		
		ОбъектМД = Метаданные.РегистрыСведений.ПравилаУточненияСчетовВМеждународномУчете;
		ИмяИзмеренияДляОтбора = ИмяИзмеренияДляОтбора(ОбъектМДОтбора, ОбъектМД);
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД, Неопределено, ШаблоныПроводокКВыгрузке, ИмяИзмеренияДляОтбора);
	КонецЕсли;
	
	Выборка = Справочники.ТиповыеОперацииМеждународныйУчет.Выбрать();
	ТиповыеОперацииМеждународныйУчетКВыгрузке = Новый Массив();
	Пока Выборка.Следующий() Цикл
		ТиповыеОперацииМеждународныйУчетКВыгрузке.Добавить(Выборка.Ссылка);
	КонецЦикла;
	Если ТиповыеОперацииМеждународныйУчетКВыгрузке.Количество() Тогда
		ОбъектМД = Метаданные.Справочники.ТиповыеОперацииМеждународныйУчет;
		ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД, Неопределено, ТиповыеОперацииМеждународныйУчетКВыгрузке);
	КонецЕсли;
	
	ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, Метаданные.Справочники.ГруппыФинансовогоУчетаРасчетов);
	ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, Метаданные.Справочники.ГруппыФинансовогоУчетаДенежныхСредств);
	ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, Метаданные.Справочники.ГруппыФинансовогоУчетаДоходовРасходов);
	ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, Метаданные.Справочники.ГруппыФинансовогоУчетаНоменклатуры);
	ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, Метаданные.Справочники.СтатьиДвиженияДенежныхСредств);
	ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, Метаданные.ПланыВидовХарактеристик.СтатьиДоходов);
	ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, Метаданные.ПланыВидовХарактеристик.СтатьиРасходов);
	ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, Метаданные.Справочники.ГруппыНМА);
	ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, Метаданные.Справочники.ГруппыОСМеждународныйУчет);
	ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, Метаданные.Справочники.НемонетарныеПоказатели);

	Параметры = Новый Структура;
	Параметры.Вставить("ДанныеВыгрузки", ДанныеВыгрузки);
	Параметры.Вставить("ЭтоМодельМеждународногоУчета", Истина);
	
	ВыгрузитьМодельУчета(Параметры, ИмяФайлаНаСервере)

КонецПроцедуры

//-- НЕ УТКА

// Выполняет загрузку модели международного учета из файла
//
// Параметры:
// 	Параметры - Структура - Содержит адрес данных для загрузки:
// 	 * ДвоичныеДанные - ДвоичныеДанные - 
//
Процедура ЗагрузитьМодельУчета(Параметры) Экспорт
	
	ТипЗагружаемойМодели = ТипМодели(Параметры);
	
	Если Параметры.Свойство("ЭтоМодельБюджетирования") И Параметры.ЭтоМодельБюджетирования Тогда
		ЭтоМодельБюджетирования = Истина;
	Иначе
		ЭтоМодельБюджетирования = Ложь;
	КонецЕсли;
	
	ИмяВременногоФайлаДанных = ПолучитьИмяВременногоФайла("xml");
	Параметры.ДвоичныеДанные.Записать(ИмяВременногоФайлаДанных);
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяВременногоФайлаДанных);
	// проверка формата файла обмена
	Если Не ЧтениеXML.Прочитать()
		Или ЧтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента
		Или ЧтениеXML.ЛокальноеИмя <> "_1CV8DtUD"
		Или ЧтениеXML.URIПространстваИмен <> "http://www.1c.ru/V8/1CV8DtUD/" Тогда
		
		ОписаниеОшибки = Нстр("ru = 'Неверный формат файла загрузки';
								|en = 'Incorrect import file format'");
		ВызватьИсключение ОписаниеОшибки;
		
	КонецЕсли;
	
	ОписаниеВерсии = Метаданные.Имя + " " + Метаданные.Версия;
	Если ЧтениеXML.ЗначениеАтрибута("Version") <> ОписаниеВерсии Тогда
		
		ОписаниеОшибки = Нстр("ru = 'Версия конфигурации выгрузки модели не совпадает с текущей версией конфигурации';
								|en = 'The version of the model export configuration does not match the current configuration version'");
		ВызватьИсключение ОписаниеОшибки;
		
	КонецЕсли;
	
	Если ЧтениеXML.ЗначениеАтрибута("ModelType") <> ТипЗагружаемойМодели Тогда
		
		ОписаниеОшибки = Нстр("ru = 'Неверный тип загружаемой модели';
								|en = 'Incorrect type of model to import'");
		ВызватьИсключение ОписаниеОшибки;
		
	КонецЕсли;

	Если Не ЧтениеXML.Прочитать()
		Или ЧтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента
		Или ЧтениеXML.ЛокальноеИмя <> "Data" Тогда
		
		ОписаниеОшибки = Нстр("ru = 'Неверный формат файла выгрузки';
								|en = 'Incorrect export file format'");
		ВызватьИсключение ОписаниеОшибки;
		
	КонецЕсли;
	
	НеЗаписываемыеУИН = Новый Массив;
	Если ЭтоМодельБюджетирования Тогда
		ОбработатьПредопределенныеДанные(ЧтениеXML, Параметры.ЗаменяемыеУИН, НеЗаписываемыеУИН);
		ЗаменитьСсылкиВФайле(ИмяВременногоФайлаДанных, Параметры.ЗаменяемыеУИН);
		
		Для Каждого Строка Из Параметры.ЗаменаЗначенийАналитик Цикл
			НеЗаписываемыеУИН.Добавить(XMLСтрока(Строка.АналитикаБД));
		КонецЦикла;
			
		Для Каждого Строка Из Параметры.ЗаменяемыеУИН Цикл
			Если Параметры.ЗагружаемыеУИН.Найти(Строка.СсылкаЗамены) = Неопределено Тогда
				НеЗаписываемыеУИН.Добавить(Строка.СсылкаЗамены);
			КонецЕсли;
		КонецЦикла;
	Иначе
		ТаблицаЗаменяемыхУИН = ТаблицаЗаменяемыхУИН();//ТаблицаЗначений
		ОбработатьПредопределенныеДанные(ЧтениеXML, ТаблицаЗаменяемыхУИН, НеЗаписываемыеУИН);
		ЗаменитьСсылкиВФайле(ИмяВременногоФайлаДанных, ТаблицаЗаменяемыхУИН);
	КонецЕсли;
	
	ЧтениеXML.ОткрытьФайл(ИмяВременногоФайлаДанных);
	ЧтениеXML.Прочитать();
	ЧтениеXML.Прочитать();
	
	// чтение и запись в ИБ записанных в выгрузке объектов
	Если Не ЧтениеXML.Прочитать() Тогда 
		
		ОписаниеОшибки = Нстр("ru = 'Нарушен формат файла после замены предопределенных ссылок';
								|en = 'File format is violated after replacing predefined links'");
		ВызватьИсключение ОписаниеОшибки;
		
	КонецЕсли;
	
	Сериализатор = СериализаторXDTOСАннотациейТипов();
	
	РазделениеВключено = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаВМоделиСервиса") Тогда
		МодульРаботаВМоделиСервисаПовтИсп = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервисаПовтИсп");
		МодульМиграцияПриложенийПовтИсп = ОбщегоНазначения.ОбщийМодуль("МиграцияПриложенийПовтИсп");
		
		РазделениеВключено = МодульРаботаВМоделиСервисаПовтИсп.РазделениеВключено();
		Если РазделениеВключено Тогда
			РазделенныеОбъектыМетаданных = МодульМиграцияПриложенийПовтИсп.МодельДанныхОбласти();
		КонецЕсли;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Если ЭтоМодельБюджетирования Тогда
			ВыбранныеВидыБюджета = Параметры.ВыбранныеВидыБюджета;
			Для Каждого ВыбранныйВидБюджета Из ВыбранныеВидыБюджета Цикл
				
				СтрокаЗамены = Параметры.ЗаменяемыеУИН.Найти(ВыбранныйВидБюджета, "СсылкаПоиска");
				Если СтрокаЗамены = Неопределено Тогда
					ВидБюджетаУИН = ВыбранныйВидБюджета;
				Иначе
					ВидБюджетаУИН = СтрокаЗамены.СсылкаЗамены;
				КонецЕсли;
				
				ВидБюджетаУИД = Новый УникальныйИдентификатор(ВидБюджетаУИН);
				НайденныйВидБюджета = Справочники.ВидыБюджетов.ПолучитьСсылку(ВидБюджетаУИД);
				Если ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НайденныйВидБюджета, "Наименование")) Тогда
					//@skip-check query-in-loop - необходимо для каждого вида бюджета перед записью
					УдалитьЭлементыФинансовыхОтчетов(НайденныйВидБюджета);
				
					НаборЗаписей = РегистрыСведений.КэшВспомогательныхДанныхВидаБюджета.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.ВидБюджета.Установить(НайденныйВидБюджета);
					НаборЗаписей.Записать(Истина);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
			
		Пока Сериализатор.ВозможностьЧтенияXML(ЧтениеXML) Цикл
			
			ТипЗагружаемогоОбъекта = ЧтениеXML.ЛокальноеИмя;
			
			ЗаписанноеЗначение = Сериализатор.ПрочитатьXML(ЧтениеXML); //СправочникОбъект
			ЗаписанноеЗначение.ОбменДанными.Загрузка = Истина;
			
			Если РазделениеВключено Тогда
				Если РазделенныеОбъектыМетаданных[ЗаписанноеЗначение.Метаданные()] = Неопределено Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;

			Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(ЗаписанноеЗначение, "Ссылка") Тогда
				
				Если ЗаписанноеЗначение.ЭтоНовый() Тогда
					ИскомаяСтрокаXML = XMLСтрока(ЗаписанноеЗначение.ПолучитьСсылкуНового());
				Иначе
					ИскомаяСтрокаXML = XMLСтрока(ЗаписанноеЗначение.Ссылка);
				КонецЕсли;

				НайденнаяСтрока = НеЗаписываемыеУИН.Найти(ИскомаяСтрокаXML);
				Если НайденнаяСтрока <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
			
			КонецЕсли;
			
			Записывать = Истина;
			Если ЭтоМодельБюджетирования Тогда
				Если СтрНайти(ТипЗагружаемогоОбъекта, "InformationRegisterRecordSet") <> 0 Тогда
					Для Каждого Отбор Из ЗаписанноеЗначение.Отбор Цикл
						Если Параметры.ЗагружаемыеУИН.Найти(XMLСтрока(Отбор.Значение)) = Неопределено Тогда
							Записывать = Ложь;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				Иначе
					Если ЗаписанноеЗначение.ЭтоНовый() Тогда
						ИскомаяСтрокаXML = XMLСтрока(ЗаписанноеЗначение.ПолучитьСсылкуНового());
					Иначе
						ИскомаяСтрокаXML = XMLСтрока(ЗаписанноеЗначение.Ссылка);
					КонецЕсли;
					Если Параметры.ЗагружаемыеУИН.Найти(ИскомаяСтрокаXML) = Неопределено Тогда
						Записывать = Ложь;
					КонецЕсли;
				КонецЕсли;
				
				Если Записывать Тогда
					Если ТипЗнч(ЗаписанноеЗначение) = Тип("СправочникОбъект.ЭлементыФинансовыхОтчетов") Тогда
						РеквизитХранилище = ЗаписанноеЗначение.ДополнительныйОтбор;
						ЗаменаСсылокВНастройкахКД(РеквизитХранилище, Параметры);
						ЗаписанноеЗначение.ДополнительныйОтбор = РеквизитХранилище;
					ИначеЕсли ТипЗнч(ЗаписанноеЗначение) = Тип("СправочникОбъект.ПравилаПолученияФактаПоПоказателямБюджетов")
						Или ТипЗнч(ЗаписанноеЗначение) = Тип("СправочникОбъект.ПравилаПолученияФактаПоСтатьямБюджетов") Тогда
							РеквизитХранилище = ЗаписанноеЗначение.КомпоновщикНастроек;
							ЗаменаСсылокВНастройкахКД(РеквизитХранилище, Параметры);
							ЗаписанноеЗначение.КомпоновщикНастроек = РеквизитХранилище;
					ИначеЕсли ТипЗнч(ЗаписанноеЗначение) = Тип("СправочникОбъект.ШаблоныВводаНефинансовыхПоказателей") Тогда
						Для Каждого Стр Из ЗаписанноеЗначение.ПоказателиШаблона Цикл
							РеквизитХранилище = Стр.ХранилищеНастроекКомпоновкиДанных;
							ЗаменаСсылокВНастройкахКД(РеквизитХранилище, Параметры);
							Стр.ХранилищеНастроекКомпоновкиДанных = РеквизитХранилище;
						КонецЦикла;
					ИначеЕсли ТипЗнч(ЗаписанноеЗначение) = Тип("СправочникОбъект.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов") Тогда
						РеквизитХранилище = ЗаписанноеЗначение.ХранилищеЗапроса;
						ЗаменаСсылокВНастройкахКД(РеквизитХранилище, Параметры);
						ЗаписанноеЗначение.ХранилищеЗапроса = РеквизитХранилище;
						Для Каждого Стр Из ЗаписанноеЗначение.НастройкиРасчетаДанных Цикл
							РеквизитХранилище = Стр.КомпоновщикНастроек;
							ЗаменаСсылокВНастройкахКД(РеквизитХранилище, Параметры);
							Стр.КомпоновщикНастроек = РеквизитХранилище;
							РеквизитХранилище = Стр.ХранилищеСхемыКомпоновкиДанных;
							ЗаменаСсылокВНастройкахКД(РеквизитХранилище, Параметры);
							Стр.ХранилищеСхемыКомпоновкиДанных = РеквизитХранилище;
							РеквизитХранилище = Стр.ХранилищеНастроекКомпоновкиДанных;
							ЗаменаСсылокВНастройкахКД(РеквизитХранилище, Параметры);
							Стр.ХранилищеНастроекКомпоновкиДанных = РеквизитХранилище;
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
			Если Записывать Тогда
				
				ЗаписанноеЗначение.Записать();
				
				Если ТипЗнч(ЗаписанноеЗначение) = Тип("СправочникОбъект.ВидыБюджетов") Тогда
					НаборЗаписей = РегистрыСведений.КэшВспомогательныхДанныхВидаБюджета.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.ВидБюджета.Установить(ЗаписанноеЗначение.Ссылка);
					НаборЗаписей.Записать(Истина);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		// проверка формата файла обмена
		Если ЧтениеXML.ТипУзла <> ТипУзлаXML.КонецЭлемента
			Или ЧтениеXML.ЛокальноеИмя <> "Data" Тогда
			
			ОписаниеОшибки = Нстр("ru = 'Неверный формат файла выгрузки';
									|en = 'Incorrect export file format'");
			ВызватьИсключение ОписаниеОшибки;
			
		КонецЕсли;
		
		Если Не ЧтениеXML.Прочитать()
			Или ЧтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента
			Или ЧтениеXML.ЛокальноеИмя <> "PredefinedData" Тогда
			
			ОписаниеОшибки = Нстр("ru = 'Неверный формат файла выгрузки';
									|en = 'Incorrect export file format'");
			ВызватьИсключение ОписаниеОшибки;
			
		КонецЕсли;
		
		ЧтениеXML.Пропустить();
		
		Если ЭтоМодельБюджетирования Тогда
			ЧтениеXML.Пропустить();
			ЧтениеXML.Пропустить();
		КонецЕсли;
		
		Если Не ЧтениеXML.Прочитать()
			Или ЧтениеXML.ТипУзла <> ТипУзлаXML.КонецЭлемента
			Или ЧтениеXML.ЛокальноеИмя <> "_1CV8DtUD"
			Или ЧтениеXML.URIПространстваИмен <> "http://www.1c.ru/V8/1CV8DtUD/" Тогда
			
			ОписаниеОшибки = Нстр("ru = 'Неверный формат файла выгрузки';
									|en = 'Incorrect export file format'");
			ВызватьИсключение ОписаниеОшибки;
			
		КонецЕсли;
		
		ЧтениеXML.Закрыть();
		УдалитьФайлы(ИмяВременногоФайлаДанных);
		
		Если Параметры.Свойство("ЭтоМодельМеждународногоУчета") И Параметры.ЭтоМодельМеждународногоУчета Тогда
			
			ОбработатьФункциональныеОпции();
			
			СтруктураРезультата = Новый Структура();
			ПоместитьВоВременноеХранилище(СтруктураРезультата, Параметры.АдресРезультата);
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ЧтениеXML.Закрыть();
		УдалитьФайлы(ИмяВременногоФайлаДанных);
		
		МассивСтрокОшибки = Новый Массив;
		МассивСтрокОшибки.Добавить(Нстр("ru = 'Возможно структура загружаемых данных не соответствует конфигурации, проверьте актуальность файла данных';
										|en = 'Structure of the data to import might not correspond to the application. Check the data file relevance'"));
		МассивСтрокОшибки.Добавить(ОписаниеОшибки());
		ТекстОшибки = СтрСоединить(МассивСтрокОшибки, Символы.ПС);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецПопытки;
	
КонецПроцедуры

// Выполняет выгрузку модели международного учета в файла
//
// Параметры:
// 	Параметры - Структура - Структура параметров
// 	ИмяФайлаНаСервере - Строка - Имя файла на сервере
//
Процедура ВыгрузитьМодельУчета(Параметры, ИмяФайлаНаСервере = Неопределено) Экспорт
	
	ТипВыгружаемойМодели = ТипМодели(Параметры);
	Если Параметры.Свойство("ЭтоМодельБюджетирования") И Параметры.ЭтоМодельБюджетирования Тогда
		ЭтоМодельБюджетирования = Истина;
	Иначе
		ЭтоМодельБюджетирования = Ложь;
	КонецЕсли;
	
	Если ИмяФайлаНаСервере = Неопределено Тогда
		ИмяФайла = ПолучитьИмяВременногоФайла("xml");
	Иначе
		ИмяФайла = ИмяФайлаНаСервере;
	КонецЕсли;
	
	СоставВыгрузки = СоставВыгрузки(Параметры.ДанныеВыгрузки);
	
	Если СоставВыгрузки.Количество() = 0 Тогда
		
		ОписаниеОшибки = Нстр("ru = 'Не задан состав выгрузки';
								|en = 'Export is not specified'");
		ВызватьИсключение ОписаниеОшибки;
		
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайла, "UTF-8");
		
	ЗаписьXML.ЗаписатьОбъявлениеXML();	
	ЗаписьXML.ЗаписатьНачалоЭлемента("_1CV8DtUD", "http://www.1c.ru/V8/1CV8DtUD/");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("V8Exch", "http://www.1c.ru/V8/1CV8DtUD/");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xsi", "http://www.w3.org/2001/XMLSchema-instance");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("core", "http://v8.1c.ru/data");
	
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("v8", "http://v8.1c.ru/8.1/data/enterprise/current-config");
	ЗаписьXML.ЗаписатьСоответствиеПространстваИмен("xs", "http://www.w3.org/2001/XMLSchema");
	
	ЗаписьXML.ЗаписатьАтрибут("Version", Метаданные.Имя + " " + Метаданные.Версия);
	ЗаписьXML.ЗаписатьАтрибут("ModelType", ТипВыгружаемойМодели);

	ЗаписьXML.ЗаписатьНачалоЭлемента("Data");
	
	Сериализатор = СериализаторXDTOСАннотациейТипов();
	ТаблицаПредопределенных = ТаблицаПредопределенныхДанных();
	ТаблицаФильтрации = ТаблицаФильтрации();

	ВыгрузкаДанных(Сериализатор, ЗаписьXML, СоставВыгрузки, ТаблицаПредопределенных, ТаблицаФильтрации, ЭтоМодельБюджетирования);
	
	ТаблицаФильтрации.Свернуть("ИмяТаблицы, Ссылка, СсылкаВладелец, ПредставлениеСсылка, ПредставлениеСсылкаВладелец");
	
	ЗаписьXML.ЗаписатьКонецЭлемента(); //V8Exc:Data
	ВыгрузитьТаблицуПредопределенных(ТаблицаПредопределенных, ЗаписьXML);
	
	Если ЭтоМодельБюджетирования Тогда
		ВыгрузитьТаблицуФильтрации(ТаблицаФильтрации, ЗаписьXML);
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьКонецЭлемента(); //V8Exc:_1CV8DtUD 
	
	ЗаписьXML.Закрыть();
	
	Если ИмяФайлаНаСервере = Неопределено Тогда
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайла);
		ПоместитьВоВременноеХранилище(ДвоичныеДанные, Параметры.АдресХранилища);
	
		УдалитьФайлы(ИмяФайла);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Таблица данных к выгрузке.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Описание:
//   * ОбъектМД - ОбъектМетаданных - объект метаданных для выгрузки
//   * РезультатЗапроса - РезультатЗапроса - результат запроса на выгрузку ссылок
//
Функция ТаблицаДанныеВыгрузки() Экспорт
	
	ДанныеВыгрузки = Новый ТаблицаЗначений;
	ДанныеВыгрузки.Колонки.Добавить("ИмяМД");
	ДанныеВыгрузки.Колонки.Добавить("ТекстЗапроса");
	ДанныеВыгрузки.Колонки.Добавить("Владелец"); 
	ДанныеВыгрузки.Колонки.Добавить("МассивОтбора"); 
	ДанныеВыгрузки.Колонки.Добавить("ПолеОтбора"); 
	
	Возврат ДанныеВыгрузки;
	
КонецФункции

// Добавляет строку в данные выгрузки с учетом отбора.
// 
// Параметры:
//  ДанныеВыгрузки - ТаблицаЗначений - Данные выгрузки:
//   * ОбъектМД - ОбъектМетаданных - объект метаданных для выгрузки
//   * РезультатЗапроса - РезультатЗапроса - результат запроса на выгрузку ссылок
//  ОбъектМД - ОбъектМетаданныхСправочник, ОбъектМетаданныхПланВидовХарактеристик, ОбъектМетаданныхРегистрСведений, ОбъектМетаданныхПланСчетов - Объект МД
//  Владелец - ЛюбаяСсылка - Ссылка на владельца фильтра загрузки
//  ОтборПоСсылкам - Неопределено, Массив из ЛюбаяСсылка - Отбор по ссылкам
//  ПолеОтбора - Строка - Поле отбора для отбора
Процедура ДобавитьСтрокуВДанныеВыгрузки(ДанныеВыгрузки, ОбъектМД, Владелец = Неопределено, ОтборПоСсылкам = Неопределено, ПолеОтбора = "") Экспорт
	
	ЕстьОтборПоСсылкам = ОтборПоСсылкам <> Неопределено;
	
	ТекстЗапроса = ПолучитьТекстЗапроса(ОбъектМД, ЕстьОтборПоСсылкам, ПолеОтбора);
	
	НоваяСтрока = ДанныеВыгрузки.Добавить();
	НоваяСтрока.ИмяМД = ОбъектМД.ПолноеИмя();
	НоваяСтрока.ТекстЗапроса = ТекстЗапроса;
	НоваяСтрока.Владелец = Владелец;
	НоваяСтрока.МассивОтбора = ОтборПоСсылкам;
	НоваяСтрока.ПолеОтбора = ПолеОтбора;
	
КонецПроцедуры

// Получает имя измерения для отбора в регистре
// 
// Параметры:
//  ОбъектМДОтбора - ОбъектМетаданных - Объект метаданных в котором ищем тип
//  ОбъектМД - ОбъектМетаданных - Объект метаданных поиска
// 
// Возвращаемое значение:
//  Строка - Имя измерения для отбора
//
Функция ИмяИзмеренияДляОтбора(ОбъектМДОтбора, ОбъектМД) Экспорт
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ОбъектМДОтбора.ПолноеИмя());
	ИскомыйТип = ТипЗнч(МенеджерОбъекта.ПолучитьСсылку());
		
	ИзмеренияОбъектаМД = ОбъектМД.Измерения;
	Для Каждого Измерение Из ИзмеренияОбъектаМД Цикл
		Если Измерение.Тип.СодержитТип(ИскомыйТип) Тогда
			Возврат Измерение.Имя;
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТипМодели(Параметры)
	
	ТипМодели = "";
	
	Если Параметры.Свойство("ЭтоМодельБюджетирования") И Параметры.ЭтоМодельБюджетирования Тогда
		ТипМодели = "Budgeting Model";
	ИначеЕсли Параметры.Свойство("ЭтоМодельМеждународногоУчета") И Параметры.ЭтоМодельМеждународногоУчета Тогда
		ТипМодели = "International Accounting Model";
	Иначе 
		ОписаниеОшибки = Нстр("ru = 'Не определен тип модели';
								|en = 'Model type is not determined'");
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
	
	Возврат ТипМодели;
	
КонецФункции

Функция ТипыАналитикБюджетирования()
	
	ТипыАналитик = БюджетированиеСервер.ВсеТипыАналитик().Типы();
	
	ТипыАналитик.Добавить(Тип("СправочникСсылка.СценарииТоварногоПланирования"));
	ТипыАналитик.Добавить(Тип("СправочникСсылка.ВидыПланов"));
	
	Возврат ТипыАналитик;
	
КонецФункции

Процедура ВыгрузкаДанных(Сериализатор, ЗаписьXML, СоставВыгрузки, ТаблицаПредопределенных, ТаблицаФильтрации, ЭтоМодельБюджетирования)
	
	ВыгруженныеОбъекты = Новый ТаблицаЗначений;
	ВыгруженныеОбъекты.Колонки.Добавить("Ссылка");
	ВыгруженныеОбъекты.Индексы.Добавить("Ссылка");
	
	ТипыАналитикБюджетирования = ТипыАналитикБюджетирования();
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЭтоМодельБюджетирования", ЭтоМодельБюджетирования);
	ДополнительныеПараметры.Вставить("ТаблицаПредопределенных", ТаблицаПредопределенных);
	ДополнительныеПараметры.Вставить("ВыгруженныеОбъекты", ВыгруженныеОбъекты);
	ДополнительныеПараметры.Вставить("ТаблицаФильтрации", ТаблицаФильтрации);
	ДополнительныеПараметры.Вставить("ТипыАналитикБюджетирования", ТипыАналитикБюджетирования);
	
	Попытка
		
		Для Каждого СтрокаВыгрузки Из СоставВыгрузки Цикл
			
			Если ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(СтрокаВыгрузки.ОбъектМД.ПолноеИмя()) = Неопределено Тогда
				ОписаниеОшибки = Нстр("ru = 'Выгрузка данных. Внутренняя ошибка';
										|en = 'Data export. Internal error'");
				ВызватьИсключение ОписаниеОшибки;
			КонецЕсли;
			
			Если Метаданные.РегистрыСведений.Содержит(СтрокаВыгрузки.ОбъектМД) Тогда
				ЗаписьРегистра(СтрокаВыгрузки, Сериализатор, ЗаписьXML, ДополнительныеПараметры);
			Иначе
				ЗаписьДанныхОбъектногоТипа(СтрокаВыгрузки, Сериализатор, ЗаписьXML, ДополнительныеПараметры);
			КонецЕсли;
		
		КонецЦикла;
		
	Исключение
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗаписьДанныхОбъектногоТипа(СтрокаВыгрузки, Сериализатор, ЗаписьXML, ДополнительныеПараметры)
	ЗапросИЗапись(СтрокаВыгрузки.РезультатЗапроса, СтрокаВыгрузки.Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры);
КонецПроцедуры

Процедура ЗапросИЗапись(РезультатЗапроса, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры)
	ОбработкаРезультатаЗапроса(РезультатЗапроса, Владелец, Сериализатор, ЗаписьXML, Истина, ДополнительныеПараметры);
КонецПроцедуры

Процедура ОбработкаРезультатаЗапроса(РезультатЗапроса, Владелец, Сериализатор, ЗаписьXML, ЭтоЗапросПоОбъекту, ДополнительныеПараметры)
	
	ЭтоМодельБюджетирования = ДополнительныеПараметры.ЭтоМодельБюджетирования;
	ТаблицаПредопределенных = ДополнительныеПараметры.ТаблицаПредопределенных;
	ВыгруженныеОбъекты = ДополнительныеПараметры.ВыгруженныеОбъекты;
	ТаблицаФильтрации = ДополнительныеПараметры.ТаблицаФильтрации;
	ТипыАналитикБюджетирования = ДополнительныеПараметры.ТипыАналитикБюджетирования;
	
	ВыборкаИзРезультатовЗапроса = РезультатЗапроса.Выбрать(); 
	Пока ВыборкаИзРезультатовЗапроса.Следующий() Цикл
		
		Если ЭтоЗапросПоОбъекту Тогда
			
			// Выгрузка ссылочных объектов
			Ссылка = ВыборкаИзРезультатовЗапроса.Ссылка;
			Объект = Ссылка.ПолучитьОбъект();
			МетаданныеОбъекта = Объект.Метаданные();
			
			Если ЭтоМодельБюджетирования Тогда
				ДобавитьСтрокуТаблицыФильтрации(ТаблицаФильтрации, МетаданныеОбъекта.ПолноеИмя(), Ссылка, Владелец); //МетаданныеОбъекта.ПолноеИмя()
			КонецЕсли;
			
			Если СсылкаВыгружена(ВыгруженныеОбъекты, Ссылка) Тогда
				Продолжить;
			КонецЕсли;
			
			ДобавитьСсылкуКВыгруженным(ВыгруженныеОбъекты, Ссылка);
			
		КонецЕсли;
		
		Если ЭтоМодельБюджетирования И ТипыАналитикБюджетирования.Найти(ТипЗнч(Ссылка)) = Неопределено Тогда
			
			// Перебираем колонки запроса в поисках ссылочных значений, которые, возможно, нужно выгрузить
			Для Каждого КолонкаЗапроса Из РезультатЗапроса.Колонки Цикл
				
				ЗначениеКолонки = ВыборкаИзРезультатовЗапроса[КолонкаЗапроса.Имя];
				Если ТипЗнч(ЗначениеКолонки) = Тип("РезультатЗапроса") Тогда
					//@skip-check query-in-loop - выборка ссылочных данных для записи необходимых для ссылочной целостности
					ОбработкаРезультатаЗапроса(ЗначениеКолонки, Владелец, Сериализатор, ЗаписьXML, Ложь, ДополнительныеПараметры);
				Иначе
					Если ТипЗнч(ЗначениеКолонки) = Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения") Тогда
						Продолжить;
					КонецЕсли;
					//@skip-check query-in-loop - запись данных необходимых для ссылочной целостности
					ЗаписатьЗначениеПриНеобходимости(ЗначениеКолонки, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры);
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Если ЭтоЗапросПоОбъекту Тогда
			
			Объект = Ссылка.ПолучитьОбъект();
			
			Попытка
				
				Сериализатор.ЗаписатьXML(ЗаписьXML, Объект);
				
				МетаданныеОбъекта = Объект.Метаданные();
				
				Если ЭтоМетаданныеСПредопределеннымиЭлементами(МетаданныеОбъекта) И Объект.Предопределенный Тогда
					
					ДобавитьСтрокуТаблицыПредопределенных(
							ТаблицаПредопределенных,
							МетаданныеОбъекта.ПолноеИмя(),
							XMLСтрока(Ссылка),
							"ИмяПредопределенныхДанных",
							Объект.ИмяПредопределенныхДанных);
					
				ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.Валюты") Тогда
					
					ДобавитьСтрокуТаблицыПредопределенных(
							ТаблицаПредопределенных,
							МетаданныеОбъекта.ПолноеИмя(),
							XMLСтрока(Ссылка),
							Объект.Метаданные().СтандартныеРеквизиты.Код.Имя,
							Объект.Код);
				
				ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.ИдентификаторыОбъектовМетаданных") Тогда
					
					ДобавитьСтрокуТаблицыПредопределенных(
							ТаблицаПредопределенных,
							МетаданныеОбъекта.ПолноеИмя(),
							XMLСтрока(Ссылка),
							Объект.Метаданные().СтандартныеРеквизиты.Наименование.Имя,
							Объект.Наименование);
					
				КонецЕсли;

			Исключение
				
				ШаблонОшибки = Нстр("ru = 'При выгрузке объекта %1(%2) возникла ошибка: |%3';
									|en = 'An error occurred when exporting the %1(%2) object: |%3'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, Объект, ТипЗнч(Объект), ОписаниеОшибки());
				ВызватьИсключение ТекстОшибки;
				
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьЗначениеПриНеобходимости(АнализируемоеЗначение, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры)
	
	ЭтоМодельБюджетирования = ДополнительныеПараметры.ЭтоМодельБюджетирования;
	ВыгруженныеОбъекты = ДополнительныеПараметры.ВыгруженныеОбъекты;
	ТаблицаФильтрации = ДополнительныеПараметры.ТаблицаФильтрации;
	ТипыАналитикБюджетирования = ДополнительныеПараметры.ТипыАналитикБюджетирования;
	
	Если Не ЗначениеЗаполнено(АнализируемоеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ТипЗначения = ТипЗнч(АнализируемоеЗначение);
	
	Если ТипЗначения = Тип("ХранилищеЗначения") Тогда
		ВыгрузкаСсылокИзНастроекКД(АнализируемоеЗначение, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры);
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ЭтоСсылка(ТипЗначения) Тогда
		Возврат; // это не ссылка
	КонецЕсли;
	
	ОбъектМД = Метаданные.НайтиПоТипу(ТипЗначения);
	
	Если ОбщегоНазначения.ЭтоПеречисление(ОбъектМД) Тогда
		Возврат; // это перечисление
	КонецЕсли;
	
	Если СсылкаВыгружена(ВыгруженныеОбъекты, АнализируемоеЗначение) Тогда
		Возврат; // объект уже был выгружен
	КонецЕсли;
	
	Если ЭтоМодельБюджетирования И ТипыАналитикБюджетирования.Найти(ТипЗнч(АнализируемоеЗначение)) <> Неопределено Тогда
		
		ДобавитьСтрокуТаблицыФильтрации(ТаблицаФильтрации, ОбъектМД.ПолноеИмя(), АнализируемоеЗначение, Владелец);
		
		ТекстПодзапроса = "ВЫБРАТЬ ТаблицаОбъекта.Ссылка ИЗ #Таблица КАК ТаблицаОбъекта ГДЕ Ссылка = &Ссылка";
		
	Иначе
		ТекстПодзапроса = "ВЫБРАТЬ * ИЗ #Таблица КАК ТаблицаОбъекта ГДЕ Ссылка = &Ссылка";
	КонецЕсли;
	
	ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "#Таблица", ОбъектМД.ПолноеИмя());
	
	ДопЗапрос = Новый Запрос;
	ДопЗапрос.Текст = ТекстПодзапроса;
	ДопЗапрос.УстановитьПараметр("Ссылка", АнализируемоеЗначение);
	РезультатЗапроса = ДопЗапрос.Выполнить();
	
	ЗапросИЗапись(РезультатЗапроса, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ЗаписьРегистра(СтрокаВыгрузки, Сериализатор, ЗаписьXML, ДополнительныеПараметры)
	
	ОбъектМД = СтрокаВыгрузки.ОбъектМД;
	РезультатЗапроса = СтрокаВыгрузки.РезультатЗапроса;
	Владелец = СтрокаВыгрузки.Владелец;

	ЗаписьЧерезНаборЗаписей(ОбъектМД, РезультатЗапроса, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ЗаписьЧерезНаборЗаписей(ОбъектМД, РезультатЗапроса, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры)
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ОбъектМД.ПолноеИмя());
	НаборЗаписей = МенеджерОбъекта.СоздатьНаборЗаписей();
	Отбор = НаборЗаписей.Отбор;
	
	КоллекцияКолонок = РезультатЗапроса.Колонки; // КоллекцияКолонокРезультатаЗапроса -
	
	ВыборкаИзРезультата = РезультатЗапроса.Выбрать();
	Пока ВыборкаИзРезультата.Следующий() Цикл
		
		// Отбор устанавливаем для регистров, у которых есть хотя бы один отбор (измерение)
		Для Каждого Колонка Из КоллекцияКолонок Цикл 
			Отбор[Колонка.Имя].Значение = ВыборкаИзРезультата[Колонка.Имя];
			Отбор[Колонка.Имя].ВидСравнения = ВидСравнения.Равно;
			Отбор[Колонка.Имя].Использование = Истина;
		КонецЦикла;
			
		НаборЗаписей.Прочитать();
		
		// проверяем все записанные в наборе значения на необходимость записи "по ссылке"
		//@skip-check query-in-loop - запись данных необходимых для ссылочной целостности
		ВыгрузитьПодчиненныеЗначенияНабора(Сериализатор, ЗаписьXML, НаборЗаписей, Владелец, КоллекцияКолонок, ДополнительныеПараметры);
		
		Попытка
			Сериализатор.ЗаписатьXML(ЗаписьXML, НаборЗаписей);
		Исключение
			ВызватьИсключение ОписаниеОшибки();
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыгрузитьПодчиненныеЗначенияНабора(Сериализатор, ЗаписьXML, НаборЗаписей, Владелец, МассивКолонок, ДополнительныеПараметры)
	
	ЭтоМодельБюджетирования = ДополнительныеПараметры.ЭтоМодельБюджетирования;
	ТипыАналитикБюджетирования = ДополнительныеПараметры.ТипыАналитикБюджетирования;
	
	Для Каждого ЗаписьИзНабора Из НаборЗаписей Цикл
		
		Для Каждого Колонка Из МассивКолонок Цикл //КолонкаРезультатаЗапроса
			
			СохраненноеЗначение = ЗаписьИзНабора[Колонка.Имя]; // КоллекцияКолонокРезультатаЗапроса -
			
			Если ЭтоМодельБюджетирования И ТипыАналитикБюджетирования.Найти(ТипЗнч(СохраненноеЗначение)) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			//@skip-check query-in-loop - запись данных необходимых для ссылочной целостности
			ЗаписатьЗначениеПриНеобходимости(СохраненноеЗначение, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СоставВыгрузки(ДанныеВыгрузки)
	
	СоставВыгрузки = Новый ТаблицаЗначений;
	СоставВыгрузки.Колонки.Добавить("ОбъектМД");
	СоставВыгрузки.Колонки.Добавить("Владелец");
	СоставВыгрузки.Колонки.Добавить("РезультатЗапроса");
	
	Для Каждого Строка из ДанныеВыгрузки Цикл
		
		ЕстьОтборПоСсылкам = Строка.МассивОтбора <> Неопределено;
		
		Запрос = Новый Запрос;
		Запрос.Текст = Строка.ТекстЗапроса;
		Если ЕстьОтборПоСсылкам Тогда
			Запрос.УстановитьПараметр("МассивСсылок", Строка.МассивОтбора);
		КонецЕсли;
		//@skip-check query-in-loop - необходимо для формировании выборка по каждому типу данных
		РезультатЗапроса = Запрос.Выполнить();
		
		НоваяСтрока = СоставВыгрузки.Добавить();
		НоваяСтрока.ОбъектМД = Метаданные.НайтиПоПолномуИмени(Строка.ИмяМД);
		НоваяСтрока.Владелец = Строка.Владелец;
		НоваяСтрока.РезультатЗапроса = РезультатЗапроса;
		
	КонецЦикла;
	
	Возврат СоставВыгрузки;
	
КонецФункции

Функция ТаблицаФильтрации()
	
	ТаблицаФильтрации = Новый ТаблицаЗначений;
	ТаблицаФильтрации.Колонки.Добавить("ИмяТаблицы");
	ТаблицаФильтрации.Колонки.Добавить("Ссылка");
	ТаблицаФильтрации.Колонки.Добавить("СсылкаВладелец");
	ТаблицаФильтрации.Колонки.Добавить("ПредставлениеСсылка");
	ТаблицаФильтрации.Колонки.Добавить("ПредставлениеСсылкаВладелец");
	
	Возврат ТаблицаФильтрации;
	
КонецФункции

Процедура ДобавитьСтрокуТаблицыФильтрации(ТаблицаФильтрации, ИмяТаблицыПоиска, СсылкаНаЭлемент, СсылкаВладелец)
	
	НоваяСтрока = ТаблицаФильтрации.Добавить();
	НоваяСтрока.ИмяТаблицы = ИмяТаблицыПоиска;
	НоваяСтрока.Ссылка = XMLСтрока(СсылкаНаЭлемент);
	НоваяСтрока.СсылкаВладелец = XMLСтрока(СсылкаВладелец);
	НоваяСтрока.ПредставлениеСсылка = СсылкаНаЭлемент;
	НоваяСтрока.ПредставлениеСсылкаВладелец = СсылкаВладелец;

КонецПроцедуры

Процедура ВыгрузитьТаблицуФильтрации(ТаблицаФильтрации, ЗаписьXML)
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("FilterData");
	
	Если ТаблицаФильтрации.Количество() > 0 Тогда
		
		Для Каждого Элемент Из ТаблицаФильтрации Цикл
			
			ЗаписьXML.ЗаписатьНачалоЭлемента("itemFilterData");
			ЗаписьXML.ЗаписатьАтрибут("ИмяТаблицы", Элемент.ИмяТаблицы);
			ЗаписьXML.ЗаписатьАтрибут("Ссылка", Элемент.Ссылка);
			ЗаписьXML.ЗаписатьАтрибут("СсылкаВладелец", Элемент.СсылкаВладелец);
			ЗаписьXML.ЗаписатьКонецЭлемента();
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Функция ТаблицаЗаменяемыхУИН()
	
	ТаблицаЗаменяемыхУИН = Новый ТаблицаЗначений;
	ТаблицаЗаменяемыхУИН.Колонки.Добавить("СсылкаПоиска");
	ТаблицаЗаменяемыхУИН.Колонки.Добавить("СсылкаЗамены");
	
	Возврат ТаблицаЗаменяемыхУИН;
	
КонецФункции

Функция ТаблицаПредопределенныхДанных()
	
	ТаблицаПредопределенных = Новый ТаблицаЗначений;
	ТаблицаПредопределенных.Колонки.Добавить("ИмяТаблицы");
	ТаблицаПредопределенных.Колонки.Добавить("Ссылка");
	ТаблицаПредопределенных.Колонки.Добавить("ПолеПоиска");
	ТаблицаПредопределенных.Колонки.Добавить("ЗначениеПоиска");
	
	Возврат ТаблицаПредопределенных;
	
КонецФункции

Процедура ДобавитьСтрокуТаблицыПредопределенных(ТаблицаПредопределенных, ИмяТаблицыПоиска, СсылкаНаЭлемент, ПолеПоиска, ЗначениеПоиска)
	
	НоваяСтрока = ТаблицаПредопределенных.Добавить();
	НоваяСтрока.ИмяТаблицы = ИмяТаблицыПоиска;
	НоваяСтрока.Ссылка = СсылкаНаЭлемент;
	НоваяСтрока.ПолеПоиска = ПолеПоиска;
	НоваяСтрока.ЗначениеПоиска = ЗначениеПоиска;
	
КонецПроцедуры

Процедура ВыгрузитьТаблицуПредопределенных(ТаблицаПредопределенных, ЗаписьXML)
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("PredefinedData");
	
	Если ТаблицаПредопределенных.Количество() > 0 Тогда
		
		ТаблицаПредопределенных.Сортировать("ИмяТаблицы");
		
		ИмяПредыдущейТаблицы = "";
		
		Для Каждого Элемент Из ТаблицаПредопределенных Цикл
			
			Если ИмяПредыдущейТаблицы <> Элемент.ИмяТаблицы Тогда
				Если Не ПустаяСтрока(ИмяПредыдущейТаблицы) Тогда
					ЗаписьXML.ЗаписатьКонецЭлемента();
				КонецЕсли;
				ЗаписьXML.ЗаписатьНачалоЭлемента(Элемент.ИмяТаблицы);
			КонецЕсли;
			
			ЗаписьXML.ЗаписатьНачалоЭлемента("itemPredefinedData");
			ЗаписьXML.ЗаписатьАтрибут("Ссылка", Элемент.Ссылка);
			ЗаписьXML.ЗаписатьАтрибут("ПолеПоиска", Элемент.ПолеПоиска);
			ЗаписьXML.ЗаписатьАтрибут("ЗначениеПоиска", Элемент.ЗначениеПоиска);
			ЗаписьXML.ЗаписатьКонецЭлемента();
			
			ИмяПредыдущейТаблицы = Элемент.ИмяТаблицы;
			
		КонецЦикла;
		
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

Процедура ОбработатьПредопределенныеДанные(ЧтениеXML, ТаблицаЗаменяемыхУИН, НеЗаписываемыеУИН)
	
	ТаблицаПредопределенных = ТаблицаПредопределенныхДанных();
	
	ЧтениеXML.Пропустить(); // При первом чтении пропускам основной блок данных
	ЧтениеXML.Прочитать();
	
	ВременнаяСтрока = ТаблицаПредопределенных.Добавить();
	
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Если ЧтениеXML.ЛокальноеИмя <> "itemPredefinedData" Тогда
				ВременнаяСтрока.ИмяТаблицы = ЧтениеXML.ЛокальноеИмя;
				
				ТекстЗапроса = 
				"ВЫБРАТЬ
				|	Таблица.Ссылка КАК Ссылка
				|ИЗ
				|	#Таблица КАК Таблица
				|ГДЕ
				|	&ПолеПоиска = &ЗначениеПоиска";
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Таблица", ВременнаяСтрока.ИмяТаблицы);
			Иначе
				
				Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
					ВременнаяСтрока[ЧтениеXML.ЛокальноеИмя] = ЧтениеXML.Значение;
				КонецЦикла;
				
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеПоиска", "Таблица." + ВременнаяСтрока.ПолеПоиска);
				Запрос = Новый Запрос(ТекстЗапроса);

				Запрос.УстановитьПараметр("ЗначениеПоиска", ВременнаяСтрока.ЗначениеПоиска);
				
				//@skip-check query-in-loop - необходимо для формировании выборка по каждому типу данных
				РезультатЗапроса = Запрос.Выполнить();
				Если РезультатЗапроса.Пустой() Тогда
					
					Если ВременнаяСтрока.ПолеПоиска = "ИмяПредопределенныхДанных" Тогда
						ТекстИсключения = НСтр("ru = 'Не найден предопределнный элемент %1 в таблице %2';
												|en = 'Predefined %1 item is not found in the %2 table'");
						ТекстИсключения = СтрЗаменить(ТекстИсключения, "%1", ВременнаяСтрока.ИмяПредопределенныхДанных);
						ТекстИсключения = СтрЗаменить(ТекстИсключения, "%2", ВременнаяСтрока.ИмяТаблицы);
						
						ВызватьИсключение ТекстИсключения;
					КонецЕсли;
					
				Иначе
					
					Выборка = РезультатЗапроса.Выбрать();
					
					Если Выборка.Количество() = 1 Тогда
						Выборка.Следующий();
						
						СсылкаВБазе = XMLСтрока(Выборка.Ссылка);
						СсылкаВФайле = ВременнаяСтрока.Ссылка;
						
						Если СсылкаВБазе <> СсылкаВФайле Тогда
							НоваяСтрока = ТаблицаЗаменяемыхУИН.Добавить();
							НоваяСтрока.СсылкаПоиска = СсылкаВФайле;
							НоваяСтрока.СсылкаЗамены = СсылкаВБазе;
						КонецЕсли;
						
						НеЗаписываемыеУИН.Добавить(СсылкаВБазе);
						
					Иначе
						ТекстИсключения = НСтр("ru = 'Обнаружено дублирование предопределенных элементов по полю ""%1"" %2 в таблице %3';
												|en = 'Duplicate predefined items are detected in the ""%1"" %2 field in the %3 table'");
						ТекстИсключения = СтрЗаменить(ТекстИсключения, "%1", ВременнаяСтрока.ПолеПоиска);
						ТекстИсключения = СтрЗаменить(ТекстИсключения, "%2", ВременнаяСтрока.ЗначениеПоиска);
						ТекстИсключения = СтрЗаменить(ТекстИсключения, "%3", ВременнаяСтрока.ИмяТаблицы);
						
						ВызватьИсключение ТекстИсключения;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ЧтениеXML.Закрыть();
	
КонецПроцедуры

Процедура ЗаменитьСсылкиВФайле(ИмяФайла, ТаблицаЗаменяемыхУИН)
	
	ПотокЧтения = Новый ЧтениеТекста(ИмяФайла);
	
	ВременныйФайл = ПолучитьИмяВременногоФайла("xml");
	
	ПотокЗаписи = Новый ЗаписьТекста(ВременныйФайл);
	
	// Константы для разбора текста
	НачалоТипа = "xsi:type=""v8:";
	ДлинаНачалаТипа = СтрДлина(НачалоТипа);
	КонецТипа = """>";
	ДлинаКонцаТипа = СтрДлина(КонецТипа);
	
	ИсходнаяСтрока = ПотокЧтения.ПрочитатьСтроку();
	Пока ИсходнаяСтрока <> Неопределено Цикл
		
		ОстатокСтроки = Неопределено;
		
		ТекущаяПозиция = 1;
		ПозицияТипа = Найти(ИсходнаяСтрока, НачалоТипа);
		Пока ПозицияТипа > 0 Цикл
			
			ПотокЗаписи.Записать(Сред(ИсходнаяСтрока, ТекущаяПозиция, ПозицияТипа - 1 + ДлинаНачалаТипа));
			
			ОстатокСтроки = Сред(ИсходнаяСтрока, ТекущаяПозиция + ПозицияТипа + ДлинаНачалаТипа - 1);
			ТекущаяПозиция = ТекущаяПозиция + ПозицияТипа + ДлинаНачалаТипа - 1;
			
			ПозицияКонцаТипа = Найти(ОстатокСтроки, КонецТипа);
			Если ПозицияКонцаТипа = 0 Тогда
				Прервать;
			КонецЕсли;
			
			ИмяТипа = Лев(ОстатокСтроки, ПозицияКонцаТипа - 1);
			
			ПотокЗаписи.Записать(ИмяТипа);
			ПотокЗаписи.Записать(КонецТипа);
			
			ИсходнаяСсылкаXML = Сред(ОстатокСтроки, ПозицияКонцаТипа + ДлинаКонцаТипа, 36);
			
			СтрокаЗамены = ТаблицаЗаменяемыхУИН.Найти(ИсходнаяСсылкаXML, "СсылкаПоиска");
			Если СтрокаЗамены = Неопределено Тогда
				ПотокЗаписи.Записать(ИсходнаяСсылкаXML);
			Иначе
				ПотокЗаписи.Записать(СтрокаЗамены.СсылкаЗамены);
			КонецЕсли;
			
			ТекущаяПозиция = ТекущаяПозиция + ПозицияКонцаТипа - 1 + ДлинаКонцаТипа + 36;
			ОстатокСтроки = Сред(ОстатокСтроки, ПозицияКонцаТипа + ДлинаКонцаТипа + 36);
			ПозицияТипа = Найти(ОстатокСтроки, НачалоТипа);
			
		КонецЦикла;
		
		Если ОстатокСтроки <> Неопределено Тогда
			ПотокЗаписи.ЗаписатьСтроку(ОстатокСтроки);
		Иначе
			ПотокЗаписи.ЗаписатьСтроку(ИсходнаяСтрока);
		КонецЕсли;
		
		ИсходнаяСтрока = ПотокЧтения.ПрочитатьСтроку();
		
	КонецЦикла;
	
	ПотокЧтения.Закрыть();
	ПотокЗаписи.Закрыть();
	
	//@skip-check bsl-legacy-check-static-feature-access
	КопироватьФайл(ВременныйФайл, ИмяФайла);
	УдалитьФайлы(ВременныйФайл);
	
КонецПроцедуры

Процедура ЗаменаСсылокВНастройкахКД(РеквизитХранилище, Параметры)
	
	Если ТипЗнч(РеквизитХранилище) = Тип("ХранилищеЗначения") Тогда
		НастройкиКомпоновки = РеквизитХранилище.Получить();
		Если ТипЗнч(НастройкиКомпоновки) = Тип("НастройкиКомпоновкиДанных") Тогда 
			Для Каждого Стр Из НастройкиКомпоновки.Отбор.Элементы Цикл
				Если ТипЗнч(Стр) <> Тип("ЭлементОтбораКомпоновкиДанных") Тогда
					Продолжить;
				КонецЕсли;	
				Если ТипЗнч(Стр.ПравоеЗначение) = Тип("СписокЗначений") Тогда
					Для Каждого Эл Из Стр.ПравоеЗначение Цикл
						ОбработатьЗначениеОтбора(Эл.Значение, Параметры);
					КонецЦикла;
				Иначе 
					ОбработатьЗначениеОтбора(Стр.ПравоеЗначение, Параметры);
				КонецЕсли;
			КонецЦикла;
			РеквизитХранилище = Новый ХранилищеЗначения(НастройкиКомпоновки);
		ИначеЕсли ТипЗнч(НастройкиКомпоновки) = Тип("Структура") Тогда
			ОбработатьСтруктуруРекурсивноПриЗагрузке(НастройкиКомпоновки, Параметры);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьСтруктуруРекурсивноПриЗагрузке(СтруктураДанных, Параметры)
	
	Для Каждого КлючИЗначение Из СтруктураДанных Цикл
		Если ТипЗнч(КлючИЗначение.Значение) = Тип("Структура") Тогда
			ОбработатьСтруктуруРекурсивноПриЗагрузке(КлючИЗначение.Значение, Параметры);
		Иначе
			Значение = КлючИЗначение.Значение;
			ОбработатьЗначениеОтбора(Значение, Параметры);
			СтруктураДанных[КлючИЗначение.Ключ] = Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработатьЗначениеОтбора(Значение, Параметры)
	
	Если Значение <> Неопределено Тогда 
		
		ТипЗначения = ТипЗнч(Значение);
		Если (Справочники.ТипВсеСсылки().СодержитТип(ТипЗначения) 
			ИЛИ ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипЗначения)) Тогда
			
			УИН = Строка(Значение.УникальныйИдентификатор());
			НайденнаяСтрока = Параметры.ЗаменаЗначенийАналитик.Найти(УИН, "АналитикаФайлаУИН");
			Если НайденнаяСтрока <> Неопределено Тогда
				АналитикаБД = НайденнаяСтрока.АналитикаБД;
				Если ЗначениеЗаполнено(АналитикаБД) Тогда
					Значение = АналитикаБД;
				КонецЕсли;
			Иначе
				СтрокаЗамены = Параметры.ЗаменяемыеУИН.Найти(УИН, "СсылкаПоиска");
				Если СтрокаЗамены <> Неопределено Тогда
					УИД = Новый УникальныйИдентификатор(СтрокаЗамены.СсылкаЗамены);
					ИмяСправочника = Метаданные.НайтиПоТипу(ТипЗначения).Имя;
					Значение = Справочники[ИмяСправочника].ПолучитьСсылку(УИД);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоМетаданныеСПредопределеннымиЭлементами(ОбъектМетаданных)
	
	Возврат ОбщегоНазначения.ЭтоСправочник(ОбъектМетаданных)
		Или ОбщегоНазначения.ЭтоПланСчетов(ОбъектМетаданных)
		Или ОбщегоНазначения.ЭтоПланВидовХарактеристик(ОбъектМетаданных)
		Или ОбщегоНазначения.ЭтоПланВидовРасчета(ОбъектМетаданных);
	
КонецФункции

Процедура ВыгрузкаСсылокИзНастроекКД(РеквизитХранилище, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры)
	
	Если ТипЗнч(РеквизитХранилище) = Тип("ХранилищеЗначения") Тогда
		НастройкиКомпоновки = РеквизитХранилище.Получить();
		Если ТипЗнч(НастройкиКомпоновки) = Тип("НастройкиКомпоновкиДанных") Тогда 
			Для Каждого Стр Из НастройкиКомпоновки.Отбор.Элементы Цикл
					Если ТипЗнч(Стр) <> Тип("ЭлементОтбораКомпоновкиДанных") Тогда
						Продолжить;
					КонецЕсли;			
					Если ТипЗнч(Стр.ПравоеЗначение) = Тип("СписокЗначений") Тогда
						Для Каждого Эл Из Стр.ПравоеЗначение Цикл
							Если Эл.Значение <> Неопределено И (Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Эл.Значение)) 
									ИЛИ ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипЗнч(Эл.Значение))) Тогда
								//@skip-check query-in-loop - запись данных необходимых для ссылочной целостности
								ЗаписатьЗначениеПриНеобходимости(Эл.Значение, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры);
							КонецЕсли;
						КонецЦикла;
					Иначе 
						Если Стр.ПравоеЗначение <> Неопределено И (Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Стр.ПравоеЗначение)) 
								ИЛИ ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипЗнч(Стр.ПравоеЗначение))) Тогда
							//@skip-check query-in-loop - запись данных необходимых для ссылочной целостности
							ЗаписатьЗначениеПриНеобходимости(Стр.ПравоеЗначение, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры);
						КонецЕсли;
					КонецЕсли;
			КонецЦикла;
		ИначеЕсли ТипЗнч(НастройкиКомпоновки) = Тип("Структура") Тогда
			ОбработатьСтруктуруРекурсивноПриВыгрузке(НастройкиКомпоновки, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьСтруктуруРекурсивноПриВыгрузке(СтруктураДанных, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры)
	
	Для Каждого КлючИЗначение Из СтруктураДанных Цикл
		Если ТипЗнч(КлючИЗначение.Значение) = Тип("Структура") Тогда
			//@skip-check query-in-loop - запись данных необходимых для ссылочной целостности
			ОбработатьСтруктуруРекурсивноПриВыгрузке(КлючИЗначение.Значение, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры);
		ИначеЕсли КлючИЗначение.Значение <> Неопределено И (Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(КлючИЗначение.Значение)) 
			ИЛИ ПланыВидовХарактеристик.ТипВсеСсылки().СодержитТип(ТипЗнч(КлючИЗначение.Значение))) Тогда
			//@skip-check query-in-loop - запись данных необходимых для ссылочной целостности
			ЗаписатьЗначениеПриНеобходимости(КлючИЗначение.Значение, Владелец, Сериализатор, ЗаписьXML, ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Сериализатор XDTOСАннотацией типов.
// 
// Возвращаемое значение:
//  СериализаторXDTO - Сериализатор XDTOСАннотацией типов
Функция СериализаторXDTOСАннотациейТипов() Экспорт
	
	ТипыСАннотациейСсылок = ПредопределенныеТипыПриВыгрузке();
	
	Если ТипыСАннотациейСсылок.Количество() > 0 Тогда
		Фабрика = ПолучитьФабрикуСУказаниемТипов(ТипыСАннотациейСсылок);
		Сериализатор = Новый СериализаторXDTO(Фабрика);
	Иначе
		Сериализатор = СериализаторXDTO;
	КонецЕсли;
	
	Возврат Сериализатор;
	
КонецФункции

Функция ПредопределенныеТипыПриВыгрузке()
	
	Типы = Новый Массив;
	
	Для Каждого ОбъектМетаданных Из Метаданные.Справочники Цикл
		Если ЭтоОбъектМетаданныхКонфигурации(ОбъектМетаданных) Тогда
			Типы.Добавить(ОбъектМетаданных);	
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ОбъектМетаданных Из Метаданные.ПланыСчетов Цикл
		Если ЭтоОбъектМетаданныхКонфигурации(ОбъектМетаданных) Тогда
			Типы.Добавить(ОбъектМетаданных);	
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ОбъектМетаданных Из Метаданные.ПланыВидовХарактеристик Цикл
		Если ЭтоОбъектМетаданныхКонфигурации(ОбъектМетаданных) Тогда
			Типы.Добавить(ОбъектМетаданных);	
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ОбъектМетаданных Из Метаданные.ПланыВидовРасчета Цикл
		Если ЭтоОбъектМетаданныхКонфигурации(ОбъектМетаданных) Тогда
			Типы.Добавить(ОбъектМетаданных);	
		КонецЕсли;
	КонецЦикла;
	
	Возврат Типы;
	
КонецФункции

Функция ЭтоОбъектМетаданныхКонфигурации(ОбъектМетаданных)
	ИдентификаторОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектМетаданных);
	Если ТипЗнч(ИдентификаторОбъекта) = Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных") Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

Функция ПолучитьФабрикуСУказаниемТипов(Знач Типы)
	
	НаборСхем = ФабрикаXDTO.ЭкспортСхемыXML("http://v8.1c.ru/8.1/data/enterprise/current-config");
	Схема = НаборСхем[0];
	Схема.ОбновитьЭлементDOM();
	
	УказанныеТипы = Новый Соответствие;
	Для каждого Тип Из Типы Цикл
		УказанныеТипы.Вставить(XMLТипСсылки(Тип), Истина);
	КонецЦикла;
	
	ПространствоИмен = Новый Соответствие;
	ПространствоИмен.Вставить("xs", "http://www.w3.org/2001/XMLSchema");
	РазыменовательПространствИменDOM = Новый РазыменовательПространствИменDOM(ПространствоИмен);
	ТекстXPath = "/xs:schema/xs:complexType/xs:sequence/xs:element[starts-with(@type,'tns:')]";
	
	Запрос = Схема.ДокументDOM.СоздатьВыражениеXPath(ТекстXPath, РазыменовательПространствИменDOM);
	
	УстановитьБезопасныйРежим(Истина);
	Результат = Запрос.Вычислить(Схема.ДокументDOM);
	УстановитьБезопасныйРежим(Ложь);

	Пока Истина Цикл
		
		УзелПоля = Результат.ПолучитьСледующий();
		Если УзелПоля = Неопределено Тогда
			Прервать;
		КонецЕсли;
		АтрибутТип = УзелПоля.Атрибуты.ПолучитьИменованныйЭлемент("type");
		ТипБезNSПрефикса = Сред(АтрибутТип.ТекстовоеСодержимое, СтрДлина("tns:") + 1);
		
		Если УказанныеТипы.Получить(ТипБезNSПрефикса) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		УзелПоля.УстановитьАтрибут("nillable", "true");
		УзелПоля.УдалитьАтрибут("type");
	КонецЦикла;
	
	ЗаписьXML = Новый ЗаписьXML;
	ИмяФайлаСхемы = ПолучитьИмяВременногоФайла("xsd");
	ЗаписьXML.ОткрытьФайл(ИмяФайлаСхемы);
	ЗаписьDOM = Новый ЗаписьDOM;
	ЗаписьDOM.Записать(Схема.ДокументDOM, ЗаписьXML);
	ЗаписьXML.Закрыть();
	
	Фабрика = СоздатьФабрикуXDTO(ИмяФайлаСхемы);
	
	Попытка
		УдалитьФайлы(ИмяФайлаСхемы);
	Исключение
		ТекстИсключения = НСтр("ru = 'Ошибка создания фабрики XML';
								|en = 'An error occurred while creating the XML factory'");
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	Возврат Фабрика;
	
КонецФункции

Функция XMLТипСсылки(Знач Значение)
	
	Если ТипЗнч(Значение) = Тип("ОбъектМетаданных") Тогда
		ОбъектМетаданных = Значение;
		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ОбъектМетаданных.ПолноеИмя());
		Ссылка = МенеджерОбъекта.ПолучитьСсылку();
	Иначе
		ОбъектМетаданных = Значение.Метаданные();
		Ссылка = Значение;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Ссылка)) Тогда
		
		Возврат СериализаторXDTO.XMLТипЗнч(Ссылка).ИмяТипа;
		
	Иначе
		
		ТекстИсключения = НСтр("ru = 'Ошибка при определении XML-типа ссылки для объекта %1: объект не является ссылочным';
								|en = 'An error occurred while determining the XML ref type for the %1 object: this is not a reference object'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%1", ОбъектМетаданных.ПолноеИмя());
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьТекстЗапроса(ОбъектМетаданных, ЕстьОтборПоСсылкам, ПолеОтбора = "")
	
	ИмяМетаданных = ОбъектМетаданных.ПолноеИмя();
	
	Если ОбщегоНазначения.ЭтоРегистрСведений(ОбъектМетаданных) Тогда
		
		Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяМетаданных);
		НаборЗаписей = Менеджер.СоздатьНаборЗаписей();
		
		Отбор = НаборЗаписей.Отбор;
		СтрокаПолейДляВыборки = "";
		Для Каждого ЭлементОтбора Из Отбор Цикл 
			СтрокаПолейДляВыборки = ?(ПустаяСтрока(СтрокаПолейДляВыборки), СтрокаПолейДляВыборки, СтрокаПолейДляВыборки + ", ") + ЭлементОтбора.Имя;
		КонецЦикла;
		
	Иначе
		СтрокаПолейДляВыборки = "_.*";
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ &ПоляВыборки ИЗ #Таблица КАК _ ";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляВыборки", СтрокаПолейДляВыборки);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Таблица", ИмяМетаданных);
	
	Если ЕстьОтборПоСсылкам Тогда
		
		СтрокаОтбор = "
		|	ГДЕ
		|		_." + ?(ЗначениеЗаполнено(ПолеОтбора), ПолеОтбора, "Ссылка") + " В(&МассивСсылок)";
		
		ТекстЗапроса = ТекстЗапроса + Символы.ПС + СтрокаОтбор;
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции 

Функция СсылкаВыгружена(ВыгруженныеОбъекты, Ссылка)
	
	Возврат ВыгруженныеОбъекты.Найти(Ссылка, "Ссылка") <> Неопределено;
	
КонецФункции

// Добавить ссылку к выгруженным.
// 
// Параметры:
//  ВыгруженныеОбъекты - ТаблицаЗначений - Выгруженные объекты:
// 	* Ссылка - ЛюбаяСсылка - Ссылка на выгруженный объект
//  Ссылка - ЛюбаяСсылка - Ссылка на выгружаемый объект
Процедура ДобавитьСсылкуКВыгруженным(ВыгруженныеОбъекты, Ссылка)
	
	СтрокаДобавления = ВыгруженныеОбъекты.Добавить();
	СтрокаДобавления.Ссылка = Ссылка;
	
КонецПроцедуры

Процедура ОбработатьФункциональныеОпции()
	
	//++ НЕ УТКА
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоПлановСчетовВМеждународномУчете") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(ПланыСчетовМеждународногоУчета.Ссылка) КАК Количество
		|ИЗ
		|	Справочник.ПланыСчетовМеждународногоУчета КАК ПланыСчетовМеждународногоУчета";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.Количество > 1 Тогда
				Константы.ИспользоватьНесколькоПлановСчетовВМеждународномУчете.Установить(Истина);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют") Тогда
		
		КоличествоВалют = Справочники.Валюты.КоличествоВалют();
		Если КоличествоВалют > 1 Тогда
			Константы.ИспользоватьНесколькоВалют.Установить(Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	//-- НЕ УТКА
	
КонецПроцедуры

Процедура УдалитьЭлементыФинансовыхОтчетов(ВидОтчета)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭлементыФинансовыхОтчетов.Ссылка КАК Ссылка,
	|	ЭлементыФинансовыхОтчетов.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ЭлементыФинансовыхОтчетов КАК ЭлементыФинансовыхОтчетов
	|ГДЕ
	|	ЭлементыФинансовыхОтчетов.Владелец = &Владелец";

	Запрос.УстановитьПараметр("Владелец", ВидОтчета);

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		Объект = Выборка.Ссылка.ПолучитьОбъект(); // СправочникОбъект
		Если Объект <> Неопределено Тогда
			Попытка
				Объект.Удалить();
			Исключение
				
				ИмяСобытияЖурнала = НСтр("ru = 'Запись вида бюджета';
										|en = 'Save budget profile'");
				ТекстОшибки = НСтр("ru = 'При записи вида бюджета %1 не удалось удалить элемент финансового отчета: %2 по причине: %3';
									|en = 'Cannot delete financial report item: %2 while saving the budget profile %1. Reason: %3'",);
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ВидОтчета,
					Выборка.Наименование, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала,
						УровеньЖурналаРегистрации.Ошибка,
						Метаданные.Справочники.ЭлементыФинансовыхОтчетов,
						Выборка.Ссылка,
						ТекстОшибки);
						
				ВызватьИсключение ТекстОшибки;
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
