////////////////////////////////////////////////////////////////////////////////
// Подсистема "Интеграция с 1С:Документооборотом"
// Модуль ИнтеграцияС1СДокументооборот3Обмен, сервер, внешнее соединение
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Помещает изменения в базу и возвращает ответ на запрос DMILPutChangesRequest.
//
// Параметры:
//   Сообщение - ОбъектXDTO - объект XDTO типа DMILPutChangesRequest:
//     * messageData - ДвоичныеДанные
//
// Возвращаемое значение:
//   ОбъектXDTO - объект XDTO типа DMILPutChangesResponse или DMILError.
//
Функция ЗаписатьИзмененияОбъектов(Сообщение) Экспорт
	
	ИмяСобытия = НСтр("ru = 'Получение данных';
						|en = 'Get data'");
	
	Попытка
		
		МассивОшибок = Новый Массив;
		УзелДокументооборота = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.УзелДокументооборота();
		СоставПланаОбмена = Метаданные.ПланыОбмена.ИнтеграцияС1СДокументооборотомПереопределяемый.Состав;
		
		Для Каждого ОбъектXDTO Из ИнтеграцияС1СДокументооборот3.СписокОбъектовXDTOизСообщения(Сообщение.messageData) Цикл
			Попытка
				
				Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(
						Неопределено, ОбъектXDTO, "DMApprovalStateRecord") Тогда
					
					ЗаголовокОшибки = СтрШаблон(
						НСтр("ru = 'Ошибка при загрузке из 1С:Документооборота статуса ""%1"":
							|   * Идентификатор объекта 1С:Документооборота: %2.
							|   * Тип объекта 1С:Документооборота: %3.';
							|en = 'An error occurred when importing the ""%1"" status from 1C:Document Management:
							|   * 1C:Document Management object ID: %2.
							|   * 1C:Document Management object type: %3.'",
							ОбщегоНазначения.КодОсновногоЯзыка()),
						ОбъектXDTO.status,
						ОбъектXDTO.id,
						ОбъектXDTO.type);
					
					ЗагрузитьСостояниеДокумента(ОбъектXDTO);
					
				Иначе
					
					Ссылки = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СсылкиПоВнешнимОбъектам(ОбъектXDTO);
					Для Каждого ОбъектСсылка Из Ссылки Цикл
						ЗаголовокОшибки = СтрШаблон(
							НСтр("ru = 'Ошибка при загрузке данных из объекта 1С:Документооборота ""%1"":
								|   * Навигационная ссылка на объект 1С:Документооборота: %2.
								|   * Навигационная ссылка на объект %3: %4.';
								|en = 'An error occurred when importing data from 1C:Document Management object ""%1"":
								|   * 1C:Document Management object URL: %2.
								|   * The %3 object URL: %4.'",
								ОбщегоНазначения.КодОсновногоЯзыка()),
							ОбъектXDTO.name,
							ОбъектXDTO.objectID.navigationRef,
							ИнтеграцияС1СДокументооборотБазоваяФункциональность.СокращенноеНаименованиеКонфигурации(),
							ПолучитьНавигационнуюСсылку(ОбъектСсылка));
						
						ПодходящиеПравила =
							ИнтеграцияС1СДокументооборот3ВызовСервера.ПодходящиеПравилаИнтеграцииОбъекта(
								ОбъектСсылка,
								Истина);
						Если ПодходящиеПравила = Неопределено Или ПодходящиеПравила.Количество() = 0 Тогда
							ТекстПредупреждения = СтрШаблон(
								"%1
								|%2",
								ЗаголовокОшибки,
								НСтр("ru = 'Отсутствуют правила интеграции';
									|en = 'Integration rules are missing'"));
							ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаписатьПредупреждение(
								ТекстПредупреждения,
								ИмяСобытия);
							Продолжить;
						ИначеЕсли ПодходящиеПравила.Количество() > 1 Тогда
							ВызватьИсключение НСтр("ru = 'Не найдены подходящие правила интеграции';
													|en = 'Suitable integration rules are not found'");
						КонецЕсли;
						Правило = ПодходящиеПравила[0];
						
						ЗагрузитьСсылочныйОбъект(
							ОбъектСсылка,
							Правило,
							ОбъектXDTO,
							УзелДокументооборота,
							СоставПланаОбмена);
						
					КонецЦикла;
					
				КонецЕсли;
				
			Исключение
				ТекстОшибки = СтрШаблон(
					"%1
					|%2",
					ЗаголовокОшибки,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				МассивОшибок.Добавить(ТекстОшибки);
				ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаписатьОшибку(ТекстОшибки, ИмяСобытия);
			КонецПопытки;
		КонецЦикла;
		
		Если МассивОшибок.Количество() = 0 Тогда
			Возврат ИнтеграцияС1СДокументооборот3.СоздатьОбъектБИД("DMILPutChangesResponse");
		Иначе
			Возврат ИнтеграцияС1СДокументооборот3.ОписаниеОшибкиXDTO(
				НСтр("ru = 'Ошибка при загрузке сообщения обмена из 1С:Документооборота';
					|en = 'An error occurred when importing the exchange message from 1C:Document Management'"),
				СтрСоединить(
					МассивОшибок,
					"
					|-----
					|
					|"));
		КонецЕсли;
		
	Исключение
		
		Возврат ИнтеграцияС1СДокументооборот3.ОписаниеОшибкиXDTO(
			НСтр("ru = 'Ошибка при загрузке сообщения обмена из 1С:Документооборота';
				|en = 'An error occurred when importing the exchange message from 1C:Document Management'"),
			ИнтеграцияС1СДокументооборот3.ПолучитьОписаниеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
КонецФункции

// Возвращает запрос на обновление данных объекта ИС в ДО.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   ВнешнийОбъект - ЛюбаяСсылка - ссылка на объект ИС.
//   КонтрольОтправкиФайлов - см. ИнтеграцияС1СДокументооборотБазоваяФункциональность.КонтрольОтправкиФайлов
//
// Возвращаемое значение:
//   ОбъектXDTO - объект XDTO типа DMUpdateFromIncomingDataRequest.
//   Неопределено - если объект ИС невозможно обновить в ДО, например, из-за отсутствия подходящего правила.
//
Функция ЗапросОбновитьОбъект(Прокси, ВнешнийОбъект, КонтрольОтправкиФайлов) Экспорт
	
	ИмяСобытия = НСтр("ru = 'Обновление данных объекта ИС в ДО';
						|en = 'Update the data of IS object in the DI'");
	
	КонтрольОтправкиФайлов = ИнтеграцияС1СДокументооборотБазоваяФункциональность.КонтрольОтправкиФайлов();
	
	ИнтегрированныеОбъекты = МассивДанныхДляВыгрузки(
		Прокси,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВнешнийОбъект),
		ИмяСобытия);
	Если ИнтегрированныеОбъекты.Количество() <> 1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЗапросОбновитьОбъект = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
		Прокси,
		"DMUpdateFromIncomingDataRequest");
	СписокОбъектовКОбновлению = ЗапросОбновитьОбъект.objects; // СписокXDTO
	
	СписокОбъектовКОбновлению.Добавить(
		ПолучитьXDTOИзмененийИзОбъекта(
			Прокси,
			ИнтегрированныеОбъекты[0],
			КонтрольОтправкиФайлов));
	
	Возврат ЗапросОбновитьОбъект;
	
КонецФункции

// Возвращает измененные объекты, интегрированные с 1С:Документооборотом, и готовые к выгрузке.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   УзелОбмена - ПланОбменаСсылка.ИнтеграцияС1СДокументооборотомПереопределяемый - узел, по которому нужно
//     получить изменения.
//   ОбъектыКУдалениюИзРегистрацииИзменений - Массив из ЛюбаяСсылка - неявно возвращаемое значение,
//     содержит ссылки на объекты, не требующие выгрузки, и на успешно отправленные объекты.
//   ИмяСобытия - Строка - имя события для ЖР.
//
// Возвращаемое значение:
//   Массив из Структура:
//     * Объект - ЛюбаяСсылка
//     * ИдентификаторОбъектаДО - Строка
//     * ТипОбъектаДО - Строка
//     * СписокВыгружаемыхТЧ - Массив из Строка
//     * СписокВыражений - СписокXDTO
//     * ПравилоЗагрузкиВДО - ОбъектXDTO
//     * ДоступенФункционалФайлов - Булево
//     * ПечатныеФормы - СписокXDTO
//     * ТипыФайловПечатныхФорм - СписокXDTO
//     * ДоступенФункционалСозданияСвязей - Булево
//     * СвязиКСозданию - Массив из см. ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СвязьКСозданию
//
Функция ЗарегистрированныеДанные(Прокси, УзелОбмена, ОбъектыКУдалениюИзРегистрацииИзменений, ИмяСобытия) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СписокОбъектовИС = Новый Массив;
	СвязанныеОбъектыДО = Новый Соответствие;
	
	Для Каждого ЭлементСоставаПланаОбмена Из УзелОбмена.Метаданные().Состав Цикл
		ЗапросИзменения = Новый Запрос;
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТаблицаИзменения.Ссылка КАК Объект,
			|	ЕСТЬNULL(ОбъектыИнтегрированныеС1СДокументооборотом.ИдентификаторОбъектаДО, """") КАК ИдентификаторОбъектаДО,
			|	ЕСТЬNULL(ОбъектыИнтегрированныеС1СДокументооборотом.ТипОбъектаДО, """") КАК ТипОбъектаДО
			|ИЗ
			|	&ТаблицаИзменения КАК ТаблицаИзменения
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыИнтегрированныеС1СДокументооборотом КАК ОбъектыИнтегрированныеС1СДокументооборотом
			|		ПО ТаблицаИзменения.Ссылка = ОбъектыИнтегрированныеС1СДокументооборотом.Объект
			|ГДЕ
			|	ТаблицаИзменения.Узел = &УзелОбмена";
		ЗапросИзменения.Текст = СтрЗаменить(
			ТекстЗапроса,
			"&ТаблицаИзменения",
			СтрШаблон("%1.%2",
				ЭлементСоставаПланаОбмена.Метаданные.ПолноеИмя(),
				"Изменения"));
		ЗапросИзменения.УстановитьПараметр("УзелОбмена", УзелОбмена);
		
		ВыборкаОбъекты = ЗапросИзменения.Выполнить().Выбрать();
		
		Пока ВыборкаОбъекты.Следующий() Цикл
			Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ОбъектДОПоддерживаетОбмен(ВыборкаОбъекты.ТипОбъектаДО)
					И ЗначениеЗаполнено(ВыборкаОбъекты.ИдентификаторОбъектаДО) Тогда
				СвязанныйОбъект = Новый Структура("ТипОбъектаДО, ИдентификаторОбъектаДО",
					ВыборкаОбъекты.ТипОбъектаДО,
					ВыборкаОбъекты.ИдентификаторОбъектаДО);
				СписокОбъектовИС.Добавить(ВыборкаОбъекты.Объект);
				СвязанныеОбъектыДО.Вставить(ВыборкаОбъекты.Объект, СвязанныйОбъект);
			Иначе
				// Выгружать объект не требуется.
				ОбъектыКУдалениюИзРегистрацииИзменений.Добавить(ВыборкаОбъекты.Объект);
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	Возврат МассивДанныхДляВыгрузки(
		Прокси,
		СписокОбъектовИС,
		ИмяСобытия,
		СвязанныеОбъектыДО,
		ОбъектыКУдалениюИзРегистрацииИзменений);
	
КонецФункции

// Возвращает объект XDTO, содержащий обновляемые изменения объекта.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   ДанныеОбъекта - см. ИнтеграцияС1СДокументооборот3Обмен.ЗарегистрированныеДанные
//   КонтрольОтправкиФайлов - см. ИнтеграцияС1СДокументооборотБазоваяФункциональность.КонтрольОтправкиФайлов
//
// Возвращаемое значение:
//   ОбъектXDTO
//
Функция ПолучитьXDTOИзмененийИзОбъекта(Прокси, ДанныеОбъекта, КонтрольОтправкиФайлов) Экспорт
	
	СтруктураСозданияОбъекта = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
		Прокси,
		"DMIncomingDataRequestStructure",
		ДанныеОбъекта.Объект);
	
	СтруктураСозданияОбъекта.incomingData = ИнтеграцияС1СДокументооборот3.ВходящиеДанныеОбъектаИС(
		Прокси,
		ДанныеОбъекта.Объект,
		ДанныеОбъекта.СписокВыгружаемыхТЧ,
		ДанныеОбъекта.СписокВыражений);
	
	СтруктураСозданияОбъекта.updatingObject = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьObjectID(
		Прокси,
		ДанныеОбъекта.ИдентификаторОбъектаДО,
		ДанныеОбъекта.ТипОбъектаДО);
	
	СтруктураСозданияОбъекта.dataLoadingRule = ДанныеОбъекта.ПравилоЗагрузкиВДО;
	
	Если ДанныеОбъекта.ДоступенФункционалФайлов Тогда
		СписокФайлов = СтруктураСозданияОбъекта.files; // СписокXDTO
		ПечатныеФормыКСозданию = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПечатныеФормыКСозданию(
			ДанныеОбъекта.Объект,
			ИнтеграцияС1СДокументооборот3.ПрисоединяемыеПечатныеФормы(ДанныеОбъекта.ПечатныеФормы),
			Перечисления.ТипыФайловСохраненияПечатныхФормОбъектов[ДанныеОбъекта.ТипыФайловПечатныхФорм],
			КонтрольОтправкиФайлов,
			Истина);
		Для Каждого ПараметрыСоздания Из ПечатныеФормыКСозданию Цикл
			ФайлXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ФайлXDTOИзПараметровСоздания(
				Прокси,
				ПараметрыСоздания);
			СписокФайлов.Добавить(ФайлXDTO);
		КонецЦикла;
	КонецЕсли;
	
	Если ДанныеОбъекта.ДоступенФункционалСозданияСвязей Тогда
		ИнтеграцияС1СДокументооборот3.ПеренестиСвязиКСозданиюВСтруктуруВходящихДанных(
			Прокси,
			ДанныеОбъекта.СвязиКСозданию,
			СтруктураСозданияОбъекта);
	КонецЕсли;
	
	Возврат СтруктураСозданияОбъекта;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗагрузитьСостояниеДокумента(ОбъектXDTO)
	
	ЗапросСвязанныйОбъект = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Объект
		|ИЗ
		|	РегистрСведений.ОбъектыИнтегрированныеС1СДокументооборотом
		|ГДЕ
		|	ТипОбъектаДО = &ТипОбъектаДО
		|	И ИдентификаторОбъектаДО = &ИдентификаторОбъектаДО");
	ЗапросСвязанныйОбъект.УстановитьПараметр("ТипОбъектаДО", ОбъектXDTO.type);
	ЗапросСвязанныйОбъект.УстановитьПараметр("ИдентификаторОбъектаДО", ОбъектXDTO.ID);
	
	Выборка = ЗапросСвязанныйОбъект.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Если ОбъектXDTO.status = Неопределено Тогда // Запись удалена (например, прерывание).
			Действие = НСтр("ru = 'Удаление статуса';
							|en = 'Delete status'", ОбщегоНазначения.КодОсновногоЯзыка());
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ПриИзмененииСостоянияСогласования(
				ОбъектXDTO.ID,
				ОбъектXDTO.type,
				Неопределено,
				Ложь,
				Выборка.Объект);
		Иначе
			Действие = НСтр("ru = 'Установка статуса';
							|en = 'Set status'", ОбщегоНазначения.КодОсновногоЯзыка());
			Состояние = Перечисления.СостоянияСогласованияВДокументообороте.ЗначениеСоответствующееСтатусуДО(
				ОбъектXDTO.status);
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ПриИзмененииСостоянияСогласования(
				ОбъектXDTO.ID,
				ОбъектXDTO.type,
				Состояние,
				Ложь,
				Выборка.Объект,
				ОбъектXDTO.name,
				ОбъектXDTO.date);
		КонецЕсли;
	Исключение
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Действие: %1.
									|%2';
									|en = 'Action: %1.
									|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
			Действие,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗагрузитьСсылочныйОбъект(ОбъектСсылка, Правило, ОбъектXDTO, УзелДокументооборота, СоставПланаОбмена)
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ОбъектXDTO, "files") Тогда
		
		Если ОбъектXDTO.files.Количество() > 0 Тогда
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриПоявленииПрисоединенныхФайловДокументооборота(
				ОбъектXDTO.objectID.ID,
				ОбъектXDTO.objectID.type,
				ОбъектСсылка,
				Истина);
		Иначе
			ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриУдаленииПрисоединенныхФайловДокументооборота(
				ОбъектXDTO.objectID.ID,
				ОбъектXDTO.objectID.type,
				ОбъектСсылка);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИнтеграцияС1СДокументооборот3.ДоступенФункционалФайлов() Тогда
		РегистрыСведений.ОбъектыКОбновлениюПечатныхФорм.Добавить(ОбъектСсылка, Правило);
	КонецЕсли;
	
	Объект = ОбъектСсылка.ПолучитьОбъект();
	
	ИсходнаяПометкаУдаления = Объект.ПометкаУдаления;
	Обновление = Истина;
	ТребуетсяПерепроведение = Ложь;
	
	ЕстьИзменения = Справочники.ПравилаИнтеграцииС1СДокументооборотом3.ЗаполнитьОбъектИСПоОбъектуXDTO(
		Неопределено,
		Объект,
		ОбъектXDTO,
		Правило,
		Обновление,
		ТребуетсяПерепроведение);
	
	Если Не ЕстьИзменения Тогда
		Возврат;
	КонецЕсли;
	
	ОшибкаПроверкиЗаполнения = "";
	
	Попытка
		
		Действие = НСтр("ru = 'Сохранение изменений';
						|en = 'Save the changes'", ОбщегоНазначения.КодОсновногоЯзыка());
		
		Если Объект.ПометкаУдаления И Не ИсходнаяПометкаУдаления Тогда
			
			Объект.Заблокировать();
			
			МетаданныеОбъекта = Объект.Метаданные();
			Если Метаданные.Документы.Содержит(МетаданныеОбъекта)
					И МетаданныеОбъекта.Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить
					И Объект.Проведен Тогда
				
				Действие = НСтр("ru = 'Удаление с отменой проведения';
								|en = 'Delete and cancel posting'",
					ОбщегоНазначения.КодОсновногоЯзыка());
				
				Отказ = Ложь;
				РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения;
				ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюОбъектаИС(
					Объект,
					Отказ,
					РежимЗаписи);
				Если Не Отказ Тогда
					Объект.Записать(РежимЗаписи);
				КонецЕсли;
				
			Иначе
				
				Действие = НСтр("ru = 'Удаление';
								|en = 'Delete'", ОбщегоНазначения.КодОсновногоЯзыка());
				Объект.ОбменДанными.Загрузка = Истина;
				
				Отказ = Ложь;
				ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюОбъектаИС(
					Объект,
					Отказ);
				Если Не Отказ Тогда
					Объект.Записать();
				КонецЕсли;
				
			КонецЕсли;
			
			Объект.Разблокировать();
			
		Иначе
			
			Если Объект.ПроверитьЗаполнение() Тогда
				
				Объект.Заблокировать();
				
				Если ТребуетсяПерепроведение Тогда
					
					Действие = НСтр("ru = 'Проведение';
									|en = 'Posting'", ОбщегоНазначения.КодОсновногоЯзыка());
					
					Отказ = Ложь;
					РежимЗаписи = РежимЗаписиДокумента.Проведение;
					РежимПроведения = РежимПроведенияДокумента.Неоперативный;
					ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюОбъектаИС(
						Объект,
						Отказ,
						РежимЗаписи,
						РежимПроведения);
					Если Не Отказ Тогда
						Объект.Записать(РежимЗаписи, РежимПроведения);
					КонецЕсли;
					
				Иначе
					
					Действие = НСтр("ru = 'Запись';
									|en = 'Record'", ОбщегоНазначения.КодОсновногоЯзыка());
					Объект.ОбменДанными.Загрузка = Истина;
					
					Отказ = Ложь;
					ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюОбъектаИС(
						Объект,
						Отказ);
					Если Не Отказ Тогда
						Объект.Записать();
					КонецЕсли;
					
				КонецЕсли;
				
				Объект.Разблокировать();
				
			Иначе
				
				СообщенияПользователю = ПолучитьСообщенияПользователю(Истина);
				ТекстСообщения = Новый Массив;
				ТекстСообщения.Добавить(
					НСтр("ru = 'Ошибка заполнения:';
						|en = 'Filling error:'", ОбщегоНазначения.КодОсновногоЯзыка()));
				Для Каждого СообщениеПользователю Из СообщенияПользователю Цикл
					ТекстСообщения.Добавить(СообщениеПользователю.Текст);
				КонецЦикла;
				ОшибкаПроверкиЗаполнения = СтрСоединить(ТекстСообщения, Символы.ПС);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Исключение
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Действие: %1.
									|%2';
									|en = 'Action: %1.
									|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
			Действие,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Если ОшибкаПроверкиЗаполнения <> "" Тогда
		ВызватьИсключение ОшибкаПроверкиЗаполнения;
	КонецЕсли;
	
	Если СоставПланаОбмена.Содержит(Объект.Метаданные()) Тогда
		ПланыОбмена.УдалитьРегистрациюИзменений(УзелДокументооборота, Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает массив данных, подготовленных к выгрузке в 1С:Документооборот.
//
// Параметры:
//   Прокси - WSПрокси - объект для подключения к web-сервисам Документооборота.
//   СписокОбъектовИС - Массив из ЛюбаяСсылка - список объектов ИС, изменения которых требуется отправить в ДО.
//   ИмяСобытия - Строка - имя события для ЖР.
//   СвязанныеОбъектыДО - Соответствие из КлючИЗначение:
//     * Ключ - ЛюбаяСсылка - объект ИС.
//     * Значение - Структура:
//         ** ТипОбъектаДО - Строка - тип объекта ДО.
//         ** ИдентификаторОбъектаДО - Строка - идентификатор объекта ДО.
//   ОбъектыКУдалениюИзРегистрацииИзменений - Массив из ЛюбаяСсылка - неявно возвращаемое значение,
//     содержит ссылки на объекты, не требующие выгрузки, и на успешно отправленные объекты.
//
// Возвращаемое значение:
//   Массив из Структура:
//     * Объект - ЛюбаяСсылка
//     * ИдентификаторОбъектаДО - Строка
//     * ТипОбъектаДО - Строка
//     * СписокВыгружаемыхТЧ - Массив из Строка
//     * СписокВыражений - СписокXDTO
//     * ПравилоЗагрузкиВДО - ОбъектXDTO
//     * ДоступенФункционалФайлов - Булево
//     * ПечатныеФормы - СписокXDTO
//     * ТипыФайловПечатныхФорм - СписокXDTO
//     * ДоступенФункционалСозданияСвязей - Булево
//     * СвязиКСозданию - Массив из см. ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СвязьКСозданию
//
Функция МассивДанныхДляВыгрузки(Прокси, СписокОбъектовИС, ИмяСобытия, СвязанныеОбъектыДО = Неопределено,
		ОбъектыКУдалениюИзРегистрацииИзменений = Неопределено)
	
	МассивДанных = Новый Массив;
	
	ПодходящиеПравилаИнтеграции = ИнтеграцияС1СДокументооборот3ВызовСервера.ПодходящиеПравилаИнтеграции(
		СписокОбъектовИС,
		Истина);
	ПредварительныеДанные = ИнтеграцияС1СДокументооборот3.ПредварительныеДанныеДляВыгрузкиВДО(
		Прокси,
		СписокОбъектовИС,
		ПодходящиеПравилаИнтеграции,,
		Ложь);
	
	// Добавим в выгрузку те объекты ИС, для которых есть подходящее правило загрузки в ДО.
	Для Каждого ОбъектИС Из СписокОбъектовИС Цикл
		
		ПравилоЗагрузкиВДО = ИнтеграцияС1СДокументооборот3.ПравилоЗагрузкиВДОИзПредварительныхДанных(
			Прокси,
			ПредварительныеДанные,
			ОбъектИС);
		Если ПравилоЗагрузкиВДО = Неопределено Тогда
			Если ПодходящиеПравилаИнтеграции[ОбъектИС] = Неопределено
					Или ПодходящиеПравилаИнтеграции[ОбъектИС].Количество() = 0 Тогда
				ТекстПредупреждения = СтрШаблон(
					НСтр("ru = 'Объект ""%1"" (%2) зарегистрирован к обмену данными со связанным объектом в 1С:Документооборот,
						|но правила интеграции отсутствуют.';
						|en = 'The ""%1"" (%2) object is registered to exchange data with the related object in 1C:Document Management,
						|but integration rules are missing.'"),
					ОбъектИС,
					ТипЗнч(ОбъектИС));
				ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаписатьПредупреждение(
					ТекстПредупреждения,
					ИмяСобытия,
					ОбъектИС);
				Если ТипЗнч(ОбъектыКУдалениюИзРегистрацииИзменений) = Тип("Массив") Тогда
					ОбъектыКУдалениюИзРегистрацииИзменений.Добавить(ОбъектИС);
				КонецЕсли;
			Иначе
				ТекстОшибки = СтрШаблон(
					НСтр("ru = 'Объект ""%1"" (%2) зарегистрирован к обмену данными со связанным объектом в 1С:Документооборот,
						|но не найдено подходящее правило загрузки данных в 1С:Документооборот.';
						|en = 'The ""%1"" (%2) object is registered to exchange data with the related object in 1C:Document Management.
						|However, no suitable data import rules are found.'"),
					ОбъектИС,
					ТипЗнч(ОбъектИС));
				ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаписатьОшибку(ТекстОшибки, ИмяСобытия, ОбъектИС);
			КонецЕсли;
			
			Продолжить;
		КонецЕсли;
		
		ИнтегрированныйОбъект = Новый Структура("Объект", ОбъектИС);
		
		Если СвязанныеОбъектыДО = Неопределено Тогда
			СвязанныйОбъектДО =
				РегистрыСведений.ОбъектыИнтегрированныеС1СДокументооборотом.ДанныеОбъектаДОПоВнешнемуОбъекту(ОбъектИС);
			Если СвязанныйОбъектДО = Неопределено Тогда
				ВызватьИсключение СтрШаблон(
					НСтр("ru = 'Объект ""%1"" (%2) не связан ни с каким объектом в 1С:Документообороте.';
						|en = 'The ""%1""(%2) object is not linked to any object in 1C:Document Management.'"),
					ОбъектИС,
					ТипЗнч(ОбъектИС));
			КонецЕсли;
			ИнтегрированныйОбъект.Вставить("ИдентификаторОбъектаДО", СвязанныйОбъектДО.ID);
			ИнтегрированныйОбъект.Вставить("ТипОбъектаДО", СвязанныйОбъектДО.type);
		Иначе
			ИнтегрированныйОбъект.Вставить("ИдентификаторОбъектаДО", СвязанныеОбъектыДО[ОбъектИС].ИдентификаторОбъектаДО);
			ИнтегрированныйОбъект.Вставить("ТипОбъектаДО", СвязанныеОбъектыДО[ОбъектИС].ТипОбъектаДО);
		КонецЕсли;
		
		ИнтегрированныйОбъект.Вставить("СписокВыгружаемыхТЧ", ПредварительныеДанные.СоответствиеСпискаВыгружаемыхТЧОбъектамИС[ОбъектИС]);
		ИнтегрированныйОбъект.Вставить("СписокВыражений", ПредварительныеДанные.СоответствиеСпискаВыраженийОбъектамИС[ОбъектИС]);
		ИнтегрированныйОбъект.Вставить("ПравилоЗагрузкиВДО", ПравилоЗагрузкиВДО);
		ИнтегрированныйОбъект.Вставить("ДоступенФункционалФайлов", ПредварительныеДанные.ДоступенФункционалФайлов);
		ИнтегрированныйОбъект.Вставить("ПечатныеФормы", Новый Массив);
		ИнтегрированныйОбъект.Вставить("ТипыФайловПечатныхФорм", Новый Массив);
		ИнтегрированныйОбъект.Вставить("ДоступенФункционалСозданияСвязей", ПредварительныеДанные.ДоступенФункционалСозданияСвязей);
		ИнтегрированныйОбъект.Вставить("СвязиКСозданию", ПредварительныеДанные.СоответствиеСоздаваемыхСвязейОбъектамИС[ОбъектИС]);
		
		Если ПредварительныеДанные.ДоступенФункционалФайлов Тогда
			ИнтегрированныйОбъект.ПечатныеФормы = ПредварительныеДанные.СоответствиеПечатныхФормОбъектамИС[ОбъектИС];
			ИнтегрированныйОбъект.ТипыФайловПечатныхФорм = ПредварительныеДанные.ТипыФайловПечатныхФорм[ОбъектИС];
		КонецЕсли;
		
		Если ТипЗнч(ОбъектыКУдалениюИзРегистрацииИзменений) = Тип("Массив") Тогда
			ОбъектыКУдалениюИзРегистрацииИзменений.Добавить(ОбъектИС);
		КонецЕсли;
		
		МассивДанных.Добавить(ИнтегрированныйОбъект);
		
	КонецЦикла;
	
	Возврат МассивДанных;
	
КонецФункции

#КонецОбласти