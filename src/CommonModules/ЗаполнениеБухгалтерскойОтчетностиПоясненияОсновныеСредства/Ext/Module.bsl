#Область ПрограммныйИнтерфейс

// Описывает состав прикладных данных таблицы отчета.
// Под таблицей понимается совокупность строк с одинаковым составом колонок.
// Обычно каждая таблица в отчете имеет отдельное название, номер и шапку с описанием колонок.
//
// Параметры:
//  Описание - см. ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйОписаниеТаблицыОтчета - заполняемая коллекция
//  ИмяТаблицыОтчета - Строка - имя таблицы из ЗаполнениеБухгалтерскойОтчетностиРазмещение.СоставТаблицОтчета
//
Процедура ОписатьТаблицуОтчета(Описание, ИмяТаблицыОтчета) Экспорт
	
	Если ИмяТаблицыОтчета = "ОсновныеСредстваНаличиеДвижение" Тогда
		ОписатьТаблицуОтчетаОсновныеСредстваНаличиеДвижение(Описание);
	ИначеЕсли ИмяТаблицыОтчета = "ПраваПользованияАктивамиНаличиеДвижение" Тогда
		ОписатьТаблицуОтчетаПраваПользованияАктивамиНаличиеДвижение(Описание);
	ИначеЕсли ИмяТаблицыОтчета = "КапитальныеВложенияОсновныеСредства" Тогда
		ОписатьТаблицуОтчетаКапитальныеВложенияОсновныеСредства(Описание);
	ИначеЕсли ИмяТаблицыОтчета = "ОсновныеСредстваАмортизируемыеНеамортизируемые" Тогда
		ОписатьТаблицуОтчетаОсновныеСредстваАмортизируемыеНеамортизируемые(Описание);
	ИначеЕсли ИмяТаблицыОтчета = "ОсновныеСредстваОграничения" Тогда
		ОписатьТаблицуОтчетаОсновныеСредстваОграничения(Описание);
	КонецЕсли;
	
КонецПроцедуры

// Определяет состав заполняемых пояснений конкретного отчета (за конкретный период, по конкретной организации)
//
// Параметры:
//  ТаблицыОтчета - Структура - Ключ - имя таблицы из ЗаполнениеБухгалтерскойОтчетностиРазмещение.СоставТаблицОтчета; Значение - Булево.
//                  Для заполняемых пояснений следует установить значение Истина, состав коллекции изменять не следует.
//  КонтекстОтчета - см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстОтчета
//
Процедура ОпределитьСоставЗаполняемыхПояснений(ТаблицыОтчета, КонтекстОтчета) Экспорт
	
	ТаблицыОтчета.ОсновныеСредстваНаличиеДвижение                = Истина;
	ТаблицыОтчета.ПраваПользованияАктивамиНаличиеДвижение        = Истина;
	ТаблицыОтчета.КапитальныеВложенияОсновныеСредства            = Истина;
	ТаблицыОтчета.ОсновныеСредстваАмортизируемыеНеамортизируемые = Истина;
	ТаблицыОтчета.ОсновныеСредстваОграничения                    = Истина;
	
КонецПроцедуры

// Заполняет таблицы отчета за один период.
//
// Параметры:
//  ПолучениеДанныхТаблицОтчета - см. ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйПолучениеДанныхТаблицОтчета -
//     процессор, который содержит исходные параметры для получения данных,
//     и в который нужно разместить полученные данные.
//  Классификаторы - Структура - Ключ: имя таблицы отчета; Значение: Структура - Ключ и Значение: имя колонки, содержащей значения по классификатору
//  КонтекстЗаполненияОтчета - см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета
//
Процедура ЗаполнитьТаблицыОтчета(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета) Экспорт
	
	ЗаполнитьПояснениеОсновныеСредства(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета);
	ЗаполнитьПояснениеКапитальныеВложения(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОписатьТаблицуОтчетаОсновныеСредстваНаличиеДвижение(Описание)
	
	Описание.Представление = НСтр("ru = 'Основные средства';
									|en = 'Fixed assets'");
	
	Описание.Структура.Добавить("ВариантУчета");
	Описание.Структура.Добавить("Группа");
	
	Описание.Классификаторы.Вставить("ВариантУчета", Новый СписокЗначений);
	Описание.Классификаторы.ВариантУчета.Добавить("ОсновныеСредства",           НСтр("ru = 'Основные средства (кроме инвестиционной недвижимости)';
																					|en = 'Fixed assets (except for investment property)'"));
	Описание.Классификаторы.ВариантУчета.Добавить("ИнвестиционнаяНедвижимость", НСтр("ru = 'Инвестиционная недвижимость';
																					|en = 'Инвестиционная недвижимость'"));
	
	ДобавитьОписаниеЧисловыеЗначенияНаличиеДвижение(Описание.ЧисловыеЗначения, Истина);
	
КонецПроцедуры

Процедура ОписатьТаблицуОтчетаПраваПользованияАктивамиНаличиеДвижение(Описание)
	
	Описание.Представление = НСтр("ru = 'Права пользования активами';
									|en = 'Права пользования активами'");
	
	Описание.Структура.Добавить("Группа");
	
	ДобавитьОписаниеЧисловыеЗначенияНаличиеДвижение(Описание.ЧисловыеЗначения, Ложь);
	
КонецПроцедуры

Процедура ДобавитьОписаниеЧисловыеЗначенияНаличиеДвижение(ЧисловыеЗначения, КПродаже)
	
	ЧисловыеЗначения.Добавить("НачалоПервоначальная");
	ЧисловыеЗначения.Добавить("НачалоАмортизация"); // и обесценение
	ЧисловыеЗначения.Добавить("Поступило");
	ЧисловыеЗначения.Добавить("СписаноПервоначальная");
	ЧисловыеЗначения.Добавить("СписаноАмортизация"); // и обесценение
	ЧисловыеЗначения.Добавить("Амортизация");
	ЧисловыеЗначения.Добавить("Обесценение");
	ЧисловыеЗначения.Добавить("ПереоцененоПервоначальная");
	ЧисловыеЗначения.Добавить("ПереоцененоАмортизация");
	
	Если КПродаже Тогда
		ЧисловыеЗначения.Добавить("ПереклассифицированоПервоначальная"); // в ДАП - см. п. 45(б) ФСБУ 6
		ЧисловыеЗначения.Добавить("ПереклассифицированоАмортизация"); // и обесценение
	КонецЕсли;
	
	ЧисловыеЗначения.Добавить("КонецПервоначальная");
	ЧисловыеЗначения.Добавить("КонецАмортизация");
	
КонецПроцедуры

Процедура ОписатьТаблицуОтчетаКапитальныеВложенияОсновныеСредства(Описание)
	
	Описание.Представление = НСтр("ru = 'Капитальные вложения в основные средства';
									|en = 'Capital expenditure in fixed assets'");
	
	Описание.Структура.Добавить("Стадия");
	Описание.Структура.Добавить("Группа");
	
	Описание.Классификаторы.Вставить("Стадия", Новый СписокЗначений);
	Описание.Классификаторы.Стадия.Добавить("Приобретение",        НСтр("ru = 'Приобретение основных средств';
																		|en = 'Fixed asset acquisition'"));
	Описание.Классификаторы.Стадия.Добавить("Создание",            НСтр("ru = 'Создание основных средств';
																		|en = 'Fixed asset creation'"));
	Описание.Классификаторы.Стадия.Добавить("ПоследующиеВложения", НСтр("ru = 'Улучшение и восстановление основных средств';
																		|en = 'Improvement and restoration of fixed assets'"));
	
	ЗаполнениеБухгалтерскойОтчетностиКапитальныеВложения.ДобавитьОписаниеЧисловыеЗначенияКапитальныеВложения(Описание.ЧисловыеЗначения);
	
КонецПроцедуры

Процедура ОписатьТаблицуОтчетаОсновныеСредстваАмортизируемыеНеамортизируемые(Описание)
	
	Описание.Представление = НСтр("ru = 'Амортизируемые и неамортизируемые основные средства';
									|en = 'Depreciable and non-depreciable fixed assets'");
	
	Описание.Структура.Добавить("Вид");
	// Не используем уровень структуры Основание ввиду ограничений механики размещения - см. ЗаполнениеБухгалтерскойОтчетностиРазмещение.ЗаполнитьПланКонтейнераОсновныеСредстваАмортизируемыеНеамортизируемые
	Описание.Структура.Добавить("Группа");
	
	Описание.Классификаторы.Вставить("Вид", Новый СписокЗначений);
	Описание.Классификаторы.Вид.Добавить("Амортизируемые",   НСтр("ru = 'Амортизируемые основные средства';
																	|en = 'Depreciable fixed assets'"));
	Описание.Классификаторы.Вид.Добавить("Неамортизируемые", НСтр("ru = 'Неамортизируемые основные средства';
																	|en = 'Non-depreciable fixed assets'"));
	
	// Не используем уровень структуры Основание ввиду ограничений механики размещения - см. ЗаполнениеБухгалтерскойОтчетностиРазмещение.ЗаполнитьПланКонтейнераОсновныеСредстваАмортизируемыеНеамортизируемые
	Описание.Классификаторы.Вставить("Основание", Новый СписокЗначений);
	Описание.Классификаторы.Основание.Добавить("Нет", НСтр("ru = 'Амортизируемые объекты';
															|en = 'Depreciable objects'"));
	Описание.Классификаторы.Основание.Добавить("ПостоянныеПотребительскиеСвойства", НСтр("ru = 'Объекты, потребительские свойства которых с течением времени не изменяются';
																						|en = 'Objects whose consumer properties do not change over time'"));
	
	Описание.ВариантПериода = "НачалоКонец";
	
	Описание.ЧисловыеЗначения.Добавить("БалансоваяСтоимость"); // НачалоБалансоваяСтоимость, КонецБалансоваяСтоимость
	
КонецПроцедуры

Процедура ОписатьТаблицуОтчетаОсновныеСредстваОграничения(Описание)
	
	Описание.Представление = НСтр("ru = 'Основные средства, в отношении которых имеются ограничения использования';
									|en = 'Fixed assets with restrictions on use'");
	
	Описание.Структура.Добавить("Показатель");
	Описание.Структура.Добавить("Группа");
	
	Описание.Классификаторы.Вставить("Показатель", Новый СписокЗначений);
	Описание.Классификаторы.Показатель.Добавить("Неиспользуемые",               НСтр("ru = 'Пригодные для использования, но не используемые';
																					|en = 'Usable, but not used'"));
	Описание.Классификаторы.Показатель.Добавить("ВАренде",                      НСтр("ru = 'Предоставленные во временное пользование';
																					|en = 'Provided for temporary use'"));
	Описание.Классификаторы.Показатель.Добавить("ОграничениеИмущественныхПрав", НСтр("ru = 'Имеющие ограничение имущественных прав';
																					|en = 'Having limited property rights'"));
	
	Описание.ВариантПериода = "НачалоКонец";
	
	Описание.ЧисловыеЗначения.Добавить("БалансоваяСтоимость"); // НачалоБалансоваяСтоимость, КонецБалансоваяСтоимость
	
КонецПроцедуры

Процедура ЗаполнитьПояснениеОсновныеСредства(ЗаполняемыеДанные, Классификаторы, КонтекстЗаполненияОтчета)
	
	Если ЗаполнениеБухгалтерскойОтчетностиТабличное.ТаблицаОтчета(ЗаполняемыеДанные, "ОсновныеСредстваНаличиеДвижение") = Неопределено
		И ЗаполнениеБухгалтерскойОтчетностиТабличное.ТаблицаОтчета(ЗаполняемыеДанные, "ПраваПользованияАктивамиНаличиеДвижение") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВариантыУчета = ВариантыУчетаОсновныхСредств();
	ОбщиеПравилаУчета = ОбщиеПравилаУчета();
	
	КлассификацияСчетов = КлассифицироватьСчетаПоИерархии(ВариантыУчета);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СоздатьСчетаУчетаОсновныхСредств(МенеджерВременныхТаблиц, КлассификацияСчетов, ОбщиеПравилаУчета);
	СоздатьСтоимостьОсновныхСредств(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, ОбщиеПравилаУчета);
	СоздатьАмортизацияЗапрещена(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, Классификаторы);
	СоздатьОграниченияОсновныхСредств(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, ОбщиеПравилаУчета, Классификаторы);
	СоздатьГруппыОсновныхСредств(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета);
	
	ЗаполнитьТаблицыНаличиеДвижение(ЗаполняемыеДанные, Классификаторы, КонтекстЗаполненияОтчета, МенеджерВременныхТаблиц, ВариантыУчета, ОбщиеПравилаУчета);
	
	ЗаполнитьТаблицуОсновныеСредстваАмортизируемыеНеамортизируемые(ЗаполняемыеДанные, Классификаторы, КонтекстЗаполненияОтчета, МенеджерВременныхТаблиц, ВариантыУчета, ОбщиеПравилаУчета);
	
	ЗаполнитьТаблицуОсновныеСредстваОграничения(ЗаполняемыеДанные, Классификаторы, КонтекстЗаполненияОтчета, МенеджерВременныхТаблиц, ВариантыУчета, ОбщиеПравилаУчета);
	
КонецПроцедуры

Процедура ЗаполнитьТаблицыНаличиеДвижение(ЗаполняемыеДанные, Классификаторы, КонтекстЗаполненияОтчета, МенеджерВременныхТаблиц, ВариантыУчета, ОбщиеПравилаУчета)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	""ОсновныеСредства"" КАК ВариантУчета,
	|	0 КАК Порядок
	|ПОМЕСТИТЬ ПорядокРазделовОтчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ИнвестиционнаяНедвижимость"",
	|	1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ПраваПользованияАктивами"",
	|	2
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВариантУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПорядокРазделовОтчета.ВариантУчета КАК ВариантУчета
	|ИЗ
	|	ПорядокРазделовОтчета КАК ПорядокРазделовОтчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокРазделовОтчета.Порядок";
	ПорядокВариантовУчета = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ВариантУчета");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	""Стоимость"" КАК ШагРасчета,
	|	10 КАК Порядок
	|ПОМЕСТИТЬ ПорядокШаговРасчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""СписаноОбесценение"",
	|	20
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""КПродаже"",
	|	30
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Перемещение"",
	|	40
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""МежфилиальныйОборот"",
	|	50
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Переоценка"",
	|	60
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ШагРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаУчетаОсновныхСредств.ВариантУчета КАК ВариантУчета,
	|	ЕСТЬNULL(ПорядокРазделовОтчета.Порядок, 255) КАК РазделОтчетаПорядок,
	|	ГруппыОсновныхСредств.Группа КАК Группа,
	|	ЕСТЬNULL(ГруппыОсновныхСредств.Группа.Порядок, 255) КАК ГруппаПорядок,
	|	СтоимостьОсновныхСредств.Организация КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(СтоимостьОсновныхСредств.Организация) КАК ОрганизацияПредставление,
	|	СтоимостьОсновныхСредств.Организация.Наименование КАК ОрганизацияПорядок,
	|	СчетаУчетаОсновныхСредств.ВидСчета КАК ВидСчета,
	|	СтоимостьОсновныхСредств.Счет КАК Счет,
	|	СчетаУчетаОсновныхСредств.Порядок КАК СчетПорядок,
	|	СтоимостьОсновныхСредств.ШагРасчета КАК ШагРасчета,
	|	ЕСТЬNULL(ПорядокШаговРасчета.Порядок, 255) КАК ШагРасчетаПорядок,
	|	СтоимостьОсновныхСредств.КорСчет КАК КорСчет,
	|	СтоимостьОсновныхСредств.КорСчет.Порядок КАК КорСчетПорядок,
	|	СУММА(СтоимостьОсновныхСредств.Начало) КАК Начало,
	|	СУММА(СтоимостьОсновныхСредств.Конец) КАК Конец,
	|	СУММА(СтоимостьОсновныхСредств.Дт) КАК Дт,
	|	СУММА(СтоимостьОсновныхСредств.Кт) КАК Кт
	|ИЗ
	|	СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГруппыОсновныхСредств КАК ГруппыОсновныхСредств
	|		ПО СтоимостьОсновныхСредств.Объект = ГруппыОсновныхСредств.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПорядокШаговРасчета КАК ПорядокШаговРасчета
	|		ПО СтоимостьОсновныхСредств.ШагРасчета = ПорядокШаговРасчета.ШагРасчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаУчетаОсновныхСредств КАК СчетаУчетаОсновныхСредств
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокРазделовОтчета КАК ПорядокРазделовОтчета
	|			ПО СчетаУчетаОсновныхСредств.ВариантУчета = ПорядокРазделовОтчета.ВариантУчета
	|		ПО СтоимостьОсновныхСредств.Счет = СчетаУчетаОсновныхСредств.Счет
	|
	|СГРУППИРОВАТЬ ПО
	|	ГруппыОсновныхСредств.Группа,
	|	СтоимостьОсновныхСредств.КорСчет.Порядок,
	|	СтоимостьОсновныхСредств.Организация.Наименование,
	|	СтоимостьОсновныхСредств.Счет,
	|	СчетаУчетаОсновныхСредств.ВидСчета,
	|	СтоимостьОсновныхСредств.ШагРасчета,
	|	СчетаУчетаОсновныхСредств.ВариантУчета,
	|	СчетаУчетаОсновныхСредств.Порядок,
	|	СтоимостьОсновныхСредств.Организация,
	|	СтоимостьОсновныхСредств.КорСчет,
	|	ЕСТЬNULL(ПорядокРазделовОтчета.Порядок, 255),
	|	ЕСТЬNULL(ГруппыОсновныхСредств.Группа.Порядок, 255),
	|	ЕСТЬNULL(ПорядокШаговРасчета.Порядок, 255)
	|
	|УПОРЯДОЧИТЬ ПО
	|	РазделОтчетаПорядок,
	|	ВариантУчета,
	|	ГруппаПорядок,
	|	Группа,
	|	ОрганизацияПорядок,
	|	Организация,
	|	СчетПорядок,
	|	Счет,
	|	ШагРасчетаПорядок,
	|	ШагРасчета,
	|	КорСчетПорядок,
	|	КорСчет
	|ИТОГИ
	|	МАКСИМУМ(РазделОтчетаПорядок),
	|	МАКСИМУМ(ГруппаПорядок)
	|ПО
	|	ВариантУчета,
	|	Группа,
	|	Организация";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаВариантУчета = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ШаблоныЗаголовковКолонок = Новый Соответствие; // Одинаковые для обеих таблиц. Ключ: имя колонки; Значение: Строка, шаблон
	ШаблоныЗаголовковРасшифровки = Новый Структура; // Ключ: имя варианта; Значение: Соответствие - Ключ: имя колонки; Значение - ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки
	Для Каждого ВариантУчета Из ПорядокВариантовУчета Цикл
		ШаблоныЗаголовковРасшифровкиКолонки = Новый Соответствие;
		ЗаполнитьШаблоныКолонокОсновныеСредства(ШаблоныЗаголовковКолонок, ШаблоныЗаголовковРасшифровкиКолонки, ВариантУчета);
		ШаблоныЗаголовковРасшифровки.Вставить(ВариантУчета, ШаблоныЗаголовковРасшифровкиКолонки);
	КонецЦикла;
	
	ПараметрыТекстовРасшифровки = Новый Структура;
	УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстовРасшифровки, ОбщиеПравилаУчета, КонтекстЗаполненияОтчета);
	
	ОсновныеСредстваНаличиеДвижение = ЗаполнениеБухгалтерскойОтчетностиТабличное.НачатьВыводТаблицы(
		ЗаполняемыеДанные,
		"ОсновныеСредстваНаличиеДвижение",
		ШаблоныЗаголовковКолонок,
		ПараметрыТекстовРасшифровки);
	
	ПраваПользованияАктивамиНаличиеДвижение = ЗаполнениеБухгалтерскойОтчетностиТабличное.НачатьВыводТаблицы(
		ЗаполняемыеДанные,
		"ПраваПользованияАктивамиНаличиеДвижение",
		ШаблоныЗаголовковКолонок,
		ПараметрыТекстовРасшифровки);
	
	Для Каждого ВариантУчета Из ПорядокВариантовУчета Цикл
		
		НаличиеДвижение = Неопределено;
		Если ВариантУчета = "ОсновныеСредства" Или ВариантУчета = "ИнвестиционнаяНедвижимость" Тогда
			НаличиеДвижение = ОсновныеСредстваНаличиеДвижение;
		ИначеЕсли ВариантУчета = "ПраваПользованияАктивами" Тогда
			НаличиеДвижение = ПраваПользованияАктивамиНаличиеДвижение;
		КонецЕсли;
		
		Если НаличиеДвижение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ШаблоныЗаголовковРасшифровкиКолонки = ШаблоныЗаголовковРасшифровки[ВариантУчета];
		УстановитьПараметрыТекстовРасшифровкиВариантыУчета(
			ПараметрыТекстовРасшифровки,
			ВариантУчета,
			ВариантыУчета[ВариантУчета],
			МенеджерВременныхТаблиц);
		
		ЕстьДанныеВариантаУчета = ВыборкаВариантУчета.НайтиСледующий(ВариантУчета, "ВариантУчета");
		
		Для Каждого Группа Из Перечисления.ГруппыОС Цикл
			
			УстановитьПараметрыТекстовРасшифровкиГруппы(ПараметрыТекстовРасшифровки, Группа);
			
			ЕстьДанныеГруппы = Ложь;
			Если ЕстьДанныеВариантаУчета Тогда
				ВыборкаГруппа = ВыборкаВариантУчета.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				ЕстьДанныеГруппы = ВыборкаГруппа.НайтиСледующий(Группа, "Группа");
			КонецЕсли;
			
			ВыводитьГруппу = (ЕстьДанныеГруппы Или ЭтоОбязательнаяГруппа(Группа, ВариантУчета));
			
			Если ВыводитьГруппу Тогда
				
				НачатьДобавлениеСтрокиОсновныеСредства(
					НаличиеДвижение,
					ВариантУчета,
					Группа,
					ШаблоныЗаголовковРасшифровкиКолонки,
					ПараметрыТекстовРасшифровки,
					Классификаторы);
				
			КонецЕсли;
			
			Если ЕстьДанныеГруппы Тогда
				
				ВыборкаОрганизация = ВыборкаГруппа.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаОрганизация.Следующий() Цикл
					
					ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьФилиал(
						НаличиеДвижение,
						ШаблоныЗаголовковРасшифровкиКолонки,
						ВыборкаОрганизация.Организация,
						ВыборкаОрганизация.ОрганизацияПредставление);
					
					Выборка = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока Выборка.Следующий() Цикл
						
						ЗначенияНастроекОтчета = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.ЗначенияНастроекОтчетаОсновныеСредства(Выборка);
						Если Выборка.ШагРасчета = "Стоимость" Тогда
							Отчет = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастройкиОтчетаПоЗначениям(ЗначенияНастроекОтчета, КонтекстЗаполненияОтчета.Период);
							Если Выборка.ВидСчета = "Первоначальная" Тогда
								ДобавитьСлагаемоеОборотПоСчету(НаличиеДвижение, "НачалоПервоначальная",  Выборка, Выборка.Начало, Отчет);
								ДобавитьСлагаемоеОборотПоСчету(НаличиеДвижение, "КонецПервоначальная",   Выборка, Выборка.Конец, Отчет);
								ДобавитьСлагаемоеОборотПоСчету(НаличиеДвижение, "Поступило",             Выборка, Выборка.Дт, Отчет);
								ДобавитьСлагаемоеОборотПоСчету(НаличиеДвижение, "СписаноПервоначальная", Выборка, Выборка.Кт, Отчет);
							Иначе
								ДобавитьСлагаемоеОборотПоСчету(НаличиеДвижение, "НачалоАмортизация",     Выборка, Выборка.Начало, Отчет);
								ДобавитьСлагаемоеОборотПоСчету(НаличиеДвижение, "КонецАмортизация",      Выборка, Выборка.Конец, Отчет);
								Если Выборка.ВидСчета = "Обесценение" Тогда
									ДобавитьСлагаемоеОборотПоСчету(НаличиеДвижение, "Обесценение",           Выборка, Выборка.Дт + Выборка.Кт, Отчет);
								Иначе
									ДобавитьСлагаемоеОборотПоСчету(НаличиеДвижение, "СписаноАмортизация",    Выборка, Выборка.Дт, Отчет);
									ДобавитьСлагаемоеОборотПоСчету(НаличиеДвижение, "Амортизация",           Выборка, Выборка.Кт, Отчет);
								КонецЕсли;
							КонецЕсли;
							
						Иначе
							
							Объяснение = "";
							ФормулаПроводок = СтрШаблон(НСтр("ru = 'оборот по счету %1 в корреспонденции с %2';
															|en = '%1 turnover in correspondence with %2'"), Выборка.Счет, Выборка.КорСчет);
							
							Если Выборка.ШагРасчета = "Перемещение" Тогда
								Объяснение = НСтр("ru = 'Перемещение основных средств';
													|en = 'Transfer fixed assets'"); // по тому же объекту основных средств
							ИначеЕсли Выборка.ШагРасчета = "МежфилиальныйОборот" Тогда
								Объяснение = НСтр("ru = 'Перемещение между филиалами';
													|en = 'Transfer between branch offices'");
							ИначеЕсли Выборка.ШагРасчета = "СписаноОбесценение" Тогда
								Объяснение = НСтр("ru = 'Накопленное обесценение при выбытии';
													|en = 'Accumulated impairment on retirement'");
							ИначеЕсли Выборка.ШагРасчета = "КПродаже" Тогда
								Объяснение = НСтр("ru = 'Переклассифицировано в долгосрочные активы к продаже';
													|en = 'Reclassified to long-term assets for sale'");
								НастроитьОтчетДолгосрочныеАктивыКПродаже(ЗначенияНастроекОтчета, Выборка);
							ИначеЕсли Выборка.ШагРасчета = "Переоценено" Тогда
								Объяснение = НСтр("ru = 'Переоценка внеоборотных активов';
													|en = 'Revaluation of capital assets'");
								ЗаполнениеБухгалтерскойОтчетностиРасшифровка.УстановитьОтборВидДоходовРасходовПереоценка(ЗначенияНастроекОтчета);
							КонецЕсли;
							Если Выборка.ШагРасчета = "КПродаже" Тогда
								ФормулаПроводок = СтрШаблон(НСтр("ru = 'оборот по счету %1 в корреспонденции с %2 по объектам, имеющим на отчетную дату сальдо по счету %2';
																|en = '%1 turnover in correspondence with %2 by objects having a balance on %2 on the reporting date'"), Выборка.Счет, Выборка.КорСчет);
							КонецЕсли;
							
							Описание = ОбщегоНазначенияБПКлиентСервер.КапитализироватьСтроку(ФормулаПроводок);
							ОписаниеВычет = СтрШаблон(НСтр("ru = 'За вычетом: %1 (%2)';
															|en = 'Minus: %1 (%2)'"), Объяснение, ФормулаПроводок);
							Отчет = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастройкиОтчетаПоЗначениям(ЗначенияНастроекОтчета, КонтекстЗаполненияОтчета.Период);
							Если Выборка.ШагРасчета = "СписаноОбесценение" Тогда
								ПеренестиМеждуКолонками(НаличиеДвижение, Выборка.Дт, "Обесценение", ОписаниеВычет, "СписаноАмортизация", Описание, Выборка.КорСчет, Отчет);
							ИначеЕсли Выборка.ШагРасчета = "КПродаже" Тогда
								Если Выборка.ВидСчета = "Первоначальная" Тогда
									ПеренестиМеждуКолонками(НаличиеДвижение, Выборка.Кт, "СписаноПервоначальная", ОписаниеВычет, "ПереклассифицированоПервоначальная", Описание, Выборка.КорСчет, Отчет);
								Иначе
									ПеренестиМеждуКолонками(НаличиеДвижение, Выборка.Дт, "СписаноАмортизация", ОписаниеВычет, "ПереклассифицированоАмортизация", Описание, Выборка.КорСчет, Отчет);
								КонецЕсли;
							ИначеЕсли Выборка.ШагРасчета = "Переоценено" Тогда
								Если Выборка.ВидСчета = "Первоначальная" Тогда
									ПеренестиМеждуКолонками(НаличиеДвижение, Выборка.Дт, "Поступило", ОписаниеВычет, "ПереоцененоПервоначальная", Описание, Выборка.КорСчет, Отчет);
								Иначе
									ПеренестиМеждуКолонками(НаличиеДвижение, Выборка.Кт, "Амортизация", ОписаниеВычет, "ПереоцененоАмортизация", Описание, Выборка.КорСчет, Отчет);
								КонецЕсли;
							ИначеЕсли Выборка.ВидСчета = "Первоначальная" Тогда
								ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(НаличиеДвижение, "Поступило", ОписаниеВычет, Выборка.Дт, Выборка.КорСчет, Отчет);
								ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(НаличиеДвижение, "СписаноПервоначальная", ОписаниеВычет, Выборка.Кт, Выборка.КорСчет, Отчет);
							ИначеЕсли Выборка.ВидСчета = "Обесценение" Тогда
								ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(НаличиеДвижение, "Обесценение", ОписаниеВычет, Выборка.Дт + Выборка.Кт, Выборка.КорСчет, Отчет);
							Иначе
								ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(НаличиеДвижение, "СписаноАмортизация", ОписаниеВычет, Выборка.Дт, Выборка.КорСчет, Отчет);
								ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(НаличиеДвижение, "Амортизация", ОписаниеВычет, Выборка.Кт, Выборка.КорСчет, Отчет);
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЦикла; // По ВыборкаОрганизация
				КонецЦикла; // По ВыборкаГруппа
				
			КонецЕсли;
			
			Если ВыводитьГруппу Тогда
				ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(НаличиеДвижение, ШаблоныЗаголовковРасшифровкиКолонки);
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьТаблицуОсновныеСредстваАмортизируемыеНеамортизируемые(ЗаполняемыеДанные, Классификаторы, КонтекстЗаполненияОтчета, МенеджерВременныхТаблиц, ВариантыУчета, ОбщиеПравилаУчета)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	АмортизируемыеНеамортизируемые.Основание КАК Основание,
	|	АмортизируемыеНеамортизируемые.Объект КАК Объект,
	|	ВЫРАЗИТЬ(АмортизируемыеНеамортизируемые.Организация КАК Справочник.Организации) КАК Организация,
	|	АмортизируемыеНеамортизируемые.Счет КАК Счет,
	|	СУММА(АмортизируемыеНеамортизируемые.Начало) КАК Начало,
	|	СУММА(АмортизируемыеНеамортизируемые.Конец) КАК Конец
	|ПОМЕСТИТЬ ВТ_АмортизируемыеНеамортизируемые
	|ИЗ
	|	(ВЫБРАТЬ
	|		NULL КАК Основание,
	|		Амортизируемые.Объект КАК Объект,
	|		Амортизируемые.Организация КАК Организация,
	|		Амортизируемые.Счет КАК Счет,
	|		Амортизируемые.Начало КАК Начало,
	|		Амортизируемые.Конец КАК Конец
	|	ИЗ
	|		СтоимостьОсновныхСредств КАК Амортизируемые
	|			ЛЕВОЕ СОЕДИНЕНИЕ АмортизацияЗапрещена КАК АмортизацияЗапрещена
	|			ПО Амортизируемые.Объект = АмортизацияЗапрещена.Объект
	|				И Амортизируемые.Организация = АмортизацияЗапрещена.Организация
	|	ГДЕ
	|		АмортизацияЗапрещена.Объект ЕСТЬ NULL
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА АмортизацияЗапрещена.Начало
	|				ТОГДА АмортизацияЗапрещена.Основание
	|		КОНЕЦ,
	|		НеамортизируемыеНачало.Объект,
	|		НеамортизируемыеНачало.Организация,
	|		НеамортизируемыеНачало.Счет,
	|		НеамортизируемыеНачало.Начало,
	|		0
	|	ИЗ
	|		СтоимостьОсновныхСредств КАК НеамортизируемыеНачало
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ АмортизацияЗапрещена КАК АмортизацияЗапрещена
	|			ПО НеамортизируемыеНачало.Объект = АмортизацияЗапрещена.Объект
	|				И НеамортизируемыеНачало.Организация = АмортизацияЗапрещена.Организация
	|	ГДЕ
	|		НеамортизируемыеНачало.Начало <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА АмортизацияЗапрещена.Конец
	|				ТОГДА АмортизацияЗапрещена.Основание
	|		КОНЕЦ,
	|		НеамортизируемыеКонец.Объект,
	|		НеамортизируемыеКонец.Организация,
	|		НеамортизируемыеКонец.Счет,
	|		0,
	|		НеамортизируемыеКонец.Конец
	|	ИЗ
	|		СтоимостьОсновныхСредств КАК НеамортизируемыеКонец
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ АмортизацияЗапрещена КАК АмортизацияЗапрещена
	|			ПО НеамортизируемыеКонец.Объект = АмортизацияЗапрещена.Объект
	|				И НеамортизируемыеКонец.Организация = АмортизацияЗапрещена.Организация
	|	ГДЕ
	|		НеамортизируемыеКонец.Конец <> 0) КАК АмортизируемыеНеамортизируемые
	|
	|СГРУППИРОВАТЬ ПО
	|	АмортизируемыеНеамортизируемые.Основание,
	|	АмортизируемыеНеамортизируемые.Объект,
	|	ВЫРАЗИТЬ(АмортизируемыеНеамортизируемые.Организация КАК Справочник.Организации),
	|	АмортизируемыеНеамортизируемые.Счет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АмортизируемыеНеамортизируемые.Основание КАК Основание,
	|	ГруппыОсновныхСредств.Группа КАК Группа,
	|	ЕСТЬNULL(ГруппыОсновныхСредств.Группа.Порядок, 255) КАК ГруппаПорядок,
	|	АмортизируемыеНеамортизируемые.Организация КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(АмортизируемыеНеамортизируемые.Организация) КАК ОрганизацияПредставление,
	|	АмортизируемыеНеамортизируемые.Организация.Наименование КАК ОрганизацияПорядок,
	|	АмортизируемыеНеамортизируемые.Счет КАК Счет,
	|	СчетаУчетаОсновныхСредств.Порядок КАК СчетПорядок,
	|	АмортизируемыеНеамортизируемые.Начало КАК Начало,
	|	АмортизируемыеНеамортизируемые.Конец КАК Конец
	|ИЗ
	|	ВТ_АмортизируемыеНеамортизируемые КАК АмортизируемыеНеамортизируемые
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаУчетаОсновныхСредств КАК СчетаУчетаОсновныхСредств
	|		ПО АмортизируемыеНеамортизируемые.Счет = СчетаУчетаОсновныхСредств.Счет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГруппыОсновныхСредств КАК ГруппыОсновныхСредств
	|		ПО АмортизируемыеНеамортизируемые.Объект = ГруппыОсновныхСредств.Объект
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГруппаПорядок,
	|	Группа,
	|	ОрганизацияПорядок,
	|	Организация,
	|	СчетПорядок,
	|	Счет
	|ИТОГИ
	|	МАКСИМУМ(ГруппаПорядок),
	|	МАКСИМУМ(СчетПорядок),
	|	СУММА(Начало),
	|	СУММА(Конец)
	|ПО
	|	Основание,
	|	Группа,
	|	Организация,
	|	Счет";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаОснование = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ШаблоныЗаголовковКолонок = Новый Соответствие; // Ключ: имя колонки; Значение: Строка, шаблон
	ШаблоныЗаголовковКолонок.Вставить("НачалоБалансоваяСтоимость", НСтр("ru = 'балансовая стоимость на [КонецПредшествующегоПериода]';
																		|en = 'net book value as of [КонецПредшествующегоПериода]'"));
	ШаблоныЗаголовковКолонок.Вставить("КонецБалансоваяСтоимость",  НСтр("ru = 'балансовая стоимость на [КонецПериода]';
																		|en = 'Net book value as of [КонецПериода]'"));
	
	ПараметрыТекстов = Новый Структура;
	ЗаполнениеБухгалтерскойОтчетностиТабличное.УстановитьПараметрыТекстовРасшифровки(
		ПараметрыТекстов,
		КонтекстЗаполненияОтчета);
	
	АмортизируемыеНеамортизируемые = ЗаполнениеБухгалтерскойОтчетностиТабличное.НачатьВыводТаблицы(
		ЗаполняемыеДанные,
		"ОсновныеСредстваАмортизируемыеНеамортизируемые",
		ШаблоныЗаголовковКолонок,
		ПараметрыТекстов);
		
	ШаблонКолонкиАмортизируемые = ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки();
	
	ШаблонКолонкиАмортизируемые.ЗаголовокГруппы = НСтр("ru = '[НаименованиеГруппы] (амортизируемые): [ПредставлениеКолонки]';
														|en = '[НаименованиеГруппы] (depreciable): [ПредставлениеКолонки]'");
	ШаблонКолонкиАмортизируемые.ПорядокРасчета  = НСтр("ru = 'Сальдо по [СчетуБалансовойСтоимости] по основным средствам из группы учета [НаименованиеГруппы], кроме списка неамортизируемых объектов';
														|en = '[СчетуБалансовойСтоимости] balance for fixed assets from the [НаименованиеГруппы] accounting group, except for the list of non-depreciable objects'");
	
	ШаблонКолонкиНеамортизируемые = ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки();
	
	ШаблонКолонкиНеамортизируемые.ЗаголовокГруппы = НСтр("ru = '[НаименованиеГруппы] (неамортизируемые): [ПредставлениеКолонки]';
														|en = '[НаименованиеГруппы] (non-depreciable): [ПредставлениеКолонки]'");
	ШаблонКолонкиНеамортизируемые.ПорядокРасчета  = НСтр("ru = 'Сальдо по [СчетуБалансовойСтоимости] по списку основных средств из группы учета [НаименованиеГруппы].
                                                          |Список неамортизируемых объектов определяется по [СпособОпределенияОбъектов]';
                                                          |en = '[СчетуБалансовойСтоимости] balance for the list of fixed assets from the [НаименованиеГруппы] accounting group.
                                                          |The list of non-depreciable objects is defined by [СпособОпределенияОбъектов]'");
	
	ПредставлениеСписка = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(ВсеСчета(ВариантыУчета));
	ПараметрыТекстов.Вставить(
	"СчетуБалансовойСтоимости",
		ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовДательныйПадеж(ПредставлениеСписка));
	
	СпособОпределенияОбъектовПостоянные = СтрШаблон(
		НСтр("ru = 'данным регистра %1 - см. документ %2';
			|en = 'the %1 register data - see the %2 document'"),
		Метаданные.РегистрыСведений.ПорядокУчетаОСБУ.Представление(),
		Метаданные.Документы.ИзменениеСостоянияОС.Представление());
	
	СпособыОпределенияОбъектов = Новый Соответствие;
	СпособыОпределенияОбъектов.Вставить(
		Классификаторы.ОсновныеСредстваАмортизируемыеНеамортизируемые.Основание.ПостоянныеПотребительскиеСвойства,
		СпособОпределенияОбъектовПостоянные);
	
	Пока ВыборкаОснование.Следующий() Цикл
		
		ШаблоныЗаголовковРасшифровкиКолонки = Новый Соответствие;
		
		СпособОпределенияОбъектовОснования = СпособыОпределенияОбъектов[ВыборкаОснование.Основание];
		
		Если СпособОпределенияОбъектовОснования = Неопределено Тогда
			ШаблоныЗаголовковРасшифровкиКолонки.Вставить("НачалоБалансоваяСтоимость", ШаблонКолонкиАмортизируемые);
			ШаблоныЗаголовковРасшифровкиКолонки.Вставить("КонецБалансоваяСтоимость",  ШаблонКолонкиАмортизируемые);
		Иначе
			ШаблоныЗаголовковРасшифровкиКолонки.Вставить("НачалоБалансоваяСтоимость", ШаблонКолонкиНеамортизируемые);
			ШаблоныЗаголовковРасшифровкиКолонки.Вставить("КонецБалансоваяСтоимость",  ШаблонКолонкиНеамортизируемые);
			ПараметрыТекстов.Вставить("СпособОпределенияОбъектов", СпособОпределенияОбъектовОснования);
		КонецЕсли;
		
		ВыборкаГруппа = ВыборкаОснование.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаГруппа.Следующий() Цикл
			
			УстановитьПараметрыТекстовРасшифровкиГруппы(ПараметрыТекстов, ВыборкаГруппа.Группа);
			
			НачатьДобавлениеСтрокиОсновныеСредстваАмортизируемыеНеамортизируемые(
				АмортизируемыеНеамортизируемые,
				ВыборкаГруппа.Основание,
				ВыборкаГруппа.Группа,
				ШаблоныЗаголовковРасшифровкиКолонки,
				ПараметрыТекстов,
				Классификаторы);
			
			ВыборкаОрганизация = ВыборкаГруппа.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаОрганизация.Следующий() Цикл
				
				ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьФилиал(
					АмортизируемыеНеамортизируемые,
					ШаблоныЗаголовковРасшифровкиКолонки,
					ВыборкаОрганизация.Организация,
					ВыборкаОрганизация.ОрганизацияПредставление);
				
				ВыборкаСчет = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаСчет.Следующий() Цикл
					
					Отчет = Неопределено;
					ДобавитьСлагаемоеОборотПоСчету(АмортизируемыеНеамортизируемые, "НачалоБалансоваяСтоимость",  ВыборкаСчет, ВыборкаСчет.Начало, Отчет);
					ДобавитьСлагаемоеОборотПоСчету(АмортизируемыеНеамортизируемые, "КонецБалансоваяСтоимость",   ВыборкаСчет, ВыборкаСчет.Конец, Отчет);
					
				КонецЦикла;
				
			КонецЦикла;
			
			ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(АмортизируемыеНеамортизируемые, ШаблоныЗаголовковРасшифровкиКолонки);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТаблицуОсновныеСредстваОграничения(ЗаполняемыеДанные, Классификаторы, КонтекстЗаполненияОтчета, МенеджерВременныхТаблиц, ВариантыУчета, ОбщиеПравилаУчета)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОграниченияОсновныхСредств.Показатель КАК Показатель,
	|	ГруппыОсновныхСредств.Группа КАК Группа,
	|	ЕСТЬNULL(ГруппыОсновныхСредств.Группа.Порядок, 255) КАК ГруппаПорядок,
	|	СтоимостьОсновныхСредств.Организация КАК Организация,
	|	ПРЕДСТАВЛЕНИЕ(СтоимостьОсновныхСредств.Организация) КАК ОрганизацияПредставление,
	|	СтоимостьОсновныхСредств.Организация.Наименование КАК ОрганизацияПорядок,
	|	СчетаУчетаОсновныхСредств.ВидСчета КАК ВидСчета,
	|	СтоимостьОсновныхСредств.Счет КАК Счет,
	|	СчетаУчетаОсновныхСредств.Порядок КАК СчетПорядок,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА ОграниченияОсновныхСредств.Начало
	|					ТОГДА СтоимостьОсновныхСредств.Начало
	|			КОНЕЦ), 0) КАК Начало,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА ОграниченияОсновныхСредств.Конец
	|					ТОГДА СтоимостьОсновныхСредств.Конец
	|			КОНЕЦ), 0) КАК Конец
	|ИЗ
	|	СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОграниченияОсновныхСредств КАК ОграниченияОсновныхСредств
	|		ПО СтоимостьОсновныхСредств.Объект = ОграниченияОсновныхСредств.Объект
	|			И СтоимостьОсновныхСредств.Организация = ОграниченияОсновныхСредств.Организация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетаУчетаОсновныхСредств КАК СчетаУчетаОсновныхСредств
	|		ПО СтоимостьОсновныхСредств.Счет = СчетаУчетаОсновныхСредств.Счет
	|			И (СчетаУчетаОсновныхСредств.ВариантУчета = ""ОсновныеСредства"")
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ГруппыОсновныхСредств КАК ГруппыОсновныхСредств
	|		ПО СтоимостьОсновныхСредств.Объект = ГруппыОсновныхСредств.Объект
	|ГДЕ
	|	СтоимостьОсновныхСредств.ШагРасчета = ""Стоимость""
	|
	|СГРУППИРОВАТЬ ПО
	|	ОграниченияОсновныхСредств.Показатель,
	|	ГруппыОсновныхСредств.Группа,
	|	СтоимостьОсновныхСредств.Организация.Наименование,
	|	СтоимостьОсновныхСредств.Счет,
	|	СчетаУчетаОсновныхСредств.ВидСчета,
	|	СтоимостьОсновныхСредств.ШагРасчета,
	|	СчетаУчетаОсновныхСредств.ВариантУчета,
	|	СчетаУчетаОсновныхСредств.Порядок,
	|	СтоимостьОсновныхСредств.Организация,
	|	ЕСТЬNULL(ГруппыОсновныхСредств.Группа.Порядок, 255)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГруппаПорядок,
	|	Группа,
	|	ОрганизацияПорядок,
	|	Организация,
	|	СчетПорядок,
	|	Счет
	|ИТОГИ
	|	МАКСИМУМ(ГруппаПорядок)
	|ПО
	|	Показатель,
	|	Группа,
	|	Организация";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаПоказатель = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ШаблоныЗаголовковКолонок = Новый Соответствие; // Ключ: имя колонки; Значение: Строка, шаблон
	ШаблоныЗаголовковКолонок.Вставить("НачалоБалансоваяСтоимость", НСтр("ru = 'балансовая стоимость на [КонецПредшествующегоПериода]';
																		|en = 'net book value as of [КонецПредшествующегоПериода]'"));
	ШаблоныЗаголовковКолонок.Вставить("КонецБалансоваяСтоимость",  НСтр("ru = 'балансовая стоимость на [КонецПериода]';
																		|en = 'Net book value as of [КонецПериода]'"));
	
	ПараметрыТекстов = Новый Структура;
	ЗаполнениеБухгалтерскойОтчетностиТабличное.УстановитьПараметрыТекстовРасшифровки(
		ПараметрыТекстов,
		КонтекстЗаполненияОтчета);
	
	ОсновныеСредстваОграничения = ЗаполнениеБухгалтерскойОтчетностиТабличное.НачатьВыводТаблицы(
		ЗаполняемыеДанные,
		"ОсновныеСредстваОграничения",
		ШаблоныЗаголовковКолонок,
		ПараметрыТекстов);
		
	ШаблонКолонки = ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки();
	
	ШаблонКолонки.ЗаголовокГруппы = НСтр("ru = '[НаименованиеГруппы]: [ПредставлениеКолонки]';
										|en = '[НаименованиеГруппы]: [ПредставлениеКолонки]'");
	ШаблонКолонки.ПорядокРасчета  = НСтр("ru = 'Сальдо по [СчетуБалансовойСтоимости] по списку основных средств из группы учета [НаименованиеГруппы].
                                    |Список объектов определяется по [СпособОпределенияОбъектов]';
                                    |en = '[СчетуБалансовойСтоимости] balance for the list of fixed assets from the [НаименованиеГруппы] accounting group.
                                    |The list of objects is defined by [СпособОпределенияОбъектов]'");
	
	ПредставлениеСписка = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(ВариантыУчета.ОсновныеСредства);
	ПараметрыТекстов.Вставить(
		"СчетуБалансовойСтоимости",
		ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовДательныйПадеж(ПредставлениеСписка));
	
	СпособОпределенияОбъектовВАренде = СтрШаблон(
		НСтр("ru = 'наличию сальдо на счете %1, регистру %2 (находящимся в аренде соответствует заполненное поле %3)';
			|en = 'there is a balance on %1, the %2 register (the filled %3 field corresponds to the leased ones)'"),
		ОбщиеПравилаУчета.СчетВАренде,
		Метаданные.РегистрыСведений.МестонахождениеОС.Представление(),
		Метаданные.РегистрыСведений.МестонахождениеОС.Ресурсы.Арендатор.Представление());
	
	СпособыОпределенияОбъектов = Новый Соответствие;
	СпособыОпределенияОбъектов.Вставить(
		Классификаторы.ОсновныеСредстваОграничения.Показатель.ВАренде,
		СпособОпределенияОбъектовВАренде);
	
	Пока ВыборкаПоказатель.Следующий() Цикл
		
		ШаблоныЗаголовковРасшифровкиКолонки = Новый Соответствие;
		СпособОпределенияОбъектовПоказателя = СпособыОпределенияОбъектов[ВыборкаПоказатель.Показатель];
		
		Если СпособОпределенияОбъектовПоказателя <> Неопределено Тогда
			ШаблоныЗаголовковРасшифровкиКолонки.Вставить("НачалоБалансоваяСтоимость", ШаблонКолонки);
			ШаблоныЗаголовковРасшифровкиКолонки.Вставить("КонецБалансоваяСтоимость",  ШаблонКолонки);
			ПараметрыТекстов.Вставить("СпособОпределенияОбъектов", СпособОпределенияОбъектовПоказателя);
		КонецЕсли;
		
		ВыборкаГруппа = ВыборкаПоказатель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаГруппа.Следующий() Цикл

			УстановитьПараметрыТекстовРасшифровкиГруппы(ПараметрыТекстов, ВыборкаГруппа.Группа);
			
			НачатьДобавлениеСтрокиОсновныеСредстваОграничения(
				ОсновныеСредстваОграничения,
				ВыборкаГруппа.Показатель,
				ВыборкаГруппа.Группа,
				ШаблоныЗаголовковРасшифровкиКолонки,
				ПараметрыТекстов);
			
			ВыборкаОрганизация = ВыборкаГруппа.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаОрганизация.Следующий() Цикл
				
				ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьФилиал(
					ОсновныеСредстваОграничения,
					ШаблоныЗаголовковРасшифровкиКолонки,
					ВыборкаОрганизация.Организация,
					ВыборкаОрганизация.ОрганизацияПредставление);
				
				Выборка = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока Выборка.Следующий() Цикл
					
					Отчет = Неопределено;
					ДобавитьСлагаемоеОборотПоСчету(ОсновныеСредстваОграничения, "НачалоБалансоваяСтоимость",  Выборка, Выборка.Начало, Отчет);
					ДобавитьСлагаемоеОборотПоСчету(ОсновныеСредстваОграничения, "КонецБалансоваяСтоимость",   Выборка, Выборка.Конец, Отчет);
					
				КонецЦикла;
				
			КонецЦикла;
			
			ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(ОсновныеСредстваОграничения, ШаблоныЗаголовковРасшифровкиКолонки);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПояснениеКапитальныеВложения(ПолучениеДанныхТаблицОтчета, Классификаторы, КонтекстЗаполненияОтчета)
	
	ИмяТаблицы = "КапитальныеВложенияОсновныеСредства";
	Если ЗаполнениеБухгалтерскойОтчетностиТабличное.ТаблицаОтчета(ПолучениеДанныхТаблицОтчета, ИмяТаблицы) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнениеБухгалтерскойОтчетностиКапитальныеВложения.ЗаполнитьПояснениеКапитальныеВложения(ПолучениеДанныхТаблицОтчета, ИмяТаблицы, Классификаторы, КонтекстЗаполненияОтчета);
	
КонецПроцедуры

Процедура НастроитьОтчетДолгосрочныеАктивыКПродаже(ЗначенияНастроекОтчета, Выборка)
	ЗначенияНастроекОтчета.Счет = Выборка.КорСчет;
	ЗначенияНастроекОтчета.КорСчет = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
	ЗначенияНастроекОтчета.ИмяОтчета = "ОборотыСчета";
	ЗначенияНастроекОтчета.ПоСубсчетамКорСчетов = Истина;
	Если Выборка.ВидСчета = "Первоначальная" Тогда
		ЗначенияНастроекОтчета.Показатели.ОборотыСоСчетамиДт = Истина;
	Иначе
		ЗначенияНастроекОтчета.Показатели.ОборотыСоСчетамиКт = Истина;
	КонецЕсли;
	ЗначенияНастроекОтчета.Показатели.СальдоНаКонецДт = Истина;
	ЗначенияНастроекОтчета.ГруппировкаСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
КонецПроцедуры

Функция ВариантыУчетаОсновныхСредств()
	
	ВариантыУчета = Новый Структура;
	
	ВариантыУчета.Вставить("ОсновныеСредства",           Новый Массив);
	ВариантыУчета.ОсновныеСредства.Добавить(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
	ВариантыУчета.ОсновныеСредства.Добавить(ПланыСчетов.Хозрасчетный.АмортизацияОсновныхСредств);
	
	ВариантыУчета.Вставить("ПраваПользованияАктивами",   Новый Массив);
	ВариантыУчета.ПраваПользованияАктивами.Добавить(ПланыСчетов.Хозрасчетный.АрендованноеИмущество);
	ВариантыУчета.ПраваПользованияАктивами.Добавить(ПланыСчетов.Хозрасчетный.АмортизацияАрендованногоИмущества);
	
	ВариантыУчета.Вставить("ИнвестиционнаяНедвижимость", Новый Массив);
	ВариантыУчета.ИнвестиционнаяНедвижимость.Добавить(ПланыСчетов.Хозрасчетный.ДоходныеВложенияВ_МЦ);
	ВариантыУчета.ИнвестиционнаяНедвижимость.Добавить(ПланыСчетов.Хозрасчетный.АмортизацияОС_03);
	
	Возврат ВариантыУчета;
	
КонецФункции

Функция ОбщиеПравилаУчета()
	
	ОбщиеПравилаУчета = Новый Структура;
	
	ОбщиеПравилаУчета.Вставить("СчетаОбесценение",                СчетаОбесценение());
	ОбщиеПравилаУчета.Вставить("СчетаДолгосрочныеАктивыКПродаже", СчетаДолгосрочныеАктивыКПродаже());
	
	ОбщиеПравилаУчета.Вставить("СчетМежфилиальныхОборотов", ПланыСчетов.Хозрасчетный.ВнутрихозяйственныеРасчеты);
	ОбщиеПравилаУчета.Вставить("СчетПереоценкаКапитал",     ПланыСчетов.Хозрасчетный.ПриростСтоимостиПоПереоценке);
	ОбщиеПравилаУчета.Вставить("СчетПереоценкаПрибыль",     ПланыСчетов.Хозрасчетный.ПрочиеДоходыИРасходы);
	
	ОбщиеПравилаУчета.Вставить("СчетВАренде", ПланыСчетов.Хозрасчетный.ОсновныеСредстваСданныеВАренду);
	
	ОбщиеПравилаУчета.Вставить("ВидСтатьиПереоценка", Перечисления.ВидыПрочихДоходовИРасходов.ПереоценкаВнеоборотныхАктивов);
	
	Возврат ОбщиеПравилаУчета;
	
КонецФункции

Функция СчетаОбесценение()
	
	Счета = Новый Массив;
	Возврат Счета;
	
КонецФункции

Функция СчетаДолгосрочныеАктивыКПродаже()
	
	Счета = Новый Массив;
	Счета.Добавить(ПланыСчетов.Хозрасчетный.ВыбытиеОС);
	Счета.Добавить(ПланыСчетов.Хозрасчетный.ВыбытиеМЦ);
	Возврат Счета;
	
КонецФункции

Функция ЭтоОбязательнаяГруппа(Группа, ВариантУчета)
	
	Если ВариантУчета = "ИнвестиционнаяНедвижимость" Тогда
		Возврат Группа = Перечисления.ГруппыОС.Здания
			Или Группа = Перечисления.ГруппыОС.Сооружения
			Или Группа = Перечисления.ГруппыОС.ЗемельныеУчастки;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

Процедура СоздатьСчетаУчетаОсновныхСредств(МенеджерВременныхТаблиц, КлассификацияСчетов, ОбщиеПравилаУчета)
	
	ВариантыУчета = Новый ТаблицаЗначений;
	ВариантыУчета.Колонки.Добавить("ВариантУчета", ОбщегоНазначения.ОписаниеТипаСтрока(255));
	ВариантыУчета.Колонки.Добавить("Счет",         Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	
	Для Каждого ВариантУчета Из КлассификацияСчетов Цикл
		ВариантУчетаНормализованный = ВариантыУчета.Добавить();
		ВариантУчетаНормализованный.ВариантУчета = ВариантУчета.Значение;
		ВариантУчетаНормализованный.Счет         = ВариантУчета.Ключ;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВариантыУчета",                   ВариантыУчета);
	Запрос.УстановитьПараметр("СчетаОбесценение",                ОбщиеПравилаУчета.СчетаОбесценение);
	Запрос.УстановитьПараметр("СчетаДолгосрочныеАктивыКПродаже", ОбщиеПравилаУчета.СчетаДолгосрочныеАктивыКПродаже);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВариантыУчета.ВариантУчета КАК ВариантУчета,
	|	ВариантыУчета.Счет КАК Счет
	|ПОМЕСТИТЬ ВТ_ВариантыУчета
	|ИЗ
	|	&ВариантыУчета КАК ВариантыУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВариантыУчета.Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет,
	|	ВариантыУчета.ВариантУчета КАК ВариантУчета,
	|	ВЫБОР
	|		КОГДА Хозрасчетный.Вид <> ЗНАЧЕНИЕ(ВидСчета.Пассивный)
	|			ТОГДА ВЫБОР
	|					КОГДА Хозрасчетный.Ссылка В (&СчетаДолгосрочныеАктивыКПродаже)
	|						ТОГДА ""КПродаже""
	|					ИНАЧЕ ""Первоначальная""
	|				КОНЕЦ
	|		ИНАЧЕ ""Амортизация""
	|	КОНЕЦ КАК ВидСчета,
	|	Хозрасчетный.Порядок КАК Порядок
	|ПОМЕСТИТЬ СчетаУчетаОсновныхСредств
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ВариантыУчета КАК ВариантыУчета
	|		ПО Хозрасчетный.Ссылка = ВариантыУчета.Счет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Счет";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ЗаполнитьШаблоныКолонокОсновныеСредства(ШаблоныЗаголовковКолонок, ШаблоныЗаголовковРасшифровкиКолонки, ВариантУчета)
	
	ЗаполнениеШаблонов = Новый Структура;
	ЗаполнениеШаблонов.Вставить("ШаблоныЗаголовковКолонок",     ШаблоныЗаголовковКолонок);
	ЗаполнениеШаблонов.Вставить("ШаблоныЗаголовковРасшифровки", ШаблоныЗаголовковРасшифровкиКолонки);
	ЗаполнениеШаблонов.Вставить("ВариантУчета", ВариантУчета);
	ЗаполнениеШаблонов.Вставить("Исключения",   Новый СписокЗначений);
	ЗаполнениеШаблонов.Исключения.Добавить("Перемещение",         НСтр("ru = 'перемещению';
																		|en = 'transfer'"));
	ЗаполнениеШаблонов.Исключения.Добавить("Переоценка",          НСтр("ru = 'переоценке';
																		|en = 'revaluation'"));
	Если ВариантУчета = "ОсновныеСредства" Или ВариантУчета = "ИнвестиционнаяНедвижимость" Тогда
		ЗаполнениеШаблонов.Исключения.Добавить("КПродаже",            НСтр("ru = 'переклассификации в долгосрочные активы к продаже';
																			|en = 'reclassification to long-term assets for sale'"));
		ЗаполнениеШаблонов.Исключения.Добавить("СписаниеОбесценения", НСтр("ru = 'списанию обесценения при выбытии';
																			|en = 'impairment write-off on retirement'"));
	КонецЕсли;
	
	ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов,
		"НачалоПервоначальная",
		НСтр("ru = 'первоначальная стоимость на [НачалоПериода]';
			|en = 'initial cost as of [НачалоПериода]'"),
		НСтр("ru = 'Сальдо по [СчетуЗатрат] - по объектам группы учета [НаименованиеГруппы]';
			|en = '[СчетуЗатрат] balance - by objects of the [НаименованиеГруппы] accounting group'"));
		
	ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов,
		"НачалоАмортизация",
		НСтр("ru = 'накопленная амортизация на [НачалоПериода]';
			|en = 'depreciation accumulated as of [НачалоПериода]'"),
		НСтр("ru = 'Сальдо по [СчетуАмортизации] - по объектам группы учета [НаименованиеГруппы]';
			|en = '[СчетуАмортизации] balance - by objects of the [НаименованиеГруппы] accounting group'"));
		
	ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов,
		"Поступило",
		НСтр("ru = 'поступило за [Период] - первоначальная стоимость';
			|en = 'received for [Период] - initial cost'"),
		НСтр("ru = 'Оборот по дебету [СчетаЗатрат] - по объектам группы учета [НаименованиеГруппы]';
			|en = '[СчетаЗатрат] debit turnover by objects of the [НаименованиеГруппы] accounting group'"),
		"Перемещение,Переоценка");
		
	ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов,
		"СписаноПервоначальная",
		НСтр("ru = 'списано за [Период] - первоначальная стоимость';
			|en = 'written off for [Период] - initial cost'"),
		НСтр("ru = 'Оборот по кредиту [СчетаЗатрат] - по объектам группы учета [НаименованиеГруппы]';
			|en = '[СчетаЗатрат] credit turnover by objects of the [НаименованиеГруппы] accounting group'"),
		"Перемещение,Переоценка,КПродаже");
		
	ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов,
		"СписаноАмортизация",
		НСтр("ru = 'списано за [Период] - накопленные амортизация и обесценение';
			|en = 'written off for [Период] - accumulated depreciation and impairment'"),
		НСтр("ru = 'Оборот по дебету [СчетаАмортизацииБезОбесценения], оборот по [СчетуОбесценения] в корреспонденции со [СчетомВыбытия] - все по объектам группы учета [НаименованиеГруппы]';
			|en = '[СчетаАмортизацииБезОбесценения] debit turnover, [СчетуОбесценения] turnover in correspondence with [СчетомВыбытия] all by objects of the [НаименованиеГруппы] accounting group'"));
	
	ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов,
		"Амортизация",
		НСтр("ru = 'амортизация за [Период]';
			|en = 'depreciation for [Период]'"),
		НСтр("ru = 'Оборот по кредиту [СчетаАмортизацииБезОбесценения] - по объектам группы учета [НаименованиеГруппы]';
			|en = '[СчетаАмортизацииБезОбесценения] credit turnover by objects of the [НаименованиеГруппы] accounting group'"),
		"Перемещение,Переоценка,КПродаже");
	
	ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов,
		"Обесценение",
		НСтр("ru = 'обесценение за [Период]';
			|en = 'impairment for [Период]'"),
		НСтр("ru = 'Оборот по [СчетуОбесценения] - по объектам группы учета [НаименованиеГруппы]';
			|en = '[СчетуОбесценения] turnover by objects of the [НаименованиеГруппы] accounting group'"),
		"Перемещение,КПродаже,СписаниеОбесценения");
	
	ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов,
		"ПереоцененоПервоначальная",
		НСтр("ru = 'переоценено за [Период] - первоначальная стоимость';
			|en = 'revalued for [Период] - initial cost'"),
		НСтр("ru = 'Оборот по [СчетуЗатрат] в корреспонденции со счетом [СчетПереоценкаКапитал], со счетом [СчетПереоценкаПрибыль] по статьям вида [ВидСтатьиПереоценка] - все по объектам группы учета [НаименованиеГруппы]';
			|en = '[СчетуЗатрат] turnover in correspondence with account [СчетПереоценкаКапитал], with account [СчетПереоценкаПрибыль] by items of the [ВидСтатьиПереоценка] type all by objects of the [НаименованиеГруппы] accounting group'"));
		
	ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов,
		"ПереоцененоАмортизация",
		НСтр("ru = 'переоценено за [Период] - накопленные амортизация и обесценение';
			|en = 'revalued for [Период] - accumulated depreciation and impairment'"),
		НСтр("ru = 'Оборот по [СчетуАмортизации] в корреспонденции со счетом [СчетПереоценкаКапитал], со счетом [СчетПереоценкаПрибыль] по статьям вида [ВидСтатьиПереоценка] - все по объектам группы учета [НаименованиеГруппы]';
			|en = '[СчетуАмортизации] turnover in correspondence with account [СчетПереоценкаКапитал], with account [СчетПереоценкаПрибыль] by items of the [ВидСтатьиПереоценка] type all by objects of the [НаименованиеГруппы] accounting group'"));
		
	ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов,
		"ПереклассифицированоПервоначальная",
		НСтр("ru = 'переклассифицировано в долгосрочные активы к продаже за [Период] - первоначальная стоимость';
			|en = 'reclassified to long-term assets for sale for [Период] - initial cost'"),
		НСтр("ru = 'Оборот по [СчетуЗатрат] в корреспонденции со [СчетомВыбытия] по объектам группы учета [НаименованиеГруппы], имеющим на отчетную дату сальдо по [СчетуВыбытия]';
			|en = '[СчетуЗатрат] turnover in correspondence with [СчетомВыбытия] by objects of the [НаименованиеГруппы] accounting group having a balance on [СчетуВыбытия] on the reporting date'"));
		
	ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов,
		"ПереклассифицированоАмортизация",
		НСтр("ru = 'переклассифицировано в долгосрочные активы к продаже за [Период] - накопленные амортизация и обесценение';
			|en = 'reclassified to long-term assets for sale for [Период] - accumulated depreciation and impairment'"),
		НСтр("ru = 'Оборот по [СчетуАмортизации] в корреспонденции со [СчетомВыбытия] по объектам группы учета [НаименованиеГруппы], имеющим на отчетную дату сальдо по [СчетуВыбытия]';
			|en = '[СчетуАмортизации] turnover in correspondence with [СчетомВыбытия] by objects of the [НаименованиеГруппы] accounting group having a balance on [СчетуВыбытия] on the reporting date'"));
		
	ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов,
		"КонецПервоначальная",
		НСтр("ru = 'первоначальная стоимость на [КонецПериода]';
			|en = 'initial cost as of [КонецПериода]'"),
		НСтр("ru = 'Сальдо по [СчетуЗатрат] - по объектам группы учета [НаименованиеГруппы]';
			|en = '[СчетуЗатрат] balance - by objects of the [НаименованиеГруппы] accounting group'"));
		
	ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов,
		"КонецАмортизация",
		НСтр("ru = 'накопленная амортизация на [КонецПериода]';
			|en = 'accumulated depreciation as of [КонецПериода]'"),
		НСтр("ru = 'Сальдо по [СчетуАмортизации] - по объектам группы учета [НаименованиеГруппы]';
			|en = '[СчетуАмортизации] balance - by objects of the [НаименованиеГруппы] accounting group'"));
		
КонецПроцедуры

Процедура УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстов, ОбщиеПравилаУчета, КонтекстЗаполненияОтчета)
	
	ЗаполнениеБухгалтерскойОтчетностиТабличное.УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстов, КонтекстЗаполненияОтчета);
	
	ПараметрыТекстов.Вставить("ВидСтатьиПереоценка",    Строка(ОбщиеПравилаУчета.ВидСтатьиПереоценка));
	ПараметрыТекстов.Вставить("СчетПереоценкаКапитал", Строка(ОбщиеПравилаУчета.СчетПереоценкаКапитал));
	ПараметрыТекстов.Вставить("СчетПереоценкаПрибыль", Строка(ОбщиеПравилаУчета.СчетПереоценкаПрибыль));
	
КонецПроцедуры

Функция НовыйПравилоСчета()
	Правило = Новый Структура;
	Правило.Вставить("Правило",    Новый Массив); // Из ПланСчетовСсылка.Хозрасчетный
	Правило.Вставить("Исключения", Новый Массив); // Из ПланСчетовСсылка.Хозрасчетный
	Возврат Правило;
КонецФункции

Процедура УстановитьПараметрыТекстовРасшифровкиВариантыУчета(ПараметрыТекстов, ВариантУчета, СчетаВариантаУчета, МенеджерВременныхТаблиц)
	
	СчетаПоВидуСчета = СчетаПоВидуСчета(ВариантУчета, СчетаВариантаУчета, МенеджерВременныхТаблиц);
	
	СчетаЗатрат = СчетаПоВидуСчета["Первоначальная"];
	Если СчетаЗатрат = Неопределено Тогда
		ПараметрыТекстов.Вставить("СчетаЗатрат", НСтр("ru = 'счета первоначальной стоимости';
														|en = 'initial cost accounts'"));
		ПараметрыТекстов.Вставить("СчетуЗатрат", НСтр("ru = 'счету первоначальной стоимости';
														|en = 'initial cost account'"));
	Иначе
		ПараметрыТекстов.Вставить("СчетаЗатрат", ПредставлениеПравилаСчетов(СчетаЗатрат, "Родительный"));
		ПараметрыТекстов.Вставить("СчетуЗатрат", ПредставлениеПравилаСчетов(СчетаЗатрат, "Дательный"));
	КонецЕсли;
	
	СчетаАмортизации = СчетаПоВидуСчета["Амортизация"];
	Если СчетаАмортизации = Неопределено Тогда
		ПараметрыТекстов.Вставить("СчетаАмортизацииБезОбесценения", НСтр("ru = 'счета амортизации';
																		|en = 'depreciation accounts'"));
		ПараметрыТекстов.Вставить("СчетуАмортизации", НСтр("ru = 'счету амортизации';
															|en = 'depreciation account'"));
		ПараметрыТекстов.Вставить("СчетаАмортизации", НСтр("ru = 'счета амортизации';
															|en = 'depreciation accounts'"));
	Иначе
		ПараметрыТекстов.Вставить("СчетаАмортизацииБезОбесценения", ПредставлениеПравилаСчетов(СчетаАмортизации, "Родительный"));
		// потом при необходимости добавим и счета обесценения
		ПараметрыТекстов.Вставить("СчетаАмортизации", ПредставлениеПравилаСчетов(СчетаАмортизации, "Родительный"));
		ПараметрыТекстов.Вставить("СчетуАмортизации", ПредставлениеПравилаСчетов(СчетаАмортизации, "Дательный"));
	КонецЕсли;
	
	СчетаОбесценения = СчетаПоВидуСчета["Обесценение"];
	Если СчетаОбесценения = Неопределено Тогда
		ПараметрыТекстов.Вставить("СчетуОбесценения", НСтр("ru = 'счету обесценения';
															|en = 'impairment account'"));
	Иначе
		ПараметрыТекстов.Вставить("СчетуОбесценения", ПредставлениеПравилаСчетов(СчетаОбесценения, "Дательный"));
	КонецЕсли;
	
	СчетаВыбытия = СчетаПоВидуСчета["КПродаже"];
	Если СчетаВыбытия = Неопределено Тогда
		ПараметрыТекстов.Вставить("СчетуВыбытия", НСтр("ru = 'счету выбытия';
														|en = 'outflow account'"));
		ПараметрыТекстов.Вставить("СчетомВыбытия", НСтр("ru = 'счету выбытия';
														|en = 'outflow account'"));
	Иначе
		ПараметрыТекстов.Вставить("СчетуВыбытия", ПредставлениеПравилаСчетов(СчетаВыбытия, "Дательный"));
		ПараметрыТекстов.Вставить("СчетомВыбытия", ПредставлениеПравилаСчетов(СчетаВыбытия, "Творительный"));
	КонецЕсли;
	
	Если СчетаАмортизации <> Неопределено И СчетаОбесценения <> Неопределено Тогда
		СчетаАмортизацииБезОбесценения = СуммаПравилСчетов(СчетаАмортизации, СчетаОбесценения);
		ПараметрыТекстов.Вставить("СчетаАмортизации", ПредставлениеПравилаСчетов(СчетаАмортизацииБезОбесценения, "Родительный"));
		ПараметрыТекстов.Вставить("СчетуАмортизации", ПредставлениеПравилаСчетов(СчетаАмортизацииБезОбесценения, "Дательный"));
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПараметрыТекстовРасшифровкиГруппы(ПараметрыТекстов, Группа)
	ПараметрыТекстов.Вставить("НаименованиеГруппы", Группа);
КонецПроцедуры

Функция СуммаПравилСчетов(ПервоеСлагаемое, ВтороеСлагаемое)
	
	СуммаПравил = ОбщегоНазначения.СкопироватьРекурсивно(ПервоеСлагаемое);
	
	СуммаПравил.Правило = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СуммаПравил.Правило, ВтороеСлагаемое.Исключения);
	ОставшиесяИсключения = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ВтороеСлагаемое.Исключения, СуммаПравил.Правило);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СуммаПравил.Исключения, ОставшиесяИсключения, Истина);
	СуммаПравил.Исключения = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СуммаПравил.Исключения, ВтороеСлагаемое.Правило);
	
	Возврат СуммаПравил;

КонецФункции

Функция СчетаПоВидуСчета(ВариантУчета, СчетаВариантаУчета, МенеджерВременныхТаблиц)
	
	СчетаПоВидуСчета = Новый Соответствие; // Ключ - вид счета; Значение - НовыйПравилоСчета()
	
	Для Каждого Счет Из СчетаВариантаУчета Цикл
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ВариантУчета", ВариантУчета);
		Запрос.УстановитьПараметр("Счет",         Счет);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Счета.ВидСчета КАК ВидСчета
		|ИЗ
		|	СчетаУчетаОсновныхСредств КАК Счета
		|ГДЕ
		|	Счета.Счет = &Счет";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Не Выборка.Следующий() Тогда
			Продолжить;
		КонецЕсли;
		
		ВидСчетаУчета = Выборка.ВидСчета;
		
		Запрос.УстановитьПараметр("ВидСчета", ВидСчетаУчета);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Счета.Счет КАК Счет
		|ИЗ
		|	СчетаУчетаОсновныхСредств КАК Счета
		|ГДЕ
		|	Счета.Счет В ИЕРАРХИИ(&Счет)
		|	И (Счета.ВидСчета <> &ВидСчета
		|			ИЛИ Счета.ВариантУчета <> &ВариантУчета)
		|	И НЕ Счета.Счет.ЗапретитьИспользоватьВПроводках
		|
		|УПОРЯДОЧИТЬ ПО
		|	Счета.Порядок,
		|	Счета.Счет";
		
		Правило = НовыйПравилоСчета();
		Правило.Правило.Добавить(Счет);
		Правило.Исключения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Счет");
		
		СчетаПоВидуСчета.Вставить(ВидСчетаУчета, Правило);
		
	КонецЦикла;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Счета.Счет КАК Счет,
	|	Счета.ВидСчета КАК ВидСчета
	|ИЗ
	|	СчетаУчетаОсновныхСредств КАК Счета
	|ГДЕ
	|	Счета.ВариантУчета = &ВариантУчета
	|	И НЕ Счета.Счет.ЗапретитьИспользоватьВПроводках
	|
	|УПОРЯДОЧИТЬ ПО
	|	Счета.Порядок,
	|	Счета.Счет
	|ИТОГИ ПО
	|	ВидСчета";
	ВыборкаВидСчета = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаВидСчета.Следующий() Цикл
		Если СчетаПоВидуСчета[ВыборкаВидСчета.ВидСчета] <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Правило = НовыйПравилоСчета();
		СчетаПоВидуСчета.Вставить(ВыборкаВидСчета.ВидСчета, Правило);
		ВыборкаСчет = ВыборкаВидСчета.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаСчет.Следующий() Цикл
			Правило.Правило.Добавить(ВыборкаСчет.Счет);
		КонецЦикла;
	КонецЦикла;
	
	Возврат СчетаПоВидуСчета;
	
КонецФункции

Функция ПредставлениеПравилаСчетов(Правило, Падеж)
	
	ПредставлениеСписка = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(Правило.Правило);
	ПредставлениеПравила = "";
	Если Падеж = "Родительный" Тогда
		ПредставлениеПравила = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовРодительныйПадеж(ПредставлениеСписка);
	ИначеЕсли Падеж = "Дательный" Тогда
		ПредставлениеПравила = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовДательныйПадеж(ПредставлениеСписка);
	ИначеЕсли Падеж = "Творительный" Тогда
		ПредставлениеПравила = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовТворительныйПадеж(ПредставлениеСписка);
	Иначе
		Возврат ПредставлениеСписка.Текст;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Правило.Исключения) Тогда
		Возврат ПредставлениеПравила;
	КонецЕсли;
	
	ПредставлениеИсключений = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(Правило.Исключения);
	Возврат СтрШаблон(НСтр("ru = '%1 (кроме %2)';
							|en = '%1 (except for %2)'"), ПредставлениеПравила, ПредставлениеИсключений.Текст);
	
КонецФункции

Процедура СоздатьСтоимостьОсновныхСредств(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, ОбщиеПравилаУчета)
	
	Счета = Новый Структура;
	Счета.Вставить("ОсновныеСредства", Новый Массив); // Все, кроме КПродаже
	Счета.Вставить("КПродаже",         Новый Массив);
	Счета.Вставить("Обесценение",      Новый Массив);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетаУчета.Счет КАК Счет,
	|	СчетаУчета.ВидСчета КАК ВидСчета
	|ИЗ
	|	СчетаУчетаОсновныхСредств КАК СчетаУчета";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ВидСчета = "КПродаже" Тогда
			Счета.КПродаже.Добавить(Выборка.Счет);
		Иначе
			Счета.ОсновныеСредства.Добавить(Выборка.Счет);
			Если Счета.Свойство(Выборка.ВидСчета) Тогда
				Счета[Выборка.ВидСчета].Добавить(Выборка.Счет);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НачалоПериода",              КонтекстЗаполненияОтчета.Период.Начало);
	Запрос.УстановитьПараметр("КонецПериода",               Новый Граница(КонтекстЗаполненияОтчета.Период.Конец, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организации",                КонтекстЗаполненияОтчета.Организации);
	Запрос.УстановитьПараметр("СчетаОсновныеСредства",      Счета.ОсновныеСредства);
	Запрос.УстановитьПараметр("СчетаКПродаже",              Счета.КПродаже);
	Запрос.УстановитьПараметр("СчетаОбесценение",           Счета.Обесценение);
	Запрос.УстановитьПараметр("СчетаМежфилиальныхОборотов", БухгалтерскийУчетПовтИсп.СчетаВИерархии(ОбщиеПравилаУчета.СчетМежфилиальныхОборотов));
	Запрос.УстановитьПараметр("СчетаПереоценкаКапитал",     БухгалтерскийУчетПовтИсп.СчетаВИерархии(ОбщиеПравилаУчета.СчетПереоценкаКапитал));
	Запрос.УстановитьПараметр("СчетаПереоценкаПрибыль",     БухгалтерскийУчетПовтИсп.СчетаВИерархии(ОбщиеПравилаУчета.СчетПереоценкаПрибыль));
	Запрос.УстановитьПараметр("ВидСтатьиПереоценка",        ОбщиеПравилаУчета.ВидСтатьиПереоценка);
	
	// Знак в запросе выбирается так, чтобы сумма всех слагаемых (шагов) соответствовала корректировке основных колонок отчета (поступило, амортизация).
	// Например, если нужно исключить переоценку из первоначальной стоимости, то знак "-".
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КПродаже.Счет КАК Счет,
	|	КПродаже.Субконто1 КАК Объект
	|ПОМЕСТИТЬ ВТ_ОбъектыКПродаже
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецПериода, Счет В (&СчетаКПродаже), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), Организация В (&Организации)) КАК КПродаже
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект,
	|	Счет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статьи.Ссылка КАК Статья
	|ПОМЕСТИТЬ ВТ_СтатьиПереоценки
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиДоходов КАК Статьи
	|ГДЕ
	|	Статьи.ВидПрочихДоходовИРасходов = &ВидСтатьиПереоценка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Статьи.Ссылка КАК Статья
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
	|ГДЕ
	|	Статьи.ВидПрочихДоходовИРасходов = &ВидСтатьиПереоценка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Статьи.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Стоимость"" КАК ШагРасчета,
	|	ОстаткиИОбороты.Счет КАК Счет,
	|	ОстаткиИОбороты.Субконто1 КАК Объект,
	|	ОстаткиИОбороты.Организация КАК Организация,
	|	ОстаткиИОбороты.СуммаНачальныйОстаток КАК Начало,
	|	ОстаткиИОбороты.СуммаКонечныйОстаток КАК Конец,
	|	ОстаткиИОбороты.СуммаОборотДт КАК Дт,
	|	-ОстаткиИОбороты.СуммаОборотКт КАК Кт,
	|	NULL КАК КорСчет
	|ПОМЕСТИТЬ СтоимостьОсновныхСредств
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, , , Счет В (&СчетаОсновныеСредства), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), Организация В (&Организации)) КАК ОстаткиИОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Перемещение"",
	|	Перемещение.Счет,
	|	Перемещение.Субконто1,
	|	Перемещение.Организация,
	|	0,
	|	0,
	|	-Перемещение.СуммаОборотДт,
	|	Перемещение.СуммаОборотКт,
	|	Перемещение.КорСчет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаОсновныеСредства), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), Организация В (&Организации), КорСчет В (&СчетаОсновныеСредства), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства)) КАК Перемещение
	|ГДЕ
	|	Перемещение.Субконто1 = Перемещение.КорСубконто1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""МежфилиальныйОборот"",
	|	МежфилиальныйОборот.Счет,
	|	МежфилиальныйОборот.Субконто1,
	|	МежфилиальныйОборот.Организация,
	|	0,
	|	0,
	|	-МежфилиальныйОборот.СуммаОборотДт,
	|	МежфилиальныйОборот.СуммаОборотКт,
	|	МежфилиальныйОборот.КорСчет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Счет В (&СчетаОсновныеСредства),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
	|			Организация В (&Организации)
	|				И КорСубконто1 В (&Организации),
	|			КорСчет В (&СчетаМежфилиальныхОборотов),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбособленныеПодразделения)) КАК МежфилиальныйОборот
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""КПродаже"",
	|	КПродаже.Счет,
	|	КПродаже.Субконто1,
	|	КПродаже.Организация,
	|	0,
	|	0,
	|	-КПродаже.СуммаОборотДт,
	|	КПродаже.СуммаОборотКт,
	|	КПродаже.КорСчет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Счет В (&СчетаОсновныеСредства),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
	|			Организация В (&Организации)
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						ОбъектыКПродаже.Объект
	|					ИЗ
	|						ВТ_ОбъектыКПродаже КАК ОбъектыКПродаже),
	|			КорСчет В (&СчетаКПродаже),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства)) КАК КПродаже
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбъектыКПродаже КАК Отбор
	|		ПО КПродаже.КорСчет = Отбор.Счет
	|			И КПродаже.КорСубконто1 = Отбор.Объект
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Переоценено"",
	|	ПереоцененоКапитал.Счет,
	|	ПереоцененоКапитал.Субконто1,
	|	ПереоцененоКапитал.Организация,
	|	0,
	|	0,
	|	-ПереоцененоКапитал.СуммаОборотДт,
	|	ПереоцененоКапитал.СуммаОборотКт,
	|	ПереоцененоКапитал.КорСчет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаОсновныеСредства), ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства), Организация В (&Организации), КорСчет В (&СчетаПереоценкаКапитал), ) КАК ПереоцененоКапитал
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Переоценено"",
	|	ПереоцененоПрибыль.Счет,
	|	ПереоцененоПрибыль.Субконто1,
	|	ПереоцененоПрибыль.Организация,
	|	0,
	|	0,
	|	-ПереоцененоПрибыль.СуммаОборотДт,
	|	ПереоцененоПрибыль.СуммаОборотКт,
	|	ПереоцененоПрибыль.КорСчет
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Счет В (&СчетаОсновныеСредства),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
	|			Организация В (&Организации)
	|				И КорСубконто1 В
	|					(ВЫБРАТЬ
	|						СтатьиПереоценки.Статья
	|					ИЗ
	|						ВТ_СтатьиПереоценки КАК СтатьиПереоценки),
	|			КорСчет В (&СчетаПереоценкаПрибыль),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы)) КАК ПереоцененоПрибыль
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ОбъектыКПродаже
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_СтатьиПереоценки";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьАмортизацияЗапрещена(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, Классификаторы)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НачалоПериода", Новый Граница(КонтекстЗаполненияОтчета.Период.Начало, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("КонецПериода",  Новый Граница(КонтекстЗаполненияОтчета.Период.КонецДата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр(
		"ОснованиеПостоянныеПотребительскиеСвойства",
		Классификаторы.ОсновныеСредстваАмортизируемыеНеамортизируемые.Основание.ПостоянныеПотребительскиеСвойства);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Постоянные.Основание КАК Основание,
	|	Постоянные.Организация КАК Организация,
	|	Постоянные.Объект КАК Объект,
	|	МАКСИМУМ(Постоянные.Начало) КАК Начало,
	|	МАКСИМУМ(Постоянные.Конец) КАК Конец
	|ПОМЕСТИТЬ АмортизацияЗапрещена
	|ИЗ
	|	(ВЫБРАТЬ
	|		&ОснованиеПостоянныеПотребительскиеСвойства КАК Основание,
	|		Настройка.Организация КАК Организация,
	|		Настройка.ОсновноеСредство КАК Объект,
	|		ИСТИНА КАК Начало,
	|		NULL КАК Конец
	|	ИЗ
	|		РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|				&НачалоПериода,
	|				(ОсновноеСредство, Организация) В
	|					(ВЫБРАТЬ
	|						Стоимость.Объект,
	|						Стоимость.Организация
	|					ИЗ
	|						СтоимостьОсновныхСредств КАК Стоимость)) КАК Настройка
	|	ГДЕ
	|		НЕ Настройка.НачислятьАмортизациюБУ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&ОснованиеПостоянныеПотребительскиеСвойства,
	|		Настройка.Организация,
	|		Настройка.ОсновноеСредство,
	|		NULL,
	|		ИСТИНА
	|	ИЗ
	|		РегистрСведений.ПорядокУчетаОСБУ.СрезПоследних(
	|				&КонецПериода,
	|				(ОсновноеСредство, Организация) В
	|					(ВЫБРАТЬ
	|						Стоимость.Объект,
	|						Стоимость.Организация
	|					ИЗ
	|						СтоимостьОсновныхСредств КАК Стоимость)) КАК Настройка
	|	ГДЕ
	|		НЕ Настройка.НачислятьАмортизациюБУ) КАК Постоянные
	|
	|СГРУППИРОВАТЬ ПО
	|	Постоянные.Основание,
	|	Постоянные.Организация,
	|	Постоянные.Объект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Постоянные.Объект,
	|	Постоянные.Организация";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьОграниченияОсновныхСредств(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, ОбщиеПравилаУчета, Классификаторы)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НачалоПериода", Новый Граница(КонтекстЗаполненияОтчета.Период.Начало, ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("КонецПериода",  Новый Граница(КонтекстЗаполненияОтчета.Период.КонецДата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("СчетаВАренде",  БухгалтерскийУчетПовтИсп.СчетаВИерархии(ОбщиеПравилаУчета.СчетВАренде));
	Запрос.УстановитьПараметр("ПоказательВАренде", Классификаторы.ОсновныеСредстваОграничения.Показатель.ВАренде);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВАренде.Показатель КАК Показатель,
	|	ВАренде.Объект КАК Объект,
	|	ВАренде.Организация КАК Организация,
	|	МАКСИМУМ(ВАренде.Начало) КАК Начало,
	|	МАКСИМУМ(ВАренде.Конец) КАК Конец
	|ПОМЕСТИТЬ ОграниченияОсновныхСредств
	|ИЗ
	|	(ВЫБРАТЬ
	|		&ПоказательВАренде КАК Показатель,
	|		ВАрендеПоМестонахождениюНачало.ОсновноеСредство КАК Объект,
	|		ВАрендеПоМестонахождениюНачало.Организация КАК Организация,
	|		ИСТИНА КАК Начало,
	|		ЛОЖЬ КАК Конец
	|	ИЗ
	|		РегистрСведений.МестонахождениеОС.СрезПоследних(
	|				&НачалоПериода,
	|				(ОсновноеСредство, Организация) В
	|					(ВЫБРАТЬ
	|						Стоимость.Объект,
	|						Стоимость.Организация
	|					ИЗ
	|						СтоимостьОсновныхСредств КАК Стоимость)) КАК ВАрендеПоМестонахождениюНачало
	|	ГДЕ
	|		ВАрендеПоМестонахождениюНачало.Арендатор <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&ПоказательВАренде,
	|		ВАрендеПоМестонахождениюКонец.ОсновноеСредство,
	|		ВАрендеПоМестонахождениюКонец.Организация,
	|		ЛОЖЬ,
	|		ИСТИНА
	|	ИЗ
	|		РегистрСведений.МестонахождениеОС.СрезПоследних(
	|				&КонецПериода,
	|				(ОсновноеСредство, Организация) В
	|					(ВЫБРАТЬ
	|						Стоимость.Объект,
	|						Стоимость.Организация
	|					ИЗ
	|						СтоимостьОсновныхСредств КАК Стоимость)) КАК ВАрендеПоМестонахождениюКонец
	|	ГДЕ
	|		ВАрендеПоМестонахождениюКонец.Арендатор <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&ПоказательВАренде,
	|		ВАрендеПоЗабалансуНачало.Субконто1,
	|		ВАрендеПоЗабалансуНачало.Организация,
	|		ВАрендеПоЗабалансуНачало.СуммаОстаток > 0,
	|		ЛОЖЬ
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&НачалоПериода,
	|				Счет В (&СчетаВАренде),
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
	|				(Субконто1, Организация) В
	|					(ВЫБРАТЬ
	|						Стоимость.Объект,
	|						Стоимость.Организация
	|					ИЗ
	|						СтоимостьОсновныхСредств КАК Стоимость)) КАК ВАрендеПоЗабалансуНачало
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&ПоказательВАренде,
	|		ВАрендеПоЗабалансуНачало.Субконто1,
	|		ВАрендеПоЗабалансуНачало.Организация,
	|		ЛОЖЬ,
	|		ВАрендеПоЗабалансуНачало.СуммаОстаток > 0
	|	ИЗ
	|		РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&КонецПериода,
	|				Счет В (&СчетаВАренде),
	|				ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
	|				(Субконто1, Организация) В
	|					(ВЫБРАТЬ
	|						Стоимость.Объект,
	|						Стоимость.Организация
	|					ИЗ
	|						СтоимостьОсновныхСредств КАК Стоимость)) КАК ВАрендеПоЗабалансуНачало) КАК ВАренде
	|
	|СГРУППИРОВАТЬ ПО
	|	ВАренде.Объект,
	|	ВАренде.Организация,
	|	ВАренде.Показатель
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВАренде.Объект,
	|	ВАренде.Организация";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьГруппыОсновныхСредств(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтоимостьОсновныхСредств.Объект КАК Объект,
	|	МАКСИМУМ(ЕСТЬNULL(ГруппыОС.Ссылка, ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ДругиеВидыОсновныхСредств))) КАК Группа
	|ПОМЕСТИТЬ ГруппыОсновныхСредств
	|ИЗ
	|	СтоимостьОсновныхСредств КАК СтоимостьОсновныхСредств
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|			ЛЕВОЕ СОЕДИНЕНИЕ Перечисление.ГруппыОС КАК ГруппыОС
	|			ПО ОсновныеСредства.ГруппаОС = ГруппыОС.Ссылка
	|		ПО СтоимостьОсновныхСредств.Объект = ОсновныеСредства.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	СтоимостьОсновныхСредств.Объект
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Объект";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ДобавитьШаблонЗаголовковРасшифровки(ЗаполнениеШаблонов, ИмяКолонки, ШаблонЗаголовкаКолонки, ШаблонПорядкаРасчета, ВозможныеИсключения = "")
	
	ЗаполнениеШаблонов.ШаблоныЗаголовковКолонок.Вставить(ИмяКолонки, ШаблонЗаголовкаКолонки);
	
	ШаблонКолонки = ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки();
	
	ШаблонКолонки.ЗаголовокГруппы = НСтр("ru = '[НаименованиеГруппы]: [ПредставлениеКолонки]';
										|en = '[НаименованиеГруппы]: [ПредставлениеКолонки]'");
	ШаблонКолонки.ПорядокРасчета  = ДополнитьПорядокРасчетаИсключениями(ШаблонПорядкаРасчета, ВозможныеИсключения, ЗаполнениеШаблонов);
	
	ЗаполнениеШаблонов.ШаблоныЗаголовковРасшифровки.Вставить(ИмяКолонки, ШаблонКолонки);
	
КонецПроцедуры

Функция ДополнитьПорядокРасчетаИсключениями(ПорядокРасчета, ВозможныеИсключенияСтрокой, ЗаполнениеШаблонов)
	
	Если ПустаяСтрока(ВозможныеИсключенияСтрокой) Тогда
		Возврат ПорядокРасчета;
	КонецЕсли;
	
	Предложения = Новый Массив;
	Предложения.Добавить(ПорядокРасчета);
	
	ВозможныеИсключения = Новый Структура(ВозможныеИсключенияСтрокой);
	
	ПредставленияИсключений = Новый Массив;
	
	Для Каждого ОписаниеИсключения Из ЗаполнениеШаблонов.Исключения Цикл
		
		Если Не ВозможныеИсключения.Свойство(ОписаниеИсключения.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		ПредставленияИсключений.Добавить(ОписаниеИсключения.Представление);
		
	КонецЦикла;
	
	Предложения.Добавить(СтрШаблон(
		НСтр("ru = 'Исключаются обороты по %1 основных средств';
			|en = 'Turnover of fixed asset %1 is excluded'"),
		СтрСоединить(ПредставленияИсключений, ", ")));
		
	Возврат СтрСоединить(Предложения, "." + Символы.ПС);
	
КонецФункции

Процедура НачатьДобавлениеСтрокиОсновныеСредства(НаличиеДвижение, ВариантУчета, Группа, ШаблоныКолонок, ПараметрыТекстовРасшифровки, Классификаторы)
	
	НоваяСтрока = ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСтрокуТаблицыОтчета(
		НаличиеДвижение,
		ШаблоныКолонок,
		ПараметрыТекстовРасшифровки);
		
	НоваяСтрока.Группа = Группа;
	
	Если НаличиеДвижение.ИмяТаблицы = "ОсновныеСредстваНаличиеДвижение" Тогда
		НоваяСтрока.ВариантУчета = Классификаторы.ОсновныеСредстваНаличиеДвижение.ВариантУчета[ВариантУчета];
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьДобавлениеСтрокиОсновныеСредстваАмортизируемыеНеамортизируемые(АмортизируемыеНеамортизируемые, Основание, Группа, ШаблоныКолонок, ПараметрыТекстовРасшифровки, Классификаторы)
	
	НоваяСтрока = ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСтрокуТаблицыОтчета(
		АмортизируемыеНеамортизируемые,
		ШаблоныКолонок,
		ПараметрыТекстовРасшифровки);
		
	Если ЗначениеЗаполнено(Основание) Тогда
		НоваяСтрока.Вид = Классификаторы.ОсновныеСредстваАмортизируемыеНеамортизируемые.Вид.Неамортизируемые;
	Иначе
		НоваяСтрока.Вид = Классификаторы.ОсновныеСредстваАмортизируемыеНеамортизируемые.Вид.Амортизируемые;
	КонецЕсли;
	// Не используем уровень структуры Основание ввиду ограничений механики размещения - см. ЗаполнениеБухгалтерскойОтчетностиРазмещение.ЗаполнитьПланКонтейнераОсновныеСредстваАмортизируемыеНеамортизируемые	
	
	НоваяСтрока.Группа = Группа;
	
КонецПроцедуры

Процедура НачатьДобавлениеСтрокиОсновныеСредстваОграничения(Ограничения, Показатель, Группа, ШаблоныКолонок, ПараметрыТекстовРасшифровки)
	
	НоваяСтрока = ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСтрокуТаблицыОтчета(
		Ограничения,
		ШаблоныКолонок,
		ПараметрыТекстовРасшифровки);
		
	НоваяСтрока.Показатель     = Показатель;
	НоваяСтрока.Группа         = Группа;
	
КонецПроцедуры

Функция ДобавитьСлагаемоеОборотПоСчету(ВыводТаблицы, ИмяКолонки, Выборка, Сумма, Отчет)
	
	ПредметЗаписи      = Выборка.Счет;
	ПредставлениеСчета = Строка(ПредметЗаписи);
	
	Возврат ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(ВыводТаблицы, ИмяКолонки, ПредставлениеСчета, Сумма, ПредметЗаписи, Отчет);
	
КонецФункции

Процедура ПеренестиМеждуКолонками(НаличиеДвижение, Сумма, Откуда, ОписаниеОткуда, Куда, ОписаниеКуда, ПредметЗаписи, Отчет)
	ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(НаличиеДвижение, Откуда, ОписаниеОткуда, Сумма, ПредметЗаписи, Отчет);
	ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(НаличиеДвижение, Куда, ОписаниеКуда, -Сумма, ПредметЗаписи, Отчет);
КонецПроцедуры

// Классифицирует счета по заданному классификатору с учетом иерархии.
// То есть, если в плане счетов есть счет 1 и подчиненные ему 1.1, 1.2 и задан классификатор, что в группе А есть счет 1, а в группе Б - 1.2,
// то в результате классификации к А нужно отнести 1 и 1.1, а к Б - 1.2.
//
// Параметры:
//  Классификатор - Структура,Соответствие - правила соответствия счетов классификатору; Ключ - правило, Значение - массив счетов
// 
// Возвращаемое значение:
//  Соответствие - Ключ: счет; Значение: правило классификации
//
Функция КлассифицироватьСчетаПоИерархии(Классификатор)
	
	КлассификацияСчетов = Новый Соответствие;// Ключ - счет, Значение - правило классификации
	
	УровниСчетов = Новый Соответствие; // Ключ - счет; Значение - уровень счета, которым этому был установлен вариант
	
	Для Каждого Правило Из Классификатор Цикл
		Для Каждого СчетУчета Из Правило.Значение Цикл
			
			УровеньУстановленногоСчета = СчетУчета.Уровень();
			Для Каждого Субсчет Из БухгалтерскийУчетПовтИсп.СчетаВИерархии(СчетУчета) Цикл
				
				УровеньСчета = УровниСчетов[Субсчет];
				Если УровеньСчета = Неопределено Тогда
					УровеньСчета = УровеньУстановленногоСчета;
				ИначеЕсли УровеньСчета > УровеньУстановленногоСчета Тогда
					Продолжить;
				КонецЕсли;
				
				УровниСчетов.Вставить(Субсчет, УровеньСчета);
				КлассификацияСчетов.Вставить(Субсчет, Правило.Ключ);
				
			КонецЦикла;
			
		КонецЦикла;
	КонецЦикла;
	
	Возврат КлассификацияСчетов;
	
КонецФункции

Функция ВсеСчета(ВариантыУчета)
	
	ВсеСчета = Новый Массив;
	
	УровниСчетов = Новый Соответствие;
	
	НастроенныеСчета = Новый Соответствие;
	Для Каждого ВариантУчета Из ВариантыУчета Цикл
		Для Каждого СчетНастройки Из ВариантУчета.Значение Цикл
			ВедущийСчет = НастроенныеСчета[СчетНастройки];
			Если ВедущийСчет <> Неопределено
				И УровеньСчета(ВедущийСчет, УровниСчетов) < УровеньСчета(СчетНастройки, УровниСчетов) Тогда
				Продолжить;
			КонецЕсли;
			Для Каждого Субсчет Из БухгалтерскийУчетПовтИсп.СчетаВИерархии(СчетНастройки) Цикл
				НастроенныеСчета.Вставить(Субсчет, СчетНастройки);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ВедущиеСчета = Новый Массив;
	Для Каждого НастроенныйСчет Из НастроенныеСчета Цикл
		ВедущиеСчета.Добавить(НастроенныйСчет.Значение);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Счета", ВедущиеСчета);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет,
	|	Хозрасчетный.Порядок КАК Порядок
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка В(&Счета)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	Ссылка";
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Счет");
	
КонецФункции

Функция УровеньСчета(Счет, Кеш)
	
	Уровень = Кеш[Счет];
	Если Уровень <> Неопределено Тогда
		Возврат Уровень;
	КонецЕсли;
	
	Уровень = Счет.Уровень();
	Кеш.Вставить(Счет, Уровень);
	
	Возврат Уровень;
	
КонецФункции

#КонецОбласти
