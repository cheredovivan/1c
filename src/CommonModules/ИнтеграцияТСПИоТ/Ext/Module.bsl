
#Область ПрограммныйИнтерфейс

// Функция возвращает признак, что проверка разрешительного режима настроена через ТС ПИоТ для ФН.
// 
// Параметры:
//  ПараметрыСканирования - см. ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования
//  ВидПродукции          - ПеречислениеСсылка.ВидыПродукцииИС - вид продукции для отбора
// 
// Возвращаемое значение:
//  Булево - Истина, если для указанного ФН используется проверка кодов маркировки через ТС ПИоТ.
Функция РазрешительныйРежимИспользуетсяПодключениеТСПИоТ(ПараметрыСканирования, ВидПродукции = Неопределено) Экспорт
	
	Если ВидПродукции = Неопределено
		И Не ШтрихкодированиеОбщегоНазначенияИС.ДопустимаПродукцияИСМП(ПараметрыСканирования, Истина) Тогда
		Возврат Ложь;
	ИначеЕсли ВидПродукции <> Неопределено
		И Не ОбщегоНазначенияИСПовтИсп.ЭтоПродукцияИСМП(ВидПродукции, Истина) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыСканирования.НомерФискальногоНакопителя) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НастроеноПодключениеТСПИоТ = ПоНомеруФискальногоНакопителяНастроеноПодключениеТСПИоТ(ПараметрыСканирования.НомерФискальногоНакопителя);
	
	Возврат НастроеноПодключениеТСПИоТ;
	
КонецФункции

// Функция возвращает признак, что по указанному номеру фискального накопителя проверка разрешительного режима настроена через ТС ПИоТ для ФН.
// 
// Параметры:
//  НомерФискальногоНакопителя - Строка - номер фискального накопителя
// 
// Возвращаемое значение:
//  Булево - Истина, если для указанного ФН используется проверка кодов маркировки через ТС ПИоТ.
Функция ПоНомеруФискальногоНакопителяНастроеноПодключениеТСПИоТ(НомерФискальногоНакопителя) Экспорт
	
	НастроеноПодключениеТСПИоТ = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	НастройкиПодключенияТСПИоТ.НомерФискальногоНакопителя
		|ИЗ
		|	РегистрСведений.НастройкиПодключенияТСПИоТ КАК НастройкиПодключенияТСПИоТ
		|ГДЕ
		|	НастройкиПодключенияТСПИоТ.НомерФискальногоНакопителя = &НомерФискальногоНакопителя";
	
	Запрос.УстановитьПараметр("НомерФискальногоНакопителя", НомерФискальногоНакопителя);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		НастроеноПодключениеТСПИоТ = Истина;
	КонецЕсли;
	
	Возврат НастроеноПодключениеТСПИоТ;
	
КонецФункции

// Функция возвращает признак, что проверка разрешительного режима настроена через ТС ПИоТ для ФН.
// 
// Параметры:
//  ПараметрыСканирования - см. ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования
// 
// Возвращаемое значение:
//  Структура - результат проверки настройки:
//   * Настроено - Булево - Истина, если для указанного ФН используется проверка кодов маркировки через ТС ПИоТ
//   * НастройкиПодключения - см. ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияТСПИоТ.
Функция НастройкиПодключенияЭкземпляраТСПИоТ(ПараметрыСканирования) Экспорт
	
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("Настроено",            Ложь);
	СтруктураВозврата.Вставить("НастройкиПодключения", ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияТСПИоТ());
	
	Если Не ШтрихкодированиеОбщегоНазначенияИС.ДопустимаПродукцияИСМП(ПараметрыСканирования, Истина) Тогда
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыСканирования.НомерФискальногоНакопителя) Тогда
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	НастройкиПодключенияТСПИоТ.Адрес                КАК АдресПодключения,
		|	НастройкиПодключенияТСПИоТ.ОбменНаСервере       КАК ОбменНаСервере,
		|	НастройкиПодключенияТСПИоТ.Сервер               КАК Сервер,
		|	НастройкиПодключенияТСПИоТ.Порт                 КАК Порт,
		|	НастройкиПодключенияТСПИоТ.ЗащищенноеСоединение КАК ЗащищенноеСоединение
		|ИЗ
		|	РегистрСведений.НастройкиПодключенияТСПИоТ КАК НастройкиПодключенияТСПИоТ
		|ГДЕ
		|	НастройкиПодключенияТСПИоТ.НомерФискальногоНакопителя = &НомерФискальногоНакопителя";
	
	Запрос.УстановитьПараметр("НомерФискальногоНакопителя", ПараметрыСканирования.НомерФискальногоНакопителя);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	НастройкиПодключения   = Неопределено;
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		НастройкиПодключения = ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияТСПИоТ();
		ЗаполнитьЗначенияСвойств(НастройкиПодключения, ВыборкаДетальныеЗаписи);
		
		СтруктураВозврата.Настроено            = Истина;
		СтруктураВозврата.НастройкиПодключения = НастройкиПодключения;
		
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Функция возвращает дополнительные данные для запроса в ТС ПИоТ. Передача этих данных помогает сократить запрос на сервер при
// 		установке ТС ПИоТ на клиенте.
// 
// Параметры:
//  ПараметрыСканирования - см. ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования
// 
// Возвращаемое значение:
//  Структура - Дополнительные данные запроса ТС ПИоТ:
// * Организация                - ОпределяемыйТип.Организация - организация, проверяющая марку
// * ИННОрганизации             - Строка - ИНН организации, проверяющей марку
// * КешОрганизаций             - Соответствие из КлючИЗначение:
// 		** Ключ - ОпределяемыйТип.Организация
// 		** Значение - Строка - ИНН
// * ЛогироватьУспешныеПроверки - Булево - Истина, если требуется логирование успешных операций запросов в РР
// * ДанныеКлиента -  Структура из КлючИЗначение:
//		** НаименованиеКассовогоПО  - Строка - Наименование ПМСР (кассового ПО)
//		** ВерсияКассовогоПО        - Строка - Версия ПМСР (кассового ПО)
//		** ИдентификаторКассовогоПО - Строка - Идентификатор ПМСР (кассового ПО) в реестре ГИС МТ
//		** КонтрольнаяСумма         - Строка - Контрольная сумма/ЭЦП исполняемого файла ПМСР (кассового ПО)
// * ПроверкаЧерезТСПИоТ - Булево - Истина, признак, что проверка идет через ТС ПИоТ.
Функция ДополнительныеДанныеЗапросаТСПИоТ(ПараметрыСканирования) Экспорт
	
	СтруктураДополнительныхПараметров = Новый Структура();
	
	НастройкиСканирования = ОбщегоНазначенияИСМПКлиентСерверПовтИсп.НастройкиСканированияКодовМаркировки();
	РежимыКонтроля        = НастройкаПараметровСканированияСлужебныйКлиентСерверПовтИсп.РежимыКонтроляСредствамиККТ();
	
	ЛогироватьУспешныеПроверки = (НастройкиСканирования.РежимКонтроляСредствамиККТ = РежимыКонтроля.ПередПробитиемЧека)
		И ОбщегоНазначенияИСМППовтИсп.ХранитьУспешныеОперацииПриПроверкеКМ();
	
	ИнформацияОСистеме = Новый СистемнаяИнформация();
	
	ВерсияБГосИС       = ОбщегоНазначенияИС.ВерсияБиблиотеки();
	НаименованиеБГосИС = "БиблиотекаИнтеграцииГосИС";
	
	// todo - ИдентификаторКассовогоПО - уникальный код после получения сертификации
	
	ДанныеКлиента = Новый Структура();
	ДанныеКлиента.Вставить("НаименованиеКассовогоПО",  НаименованиеБГосИС);                               // Наименование ПМСР (кассового ПО)
	ДанныеКлиента.Вставить("ВерсияКассовогоПО",        ВерсияБГосИС);                                     // Версия ПМСР (кассового ПО)
	ДанныеКлиента.Вставить("ИдентификаторКассовогоПО", ПараметрыСканирования.НомерФискальногоНакопителя); // Идентификатор ПМСР (кассового ПО) в реестре ГИС МТ
	ДанныеКлиента.Вставить("КонтрольнаяСумма",         Строка(ИнформацияОСистеме.ИдентификаторКлиента));  // Контрольная сумма/ЭЦП исполняемого файла ПМСР (кассового ПО)
	
	СтруктураДополнительныхПараметров.Вставить("Организация",                ПараметрыСканирования.Организация);
	СтруктураДополнительныхПараметров.Вставить("ИННОрганизации",             "");
	СтруктураДополнительныхПараметров.Вставить("КешОрганизаций",             Новый Соответствие());
	СтруктураДополнительныхПараметров.Вставить("ЛогироватьУспешныеПроверки", ЛогироватьУспешныеПроверки);
	СтруктураДополнительныхПараметров.Вставить("ДанныеКлиента",              ДанныеКлиента);
	СтруктураДополнительныхПараметров.Вставить("ПроверкаЧерезТСПИоТ",        Истина);
	
	СтруктураДополнительныхПараметров.ИННОрганизации = РаботаСКонтрагентамиИСВызовСервера.ИННКПППоОрганизацииКонтрагенту(ПараметрыСканирования.Организация).ИНН;
	СтруктураДополнительныхПараметров.КешОрганизаций.Вставить(ПараметрыСканирования.Организация, СтруктураДополнительныхПараметров.ИННОрганизации);
	
	Возврат СтруктураДополнительныхПараметров;
	
КонецФункции

// Процедура заполняет данные разрешительного режима при розничной продаже с использованием ТС ПИоТ.
// 
// Параметры:
//  ДанныеПоШтрихкодам         - см. ШтрихкодированиеОбщегоНазначенияИС.ИнициализацияТаблицыДанныхКодовМаркировки
//  ДанныеКМДляПроверки        - Массив Из СтрокаТабличнойЧасти: см. ШтрихкодированиеОбщегоНазначенияИС.ИнициализацияТаблицыДанныхКодовМаркировки - Коллекция строк таблицы
//  НастройкиПодключенияТСПИоТ - см. НастройкиПодключенияЭкземпляраТСПИоТ
//  ПараметрыСканирования      - см. ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования.
Процедура ЗаполнитьДанныеИдентификатораРозничнойПродажиПоДаннымТСПИоТ(ДанныеПоШтрихкодам, ДанныеКМДляПроверки, НастройкиПодключенияТСПИоТ, ПараметрыСканирования) Экспорт
	
	Если Не НастройкиПодключенияТСПИоТ.Настроено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметрыЗапроса = ДополнительныеДанныеЗапросаТСПИоТ(ПараметрыСканирования);
	
	Если ОбщегоНазначенияИСМПКлиентСерверПовтИсп.ДействуетАварийныйРежимДляРозничныхПродаж() Тогда
		
		Для Каждого СтрокаДанныхПоШтрихкодам Из ДанныеПоШтрихкодам.ДанныеКодовМаркировки Цикл
			СтрокаДанныхПоШтрихкодам.РазрешительныйРежимКодОтвета = ОбщегоНазначенияИСМПКлиентСерверПовтИсп.АварийныйРежимКодОтветаСервисаГИСМТ();
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	Если Не НастройкиПодключенияТСПИоТ.НастройкиПодключения.ОбменНаСервере Тогда
		
		ДанныеНастройкиТСПИоТ = Новый Структура();
		ДанныеНастройкиТСПИоТ.Вставить("НастройкиПодключенияТСПИоТ", НастройкиПодключенияТСПИоТ);
		ДанныеНастройкиТСПИоТ.Вставить("ДополнительныеПараметры",    ДополнительныеПараметрыЗапроса);
		
		Для Каждого СтрокаДанныхПоШтрихкодам Из ДанныеКМДляПроверки Цикл
			
			СтрокаДанныхПоШтрихкодам.ТребуетсяПроверкаРазрешительногоРежимаНаТСПИоТ = Истина;
			
			Если Не СтрокаДанныхПоШтрихкодам.СтрокаДерева = Неопределено Тогда
				СтрокаДанныхПоШтрихкодам.СтрокаДерева.ТребуетсяПроверкаРазрешительногоРежимаНаТСПИоТ = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		ДанныеПоШтрихкодам.ДанныеНастройкиТСПИоТ = ДанныеНастройкиТСПИоТ;
		Возврат;
		
	КонецЕсли;
	
	РезультатПроверкиТСПИоТ = ИнтеграцияТСПИоТКлиентСервер.ДанныеПоКодуМаркировкиПриРозничнойПродаже(НастройкиПодключенияТСПИоТ.НастройкиПодключения,
		ДанныеКМДляПроверки,
		ПараметрыСканирования,
		ДополнительныеПараметрыЗапроса);
	
	Если РезультатПроверкиТСПИоТ.АварийныйРежим Тогда
		
		ОбщегоНазначенияИСМП.ВключитьАварийныйРежимРазрешительнойСистемы();
		
		Для Каждого СтрокаДанныхПоШтрихкодам Из ДанныеПоШтрихкодам.ДанныеКодовМаркировки Цикл
			СтрокаДанныхПоШтрихкодам.РазрешительныйРежимКодОтвета = ОбщегоНазначенияИСМПКлиентСерверПовтИсп.АварийныйРежимКодОтветаСервисаГИСМТ();
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	МассивСтрокДляУдаления = Новый Массив;
	
	Для Каждого СтрокаДанныхПоШтрихкодам Из ДанныеПоШтрихкодам.ДанныеКодовМаркировки Цикл
		
		ПараметрыКодаМаркировкиОнлайн = РезультатПроверкиТСПИоТ.СтатусыКодовМаркировкиГИСМТ.Получить(СтрокаДанныхПоШтрихкодам);
		НаличиеСерыхКодов             = Ложь;
		
		// обработка онлайн запроса
		Если ПараметрыКодаМаркировкиОнлайн <> Неопределено Тогда
			
			Для Каждого КлючИЗначение Из ПараметрыКодаМаркировкиОнлайн Цикл
				
				Если Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
					Продолжить;
				КонецЕсли;
				
				Если ДанныеПоШтрихкодам.ДанныеКодовМаркировки.Колонки.Найти(КлючИЗначение.Ключ) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				СтрокаДанныхПоШтрихкодам[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
				
			КонецЦикла;
			
			НаличиеСерыхКодов = ОбщегоНазначенияИСПовтИсп.ЭтоПродукцияМОТП(СтрокаДанныхПоШтрихкодам.ВидПродукции)
				И ПараметрыКодаМаркировкиОнлайн.Свойство("ВСеройЗоне")
				И ПараметрыКодаМаркировкиОнлайн.ВСеройЗоне;
			
			Если НаличиеСерыхКодов Тогда
				
				СтрокаДанныхПоШтрихкодам.КоличествоПотребительскихУпаковок = СтрокаДанныхПоШтрихкодам.ПлановоеКоличествоПотребительскихУпаковок;
				СтрокаДанныхПоШтрихкодам.Количество                        = СтрокаДанныхПоШтрихкодам.ПлановоеКоличествоПотребительскихУпаковок;
				СтрокаДанныхПоШтрихкодам.СодержитСерыеКоды                 = НаличиеСерыхКодов;
				
			КонецЕсли;
			
			// продажа разрешена без ответа разрешительного режима
			СтрокаДанныхПоШтрихкодам.РазрешительныйРежимДатаЗапросаГИСМТ = Формат(ОбщегоНазначенияИС.ДатаВСтрокуUNIX(ТекущаяДатаСеанса()), "ЧРГ=; ЧГ=0;");
			
		Иначе
			
			Если ЗначениеЗаполнено(РезультатПроверкиТСПИоТ.ТекстОшибки) Тогда
				
				СтрокаДанныхПоШтрихкодам.ТекстОшибки = РезультатПроверкиТСПИоТ.ТекстОшибки;
				СтрокаДанныхПоШтрихкодам.КонтролиРазрешительногоРежима.Добавить(СтрокаДанныхПоШтрихкодам.ТекстОшибки);
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Коды упаковки были дозагружены из ГИС МТ
		СтрокаДанныхПоШтрихкодам.Идентифицирован = Истина;

		Если СтрокаДанныхПоШтрихкодам.СтрокаДерева = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаДанныхПоШтрихкодам.СтрокаДерева, СтрокаДанныхПоШтрихкодам);
		
		Если Не СтрокаДанныхПоШтрихкодам.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаПодчиненнойУпаковки Из СтрокаДанныхПоШтрихкодам.СтрокаДерева.Строки Цикл
			
			СтруктураЗаполнения = Новый Структура();
			СтруктураЗаполнения.Вставить("GTIN",                             Неопределено);
			СтруктураЗаполнения.Вставить("Статус",                           Неопределено);
			СтруктураЗаполнения.Вставить("ИННВладельца",                     Неопределено);
			СтруктураЗаполнения.Вставить("ИННПроизводителя",                 Неопределено);
			СтруктураЗаполнения.Вставить("МРЦ",                              Неопределено);
			СтруктураЗаполнения.Вставить("ПродажаПоДаннымОффлайнРежима",     Неопределено);
			
			СтруктураЗаполнения.Вставить("РазрешительныйРежимИдентификаторЗапросаГИСМТ", Неопределено);
			СтруктураЗаполнения.Вставить("РазрешительныйРежимДатаЗапросаГИСМТ",          Неопределено);
			СтруктураЗаполнения.Вставить("РазрешительныйРежимИдентификаторЭкземпляраПО", Неопределено);
			СтруктураЗаполнения.Вставить("РазрешительныйРежимВерсияБазы",                Неопределено);
			СтруктураЗаполнения.Вставить("РазрешительныйРежимАдресСервера",              Неопределено);
			СтруктураЗаполнения.Вставить("РазрешительныйРежимТелоЗапросаJSON",           Неопределено);
			СтруктураЗаполнения.Вставить("РазрешительныйРежимТелоОтветаJSON",            Неопределено);
			СтруктураЗаполнения.Вставить("РазрешительныйРежимКодОтвета",                 Неопределено);
			
			Если ПараметрыКодаМаркировкиОнлайн <> Неопределено Тогда
				
				ЗаполнитьЗначенияСвойств(СтруктураЗаполнения, ПараметрыКодаМаркировкиОнлайн);
				
				Для Каждого КлючИЗначение Из СтруктураЗаполнения Цикл
				
					Если Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
						Продолжить;
					КонецЕсли;
					
					СтруктураПроверки = Новый Структура(КлючИЗначение.Ключ, Null);
					ЗаполнитьЗначенияСвойств(СтруктураПроверки, СтрокаПодчиненнойУпаковки);
					
					Если СтруктураПроверки[КлючИЗначение.Ключ] = Null Тогда
						Продолжить;
					КонецЕсли;
					
					СтрокаПодчиненнойУпаковки[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
					
				КонецЦикла;
				
			КонецЕсли;
			
			СтруктураОтбора = Новый Структура("Штрихкод", СтрокаПодчиненнойУпаковки.Штрихкод);
			ДанныеВложеннойУпаковки = ДанныеПоШтрихкодам.ДанныеКодовМаркировки.НайтиСтроки(СтруктураОтбора);
			
			Для Каждого СтруктураВложеннойУпаковки Из ДанныеВложеннойУпаковки Цикл
				
				ЗаполнитьЗначенияСвойств(СтруктураВложеннойУпаковки, СтрокаПодчиненнойУпаковки);
			
				Если НаличиеСерыхКодов И СтрокаДанныхПоШтрихкодам.СодержитСерыеКоды Тогда
					
					МассивСтрокДляУдаления.Добавить(СтруктураВложеннойУпаковки);
					
				КонецЕсли;
			
			КонецЦикла;
			
		КонецЦикла;
		
		Если НаличиеСерыхКодов И СтрокаДанныхПоШтрихкодам.СодержитСерыеКоды Тогда
			СтрокаДанныхПоШтрихкодам.СтрокаДерева.Строки.Очистить();
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого УдаляемаяСтрока Из МассивСтрокДляУдаления Цикл
		ДанныеПоШтрихкодам.ДанныеКодовМаркировки.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
