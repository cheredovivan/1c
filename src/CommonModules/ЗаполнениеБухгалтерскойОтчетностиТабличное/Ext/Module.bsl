#Область ПрограммныйИнтерфейс

// Обеспечивает заполнение отчета (в терминах БРО - "раздела") в составе бухгалтерской отчетности в виде простой таблицы,
// которая по составу колонок соответствует визуализации отчета за один период.
//
// Параметры:
//  Контейнер      - ДеревоЗначений - заполняемые данные разделов в терминах формы отчета; в Контейнер помещается результат заполнения
//  ОтборРазделов  - Структура - Ключ: имя раздела; Значение: Булево, Истина, если раздел нужно заполнить
//  Контекст - БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета
//  Периоды - Массив из БухгалтерскаяОтчетностьБРО.НовыйПериодОтчета - периоды, за которые нужно заполнить отчет
//  Организации - Массив Из СправочникСсылка.Организации
//
Процедура ЗаполнитьТаблицыОтчета(Контейнер, ОтборРазделов, КонтекстОтчета, Периоды, Организации) Экспорт
	
	ТаблицыОтчетаКЗаполнению = ТаблицыОтчетаКЗаполнению(КонтекстОтчета, ОтборРазделов);
	Если Не ЗначениеЗаполнено(ТаблицыОтчетаКЗаполнению) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнениеОтчета = НачатьЗаполнениеОтчета(Периоды, Организации, ТаблицыОтчетаКЗаполнению);
	ЗаполнитьОтчет(ЗаполнениеОтчета);
	ЗаполнениеБухгалтерскойОтчетностиРазмещение.ЗаполнитьКонтейнер(Контейнер, ЗаполнениеОтчета.Результат);
	
КонецПроцедуры

// Определяет описание таблиц заполняемых разделов.
// Раздел может содержать одну или несколько таблиц.
// Описание включает состав периодов, набор колонок, варианты значений полей.
//
// Параметры:
//  ОтборРазделов  - Структура - Ключ: имя раздела; Значение: Булево, Истина, если раздел нужно заполнить
//  КонтекстОтчета - БухгалтерскаяОтчетностьБРО.НовыйКонтекстОтчета
//
// Возвращаемое значение:
//  Структура - описывает таблицы отчета, входящие в состав заполняемых разделов;
//   * Ключ: имя таблицы отчета по ЗаполнениеБухгалтерскойОтчетностиРазмещение.СоставТаблицОтчета
//   * Значение: см. НовыйОписаниеТаблицыОтчета
//
Функция ЗаполняемыеОтчеты(ОтборРазделов, КонтекстОтчета) Экспорт
	
	ЗаполняемыеОтчеты = Новый Структура;
	
	ТаблицыОтчетаКЗаполнению = ТаблицыОтчетаКЗаполнению(КонтекстОтчета, ОтборРазделов);
	
	Для Каждого ИмяТаблицыОтчета Из ТаблицыОтчетаКЗаполнению Цикл
		
		ОписаниеТаблицыОтчета = НовыйОписаниеТаблицыОтчета();
		ЗаполнитьОписаниеТаблицыОтчета(ОписаниеТаблицыОтчета, ИмяТаблицыОтчета);
		
		ЗаполняемыеОтчеты.Вставить(ИмяТаблицыОтчета, ОписаниеТаблицыОтчета);
		
	КонецЦикла;
	
	Возврат ЗаполняемыеОтчеты;
	
КонецФункции

// Определяет данные для заполнения отчета в виде набора простых таблиц.
// Следует использовать, если данные отчета нужны вне контекста заполнения регламентированного отчета
// (например, для демонстрации пользователю ключевых показателей).
//
// Параметры:
//  Периоды - Массив из БухгалтерскаяОтчетностьБРО.НовыйПериодОтчета - периоды, за которые нужны данные
//  Организации - Массив Из СправочникСсылка.Организации
//  ТаблицыОтчетаКЗаполнению - Массив Из Строка - имена таблиц отчета по ЗаполнениеБухгалтерскойОтчетностиРазмещение.СоставТаблицОтчета,
//                             данные которых нужны
// 
// Возвращаемое значение:
//  см. ЗаполнениеБухгалтерскойОтчетностиРазмещение.НовыйРезультатЗаполненияТаблицОтчета - данные заполнения, включая расшифровку порядка заполнения
//
Функция ДанныеЗаполненияТаблицОтчета(Периоды, Организации, ТаблицыОтчетаКЗаполнению) Экспорт
	
	ЗаполнениеОтчета = НачатьЗаполнениеОтчета(Периоды, Организации, ТаблицыОтчетаКЗаполнению);
	
	ЗаполнитьОтчет(ЗаполнениеОтчета);
	
	Возврат ЗаполнениеОтчета.Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ПолучениеДанныхТаблицыОтчета

// Общие методы по составлению таблицы с прикладными данными за один период и заполнению ее расшифровки

Функция ТаблицаОтчета(ПолучениеДанныхТаблицОтчета, ИмяТаблицы) Экспорт
	
	ДанныеТаблицыОтчета = Неопределено;
	Если Не ПолучениеДанныхТаблицОтчета.ТаблицыОтчета.Свойство(ИмяТаблицы, ДанныеТаблицыОтчета) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДанныеТаблицыОтчета.ТаблицаОтчета;
	
КонецФункции

Процедура УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстов, КонтекстЗаполненияОтчета) Экспорт
	
	КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	ФорматДаты = "ДЛФ=DD";
	
	ПредставлениеПериода = БухгалтерскийУчет.ПредставлениеПериодаБухгалтерскойОтчетности(
		КонтекстЗаполненияОтчета.Период.КонецДата,
		КодЯзыка);
		
	ПараметрыТекстов.Вставить("Период", ПредставлениеПериода);
	ПараметрыТекстов.Вставить("КонецПредшествующегоПериода", Формат(КонтекстЗаполненияОтчета.Период.Начало - 1, ФорматДаты));
	ПараметрыТекстов.Вставить("НачалоПериода", Формат(КонтекстЗаполненияОтчета.Период.Начало,    ФорматДаты));
	ПараметрыТекстов.Вставить("КонецПериода",  Формат(КонтекстЗаполненияОтчета.Период.КонецДата, ФорматДаты));
	
КонецПроцедуры

Функция НачатьВыводТаблицы(ПолучениеДанныхТаблицОтчета, ИмяТаблицы, ШаблоныЗаголовковКолонок = Неопределено, ПараметрыТекста = Неопределено) Экспорт
	
	Если Не ПолучениеДанныхТаблицОтчета.ТаблицыОтчета.Свойство(ИмяТаблицы) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВыводТаблицы = НовыйПолучениеДанныхТаблицыОтчета();
	
	ВыводТаблицы.ИмяТаблицы = ИмяТаблицы;
	ЗаполнитьЗначенияСвойств(ВыводТаблицы, ПолучениеДанныхТаблицОтчета, "КонтекстЗаполненияОтчета, Расшифровка");
	ВыводТаблицы.Данные = ПолучениеДанныхТаблицОтчета.ТаблицыОтчета[ИмяТаблицы];
	
	Если ВыводТаблицы.КонтекстЗаполненияОтчета.Организации.Количество() > 1 Тогда
		ВыводТаблицы.ВыводитьФилиалы = Истина;
	КонецЕсли;
	
	Если ШаблоныЗаголовковКолонок <> Неопределено Тогда
		Для Каждого ШаблонКолонки Из ШаблоныЗаголовковКолонок Цикл
			Если ПараметрыТекста = Неопределено Тогда
				ЗаголовокКолонки = ШаблонКолонки.Значение;
			Иначе
				ЗаголовокКолонки = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонКолонки.Значение, ПараметрыТекста);
			КонецЕсли;
			ВыводТаблицы.Данные.ЗаголовкиКолонок.Вставить(ШаблонКолонки.Ключ, ЗаголовокКолонки);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ВыводТаблицы;
	
КонецФункции

Функция ДобавитьСтрокуТаблицыОтчета(ВыводТаблицы, ШаблоныКолонок, ПараметрыТекстов) Экспорт
	
	НоваяСтрока = ВыводТаблицы.Данные.ТаблицаОтчета.Добавить();
	
	ВыводТаблицы.НачальныйУровеньРасшифровкиДанныхПоОрганизации = 0;
	
	Для Каждого ШаблонКолонки Из ШаблоныКолонок Цикл
		
		ВывестиЭлементЗаголовкаРасшифровки(ВыводТаблицы, "ЗаголовокГруппы", ПараметрыТекстов, ШаблонКолонки.Ключ, ШаблонКолонки.Значение, Истина);
		ВывестиЭлементЗаголовкаРасшифровки(ВыводТаблицы, "ПорядокРасчета",  ПараметрыТекстов, ШаблонКолонки.Ключ, ШаблонКолонки.Значение);
		
	КонецЦикла;
		
	Если ВыводТаблицы.ВыводитьФилиалы Тогда
		ВыводТаблицы.НачальныйУровеньРасшифровкиДанныхПоОрганизации = 1;
	КонецЕсли;
	
	Возврат НоваяСтрока;
	
КонецФункции

Функция ДобавитьСлагаемое(ВыводТаблицы, ИмяКолонки, Описание, Сумма = 0, ПредметЗаписи = Неопределено, Отчет = Неопределено, Уровень = 0) Экспорт
	
	СтрокаТаблицыОтчета = КрайняяСтрокаТаблицы(ВыводТаблицы);
	
	СтрокаТаблицыОтчета[ИмяКолонки] = СтрокаТаблицыОтчета[ИмяКолонки] + Сумма;
	
	Возврат ДобавитьЗаписьРасшифровкиСтроки(ВыводТаблицы, ИмяКолонки, "Слагаемое", Описание, Сумма, ПредметЗаписи, Отчет, Уровень);
	
КонецФункции

Процедура ДобавитьФилиал(Знач ВыводимыеТаблицы, ШаблоныКолонок, Организация, ОрганизацияПредставление) Экспорт
	
	ПредставитьМассивом(ВыводимыеТаблицы);
	
	Для Каждого ВыводТаблицы Из ВыводимыеТаблицы Цикл
		Если Не ВыводТаблицы.ВыводитьФилиалы Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого ШаблонКолонки Из ШаблоныКолонок Цикл
			Если Не ШаблонКолонки.Значение.Определен Тогда
				Продолжить;
			КонецЕсли;
			ДобавитьЗаписьРасшифровкиСтрокиОрганизация(ВыводТаблицы, ШаблонКолонки.Ключ, Организация, ОрганизацияПредставление);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗакончитьДобавлениеСтрокиТаблицыОтчета(Знач ВыводимыеТаблицы, ШаблоныКолонок) Экспорт
	
	ПредставитьМассивом(ВыводимыеТаблицы);
	
	Для Каждого ВыводТаблицы Из ВыводимыеТаблицы Цикл
	
		СтрокаТаблицыОтчета = КрайняяСтрокаТаблицы(ВыводТаблицы);
		
		Для Каждого Колонка Из ВыводТаблицы.Данные.ТаблицаОтчета.Колонки Цикл
		
			ШаблонКолонки = ШаблоныКолонок[Колонка.Имя];
			
			Если ШаблонКолонки = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ДобавитьЗаписьРасшифровкиСтроки(
				ВыводТаблицы,
				Колонка.Имя,
				"Итог",
				ТекстИтого(),
				СтрокаТаблицыОтчета[Колонка.Имя]);
				
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДобавитьЗаписьРасшифровкиСтроки(ВыводТаблицы, ИмяКолонки, ТипЗаписи, Описание, Сумма = 0, ПредметЗаписи = Неопределено, Отчет = Неопределено, Уровень = 0) Экспорт
	
	Возврат ДобавитьЗаписьРасшифровкиТаблицыОтчета(
		ВыводТаблицы.Расшифровка,
		ВыводТаблицы.Данные,
		КрайняяСтрокаТаблицы(ВыводТаблицы),
		ИмяКолонки,
		ТипЗаписи,
		Описание,
		Сумма,
		ПредметЗаписи,
		Отчет,
		ВыводТаблицы.НачальныйУровеньРасшифровкиДанныхПоОрганизации + Уровень);
		
КонецФункции

Функция ДобавитьЗаписьРасшифровкиСтрокиОрганизация(ВыводТаблицы, ИмяКолонки, Организация, Знач ПредставлениеОрганизации = "", Сумма = 0, Отчет = Неопределено) Экспорт
	
	Если Не ВыводТаблицы.ВыводитьФилиалы Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ПустаяСтрока(ПредставлениеОрганизации) Тогда
		ПредставлениеОрганизации = Строка(Организация);
	КонецЕсли;
	
	Возврат ДобавитьЗаписьРасшифровкиСтроки(
		ВыводТаблицы,
		ИмяКолонки,
		"Организация",
		ПредставлениеОрганизации,
		Сумма,
		Организация,
		Отчет,
		-1); // Сведения о филиале выводятся перед начальным уровнем
	
КонецФункции

Функция ДобавитьЗаписьРасшифровкиТаблицыОтчета(Расшифровка, ДанныеТаблицыОтчета, СтрокаТаблицыОтчета, КолонкаТаблицыОтчета, ТипЗаписи, Описание, Сумма = 0, ПредметЗаписи = Неопределено, Отчет = Неопределено, Уровень = 0) Экспорт
	
	// См. также ЗаполнениеБухгалтерскойОтчетностиНастраиваемая.ДобавитьЗаписьРасшифровкиЗаполненияПоказателя
	
	ЗаписьРасшифровки = Расшифровка.Добавить();
	ЗаписьРасшифровки.Уровень       = Уровень;
	ЗаписьРасшифровки.ТипЗаписи     = ТипЗаписи;
	ЗаписьРасшифровки.ПредметЗаписи = ПредметЗаписи;
	ЗаписьРасшифровки.Описание      = Описание;
	ЗаписьРасшифровки.Сумма         = Сумма;
	Если Отчет <> Неопределено Тогда
		ЗаписьРасшифровки.Отчет = Отчет;
	КонецЕсли;
	
	ДополнитьИндексРасшифровки(
		ДанныеТаблицыОтчета.ИндексРасшифровки,
		СтрокаТаблицыОтчета,
		КолонкаТаблицыОтчета,
		ЗаписьРасшифровки);
	
	Возврат ЗаписьРасшифровки;
	
КонецФункции

Функция КрайняяСтрокаТаблицы(ВыводТаблицы) Экспорт
	
	Возврат ВыводТаблицы.Данные.ТаблицаОтчета[ВыводТаблицы.Данные.ТаблицаОтчета.Количество() - 1];
	
КонецФункции

Функция НовыйШаблонЗаголовковРасшифровки() Экспорт
	
	Шаблон = Новый Структура;
	
	Шаблон.Вставить("Определен",        Истина);
	Шаблон.Вставить("ЗаголовокГруппы",  "");
	Шаблон.Вставить("ПорядокРасчета",   "");
	
	Возврат Шаблон;
	
КонецФункции

Функция ПредставлениеСписка(Список) Экспорт
	
	Представление = Новый Структура;
	
	Представление.Вставить("Количество", Список.Количество());
	Представление.Вставить("Текст",      "");
	
	Если ТипЗнч(Список) = Тип("СписокЗначений") Тогда
		Представления = Новый Массив;
		Для Каждого ЭлементСписка Из Список Цикл
			Если Не ЭлементСписка.Пометка Тогда
				Продолжить;
			КонецЕсли;
			Представления.Добавить(ЭлементСписка.Представление);
		КонецЦикла;
	Иначе
		Представления = Список;
	КонецЕсли;
	
	Представление.Текст = СтрСоединить(Представления, ", ");
	
	Возврат Представление;
	
КонецФункции

Функция ПредставлениеСпискаСчетовРодительныйПадеж(ПредставлениеСписка) Экспорт
	
	КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	
	Шаблон = НСтр("ru = 'счета %1';
					|en = '%1 accounts'", КодЯзыка);
	Если ПредставлениеСписка.Количество > 1 Тогда
		Шаблон = НСтр("ru = 'счетов %1';
						|en = '%1 accounts'", КодЯзыка);
	КонецЕсли;
	
	Возврат СтрШаблон(Шаблон, ПредставлениеСписка.Текст);
	
КонецФункции

Функция ПредставлениеСпискаСчетовДательныйПадеж(ПредставлениеСписка) Экспорт
	
	КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	
	Шаблон = НСтр("ru = 'счету %1';
					|en = '%1 account'", КодЯзыка);
	Если ПредставлениеСписка.Количество > 1 Тогда
		Шаблон = НСтр("ru = 'счетам %1';
						|en = '%1 accounts'", КодЯзыка);
	КонецЕсли;
	
	Возврат СтрШаблон(Шаблон, ПредставлениеСписка.Текст);
	
КонецФункции

Функция ПредставлениеСпискаСчетовТворительныйПадеж(ПредставлениеСписка) Экспорт
	
	КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	
	Шаблон = НСтр("ru = 'счетом %1';
					|en = '%1 account'", КодЯзыка);
	Если ПредставлениеСписка.Количество > 1 Тогда
		Шаблон = НСтр("ru = 'счетами %1';
						|en = '%1 accounts'", КодЯзыка);
	КонецЕсли;
	
	Возврат СтрШаблон(Шаблон, ПредставлениеСписка.Текст);
	
КонецФункции

// Возвращает счета из списка счетов (включая субсчета), на которых ведется учет в разрезе
// указанного вида субконто.
// 
// Параметры:
//  ПредопределенныеСчета - Массив из ПланСчетовСсылка.Хозрасчетный
//  ВидСубконто - ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные
// 
// Возвращаемое значение:
//  Массив из ПланСчетовСсылка.Хозрасчетный
Функция СчетаПоСубконто(ПредопределенныеСчета, ВидСубконто) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Счета",       ПредопределенныеСчета);
	Запрос.УстановитьПараметр("ВидСубконто", ВидСубконто);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйВидыСубконто.Ссылка КАК Ссылка
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
	|ГДЕ
	|	ХозрасчетныйВидыСубконто.Ссылка В ИЕРАРХИИ(&Счета)
	|	И ХозрасчетныйВидыСубконто.ВидСубконто = &ВидСубконто
	|	И ХозрасчетныйВидыСубконто.Суммовой";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПодключениеПрикладныхМетодов

Процедура ЗаполнитьОписаниеТаблицыОтчета(ОписаниеТаблицыОтчета, ИмяТаблицыОтчета)
	
	ЗаполнениеБухгалтерскойОтчетностиПоясненияНематериальныеАктивы.ОписатьТаблицуОтчета(ОписаниеТаблицыОтчета, ИмяТаблицыОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияОсновныеСредства.ОписатьТаблицуОтчета(ОписаниеТаблицыОтчета, ИмяТаблицыОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияЗапасы.ОписатьТаблицуОтчета(ОписаниеТаблицыОтчета, ИмяТаблицыОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияРасходы.ОписатьТаблицуОтчета(ОписаниеТаблицыОтчета, ИмяТаблицыОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияЗадолженность.ОписатьТаблицуОтчета(ОписаниеТаблицыОтчета, ИмяТаблицыОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияОбязательства.ОписатьТаблицуОтчета(ОписаниеТаблицыОтчета, ИмяТаблицыОтчета);
	
КонецПроцедуры

Процедура ОпределитьСоставЗаполняемыхТаблицОтчета(ТаблицыОтчетаКЗаполнению, КонтекстОтчета)
	
	ЗаполнениеБухгалтерскойОтчетностиПоясненияНематериальныеАктивы.ОпределитьСоставЗаполняемыхПояснений(ТаблицыОтчетаКЗаполнению, КонтекстОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияОсновныеСредства.ОпределитьСоставЗаполняемыхПояснений(ТаблицыОтчетаКЗаполнению, КонтекстОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияЗапасы.ОпределитьСоставЗаполняемыхПояснений(ТаблицыОтчетаКЗаполнению, КонтекстОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияРасходы.ОпределитьСоставЗаполняемыхПояснений(ТаблицыОтчетаКЗаполнению, КонтекстОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияЗадолженность.ОпределитьСоставЗаполняемыхПояснений(ТаблицыОтчетаКЗаполнению, КонтекстОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияОбязательства.ОпределитьСоставЗаполняемыхПояснений(ТаблицыОтчетаКЗаполнению, КонтекстОтчета);
	
КонецПроцедуры

Процедура ПолучитьДанныеТаблицОтчета(ПолучениеДанныхТаблицОтчета, Классификаторы)
	
	ЗаполнениеБухгалтерскойОтчетностиПоясненияНематериальныеАктивы.ЗаполнитьТаблицыОтчета(ПолучениеДанныхТаблицОтчета, Классификаторы, ПолучениеДанныхТаблицОтчета.КонтекстЗаполненияОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияОсновныеСредства.ЗаполнитьТаблицыОтчета(ПолучениеДанныхТаблицОтчета, Классификаторы, ПолучениеДанныхТаблицОтчета.КонтекстЗаполненияОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияЗапасы.ЗаполнитьТаблицыОтчета(ПолучениеДанныхТаблицОтчета, Классификаторы, ПолучениеДанныхТаблицОтчета.КонтекстЗаполненияОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияРасходы.ЗаполнитьТаблицыОтчета(ПолучениеДанныхТаблицОтчета, Классификаторы, ПолучениеДанныхТаблицОтчета.КонтекстЗаполненияОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияЗадолженность.ЗаполнитьТаблицыОтчета(ПолучениеДанныхТаблицОтчета, Классификаторы, ПолучениеДанныхТаблицОтчета.КонтекстЗаполненияОтчета);
	ЗаполнениеБухгалтерскойОтчетностиПоясненияОбязательства.ЗаполнитьТаблицыОтчета(ПолучениеДанныхТаблицОтчета, Классификаторы, ПолучениеДанныхТаблицОтчета.КонтекстЗаполненияОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область КонструкторыОбъектов

// Данные

Функция НовыйОписаниеТаблицыОтчета()
	
	Описание = Новый Структура;
	
	// Таблица может содержать данные за период (в xbrl - duration, "ЗаПериод") или на дату (instant, "НаДату").
	// В некоторых случаях данные на дату удобнее заполнять в виде двух колонок - на начало и конец периода.
	// Этот вариант периода называется "НачалоКонец".
	// При его использовании таблица отчета на каждое из указанных в описании числовых значений будет содержать две колонки:
	// одна с данными на начало периода, вторая - на конец. См. также ИмяКолонкиНачалоПериода, ИмяКолонкиКонецПериода
	Описание.Вставить("ВариантПериода",   "ЗаПериод");
	Описание.Вставить("Представление",    "");              // отчета
	Описание.Вставить("Структура",        Новый Массив);    // из Строка: Имена колонок, описывающих структуру отчета
	Описание.Вставить("Классификаторы",   Новый Структура); // Ключ - имя колонки из Структура; Значение - строка, значения классификатора, применяемого для колонки (через запятую)
	                                                        //                                           - СписокЗначений, из значений классификатора, применяемого для колонки, и представлений
	Описание.Вставить("ЧисловыеЗначения", Новый Массив);    // из Строка: имена колонок, описывающих значения отчета
	
	Возврат Описание;
	
КонецФункции

Функция НовыйТаблицаОтчета(ОписаниеТаблицыОтчета)
	
	ТаблицаОтчета = Новый ТаблицаЗначений;
	
	ТипСумма  = БухгалтерскаяОтчетностьБРО.ТипСумма();
	
	ТаблицаОтчета.Колонки.Добавить("Вывод", Новый ОписаниеТипов("Булево")); // Истина, если показатель нужно вывести отдельной строкой (обычно - "в том числе")
	
	Для Каждого ИмяКолонки Из ОписаниеТаблицыОтчета.Структура Цикл
		ТаблицаОтчета.Колонки.Добавить(ИмяКолонки);
	КонецЦикла;
	
	Для Каждого ИмяКолонки Из ОписаниеТаблицыОтчета.ЧисловыеЗначения Цикл
		Если ОписаниеТаблицыОтчета.ВариантПериода = "НачалоКонец" Тогда
			ТаблицаОтчета.Колонки.Добавить(ИмяКолонкиНачалоПериода(ИмяКолонки), ТипСумма);
			ТаблицаОтчета.Колонки.Добавить(ИмяКолонкиКонецПериода(ИмяКолонки), ТипСумма);
		Иначе
			ТаблицаОтчета.Колонки.Добавить(ИмяКолонки, ТипСумма);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаОтчета;
	
КонецФункции

// Агрегирующая часть - для передачи данных в отчет

Функция НовыйЗаполнениеОтчета() // за все периоды по всем таблицам
	
	Процессор = Новый Структура;
	
	Процессор.Вставить("Периоды",              Новый Массив);    // из БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета
	Процессор.Вставить("Результат",            ЗаполнениеБухгалтерскойОтчетностиРазмещение.НовыйРезультатЗаполненияТаблицОтчета());
	Процессор.Вставить("ОписаниеТаблицОтчета", Новый Структура); // Ключ - имя таблицы по СоставТаблицОтчета; Значение - НовыйОписаниеТаблицыОтчета
	Процессор.Вставить("Данные",               Новый Массив);    // из Структура по числу Периоды; Ключ - имя таблицы; Значение - НовыйДанныеТаблицыОтчета
	
	Возврат Процессор;
	
КонецФункции

Функция НовыйЗаполнениеТаблицыОтчета() // за все периоды по одной таблице
	
	Процессор = Новый Структура;
	
	Процессор.Вставить("Описание"); // НовыйОписаниеТаблицыОтчета
	Процессор.Вставить("Классификаторы", Новый Структура); // Ключ - Строка, имя классификатора, Значение - Структура: Ключ,Значение - Строка, имя элемента классификатора
	
	Процессор.Вставить("Периоды"); // Массив из БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета
	
	// "Сырые" данные за каждый из периодов
	Процессор.Вставить("Данные", Новый Массив); // Массив по Периоды из НовыйДанныеТаблицыОтчета
	
	// Данные за все периоды - в виде, удобном для помещения результата заполнения в отчет
	Процессор.Вставить("Компоновка"); // ЗаполнениеБухгалтерскойОтчетностиРазмещение.НовыйСкомпонованнаяТаблицаОтчета
	
	// private
	Процессор.Вставить("МаксимальныйИдентификаторСтруктуры", 0); // для заполнения Компоновка
	
	Возврат Процессор;
	
КонецФункции

Функция НовыйДанныеТаблицыОтчета() // за один период по одной таблице
	
	ДанныеТаблицыОтчета = Новый Структура;
	
	ДанныеТаблицыОтчета.Вставить("ТаблицаОтчета"); // НовыйТаблицаОтчета
	ДанныеТаблицыОтчета.Вставить("ИндексРасшифровки", НовыйИндексРасшифровки());
	ДанныеТаблицыОтчета.Вставить("ЗаголовкиКолонок", Новый Соответствие); // Ключ - Строка, имя колонки; Значение - Строка, заголовок
	ДанныеТаблицыОтчета.Вставить("ЗаголовкиГрупп",   Новый Соответствие); // Ключ - индекс строки расшифровки, соответствующий заголовку группы; Значение - Истина
	
	Возврат ДанныеТаблицыОтчета;
	
КонецФункции

// Прикладная часть - для работы в модулях, получающих данные


Функция НовыйПолучениеДанныхТаблицОтчета() // за один период по всем таблицам
	
	Процессор = Новый Структура;
	
	Процессор.Вставить("КонтекстЗаполненияОтчета"); // БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета
	Процессор.Вставить("Расшифровка"); // см. ЗаполнениеБухгалтерскойОтчетностиНастраиваемая.НовыйДанныеРасшифровки
	Процессор.Вставить("ТаблицыОтчета", Новый Структура); // Ключ - имя таблицы по СоставТаблицОтчета, Значение - НовыйДанныеТаблицыОтчета
	
	Возврат Процессор;
	
КонецФункции

Функция НовыйПолучениеДанныхТаблицыОтчета() // за один период по одной таблице
	
	Процессор = Новый Структура;
	
	Процессор.Вставить("ИмяТаблицы", "");
	
	Процессор.Вставить("КонтекстЗаполненияОтчета"); // см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета
	Процессор.Вставить("Расшифровка");              // см. ЗаполнениеБухгалтерскойОтчетностиНастраиваемая.НовыйДанныеРасшифровки
	Процессор.Вставить("Данные");                   // см. НовыйДанныеТаблицыОтчета

	Процессор.Вставить("ВыводитьФилиалы", Ложь);
	Процессор.Вставить("НачальныйУровеньРасшифровкиДанныхПоОрганизации", 0);
	
	Возврат Процессор;
	
КонецФункции

#КонецОбласти

#Область ИндексРасшифровки

Функция НовыйИндексРасшифровки()
	
	Возврат Новый Соответствие; // Ключ - Строка, см. КлючЗаписиИндексаРасшифровки; Значение - Массив из Число, индексов строк в Расшифровка
	
КонецФункции

Функция КлючЗаписиИндексаРасшифровки(Знач СтрокаТаблицыОтчета, КолонкаТаблицыОтчета)
	
	Если ТипЗнч(СтрокаТаблицыОтчета) = Тип("СтрокаТаблицыЗначений") Тогда
		СтрокаТаблицыОтчета = СтрокаТаблицыОтчета.Владелец().Индекс(СтрокаТаблицыОтчета);
	КонецЕсли;
	
	Возврат СтрШаблон("[%1].%2", XMLСтрока(СтрокаТаблицыОтчета), КолонкаТаблицыОтчета);
	
КонецФункции

Процедура ДополнитьИндексРасшифровки(ИндексРасшифровки, СтрокаТаблицыОтчета, КолонкаТаблицыОтчета, ЗаписьРасшифровки)
	
	КлючЗаписиИндексаРасшифровки = КлючЗаписиИндексаРасшифровки(СтрокаТаблицыОтчета, КолонкаТаблицыОтчета);
	ОбщегоНазначенияБПКлиентСервер.ДополнитьРазделенныйМассив(
		ИндексРасшифровки,
		КлючЗаписиИндексаРасшифровки,
		ЗаписьРасшифровки.Владелец().Индекс(ЗаписьРасшифровки));
		
КонецПроцедуры

Функция СодержимоеИндексаРасшифровки(ИндексРасшифровки, СтрокаТаблицыОтчета, КолонкаТаблицыОтчета)
	
	КлючЗаписиИндексаРасшифровки = КлючЗаписиИндексаРасшифровки(СтрокаТаблицыОтчета, КолонкаТаблицыОтчета);
	ЗаписиРасшифровки = ИндексРасшифровки[КлючЗаписиИндексаРасшифровки];
	
	Если ЗаписиРасшифровки = Неопределено Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	Возврат ЗаписиРасшифровки;
	
КонецФункции

#КонецОбласти

Процедура ЗаполнитьОтчет(ЗаполнениеОтчета)
	
	ЗаполнениеТаблицОтчета = Новый Структура;
	Классификаторы         = Новый Структура;
	Для Каждого ОписаниеТаблицыОтчета Из ЗаполнениеОтчета.ОписаниеТаблицОтчета Цикл
		
		ИмяТаблицыОтчета = ОписаниеТаблицыОтчета.Ключ;
		
		ЗаполнениеТаблицыОтчета = НовыйЗаполнениеТаблицыОтчета();
		
		ЗаполнениеТаблицыОтчета.Описание       = ОписаниеТаблицыОтчета.Значение;
		ЗаполнениеТаблицыОтчета.Классификаторы = КлассификаторыТаблицыОтчета(ЗаполнениеТаблицыОтчета.Описание.Классификаторы);
		ЗаполнениеТаблицыОтчета.Компоновка     = ЗаполнениеОтчета.Результат.СкомпонованныеТаблицыОтчета[ОписаниеТаблицыОтчета.Ключ];
		ЗаполнениеТаблицыОтчета.Периоды        = ЗаполнениеОтчета.Периоды;
		Для ИндексПериода = 0 По ЗаполнениеОтчета.Периоды.ВГраница() Цикл
			ЗаполнениеТаблицыОтчета.Данные.Добавить(ЗаполнениеОтчета.Данные[ИндексПериода][ОписаниеТаблицыОтчета.Ключ]);
		КонецЦикла;
		
		ЗаполнениеТаблицОтчета.Вставить(ИмяТаблицыОтчета, ЗаполнениеТаблицыОтчета);
		
		Классификаторы.Вставить(ИмяТаблицыОтчета, ЗаполнениеТаблицыОтчета.Классификаторы);
		
	КонецЦикла;
	
	Для ИндексПериода = 0 По ЗаполнениеОтчета.Периоды.ВГраница() Цикл
		
		ПолучениеДанныхТаблицОтчета = НовыйПолучениеДанныхТаблицОтчета();
		
		ПолучениеДанныхТаблицОтчета.КонтекстЗаполненияОтчета = ЗаполнениеОтчета.Периоды[ИндексПериода];
		ПолучениеДанныхТаблицОтчета.Расшифровка              = ЗаполнениеОтчета.Результат.Расшифровка;
		ПолучениеДанныхТаблицОтчета.ТаблицыОтчета            = ЗаполнениеОтчета.Данные[ИндексПериода];
		
		ПолучитьДанныеТаблицОтчета(ПолучениеДанныхТаблицОтчета, Классификаторы);
		
		ДополнитьДанныеЗаполненияТаблицОтчета(ЗаполнениеТаблицОтчета, ПолучениеДанныхТаблицОтчета, ИндексПериода);
		
	КонецЦикла;
	
	Для Каждого ОписаниеТаблицыОтчета Из ЗаполнениеТаблицОтчета Цикл
		РассчитатьИтоги(ОписаниеТаблицыОтчета.Значение, ЗаполнениеОтчета.Результат.Расшифровка);
	КонецЦикла;
	
КонецПроцедуры

Функция НачатьЗаполнениеОтчета(Периоды, Организации, ТаблицыОтчетаКЗаполнению)
	
	ЗаполнениеОтчета = НовыйЗаполнениеОтчета();
	
	ЗаполнениеОтчета.Периоды = ПериодыЗаполненияОтчета(Периоды, Организации);
	
	ВГраницаПериодов = ЗаполнениеОтчета.Периоды.ВГраница();
	
	Для ИндексПериода = 0 По ВГраницаПериодов Цикл
		ДанныеЗаПериод = Новый Структура;
		ЗаполнениеОтчета.Данные.Добавить(ДанныеЗаПериод);
	КонецЦикла;
	
	Для Каждого ИмяТаблицыОтчета Из ТаблицыОтчетаКЗаполнению Цикл
		
		ЗаполнениеОтчета.Результат.СкомпонованныеТаблицыОтчета.Вставить(
			ИмяТаблицыОтчета,
			ЗаполнениеБухгалтерскойОтчетностиРазмещение.НовыйСкомпонованнаяТаблицаОтчета());
		
		ОписаниеТаблицыОтчета = НовыйОписаниеТаблицыОтчета();
		ЗаполнитьОписаниеТаблицыОтчета(ОписаниеТаблицыОтчета, ИмяТаблицыОтчета);
		ЗаполнениеОтчета.ОписаниеТаблицОтчета.Вставить(ИмяТаблицыОтчета, ОписаниеТаблицыОтчета);
		
		ШаблонТаблицыОтчета = НовыйТаблицаОтчета(ОписаниеТаблицыОтчета);
		Для ИндексПериода = 0 По ВГраницаПериодов Цикл
			ДанныеТаблицыОтчета = НовыйДанныеТаблицыОтчета();
			ДанныеТаблицыОтчета.ТаблицаОтчета = ШаблонТаблицыОтчета.Скопировать();
			ЗаполнениеОтчета.Данные[ИндексПериода].Вставить(ИмяТаблицыОтчета, ДанныеТаблицыОтчета);
		КонецЦикла;
		
	КонецЦикла;
	
	ЗаполнениеОтчета.Результат.Расшифровка = ЗаполнениеБухгалтерскойОтчетностиНастраиваемая.НовыйДанныеРасшифровки();
	
	Возврат ЗаполнениеОтчета;
	
КонецФункции

Функция ТаблицыОтчетаКЗаполнению(КонтекстОтчета, ОтборРазделов)
	
	ТаблицыОтчетаСтруктура = Новый Структура;
	Для Каждого ИмяТаблицыОтчета Из ЗаполнениеБухгалтерскойОтчетностиРазмещение.СоставТаблицОтчета() Цикл
		
		ТаблицыОтчетаСтруктура.Вставить(ИмяТаблицыОтчета, Ложь);
		
	КонецЦикла;
	
	ОпределитьСоставЗаполняемыхТаблицОтчета(ТаблицыОтчетаСтруктура, КонтекстОтчета);
	
	ТаблицыОтчетаМассив = Новый Массив;
	Для Каждого ОписаниеТаблицыОтчета Из ТаблицыОтчетаСтруктура Цикл
		
		Если Не ОписаниеТаблицыОтчета.Значение Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяРаздела = ЗаполнениеБухгалтерскойОтчетностиРазмещение.ИмяРазделаТаблицыОтчета(ОписаниеТаблицыОтчета.Ключ);
		
		Если ПустаяСтрока(ИмяРаздела) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ОтборРазделов.Свойство(ИмяРаздела) Или Не ОтборРазделов[ИмяРаздела] Тогда
			Продолжить;
		КонецЕсли;
		
		ТаблицыОтчетаМассив.Добавить(ОписаниеТаблицыОтчета.Ключ);
		
	КонецЦикла;
	
	Возврат ТаблицыОтчетаМассив;
	
КонецФункции

Функция ПериодыЗаполненияОтчета(Периоды, Организации)
	
	ПериодыОтчета = Новый Массив;
	Для Каждого Период Из Периоды Цикл
		
		КонтекстЗаполненияОтчета = БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета();
		КонтекстЗаполненияОтчета.Организации = Организации;
		ЗаполнитьЗначенияСвойств(КонтекстЗаполненияОтчета.Период, Период);
		
		ПериодыОтчета.Добавить(КонтекстЗаполненияОтчета);
		
	КонецЦикла;
	
	Возврат ПериодыОтчета;

КонецФункции

#Область ВариантПериодаНачалоКонец

// Прикладные данные содержат по два значения в двух периодах, как это принято в отчетах за период (например, ОФР).
// При компоновке эти данные сокращаются до одного значения в трех периодах, как это принято в отчетах на дату (балансе).
// Например
// - прикладные данные содержат
// -- Период 0: с 01.01.2025 по 31.03.2025
// -- Период 1: с 01.01.2024 по 31.03.2024
// - в отчете они представляются
// -- Период 0: 31.03.2025
// -- Период 1: 01.01.2025 (31.12.2025)
// -- Период 2: 01.01.2024 (31.12.2024)

Функция ИмяКолонкиНачалоПериода(ИмяКолонкиИзОписания)
	Возврат СтрШаблон("Начало%1", ИмяКолонкиИзОписания);
КонецФункции

Функция ИмяКолонкиКонецПериода(ИмяКолонкиИзОписания)
	Возврат СтрШаблон("Конец%1", ИмяКолонкиИзОписания);
КонецФункции

Функция ИндексПериодаКонец(ПрикладнойИндексПериода)
	Если ПрикладнойИндексПериода = 0 Тогда
		// Только одна колонка может содержать данные на конец прикладного периода
		Возврат 0;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

Функция ИндексПериодаНачало(ПрикладнойИндексПериода)
	Возврат ПрикладнойИндексПериода + 1; // Колонка 0 занята, см. ИндексПериодаКонец
КонецФункции

#КонецОбласти

Функция КлассификаторыТаблицыОтчета(ОписаниеКлассификаторов)
	
	Классификаторы = Новый Структура;
	
	Для Каждого ОписаниеКлассификатора Из ОписаниеКлассификаторов Цикл
		СодержимоеКлассификатора = Новый Структура;
		
		// Адаптер, планируется к удалению
		ЭлементыКлассификатора = Новый Массив;
		Если ТипЗнч(ОписаниеКлассификатора.Значение) = Тип("Строка") Тогда
			ЭлементыКлассификатора = СтрРазделить(ОписаниеКлассификатора.Значение, ",");
		Иначе
		///
			ЭлементыКлассификатора = ОписаниеКлассификатора.Значение.ВыгрузитьЗначения();
		КонецЕсли;
		
		Для Каждого ЗначениеКлассификатора Из ЭлементыКлассификатора Цикл
			СодержимоеКлассификатора.Вставить(ЗначениеКлассификатора, ЗначениеКлассификатора);
		КонецЦикла;
		Классификаторы.Вставить(ОписаниеКлассификатора.Ключ, СодержимоеКлассификатора);
	КонецЦикла;
	
	Возврат Классификаторы;
	
КонецФункции

Процедура ДополнитьДанныеЗаполненияТаблицОтчета(ЗаполнениеТаблицОтчета, ПолучениеДанныхТаблицОтчета, ИндексПериода)
	
	Для Каждого ЗаполнениеТаблицыОтчета Из ЗаполнениеТаблицОтчета Цикл
		
		ДанныеТаблицыОтчета = ПолучениеДанныхТаблицОтчета.ТаблицыОтчета[ЗаполнениеТаблицыОтчета.Ключ];
		
		ПоказателиТаблицыОтчета = ДополнитьСтруктуруОтчета(ЗаполнениеТаблицыОтчета.Значение, ДанныеТаблицыОтчета.ТаблицаОтчета);
		
		ДополнитьЗначенияЗаполненияОтчета(
			ЗаполнениеТаблицыОтчета.Значение,
			ПоказателиТаблицыОтчета,
			ДанныеТаблицыОтчета,
			ИндексПериода);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДополнитьСтруктуруОтчета(ЗаполнениеТаблицыОтчета, ТаблицаОтчета)
	
	ПоказателиТаблицыОтчета = Новый Массив; // соответствует по индексам ТаблицаОтчета, хранит идентификаторы в Структура
	
	Для Каждого Запись Из ТаблицаОтчета Цикл
		
		ЭлементСтруктуры = ЗаполнениеТаблицыОтчета.Компоновка.Структура.Строки[0]; // итоговый
		
		Для Каждого ИмяКолонки Из ЗаполнениеТаблицыОтчета.Описание.Структура Цикл
			
			ИдентификаторПоказателя = Запись[ИмяКолонки];
			
			ВложенныйЭлементСтруктуры = ЭлементСтруктуры.Строки.Найти(ИдентификаторПоказателя, "Показатель");
			Если ВложенныйЭлементСтруктуры = Неопределено Тогда
				ВложенныйЭлементСтруктуры = ЭлементСтруктуры.Строки.Добавить();
				ЗаполнениеТаблицыОтчета.МаксимальныйИдентификаторСтруктуры = ЗаполнениеТаблицыОтчета.МаксимальныйИдентификаторСтруктуры + 1;
				ВложенныйЭлементСтруктуры.Идентификатор = ЗаполнениеТаблицыОтчета.МаксимальныйИдентификаторСтруктуры;
				ВложенныйЭлементСтруктуры.Показатель = ИдентификаторПоказателя;
				Если Не ЗаполнениеТаблицыОтчета.Описание.Классификаторы.Свойство(ИмяКолонки) Тогда
					ВложенныйЭлементСтруктуры.Наименование = Строка(ИдентификаторПоказателя);
				Иначе
					Классификатор = ЗаполнениеТаблицыОтчета.Описание.Классификаторы[ИмяКолонки];
					// Адаптер, планируется к удалению
					Если ТипЗнч(Классификатор) = Тип("Строка") Тогда 
						ВложенныйЭлементСтруктуры.Наименование = Строка(ИдентификаторПоказателя);
					Иначе
					///
						ВложенныйЭлементСтруктуры.Наименование = Классификатор.НайтиПоЗначению(ИдентификаторПоказателя).Представление;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
			ЭлементСтруктуры = ВложенныйЭлементСтруктуры;
			
		КонецЦикла;
		
		ПоказателиТаблицыОтчета.Добавить(ЭлементСтруктуры.Идентификатор);
		
	КонецЦикла;
	
	Возврат ПоказателиТаблицыОтчета;
	
КонецФункции

Процедура ДополнитьЗначенияЗаполненияОтчета(ЗаполнениеТаблицыОтчета, ПоказателиТаблицыОтчета, ДанныеТаблицыОтчета, ИндексПериода)
	
	Для ИндексЗаписи = 0 По ДанныеТаблицыОтчета.ТаблицаОтчета.Количество() - 1 Цикл
		
		Запись = ДанныеТаблицыОтчета.ТаблицаОтчета[ИндексЗаписи];
		Показатель = ПоказателиТаблицыОтчета[ИндексЗаписи];
		
		Для Каждого ИмяКолонки Из ЗаполнениеТаблицыОтчета.Описание.ЧисловыеЗначения Цикл
			
			Если ЗаполнениеТаблицыОтчета.Описание.ВариантПериода = "НачалоКонец" Тогда
				
				ИндексПериодаНачало = ИндексПериодаНачало(ИндексПериода);
				ИмяКолонкиНачало    = ИмяКолонкиНачалоПериода(ИмяКолонки);
				
				ДобавитьЗначениеЗаполненияОтчета(
					ЗаполнениеТаблицыОтчета,
					ИндексПериодаНачало,
					Запись,
					Показатель,
					ИмяКолонкиНачало,
					ИмяКолонки,
					ИндексЗаписи,
					ДанныеТаблицыОтчета);
				
				ИндексПериодаКонец  = ИндексПериодаКонец(ИндексПериода);
				ИмяКолонкиКонец     = ИмяКолонкиКонецПериода(ИмяКолонки);
				
				ДобавитьЗначениеЗаполненияОтчета(
					ЗаполнениеТаблицыОтчета,
					ИндексПериодаКонец,
					Запись,
					Показатель,
					ИмяКолонкиКонец,
					ИмяКолонки,
					ИндексЗаписи,
					ДанныеТаблицыОтчета);
				
			Иначе
			
				ДобавитьЗначениеЗаполненияОтчета(
					ЗаполнениеТаблицыОтчета,
					ИндексПериода,
					Запись,
					Показатель,
					ИмяКолонки,
					ИмяКолонки,
					ИндексЗаписи,
					ДанныеТаблицыОтчета);
					
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьЗначениеЗаполненияОтчета(ЗаполнениеТаблицыОтчета, ИндексПериода, Запись, Показатель, ИмяКолонкиТаблицыОтчета, ИмяКолонкиКомпоновки, ИндексЗаписи, ДанныеТаблицыОтчета)
	
	Если ИндексПериода = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеЗаполнения = ЗаполнениеТаблицыОтчета.Компоновка.Значения.Добавить();
	ЗначениеЗаполнения.Показатель = Показатель;
	ЗначениеЗаполнения.Период     = ИндексПериода;
	ЗначениеЗаполнения.ИмяКолонки = ИмяКолонкиКомпоновки;
	ЗначениеЗаполнения.Значение   = Запись[ИмяКолонкиТаблицыОтчета];
	
	ЗначениеЗаполнения.Расшифровка = СодержимоеИндексаРасшифровки(
		ДанныеТаблицыОтчета.ИндексРасшифровки,
		ИндексЗаписи,
		ИмяКолонкиТаблицыОтчета);
	
КонецПроцедуры

Процедура ВывестиЭлементЗаголовкаРасшифровки(ВыводТаблицы, ТипЗаписи, ПараметрыТекстов, ИмяКолонки, ШаблоныЗаголовка, ЭтоГлавныйЗаголовок = Ложь)
	
	СтандартныеПараметрыТекстаЗаголовкаГруппы = Новый Структура;
	
	Если ЭтоГлавныйЗаголовок Тогда
		ЗаголовокКолонки = ВыводТаблицы.Данные.ЗаголовкиКолонок[ИмяКолонки];
		Если ЗначениеЗаполнено(ЗаголовокКолонки) Тогда
			СтандартныеПараметрыТекстаЗаголовкаГруппы.Вставить("ПредставлениеКолонки", ЗаголовокКолонки);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ШаблоныЗаголовка.Свойство(ТипЗаписи) Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонТекста = ШаблоныЗаголовка[ТипЗаписи];
	Если ПустаяСтрока(ШаблонТекста) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонТекста, ПараметрыТекстов);
	Если ЭтоГлавныйЗаголовок Тогда
		Текст = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Текст, СтандартныеПараметрыТекстаЗаголовкаГруппы);
	КонецЕсли;
	
	ЗаписьРасшифровки = ДобавитьЗаписьРасшифровкиСтроки(ВыводТаблицы, ИмяКолонки, ТипЗаписи, Текст);
	
	Если ЭтоГлавныйЗаголовок Тогда
		ВыводТаблицы.Данные.ЗаголовкиГрупп.Вставить(ЗаписьРасшифровки.Владелец().Индекс(ЗаписьРасшифровки), Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура РассчитатьИтоги(ЗаполнениеТаблицыОтчета, Расшифровка)
	
	Итоги = Новый Соответствие;
	ОпределитьПоказателиИтогов(Итоги, ЗаполнениеТаблицыОтчета.Компоновка.Структура.Строки, Новый Массив);
	
	ЗначенияИтогов = ЗаполнениеТаблицыОтчета.Компоновка.Значения.СкопироватьКолонки();
	СоставИтогов = Новый Соответствие;
	
	Для Каждого ДетальнаяЗапись Из ЗаполнениеТаблицыОтчета.Компоновка.Значения Цикл
		ИтогиПоказателя = Итоги[ДетальнаяЗапись.Показатель];
		Для Каждого ИдентификаторИтога Из ИтогиПоказателя Цикл
			
			ИтоговаяЗапись = ЗначенияИтогов.Добавить();
			ЗаполнитьЗначенияСвойств(ИтоговаяЗапись, ДетальнаяЗапись);
			ИтоговаяЗапись.Показатель = ИдентификаторИтога;
			
			// Данные расшифровки детальных показателей
			Если ЗначениеЗаполнено(ДетальнаяЗапись.Расшифровка) Тогда
				КлючРасшифровкиИтогов = КлючРасшифровкиИтогов(ИтоговаяЗапись);
				СоставИтога = СоставИтогов[КлючРасшифровкиИтогов];
				Если СоставИтога = Неопределено Тогда
					СоставИтога = Новый Массив();
					СоставИтогов.Вставить(КлючРасшифровкиИтогов, СоставИтога);
				КонецЕсли;
				СоставИтога.Добавить(ДетальнаяЗапись);
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	ЗначенияИтогов.Свернуть("Показатель, Период, ИмяКолонки", "Значение");
	
	Для Каждого ЗначениеИтога Из ЗначенияИтогов Цикл
		
		ИтоговаяЗапись = ЗаполнениеТаблицыОтчета.Компоновка.Значения.Добавить();
		ЗаполнитьЗначенияСвойств(ИтоговаяЗапись, ЗначениеИтога);
		
		КлючРасшифровкиИтогов = КлючРасшифровкиИтогов(ИтоговаяЗапись);
		ЗаполнитьРасшифровкуИтога(Расшифровка, ИтоговаяЗапись, СоставИтогов[КлючРасшифровкиИтогов], ЗаполнениеТаблицыОтчета);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПредставлениеСтрокиОтчета(ЗначениеОтчета, ЗаполнениеТаблицыОтчета, СтрокаОтчетаПределПолейПредставления = Неопределено)
	
	СтрокаОтчетаЗначение = ЗаполнениеТаблицыОтчета.Компоновка.Структура.Строки.Найти(
		ЗначениеОтчета.Показатель,
		"Идентификатор",
		Истина);
		
	Если СтрокаОтчетаЗначение = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	УровеньЗначения = СтрокаОтчетаЗначение.Уровень();
	
	Если УровеньЗначения = 0 Тогда
		Если ПустаяСтрока(ЗаполнениеТаблицыОтчета.Описание.Представление) Тогда
			Возврат НСтр("ru = 'Итого по колонке таблицы';
						|en = 'Table column total'");
		Иначе
			Возврат ЗаполнениеТаблицыОтчета.Описание.Представление;
		КонецЕсли;
	КонецЕсли;
	
	ПоляОписанияЗначения = Новый Массив;
	
	ПределПолейПредставления = 0;
	Если СтрокаОтчетаПределПолейПредставления <> Неопределено Тогда
		ПределПолейПредставления = СтрокаОтчетаПределПолейПредставления.Уровень();
	КонецЕсли;
	
	Пока УровеньЗначения > ПределПолейПредставления Цикл
		
		ПоляОписанияЗначения.Вставить(0, СтрокаОтчетаЗначение.Наименование); // Заполняем двигаясь "снизу-вверх", а пользователю нужно вывести "сверху-вниз"
		СтрокаОтчетаЗначение = СтрокаОтчетаЗначение.Родитель;
		УровеньЗначения = УровеньЗначения - 1;
		
	КонецЦикла;
	
	Возврат СтрСоединить(ПоляОписанияЗначения, " / ");
	
КонецФункции

Функция ТекстИтого()
	
	Возврат НСтр("ru = 'Итого';
				|en = 'Total'");
	
КонецФункции

Процедура ОпределитьПоказателиИтогов(Итоги, ЭлементыСтруктуры, ИтогиПоказателяПредыдущие)
	
	Для Каждого ЭлементСтруктуры Из ЭлементыСтруктуры Цикл
		Если Не ЗначениеЗаполнено(ЭлементСтруктуры.Строки) Тогда
			Итоги.Вставить(ЭлементСтруктуры.Идентификатор, ИтогиПоказателяПредыдущие);
		Иначе
			ИтогиПоказателя = ОбщегоНазначения.СкопироватьРекурсивно(ИтогиПоказателяПредыдущие);
			ИтогиПоказателя.Добавить(ЭлементСтруктуры.Идентификатор);
			ОпределитьПоказателиИтогов(Итоги, ЭлементСтруктуры.Строки, ИтогиПоказателя);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция КлючРасшифровкиИтогов(ИтоговаяЗапись)
	
	Возврат СтрШаблон(
		"[%1].%2[%3]",
		XMLСтрока(ИтоговаяЗапись.Показатель),
		ИтоговаяЗапись.ИмяКолонки,
		XMLСтрока(ИтоговаяЗапись.Период));
	
КонецФункции

Процедура ЗаполнитьРасшифровкуИтога(Расшифровка, ИтоговаяЗапись, СоставИтога, ЗаполнениеТаблицыОтчета)
	
	Если СоставИтога = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПрикладнойИндексПериода = ИтоговаяЗапись.Период;
	ИмяКолонкиТаблицыОтчета = ИтоговаяЗапись.ИмяКолонки;
	Если ЗаполнениеТаблицыОтчета.Описание.ВариантПериода = "НачалоКонец" Тогда
		Если ПрикладнойИндексПериода = 0 Тогда
			ИмяКолонкиТаблицыОтчета = ИмяКолонкиКонецПериода(ИмяКолонкиТаблицыОтчета);
		Иначе
			ИмяКолонкиТаблицыОтчета = ИмяКолонкиНачалоПериода(ИмяКолонкиТаблицыОтчета);
			ПрикладнойИндексПериода = ПрикладнойИндексПериода - 1;
		КонецЕсли;
	КонецЕсли;
	
	ДанныеТаблицыОтчета = ЗаполнениеТаблицыОтчета.Данные[ПрикладнойИндексПериода];
	
	// 1. Первая часть расшифровки: суммирует подытоженные показатели
	
	// - заголовок
	ЗаголовокИтога = "";
	
	ЗаголовокКолонки = ДанныеТаблицыОтчета.ЗаголовкиКолонок[ИмяКолонкиТаблицыОтчета];
	ЗаголовокСтроки = ПредставлениеСтрокиОтчета(ИтоговаяЗапись, ЗаполнениеТаблицыОтчета);
	
	Если ПустаяСтрока(ЗаголовокКолонки) Тогда
		ЗаголовокИтога = ЗаголовокСтроки;
	ИначеЕсли ПустаяСтрока(ЗаголовокСтроки) Тогда
		ЗаголовокИтога = ОбщегоНазначенияБПКлиентСервер.КапитализироватьСтроку(ЗаголовокКолонки);
	Иначе
		ЗаголовокИтога = СтрШаблон("%1: %2", ЗаголовокСтроки, ЗаголовокКолонки);
	КонецЕсли;
	
	ПредставленияПодытоженныхЗначений = Новый Массив;
	
	ДобавитьЗаписьРасшифровкиЗначения(Расшифровка, ИтоговаяЗапись.Расшифровка, "ЗаголовокГруппы", ЗаголовокИтога);
	
	// - перечень подытоженных строк
	
	СтрокаОтчетаИтог = ЗаполнениеТаблицыОтчета.Компоновка.Структура.Строки.Найти(
		ИтоговаяЗапись.Показатель,
		"Идентификатор",
		Истина);
	
	Для Каждого ПодытоженноеЗначение Из СоставИтога Цикл
		
		ПредставлениеСтроки = ПредставлениеСтрокиОтчета(ПодытоженноеЗначение, ЗаполнениеТаблицыОтчета, СтрокаОтчетаИтог);
		
		ДобавитьЗаписьРасшифровкиЗначения(
			Расшифровка,
			ИтоговаяЗапись.Расшифровка,
			"Слагаемое",
			ПредставлениеСтроки,
			ПодытоженноеЗначение.Значение);
		
		ПредставленияПодытоженныхЗначений.Добавить(ПредставлениеСтроки);
		
	КонецЦикла;
	
	// - итог
	ДобавитьЗаписьРасшифровкиЗначения(
		Расшифровка,
		ИтоговаяЗапись.Расшифровка,
		"Итог",
		ТекстИтого(),
		ИтоговаяЗапись.Значение);
		
	// 2. Вторая часть расшифровки: приводит детальный расчет каждого подытоженного показателя
	
	ОпределенОбщийЗаголовокКолонки = ЗначениеЗаполнено(ЗаголовокКолонки);
	
	Для ИндексПодытоженногоЗначения = 0 По СоставИтога.ВГраница() Цикл
		
		ПодытоженноеЗначение = СоставИтога[ИндексПодытоженногоЗначения];
		
		Для Каждого ИдентификаторРасшифровки Из ПодытоженноеЗначение.Расшифровка Цикл
			
			Если Не ОпределенОбщийЗаголовокКолонки Или ДанныеТаблицыОтчета.ЗаголовкиГрупп[ИдентификаторРасшифровки] = Неопределено Тогда
				ИтоговаяЗапись.Расшифровка.Добавить(ИдентификаторРасшифровки);
			Иначе
				ДобавитьЗаписьРасшифровкиЗначения(
					Расшифровка,
					ИтоговаяЗапись.Расшифровка,
					"Заголовок",
					ПредставленияПодытоженныхЗначений[ИндексПодытоженногоЗначения]);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДобавитьЗаписьРасшифровкиЗначения(Расшифровка, РасшифровкаЗначения, ТипЗаписи, Описание, Сумма = 0, ПредметЗаписи = Неопределено, Отчет = Неопределено, Уровень = 0)
	
	ЗаписьРасшифровки = Расшифровка.Добавить();
	
	ЗаписьРасшифровки.ТипЗаписи     = ТипЗаписи;
	ЗаписьРасшифровки.Описание      = Описание;
	ЗаписьРасшифровки.Сумма         = Сумма;
	ЗаписьРасшифровки.ПредметЗаписи = ПредметЗаписи;
	ЗаписьРасшифровки.Отчет         = Отчет;
	ЗаписьРасшифровки.Уровень       = Уровень;
	
	РасшифровкаЗначения.Добавить(Расшифровка.Индекс(ЗаписьРасшифровки));
	
	Возврат ЗаписьРасшифровки;
	
КонецФункции

Процедура ПредставитьМассивом(Значение)
	
	Если ТипЗнч(Значение) <> Тип("Массив") Тогда
		Значение = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти