// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
//  ФайлОригинал - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
//  НастройкиПредставления - см. НовыеНастройкиПредставления
// 
// Возвращаемое значение:
//  Булево - данные кэша по по файлу требуют обновления
Функция ТребуетсяОбновитьКэшВизуализации(ФайлОригинал, НастройкиПредставления = Неопределено) Экспорт
	
	ДанныеФайлаОригинала = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ФайлОригинал,
		"ПометкаУдаления, ВладелецФайла, Расширение, Размер");
	
	Если ДанныеФайлаОригинала.ПометкаУдаления Тогда
		Возврат Ложь;
	КонецЕсли;
	Если ТипЗнч(ДанныеФайлаОригинала.ВладелецФайла) <> Тип("ДокументСсылка.СообщениеЭДО") Тогда
		Возврат Ложь;
	КонецЕсли;
	Если Не ВизуализацияЭДОСлужебный.РегистрироватьДокументЭДОДляКэшированияВФоне(ДанныеФайлаОригинала.Расширение,
		ДанныеФайлаОригинала.Размер) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СообщениеЭДО = ДанныеФайлаОригинала.ВладелецФайла;
	Если Не ЗначениеЗаполнено(СообщениеЭДО) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЭлектронныйДокумент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СообщениеЭДО, "ЭлектронныйДокумент");
	Если НастройкиПредставления = Неопределено Тогда
		НастройкиПредставления = ВизуализацияЭДО.НовыеНастройкиПредставления();
	КонецЕсли;
	
	КэшВизуализации = ВизуализацияЭДО.ПолучитьВизуализациюИзКэша(ЭлектронныйДокумент, НастройкиПредставления);
	Возврат КэшВизуализации.ТребуетсяОбновить;
КонецФункции

#КонецОбласти

