
#Область ПрограммныйИнтерфейс

// См. ОбщегоНазначенияКлиентПереопределяемый.ПослеНачалаРаботыСистемы
Процедура ПослеНачалаРаботы() Экспорт
	
	ПодключитьОбработкуСсылкиПоказатьРасхождения();
	
КонецПроцедуры

// Подключает обработчики отдельных событий СистемыВзаимодействия.
//
Процедура ПодключитьОбработкуСсылкиПоказатьРасхождения() Экспорт
	
	Обработчик = Новый ОписаниеОповещения("ОбработатьПереходПользователяПоСсылке", ЭтотОбъект);
	СистемаВзаимодействия.ПодключитьОбработчикДействияСообщения(Обработчик);
	
КонецПроцедуры

Процедура ОбработатьПереходПользователяПоСсылке(Сообщение, ЗначениеСсылки, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеСсылки = "ПоказатьРасхождения" Тогда
		
		СформироватьОтчетОСверкеКонтекстно(Сообщение.Данные);
		
	ИначеЕсли ЗначениеСсылки = "ПоказатьНастройки" Тогда
		
		КлючНастройкиОповещенийТекущегоПользователя = СервисСверкиРасчетовВызовСервера.КлючНастройкиОповещенийТекущегоПользователя();
		Если КлючНастройкиОповещенийТекущегоПользователя <> Неопределено Тогда
			ОткрытьФорму("Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей.ФормаОбъекта", 
				Новый Структура("Ключ, ПерсональнаяНастройка", КлючНастройкиОповещенийТекущегоПользователя, Истина));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьПереходПользователяПоСсылкеСОтбормПоПериоду(НавигационнаяСсылкаФорматированнойСтроки) Экспорт
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "e1cib/command/ОбщаяКоманда.СверкаДокументовСКонтрагентами" Тогда
		
		СформироватьОтчетОСверкеКонтекстно();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьНавигационнуюСсылкуПодробнееОСервисе() Экспорт
	
	ИнформационнаяСсылка = "https://portal.1c.ru/applications/1C-Sverka";
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ИнформационнаяСсылка);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьОтчетОСверкеКонтекстно(ИдентификаторыРасхождений = Неопределено)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимРасшифровки", Истина);
	Если ИдентификаторыРасхождений <> Неопределено Тогда
		СписокИдентификаторов = Новый СписокЗначений;
		СписокИдентификаторов.ЗагрузитьЗначения(ИдентификаторыРасхождений);
		ПараметрыФормы.Вставить("ИдентификаторыРасхождений", СписокИдентификаторов);
	КонецЕсли;
	
	ОткрытьФорму("Отчет.СверкаРасчетовСКонтрагентами.Форма.ФормаОтчета", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти