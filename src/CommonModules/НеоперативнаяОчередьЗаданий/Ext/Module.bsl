#Область ПрограммныйИнтерфейс

#Область РегистрацияЗаданий

// Создание заданий неоперативной очереди при записи набора записей регистра.
// 
// Параметры:
//  Источник - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
//
Процедура СоздатьЗаданияНеоперативнойОчередиПриЗаписиНабораЗаписей(Источник, МенеджерВременныхТаблиц = Неопределено,
	ДанныеКРегистрацииЗаданий = Неопределено) Экспорт
	
	Если Источник.ДополнительныеСвойства.Свойство("НеРегистрироватьЗаданияНеоперативнойОчереди") Тогда
		Возврат;
	КонецЕсли;

	МетаданныеРегистра = Источник.Метаданные();
	МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеРегистра.ПолноеИмя());

	ВидыЗаданийКРегистрацииВОчереди = ВидыЗаданийКРегистрацииВОчереди(МетаданныеРегистра);
	Для Каждого ТекущиеДанные Из ВидыЗаданийКРегистрацииВОчереди Цикл

		МетаданныеРегистраЗаданий = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ТекущиеДанные.РегистрОчереди);
		МенеджерРегистраЗаданий = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеРегистраЗаданий.ПолноеИмя());

		МенеджерРегистраЗаданий.СоздатьЗадания(МенеджерРегистра, ТекущиеДанные.ВидыЗаданий, Источник,
			МенеджерВременныхТаблиц, ДанныеКРегистрацииЗаданий);

	КонецЦикла;

КонецПроцедуры

// Получает список видов заданий для изменяемого регистра
// 
// Параметры:
//  МетаданныеРегистра - ОбъектМетаданныхРегистрНакопления, ОбъектМетаданныхРегистрСведений - Метаданные регистра
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Виды заданий к регистрации в очереди:
// * РегистрОчереди - СправочникСсылка.ИдентификаторыОбъектовМетаданных - 
// * ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий
Функция ВидыЗаданийКРегистрацииВОчереди(МетаданныеРегистра) Экспорт

	ТаблицаЗаданий = Новый ТаблицаЗначений;
	ТаблицаЗаданий.Колонки.Добавить("РегистрОчереди",
		Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных"));
	ТаблицаЗаданий.Колонки.Добавить("ВидыЗаданий", Новый ОписаниеТипов("Массив"));

	ИдентификаторРегистра = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(МетаданныеРегистра.ПолноеИмя());

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	РегистрыЧтение.Ссылка КАК ВидЗадания,
	|	РегистрыЧтение.Ссылка.Регистр КАК РегистрОчереди
	|ИЗ
	|	Справочник.ВидыНеоперативныхЗаданий.ЧитаемыеРегистры КАК РегистрыЧтение
	|ГДЕ
	|	РегистрыЧтение.Регистр = &ИдентификаторРегистра
	|	И ВЫБОР
	|		КОГДА
	|			РегистрыЧтение.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу)
	|			ТОГДА &ФормироватьУправленческийБаланс
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|ИТОГИ
	|ПО
	|	РегистрОчереди";

	Запрос.УстановитьПараметр("ИдентификаторРегистра", ИдентификаторРегистра);
	Запрос.УстановитьПараметр("ФормироватьУправленческийБаланс", ПолучитьФункциональнуюОпцию(
		"ФормироватьУправленческийБаланс"));
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаОчередь = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	УстановитьПривилегированныйРежим(Ложь);

	Пока ВыборкаОчередь.Следующий() Цикл

		ВыборкаВидыЗаданий = ВыборкаОчередь.Выбрать();

		СписокВидовЗаданий = Новый Массив;

		Пока ВыборкаВидыЗаданий.Следующий() Цикл

			СписокВидовЗаданий.Добавить(ВыборкаВидыЗаданий.ВидЗадания);

		КонецЦикла;

		Если СписокВидовЗаданий.Количество() > 0 Тогда

			НоваяСтрока = ТаблицаЗаданий.Добавить();
			НоваяСтрока.РегистрОчереди = ВыборкаОчередь.РегистрОчереди;
			НоваяСтрока.ВидыЗаданий = СписокВидовЗаданий;

		КонецЕсли;

	КонецЦикла;

	Возврат ТаблицаЗаданий;

КонецФункции

// Проверка условий доступности создания заданий неоперативной очереди в информационной базе.
// 
// Параметры:
//  Источник - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника
// 
// Возвращаемое значение:
//  Булево - Доступно создание заданий неоперативной очереди
Функция ДоступноСозданиеЗаданийНеоперативнойОчереди(Источник) Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат Ложь; // Регистрация заданий выполняется только в главном узле
	КонецЕсли;
	
	Если Источник.ОбменДанными.Загрузка Тогда
		
		// Проверим наличие служебного дополнительного свойства у набора записей (добавляется при обмене РИБ)
		Если Источник.ДополнительныеСвойства.Свойство("ОбменРИБСоздаватьЗаданияНеоперативнойОчереди") Тогда
			Возврат Истина;
		КонецЕсли;
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ИсполнениеНеоперативнойОчередиЗаданий

// Процедура запуска регламентного задания исполнения неоперативной очереди.
// 
//Параметры:
//  ПараметрыЗапускаЗаданий - см. ПараметрыЗапускаВыполненияЗаданий
Процедура РегламентноеИсполнениеНеоперативнойОчередиЗаданий(ПараметрыЗапускаЗаданий = Неопределено) Экспорт

	РегламентноеЗадание = Метаданные.РегламентныеЗадания.ИсполнениеНеоперативнойОчередиЗаданий;
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(РегламентноеЗадание);
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыЗапускаЗаданий = Неопределено Тогда
		ПараметрыЗапускаЗаданий = ПараметрыЗапускаВыполненияЗаданий();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	РежимРазделенияДанных = ОбщегоНазначения.РазделениеВключено();
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	ИдентификаторФоновогоЗадания = Неопределено;
	
	ФоновоеЗаданиеСеанса = ПолучитьТекущийСеансИнформационнойБазы().ПолучитьФоновоеЗадание();
	
	Если ФоновоеЗаданиеСеанса <> Неопределено Тогда
		
		ИдентификаторФоновогоЗадания = ФоновоеЗаданиеСеанса.УникальныйИдентификатор;
		
		ВремяЗавершенияРаботыРегламентногоЗадания = Дата(1, 1, 1, 0, 0, 0);
		
		Если РежимРазделенияДанных Тогда

			ОтборЗаданий = Новый Структура;
			ОтборЗаданий.Вставить("ИмяМетода", РегламентноеЗадание.ИмяМетода);
			ОтборЗаданий.Вставить("СостояниеЗадания", Перечисления.СостоянияЗаданий.Выполняется);
			Задания = РегламентныеЗаданияСервер.НайтиЗадания(ОтборЗаданий);

			Для Каждого Задание Из Задания Цикл

				ИдентификаторИсполняющегоФоновогоЗадания = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					Задание.Идентификатор, "ИсполняющееФоновоеЗадание");

				Если ИдентификаторФоновогоЗадания = ИдентификаторИсполняющегоФоновогоЗадания Тогда
					ВремяЗавершенияРаботыРегламентногоЗадания = ВремяЗавершенияРаботыРегламентногоЗаданияПоРасписанию(
						Задание.Расписание, ТекущаяДатаСеанса);
					Прервать;
				КонецЕсли;

			КонецЦикла;

		Иначе

			Если ФоновоеЗаданиеСеанса.РегламентноеЗадание <> Неопределено Тогда
				ВремяЗавершенияРаботыРегламентногоЗадания = ВремяЗавершенияРаботыРегламентногоЗаданияПоРасписанию(
					ФоновоеЗаданиеСеанса.РегламентноеЗадание.Расписание, ТекущаяДатаСеанса);
			КонецЕсли;

		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВремяЗавершенияРаботыРегламентногоЗадания) Тогда
			ПараметрыЗапускаЗаданий.ВремяЗавершенияРаботыУправляющегоПотока = ВремяЗавершенияРаботыРегламентногоЗадания;
		КонецЕсли;
		
	КонецЕсли;
	
	ФоновоеЗаданиеУправляющийПоток = ПроверитьЗапуститьИсполнениеНеоперативнойОчередиЗаданий(ПараметрыЗапускаЗаданий);
	
	Если РежимРазделенияДанных И ФоновоеЗаданиеУправляющийПоток <> Неопределено Тогда
		ФоновоеЗаданиеУправляющийПоток.ОжидатьЗавершенияВыполнения();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

// Процедура завершения исполнения неоперативной очереди заданий.
Процедура ЗавершениеИсполненияНеоперативнойОчередиЗаданий() Экспорт
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Возврат;
	КонецЕсли;

	НомерПопытки = 1;
	
	Пока НомерПопытки <= 3 Цикл
		
		УспешноеВыполнениеОперации = Истина;
		
		НачатьТранзакцию();

		Попытка

			ПричинаИсключения = "Блокировка";

			Блокировка = Новый БлокировкаДанных;
			Элемент = Блокировка.Добавить("РегистрСведений.ПриоритетныеЗаданияНеоперативнойОчереди");
			Элемент.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();

			ПричинаИсключения = "ОстановкаУправляющегоПотока";

			Запрос = Новый Запрос;
			Запрос.Текст = "
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ПриоритетныеЗаданияНеоперативнойОчереди.Организация КАК Организация,
			|	ПриоритетныеЗаданияНеоперативнойОчереди.ПериодЗадания КАК ПериодЗадания
			|ИЗ
			|	РегистрСведений.ПриоритетныеЗаданияНеоперативнойОчереди КАК ПриоритетныеЗаданияНеоперативнойОчереди";

			УстановитьПривилегированныйРежим(Истина);

			РезультатЗакрытиеМесяца = Запрос.Выполнить();
			Если РезультатЗакрытиеМесяца.Пустой() Тогда

				ИмяМетода = "НеоперативнаяОчередьЗаданий.ВыполнитьЗаданияНеоперативнойОчереди";
				Отбор = Новый Структура;
				Отбор.Вставить("ИмяМетода", ИмяМетода);
				Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
				ФоновыеЗаданияУправляющийПоток = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
				Для Каждого АктивноеЗадание Из ФоновыеЗаданияУправляющийПоток Цикл
					АктивноеЗадание.Отменить();
				КонецЦикла;

			КонецЕсли;

			УстановитьПривилегированныйРежим(Ложь);

			ЗафиксироватьТранзакцию();
			
		Исключение

			ОтменитьТранзакцию();
			
			УспешноеВыполнениеОперации = Ложь;
			
			ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());

			Если ПричинаИсключения = "Блокировка" Тогда

				Событие = НСтр("ru = 'Завершение управляющего потока неоперативной очереди: Блокировка';
								|en = 'Complete the thread-of-control of the backdate queue: Lock'",
					ОбщегоНазначения.КодОсновногоЯзыка());

			Иначе

				Событие = НСтр("ru = 'Завершение работы рабочего потока неоперативной очереди заданий';
								|en = 'Complete the worker thread of the backdate job queue'",
					ОбщегоНазначения.КодОсновногоЯзыка());

			КонецЕсли;

			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);

		КонецПопытки;
		
		Если УспешноеВыполнениеОперации Тогда
			Прервать;
		КонецЕсли;
		
		НомерПопытки = НомерПопытки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

// Запускает выполнение заданий неоперативной очереди (управляющий поток).
// Запуск управляющего потока может выполняться в 3-х режимах:
// 1. Регламентным заданием (обрабатываются все задания согласно периоду).
// 2. Закрытие месяца (задания, относящиеся к закрытию месяца, выполняются приоритетно).
// 3. Непосредственный запуск выполнения заданий по списку документов (см. ВыполнитьЗаданияНеоперативнойОчередиПоСпискуДокументов).
// 4. Непосредственный запуск выполнения заданий по документу в транзакции проведения (см. ВыполнитьЗаданияНеоперативнойОчередиВТранзакцииПроведенияДокумента).
// 
// Параметры:
//  ПараметрыЗапускаЗаданий - см. ПараметрыЗапускаВыполненияЗаданий
//
Процедура ВыполнитьЗаданияНеоперативнойОчереди(ПараметрыЗапускаЗаданий = Неопределено) Экспорт
	
	Если ПараметрыЗапускаЗаданий = Неопределено Тогда
		ПараметрыЗапускаЗаданий = ПараметрыЗапускаВыполненияЗаданий();
	КонецЕсли;
	
	СтруктураФормированияПорцийДанных = СтруктураФормированияПорцийДанных();
	
	ПараметрыВыполненияЗаданий = ПараметрыВыполненияЗаданий();
	ПараметрыВыполненияЗаданий.ВыполнениеЗаданийНепосредственно = (ПараметрыЗапускаЗаданий.ИнформационнаяБазаФайловая
		Или ПараметрыЗапускаЗаданий.РежимОтладки Или ПараметрыЗапускаЗаданий.ВыполнениеЗаданийПоСпискуДокументов);
	
	Если ПараметрыЗапускаЗаданий.ИнформационнаяБазаФайловая И ЗначениеЗаполнено(ПараметрыЗапускаЗаданий.ПериодЗакрытия) Тогда
		ПараметрыЗапускаЗаданий.РежимЗакрытияМесяцаФайловаяИБ = Истина;
	КонецЕсли;
	
	Если ПараметрыВыполненияЗаданий.ВыполнениеЗаданийНепосредственно Тогда
		КоличествоРабочихПотоков = 1;
		СтруктураФормированияПорцийДанных.ФоноваяОбработкаЗаданий = Ложь;
	Иначе
		КоличествоРабочихПотоков = КоличествоПотоковНеоперативнойОчередиЗаданий();
	КонецЕсли;
	
	СведенияПоВидамЗаданий = ПараметрыЗапускаЗаданий["ОчередиЗаданий"].ВидыЗаданий;
	
	Запрос = Новый Запрос;

	РабочиеПотоки = Новый Массив;
	КоличествоСвободныхРабочихПотоков = КоличествоРабочихПотоков;
	ЕстьЗаданияКОбработке = Истина;
	
	Пока ЕстьЗаданияКОбработке Или КоличествоСвободныхРабочихПотоков < КоличествоРабочихПотоков Цикл
		
		Запрос.УстановитьПараметр("Организации", ПараметрыЗапускаЗаданий.Организации);
		Запрос.УстановитьПараметр("ПериодЗакрытия", ПараметрыЗапускаЗаданий.ПериодЗакрытия);
		Запрос.УстановитьПараметр("ВидЗадания", ПараметрыЗапускаЗаданий.ВидЗадания);
		УстановитьПараметрыЗапросаПоОчередям(Запрос, ПараметрыЗапускаЗаданий["ОчередиЗаданий"]);
		
		Если ПараметрыЗапускаЗаданий.ВыполнениеЗаданийПоСпискуДокументов Тогда
			Запрос.Текст = ТекстЗапросаОтбораЗаданийНеоперативнойОчередиКВыполнениюПоСпискуДокументов(
				ПараметрыЗапускаЗаданий.ТаблицаДокументов, ПараметрыЗапускаЗаданий.ВидЗадания,
				ПараметрыЗапускаЗаданий["ОчередиЗаданий"], ПараметрыЗапускаЗаданий.ВыполнятьЗаданияВТранзакцииПроведения);
			Запрос.УстановитьПараметр("ВидЗадания", ПараметрыЗапускаЗаданий.ВидЗадания);
			Запрос.УстановитьПараметр("ДокументыКОтражению", ПараметрыЗапускаЗаданий.ТаблицаДокументов);
		Иначе
			
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

			Запрос.Текст = ТекстЗапросаМинимальныйПериодОбрабатываемыхЗаданий(ПараметрыЗапускаЗаданий);
			УстановитьПривилегированныйРежим(Истина);
			РезультатЗапроса = Запрос.ВыполнитьПакет();
			УстановитьПривилегированныйРежим(Ложь);
			РезультатРежимЗакрытияМесяца = РезультатЗапроса[РезультатЗапроса.ВГраница()].Выгрузить();

			РежимЗакрытияМесяца = РезультатРежимЗакрытияМесяца[0].РежимЗакрытияМесяца;
			Если РежимЗакрытияМесяца Тогда
				ПериодыЗакрытия = РезультатЗапроса[РезультатЗапроса.ВГраница() - 1].Выгрузить();
				ЗакрытиеМесяцаНачалоПериода = ПериодыЗакрытия[0].ПериодМинимальный;
				ЗакрытиеМесяцаКонецПериода = ПериодыЗакрытия[0].ПериодМаксимальный;
				СписокОрганизаций = РезультатЗапроса[РезультатЗапроса.ВГраница() - 2].Выгрузить().ВыгрузитьКолонку(
					"Организация");
				Запрос.УстановитьПараметр("Организации", СписокОрганизаций);
				Запрос.УстановитьПараметр("ЗакрытиеМесяцаНачалоПериода", ЗакрытиеМесяцаНачалоПериода);
				Запрос.УстановитьПараметр("ЗакрытиеМесяцаКонецПериода", ЗакрытиеМесяцаКонецПериода);
			Иначе
				Если ПараметрыЗапускаЗаданий.РежимЗакрытияМесяцаФайловаяИБ Тогда
					Возврат;
				КонецЕсли;
				
				Если Не ПараметрыЗапускаЗаданий.ИнформационнаяБазаФайловая 
					И ЗначениеЗаполнено(ПараметрыЗапускаЗаданий.ВремяЗавершенияРаботыУправляющегоПотока)
					И ПараметрыЗапускаЗаданий.ВремяЗавершенияРаботыУправляющегоПотока < ТекущаяДатаСеанса() Тогда

					Возврат;
					
				КонецЕсли;
				
			КонецЕсли;
			Запрос.Текст = ТекстЗапросаОтбораЗаданийНеоперативнойОчередиКВыполнению(ПараметрыЗапускаЗаданий,
				РежимЗакрытияМесяца);
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
		ВыборкаВидЗадания = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		УстановитьПривилегированныйРежим(Ложь);
		
		Если ВыборкаВидЗадания.Следующий() Тогда

			ВыборкаДень = ВыборкаВидЗадания.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

			Если ВыборкаДень.Следующий() Тогда

				ЕстьЗаданияКОбработке = Истина;
				ОписаниеВидаЗадания = СведенияПоВидамЗаданий.Получить(ВыборкаВидЗадания.ВидЗадания);
				
				ЗаполнитьЗначенияСвойств(СтруктураФормированияПорцийДанных, ВыборкаВидЗадания);
				СтруктураФормированияПорцийДанных.МаксимальнаяПорция = ОписаниеВидаЗадания.МаксимальнаяПорция;
				
				ВыборкаДокументы = ВыборкаДень.Выбрать();
				
				СтруктураФормированияПорцийДанных.ТаблицаДокументов.Очистить();
				Пока ВыборкаДокументы.Следующий() Цикл
					ДокументКОбработке = СтруктураФормированияПорцийДанных.ТаблицаДокументов.Добавить();
					ДокументКОбработке.Документ = ВыборкаДокументы.Документ;
				КонецЦикла;
				
				ДанныеОчереди = ПараметрыЗапускаЗаданий["ОчередиЗаданий"].ТаблицаОчередей.НайтиСтроки(
					Новый Структура("Регистр", ОписаниеВидаЗадания.РегистрОчереди));
				
				ПараметрыВыполненияЗаданий.ИмяРегистраОчереди = ДанныеОчереди[0].ИмяОчереди;
				ПараметрыВыполненияЗаданий.МенеджерРегистраЗаданий = ДанныеОчереди[0].МенеджерРегистраЗаданий;
				ПараметрыВыполненияЗаданий.ВыполнениеЗаданийВТранзакцииПроведения = ПараметрыЗапускаЗаданий.ВыполнятьЗаданияВТранзакцииПроведения;

				ДанныеКОбработке = ДанныеОчереди[0]["МенеджерРегистраЗаданий"].ДанныеДляМногопоточнойФоновойОбработки(
					СтруктураФормированияПорцийДанных, КоличествоСвободныхРабочихПотоков);
					
				ЗапуститьОбработкуПорцийДанных(ДанныеКОбработке["ГранулыРасчетаКОбработке"],
					ДанныеКОбработке["КоличествоПорций"], ПараметрыВыполненияЗаданий,
					ДанныеОчереди[0].ГранулыРасчета, РабочиеПотоки, ПараметрыЗапускаЗаданий["ОчередиЗаданий"]);
			КонецЕсли;
		
		Иначе
			
			// Пока гранулы расчета находятся в обработке, результат запроса может быть пустым (учитывается зависимость).
			// Поэтому, пока задания обрабатываются, нельзя устанавливать ЕстьЗаданияКОбработке в Ложь
			Если КоличествоСвободныхРабочихПотоков = КоличествоРабочихПотоков Тогда 
				ЕстьЗаданияКОбработке = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ПараметрыВыполненияЗаданий.ВыполнениеЗаданийНепосредственно Тогда
			
			Пока Истина Цикл

				ОбработатьСтатусыРабочихПотоков(РабочиеПотоки,
					ПараметрыЗапускаЗаданий["ОчередиЗаданий"].ТаблицаОчередей);
				КоличествоСвободныхРабочихПотоков = КоличествоРабочихПотоков - РабочиеПотоки.Количество();

				Если КоличествоСвободныхРабочихПотоков > 0 Тогда
					Прервать;
				КонецЕсли;

			КонецЦикла;
			
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

// Выполнение заданий неоперативной очереди по списку документов.
// 
// Параметры:
//  МассивДокументов - Неопределено, Массив Из ДокументСсылка - Документы, по которым требуется выполнить задания. Если в качестве
//  параметра передано Неопределено, то будет выполняться обработка всех документов по указанному виду задания.
//  ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания, для выполнения которого необходимо выполнить
//															 задания с меньшим номером очереди.
//
Процедура ВыполнитьЗаданияНеоперативнойОчередиПоСпискуДокументов(МассивДокументов, ВидЗадания) Экспорт
	
	Если ТипЗнч(МассивДокументов) = Тип("Массив") И МассивДокументов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОчередиЗаданий = НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий();
	
	УстановитьПривилегированныйРежим(Истина);
	СнятьПризнакВыполненоСОшибкойПоДокументам(МассивДокументов, Неопределено, ОчередиЗаданий);
	УстановитьПривилегированныйРежим(Ложь);
	
	ТаблицаДокументов = Новый ТаблицаЗначений;
	ТаблицаДокументов.Колонки.Добавить("Документ", Документы.ТипВсеСсылки());
	
	Если Не МассивДокументов = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицуИзМассива(ТаблицаДокументов, МассивДокументов, "Документ");
	КонецЕсли;
	
	ПараметрыЗапускаВыполненияЗаданий = ПараметрыЗапускаВыполненияЗаданий();
	ПараметрыЗапускаВыполненияЗаданий.ВыполнениеЗаданийПоСпискуДокументов = Истина;
	ПараметрыЗапускаВыполненияЗаданий.ТаблицаДокументов = ТаблицаДокументов;
	ПараметрыЗапускаВыполненияЗаданий.ВидЗадания = ВидЗадания;
	ПараметрыЗапускаВыполненияЗаданий.Вставить("ОчередиЗаданий", ОчередиЗаданий);
	
	ВыполнитьЗаданияНеоперативнойОчереди(ПараметрыЗапускаВыполненияЗаданий);
	
	#Область ОшибкиВыполненияЗаданий

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидЗадания", ВидЗадания);
	Запрос.УстановитьПараметр("СписокДокументов", МассивДокументов);
	УстановитьПараметрыЗапросаПоОчередям(Запрос, ОчередиЗаданий);

	ТаблицаОчередей = ОчередиЗаданий.ТаблицаОчередей;

	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Задания.Документ КАК Документ
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК Задания";

	ТекстПодзапросаШаблон = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОчереди.Документ КАК Документ
	|ИЗ
	|	&ТаблицаОчереди КАК ТаблицаОчереди
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиОчередейЗаданий КАК РегистрОшибки
	|		ПО ТаблицаОчереди.ИдентификаторОшибки = РегистрОшибки.ИдентификаторОшибки
	|ГДЕ
	|	&ПолеВидЗадания = &ВидЗадания
	|	И ТаблицаОчереди.ВыполненоСОшибкой
	|	И &ОтборПоСпискуДокументов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОчереди.Документ КАК Документ
	|ИЗ
	|	&ТаблицаОчереди КАК ТаблицаОчереди
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиОчередейЗаданий КАК РегистрОшибки
	|		ПО ТаблицаОчереди.ИдентификаторОшибки = РегистрОшибки.ИдентификаторОшибки
	|ГДЕ
	|	&ПолеНомерОчереди < ВЫРАЗИТЬ(&ВидЗадания КАК Справочник.ВидыНеоперативныхЗаданий).НомерОчереди
	|	И ТаблицаОчереди.ВыполненоСОшибкой
	|	И &ОтборПоСпискуДокументов";

	МассивПодзапросов = Новый Массив;

	Для Каждого ТекущаяОчередь Из ТаблицаОчередей Цикл

		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);

		Если МассивДокументов = Неопределено Тогда
			ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ОтборПоСпискуДокументов", "ИСТИНА");
		Иначе
			ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ОтборПоСпискуДокументов",
				"ТаблицаОчереди.Документ В (&СписокДокументов)");
		КонецЕсли;

		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеВидЗадания", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаВидЗадания), "&" + ТекущаяОчередь.ПараметрЗапросаВидЗадания,
			"ТаблицаОчереди.ВидЗадания"));

		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеНомерОчереди", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаНомерОчереди), "&" + ТекущаяОчередь.ПараметрЗапросаНомерОчереди,
			"ТаблицаОчереди.ВидЗадания.НомерОчереди"));

		МассивПодзапросов.Добавить(ТекстЗапросаОчереди);

	КонецЦикла;

	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");

	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);

	Если Не Результат.Пустой() Тогда

		ДокументыСОшибками =  Результат.Выгрузить();
		КоличествоДокументовСОшибками = ДокументыСОшибками.Количество();

		ПараметрИсключения = ?(КоличествоДокументовСОшибками = 1, ДокументыСОшибками[0].Документ, Формат(
			КоличествоДокументовСОшибками, "ЧГ=3,0;"));

		ШаблонТекстаИсключения = ?(КоличествоДокументовСОшибками = 1, НСтр(
			"ru = 'При выполнении заданий неоперативного допроведения возникли ошибки по документу: %1. Подробности в журнале регистрации.';
			|en = 'Errors occurred when performing jobs of backdate additional posting in the ""%1"" document. See the event log for details.'",
			ОбщегоНазначения.КодОсновногоЯзыка()), НСтр(
			"ru = 'При выполнении заданий неоперативного допроведения возникли ошибки по %1 документам. Подробности в журнале регистрации.';
			|en = 'Errors occurred when performing jobs of backdate additional posting in the documents: %1. See the event log for details.'",
			ОбщегоНазначения.КодОсновногоЯзыка()));

		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаИсключения,
			ПараметрИсключения);

		ОписаниеВидаЗадания = ОчередиЗаданий.ВидыЗаданий.Получить(ВидЗадания);
		Если Не ОписаниеВидаЗадания = Неопределено Тогда
			ДанныеОчереди = ОчередиЗаданий.ТаблицаОчередей.НайтиСтроки(Новый Структура("Регистр",
				ОписаниеВидаЗадания["РегистрОчереди"]));
			ИмяОчереди = ДанныеОчереди[0].ИмяОчереди;
			МетаданныеОшибка = Метаданные.РегистрыСведений[ИмяОчереди];
		Иначе
			МетаданныеОшибка = Метаданные.ОбщиеМодули.НеоперативнаяОчередьЗаданий;
		КонецЕсли;

		Если КоличествоДокументовСОшибками = 1 Тогда
			ЗаписатьОшибкуВЖурналРегистрации(МетаданныеОшибка, ВидЗадания, ТекстИсключения);
		Иначе
			ШаблонОшибки = НСтр("ru = 'Выполнение задания %1 по документу %2 завершилось с ошибкой';
								|en = 'The ""%1"" job for the ""%2"" document is completed with an error'",
				ОбщегоНазначения.КодОсновногоЯзыка());

			Для Каждого СтрокаСОшибкой Из ДокументыСОшибками Цикл
				ТекстОшибкиПоДокументу = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки,
					ВидЗадания, СтрокаСОшибкой.Документ);
				ЗаписатьОшибкуВЖурналРегистрации(МетаданныеОшибка, ВидЗадания, ТекстОшибкиПоДокументу, СтрокаСОшибкой.Документ);
			КонецЦикла;
		КонецЕсли;

		ВызватьИсключение ТекстИсключения;

	КонецЕсли;

#КонецОбласти
	
КонецПроцедуры

// Выполнение заданий неоперативной очереди по документу в транзакции проведения.
// 
// Параметры:
//  Документ - ДокументСсылка - Документ, по которому требуется выполнить задания.
//  ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания, для выполнения которого необходимо выполнить
//															 задания с меньшим номером очереди.
//
Процедура ВыполнитьЗаданияНеоперативнойОчередиВТранзакцииПроведенияДокумента(Документ, ВидЗадания) Экспорт
	
	ОчередиЗаданий = НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий();
	
	ТаблицаДокументов = Новый ТаблицаЗначений;
	ТаблицаДокументов.Колонки.Добавить("Документ", Документы.ТипВсеСсылки());
	ТаблицаДокументов.Добавить().Документ = Документ;

	ПараметрыЗапускаВыполненияЗаданий = ПараметрыЗапускаВыполненияЗаданий();
	ПараметрыЗапускаВыполненияЗаданий.ВыполнениеЗаданийПоСпискуДокументов = Истина;
	ПараметрыЗапускаВыполненияЗаданий.ТаблицаДокументов = ТаблицаДокументов;
	ПараметрыЗапускаВыполненияЗаданий.ВидЗадания = ВидЗадания;
	ПараметрыЗапускаВыполненияЗаданий.ВыполнятьЗаданияВТранзакцииПроведения = Истина;
	ПараметрыЗапускаВыполненияЗаданий.Вставить("ОчередиЗаданий", ОчередиЗаданий);
	
	ВыполнитьЗаданияНеоперативнойОчереди(ПараметрыЗапускаВыполненияЗаданий);
	
КонецПроцедуры

// Проверяет наличие запущенного экземпляра управляющего потока и в случае, если управляющий поток не активен - запускает 
// выполнение заданий неоперативной очереди с отбором по виду.
// 
// Параметры:
//  ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - К выполнению будут получены задания по всем документам, которые соответствуют 
// переданному в параметре значению, а также все задания, у которых меньший номер выполнения.
//
Процедура ВыполнитьЗаданияНеоперативнойОчередиСОтборомПоВиду(ВидЗадания) Экспорт
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыЗапускаЗаданий = ПараметрыЗапускаВыполненияЗаданий();
	ПараметрыЗапускаЗаданий.ВидЗадания = ВидЗадания;
	ПараметрыЗапускаЗаданий.ОтборПоВидуЗадания = Истина;
	
	Пока Истина Цикл
		
		ФоновоеЗаданиеУправляющийПоток = ПроверитьЗапуститьИсполнениеНеоперативнойОчередиЗаданий(ПараметрыЗапускаЗаданий);
		
		Если ФоновоеЗаданиеУправляющийПоток <> Неопределено Тогда
			Прервать;
		Иначе
			ОбщегоНазначенияУТ.Пауза(5);
		КонецЕсли;

	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Запускает фоновое выполнение заданий неоперативной очереди с отбором по виду.
// 
// Параметры:
//  ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - К выполнению будут получены задания по всем документам, которые соответствуют 
// переданному в параметре значению, а также все задания, у которых меньший номер выполнения.
//
Процедура ЗапуститьФоновоеВыполнениеЗаданийНеоперативнойОчередиСОтборомПоВиду(ВидЗадания) Экспорт
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФоновогоЗадания = Новый Массив;
	ПараметрыФоновогоЗадания.Добавить(ВидЗадания);
	
	УстановитьПривилегированныйРежим(Истина);
	НаименованиеФоновогоЗадания = НСтр(
		"ru = 'Запуск фонового выполнения заданий неоперативной очереди с отбором по виду';
		|en = 'Run background execution of backdate queue jobs with filter by type'");
	Ключ = Строка(Новый УникальныйИдентификатор);
	ФоновыеЗадания.Выполнить(
		"НеоперативнаяОчередьЗаданий.ВыполнитьЗаданияНеоперативнойОчередиСОтборомПоВиду", ПараметрыФоновогоЗадания,
		Ключ, НаименованиеФоновогоЗадания);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Запись ошибки в журнал регистрации.
// 
// Параметры:
//  МетаданныеОшибка - ОбъектМетаданных - Метаданные, по которым фиксируется ошибка в журнале регистрации
//  ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания, по которому регистрируется ошибка выполнения
//  ТекстОшибки -Строка - Подробный текст ошибки
//  Документ - ДокументСсылка, Строка - Обрабатываемый документ или кортеж
//
Процедура ЗаписатьОшибкуВЖурналРегистрации(МетаданныеОшибка, ВидЗадания, ТекстОшибки, Документ = Неопределено) Экспорт
	
	ИмяСобытия = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
		"ru = 'Неоперативное допроведение документов (%1)';
		|en = 'Backdate additional document posting (%1)'", ОбщегоНазначения.КодОсновногоЯзыка()), ВидЗадания);

	ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,
		МетаданныеОшибка, Документ, ТекстОшибки);

КонецПроцедуры

// Удаление заданий очереди по переданным идентификаторам.
// 
// Параметры:
//  ИмяОчереди - Строка - Имя регистра очереди
//  ИдентификаторыЗаданий - Массив из УникальныйИдентификатор - Список идентификаторов заданий к удалению
//  ОчередиЗаданий - см. НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий
//
Процедура УдалитьЗаданияОчереди(ИмяОчереди, ИдентификаторыЗаданий, ОчередиЗаданий) Экспорт

	Если ИдентификаторыЗаданий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений[ИмяОчереди].СоздатьНаборЗаписей();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторыЗаданий", ИдентификаторыЗаданий);
	Запрос.Текст = "
	|ВЫБРАТЬ *
	|ИЗ
	|	&ТаблицаОчереди КАК Очередь
	|ГДЕ
	|	Очередь.ИдентификаторЗаписи В (&ИдентификаторыЗаданий)";
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"&ТаблицаОчереди" , "РегистрСведений." + ИмяОчереди);
	ЗаписиЗаданий = Запрос.Выполнить().Выгрузить();
	
	Если ЗаписиЗаданий.Количество() > 0 Тогда
		НаборЗаписей.Загрузить(ЗаписиЗаданий);
		НаборЗаписей.Записать(РежимЗамещения.Удаление);
		
		ИдентификаторыОшибок = ЗаписиЗаданий.ВыгрузитьКолонку("ИдентификаторОшибки");
		УдалениеЗаписейРегистраОшибокОчередей(ОчередиЗаданий, ИдентификаторыОшибок);
		
	КонецЕсли;
	
	ПроверитьНаличиеЗаданийСПовышеннымПриоритетомВыполнения(ОчередиЗаданий);

КонецПроцедуры

// По переданному списку документов и виду задания обрабатываются данные об ошибках.
//
// Параметры:
//  СписокДокументов - Неопределено, Массив Из ДокументСсылка - Обрабатываемые документы
//  ВидЗадания - Неопределено, СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания
//  ОчередиЗаданий - см. НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий
//
Процедура СнятьПризнакВыполненоСОшибкойПоДокументам(СписокДокументов, ВидЗадания = Неопределено,
	ОчередиЗаданий = Неопределено) Экспорт

	Если ОчередиЗаданий = Неопределено Тогда
		ОчередиЗаданий = НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий();
	КонецЕсли;

	ТаблицаОчередей = ОчередиЗаданий.ТаблицаОчередей;

	ИдентификаторыОшибок = Новый Массив;

	Для Каждого ТекущаяОчередь Из ТаблицаОчередей Цикл

		МенеджерРегистраЗаданий = ТекущаяОчередь.МенеджерРегистраЗаданий;
		МенеджерРегистраЗаданий.СнятьПризнакВыполненоСОшибкойПоДокументам(СписокДокументов, ИдентификаторыОшибок,
			ВидЗадания);

	КонецЦикла;

	УдалениеЗаписейРегистраОшибокОчередей(ОчередиЗаданий, ИдентификаторыОшибок);
	
КонецПроцедуры

// Параметры регламентного задания исполнения заданий неоперативной очереди.
// Данные параметры используются в форме обработки "Операции закрытия месяца" с целью отражения на форме гиперссылки на настройку регламентного задания.
// 
// Возвращаемое значение:
//  Структура - Параметры регламентного задания допроведения документов:
// * ИдентификаторРегламентногоЗадания - Неопределено, УникальныйИдентификатор - Идентификатор регламентного задания
// * РегламентноеЗаданиеИспользуется - Булево - Признак, что регламентное задание используется 
Функция ПараметрыРегламентногоЗаданияНеоперативнойОчереди() Экспорт
	
	ПараметрыРегламентногоЗадания = Новый Структура;
	ПараметрыРегламентногоЗадания.Вставить("ИдентификаторРегламентногоЗадания", Неопределено);
	ПараметрыРегламентногоЗадания.Вставить("РегламентноеЗаданиеИспользуется", Ложь);
	
	ИнформационнаяБазаФайловая = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	Если Не ИнформационнаяБазаФайловая И ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда

		УстановитьПривилегированныйРежим(Истина);
		ИдентификаторРегламентногоЗадания = РегламентныеЗаданияСервер.УникальныйИдентификатор(
			Метаданные.РегламентныеЗадания.ИсполнениеНеоперативнойОчередиЗаданий);
		ПараметрыРегламентногоЗадания.ИдентификаторРегламентногоЗадания = ИдентификаторРегламентногоЗадания;

		Если Не ИдентификаторРегламентногоЗадания = Неопределено Тогда
			Задание = РегламентныеЗаданияСервер.Задание(ИдентификаторРегламентногоЗадания);
			ПараметрыРегламентногоЗадания.РегламентноеЗаданиеИспользуется = Задание <> Неопределено И Задание.Использование;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);

	КонецЕсли;

	Возврат ПараметрыРегламентногоЗадания;
	
КонецФункции

#Область ПараметрыОбработкиДанных

// Структура формирования порций данных.
// 
// Возвращаемое значение:
//  Структура - Структура формирования порций данных:
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания
// * МаксимальнаяПорция - Число - Максимальная порция гранул расчета
// * ФоноваяОбработкаЗаданий - Булево - Признак того, что задания будут выполняться фоновыми потоками
// * ТаблицаДокументов - ТаблицаЗначений - :
// ** Документ - ДокументСсылка
Функция СтруктураФормированияПорцийДанных() Экспорт

	СтруктураПотоков = Новый Структура;
	СтруктураПотоков.Вставить("ВидЗадания", Справочники.ВидыНеоперативныхЗаданий.ПустаяСсылка());
	СтруктураПотоков.Вставить("МаксимальнаяПорция", 0);
	СтруктураПотоков.Вставить("ФоноваяОбработкаЗаданий", Истина);
	
	ТаблицаДокументов = Новый ТаблицаЗначений();
	ТаблицаДокументов.Колонки.Добавить("Документ", Документы.ТипВсеСсылки());
	СтруктураПотоков.Вставить("ТаблицаДокументов", ТаблицаДокументов);

	Возврат СтруктураПотоков;

КонецФункции

// Структура параметров выполнения заданий.
// 
// Возвращаемое значение:
//  Структура - Структура параметров выполнения заданий:
// * ВыполнениеЗаданийНепосредственно - Булево - Если Истина, то задания будут выполняться не в фоновом задании
// * ИмяРегистраОчереди - Строка - Имя регистра очереди
// * МенеджерРегистраЗаданий - Неопределено - Менеджер регистра заданий
// * ВыполнениеЗаданийВТранзакцииПроведения - Булево - Признак того, что задания выполняются в транзакции проведения документа.
Функция ПараметрыВыполненияЗаданий() Экспорт

	СтруктураПотоков = Новый Структура;
	СтруктураПотоков.Вставить("ВыполнениеЗаданийНепосредственно", Ложь);
	СтруктураПотоков.Вставить("ИмяРегистраОчереди", "");
	СтруктураПотоков.Вставить("МенеджерРегистраЗаданий", Неопределено);
	СтруктураПотоков.Вставить("ВыполнениеЗаданийВТранзакцииПроведения", Ложь);
	
	Возврат СтруктураПотоков;

КонецФункции

// Структура данных обработки заданий.
// 
// Возвращаемое значение:
//  Структура - Структура данных обработки заданий:
// * КоличествоПорций - Число - Число порций гранул расчета.
// * ГранулыРасчетаКОбработке - ТаблицаЗначений - Таблица с описанием гранул расчета по заданиям.
Функция СтруктураДанныхОбработкиЗаданий() Экспорт

	СтруктураПотоков = Новый Структура;
	СтруктураПотоков.Вставить("КоличествоПорций", 0);
	СтруктураПотоков.Вставить("ГранулыРасчетаКОбработке", Новый ТаблицаЗначений);
	
	Возврат СтруктураПотоков;

КонецФункции

// Параметры обработки порции данных.
// 
// Возвращаемое значение:
//  Структура - Параметры обработки порции данных:
// * ПорцияДанных - ТаблицаЗначений - Таблица с описанием гранул расчета по заданиям.
// * ИмяРегистраОчереди - Строка - Имя регистра очереди.
// * ВыполнениеЗаданийВТранзакцииПроведения - Булево - Признак того, что задания выполняются в транзакции проведения документа.
Функция ПараметрыОбработкиПорцииДанных() Экспорт

	ПараметрыДанных = Новый Структура;
	ПараметрыДанных.Вставить("ПорцияДанных", Новый ТаблицаЗначений);
	ПараметрыДанных.Вставить("ИмяРегистраОчереди", "");
	ПараметрыДанных.Вставить("ВыполнениеЗаданийВТранзакцииПроведения", Ложь);

	Возврат ПараметрыДанных;

КонецФункции

// Структура описания параметров очереди по-умолчанию.
// Используется при формировании таблицы очередей: см. НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий.
// Определяет основные параметры переопределения запросов, характерные для каждого из регистра заданий.
// Используется в функции ОписаниеОчереди модуля менеджера регистра заданий.
// 
// Возвращаемое значение:
//  Структура - Структура описания параметров очереди:
// * ПараметрЗапросаВидЗадания - Строка - Описывает параметра запроса ВидЗадания
// * ЗначениеВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания, задаваемое в параметре запроса.
// * ПараметрЗапросаНомерОчереди - Строка - Описывает параметр поля "Номер очереди"
// * ЗначениеНомерОчереди - Число - Номер очереди, задаваемый в параметра запроса.
// * ТекстЗапросаГранулыРасчетаВОбработке - Строка - Текст запроса по выборе гранул расчета.
// * ТекстЗапросаИсключаемыеДокументы - Строка - Тект запроса по отбору документов гранул расчета.
// * ОбрабатываемыеГранулыИмяТаблицы - Строка - Имя временной таблицы, содержащей информацию по обрабатываемым гранулам.
Функция СтруктураОписанияПараметровОчереди() Экспорт
	
	СтруктураОписания = Новый Структура;
	СтруктураОписания.Вставить("ПараметрЗапросаВидЗадания", "");
	СтруктураОписания.Вставить("ЗначениеВидЗадания", Справочники.ВидыНеоперативныхЗаданий.ПустаяСсылка());
	СтруктураОписания.Вставить("ПараметрЗапросаНомерОчереди", "");
	СтруктураОписания.Вставить("ЗначениеНомерОчереди", 0);
	
	СтруктураОписания.Вставить("ТекстЗапросаГранулыРасчетаВОбработке", "");
	СтруктураОписания.Вставить("ТекстЗапросаИсключаемыеДокументы", "");
	СтруктураОписания.Вставить("ОбрабатываемыеГранулыИмяТаблицы", "");
	
	Возврат СтруктураОписания;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ЗакрытиеМесяца

#Область НеоперативноеДопроведениеДокументов

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
// 
// Параметры:
// 	ТаблицаЭтапов - (См. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца)
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_РасчетСуммДокументовВВалютахУчетаИПерепроведениеПоФинансовымРегистрам(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Справочники.ОперацииЗакрытияМесяца.НеоперативноеДопроведениеДокументов);
	
	НоваяСтрока.МультиязычноеНаименование = "ru = 'Допроведение документов';
											|en = 'Additional document posting'"; // @НСтр-1
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"НеоперативнаяОчередьЗаданий.Использование_РасчетСуммДокументовВВалютахУчетаИПерепроведениеПоФинансовымРегистрам");
	НоваяСтрока.ДействиеОформление = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"НеоперативнаяОчередьЗаданий.Оформление_РасчетСуммДокументовВВалютахУчетаИПерепроведениеПоФинансовымРегистрам");
	НоваяСтрока.ДействиеВыполнить = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"НеоперативнаяОчередьЗаданий.Выполнить_РасчетСуммДокументовВВалютахУчетаИПерепроведениеПоФинансовымРегистрам");
		НоваяСтрока.ВыполняетсяПриПредварительномЗакрытииМесяца = Истина;
КонецПроцедуры

// Определяет статус этапа закрытия месяца
// 
// Параметры:
//  ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
//
Процедура Использование_РасчетСуммДокументовВВалютахУчетаИПерепроведениеПоФинансовымРегистрам(ПараметрыОбработчика) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда

		ЗакрытиеМесяцаСервер.УстановитьСостояниеОтключено(
			ПараметрыОбработчика, НСтр("ru = 'Отсутствуют задания к допроведению документов';
										|en = 'There are no jobs for additional document posting'",
			ОбщегоНазначения.КодОсновногоЯзыка()));

		Возврат;

	КонецЕсли;
	
	ОчередиЗаданий = НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий();
	ТаблицаОчередей = ОчередиЗаданий.ТаблицаОчередей;
		
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(ЗаданияКОбработке.ПериодЗадания, МЕСЯЦ)) КАК Месяц
	|ИЗ
	|	&ТекстВложенногоЗапросаПериод КАК ЗаданияКОбработке
	|ИМЕЮЩИЕ
	|	НЕ МИНИМУМ(НАЧАЛОПЕРИОДА(ЗаданияКОбработке.ПериодЗадания, МЕСЯЦ)) ЕСТЬ NULL
	|";
	
	ТекстПодзапросаПериодШаблон = "
	|ВЫБРАТЬ
	|	МИНИМУМ(ВЫБОР
	|		КОГДА ТаблицаОчереди.ПериодЗадания = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &КонецПериода
	|		ИНАЧЕ ТаблицаОчереди.ПериодЗадания
	|	КОНЕЦ) КАК ПериодЗадания
	|ИЗ
	|	&ТаблицаОчереди КАК ТаблицаОчереди
	|ГДЕ
	|	ТаблицаОчереди.Организация В (&МассивОрганизаций)
	|	И ТаблицаОчереди.ПериодЗадания <= &КонецПериода";
		
	МассивПодзапросовПериод = Новый Массив;
	Для каждого ТекущаяОчередь Из ТаблицаОчередей Цикл
		
		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаПериодШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);
		МассивПодзапросовПериод.Добавить(ТекстЗапросаОчереди);
		
	КонецЦикла;
	
	ТекстПодзапросаПериод = СтрСоединить(МассивПодзапросовПериод, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстВложенногоЗапросаПериод", "(" + ТекстПодзапросаПериод + ")");
	Выборка = Запрос.Выполнить().Выбрать();

	ЕстьЗаданияПоЭтапуЗакрытияМесяца = Ложь;
	
	Если Выборка.Следующий() Тогда

		Если Не ЗначениеЗаполнено(Выборка.Месяц) Тогда
			Возврат;
		КонецЕсли;
		
		ЕстьЗаданияПоЭтапуЗакрытияМесяца = Истина;

		МесяцОбработкиЗаданий = Выборка.Месяц;

		ПараметрыОбработчика.ДанныеЭтапа.ДатаНачалаРасчета = МесяцОбработкиЗаданий;

		ШаблонСостояния = НСтр("ru = 'Начиная с периода %1 по документам требуется обработать задания';
								|en = 'Starting from the period of %1, process jobs for documents'",
			ОбщегоНазначения.КодОсновногоЯзыка());

		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
			ПараметрыОбработчика, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСостояния, РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(МесяцОбработкиЗаданий)));

	КонецЕсли;
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	Запрос.УстановитьПараметр("ЕстьЗаданияПоЭтапуДопроведенияДокументов", ЕстьЗаданияПоЭтапуЗакрытияМесяца);
	
	МассивТекстовЗапроса = Новый Массив;
	МассивТекстовЗапроса.Добавить(ОперативныеВзаиморасчетыСервер.ТекстЗапросаСверкаСуммОперативныхИФинансовыхРегистров());
	
	ТекстЗапросаКлиенты = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расхождения.Документ КАК Регистратор,
	|	Расхождения.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_РасхожденияКлиенты
	|ИЗ
	|	РасчетыСКлиентамиИзменения КАК Расхождения";
	МассивТекстовЗапроса.Добавить(ТекстЗапросаКлиенты);
	
	ТекстЗапросаПоставщики = "
	|ВЫБРАТЬ
	|	Расхождения.Документ КАК Регистратор,
	|	Расхождения.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_РасхожденияПоставщики
	|ИЗ
	|	РасчетыСПоставщикамиИзменения КАК Расхождения";
	МассивТекстовЗапроса.Добавить(ТекстЗапросаПоставщики);
	
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
		
	Запрос.Выполнить();
	
КонецПроцедуры

// Определяет пользовательское представление этапа закрытия месяца
// 
// Параметры:
//  ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
//
Процедура Оформление_РасчетСуммДокументовВВалютахУчетаИПерепроведениеПоФинансовымРегистрам(ПараметрыОбработчика) Экспорт
	
	ПараметрыОбработчика.ДанныеЭтапа.Наименование = НСтр(
		"ru = 'Расчет сумм документов в валютах учета и перепроведение по финансовым регистрам';
		|en = 'Calculate document amounts in accounting currencies and repost in financial registers'");
	ПараметрыОбработчика.ДанныеЭтапа.ТекстВыполнить = НСтр("ru = 'Выполнить';
															|en = 'Run'");
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика);
	
КонецПроцедуры

// Выполняет операции по этапу закрытия месяца. В том числе перед выполнением заданий выполняет исправление остатков по взаиморасчетам
// 
// Параметры:
//  ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
//
Процедура Выполнить_РасчетСуммДокументовВВалютахУчетаИПерепроведениеПоФинансовымРегистрам(ПараметрыОбработчика) Экспорт
	
	ОчиститьПроблемыВыполненияРасчета(ПараметрыОбработчика);
	
	ОчередиЗаданий = НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий();
	
	#Область ОбработкаОшибокЗаданийОчереди
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ЗаданияКОбработке.Документ КАК Документ
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК ЗаданияКОбработке";
	
	ТекстПодзапросаШаблон = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОчереди.Документ КАК Документ
	|ИЗ
	|	&ТаблицаОчереди КАК ТаблицаОчереди
	|ГДЕ
	|	ТаблицаОчереди.Организация В (&МассивОрганизаций)
	|	И ТаблицаОчереди.ПериодЗадания <= &КонецПериода
	|	И ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			&ТаблицаОчереди КАК ВЗТаблицаОчереди
	|		ГДЕ
	|			ТаблицаОчереди.Документ = ВЗТаблицаОчереди.Документ
	|			И ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					РегистрСведений.ОшибкиОчередейЗаданий КАК РегистрОшибки
	|				ГДЕ
	|					ВЗТаблицаОчереди.ИдентификаторОшибки = РегистрОшибки.ИдентификаторОшибки))";
	
	МассивПодзапросов = Новый Массив;
	Для каждого ТекущаяОчередь Из ОчередиЗаданий.ТаблицаОчередей Цикл
		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);
		МассивПодзапросов.Добавить(ТекстЗапросаОчереди);
	КонецЦикла;

	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
	
	СписокДокументовКОбработкеОшибок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Документ");
	Если СписокДокументовКОбработкеОшибок.Количество() > 0 Тогда
		СнятьПризнакВыполненоСОшибкойПоДокументам(СписокДокументовКОбработкеОшибок, Неопределено, ОчередиЗаданий);
	КонецЕсли;

	#КонецОбласти
	
	НачалоРасчета = ПараметрыОбработчика.ПараметрыРасчета.НачалоПериода;

	ЗакрытиеМесяцаВыполнитьЗаданияНеоперативнойОчереди(ПараметрыОбработчика, ОчередиЗаданий);
	
	РезультатыПроверки = ПроверитьДвиженияДокументовПоРегистрамВзаиморасчетов(ПараметрыОбработчика, НачалоРасчета);
	Если РезультатыПроверки.ПроблемныеДокументыСуммыВВалютахУчета.Количество() > 0
		Или РезультатыПроверки.МВТРасхожденияВРегистрахВзаиморасчетов <> Неопределено Тогда
		ИсправитьДвиженияДокументовПоРегистрамВзаиморасчетов(ПараметрыОбработчика, РезультатыПроверки, ОчередиЗаданий);
	КонецЕсли;
	
	ПроверитьИЗарегистрироватьПроблемыВыполненияРасчета(ПараметрыОбработчика, ОчередиЗаданий, Неопределено);

КонецПроцедуры

// Проверки состояния системы, относящиеся к этапу.
// 
// Параметры:
// 	ТаблицаПроверок - см. АудитСостоянияСистемы.ТаблицаПроверокСостоянияСистемы
//
Процедура ОписаниеПроверок_РасчетСуммДокументовВВалютахУчетаИПерепроведениеПоФинансовымРегистрам(ТаблицаПроверок) Экспорт
	
	ОписаниеПроверки = ЗакрытиеМесяцаСервер.ДобавитьОписаниеНовойПроверки(ТаблицаПроверок,
		"РасхождениеСуммВОперативныхИФинансовыхРегистрахВзаиморасчетов",
		Справочники.ОперацииЗакрытияМесяца.НеоперативноеДопроведениеДокументов,
		Перечисления.МоментЗапускаПроверкиОперацииЗакрытияМесяца.ДоИПослеРасчета,
		"НеоперативнаяОчередьЗаданий.ПроверкаЭтапа_РасхождениеСуммВОперативныхИФинансовыхРегистрахВзаиморасчетов");
	
	ЗакрытиеМесяцаСервер.ЗаполнитьПредставлениеНовойПроверки(ОписаниеПроверки,
		НСтр("ru = 'Расхождения в регистрах взаиморасчетов';
			|en = 'Discrepancies in the mutual settlement registers'", ОбщегоНазначения.КодОсновногоЯзыка()),
		НСтр("ru = 'Не должно быть документов, по которым имеются расхождения в регистрах взаиморасчетов';
			|en = 'There must be no documents with discrepancies in AR/AP accounting registers'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
КонецПроцедуры

// Регистрация ошибок.
// 
// Параметры:
// 	ПараметрыПроверки - см. АудитСостоянияСистемы.ИнициализироватьПараметрыПроверки
//
Процедура ПроверкаЭтапа_РасхождениеСуммВОперативныхИФинансовыхРегистрахВзаиморасчетов(ПараметрыПроверки) Экспорт
	
	Если НЕ ЗакрытиеМесяцаСервер.ПроверкаВыполняетсяМеханизмомЗакрытияМесяца(ПараметрыПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	СписокПолей = Новый СписокЗначений;
	СписокПолей.Добавить("Регистратор", НСтр("ru = 'Документ';
											|en = 'Document'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Организация", НСтр("ru = 'Организация';
											|en = 'Company'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	ШаблонПредставленияРегистровРасчетов = НСтр("ru = ' ""%1"" и ""%2""';
												|en = ' ""%1"" and ""%2""'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	ПредставлениеРегистровРасчетовСКлиентами = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставленияРегистровРасчетов,
		Метаданные.РегистрыНакопления.РасчетыСКлиентами.Представление(),
		Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Представление());
	
	ПредставлениеРегистровРасчетовСПоставщиками = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставленияРегистровРасчетов,
		Метаданные.РегистрыНакопления.РасчетыСПоставщиками.Представление(),
		Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.Представление());
	
	ПараметрыРегистрацииКлиенты = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемПроверки(
		"ВТ_РасхожденияКлиенты", НСтр(
		"ru = 'Обнаружены документы, по которым различаются движения в регистрах';
		|en = 'Documents with discrepancies in register records are found'",
		ОбщегоНазначения.КодОсновногоЯзыка()) + ПредставлениеРегистровРасчетовСКлиентами, СписокПолей);
		
	ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемыВыполненияПроверки(
	 	ПараметрыПроверки,
		ПараметрыРегистрацииКлиенты,
		"",
		Неопределено,
		Перечисления.ВажностьПроблемыУчета.Предупреждение);
		
	ПараметрыРегистрацииПоставщики = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемПроверки(
		"ВТ_РасхожденияПоставщики", НСтр(
		"ru = 'Обнаружены документы, по которым различаются движения в регистрах';
		|en = 'Documents with discrepancies in register records are found'",
		ОбщегоНазначения.КодОсновногоЯзыка()) + ПредставлениеРегистровРасчетовСПоставщиками, СписокПолей);
		
	ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемыВыполненияПроверки(
	 	ПараметрыПроверки,
		ПараметрыРегистрацииПоставщики,
		"",
		Неопределено,
		Перечисления.ВажностьПроблемыУчета.Предупреждение);
	
КонецПроцедуры

#КонецОбласти

#Область НеоперативноеДопроведениеДокументовУпрБаланс

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
// 
// Параметры:
// 	ТаблицаЭтапов - (См. Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца)
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_ОтражениеДвиженийДокументовПоУправленческомуБалансу(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Справочники.ОперацииЗакрытияМесяца.ОтражениеДвиженийДокументовПоУправленческомуБалансу);
	
	НоваяСтрока.МультиязычноеНаименование = "ru = 'Управленческий баланс';
											|en = 'Balance sheet statement'"; // @НСтр-1
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"НеоперативнаяОчередьЗаданий.Использование_ОтражениеДвиженийДокументовПоУправленческомуБалансу");
	НоваяСтрока.ДействиеОформление = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"НеоперативнаяОчередьЗаданий.Оформление_ОтражениеДвиженийДокументовПоУправленческомуБалансу");
	НоваяСтрока.ДействиеВыполнить = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"НеоперативнаяОчередьЗаданий.Выполнить_ОтражениеДвиженийДокументовПоУправленческомуБалансу");
		НоваяСтрока.ВыполняетсяПриПредварительномЗакрытииМесяца = Истина;
		
	НоваяСтрока.ОбновитьСостояниеЭтапов.Добавить(
		Справочники.ОперацииЗакрытияМесяца.НеоперативноеДопроведениеДокументов);
КонецПроцедуры

// Определяет статус этапа закрытия месяца
// 
// Параметры:
//  ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
//
Процедура Использование_ОтражениеДвиженийДокументовПоУправленческомуБалансу(ПараметрыОбработчика) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Или Не ПолучитьФункциональнуюОпцию(
		"ФормироватьУправленческийБаланс") Тогда

		ЗакрытиеМесяцаСервер.УстановитьСостояниеОтключено(
			ПараметрыОбработчика, НСтр(
			"ru = 'Отсутствуют задания к отражению движений документов в управленческом балансе';
			|en = 'There are no jobs for recording document records in the balance sheet statement'",
			ОбщегоНазначения.КодОсновногоЯзыка()));

		Возврат;

	КонецЕсли;
	
	ОчередиЗаданий = НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий();

	ТаблицаОчередей = ОчередиЗаданий.ТаблицаОчередей;
		
	Запрос = Новый Запрос;
	УстановитьПараметрыЗапросаПоОчередям(Запрос, ОчередиЗаданий);
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(ЗаданияКОбработке.ПериодЗадания, МЕСЯЦ)) КАК Месяц
	|ИЗ
	|	&ТекстВложенногоЗапросаПериод КАК ЗаданияКОбработке
	|ИМЕЮЩИЕ
	|	НЕ МИНИМУМ(НАЧАЛОПЕРИОДА(ЗаданияКОбработке.ПериодЗадания, МЕСЯЦ)) ЕСТЬ NULL
	|";
	
	ТекстПодзапросаПериодШаблон = "
	|ВЫБРАТЬ
	|	МИНИМУМ(ВЫБОР
	|		КОГДА ТаблицаОчереди.ПериодЗадания = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &КонецПериода
	|		ИНАЧЕ ТаблицаОчереди.ПериодЗадания
	|	КОНЕЦ) КАК ПериодЗадания
	|ИЗ
	|	&ТаблицаОчереди КАК ТаблицаОчереди
	|ГДЕ
	|	ТаблицаОчереди.Организация В (&МассивОрганизаций)
	|	И ТаблицаОчереди.ПериодЗадания <= &КонецПериода
	|	И &ПолеВидЗадания = ЗНАЧЕНИЕ(Справочник.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу)";
	
	МассивПодзапросовПериод = Новый Массив;
	Для каждого ТекущаяОчередь Из ТаблицаОчередей Цикл
		
		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаПериодШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);
		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеВидЗадания", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаВидЗадания), "&" + ТекущаяОчередь.ПараметрЗапросаВидЗадания,
			"ТаблицаОчереди.ВидЗадания"));
		МассивПодзапросовПериод.Добавить(ТекстЗапросаОчереди);
		
	КонецЦикла;
	
	ТекстПодзапросаПериод = СтрСоединить(МассивПодзапросовПериод, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());

	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстВложенногоЗапросаПериод", "(" + ТекстПодзапросаПериод + ")");
	
	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда

		Если Не ЗначениеЗаполнено(Выборка.Месяц) Тогда
			Возврат;
		КонецЕсли;

		МесяцОбработкиЗаданий = Выборка.Месяц;

		ПараметрыОбработчика.ДанныеЭтапа.ДатаНачалаРасчета = МесяцОбработкиЗаданий;

		ШаблонСостояния = НСтр("ru = 'Требуется выполнение заданий с периода %1';
								|en = 'Perform jobs from the period of %1'",
			ОбщегоНазначения.КодОсновногоЯзыка());

		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеВыполнен(
			ПараметрыОбработчика, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСостояния, РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(МесяцОбработкиЗаданий)));

	КонецЕсли;

КонецПроцедуры

// Определяет пользовательское представление этапа закрытия месяца
// 
// Параметры:
//  ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
//
Процедура Оформление_ОтражениеДвиженийДокументовПоУправленческомуБалансу(ПараметрыОбработчика) Экспорт
	
	ПараметрыОбработчика.ДанныеЭтапа.Наименование = НСтр("ru = 'Отражение движений по управленческому балансу';
														|en = 'Record balance sheet statement entries'");
	ПараметрыОбработчика.ДанныеЭтапа.ТекстВыполнить = НСтр("ru = 'Выполнить';
															|en = 'Run'");
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика);
	
КонецПроцедуры

// Выполняет операции по этапу закрытия месяца
// 
// Параметры:
//  ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
//
Процедура Выполнить_ОтражениеДвиженийДокументовПоУправленческомуБалансу(ПараметрыОбработчика) Экспорт
	
	ОчиститьПроблемыВыполненияРасчета(ПараметрыОбработчика);
	
	ОчередиЗаданий = НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий();
	
	#Область ОбработкаОшибокЗаданийОчереди
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	УстановитьПараметрыЗапросаПоОчередям(Запрос, ОчередиЗаданий);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ЗаданияКОбработке.Документ КАК Документ
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК ЗаданияКОбработке";
	
	ТекстПодзапросаШаблон = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОчереди.Документ КАК Документ
	|ИЗ
	|	&ТаблицаОчереди КАК ТаблицаОчереди
	|ГДЕ
	|	ТаблицаОчереди.Организация В (&МассивОрганизаций)
	|	И ТаблицаОчереди.ПериодЗадания <= &КонецПериода
	|	И &ПолеВидЗадания = ЗНАЧЕНИЕ(Справочник.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу)
	|	И ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			&ТаблицаОчереди КАК ВЗТаблицаОчереди
	|		ГДЕ
	|			ТаблицаОчереди.Документ = ВЗТаблицаОчереди.Документ
	|			И ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					РегистрСведений.ОшибкиОчередейЗаданий КАК РегистрОшибки
	|				ГДЕ
	|					ВЗТаблицаОчереди.ИдентификаторОшибки = РегистрОшибки.ИдентификаторОшибки))";
	
	МассивПодзапросов = Новый Массив;
	Для каждого ТекущаяОчередь Из ОчередиЗаданий.ТаблицаОчередей Цикл
		
		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);
		
		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеВидЗадания", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаВидЗадания), "&" + ТекущаяОчередь.ПараметрЗапросаВидЗадания,
			"ТаблицаОчереди.ВидЗадания"));
			
		МассивПодзапросов.Добавить(ТекстЗапросаОчереди);
	КонецЦикла;

	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
	
	СписокДокументовКОбработкеОшибок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Документ");
	
	Если СписокДокументовКОбработкеОшибок.Количество() > 0 Тогда
		СнятьПризнакВыполненоСОшибкойПоДокументам(СписокДокументовКОбработкеОшибок,
			Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу, ОчередиЗаданий);
	КонецЕсли;
		
	#КонецОбласти
	
	ЗакрытиеМесяцаВыполнитьЗаданияНеоперативнойОчереди(ПараметрыОбработчика, ОчередиЗаданий);

	ПроверитьИЗарегистрироватьПроблемыВыполненияРасчета(ПараметрыОбработчика, ОчередиЗаданий,
		Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

// Заполняет массив типов неразделенных данных, для которых поддерживается сопоставление ссылок
// при загрузке данных в другую информационную базу.
// см. ВыгрузкаЗагрузкаДанныхПереопределяемый.ПриЗаполненииТиповОбщихДанныхПоддерживающихСопоставлениеСсылокПриЗагрузке 
//
Процедура ПриЗаполненииТиповОбщихДанныхПоддерживающихСопоставлениеСсылокПриЗагрузке(Типы) Экспорт
	
	Типы.Добавить(Метаданные.Справочники.ВидыНеоперативныхЗаданий);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИсполнениеНеоперативнойОчередиЗаданий

// Возвращает параметры выполнения заданий.
// 
// Возвращаемое значение:
//  Структура - Параметры выполнения заданий:
// * ИнформационнаяБазаФайловая - Булево - Признак того, что, информационная база запущена в файловом варианте.
// * РежимОтладки - Булево - Признак того, что, информационная база запущена в режиме отладки.
// * Организации - Массив Из СправочникСсылка.Организации - Список организаций. Параметр заполняется только в режиме закрытия месяца.
// * ПериодЗакрытия - Дата - Период закрытия месяца. Параметр заполняется только в режиме закрытия месяца.
// * РежимЗакрытияМесяцаФайловаяИБ - Булево - Признак того, что управляющий поток работает в режиме закрытия месяца и информационная база запущена в файловом варианте.
// * ВыполнениеЗаданийПоСпискуДокументов - Булево - Признак того, что задания по списку документов будут выполняться вне управляющего потока.
// * ТаблицаДокументов - Неопределено, ТаблицаЗначений - Таблица со списком документов к обработке.
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания.
// По переданному списку документов будут выполнены также все виды заданий, от которых зависит выполнение указанного в данном параметре вида задания.
// * ОчередиЗаданий - см. НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий
// * ВыполнятьЗаданияВТранзакцииПроведения - Булево - Признак того, что задания будут выполняться в транзакции проведения документа
// * ОтборПоВидуЗадания - Булево - Признак того, что к выполнению будут получены задания по всем документам, которые соответствуют 
// переданному в свойстве "ВидЗадания" значению, а также все задания, у которых меньший номер выполнения.
// * ВремяЗавершенияРаботыУправляющегоПотока - Дата - Время, после которого управляющий поток должен завершить свою работу.
//
Функция ПараметрыЗапускаВыполненияЗаданий()
	
	ПараметрыЗапускаВыполненияЗаданий = Новый Структура;
	ПараметрыЗапускаВыполненияЗаданий.Вставить("ИнформационнаяБазаФайловая", ОбщегоНазначения.ИнформационнаяБазаФайловая());
	ПараметрыЗапускаВыполненияЗаданий.Вставить("РежимОтладки", ОбщегоНазначения.РежимОтладки());
	ПараметрыЗапускаВыполненияЗаданий.Вставить("Организации", Новый Массив);
	ПараметрыЗапускаВыполненияЗаданий.Вставить("ПериодЗакрытия", Дата(1,1,1));
	ПараметрыЗапускаВыполненияЗаданий.Вставить("РежимЗакрытияМесяцаФайловаяИБ", Ложь);
	ПараметрыЗапускаВыполненияЗаданий.Вставить("ВыполнениеЗаданийПоСпискуДокументов", Ложь);
	ПараметрыЗапускаВыполненияЗаданий.Вставить("ТаблицаДокументов", Неопределено);
	ПараметрыЗапускаВыполненияЗаданий.Вставить("ВидЗадания", Справочники.ВидыНеоперативныхЗаданий.ПустаяСсылка());
	ПараметрыЗапускаВыполненияЗаданий.Вставить("ОчередиЗаданий", НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий());
	ПараметрыЗапускаВыполненияЗаданий.Вставить("ВыполнятьЗаданияВТранзакцииПроведения", Ложь);
	ПараметрыЗапускаВыполненияЗаданий.Вставить("ОтборПоВидуЗадания", Ложь);
	ПараметрыЗапускаВыполненияЗаданий.Вставить("ВремяЗавершенияРаботыУправляющегоПотока", Дата(1, 1, 1, 0, 0, 0));
	
	Возврат ПараметрыЗапускаВыполненияЗаданий;
	
КонецФункции

// Проверяет наличие запущенного экземпляра управляющего потока и в случае его отсутствия - выполняется 
// программный запуск управляющего потока неоперативной очереди заданий.
// 
// Параметры:
//  ПараметрыЗапускаЗаданий - см. ПараметрыЗапускаВыполненияЗаданий
// 
// Возвращаемое значение:
//  Неопределено, ФоновоеЗадание - Возвращает фоновое задание управляющего потока (если запуск был выполнен), либо Неопределено - если
// фоновое задание управляющего потока не было запущено.
Функция ПроверитьЗапуститьИсполнениеНеоперативнойОчередиЗаданий(ПараметрыЗапускаЗаданий = Неопределено)
	
	ФоновоеЗаданиеУправляющегоПотока = Неопределено;
	
	ИмяМетодаУправляющегоПотока = "НеоперативнаяОчередьЗаданий.ВыполнитьЗаданияНеоперативнойОчереди";
	
	Если Не УправляющийПотокЗапущен(ИмяМетодаУправляющегоПотока) Тогда
		
		Попытка
			
			УстановитьПривилегированныйРежим(Истина);
			
			ПричинаИсключения = "ОстановкаРабочихПотоков";
			ИмяМетодаРабочихПотоков = "НеоперативнаяОчередьЗаданий.ВыполнитьОбработкуПорцииФоновымЗаданием";
			Отбор = Новый Структура;
			Отбор.Вставить("ИмяМетода", ИмяМетодаРабочихПотоков);
			Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
			ФоновыеЗаданияРабочийПоток = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
			Для Каждого АктивноеЗадание Из ФоновыеЗаданияРабочийПоток Цикл
				АктивноеЗадание.Отменить();
			КонецЦикла;

			ПричинаИсключения = "ЗапускУправляющегоПотока";
			КлючФоновогоЗадания = "ИсполнениеНеоперативнойОчередиЗаданий";
			НаименованиеФоновогоЗадания = НСтр("ru = 'Исполнение неоперативной очереди заданий';
												|en = 'Backdate job queue execution'");
			ПараметрыФоновогоЗадания = Новый Массив;
			ПараметрыФоновогоЗадания.Добавить(ПараметрыЗапускаЗаданий);
			ФоновоеЗаданиеУправляющегоПотока = ФоновыеЗадания.Выполнить(ИмяМетодаУправляющегоПотока, ПараметрыФоновогоЗадания,
				КлючФоновогоЗадания, НаименованиеФоновогоЗадания);
				
			УстановитьПривилегированныйРежим(Ложь);
			
		Исключение

			ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());

			Если ПричинаИсключения = "ЗапускУправляющегоПотока" Тогда

				Событие = НСтр("ru = 'Запуск управляющего потока неоперативной очереди заданий';
								|en = 'Start the thread-of-control of the backdate job queue'",
					ОбщегоНазначения.КодОсновногоЯзыка());

			Иначе

				Событие = НСтр("ru = 'Завершение работы рабочих потоков неоперативной очереди заданий';
								|en = 'Complete the worker threads of the backdate job queue'",
					ОбщегоНазначения.КодОсновногоЯзыка());

			КонецЕсли;

			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);

		КонецПопытки;
		
	КонецЕсли;

	Возврат ФоновоеЗаданиеУправляющегоПотока;
	
КонецФункции

Функция КоличествоПотоковНеоперативнойОчередиЗаданий()
	
	Если Не РаботаВМоделиСервиса.РазделениеВключено()
		Или Не РаботаВМоделиСервиса.ДоступноИспользованиеРазделенныхДанных() Тогда
			
		УстановитьПривилегированныйРежим(Истина);
		КоличествоПотоков = Константы.КоличествоПотоковНеоперативнойОчередиЗаданий.Получить();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если КоличествоПотоков = 0 Тогда
			КоличествоПотоков = 8;
		КонецЕсли;
		
	Иначе
		КоличествоПотоков = 1;
	КонецЕсли;
	
	Возврат КоличествоПотоков;

КонецФункции

Процедура ОбработатьСтатусыРабочихПотоков(РабочиеПотоки, ОчередиЗаданий)

	Если РабочиеПотоки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИндексРабочегоПотока = РабочиеПотоки.Количество() - 1;

	Пока ИндексРабочегоПотока >= 0 Цикл

		РабочийПоток = РабочиеПотоки[ИндексРабочегоПотока];

		ИдентификаторПотока = РабочийПоток.ИдентификаторПотока;

		Если РабочийПоток.ФоновоеВыполнение Тогда

			ФоновоеЗадание = ФоновыеЗадания.НайтиПоУникальномуИдентификатору(ИдентификаторПотока);

			ЗаданиеВыполнено = Ложь;

			Если ФоновоеЗадание = Неопределено Тогда

				ЗаданиеВыполнено = Истина;

			Иначе

				ФоновоеЗадание = ФоновоеЗадание.ОжидатьЗавершенияВыполнения(1);

				Если Не ФоновоеЗадание.Состояние = СостояниеФоновогоЗадания.Активно Тогда

					ЗаданиеВыполнено = Истина;

				КонецЕсли;

			КонецЕсли;

		Иначе

			ЗаданиеВыполнено = Истина;

		КонецЕсли;
		
		Если ЗаданиеВыполнено Тогда

			СтруктураОтбора = Новый Структура("ИдентификаторПотока", ИдентификаторПотока);
			
			Для Каждого ТекущаяОчередь Из ОчередиЗаданий Цикл
				
				СтрокиКУдалению = ТекущаяОчередь.ГранулыРасчета.НайтиСтроки(СтруктураОтбора);
				Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
					ТекущаяОчередь.ГранулыРасчета.Удалить(СтрокаКУдалению);
				КонецЦикла;
			КонецЦикла;

			РабочиеПотоки.Удалить(ИндексРабочегоПотока);

		КонецЕсли;

		ИндексРабочегоПотока = ИндексРабочегоПотока - 1;

	КонецЦикла;

КонецПроцедуры

Функция ТекстЗапросаМинимальныйПериодОбрабатываемыхЗаданий(ПараметрыВыполненияЗаданий)

	ТаблицаОчередей = ПараметрыВыполненияЗаданий["ОчередиЗаданий"].ТаблицаОчередей;
	
	ТекстыЗапроса = Новый Массив;

#Область ИсключаемыеИзОбработкиДокументы

	МассивПодзапросов = Новый Массив;
	
	Для каждого ТекущаяОчередь Из ТаблицаОчередей Цикл
		ТекстыЗапроса.Добавить(ТекущаяОчередь.ТекстЗапросаГранулыРасчетаВОбработке);
		МассивПодзапросов.Добавить(ТекущаяОчередь.ТекстЗапросаИсключаемыеДокументы);
	КонецЦикла;

	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
	
	ТекстЗапросаОбрабатываемыеДокументы = "
	|ВЫБРАТЬ
	|	ОбрабатываемыеДокументы.Документ КАК Документ,
	|	ОбрабатываемыеДокументы.ВидЗадания КАК ВидЗадания,
	|	ОбрабатываемыеДокументы.НомерОчереди КАК НомерОчереди
	|ПОМЕСТИТЬ ВТ_ОбрабатываемыеДокументы
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК ОбрабатываемыеДокументы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	ВидЗадания,
	|	НомерОчереди";
	ТекстЗапросаОбрабатываемыеДокументы = СтрЗаменить(ТекстЗапросаОбрабатываемыеДокументы, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");

	ТекстыЗапроса.Добавить(ТекстЗапросаОбрабатываемыеДокументы);
	
	ТекстМинимальныйНомерОчередиОбрабатываемыеДокументы = "
	|ВЫБРАТЬ
	|	ОбрабатываемыеДокументы.Документ КАК Документ,
	|	МИНИМУМ(ОбрабатываемыеДокументы.НомерОчереди) КАК МинимальныйНомерОчереди
	|ПОМЕСТИТЬ ВТ_ОбрабатываемыеДокументыМинОчередь
	|ИЗ
	|	ВТ_ОбрабатываемыеДокументы КАК ОбрабатываемыеДокументы
	|	
	|СГРУППИРОВАТЬ ПО
	|	ОбрабатываемыеДокументы.Документ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ";

	ТекстыЗапроса.Добавить(ТекстМинимальныйНомерОчередиОбрабатываемыеДокументы);
	
	ТекстЗапросаИсключаемыеДокументы = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИсключаемыеДокументы.Документ КАК Документ,
	|	ИсключаемыеДокументы.ВидЗадания КАК ВидЗадания
	|ПОМЕСТИТЬ ВТ_ИсключаемыеДокументы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОбрабатываемыеДокументы.Документ КАК Документ,
	|		ВидыНеоперативныхЗаданий.Ссылка КАК ВидЗадания
	|	ИЗ
	|		ВТ_ОбрабатываемыеДокументыМинОчередь КАК ОбрабатываемыеДокументы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыНеоперативныхЗаданий КАК ВидыНеоперативныхЗаданий
	|			ПО ОбрабатываемыеДокументы.МинимальныйНомерОчереди < ВидыНеоперативныхЗаданий.НомерОчереди
	|			И ВидыНеоперативныхЗаданий.НомерОчереди <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ОбрабатываемыеДокументы.Документ КАК Документ,
	|		ОбрабатываемыеДокументы.ВидЗадания КАК ВидЗадания
	|	ИЗ
	|		ВТ_ОбрабатываемыеДокументы КАК ОбрабатываемыеДокументы) КАК ИсключаемыеДокументы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	ВидЗадания";

	ТекстыЗапроса.Добавить(ТекстЗапросаИсключаемыеДокументы);
	
#КонецОбласти

#Область ДокументыСОшибками

	ТекстДокументыСОшибками = "
	|ВЫБРАТЬ
	|	ОшибкиПоДокументам.Документ КАК Документ,
	|	МИНИМУМ(ОшибкиПоДокументам.НомерОчереди) КАК НомерОчереди
	|ПОМЕСТИТЬ ВТ_ОшибкиПоДокументам
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК ОшибкиПоДокументам
	|СГРУППИРОВАТЬ ПО
	|	ОшибкиПоДокументам.Документ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	НомерОчереди";
	
	ТекстПодзапросаШаблон = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОчереди.Документ КАК Документ,
	|	&ПолеНомерОчереди КАК НомерОчереди
	|ИЗ
	|	&ТаблицаОчереди КАК ТаблицаОчереди
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиОчередейЗаданий КАК РегистрОшибки
	|		ПО ТаблицаОчереди.ИдентификаторОшибки = РегистрОшибки.ИдентификаторОшибки";
	
	МассивПодзапросов = Новый Массив;
	
	Для Каждого ТекущаяОчередь Из ТаблицаОчередей Цикл

		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);

		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеНомерОчереди", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаНомерОчереди), "&" + ТекущаяОчередь.ПараметрЗапросаНомерОчереди,
			"ТаблицаОчереди.ВидЗадания.НомерОчереди"));

		МассивПодзапросов.Добавить(ТекстЗапросаОчереди);

	КонецЦикла;
	
	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
	ТекстДокументыСОшибками = СтрЗаменить(ТекстДокументыСОшибками, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");

	ТекстыЗапроса.Добавить(ТекстДокументыСОшибками);
	
#КонецОбласти

#Область ЗаданияСУчетомПриоритетовВыполнения
	
	Если Не ПараметрыВыполненияЗаданий.ОтборПоВидуЗадания Тогда
			
		ТекстЗапросаРежимЗакрытияМесяца = "
		|ВЫБРАТЬ
		|	ПараметрыЗакрытияМесяца.Организация КАК Организация,
		|	МАКСИМУМ(КОНЕЦПЕРИОДА(ПараметрыЗакрытияМесяца.ПериодЗадания, МЕСЯЦ)) КАК ПериодЗакрытияКонец,
		|	МИНИМУМ(НАЧАЛОПЕРИОДА(ПараметрыЗакрытияМесяца.ПериодЗадания, МЕСЯЦ)) КАК ПериодЗакрытияНачало
		|ПОМЕСТИТЬ ВТ_ПараметрыЗакрытияМесяца
		|ИЗ
		|	РегистрСведений.ПриоритетныеЗаданияНеоперативнойОчереди КАК ПараметрыЗакрытияМесяца
		|ГДЕ
		|	&УсловиеРежимаЗакрытияМесяца
		|СГРУППИРОВАТЬ ПО
		|	ПараметрыЗакрытияМесяца.Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПериодЗакрытияКонец,
		|	ПериодЗакрытияНачало,
		|	Организация
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПараметрыЗакрытияМесяца.Организация КАК Организация,
		|	ПараметрыЗакрытияМесяца.ПериодЗакрытияНачало КАК ПериодЗакрытияНачало,
		|	ПараметрыЗакрытияМесяца.ПериодЗакрытияКонец КАК ПериодЗакрытияКонец
		|ИЗ
		|	ВТ_ПараметрыЗакрытияМесяца КАК ПараметрыЗакрытияМесяца
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(ВТ_ПараметрыЗакрытияМесяца.ПериодЗакрытияНачало) КАК ПериодМинимальный,
		|	МАКСИМУМ(ВТ_ПараметрыЗакрытияМесяца.ПериодЗакрытияКонец) КАК ПериодМаксимальный
		|ИЗ
		|	ВТ_ПараметрыЗакрытияМесяца КАК ВТ_ПараметрыЗакрытияМесяца
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(Таблица.РежимЗакрытияМесяца) КАК РежимЗакрытияМесяца
		|ИЗ
		|	&ТекстВложенногоЗапроса КАК Таблица";
		
		ТекстПодзапросаШаблон = "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|		ИСТИНА КАК РежимЗакрытияМесяца
		|	ИЗ
		|		&ТаблицаОчереди КАК ТаблицаОчереди
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсключаемыеДокументы КАК ИсключаемыеДокументы
		|			ПО ТаблицаОчереди.Документ = ИсключаемыеДокументы.Документ
		|				И &ПолеВидЗадания = ИсключаемыеДокументы.ВидЗадания
		|			
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОшибкиПоДокументам КАК ЗапретВыполненияЗаданий
		|			ПО ТаблицаОчереди.Документ = ЗапретВыполненияЗаданий.Документ
		|			И &ПолеНомерОчереди > ЗапретВыполненияЗаданий.НомерОчереди
		|
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПараметрыЗакрытияМесяца КАК ВТ_ПараметрыЗакрытияМесяца
		|			ПО ТаблицаОчереди.Организация = ВТ_ПараметрыЗакрытияМесяца.Организация
		|			И ТаблицаОчереди.ПериодЗадания
		|				МЕЖДУ ВТ_ПараметрыЗакрытияМесяца.ПериодЗакрытияНачало И ВТ_ПараметрыЗакрытияМесяца.ПериодЗакрытияКонец
		|	ГДЕ
		|		ИсключаемыеДокументы.Документ ЕСТЬ NULL
		|		И ЗапретВыполненияЗаданий.Документ ЕСТЬ NULL
		|		И НЕ ТаблицаОчереди.ВыполненоСОшибкой";
		
		МассивПодзапросов = Новый Массив;
		Для каждого ТекущаяОчередь Из ТаблицаОчередей Цикл
			ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);
			ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеНомерОчереди", ?(ЗначениеЗаполнено(
				ТекущаяОчередь.ПараметрЗапросаНомерОчереди), "&" + ТекущаяОчередь.ПараметрЗапросаНомерОчереди,
				"ТаблицаОчереди.ВидЗадания.НомерОчереди"));
			ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеВидЗадания", ?(ЗначениеЗаполнено(
				ТекущаяОчередь.ПараметрЗапросаВидЗадания), "&" + ТекущаяОчередь.ПараметрЗапросаВидЗадания,
				"ТаблицаОчереди.ВидЗадания"));
			МассивПодзапросов.Добавить(ТекстЗапросаОчереди);
		КонецЦикла;
		МассивПодзапросов.Добавить("ВЫБРАТЬ ЛОЖЬ КАК РежимЗакрытияМесяца");
		ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		ТекстЗапросаРежимЗакрытияМесяца = СтрЗаменить(ТекстЗапросаРежимЗакрытияМесяца, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
		
		Если ПараметрыВыполненияЗаданий.РежимЗакрытияМесяцаФайловаяИБ Тогда
	
			УсловиеРежимаЗакрытияМесяца = "ПараметрыЗакрытияМесяца.Организация В (&Организации) 
			|И ПараметрыЗакрытияМесяца.ПериодЗадания <= &ПериодЗакрытия";
			
		Иначе
			УсловиеРежимаЗакрытияМесяца = "ИСТИНА";
		КонецЕсли;
		
		ТекстЗапросаРежимЗакрытияМесяца = СтрЗаменить(ТекстЗапросаРежимЗакрытияМесяца, "&УсловиеРежимаЗакрытияМесяца",
			УсловиеРежимаЗакрытияМесяца);
		
		ТекстыЗапроса.Добавить(ТекстЗапросаРежимЗакрытияМесяца);
		
	Иначе
		
		ТекстЗапросаРежимЗакрытияМесяца = "
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК РежимЗакрытияМесяца";
		
		ТекстыЗапроса.Добавить(ТекстЗапросаРежимЗакрытияМесяца);
		
	КонецЕсли;
	
#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаОтбораЗаданийНеоперативнойОчередиКВыполнению(ПараметрыВыполненияЗаданий, РежимЗакрытияМесяца = Ложь)

	ТаблицаОчередей = ПараметрыВыполненияЗаданий["ОчередиЗаданий"].ТаблицаОчередей;
	
	МассивТекстовЗапросов = Новый Массив;
	
	Если ПараметрыВыполненияЗаданий.ОтборПоВидуЗадания Тогда
		
		ТекстЗапросаВидыЗаданий = "
		|ВЫБРАТЬ
		|	ВидыНеоперативныхЗаданий.Ссылка КАК ВидЗадания
		|ПОМЕСТИТЬ ВТ_ОтборПоВидуЗадания
		|ИЗ
		|	Справочник.ВидыНеоперативныхЗаданий КАК ВидыНеоперативныхЗаданий
		|ГДЕ
		|	ВидыНеоперативныхЗаданий.Ссылка = &ВидЗадания
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВидыНеоперативныхЗаданий.Ссылка КАК ВидЗадания
		|ИЗ
		|	Справочник.ВидыНеоперативныхЗаданий КАК ВидыНеоперативныхЗаданий
		|ГДЕ
		|	ВидыНеоперативныхЗаданий.НомерОчереди < ВЫРАЗИТЬ(&ВидЗадания КАК Справочник.ВидыНеоперативныхЗаданий).НомерОчереди";
		
		МассивТекстовЗапросов.Добавить(ТекстЗапросаВидыЗаданий);
		
	КонецЕсли; 

#Область МинимальныйПериодДень
	
	ТекстЗапросаМинимальныйПериод = "
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(МИНИМУМ(ЗаданияКОбработке.ПериодЗадания), ДЕНЬ) КАК НачалоПериода,
	|	КОНЕЦПЕРИОДА(МИНИМУМ(ЗаданияКОбработке.ПериодЗадания), ДЕНЬ) КАК КонецПериода
	|ПОМЕСТИТЬ ВТ_МинимальныйПериодДень
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК ЗаданияКОбработке
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НачалоПериода,
	|	КонецПериода
	|";
	
	Если РежимЗакрытияМесяца Тогда
		ТекстПодзапросаШаблон = "
		|ВЫБРАТЬ
		|	ВложеннаяТаблица.ПериодЗадания КАК ПериодЗадания
		|ИЗ
		|	(ВЫБРАТЬ ПЕРВЫЕ 1
		|		ТаблицаОчереди.ПериодЗадания КАК ПериодЗадания
		|	ИЗ
		|		&ТаблицаОчереди КАК ТаблицаОчереди
		|			
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсключаемыеДокументы КАК ИсключаемыеДокументы
		|			ПО ТаблицаОчереди.Документ = ИсключаемыеДокументы.Документ
		|			И &ПолеВидЗадания = ИсключаемыеДокументы.ВидЗадания
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОшибкиПоДокументам КАК ЗапретВыполненияЗаданий
		|			ПО ТаблицаОчереди.Документ = ЗапретВыполненияЗаданий.Документ
		|			И &ПолеНомерОчереди > ЗапретВыполненияЗаданий.НомерОчереди
		
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПараметрыЗакрытияМесяца КАК ВТ_ПараметрыЗакрытияМесяца
		|			ПО ТаблицаОчереди.Организация = ВТ_ПараметрыЗакрытияМесяца.Организация
		|			И ТаблицаОчереди.ПериодЗадания
		|				МЕЖДУ ВТ_ПараметрыЗакрытияМесяца.ПериодЗакрытияНачало И ВТ_ПараметрыЗакрытияМесяца.ПериодЗакрытияКонец
		|	ГДЕ
		|		ИсключаемыеДокументы.Документ ЕСТЬ NULL
		|		И ЗапретВыполненияЗаданий.Документ ЕСТЬ NULL
		|		И ТаблицаОчереди.ПериодЗадания МЕЖДУ &ЗакрытиеМесяцаНачалоПериода И &ЗакрытиеМесяцаКонецПериода
		|		И ТаблицаОчереди.Организация В (&Организации)
		|		И ТаблицаОчереди.ВыполненоСОшибкой = ЛОЖЬ
		|
		|	УПОРЯДОЧИТЬ ПО
		|		ПериодЗадания) КАК ВложеннаяТаблица";
	Иначе
		
		ТекстПодзапросаШаблон = "
		|ВЫБРАТЬ
		|	ВложеннаяТаблица.ПериодЗадания КАК ПериодЗадания
		|ИЗ
		|	(ВЫБРАТЬ ПЕРВЫЕ 1
		|		ТаблицаОчереди.ПериодЗадания КАК ПериодЗадания
		|	ИЗ
		|		&ТаблицаОчереди КАК ТаблицаОчереди
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсключаемыеДокументы КАК ИсключаемыеДокументы
		|			ПО ТаблицаОчереди.Документ = ИсключаемыеДокументы.Документ
		|			И &ПолеВидЗадания = ИсключаемыеДокументы.ВидЗадания
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОшибкиПоДокументам КАК ЗапретВыполненияЗаданий
		|			ПО ТаблицаОчереди.Документ = ЗапретВыполненияЗаданий.Документ
		|			И &ПолеНомерОчереди > ЗапретВыполненияЗаданий.НомерОчереди
		|	ГДЕ
		|		ИсключаемыеДокументы.Документ ЕСТЬ NULL
		|		И ЗапретВыполненияЗаданий.Документ ЕСТЬ NULL
		|		И ТаблицаОчереди.ВыполненоСОшибкой = ЛОЖЬ
		|		И &ОтборПоВидуЗадания
		|
		|	УПОРЯДОЧИТЬ ПО
		|		ПериодЗадания) КАК ВложеннаяТаблица";
	
	КонецЕсли;
	
	Если ПараметрыВыполненияЗаданий.ОтборПоВидуЗадания Тогда
		ТекстПодзапросаШаблон = СтрЗаменить(ТекстПодзапросаШаблон, "&ОтборПоВидуЗадания",
			"&ПолеВидЗадания В (ВЫБРАТЬ ВТ_ОтборПоВидуЗадания.ВидЗадания ИЗ ВТ_ОтборПоВидуЗадания КАК ВТ_ОтборПоВидуЗадания)");
	Иначе
		ТекстПодзапросаШаблон = СтрЗаменить(ТекстПодзапросаШаблон, "&ОтборПоВидуЗадания", "ИСТИНА");
	КонецЕсли;
	
	МассивПодзапросов = Новый Массив;
	Для каждого ТекущаяОчередь Из ТаблицаОчередей Цикл
		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);
		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеНомерОчереди", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаНомерОчереди), "&" + ТекущаяОчередь.ПараметрЗапросаНомерОчереди,
			"ТаблицаОчереди.ВидЗадания.НомерОчереди"));
		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеВидЗадания", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаВидЗадания), "&" + ТекущаяОчередь.ПараметрЗапросаВидЗадания,
			"ТаблицаОчереди.ВидЗадания"));
		МассивПодзапросов.Добавить(ТекстЗапросаОчереди);
	КонецЦикла;
	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстЗапросаМинимальныйПериод = СтрЗаменить(ТекстЗапросаМинимальныйПериод, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
	МассивТекстовЗапросов.Добавить(ТекстЗапросаМинимальныйПериод);
#КонецОбласти

#Область ДокументыВМинимальномДне
	ТекстЗапросаЗаданияПоДокументамКОбработке="
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданияКОбработке.Документ КАК Документ,
	|	ЗаданияКОбработке.НомерОчереди КАК НомерОчереди,
	|	ЗаданияКОбработке.ВидЗадания КАК ВидЗадания
	|ПОМЕСТИТЬ ВТ_ЗаданияПоДокументамКОбработке
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК ЗаданияКОбработке
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	ВидЗадания
	|";
	
	Если РежимЗакрытияМесяца Тогда
		
		ТекстПодзапросаШаблон = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаОчереди.Документ КАК Документ,
		|	&ПолеНомерОчереди КАК НомерОчереди,
		|	&ПолеВидЗадания КАК ВидЗадания
		|ИЗ
		|	&ТаблицаОчереди КАК ТаблицаОчереди
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_МинимальныйПериодДень КАК МинимальныйПериод
		|		ПО ТаблицаОчереди.ПериодЗадания МЕЖДУ МинимальныйПериод.НачалоПериода И МинимальныйПериод.КонецПериода
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсключаемыеДокументы КАК ИсключаемыеДокументы
		|		ПО ТаблицаОчереди.Документ = ИсключаемыеДокументы.Документ
		|		И &ПолеВидЗадания = ИсключаемыеДокументы.ВидЗадания
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОшибкиПоДокументам КАК ЗапретВыполненияЗаданий
		|		ПО ТаблицаОчереди.Документ = ЗапретВыполненияЗаданий.Документ
		|		И &ПолеНомерОчереди > ЗапретВыполненияЗаданий.НомерОчереди
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПараметрыЗакрытияМесяца КАК ВТ_ПараметрыЗакрытияМесяца
		|		ПО ТаблицаОчереди.Организация = ВТ_ПараметрыЗакрытияМесяца.Организация
		|		И ТаблицаОчереди.ПериодЗадания
		|			МЕЖДУ ВТ_ПараметрыЗакрытияМесяца.ПериодЗакрытияНачало И ВТ_ПараметрыЗакрытияМесяца.ПериодЗакрытияКонец
		|ГДЕ
		|	ИсключаемыеДокументы.Документ ЕСТЬ NULL
		|	И ЗапретВыполненияЗаданий.Документ ЕСТЬ NULL
		|	И ТаблицаОчереди.Организация В (&Организации)
		|	И ТаблицаОчереди.ВыполненоСОшибкой = ЛОЖЬ";
		
	Иначе
		
		ТекстПодзапросаШаблон = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаОчереди.Документ КАК Документ,
		|	&ПолеНомерОчереди КАК НомерОчереди,
		|	&ПолеВидЗадания КАК ВидЗадания
		|ИЗ
		|	&ТаблицаОчереди КАК ТаблицаОчереди
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_МинимальныйПериодДень КАК МинимальныйПериод
		|		ПО ТаблицаОчереди.ПериодЗадания МЕЖДУ МинимальныйПериод.НачалоПериода И МинимальныйПериод.КонецПериода
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИсключаемыеДокументы КАК ИсключаемыеДокументы
		|		ПО ТаблицаОчереди.Документ = ИсключаемыеДокументы.Документ
		|		И &ПолеВидЗадания = ИсключаемыеДокументы.ВидЗадания
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОшибкиПоДокументам КАК ЗапретВыполненияЗаданий
		|		ПО ТаблицаОчереди.Документ = ЗапретВыполненияЗаданий.Документ
		|		И &ПолеНомерОчереди > ЗапретВыполненияЗаданий.НомерОчереди
		|
		|ГДЕ
		|	ИсключаемыеДокументы.Документ ЕСТЬ NULL
		|	И ЗапретВыполненияЗаданий.Документ ЕСТЬ NULL
		|	И ТаблицаОчереди.ВыполненоСОшибкой = ЛОЖЬ
		|	И &ОтборПоВидуЗадания";	
				
	КонецЕсли;
	
	Если ПараметрыВыполненияЗаданий.ОтборПоВидуЗадания Тогда
		ТекстПодзапросаШаблон = СтрЗаменить(ТекстПодзапросаШаблон, "&ОтборПоВидуЗадания",
			"&ПолеВидЗадания В (ВЫБРАТЬ ВТ_ОтборПоВидуЗадания.ВидЗадания ИЗ ВТ_ОтборПоВидуЗадания КАК ВТ_ОтборПоВидуЗадания)");
	Иначе
		ТекстПодзапросаШаблон = СтрЗаменить(ТекстПодзапросаШаблон, "&ОтборПоВидуЗадания", "ИСТИНА");
	КонецЕсли;
	
	МассивПодзапросов = Новый Массив;
	
	Для каждого ТекущаяОчередь Из ТаблицаОчередей Цикл
		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);

		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеНомерОчереди", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаНомерОчереди), "&" + ТекущаяОчередь.ПараметрЗапросаНомерОчереди,
			"ТаблицаОчереди.ВидЗадания.НомерОчереди"));

		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеВидЗадания", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаВидЗадания), "&" + ТекущаяОчередь.ПараметрЗапросаВидЗадания,
			"ТаблицаОчереди.ВидЗадания"));

		МассивПодзапросов.Добавить(ТекстЗапросаОчереди);
	КонецЦикла;
	
	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстЗапросаЗаданияПоДокументамКОбработке = СтрЗаменить(ТекстЗапросаЗаданияПоДокументамКОбработке, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
	МассивТекстовЗапросов.Добавить(ТекстЗапросаЗаданияПоДокументамКОбработке);
#КонецОбласти

#Область ЗаданияПоВыбраннымДокументам

ТекстЗапросаЗаданияПоДокументам ="
	|ВЫБРАТЬ
	|	ЗаданияКОбработке.ПериодЗадания КАК ПериодЗадания,
	|	ЗаданияКОбработке.ВидЗадания КАК ВидЗадания,
	|	ЗаданияКОбработке.Документ КАК Документ
	|ПОМЕСТИТЬ ВТ_ДокументыВидыЗаданий
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК ЗаданияКОбработке
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидЗадания";
	
	ТекстПодзапросаШаблон = "
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаОчереди.ПериодЗадания) КАК ПериодЗадания,
	|	&ПолеВидЗадания КАК ВидЗадания,
	|	ТаблицаОчереди.Документ КАК Документ
	|ИЗ
	|	&ТаблицаОчереди КАК ТаблицаОчереди
	|ГДЕ
	|	ТаблицаОчереди.ВыполненоСОшибкой = ЛОЖЬ
	|	И ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ВТ_ЗаданияПоДокументамКОбработке КАК ЗаданияПоДокументамКОбработке
	|		ГДЕ
	|			ТаблицаОчереди.Документ = ЗаданияПоДокументамКОбработке.Документ
	|			И &ПолеВидЗадания = ЗаданияПоДокументамКОбработке.ВидЗадания)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОчереди.Документ,
	|	&ПолеВидЗадания";
	
	МассивПодзапросов = Новый Массив;
	
	Для Каждого ТекущаяОчередь Из ТаблицаОчередей Цикл
		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);

		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеВидЗадания", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаВидЗадания), "&" + ТекущаяОчередь.ПараметрЗапросаВидЗадания,
			"ТаблицаОчереди.ВидЗадания"));
		МассивПодзапросов.Добавить(ТекстЗапросаОчереди);
	КонецЦикла;
	
	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
	ТекстЗапросаЗаданияПоДокументам = СтрЗаменить(ТекстЗапросаЗаданияПоДокументам, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
	МассивТекстовЗапросов.Добавить(ТекстЗапросаЗаданияПоДокументам);
	
	ТекстЗапросаДокументыВидыЗаданийКОбработке ="
	|ВЫБРАТЬ
	|	ТаблицаОчереди.ПериодЗадания КАК ПериодЗадания,
	|	ТаблицаОчереди.ВидЗадания КАК ВидЗадания,
	|	ТаблицаОчереди.Документ КАК Документ,
	|	ТаблицаОчереди.ВидЗадания.НомерОчереди КАК НомерОчереди
	|ПОМЕСТИТЬ ВТ_ДокументыВидыЗаданийКОбработке
	|ИЗ
	|	ВТ_ДокументыВидыЗаданий КАК ТаблицаОчереди
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	НомерОчереди,
	|	ВидЗадания";
	МассивТекстовЗапросов.Добавить(ТекстЗапросаДокументыВидыЗаданийКОбработке);
	
#КонецОбласти
	
	ТекстЗапросаЗаданияПоДокументам ="
	|ВЫБРАТЬ
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(ТаблицаОчереди.ПериодЗадания, ДЕНЬ)) КАК ПериодЗаданияДень,
	|	ТаблицаОчереди.ВидЗадания КАК ВидЗадания,
	|	ТаблицаОчереди.Документ КАК Документ,
	|	ТаблицаОчереди.НомерОчереди КАК НомерОчереди
	|ПОМЕСТИТЬ ВТ_ЗаданияПоДокументам
	|ИЗ
	|	ВТ_ДокументыВидыЗаданийКОбработке КАК ТаблицаОчереди
	|ГДЕ
	|	ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ВТ_ДокументыВидыЗаданийКОбработке КАК ДокументыВидыЗаданий
	|		ГДЕ
	|			ТаблицаОчереди.Документ = ДокументыВидыЗаданий.Документ
	|			И ТаблицаОчереди.НомерОчереди <= ДокументыВидыЗаданий.НомерОчереди)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОчереди.ВидЗадания,
	|	ТаблицаОчереди.Документ,
	|	ТаблицаОчереди.НомерОчереди
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПериодЗаданияДень,
	|	ВидЗадания,
	|	НомерОчереди";
	
	МассивТекстовЗапросов.Добавить(ТекстЗапросаЗаданияПоДокументам);

	ТекстЗапросаПервыйМинимальныйДень="
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	МинимальныйДень.НомерОчереди КАК НомерОчереди,
	|	МинимальныйДень.ПериодЗаданияДень КАК ПериодЗаданияДень,
	|	МинимальныйДень.ВидЗадания КАК ВидЗадания
	|ПОМЕСТИТЬ ВТ_ОчередьМинимальныйДень
	|ИЗ
	|	(ВЫБРАТЬ
	|		МИНИМУМ(ВТ_ЗаданияПоДокументам.ПериодЗаданияДень) КАК ПериодЗаданияДень,
	|		ВТ_ЗаданияПоДокументам.ВидЗадания КАК ВидЗадания,
	|		ВТ_ЗаданияПоДокументам.НомерОчереди КАК НомерОчереди
	|	ИЗ
	|		ВТ_ЗаданияПоДокументам КАК ВТ_ЗаданияПоДокументам
	|	СГРУППИРОВАТЬ ПО
	|		ВТ_ЗаданияПоДокументам.ВидЗадания,
	|		ВТ_ЗаданияПоДокументам.НомерОчереди) КАК МинимальныйДень
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерОчереди,
	|	ПериодЗаданияДень
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПериодЗаданияДень,
	|	НомерОчереди,
	|	ВидЗадания";
	
	МассивТекстовЗапросов.Добавить(ТекстЗапросаПервыйМинимальныйДень);
	
	ТекстЗапросаИтоговый = "
	|ВЫБРАТЬ
	|	ВТ_ЗаданияПоДокументам.ВидЗадания КАК ВидЗадания,
	|	ВТ_ЗаданияПоДокументам.ПериодЗаданияДень КАК ПериодЗаданияДень,
	|	ВТ_ЗаданияПоДокументам.Документ КАК Документ
	|ИЗ
	|	ВТ_ЗаданияПоДокументам КАК ВТ_ЗаданияПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОчередьМинимальныйДень КАК МинимальныйДень
	|		ПО ВТ_ЗаданияПоДокументам.ПериодЗаданияДень = МинимальныйДень.ПериодЗаданияДень
	|		И ВТ_ЗаданияПоДокументам.НомерОчереди = МинимальныйДень.НомерОчереди
	|		И ВТ_ЗаданияПоДокументам.ВидЗадания = МинимальныйДень.ВидЗадания
	|ИТОГИ
	|ПО
	|	ВидЗадания,
	|	ПериодЗаданияДень";
	МассивТекстовЗапросов.Добавить(ТекстЗапросаИтоговый);
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаОтбораЗаданийНеоперативнойОчередиКВыполнениюПоСпискуДокументов(СписокДокументов, ВидЗадания,
	ОчередиЗаданий, ВыполнениеВТранзакцииПроведения = Ложь)
	
	ТаблицаОчередей = ОчередиЗаданий.ТаблицаОчередей;
	
	ТекстыЗапроса = Новый Массив;

#Область ДокументыКОбработке

	Если СписокДокументов.Количество() > 0 Тогда
		
		ТекстЗапросаДокументы = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументыКОтражению.Документ КАК Документ
		|ПОМЕСТИТЬ ВТ_ДокументыКОтражению
		|ИЗ
		|	&ДокументыКОтражению КАК ДокументыКОтражению
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ";
		
	Иначе

		ТекстЗапросаДокументы = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Задания.Документ КАК Документ
		|ПОМЕСТИТЬ ВТ_ДокументыКОтражению
		|ИЗ
		|	&ТекстВложенногоЗапроса КАК Задания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ";
		
		ТекстПодзапросаШаблон = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаОчереди.Документ КАК Документ
		|ИЗ
		|	&ТаблицаОчереди КАК ТаблицаОчереди
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиОчередейЗаданий КАК РегистрОшибки
		|	ПО ТаблицаОчереди.ИдентификаторОшибки = РегистрОшибки.ИдентификаторОшибки
		|ГДЕ
		|	&ПолеВидЗадания = &ВидЗадания
		|	И РегистрОшибки.ИдентификаторОшибки ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаОчереди.Документ КАК Документ
		|ИЗ
		|	&ТаблицаОчереди КАК ТаблицаОчереди
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиОчередейЗаданий КАК РегистрОшибки
		|	ПО ТаблицаОчереди.ИдентификаторОшибки = РегистрОшибки.ИдентификаторОшибки
		|ГДЕ
		|	&ПолеНомерОчереди < ВЫРАЗИТЬ(&ВидЗадания КАК Справочник.ВидыНеоперативныхЗаданий).НомерОчереди
		|	И РегистрОшибки.ИдентификаторОшибки ЕСТЬ NULL";
		
		МассивПодзапросов = Новый Массив;
		
		Для Каждого ТекущаяОчередь Из ТаблицаОчередей Цикл

			ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);
			
			ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеВидЗадания", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаВидЗадания), "&" + ТекущаяОчередь.ПараметрЗапросаВидЗадания,
				"ТаблицаОчереди.ВидЗадания"));
				
			ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеНомерОчереди", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаНомерОчереди), "&" + ТекущаяОчередь.ПараметрЗапросаНомерОчереди,
				"ТаблицаОчереди.ВидЗадания.НомерОчереди"));
				
			МассивПодзапросов.Добавить(ТекстЗапросаОчереди);

		КонецЦикла;
		
		ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
		ТекстЗапросаДокументы = СтрЗаменить(ТекстЗапросаДокументы, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
		
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапросаДокументы);
	
#Область ДокументыСОшибками
	
	Если Не ВыполнениеВТранзакцииПроведения Тогда
		
		ТекстДокументыСОшибками = "
		|ВЫБРАТЬ
		|	ОшибкиПоДокументам.Документ КАК Документ,
		|	МИНИМУМ(ОшибкиПоДокументам.НомерОчереди) КАК НомерОчереди
		|ПОМЕСТИТЬ ВТ_ОшибкиПоДокументам
		|ИЗ
		|	&ТекстВложенногоЗапроса КАК ОшибкиПоДокументам
		|СГРУППИРОВАТЬ ПО
		|	ОшибкиПоДокументам.Документ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ,
		|	НомерОчереди";
		
		ТекстПодзапросаШаблон = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаОчереди.Документ КАК Документ,
		|	&ПолеНомерОчереди КАК НомерОчереди
		|ИЗ
		|	&ТаблицаОчереди КАК ТаблицаОчереди
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДокументыКОтражению КАК ДокументыКОтражению
		|		ПО ТаблицаОчереди.Документ = ДокументыКОтражению.Документ
		|ГДЕ
		|	ТаблицаОчереди.ВыполненоСОшибкой = ИСТИНА";
		
		МассивПодзапросов = Новый Массив;
		
		Для Каждого ТекущаяОчередь Из ТаблицаОчередей Цикл
	
			ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);
	
			ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеНомерОчереди", ?(ЗначениеЗаполнено(
				ТекущаяОчередь.ПараметрЗапросаНомерОчереди), "&" + ТекущаяОчередь.ПараметрЗапросаНомерОчереди,
				"ТаблицаОчереди.ВидЗадания.НомерОчереди"));
	
			МассивПодзапросов.Добавить(ТекстЗапросаОчереди);
	
		КонецЦикла;
		
		ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
		ТекстДокументыСОшибками = СтрЗаменить(ТекстДокументыСОшибками, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
	
		ТекстыЗапроса.Добавить(ТекстДокументыСОшибками);
	
	КонецЕсли;
	
#КонецОбласти
	
	ТекстЗапросаДокументыКОбработке = "
	|ВЫБРАТЬ
	|	МИНИМУМ(ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ПериодЗаданияДень,
	|	ЗаданияКОбработке.Документ КАК Документ,
	|	ЗаданияКОбработке.НомерОчереди КАК НомерОчереди,
	|	ЗаданияКОбработке.ВидЗадания КАК ВидЗадания
	|ПОМЕСТИТЬ ВТ_ЗаданияПоДокументам
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК ЗаданияКОбработке
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаданияКОбработке.Документ,
	|	ЗаданияКОбработке.НомерОчереди,
	|	ЗаданияКОбработке.ВидЗадания
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидЗадания,
	|	Документ
	|";
	
	Если Не ВыполнениеВТранзакцииПроведения Тогда
		ТекстПодзапросаШаблон = "
		|ВЫБРАТЬ
		|	ТаблицаОчереди.Документ КАК Документ,
		|	&ПолеНомерОчереди КАК НомерОчереди,
		|	&ПолеВидЗадания КАК ВидЗадания
		|ИЗ
		|	&ТаблицаОчереди КАК ТаблицаОчереди
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДокументыКОтражению КАК ДокументыКОтражению
		|		ПО ТаблицаОчереди.Документ = ДокументыКОтражению.Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОшибкиПоДокументам КАК ЗапретВыполненияЗаданий
		|		ПО ТаблицаОчереди.Документ = ЗапретВыполненияЗаданий.Документ
		|		И &ПолеНомерОчереди > ЗапретВыполненияЗаданий.НомерОчереди
		|ГДЕ
		|	ЗапретВыполненияЗаданий.Документ ЕСТЬ NULL
		|	И &ПолеНомерОчереди < ВЫРАЗИТЬ(&ВидЗадания КАК Справочник.ВидыНеоперативныхЗаданий).НомерОчереди
		|	И НЕ ТаблицаОчереди.ВыполненоСОшибкой
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаОчереди.Документ КАК Документ,
		|	&ПолеНомерОчереди КАК НомерОчереди,
		|	&ПолеВидЗадания КАК ВидЗадания
		|ИЗ
		|	&ТаблицаОчереди КАК ТаблицаОчереди
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДокументыКОтражению КАК ДокументыКОтражению
		|		ПО ТаблицаОчереди.Документ = ДокументыКОтражению.Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОшибкиПоДокументам КАК ЗапретВыполненияЗаданий
		|		ПО ТаблицаОчереди.Документ = ЗапретВыполненияЗаданий.Документ
		|		И &ПолеНомерОчереди > ЗапретВыполненияЗаданий.НомерОчереди
		|ГДЕ
		|	ЗапретВыполненияЗаданий.Документ ЕСТЬ NULL
		|	И &ПолеВидЗадания = &ВидЗадания
		|	И НЕ ТаблицаОчереди.ВыполненоСОшибкой";
	Иначе
		ТекстПодзапросаШаблон = "
		|ВЫБРАТЬ
		|	ТаблицаОчереди.Документ КАК Документ,
		|	&ПолеНомерОчереди КАК НомерОчереди,
		|	&ПолеВидЗадания КАК ВидЗадания
		|ИЗ
		|	&ТаблицаОчереди КАК ТаблицаОчереди
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДокументыКОтражению КАК ДокументыКОтражению
		|		ПО ТаблицаОчереди.Документ = ДокументыКОтражению.Документ
		|ГДЕ
		|	&ПолеНомерОчереди < ВЫРАЗИТЬ(&ВидЗадания КАК Справочник.ВидыНеоперативныхЗаданий).НомерОчереди
		|	И НЕ ТаблицаОчереди.ВыполненоСОшибкой
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаОчереди.Документ КАК Документ,
		|	&ПолеНомерОчереди КАК НомерОчереди,
		|	&ПолеВидЗадания КАК ВидЗадания
		|ИЗ
		|	&ТаблицаОчереди КАК ТаблицаОчереди
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДокументыКОтражению КАК ДокументыКОтражению
		|		ПО ТаблицаОчереди.Документ = ДокументыКОтражению.Документ
		|ГДЕ
		|	&ПолеВидЗадания = &ВидЗадания
		|	И НЕ ТаблицаОчереди.ВыполненоСОшибкой";
	КонецЕсли;
	
	МассивПодзапросов = Новый Массив;
	
	Для Каждого ТекущаяОчередь Из ТаблицаОчередей Цикл

		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);

		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеВидЗадания", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаВидЗадания), "&" + ТекущаяОчередь.ПараметрЗапросаВидЗадания,
			"ТаблицаОчереди.ВидЗадания"));

		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеНомерОчереди", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаНомерОчереди), "&" + ТекущаяОчередь.ПараметрЗапросаНомерОчереди,
			"ТаблицаОчереди.ВидЗадания.НомерОчереди"));

		МассивПодзапросов.Добавить(ТекстЗапросаОчереди);

	КонецЦикла;
	
	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстЗапросаДокументыКОбработке = СтрЗаменить(ТекстЗапросаДокументыКОбработке, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
	ТекстыЗапроса.Добавить(ТекстЗапросаДокументыКОбработке);
	
	ТекстЗапросаПервыйМинимальныйДень="
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	МинимальныйДень.НомерОчереди КАК НомерОчереди,
	|	МинимальныйДень.ПериодЗаданияДень КАК ПериодЗаданияДень,
	|	МинимальныйДень.ВидЗадания КАК ВидЗадания
	|ПОМЕСТИТЬ ВТ_ОчередьМинимальныйДень
	|ИЗ
	|	(ВЫБРАТЬ
	|		МИНИМУМ(ВТ_ЗаданияПоДокументам.ПериодЗаданияДень) КАК ПериодЗаданияДень,
	|		ВТ_ЗаданияПоДокументам.ВидЗадания КАК ВидЗадания,
	|		ВТ_ЗаданияПоДокументам.НомерОчереди КАК НомерОчереди
	|	ИЗ
	|		ВТ_ЗаданияПоДокументам КАК ВТ_ЗаданияПоДокументам
	|	СГРУППИРОВАТЬ ПО
	|		ВТ_ЗаданияПоДокументам.ВидЗадания,
	|		ВТ_ЗаданияПоДокументам.НомерОчереди) КАК МинимальныйДень
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерОчереди,
	|	ПериодЗаданияДень
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПериодЗаданияДень,
	|	НомерОчереди,
	|	ВидЗадания";
	
	ТекстыЗапроса.Добавить(ТекстЗапросаПервыйМинимальныйДень);
	
	ТекстЗапросаИтоговый = "
	|ВЫБРАТЬ
	|	ВТ_ЗаданияПоДокументам.ВидЗадания КАК ВидЗадания,
	|	ВТ_ЗаданияПоДокументам.ПериодЗаданияДень КАК ПериодЗаданияДень,
	|	ВТ_ЗаданияПоДокументам.Документ КАК Документ
	|ИЗ
	|	ВТ_ЗаданияПоДокументам КАК ВТ_ЗаданияПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОчередьМинимальныйДень КАК МинимальныйДень
	|		ПО ВТ_ЗаданияПоДокументам.ПериодЗаданияДень = МинимальныйДень.ПериодЗаданияДень
	|		И ВТ_ЗаданияПоДокументам.НомерОчереди = МинимальныйДень.НомерОчереди
	|		И ВТ_ЗаданияПоДокументам.ВидЗадания = МинимальныйДень.ВидЗадания
	|ИТОГИ
	|ПО
	|	ВидЗадания,
	|	ПериодЗаданияДень";
	ТекстыЗапроса.Добавить(ТекстЗапросаИтоговый);
	
#КонецОбласти

	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());

	Возврат ТекстЗапроса;

КонецФункции

// Запускает обработку порций гранул расчета.
// Если в параметре выполнения заданий установлен режим "Выполнение заданий непосредственно" (ВыполнениеЗаданийНепосредственно = Истина), то фоновое выполнение заданий запускаться не будет.
// 
// Параметры:
//  ТаблицаДанныхКОбработке - ТаблицаЗначений - Заполняется в функции ДанныеДляМногопоточнойФоновойОбработки модуля менеджера регистра очереди.
//  КоличествоПорций - Число - Количество порций
//  ПараметрыВыполненияЗаданий - см. НеоперативнаяОчередьЗаданий.ПараметрыВыполненияЗаданий
//  ТаблицаДанныхВОбработке - Неопределено, ТаблицаЗначений - Таблица гранул расчета, которые уже находятся в обработке.
//  РабочиеПотоки - Массив Из см. ОписаниеРабочегоПотока - Массив используемых рабочих потоков.
//  ОчередиЗаданий - см. НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий
//
Процедура ЗапуститьОбработкуПорцийДанных(ТаблицаДанныхКОбработке, КоличествоПорций,
	ПараметрыВыполненияЗаданий, ТаблицаДанныхВОбработке, РабочиеПотоки, ОчередиЗаданий)

	Если ТаблицаДанныхКОбработке.Количество() > 0 Тогда

		Для ТекущийПорядокОбработки = 1 По КоличествоПорций Цикл

			ТаблицаДанных = ТаблицаДанныхКОбработке.Скопировать(Новый Структура("Порция", ТекущийПорядокОбработки), );
			Если Не ПараметрыВыполненияЗаданий.ВыполнениеЗаданийНепосредственно Тогда
				ТаблицаДанных.Колонки.Добавить("ИдентификаторПотока", Новый ОписаниеТипов("УникальныйИдентификатор"));
				ТаблицаДанных.Индексы.Добавить("ИдентификаторПотока");
			КонецЕсли;
			
			Если ТаблицаДанных.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			МенеджерРегистраЗаданий = ПараметрыВыполненияЗаданий.МенеджерРегистраЗаданий;

			ПараметрыПроцедуры = ПараметрыОбработкиПорцииДанных();
			ПараметрыПроцедуры.ПорцияДанных = ОбщегоНазначения.СкопироватьРекурсивно(ТаблицаДанных);
			ПараметрыПроцедуры.ИмяРегистраОчереди = ПараметрыВыполненияЗаданий.ИмяРегистраОчереди;
			ПараметрыПроцедуры.Вставить("ОчередиЗаданий", ОчередиЗаданий);

			Если ПараметрыВыполненияЗаданий.ВыполнениеЗаданийНепосредственно Тогда
				
				ПараметрыПроцедуры.ВыполнениеЗаданийВТранзакцииПроведения = ПараметрыВыполненияЗаданий.ВыполнениеЗаданийВТранзакцииПроведения;
				МенеджерРегистраЗаданий.ВыполнитьОбработкуПорции(ПараметрыПроцедуры);
				
			Иначе
				
				НовыйРабочийПоток = ОписаниеРабочегоПотока();
				НовыйРабочийПоток.МенеджерРегистраЗаданий = ПараметрыВыполненияЗаданий.МенеджерРегистраЗаданий;
				НовыйРабочийПоток.ПараметрыПроцедуры = ПараметрыПроцедуры;

				НаименованиеФоновогоЗадания = НСтр("ru = 'Обработка порции документов неоперативной очереди заданий';
													|en = 'Process a batch of backdate job queue documents'");
				ИмяПроцедурыФоновогоЗадания = "НеоперативнаяОчередьЗаданий.ВыполнитьОбработкуПорцииФоновымЗаданием";
				ПараметрыФоновогоЗадания = Новый Массив;
				ПараметрыФоновогоЗадания.Добавить(МенеджерРегистраЗаданий);
				ПараметрыФоновогоЗадания.Добавить(ПараметрыПроцедуры);
				ФоновоеЗадание = ФоновыеЗадания.Выполнить(ИмяПроцедурыФоновогоЗадания, ПараметрыФоновогоЗадания, ,
					НаименованиеФоновогоЗадания);

				НовыйРабочийПоток.ИдентификаторПотока = ФоновоеЗадание.УникальныйИдентификатор;
				
				Если Не РабочиеПотоки = Неопределено Тогда
					РабочиеПотоки.Добавить(НовыйРабочийПоток);
				КонецЕсли;
				
				ТаблицаДанных.ЗаполнитьЗначения(НовыйРабочийПоток.ИдентификаторПотока, "ИдентификаторПотока");
				Если Не ТаблицаДанныхВОбработке = Неопределено Тогда
					ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаДанных, ТаблицаДанныхВОбработке);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;

	КонецЕсли;

КонецПроцедуры

Процедура ВыполнитьОбработкуПорцииФоновымЗаданием(МенеджерРегистраЗаданий, ПараметрыПроцедуры) Экспорт
	
	МенеджерРегистраЗаданий.ВыполнитьОбработкуПорции(ПараметрыПроцедуры);
	
КонецПроцедуры

// Удаление записей из регистра ошибок очередей.
// 
// Параметры:
//  ОчередиЗаданий - см. НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий
//  СписокИдентификаторов - Массив из УникальныйИдентификатор - Список идентификаторов ошибок
//
Процедура УдалениеЗаписейРегистраОшибокОчередей(ОчередиЗаданий, СписокИдентификаторов) Экспорт

	ТаблицаОчередей = ОчередиЗаданий.ТаблицаОчередей;
	СписокИдентификаторов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СписокИдентификаторов);

	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(СписокИдентификаторов,
		ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
		
	Если СписокИдентификаторов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторыОшибок", СписокИдентификаторов);
	
	МассивЗапросов = Новый Массив;
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОшибкиОчередей.ИдентификаторОшибки КАК ИдентификаторОшибки
	|ПОМЕСТИТЬ ВТ_ОшибкиИсключения
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК ОшибкиОчередей
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИдентификаторОшибки";

	ТекстПодзапросаШаблон = "
	|ВЫБРАТЬ
	|	ТаблицаОчереди.ИдентификаторОшибки КАК ИдентификаторОшибки
	|ИЗ
	|	&ТаблицаОчереди КАК ТаблицаОчереди
	|ГДЕ
	|	ТаблицаОчереди.ИдентификаторОшибки В (&ИдентификаторыОшибок)";
	
	МассивПодзапросов = Новый Массив;
	Для Каждого ТекущаяОчередь Из ТаблицаОчередей Цикл

		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);
		МассивПодзапросов.Добавить(ТекстЗапросаОчереди);

	КонецЦикла;
	
	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
	МассивЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ОшибкиОчередейЗаданий.ИдентификаторОшибки КАК ИдентификаторОшибки
	|ИЗ
	|	РегистрСведений.ОшибкиОчередейЗаданий КАК ОшибкиОчередейЗаданий
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОшибкиИсключения КАК ОшибкиИсключения
	|		ПО ОшибкиОчередейЗаданий.ИдентификаторОшибки = ОшибкиИсключения.ИдентификаторОшибки
	|ГДЕ
	|	ОшибкиОчередейЗаданий.ИдентификаторОшибки В (&ИдентификаторыОшибок)
	|	И ОшибкиИсключения.ИдентификаторОшибки ЕСТЬ NULL";
	МассивЗапросов.Добавить(ТекстЗапроса);
	
	Запрос.Текст = СтрСоединить(МассивЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	УстановитьПривилегированныйРежим(Истина);
	ОшибкиОчередейЗаданий = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
			
	НачатьТранзакцию();
	
	Попытка

		НаборЗаписей = РегистрыСведений.ОшибкиОчередейЗаданий.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(ОшибкиОчередейЗаданий);
		УстановитьПривилегированныйРежим(Истина);
		НаборЗаписей.Записать(РежимЗамещения.Удаление);
		УстановитьПривилегированныйРежим(Ложь);

		ЗафиксироватьТранзакцию();

	Исключение

		ОтменитьТранзакцию();

		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());

		Событие = НСтр("ru = 'Неоперативная очередь заданий: Удаление данных об ошибках';
						|en = 'Backdate job queue: Delete error data'",
			ОбщегоНазначения.КодОсновногоЯзыка());

		ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);

	КонецПопытки;

КонецПроцедуры

// Выполняет проверку наличия заданий с повышенным приоритетом.
// В случае отсутствия заданий по указанным параметрам повышения приоритетов, выполняется удаление таких параметров.
// 
// Параметры:
//  ОчередиЗаданий - см. НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий
//
Процедура ПроверитьНаличиеЗаданийСПовышеннымПриоритетомВыполнения(ОчередиЗаданий)
	
	ТаблицаОчередей = ОчередиЗаданий.ТаблицаОчередей;
	
	Запрос = Новый Запрос;
	УстановитьПараметрыЗапросаПоОчередям(Запрос, ОчередиЗаданий);
	
	МассивТекстовЗапросов = Новый Массив;
	
	#Область ДокументыСОшибками
	
	ТекстДокументыСОшибками = "
	|ВЫБРАТЬ
	|	ОшибкиПоДокументам.Документ КАК Документ,
	|	МИНИМУМ(ОшибкиПоДокументам.НомерОчереди) КАК НомерОчереди
	|ПОМЕСТИТЬ ВТ_ОшибкиПоДокументам
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК ОшибкиПоДокументам
	|СГРУППИРОВАТЬ ПО
	|	ОшибкиПоДокументам.Документ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	НомерОчереди";
	
	ТекстПодзапросаШаблон = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОчереди.Документ КАК Документ,
	|	&ПолеНомерОчереди КАК НомерОчереди
	|ИЗ
	|	&ТаблицаОчереди КАК ТаблицаОчереди
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиОчередейЗаданий КАК РегистрОшибки
	|		ПО ТаблицаОчереди.ИдентификаторОшибки = РегистрОшибки.ИдентификаторОшибки
	|";
	
	МассивПодзапросов = Новый Массив;
	
	Для Каждого ТекущаяОчередь Из ТаблицаОчередей Цикл

		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);

		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеНомерОчереди", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаНомерОчереди), "&" + ТекущаяОчередь.ПараметрЗапросаНомерОчереди,
			"ТаблицаОчереди.ВидЗадания.НомерОчереди"));

		МассивПодзапросов.Добавить(ТекстЗапросаОчереди);

	КонецЦикла;
	
	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
	ТекстДокументыСОшибками = СтрЗаменить(ТекстДокументыСОшибками, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
	МассивТекстовЗапросов.Добавить(ТекстДокументыСОшибками);
	
	#КонецОбласти
	
	ТекстЗапросаПриоритеты = "
	|ВЫБРАТЬ
	|	ПараметрыЗакрытияМесяца.Организация КАК Организация,
	|	КОНЕЦПЕРИОДА(ПараметрыЗакрытияМесяца.ПериодЗадания, МЕСЯЦ) КАК ПериодЗакрытия
	|ПОМЕСТИТЬ ВТ_ПараметрыЗакрытияМесяца
	|ИЗ
	|	РегистрСведений.ПриоритетныеЗаданияНеоперативнойОчереди КАК ПараметрыЗакрытияМесяца
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодЗакрытия";
	МассивТекстовЗапросов.Добавить(ТекстЗапросаПриоритеты);
	
	#Область ЗаданияПоДокументам
	
	ТекстЗапросаЗаданияПоДокументам = "
	|ВЫБРАТЬ
	|	ЗаданияКОбработке.Организация КАК Организация,
	|	ЗаданияКОбработке.ПериодЗакрытия КАК ПериодЗакрытия
	|ПОМЕСТИТЬ ВТ_Задания
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК ЗаданияКОбработке
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодЗакрытия";
	
	ТекстПодзапросаШаблон = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ПараметрыЗакрытияМесяца.Организация КАК Организация,
	|	КОНЕЦПЕРИОДА(ВТ_ПараметрыЗакрытияМесяца.ПериодЗакрытия, МЕСЯЦ) КАК ПериодЗакрытия
	|ИЗ
	|	ВТ_ПараметрыЗакрытияМесяца КАК ВТ_ПараметрыЗакрытияМесяца
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ &ТаблицаОчереди КАК ТаблицаОчереди
	|		ПО ТаблицаОчереди.Организация = ВТ_ПараметрыЗакрытияМесяца.Организация
	|		И ТаблицаОчереди.ПериодЗадания <= ВТ_ПараметрыЗакрытияМесяца.ПериодЗакрытия
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОшибкиПоДокументам КАК Ошибки
	|		ПО ТаблицаОчереди.Документ = Ошибки.Документ
	|		И &ПолеНомерОчереди > Ошибки.НомерОчереди
	|ГДЕ
	|	ТаблицаОчереди.ВыполненоСОшибкой = ЛОЖЬ
	|	И Ошибки.Документ ЕСТЬ NULL";
	
	МассивПодзапросов = Новый Массив;
	
	Для Каждого ТекущаяОчередь Из ТаблицаОчередей Цикл

		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);

		ТекстЗапросаОчереди = СтрЗаменить(ТекстЗапросаОчереди, "&ПолеНомерОчереди", ?(ЗначениеЗаполнено(
			ТекущаяОчередь.ПараметрЗапросаНомерОчереди), "&" + ТекущаяОчередь.ПараметрЗапросаНомерОчереди,
			"ТаблицаОчереди.ВидЗадания.НомерОчереди"));

		МассивПодзапросов.Добавить(ТекстЗапросаОчереди);

	КонецЦикла;
	
	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
	ТекстЗапросаЗаданияПоДокументам = СтрЗаменить(ТекстЗапросаЗаданияПоДокументам, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
	МассивТекстовЗапросов.Добавить(ТекстЗапросаЗаданияПоДокументам);
	
	#КонецОбласти
	
	ТекстЗапросаИтог = "
	|ВЫБРАТЬ
	|	ВТ_ПараметрыЗакрытияМесяца.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(ВТ_ПараметрыЗакрытияМесяца.ПериодЗакрытия, МЕСЯЦ) КАК ПериодЗадания
	|ИЗ
	|	ВТ_ПараметрыЗакрытияМесяца КАК ВТ_ПараметрыЗакрытияМесяца
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Задания КАК ВТ_Задания
	|		ПО ВТ_ПараметрыЗакрытияМесяца.Организация = ВТ_Задания.Организация
	|		И ВТ_ПараметрыЗакрытияМесяца.ПериодЗакрытия = ВТ_Задания.ПериодЗакрытия
	|ГДЕ
	|	ВТ_Задания.Организация ЕСТЬ NULL";
	
	МассивТекстовЗапросов.Добавить(ТекстЗапросаИтог);
	
	Запрос.Текст = СтрСоединить(МассивТекстовЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	УстановитьПривилегированныйРежим(Истина);
	ПриоритетныеЗадания = Запрос.Выполнить().Выгрузить();
	Если ПриоритетныеЗадания.Количество() > 0 Тогда
		НаборЗаписей = РегистрыСведений.ПриоритетныеЗаданияНеоперативнойОчереди.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(ПриоритетныеЗадания);
		НаборЗаписей.Записать(РежимЗамещения.Удаление);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
		
КонецПроцедуры

Функция ВремяЗавершенияРаботыРегламентногоЗаданияПоРасписанию(Расписание, ТекущаяДатаСеанса)
	
	ВремяЗапуска = Дата(1, 1, 1, Час(ТекущаяДатаСеанса), Минута(ТекущаяДатаСеанса), Секунда(ТекущаяДатаСеанса));
	МаксимальнаяДата = Дата('39991231235959');
	ВремяЗавершения = МаксимальнаяДата;

	Если Расписание.ДетальныеРасписанияДня.Количество() > 0 Тогда

		Для Каждого ТекущееРасписание Из Расписание.ДетальныеРасписанияДня Цикл

			ОпределитьВремяЗавершенияПоРасписанию(ТекущееРасписание, ВремяЗапуска, ВремяЗавершения);

		КонецЦикла;

	Иначе

		ОпределитьВремяЗавершенияПоРасписанию(Расписание, ВремяЗапуска, ВремяЗавершения);

	КонецЕсли;

	Возврат ?(ВремяЗавершения <> МаксимальнаяДата,
			Дата(Год(ТекущаяДатаСеанса), Месяц(ТекущаяДатаСеанса), День(ТекущаяДатаСеанса),
				Час(ВремяЗавершения), Минута(ВремяЗавершения), Секунда(ВремяЗавершения)),
			Дата(1, 1, 1, 0, 0, 0));
		
КонецФункции

Процедура ОпределитьВремяЗавершенияПоРасписанию(ТекущееРасписание, ВремяЗапуска, ВремяЗавершения)
	
	ВремяОкончания = ?(ЗначениеЗаполнено(ТекущееРасписание.ВремяКонца), ТекущееРасписание.ВремяКонца, Дата(1, 1, 1, 23, 59, 59));
	
	Если ВремяЗапуска >= ТекущееРасписание.ВремяНачала И ВремяЗапуска <= ВремяОкончания И ЗначениеЗаполнено(ТекущееРасписание.ВремяЗавершения) Тогда
		ВремяЗавершения = Мин(ТекущееРасписание.ВремяЗавершения, ВремяЗавершения);
	КонецЕсли;
	Если ВремяЗапуска >= ТекущееРасписание.ВремяНачала И ВремяЗапуска <= ВремяОкончания И ЗначениеЗаполнено(ТекущееРасписание.ИнтервалЗавершения) Тогда
		ВремяЗавершения = Мин(ВремяЗапуска + ТекущееРасписание.ИнтервалЗавершения, ВремяЗавершения);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

Функция УправляющийПотокЗапущен(ИмяМетода)
	
	// Проверка, выполняется ли фоновое задание.
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяМетода", ИмяМетода);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	ФоновыеЗаданияУправляющийПоток = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Возврат ФоновыеЗаданияУправляющийПоток.Количество() > 0;
	
КонецФункции

// Установка дополнительных параметров запроса по очередям заданий.
// 
// Параметры:
//  Запрос - Запрос - Запрос
//  ОчередиЗаданий - см. НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий
//
Процедура УстановитьПараметрыЗапросаПоОчередям(Запрос, ОчередиЗаданий)
	
	ТаблицаОчередей = ОчередиЗаданий.ТаблицаОчередей;
	
	Для Каждого ТекущаяОчередь Из ТаблицаОчередей Цикл
		Запрос.УстановитьПараметр(ТекущаяОчередь.ОбрабатываемыеГранулыИмяТаблицы, ТекущаяОчередь.ГранулыРасчета);
		Запрос.УстановитьПараметр(ТекущаяОчередь.ПараметрЗапросаВидЗадания, ТекущаяОчередь.ЗначениеВидЗадания);
		Запрос.УстановитьПараметр(ТекущаяОчередь.ПараметрЗапросаНомерОчереди, ТекущаяОчередь.ЗначениеНомерОчереди);
	КонецЦикла;
	
КонецПроцедуры

Функция ОписаниеРабочегоПотока()

	ОписаниеРабочегоПотока = Новый Структура;
	ОписаниеРабочегоПотока.Вставить("ИдентификаторПотока", Неопределено);
	ОписаниеРабочегоПотока.Вставить("МенеджерРегистраЗаданий", Неопределено);
	ОписаниеРабочегоПотока.Вставить("ПараметрыПроцедуры", Новый Структура);
	ОписаниеРабочегоПотока.Вставить("ФоновоеВыполнение", Истина);

	Возврат ОписаниеРабочегоПотока;

КонецФункции

#Область ЗакрытиеМесяца

Процедура ЗакрытиеМесяцаВыполнитьЗаданияНеоперативнойОчереди(ПараметрыОбработчика, ОчередиЗаданий)
	
	ИнформационнаяБазаФайловая = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	ПараметрыРасчета = ПараметрыОбработчика.ПараметрыРасчета;
	
	НачалоРасчета = ПараметрыРасчета.КонецПериода;
	
	#Область УстановкаПриоритетовВыполненияЗаданий

	СписокОрганизаций = ПараметрыРасчета.МассивОрганизаций;
	КонецРасчета = КонецМесяца(ПараметрыРасчета.КонецПериода);
	
	ДанныеКРасчетуЗаМесяц = ДанныеКРасчетуЗаМесяц(КонецРасчета, СписокОрганизаций, ОчередиЗаданий);
	
	НачалоРасчета = ДанныеКРасчетуЗаМесяц.НачалоРасчета;
	
	ТекущийМесяцРасчета = НачалоМесяца(НачалоРасчета);
	
	СтруктураОтбораЗаданий = Новый Структура;
	
	Пока ТекущийМесяцРасчета <= КонецРасчета Цикл

		ДоступныеОрганизации = ЗакрытиеМесяцаСервер.ДоступныеДляРасчетаОрганизации(ТекущийМесяцРасчета,
			СписокОрганизаций);

		Для Каждого Организация Из ДоступныеОрганизации Цикл

			СтруктураОтбораЗаданий.Вставить("Организация", Организация);
			СтруктураОтбораЗаданий.Вставить("ПериодЗадания", ТекущийМесяцРасчета);

			ДанныеКРасчету = ДанныеКРасчетуЗаМесяц.ДанныеКРасчету.НайтиСтроки(СтруктураОтбораЗаданий);
			Если ДанныеКРасчету.Количество() > 0 Тогда

				МенеджерЗаписи = РегистрыСведений.ПриоритетныеЗаданияНеоперативнойОчереди.СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтруктураОтбораЗаданий);
				МенеджерЗаписи.Записать();

			КонецЕсли;

		КонецЦикла;
		
		ТекущийМесяцРасчета = КонецМесяца(ТекущийМесяцРасчета) + 1;

	КонецЦикла;
	
	Для каждого ТекущаяОрганизация Из ДанныеКРасчетуЗаМесяц.ОрганизацииПустойПериодЗаданий Цикл
		МенеджерЗаписи = РегистрыСведений.ПриоритетныеЗаданияНеоперативнойОчереди.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Организация = ТекущаяОрганизация;
		МенеджерЗаписи.ПериодЗадания = Дата(1,1,1);
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
	#КонецОбласти

	ПараметрыЗакрытия = ПараметрыЗапускаВыполненияЗаданий();
	ПараметрыЗакрытия.Организации = ПараметрыРасчета.МассивОрганизаций;
	ПараметрыЗакрытия.ПериодЗакрытия = ПараметрыРасчета.КонецПериода;
	ПараметрыЗакрытия.Вставить("ОчередиЗаданий", ОчередиЗаданий);

	Если ИнформационнаяБазаФайловая Тогда
		
		ВыполнитьЗаданияНеоперативнойОчереди(ПараметрыЗакрытия);

	Иначе

		ИмеютсяЗаданияКЗакрытиюМесяца = Истина;

		Пока ИмеютсяЗаданияКЗакрытиюМесяца Цикл

			ПроверитьЗапуститьИсполнениеНеоперативнойОчередиЗаданий();

			ОбщегоНазначенияУТ.Пауза(5);

			ЗапросПриоритеты = Новый Запрос;
			ЗапросПриоритеты.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	ПараметрыЗакрытияМесяца.Организация КАК Организация,
			|	ПараметрыЗакрытияМесяца.ПериодЗадания КАК ПериодЗакрытия
			|ИЗ
			|	РегистрСведений.ПриоритетныеЗаданияНеоперативнойОчереди КАК ПараметрыЗакрытияМесяца
			|ГДЕ
			|	ПараметрыЗакрытияМесяца.Организация В (&СписокОрганизаций)
			|	И ПараметрыЗакрытияМесяца.ПериодЗадания <= &ПериодЗакрытия";

			ЗапросПриоритеты.УстановитьПараметр("СписокОрганизаций", ПараметрыЗакрытия.Организации);
			ЗапросПриоритеты.УстановитьПараметр("ПериодЗакрытия", КонецМесяца(ПараметрыЗакрытия.ПериодЗакрытия));
			
			//@skip-check query-in-loop
			// Проверка отключена т.к. управляющий поток должен контролировать работу в режиме закрытия месяца
			// и в случае обработки всех заданий очередей - должен завершаться.
			РезультатЗапроса = ЗапросПриоритеты.Выполнить();
	
			ИмеютсяЗаданияКЗакрытиюМесяца = Не РезультатЗапроса.Пустой();

		КонецЦикла;
		
		ЗавершениеИсполненияНеоперативнойОчередиЗаданий();

	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеКРасчетуЗаМесяц(КонецПериода, МассивОрганизаций, ОчередиЗаданий)

	СтруктураДанных = Новый Структура;
	
	ПериодНачалаРасчета = НачалоМесяца(КонецПериода);
	СтруктураДанных.Вставить("НачалоРасчета", ПериодНачалаРасчета);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(КонецПериода));
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.Текст ="
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданияКОбработке.ПериодЗадания КАК ПериодЗадания,
	|	ЗаданияКОбработке.Организация КАК Организация
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК ЗаданияКОбработке
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПериодЗадания";
		
	ТекстПодзапросаШаблон = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(ВЫБОР
	|		КОГДА ТаблицаОчереди.ПериодЗадания = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &КонецПериода
	|		ИНАЧЕ ТаблицаОчереди.ПериодЗадания
	|	КОНЕЦ, МЕСЯЦ) КАК ПериодЗадания,
	|	ТаблицаОчереди.Организация КАК Организация
	|ИЗ
	|	&ТаблицаОчереди КАК ТаблицаОчереди
	|ГДЕ
	|	ТаблицаОчереди.Организация В (&МассивОрганизаций)
	|	И ТаблицаОчереди.ПериодЗадания <= &КонецПериода";

	МассивПодзапросов = Новый Массив;
	Для каждого ТекущаяОчередь Из ОчередиЗаданий.ТаблицаОчередей Цикл
		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);
		МассивПодзапросов.Добавить(ТекстЗапросаОчереди);
	КонецЦикла;
	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеКРасчету = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	ДанныеКРасчету.Индексы.Добавить("Организация, ПериодЗадания");
	
	Если ДанныеКРасчету.Количество() > 0 Тогда
		СтруктураДанных.Вставить("НачалоРасчета", ДанныеКРасчету[0].ПериодЗадания);
	КонецЕсли;
	
	СтруктураДанных.Вставить("ДанныеКРасчету", ДанныеКРасчету);
	
	ЗапросПустойПериод = Новый Запрос;
	ЗапросПустойПериод.УстановитьПараметр("КонецПериода", КонецМесяца(КонецПериода));
	ЗапросПустойПериод.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	ЗапросПустойПериод.Текст ="
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданияКОбработке.Организация КАК Организация
	|ИЗ
	|	&ТекстВложенногоЗапроса КАК ЗаданияКОбработке";
		
	ТекстПодзапросаШаблон = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОчереди.Организация КАК Организация
	|ИЗ
	|	&ТаблицаОчереди КАК ТаблицаОчереди
	|ГДЕ
	|	ТаблицаОчереди.Организация В (&МассивОрганизаций)
	|	И ТаблицаОчереди.ПериодЗадания = ДАТАВРЕМЯ(1,1,1)";

	МассивПодзапросов = Новый Массив;
	Для каждого ТекущаяОчередь Из ОчередиЗаданий.ТаблицаОчередей Цикл
		ТекстЗапросаОчереди = СтрЗаменить(ТекстПодзапросаШаблон, "&ТаблицаОчереди", ТекущаяОчередь.ПолноеИмяОчереди);
		МассивПодзапросов.Добавить(ТекстЗапросаОчереди);
	КонецЦикла;
	ТекстПодзапроса = СтрСоединить(МассивПодзапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
	ЗапросПустойПериод.Текст = СтрЗаменить(ЗапросПустойПериод.Текст, "&ТекстВложенногоЗапроса", "(" + ТекстПодзапроса + ")");
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеКРасчетуПустойПериод = ЗапросПустойПериод.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	СтруктураДанных.Вставить("ОрганизацииПустойПериодЗаданий", ДанныеКРасчетуПустойПериод.ВыгрузитьКолонку("Организация"));
	
	Возврат СтруктураДанных;
	
КонецФункции

#Область ЗакрытиеМесяцаПроверки

// Выполняет контроль корректности сформированных движений документов по регистрам взаиморасчетов.
// 
// Параметры:
//  ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
//  НачалоРасчета - Дата - Период, с которого выполяется закрытие месяца
// 
// Возвращаемое значение:
//  Структура - Результаты проверки движений:
// * ПроблемныеДокументыСуммыВВалютахУчета - Массив Из ДокументСсылка - Список документов, в которых движения регистров 
// взаиморасчетов отличаются от движений по регистру "Суммы документов в валютах учета".
// * МВТРасхожденияВРегистрахВзаиморасчетов - Неопределено, МенеджерВременныхТаблиц - Содержит временные таблицы с данными 
// о расхождениях в оперативных и финансовых регистрах взаиморасчетов по клиентам и поставщикам. 
// Менеджер временных таблиц включает следующие таблицы: 
// "РасчетыСКлиентамиИзменения" - содержит информацию о расхождениях движений в регистрах "Расчеты с клиентами" и "Расчеты с клиентами по срокам", 
// "РасчетыСПоставщикамиИзменения" - содержит информацию о расхождениях движений в регистрах "Расчеты с поставщиками" и "Расчеты с поставщиками по срокам".
// Если расхождений не обнаружено, то в параметр будет передано Неопределено.
Функция ПроверитьДвиженияДокументовПоРегистрамВзаиморасчетов(ПараметрыОбработчика, НачалоРасчета)
	
	ПараметрыРасчета = ПараметрыОбработчика.ПараметрыРасчета;
	СписокОрганизаций = ПараметрыРасчета.МассивОрганизаций;
	КонецРасчета = КонецМесяца(ПараметрыРасчета.КонецПериода);
	ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();

	ПроблемныеДокументыСуммыВВалютахУчета = Новый Массив;

	ТекущийМесяцРасчета = НачалоМесяца(НачалоРасчета);
	
	Пока ТекущийМесяцРасчета <= КонецРасчета Цикл

		ДоступныеОрганизации = ЗакрытиеМесяцаСервер.ДоступныеДляРасчетаОрганизации(ТекущийМесяцРасчета,
			СписокОрганизаций);

		Для Каждого Организация Из ДоступныеОрганизации Цикл
			
			ОкончаниеПериода = КонецМесяца(ТекущийМесяцРасчета);
			
			#Область СуммыДокументовВВалютахУчета
			
			ЗапросПроверка = Новый Запрос;
			ЗапросПроверка.УстановитьПараметр("НачалоПериода", ТекущийМесяцРасчета);
			ЗапросПроверка.УстановитьПараметр("КонецПериода", ОкончаниеПериода);
			ЗапросПроверка.УстановитьПараметр("Организация", Организация);
			
			ЗапросПроверка.Текст = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетыСКлиентами.Регистратор КАК Регистратор,
			|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
			|	РасчетыСКлиентами.Период КАК Период
			|ПОМЕСТИТЬ ПредварительныеРасчетыКОтражениюВУчете
			|ИЗ
			|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
			|ГДЕ
			|	РасчетыСКлиентами.Период <= &КонецПериода
			|	И РасчетыСКлиентами.Период >= &НачалоПериода
			|	И РасчетыСКлиентами.АналитикаУчетаПоПартнерам.Организация = &Организация
			|	И РасчетыСКлиентами.Сумма <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетыСПоставщиками.Регистратор КАК Регистратор,
			|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
			|	РасчетыСПоставщиками.Период КАК Период
			|ИЗ
			|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
			|ГДЕ
			|	РасчетыСПоставщиками.Период <= &КонецПериода
			|	И РасчетыСПоставщиками.Период >= &НачалоПериода
			|	И РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Организация = &Организация
			|	И РасчетыСПоставщиками.Сумма <> 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Расчеты.Регистратор КАК ДокументРегистратор
			|ИЗ
			|	ПредварительныеРасчетыКОтражениюВУчете КАК Расчеты";
			МассивВсехДокументов = ЗапросПроверка.Выполнить().Выгрузить().ВыгрузитьКолонку("ДокументРегистратор");
		
			Запрос = Новый Запрос;
			Запрос.Текст = РегистрыСведений.СуммыДокументовВВалютахУчета.ТекстЗапросаДокументовДляПересчета();
			Запрос.УстановитьПараметр("МассивДокументов", МассивВсехДокументов);
			Запрос.УстановитьПараметр("ЭтоПроверка", Истина);
			Запрос.УстановитьПараметр("ВалютаУпрУчета", ВалютаУправленческогоУчета);
			Запрос.УстановитьПараметр("НоваяАрхитектураВзаиморасчетов", Истина);
			Запрос.УстановитьПараметр("ПоВсемДокументам", Ложь);
		
			ПроблемныеДокументы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("РасчетныйДокумент");
			
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПроблемныеДокументыСуммыВВалютахУчета, ПроблемныеДокументы, Истина);

			Если ПроблемныеДокументы.Количество() > 0 Тогда
								
				ПредставлениеСуммыДокументовВВалютахУчета = Метаданные.РегистрыСведений.СуммыДокументовВВалютахУчета.Представление();
				
				ШаблонТекстаОшибки = НСтр(
					"ru = 'Движения документа %1 по регистрам взаиморасчетов отличаются от движений по регистру ""%2"".';
					|en = 'AR/AP accounting register records of the %1 document differ from the ""%2"" register records.'",
					ОбщегоНазначения.КодОсновногоЯзыка());

				Для Каждого Документ Из ПроблемныеДокументы Цикл

					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаОшибки,
						Документ, ПредставлениеСуммыДокументовВВалютахУчета);
						
					ЗаписьЖурналаРегистрации(НСтр("ru = 'Расхождения в движениях регистров взаиморасчетов';
													|en = 'Discrepancies in AR/AP accounting register records'",
						ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,
						Метаданные.ОбщиеМодули.ЗакрытиеМесяцаСервер, , ТекстОшибки);
							
				КонецЦикла;
				
			КонецЕсли;
			
			#КонецОбласти
			
		КонецЦикла;

		ТекущийМесяцРасчета = КонецМесяца(ТекущийМесяцРасчета) + 1;

	КонецЦикла;
	
	#Область СверкаСуммОперативныхИФинансовыхРегистров
			
	ЗапросПроверка = Новый Запрос;
	ЗапросПроверка.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗапросПроверка.УстановитьПараметр("КонецПериода", КонецРасчета);
	ЗапросПроверка.УстановитьПараметр("МассивОрганизаций", СписокОрганизаций);
	ЗапросПроверка.УстановитьПараметр("ЕстьЗаданияПоЭтапуДопроведенияДокументов", Ложь);
	
	ЗапросПроверка.Текст = ОперативныеВзаиморасчетыСервер.ТекстЗапросаСверкаСуммОперативныхИФинансовыхРегистров();
	
	ЗапросПроверка.Выполнить();

	ТаблицаПроблемныхДокументов = ЗапросПроверка.МенеджерВременныхТаблиц.Таблицы["ДокументыСРасхождениямиВРегистрахВзаиморасчетов"].ПолучитьДанные().Выгрузить();
	
	Если ТаблицаПроблемныхДокументов.Количество() > 0 Тогда

		ШаблонТекстаОшибки = НСтр(
			"ru = 'По документу %1 различаются движения в регистрах взаиморасчетов ""%2"" и ""%3""';
			|en = 'For the %1 document, records in the ""%2"" and ""%3"" AR/AP accounting registers differ'",
			ОбщегоНазначения.КодОсновногоЯзыка());

		ПредставлениеРасчетыСКлиентами = Метаданные.РегистрыНакопления.РасчетыСКлиентами.Представление();
		ПредставлениеРасчетыСКлиентамиПоСрокам = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Представление();
		ПредставлениеРасчетыСПоставщиками = Метаданные.РегистрыНакопления.РасчетыСПоставщиками.Представление();
		ПредставлениеРасчетыСПоставщикамиПоСрокам = Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.Представление();

		Для Каждого СтрокаТаблицы Из ТаблицаПроблемныхДокументов Цикл
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекстаОшибки,
				СтрокаТаблицы.Документ,
				?(СтрокаТаблицы.ЭтоРасчетыСКлиентами, ПредставлениеРасчетыСКлиентами, ПредставлениеРасчетыСПоставщиками),
				?(СтрокаТаблицы.ЭтоРасчетыСКлиентами, ПредставлениеРасчетыСКлиентамиПоСрокам, ПредставлениеРасчетыСПоставщикамиПоСрокам));
			
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Расхождения в движениях регистров взаиморасчетов';
											|en = 'Discrepancies in AR/AP accounting register records'",
				ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка,
				Метаданные.ОбщиеМодули.ЗакрытиеМесяцаСервер, , ТекстОшибки);
				
		КонецЦикла;

	КонецЕсли;

	#КонецОбласти
	
	РезультатыПроверки = Новый Структура;
	РезультатыПроверки.Вставить("ПроблемныеДокументыСуммыВВалютахУчета", ПроблемныеДокументыСуммыВВалютахУчета);
	Если ТаблицаПроблемныхДокументов.Количество() > 0 Тогда
		РезультатыПроверки.Вставить("МВТРасхожденияВРегистрахВзаиморасчетов", ЗапросПроверка.МенеджерВременныхТаблиц);
	Иначе
		РезультатыПроверки.Вставить("МВТРасхожденияВРегистрахВзаиморасчетов", Неопределено);
	КонецЕсли;
	
	Возврат РезультатыПроверки;
	
КонецФункции

// Выполняется попытка исправлений движений документов по регистрам взаиморасчетов.
// 
// Параметры:
//  ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
//  РезультатыПроверки - Структура - Результаты проверки движений:
// * ПроблемныеДокументыСуммыВВалютахУчета - Массив Из ДокументСсылка - Список документов, в которых движения регистров 
// взаиморасчетов отличаются от движений по регистру "Суммы документов в валютах учета".
// * МВТРасхожденияВРегистрахВзаиморасчетов - Неопределено, МенеджерВременныхТаблиц - Содержит временные таблицы с данными 
// о расхождениях в оперативных и финансовых регистрах взаиморасчетов по клиентам и поставщикам. 
// Менеджер временных таблиц включает следующие таблицы: 
// "РасчетыСКлиентамиИзменения" - содержит информацию о расхождениях движений в регистрах "Расчеты с клиентами" и "Расчеты с клиентами по срокам", 
// "РасчетыСПоставщикамиИзменения" - содержит информацию о расхождениях движений в регистрах "Расчеты с поставщиками" и "Расчеты с поставщиками по срокам".
// Если расхождений не обнаружено, то параметр принимает значение Неопределено.
//  ОчередиЗаданий - см. НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий.
Процедура ИсправитьДвиженияДокументовПоРегистрамВзаиморасчетов(ПараметрыОбработчика, РезультатыПроверки, ОчередиЗаданий)
	
	ПараметрыРасчета = ПараметрыОбработчика.ПараметрыРасчета;
	СписокОрганизаций = ПараметрыРасчета.МассивОрганизаций;
	
	#Область СуммыДокументовВВалютахУчета

	Если РезультатыПроверки.ПроблемныеДокументыСуммыВВалютахУчета.Количество() > 0 Тогда
		
		ПроблемныеДокументы = РезультатыПроверки.ПроблемныеДокументыСуммыВВалютахУчета;
		
		// Попытка перепровести проблемные документы.
		Для Каждого Документ Из ПроблемныеДокументы Цикл
	
			Попытка
				Если Не ЗначениеЗаполнено(Документ) Тогда
					Продолжить;
				КонецЕсли;
				ДокументОбъект = Документ.ПолучитьОбъект(); // ДокументОбъект
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'При формировании движений по данным взаиморасчетов за период %1 
									 |при попытке перепроведения документа %2 произошла ошибка:
									 |%3';
									 |en = 'When generating the movements by data of mutual settlements for the period %1 
									 |when attempting to repost the %2 document, an error occurred:
									 |%3'"), 
								РасчетСебестоимостиПротоколРасчета.ПредставлениеПериодаРасчета(ПараметрыРасчета.ПериодРегистрации),
								Документ,
								ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Попытка перепроведения документов';
												|en = 'Attempt to repost documents'", ОбщегоНазначения.КодОсновногоЯзыка()),
					УровеньЖурналаРегистрации.Ошибка, Метаданные.ОбщиеМодули.ЗакрытиеМесяцаСервер, , ТекстОшибки);
			КонецПопытки;
	
		КонецЦикла;
	
		Если ПроблемныеДокументы.Количество() > 0 Тогда
	
			Шаблон = НСтр("ru = 'Не удалось выполнить распределение суммы взаиморасчетов на строки документа %1.
						  |Итоги движений документа по регистрам взаиморасчетов отличаются от движений по регистру ""%2"".
						  |Попробуйте перепровести документ вручную.';
						  |en = 'Cannot allocate the AR/AP amount to the ""%1"" document lines.
						  |Document totals of AR/AP accounting register records differs from the ""%2"" register records.
						  |Try to repost the document manually.'", ОбщегоНазначения.КодОсновногоЯзыка());
	
			ПредставлениеСуммыДокументовВВалютахУчета = Метаданные.РегистрыСведений.СуммыДокументовВВалютахУчета.Представление();
	
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);
			Запрос.УстановитьПараметр("СписокРегистраторов", ПроблемныеДокументы);
			Запрос.Текст = "
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РеестрДокументов.Ссылка КАК РасчетныйДокумент,
			|	РеестрДокументов.Организация КАК Организация
			|ИЗ
			|	РегистрСведений.РеестрДокументов КАК РеестрДокументов
			|ГДЕ
			|	РеестрДокументов.Организация В (&СписокОрганизаций)
			|	И РеестрДокументов.Ссылка В (&СписокРегистраторов)";
			
			ТаблицаПроблемныхДокументов = Запрос.Выполнить().Выгрузить();
	
			Для Каждого СтрокаДокумента Из ТаблицаПроблемныхДокументов Цикл
	
				ГруппаПроблем = НСтр("ru = 'При выполнении операции были диагностированы ошибки';
									|en = 'Errors were found when executing the operation'",
					ОбщегоНазначения.КодОсновногоЯзыка());
					
				ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемыРасчета(
							ПараметрыОбработчика.ДанныеЭтапа.Код, СтрокаДокумента.Организация, ПараметрыРасчета.ПериодРегистрации);
	
				ПолныйТекстПроблемы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон,
					СтрокаДокумента.РасчетныйДокумент, ПредставлениеСуммыДокументовВВалютахУчета);
	
				ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемуВыполненияРасчета(
								ПараметрыРегистрации, ГруппаПроблем, Перечисления.ВажностьПроблемыУчета.Предупреждение,
					ПолныйТекстПроблемы, СтрокаДокумента.РасчетныйДокумент);
	
			КонецЦикла;
	
		КонецЕсли;
	
	КонецЕсли;

	#КонецОбласти
	
	#Область СверкаСуммОперативныхИФинансовыхРегистров
	
	Если РезультатыПроверки.МВТРасхожденияВРегистрахВзаиморасчетов <> Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = РезультатыПроверки.МВТРасхожденияВРегистрахВзаиморасчетов;
		Запрос.УстановитьПараметр("НовыеРасчеты", Истина);
		Запрос.УстановитьПараметр("СоздаватьЗаданияКРаспределениюВзаиморасчетов", Истина);
		Запрос.УстановитьПараметр("ТипыРегистраторовБезПроверкиИзменения",
			ОперативныеВзаиморасчетыСервер.СписокТиповРегистраторовПланов());
	
		#Область РегистрацияЗаданий
		
		МассивТекстовЗапроса = Новый Массив;
		МассивТекстовЗапроса.Добавить(РегистрыНакопления.РасчетыСКлиентами.ТекстЗапросаДанныеКРегистрацииЗаданийРаспределенияВзаиморасчетов());
		МассивТекстовЗапроса.Добавить(РегистрыНакопления.РасчетыСПоставщиками.ТекстЗапросаДанныеКРегистрацииЗаданийРаспределенияВзаиморасчетов());
		
		Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
		Запрос.Выполнить();
		
		РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов.СоздатьЗаданияПоДаннымТаблицыЗначений(
				Запрос.МенеджерВременныхТаблиц.Таблицы["ЗаданияКРаспределениюВзаиморасчетовКлиенты"].ПолучитьДанные().Выгрузить());
		РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов.СоздатьЗаданияПоДаннымТаблицыЗначений(
				Запрос.МенеджерВременныхТаблиц.Таблицы["ЗаданияКРаспределениюВзаиморасчетовПоставщики"].ПолучитьДанные().Выгрузить());
	
		#КонецОбласти
		
	КонецЕсли;

	#КонецОбласти
	
	ЗакрытиеМесяцаВыполнитьЗаданияНеоперативнойОчереди(ПараметрыОбработчика, ОчередиЗаданий);
	
КонецПроцедуры

#КонецОбласти

// Удаляются ранее зарегистрированные проблемы выполнения расчета по этапу закрытия месяца.
// 
// Параметры:
//  ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
// 
Процедура ОчиститьПроблемыВыполненияРасчета(ПараметрыОбработчика)
	
	ПараметрыРасчета = ПараметрыОбработчика.ПараметрыРасчета;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МИНИМУМ(Проблемы.ПроверяемыйПериод) КАК ПроверяемыйПериод
	|ИЗ
	|	РегистрСведений.ПроблемыСостоянияСистемы КАК Проблемы
	|ГДЕ
	|	Проблемы.Проверка = &Проверка
	|	И Проблемы.Организация В (&МассивОрганизаций)
	|	И НАЧАЛОПЕРИОДА(Проблемы.ПроверяемыйПериод, МЕСЯЦ) <= &Период
	|	И Проблемы.Важность = ЗНАЧЕНИЕ(Перечисление.ВажностьПроблемыУчета.Ошибка)";
	
	Запрос.УстановитьПараметр("Проверка", ЗакрытиеМесяцаСервер.СлужебнаяПроверкаЭтапа(ПараметрыОбработчика.ДанныеЭтапа.Код));
	МассивОрганизаций = ОбщегоНазначенияУТКлиентСервер.Массив(ПараметрыРасчета.МассивОрганизаций);
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("Период", КонецМесяца(ПараметрыРасчета.КонецПериода));
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если ЗначениеЗаполнено(Выборка.ПроверяемыйПериод) Тогда
			
			ТекущийМесяцРасчета = НачалоМесяца(Выборка.ПроверяемыйПериод);
			КонецРасчета = КонецМесяца(ПараметрыРасчета.КонецПериода);
			
			Пока ТекущийМесяцРасчета <= КонецРасчета Цикл
				
				ЗакрытиеМесяцаСервер.ОчиститьПроблемыВыполненияРасчета(ПараметрыОбработчика.ДанныеЭтапа.Код,
					МассивОрганизаций, ТекущийМесяцРасчета);

				ТекущийМесяцРасчета = КонецМесяца(ТекущийМесяцРасчета) + 1;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняется регистрация проблем выполнения операции по этапу закрытия месяца.
// 
// Параметры:
//  ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
//  ОчередиЗаданий - см. НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий
//  ВидЗадания - Неопределено, СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания
//
Процедура ПроверитьИЗарегистрироватьПроблемыВыполненияРасчета(ПараметрыОбработчика, ОчередиЗаданий, ВидЗадания = Неопределено)
	
	ТаблицаОчередей = ОчередиЗаданий.ТаблицаОчередей;
	
	Для каждого ТекущаяОчередь Из ТаблицаОчередей Цикл
		
		МенеджерРегистраЗаданий = ТекущаяОчередь.МенеджерРегистраЗаданий; // РегистрСведенийМенеджер
		МенеджерРегистраЗаданий.ЗарегистрироватьПроблемуВыполненияРасчета(ПараметрыОбработчика, ВидЗадания);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти