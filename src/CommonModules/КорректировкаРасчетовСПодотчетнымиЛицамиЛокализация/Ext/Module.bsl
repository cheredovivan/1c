#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив из Строка - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений из Строка - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	//-- НЕ УТ
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	
	//++ Локализация
	
#Область СписаниеЗадолженностиПодотчетногоЛица // (Дт <20, 23, 25, 26, 91> :: Кт <71>)
	ТекстЗапроса =
	"ВЫБРАТЬ //// Списание в расходы (Дт <20, 23, 25, 26, 91> :: Кт <71>)
	|
	|	Операция.Ссылка                                                     КАК Ссылка,
	|	Операция.Дата                                                       КАК Период,
	|	Операция.Организация                                                КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                                        КАК ИдентификаторСтроки,
	|	Строки.СуммаРегл                                                    КАК Сумма,
	|	Строки.СуммаУпр                                                     КАК СуммаУУ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)                  КАК ВидСчетаДт,
	|	Строки.Статья                                                       КАК АналитикаУчетаДт,
	|	Операция.Подразделение                                              КАК МестоУчетаДт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)                            КАК ВалютаДт,
	|	Операция.Подразделение                                              КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)           КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)                      КАК СчетДт,
	|	Строки.Статья                                                       КАК СубконтоДт1,
	|	Строки.АналитикаРасходов                                            КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СтатьиРасходов.ВариантРаздельногоУчетаНДС,
	|			НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|			ТОГДА ЕСТЬNULL(ОбъектыСтроительства.СпособСтроительства, ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный))
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ                                                               КАК СубконтоДт3,
	|	0                                                                   КАК ВалютнаяСуммаДт,
	|	0                                                                   КАК КоличествоДт,
	|	0                                                                   КАК СуммаНУДт,
	|	0                                                                   КАК СуммаПРДт,
	|	0                                                                   КАК СуммаВРДт,
	|	НЕОПРЕДЕЛЕНО                                                        КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО                                                        КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО                                                        КАК МестоУчетаКт,
	|	Строки.Валюта                                                       КАК ВалютаКт,
	|	Операция.Подразделение                                              КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)           КАК НаправлениеДеятельностиКт,
	|	ВЫБОР КОГДА Строки.Валюта = &ВалютаРеглУчета ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал)
	|	КОНЕЦ                                                               КАК СчетКт,
	|	Строки.ПодотчетноеЛицо                                              КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО                                                        КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО                                                        КАК СубконтоКт3,
	|	Строки.Сумма                                                        КАК ВалютнаяСуммаКт,
	|	0                                                                   КАК КоличествоКт,
	|	0                                                                   КАК СуммаНУКт,
	|	0                                                                   КАК СуммаПРКт,
	|	0                                                                   КАК СуммаВРКт,
	|	&СодержаниеОперации                                                 КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРасчетовСПодотчетнымиЛицами КАК Операция
	|	ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРасчетовСПодотчетнымиЛицами.КорректировкаЗадолженности КАК Строки
	|	ПО Строки.Ссылка = ДокументыКОтражению.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО Строки.Статья = СтатьиРасходов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|	ПО Строки.АналитикаРасходов = ОбъектыСтроительства.Ссылка
	|	И СтатьиРасходов.Ссылка ЕСТЬ НЕ NULL
	|	И СтатьиРасходов.ВариантРаспределенияРасходовРегл = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	И ОбъектыСтроительства.СпособСтроительства <> ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.ПустаяСсылка)
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Строки.Статья) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка                                                     КАК Ссылка,
	|	Операция.Дата                                                       КАК Период,
	|	Операция.Организация                                                КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                                        КАК ИдентификаторСтроки,
	|	Строки.СуммаРегл                                                    КАК Сумма,
	|	Строки.СуммаУпр                                                     КАК СуммаУУ,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы)      КАК ВидСчетаДт,
	|	ВЫБОР
	|		КОГДА НЕ СтатьиРасходов.Ссылка ЕСТЬ NULL
	|			ТОГДА СтатьиРасходов.Ссылка
	|		ИНАЧЕ ЕСТЬNULL(Строки.Статья, НЕОПРЕДЕЛЕНО)
	|	КОНЕЦ                                                               КАК АналитикаУчетаДт,
	|	Операция.Подразделение                                              КАК МестоУчетаДт,
	|	Строки.Валюта                                                       КАК ВалютаДт,
	|	Операция.Подразделение                                              КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)           КАК НаправлениеДеятельностиДт,
	|	ЕСТЬNULL(НастройкиСчетовУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетДт,
	|	ЕСТЬNULL(НастройкиСчетовУчета.Субконто1, Строки.Статья)             КАК СубконтоДт1,
	|	ЕСТЬNULL(НастройкиСчетовУчета.Субконто2, Строки.АналитикаАктивовПассивов) КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА НЕ НастройкиСчетовУчета.Ссылка ЕСТЬ NULL
	|			ТОГДА НастройкиСчетовУчета.Субконто3
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ                                                               КАК СубконтоДт3,
	|	Строки.Сумма                                                        КАК ВалютнаяСуммаДт,
	|	0                                                                   КАК КоличествоДт,
	|	0                                                                   КАК СуммаНУДт,
	|	0                                                                   КАК СуммаПРДт,
	|	0                                                                   КАК СуммаВРДт,
	|	НЕОПРЕДЕЛЕНО                                                        КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО                                                        КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО                                                        КАК МестоУчетаКт,
	|	Строки.Валюта                                                       КАК ВалютаКт,
	|	Операция.Подразделение                                              КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)           КАК НаправлениеДеятельностиКт,
	|	ВЫБОР
	|		КОГДА Строки.Валюта = &ВалютаРеглУчета
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал)
	|	КОНЕЦ                                                               КАК СчетКт,
	|	Строки.ПодотчетноеЛицо                                              КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО                                                        КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО                                                        КАК СубконтоКт3,
	|	Строки.Сумма                                                        КАК ВалютнаяСуммаКт,
	|	0                                                                   КАК КоличествоКт,
	|	0                                                                   КАК СуммаНУКт,
	|	0                                                                   КАК СуммаПРКт,
	|	0                                                                   КАК СуммаВРКт,
	|	&СодержаниеОперации                                                 КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРасчетовСПодотчетнымиЛицами КАК Операция
	|	ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРасчетовСПодотчетнымиЛицами.КорректировкаЗадолженности КАК Строки
	|	ПО Строки.Ссылка = ДокументыКОтражению.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиАктивовПассивов КАК СтатьиРасходов
	|		ПО Строки.Статья = СтатьиРасходов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиСчетовУчетаПрочихОпераций КАК НастройкиСчетовУчета
	|		ПО Строки.НастройкаСчетовУчета = НастройкиСчетовУчета.Ссылка
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Строки.Статья) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеЗадолженностиПодотчетногоЛица)
	|";
	
	СодержаниеОперации = СтрШаблон("""%1""", НСтр("ru = 'Списание задолженности подотчетного лица';
													|en = 'Advance holder AR/AP write-off'"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СодержаниеОперации", СодержаниеОперации);
	ТекстыОтражения.Добавить(ТекстЗапроса);
	
#КонецОбласти

#Область ОтнесениеНаДоходыЗадолженностиПередПодотчетнымЛицом // (Дт 71 :: Кт 91)

	ТекстЗапроса =
	"ВЫБРАТЬ //// Отнесение в доходы (Дт 71 :: Кт 91)
	|	Операция.Ссылка                                                     КАК Ссылка,
	|	Операция.Дата                                                       КАК Период,
	|	Операция.Организация                                                КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                                        КАК ИдентификаторСтроки,
	|	Строки.СуммаРегл                                                    КАК Сумма,
	|	Строки.СуммаУпр                                                     КАК СуммаУУ,
	|	НЕОПРЕДЕЛЕНО                                                        КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО                                                        КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|	Строки.Валюта                                                       КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)           КАК НаправлениеДеятельностиДт,
	|	ВЫБОР КОГДА Строки.Валюта = &ВалютаРеглУчета ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал)
	|	КОНЕЦ                                                               КАК СчетДт,
	|	Строки.ПодотчетноеЛицо                                              КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО                                                        КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО                                                        КАК СубконтоДт3,
	|	Строки.Сумма                                                        КАК ВалютнаяСуммаДт,
	|	0                                                                   КАК КоличествоДт,
	|	0                                                                   КАК СуммаНУДт,
	|	0                                                                   КАК СуммаПРДт,
	|	0                                                                   КАК СуммаВРДт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы)                   КАК ВидСчетаКт,
	|	Строки.Статья                                                       КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО                                                        КАК МестоУчетаКт,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)                            КАК ВалютаКт,
	|	Операция.Подразделение                                              КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)           КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)                      КАК СчетКт,
	|	Строки.Статья                                                       КАК СубконтоКт1,
	|	Строки.АналитикаДоходов                                             КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО                                                        КАК СубконтоКт3,
	|	0                                                                   КАК ВалютнаяСуммаКт,
	|	0                                                                   КАК КоличествоКт,
	|	0                                                                   КАК СуммаНУКт,
	|	0                                                                   КАК СуммаПРКт,
	|	0                                                                   КАК СуммаВРКт,
	|	&СодержаниеОперации                                                 КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРасчетовСПодотчетнымиЛицами КАК Операция
	|	ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРасчетовСПодотчетнымиЛицами.КорректировкаЗадолженности КАК Строки
	|	ПО Строки.Ссылка = ДокументыКОтражению.Ссылка
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Строки.Статья) = ТИП(ПланВидовХарактеристик.СтатьиДоходов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка                                                     КАК Ссылка,
	|	Операция.Дата                                                       КАК Период,
	|	Операция.Организация                                                КАК Организация,
	|	НЕОПРЕДЕЛЕНО                                                        КАК ИдентификаторСтроки,
	|	Строки.СуммаРегл                                                    КАК Сумма,
	|	Строки.СуммаУпр                                                     КАК СуммаУУ,
	|	НЕОПРЕДЕЛЕНО                                                        КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО                                                        КАК АналитикаУчетаДт,
	|	Операция.Подразделение                                              КАК МестоУчетаДт,
	|	Строки.Валюта                                                       КАК ВалютаДт,
	|	Операция.Подразделение                                              КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)           КАК НаправлениеДеятельностиДт,
	|	ВЫБОР
	|		КОГДА Строки.Валюта = &ВалютаРеглУчета
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицами)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПодотчетнымиЛицамиВал)
	|	КОНЕЦ                                                               КАК СчетДт,
	|	Строки.ПодотчетноеЛицо                                              КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО                                                        КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО                                                        КАК СубконтоДт3,
	|	Строки.Сумма                                                        КАК ВалютнаяСуммаДт,
	|	0                                                                   КАК КоличествоДт,
	|	0                                                                   КАК СуммаНУДт,
	|	0                                                                   КАК СуммаПРДт,
	|	0                                                                   КАК СуммаВРДт,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы)      КАК ВидСчетаКт,
	|	ВЫБОР
	|		КОГДА НЕ СтатьиДоходов.Ссылка ЕСТЬ NULL
	|			ТОГДА СтатьиДоходов.Ссылка
	|		ИНАЧЕ ЕСТЬNULL(Строки.Статья, НЕОПРЕДЕЛЕНО)
	|	КОНЕЦ                                                               КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО                                                        КАК МестоУчетаКт,
	|	Строки.Валюта                                                       КАК ВалютаКт,
	|	Операция.Подразделение                                              КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)           КАК НаправлениеДеятельностиКт,
	|	ЕСТЬNULL(НастройкиСчетовУчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) КАК СчетКт,
	|	ЕСТЬNULL(НастройкиСчетовУчета.Субконто1, Строки.Статья)             КАК СубконтоКт1,
	|	ЕСТЬNULL(НастройкиСчетовУчета.Субконто2, Строки.АналитикаАктивовПассивов) КАК СубконтоКт2,
	|	ЕСТЬNULL(НастройкиСчетовУчета.Субконто3, НЕОПРЕДЕЛЕНО)              КАК СубконтоКт3,
	|	Строки.Сумма                                                        КАК ВалютнаяСуммаКт,
	|	0                                                                   КАК КоличествоКт,
	|	0                                                                   КАК СуммаНУКт,
	|	0                                                                   КАК СуммаПРКт,
	|	0                                                                   КАК СуммаВРКт,
	|	&СодержаниеОперации                                                 КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРасчетовСПодотчетнымиЛицами КАК Операция
	|	ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРасчетовСПодотчетнымиЛицами.КорректировкаЗадолженности КАК Строки
	|	ПО Строки.Ссылка = ДокументыКОтражению.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиАктивовПассивов КАК СтатьиДоходов
	|		ПО Строки.Статья = СтатьиДоходов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиСчетовУчетаПрочихОпераций КАК НастройкиСчетовУчета
	|		ПО Строки.НастройкаСчетовУчета = НастройкиСчетовУчета.Ссылка
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Строки.Статья) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|	И Операция.ХозяйственнаяОперация =
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтнесениеНаДоходыЗадолженностиПередПодотчетнымЛицом)
	|";
	
	СодержаниеОперации = СтрШаблон("""%1""", НСтр("ru = 'Отнесение на доходы задолженности перед подотчетным лицом';
													|en = 'Recognition of the debt to the advance holder as income'"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СодержаниеОперации", СодержаниеОперации);
	ТекстыОтражения.Добавить(ТекстЗапроса);

#КонецОбласти
	
	//-- Локализация
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
//  необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстыЗапроса = Новый Массив;
	
	//++ Локализация
	
	//-- Локализация
	
	Если ТекстыЗапроса.Количество() Тогда
		ТекстыЗапроса.Добавить("");
	КонецЕсли;
	
	Возврат СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());;
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Локализация

//++ НЕ УТ

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстыЗапросаОтражения = Новый Массив;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения";
	ТекстыЗапросаОтражения.Добавить(ТекстЗапроса);
	
	ТекстыЗапросаОтражения.Добавить(ДоходыИРасходыСервер.ДополнитьТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ЛОЖЬ));
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросаОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- НЕ УТ

//-- Локализация

#КонецОбласти
