
////////////////////////////////////////////////////////////////////////////////
// Модуль "РасхожденияКлиентСервер" содержит процедуры и функции, работающие на клиенте и сервер
// для работы документов отражения расхождений после отгрузки.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция ТипОснованияРеализацияТоваровУслуг(ТипОснованияАктаОРасхождении) Экспорт
	
	Возврат ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.РеализацияТоваровУслуг")
	        ИЛИ ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПустаяСсылка");
	
КонецФункции

Функция ТипОснованияОтгрузкаТоваровКлиенту(ТипОснованияАктаОРасхождении) Экспорт
	
	Возврат ТипОснованияАктаОРасхождении =
				ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ОтгрузкаТоваровКлиенту");
	
КонецФункции

Функция ТипОснованияПередачаТоваровХранителю(ТипОснованияАктаОРасхождении) Экспорт
	
	ТипыОснованияАкта = Новый Массив;
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаТоваровХранителю"));
	//++ НЕ УТ
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаТоваровПереработчику"));
	//-- НЕ УТ	
	
	Возврат ТипыОснованияАкта.Найти(ТипОснованияАктаОРасхождении) <> Неопределено;
	
КонецФункции

Функция ТипОснованияПередачаНаКомиссию(ТипОснованияАктаОРасхождении) Экспорт
	
	Возврат ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаНаКомиссию");
	
КонецФункции

Функция ТипОснованияПриемкаТоваровНаХранение(ТипОснованияАктаОРасхождении) Экспорт
	
	ТипыОснованияАкта = Новый Массив;
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение"));
	//++ НЕ УТКА
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПоступлениеОтДавальца"));
	//-- НЕ УТКА
	
	Возврат ТипыОснованияАкта.Найти(ТипОснованияАктаОРасхождении) <> Неопределено;
	
КонецФункции

Функция ТипОснованияПоступлениеТоваровОтХранителя(ТипОснованияАктаОРасхождении) Экспорт
	
	ТипыОснованияАкта = ТипыОснованияПоступлениеТоваровОтХранителя();
	
	Возврат ТипыОснованияАкта.Найти(ТипОснованияАктаОРасхождении) <> Неопределено;
	
КонецФункции

Функция ТипОснованияПриобретениеТоваровУслуг(ТипОснованияАктаОРасхождении) Экспорт
	
	Возврат ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПриобретениеТоваровУслуг");
	
КонецФункции

Функция ТипОснованияВозвратТоваровОтКлиента(ТипОснованияАктаОРасхождении) Экспорт
	
	Возврат ТипОснованияАктаОРасхождении =
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтКлиента");
	
КонецФункции

Функция ТипОснованияВозвратПоставщику(ТипОснованияАктаОРасхождении) Экспорт
	
	Возврат ТипОснованияАктаОРасхождении =
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратПоставщику");
	
КонецФункции

Функция ТипОснованияОтгрузкаТоваровСХранения(ТипОснованияАктаОРасхождении) Экспорт
	
	Возврат ТипОснованияАктаОРасхождении =
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ОтгрузкаТоваровСХранения");
	
КонецФункции

Функция ТипОснованияПеремещениеТоваров(ДокументСсылка) Экспорт
	
	Возврат ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.АктОРасхожденияхПослеПеремещения");
	
КонецФункции

Функция ТипыОснованияПоступлениеТоваровОтХранителя() Экспорт
	
	ТипыОснования = Новый Массив;
	ТипыОснования.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтХранителя"));
	ТипыОснования.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратОтКомиссионера"));
	//++ НЕ УТ
	ТипыОснования.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПоступлениеТоваровОтПереработчика"));
	ТипыОснования.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтПереработчика"));
	//-- НЕ УТ
	
	Возврат ТипыОснования;
	
КонецФункции

Функция ТипОснованияВозвратОтКомиссионера(ТипОснованияАктаОРасхождении) Экспорт
	
	Возврат ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратОтКомиссионера");
	
КонецФункции

// Все типы оснований акта о расхождениях после отгрузки.
//
// Возвращаемое значение:
//  Массив Из ПеречислениеСсылка.ТипыОснованияАктаОРасхождении -
Функция ТипыОснованийАктаОРасхожденияхПослеОтгрузки() Экспорт
	
	ТипыОснованияАкта = Новый Массив;
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.РеализацияТоваровУслуг"));
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратПоставщику"));
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ОтгрузкаТоваровСХранения"));
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаТоваровХранителю"));
	//++ НЕ УТ
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаТоваровПереработчику"));
	//-- НЕ УТ
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаНаКомиссию"));
	//++ НЕ УТКА
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаДавальцу"));
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратДавальцу"));
	//-- НЕ УТКА
	
	Возврат ТипыОснованияАкта;
	
КонецФункции

// Все типы оснований акта о расхождениях после приемки.
//
// Возвращаемое значение:
//  Массив Из ПеречислениеСсылка.ТипыОснованияАктаОРасхождении -
Функция ТипыОснованийАктаОРасхожденияхПослеПриемки() Экспорт
	
	ТипыОснованияАкта = ТипыОснованияПоступлениеТоваровОтХранителя(); // Массив -
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтКлиента"));
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПриобретениеТоваровУслуг"));
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение"));
	//++ НЕ УТКА
	ТипыОснованияАкта.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПоступлениеОтДавальца"));
	//-- НЕ УТКА
	
	Возврат ТипыОснованияАкта;
	
КонецФункции

Функция УчетНДСНеВедется(ТипОснованияАктаОРасхождении) Экспорт
	
	ТипыОснования = Новый Массив;
	ТипыОснования.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ОтгрузкаТоваровКлиенту"));
	ТипыОснования.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ОтгрузкаТоваровСХранения"));
	ТипыОснования.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение"));
	//++ НЕ УТКА
	ТипыОснования.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаДавальцу"));
	ТипыОснования.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратДавальцу"));
	ТипыОснования.Добавить(
		ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПоступлениеОтДавальца"));
	//-- НЕ УТКА
	
	Возврат ТипыОснования.Найти(ТипОснованияАктаОРасхождении) <> Неопределено;
	
КонецФункции

Функция РассчитатьИтоговыеКоличественныеПоказателиФормы(ТаблицаТовары) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Недостачи", 0);
	Результат.Вставить("Излишки", 0);
	Результат.Вставить("Расхождения", 0);
	Результат.Вставить("СтрокСРасхождениями", 0);
	
	Результат.Недостачи = 0;
	Результат.Излишки   = 0;
	
	Для Каждого СтрокаТовары Из ТаблицаТовары Цикл
		
		Если СтрокаТовары.КоличествоУпаковокРасхождения <> 0 Тогда
			Результат.СтрокСРасхождениями = Результат.СтрокСРасхождениями + 1;
		КонецЕсли;
		
		Если СтрокаТовары.КоличествоУпаковокРасхождения > 0 Тогда
			Результат.Излишки = Результат.Излишки + СтрокаТовары.КоличествоУпаковокРасхождения;
		ИначеЕсли СтрокаТовары.КоличествоУпаковокРасхождения < 0 Тогда
			Результат.Недостачи = Результат.Недостачи + (-СтрокаТовары.КоличествоУпаковокРасхождения);
		КонецЕсли;
		
	КонецЦикла;
	
	Результат.Вставить("ЕстьИзлишки", Результат.Излишки > 0);
	Результат.Вставить("ЕстьНедостачи", Результат.Недостачи > 0);
	
	Результат.Расхождения = ТаблицаТовары.Итог("Количество") - ТаблицаТовары.Итог("КоличествоПоДокументу");
	
	Возврат Результат;
	
КонецФункции

Функция СтруктураДействийПриИзмененииКоличестваУпаковок() Экспорт

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьРасхождения");

	Возврат СтруктураДействий;

КонецФункции

Функция СтруктураДействийЗаполнитьЗависимыеРеквизитыТоваров() Экспорт

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьРасхождения");
	СтруктураДействий.Вставить("УстановитьПризнакДляЗаполненияТекстовогоОписания");
	СтруктураДействий.Вставить("ПризнакНаличиеКомментария");

	Возврат СтруктураДействий;

КонецФункции

Функция СтруктураДействийЗаполнитьЗависимыеРеквизитыТоваровРасхожденияПослеОтгрузки() Экспорт

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьРасхожденияПослеОтгрузки");
	СтруктураДействий.Вставить("УстановитьПризнакДляЗаполненияТекстовогоОписания");
	СтруктураДействий.Вставить("ПризнакНаличиеКомментарияОтгрузка");

	Возврат СтруктураДействий;

КонецФункции

Функция СтруктураДействийЗаполнитьЗависимыеРеквизитыТоваровРасхожденияПослеПриемки() Экспорт

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьРасхожденияПослеПриемки");
	СтруктураДействий.Вставить("ПризнакНаличиеКомментарияПриемка");

	Возврат СтруктураДействий;

КонецФункции

// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Изменяемая форма, содержит в том числе:
// 		* Объект - ДокументОбъект - 
// 		* Элементы - ЭлементыФормы - 
// 
Процедура УправлениеДоступностью(Форма) Экспорт
	
	ВлияющиеРеквизитыДоступны = Форма.ДокументыОснования.Количество() = 0;
	
	Если ТипОснованияПеремещениеТоваров(Форма.Объект.Ссылка) Тогда
		
		ПодборДокументовОснованийДоступен = ЗначениеЗаполнено(Форма.Объект.СкладОтправитель)
		                                    И ЗначениеЗаполнено(Форма.Объект.СкладПолучатель)
		                                    И ЗначениеЗаполнено(Форма.Объект.Организация);
		
	ИначеЕсли ТипОснованияРеализацияТоваровУслуг(Форма.Объект.ТипОснованияАктаОРасхождении)
		Или ТипОснованияОтгрузкаТоваровКлиенту(Форма.Объект.ТипОснованияАктаОРасхождении) Тогда
		
		ПодборДокументовОснованийДоступен = (Не Форма.ИспользоватьСоглашенияСКлиентами Или ЗначениеЗаполнено(Форма.Объект.Соглашение))
		                                    И ЗначениеЗаполнено(Форма.Объект.Партнер)
		                                    И ЗначениеЗаполнено(Форма.Объект.Контрагент)
		                                    И ЗначениеЗаполнено(Форма.Объект.Организация)
		                                    И ?(Форма.Элементы.Договор.Видимость И Форма.ИспользоватьСоглашенияСКлиентами,
		                                        ЗначениеЗаполнено(Форма.Объект.Договор), Истина)
		                                    И ЗначениеЗаполнено(Форма.Объект.Валюта)
		                                    И (ЗначениеЗаполнено(Форма.Объект.НалогообложениеНДС)
		                                        Или Форма.Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию")
		                                        Или Форма.Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПередачаНаХранениеСПравомПродажи"))
		                                    И ЗначениеЗаполнено(Форма.Объект.ХозяйственнаяОперация);
		
	ИначеЕсли ТипОснованияОтгрузкаТоваровСХранения(Форма.Объект.ТипОснованияАктаОРасхождении)
		Или ТипОснованияПередачаТоваровХранителю(Форма.Объект.ТипОснованияАктаОРасхождении)
		Или ТипОснованияПоступлениеТоваровОтХранителя(Форма.Объект.ТипОснованияАктаОРасхождении)
		Или ТипОснованияПередачаНаКомиссию(Форма.Объект.ТипОснованияАктаОРасхождении)
		Или ТипОснованияВозвратОтКомиссионера(Форма.Объект.ТипОснованияАктаОРасхождении)
		//++ НЕ УТКА
		Или Форма.Объект.ТипОснованияАктаОРасхождении =
							ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПоступлениеОтДавальца")
		Или Форма.Объект.ТипОснованияАктаОРасхождении =
							ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаДавальцу")
		Или Форма.Объект.ТипОснованияАктаОРасхождении =
							ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратДавальцу")
		//-- НЕ УТКА
		Или Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение") Тогда
		
		ПодборДокументовОснованийДоступен = ?(ТипОснованияПередачаТоваровХранителю(Форма.Объект.ТипОснованияАктаОРасхождении),
												Не Форма.ИспользоватьСоглашенияСКлиентами
													Или ЗначениеЗаполнено(Форма.Объект.Соглашение),
												Истина)
							И ЗначениеЗаполнено(Форма.Объект.Партнер)
							И ЗначениеЗаполнено(Форма.Объект.Контрагент)
							И ЗначениеЗаполнено(Форма.Объект.Организация)
							И ЗначениеЗаполнено(Форма.Объект.Валюта)
							И ЗначениеЗаполнено(Форма.Объект.ХозяйственнаяОперация)
							И ?(ТипОснованияВозвратОтКомиссионера(Форма.Объект.ТипОснованияАктаОРасхождении),
								ЗначениеЗаполнено(Форма.Объект.Договор), Истина);
		
	Иначе
		
		ПодборДокументовОснованийДоступен = ЗначениеЗаполнено(Форма.Объект.Партнер)
		                                    И ЗначениеЗаполнено(Форма.Объект.Контрагент)
		                                    И ЗначениеЗаполнено(Форма.Объект.Организация)
		                                    И ЗначениеЗаполнено(Форма.Объект.Валюта)
		                                    И (ЗначениеЗаполнено(Форма.Объект.НалогообложениеНДС)
		                                        Или Форма.Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера")
		                                        Или Форма.Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратОтХранителя"))
		                                    И ЗначениеЗаполнено(Форма.Объект.ХозяйственнаяОперация);
		
	КонецЕсли;
	
	ПодборТоваровДоступен = ПодборДокументовОснованийДоступен И (Форма.ДокументыОснования.Количество() > 0);
	
	Форма.Элементы.Организация.ТолькоПросмотр           = НЕ ВлияющиеРеквизитыДоступны;
	
	Если Не ТипОснованияПеремещениеТоваров(Форма.Объект.Ссылка) Тогда
		
		Форма.Элементы.Партнер.ТолькоПросмотр               = НЕ ВлияющиеРеквизитыДоступны;
		Форма.Элементы.Контрагент.ТолькоПросмотр            = НЕ ВлияющиеРеквизитыДоступны;
		Форма.Элементы.Соглашение.ТолькоПросмотр            = НЕ ВлияющиеРеквизитыДоступны;
		Форма.Элементы.Договор.ТолькоПросмотр               = НЕ ВлияющиеРеквизитыДоступны;
		Форма.Элементы.Валюта.ТолькоПросмотр                = НЕ ВлияющиеРеквизитыДоступны;
		Форма.Элементы.ЦенаВключаетНДС.ТолькоПросмотр       = НЕ ВлияющиеРеквизитыДоступны;
		Форма.Элементы.НалогообложениеНДС.ТолькоПросмотр    = НЕ ВлияющиеРеквизитыДоступны;
		Форма.Элементы.ХозяйственнаяОперация.ТолькоПросмотр = НЕ ВлияющиеРеквизитыДоступны;
		Форма.Элементы.ХозяйственнаяОперация.ТолькоПросмотр = НЕ ВлияющиеРеквизитыДоступны
									Или ТипОснованияОтгрузкаТоваровСХранения(Форма.Объект.ТипОснованияАктаОРасхождении)
									Или ТипОснованияПередачаТоваровХранителю(Форма.Объект.ТипОснованияАктаОРасхождении)
									//++ НЕ УТКА
									Или Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПоступлениеОтДавальца")
									Или Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаДавальцу")
									Или Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратДавальцу")
									//-- НЕ УТКА
									Или Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение");
		
	КонецЕсли;
	
	ЭлементИзменитьДокументыОснования     = Форма.Элементы.ИзменитьДокументыОснования; // ДекорацияФормы
	ЭлементТоварыЗаполнитьПоОснованиям    = Форма.Элементы.ТоварыЗаполнитьПоОснованиям; // КнопкаКоманднойПанели
	ЭлементТоварыЗагрузитьИзВнешнегоФайла = Форма.Элементы.ТоварыЗагрузитьИзВнешнегоФайла; // КнопкаКоманднойПанели
	ЭлементТоварыОткрытьПодбор            = Форма.Элементы.ТоварыОткрытьПодбор; // КнопкаКоманднойПанели
	ЭлементТоварыДобавить                 = Форма.Элементы.ТоварыДобавить; // КнопкаКоманднойПанели
	ЭлементТовары                         = Форма.Элементы.Товары; // КнопкаКоманднойПанели
	
	ЭлементИзменитьДокументыОснования.Доступность     = ПодборДокументовОснованийДоступен;
	ЭлементТоварыЗаполнитьПоОснованиям.Доступность    = ПодборТоваровДоступен;
	ЭлементТоварыЗагрузитьИзВнешнегоФайла.Доступность = ПодборТоваровДоступен;
	ЭлементТоварыОткрытьПодбор.Доступность            = ПодборТоваровДоступен;
	ЭлементТоварыДобавить.Доступность                 = ПодборТоваровДоступен;
	ЭлементТовары.Доступность                         = ПодборТоваровДоступен;
	
	Если ТипОснованияПеремещениеТоваров(Форма.Объект.Ссылка) Тогда
		// Не обрабатывается
	ИначеЕсли ТипОснованияРеализацияТоваровУслуг(Форма.Объект.ТипОснованияАктаОРасхождении) Тогда 
		
		УправлениеСпособомОтраженияРасхожденияРеализация(Форма);
	
	ИначеЕсли ТипОснованияОтгрузкаТоваровКлиенту(Форма.Объект.ТипОснованияАктаОРасхождении) Тогда
		
		УправлениеСпособомОтраженияРасхожденияОтгрузкаТоваровКлиенту(Форма);
		
	ИначеЕсли ТипОснованияПриобретениеТоваровУслуг(Форма.Объект.ТипОснованияАктаОРасхождении) Тогда
		
		УправлениеСпособомОтраженияРасхожденияПриобретение(Форма);
		
	ИначеЕсли ТипОснованияПередачаТоваровХранителю(Форма.Объект.ТипОснованияАктаОРасхождении)
		Или ТипОснованияПередачаНаКомиссию(Форма.Объект.ТипОснованияАктаОРасхождении) Тогда
		
		УправлениеСпособомОтраженияРасхожденияПередача(Форма);
		
	ИначеЕсли ТипОснованияОтгрузкаТоваровСХранения(Форма.Объект.ТипОснованияАктаОРасхождении)
		//++ НЕ УТКА
		Или Форма.Объект.ТипОснованияАктаОРасхождении =
			ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаДавальцу")
		Или Форма.Объект.ТипОснованияАктаОРасхождении =
			ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратДавальцу")
		//-- НЕ УТКА
		Тогда
		
		УправлениеСпособомОтраженияРасхожденияОтгрузка(Форма);
		
	ИначеЕсли ТипОснованияПриемкаТоваровНаХранение(Форма.Объект.ТипОснованияАктаОРасхождении)
		Или ТипОснованияПоступлениеТоваровОтХранителя(Форма.Объект.ТипОснованияАктаОРасхождении) Тогда
		
		УправлениеСпособомОтраженияРасхожденияПриемкаИПоступление(Форма);
		
	КонецЕсли;
	
	НадписьОформитьДокументы = Форма.Элементы.ОформитьДокументы;//РасширениеДекорацииФормыДляНадписи
	
	Если (Форма.СтрокиАктаСодержатДействияТребующиеОбработки
	      И ЗначениеЗаполнено(Форма.Объект.СпособОтраженияРасхождений)
	      И (Форма.Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыАктаОРасхождениях.КВыполнению")
	      Или Форма.Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыАктаОРасхождениях.Отработано"))) Тогда
	      	
		НадписьОформитьДокументы.Гиперссылка = Истина;
		НадписьОформитьДокументы.Заголовок   = НСтр("ru = 'Оформить документы';
													|en = 'Issue documents'");
		НадписьОформитьДокументы.Доступность = Истина;
		
	Иначе
		
		НадписьОформитьДокументы.Гиперссылка = Ложь;
		НадписьОформитьДокументы.Доступность = Ложь;
		
		НадписьОформитьДокументы.Заголовок  = НСтр("ru = 'Оформление документов доступно при наличии расхождений в статусах ""Отрабатывается"" и ""Отработано""';
													|en = 'Document registration is available if there are discrepancies in the ""In progress"" and ""Processed"" statuses'");
		
	КонецЕсли;
	
	СобытияФормКлиентСервер.ПриИзмененииЭлемента(Форма, Форма.Элементы.ОформитьДокументы.Имя);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы,
	                                                             "ТоварыЗаполнитьФактПоПриемке",
	                                                             "Доступность",
	                                                             Форма.Объект.Товары.Количество() > 0);
	
	ЭлементТоварыИзменитьДействие = Форма.Элементы.ТоварыИзменитьДействие; // КнопкаКоманднойПанели
	
	ЭлементТоварыИзменитьДействие.Доступность = Форма.ЕстьИзлишки Или Форма.ЕстьНедостачи;
	
КонецПроцедуры

// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Изменяемая форма
// 
Процедура УправлениеСпособомОтраженияРасхожденияПриобретение(Форма)
	
	Объект                        = Форма.Объект;
	Элементы                      = Форма.Элементы;
	ДоступныеСпособыОтработкиАкта = Форма.ДоступныеСпособыОтработкиАкта;
	
	Если Не (Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика")
			Или Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути")
			Или Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаНеотфактурованнаяПоставка")
			Или Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки")) Тогда
				
		Элементы.СпособОтраженияРасхожденийИзменениеДокумента.Видимость = Истина;
		Элементы.СпособОтраженияРасхожденийКорректировкаПоступления.Видимость = Ложь;
		Элементы.ГруппаИнформацияОДоступностиСпособаОтражения.Видимость = Ложь;	
		
	Иначе
		
		Элементы.СпособОтраженияРасхожденийИзменениеДокумента.Доступность = 
			ДоступныеСпособыОтработкиАкта.НайтиПоЗначению(
				ПредопределенноеЗначение("Перечисление.СпособыОтраженияАктовОРасхожденияПослеПоступления.ИсправлениеПервичныхДокументов")) <> Неопределено;
		
		Элементы.СпособОтраженияРасхожденийКорректировкаПоступления.Доступность = 
			ДоступныеСпособыОтработкиАкта.НайтиПоЗначению(
				ПредопределенноеЗначение("Перечисление.СпособыОтраженияАктовОРасхожденияПослеПоступления.ОформлениеКорректировокПоступления")) <> Неопределено;
		
	КонецЕсли;
	
	ТребуетсяСообщениеОНесоответствииСпособа = Ложь;
	ОпределитьНеобходимостьОНесоответствииСпособа(Объект, ДоступныеСпособыОтработкиАкта, ТребуетсяСообщениеОНесоответствииСпособа);
	
	ТекстПояснения = ТекстПоясненияСпособОтработкиАктПослеПриобретения(Форма, ТребуетсяСообщениеОНесоответствииСпособа);
	НастроитьЭлементыФормыСпособОтражения(Объект, Элементы, ТребуетсяСообщениеОНесоответствииСпособа, ТекстПояснения);
	
КонецПроцедуры

// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Изменяемая форма
// 
Процедура УправлениеСпособомОтраженияРасхожденияПередача(Форма)
	
	Объект                        = Форма.Объект;
	Элементы                      = Форма.Элементы;
	ДоступныеСпособыОтработкиАкта = Форма.ДоступныеСпособыОтработкиАкта;
	
	Элементы.СпособОтраженияРасхожденийИзменениеДокумента.Доступность = 
		ДоступныеСпособыОтработкиАкта.НайтиПоЗначению(
			ПредопределенноеЗначение("Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ИсправлениеПервичныхДокументов")) <> Неопределено;

	Элементы.СпособОтраженияРасхожденийКорректировкаИсправление.Доступность = 
		Форма.ИспользоватьИсправлениеДокументов
		И ДоступныеСпособыОтработкиАкта.НайтиПоЗначению(
			ПредопределенноеЗначение("Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировкиКакИсправлениеПервичныхДокументов")) <> Неопределено;
	
	ТребуетсяСообщениеОНесоответствииСпособа = Ложь;
	ОпределитьНеобходимостьОНесоответствииСпособа(Объект, ДоступныеСпособыОтработкиАкта, ТребуетсяСообщениеОНесоответствииСпособа);
	
	ТекстПояснения = ТекстПоясненияСпособОтработкиАктПослеПередачи(Форма, ТребуетсяСообщениеОНесоответствииСпособа);
	НастроитьЭлементыФормыСпособОтражения(Объект, Элементы, ТребуетсяСообщениеОНесоответствииСпособа, ТекстПояснения);
	
КонецПроцедуры

// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Изменяемая форма
//
Процедура УправлениеСпособомОтраженияРасхожденияОтгрузка(Форма)
	
	Объект                        = Форма.Объект;
	Элементы                      = Форма.Элементы;
	ДоступныеСпособыОтработкиАкта = Форма.ДоступныеСпособыОтработкиАкта;
	
	Элементы.СпособОтраженияРасхожденийИзменениеДокумента.Доступность =
		ДоступныеСпособыОтработкиАкта.НайтиПоЗначению(
			ПредопределенноеЗначение("Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ИсправлениеПервичныхДокументов")) <> Неопределено;

	Элементы.СпособОтраженияРасхожденийКорректировкаИсправление.Доступность =
		Форма.ИспользоватьИсправлениеДокументов
		И ДоступныеСпособыОтработкиАкта.НайтиПоЗначению(
			ПредопределенноеЗначение("Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировкиКакИсправлениеПервичныхДокументов")) <> Неопределено;
	
	ТребуетсяСообщениеОНесоответствииСпособа = Ложь;
	ОпределитьНеобходимостьОНесоответствииСпособа(Объект, ДоступныеСпособыОтработкиАкта, ТребуетсяСообщениеОНесоответствииСпособа);
	
	ТекстПояснения = ТекстПоясненияСпособОтработкиАктПослеОтгрузки(Форма, ТребуетсяСообщениеОНесоответствииСпособа);
	НастроитьЭлементыФормыСпособОтражения(Объект, Элементы, ТребуетсяСообщениеОНесоответствииСпособа, ТекстПояснения);
	
КонецПроцедуры

// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - Изменяемая форма
// 
Процедура УправлениеСпособомОтраженияРасхожденияПриемкаИПоступление(Форма)
	
	Объект                        = Форма.Объект;
	Элементы                      = Форма.Элементы;
	ДоступныеСпособыОтработкиАкта = Форма.ДоступныеСпособыОтработкиАкта;
	
	Элементы.СпособОтраженияРасхожденийИзменениеДокумента.Доступность = 
		ДоступныеСпособыОтработкиАкта.НайтиПоЗначению(
			ПредопределенноеЗначение("Перечисление.СпособыОтраженияАктовОРасхожденияПослеПоступления.ИсправлениеПервичныхДокументов")) <> Неопределено;

	Элементы.СпособОтраженияРасхожденийКорректировкаПоступления.Доступность = 
		Форма.ИспользоватьИсправлениеДокументов
		И ДоступныеСпособыОтработкиАкта.НайтиПоЗначению(
			ПредопределенноеЗначение("Перечисление.СпособыОтраженияАктовОРасхожденияПослеПоступления.ОформлениеКорректировокПоступления")) <> Неопределено;
	
	ТребуетсяСообщениеОНесоответствииСпособа = Ложь;
	ОпределитьНеобходимостьОНесоответствииСпособа(Объект, ДоступныеСпособыОтработкиАкта, ТребуетсяСообщениеОНесоответствииСпособа);
	
	ТекстПояснения = ТекстПоясненияСпособОтработкиАктПослеПриемкиИПоступления(Форма, ТребуетсяСообщениеОНесоответствииСпособа);
	НастроитьЭлементыФормыСпособОтражения(Объект, Элементы, ТребуетсяСообщениеОНесоответствииСпособа, ТекстПояснения);
	
КонецПроцедуры

Процедура ОпределитьНеобходимостьОНесоответствииСпособа(Объект, ДоступныеСпособыОтработкиАкта, ТребуетсяСообщениеОНесоответствииСпособа);
	
	Если ЗначениеЗаполнено(Объект.СпособОтраженияРасхождений)
		И ДоступныеСпособыОтработкиАкта.НайтиПоЗначению(Объект.СпособОтраженияРасхождений) = Неопределено Тогда
			
		Если Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыАктаОРасхождениях.Отработано")
			Или Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыАктаОРасхождениях.КВыполнению") Тогда
			
			ТребуетсяСообщениеОНесоответствииСпособа = Истина;
			
		Иначе
		
			Если ДоступныеСпособыОтработкиАкта.Количество() = 0 Тогда
				Объект.СпособОтраженияРасхождений = Неопределено;
			Иначе
				Объект.СпособОтраженияРасхождений = ДоступныеСпособыОтработкиАкта[0].Значение;
			КонецЕсли;
				
		КонецЕсли;	
		
	КонецЕсли;
	
КонецПроцедуры

Процедура НастроитьЭлементыФормыСпособОтражения(Объект, Элементы, ТребуетсяСообщениеОНесоответствииСпособа, ТекстПояснения)
	
	Элементы.ГруппаИнформацияОДоступностиСпособаОтражения.Видимость = Не ПустаяСтрока(ТекстПояснения);
	
	ЭлементИнформацияОДоступностиСпособаОтражения = Элементы.ИнформацияОДоступностиСпособаОтражения; // ДекорацияФормы
	ЭлементГруппаСпособОтражения = Элементы.ГруппаСпособОтражения; // ГруппаФормы
	
	ЭлементИнформацияОДоступностиСпособаОтражения.Заголовок       = ТекстПояснения;
	
	ЭлементГруппаСпособОтражения.Доступность = Не (Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыАктаОРасхождениях.Отработано")
	                                            Или Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыАктаОРасхождениях.КВыполнению"));
	
	Если ТребуетсяСообщениеОНесоответствииСпособа Тогда
		Элементы.СтраницыИнформацияСпособыОтражения.ТекущаяСтраница = Элементы.СтраницаПроблемаИнформацияСпособОтражения;
	Иначе
		Элементы.СтраницыИнформацияСпособыОтражения.ТекущаяСтраница = Элементы.СтраницаОбычнаяИнформацияСпособОтражения;
	КонецЕсли;
	
КонецПроцедуры

Процедура УправлениеСпособомОтраженияРасхожденияРеализация(Форма)

	Объект                        = Форма.Объект;
	Элементы                      = Форма.Элементы;
	ДоступныеСпособыОтработкиАкта = Форма.ДоступныеСпособыОтработкиАкта;
	
	Если Объект.ХозяйственнаяОперация = 
					ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту")
			Или Объект.ХозяйственнаяОперация = 
					ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПереходПраваСобственности")
		Тогда

		Элементы.СпособОтраженияРасхожденийИзменениеДокумента.Доступность = 
			ДоступныеСпособыОтработкиАкта.НайтиПоЗначению(
				ПредопределенноеЗначение("Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ИсправлениеПервичныхДокументов")) <> Неопределено;

		Элементы.СпособОтраженияРасхожденийКорректировкаИсправление.Доступность = 
			ДоступныеСпособыОтработкиАкта.НайтиПоЗначению(
				ПредопределенноеЗначение("Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировкиКакИсправлениеПервичныхДокументов")) <> Неопределено;
				
		Элементы.СпособОтраженияРасхожденийКорректировкаПоСогласованию.Доступность = 
			ДоступныеСпособыОтработкиАкта.НайтиПоЗначению(
				ПредопределенноеЗначение("Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировкиПоСогласованию")) <> Неопределено;
		
		Элементы.СпособОтраженияРасхожденийКорректировкаНовые.Доступность = 
			ДоступныеСпособыОтработкиАкта.НайтиПоЗначению(
				ПредопределенноеЗначение("Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировокКакНовыеПервичныеДокументы")) <> Неопределено;

		Если Объект.ХозяйственнаяОперация =
				ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПереходПраваСобственности")
			Тогда
	
			Если Элементы.СпособОтраженияРасхожденийИзменениеДокумента.СписокВыбора.Количество() > 0 Тогда
				Элементы.СпособОтраженияРасхожденийИзменениеДокумента.СписокВыбора[0].Представление =
					НСтр("ru = 'Изменить документы';
						|en = 'Change documents'");
			КонецЕсли;
	
			Если Элементы.СпособОтраженияРасхожденийКорректировкаИсправление.СписокВыбора.Количество() > 0 Тогда
				Элементы.СпособОтраженияРасхожденийКорректировкаИсправление.СписокВыбора[0].Представление =
					НСтр("ru = 'Оформить корректировку документов (исправление ошибок)';
						|en = 'Register document adjustment (errors correction)'");
			КонецЕсли;
	
			Если Элементы.СпособОтраженияРасхожденийКорректировкаПоСогласованию.СписокВыбора.Количество() > 0 Тогда
				Элементы.СпособОтраженияРасхожденийКорректировкаПоСогласованию.СписокВыбора[0].Представление =
					НСтр("ru = 'Оформить корректировку документов (по согласованию сторон)';
						|en = 'Register document adjustment (as agreed by the parties)'");
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		Элементы.СпособОтраженияРасхожденийИзменениеДокумента.Видимость       = Истина;
		Элементы.СпособОтраженияРасхожденийКорректировкаИсправление.Видимость = Ложь;
		Элементы.СпособОтраженияРасхожденийКорректировкаПоСогласованию.Видимость = Ложь;

		Элементы.СпособОтраженияРасхожденийКорректировкаНовые.Видимость       = Ложь;
		Элементы.ГруппаИнформацияОДоступностиСпособаОтражения.Видимость       = Ложь;
		
	КонецЕсли;

	ТребуетсяСообщениеОНесоответствииСпособа = Ложь;
	ОпределитьНеобходимостьОНесоответствииСпособа(Объект, ДоступныеСпособыОтработкиАкта, ТребуетсяСообщениеОНесоответствииСпособа);
	
	ТекстПояснения = ТекстПоясненияСпособОтработкиАктПослеРеализации(Форма, ТребуетсяСообщениеОНесоответствииСпособа);
	НастроитьЭлементыФормыСпособОтражения(Объект, Элементы, ТребуетсяСообщениеОНесоответствииСпособа, ТекстПояснения);

КонецПроцедуры

Процедура УправлениеСпособомОтраженияРасхожденияОтгрузкаТоваровКлиенту(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Элементы.СпособОтраженияРасхожденийИзменениеДокумента.СписокВыбора.Количество() > 0 Тогда
		Элементы.СпособОтраженияРасхожденийИзменениеДокумента.СписокВыбора[0].Представление =
			НСтр("ru = 'Изменить отгрузку товаров клиенту';
				|en = 'Change goods shipment to customer'");
	КонецЕсли;

	Если Элементы.СпособОтраженияРасхожденийКорректировкаИсправление.СписокВыбора.Количество() > 0 Тогда
		Элементы.СпособОтраженияРасхожденийКорректировкаИсправление.СписокВыбора[0].Представление =
			НСтр("ru = 'Оформить исправление отгрузки товаров клиенту';
				|en = 'Register an adjustment of goods shipment to customer'");
	КонецЕсли;

	Элементы.СпособОтраженияРасхожденийКорректировкаПоСогласованию.Видимость = Ложь;
	Элементы.СпособОтраженияРасхожденийКорректировкаНовые.Видимость          = Ложь;
	
	НастроитьЭлементыФормыСпособОтражения(Форма.Объект, Элементы, Ложь, "");
	
КонецПроцедуры

Функция ТекстПоясненияСпособОтработкиАктПослеПриобретения(Форма, ТребуетсяСообщениеОНесоответствииСпособа)
	
	ТекстПояснения = "";
	
	Если Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.Количество() = 0 Тогда
		
		Если Форма.СтатусЭДООснования = "ВОбработке" Тогда
			
			ТекстПояснения = НСтр("ru = 'Документ-основание участвует в ЭДО, но контрагент пока не отклонил и не подтвердил документ.
										|Отработка расхождений недоступна.';
										|en = 'The base document is used in EDI, but the counterparty has neither rejected nor confirmed the document.
										|Discrepancy processing is not available.'");
			
		КонецЕсли;
		
	ИначеЕсли Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.НайтиПоЗначению(
			ПредопределенноеЗначение("Перечисление.СпособыОтраженияАктовОРасхожденияПослеПоступления.ОформлениеКорректировокПоступления")) = Неопределено 
			И Форма.ИспользоватьКорректировкиПриобретений Тогда
			
			ТекстПояснения = НСтр("ru = 'Электронный документ по приобретению был отклонен. Возможно только изменение поступления.';
									|en = 'Electronic purchase document was rejected. You can only change receipt.'");
	
	ИначеЕсли Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.НайтиПоЗначению(
		ПредопределенноеЗначение("Перечисление.СпособыОтраженияАктовОРасхожденияПослеПоступления.ИсправлениеПервичныхДокументов")) = Неопределено Тогда
			
		Если Не Форма.ИспользоватьКорректировкиПриобретений  Тогда
	
			ТекстПояснения = НСтр("ru = 'Электронный документ по приобретению подтвержден. Возможна только корректировка приобретения,
			                     |но их использование отключено.';
			                     |en = 'Electronic purchase document is confirmed. You can only create purchase adjustments,
			                     |but cannot use it.'");
			                             
		Иначе
			
			ТекстПояснения = НСтр("ru = 'Электронный документ по приобретению подтвержден. Возможна только корректировка приобретения.';
									|en = 'Electronic purchase document is confirmed. You can only create purchase adjustments.'");
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТребуетсяСообщениеОНесоответствииСпособа Тогда
		
		СообщениеОНесоответствииСпособа = НСтр("ru = 'Текущий способ отражения некорректен. Изменить его можно в статусе ""Не согласовано"" или ""Обеспечивается""';
												|en = 'The current record method is incorrect. You can change it in the ""Not approved"" or the ""Supplied"" status'");
		
		ТекстПояснения = ТекстПояснения + Символы.ПС + СообщениеОНесоответствииСпособа;
	
	КонецЕсли;

	Возврат ТекстПояснения;

КонецФункции

Функция ТекстПоясненияСпособОтработкиАктПослеРеализации(Форма, ТребуетсяСообщениеОНесоответствииСпособа)

	ТекстПояснения = "";

	Если Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.Количество() = 0 Тогда
			
		Если (Форма.СтатусЭДООснования = "ВОбработке"
			Или Форма.СтатусЭДООснования = "Получен"
			Или Форма.СтатусЭДООснования = "ОтклонениеВОбработке"
			Или Форма.СтатусЭДООснования = "УтверждениеВОбработке")Тогда
		
			ТекстПояснения = НСтр("ru = 'Документ-основание участвует в ЭДО, но покупатель пока не отклонил и не подтвердил документ.
			                             |Отработка расхождений недоступна.';
			                             |en = 'The base document is used in EDI, but the customer has neither rejected nor confirmed the document.
			                             |Discrepancy processing is not available.'");
		
		КонецЕсли;
			
	ИначеЕсли Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.НайтиПоЗначению(
				ПредопределенноеЗначение("Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ИсправлениеПервичныхДокументов")) = Неопределено Тогда
				
		Если Не Форма.ИспользоватьКорректировкиРеализаций  Тогда
	
			ТекстПояснения = НСтр("ru = 'Электронный документ по реализации подтвержден покупателем. Возможна только корректировка реализации,
			                             |но их использование отключено.';
			                             |en = 'Electronic sale document is confirmed by the customer. You can create sales adjustments for it,
			                             |but cannot use it.'");
			                             
		Иначе
			
			ТекстПояснения = НСтр("ru = 'Электронный документ по реализации подтвержден покупателем. Возможна только корректировка реализации.';
									|en = 'An electronic sales document is confirmed by the customer. You can only create sales adjustments for it.'");
			
		КонецЕсли;
		
	ИначеЕсли Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.НайтиПоЗначению(
				ПредопределенноеЗначение("Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировкиКакИсправлениеПервичныхДокументов")) = Неопределено 
			И Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.НайтиПоЗначению(
				ПредопределенноеЗначение("Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировокКакНовыеПервичныеДокументы")) = Неопределено 
			И Форма.ИспользоватьКорректировкиРеализаций Тогда
				
			ТекстПояснения = НСтр("ru = 'Электронный документ по реализации отклонен покупателем. Возможно только изменение реализации.';
									|en = 'Electronic purchase document is rejected by the customer. You can only change implementation.'");
		
	КонецЕсли;
	
	Если ТребуетсяСообщениеОНесоответствииСпособа Тогда
		
		СообщениеОНесоответствииСпособа = НСтр("ru = 'Текущий способ отражения некорректен. Изменить его можно в статусе ""Не согласовано"" или ""Обеспечивается""';
												|en = 'The current record method is incorrect. You can change it in the ""Not approved"" or the ""Supplied"" status'");
		
		ТекстПояснения = ТекстПояснения + Символы.ПС + СообщениеОНесоответствииСпособа;
	
	КонецЕсли;

	Возврат ТекстПояснения;

КонецФункции

Функция ТекстПоясненияСпособОтработкиАктПослеПередачи(Форма, ТребуетсяСообщениеОНесоответствииСпособа)
	
	ТекстПояснения = "";
	ДоступноИсправление = Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.НайтиПоЗначению(
		ПредопределенноеЗначение(
			"Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировкиКакИсправлениеПервичныхДокументов")) <> Неопределено;
	
	Если Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.Количество() = 0 Тогда
		
		Если (Форма.СтатусЭДООснования = "ВОбработке"
			Или Форма.СтатусЭДООснования = "Получен"
			Или Форма.СтатусЭДООснования = "ОтклонениеВОбработке"
			Или Форма.СтатусЭДООснования = "УтверждениеВОбработке") Тогда
			
			ТекстПояснения = НСтр("ru = 'Документ-основание участвует в ЭДО. Для отработки расхождений нужно подписать или отклонить документ.';
									|en = 'The base document is used in EDI. To process discrepancies, sign or reject the document.'");
			
		КонецЕсли;
		
	ИначеЕсли Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.НайтиПоЗначению(
		ПредопределенноеЗначение("Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ИсправлениеПервичныхДокументов")) = Неопределено Тогда
		
		ТекстПояснения = НСтр("ru = 'Электронный документ по передаче подтвержден. Возможно только исправление передачи.';
								|en = 'The electronic transfer document is confirmed. You can only adjust the transfer.'");
		
	ИначеЕсли Не ДоступноИсправление Тогда
			ТекстПояснения = НСтр("ru = 'Электронный документ по передаче был отклонен. Возможно только изменение передачи.';
									|en = 'The electronic transfer document is rejected. You can only edit the transfer.'");
	КонецЕсли;
	
	Если ДоступноИсправление И Не Форма.ИспользоватьИсправлениеДокументов Тогда
		СообщениеОНевозможностиИспользованияИсправления =
			НСтр("ru = 'Для того чтобы оформить исправление отдельным документом, включите функциональную опцию ""Исправление и сторно финансовых документов""';
				|en = 'To issue a correction as a separate document, enable the ""Financial document correction and storno"" functional option'");
		Если ПустаяСтрока(ТекстПояснения) Тогда
			ТекстПояснения = СообщениеОНевозможностиИспользованияИсправления;
		Иначе
			ТекстПояснения = ТекстПояснения + Символы.ПС + СообщениеОНевозможностиИспользованияИсправления;
		КонецЕсли;
	КонецЕсли;
	
	Если ТребуетсяСообщениеОНесоответствииСпособа Тогда
		СообщениеОНесоответствииСпособа = НСтр("ru = 'Текущий способ отражения некорректен. Изменить его можно в статусе ""Не согласовано"" или ""Обеспечивается""';
												|en = 'The current record method is incorrect. You can change it in the ""Not approved"" or the ""Supplied"" status'");
		ТекстПояснения = ТекстПояснения + Символы.ПС + СообщениеОНесоответствииСпособа;
	КонецЕсли;
	
	Возврат ТекстПояснения;
	
КонецФункции

Функция ТекстПоясненияСпособОтработкиАктПослеОтгрузки(Форма, ТребуетсяСообщениеОНесоответствииСпособа)
	
	ТекстПояснения = "";
	ДоступноИсправление = Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.НайтиПоЗначению(
		ПредопределенноеЗначение(
			"Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ОформлениеКорректировкиКакИсправлениеПервичныхДокументов")) <> Неопределено;
	
	Если Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.Количество() = 0 Тогда
		
		Если (Форма.СтатусЭДООснования = "ВОбработке"
			Или Форма.СтатусЭДООснования = "Получен"
			Или Форма.СтатусЭДООснования = "ОтклонениеВОбработке"
			Или Форма.СтатусЭДООснования = "УтверждениеВОбработке") Тогда
			
			ТекстПояснения = НСтр("ru = 'Документ-основание участвует в ЭДО. Для отработки расхождений нужно подписать или отклонить документ.';
									|en = 'The base document is used in EDI. To process discrepancies, sign or reject the document.'");
			
		КонецЕсли;
		
	ИначеЕсли Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.НайтиПоЗначению(
		ПредопределенноеЗначение("Перечисление.СпособыОтраженияРасхожденийАктПриемкиКлиента.ИсправлениеПервичныхДокументов")) = Неопределено Тогда
		
		Если Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ОтгрузкаТоваровСХранения") Тогда
			ТекстПояснения = НСтр("ru = 'Электронный документ по отгрузке подтвержден. Возможно только исправление отгрузки.';
									|en = 'The electronic shipment document is confirmed. You can only adjust the shipment.'");
		//++ НЕ УТКА
		ИначеЕсли Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаДавальцу") Тогда
			ТекстПояснения = НСтр("ru = 'Электронный документ по передаче давальцу подтвержден. Возможно только исправление передачи.';
									|en = 'The electronic document on transfer to material provider is confirmed. You can only adjust the transfer.'");
		ИначеЕсли Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратДавальцу") Тогда
			ТекстПояснения = НСтр("ru = 'Электронный документ по возврату сырья давальцу подтвержден. Возможно только исправление возврата.';
									|en = 'The electronic document on return of raw materials to provider is confirmed. You can only adjust the return.'");
		//-- НЕ УТКА
		КонецЕсли;
		
	ИначеЕсли Не ДоступноИсправление Тогда
		
		Если Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ОтгрузкаТоваровСХранения") Тогда
			ТекстПояснения = НСтр("ru = 'Электронный документ по отгрузке был отклонен. Возможно только изменение отгрузки.';
									|en = 'The electronic shipment document is rejected. You can only edit the shipment.'");
		//++ НЕ УТКА
		ИначеЕсли Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаДавальцу") Тогда
			ТекстПояснения = НСтр("ru = 'Электронный документ по передаче давальцу был отклонен. Возможно только изменение передачи.';
									|en = 'The electronic document on transfer to material provider is rejected. You can only edit the transfer.'");
		ИначеЕсли Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратДавальцу") Тогда
			ТекстПояснения = НСтр("ru = 'Электронный документ по возврату сырья давальцу был отклонен. Возможно только изменение возврата.';
									|en = 'The electronic document on return of raw materials to provider is rejected. You can only edit the return.'");
		//-- НЕ УТКА
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДоступноИсправление И Не Форма.ИспользоватьИсправлениеДокументов Тогда
		СообщениеОНевозможностиИспользованияИсправления
			= НСтр("ru = 'Для того чтобы оформить исправление отдельным документом, включите функциональную опцию ""Исправление и сторно финансовых документов""';
					|en = 'To issue a correction as a separate document, enable the ""Financial document correction and storno"" functional option'");
		Если ПустаяСтрока(ТекстПояснения) Тогда
			ТекстПояснения = СообщениеОНевозможностиИспользованияИсправления;
		Иначе
			ТекстПояснения = ТекстПояснения + Символы.ПС + СообщениеОНевозможностиИспользованияИсправления;
		КонецЕсли;
	КонецЕсли;
	
	Если ТребуетсяСообщениеОНесоответствииСпособа Тогда
		СообщениеОНесоответствииСпособа = НСтр("ru = 'Текущий способ отражения некорректен. Изменить его можно в статусе ""Не согласовано"" или ""Обеспечивается""';
												|en = 'The current record method is incorrect. You can change it in the ""Not approved"" or the ""Supplied"" status'");
		ТекстПояснения = ТекстПояснения + Символы.ПС + СообщениеОНесоответствииСпособа;
	КонецЕсли;
	
	Возврат ТекстПояснения;
	
КонецФункции

Функция ТекстПоясненияСпособОтработкиАктПослеПриемкиИПоступления(Форма, ТребуетсяСообщениеОНесоответствииСпособа)
	
	ТекстПояснения = "";
	ДоступноИсправление = Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.НайтиПоЗначению(
		ПредопределенноеЗначение(
			"Перечисление.СпособыОтраженияАктовОРасхожденияПослеПоступления.ОформлениеКорректировокПоступления")) <> Неопределено;
	
	Если Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.Количество() = 0 Тогда
		
		Если (Форма.СтатусЭДООснования = "ВОбработке"
			Или Форма.СтатусЭДООснования = "Получен"
			Или Форма.СтатусЭДООснования = "ОтклонениеВОбработке"
			Или Форма.СтатусЭДООснования = "УтверждениеВОбработке") Тогда
			
			ТекстПояснения = НСтр("ru = 'Документ-основание участвует в ЭДО. Для отработки расхождений нужно подписать или отклонить документ.';
									|en = 'The base document is used in EDI. To process discrepancies, sign or reject the document.'");
			
		КонецЕсли;
		
	ИначеЕсли Форма.ДоступныеСпособыОтработкиАктаПоСостояниюЭДО.НайтиПоЗначению(
		ПредопределенноеЗначение("Перечисление.СпособыОтраженияАктовОРасхожденияПослеПоступления.ИсправлениеПервичныхДокументов")) = Неопределено Тогда
		
		Если Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение") Тогда
			ТекстПояснения = НСтр("ru = 'Электронный документ по приемке подтвержден. Возможно только исправление приемки.';
									|en = 'The electronic receipt document is confirmed. You can only adjust the receipt.'");
		//++ НЕ УТ
		ИначеЕсли Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтПереработчика") Тогда
			ТекстПояснения = НСтр("ru = 'Электронный документ по возврату сырья от переработчика подтвержден. Возможно только исправление возврата.';
									|en = 'The electronic document on return of raw materials from subcontractor is confirmed. You can only adjust the return.'");
		//-- НЕ УТ
		Иначе
			ТекстПояснения = НСтр("ru = 'Электронный документ по поступлению подтвержден. Возможно только исправление поступления.';
									|en = 'The electronic receipt document is confirmed. You can only adjust the receipt.'");
		КонецЕсли;
		
	ИначеЕсли Не ДоступноИсправление Тогда
		
		Если Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПриемкаТоваровНаХранение") Тогда
			ТекстПояснения = НСтр("ru = 'Электронный документ по приемке был отклонен. Возможно только изменение приемки.';
									|en = 'The electronic receipt document is rejected. You can only edit the receipt.'");
		//++ НЕ УТ
		ИначеЕсли Форма.Объект.ТипОснованияАктаОРасхождении = ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратТоваровОтПереработчика") Тогда
			ТекстПояснения = НСтр("ru = 'Электронный документ по возврату сырья от переработчика был отклонен. Возможно только изменение возврата.';
									|en = 'The electronic document on return of raw materials from subcontractor is rejected. You can only edit the return.'");
		//-- НЕ УТ
		Иначе
			ТекстПояснения = НСтр("ru = 'Электронный документ по поступлению был отклонен. Возможно только изменение поступления.';
									|en = 'The electronic receipt document is rejected. You can only edit the receipt.'");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДоступноИсправление И Не Форма.ИспользоватьИсправлениеДокументов Тогда
		СообщениеОНевозможностиИспользованияИсправления =
			НСтр("ru = 'Для того чтобы оформить исправление отдельным документом, включите функциональную опцию ""Исправление и сторно финансовых документов""';
				|en = 'To issue a correction as a separate document, enable the ""Financial document correction and storno"" functional option'");
		Если ПустаяСтрока(ТекстПояснения) Тогда
			ТекстПояснения = СообщениеОНевозможностиИспользованияИсправления;
		Иначе
			ТекстПояснения = ТекстПояснения + Символы.ПС + СообщениеОНевозможностиИспользованияИсправления;
		КонецЕсли;
	КонецЕсли;
	
	Если ТребуетсяСообщениеОНесоответствииСпособа Тогда
		СообщениеОНесоответствииСпособа = НСтр("ru = 'Текущий способ отражения некорректен. Изменить его можно в статусе ""Не согласовано"" или ""Обеспечивается""';
												|en = 'The current record method is incorrect. You can change it in the ""Not approved"" or the ""Supplied"" status'");
		ТекстПояснения = ТекстПояснения + Символы.ПС + СообщениеОНесоответствииСпособа;
	КонецЕсли;
	
	Возврат ТекстПояснения;
	
КонецФункции

Процедура ЗаполнитьДокументОснованиеВСтроке(СтрокаТаблицыТовары, ДокументыОснования) Экспорт

	Если Не СтрокаТаблицыТовары.ЗаполненоПоОснованию И Не ЗначениеЗаполнено(СтрокаТаблицыТовары.ДокументОснование) Тогда
		Если ДокументыОснования.Количество() = 1 Тогда
			СтрокаТаблицыТовары.ДокументОснование = ДокументыОснования[0].ДокументОснование;
			УстановитьПризнакОснованиеПоЗаказам(СтрокаТаблицыТовары, ДокументыОснования[0]);
			ЗаполнитьЗаказВСтроке(СтрокаТаблицыТовары, ДокументыОснования);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьЗаказВСтроке(СтрокаТаблицыТовары, ДокументыОснования) Экспорт

	НайденныеСтроки = ДокументыОснования.НайтиСтроки(Новый Структура("ДокументОснование", СтрокаТаблицыТовары.ДокументОснование));
	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	НайденнаяСтрока = НайденныеСтроки[0];

	Если Не ЗначениеЗаполнено(СтрокаТаблицыТовары.Заказ) Тогда
		Если НайденнаяСтрока.ЗаказыОснования.Количество() = 1 Тогда
			СтрокаТаблицыТовары.Заказ = НайденнаяСтрока.ЗаказыОснования.Получить(0).Значение;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьПризнакОснованиеПоЗаказам(СтрокаТаблицыТовары, СтрокаДокументыОснования) Экспорт
	
	СтрокаТаблицыТовары.ОснованиеПоЗаказам = (СтрокаДокументыОснования.ЗаказыОснования.Количество() > 0);
	
КонецПроцедуры

Процедура УстановитьОтборСтатусаДинамическогоСписка(ДинамическийСписок, СтатусДляОтбора) Экспорт
	
	УстанавливаетсяОтбор = ЗначениеЗаполнено(СтатусДляОтбора);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, 
	                                                                        "Статус",
	                                                                        СтатусДляОтбора, 
	                                                                        ВидСравненияКомпоновкиДанных.Равно,
	                                                                        ,
	                                                                        УстанавливаетсяОтбор);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ДинамическийСписок, 
	                                                                        "Проведен",
	                                                                        Истина, 
	                                                                        ВидСравненияКомпоновкиДанных.Равно,
	                                                                        ,
	                                                                        УстанавливаетсяОтбор);
	
КонецПроцедуры

Процедура ЗаполнитьСделкуВСтроке(ТекущиеДанные, ЗаказыСделки) Экспорт

	Если ТекущиеДанные.ОснованиеПоЗаказам И ЗначениеЗаполнено(ТекущиеДанные.ЗаказПоставщику) Тогда
		
		НайденныеСтроки = ЗаказыСделки.НайтиСтроки(Новый Структура("ЗаказПоставщику", ТекущиеДанные.ЗаказПоставщику));
		Если НайденныеСтроки.Количество() > 0 Тогда
			ТекущиеДанные.Сделка = НайденныеСтроки[0].Сделка;
			Возврат;
		КонецЕсли;
	
	КонецЕсли;
	
	ТекущиеДанные.Сделка = ПредопределенноеЗначение("Справочник.СделкиСКлиентами.ПустаяСсылка");
	
КонецПроцедуры

Функция ИменаРеквизитовВЗависимостиОтТипаАкта(ЭтоАктОРасхожденияхПослеОтгрузки) Экспорт
	
	СтруктураИмен = Новый Структура;
	СтруктураИмен.Вставить("ЗаполненоПоОснованию", ?(ЭтоАктОРасхожденияхПослеОтгрузки,"ЗаполненоПоРеализации","ЗаполненоПоОснованию"));
	СтруктураИмен.Вставить("Основание", ?(ЭтоАктОРасхожденияхПослеОтгрузки,"Реализация","ДокументОснование"));
	СтруктураИмен.Вставить("Заказ", ?(ЭтоАктОРасхожденияхПослеОтгрузки,"ЗаказКлиента","ЗаказПоставщику"));
	СтруктураИмен.Вставить("ОснованиеПоЗаказам", ?(ЭтоАктОРасхожденияхПослеОтгрузки,"РеализацияПоЗаказам","ОснованиеПоЗаказам"));
	
	Возврат СтруктураИмен;
	
КонецФункции

// Процедура рассчитывает итоговые показатели формы.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа Акта о расхождениях
//
Процедура РассчитатьИтоговыеПоказателиФормы(Форма) Экспорт
	
	Форма.Недостачи = 0;
	Форма.Излишки   = 0;
	Форма.НедостачиСуммаНДС = 0;
	Форма.ИзлишкиСуммаНДС   = 0;
	Форма.ЕстьИзлишки     = Ложь;
	Форма.ЕстьНедостачи   = Ложь;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Объект, "ХозяйственнаяОперация")
		И Форма.Объект.ХозяйственнаяОперация =
		ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ОтгрузкаТоваровКлиенту") Тогда
		
		ОтображатьИтогСуммыНДС = Ложь;
		
	Иначе
		
		ОтображатьИтогСуммыНДС = УчетНДСУПКлиентСервер.ОтображатьНДСВИтогахДокументаПродажи(
			Форма.Объект.НалогообложениеНДС);
		
	КонецЕсли;
	
	Для Каждого СтрокаТовары Из Форма.Объект.Товары Цикл
		
		Если ОтображатьИтогСуммыНДС Тогда
			Если СтрокаТовары.СуммаСНДСРасхождения > 0 Тогда
				Форма.Излишки = Форма.Излишки + СтрокаТовары.СуммаСНДСРасхождения;
				Форма.ИзлишкиСуммаНДС = Форма.ИзлишкиСуммаНДС + СтрокаТовары.СуммаНДСРасхождения;
			ИначеЕсли СтрокаТовары.СуммаСНДСРасхождения < 0 Тогда
				Форма.Недостачи = Форма.Недостачи + (-СтрокаТовары.СуммаСНДСРасхождения);
				Форма.НедостачиСуммаНДС = Форма.НедостачиСуммаНДС + (-СтрокаТовары.СуммаНДСРасхождения);
			КонецЕсли;
		Иначе
			Если СтрокаТовары.СуммаРасхождения > 0 Тогда
				Форма.Излишки = Форма.Излишки + СтрокаТовары.СуммаРасхождения;
			ИначеЕсли СтрокаТовары.СуммаРасхождения < 0 Тогда
				Форма.Недостачи = Форма.Недостачи + (-СтрокаТовары.СуммаРасхождения);
			КонецЕсли;
		КонецЕсли;
		
		Если СтрокаТовары.КоличествоУпаковокРасхождения < 0 Тогда
			Форма.ЕстьНедостачи = Истина;
		КонецЕсли;
		Если СтрокаТовары.КоличествоУпаковокРасхождения > 0 Тогда
			Форма.ЕстьИзлишки = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОтображатьИтогСуммыНДС Тогда
		Форма.Расхождения = Форма.Объект.Товары.Итог("СуммаСНДС") - Форма.Объект.Товары.Итог("СуммаСНДСПоДокументу");
		Форма.РасхожденияСуммаНДС = Форма.Объект.Товары.Итог("СуммаНДС") - Форма.Объект.Товары.Итог("СуммаНДСПоДокументу");
	Иначе
		Форма.Расхождения = Форма.Объект.Товары.Итог("Сумма") - Форма.Объект.Товары.Итог("СуммаПоДокументу");
	КонецЕсли;
	
	Форма.Элементы.ГруппаИтогиБезНДС.Видимость = Не ОтображатьИтогСуммыНДС;
	Форма.Элементы.ГруппаИтогиСНДС.Видимость = ОтображатьИтогСуммыНДС;
	
КонецПроцедуры

Функция ЭтоАктОРасхожденияхПослеОтгрузки(ТипОснованияАктаОРасхождении) Экспорт
	
	Возврат ТипОснованияРеализацияТоваровУслуг(ТипОснованияАктаОРасхождении)
		Или ТипОснованияОтгрузкаТоваровКлиенту(ТипОснованияАктаОРасхождении)
		Или ТипОснованияОтгрузкаТоваровСХранения(ТипОснованияАктаОРасхождении)
		//++ НЕ УТКА
		Или ТипОснованияАктаОРасхождении =
					ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ПередачаДавальцу")
		Или ТипОснованияАктаОРасхождении =
					ПредопределенноеЗначение("Перечисление.ТипыОснованияАктаОРасхождении.ВозвратДавальцу")
		//-- НЕ УТКА
		Или ТипОснованияПередачаТоваровХранителю(ТипОснованияАктаОРасхождении)
		Или ТипОснованияВозвратПоставщику(ТипОснованияАктаОРасхождении)
		Или ТипОснованияПередачаНаКомиссию(ТипОснованияАктаОРасхождении);
	
КонецФункции

// Изменить действие в строке.
// 
// Параметры:
// 	Строка - СтрокаТаблицыЗначений - содержит в том числе:
// 		* Действие - ПеречислениеСсылка.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки, ПеречислениеСсылка.ВариантыДействийПоРасхождениямВАктеПослеПриемки -
// 	РезультатВыбораПользователя - Структура -
// 
// Возвращаемое значение:
//  Булево - Изменить действие в строке
Функция ИзменитьДействиеВСтроке(Строка, РезультатВыбораПользователя) Экспорт
	
	Если ЗначениеЗаполнено(РезультатВыбораПользователя.ДействиеНедостачи)
		И Строка.КоличествоУпаковокРасхождения < 0 Тогда
		ВариантДействия = РезультатВыбораПользователя.ДействиеНедостачи;
	Иначе
		ВариантДействия = РезультатВыбораПользователя.ДействиеИзлишки;
	КонецЕсли;
	
	ЕстьРеквизитПоВинеСтороннейКомпании = РезультатВыбораПользователя.Свойство("ПоВинеСтороннейКомпании");
	
	Если Строка.Действие = ВариантДействия Тогда
		Если ЕстьРеквизитПоВинеСтороннейКомпании Тогда
			Если Строка.ПоВинеСтороннейКомпании = РезультатВыбораПользователя.ПоВинеСтороннейКомпании Тогда
				Возврат Ложь;
			КонецЕсли;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПокупкаПерепоставленного")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОприходоватьСейчас")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ПерепоставленноеДарится")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.УвеличитьПереходПраваСобственности")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяОприходование")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяОприходованиеВозврат")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ВернутьПерепоставленноеБезОформления")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиПерепоставленноеНаПрочиеДоходы")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленноеИВернуть")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленное") Тогда
		
		Если НЕ Строка.КоличествоУпаковокРасхождения > 0  Тогда
			Возврат Ложь;
		КонецЕсли;
		
	ИначеЕсли ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяДопоставка")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ДопоставкаНеТребуется")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ОтгрузитьСейчас")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.НедостачаНеПризнана")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.УменьшитьПереходПраваСобственности")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяСписание")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяСписаниеДопоставка")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачу")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачуИОжидатьДопоставку")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы")
		Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОжидатьДопоставкуБезОформления") Тогда
		
		Если НЕ Строка.КоличествоУпаковокРасхождения < 0 Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Если (Строка.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга")
		Или Строка.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа")) Тогда
		
		Если ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного")
			Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы") 
			Или ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленноеИВернуть") Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	Строка.Действие = ВариантДействия;
	Если ЕстьРеквизитПоВинеСтороннейКомпании Тогда
		Если ВариантДействия = ПредопределенноеЗначение("Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы") Тогда
			Строка.ПоВинеСтороннейКомпании =  РезультатВыбораПользователя.ПоВинеСтороннейКомпании;
		Иначе
			Строка.ПоВинеСтороннейКомпании =  Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

// Заполняет реквизиты строки для многооборотной тары
// 
// Параметры:
// 	Форма - ДанныеФормыСтруктура - Форма, в которой необходимо заполнить информацию
// 	ДокументыОснования - ДинамическийСписок - Документы, откуда необходимо взять информацию. Должны содержать:
// 		* ВернутьМногооборотнуюТару - Булево - 
// 		* ТребуетсяЗалогЗаТару - Булево - 
//
Процедура ЗаполнитьРеквизитыМногооборотнойТары(Форма, ДокументыОснования) Экспорт
	
	ТекущийДокументОснования = Неопределено;
	НайденнаяСтрока = Неопределено;
	Для каждого СтрокаТаблицыТовары Из Форма.Товары Цикл
		
		Если ТекущийДокументОснования <> СтрокаТаблицыТовары.Реализация Тогда
			ТекущийДокументОснования = СтрокаТаблицыТовары.Реализация;
			НайденныеСтроки = ДокументыОснования.НайтиСтроки(Новый Структура("Реализация", ТекущийДокументОснования));		
			Если НайденныеСтроки.Количество() > 0 Тогда
				НайденнаяСтрока = НайденныеСтроки[0];
			иначе	
				НайденнаяСтрока = Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		Если НайденнаяСтрока <> Неопределено Тогда
			ЗаполнитьРеквизитыМногооборотнойТарыВСтроке(СтрокаТаблицыТовары, НайденнаяСтрока);		
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыМногооборотнойТарыВСтроке(СтрокаТаблицыТовары, СтрокаДокументыОснования) Экспорт
	
	СтрокаТаблицыТовары.ВернутьМногооборотнуюТару = СтрокаДокументыОснования.ВернутьМногооборотнуюТару;
	СтрокаТаблицыТовары.ТребуетсяЗалогЗаТару 	  = СтрокаДокументыОснования.ТребуетсяЗалогЗаТару;	
	
КонецПроцедуры


#КонецОбласти

