#Область СлужебныйПрограммныйИнтерфейс

Функция ОтображатьСверкуПриФормировании(ИмяОтчета) Экспорт
	
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
	"РазворачиватьРезультатыСверкиДекларацийСразу",
	ИмяОтчета,
	Истина);
	
КонецФункции

Процедура ОтменитьВыполнениеСверки(ИдентификаторЗаданияСверки) Экспорт
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗаданияСверки);
	
КонецПроцедуры

// Возвращает структуру данных, в которой по каждому разделу сверки определяется
// сумма расхождений / без расхождений
// сумма НДС, количество документов
// 
//Параметры:
// АдресРезультата - Строка - адрес временного хранилища, по которому помещены результаты сверки
// ИмяОтчета - Строка - для которого производилась сверка
//           могут быть значения "КнигаПокупок", "КнигаПродаж", "Декларация"
// ДопЛисты - Булево - для книг может быть произведена сверка для основного раздела и для доп листов
// НомерРаздела - Строка - для декларации по НДС подготавливаем и выводим данные для нескольких разделов
//
// Возвращаемое значение:
// Структура - в которой описано по каким разделам сверки (расхождения/ нет у контрагента/ не сверились / нет расхождений)
// на какую сумму есть расхождения и сколько документов
//
Функция ПодготовитьДанныеДляОтображенияСверки(АдресРезультата, ИмяОтчета, ДопЛисты, НомерРаздела = "") Экспорт
	
	ТаблицыСверки = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ТаблицыСверки = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ДопЛисты Тогда
		
		Если ТаблицыСверки.ТаблицаРасхожденияДопЛист = Неопределено Тогда
			ТаблицаРасхождения = Неопределено
		Иначе
			ТаблицаРасхождения = ТаблицыСверки.ТаблицаРасхожденияДопЛист.Выбрать();
		КонецЕсли;
		
		Если ТаблицыСверки.ТаблицаНетУКонтрагентаДопЛист = Неопределено Тогда
			ТаблицаНетУКонтрагента = Неопределено;
		Иначе
			ТаблицаНетУКонтрагента = ТаблицыСверки.ТаблицаНетУКонтрагентаДопЛист.Выбрать();
		КонецЕсли;
		
		Если ТаблицыСверки.ТаблицаНеСверилиДопЛист = Неопределено Тогда
			ТаблицаНеСверили = Неопределено;
		Иначе
			ТаблицаНеСверили = ТаблицыСверки.ТаблицаНеСверилиДопЛист.Выбрать();
		КонецЕсли;
		
		Если ТаблицыСверки.ТаблицаСверилиБезОшибокДопЛист = Неопределено Тогда
			ТаблицаСверилиБезОшибок = Неопределено;
		Иначе
			ТаблицаСверилиБезОшибок = ТаблицыСверки.ТаблицаСверилиБезОшибокДопЛист.Выбрать();
		КонецЕсли;
		
	Иначе
		Если ТаблицыСверки["ТаблицаРасхождения" + НомерРаздела] = Неопределено Тогда
			ТаблицаРасхождения = Неопределено;
		Иначе
			ТаблицаРасхождения      = ТаблицыСверки["ТаблицаРасхождения" + НомерРаздела].Выбрать();
		КонецЕсли;
		Если ТаблицыСверки["ТаблицаНетУКонтрагента" + НомерРаздела] = Неопределено Тогда
			ТаблицаНетУКонтрагента = Неопределено;
		Иначе
			ТаблицаНетУКонтрагента  = ТаблицыСверки["ТаблицаНетУКонтрагента" + НомерРаздела].Выбрать();
		КонецЕсли;
		Если ТаблицыСверки["ТаблицаНеСверили" + НомерРаздела] = Неопределено Тогда
			ТаблицаНеСверили = Неопределено;
		Иначе
			ТаблицаНеСверили        = ТаблицыСверки["ТаблицаНеСверили" + НомерРаздела].Выбрать();
		КонецЕсли;
		Если ТаблицыСверки["ТаблицаСверилиБезОшибок" + НомерРаздела] = Неопределено Тогда
			ТаблицаСверилиБезОшибок = Неопределено
		Иначе
			
			ТаблицаСверилиБезОшибок = ТаблицыСверки["ТаблицаСверилиБезОшибок" + НомерРаздела].Выбрать();
		КонецЕсли;
		
		Если ИмяОтчета = "КнигаПродаж" Или НомерРаздела = "Раздел9" Тогда
			Если ТаблицыСверки["ТаблицаНетВОрганизации" + НомерРаздела] = Неопределено Тогда
				ТаблицаНетВОрганизации = Неопределено;
			Иначе
				ТаблицаНетВОрганизации = ТаблицыСверки["ТаблицаНетВОрганизации" + НомерРаздела].Выбрать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Результат = Новый Структура;
	
	Если НЕ ТаблицаРасхождения = Неопределено И ТаблицаРасхождения.Следующий() Тогда
		Результат.Вставить("ТаблицаРасхожденияНДС",                  ТаблицаРасхождения.НДС);
		Результат.Вставить("ТаблицаРасхожденияВсего",                ТаблицаРасхождения.Всего);
		Результат.Вставить("ТаблицаРасхожденияКоличествоДокументов", ТаблицаРасхождения.КоличествоДокументов);
	Иначе
		Результат.Вставить("ТаблицаРасхожденияНДС",                  0);
		Результат.Вставить("ТаблицаРасхожденияВсего",                0);
		Результат.Вставить("ТаблицаРасхожденияКоличествоДокументов", 0);
	КонецЕсли;
	
	Если НЕ ТаблицаНетУКонтрагента = Неопределено И  ТаблицаНетУКонтрагента.Следующий() Тогда
		Результат.Вставить("ТаблицаНетУКонтрагентаНДС",                  ТаблицаНетУКонтрагента.НДС);
		Результат.Вставить("ТаблицаНетУКонтрагентаВсего",                ТаблицаНетУКонтрагента.Всего);
		Результат.Вставить("ТаблицаНетУКонтрагентаКоличествоДокументов", ТаблицаНетУКонтрагента.КоличествоДокументов);
	Иначе
		Результат.Вставить("ТаблицаНетУКонтрагентаНДС",                  0);
		Результат.Вставить("ТаблицаНетУКонтрагентаВсего",                0);
		Результат.Вставить("ТаблицаНетУКонтрагентаКоличествоДокументов", 0);
	КонецЕсли;
	
	Если Не ТаблицаНеСверили = Неопределено И ТаблицаНеСверили.Следующий() Тогда
		Результат.Вставить("ТаблицаНеСверилиНДС",                  ТаблицаНеСверили.НДС);
		Результат.Вставить("ТаблицаНеСверилиВсего",                ТаблицаНеСверили.Всего);
		Результат.Вставить("ТаблицаНеСверилиКоличествоДокументов", ТаблицаНеСверили.КоличествоДокументов);
	Иначе
		Результат.Вставить("ТаблицаНеСверилиНДС",                  0);
		Результат.Вставить("ТаблицаНеСверилиВсего",                0);
		Результат.Вставить("ТаблицаНеСверилиКоличествоДокументов", 0);
	КонецЕсли;
	
	Если НЕ ТаблицаСверилиБезОшибок = Неопределено И ТаблицаСверилиБезОшибок.Следующий() Тогда
		Результат.Вставить("ТаблицаСверилиБезОшибокНДС",                  ТаблицаСверилиБезОшибок.НДС);
		Результат.Вставить("ТаблицаСверилиБезОшибокВсего",                ТаблицаСверилиБезОшибок.Всего);
		Результат.Вставить("ТаблицаСверилиБезОшибокКоличествоДокументов", ТаблицаСверилиБезОшибок.КоличествоДокументов);
	Иначе
		Результат.Вставить("ТаблицаСверилиБезОшибокНДС",                  0);
		Результат.Вставить("ТаблицаСверилиБезОшибокВсего",                0);
		Результат.Вставить("ТаблицаСверилиБезОшибокКоличествоДокументов", 0);
	КонецЕсли;
	
	Если (ИмяОтчета = "КнигаПродаж" Или НомерРаздела = "Раздел9") И Не ДопЛисты Тогда
		Если Не ТаблицаНетВОрганизации = Неопределено И ТаблицаНетВОрганизации.Следующий() Тогда
			Результат.Вставить("ТаблицаНетВОрганизацииНДС",                  ТаблицаНетВОрганизации.НДС);
			Результат.Вставить("ТаблицаНетВОрганизацииВсего",                ТаблицаНетВОрганизации.Всего);
			Результат.Вставить("ТаблицаНетВОрганизацииКоличествоДокументов", ТаблицаНетВОрганизации.КоличествоДокументов);
		Иначе
			Результат.Вставить("ТаблицаНетВОрганизацииНДС",                  0);
			Результат.Вставить("ТаблицаНетВОрганизацииВсего",                0);
			Результат.Вставить("ТаблицаНетВОрганизацииКоличествоДокументов", 0);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Область КнигиПокупокПродаж

Функция СверитьОтчет(СтруктураПараметров) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(СтруктураПараметров.УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Сверка отчета""';
															|en = 'Reconciliation of the'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	Если СтруктураПараметров.ВыводитьТолькоДопЛисты Тогда
		СтруктураПараметров.Вставить("ДанныеОтчета", Неопределено);
	Иначе
		Если СтруктураПараметров.ДанныеДляСверкиДеклараций.АдресДанныхОтчета = Неопределено Тогда
			ДанныеОтчета = Неопределено
		Иначе
			ДанныеОтчета = ПолучитьИзВременногоХранилища(СтруктураПараметров.ДанныеДляСверкиДеклараций.АдресДанныхОтчета);
		КонецЕсли;
		МассивДанныхОтчета = ТаблицаЗначенийВМассив(ДанныеОтчета);
		СтруктураПараметров.Вставить("ДанныеОтчета", МассивДанныхОтчета);
	КонецЕсли;
	Если СтруктураПараметров.ФормироватьДополнительныеЛисты Тогда
		Если СтруктураПараметров.ДанныеДляСверкиДеклараций.АдресДанныхОтчета = Неопределено Тогда
			ДанныеДопЛистов = Неопределено
		Иначе
			ДанныеДопЛистов = ПолучитьИзВременногоХранилища(Структурапараметров.ДанныеДляСверкиДеклараций.АдресДанныхДопЛиста);
		КонецЕсли;
		МассивДанныхДопЛистов = ТаблицаЗначенийВМассив(ДанныеДопЛистов);
		СтруктураПараметров.Вставить("ДанныеДопЛистов", МассивДанныхДопЛистов);
	Иначе
		СтруктураПараметров.Вставить("ДанныеДопЛистов", Неопределено);
	КонецЕсли;
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, "СервисСверкиРасчетовСверкаДекларацийНДС.СверитьОтчетВФоне", СтруктураПараметров);
	
КонецФункции

#КонецОбласти

#Область ДекларацияПоНДС

Функция СверитьДекларацию(СтруктураПараметров) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(СтруктураПараметров.УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Сверка счетов-фактур в декларации по НДС""';
															|en = '""Tax invoice reconciliation in VAT declaration"" report'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	Разделы = СервисСверкиРасчетовСверкаДекларацийНДС.ПроверяемыеРазделыДекларацииПоНДС();
	
	СтраницыРазделов = ПолучитьИзВременногоХранилища(СтруктураПараметров.ДанныеДляСверкиДеклараций.СтраницыРазделов);
	СтруктураСтраниц = Новый Структура;
	Для Каждого НомерРаздела Из Разделы Цикл
		СтраницыРаздела = СтраницыРазделов["СтраницыРаздел" + НомерРаздела.Значение];
		СтраницаМассив  = ТаблицаЗначенийВМассив(СтраницыРаздела);
		СтруктураСтраниц.Вставить("СтраницыРаздел" + НомерРаздела.Значение, СтраницаМассив);
	КонецЦикла;

	СтруктураПараметров.Вставить("СтруктураСтраницРазделов", СтруктураСтраниц);

	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, "СервисСверкиРасчетовСверкаДекларацийНДС.СверитьДекларациюВФоне", СтруктураПараметров);

КонецФункции

Функция ПроверитьНаличиеРасхождений(АдресРезультатовСверки) Экспорт

	ТаблицыСверкиДекларацииПоНДС = ПолучитьИзВременногоХранилища(АдресРезультатовСверки);
	Разделы = СервисСверкиРасчетовСверкаДекларацийНДС.ПроверяемыеРазделыДекларацииПоНДС();
	ЕстьРасхождения          = Ложь;
	ЕстьНеСверенные          = Ложь;
	НеУдалосьВыполнитьСверку = Ложь;
	
	Если ТаблицыСверкиДекларацииПоНДС = Неопределено Тогда
		НеУдалосьВыполнитьСверку = Истина;
		Возврат Новый Структура("ЕстьРасхождения, ЕстьНеСверенные, НеУдалосьВыполнитьСверку", ЕстьРасхождения, ЕстьНеСверенные, НеУдалосьВыполнитьСверку);
	КонецЕсли;
	
	Для Каждого НомерРаздела Из Разделы Цикл
		Если ЕстьНеСверенные = Ложь Или НеУдалосьВыполнитьСверку = Ложь Тогда
			Если ТаблицыСверкиДекларацииПоНДС.Свойство("ТаблицаНеСверилиРаздел"        + НомерРаздела.Значение) = Ложь Тогда
				НеУдалосьВыполнитьСверку = Истина;
			Иначе
				Если ТаблицыСверкиДекларацииПоНДС["ТаблицаНеСверилиРаздел"        + НомерРаздела.Значение] <> Неопределено Тогда
					Выборка = ТаблицыСверкиДекларацииПоНДС["ТаблицаНеСверилиРаздел"        + НомерРаздела.Значение].Выбрать();
					Если Выборка.Следующий() Тогда
						ЕстьНеСверенные = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ЕстьРасхождения = Ложь Или НеУдалосьВыполнитьСверку = Ложь Тогда
			Если ТаблицыСверкиДекларацииПоНДС.Свойство("ТаблицаРасхожденияРаздел"        + НомерРаздела.Значение) = Ложь Тогда
				НеУдалосьВыполнитьСверку = Истина;
			Иначе
				Если ТаблицыСверкиДекларацииПоНДС["ТаблицаРасхожденияРаздел"        + НомерРаздела.Значение] <> Неопределено Тогда
					Выборка = ТаблицыСверкиДекларацииПоНДС["ТаблицаРасхожденияРаздел"      + НомерРаздела.Значение].Выбрать();
					Если Выборка.Следующий() Тогда
						ЕстьРасхождения = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ЕстьРасхождения = Ложь Или НеУдалосьВыполнитьСверку = Ложь Тогда
			Если ТаблицыСверкиДекларацииПоНДС.Свойство("ТаблицаНетУКонтрагентаРаздел"        + НомерРаздела.Значение) = Ложь Тогда
				НеУдалосьВыполнитьСверку = Истина;
			Иначе
				Если ТаблицыСверкиДекларацииПоНДС["ТаблицаНетУКонтрагентаРаздел"        + НомерРаздела.Значение] <> Неопределено Тогда
					
					Выборка = ТаблицыСверкиДекларацииПоНДС["ТаблицаНетУКонтрагентаРаздел"  + НомерРаздела.Значение].Выбрать();
					Если Выборка.Следующий() Тогда
						ЕстьРасхождения = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если (ЕстьРасхождения = Ложь Или НеУдалосьВыполнитьСверку = Ложь) И НомерРаздела.Значение = "9" Тогда
			Если ТаблицыСверкиДекларацииПоНДС.Свойство("ТаблицаНетВОрганизацииРаздел9") = Ложь Тогда
				НеУдалосьВыполнитьСверку = Истина;
			Иначе
				Если ТаблицыСверкиДекларацииПоНДС.ТаблицаНетВОрганизацииРаздел9 <> Неопределено Тогда
					Выборка = ТаблицыСверкиДекларацииПоНДС.ТаблицаНетВОрганизацииРаздел9.Выбрать();
					Если Выборка.Следующий() Тогда
						ЕстьРасхождения = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый Структура("ЕстьРасхождения, ЕстьНеСверенные, НеУдалосьВыполнитьСверку", ЕстьРасхождения, ЕстьНеСверенные, НеУдалосьВыполнитьСверку);

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТаблицаЗначенийВМассив(ТаблицаЗначений)
	
	Если ТаблицаЗначений = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Массив = Новый Массив();
	СтруктураСтрокой = "";
	НужнаЗапятая = Ложь;
	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		Если НужнаЗапятая Тогда
			СтруктураСтрокой = СтруктураСтрокой + ",";
		КонецЕсли;
		СтруктураСтрокой = СтруктураСтрокой + Колонка.Имя;
		НужнаЗапятая = Истина;
	КонецЦикла;
	Для Каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
		НоваяСтрока = Новый Структура(СтруктураСтрокой);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		Массив.Добавить(НоваяСтрока);
	КонецЦикла;
	Возврат Массив;

КонецФункции

#КонецОбласти
