
#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

// См. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.27.82";
	Обработчик.РежимВыполнения = "Монопольно";
	Обработчик.Процедура       = "ИнтеграцияКабинетСотрудника.ПеренестиДанныеВНовыеРегистры";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.28.27";
	Обработчик.Процедура       = "ИнтеграцияКабинетСотрудника.ОбновитьВыгруженныеДанные";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.28.34";
	Обработчик.Процедура       = "ИнтеграцияКабинетСотрудника.ЗарегистрироватьОграничениеДоступаКРабочимКонтактам";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия 			= "3.1.34.95";
	Обработчик.Процедура 		= "ИнтеграцияКабинетСотрудника.ЗарегистрироватьБудущиеСобытияВычетыНДФЛ";
	Обработчик.РежимВыполнения 	= "Отложенно";
	Обработчик.Идентификатор 	= Новый УникальныйИдентификатор("d9cf0ec2-b666-4664-9125-17621a40e95f");
	Обработчик.Комментарий 		= НСтр("ru = 'Регистрация будущих событий применяемых вычетов НДФЛ для сервиса 1С:Кабинет сотрудника.';
											|en = 'Register future events of applicable personal income tax deductions for the 1C:Employee account service.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия 			= "3.1.34.95";
	Обработчик.Процедура 		= "Справочники.ТипыДанныхДляОформленияНаРаботу.НачальноеЗаполнение";
	Обработчик.РежимВыполнения 	= ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();;
	Обработчик.Идентификатор 	= Новый УникальныйИдентификатор("37644a4d-3d19-4884-bb0f-3f0694f2cbcf");
	Обработчик.Комментарий 		= НСтр("ru = 'Начальное заполнение типов данных для оформления на работу.';
											|en = 'Initial filling of data types for employment registration.'");
	Обработчик.НачальноеЗаполнение = Истина;
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия 			= "3.1.34.95";
	Обработчик.Процедура 		= "ИнтеграцияКабинетСотрудника.ЗарегистрироватьОбъектыВыгружаемыеПоСсылкам";
	Обработчик.РежимВыполнения 	= "Отложенно";
	Обработчик.Идентификатор 	= Новый УникальныйИдентификатор("8445b849-0f86-4e58-a314-632de4e75dba");
	Обработчик.Комментарий 		= НСтр("ru = 'Регистрация объектов, выгружаемых по ссылке в сервис 1С:Кабинет сотрудника.';
											|en = 'Register objects to export to 1C:Employee account by link.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия 			= "3.1.34.96";
	Обработчик.Процедура 		= "ИнтеграцияКабинетСотрудника.ОбновитьНастройкиФункциональности";
	Обработчик.РежимВыполнения 	= "Монопольно";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия 			= "3.1.34.163";
	Обработчик.Процедура 		= "ИнтеграцияКабинетСотрудника.ОбновитьПрименяемыеВычетыНДФЛ";
	Обработчик.РежимВыполнения 	= "Отложенно";
	Обработчик.Идентификатор 	= Новый УникальныйИдентификатор("23ee77c0-d784-410d-a78e-d47692a6bc42");
	Обработчик.Комментарий 		= НСтр("ru = 'Обновление применяемых вычетов НДФЛ в сервисе 1С:Кабинет сотрудника.';
											|en = 'Update the applicable personal income tax deductions in the 1C:Employee account service.'");
	
КонецПроцедуры

#КонецОбласти

#Область ОпределениеДоступностиДанных

Функция ДоступноИзменениеВариантаИспользованияСервиса() Экспорт

	Возврат ПравоДоступа("Изменение", Метаданные.Константы.СервисКабинетСотрудникаВЛокальнойСети);

КонецФункции

Функция ДоступноЧтениеВариантаИспользованияСервиса() Экспорт

	Возврат ПравоДоступа("Чтение", Метаданные.Константы.СервисКабинетСотрудникаВЛокальнойСети);

КонецФункции

#КонецОбласти

#Область УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.РегистрыСведений.РасчетныеЛисткиКабинетСотрудника, Истина);
	Списки.Вставить(Метаданные.Справочники.ДанныеДляОформленияНаРаботуПрисоединенныеФайлы, Истина);
	Списки.Вставить(Метаданные.Документы.ДанныеДляОформленияНаРаботу, Истина);
	
КонецПроцедуры

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииВидовОграниченийПравОбъектовМетаданных.
Процедура ПриЗаполненииВидовОграниченийПравОбъектовМетаданных(Описание) Экспорт
	
	Описание = Описание + "
	|РегистрСведений.РасчетныеЛисткиКабинетСотрудника.Чтение.ГруппыФизическихЛиц
	|РегистрСведений.РасчетныеЛисткиКабинетСотрудника.Чтение.Организации
	|Справочник.ДанныеДляОформленияНаРаботуПрисоединенныеФайлы.Чтение.ГруппыФизическихЛиц
	|Справочник.ДанныеДляОформленияНаРаботуПрисоединенныеФайлы.Чтение.Организации
	|Справочник.ДанныеДляОформленияНаРаботуПрисоединенныеФайлы.Изменение.ГруппыФизическихЛиц
	|Справочник.ДанныеДляОформленияНаРаботуПрисоединенныеФайлы.Изменение.Организации
	|Документ.ДанныеДляОформленияНаРаботу.Чтение.ГруппыФизическихЛиц
	|Документ.ДанныеДляОформленияНаРаботу.Чтение.Организации
	|Документ.ДанныеДляОформленияНаРаботу.Изменение.ГруппыФизическихЛиц
	|Документ.ДанныеДляОформленияНаРаботу.Изменение.Организации";
	
КонецПроцедуры

#КонецОбласти

#Область РегламентныеЗадания 

// СтандартныеПодсистемы.РегламентныеЗадания

// См. РегламентныеЗаданияПереопределяемый.ПриОпределенииНастроекРегламентныхЗаданий
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки) Экспорт
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ОбменССервисомКабинетСотрудника;
	Настройка.ФункциональнаяОпция = Метаданные.ФункциональныеОпции.ИспользуетсяСервисКабинетСотрудника;
	Настройка.ДоступноВАвтономномРабочемМесте = Ложь;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ПодключениеСервисаКабинетСотрудника;
	Настройка.ДоступноВАвтономномРабочемМесте = Ложь;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
	Настройка.ВключатьПриВключенииФункциональнойОпции = Ложь;
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ПроверкаСостоянияСервисаКабинетСотрудника;
	Настройка.ФункциональнаяОпция = Метаданные.ФункциональныеОпции.ИспользуетсяСервисКабинетСотрудника;
	Настройка.ДоступноВАвтономномРабочемМесте = Ложь;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ОбработатьНовыеОбсужденияКабинетСотрудника;
	Настройка.ФункциональнаяОпция = Метаданные.ФункциональныеОпции.ИспользуютсяОбсужденияКабинетСотрудника;
	Настройка.ДоступноВАвтономномРабочемМесте = Ложь;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
	
	ИнтеграцияКабинетСотрудникаВнутренний.ПриОпределенииНастроекРегламентныхЗаданий(Настройки);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.РегламентныеЗадания

// СтандартныеПодсистемы.РаботаВМоделиСервиса.ОчередьЗаданий

// См. ОчередьЗаданийПереопределяемый.ПриПолученииСпискаШаблонов.
//
Процедура ПриПолученииСпискаШаблоновОчередиЗаданий(Шаблоны) Экспорт
	
	Шаблоны.Добавить(Метаданные.РегламентныеЗадания.ОбменССервисомКабинетСотрудника.Имя);
	Шаблоны.Добавить(Метаданные.РегламентныеЗадания.ПроверкаСостоянияСервисаКабинетСотрудника.Имя);
	Шаблоны.Добавить(Метаданные.РегламентныеЗадания.ОбработатьНовыеОбсужденияКабинетСотрудника.Имя);
	
	ИнтеграцияКабинетСотрудникаВнутренний.ПриПолученииСпискаШаблоновОчередиЗаданий(Шаблоны);
	
КонецПроцедуры

// См. ОчередьЗаданийПереопределяемый.ПриОпределенииПсевдонимовОбработчиков.
//
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ОбменССервисомКабинетСотрудника.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ПроверкаСостоянияСервисаКабинетСотрудника.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ОбработатьНовыеОбсужденияКабинетСотрудника.ИмяМетода);
	
	ИнтеграцияКабинетСотрудникаВнутренний.ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.РаботаВМоделиСервиса.ОчередьЗаданий

#КонецОбласти

// Возвращает таблицу с данными публикуемых физических лиц.
//
// 	Возвращаемое значение:
// 		ТаблицаЗначений
// 			* ФизическоеЛицо
// 			* Организация
//
Функция ДанныеДляФормированияСогласийНаПрисоединениеКЭДОБЗК() Экспорт
	
	ФизическиеЛицаОрганизация = Новый ТаблицаЗначений;
	ФизическиеЛицаОрганизация.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ФизическиеЛицаОрганизация.Колонки.Добавить("Организация", 	Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ФизическиеЛицаОрганизация.Колонки.Добавить("Период", 	Новый ОписаниеТипов("Дата"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПубликуемыеОбъекты.Ссылка КАК Сотрудник
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ПубликуемыеОбъекты
	|ГДЕ
	|	НЕ ПубликуемыеОбъекты.УсловноВыгружается
	|	И ПубликуемыеОбъекты.Ссылка ССЫЛКА Справочник.Сотрудники";
	Сотрудники = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
	
	Если Сотрудники.Количество() = 0 Тогда
		Возврат ФизическиеЛицаОрганизация;
	КонецЕсли;
	
	ИспользоватьКадровыйУчет = ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет");
	ДатаАктуальности = ТекущаяДатаСеанса();
	
	Если ИспользоватьКадровыйУчет Тогда
		ВыбираемыеПоля = "Сотрудник,ФизическоеЛицо,Организация,ВидСобытия";
		ТаблицаДанных = КадровыйУчет.КадровыеДанныеСотрудников(Ложь, Сотрудники, ВыбираемыеПоля, ДатаАктуальности);
		Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
			Если СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Перемещение
				Или СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием
				Или СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.НачальныеДанные
				Или СтрокаТЗ.ВидСобытия = Перечисления.ВидыКадровыхСобытий.ВосстановлениеВДолжности Тогда
				ЗаполнитьЗначенияСвойств(ФизическиеЛицаОрганизация.Добавить(), СтрокаТЗ);
			КонецЕсли;
		КонецЦикла;
	Иначе
		ВыбираемыеПоля = "Сотрудник,ФизическоеЛицо,Организация,ДатаПриема,ДатаУвольнения";
		ТаблицаДанных = КадровыйУчет.КадровыеДанныеСотрудников(Ложь, Сотрудники, ВыбираемыеПоля, ДатаАктуальности);
		ТаблицаДанных.Колонки.Добавить("ВидСобытия");
		Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
			Если ЗначениеЗаполнено(СтрокаТЗ.ДатаУвольнения) И СтрокаТЗ.ДатаУвольнения < ДатаАктуальности Тогда
				Продолжить;
			ИначеЕсли ЗначениеЗаполнено(СтрокаТЗ.ДатаПриема) И СтрокаТЗ.ДатаПриема <= ДатаАктуальности Тогда
				ЗаполнитьЗначенияСвойств(ФизическиеЛицаОрганизация.Добавить(), СтрокаТЗ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ФизическиеЛицаОрганизация.Свернуть("ФизическоеЛицо,Организация,Период");
	
	Возврат ФизическиеЛицаОрганизация;
	
КонецФункции

// См. УправлениеПечатьюПереопределяемый.ПечатьДокументовПриСозданииНаСервере.
Процедура ПечатьДокументовПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользуетсяЭДОБЗККабинетСотрудника") Тогда
		
		Если Не Форма.Параметры.Свойство("ПодписаниеПечатныхФорм") Тогда
			
			ИмяКоманды = ИмяКомандыПодписатьФормыПечатьДокументов();
			
			КомандаФормы = КомандаПодписатьФормыПечатьДокументов(Форма);
			Если КомандаФормы = Неопределено Тогда
				
				СвойстваКоманды = СвойстваКомандыПередачиВКабинетСотрудника();
				
				КомандаФормы = Форма.Команды.Добавить(ИмяКоманды);
				КомандаФормы.Действие = "Подключаемый_ВыполнитьКоманду";
				КомандаФормы.Заголовок = СвойстваКоманды.Заголовок;
				КомандаФормы.Подсказка = СвойстваКоманды.Подсказка;
				КомандаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
				КомандаФормы.Картинка = СвойстваКоманды.Картинка;
				
				КнопкаФормы = ЭДОБЗК.РазместитьКомандуНаФормеПечатьДокументов(Форма, КомандаФормы);
				Если КнопкаФормы <> Неопределено Тогда
					КнопкаФормы.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
					КнопкаФормы.ИмяКоманды = КомандаФормы.Имя;
					КнопкаФормы.ТолькоВоВсехДействиях = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция КомандаПодписатьФормыПечатьДокументов(Форма) Экспорт
	
	Возврат Форма.Команды.Найти(ИмяКомандыПодписатьФормыПечатьДокументов());
	
КонецФункции

Функция СвойстваКомандыПередачиВКабинетСотрудника() Экспорт
	
	Возврат Новый Структура("Заголовок,Подсказка,Картинка",
		НСтр("ru = 'Передать в ""1С:Кабинет сотрудника""';
			|en = 'Pass to 1C:Employee Account'"),
		НСтр("ru = 'Подписать и передать в ""1С:Кабинет сотрудника""';
			|en = 'Sign and pass to 1C:Employee Account'"),
		БиблиотекаКартинок.СервисКабинетСотрудника);
		
КонецФункции

Функция ФайлРасчетногоЛисткаОпубликован(ДокументЭДОБЗК) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументЭДОБЗК", ДокументЭДОБЗК);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасчетныеЛисткиКабинетСотрудника.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	РегистрСведений.РасчетныеЛисткиКабинетСотрудника КАК РасчетныеЛисткиКабинетСотрудника
	|ГДЕ
	|	РасчетныеЛисткиКабинетСотрудника.ДокументЭДОБЗК = &ДокументЭДОБЗК";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроверкаСостоянияСервиса

// Процедура регламентного задания ПроверкаСостоянияСервисаКабинетСотрудника
Процедура ПроверкаСостоянияСервиса() Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ПроверкаСостоянияСервисаКабинетСотрудника);
	
	Если ИнтеграцияУправлениеПерсоналомОбмен.РегламентныеЗаданияЗаблокированы(Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника) Тогда
		Возврат;
	КонецЕсли;
	
	ИСДоступна = ИнтеграцияКабинетСотрудникаОбмен.ИнформационнаяСистемаДоступна();
	Если ИСДоступна = Неопределено Тогда
		// Не удалось получить информацию о состоянии ИС.
		Возврат;
	КонецЕсли;
	
	СервисЗаблокирован = Не ИСДоступна;
	РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьСостояниеБлокировкиСервиса(СервисЗаблокирован);

КонецПроцедуры

#КонецОбласти

#Область Обмен

// Обработчик регламентного задания ОбменССервисомКабинетСотрудника
//
Процедура ОбменССервисомКабинетСотрудника() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОбменССервисомКабинетСотрудника);
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	Если Не ИнтеграцияУправлениеПерсоналомОбмен.ОбменДоступен(Приложение) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПриложениеЗаблокировано() Тогда
		// Приложение заблокировано.
		Возврат;
	КонецЕсли;
	
	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	Если Не Настройки.ВыполнятьРегламентноеЗадание Тогда
		// Реализации паузы первого запуска регламентного задания.
		РегистрыСведений.НастройкиСервисаКабинетСотрудника.ВключитьВыполнениеРегламентногоЗадания();
		Возврат;
	КонецЕсли;
	
	БлокировкаУстановлена = Ложь;
	ИнтеграцияУправлениеПерсоналом.УстановитьБлокировкуДляВыполненияОбмена(Приложение, БлокировкаУстановлена, Ложь);
	Если Не БлокировкаУстановлена Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОтправкуФайловПоЭлектроннойПочте();
	УдалитьНеподтвержденныеЗаписиДокументыПоЗаявкам();
	
	Если ДоступнаВерсияDTO_2_0() Тогда
		БылиОшибки = ИнтеграцияУправлениеПерсоналомОбмен.ВыполнитьОбмен(Приложение, Ложь);
		Если Не БылиОшибки Тогда
			ЭДОБЗК.ОбновитьСостоянияДокументовЭДОБЗК();
		КонецЕсли;
	Иначе
		БылиОшибки = КабинетСотрудникаМенеджерОбмена.РезультатВыполненияОбмена(Ложь);
	КонецЕсли;
	
	Если БылиОшибки Тогда
		ВызватьИсключение НСтр("ru = 'Обмен с сервисом 1С:Кабинет сотрудника завершен с ошибками.';
								|en = 'Exchange with the 1C:Employee account service completed with errors.'");
	КонецЕсли;
	
КонецПроцедуры

// Обработчик фонового задания обмена с приложением.
Процедура ВыполнитьОбменФоновоеЗадание(Параметры, АдресХранилища) Экспорт
	
	Результат = Новый Структура("БылиОшибки,ПодготовитьДанныеДляТехПоддержки,ДатаНачала,ДатаОкончания", Ложь);
	Результат.ПодготовитьДанныеДляТехПоддержки = Параметры.ПодготовитьДанныеДляТехПоддержки;
	Результат.ДатаНачала = ТекущаяДатаСеанса();
	
	// Привилегированный режим устанавливается для публикации всех данных,
	// не зависимо от ограничений доступа для пользователя, который инициировал обмен.
	УстановитьПривилегированныйРежим(Истина);
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	ИнтеграцияУправлениеПерсоналом.УстановитьБлокировкуДляВыполненияОбмена(Приложение, Ложь, Истина);
	
	ВыполнитьОтправкуФайловПоЭлектроннойПочте();
	УдалитьНеподтвержденныеЗаписиДокументыПоЗаявкам();
	
	Если ДоступнаВерсияDTO_2_0() Тогда
		БылиОшибки = ИнтеграцияУправлениеПерсоналомОбмен.ВыполнитьОбмен(Приложение, Параметры.ПодготовитьДанныеДляТехПоддержки);
		Если Не БылиОшибки Тогда
			ЭДОБЗК.ОбновитьСостоянияДокументовЭДОБЗК();
		КонецЕсли;
	Иначе
		БылиОшибки = КабинетСотрудникаМенеджерОбмена.РезультатВыполненияОбмена(Параметры.ПодготовитьДанныеДляТехПоддержки);
	КонецЕсли;
	
	Результат.ДатаОкончания = ТекущаяДатаСеанса();
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);

КонецПроцедуры

// Используется при подключении к новому приложению.
// Выполняется получение версий формата или приложения, результат будет записан
// в регистр сведений НастройкиСервисаКабинетСотрудника.
//
Процедура ПолучитьВерсииФорматаПриложения() Экспорт
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	МенеджерОбмена = ИнтеграцияУправлениеПерсоналомОбмен.НовыйМенеджерОбмена(Приложение, Ложь);
	
	ВерсияAPI = "";
	ВерсияDTO = "";
	ВерсияПриложения = "";
	
	Ответ = МенеджерОбмена.ВерсииФорматаОбменаПриложения();
	Если Ответ.Использовать = Истина Тогда
		ВерсииФормата = Ответ.ОбъектВерсииФормата;
		Если ЗначениеЗаполнено(ВерсииФормата.ВерсияDTO) И ЗначениеЗаполнено(ВерсииФормата.ВерсияAPI) Тогда
			ВерсияAPI = ИнтеграцияУправлениеПерсоналом.ПодобратьВерсию(ВерсииФормата.ВерсияAPI, ИнтеграцияУправлениеПерсоналом.ВерсииAPI(Приложение));
			ВерсияDTO = ИнтеграцияУправлениеПерсоналом.ПодобратьВерсию(ВерсииФормата.ВерсияDTO, ИнтеграцияУправлениеПерсоналом.ВерсииDTO(Приложение));
			Если Не ЗначениеЗаполнено(ВерсияAPI) Или Не ЗначениеЗаполнено(ВерсияDTO) Тогда
				ВерсияAPI = ИнтеграцияУправлениеПерсоналом.ПодобратьВерсию(ВерсииФормата.ВерсияAPI, КабинетСотрудникаМенеджерОбмена.ВерсииAPI());
				ВерсияDTO = ИнтеграцияУправлениеПерсоналом.ПодобратьВерсию(ВерсииФормата.ВерсияDTO, КабинетСотрудникаМенеджерОбмена.ВерсииФорматаОбмена());
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВерсияAPI) И ЗначениеЗаполнено(ВерсияDTO) Тогда
		Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
		Настройки.ВерсияAPI = ВерсияAPI;
		Настройки.ВерсияDTO = ВерсияDTO;
		НаборЗаписей = РегистрыСведений.НастройкиСервисаКабинетСотрудника.СоздатьНаборЗаписей();
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Настройки);
		НаборЗаписей.Записать();
	Иначе
		ИнформацияОПриложении = КабинетСотрудникаМенеджерОбмена.ИнформацияОПриложении();
		Если ИнформацияОПриложении <> Неопределено Тогда
			ВерсияПриложения = ИнформацияОПриложении.ВерсияПриложения;
			РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьВерсиюПриложения(ВерсияПриложения);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ОтключитьКабинетыНепубликуемыхСотрудниковФоновоеЗадание(Параметры, АдресХранилища) Экспорт

	Результат = Новый Структура("СообщениеОбОшибке");
	
	Если ИспользуетсяВерсияDTO("2.0") Тогда
		Ответ = ОтключитьКабинетыНепубликуемыхСотрудников();
	Иначе
		Ответ = КабинетСотрудника.ОтключитьКабинетыНепубликуемыхСотрудников();
	КонецЕсли;
	
	Если Ответ.СообщениеОбОшибке <> Неопределено Тогда
		Результат.СообщениеОбОшибке = Ответ.СообщениеОбОшибке;
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);

КонецПроцедуры

Процедура ОбновитьНастройкиСервисаФоновоеЗадание(Параметры, АдресХранилища) Экспорт

	Результат = Новый Структура("БылиОшибки", Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	
	УстановитьТребуетсяОбновитьНастройкиФункциональности(Истина);
	УстановитьТребуетсяОбновитьНастройкиЗаявленийНаОтпуск(Истина);
	
	Если ИспользуетсяВерсияDTO("2.0") Тогда
		Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
		Результат.БылиОшибки = ИнтеграцияКабинетСотрудникаОбмен.ВыгрузитьНастройкиПриложения();
	Иначе
		Результат.БылиОшибки = КабинетСотрудникаМенеджерОбмена.ВыгрузитьНастройкиСервиса();
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);

КонецПроцедуры

#КонецОбласти

#Область ПубликацияРасчетныхЛистков

// Публикация расчетных листков из формы публикации.
Процедура ОпубликоватьРасчетныеЛистыВФоне(Параметры, АдресХранилища) Экспорт
	
	// Параметры ПодготовитьДанныеДляТехПоддержки, ДатаНачала, ДатаОкончания
	// используются для получения списка событий журнала регистрации при подготовке
	// данных для техподдержки.
	Результат = Новый Структура("БылиОшибки,ПодготовитьДанныеДляТехПоддержки,ДатаНачала,ДатаОкончания,НеОбработаны,КоличествоВыгружено", Ложь);
	Результат.ПодготовитьДанныеДляТехПоддержки = Параметры.ПодготовитьДанныеДляТехПоддержки;
	Результат.ДатаНачала = ТекущаяДатаСеанса();
	
	ФизическиеЛицаОрганизации = Новый Соответствие;
	ФизическиеЛицаОрганизации.Вставить(Параметры.Организация, Параметры.СписокФизическихЛиц);
	
	ДанныеДляПубликации = Новый Соответствие;
	ДанныеДляПубликации.Вставить(Параметры.МесяцРасчетныхЛистов, ФизическиеЛицаОрганизации);
	
	ПараметрыПубликации = ПараметрыПубликацииРасчетныхЛистов();
	ПараметрыПубликации.ВестиПротокол 								= Параметры.ПодготовитьДанныеДляТехПоддержки;
	ПараметрыПубликации.ПовторнаяПубликация 						= Параметры.ПовторнаяПубликация;
	ПараметрыПубликации.ПерваяПоловинаМесяца 						= Параметры.ПерваяПоловинаМесяца;
	ПараметрыПубликации.ВариантОтчета 								= Параметры.ВариантОтчета;
	ПараметрыПубликации.ФормироватьРасчетныеЛистыОтдельнымЗапросом 	= Параметры.ФормироватьРасчетныеЛистыОтдельнымЗапросом;
	
	УстановитьПривилегированныйРежим(Истина);
	
	УстановитьБлокировкуПубликацииРЛ(Параметры);
	
	Если ДоступнаВерсияDTO_2_0() Тогда
		Ответ = ИнтеграцияКабинетСотрудникаОбмен.РезультатПубликацииРасчетныхЛистков(ДанныеДляПубликации, ПараметрыПубликации);
	Иначе
		Ответ = КабинетСотрудникаМенеджерОбмена.РезультатПубликацииРасчетныхЛистков(ДанныеДляПубликации, ПараметрыПубликации);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат.ДатаОкончания 		= ТекущаяДатаСеанса();
	Результат.БылиОшибки 			= Ответ.БылиОшибки;
	Результат.НеОбработаны 			= Ответ.НеОбработаны;
	Результат.КоличествоВыгружено 	= Ответ.КоличествоВыгружено;
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Публикация расчетных листков из формы интеграции с сервисом.
Процедура ОпубликоватьРасчетныеЛистыЗаМесяцВФоне(ПараметрыПроцедуры, АдресХранилища) Экспорт
	
	Результат = Новый Структура("БылиОшибки", Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ФизическиеЛица.Ссылка КАК ФизическоеЛицо
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ФизическиеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиЗаполненияОбъектовУправлениеПерсоналом КАК ОшибкиЗаполнения
	|		ПО ФизическиеЛица.Ссылка = ОшибкиЗаполнения.Ссылка
	|			И (ОшибкиЗаполнения.БлокирующаяОшибка)
	|			И (ОшибкиЗаполнения.Приложение = ЗНАЧЕНИЕ(Перечисление.ПриложенияДляИнтеграции.КабинетСотрудника))
	|ГДЕ
	|	ОшибкиЗаполнения.БлокирующаяОшибка ЕСТЬ NULL
	|	И ФизическиеЛица.Ссылка ССЫЛКА Справочник.ФизическиеЛица";
	СписокФизическихЛиц = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
			
	ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудников.НачалоПериода = НачалоМесяца(ПараметрыПроцедуры.Месяц);
	ПараметрыПолученияСотрудников.ОкончаниеПериода = КонецМесяца(ПараметрыПроцедуры.Месяц);
	ПараметрыПолученияСотрудников.КадровыеДанные = "Организация";
	ПараметрыПолученияСотрудников.СписокФизическихЛиц = СписокФизическихЛиц;
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудников);
	
	Запрос.УстановитьПараметр("УправленческаяОрганизация", ИнтеграцияУправлениеПерсоналом.УправленческаяОрганизация());
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Сотрудники.Организация КАК Организация
	|ИЗ
	|	ВТСотрудникиОрганизации КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Организация <> &УправленческаяОрганизация
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	ФизическоеЛицо";
	РезультатЗапроса = Запрос.Выполнить();
	
	ФизическиеЛицаОрганизации = Новый Соответствие;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Организация") Цикл
		
		Организация = Выборка.Организация;
		
		СписокФизическихЛиц = Новый Массив;
		Пока Выборка.Следующий() Цикл
			СписокФизическихЛиц.Добавить(Выборка.ФизическоеЛицо);
		КонецЦикла;
		
		Если СписокФизическихЛиц.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ФизическиеЛицаОрганизации.Вставить(Организация, СписокФизическихЛиц);
		
		ПараметрыБлокировки = Новый Структура("Организация,СписокФизическихЛиц,МесяцРасчетныхЛистов,ПерваяПоловинаМесяца");
		ПараметрыБлокировки.Организация 			= Организация;
		ПараметрыБлокировки.СписокФизическихЛиц 	= СписокФизическихЛиц;
		ПараметрыБлокировки.МесяцРасчетныхЛистов 	= НачалоМесяца(ПараметрыПроцедуры.Месяц);
		ПараметрыБлокировки.ПерваяПоловинаМесяца 	= Ложь;
		УстановитьБлокировкуПубликацииРЛ(ПараметрыБлокировки);
		
	КонецЦикла;
	
	ДанныеДляПубликации = Новый Соответствие;
	ДанныеДляПубликации.Вставить(ПараметрыПроцедуры.Месяц, ФизическиеЛицаОрганизации);
	
	ПараметрыПубликации = ПараметрыПубликацииРасчетныхЛистовПоУмолчанию();
	Если ДоступнаВерсияDTO_2_0() Тогда
		Ответ = ИнтеграцияКабинетСотрудникаОбмен.РезультатПубликацииРасчетныхЛистков(ДанныеДляПубликации, ПараметрыПубликации);
	Иначе
		Ответ = КабинетСотрудникаМенеджерОбмена.РезультатПубликацииРасчетныхЛистков(ДанныеДляПубликации, ПараметрыПубликации);
	КонецЕсли;
	
	Результат.БылиОшибки = Ответ.БылиОшибки;
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

// Публикация расчетных листков после подключения к сервису.
Процедура ОпубликоватьВсеРасчетныеЛисткиЗаПериоды(ПериодыРасчетныхЛистков) Экспорт

	Если ПериодыРасчетныхЛистков.Количество() = 0 Тогда
		Возврат
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВыгружаемыеОбъекты.Ссылка КАК Сотрудник,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТСотрудникиКПубликации
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиЗаполненияОбъектовУправлениеПерсоналом КАК ОшибкиЗаполнения
	|			ПО Сотрудники.ФизическоеЛицо = ОшибкиЗаполнения.Ссылка
	|				И (ОшибкиЗаполнения.БлокирующаяОшибка)
	|				И (ОшибкиЗаполнения.Приложение = ЗНАЧЕНИЕ(Перечисление.ПриложенияДляИнтеграции.КабинетСотрудника))
	|		ПО ВыгружаемыеОбъекты.Ссылка = Сотрудники.Ссылка
	|ГДЕ
	|	ВыгружаемыеОбъекты.Ссылка ССЫЛКА Справочник.Сотрудники
	|	И ОшибкиЗаполнения.БлокирующаяОшибка ЕСТЬ NULL";
	Запрос.Выполнить();
	Если Не ЗарплатаКадры.ВТСодержитСтроки(Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиКПубликации") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляПубликации = Новый Соответствие;
	Для каждого МесяцПубликации Из ПериодыРасчетныхЛистков Цикл
		ФизическиеЛицаОрганизации = ФизическиеЛицаОрганизаций(МесяцПубликации, Запрос.МенеджерВременныхТаблиц);
		ДанныеДляПубликации.Вставить(НачалоМесяца(МесяцПубликации), ФизическиеЛицаОрганизации);
	КонецЦикла;

	
	ПараметрыПубликации = ПараметрыПубликацииРасчетныхЛистовПоУмолчанию();
	Если ДоступнаВерсияDTO_2_0() Тогда
		Ответ = ИнтеграцияКабинетСотрудникаОбмен.РезультатПубликацииРасчетныхЛистков(ДанныеДляПубликации, ПараметрыПубликации);
	Иначе
		Ответ = КабинетСотрудникаМенеджерОбмена.РезультатПубликацииРасчетныхЛистков(ДанныеДляПубликации, ПараметрыПубликации);
	КонецЕсли;

КонецПроцедуры

Функция ФизическиеЛицаОрганизаций(Месяц, МенеджерВременныхТаблиц)
	
	ФизическиеЛицаПоОрганизациям = Новый Соответствие;
	
	ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ПараметрыПолученияСотрудников.НачалоПериода = НачалоМесяца(Месяц);
	ПараметрыПолученияСотрудников.ОкончаниеПериода = КонецМесяца(Месяц);
	ПараметрыПолученияСотрудников.КадровыеДанные = "Организация";
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудников);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СотрудникиОрганизации.Организация КАК Организация,
	|	СотрудникиОрганизации.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиКПубликации КАК ПубликуемыеСотрудники
	|		ПО СотрудникиОрганизации.ФизическоеЛицо = ПубликуемыеСотрудники.ФизическоеЛицо
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ПубликуемыеОбъекты
	|		ПО СотрудникиОрганизации.Организация = ПубликуемыеОбъекты.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	ФизическоеЛицо";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Организация") Цикл
		
		СписокФизическихЛиц = Новый Массив;
		Пока Выборка.Следующий() Цикл
			СписокФизическихЛиц.Добавить(Выборка.ФизическоеЛицо);
		КонецЦикла;
		
		Если СписокФизическихЛиц.Количество() > 0 Тогда
			ФизическиеЛицаПоОрганизациям.Вставить(Выборка.Организация, СписокФизическихЛиц);
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.Текст = "УНИЧТОЖИТЬ ВТСотрудникиОрганизации";
	Запрос.Выполнить();
	
	Возврат ФизическиеЛицаПоОрганизациям;
	
КонецФункции

Функция ПараметрыПубликацииРасчетныхЛистовПоУмолчанию()
	
	ПараметрыПубликации = ПараметрыПубликацииРасчетныхЛистов();
	Настройки = РегистрыСведений.НастройкиИнтеграцииКабинетСотрудника.НастройкиИнтеграции();
	ПараметрыПубликации.ВариантОтчета = Настройки.ВариантОтчетаРасчетныйЛисток;
	ПараметрыПубликации.ФормироватьРасчетныеЛистыОтдельнымЗапросом = Настройки.ФормироватьРасчетныеЛистыОтдельнымЗапросом;
	Возврат ПараметрыПубликации;
	
КонецФункции

Функция ПараметрыПубликацииРасчетныхЛистов()

	ПараметрыПубликации = Новый Структура("
	|Организация,
	|Месяц,
	|ПерваяПоловинаМесяца,
	|СписокФизическихЛиц,
	|ПовторнаяПубликация,
	|ВестиПротокол,
	|ВариантОтчета,
	|ФормироватьРасчетныеЛистыОтдельнымЗапросом");
	
	ПараметрыПубликации.ПовторнаяПубликация 						= Ложь;
	ПараметрыПубликации.ВестиПротокол 								= Ложь;
	ПараметрыПубликации.ПерваяПоловинаМесяца 						= Ложь;
	ПараметрыПубликации.ФормироватьРасчетныеЛистыОтдельнымЗапросом 	= Ложь;
	
	Возврат ПараметрыПубликации;

КонецФункции

Процедура УстановитьБлокировкуПубликацииРЛ(ПараметрыБлокировки)
	
	ОписаниеКлюча = Новый Структура("Организация,ФизическоеЛицо,Месяц,ПерваяПоловинаМесяца");
	ОписаниеКлюча.Организация 			= ПараметрыБлокировки.Организация;
	ОписаниеКлюча.Месяц 				= ПараметрыБлокировки.МесяцРасчетныхЛистов;
	ОписаниеКлюча.ПерваяПоловинаМесяца 	= ПараметрыБлокировки.ПерваяПоловинаМесяца;
	
	Попытка
		Для каждого ФизическоеЛицо Из ПараметрыБлокировки.СписокФизическихЛиц Цикл
			ОписаниеКлюча.ФизическоеЛицо = ФизическоеЛицо;
			КлючЗаписи = РегистрыСведений.РасчетныеЛисткиКабинетСотрудника.СоздатьКлючЗаписи(ОписаниеКлюча);
			ЗаблокироватьДанныеДляРедактирования(КлючЗаписи);
		КонецЦикла;
	Исключение
		СообщениеОбОшибке = НСтр("ru = 'Не удалось заблокировать данные для публикации расчетных лисков.
		|Вероятно, по выбранным сотрудникам публикация уже выполняется. Повторите попытку позже.';
		|en = 'Cannot lock the data to publish payslips.
		|The publication might be already in progress for the selected employees. Try again later.'", ОбщегоНазначения.КодОсновногоЯзыка()); 
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Комментарий = СтрШаблон("%1%2%3", СообщениеОбОшибке, Символы.ПС, ПодробноеПредставлениеОшибки);
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Предупреждение,,, Комментарий);
		ВызватьИсключение СообщениеОбОшибке;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ПравилаВыгрузки

Процедура ЗаписатьОбъектыПриСохраненииПравилВыгрузки(ОбъектыДляРегистрации) Экспорт
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПравилаВыгрузкиУправлениеПерсоналом");
		ЭлементБлокировки.УстановитьЗначение("Приложение", Приложение);
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.БудущиеСобытияУправлениеПерсоналом");
		ЭлементБлокировки.УстановитьЗначение("Приложение", Приложение);
		Блокировка.Добавить("РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника");
		Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ИнтеграцияКабинетСотрудникаВнутренний.ДобавитьЭлементыБлокировкиПриСохраненииПравилВыгрузки(Блокировка);
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ПравилаВыгрузкиУправлениеПерсоналом.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Приложение.Установить(Приложение);
		НаборЗаписей.Загрузить(ОбъектыДляРегистрации.НовыеПравила);
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.ВыгружаемыеОбъектыКабинетСотрудника.СоздатьНаборЗаписей();
		Для каждого СтрокаТЗ Из ОбъектыДляРегистрации.ВыгружаемыеОбъекты Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(),СтрокаТЗ);
		КонецЦикла;
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьНаборЗаписей();
		Для каждого СтрокаТЗ Из ОбъектыДляРегистрации.ИзмененияДляОбмена Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(),СтрокаТЗ);
		КонецЦикла;
		НаборЗаписей.Записать();
		
		Если ЗначениеЗаполнено(ОбъектыДляРегистрации.БудущиеСобытия) Тогда
			НаборЗаписей = РегистрыСведений.БудущиеСобытияУправлениеПерсоналом.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Приложение.Установить(Приложение);
			Для каждого СтрокаТЗ Из ОбъектыДляРегистрации.БудущиеСобытия Цикл
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(),СтрокаТЗ);
			КонецЦикла;
			НаборЗаписей.Записать();
		КонецЕсли;
		
		ИнтеграцияКабинетСотрудникаВнутренний.ЗаписатьОбъектыДляОбменаПриСохраненииПравилВыгрузки(ОбъектыДляРегистрации);
		Если ЗначениеЗаполнено(ОбъектыДляРегистрации.ФизическиеЛицаОрганизации) Тогда
			ЭДОБЗК.СформироватьСогласияНаПрисоединениеКЭДОБЗК(ОбъектыДляРегистрации.ФизическиеЛицаОрганизации);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ШаблонОписания = НСтр("ru = 'Ошибка записи правил.
			|%1';
			|en = 'Rule saving error.
			|%1'");
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Комментарий = СтрШаблон(ШаблонОписания, ПодробноеПредставлениеОшибки);
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
		// Исключение обрабатывает вызывающий метод.
		ВызватьИсключение
	КонецПопытки;
	
	ИнтеграцияУправлениеПерсоналом.ПроверитьЗаполнениеОбъектов(Приложение);

КонецПроцедуры

Функция ОбъектВыгружается(Ссылка) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|ГДЕ
	|	ВыгружаемыеОбъекты.Ссылка = &Ссылка";
	
	Возврат Не Запрос.Выполнить().Пустой(); 
	
КонецФункции

Процедура ЗарегистрироватьПрочиеВыгружаемыеОбъекты1СПерсонала(ВыгружаемыеОбъекты) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПравилаВыгрузки.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.ПравилаВыгрузкиУправлениеПерсоналом КАК ПравилаВыгрузки
	|ГДЕ
	|	ПравилаВыгрузки.Приложение = ЗНАЧЕНИЕ(Перечисление.ПриложенияДляИнтеграции.КабинетСотрудника)";
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ПубликоватьСтруктуруЮридическихЛиц = ИнтеграцияУправлениеПерсоналом.ПубликоватьСтруктуруЮридическихЛиц();
	ТипыДанных = Новый Массив;
	ТипыДанных.Добавить(Тип("СправочникСсылка.Должности"));
	Если ПубликоватьСтруктуруЮридическихЛиц Тогда
		ТипыДанных.Добавить(Тип("СправочникСсылка.ПодразделенияОрганизаций"));
		ТипыДанных.Добавить(Тип("СправочникСсылка.Организации"));
	Иначе
		ТипыДанных.Добавить(Тип("СправочникСсылка.СтруктураПредприятия"));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ВыгружаемыеОбъекты", ВыгружаемыеОбъекты);
	Запрос.УстановитьПараметр("ТипыДанных", ТипыДанных);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТНовыеОбъекты
	|ИЗ
	|	&ВыгружаемыеОбъекты КАК ВыгружаемыеОбъекты
	|ГДЕ
	|	НЕ ВыгружаемыеОбъекты.УсловноВыгружается
	|	И НЕ ВыгружаемыеОбъекты.Удалить
	|	И НЕ ВыгружаемыеОбъекты.ВыгружаетсяПоСсылке
	|	И ТИПЗНАЧЕНИЯ(ВыгружаемыеОбъекты.Ссылка) В (&ТипыДанных)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТТекущиеВыгружаемыеОбъекты
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|ГДЕ
	|	НЕ ВыгружаемыеОбъекты.УсловноВыгружается
	|	И ТИПЗНАЧЕНИЯ(ВыгружаемыеОбъекты.Ссылка) В (&ТипыДанных)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НовыеОбъекты.Ссылка КАК Ссылка
	|ИЗ
	|	ВТНовыеОбъекты КАК НовыеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТекущиеВыгружаемыеОбъекты КАК ТекущиеВыгружаемыеОбъекты
	|		ПО НовыеОбъекты.Ссылка = ТекущиеВыгружаемыеОбъекты.Ссылка
	|ГДЕ
	|	ТекущиеВыгружаемыеОбъекты.Ссылка ЕСТЬ NULL";
	НовыеОбъекты = Запрос.Выполнить().Выгрузить();
	
	Если Не ЗначениеЗаполнено(НовыеОбъекты) Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = НовыеОбъекты;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из НовыеОбъекты Цикл
			МенеджерЗаписи = РегистрыСведений.ВыгружаемыеОбъектыКабинетСотрудника.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТЗ);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ШаблонОписания = НСтр("ru = 'Ошибка записи правил.
			|%1';
			|en = 'Rule saving error.
			|%1'");
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Комментарий = СтрШаблон(ШаблонОписания, ПодробноеПредставлениеОшибки);
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
		// Исключение обрабатывает вызывающий метод.
		ВызватьИсключение
	КонецПопытки;

КонецПроцедуры

#КонецОбласти

#Область Версионирование

Процедура ЗаполнитьВерсииDTO(Версии) Экспорт

	Версии.Добавить("2.0");
	Версии.Добавить("2.1");
	Версии.Добавить("2.2");
	Версии.Добавить("2.3");
	Версии.Добавить("2.4");
	Версии.Добавить("3.0");
	Версии.Добавить("3.1");

КонецПроцедуры

Процедура ЗаполнитьВерсииAPI(Версии) Экспорт

	Версии.Добавить("2.0");
	Версии.Добавить("2.1");
	Версии.Добавить("2.2");
	Версии.Добавить("2.3");

КонецПроцедуры

Процедура УстановитьНовуюВерсиюDTO(ТекущаяВерсияDTO, НоваяВерсияDTO) Экспорт

	РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьВерсиюDTO(НоваяВерсияDTO);
	ВыполнитьДействияПриПереходеНаВерсиюDTO(ТекущаяВерсияDTO, НоваяВерсияDTO);

КонецПроцедуры

Процедура УстановитьНовуюВерсиюAPI(ТекущаяВерсияAPI, НоваяВерсияAPI) Экспорт

	РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьВерсиюAPI(НоваяВерсияAPI);
	ВыполнитьДействияПриПереходеНаВерсиюAPI(ТекущаяВерсияAPI, НоваяВерсияAPI);

КонецПроцедуры

Функция ИспользуетсяВерсияDTO(ВерсияДляСравнения) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ЗначениеЗаполнено(Настройки.ВерсияDTO) Тогда
		Возврат Ложь;
	Иначе
		Возврат ИнтеграцияУправлениеПерсоналом.СравнитьВерсии(Настройки.ВерсияDTO, ВерсияДляСравнения) >= 0;
	КонецЕсли;

КонецФункции

Функция ИспользуетсяВерсияAPI(ВерсияДляСравнения) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ЗначениеЗаполнено(Настройки.ВерсияAPI) Тогда
		Возврат Ложь;
	Иначе
		Возврат ИнтеграцияУправлениеПерсоналом.СравнитьВерсии(Настройки.ВерсияAPI, ВерсияДляСравнения) >= 0;
	КонецЕсли;

КонецФункции

Функция ВерсионированиеИспользуется() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ЗначениеЗаполнено(Настройки.ВерсияAPI);

КонецФункции

#КонецОбласти

#Область ОбработчикиСменыВерсииDTOИлиAPI

Процедура ВыполнитьДействияПриПереходеНаВерсиюDTO(ТекущаяВерсияDTO, НоваяВерсияDTO)

	Если Не ЗначениеЗаполнено(ТекущаяВерсияDTO) Тогда
		ЗарегистрироватьОбработчикОбмена(КабинетСотрудника.ИмяОбработчикаЗарегистрироватьПубликациюВидовСправок());
		ЗарегистрироватьОбработчикОбмена(КабинетСотрудника.ИмяОбработчикаОбновлениеФизическихЛиц());
		ЗарегистрироватьОбработчикОбмена(КабинетСотрудника.ИмяОбработчикаЗарегистрироватьОбновлениеУровнейДоступаКИ());
		РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьТребуетсяОбновитьНастройкиФункциональности(Истина);
	КонецЕсли;
	
	Если ИнтеграцияУправлениеПерсоналом.ВерсияФорматаМеньшеВерсии(ТекущаяВерсияDTO, "1.1")
		И ИнтеграцияУправлениеПерсоналом.ВерсияФорматаБольшеИлиРавнаВерсии(НоваяВерсияDTO, "1.1") Тогда
		ВключитьСборГрафиковОтпусков();
	КонецЕсли;
	
	Если ИнтеграцияУправлениеПерсоналом.ВерсияФорматаМеньшеВерсии(ТекущаяВерсияDTO, "2.0")
		И ИнтеграцияУправлениеПерсоналом.ВерсияФорматаБольшеИлиРавнаВерсии(НоваяВерсияDTO, "2.0") Тогда
		ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьПубликациюМашиночитаемыхДоверенностей());
		ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаОбновлениеОрганизацийИДолжностей());
		ЗаявкиСотрудников.ВыполнитьПереходНаВерсиюDTO_2_0();
	КонецЕсли;
	
	Если ИнтеграцияУправлениеПерсоналом.ВерсияФорматаМеньшеВерсии(ТекущаяВерсияDTO, "2.2")
		И ИнтеграцияУправлениеПерсоналом.ВерсияФорматаБольшеИлиРавнаВерсии(НоваяВерсияDTO, "2.2") Тогда
		ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаОбновлениеОрганизаци());
		ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьВыгрузкуШаблоновДокументов());
		ЭДОБЗК.ВключитьИспользованиеШаблоновДокументов();
	КонецЕсли;
	
	Если ИнтеграцияУправлениеПерсоналом.ВерсияФорматаМеньшеВерсии(ТекущаяВерсияDTO, "3.0")
		И ИнтеграцияУправлениеПерсоналом.ВерсияФорматаБольшеИлиРавнаВерсии(НоваяВерсияDTO, "3.0") Тогда
		РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьТребуетсяОбновитьНастройкиФункциональности(Истина);
		ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьВыгрузкуШаблоновДокументов_3_0());
		ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьОбновлениеОбъектов_3_0());
		Если ИнтеграцияУправлениеПерсоналом.ДоступенРасширенныйФункционал() И ИспользоватьРабочиеМестаОхраныТруда() Тогда
			ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьОбновлениеСотрудниковОхранаТруда());
			ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьВыгрузкуОхраныТруда());
		КонецЕсли;
	КонецЕсли;
	
	Если ИнтеграцияУправлениеПерсоналом.ВерсияФорматаМеньшеВерсии(ТекущаяВерсияDTO, "3.1")
		И ИнтеграцияУправлениеПерсоналом.ВерсияФорматаБольшеИлиРавнаВерсии(НоваяВерсияDTO, "3.1") Тогда
		УстановитьНовыеНастройкиФункциональности_3_1();
		Если ИнтеграцияУправлениеПерсоналом.ДоступенРасширенныйФункционал() Тогда
			ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьШаблоныДокументов_3_1());
			Если ИспользоватьРабочиеМестаОхраныТруда() Тогда
				УстановитьИспользоватьРабочиеМестаОхраныТруда();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЭДОБЗК.ПриИзмененииВерсииDTO();
	
КонецПроцедуры

Процедура ВыполнитьДействияПриПереходеНаВерсиюAPI(ТекущаяВерсияAPI, НоваяВерсияAPI)

	Если Не ЗначениеЗаполнено(ТекущаяВерсияAPI) Тогда
		// Переход на приложение с поддержкой версионирования.
		Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
		КабинетСотрудника.ВыполнитьДействияПриПереходеНаВерсию(Настройки.ВерсияПриложения, "6.0.1.1");
		БизнесПроцессыЗаявокСотрудников.ОбработатьПереходНаВерсиюФормата_1_0();
	КонецЕсли;
	
	Если ИнтеграцияУправлениеПерсоналом.ВерсияФорматаМеньшеВерсии(ТекущаяВерсияAPI, "2.2")
		И ИнтеграцияУправлениеПерсоналом.ВерсияФорматаБольшеИлиРавнаВерсии(НоваяВерсияAPI, "2.2") Тогда
		// Регистрируем к обмену виды отпусков, и обновление остатков отпусков
		Если ИнтеграцияУправлениеПерсоналом.ДоступенРасширенныйФункционал() Тогда
			ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьОбновлениеВидовОтпусков());
			ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьОбновлениеОстатковОтпусков());
			РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьТребуетсяОбновитьНастройкиФункциональности(Истина);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ДоступнаяФункциональность

Функция ИспользуютсяОграниченияДоступаКРабочимКонтактам() Экспорт

	Возврат ВерсионированиеИспользуется();

КонецФункции

Функция ИспользуютсяДоступныеФункцииФизическогоЛица() Экспорт

	Возврат ПолучитьФункциональнуюОпцию("ВедетсяУчетСогласийНаПрисоединениеКЭДОБЗК") И КабинетСотрудника.ИспользоватьФормат503();

КонецФункции

#КонецОбласти

#Область РучнаяРегистрацияИзмененийДляОбмена

Процедура ТипыОбъектовДляРучнойРегистрацииИзменений(ТипыОбъектов) Экспорт
	
	НоваяСтрока = ТипыОбъектов.Добавить();
	НоваяСтрока.ТипОбъекта 	= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ПрименяемыеВычетыНДФЛ;
	НоваяСтрока.ИмяТаблицы 	= ОбщегоНазначения.ИмяТаблицыПоСсылке(Справочники.ФизическиеЛица.ПустаяСсылка());
	
	Если ИспользуютсяОграниченияДоступаКРабочимКонтактам() Тогда
		НоваяСтрока = ТипыОбъектов.Добавить();
		НоваяСтрока.ТипОбъекта 	= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ОграничениеДоступаКРабочимКонтактам;
		НоваяСтрока.ИмяТаблицы 	= ОбщегоНазначения.ИмяТаблицыПоСсылке(Справочники.ФизическиеЛица.ПустаяСсылка());
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользуетсяЭДОБЗККабинетСотрудника") И ИспользуетсяВерсияDTO("2.0") Тогда
		НоваяСтрока = ТипыОбъектов.Добавить();
		НоваяСтрока.ТипОбъекта 	= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.МашиночитаемаяДоверенность;
		НоваяСтрока.ИмяТаблицы 	= ОбщегоНазначения.ИмяТаблицыПоСсылке(Справочники.МашиночитаемыеДоверенности.ПустаяСсылка());
	КонецЕсли;
	
	Если ИспользуютсяДоступныеФункцииФизическогоЛица() Тогда
		НоваяСтрока = ТипыОбъектов.Добавить();
		НоваяСтрока.ТипОбъекта 	= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДоступныеФункцииФизическогоЛица;
		НоваяСтрока.ИмяТаблицы 	= ОбщегоНазначения.ИмяТаблицыПоСсылке(Справочники.ФизическиеЛица.ПустаяСсылка());
	КонецЕсли;
	
	Если ИспользуетсяВерсияDTO("2.2") Тогда
		НоваяСтрока = ТипыОбъектов.Добавить();
		НоваяСтрока.ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ШаблонДокумента;
		НоваяСтрока.ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(Справочники.ШаблоныДокументов.ПустаяСсылка());
	КонецЕсли;
	
	ИнтеграцияКабинетСотрудникаВнутренний.ТипыОбъектовДляРучнойРегистрацииИзменений(ТипыОбъектов);
	
КонецПроцедуры

#КонецОбласти

 #Область ОбслуживаниеПодключенияОтключенияПриложения

Процедура ВыполнитьОбменПослеПодключенияПриложения(ПериодыРасчетныхЛистков) Экспорт
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	ИнтеграцияУправлениеПерсоналом.УстановитьБлокировкуДляВыполненияОбмена(Приложение, Ложь, Ложь);
	
	Попытка
		Если ИспользуетсяВерсияDTO("2.0") Тогда
			ИнтеграцияУправлениеПерсоналомОбмен.ВыполнитьОбменПослеПодключенияПриложения(Приложение);
		Иначе
			КабинетСотрудникаМенеджерОбмена.РезультатПубликацииПослеПодключения();
		КонецЕсли;
		Если ПериодыРасчетныхЛистков.Количество() > 0 И ИнтеграцияУправлениеПерсоналом.ПравилаВыгрузкиЗаданы(Приложение) Тогда
			ОпубликоватьВсеРасчетныеЛисткиЗаПериоды(ПериодыРасчетныхЛистков);
		КонецЕсли;
	Исключение 
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;
		
КонецПроцедуры

Процедура ВключитьНастройкиПрограммыПослеПодключенияПриложения() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не Константы.ИспользоватьБизнесПроцессыИЗадачи.Получить() Тогда
		Константы.ИспользоватьБизнесПроцессыИЗадачи.Установить(Истина);
	КонецЕсли;
	
	Попытка
		ЗаявкиСотрудников.ИнициализироватьРолиИсполнителей();
	Исключение
		ТекстОшибки = НСтр("ru = 'Не удалось инициализировать роли исполнителей задач';
							|en = 'Cannot initialize task assignee roles'");
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Комментарий = НСтр(
		"ru = '%1
		|
		|%2';
		|en = '%1
		|
		|%2'");
		Комментарий = СтрШаблон(Комментарий, ТекстОшибки, ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
	КонецПопытки;
	
	Если Не ИнтеграцияУправлениеПерсоналом.ПубликоватьСтруктуруЮридическихЛиц() Тогда
		Попытка
			ИнтеграцияУправлениеПерсоналом.ОбновитьСтруктуруПредприятия();
		Исключение
			ТекстОшибки = НСтр("ru = 'Не удалось обновить структуру предприятия';
								|en = 'Cannot update the enterprise structure'");
			ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Комментарий = НСтр(
			"ru = '%1
			|
			|%2';
			|en = '%1
			|
			|%2'");
			Комментарий = СтрШаблон(Комментарий, ТекстОшибки, ИнформацияОбОшибке);
			ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
		КонецПопытки;
	КонецЕсли;
		
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Попытка
		ЗаявкиСотрудников.НачальноеЗаполнениеИсполнителейЗадач(ТекущийПользователь);
	Исключение
		ТекстОшибки = НСтр("ru = 'Не удалось выполнить начальное заполнение исполнителей задач';
							|en = 'Cannot initially fill in task assignees'");
		ИнформацияОбОшибке = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Комментарий = НСтр(
		"ru = '%1
		|
		|%2';
		|en = '%1
		|
		|%2'");
		Комментарий = СтрШаблон(Комментарий, ТекстОшибки, ИнформацияОбОшибке);
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Истина);
	Константы.КонтролироватьОзнакомлениеСРасчетнымиЛистками.Установить(Истина);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ИнтеграцияУправлениеПерсоналом.ДоступнаВыгрузкаВидовСправок() И КабинетСотрудника.ИспользоватьФормат50375() Тогда
		ЗарегистрироватьПубликациюВидовСправок();
	КонецЕсли;
	
	Если ИспользуетсяВерсияDTO("1.1") Тогда
		ВключитьСборГрафиковОтпусков();
	КонецЕсли;
	
	Если ИспользуетсяВерсияDTO("2.0") Тогда
		ЗарегистрироватьПубликациюМашиночитаемыхДоверенностей();
	КонецЕсли;
	
	Если ИспользуетсяВерсияDTO("2.2") Тогда
		ЗарегистрироватьВыгрузкуШаблоновДокументов();
		ЭДОБЗК.ВключитьИспользованиеШаблоновДокументов();
	КонецЕсли;
	
	Если ИспользуетсяВерсияDTO("3.0") Тогда
		ЗарегистрироватьОбновлениеВидовОтпусков();
		ЗарегистрироватьВыгрузкуОхраныТруда();
		ЗарегистрироватьВыгрузкуШаблоновДокументов_3_0();
	КонецЕсли;
	
	Если ИспользуетсяВерсияDTO("3.1") Тогда
		ЗарегистрироватьШаблоныДокументов_3_1();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьДанныеПриОтключенииПриложения() Экспорт

	РегистрыСведений.НастройкиСервисаКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.ВыгружаемыеОбъектыКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	
	Константы.ИспользуетсяСервисКабинетСотрудника.Установить(Ложь);
	Константы.ИспользуютсяОбсужденияКабинетСотрудника.Установить(Ложь);
	Константы.КонтролироватьОзнакомлениеСРасчетнымиЛистками.Установить(Ложь);
	
	Если Константы.СервисКабинетСотрудникаВЛокальнойСети.Получить() Тогда
		МенеджерЗначенияКонстанты = Константы.СервисКабинетСотрудникаВЛокальнойСети.СоздатьМенеджерЗначения();
		МенеджерЗначенияКонстанты.ДополнительныеСвойства.Вставить("ИзменениеРазрешено");
		МенеджерЗначенияКонстанты.Значение = Ложь;
		МенеджерЗначенияКонстанты.Записать();
	КонецЕсли;
	
	РегистрыСведений.НастройкиКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.НастройкиИнтеграцииКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.НастройкиОтсутствийКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.НастройкиПравилОбработкиЗаявокКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.ОбработчикиОбменаКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.ПравилаОбработкиЗаявокКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.РасчетныеЛисткиКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	РегистрыСведений.ФайлыДляОтправкиНаПочтуКабинетСотрудника.СоздатьНаборЗаписей().Записать();
	
	Справочники.ПравилаСогласованияЗаявокКабинетСотрудника.УдалитьВсеПравила();
	
	ИнтеграцияКабинетСотрудникаВнутренний.ОчиститьДанныеПриОтключенииПриложения();

КонецПроцедуры

Процедура ЗаписатьНастройкиПриПодключенииПриложения(Ответственный, ОписаниеПриложения, ОписаниеИС) Экспорт
	
	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	Настройки.АдресПриложения 			= ОписаниеПриложения.АдресПриложения;
	Настройки.АдресПриложенияПоИмени 	= ОписаниеПриложения.АдресПриложенияПоИмени;
	Настройки.ИдентификаторПриложения 	= ОписаниеПриложения.Идентификатор;
	Настройки.НаименованиеПриложения  	= ОписаниеПриложения.Наименование;
	
	Настройки.Идентификатор = ОписаниеИС.Идентификатор;
	Настройки.Наименование  = ОписаниеИС.Наименование;
	Настройки.Ответственный = Ответственный;
	
	НаборЗаписей = РегистрыСведений.НастройкиСервисаКабинетСотрудника.СоздатьНаборЗаписей();
	ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Настройки);
	НаборЗаписей.Записать();
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	КлючиПриложения = Новый Структура("ИдентификаторКлиента,СекретКлиента");
	КлючиПриложения.ИдентификаторКлиента 	= ОписаниеИС.ИдентификаторКлиента;
	КлючиПриложения.СекретКлиента 			= ОписаниеИС.СекретКлиента;
	ИнтеграцияУправлениеПерсоналом.СохранитьКлючиПриложения(Приложение, КлючиПриложения);
	
	ПолучитьВерсииФорматаПриложения();
	РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьТребуетсяОбновитьНастройкиФункциональности(Истина);
	Если ИспользуетсяВерсияDTO("2.0") Тогда
		ИнтеграцияКабинетСотрудникаОбмен.ВыгрузитьНастройкиПриложения();
	Иначе
		КабинетСотрудникаМенеджерОбмена.ОбновитьНастройкиФункциональностиСервиса();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ответственный) Тогда
		ЗарегистрироватьОпубликоватьОтветственного(Ответственный);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ИзменениеНастроекПодключения 

Процедура СохранитьНовыеНастройкиПодключения(АдресПриложения, АдресСервисаАвторизации) Экспорт
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиСервисаКабинетСотрудника");
	Блокировка.Заблокировать();
	
	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	Настройки.АдресПриложения = АдресПриложения;
	Настройки.АдресСервисаАвторизации = АдресСервисаАвторизации;
	Настройки.АдресПриложенияПоИмени = "";
	Настройки.Идентификатор = "local";
	
	Запись = РегистрыСведений.НастройкиСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, Настройки);
	Запись.Записать();
	
	Если Не Константы.СервисКабинетСотрудникаВЛокальнойСети.Получить() Тогда
		МенеджерЗначенияКонстанты = Константы.СервисКабинетСотрудникаВЛокальнойСети.СоздатьМенеджерЗначения();
		МенеджерЗначенияКонстанты.ДополнительныеСвойства.Вставить("ИзменениеРазрешено");
		МенеджерЗначенияКонстанты.Значение = Истина;
		МенеджерЗначенияКонстанты.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура СохранитьНовыйАдресСервисаАвторизации(АдресСервисаАвторизации) Экспорт

	РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьАдресСервисаАвторизации(АдресСервисаАвторизации);

КонецПроцедуры

#КонецОбласти

#Область КонструкторыОбъектов

Функция НоваяТаблицаВыгружаемыеОбъекты() Экспорт

	Таблица = ОбщегоНазначенияБЗК.ТаблицаЗначенийПоИмениРегистраСведений("ВыгружаемыеОбъектыКабинетСотрудника");
	Таблица.Колонки.Добавить("Удалить", Новый ОписаниеТипов("Булево"));
	
	Возврат Таблица;
	
КонецФункции

Функция НоваяТаблицаИзмененияДляОбмена() Экспорт

	Таблица = ОбщегоНазначенияБЗК.ТаблицаЗначенийПоИмениРегистраСведений("ИзмененияДляОбменаКабинетСотрудника");
	Таблица.Колонки.Добавить("Удалить", Новый ОписаниеТипов("Булево"));
	
	Возврат Таблица;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиОбменаКабинетСотрудника

Процедура ВыполнитьОбработчикиОбмена(МенеджерОбмена) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Обработчики.Обработчик КАК Обработчик,
	|	Обработчики.НомерИтерации КАК НомерИтерации,
	|	Обработчики.ДатаРегистрации КАК ДатаРегистрации,
	|	Обработчики.ДатаВыполнения КАК ДатаВыполнения
	|ИЗ
	|	РегистрСведений.ОбработчикиОбменаКабинетСотрудника КАК Обработчики
	|ГДЕ
	|	НЕ Обработчики.Выполнено";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.Обработчик = ИмяОбработчикаЗарегистрироватьИзмененияПлановыхУдержаний() Тогда
			ИнтеграцияКабинетСотрудникаВнутренний.ЗарегистрироватьИзмененияПлановыхУдержаний();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаЗарегистрироватьПубликациюМашиночитаемыхДоверенностей() Тогда
			ЗарегистрироватьПубликациюМашиночитаемыхДоверенностей();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаОбновлениеОрганизацийИДолжностей() Тогда
			ЗарегистрироватьОбновлениеОрганизацийИДолжностей();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаЗарегистрироватьВыгрузкуШаблоновДокументов() Тогда
			ЗарегистрироватьВыгрузкуШаблоновДокументов();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаОбновлениеОрганизаци() Тогда
			ЗарегистрироватьОбновлениеОрганизаций();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаЗарегистрироватьОбновлениеВидовОтпусков() Тогда
			ЗарегистрироватьОбновлениеВидовОтпусков();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаЗарегистрироватьОбновлениеОстатковОтпусков() Тогда
			ЗарегистрироватьОбновлениеОстатковОтпусков();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаЗарегистрироватьВыгрузкуШаблоновДокументов_3_0() Тогда
			ЗарегистрироватьВыгрузкуШаблоновДокументов_3_0();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаЗарегистрироватьОбновлениеОбъектов_3_0() Тогда
			ЗарегистрироватьОбновлениеОбъектов_3_0();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаЗарегистрироватьОбновлениеСотрудниковОхранаТруда() Тогда
			ЗарегистрироватьОбновлениеСотрудниковОхранаТруда();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаЗарегистрироватьВыгрузкуОхраныТруда() Тогда
			ЗарегистрироватьВыгрузкуОхраныТруда();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаОтменитьВыгрузкуОхраныТруда() Тогда
			ОтменитьВыгрузкуОхраныТруда();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаЗарегистрироватьВыгрузкуМедицинскиеОсмотры() Тогда
			ЗарегистрироватьВыгрузкуМедицинскиеОсмотры();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаОтменитьВыгрузкуМедицинскиеОсмотры() Тогда
			ОтменитьВыгрузкуМедицинскиеОсмотры();
		ИначеЕсли Выборка.Обработчик = ИмяОбработчикаЗарегистрироватьШаблоныДокументов_3_1() Тогда
			ЗарегистрироватьШаблоныДокументов_3_1();
		КонецЕсли;
	КонецЦикла;
	
	КабинетСотрудника.ВыполнитьОбработчикиОбмена();

КонецПроцедуры

Процедура ЗарегистрироватьОбработчикОбмена(ИмяОбработчика, ПроверятьРегистрацию = Истина) Экспорт
	
	Если ПроверятьРегистрацию Тогда
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ИмяОбработчика", ИмяОбработчика);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОбработчикиОбмена.Обработчик КАК Обработчик
		|ИЗ
		|	РегистрСведений.ОбработчикиОбменаКабинетСотрудника КАК ОбработчикиОбмена
		|ГДЕ
		|	ОбработчикиОбмена.Обработчик = &ИмяОбработчика";
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ОбработчикиОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Обработчик = ИмяОбработчика;
	МенеджерЗаписи.Выполнено = Ложь;
	МенеджерЗаписи.Записать();

КонецПроцедуры

Процедура ОтключитьОбработчикОбмена(ИмяОбработчика) Экспорт

	МенеджерЗаписи = РегистрыСведений.ОбработчикиОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Обработчик = ИмяОбработчика;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Выполнено = Истина;
		МенеджерЗаписи.Записать();
	КонецЕсли;

КонецПроцедуры

Функция ИмяОбработчикаЗарегистрироватьИзмененияПлановыхУдержаний() Экспорт

	Возврат "ЗарегистрироватьИзмененияПлановыхУдержаний";

КонецФункции

Функция ИмяОбработчикаЗарегистрироватьПубликациюМашиночитаемыхДоверенностей()

	Возврат "ЗарегистрироватьПубликациюМашиночитаемыхДоверенностей";

КонецФункции

Процедура ЗарегистрироватьПубликациюМашиночитаемыхДоверенностей()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МашиноЧитаемыеДоверенности.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.МашиночитаемаяДоверенность) КАК ТипОбъекта
	|ИЗ
	|	Справочник.МашиночитаемыеДоверенности КАК МашиноЧитаемыеДоверенности
	|ГДЕ
	|	МашиноЧитаемыеДоверенности.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыМЧД.Действует)";
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ЗначениеЗаполнено(ТаблицаОбъектов) Тогда
		ОтключитьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьПубликациюМашиночитаемыхДоверенностей());
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = ТаблицаОбъектов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из ТаблицаОбъектов Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= СтрокаТЗ.Ссылка;
			МенеджерЗаписи.ТипОбъекта 	= СтрокаТЗ.ТипОбъекта;
			МенеджерЗаписи.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ОтключитьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьПубликациюМашиночитаемыхДоверенностей());
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;

КонецПроцедуры

Функция ИмяОбработчикаОбновлениеОрганизацийИДолжностей()

	Возврат "ОбновлениеОрганизацийИДолжностей";

КонецФункции

Процедура ЗарегистрироватьОбновлениеОрганизацийИДолжностей()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Должность) КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК Изменения
	|		ПО ВыгружаемыеОбъекты.Ссылка = Изменения.Ссылка
	|			И (Изменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Должность))
	|ГДЕ
	|	ВыгружаемыеОбъекты.Ссылка ССЫЛКА Справочник.Должности
	|	И Изменения.ВерсияДанных ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация)
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК Изменения
	|		ПО ВыгружаемыеОбъекты.Ссылка = Изменения.Ссылка
	|			И (Изменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация))
	|ГДЕ
	|	ВыгружаемыеОбъекты.Ссылка ССЫЛКА Справочник.Организации
	|	И Изменения.ВерсияДанных ЕСТЬ NULL";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ОтключитьОбработчикОбмена(ИмяОбработчикаОбновлениеОрганизацийИДолжностей());
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		Блокировка.Заблокировать();
		
		Пока Выборка.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= Выборка.Ссылка;
			МенеджерЗаписи.ТипОбъекта 	= Выборка.ТипОбъекта;
			МенеджерЗаписи.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ОтключитьОбработчикОбмена(ИмяОбработчикаОбновлениеОрганизацийИДолжностей());
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;

КонецПроцедуры

Функция ИмяОбработчикаЗарегистрироватьВыгрузкуШаблоновДокументов()
	
	Возврат "ЗарегистрироватьВыгрузкуШаблоновДокументов";
	
КонецФункции

Процедура ЗарегистрироватьВыгрузкуШаблоновДокументов()
	
	Шаблоны = Новый Массив;
	Шаблоны.Добавить(Справочники.ШаблоныДокументов.ЗапросСправки2НДФЛ);
	Шаблоны.Добавить(Справочники.ШаблоныДокументов.ЗапросСправкиПоОстаткамОтпуска);
	Шаблоны.Добавить(Справочники.ШаблоныДокументов.ЗапросСправкиСМестаРаботы);
	Шаблоны.Добавить(Справочники.ШаблоныДокументов.ЗаявлениеНаКомпенсациюОтпуска);
	Шаблоны.Добавить(Справочники.ШаблоныДокументов.ЗаявлениеНаНалоговыеВычеты);
	Шаблоны.Добавить(Справочники.ШаблоныДокументов.ЗаявлениеНаОтпуск);
	Шаблоны.Добавить(Справочники.ШаблоныДокументов.ЗаявлениеНаУдержаниеДСВ);
	Шаблоны.Добавить(Справочники.ШаблоныДокументов.ЗаявлениеОбИзмененииЛичныхДанных);
	Шаблоны.Добавить(Справочники.ШаблоныДокументов.Отсутствие);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Шаблоны", Шаблоны);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ШаблоныДокументов.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ШаблонДокумента) КАК ТипОбъекта
	|ИЗ
	|	Справочник.ШаблоныДокументов КАК ШаблоныДокументов
	|ГДЕ
	|	НЕ ШаблоныДокументов.ПометкаУдаления
	|	И НЕ ШаблоныДокументов.ВАрхиве
	|	И (НЕ ШаблоныДокументов.Предопределенный
	|			ИЛИ ШаблоныДокументов.Ссылка В (&Шаблоны))";
	
	УстановитьПривилегированныйРежим(Истина);
	ШаблоныКРегистрации = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ЗначениеЗаполнено(ШаблоныКРегистрации) Тогда
		ОтключитьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьВыгрузкуШаблоновДокументов());
		Возврат;
	КонецЕсли;
	
	ИмяОбработчика = ИмяОбработчикаЗарегистрироватьВыгрузкуШаблоновДокументов();
	
	ЗарегистрироватьВыгрузкуШаблоновПоОбработчику(ШаблоныКРегистрации, ИмяОбработчика);

КонецПроцедуры

Функция ИмяОбработчикаОбновлениеОрганизаци()

	Возврат "ОбновлениеОрганизаций";

КонецФункции

Процедура ЗарегистрироватьОбновлениеОрганизаций()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация) КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК Изменения
	|		ПО ВыгружаемыеОбъекты.Ссылка = Изменения.Ссылка
	|			И (Изменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация))
	|ГДЕ
	|	ВыгружаемыеОбъекты.Ссылка ССЫЛКА Справочник.Организации
	|	И Изменения.ВерсияДанных ЕСТЬ NULL";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ОтключитьОбработчикОбмена(ИмяОбработчикаОбновлениеОрганизаци());
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		Блокировка.Заблокировать();
		
		Пока Выборка.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= Выборка.Ссылка;
			МенеджерЗаписи.ТипОбъекта 	= Выборка.ТипОбъекта;
			МенеджерЗаписи.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ОтключитьОбработчикОбмена(ИмяОбработчикаОбновлениеОрганизаци());
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;

КонецПроцедуры

Функция ИмяОбработчикаЗарегистрироватьОбновлениеВидовОтпусков()

	Возврат "ЗарегистрироватьОбновлениеВидовОтпусков";

КонецФункции

Процедура ЗарегистрироватьОбновлениеВидовОтпусков()
	
	ИмяОбработчика = ИмяОбработчикаЗарегистрироватьОбновлениеВидовОтпусков();
	
	ТаблицаОбъектов = ИнтеграцияКабинетСотрудникаВнутренний.ТаблицаВидовОтпусковДляОбновления();
	Если Не ЗначениеЗаполнено(ТаблицаОбъектов) Тогда
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = ТаблицаОбъектов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из ТаблицаОбъектов Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= СтрокаТЗ.Ссылка;
			МенеджерЗаписи.ТипОбъекта 	= СтрокаТЗ.ТипОбъекта;
			МенеджерЗаписи.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;

КонецПроцедуры

Функция ИмяОбработчикаЗарегистрироватьОбновлениеОстатковОтпусков()

	Возврат "ЗарегистрироватьОбновлениеОстатковОтпусков";

КонецФункции

Процедура ЗарегистрироватьОбновлениеОстатковОтпусков()
	
	ИмяОбработчика = ИмяОбработчикаЗарегистрироватьОбновлениеОстатковОтпусков();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаработанныеПраваНаОтпуск) КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|ГДЕ
	|	ВыгружаемыеОбъекты.Ссылка ССЫЛКА Справочник.ФизическиеЛица";
	ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(ТаблицаОбъектов) Тогда
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = ТаблицаОбъектов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из ТаблицаОбъектов Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= СтрокаТЗ.Ссылка;
			МенеджерЗаписи.ТипОбъекта 	= СтрокаТЗ.ТипОбъекта;
			МенеджерЗаписи.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;

КонецПроцедуры

Функция ИмяОбработчикаЗарегистрироватьВыгрузкуШаблоновДокументов_3_0()

	Возврат "ЗарегистрироватьВыгрузкуШаблоновДокументов_3_0";

КонецФункции

Процедура ЗарегистрироватьВыгрузкуШаблоновДокументов_3_0()
	
	Шаблоны = Новый Массив;
	Шаблоны.Добавить(Справочники.ШаблоныДокументов.ЗаявлениеНаПереносОтпуска);
	Шаблоны.Добавить(Справочники.ШаблоныДокументов.ЗаявлениеНаОтмену);
	
	ИмяОбработчика = ИмяОбработчикаЗарегистрироватьВыгрузкуШаблоновДокументов_3_0();
	
	ЗарегистрироватьВыгрузкуШаблоновПоОбработчику(Шаблоны, ИмяОбработчика);

КонецПроцедуры

Функция ИмяОбработчикаЗарегистрироватьОбновлениеОбъектов_3_0()

	Возврат "ЗарегистрироватьОбновлениеОбъектов_3_0";

КонецФункции

Процедура ЗарегистрироватьОбновлениеОбъектов_3_0()
	
	ИмяОбработчика = ИмяОбработчикаЗарегистрироватьОбновлениеОбъектов_3_0();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо) КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК Изменения
	|		ПО ВыгружаемыеОбъекты.Ссылка = Изменения.Ссылка
	|			И (Изменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо))
	|ГДЕ
	|	ВыгружаемыеОбъекты.Ссылка ССЫЛКА Справочник.ФизическиеЛица
	|	И Изменения.ВерсияДанных ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение)
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК Изменения
	|		ПО ВыгружаемыеОбъекты.Ссылка = Изменения.Ссылка
	|			И (Изменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение))
	|ГДЕ
	|	ВыгружаемыеОбъекты.Ссылка ССЫЛКА Справочник.ПодразделенияОрганизаций
	|	И Изменения.ВерсияДанных ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник)
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК Изменения
	|		ПО ВыгружаемыеОбъекты.Ссылка = Изменения.Ссылка
	|			И (Изменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник))
	|ГДЕ
	|	ВыгружаемыеОбъекты.Ссылка ССЫЛКА Справочник.Сотрудники
	|	И Изменения.ВерсияДанных ЕСТЬ NULL";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		Блокировка.Заблокировать();
		
		Пока Выборка.Следующий() Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= Выборка.Ссылка;
			МенеджерЗаписи.ТипОбъекта 	= Выборка.ТипОбъекта;
			МенеджерЗаписи.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;

КонецПроцедуры

Функция ИмяОбработчикаЗарегистрироватьВыгрузкуОхраныТруда()

	Возврат "ЗарегистрироватьВыгрузкуОхраныТруда";

КонецФункции

Процедура ЗарегистрироватьВыгрузкуОхраныТруда()

	ИмяОбработчика = ИмяОбработчикаЗарегистрироватьВыгрузкуОхраныТруда();
	
	ТаблицаОбъектов = ИнтеграцияУправлениеПерсоналомСлужебный.РабочиеМестаОхранаТрудаДляОбновления();
	Если Не ЗначениеЗаполнено(ТаблицаОбъектов) Тогда
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		Возврат;
	КонецЕсли; 
	
	ИзмененияДляОбмена = НоваяТаблицаИзмененияДляОбмена();
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РабочееМестоОхраныТруда;
	Для каждого СтрокаТЗ Из ТаблицаОбъектов Цикл
		
		Если СтрокаТЗ.ЭтоГруппа Или СтрокаТЗ.ПометкаУдаления Или СтрокаТЗ.ВАрхиве Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ИзмененияДляОбмена.Добавить();
		НоваяСтрока.Ссылка 			= СтрокаТЗ.РабочееМестоОхраныТруда;
		НоваяСтрока.ТипОбъекта 		= ТипОбъекта;
		НоваяСтрока.ВерсияДанных 	= Строка(Новый УникальныйИдентификатор);
	
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = ИзмененияДляОбмена;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из ИзмененияДляОбмена Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТЗ);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;

КонецПроцедуры

Функция ИмяОбработчикаЗарегистрироватьОбновлениеСотрудниковОхранаТруда()

	Возврат "ЗарегистрироватьОбновлениеСотрудниковОхранаТруда";

КонецФункции

Процедура ЗарегистрироватьОбновлениеСотрудниковОхранаТруда()

	ИмяОбработчика = ИмяОбработчикаЗарегистрироватьОбновлениеСотрудниковОхранаТруда();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник) КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК Изменения
	|		ПО ВыгружаемыеОбъекты.Ссылка = Изменения.Ссылка
	|			И (Изменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник))
	|ГДЕ
	|	ВыгружаемыеОбъекты.Ссылка ССЫЛКА Справочник.Сотрудники
	|	И Изменения.ВыгружатьУдаление ЕСТЬ NULL";
	ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(ТаблицаОбъектов) Тогда
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = ТаблицаОбъектов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из ТаблицаОбъектов Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= СтрокаТЗ.Ссылка;
			МенеджерЗаписи.ТипОбъекта 	= СтрокаТЗ.ТипОбъекта;
			МенеджерЗаписи.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;

КонецПроцедуры

Функция ИмяОбработчикаОтменитьВыгрузкуОхраныТруда()

	Возврат "ОтменитьВыгрузкуОхраныТруда";

КонецФункции

Процедура ОтменитьВыгрузкуОхраныТруда()
	
	ИмяОбработчика = ИмяОбработчикаОтменитьВыгрузкуОхраныТруда();
	
	ТипСсылки = ИнтеграцияУправлениеПерсоналом.ТипРабочиеМестаОхраныТрудаСсылка();
	Если Не ОбщегоНазначения.ЭтоСсылка(ТипСсылки) Тогда
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА Изменения.ВыгружатьУдаление ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Удалить
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК Изменения
	|		ПО ВыгружаемыеОбъекты.Ссылка = Изменения.Ссылка
	|			И (Изменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РабочееМестоОхраныТруда))
	|ГДЕ
	|	&Условие";
	
	ТекстУсловия = СтрШаблон("%1 %2.%3", "ВыгружаемыеОбъекты.Ссылка ССЫЛКА","Справочник", Метаданные.НайтиПоТипу(ТипСсылки).Имя);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", ТекстУсловия);
	
	ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(ТаблицаОбъектов) Тогда
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = ТаблицаОбъектов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из ТаблицаОбъектов Цикл
			МенеджерЗаписи = РегистрыСведений.ВыгружаемыеОбъектыКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка = СтрокаТЗ.Ссылка;
			Если СтрокаТЗ.Удалить Тогда
				МенеджерЗаписи.Удалить();
			Иначе
				МенеджерЗаписи.УсловноВыгружается = Истина;
				МенеджерЗаписи.Записать();
			КонецЕсли;
		КонецЦикла;
		
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;

КонецПроцедуры

Процедура ЗарегистрироватьВыгрузкуШаблоновПоОбработчику(Шаблоны, ИмяОбработчика)

	ТаблицаШаблонов = Новый ТаблицаЗначений;
	ТаблицаШаблонов.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("СправочникСсылка.ШаблоныДокументов"));
	ТаблицаШаблонов.Колонки.Добавить("ТипОбъекта", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыОбъектовИнтеграцияУправлениеПерсоналом"));
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ШаблонДокумента;
	Для каждого ШаблонДокумента Из Шаблоны Цикл
		НоваяСтрока = ТаблицаШаблонов.Добавить();
		НоваяСтрока.Ссылка 		= ШаблонДокумента;
		НоваяСтрока.ТипОбъекта 	= ТипОбъекта;
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = ТаблицаШаблонов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из ТаблицаШаблонов Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= СтрокаТЗ.Ссылка;
			МенеджерЗаписи.ТипОбъекта 	= СтрокаТЗ.ТипОбъекта;
			МенеджерЗаписи.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;

КонецПроцедуры

Функция ИмяОбработчикаЗарегистрироватьВыгрузкуМедицинскиеОсмотры()

	Возврат "ЗарегистрироватьВыгрузкуМедицинскиеОсмотры";

КонецФункции

Процедура ЗарегистрироватьВыгрузкуМедицинскиеОсмотры()

	ИмяОбработчика = ИмяОбработчикаЗарегистрироватьВыгрузкуМедицинскиеОсмотры();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.МедицинскиеОсмотры) КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК Изменения
	|		ПО ВыгружаемыеОбъекты.Ссылка = Изменения.Ссылка
	|			И (Изменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.МедицинскиеОсмотры))
	|ГДЕ
	|	ВыгружаемыеОбъекты.Ссылка ССЫЛКА Справочник.ФизическиеЛица
	|	И Изменения.ВыгружатьУдаление ЕСТЬ NULL";
	ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(ТаблицаОбъектов) Тогда
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = ТаблицаОбъектов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из ТаблицаОбъектов Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= СтрокаТЗ.Ссылка;
			МенеджерЗаписи.ТипОбъекта 	= СтрокаТЗ.ТипОбъекта;
			МенеджерЗаписи.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;

КонецПроцедуры

Функция ИмяОбработчикаОтменитьВыгрузкуМедицинскиеОсмотры()

	Возврат "ОтменитьВыгрузкуМедицинскиеОсмотры";

КонецФункции

Процедура ОтменитьВыгрузкуМедицинскиеОсмотры()

	ИмяОбработчика = ИмяОбработчикаОтменитьВыгрузкуМедицинскиеОсмотры();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Изменения.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.МедицинскиеОсмотры) КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК Изменения
	|ГДЕ
	|	Изменения.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.МедицинскиеОсмотры)";
	ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(ТаблицаОбъектов) Тогда
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = ТаблицаОбъектов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из ТаблицаОбъектов Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= СтрокаТЗ.Ссылка;
			МенеджерЗаписи.ТипОбъекта 	= СтрокаТЗ.ТипОбъекта;
			МенеджерЗаписи.Удалить();
		КонецЦикла;
		
		ОтключитьОбработчикОбмена(ИмяОбработчика);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;

КонецПроцедуры

Функция ИмяОбработчикаЗарегистрироватьШаблоныДокументов_3_1()

	Возврат "ЗарегистрироватьШаблоныДокументов_3_1";

КонецФункции

Процедура ЗарегистрироватьШаблоныДокументов_3_1()
	
	ИмяОбработчика = ИмяОбработчикаЗарегистрироватьШаблоныДокументов_3_1();
	
	Шаблоны = Новый Массив;
	
	СсылкаПредопределенного = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.ЗаявлениеНаЕжемесячноеПособиеПоУходуЗаРебенком");
	Если ЗначениеЗаполнено(СсылкаПредопределенного) Тогда
		Шаблоны.Добавить(СсылкаПредопределенного);
	КонецЕсли;
	
	СсылкаПредопределенного = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.ЗаявлениеНаПособиеПриРожденииРебенка");
	Если ЗначениеЗаполнено(СсылкаПредопределенного) Тогда
		Шаблоны.Добавить(СсылкаПредопределенного);
	КонецЕсли;
	
	СсылкаПредопределенного = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.ЗаявлениеНаОтпускПоБеременностиИРодам");
	Если ЗначениеЗаполнено(СсылкаПредопределенного) Тогда
		Шаблоны.Добавить(СсылкаПредопределенного);
	КонецЕсли;
	
	ИнтеграцияКабинетСотрудникаВнутренний.ДополнитьШаблоныДокументов_3_1(Шаблоны);
	
	Если Не ЗначениеЗаполнено(Шаблоны) Тогда
		Возврат;
	КонецЕсли;
	
	ЗарегистрироватьВыгрузкуШаблоновПоОбработчику(Шаблоны, ИмяОбработчика);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаСостоянияПриложения

Функция ПриложениеЗаблокировано() Экспорт

	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	Возврат Настройки.СервисЗаблокирован;

КонецФункции

#КонецОбласти

#Область НастройкиФункциональностиСервиса

Функция ТребуетсяОбновитьНастройкиФункциональности() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	Возврат Настройки.ТребуетсяОбновитьНастройкиФункциональности;

КонецФункции

Процедура УстановитьТребуетсяОбновитьНастройкиФункциональности(НовоеЗначение) Экспорт

	РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьТребуетсяОбновитьНастройкиФункциональности(НовоеЗначение);

КонецПроцедуры

Функция СистемнаяУчетнаяЗаписьНастроена() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	СистемнаяУчетнаяЗаписьНастроена = РаботаСПочтовымиСообщениями.УчетнаяЗаписьНастроена(РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись(), Истина, Ложь);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СистемнаяУчетнаяЗаписьНастроена;

КонецФункции

Процедура СохранитьНастройкиФункциональностиСервиса(НовыеНастройки, ВызыватьИсключение = Ложь) Экспорт

	ТекущиеНастройки = РегистрыСведений.НастройкиКабинетСотрудника.Настройки();
	ОбновитьУровеньДоступаКИ = (ТекущиеНастройки.УровеньДоступаКИ <> НовыеНастройки.УровеньДоступаКИ);
	
	ФизическиеЛица = Неопределено;
	Если ОбновитьУровеньДоступаКИ Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка
		|ИЗ
		|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
		|ГДЕ
		|	ВыгружаемыеОбъекты.Ссылка ССЫЛКА Справочник.ФизическиеЛица";
		ФизическиеЛица = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	ИспользуетсяВерсияDTO_3_0 = ИспользуетсяВерсияDTO("3.0");
	ИспользуетсяВерсияDTO_3_1 = ИспользуетсяВерсияDTO("3.1");
	ТипОбъектаОграничениеДоступа = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ОграничениеДоступаКРабочимКонтактам;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиКабинетСотрудника");
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиСервисаКабинетСотрудника");
		Если ЗначениеЗаполнено(ФизическиеЛица) Тогда
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
			ЭлементБлокировки.ИсточникДанных = ФизическиеЛица;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
			ЭлементБлокировки.УстановитьЗначение("ТипОбъекта", ТипОбъектаОграничениеДоступа);
		ИначеЕсли ОбновитьУровеньДоступаКИ Тогда
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
			ЭлементБлокировки.УстановитьЗначение("ТипОбъекта", ТипОбъектаОграничениеДоступа);
		КонецЕсли;
		Блокировка.Заблокировать();
		
		РегистрыСведений.НастройкиКабинетСотрудника.СохранитьНовыеНастройки(НовыеНастройки);
		РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьТребуетсяОбновитьНастройкиФункциональности(Истина);
		Если ОбновитьУровеньДоступаКИ Тогда
			НаборЗаписей = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ТипОбъекта.Установить(ТипОбъектаОграничениеДоступа);
			Для каждого СтрокаТЗ Из ФизическиеЛица Цикл
				ЗаписьНабора = НаборЗаписей.Добавить();
				ЗаписьНабора.Ссылка 		= СтрокаТЗ.Ссылка;
				ЗаписьНабора.ТипОбъекта 	= ТипОбъектаОграничениеДоступа;
				ЗаписьНабора.ВерсияДанных 	= Строка(Новый УникальныйИдентификатор);
			КонецЦикла;
			НаборЗаписей.Записать();
		КонецЕсли;
		
		Если ИспользуетсяВерсияDTO_3_1 Тогда
			Если ТекущиеНастройки.ПоказыватьРабочиеМестаОхраныТруда <> НовыеНастройки.ПоказыватьРабочиеМестаОхраныТруда Тогда
				Если НовыеНастройки.ПоказыватьРабочиеМестаОхраныТруда Тогда
					ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьОбновлениеСотрудниковОхранаТруда(), Ложь);
					ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьВыгрузкуОхраныТруда(), Ложь);
					ОтключитьОбработчикОбмена(ИмяОбработчикаОтменитьВыгрузкуОхраныТруда());
				Иначе
					ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаОтменитьВыгрузкуОхраныТруда(), Ложь);
					ОтключитьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьОбновлениеСотрудниковОхранаТруда());
					ОтключитьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьВыгрузкуОхраныТруда());
				КонецЕсли;
			КонецЕсли;
			Если ТекущиеНастройки.ПоказыватьМедицинскиеОсмотры <> НовыеНастройки.ПоказыватьМедицинскиеОсмотры
				Или ТекущиеНастройки.ПоказыватьПсихиатрическиеОсвидетельствования <> НовыеНастройки.ПоказыватьПсихиатрическиеОсвидетельствования Тогда
				Если НовыеНастройки.ПоказыватьМедицинскиеОсмотры Или НовыеНастройки.ПоказыватьПсихиатрическиеОсвидетельствования Тогда
					ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьВыгрузкуМедицинскиеОсмотры(), Ложь);
					ОтключитьОбработчикОбмена(ИмяОбработчикаОтменитьВыгрузкуМедицинскиеОсмотры());
				Иначе
					ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаОтменитьВыгрузкуМедицинскиеОсмотры(), Ложь);
					ОтключитьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьВыгрузкуМедицинскиеОсмотры());
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ИспользуетсяВерсияDTO_3_0 Тогда
			Если ТекущиеНастройки.ПоказыватьОхрануТруда <> НовыеНастройки.ПоказыватьОхрануТруда Тогда
				Если НовыеНастройки.ПоказыватьОхрануТруда Тогда
					ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьОбновлениеСотрудниковОхранаТруда(), Ложь);
					ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьВыгрузкуОхраныТруда(), Ложь);
					ОтключитьОбработчикОбмена(ИмяОбработчикаОтменитьВыгрузкуОхраныТруда());
				Иначе
					ЗарегистрироватьОбработчикОбмена(ИмяОбработчикаОтменитьВыгрузкуОхраныТруда(), Ложь);
					ОтключитьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьОбновлениеСотрудниковОхранаТруда());
					ОтключитьОбработчикОбмена(ИмяОбработчикаЗарегистрироватьВыгрузкуОхраныТруда());
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ШаблонОписания = НСтр("ru = 'Не удалось сохранить настройки функциональности.';
								|en = 'Cannot save the functionality settings.'");
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Комментарий = СтрШаблон("%1%2%3", ШаблонОписания, Символы.ПС, ПодробноеПредставлениеОшибки);
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
		Если ВызыватьИсключение Тогда
			ВызватьИсключение ПодробноеПредставлениеОшибки;
		КонецЕсли;
	КонецПопытки;

КонецПроцедуры

Функция НастройкиФункциональностьДляОбновления() Экспорт
	
	НастройкиФункциональности = Новый Структура;
	НастройкиФункциональности.Вставить("РазделЗарплата", 						Ложь);
	НастройкиФункциональности.Вставить("РазделДокументы", 						Ложь);
	НастройкиФункциональности.Вставить("РазделКомпания", 						Ложь);
	НастройкиФункциональности.Вставить("РазделСправки", 						Ложь);
	НастройкиФункциональности.Вставить("РазделОтсутствия", 						Ложь);
	НастройкиФункциональности.Вставить("РазделОтпуск", 							Ложь);
	//
	НастройкиФункциональности.Вставить("ЗаявлениеНаОтпуск", 					Ложь);
	НастройкиФункциональности.Вставить("ЗаявлениеУчебныйОтпуск", 				Ложь);
	НастройкиФункциональности.Вставить("ЗаявлениеОтпускБезОплаты", 				Ложь);
	НастройкиФункциональности.Вставить("КомпенсацияОтпуска", 					Ложь);
	НастройкиФункциональности.Вставить("ЕдиновременнаяВыплатаКОтпуску", 		Ложь);
	НастройкиФункциональности.Вставить("МатериальнаяПомощьКОтпуску", 			Ложь);
	НастройкиФункциональности.Вставить("ПоказыватьОстаткиОтпусков", 			Ложь);
	НастройкиФункциональности.Вставить("ПоказыватьГрафикОтпусков", 				Ложь);
	НастройкиФункциональности.Вставить("ИспользоватьЗаявленияНаПереносОтпуска", Ложь);
	НастройкиФункциональности.Вставить("ИспользоватьЗаявленияНаОтменуОтпуска", 	Ложь);
	//
	НастройкиФункциональности.Вставить("Справки2НДФЛ", 							Ложь);
	НастройкиФункциональности.Вставить("СправкиСМестаРаботы", 					Ложь);
	НастройкиФункциональности.Вставить("СправкиОстаткиОтпусков", 				Ложь);
	//
	НастройкиФункциональности.Вставить("ОтсутствиеПоЛичнымОбстоятельствам", 	Ложь);
	НастройкиФункциональности.Вставить("Опоздание", 							Ложь);
	НастройкиФункциональности.Вставить("Болезнь", 								Ложь);
	НастройкиФункциональности.Вставить("Командировка", 							Ложь);
	НастройкиФункциональности.Вставить("Отгул", 								Ложь);
	НастройкиФункциональности.Вставить("ОтпускПоБеременностиИРодам", 			Ложь);
	НастройкиФункциональности.Вставить("ОтпускПоУходуЗаРебенком", 				Ложь);
	НастройкиФункциональности.Вставить("УходЗаРебенкомИнвалидом", 				Ложь);
	НастройкиФункциональности.Вставить("ВозвратИзОтпускаПоУходуЗаРебенком", 	Ложь);
	//
	НастройкиФункциональности.Вставить("ЗапросНаИзменениеЛичнойИнформации", 	Ложь);
	НастройкиФункциональности.Вставить("ДоступноИзменениеФото", 				Ложь);
	//
	НастройкиФункциональности.Вставить("ДоступноПолучениеФайлаДляПечати", 				Истина); // Всегда включено.
	НастройкиФункциональности.Вставить("ДоступноПолучениеСканаОригинала", 				Ложь);
	НастройкиФункциональности.Вставить("ДоступноПолучениеДокументаСЭП", 				Ложь);
	НастройкиФункциональности.Вставить("ДоступноПолучениеДокументаВБумажномВиде", 		Ложь);
	НастройкиФункциональности.Вставить("ДоступноПолучениеКопииНаЭлектроннуюПочту", 		Ложь);
	//
	НастройкиФункциональности.Вставить("ЗаявленияНаВычетыНДФЛ", 						Ложь);
	НастройкиФункциональности.Вставить("ЗаявленияНаДСВ", 								Ложь);
	НастройкиФункциональности.Вставить("СпособРасчетаДСВПроцентом", 					Ложь);
	НастройкиФункциональности.Вставить("СпособРасчетаДСВСуммой", 						Ложь);
	НастройкиФункциональности.Вставить("СпособРасчетаДСВПроцентомНеБолееСуммы", 		Ложь);
	//
	НастройкиФункциональности.Вставить("ПоказыватьОхрануТруда", 						Ложь);
	НастройкиФункциональности.Вставить("ПоказыватьРабочиеМестаОхраныТруда", 			Ложь);
	НастройкиФункциональности.Вставить("ПоказыватьМедицинскиеОсмотры", 					Ложь);
	НастройкиФункциональности.Вставить("ПоказыватьПсихиатрическиеОсвидетельствования", 	Ложь);
	//
	НастройкиФункциональности.Вставить("ИспользоватьПособияПриРожденииРебенка", 		Ложь);
	НастройкиФункциональности.Вставить("ИспользоватьПособияПоУходуЗаРебенком", 			Ложь);
	
	НастройкиФункциональности.Вставить("ДоступенОтказОтПодписания", 					Ложь);
	НастройкиФункциональности.Вставить("ПолучениеСогласияЭДОБЗКНаБумаге", 				Ложь);
	НастройкиФункциональности.Вставить("ПолучениеСогласияЭДОБЗКУНЭП", 					Ложь);
	НастройкиФункциональности.Вставить("ИспользоватьСборДанныхДляОформленияНаРаботу", 	Ложь);
	НастройкиФункциональности.Вставить("ИспользуетсяЭДОБЗК", 							Ложь);
	НастройкиФункциональности.Вставить("ИнтеграцияС1СПерсонал", 						Ложь);
	НастройкиФункциональности.Вставить("ИспользуютсяОбсуждения", 						Ложь);
	
	НастройкиФункциональности.Вставить("АдресСотрудника", 				Истина);
	НастройкиФункциональности.Вставить("ЛичнаяКонтактнаяИнформация", 	Истина);
	
	// Служебные настройки поддержки соответсвующей версии формата, устанавливаем значение Истина.
	// Выгружены будут только те настройки, которые соответсвуют формату DTO.
	НастройкиФункциональности.Вставить("ЗапросМестаРаботыВЗаявлении", 					Истина);
	НастройкиФункциональности.Вставить("РасширенныеРолиСогласования", 					Истина);
	НастройкиФункциональности.Вставить("РасширенныйЗапросНаИзменениеЛичнойИнформации", 	Истина);
	НастройкиФункциональности.Вставить("ОстаткиОтпусковВРазрезеСотрудников", 			Истина);
	НастройкиФункциональности.Вставить("ОформлятьУчебныеОтпускаОтдельнымЗаявлением", 	Истина);
	НастройкиФункциональности.Вставить("ОтпускПоУходуЗаРебенкомОтдельнымЗаявлением", 	Истина);
	НастройкиФункциональности.Вставить("ОтпускПоБеременностиИРодамОтдельнымЗаявлением", Истина);
	НастройкиФункциональности.Вставить("ПодтверждениеСогласиняНаЭДОСканом", 			Истина);
	
	// Заполнение настройками по умолчанию.
	НастройкиПоУмолчанию = НастройкиСервисаПоУмолчанию();
	ЗаполнитьЗначенияСвойств(НастройкиФункциональности, НастройкиПоУмолчанию);
	
	// Определение настроек доступных для изменения пользователем.
	ДоступныеНастройки = ДоступныеНастройкиСервиса();
	ИменаДоступныхНастроек = Новый Массив;
	Для каждого КлючИЗначение Из ДоступныеНастройки Цикл
		Если КлючиЗначение.Значение И НастройкиФункциональности.Свойство(КлючИЗначение.Ключ) Тогда
			ИменаДоступныхНастроек.Добавить(КлючИЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	ДоступныеНастройкиСтрока = СтрСоединить(ИменаДоступныхНастроек,",");
	
	// Заполнение сохраненными настройками.
	Настройки = РегистрыСведений.НастройкиКабинетСотрудника.Настройки();
	ЗаполнитьЗначенияСвойств(НастройкиФункциональности, Настройки, ДоступныеНастройкиСтрока);
	
	ИспользуетсяЭДОБЗК = ИспользуетсяЭДОБЗК();
	
	Если Не Настройки.РазделОтпуск Тогда
		НастройкиФункциональности.ПоказыватьГрафикОтпусков = Ложь;
	КонецЕсли;
	
	Если ИспользуетсяЭДОБЗК Тогда
		// Раздел всегда показываем, если используется ЭДО.
		НастройкиФункциональности.РазделДокументы = Истина;
	КонецЕсли;
	
	// Заполнение настроек
	НастройкиФункциональности.ИспользуютсяОбсуждения = ПолучитьФункциональнуюОпцию("ИспользуютсяОбсужденияКабинетСотрудника");
	НастройкиФункциональности.ИспользуетсяЭДОБЗК = ИспользуетсяЭДОБЗК;
	НастройкиФункциональности.ИспользоватьСборДанныхДляОформленияНаРаботу = ИспользуетсяСборДанныхДляОформленияНаРаботу();
	
	НастройкиФункциональности.ДоступноПолучениеДокументаСЭП = ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписи")
		И НастройкиФункциональности.ДоступноПолучениеДокументаСЭП;
	НастройкиФункциональности.ДоступноПолучениеКопииНаЭлектроннуюПочту = СистемнаяУчетнаяЗаписьНастроена()
		И НастройкиФункциональности.ДоступноПолучениеКопииНаЭлектроннуюПочту;
		
	СпособПолученияСогласия = СпособПолученияСогласияНаПрисоединениеКЭДОБЗК();
	НастройкиФункциональности.ПолучениеСогласияЭДОБЗКНаБумаге = (СпособПолученияСогласия = Перечисления.СпособыПолученияСогласияНаПрисоединениеКЭДОБЗК.БумажныйДокумент);
	НастройкиФункциональности.ПолучениеСогласияЭДОБЗКУНЭП = (СпособПолученияСогласия = Перечисления.СпособыПолученияСогласияНаПрисоединениеКЭДОБЗК.УНЭП);	
	
	// Получаем значение константы, а не ФО.
	НастройкиФункциональности.ИнтеграцияС1СПерсонал = Константы.ИспользуетсяПриложение1СПерсонал.Получить();
	
	Если ИнтеграцияУправлениеПерсоналом.ДоступенРасширенныйФункционал() Тогда
		
		НастройкиРасчетаЗарплаты = ИнтеграцияКабинетСотрудникаВнутренний.НастройкиРасчетаЗарплаты();
		
		НастройкиФункциональности.Командировка 		= НастройкиРасчетаЗарплаты.ИспользоватьОплатуКомандировок И НастройкиФункциональности.Командировка;
		НастройкиФункциональности.Отгул 			= НастройкиРасчетаЗарплаты.ИспользоватьОтгулы И НастройкиФункциональности.Отгул;
		НастройкиФункциональности.ЗаявлениеУчебныйОтпуск 	= НастройкиРасчетаЗарплаты.ИспользоватьОтпускаУчебные И НастройкиФункциональности.ЗаявлениеУчебныйОтпуск;
		НастройкиФункциональности.ЗаявлениеОтпускБезОплаты 	= НастройкиРасчетаЗарплаты.ИспользоватьОтпускаБезОплаты И НастройкиФункциональности.ЗаявлениеОтпускБезОплаты;
		НастройкиФункциональности.ЕдиновременнаяВыплатаКОтпуску = НастройкиРасчетаЗарплаты.ИспользоватьЕдиновременнуюВыплатуКОтпуску И НастройкиФункциональности.ЕдиновременнаяВыплатаКОтпуску;
		НастройкиФункциональности.МатериальнаяПомощьКОтпуску 	= НастройкиРасчетаЗарплаты.ИспользоватьМатериальнуюПомощьПриОтпуске И НастройкиФункциональности.МатериальнаяПомощьКОтпуску;
		
		Если ИспользуетсяВерсияDTO("2.3") Тогда
			НастройкиФункциональности.ПоказыватьОстаткиОтпусков = Настройки.ПоказыватьОстаткиОтпусков;
		Иначе
			НастройкиФункциональности.РазделОтпуск = Настройки.ПоказыватьОстаткиОтпусков;
		КонецЕсли;
		
		НастройкиФункциональности.ПоказыватьОхрануТруда 			= ПоказыватьОхрануТруда() И НастройкиФункциональности.ПоказыватьОхрануТруда;
		НастройкиФункциональности.ПоказыватьРабочиеМестаОхраныТруда = ИспользоватьРабочиеМестаОхраныТруда() И НастройкиФункциональности.ПоказыватьРабочиеМестаОхраныТруда;
		НастройкиФункциональности.ПоказыватьМедицинскиеОсмотры 		= ИспользоватьМедицинскиеОсмотры() И НастройкиФункциональности.ПоказыватьМедицинскиеОсмотры;
		НастройкиФункциональности.ПоказыватьПсихиатрическиеОсвидетельствования 	= ИспользоватьПсихиатрическиеОсвидетельствования() И НастройкиФункциональности.ПоказыватьПсихиатрическиеОсвидетельствования;
		
	Иначе
		
		ДоступенРасчетЗарплаты = ИнтеграцияУправлениеПерсоналом.ДоступенРасчетЗарплатыДляНебольшихОрганизаций();
		НастройкиФункциональности.РазделОтсутствия 				= ДоступенРасчетЗарплаты И НастройкиФункциональности.РазделОтсутствия;
		НастройкиФункциональности.ЗаявлениеНаОтпуск 			= ДоступенРасчетЗарплаты И НастройкиФункциональности.ЗаявлениеНаОтпуск;
		НастройкиФункциональности.ЗаявлениеОтпускБезОплаты 		= ДоступенРасчетЗарплаты И НастройкиФункциональности.ЗаявлениеОтпускБезОплаты;
		НастройкиФункциональности.Болезнь 						= ДоступенРасчетЗарплаты И НастройкиФункциональности.Болезнь;
		НастройкиФункциональности.ОтпускПоБеременностиИРодам 	= ДоступенРасчетЗарплаты И НастройкиФункциональности.ОтпускПоБеременностиИРодам;
		
		НастройкиФункциональности.ИспользоватьЗаявленияНаОтменуОтпуска 	= ДоступенРасчетЗарплаты И НастройкиФункциональности.ИспользоватьЗаявленияНаОтменуОтпуска;
		НастройкиФункциональности.ИспользоватьЗаявленияНаПереносОтпуска = ДоступенРасчетЗарплаты И НастройкиФункциональности.ИспользоватьЗаявленияНаПереносОтпуска;
		
	КонецЕсли;
	
	Возврат НастройкиФункциональности;
	
КонецФункции

Функция ДоступныеНастройкиСервиса() Экспорт

	Настройки = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(
				РегистрыСведений.НастройкиКабинетСотрудника.СоздатьМенеджерЗаписи(),
				Метаданные.РегистрыСведений.НастройкиКабинетСотрудника);
	//
	ДоступенРасширенныйФункционал = ИнтеграцияУправлениеПерсоналом.ДоступенРасширенныйФункционал();
	ИспользуетсяЭДОБЗК = ИнтеграцияКабинетСотрудника.ИспользуетсяЭДОБЗК();
	
	ИспользоватьФормат503 = КабинетСотрудника.ИспользоватьФормат503();
	ИспользуетсяВерсияDTO_1_0 = ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("1.0");
	ИспользуетсяВерсияDTO_2_0 = ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("2.0");
	ИспользуетсяВерсияDTO_2_3 = ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("2.3");
	ИспользуетсяВерсияDTO_3_0 = ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("3.0");
	ИспользуетсяВерсияDTO_3_1 = ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("3.1");
	
	Настройки.Справки2НДФЛ = Истина;
	Настройки.ДоступноПолучениеСканаОригинала 			= Истина;
	Настройки.ДоступноПолучениеДокументаВБумажномВиде 	= Истина;
	Настройки.ДоступноПолучениеДокументаСЭП 			= ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписи");
	Настройки.ДоступноПолучениеКопииНаЭлектроннуюПочту 	= ИнтеграцияКабинетСотрудника.СистемнаяУчетнаяЗаписьНастроена();
	
	Настройки.ЗаявленияНаВычетыНДФЛ = Истина;
	Настройки.ЗапросНаИзменениеЛичнойИнформации = Истина;
	Настройки.ОтсутствиеПоЛичнымОбстоятельствам = Истина;
	Настройки.Опоздание = Истина;
	
	Настройки.РазделЗарплата 	= ИспользоватьФормат503;
	Настройки.РазделСправки 	= ИспользоватьФормат503;
	Настройки.РазделКомпания 	= ИспользоватьФормат503;
	Настройки.РазделДокументы 	= ИспользоватьФормат503 И Не ИспользуетсяЭДОБЗК;
	
	Настройки.УровеньДоступаКИ = ИспользуетсяВерсияDTO_1_0;
	
	Настройки.ДоступенОтказОтПодписания = ИспользуетсяЭДОБЗК И ИспользуетсяВерсияDTO_2_3;
	
	Настройки.ИспользоватьПособияПриРожденииРебенка = ИспользуетсяВерсияDTO_3_1;
	Настройки.ИспользоватьПособияПоУходуЗаРебенком 	= ИспользуетсяВерсияDTO_3_1;
	
	Если ДоступенРасширенныйФункционал Тогда
		
		НастройкиРасчетаЗарплаты = ИнтеграцияКабинетСотрудникаВнутренний.НастройкиРасчетаЗарплаты();
		
		Настройки.СправкиСМестаРаботы = Истина;
		
		Настройки.ЗаявленияНаДСВ 			= НастройкиРасчетаЗарплаты.ИспользоватьДСВ;
		Настройки.СпособРасчетаДСВПроцентом = НастройкиРасчетаЗарплаты.ИспользоватьДСВ;
		Настройки.СпособРасчетаДСВСуммой 	= НастройкиРасчетаЗарплаты.ИспользоватьДСВ;
		Настройки.СпособРасчетаДСВПроцентомНеБолееСуммы = НастройкиРасчетаЗарплаты.ИспользоватьДСВ;
		
		Настройки.РазделОтпуск 				= ИспользуетсяВерсияDTO_2_3;
		Настройки.РазделОтсутствия 			= ИспользоватьФормат503;
		Настройки.КомпенсацияОтпуска 		= ИспользоватьФормат503;
		Настройки.ПоказыватьОстаткиОтпусков = ИспользоватьФормат503;
		
		Настройки.СправкиОстаткиОтпусков 		= ИспользуетсяВерсияDTO_1_0;
		
		Настройки.ЗаявлениеНаОтпуск 			= ИспользуетсяВерсияDTO_2_0;
		Настройки.ЗаявлениеОтпускБезОплаты 		= ИспользуетсяВерсияDTO_2_0;
		Настройки.ЗаявлениеУчебныйОтпуск 		= ИспользуетсяВерсияDTO_2_0 И НастройкиРасчетаЗарплаты.ИспользоватьОтпускаУчебные;
		
		Настройки.Болезнь 						= ИспользуетсяВерсияDTO_2_0;
		Настройки.ОтпускПоБеременностиИРодам 	= ИспользуетсяВерсияDTO_2_0;
		Настройки.ОтпускПоУходуЗаРебенком 		= ИспользуетсяВерсияDTO_2_0;
		Настройки.УходЗаРебенкомИнвалидом 		= ИспользуетсяВерсияDTO_2_0;
		Настройки.Командировка 					= ИспользуетсяВерсияDTO_2_0 И НастройкиРасчетаЗарплаты.ИспользоватьОплатуКомандировок;
		Настройки.Отгул 						= ИспользуетсяВерсияDTO_2_0 И НастройкиРасчетаЗарплаты.ИспользоватьОтгулы;
		
		Настройки.ЕдиновременнаяВыплатаКОтпуску = ИспользуетсяВерсияDTO_2_0 И НастройкиРасчетаЗарплаты.ИспользоватьЕдиновременнуюВыплатуКОтпуску;
		Настройки.МатериальнаяПомощьКОтпуску 	= ИспользуетсяВерсияDTO_2_0 И НастройкиРасчетаЗарплаты.ИспользоватьМатериальнуюПомощьПриОтпуске;
		
		Настройки.ДоступноИзменениеФото 				= ИспользуетсяВерсияDTO_3_0;
		Настройки.ИспользоватьЗаявленияНаОтменуОтпуска 	= ИспользуетсяВерсияDTO_3_0;
		Настройки.ИспользоватьЗаявленияНаПереносОтпуска = ИспользуетсяВерсияDTO_3_0;
		
		Настройки.ПоказыватьОхрануТруда 						= ДоступноИспользованиеОхраныТруда();
		Настройки.ПоказыватьРабочиеМестаОхраныТруда 			= ИспользуетсяВерсияDTO_3_1 И ДоступноИспользованиеРабочихМестОхраныТруда();
		Настройки.ПоказыватьМедицинскиеОсмотры 					= ДоступноИспользованиеМедицинскихОсмотров();
		Настройки.ПоказыватьПсихиатрическиеОсвидетельствования 	= ДоступноИспользованиеПсихиатрическихОсвидетельствований();
		
		Настройки.ПоказыватьГрафикОтпусков 			= ИспользуетсяВерсияDTO_3_1;
		Настройки.ВозвратИзОтпускаПоУходуЗаРебенком = ИспользуетсяВерсияDTO_3_1;
		
	Иначе
		
		ДоступенРасчетЗарплаты = ИнтеграцияУправлениеПерсоналом.ДоступенРасчетЗарплатыДляНебольшихОрганизаций();
		
		Настройки.РазделОтсутствия = ИспользоватьФормат503 И ДоступенРасчетЗарплаты;
		
		Настройки.ЗаявлениеНаОтпуск 			= ДоступенРасчетЗарплаты;
		Настройки.ЗаявлениеОтпускБезОплаты 		= ДоступенРасчетЗарплаты;
		Настройки.Болезнь					 	= ИспользуетсяВерсияDTO_2_0 И ДоступенРасчетЗарплаты;
		Настройки.ОтпускПоБеременностиИРодам 	= ИспользуетсяВерсияDTO_2_0 И ДоступенРасчетЗарплаты;
		
		Настройки.ИспользоватьЗаявленияНаОтменуОтпуска 	= ИспользуетсяВерсияDTO_3_0 И ДоступенРасчетЗарплаты;
		Настройки.ИспользоватьЗаявленияНаПереносОтпуска = ИспользуетсяВерсияDTO_3_0 И ДоступенРасчетЗарплаты;
	
	КонецЕсли;
	
	Возврат Настройки;
	
КонецФункции

Функция НастройкиСервисаПоУмолчанию() Экспорт

	Настройки = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(
				РегистрыСведений.НастройкиКабинетСотрудника.СоздатьМенеджерЗаписи(),
				Метаданные.РегистрыСведений.НастройкиКабинетСотрудника);
	//
	
	Настройки.РазделЗарплата 	= Истина;
	Настройки.РазделДокументы 	= Истина;
	Настройки.РазделСправки 	= Истина;
	Настройки.РазделКомпания 	= Истина;
	Настройки.Справки2НДФЛ 		= Истина;
	Настройки.ЗаявленияНаВычетыНДФЛ 				= Истина;
	Настройки.ЗапросНаИзменениеЛичнойИнформации 	= Истина;
	Настройки.ИспользоватьПособияПриРожденииРебенка = Истина;
	Настройки.ИспользоватьПособияПоУходуЗаРебенком 	= Истина;
	Настройки.ИспользоватьЗаявленияНаОтменуОтпуска 	= Ложь;
	Настройки.ИспользоватьЗаявленияНаПереносОтпуска = Ложь;
	
	Настройки.ДоступноПолучениеСканаОригинала = Истина;
	Настройки.ДоступноПолучениеДокументаВБумажномВиде = Истина;
	Настройки.ДоступноПолучениеДокументаСЭП = ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеПодписи");
	Настройки.ДоступноПолучениеКопииНаЭлектроннуюПочту = СистемнаяУчетнаяЗаписьНастроена();
	
	Настройки.УровеньДоступаКИ = Перечисления.УровниДоступаКИнформацииОСотрудниках.Руководители;
	
	Если ИнтеграцияУправлениеПерсоналом.ДоступенРасширенныйФункционал() Тогда
		
		НастройкиРасчетаЗарплаты = ИнтеграцияКабинетСотрудникаВнутренний.НастройкиРасчетаЗарплаты();
		Настройки.Командировка 					= НастройкиРасчетаЗарплаты.ИспользоватьОплатуКомандировок;
		Настройки.Отгул 						= НастройкиРасчетаЗарплаты.ИспользоватьОтгулы;
		Настройки.ЗаявлениеУчебныйОтпуск 		= НастройкиРасчетаЗарплаты.ИспользоватьОтпускаУчебные;
		Настройки.ЕдиновременнаяВыплатаКОтпуску = НастройкиРасчетаЗарплаты.ИспользоватьЕдиновременнуюВыплатуКОтпуску;
		Настройки.МатериальнаяПомощьКОтпуску 	= НастройкиРасчетаЗарплаты.ИспользоватьМатериальнуюПомощьПриОтпуске;
		
		Настройки.РазделОтсутствия 	= Истина;
		Настройки.РазделОтпуск 		= Истина;
		Настройки.ПоказыватьГрафикОтпусков 	= Истина;
		Настройки.ДоступноИзменениеФото 	= Истина;
		Настройки.ПоказыватьОстаткиОтпусков = Истина;
		Настройки.КомпенсацияОтпуска 		= Истина;
		Настройки.СправкиСМестаРаботы 		= Истина;
		Настройки.СправкиОстаткиОтпусков 	= Истина;
		Настройки.УходЗаРебенкомИнвалидом 	= Истина;
		Настройки.ОтпускПоУходуЗаРебенком 	= Истина;
		Настройки.Опоздание 				= Истина;
		Настройки.ЗаявлениеНаОтпуск 		= Истина;
		Настройки.ЗаявлениеОтпускБезОплаты 	= Истина;
		Настройки.Болезнь 					= Истина;
		Настройки.ОтпускПоБеременностиИРодам = Истина;
		
		Настройки.ВозвратИзОтпускаПоУходуЗаРебенком 	= Истина;
		Настройки.ОтсутствиеПоЛичнымОбстоятельствам 	= Истина;
		
	Иначе
		
		ДоступенРасчетЗарплатыДляНебольшихОрганизаций = ИнтеграцияУправлениеПерсоналом.ДоступенРасчетЗарплатыДляНебольшихОрганизаций();
		Настройки.РазделОтсутствия 						= ДоступенРасчетЗарплатыДляНебольшихОрганизаций;
		Настройки.ЗаявлениеНаОтпуск 					= ДоступенРасчетЗарплатыДляНебольшихОрганизаций;
		Настройки.ЗаявлениеОтпускБезОплаты 				= ДоступенРасчетЗарплатыДляНебольшихОрганизаций;
		Настройки.Болезнь 								= ДоступенРасчетЗарплатыДляНебольшихОрганизаций;
		Настройки.ОтпускПоБеременностиИРодам 			= ДоступенРасчетЗарплатыДляНебольшихОрганизаций;
		
	КонецЕсли;
	
	Возврат Настройки;
	
КонецФункции

Процедура УстановитьНовыеНастройкиФункциональности_3_1()
	
	Настройки = РегистрыСведений.НастройкиКабинетСотрудника.Настройки();
	Настройки.ИспользоватьПособияПриРожденииРебенка = Истина;
	Настройки.ИспользоватьПособияПоУходуЗаРебенком 	= Истина;
	Если ИнтеграцияУправлениеПерсоналом.ДоступенРасширенныйФункционал() Тогда
		Настройки.ВозвратИзОтпускаПоУходуЗаРебенком = Истина;
	КонецЕсли;
	РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьТребуетсяОбновитьНастройкиФункциональности(Истина);
	
КонецПроцедуры

#КонецОбласти

 #Область НастройкиОтсутствий

Функция НастройкиОтсутствийДляОбновления() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат РегистрыСведений.НастройкиОтсутствийКабинетСотрудника.НастройкиДляОбновления();

КонецФункции

Процедура СохранитьНастройкиОтсутствий(Настройки)

	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиОтсутствийКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = Настройки;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ПричинаОтсутствия", "ПричинаОтсутствия");
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из Настройки Цикл
			МенеджерЗаписи = РегистрыСведений.НастройкиОтсутствийКабинетСотрудника.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТЗ);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		СообщениеОбОшибке = НСтр("ru = 'Не удалось сохранить настройки. Подробности см. в журнале регистрации.';
								|en = 'Cannot save the settings. See the event log for details.'");
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
	КонецПопытки;

КонецПроцедуры 

Процедура УстановитьНеТребуетсяОбновитьНастройкиОтсутствий(УстановленныеНастройки) Экспорт
	
	ПричиныОтсутствий = УстановленныеНастройки.ВыгрузитьКолонку("ПричинаОтсутствия");
	
	УстановитьПривилегированныйРежим(Истина);
	ТекущиеНастройки = РегистрыСведений.НастройкиОтсутствийКабинетСотрудника.Настройки(ПричиныОтсутствий);
	Если ОбщегоНазначения.КоллекцииИдентичны(УстановленныеНастройки, ТекущиеНастройки) Тогда
		Для каждого СтрокаТЗ Из УстановленныеНастройки Цикл
			СтрокаТЗ.ТребуетсяОбновление = Ложь;
		КонецЦикла;
		СохранитьНастройкиОтсутствий(УстановленныеНастройки);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область НастройкиЗаявленийНаОтпуск

Функция ТребуетсяОбновитьНастройкиЗаявленийНаОтпуск() Экспорт
	
	Настройки = НастройкиЗаявленийНаОтпуск();
	Возврат Настройки.ОбновитьНастройкиВСервисе;

КонецФункции

Функция НастройкиЗаявленийНаОтпуск() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.НастройкиЗаявокНаОтпуск();

КонецФункции

Процедура УстановитьТребуетсяОбновитьНастройкиЗаявленийНаОтпуск(ТребуетсяОбновить, Настройки = Неопределено) Экспорт
	
	Если ТребуетсяОбновить Тогда
		РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.УстановитьТребуетсяОбновитьНастройки(ТребуетсяОбновить);
	Иначе
		Если Настройки = Неопределено Тогда
			РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.УстановитьТребуетсяОбновитьНастройки(ТребуетсяОбновить);
		Иначе
			ТекущиеНастройки = НастройкиЗаявленийНаОтпуск();
			Если ТекущиеНастройки.ВерсияИзменений = Настройки.ВерсияИзменений Тогда
				РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.УстановитьТребуетсяОбновитьНастройки(ТребуетсяОбновить);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область РегистрацияПубликацияОтветственногоЛица

Функция ЗарегистрироватьОпубликоватьОтветственного(Ответственный) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Ответственный);
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ВыгружаемыеОбъектыКабинетСотрудника.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Ссылка.Установить(Ответственный);
		
		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаписьНабора.Ссылка = Ответственный;
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки);
		Возврат Ложь;
	КонецПопытки;
	
	БылиОшибки = Ложь;
	Если ИспользуетсяВерсияDTO("2.0") Тогда
		Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
		БылиОшибки = ИнтеграцияУправлениеПерсоналомОбмен.РезультатВыгрузкиФизическогоЛица(Приложение, Ответственный);
	Иначе
		БылиОшибки = КабинетСотрудникаМенеджерОбмена.РезультатПубликацииФизическогоЛица(Ответственный);
	КонецЕсли;
	
	Возврат Не БылиОшибки;

КонецФункции

#КонецОбласти

#Область КадровыеДанныеФизическихЛиц

Функция ДобавитьПолеСведенийОДатахОтключенияФизическихЛиц(ИмяПоля, ТекстыОписанияПолей, ИсточникиДанных) Экспорт
	
	ДобавленоПолеСведений = Ложь;
	Если НеобходимыДатыОтключенияФизическихЛиц(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("СведенияОДатахОтключенияФизическихЛиц", Истина);
		
		ПутьКДанным = ПутьКДаннымСведенийОДатахОтключенияФизическихЛиц(ИмяПоля);
		ТекстыОписанияПолей.Добавить(ПутьКДанным + " КАК " + ИмяПоля);
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Функция НеобходимыДатыОтключенияФизическихЛиц(ИмяПоля) Экспорт
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	Возврат ИмяПоляВВерхнемРегистре = ВРег("ДатаОтключенияКабинетСотрудника");
	
КонецФункции

Функция ПутьКДаннымСведенийОДатахОтключенияФизическихЛиц(ИмяПоля)
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	ПутьКДанным = "";
	
	Если ИмяПоляВВерхнемРегистре = ВРег("ДатаОтключенияКабинетСотрудника") Тогда
		ПутьКДанным = "	СведенияОДатахОтключенияФизическихЛиц.ДатаОтключенияКабинетСотрудника";
	КонецЕсли;
	
	Возврат ПутьКДанным;
	
КонецФункции

Функция ЗапросВТСведенияОДатахОтключенияФизическихЛиц(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДнейСохраненияПубликации",
		РегистрыСведений.НастройкиИнтеграцииКабинетСотрудника.НастройкиИнтеграции().ДнейСохраненияПубликации);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ВыгружаемыеОбъектыКабинетСотрудника.ОкончаниеВыгрузки, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ДОБАВИТЬКДАТЕ(ВыгружаемыеОбъектыКабинетСотрудника.ОкончаниеВыгрузки, ДЕНЬ, &ДнейСохраненияПубликации)
		|	КОНЕЦ КАК ДатаОтключенияКабинетСотрудника,
		|	ТаблицаОтборов.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТСведенияОДатахОтключенияФизическихЛиц
		|ИЗ
		|	ВТОтборовФизическихЛиц КАК ТаблицаОтборов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъектыКабинетСотрудника
		|		ПО ТаблицаОтборов.ФизическоеЛицо = ВыгружаемыеОбъектыКабинетСотрудника.Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТОтборовФизическихЛиц",
		ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовФизическихЛиц);
	
	КадровыйУчет.УстановитьВТекстеЗапросаИмяПоляФизическоеЛицо(
		Запрос.Текст, "ТаблицаОтборов.ФизическоеЛицо", ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо);
	
	ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(Запрос.Текст, ТолькоРазрешенные);
	
	Возврат Запрос;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенияОДатахОтключенияФизическихЛиц(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных) Экспорт
	
	Если ИсточникиДанных.Получить("СведенияОДатахОтключенияФизическихЛиц") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЧастиЗапроса = Новый Массив;
	
	ЗапросСведении = ЗапросВТСведенияОДатахОтключенияФизическихЛиц(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов);
	ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросСведении);
	
	ЧастиЗапроса.Добавить(ЗапросСведении.Текст);
	ЧастиЗапроса.Добавить(ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов());
	ЧастиЗапроса.Добавить(Запрос.Текст);
	
	ЧастиЗапроса.Добавить(
		"	{ЛЕВОЕ СОЕДИНЕНИЕ ВТСведенияОДатахОтключенияФизическихЛиц КАК СведенияОДатахОтключенияФизическихЛиц
		|	ПО " + КадровыйУчет.ПутьКПолюФизическоеЛицо("ТаблицаОтборов", ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо) + " = СведенияОДатахОтключенияФизическихЛиц.ФизическоеЛицо}");
	
	Запрос.Текст = СтрСоединить(ЧастиЗапроса, Символы.ПС);
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиПриложения

Функция НастройкиПриложения() Экспорт

	Возврат РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();

КонецФункции

Процедура ЗаписатьВерсииФормата(ВерсияAPI, ВерсияDTO) Экспорт

	Настройки = НастройкиПриложения();
	Настройки.ВерсияAPI = ВерсияAPI;
	Настройки.ВерсияDTO = ВерсияDTO;
	
	МенеджерЗаписи = РегистрыСведений.НастройкиСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Настройки);
	МенеджерЗаписи.Записать();

КонецПроцедуры

Процедура ЗаписатьАдресПриложения(АдресПриложения, АдресСервисаАвторизации) Экспорт

	Настройки = НастройкиПриложения();
	Настройки.АдресПриложения = АдресПриложения;
	Настройки.АдресСервисаАвторизации = АдресСервисаАвторизации;
	
	МенеджерЗаписи = РегистрыСведений.НастройкиСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Настройки);
	МенеджерЗаписи.Записать();

КонецПроцедуры

Процедура ОчиститьНастройкиПодключения() Экспорт
	
	РегистрыСведений.НастройкиСервисаКабинетСотрудника.СоздатьНаборЗаписей().Записать();

КонецПроцедуры

Процедура ЗаписатьНастройкиПодключенияЛокальные(ИдентификаторПриложения, Ответственный) Экспорт

	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	Настройки.ИдентификаторПриложения 					= ИдентификаторПриложения;
	Настройки.Идентификатор 							= "local";
	Настройки.Ответственный 							= Ответственный;
	Настройки.ТребуетсяОбновитьНастройкиФункциональности = Истина;
	НаборЗаписей = РегистрыСведений.НастройкиСервисаКабинетСотрудника.СоздатьНаборЗаписей();
	ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Настройки);
	НаборЗаписей.Записать();

КонецПроцедуры

#КонецОбласти

#Область ОтправкаФайловНаПочту

Процедура ВыполнитьОтправкуФайловПоЭлектроннойПочте()
	
	Если Не СистемнаяУчетнаяЗаписьНастроена() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФайлыДляОтправки.Заявка КАК Заявка,
	|	ФайлыДляОтправки.Файл КАК Файл,
	|	ФайлыДляОтправки.АдресEmail КАК АдресEmail,
	|	ДанныеЗаявок.РезультатВыполнения КАК РезультатВыполнения,
	|	ДанныеЗаявок.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	РегистрСведений.ФайлыДляОтправкиНаПочтуКабинетСотрудника КАК ФайлыДляОтправки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеЗаявокКабинетСотрудника КАК ДанныеЗаявок
	|		ПО ФайлыДляОтправки.Заявка = ДанныеЗаявок.Заявка";
	
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	ОтменитьОтправку = ТаблицаДанных.СкопироватьКолонки();
	ДанныеДляОтправки = Новый Соответствие;
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		Если СтрокаТЗ.ПометкаУдаления Или СтрокаТЗ.РезультатВыполнения <> Перечисления.РезультатыВыполненияЗаявокКабинетСотрудника.Выполнена Тогда
			ЗаполнитьЗначенияСвойств(ОтменитьОтправку.Добавить(), СтрокаТЗ);
		Иначе
			ДанныеЗаявки = ДанныеДляОтправки[СтрокаТЗ.Заявка];
			Если ДанныеЗаявки = Неопределено Тогда
				ДанныеЗаявки = Новый Структура("АдресEmail,Файлы", СтрокаТЗ.АдресEmail, Новый Массив);
			КонецЕсли;
			ДанныеЗаявки.Файлы.Добавить(СтрокаТЗ.Файл);
			ДанныеДляОтправки.Вставить(СтрокаТЗ.Заявка, ДанныеЗаявки);
		КонецЕсли;
	КонецЦикла;
	
	Письма = Новый Массив;
	Если ДанныеДляОтправки.Количество() > 0 Тогда
		
		УчетнаяЗаписьЭлектроннойПочты = РаботаСПочтовымиСообщениями.СистемнаяУчетнаяЗапись();
		ПисьмаЗаявок = Новый Соответствие;
		Для каждого КлючИЗначение Из ДанныеДляОтправки Цикл
			
			Заявка = КлючИЗначение.Ключ;
			ДанныеЗаявки = КлючИЗначение.Значение;
			Попытка
				ФайлыПечатныхФорм = ЭДОБЗКВызовСервера.ВыводимыеФайлыПечатныхФорм(ДанныеЗаявки.Файлы);
			Исключение
				Для каждого Файл Из ДанныеЗаявки.Файлы Цикл
					НоваяСтрока = ОтменитьОтправку.Добавить();
					НоваяСтрока.Заявка 	= Заявка;
					НоваяСтрока.Файл 	= Файл;
				КонецЦикла;
				Продолжить;
			КонецПопытки;
			
			Письмо = РаботаСПочтовымиСообщениями.ПодготовитьПисьмо(
			УчетнаяЗаписьЭлектроннойПочты, ПараметрыПисьма(ДанныеЗаявки.АдресEmail, ФайлыПечатныхФорм));
			
			Письма.Добавить(Письмо);
			ПисьмаЗаявок.Вставить(Письмо, Заявка);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из ОтменитьОтправку Цикл
		Запись = РегистрыСведений.ФайлыДляОтправкиНаПочтуКабинетСотрудника.СоздатьМенеджерЗаписи();
		Запись.Заявка 	= СтрокаТЗ.Заявка;
		Запись.Файл 	= СтрокаТЗ.Файл;
		Запись.Удалить();
	КонецЦикла;
	
	РезультатыОтправки = Неопределено;
	Если Письма.Количество() > 0 Тогда
		Попытка
			РезультатыОтправки = РаботаСПочтовымиСообщениями.ОтправитьПисьма(УчетнаяЗаписьЭлектроннойПочты, Письма);
		Исключение
			ШаблонОписания = НСтр("ru = 'Ошибка отправки файлов справок.';
									|en = 'An error occurred while sending the certificate files.'");
			ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Комментарий = СтрШаблон("%1%2%3", ШаблонОписания, Символы.ПС, ПодробноеПредставлениеОшибки);
			ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
		КонецПопытки;
	КонецЕсли;
	
	Если РезультатыОтправки <> Неопределено Тогда
		
		ОбработанныеЗаявки = Новый Массив;
		
		Для каждого КлючИЗначение Из ПисьмаЗаявок Цикл
			Письмо = КлючИЗначение.Ключ;
			Заявка = КлючИЗначение.Значение;
			РезультатОтправки = РезультатыОтправки[Письмо];
			Если РезультатОтправки <> Неопределено Тогда
				Если РезультатОтправки.ОшибочныеПолучатели.Количество() > 0 Тогда
					ТекстыОшибки = Новый Массив;
					Для Каждого ОписаниеОшибки Из РезультатОтправки.ОшибочныеПолучатели Цикл
						ТекстыОшибки.Добавить(СтрШаблон("%1 - %2"), ОписаниеОшибки.Ключ, ОписаниеОшибки.Значение);
					КонецЦикла;
					ШаблонОписания = НСтр("ru = 'Ошибка отправки файлов справки.';
											|en = 'An error occurred while sending the certificate files.'");
					ТекстОшибки = СтрСоединить(ТекстыОшибки, "; ");
					Комментарий = СтрШаблон("%1: %2%3%4", ШаблонОписания, Строка(Заявка), Символы.ПС, ТекстОшибки);
					ЗаписьЖурналаРегистрации(ИменаСобытийЖР().ПрочиеСобытия, УровеньЖурналаРегистрации.Ошибка,,, Комментарий);
				КонецЕсли;
				ОбработанныеЗаявки.Добавить(Заявка);
			КонецЕсли;
		КонецЦикла;
		
		НаборЗаписей = РегистрыСведений.ФайлыДляОтправкиНаПочтуКабинетСотрудника.СоздатьНаборЗаписей();
		Для каждого Заявка Из ОбработанныеЗаявки Цикл
			НаборЗаписей.Отбор.Заявка.Установить(Заявка);
			НаборЗаписей.Записать();
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура УдалитьНеподтвержденныеЗаписиДокументыПоЗаявкам()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаАктуальности", ТекущаяДатаСеанса() - 600);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыПоЗаявкам.Заявка КАК Заявка,
	|	ДокументыПоЗаявкам.ДокументПоЗаявке КАК ДокументПоЗаявке,
	|	ДокументыПоЗаявкам.ДатаЗаписи КАК ДатаЗаписи
	|ИЗ
	|	РегистрСведений.ДокументыПоЗаявкамКабинетСотрудника КАК ДокументыПоЗаявкам
	|ГДЕ
	|	ДокументыПоЗаявкам.НеподтвержденнаяЗапись
	|	И ДокументыПоЗаявкам.ДатаЗаписи < &ДатаАктуальности";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДокументыПоЗаявкамКабинетСотрудника");
			ЭлементБлокировки.УстановитьЗначение("Заявка", Выборка.Заявка);
			ЭлементБлокировки.УстановитьЗначение("ДокументПоЗаявке", Выборка.ДокументПоЗаявке);
			Блокировка.Заблокировать();
			
			МенеджерЗаписи = РегистрыСведений.ДокументыПоЗаявкамКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Заявка 			= Выборка.Заявка;
			МенеджерЗаписи.ДокументПоЗаявке = Выборка.ДокументПоЗаявке;
			МенеджерЗаписи.Удалить();
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			// Не обрабатываем исключение, удалим неподтвержденную запись при следующем вызове.
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры

Функция ПараметрыПисьма(АдресПолучателя, ФайлыПечатныхФорм)
	
	ПараметрыПисьма = Новый Структура;
	ПараметрыПисьма.Вставить("Кому", АдресПолучателя);
	ПараметрыПисьма.Вставить("Вложения", Новый Массив);
	
	ИменаФайлов = Новый Массив;
	Для Каждого ФайлПечатнойФормы Из ФайлыПечатныхФорм Цикл
		ОписаниеВложения = Новый Структура("Представление,АдресВоВременномХранилище");
		ОписаниеВложения.Представление = ФайлПечатнойФормы.ИмяФайлаСРасширением;
		ОписаниеВложения.АдресВоВременномХранилище = ФайлПечатнойФормы.АдресВоВременномХранилище;
		ПараметрыПисьма.Вложения.Добавить(ОписаниеВложения);
		ИменаФайлов.Добавить(ФайлПечатнойФормы.ИмяФайлаСРасширением);
	КонецЦикла;
	
	ПараметрыПисьма.Вставить("Тема", НСтр("ru = 'Документы';
											|en = 'Documents'") + ": " + СтрСоединить(ИменаФайлов, "; "));
	
	ПараметрыПисьма.Вставить("Тело",
		СтрокаСЧислом(
			НСтр("ru = ';Приложен %1 файл;;Приложено %1 файла;Приложено %1 файлов;Приложены файлы';
				|en = ';Attached %1 file;; Attached %1 files; Attached %1 files; Attached files'"),
			ПараметрыПисьма.Вложения.Количество(), ВидЧисловогоЗначения.Количественное));
	
	ПараметрыПисьма.Тело = ПараметрыПисьма.Тело + ":" + Символы.ПС + Символы.Таб
		+ СтрСоединить(ИменаФайлов, ";" + Символы.ПС + Символы.Таб);
		
	Возврат ПараметрыПисьма;
	
КонецФункции

#КонецОбласти

#Область ПолучениеНастроекОхраныТруда

Функция ДоступноИспользованиеОхраныТруда() Экспорт

	Если ИспользуетсяВерсияDTO("3.0") Тогда
		Возврат ИнтеграцияКабинетСотрудникаВнутренний.ДоступноИспользованиеОхраныТруда();
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Функция ПоказыватьОхрануТруда()

	Настройки = РегистрыСведений.НастройкиКабинетСотрудника.Настройки();
	Возврат Настройки.ПоказыватьОхрануТруда И ДоступноИспользованиеОхраныТруда();

КонецФункции

Функция ДоступноИспользованиеРабочихМестОхраныТруда() Экспорт
	
	Если ИспользуетсяВерсияDTO("3.0") Тогда
		Возврат ИнтеграцияКабинетСотрудникаВнутренний.ДоступноИспользованиеРабочихМестОхраныТруда();
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Функция ИспользоватьРабочиеМестаОхраныТруда() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Настройки = РегистрыСведений.НастройкиКабинетСотрудника.Настройки();
	Возврат Настройки.ПоказыватьРабочиеМестаОхраныТруда И ДоступноИспользованиеРабочихМестОхраныТруда();

КонецФункции

Функция ДоступноИспользованиеМедицинскихОсмотров() Экспорт

	Если ИспользуетсяВерсияDTO("3.1") Тогда
		Возврат ИнтеграцияКабинетСотрудникаВнутренний.ДоступноИспользованиеМедицинскихОсмотров();
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Функция ИспользоватьМедицинскиеОсмотры() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Настройки = РегистрыСведений.НастройкиКабинетСотрудника.Настройки();
	Возврат Настройки.ПоказыватьМедицинскиеОсмотры И ДоступноИспользованиеМедицинскихОсмотров();

КонецФункции

Функция ДоступноИспользованиеПсихиатрическихОсвидетельствований() Экспорт

	Если ИспользуетсяВерсияDTO("3.1") Тогда
		Возврат ИнтеграцияКабинетСотрудникаВнутренний.ДоступноИспользованиеПсихиатрическихОсвидетельствований();
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции

Функция ИспользоватьПсихиатрическиеОсвидетельствования() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Настройки = РегистрыСведений.НастройкиКабинетСотрудника.Настройки();
	Возврат Настройки.ПоказыватьПсихиатрическиеОсвидетельствования И ДоступноИспользованиеПсихиатрическихОсвидетельствований();

КонецФункции

#КонецОбласти

#Область Печать

// Определяет объекты, в которых есть процедура ДобавитьКомандыПечати().
// Подробнее см. УправлениеПечатьюПереопределяемый.
//
// Параметры:
//  СписокОбъектов - Массив - список менеджеров объектов.
//
Процедура ПриОпределенииОбъектовСКомандамиПечати(СписокОбъектов) Экспорт
	
	СписокОбъектов.Добавить(Документы.ДанныеДляОформленияНаРаботу);
	
КонецПроцедуры

#КонецОбласти

#Область Свойства

// См. УправлениеСвойствамиПереопределяемый.ПриПолученииПредопределенныхНаборовСвойств.
Процедура ПриПолученииПредопределенныхНаборовСвойств(Наборы) Экспорт
	
	УправлениеСвойствамиБЗК.ЗарегистрироватьНаборСвойств(Наборы, "d13893d6-818d-4eeb-95eb-258ddc3aa9ef", Метаданные.Документы.ДанныеДляОформленияНаРаботу);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИБ

#Область ПереносДанных

Процедура Перенос_ИзмененияДляОбменаКабинетСотрудника()
	
	ИзмененияДляОбмена = НоваяТаблицаИзмененияДляОбмена();
	
	ТипОбъектаПоТипуОбъектаКС = ТипОбъектаПоТипуОбъектаКС();
	ТипЗначенияТипОбъекта = ТипЗначенияТипОбъекта();
	
	Запрос = Новый Запрос;
	
	// ДокументыКУдалениюКабинетСотрудника
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка,
	|	ИдентификаторыДокументов.ТипОбъекта КАК ТипОбъекта
	|ИЗ
	|	Документ.ДокументЭДОБЗК КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьДокументыКУдалениюКабинетСотрудника КАК ИдентификаторыДокументов
	|		ПО Таблица.ИдентификаторДокумента = ИдентификаторыДокументов.ИдентификаторДокумента
	|			И (ИдентификаторыДокументов.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКабинетСотрудника.ДокументНаПодпись))";
	Таблица = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТЗ Из Таблица Цикл
		НоваяСтрока = ИзмененияДляОбмена.Добавить();
		НоваяСтрока.Ссылка 				= СтрокаТЗ.Ссылка;
		НоваяСтрока.ТипОбъекта 			= ТипОбъектаПоТипуОбъектаКС[СтрокаТЗ.ТипОбъекта];
		НоваяСтрока.ВыгружатьУдаление 	= Истина;
	КонецЦикла;
	
	ТипСборГрафиков = ИнтеграцияУправлениеПерсоналом.ТипСборГрафиковОтпусковСсылка();
	Если ЗначениеЗаполнено(ТипСборГрафиков) Тогда
		
		ДокументМенеджер = ОбщегоНазначенияБЗК.МенеджерОбъектаПоТипу(ТипСборГрафиков);
		ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(ДокументМенеджер.ПустаяСсылка());
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.Ссылка КАК Ссылка,
		|	Таблица.ИдентификаторДокумента КАК ИдентификаторДокумента,
		|	Таблица.КорневойСборГрафиков КАК КорневойСборГрафиков
		|ИЗ
		|	#ИмяТаблицы КАК Таблица
		|ГДЕ
		|	Таблица.ИдентификаторДокумента = """"";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяТаблицы", ИмяТаблицы);
		РезультатЗапрос = Запрос.Выполнить();
		Если Не РезультатЗапрос.Пустой() Тогда
			Выборка = РезультатЗапрос.Выбрать();
			Пока Выборка.Следующий() Цикл
				Если ЗначениеЗаполнено(Выборка.КорневойСборГрафиков) Тогда
					Продолжить;
				КонецЕсли;
				ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
				ДокументОбъект.ИдентификаторДокумента = Строка(Выборка.Ссылка.УникальныйИдентификатор());
				ДокументОбъект.ДополнительныеСвойства.Вставить("НеОбновлятьВыгрузку");
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
			КонецЦикла;
		КонецЕсли;
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Ссылка,
		|	ИдентификаторыДокументов.ТипОбъекта
		|ИЗ
		|	#ИмяТаблицы КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьДокументыКУдалениюКабинетСотрудника КАК ИдентификаторыДокументов
		|		ПО Таблица.ИдентификаторДокумента = ИдентификаторыДокументов.ИдентификаторДокумента
		|			И (ИдентификаторыДокументов.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКабинетСотрудника.СборГрафиковОтпусков))";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяТаблицы", ИмяТаблицы);
		Таблица = Запрос.Выполнить().Выгрузить();
		Для каждого СтрокаТЗ Из Таблица Цикл
			НоваяСтрока = ИзмененияДляОбмена.Добавить();
			НоваяСтрока.Ссылка 				= СтрокаТЗ.Ссылка;
			НоваяСтрока.ТипОбъекта 			= ТипОбъектаПоТипуОбъектаКС[СтрокаТЗ.ТипОбъекта];
			НоваяСтрока.ВыгружатьУдаление 	= Истина;
		КонецЦикла;
		
	КонецЕсли;
		
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Документы.ИдентификаторДокумента КАК ИдентификаторДокумента
	|ИЗ
	|	РегистрСведений.УдалитьДокументыКУдалениюКабинетСотрудника КАК Документы
	|ГДЕ
	|	Документы.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовКабинетСотрудника.СогласиеНаПрисоединениеКЭДОБЗК)";
	Идентификаторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИдентификаторДокумента");
	Согласия = Новый Массив;
	Для каждого Идентификатор Из Идентификаторы Цикл
		Согласия.Добавить(Документы.СогласиеНаПрисоединениеКЭДОБЗК.ПолучитьСсылку(Новый УникальныйИдентификатор(Идентификатор)));
	КонецЦикла;
	Запрос.УстановитьПараметр("Ссылки", Согласия);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СогласиеНаПрисоединениеКЭДОБЗК.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.СогласиеНаПрисоединениеКЭДОБЗК КАК СогласиеНаПрисоединениеКЭДОБЗК
	|ГДЕ
	|	СогласиеНаПрисоединениеКЭДОБЗК.Ссылка В(&Ссылки)";
	Согласия = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	Для каждого Согласие Из Согласия Цикл
		НоваяСтрока = ИзмененияДляОбмена.Добавить();
		НоваяСтрока.Ссылка 				= Согласие;
		НоваяСтрока.ТипОбъекта 			= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.СогласиеНаПрисоединениеКЭДОБЗК;
		НоваяСтрока.ВыгружатьУдаление 	= Истина;
	КонецЦикла;
	
	// ИзмененияДляСервисаКабинетСотрудника
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Изменения.ПредметПубликации КАК Ссылка,
	|	ТИПЗНАЧЕНИЯ(Изменения.ПредметПубликации) КАК ТипЗначения
	|ИЗ
	|	РегистрСведений.УдалитьИзмененияДляСервисаКабинетСотрудника КАК Изменения
	|ГДЕ
	|	НЕ Изменения.ПредметПубликации ССЫЛКА БизнесПроцесс.УдалитьЗаявкаКабинетСотрудника";
	Таблица = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТЗ Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ИзмененияДляОбмена.Добавить();
		НоваяСтрока.Ссылка 				= СтрокаТЗ.Ссылка;
		НоваяСтрока.ТипОбъекта 			= ТипЗначенияТипОбъекта[СтрокаТЗ.ТипЗначения];
		НоваяСтрока.ВыгружатьУдаление 	= Ложь;
	КонецЦикла;
	
	// ИзмененияДокументовДляСервисаКабинетСотрудника
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Изменения.ПубликуемыйДокумент КАК Ссылка
	|ИЗ
	|	РегистрСведений.УдалитьИзмененияДокументовДляСервисаКабинетСотрудника КАК Изменения";
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаТЗ Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ИзмененияДляОбмена.Добавить();
		НоваяСтрока.Ссылка 				= СтрокаТЗ.Ссылка;
		НоваяСтрока.ТипОбъекта 			= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДокументНаПодпись;
		НоваяСтрока.ВыгружатьУдаление 	= Ложь;
	КонецЦикла;
	
	// ИзмененияЗаявокДляСервисаКабинетСотрудника
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Изменения.ПредметПубликации КАК Ссылка,
	|	Изменения.ПредметПубликации.ТипЗаявкиКабинетСотрудника КАК ТипЗаявки
	|ИЗ
	|	РегистрСведений.УдалитьИзмененияЗаявокДляСервисаКабинетСотрудника КАК Изменения";
	Таблица = Запрос.Выполнить().Выгрузить();
	
	ТипЗаявкиТипОбъекта = ТипЗаявкиТипОбъекта();
	Для каждого СтрокаТЗ Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ИзмененияДляОбмена.Добавить();
		НоваяСтрока.Ссылка 				= СтрокаТЗ.Ссылка;
		НоваяСтрока.ТипОбъекта 			= ТипЗаявкиТипОбъекта[СтрокаТЗ.ТипЗаявки];
		НоваяСтрока.ВыгружатьУдаление 	= Ложь;
	КонецЦикла;
	
	// ОбъектыДляУдаленияИзСервисаКабинетСотрудника
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.ОбъектДляУдаления КАК Ссылка,
	|	ТИПЗНАЧЕНИЯ(Таблица.ОбъектДляУдаления) КАК ТипЗначения
	|ИЗ
	|	РегистрСведений.УдалитьОбъектыДляУдаленияИзСервисаКабинетСотрудника КАК Таблица";
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаТЗ Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ИзмененияДляОбмена.Добавить();
		НоваяСтрока.Ссылка 				= СтрокаТЗ.Ссылка;
		НоваяСтрока.ТипОбъекта 			= ТипЗначенияТипОбъекта[СтрокаТЗ.ТипЗначения];
		НоваяСтрока.ВыгружатьУдаление 	= Истина;
	КонецЦикла;
		
	// ФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.ФизическоеЛицо КАК Ссылка
	|ИЗ
	|	РегистрСведений.УдалитьФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ КАК Таблица";
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаТЗ Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ИзмененияДляОбмена.Добавить();
		НоваяСтрока.Ссылка 				= СтрокаТЗ.Ссылка;
		НоваяСтрока.ТипОбъекта 			= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ПрименяемыеВычетыНДФЛ;
		НоваяСтрока.ВыгружатьУдаление 	= Ложь;
	КонецЦикла;
	
	Отбор = Новый Структура("ВыгружатьУдаление", Истина);
	ИзмененияКУдалению = ИзмененияДляОбмена.Скопировать(Отбор);
	
	ИзмененияДляОбмена.Свернуть("Ссылка,ТипОбъекта");
	ИзмененияДляОбмена.Колонки.Добавить("ВыгружатьУдаление", Новый ОписаниеТипов("Булево"));
	ИзмененияДляОбмена.Колонки.Добавить("ВерсияДанных");
	
	Отбор = Новый Структура("Ссылка,ТипОбъекта");
	Для каждого СтрокаТЗ Из ИзмененияКУдалению Цикл
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаТЗ);
		НайденныеСтроки = ИзмененияДляОбмена.НайтиСтроки(Отбор);
		Для каждого СтрокаТаблицы Из НайденныеСтроки Цикл
			СтрокаТаблицы.ВыгружатьУдаление = Истина;
		КонецЦикла;
	КонецЦикла;
	
	Для каждого СтрокаТЗ Из ИзмененияДляОбмена Цикл
		СтрокаТЗ.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ИзмененияДляОбмена) Тогда
		НаборЗаписей = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(ИзмененияДляОбмена);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.УдалитьДокументыКУдалениюКабинетСотрудника.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	НаборЗаписей = РегистрыСведений.УдалитьИзмененияДляСервисаКабинетСотрудника.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	НаборЗаписей = РегистрыСведений.УдалитьИзмененияДокументовДляСервисаКабинетСотрудника.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	НаборЗаписей = РегистрыСведений.УдалитьИзмененияЗаявокДляСервисаКабинетСотрудника.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	НаборЗаписей = РегистрыСведений.УдалитьОбъектыДляУдаленияИзСервисаКабинетСотрудника.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	НаборЗаписей = РегистрыСведений.УдалитьФизическиеЛицаДляОбновленияПубликацииВычетовНДФЛ.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	
КонецПроцедуры

Процедура Перенос_ВыгружаемыеОбъектыКабинетСотрудника()
	
	ВыгружаемыеОбъекты = НоваяТаблицаВыгружаемыеОбъекты();
	
	Запрос = Новый Запрос;
	
	//ПубликуемыеОбъектыКабинетСотрудника
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.ОбъектПубликации КАК Ссылка,
	|	Таблица.УсловноПубликуется КАК УсловноВыгружается
	|ИЗ
	|	РегистрСведений.УдалитьПубликуемыеОбъектыКабинетСотрудника КАК Таблица";
	Таблица = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТЗ Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ВыгружаемыеОбъекты.Добавить(), СтрокаТЗ);
	КонецЦикла;
	
	//ФизическиеЛицаКабинетСотрудника
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.ФизическоеЛицо КАК Ссылка,
	|	Таблица.ОкончаниеПубликации КАК ОкончаниеВыгрузки
	|ИЗ
	|	РегистрСведений.УдалитьФизическиеЛицаКабинетСотрудника КАК Таблица";
	Таблица = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТЗ Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ВыгружаемыеОбъекты.Добавить(), СтрокаТЗ);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ВыгружаемыеОбъекты) Тогда
		НаборЗаписей = РегистрыСведений.ВыгружаемыеОбъектыКабинетСотрудника.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(ВыгружаемыеОбъекты);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.УдалитьПубликуемыеОбъектыКабинетСотрудника.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	НаборЗаписей = РегистрыСведений.УдалитьФизическиеЛицаКабинетСотрудника.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	
КонецПроцедуры

Процедура Перенос_ИдентификаторыНезагруженныхОбъектовКабинетСотрудника()
	
	НезагруженныеОбъекты = ОбщегоНазначенияБЗК.ТаблицаЗначенийПоИмениРегистраСведений("НезагруженныеОбъектыУправлениеПерсоналом");
	ТипОбъектаПоТипуОбъектаКС = ТипОбъектаПоТипуОбъектаКС();
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Идентификаторы.ИдентификаторОбъекта КАК ИдентификаторОбъекта,
	|	Идентификаторы.ТипОбъекта КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.УдалитьИдентификаторыНезагруженныхОбъектовКабинетСотрудника КАК Идентификаторы";
	Таблица = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(Таблица) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из Таблица Цикл 
		ТипОбъекта = ТипОбъектаПоТипуОбъектаКС[СтрокаТЗ.ТипОбъекта];
		Если Не ЗначениеЗаполнено(ТипОбъекта) Или Не ЗначениеЗаполнено(СтрокаТЗ.ИдентификаторОбъекта) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = НезагруженныеОбъекты.Добавить();
		НоваяСтрока.Идентификатор 	= СтрокаТЗ.ИдентификаторОбъекта;
		НоваяСтрока.ТипОбъекта 		= ТипОбъекта;
		НоваяСтрока.Приложение 		= Приложение;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(НезагруженныеОбъекты) Тогда
		НаборЗаписей = РегистрыСведений.НезагруженныеОбъектыУправлениеПерсоналом.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Приложение.Установить(Приложение);
		НаборЗаписей.Загрузить(НезагруженныеОбъекты);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.УдалитьИдентификаторыНезагруженныхОбъектовКабинетСотрудника.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);

КонецПроцедуры

Процедура Перенос_ОбъектыСОшибкамиЗаполненияКабинетСотрудника()
	
	ОшибкиЗаполнения = ОбщегоНазначенияБЗК.ТаблицаЗначенийПоИмениРегистраСведений("ОшибкиЗаполненияОбъектовУправлениеПерсоналом");
	ТипЗначенияТипОбъекта = ТипЗначенияТипОбъекта();
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОбъектыСОшибками.ОбъектПубликации КАК Ссылка,
	|	ОбъектыСОшибками.БлокирующаяОшибка КАК БлокирующаяОшибка,
	|	ОбъектыСОшибками.Позиция КАК ПозицияШР,
	|	ОбъектыСОшибками.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОбъектыСОшибками.ОписаниеБлокирующейОшибки КАК ПредставлениеБлокирующейОшибки,
	|	ОбъектыСОшибками.ОписаниеОшибки КАК ПредставлениеОшибки,
	|	ТИПЗНАЧЕНИЯ(ОбъектыСОшибками.ОбъектПубликации) КАК ТипЗначения
	|ИЗ
	|	РегистрСведений.УдалитьОбъектыСОшибкамиЗаполненияКабинетСотрудника КАК ОбъектыСОшибками";
	Таблица = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(Таблица) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из Таблица Цикл
		ТипОбъекта = ТипЗначенияТипОбъекта[СтрокаТЗ.ТипЗначения];
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Ссылка) Или Не ЗначениеЗаполнено(ТипОбъекта) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ОшибкиЗаполнения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		НоваяСтрока.ТипОбъекта = ТипОбъекта;
		НоваяСтрока.Приложение = Приложение;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ОшибкиЗаполнения) Тогда
		НаборЗаписей = РегистрыСведений.ОшибкиЗаполненияОбъектовУправлениеПерсоналом.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Приложение.Установить(Приложение);
		НаборЗаписей.Загрузить(ОшибкиЗаполнения);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.УдалитьОбъектыСОшибкамиЗаполненияКабинетСотрудника.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);

КонецПроцедуры

Процедура Перенос_ПравилаПубликацииКабинетСотрудника()
	
	Правила = ОбщегоНазначенияБЗК.ТаблицаЗначенийПоИмениРегистраСведений("ПравилаВыгрузкиУправлениеПерсоналом");
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Правила.ОбъектПравила КАК Ссылка
	|ИЗ
	|	РегистрСведений.УдалитьПравилаПубликацииКабинетСотрудника КАК Правила";
	Таблица = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(Таблица) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = Правила.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		НоваяСтрока.Приложение = Приложение;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Правила) Тогда
		НаборЗаписей = РегистрыСведений.ПравилаВыгрузкиУправлениеПерсоналом.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Приложение.Установить(Приложение);
		НаборЗаписей.Загрузить(Правила);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.УдалитьПравилаПубликацииКабинетСотрудника.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);

КонецПроцедуры

Процедура Перенос_РезультатОбменаКабинетСотрудника()

	СобытияОбмена = ОбщегоНазначенияБЗК.ТаблицаЗначенийПоИмениРегистраСведений("СобытияОбменаУправлениеПерсоналом");
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РезультатОбмена.ИндексСобытия КАК ИндексСобытия,
	|	РезультатОбмена.ДатаНачала КАК ДатаНачала,
	|	РезультатОбмена.ДатаОкончания КАК ДатаОкончания,
	|	РезультатОбмена.БылиОшибки КАК БылиОшибки
	|ИЗ
	|	РегистрСведений.УдалитьРезультатОбменаКабинетСотрудника КАК РезультатОбмена";
	Таблица = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(Таблица) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из Таблица Цикл
		НоваяСтрока = СобытияОбмена.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		НоваяСтрока.Приложение = Приложение;
		Если СтрокаТЗ.ИндексСобытия = 0 Тогда
			НоваяСтрока.ВидСобытия = Перечисления.ВидыСобытийОбменаУправлениеПерсоналом.Обмен;
		Иначе
			НоваяСтрока.ВидСобытия = Перечисления.ВидыСобытийОбменаУправлениеПерсоналом.ОбработкаОбсуждений;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СобытияОбмена) Тогда
		НаборЗаписей = РегистрыСведений.СобытияОбменаУправлениеПерсоналом.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Приложение.Установить(Приложение);
		НаборЗаписей.Загрузить(СобытияОбмена);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.УдалитьРезультатОбменаКабинетСотрудника.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);

КонецПроцедуры

Процедура Перенос_СотрудникиДляОбновленияПубликуемыхОбъектов()

	БудущиеСобытия = ОбщегоНазначенияБЗК.ТаблицаЗначенийПоИмениРегистраСведений("БудущиеСобытияУправлениеПерсоналом");
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СотрудникиДляОбновления.Сотрудник КАК Объект,
	|	СотрудникиДляОбновления.ДатаСобытия КАК ДатаСобытия,
	|	СотрудникиДляОбновления.Событие КАК Событие
	|ИЗ
	|	РегистрСведений.УдалитьСотрудникиДляОбновленияПубликуемыхОбъектов КАК СотрудникиДляОбновления";
	Таблица = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(Таблица) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЗ.Объект) Или Не ЗначениеЗаполнено(СтрокаТЗ.Событие) Или Не ЗначениеЗаполнено(СтрокаТЗ.ДатаСобытия) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = БудущиеСобытия.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		НоваяСтрока.Приложение = Приложение;
		Если СтрокаТЗ.Событие = Перечисления.СобытияОбновленияОбъектовКабинетСотрудника.ГрафикРаботы Тогда
			НоваяСтрока.Событие = Перечисления.ВидыБудущихСобытийУправлениеПерсоналом.ГрафикРаботы;
		Иначе
			НоваяСтрока.Событие = Перечисления.ВидыБудущихСобытийУправлениеПерсоналом.КадровыеДанные;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(БудущиеСобытия) Тогда
		НаборЗаписей = РегистрыСведений.БудущиеСобытияУправлениеПерсоналом.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Приложение.Установить(Приложение);
		НаборЗаписей.Загрузить(БудущиеСобытия);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.УдалитьСотрудникиДляОбновленияПубликуемыхОбъектов.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);

КонецПроцедуры

Процедура Перенос_НеобработанныеРезультатыСогласованияКабинетСотрудника()

	НеобработанныеОбъекты = ОбщегоНазначенияБЗК.ТаблицаЗначенийПоИмениРегистраСведений("НеобработанныеОбъектыУправлениеПерсоналом");
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НеобработанныеРезультаты.РезультатСогласования КАК Объект
	|ИЗ
	|	РегистрСведений.УдалитьНеобработанныеРезультатыСогласованияКабинетСотрудника КАК НеобработанныеРезультаты";
	Таблица = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(Таблица) Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаТЗ Из Таблица Цикл
		ИдентификаторОбъекта = Неопределено;
		Попытка
			Объект = СтрокаТЗ.Объект.Получить();
			ИдентификаторОбъекта = Объект.ИдентификаторОбъекта;
		Исключение
			// Не обрабатываем этот объект.
		КонецПопытки;
		Если Не ЗначениеЗаполнено(ИдентификаторОбъекта) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = НеобработанныеОбъекты.Добавить();
		НоваяСтрока.Приложение 		= Приложение;
		НоваяСтрока.ТипОбъекта 		= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования;
		НоваяСтрока.Идентификатор 	= ИдентификаторОбъекта;
		НоваяСтрока.Объект 			= Новый ХранилищеЗначения(Объект);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(НеобработанныеОбъекты) Тогда 
		НаборЗаписей = РегистрыСведений.НеобработанныеОбъектыУправлениеПерсоналом.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Приложение.Установить(Приложение);
		НаборЗаписей.Загрузить(НеобработанныеОбъекты);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.УдалитьНеобработанныеРезультатыСогласованияКабинетСотрудника.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);

КонецПроцедуры

Процедура Перенос_ВерсияИзмененийКабинетСотрудника()
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	ВерсияИзменений = Константы.УдалитьВерсияИзмененийКабинетСотрудника.Получить();
	
	Если ВерсияИзменений = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ВерсииИзмененийУправлениеПерсоналом.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Приложение.Установить(Приложение);
	ЗаписьНабора = НаборЗаписей.Добавить();
	ЗаписьНабора.Приложение 		= Приложение;
	ЗаписьНабора.ВерсияИзменений 	= ВерсияИзменений;
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	
	Константы.УдалитьВерсияИзмененийКабинетСотрудника.Установить(0);

КонецПроцедуры

Процедура Перенос_НастройкиИнтеграцииУправлениеПерсоналом()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиИнтеграции.ВидКИМобильныйТелефон КАК ВидКИМобильныйТелефон
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииУправлениеПерсоналом КАК НастройкиИнтеграции";
	НастройкиИнтеграции = Запрос.Выполнить().Выгрузить();
	Если ЗначениеЗаполнено(НастройкиИнтеграции) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиИнтеграции.УдалитьВидКонтактнойИнформацииМобильныйТелефон КАК ВидКИМобильныйТелефон,
	|	НастройкиИнтеграции.УдалитьВидКонтактнойИнформацииАдресЭлектроннойПочты КАК ВидКИАдресЭлектроннойПочты,
	|	НастройкиИнтеграции.УдалитьПубликоватьСтруктуруЮридическихЛиц КАК ПубликоватьСтруктуруЮридическихЛиц
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииКабинетСотрудника КАК НастройкиИнтеграции";
	НастройкиИнтеграции = Запрос.Выполнить().Выгрузить();
	Если Не ЗначениеЗаполнено(НастройкиИнтеграции) Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.НастройкиИнтеграцииУправлениеПерсоналом.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(НастройкиИнтеграции);
	
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);

КонецПроцедуры

Процедура Перенос_ВыгруженныеОбъекты()
	
	ТипЗначенияТипОбъекта = ТипЗначенияТипОбъекта();
	
	ТипыОбъектов = Новый Массив;
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Должность);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДолжностьПоШтатномуРасписанию);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник);
	ТипыОбъектов.Добавить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ГрафикРаботы);
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Приложение", Приложение);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВыгруженныеОбъекты.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.ВыгруженныеОбъектыУправлениеПерсоналом КАК ВыгруженныеОбъекты
	|ГДЕ
	|	ВыгруженныеОбъекты.Приложение = &Приложение";
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТипыОбъектов", ТипыОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИзмененияДляОбменаКабинетСотрудника КАК ИзмененияДляОбмена
	|		ПО ВыгружаемыеОбъекты.Ссылка = ИзмененияДляОбмена.Ссылка
	|			И (ИзмененияДляОбмена.ТипОбъекта В (&ТипыОбъектов))
	|ГДЕ
	|	ИзмененияДляОбмена.ВерсияДанных ЕСТЬ NULL";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ВыгруженныеОбъектыУправлениеПерсоналом.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Приложение.Установить(Приложение);
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл 
		Если Не ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		ТипОбъекта = ТипЗначенияТипОбъекта[ТипЗнч(Выборка.Ссылка)];
		Если Не ЗначениеЗаполнено(ТипОбъекта) Тогда
			Продолжить;
		КонецЕсли;
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Ссылка 		= Выборка.Ссылка;
		НоваяЗапись.Приложение 	= Приложение;
		НоваяЗапись.ТипОбъекта 	= ТипОбъекта;
		НоваяЗапись.Выгружался 	= Истина;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(НаборЗаписей) Тогда
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПубличныеИдентификаторы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПубличныеИдентификаторы.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.ПубличныеИдентификаторыОбъектовУправлениеПерсоналом КАК ПубличныеИдентификаторы";
	ЕстьПубличныеИдентификаторы = Не Запрос.Выполнить().Пустой();
	
	ОбрабатываемыеТипы = Новый Соответствие;
	ОбрабатываемыеТипы.Вставить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо, Истина);
	ОбрабатываемыеТипы.Вставить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация, Истина);
	ОбрабатываемыеТипы.Вставить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение, Истина);
	ОбрабатываемыеТипы.Вставить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Сотрудник, Истина);
	ОбрабатываемыеТипы.Вставить(Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ВидПредоставляемойСотрудникамСправки, Истина);
	
	ТипЗначенияТипОбъекта = ТипЗначенияТипОбъекта();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка,
	|	ТИПЗНАЧЕНИЯ(ВыгружаемыеОбъекты.Ссылка) КАК ТипДанных
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты";
	Таблица = Запрос.Выполнить().Выгрузить();
	
	ТаблицаОбъектов = ОбщегоНазначенияБЗК.ТаблицаЗначенийПоИмениРегистраСведений("ПубличныеИдентификаторыОбъектовУправлениеПерсоналом");
	
	Для каждого СтрокаТЗ Из Таблица Цикл
		ТипОбъекта = ТипЗначенияТипОбъекта[СтрокаТЗ.ТипДанных];
		Если ОбрабатываемыеТипы[ТипОбъекта] = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ТаблицаОбъектов.Добавить();
		НоваяСтрока.Ссылка 		= СтрокаТЗ.Ссылка;
		НоваяСтрока.ТипОбъекта 	= ТипОбъекта;
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ТаблицаОбъектов) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТаблицаОбъектов", ТаблицаОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.ТипОбъекта КАК ТипОбъекта,
	|	Таблица.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТТаблицаОбъектов
	|ИЗ
	|	&ТаблицаОбъектов КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.ТипОбъекта КАК ТипОбъекта,
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	ВТТаблицаОбъектов КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПубличныеИдентификаторыОбъектовУправлениеПерсоналом КАК ПубличныеИдентификаторы
	|		ПО Таблица.ТипОбъекта = ПубличныеИдентификаторы.ТипОбъекта
	|			И Таблица.Ссылка = ПубличныеИдентификаторы.Ссылка
	|ГДЕ
	|	ПубличныеИдентификаторы.Идентификатор ЕСТЬ NULL";
	ОбъектыБезИдентификатора = Запрос.Выполнить().Выгрузить();
	
	Если Не ЗначениеЗаполнено(ОбъектыБезИдентификатора) Тогда
		Возврат;
	КонецЕсли;
	
	ПубликоватьСтруктуруЮридическихЛиц = ИнтеграцияУправлениеПерсоналом.ПубликоватьСтруктуруЮридическихЛиц();
	ОрганизацииКакПодразделение = Новый Массив;
	ОбъектыБезИдентификатора.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
	Для каждого СтрокаТЗ Из ОбъектыБезИдентификатора Цикл
		Если ПубликоватьСтруктуруЮридическихЛиц И СтрокаТЗ.ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Организация Тогда
			ОрганизацииКакПодразделение.Добавить(СтрокаТЗ.Ссылка);
		КонецЕсли;
		СтрокаТЗ.Идентификатор = Строка(СтрокаТЗ.Ссылка.УникальныйИдентификатор());
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ОрганизацииКакПодразделение) Тогда
		Запрос.УстановитьПараметр("Ссылки", ОрганизацииКакПодразделение);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Организации.Ссылка КАК Ссылка,
		|	ЕСТЬNULL(ИдентификаторыОрганизаций.Идентификатор, """") КАК Идентификатор
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИдентификаторыОрганизацийКабинетСотрудника КАК ИдентификаторыОрганизаций
		|		ПО Организации.Ссылка = ИдентификаторыОрганизаций.Организация
		|ГДЕ
		|	Организации.Ссылка В(&Ссылки)";
		Идентификаторы = Запрос.Выполнить().Выгрузить();
		Для каждого СтрокаТЗ Из Идентификаторы Цикл
			Если Не ЗначениеЗаполнено(СтрокаТЗ.Идентификатор) Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ОбъектыБезИдентификатора.Добавить();
			НоваяСтрока.Ссылка 			= СтрокаТЗ.Ссылка;
			НоваяСтрока.Идентификатор 	= СтрокаТЗ.Идентификатор;
			НоваяСтрока.ТипОбъекта 		= Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Подразделение;
		КонецЦикла;
	КонецЕсли;
	
	Если ЕстьПубличныеИдентификаторы Тогда
		Для каждого СтрокаТЗ Из ОбъектыБезИдентификатора Цикл
			НаборЗаписей = РегистрыСведений.ПубличныеИдентификаторыОбъектовУправлениеПерсоналом.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Ссылка.Установить(СтрокаТЗ.Ссылка);
			НаборЗаписей.Отбор.ТипОбъекта.Установить(СтрокаТЗ.ТипОбъекта);
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, СтрокаТЗ);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		КонецЦикла;
	Иначе
		НаборЗаписей = РегистрыСведений.ПубличныеИдентификаторыОбъектовУправлениеПерсоналом.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(ОбъектыБезИдентификатора);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЕсли;

КонецПроцедуры

Функция ТипОбъектаПоТипуОбъектаКС()

	ТипОбъектаПоТипуОбъектаКС = Новый Соответствие;
	
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыОбъектовКабинетСотрудника.ДокументНаПодпись,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ДокументНаПодпись);
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыОбъектовКабинетСотрудника.РезультатСогласования,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.РезультатСогласования);
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыОбъектовКабинетСотрудника.СогласиеНаПрисоединениеКЭДОБЗК,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.СогласиеНаПрисоединениеКЭДОБЗК);
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыОбъектовКабинетСотрудника.СборГрафиковОтпусков,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ГрафикОтпусковПодразделения);
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыОбъектовКабинетСотрудника.ПравилоСогласования,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ПравилоСогласования);
		
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаНДФЛ,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗапросСправки2НДФЛ);
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаКомпенсациюОтпуска,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаявкаНаКомпенсациюОтпуска);
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.ИзменениеЛичныхДанных,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаявкаИзменениеЛичныхДанных);
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаУдержаниеДСВ,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаявкаДСВ);
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаСМестаРаботы,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗапросСправкиСМестаРаботы);
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаОтпуск,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаявкаНаОтпуск);
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.СправкаОбОстаткеОтпуска,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаявкаОстаткиОтпусков);
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.ЗаявлениеНаНалоговыеВычеты,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ЗаявкаНалоговыйВычет);
	ТипОбъектаПоТипуОбъектаКС.Вставить(Перечисления.ТипыЗаявокКабинетСотрудника.СогласованиеОтсутствия,
		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Отсутствие);
		
	Возврат ТипОбъектаПоТипуОбъектаКС;

КонецФункции

// Используется только для переноса данных.
// Описание состава соответствует прежней версии.
//
Функция ТипЗначенияТипОбъекта()
	
	ПеречислениеМенеджер = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом;
	ТипЗначенияТипОбъекта = Новый Соответствие;
	ТипЗначенияТипОбъекта.Вставить(Тип("СправочникСсылка.ФизическиеЛица"), 					ПеречислениеМенеджер.ФизическоеЛицо);
	ТипЗначенияТипОбъекта.Вставить(Тип("СправочникСсылка.Организации"), 					ПеречислениеМенеджер.Организация);
	ТипЗначенияТипОбъекта.Вставить(Тип("СправочникСсылка.ПодразделенияОрганизаций"), 		ПеречислениеМенеджер.Подразделение);
	ТипЗначенияТипОбъекта.Вставить(Тип("СправочникСсылка.СтруктураПредприятия"), 			ПеречислениеМенеджер.Подразделение);
	ТипЗначенияТипОбъекта.Вставить(Тип("СправочникСсылка.Должности"), 						ПеречислениеМенеджер.Должность);
	ТипЗначенияТипОбъекта.Вставить(Тип("СправочникСсылка.ШтатноеРасписание"), 				ПеречислениеМенеджер.ДолжностьПоШтатномуРасписанию);
	ТипЗначенияТипОбъекта.Вставить(Тип("СправочникСсылка.Сотрудники"), 						ПеречислениеМенеджер.Сотрудник);
	ТипЗначенияТипОбъекта.Вставить(Тип("СправочникСсылка.МашиночитаемыеДоверенности"), 		ПеречислениеМенеджер.МашиночитаемаяДоверенность);
	
	ТипЗначения = ИнтеграцияУправлениеПерсоналом.ТипВидПредоставляемойСотрудникамСправкиСсылка();
	Если ЗначениеЗаполнено(ТипЗначения) Тогда
		ТипЗначенияТипОбъекта.Вставить(ТипЗначения, ПеречислениеМенеджер.ВидПредоставляемойСотрудникамСправки);
	КонецЕсли;
	ТипЗначения = ИнтеграцияУправлениеПерсоналом.ТипГрафикРаботыСсылка();
	Если ЗначениеЗаполнено(ТипЗначения) Тогда
		ТипЗначенияТипОбъекта.Вставить(ТипЗначения, ПеречислениеМенеджер.ГрафикРаботы);
	КонецЕсли;
	ТипЗначения = ИнтеграцияУправлениеПерсоналом.ТипСборГрафиковОтпусковСсылка();
	Если ЗначениеЗаполнено(ТипЗначения) Тогда
		ТипЗначенияТипОбъекта.Вставить(ТипЗначения, ПеречислениеМенеджер.ГрафикОтпусковПредприятия);
	КонецЕсли;
	
	Возврат ТипЗначенияТипОбъекта;

КонецФункции

#КонецОбласти

Процедура ПеренестиДанныеВНовыеРегистры() Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяСервисКабинетСотрудника") Тогда
		Возврат;
	КонецЕсли;
	
	Перенос_ИзмененияДляОбменаКабинетСотрудника();
	Перенос_ВыгружаемыеОбъектыКабинетСотрудника();
	Перенос_ИдентификаторыНезагруженныхОбъектовКабинетСотрудника();
	Перенос_ОбъектыСОшибкамиЗаполненияКабинетСотрудника();
	Перенос_ПравилаПубликацииКабинетСотрудника();
	Перенос_РезультатОбменаКабинетСотрудника();
	Перенос_СотрудникиДляОбновленияПубликуемыхОбъектов();
	Перенос_НеобработанныеРезультатыСогласованияКабинетСотрудника();
	Перенос_ВерсияИзмененийКабинетСотрудника();
	Перенос_ВыгруженныеОбъекты();
	ЗаполнитьПубличныеИдентификаторы();
	Перенос_НастройкиИнтеграцииУправлениеПерсоналом();
	
КонецПроцедуры

Процедура ОбновитьВыгруженныеДанные(ПараметрыОбновления = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяСервисКабинетСотрудника") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыгруженныеОбъекты.Ссылка КАК Ссылка,
	|	ВыгруженныеОбъекты.Приложение КАК Приложение,
	|	ВыгруженныеОбъекты.ТипОбъекта КАК ТипОбъекта,
	|	ВыгруженныеОбъекты.Выгружался КАК Выгружался
	|ИЗ
	|	РегистрСведений.ВыгруженныеОбъектыУправлениеПерсоналом КАК ВыгруженныеОбъекты
	|ГДЕ
	|	НЕ ВыгруженныеОбъекты.Выгружался
	|	И ВыгруженныеОбъекты.Приложение = ЗНАЧЕНИЕ(Перечисление.ПриложенияДляИнтеграции.КабинетСотрудника)";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбработкаВыполнена = Истина;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.ВыгруженныеОбъектыУправлениеПерсоналом", "Ссылка", Выборка.Ссылка) Тогда
			ОбработкаВыполнена = Ложь;
		Иначе
			
			НаборЗаписей = РегистрыСведений.ВыгруженныеОбъектыУправлениеПерсоналом.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Ссылка.Установить(Выборка.Ссылка);
			НаборЗаписей.Отбор.Приложение.Установить(Выборка.Приложение);
			НаборЗаписей.Отбор.ТипОбъекта.Установить(Выборка.ТипОбъекта);
			
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(ЗаписьНабора, Выборка);
			ЗаписьНабора.Выгружался = Истина;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбработкаВыполнена);

КонецПроцедуры

Процедура ЗарегистрироватьОграничениеДоступаКРабочимКонтактам(ПараметрыОбновления = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяСервисКабинетСотрудника")
		Или Не ВерсионированиеИспользуется() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.НастройкиСервисаКабинетСотрудника.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Прочитать();
	Если Не МенеджерЗаписи.Выбран() Или Не МенеджерЗаписи.УдалитьОбновитьУровеньДоступаКИ Тогда
		 ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ОграничениеДоступаКРабочимКонтактам) КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|ГДЕ
	|	ВыгружаемыеОбъекты.Ссылка ССЫЛКА Справочник.ФизическиеЛица";
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбработкаВыполнена = Истина;
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ОграничениеДоступаКРабочимКонтактам;
	Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.ИзмененияДляОбменаКабинетСотрудника", "ТипОбъекта", ТипОбъекта) Тогда
		ОбработкаВыполнена = Ложь;
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ТипОбъекта.Установить(ТипОбъекта);
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаписьНабора.Ссылка 		= Выборка.Ссылка;
		ЗаписьНабора.ТипОбъекта 	= ТипОбъекта;
		ЗаписьНабора.ВерсияДанных 	= Строка(Новый УникальныйИдентификатор);
	КонецЦикла;
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбработкаВыполнена);

КонецПроцедуры

Процедура ОбновитьПрименяемыеВычетыНДФЛ(ПараметрыОбновления = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяСервисКабинетСотрудника") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|ГДЕ
	|	ВыгружаемыеОбъекты.Ссылка ССЫЛКА Справочник.ФизическиеЛица
	|	И НЕ ВыгружаемыеОбъекты.УсловноВыгружается";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбработкаВыполнена = Истина;
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ПрименяемыеВычетыНДФЛ;
	Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.ИзмененияДляОбменаКабинетСотрудника", "ТипОбъекта", ТипОбъекта) Тогда
		ОбработкаВыполнена = Ложь;
	Иначе
		
		НаборЗаписей = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ТипОбъекта.Установить(ТипОбъекта);
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.Ссылка 		= Выборка.Ссылка;
			ЗаписьНабора.ТипОбъекта 	= ТипОбъекта;
			ЗаписьНабора.ВерсияДанных 	= Строка(Новый УникальныйИдентификатор);
		КонецЦикла;
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбработкаВыполнена);

КонецПроцедуры

Процедура ЗарегистрироватьБудущиеСобытияВычетыНДФЛ(ПараметрыОбновления = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяСервисКабинетСотрудника") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ДатаАктуальности = ТекущаяДатаСеанса();
	НалоговыйПериод = Год(ДатаАктуальности);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НалоговыйПериод", НалоговыйПериод);
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФизическиеЛица.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ФизическиеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БудущиеСобытияУправлениеПерсоналом КАК БудущиеСобытия
	|		ПО ФизическиеЛица.Ссылка = БудущиеСобытия.Объект
	|			И (БудущиеСобытия.Приложение = ЗНАЧЕНИЕ(Перечисление.ПриложенияДляИнтеграции.КабинетСотрудника))
	|			И (БудущиеСобытия.Событие = ЗНАЧЕНИЕ(Перечисление.ВидыБудущихСобытийУправлениеПерсоналом.ВычетыНДФЛ))
	|ГДЕ
	|	ФизическиеЛица.Ссылка ССЫЛКА Справочник.ФизическиеЛица
	|	И БудущиеСобытия.ДатаСобытия ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Таблица.ДатаНачала КАК ДатаНачала
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Сотрудник КАК ФизическоеЛицо,
	|		Таблица.ПрименятьВычетыС КАК ДатаНачала
	|	ИЗ
	|		Документ.УведомлениеОПравеНаИмущественныйВычетДляНДФЛ КАК Таблица
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизическиеЛица КАК ФизическиеЛица
	|			ПО Таблица.Сотрудник = ФизическиеЛица.Ссылка
	|	ГДЕ
	|		Таблица.НалоговыйПериод >= &НалоговыйПериод
	|		И Таблица.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Сотрудник,
	|		Таблица.ПрименятьВычетыС
	|	ИЗ
	|		Документ.ЕжегодныеСтандартныеВычеты КАК Таблица
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизическиеЛица КАК ФизическиеЛица
	|			ПО Таблица.Сотрудник = ФизическиеЛица.Ссылка
	|	ГДЕ
	|		Таблица.НалоговыйПериод >= &НалоговыйПериод
	|		И Таблица.Проведен) КАК Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Вычеты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Вычеты.МесяцРегистрации КАК ДатаНачала,
	|	Вычеты.ДействуетДо КАК ДатаОкончания
	|ИЗ
	|	РегистрСведений.СтандартныеВычетыНаДетейНДФЛ КАК Вычеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизическиеЛица КАК ФизическиеЛица
	|		ПО Вычеты.ФизическоеЛицо = ФизическиеЛица.Ссылка
	|ГДЕ
	|	(Вычеты.ДействуетДо >= &ДатаАктуальности
	|			ИЛИ Вычеты.МесяцРегистрации >= &ДатаАктуальности)";
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если РезультатЗапроса[1].Пустой() И РезультатЗапроса[2].Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	БудущиеСобытияВычеты = ОбщегоНазначенияБЗК.ТаблицаЗначенийПоИмениРегистраСведений("БудущиеСобытияУправлениеПерсоналом");
	ДлинаСуток = 86400;
	Событие = Перечисления.ВидыБудущихСобытийУправлениеПерсоналом.ВычетыНДФЛ;
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	
	БудущиеСобытияФизическихЛиц = Новый Соответствие;
	
	ДанныеВычетов = РезультатЗапроса[1].Выгрузить();
	Для каждого СтрокаТЗ Из ДанныеВычетов Цикл
		
		БудущиеСобытия = БудущиеСобытияФизическихЛиц[СтрокаТЗ.ФизическоеЛицо];
		Если БудущиеСобытия = Неопределено Тогда
			БудущиеСобытия = БудущиеСобытияВычеты.СкопироватьКолонки();
		КонецЕсли;
		
		Если СтрокаТЗ.ДатаНачала <= ДатаАктуальности Тогда
			НоваяСтрока = БудущиеСобытия.Добавить();
			НоваяСтрока.Объект 		= СтрокаТЗ.ФизическоеЛицо;
			НоваяСтрока.ДатаСобытия = КонецГода(ДатаАктуальности) + ДлинаСуток;
			НоваяСтрока.Событие 	= Событие;
			НоваяСтрока.Приложение 	= Приложение;
		Иначе
			НоваяСтрока = БудущиеСобытия.Добавить();
			НоваяСтрока.Объект 		= СтрокаТЗ.ФизическоеЛицо;
			НоваяСтрока.ДатаСобытия = СтрокаТЗ.ДатаНачала;
			НоваяСтрока.Событие 	= Событие;
			НоваяСтрока.Приложение 	= Приложение;
			НоваяСтрока = БудущиеСобытия.Добавить();
			НоваяСтрока.Объект 		= СтрокаТЗ.ФизическоеЛицо;
			НоваяСтрока.ДатаСобытия = КонецГода(ДатаАктуальности)+ ДлинаСуток;
			НоваяСтрока.Событие 	= Событие;
			НоваяСтрока.Приложение 	= Приложение;
		КонецЕсли;
		
		БудущиеСобытияФизическихЛиц.Вставить(СтрокаТЗ.ФизическоеЛицо, БудущиеСобытия);
		
	КонецЦикла;
	
	ДанныеВычетов = РезультатЗапроса[2].Выгрузить();
	Для каждого СтрокаТЗ Из ДанныеВычетов Цикл
		
		БудущиеСобытия = БудущиеСобытияФизическихЛиц[СтрокаТЗ.ФизическоеЛицо];
		Если БудущиеСобытия = Неопределено Тогда
			БудущиеСобытия = БудущиеСобытияВычеты.СкопироватьКолонки();
		КонецЕсли;
		
		Если СтрокаТЗ.ДатаНачала > ДатаАктуальности Тогда
			НоваяСтрока = БудущиеСобытия.Добавить();
			НоваяСтрока.Объект 		= СтрокаТЗ.ФизическоеЛицо;
			НоваяСтрока.ДатаСобытия = СтрокаТЗ.ДатаНачала;
			НоваяСтрока.Событие 	= Событие;
			НоваяСтрока.Приложение 	= Приложение;
		КонецЕсли;
		Если СтрокаТЗ.ДатаОкончания > ДатаАктуальности Тогда
			НоваяСтрока = БудущиеСобытия.Добавить();
			НоваяСтрока.Объект 		= СтрокаТЗ.ФизическоеЛицо;
			НоваяСтрока.ДатаСобытия = ДобавитьМесяц(СтрокаТЗ.ДатаОкончания, 1);
			НоваяСтрока.Событие 	= Событие;
			НоваяСтрока.Приложение 	= Приложение;
		КонецЕсли;
		
		БудущиеСобытияФизическихЛиц.Вставить(СтрокаТЗ.ФизическоеЛицо, БудущиеСобытия);
		
	КонецЦикла;
	
	ОбработкаЗавершена = Истина;
	Для каждого КлючИЗначение Из БудущиеСобытияФизическихЛиц Цикл
		
		ФизическоеЛицо = КлючИЗначение.Ключ;
		БудущиеСобытия = КлючИЗначение.Значение;
		БудущиеСобытия.Свернуть("Приложение,Объект,Событие,ДатаСобытия");
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.БудущиеСобытияУправлениеПерсоналом", "Объект", ФизическоеЛицо) Тогда
			ОбработкаЗавершена = Ложь;
			Продолжить;
		Иначе
			
			НаборЗаписей = РегистрыСведений.БудущиеСобытияУправлениеПерсоналом.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Приложение.Установить(Приложение);
			НаборЗаписей.Отбор.Событие.Установить(Событие);
			НаборЗаписей.Отбор.Объект.Установить(ФизическоеЛицо);
			
			Для каждого СтрокаТЗ Из БудущиеСобытия Цикл
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), СтрокаТЗ);
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбработкаЗавершена);

КонецПроцедуры

Процедура ЗарегистрироватьОбъектыВыгружаемыеПоСсылкам(ПараметрыОбновления = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяСервисКабинетСотрудника") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	Организации = ИнтеграцияУправлениеПерсоналом.ВыгружаемыеОрганизации(Приложение);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТекущиеРуководители.Руководитель КАК Ссылка
	|ПОМЕСТИТЬ ВТВыгружатьПоСсылкам
	|ИЗ
	|	РегистрСведений.ТекущиеРуководителиКабинетСотрудника КАК ТекущиеРуководители
	|ГДЕ
	|	ТекущиеРуководители.СтруктурнаяЕдиница В(&Организации)
	|	И ТекущиеРуководители.Руководитель <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТекущиеРуководители.Должность
	|ИЗ
	|	РегистрСведений.ТекущиеРуководителиКабинетСотрудника КАК ТекущиеРуководители
	|ГДЕ
	|	ТекущиеРуководители.СтруктурнаяЕдиница В(&Организации)
	|	И ТекущиеРуководители.Должность <> ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыгружатьПоСсылкам.Ссылка КАК Ссылка
	|ИЗ
	|	ВТВыгружатьПоСсылкам КАК ВыгружатьПоСсылкам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеОбъекты
	|		ПО ВыгружатьПоСсылкам.Ссылка = ВыгружаемыеОбъекты.Ссылка
	|ГДЕ
	|	ВыгружаемыеОбъекты.УсловноВыгружается ЕСТЬ NULL";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выгрузка 	= НоваяТаблицаВыгружаемыеОбъекты();
	Изменения 	= НоваяТаблицаИзмененияДляОбмена();
	
	ТипЗначенияТипОбъекта = Новый Соответствие;
	ТипЗначенияТипОбъекта.Вставить(Тип("СправочникСсылка.ФизическиеЛица"), 	Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо);
	ТипЗначенияТипОбъекта.Вставить(Тип("СправочникСсылка.Должности"), 		Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.Должность);
	
	ОбработкаЗавершена = Истина;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТипОбъекта = ТипЗначенияТипОбъекта[ТипЗнч(Выборка.Ссылка)];
		
		НачатьТранзакцию();
		Попытка
			
			Если ПараметрыОбновления <> Неопределено Тогда
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
				ЭлементБлокировки.УстановитьЗначение("ТипОбъекта", ТипОбъекта);
				Блокировка.Заблокировать();
			КонецЕсли;
			
			НаборЗаписей = РегистрыСведений.ВыгружаемыеОбъектыКабинетСотрудника.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Ссылка.Установить(Выборка.Ссылка);
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.Ссылка 				= Выборка.Ссылка;
			ЗаписьНабора.ВыгружаетсяПоСсылке 	= Истина;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			НаборЗаписей = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Ссылка.Установить(Выборка.Ссылка);
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.Ссылка 		= Выборка.Ссылка;
			ЗаписьНабора.ТипОбъекта 	= ТипОбъекта;
			ЗаписьНабора.ВерсияДанных 	= Строка(Новый УникальныйИдентификатор);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ОбработкаЗавершена = Ложь;
		КонецПопытки;
		
	КонецЦикла;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбработкаЗавершена);

КонецПроцедуры

Процедура ОбновитьНастройкиФункциональности() Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяСервисКабинетСотрудника") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиКабинетСотрудника.РазделЗарплата КАК РазделЗарплата
	|ИЗ
	|	РегистрСведений.НастройкиКабинетСотрудника КАК НастройкиКабинетСотрудника";
	РезультатЗапрос = Запрос.Выполнить();
	Если Не РезультатЗапрос.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Настройки.ЗаявленияНаДСВ КАК ЗаявленияНаДСВ,
	|	Настройки.СпособРасчетаДСВПроцентом КАК СпособРасчетаДСВПроцентом,
	|	Настройки.СпособРасчетаДСВСуммой КАК СпособРасчетаДСВСуммой,
	|	Настройки.СпособРасчетаДСВПроцентомНеБолееСуммы КАК СпособРасчетаДСВПроцентомНеБолееСуммы,
	|	Настройки.СкрытьРазделКомпания КАК СкрытьРазделКомпания,
	|	Настройки.СкрытьРазделСправки КАК СкрытьРазделСправки,
	|	Настройки.СкрытьРазделДокументы КАК СкрытьРазделДокументы,
	|	Настройки.КомпенсацияОтпускаНедоступна КАК КомпенсацияОтпускаНедоступна,
	|	Настройки.ПолучениеДокументаСЭПНедоступно КАК ПолучениеДокументаСЭПНедоступно,
	|	Настройки.УровеньДоступаКИ КАК УровеньДоступаКИ,
	|	Настройки.ИнформацияОбОстаткахОтпусковНедоступна КАК ИнформацияОбОстаткахОтпусковНедоступна,
	|	Настройки.ДоступенОтказОтПодписания КАК ДоступенОтказОтПодписания,
	|	Настройки.ИспользоватьЗаявленияНаПереносОтпуска КАК ИспользоватьЗаявленияНаПереносОтпуска,
	|	Настройки.ИспользоватьЗаявленияНаОтменуОтпуска КАК ИспользоватьЗаявленияНаОтменуОтпуска,
	|	Настройки.ПоказыватьОхрануТруда КАК ПоказыватьОхрануТруда
	|ИЗ
	|	РегистрСведений.УдалитьИспользуемаяФункциональностьСервисаКабинетСотрудника КАК Настройки";
	Настройки = Запрос.Выполнить().Выгрузить();
	
	НаборЗаписей = РегистрыСведений.НастройкиКабинетСотрудника.СоздатьНаборЗаписей();
	ЗаписьНабора = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(ЗаписьНабора, НастройкиСервисаПоУмолчанию());
	
	Если Настройки.Количество() > 0 Тогда
		
		СтарыеНастройки = Настройки[0];
		
		ЗаписьНабора.ЗаявленияНаДСВ 						= СтарыеНастройки.ЗаявленияНаДСВ;
		ЗаписьНабора.СпособРасчетаДСВПроцентом 				= СтарыеНастройки.СпособРасчетаДСВПроцентом;
		ЗаписьНабора.СпособРасчетаДСВСуммой 				= СтарыеНастройки.СпособРасчетаДСВСуммой;
		ЗаписьНабора.СпособРасчетаДСВПроцентомНеБолееСуммы 	= СтарыеНастройки.СпособРасчетаДСВПроцентомНеБолееСуммы;
		ЗаписьНабора.РазделКомпания 						= Не СтарыеНастройки.СкрытьРазделКомпания;
		ЗаписьНабора.РазделСправки 							= Не СтарыеНастройки.СкрытьРазделСправки;
		ЗаписьНабора.РазделДокументы 						= Не СтарыеНастройки.СкрытьРазделДокументы;
		ЗаписьНабора.КомпенсацияОтпуска 					= Не СтарыеНастройки.КомпенсацияОтпускаНедоступна;
		ЗаписьНабора.ДоступноПолучениеДокументаСЭП 			= Не СтарыеНастройки.ПолучениеДокументаСЭПНедоступно;
		ЗаписьНабора.УровеньДоступаКИ 						= СтарыеНастройки.УровеньДоступаКИ;
		ЗаписьНабора.ПоказыватьОстаткиОтпусков 				= Не СтарыеНастройки.ИнформацияОбОстаткахОтпусковНедоступна;
		ЗаписьНабора.ДоступенОтказОтПодписания 				= СтарыеНастройки.ДоступенОтказОтПодписания;
		ЗаписьНабора.ИспользоватьЗаявленияНаПереносОтпуска 	= СтарыеНастройки.ИспользоватьЗаявленияНаПереносОтпуска;
		ЗаписьНабора.ИспользоватьЗаявленияНаОтменуОтпуска 	= СтарыеНастройки.ИспользоватьЗаявленияНаОтменуОтпуска;
		ЗаписьНабора.ПоказыватьОхрануТруда 					= СтарыеНастройки.ПоказыватьОхрануТруда;
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);

КонецПроцедуры

#КонецОбласти

#Область Прочие

Функция ТипЗаявкиТипОбъекта() Экспорт
	
	ПеречислениеТипыОбъектов = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом;
	ПеречислениеТипыЗаявок = Перечисления.ТипыЗаявокКабинетСотрудника;
	ТипЗаявкиТипОбъекта = Новый Соответствие;
	
	ТипЗаявкиТипОбъекта.Вставить(ПеречислениеТипыЗаявок.СправкаНДФЛ, 					ПеречислениеТипыОбъектов.ЗапросСправки2НДФЛ);
	ТипЗаявкиТипОбъекта.Вставить(ПеречислениеТипыЗаявок.ЗаявлениеНаКомпенсациюОтпуска, 	ПеречислениеТипыОбъектов.ЗаявкаНаКомпенсациюОтпуска);
	ТипЗаявкиТипОбъекта.Вставить(ПеречислениеТипыЗаявок.ИзменениеЛичныхДанных, 			ПеречислениеТипыОбъектов.ЗаявкаИзменениеЛичныхДанных);
	ТипЗаявкиТипОбъекта.Вставить(ПеречислениеТипыЗаявок.ЗаявлениеНаУдержаниеДСВ, 		ПеречислениеТипыОбъектов.ЗаявкаДСВ);
	ТипЗаявкиТипОбъекта.Вставить(ПеречислениеТипыЗаявок.СправкаСМестаРаботы, 			ПеречислениеТипыОбъектов.ЗапросСправкиСМестаРаботы); 
	ТипЗаявкиТипОбъекта.Вставить(ПеречислениеТипыЗаявок.ЗаявлениеНаОтпуск, 				ПеречислениеТипыОбъектов.ЗаявкаНаОтпуск);
	ТипЗаявкиТипОбъекта.Вставить(ПеречислениеТипыЗаявок.СправкаОбОстаткеОтпуска, 		ПеречислениеТипыОбъектов.ЗаявкаОстаткиОтпусков);
	ТипЗаявкиТипОбъекта.Вставить(ПеречислениеТипыЗаявок.ЗаявлениеНаНалоговыеВычеты, 	ПеречислениеТипыОбъектов.ЗаявкаНалоговыйВычет);
	ТипЗаявкиТипОбъекта.Вставить(ПеречислениеТипыЗаявок.СогласованиеОтсутствия, 		ПеречислениеТипыОбъектов.Отсутствие);
	ТипЗаявкиТипОбъекта.Вставить(ПеречислениеТипыЗаявок.ОбращениеСотрудника, 			ПеречислениеТипыОбъектов.ОбращениеСотрудника);
	
	Возврат ТипЗаявкиТипОбъекта;

КонецФункции

Процедура ДобавитьБлокировкуРегистрацияОбъектовДляОбмена(Блокировка, ОбъектыДляРегистрации) Экспорт
	
	ВыгружаемыеОбъекты = Неопределено;
	ОбъектыДляРегистрации.Свойство("ВыгружаемыеОбъекты", ВыгружаемыеОбъекты);
	Если ЗначениеЗаполнено(ВыгружаемыеОбъекты) Тогда
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = ВыгружаемыеОбъекты;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
	КонецЕсли;
	
	ИзмененияДляОбмена = Неопределено;
	ОбъектыДляРегистрации.Свойство("ИзмененияДляОбмена", ИзмененияДляОбмена);
	Если ЗначениеЗаполнено(ИзмененияДляОбмена) Тогда
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = ИзмененияДляОбмена;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
	КонецЕсли;
	
	ИнтеграцияКабинетСотрудникаВнутренний.ДобавитьБлокировкуРегистрацияОбъектовДляОбмена(Блокировка, ОбъектыДляРегистрации);

КонецПроцедуры

Процедура ЗаписатьОбъектыДляОбмена(ОбъектыДляРегистрации) Экспорт

	ВыгружаемыеОбъекты = Неопределено;
	ОбъектыДляРегистрации.Свойство("ВыгружаемыеОбъекты", ВыгружаемыеОбъекты);
	Если ЗначениеЗаполнено(ВыгружаемыеОбъекты) Тогда
		Для каждого СтрокаТЗ Из ВыгружаемыеОбъекты Цикл 
			МенеджерЗаписи = РегистрыСведений.ВыгружаемыеОбъектыКабинетСотрудника.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТЗ);
			Если СтрокаТЗ.Удалить Тогда
				МенеджерЗаписи.Удалить();
			Иначе
				МенеджерЗаписи.Записать();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ИзмененияДляОбмена = Неопределено;
	ОбъектыДляРегистрации.Свойство("ИзмененияДляОбмена", ИзмененияДляОбмена);
	Если ЗначениеЗаполнено(ИзмененияДляОбмена) Тогда
		Для каждого СтрокаТЗ Из ИзмененияДляОбмена Цикл 
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТЗ);
			МенеджерЗаписи.Записать();
		КонецЦикла;
	КонецЕсли; 
	
	ИнтеграцияКабинетСотрудникаВнутренний.ЗаписатьОбъектыДляОбмена(ОбъектыДляРегистрации);
	
	ФизическиеЛицаОрганизации = Неопределено;
	ОбъектыДляРегистрации.Свойство("ФизическиеЛицаОрганизации", ФизическиеЛицаОрганизации);
	Если ЗначениеЗаполнено(ФизическиеЛицаОрганизации) Тогда
		ЭДОБЗК.СформироватьСогласияНаПрисоединениеКЭДОБЗК(ФизическиеЛицаОрганизации);
	КонецЕсли;

КонецПроцедуры

Функция ДоступнаВерсияDTO_2_0()
	
	Если ИспользуетсяВерсияDTO("2.0") Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверяем версии в приложении.
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	МенеджерОбмена = ИнтеграцияУправлениеПерсоналомОбмен.НовыйМенеджерОбмена(Приложение, Ложь);
	Попытка
		МенеджерОбмена.ПроверитьВерсиюФорматаОбмена();
	Исключение
		// Не удалось подобрать версию формата.
		// Исключение не обрабатываем.
		Возврат Ложь;
	КонецПопытки;

	Возврат ИспользуетсяВерсияDTO("2.0");
	
КонецФункции

Функция ИменаСобытийЖР() Экспорт
	
	Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника;
	Возврат ИнтеграцияУправлениеПерсоналом.ИменаСобытийЖР(Приложение);

КонецФункции

Функция ДоступнаПубликацияРасчетныхЛистков() Экспорт

	Возврат ПравоДоступа("Чтение", Метаданные.РегистрыСведений.РасчетныеЛисткиКабинетСотрудника);

КонецФункции

Функция ИспользуетсяЭДОБЗК() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Настройки = РегистрыСведений.НастройкиИнтеграцииКабинетСотрудника.НастройкиИнтеграции();
	Возврат Настройки.ИспользуетсяЭДОБЗК;

КонецФункции

Функция СпособПолученияСогласияНаПрисоединениеКЭДОБЗК()

	УстановитьПривилегированныйРежим(Истина);
	Настройки = РегистрыСведений.НастройкиИнтеграцииКабинетСотрудника.НастройкиИнтеграции();
	Возврат Настройки.СпособПолученияСогласияНаПрисоединениеКЭДОБЗК;

КонецФункции

Процедура ЗарегистрироватьПубликациюВидовСправок() Экспорт
	
	ВидыСправок = ИнтеграцияУправлениеПерсоналом.ВидыПредоставляемыхСотрудникамСправок();
	
	Если Не ЗначениеЗаполнено(ВидыСправок) Тогда
		ОтключитьОбработчикОбмена(КабинетСотрудника.ИмяОбработчикаЗарегистрироватьПубликациюВидовСправок());
		Возврат;
	КонецЕсли;
	
	ТаблицаВидовСправок = Новый ТаблицаЗначений;
	ТаблицаВидовСправок.Колонки.Добавить("ВидСправки");
	ТаблицаВидовСправок.Колонки.Добавить("ТипОбъекта");
	Для каждого ВидСправки Из ВидыСправок Цикл
		НоваяСтрока = ТаблицаВидовСправок.Добавить();
		НоваяСтрока.ВидСправки = ВидСправки;
		НоваяСтрока.ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ВидПредоставляемойСотрудникамСправки;
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИзмененияДляОбменаКабинетСотрудника");
		ЭлементБлокировки.ИсточникДанных = ТаблицаВидовСправок;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ссылка", "ВидСправки");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипОбъекта", "ТипОбъекта");
		Блокировка.Заблокировать();
		
		Для каждого СтрокаТЗ Из ТаблицаВидовСправок Цикл
			МенеджерЗаписи = РегистрыСведений.ИзмененияДляОбменаКабинетСотрудника.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Ссылка 		= СтрокаТЗ.ВидСправки;
			МенеджерЗаписи.ТипОбъекта 	= СтрокаТЗ.ТипОбъекта;
			МенеджерЗаписи.ВерсияДанных = Строка(Новый УникальныйИдентификатор);
			МенеджерЗаписи.Записать();
		КонецЦикла;
		
		ОтключитьОбработчикОбмена(КабинетСотрудника.ИмяОбработчикаЗарегистрироватьПубликациюВидовСправок());
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ИнтеграцияУправлениеПерсоналомСобытия.ЗаписатьОшибку(ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;

КонецПроцедуры

Процедура ВключитьСборГрафиковОтпусков() Экспорт
	
	ИнтеграцияКабинетСотрудникаВнутренний.ВключитьСборГрафиковОтпусков();

КонецПроцедуры

Функция ОтключитьКабинетыНепубликуемыхСотрудников()

	// Привилегированный режим устанавливается для обработки всех данных,
	// не зависимо от ограничений доступа для пользователя, который инициировал отключение кабинетов.
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Структура("СообщениеОбОшибке");
	
	Идентификаторы = ИнтеграцияКабинетСотрудникаОбмен.ИдентификаторыАктивныхФизическихЛиц();
	Если Идентификаторы = Неопределено Тогда
		СообщениеОбОшибке = НСтр("ru = 'Ошибка получения данных из сервиса. Подробности см. в журнале регистрации.';
								|en = 'An error occurred when receiving data from the service. For more information, see the Event log.'");
		Результат.СообщениеОбОшибке = СообщениеОбОшибке;
		Возврат Результат;
	КонецЕсли;
	
	ТипОбъекта = Перечисления.ТипыОбъектовИнтеграцияУправлениеПерсоналом.ФизическоеЛицо;
	ИдентификаторыСсылки = ИнтеграцияУправлениеПерсоналомОбмен.ПубличныйИдентификаторСсылка(Идентификаторы, ТипОбъекта);
	
	ТаблицаФизическихЛиц = Новый ТаблицаЗначений;
	ТаблицаФизическихЛиц.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаФизическихЛиц.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(36)));
	Для каждого Идентификатор Из Идентификаторы Цикл
		
		ФизическоеЛицо = ИдентификаторыСсылки[Идентификатор];
		Если ФизическоеЛицо = Неопределено Тогда
			ФизическоеЛицо = Справочники.ФизическиеЛица.ПолучитьСсылку(Новый УникальныйИдентификатор(Идентификатор));
		КонецЕсли;
		
		НоваяСтрока = ТаблицаФизическихЛиц.Добавить();
		НоваяСтрока.ФизическоеЛицо = ФизическоеЛицо;
		НоваяСтрока.Идентификатор = Идентификатор;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаФизическихЛиц", ТаблицаФизическихЛиц);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ФизическиеЛица.Идентификатор КАК Идентификатор
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	&ТаблицаФизическихЛиц КАК ФизическиеЛица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ФизическиеЛица.Идентификатор КАК Идентификатор
	|ИЗ
	|	ВТФизическиеЛица КАК ФизическиеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВыгружаемыеОбъектыКабинетСотрудника КАК ВыгружаемыеФизическиеЛица
	|		ПО (ФизическиеЛица.ФизическоеЛицо = ВыгружаемыеФизическиеЛица.Ссылка
	|				И НЕ ВыгружаемыеФизическиеЛица.ВыгружаетсяПоСсылке)
	|ГДЕ
	|	ВыгружаемыеФизическиеЛица.ОкончаниеВыгрузки ЕСТЬ NULL";
	ИдентификаторыКДеактивации = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Идентификатор");
	
	Если ИдентификаторыКДеактивации.Количество() > 0 Тогда
		Результат = ОтключитьКабинетыПоИдентификаторам(ИдентификаторыКДеактивации);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОтключитьКабинетыПоИдентификаторам(Идентификаторы) Экспорт
	
	Результат = Новый Структура("СообщениеОбОшибке");
	
	БылиОшибки = ИнтеграцияКабинетСотрудникаОбмен.РезультатДеактивацииФизическихЛиц(Идентификаторы);
	Если БылиОшибки Тогда
		СообщениеОбОшибке = НСтр("ru = 'Ошибка перевода кабинетов в неактивное состояние. Подробности см. в журнале регистрации.';
								|en = 'An error occurred when switching the accounts to an inactive state. For details, see the event log.'");
		Результат.СообщениеОбОшибке = СообщениеОбОшибке;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция ИмяКомандыПодписатьФормыПечатьДокументов()
	Возврат "ПередатьПодписанныеPDFВСервисКабинетСотрудника";
КонецФункции

Процедура ОбновитьАктуальностьИнформацииОбОтпусках(ФизическиеЛица, АктуальностьИнформацииОбОтпусках) Экспорт

	ИнтеграцияКабинетСотрудникаВнутренний.ОбновитьАктуальностьИнформацииОбОтпусках(ФизическиеЛица, АктуальностьИнформацииОбОтпусках);

КонецПроцедуры

Процедура ЗаполнитьНастройкиПодключения(НастройкиПодключения) Экспорт
	
	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	АдресПриложения = Настройки.АдресПриложения;
	Если ЗначениеЗаполнено(Настройки.АдресПриложенияПоИмени) И Настройки.АдресПриложенияПоИмениДоступен Тогда
		АдресПриложения = Настройки.АдресПриложенияПоИмени;
	КонецЕсли;
	НастройкиПодключения.ВерсияDTO 					= СокрЛП(Настройки.ВерсияDTO);
	НастройкиПодключения.ВерсияAPI 					= СокрЛП(Настройки.ВерсияAPI); 
	НастройкиПодключения.АдресПриложения 			= СокрЛП(АдресПриложения);
	НастройкиПодключения.АдресПриложенияПоИмени 	= СокрЛП(Настройки.АдресПриложенияПоИмени);
	НастройкиПодключения.АдресСервисаАвторизации 	= СокрЛП(Настройки.АдресСервисаАвторизации);
	НастройкиПодключения.Идентификатор 				= Настройки.Идентификатор;
	НастройкиПодключения.ИдентификаторПриложения 	= Настройки.ИдентификаторПриложения;
	
КонецПроцедуры

Процедура ЗаписатьНовогоОтветственногоЗаПриложение(Ответственный) Экспорт

	Настройки = РегистрыСведений.НастройкиСервисаКабинетСотрудника.НастройкиСервиса();
	Настройки.Ответственный = Ответственный;
	НаборЗаписей = РегистрыСведений.НастройкиСервисаКабинетСотрудника.СоздатьНаборЗаписей();
	ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Настройки);
	НаборЗаписей.Записать();

КонецПроцедуры

// Вызывается при изменении версии формата обмена на 3.1.
// Переносит настройку ПоказыватьОхрануТруда в ИспользоватьРабочиеМестаОхраныТруда.
//
Процедура УстановитьИспользоватьРабочиеМестаОхраныТруда()

	Настройки = РегистрыСведений.НастройкиКабинетСотрудника.Настройки();
	Настройки.ПоказыватьРабочиеМестаОхраныТруда = Настройки.ПоказыватьОхрануТруда;
	РегистрыСведений.НастройкиКабинетСотрудника.СохранитьНовыеНастройки(Настройки);
	
	РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьТребуетсяОбновитьНастройкиФункциональности(Истина);

КонецПроцедуры

Процедура ОтправитьИспользованиеИнтеграции1СПерсонал() Экспорт
	
	Если Не ИнтеграцияУправлениеПерсоналом.Используется1СПерсонал()
		Или Не ИнтеграцияКабинетСотрудника.ИспользуетсяВерсияDTO("2.0") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьТребуетсяОбновитьНастройкиФункциональности(Истина);
	Попытка
		ИнтеграцияКабинетСотрудникаОбмен.ВыгрузитьНастройкиПриложения();
	Исключение
		// Не обрабатываем исключение, если сразу не удалось опубликовать настройки,
		// следующая попытка будет в процессе штатного обмена.
	КонецПопытки;

КонецПроцедуры

Функция ИспользуетсяСборДанныхДляОформленияНаРаботу() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Настройки = РегистрыСведений.НастройкиИнтеграцииКабинетСотрудника.НастройкиИнтеграции();
	Возврат Настройки.ИспользоватьСборДанныхДляОформленияНаРаботу;

КонецФункции

#КонецОбласти

#КонецОбласти
