///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ОнлайнЗаказы".
// ОбщийМодуль.ОнлайнЗаказыСервис.
//
// Серверные процедуры онлайн-заказов:
//  - создание и получение ссылки для редактирования страницы онлайн-заказов;
//  - формирование, активация и деактивация заказов, получение статусов;
//  - подготовка и отправка вложений заказов.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область ПрикладныеОперации

// См. ОнлайнЗаказыСлужебный.НовыйЗаказ.
//
Функция СформироватьЗаказ(
		ДанныеЗаказа,
		НастройкаПодключения,
		НастройкиОплаты) Экспорт
		
	РезультатОперации = ОнлайнЗаказыСлужебный.НовыйРезультатДанныеЗаказа();
	
	// Создание/обновление онлайн-заказа.
	ДанныеСсылки = ОперацияСФормироватьЗаказ(
		ДанныеЗаказа,
		НастройкаПодключения,
		НастройкиОплаты);
		
	Если ЗначениеЗаполнено(ДанныеСсылки.КодОшибки) Тогда
		
		ЗаполнитьЗначенияСвойств(
			РезультатОперации,
			ДанныеСсылки,
			"СтатусЗаказа, КодОшибки, СообщениеОбОшибке, ИнформацияОбОшибке");
			
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	РезультатОперации.URLЗаказа    = ДанныеСсылки.URLЗаказа;
	РезультатОперации.СуммаЗаказа  = ДанныеСсылки.СуммаЗаказа;
	РезультатОперации.СтатусЗаказа = ОнлайнЗаказыКлиентСервер.СтатусЗаказаВыполняется();
	
	Возврат РезультатОперации;
	
КонецФункции

// Возвращает данные онлайн-заказов.
//
// Параметры:
//  ПараметрыОперации - Структура - Содержит параметры операции:
//    * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам -
//      настройка выполнения операции.
//    * ИдентификаторыЗаказов - Массив Из Строка - Идентификаторы онлайн-заказов
//
// Возвращаемое значение:
//  Структура - результат создания платежной ссылки онлайн-заказа:
//    * ДанныеЗаказов - Массив Из Структура - содержит данные онлайн-заказов:
//      ** ИдентификаторЗаказа - Строка - Идентификатор онлайн-заказа;
//      ** Статус - Строка - Статус онлайн-заказа;
//      ** СуммаЗаказа - Число - Сумма онлайн-заказа;
//      ** СпособОплаты - Перечисление.СпособыОплатыОнлайнЗаказов - Способ оплаты онлайн-заказа;
//      ** ДатаОплаты - дата получения данных об оплате сервисом;
//      ** КонтрольнаяСумма - Строка - Значение контрольной суммы заказа;
//      ** ВерсияКонтрольнойСуммы - Число - Версия расчета контрольной суммы заказа;
//      ** ОплатаСБП - Структура, Неопределено - Содержит данные операции СБП, в том случае когда
//         значение Свойства СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b:
//        ***ИдентификаторОплаты - Строка - идентификатор оплаты в СБП;
//        ***ИдентификаторПлатежнойСистемы - Строка - идентификатор оплаты в Системе быстрых платежей;
//        ***ДатаОперации - Дата - фактическая дата оплаты в UTC;
//      ** ОплатаИнтернетЭквайринг - Структура, Неопределено - Содержит данные операции оплаты, в том случае когда
//         значение Свойства СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг:
//        ***ИдентификаторОплаты - Строка - идентификатор оплаты;
//        ***ИдентификаторОперации - Строка - идентификатор оплаты в системе банка;
//        ***ДатаОперации - Дата - фактическая дата оплаты в UTC;
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом:
//        - <Пустая строка> - получение URL заказа выполнено успешно;
//        - "НеверныйФорматЗапроса" - передан некорректный запрос или настройка подключения;
//        - "НеверныйЛогинИлиПароль" - некорректный логин/пароль ИПП;
//        - "СервисВременноНеДоступен" - временная недоступность сервиса;
//        - "ПревышеноКоличествоПопыток" - превышено количество попыток
//          обращения к сервису с некорректным логином и паролем;
//        - "ОшибкаПодключения" - ошибка при подключении к сервису;
//        - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ПолучениеИнформацииПоЗаказам(ПараметрыОперации) Экспорт
	
	Возврат ОперацияПолученияИнформацииПоЗаказам(
		ПараметрыОперации);
	
КонецФункции

// Выполняет активацию заказа в сервисе.
//
// Параметры:
//  ИдентификаторЗаказа - Строка - идентификатор онлайн-заказа в сервисе.
//
// Возвращаемое значение:
//  Структура - результат активации онлайн-заказа:
//  * СтатусЗаказа - Строка - Статус онлайн-заказа;
//  * КодОшибки - Строка - строковый код возникшей ошибки, который
//    может быть обработан вызывающим методом:
//      - <Пустая строка> - создание нового заказа выполнено успешно;
//      - "НеверныйФорматЗапроса" - передан некорректный запрос или настройка подключения;
//      - "УжеОплачен" - документ уже оплачен;
//      - "ОтсутствуетДоступКСервису" - у пользователя нет доступа к сервису;
//      - "ПревышеноКоличествоПопыток" - превышено количество попыток
//        обращения к сервису с некорректным логином и паролем;
//      - "ОшибкаПодключения" - ошибка при подключении к сервису;
//      - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//      - "НеизвестнаяОшибка" - при получении информации возникла
//        неизвестная (не обрабатываемая) ошибка;
//      - "СервисВременноНеДоступен" - на сервере ведутся регламентные работы;
//      - "НеиспользуемаяНастройка" - использование отключено в настройках;
//  * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//  * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция АктивироватьЗаказ(ИдентификаторЗаказа) Экспорт
	
	Возврат ОперацияАктивацииЗаказа(ИдентификаторЗаказа);
	
КонецФункции

// Выполняет деактивацию заказа в сервисе.
//
// Параметры:
//  ИдентификаторЗаказа - Строка - идентификатор онлайн-заказа в сервисе.
//
// Возвращаемое значение:
//  * СтатусЗаказа - Строка - Статус онлайн-заказа;
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом:
//        - <Пустая строка> - создание нового заказа выполнено успешно;
//        - "НеверныйФорматЗапроса" - передан некорректный запрос или настройка подключения;
//        - "УжеОплачен" - документ уже оплачен;
//        - "ОтсутствуетДоступКСервису" - у пользователя нет доступа к сервису;
//        - "ПревышеноКоличествоПопыток" - превышено количество попыток
//          обращения к сервису с некорректным логином и паролем;
//        - "ОшибкаПодключения" - ошибка при подключении к сервису;
//        - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//        - "НеизвестнаяОшибка" - при получении информации возникла
//          неизвестная (не обрабатываемая) ошибка;
//        - "СервисВременноНеДоступен" - на сервере ведутся регламентные работы;
//        - "НеиспользуемаяНастройка" - использование отключено в настройках;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ДеактивироватьЗаказ(ИдентификаторЗаказа) Экспорт
	
	Возврат ОперацияДеактивацияЗаказа(ИдентификаторЗаказа);
	
КонецФункции

// Выполняет установку статуса оплаты онлайн-заказа в сервисе.
//
// Параметры:
//  ДанныеЗаказа - Структура - содержит описание данных заказа
//  См. ОнлайнЗаказыСлужебный.ДанныеЗаказаНаОплатуПоДокументу;
//  ПараметрыСсылки - Структура - Содержит описание параметров ссылки онлайн-заказа
//  См РегистрыСведений.ИдентификаторыОнлайнЗаказов.ПараметрыСсылкиПоДокументуЗаказа;
//  СпособОплаты - Перечисления.СпособыОплатыОнлайнЗаказов - Способ оплаты онлайн-заказа;
//
// Возвращаемое значение:
//  Структура - результат установки статуса онлайн-заказа:
//    * СтатусЗаказа - Строка - Статус онлайн-заказа;
//    * ХешАктуален - Булево - Признак актуальности хеша онлайн-заказа в сервисе.
//      Если Ложь требует обновления;
//    * СуммаЗаказа - Число - Сумма онлайн-заказа;
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом:
//        - <Пустая строка> - создание нового заказа выполнено успешно;
//        - "НеверныйФорматЗапроса" - передан некорректный запрос или настройка подключения;
//        - "УжеОплачен" - документ уже оплачен;
//        - "ОтсутствуетДоступКСервису" - у пользователя нет доступа к сервису;
//        - "ПревышеноКоличествоПопыток" - превышено количество попыток
//          обращения к сервису с некорректным логином и паролем;
//        - "ОшибкаПодключения" - ошибка при подключении к сервису;
//        - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//        - "НеизвестнаяОшибка" - при получении информации возникла
//          неизвестная (не обрабатываемая) ошибка;
//        - "СервисВременноНеДоступен" - на сервере ведутся регламентные работы;
//        - "НеиспользуемаяНастройка" - использование отключено в настройках;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция УстановитьСтатусОплачен(ДанныеЗаказа, ПараметрыСсылки, СпособОплаты) Экспорт
	
	Возврат ОперацияУстановитьСтатусОплачен(ДанныеЗаказа, ПараметрыСсылки, СпособОплаты);
	
КонецФункции

// Выполняет актуализацию вложений заказа.
//
// Параметры:
//  ПараметрыОперации - Структура - содержит данные для выполнения операции:
//    * ДокументЗаказа - ОпределяемыйТип.ДокументОнлайнЗаказаБИП - документ в информационной базе,
//      являющийся основанием для заказа;
//    * ДанныеВложений - Соответствие - см. ОнлайнЗаказыСлужебный.ПодготовитьДанныеВложений;
//    * ПечатныеФормы - Массив Из Структура - Содержит описание и данные печатных форм;
//    * ПрисоединенныеФайлы - Массив Из Произвольный - Содержит ссылки
//      на элементы справочника Присоединенные файлы по ДокументуЗаказа;
//
// Возвращаемое значение:
//  Структура - результат выгрузки вложений:
//    * URLЗаказа - Строка - Ссылка на онлайн-заказ;
//    * СтатусЗаказа - Строка - Текущий статус онлайн-заказа;
//    * СуммаЗаказа - Число - Сумма онлайн-заказа;
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом:
//        - <Пустая строка> - создание нового заказа выполнено успешно;
//        - "НеверныйФорматЗапроса" - передан некорректный запрос или настройка подключения;
//        - "УжеОплачен" - документ уже оплачен;
//        - "ОтсутствуетДоступКСервису" - у пользователя нет доступа к сервису;
//        - "ПревышеноКоличествоПопыток" - превышено количество попыток
//          обращения к сервису с некорректным логином и паролем;
//        - "ОшибкаПодключения" - ошибка при подключении к сервису;
//        - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//        - "НеизвестнаяОшибка" - при получении информации возникла
//          неизвестная (не обрабатываемая) ошибка;
//        - "СервисВременноНеДоступен" - на сервере ведутся регламентные работы;
//        - "НеиспользуемаяНастройка" - использование отключено в настройках;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОтправкаВложений(ПараметрыОперации) Экспорт
	
	РезультатОперации = ОнлайнЗаказыСлужебный.НовыйРезультатДанныеЗаказа();
	
	ПараметрыСсылки = РегистрыСведений.ИдентификаторыОнлайнЗаказов.ПараметрыСсылкиПоДокументуЗаказа(
		ПараметрыОперации.ДокументЗаказа);
	
	Если Не ЗначениеЗаполнено(ПараметрыСсылки.ИдентификаторЗаказа) Тогда
		
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйФорматЗапроса();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Не обнаружен идентификатор заказа.';
													|en = 'Order ID is not found.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Не обнаружен идентификатор заказа.';
													|en = 'Order ID is not found.'");
		
		Возврат РезультатОперации;
		
	ИначеЕсли ПараметрыСсылки.СтатусЗаказа = ОнлайнЗаказыСлужебный.СтатусЗаказаОплачен() Тогда
		
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиУжеОплачен();
		РезультатОперации.СообщениеОбОшибке  = НСтр("ru = 'Заказ уже оплачен, редактирование невозможно.';
													|en = 'The order has already been paid; editing is not possible.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Заказ уже оплачен, редактирование невозможно.''");
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	ПараметрыОперации.Вставить("ИдентификаторЗаказа", ПараметрыСсылки.ИдентификаторЗаказа);
	
	РезультатДеактивации = ОнлайнЗаказыСлужебный.ДеактивироватьЗаказ(ПараметрыОперации.ДокументЗаказа);
	
	Если ЗначениеЗаполнено(РезультатДеактивации.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(РезультатОперации, РезультатДеактивации);
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатПодготовки = ОперацияПодготовкаВложенийКОтправке(ПараметрыОперации);
	
	Если ЗначениеЗаполнено(РезультатПодготовки.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(РезультатОперации, РезультатПодготовки);
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатПередачи = ОперацияПередачаВложений(
		ПараметрыОперации.ДанныеВложений,
		РезультатПодготовки.ВложенияКЗагрузке);
	
	Если ЗначениеЗаполнено(РезультатПередачи.КодОшибки) Тогда
		ЗаполнитьЗначенияСвойств(РезультатОперации, РезультатПередачи);
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатАктивации = ОперацияАктивацииЗаказа(ПараметрыОперации.ИдентификаторЗаказа);
	
	ЗаполнитьЗначенияСвойств(РезультатОперации, РезультатАктивации);
	
	Возврат РезультатОперации;
	
КонецФункции

#КонецОбласти

#Область РаботаСПорталом

// Выполняет обновление настройки онлайн-заказов.
// Параметры:
// ПараметрыОперации - Структура - Содержит параметры операции:
//  * НастройкаПодключения -  - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам -
//    настройка выполнения операции.
//  * ТокенСПБb2b - Строка - Токен для сервиса payment-gateway для сценария оплаты СБП b2b
//  * ТокенСПБc2b - Строка - Токен для сервиса payment-gateway для сценария оплаты СБП c2b
//  * ТокенИнтернетЭквайринг - Строка - Токен подключения, используемый для оплаты по карте.
//
// Возвращаемое значение:
//  Структура - результат обновления настройки страницы онлайн-заказов:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом:
//        - <Пустая строка> - получение URL заказа выполнено успешно;
//        - "НеверныйФорматЗапроса" - передан некорректный запрос или настройка подключения;
//        - "НеверныйЛогинИлиПароль" - некорректный логин/пароль ИПП;
//        - "СервисВременноНеДоступен" - временная недоступность сервиса;
//        - "ПревышеноКоличествоПопыток" - превышено количество попыток
//          обращения к сервису с некорректным логином и паролем;
//        - "ОшибкаПодключения" - ошибка при подключении к сервису;
//        - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция ОбновлениеНастройки(ПараметрыОперации) Экспорт
	
	Возврат ОперацияОбновленияНастройки(
		ПараметрыОперации);
	
КонецФункции

// Выполняет получение URL для перехода к настройке внешнего вида страницы онлайн-заказов.
// Параметры:
// НастройкаПодключения -  - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам -
// настройка выполнения операции.
//
// Возвращаемое значение:
//  Структура - результат получения URL:
//    * URLНастройкиПодключения - Строка - URL для перехода к настройке внешнего вида страницы онлайн-заказов.
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом:
//        - <Пустая строка> - получение URL заказа выполнено успешно;
//        - "НеверныйФорматЗапроса" - передан некорректный запрос или настройка подключения;
//        - "НеверныйЛогинИлиПароль" - некорректный логин/пароль ИПП;
//        - "СервисВременноНеДоступен" - временная недоступность сервиса;
//        - "ПревышеноКоличествоПопыток" - превышено количество попыток
//          обращения к сервису с некорректным логином и паролем;
//        - "ОшибкаПодключения" - ошибка при подключении к сервису;
//        - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция URLНастройкиПодключения(НастройкаПодключения) Экспорт
	
	Возврат ОперацияURLНастройкиПодключения(
		НастройкаПодключения);
	
КонецФункции

#КонецОбласти

#Область ВызовОперацииСозданиеНастройки

// См. ОнлайнЗаказыСервис.ОбновлениеНастройки
//
Функция ОперацияОбновленияНастройки(ПараметрыОперации)
	
	РезультатОперации = ОнлайнЗаказыСлужебный.НовыйРезультатОперации();
	
	ПараметрыНастройки = ОнлайнЗаказыСлужебный.ПараметрыНастройки(
		ПараметрыОперации.НастройкаПодключения);
	
	Если ПараметрыНастройки = Неопределено Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = НСтр("ru = 'Параметры настройки не определены.';
													|en = 'The settings are not defined.'");
		РезультатОперации.ИнформацияОбОшибке = НСтр("ru = 'Параметры настройки не определены.';
													|en = 'The settings are not defined.'");
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатДанныеАутентификации = ДанныеАутентификацииНастройкиПодключения(
		ПараметрыОперации.НастройкаПодключения,
		Истина);
	
	Если РезультатДанныеАутентификации.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатДанныеАутентификации.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатДанныеАутентификации.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	URLОперации = ОнлайнЗаказыСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/page-settings/%1",
			РезультатДанныеАутентификации.Идентификатор));
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ПараметрыПодключения = ОнлайнЗаказыСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = page_settings(
		ПараметрыОперации,
		ПараметрыНастройки);
	
	Заголовки = НовыйЗаголовкиВызоваОперации();
	Заголовки.Вставить("Authorization", СтрокаАутентификации(РезультатИПП.ДанныеАутентификации));
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "PUT");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ОнлайнЗаказыСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОтправки.КодСостояния,
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое,
			ОнлайнЗаказыСлужебный.ИдентификаторОперацииОбновленияНастройки());
			
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить создание настройки онлайн-заказов.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При создании настройки возникли ошибки.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4';
				|en = 'Cannot create the online order setting.
				|
				|%1
				|
				|View error details:
				|Errors occurred when creating the setting.
				|URL: %2
				|Error code: %3
				|Detailed information:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(Строка(РезультатОперации.ИнформацияОбОшибке));
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	РезультатОбновления = ОбновитьХешНастройки(
		ПараметрыОперации.НастройкаПодключения,
		ПрочитатьДанные_page_settings(
			РезультатОтправки.Содержимое));
	
	Если РезультатОбновления.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеизвестнаяОшибка();
		РезультатОперации.СообщениеОбОшибке = РезультатОбновления.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатОбновления.ИнформацияОбОшибке;
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для операции /isl/authorized/page-settings/{settingId}.
//
Функция page_settings(ПараметрыОперации, ПараметрыНастройки)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("organizationName");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыНастройки.ПредставлениеОрганизации);
	
	Если Не ПустаяСтрока(ПараметрыОперации.ТокенСПБc2b) Тогда
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("sbpC2bAuthToken");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыОперации.ТокенСПБc2b);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ПараметрыОперации.ТокенСПБb2b) Тогда
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("sbpB2bAuthToken");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыОперации.ТокенСПБb2b);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ПараметрыОперации.ТокенОблачнаяКасса) Тогда
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("cloudSalesRegisterAuthToken");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыОперации.ТокенОблачнаяКасса);
	КонецЕсли;
	
	НастройкиПриемаОплатПоРеквизитам = ПараметрыНастройки.НастройкиПриемаОплатПоРеквизитам;
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("c2bRequisitesPaymentEnabled");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(НастройкиПриемаОплатПоРеквизитам.ИспользуетсяПриемОплатОтФизЛиц);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("b2bRequisitesPaymentEnabled");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(НастройкиПриемаОплатПоРеквизитам.ИспользуетсяПриемОплатОтЮрЛиц);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("active");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыНастройки.Используется);
	
	Если Не ПустаяСтрока(ПараметрыОперации.ТокенИнтернетЭквайринг) Тогда
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("acquiringAuthToken");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(ПараметрыОперации.ТокенИнтернетЭквайринг);
	КонецЕсли;
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции /isl/authorized/page-settings/{settingId}.
//
Функция ПрочитатьДанные_page_settings(ТелоJSON)
	
	// Ответ сервиса:
	//
	//  settingsHash - Хеш настройки подключения рассчитанный в сервисе.
	//  Служит ключом идемпотентности настройки подключения.
	//
	//  "settingsHash": "9b9b17022a31abb8d443202b0b5932cb"
		
	Попытка
	
		ЧтениеОтвета = Новый ЧтениеJSON;
		ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
		Результат = ПрочитатьJSON(ЧтениеОтвета);
		
		Возврат Результат.settingsHash;
		
	Исключение
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		
		ВызватьИсключение НСтр("ru = 'Не удалось обработать ответ. Подробности в журнале регистрации.';
								|en = 'Cannot process the response. For more information, see the event log.'");
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#Область ВызовОперацииURLНастройкиПодключения

// См. ОнлайнЗаказыСервис.URLНастройкиПодключения
//
Функция ОперацияURLНастройкиПодключения(НастройкаПодключения)
	
	РезультатОперации = ОнлайнЗаказыСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("URLНастройкиПодключения", Неопределено);
	
	РезультатДанныеАутентификации = ДанныеАутентификацииНастройкиПодключения(
		НастройкаПодключения,
		Истина);
	
	Если РезультатДанныеАутентификации.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатДанныеАутентификации.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатДанныеАутентификации.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	URLОперации = ОнлайнЗаказыСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/page-settings/%1/edit-url",
			РезультатДанныеАутентификации.Идентификатор));
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ПараметрыПодключения = ОнлайнЗаказыСлужебный.ИнициализироватьПараметрыПодключения();
	
	Заголовки = НовыйЗаголовкиВызоваОперации();
	Заголовки.Вставить("Authorization", СтрокаАутентификации(РезультатИПП.ДанныеАутентификации));
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "GET");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ОнлайнЗаказыСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОтправки.КодСостояния,
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
			
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить адрес настройки внешнего вида страницы онлайн-заказов.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При получении адреса возникли ошибки.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4';
				|en = 'Cannot get the address of online order page appearance setting.
				|
				|%1
				|
				|View error details:
				|Errors occurred when getting the address.
				|URL: %2
				|Error code: %3
				|Detailed information:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(Строка(РезультатОперации.ИнформацияОбОшибке));
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	РезультатОперации.URLНастройкиПодключения = РезультатОтправки.Содержимое;
	
	Возврат РезультатОперации;
	
КонецФункции

#КонецОбласти

#Область ВызовОперацииURLЗаказа

// См. ОнлайнЗаказыСлужебный.НовыйЗаказ.
//
Функция ОперацияСФормироватьЗаказ(ДанныеЗаказа, НастройкаПодключения, НастройкиОплаты)
	
	РезультатОперации = ОнлайнЗаказыСлужебный.НовыйРезультатДанныеЗаказа();
	
	РезультатДанныеАутентификации = ДанныеАутентификацииНастройкиПодключения(НастройкаПодключения);
	
	Если РезультатДанныеАутентификации.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатДанныеАутентификации.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатДанныеАутентификации.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	URLОперации = ОнлайнЗаказыСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/orders/%1",
			ДанныеЗаказа.ИдентификаторЗаказа));
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ПараметрыПодключения = ОнлайнЗаказыСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = order(
		РезультатДанныеАутентификации,
		НастройкиОплаты,
		ДанныеЗаказа);
	
	Заголовки = НовыйЗаголовкиВызоваОперации();
	Заголовки.Вставить("Authorization", СтрокаАутентификации(РезультатИПП.ДанныеАутентификации));
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "PUT");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ОнлайнЗаказыСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОтправки.КодСостояния,
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
			
		Если РезультатОперации.КодОшибки = ОнлайнЗаказыКлиентСервер.КодОшибкиНедопустимыйСтатус() Тогда
			РезультатОперации.СтатусЗаказа =
				ПрочитатьДанные_order_illegal_status(РезультатОтправки.Содержимое).СтатусЗаказа;
		КонецЕсли;
			
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить создание/обновление заказа.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При создании/обновлении заказа возникли ошибки.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4';
				|en = 'Cannot create/update the order.
				|
				|%1
				|
				|View error details:
				|Errors occurred when creating/updating the order.
				|URL: %2
				|Error code: %3
				|Detailed information:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(Строка(РезультатОперации.ИнформацияОбОшибке));
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	РезультатЧтения = ПрочитатьДанные_order(РезультатОтправки.Содержимое);
	ЗаполнитьЗначенияСвойств(
		РезультатОперации,
		РезультатЧтения);
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для операции /isl/authorized/orders/{orderId}.
//
Функция order(
		ДанныеАутентификации,
		НастройкиОплаты,
		ДанныеЗаказа)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ОнлайнЗаказыСлужебный.ЗаписатьПараметрыЗаказа(
		ЗаписьДанныхСообщения,
		ДанныеЗаказа,
		НастройкиОплаты);
	
	ЗаписатьДанныеАутентификацииЗаказа(ЗаписьДанныхСообщения, ДанныеАутентификации);
	ЗаписатьДанныеКонтрольнойСуммы(ЗаписьДанныхСообщения, ДанныеЗаказа.КонтрольнаяСумма);
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Добавляет в запись JSON параметры аутентификации.
//
// Параметры:
//  ЗаписьДанныхСообщения  - ЗаписьJSON - запись, в которую необходимо
//    добавить данные аутентификации.
//  ДанныеАутентификации - Структура - см. ОнлайнЗаказыСервис.ДанныеАутентификацииНастройкиПодключения
//
Процедура ЗаписатьДанныеАутентификацииЗаказа(ЗаписьДанныхСообщения, ДанныеАутентификации)
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("settingId");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеАутентификации.Идентификатор);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("expectedSettingChecksumValue");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеАутентификации.КонтрольнаяСумма);
	
КонецПроцедуры

// Добавляет в запись JSON данные контрольной суммы.
//
// Параметры:
//  ЗаписьДанныхСообщения  - ЗаписьJSON - запись, в которую необходимо
//    добавить данные аутентификации.
//  ДанныеКонтрольнойСуммы - Структура - содержит данные контрольной суммы
//
Процедура ЗаписатьДанныеКонтрольнойСуммы(ЗаписьДанныхСообщения, ДанныеКонтрольнойСуммы)

	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("orderDataChecksumValue");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеКонтрольнойСуммы.Значение);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("orderDataChecksumVersion");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеКонтрольнойСуммы.ВерсияРасчета);
	
КонецПроцедуры

// Чтение ответа операции /isl/authorized/orders/{orderId}.
//
Функция ПрочитатьДанные_order(ТелоJSON)
	
	// Ответ сервиса:
	//
	//  orderPageUrl - URL заказа.
	//  amount - сумма заказа.
	//  currency - валюта заказа.
	//
	//{
	//  "orderPageUrl": "orders.1c.ru",
	//  "amount": 200,
	//  "currency": "RUB"
	//}
	
	Попытка
		
		ЧтениеОтвета = Новый ЧтениеJSON;
		ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
		РезультатЧтения = ПрочитатьJSON(ЧтениеОтвета);
		
		ДанныеЗаказа = Новый Структура;
		
		ДанныеЗаказа.Вставить("URLЗаказа",    РезультатЧтения.orderPageUrl);
		ДанныеЗаказа.Вставить("СуммаЗаказа",  РезультатЧтения.amount);
		
		Возврат ДанныеЗаказа;
		
	Исключение
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		
		ВызватьИсключение НСтр("ru = 'Не удалось обработать ответ. Подробности в журнале регистрации.';
								|en = 'Cannot process the response. For more information, see the event log.'");
		
	КонецПопытки;
	
КонецФункции

// Чтение ответа операции /isl/authorized/orders/{orderId}.
//
Функция ПрочитатьДанные_order_illegal_status(ТелоJSON)
	
	// Ответ сервиса:
	//
	//  type - тип ошибки.
	//  title - заголовок ошибки.
	//  status - код ошибки.
	//  detail - детальное описание ошибки.
	//  instance - инстанс.
	//  requestId - идентификатор запроса.
	//  requestTimestamp - таймкод запроса.
	//  orderStatus - статус заказа.
	//
	//{
	//  "type": "ILLEGAL_ORDER_STATUS",
	//  "title": "Некорректный статус заказа",
	//  "status": 409,
	//  "detail": "Увеличьте срок жизни заказ и повторите попытку",
	//  "instance": "/pm/api/isl/authorized/orders/1ca4d85d-9fca-45a8-9991-8a3625674afb",
	//  "requestId": "ec414604-2e01-4a49-8268-d4aa23c9c4eb",
	//  "requestTimestamp": "2024-09-02T14:16:18.783+00:00",
	//  "data":
	//    {
	//    "orderStatus": "EXPIRED"
	//    }
	//}
	
	Содержимое = Новый Структура;
	Содержимое.Вставить("СтатусЗаказа", "");
	
	Попытка
		
		ЧтениеОтвета = Новый ЧтениеJSON;
		ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
		РезультатЧтения = ПрочитатьJSON(ЧтениеОтвета);
		
		Содержимое.СтатусЗаказа = РезультатЧтения.data.orderStatus;
		
	Исключение
		
		Содержимое.СтатусЗаказа = "";
		
	КонецПопытки;
	
	Возврат Содержимое;
	
КонецФункции

#КонецОбласти

#Область АктивацияОнлайнЗаказа

// См. ОнлайнЗаказыСервис.АктивироватьЗаказ.
//
Функция ОперацияАктивацииЗаказа(ИдентификаторЗаказа)
	
	РезультатОперации = ОнлайнЗаказыСлужебный.НовыйРезультатСтатусЗаказа();
	РезультатОперации.Вставить("СтатусЗаказаВСервисе",   "");
	РезультатОперации.Вставить("КонтрольнаяСумма",       "");
	РезультатОперации.Вставить("ВерсияКонтрольнойСуммы", "");
	
	URLОперации = ОнлайнЗаказыСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/orders/%1/activate",
			ИдентификаторЗаказа));
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ПараметрыПодключения = ОнлайнЗаказыСлужебный.ИнициализироватьПараметрыПодключения();
	
	Заголовки = НовыйЗаголовкиВызоваОперации();
	Заголовки.Вставить("Authorization", СтрокаАутентификации(РезультатИПП.ДанныеАутентификации));
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ОнлайнЗаказыСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОтправки.КодСостояния,
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		Если РезультатОперации.КодОшибки = ОнлайнЗаказыКлиентСервер.КодОшибкиНедопустимыйСтатус() Тогда
			РезультатОперации.СтатусЗаказаВСервисе = ОнлайнЗаказыСлужебный.ОпределитьСтатусЗаказаВСервисе(
				РезультатОтправки.Содержимое);
		КонецЕсли;
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить активацию онлайн-заказа.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При активации онлайн-заказа возникли ошибки.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4';
				|en = 'Cannot activate the online order.
				|
				|%1
				|
				|View error details:
				|Errors occurred when activating the online order.
				|URL: %2
				|Error code: %3
				|Detailed information:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(Строка(РезультатОперации.ИнформацияОбОшибке));
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	РезультатАктивации = ПрочитатьДанные_activate(РезультатОтправки.Содержимое);
	
	ЗаполнитьЗначенияСвойств(РезультатОперации, РезультатАктивации);
	РезультатОперации.СтатусЗаказа = ОнлайнЗаказыКлиентСервер.СтатусЗаказаВыполняется();
	
	Возврат РезультатОперации;
	
КонецФункции

// Чтение ответа операции /isl/authorized/orders/{orderId}/activate.
//
Функция ПрочитатьДанные_activate(ТелоJSON)
	
	// Ответ сервиса:
	//
	//  orderPageUrl - URL онлайн-заказа.
	//  amount - сумма онлайн-заказа.
	//  currency - валюта онлайн-заказа.
	//
	//  {
	//    "orderPageUrl":"https://orders-stage.1c.ru/pm/ui/orders/3c0d406e-119a-401a-ade9-bbf1d32df4d6",
	//    "amount":150,
	//    "currency":"RUB",
	//    "orderDataChecksumVersion":"1",
	//    "orderDataChecksumValue":"311D52DDCE19C4F5DB736601B78C0E0AD5CE8131BB0D37F34078A70FE02EC3AE"
	//  }
	
	Попытка
		
		ЧтениеОтвета = Новый ЧтениеJSON;
		ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
		ДанныеСервиса = ПрочитатьJSON(ЧтениеОтвета);
		
		ДанныеЗаказа = Новый Структура;
		ДанныеЗаказа.Вставить("URLЗаказа",              ДанныеСервиса.orderPageUrl);
		ДанныеЗаказа.Вставить("СуммаЗаказа",            ДанныеСервиса.amount);
		ДанныеЗаказа.Вставить("КонтрольнаяСумма",       ДанныеСервиса.orderDataChecksumValue);
		ДанныеЗаказа.Вставить("ВерсияКонтрольнойСуммы", ДанныеСервиса.orderDataChecksumVersion);
		
	Исключение
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		
		ВызватьИсключение НСтр("ru = 'Не удалось обработать ответ. Подробности в журнале регистрации.';
								|en = 'Cannot process the response. For more information, see the event log.'");
		
	КонецПопытки;
	
	Возврат ДанныеЗаказа;
	
КонецФункции

#КонецОбласти

#Область ДеактивацияОнлайнЗаказа

// См. ОнлайнЗаказыСервис.ДеактивироватьЗаказ.
//
Функция ОперацияДеактивацияЗаказа(ИдентификаторЗаказа)
	
	РезультатОперации = ОнлайнЗаказыСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("СтатусЗаказа", "");
	РезультатОперации.Вставить("СтатусЗаказаВСервисе",   "");
	
	URLОперации = ОнлайнЗаказыСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/orders/%1/deactivate",
			ИдентификаторЗаказа));
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ПараметрыПодключения = ОнлайнЗаказыСлужебный.ИнициализироватьПараметрыПодключения();
	
	Заголовки = НовыйЗаголовкиВызоваОперации();
	Заголовки.Вставить("Authorization", СтрокаАутентификации(РезультатИПП.ДанныеАутентификации));
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ОнлайнЗаказыСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОтправки.КодСостояния,
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		Если РезультатОперации.КодОшибки = ОнлайнЗаказыКлиентСервер.КодОшибкиНедопустимыйСтатус() Тогда
			РезультатОперации.СтатусЗаказаВСервисе = ОнлайнЗаказыСлужебный.ОпределитьСтатусЗаказаВСервисе(
				РезультатОтправки.Содержимое);
		КонецЕсли;
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить деактивацию онлайн-заказа.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При активации онлайн-заказа возникли ошибки.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4';
				|en = 'Cannot deactivate the online order.
				|
				|%1
				|
				|View error details:
				|Errors occurred when activating the online order.
				|URL: %2
				|Error code: %3
				|Detailed information:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(Строка(РезультатОперации.ИнформацияОбОшибке));
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	РезультатОперации.Вставить("СтатусЗаказа", ОнлайнЗаказыКлиентСервер.СтатусЗаказаНеАктивен());
	
	Возврат РезультатОперации;
	
КонецФункции

#КонецОбласти

#Область УстановитьОплатуЗаказа

// См. ОнлайнЗаказыСервис.УстановитьСтатусОплачен.
//
Функция ОперацияУстановитьСтатусОплачен(ДанныеЗаказа, ПараметрыСсылки, СпособОплаты)
	
	РезультатОперации = ОнлайнЗаказыСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("СуммаЗаказа",  0);
	РезультатОперации.Вставить("СтатусЗаказа", "");
	РезультатОперации.Вставить("ХешАктуален", Истина);
	
	РезультатДанныеАутентификации = ДанныеАутентификацииНастройкиПодключения(
		ПараметрыСсылки.НастройкаПодключения);
	
	Если РезультатДанныеАутентификации.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатДанныеАутентификации.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатДанныеАутентификации.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	URLОперации = ОнлайнЗаказыСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/orders/%1/set-completed-status",
			ПараметрыСсылки.ИдентификаторЗаказа));
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ПараметрыПодключения = ОнлайнЗаказыСлужебный.ИнициализироватьПараметрыПодключения();
	
	Заголовки = НовыйЗаголовкиВызоваОперации();
	Заголовки.Вставить("Authorization", СтрокаАутентификации(РезультатИПП.ДанныеАутентификации));
	
	ПараметрыЗапросаJSON = set_completed_status(
		РезультатДанныеАутентификации,
		ДанныеЗаказа.КонтрольнаяСумма,
		СпособОплаты);
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ОнлайнЗаказыСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОтправки.КодСостояния,
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
		
		Если РезультатОперации.КодОшибки = ОнлайнЗаказыКлиентСервер.КодОшибкиНедопустимыйСтатус() Тогда
			РезультатОперации.СтатусЗаказа = ОнлайнЗаказыСлужебный.ОпределитьСтатусЗаказаВСервисе(
				РезультатОтправки.Содержимое);
		КонецЕсли;
		
		Если РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйФорматЗапроса() Тогда
			РезультатОперации.ХешАктуален = ОпределитьАктуальностьХеша(РезультатОтправки.Содержимое);
		КонецЕсли;
		
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось установить статус оплаты онлайн-заказа.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При активации онлайн-заказа возникли ошибки.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4';
				|en = 'Cannot set a payment status of the online order.
				|
				|%1
				|
				|View error details:
				|Errors occurred when activating the online order.
				|URL: %2
				|Error code: %3
				|Detailed information:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(Строка(РезультатОперации.ИнформацияОбОшибке));
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	РезультатУстановкиСтатуса = ПрочитатьДанные_set_completed_status(РезультатОтправки.Содержимое);
	
	ЗаполнитьЗначенияСвойств(РезультатОперации, РезультатУстановкиСтатуса);
	РезультатОперации.СтатусЗаказа = ОнлайнЗаказыСлужебный.СтатусЗаказаОплачен();
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для операции /isl/authorized/orders/{orderId}/set-completed-status.
//
Функция set_completed_status(
		ДанныеАутентификации,
		ДанныеКонтрольнойСуммыЗаказа,
		СпособОплаты)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("expectedSettingChecksumValue");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеАутентификации.КонтрольнаяСумма);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("expectedOrderDataChecksumValue");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеКонтрольнойСуммыЗаказа.Значение);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("expectedOrderDataChecksumVersion");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеКонтрольнойСуммыЗаказа.ВерсияРасчета);
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("paymentMethod");
	ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторСпособаОплаты(СпособОплаты));
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции /isl/authorized/orders/{orderId}//set-completed-status.
//
Функция ПрочитатьДанные_set_completed_status(ТелоJSON)
	
	// Ответ сервиса:
	//
	//  orderPageUrl - URL онлайн-заказа.
	//  amount - сумма онлайн-заказа.
	//  currency - валюта онлайн-заказа.
	//
	//  {
	//    "orderPageUrl":"https://orders-stage.1c.ru/pm/ui/orders/3c0d406e-119a-401a-ade9-bbf1d32df4d6",
	//    "amount":150,
	//    "currency":"RUB",
	//    "orderDataChecksumVersion":"1",
	//    "orderDataChecksumValue":"311D52DDCE19C4F5DB736601B78C0E0AD5CE8131BB0D37F34078A70FE02EC3AE"
	//  }
	
	Попытка
		
		ЧтениеОтвета = Новый ЧтениеJSON;
		ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
		ДанныеСервиса = ПрочитатьJSON(ЧтениеОтвета);
		
		ДанныеЗаказа = Новый Структура;
		ДанныеЗаказа.Вставить("URLЗаказа",              ДанныеСервиса.orderPageUrl);
		ДанныеЗаказа.Вставить("СуммаЗаказа",            ДанныеСервиса.amount);
		ДанныеЗаказа.Вставить("КонтрольнаяСумма",       ДанныеСервиса.orderDataChecksumValue);
		ДанныеЗаказа.Вставить("ВерсияКонтрольнойСуммы", ДанныеСервиса.orderDataChecksumVersion);
		
	Исключение
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		
		ВызватьИсключение НСтр("ru = 'Не удалось обработать ответ. Подробности в журнале регистрации.';
								|en = 'Cannot process the response. For more information, see the event log.'");
		
	КонецПопытки;
	
	Возврат ДанныеЗаказа;
	
КонецФункции

// Определяет идентификатор способа оплаты для сервиса.
//
// Параметры:
//  СпособОплаты - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - способ оплаты онлайн-заказа.
//
// Возвращаемое значение:
//  Строка - код ошибки.
//
Функция ИдентификаторСпособаОплаты(СпособОплаты)
	
	Если СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Наличные Тогда
		Возврат "CASH";
	ИначеЕсли СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.ПоРеквизитам Тогда
		Возврат "REQUISITES";
	ИначеЕсли СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг Тогда
		Возврат "ACQUIRING";
	ИначеЕсли СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Прочее Тогда
		Возврат "OTHER";
	Иначе
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Способ оплаты %1 не обрабатывается сервисом.';
				|en = 'The %1 payment method is not processed by the service.'"),
			СпособОплаты);
	КонецЕсли
	
КонецФункции

// Производит чтение статуса заказа в сервисе в случае ошибки ILLEGAL_ORDER_STATUS из тела ответа.
//
// Параметры:
//  ТелоJSON - Строка - тело ответа сервиса.
//
// Возвращаемое значение:
//  Строка - код ошибки сервиса.
//
Функция ОпределитьАктуальностьХеша(ТелоJSON)
	
	// Ответ сервиса:
	//
	//  {
	//  "type": "string",
	//  "title": "string",
	//  "status": "string",
	//  "detail": "string",
	//  "instance": "string"
	// }
	
	// Определение ошибки выполняется через попытку, т.к. в случае ошибки сервиса
	// есть вероятность получить не формализованное сообщение.
	Попытка
		ЧтениеОтвета = Новый ЧтениеJSON;
		ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
		Результат = ПрочитатьJSON(ЧтениеОтвета);
		Если СокрЛП(Результат.detail) = "Контрольная сумма заказа не совпадает" Тогда
			Возврат Ложь;
		Иначе
			Возврат Истина;
		КонецЕсли;
	Исключение
		Возврат Истина;
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#Область ПодготовкаВложенийКОтправке

// См. ОнлайнЗаказыСервис.ОтправкаВложений
//
Функция ОперацияПодготовкаВложенийКОтправке(ПараметрыОперации)
	
	РезультатОперации = ОнлайнЗаказыСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("ВложенияКЗагрузке", Неопределено);
	
	URLОперации = ОнлайнЗаказыСлужебный.URLОперацииСервиса(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"/orders/%1/attachment-file-list",
			ПараметрыОперации.ИдентификаторЗаказа));
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ПараметрыПодключения = ОнлайнЗаказыСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = update_attachment_file_list(ПараметрыОперации.ДанныеВложений);
	
	Заголовки = НовыйЗаголовкиВызоваОперации();
	Заголовки.Вставить("Authorization", СтрокаАутентификации(РезультатИПП.ДанныеАутентификации));
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "PUT");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ОнлайнЗаказыСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОтправки.КодСостояния,
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
			
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось подготовить вложения онлайн-заказа.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При подготовке вложений онлайн-заказа возникли ошибки.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4';
				|en = 'Cannot prepare the online order attachments.
				|
				|%1
				|
				|View error details:
				|Errors occurred when preparing the online order attachments.
				|URL: %2
				|Error code: %3
				|Detailed information:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(Строка(РезультатОперации.ИнформацияОбОшибке));
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	РезультатОперации.ВложенияКЗагрузке = ПрочитатьДанные_update_attachment_file_list(РезультатОтправки.Содержимое);
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для операции /isl/authorized/orders/{orderId}/activate.
//
Функция update_attachment_file_list(ДанныеВложений)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("files");
	
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	Для Каждого ДанныеВложения Из ДанныеВложений Цикл
		
		ДанныеФайла = ДанныеВложения.Значение;
		
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства(ДанныеФайла.Идентификатор);
		
		ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
		
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("title");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеФайла.Представление);
		
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("fileName");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеФайла.ИмяФайла);
		
		ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("checksum");
		ЗаписьДанныхСообщения.ЗаписатьЗначение(ДанныеФайла.ХешСумма);
		
		ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
		
	КонецЦикла;
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции /isl/authorized/page-settings/{settingId}.
//
Функция ПрочитатьДанные_update_attachment_file_list(ТелоJSON)
	
	// Ответ сервиса:
	//
	//  filesToUpload - перечень файлов к загрузке.
	//    id - идентификатор файла.
	//    uploadUrl - URL для загрузки файла.
	//
	//  {
	//    "filesToUpload": [
	//      {
	//        "id": "string",
	//        "uploadUrl": "string"
	//      }
	//    ]
	//  }
	
	Попытка
		
		ЧтениеОтвета = Новый ЧтениеJSON;
		ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
		ДанныеСервиса = ПрочитатьJSON(ЧтениеОтвета);
		
		ФайлыКЗагрузке = Новый Массив;
		
		Для Каждого ДанныеФайла Из ДанныеСервиса.filesToUpload Цикл
			
			ФайлКЗагрузке = Новый Структура;
			ФайлКЗагрузке.Вставить("Идентификатор", ДанныеФайла.id);
			ФайлКЗагрузке.Вставить("URLЗагрузки",   ДанныеФайла.uploadUrl);
			
			ФайлыКЗагрузке.Добавить(ФайлКЗагрузке);
			
		КонецЦикла;
		
	Исключение
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		
		ВызватьИсключение НСтр("ru = 'Не удалось обработать ответ. Подробности в журнале регистрации.';
								|en = 'Cannot process the response. For more information, see the event log.'");
		
	КонецПопытки;
	
	Возврат ФайлыКЗагрузке;
	
КонецФункции

#КонецОбласти

#Область ОтправкаВложений

// Выполняет выгрузку вложений онлайн-заказа в сервис.
//
// Параметры:
//  ДанныеВложений - Соответствие - см. ОнлайнЗаказыСлужебный.ПодготовитьДанныеВложений;
//  ВложенияКЗагрузке - Структура - содержит описание вложений к загрузке:
//    * Идентификатор - Строка - Идентификатор вложения.
//    * URLЗагрузки - Строка - URL для загрузки вложения.
//
Функция ОперацияПередачаВложений(ДанныеВложений, ВложенияКЗагрузке)
	
	РезультатОперации = ОнлайнЗаказыСлужебный.НовыйРезультатОперации();
	ПараметрыПодключения = ОнлайнЗаказыСлужебный.ИнициализироватьПараметрыПодключения();
	
	Для Каждого ВложениеКЗагрузке Из ВложенияКЗагрузке Цикл
		
		ДанныеВложения = ДанныеВложений.Получить(ВложениеКЗагрузке.Идентификатор);
		
		Если ДанныеВложения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДвоичныеДанныеВложения = ПолучитьИзВременногоХранилища(ДанныеВложения.АдресВоВременномХранилище);
		
		РезультатИПП = ДанныеАутентификации(ВложениеКЗагрузке.URLЗагрузки);
		
		Если РезультатИПП.Ошибка Тогда
			РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
			РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
			РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
			Возврат РезультатОперации;
		КонецЕсли;
		
		Заголовки = НовыйЗаголовкиВызоваОперации();
		Заголовки.Вставить("Authorization",   СтрокаАутентификации(РезультатИПП.ДанныеАутентификации));
		Заголовки.Вставить("X-File-Checksum", ДанныеВложения.ХешСумма);
		Заголовки.Вставить("Content-Type",    "application/octet-stream");
		
		ПараметрыОтправки = Новый Структура;
		ПараметрыОтправки.Вставить("Метод"                   , "PUT");
		ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
		ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
		ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ДвоичныеДанныеВложения);
		ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 2);
		ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
		ПараметрыОтправки.Вставить("Таймаут"                 , 30);
		
		// Вызов операции сервиса.
		РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
			ВложениеКЗагрузке.URLЗагрузки,
			,
			,
			ПараметрыОтправки);
			
		Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
			РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.ПереопределитьКодОшибкиСервиса(
				РезультатОтправки.КодСостояния);
			РезультатОперации.СообщениеОбОшибке = ОнлайнЗаказыСлужебный.ПереопределитьСообщениеПользователю(
				РезультатОтправки.КодСостояния,
				РезультатОперации.КодОшибки,
				РезультатОтправки.Содержимое);
				
			РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось выполнить передачу вложений онлайн-заказа.
					|
					|%1
					|
					|Техническая информация об ошибке:
					|При передаче вложения онлайн-заказа возникли ошибки.
					|URL: %2
					|Код ошибки: %3
					|Подробная информация:
					|%4';
					|en = 'Cannot transfer the online order attachments.
					|
					|%1
					|
					|View error details:
					|Errors occurred when transferring the online order attachments.
					|URL: %2
					|Error code: %3
					|Detailed information:
					|%4'"),
				РезультатОперации.СообщениеОбОшибке,
				ВложениеКЗагрузке.URLЗагрузки,
				РезультатОтправки.КодОшибки,
				РезультатОтправки.ИнформацияОбОшибке);
			
			ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
				Строка(РезультатОперации.ИнформацияОбОшибке));
		
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатОперации;
	
КонецФункции

#КонецОбласти

#Область ВызовОперацииИнформацияПоЗаказам

// См. ОнлайнЗаказыСервис.ПолучениеИнформацииПоЗаказам.
//
Функция ОперацияПолученияИнформацииПоЗаказам(ПараметрыОперации)
	
	РезультатОперации = ОнлайнЗаказыСлужебный.НовыйРезультатОперации();
	РезультатОперации.Вставить("ДанныеЗаказов", Новый Массив);
	
	РезультатДанныеАутентификации = ДанныеАутентификацииНастройкиПодключения(
		ПараметрыОперации.НастройкаПодключения,
		Истина);
	
	Если РезультатДанныеАутентификации.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатДанныеАутентификации.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатДанныеАутентификации.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	URLОперации = ОнлайнЗаказыСлужебный.URLОперацииСервиса(
		"/orders/get-order-infos");
	
	РезультатИПП = ДанныеАутентификации(URLОперации);
	
	Если РезультатИПП.Ошибка Тогда
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиНеверныйЛогинИлиПароль();
		РезультатОперации.СообщениеОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		РезультатОперации.ИнформацияОбОшибке = РезультатИПП.ИнформацияОбОшибке;
		Возврат РезультатОперации;
	КонецЕсли;
	
	ПараметрыПодключения = ОнлайнЗаказыСлужебный.ИнициализироватьПараметрыПодключения();
	
	ПараметрыЗапросаJSON = order_infos(ПараметрыОперации.ИдентификаторыЗаказов);
	
	Заголовки = НовыйЗаголовкиВызоваОперации();
	Заголовки.Вставить("Authorization", СтрокаАутентификации(РезультатИПП.ДанныеАутентификации));
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Метод"                   , "POST");
	ПараметрыОтправки.Вставить("ФорматОтвета"            , 1);
	ПараметрыОтправки.Вставить("Заголовки"               , Заголовки);
	ПараметрыОтправки.Вставить("ДанныеДляОбработки"      , ПараметрыЗапросаJSON);
	ПараметрыОтправки.Вставить("ФорматДанныхДляОбработки", 1);
	ПараметрыОтправки.Вставить("НастройкиПрокси"         , ПараметрыПодключения.НастройкиПроксиСервера);
	ПараметрыОтправки.Вставить("Таймаут"                 , 30);
	
	// Вызов операции сервиса.
	РезультатОтправки = ИнтернетПоддержкаПользователей.ЗагрузитьСодержимоеИзИнтернет(
		URLОперации,
		,
		,
		ПараметрыОтправки);
	
	Если Не ПустаяСтрока(РезультатОтправки.КодОшибки) Тогда
		
		РезультатОперации.КодОшибки = ОнлайнЗаказыСлужебный.ПереопределитьКодОшибкиСервиса(
			РезультатОтправки.КодСостояния);
		РезультатОперации.СообщениеОбОшибке = ОнлайнЗаказыСлужебный.ПереопределитьСообщениеПользователю(
			РезультатОтправки.КодСостояния,
			РезультатОперации.КодОшибки,
			РезультатОтправки.Содержимое);
			
		РезультатОперации.ИнформацияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить данные заказов.
				|
				|%1
				|
				|Техническая информация об ошибке:
				|При получении данных заказов возникли ошибки.
				|URL: %2
				|Код ошибки: %3
				|Подробная информация:
				|%4';
				|en = 'Cannot get the order data.
				|
				|%1
				|
				|View error details:
				|Errors occurred when getting the order data.
				|URL: %2
				|Error code: %3
				|Detailed information:
				|%4'"),
			РезультатОперации.СообщениеОбОшибке,
			URLОперации,
			РезультатОтправки.КодОшибки,
			РезультатОтправки.ИнформацияОбОшибке);
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(Строка(РезультатОперации.ИнформацияОбОшибке));
		
		Возврат РезультатОперации;
		
	КонецЕсли;
	
	РезультатОперации.ДанныеЗаказов = ПрочитатьДанные_order_infos(
		РезультатОтправки.Содержимое,
		ПараметрыОперации.НастройкаПодключения);
	
	Возврат РезультатОперации;
	
КонецФункции

// Формирует параметры запроса для операции /isl/authorized/orders/get-order-infos.
//
Функция order_infos(ИдентификаторыЗаказов)
	
	ЗаписьДанныхСообщения = Новый ЗаписьJSON;
	ЗаписьДанныхСообщения.УстановитьСтроку();
	ЗаписьДанныхСообщения.ЗаписатьНачалоОбъекта();
	
	ЗаписьДанныхСообщения.ЗаписатьИмяСвойства("orderIds");
	
	ЗаписьДанныхСообщения.ЗаписатьНачалоМассива();
	
	Для Каждого ИдентификаторЗаказа Из ИдентификаторыЗаказов Цикл
		ЗаписьДанныхСообщения.ЗаписатьЗначение(ИдентификаторЗаказа);
	КонецЦикла;
	
	ЗаписьДанныхСообщения.ЗаписатьКонецМассива();
	
	ЗаписьДанныхСообщения.ЗаписатьКонецОбъекта();
	
	Возврат ЗаписьДанныхСообщения.Закрыть();
	
КонецФункции

// Чтение ответа операции /isl/authorized/orders/get-order-infos.
//
Функция ПрочитатьДанные_order_infos(ТелоJSON, НастройкаПодключения)
	
	// Ответ сервиса:
	//
	//  orders - данные заказов в сервисе.
	//    id - идентификатор заказа.
	//    status - статус заказа.
	//    paymentType - тип оплаты.
	//    amount - сумма оплаты.
	//    orderDataChecksumVersion -  версия расчета контрольной суммы заказа.
	//    orderDataChecksumValue -  контрольная сумма заказа.
	//    sbpPayment - данные оплаты СБП.
	//      qrc - идентификатор qrc в СБП.
	//      paymentId - идентификатор операции в СБП.
	//      operationDate - дата операции в системе СБП.
	//      sbpOrderId - идентификатор оплаты в СБП.
	//      merchantId - идентификатор ТСП в СБП.
	//      paymentPurpose - назначение платежа.
	//
	//  {
	//    "orders": [
	//      {
	//      "id": "string",
	//      "status": "string",
	//      "paymentType": "string",
	//      "amount": 0,
	//      "sbpPayment": {
	//        "qrc": "string",
	//        "paymentId": "string",
	//        "operationDate": "string",
	//        "sbpOrderId": "string",
	//        "merchantId": "string",
	//        "paymentPurpose": "string",
	//        "b2bAccount": "string",
	//        "b2bTakeTax": true,
	//        "b2bTotalTaxAmount": 0,
	//        "b2bUip": "string"
	//      },
	//      "acquiringPayment": {
	//        "operationId": "string",
	//        "acquiringApiProviderId": "string",
	//        "merchantId": "string",
	//        "paymentPurpose": "string"
	//      },
	//      "orderDataChecksumVersion": "string",
	//      "orderDataChecksumValue": "string"
	//      }
	//    ]
	//  }
	
	Попытка
		
		СвойстваСоЗначениемДата = Новый Массив;
		СвойстваСоЗначениемДата.Добавить("operationDate");
		
		ЧтениеОтвета = Новый ЧтениеJSON;
		ЧтениеОтвета.УстановитьСтроку(ТелоJSON);
		ДанныеСервиса = ПрочитатьJSON(
			ЧтениеОтвета,
			,
			,
			,
			"ВосстановитьДатуJSON",
			ОнлайнЗаказыСлужебный,
			,
			СвойстваСоЗначениемДата);
		
		ДанныеЗаказов = Новый Массив;
		
		Для Каждого ДанныеЗаказаВСервисе Из ДанныеСервиса.orders Цикл
			
			ОплатаСБП = Неопределено;
			ОплатаИнтернетЭквайринг = Неопределено;
			ПараметрыФискализации = Неопределено;
			ДанныеЗаказа = Новый Структура;
			
			Если ДанныеЗаказаВСервисе.Свойство("sbpPayment")
				И ЗначениеЗаполнено(ДанныеЗаказаВСервисе.sbpPayment) Тогда
				
				ОплатаСБП = Новый Структура;
				ОплатаСБП.Вставить("ИдентификаторОплаты",           ДанныеЗаказаВСервисе.sbpPayment.qrc);
				ОплатаСБП.Вставить("ИдентификаторПлатежнойСистемы", ДанныеЗаказаВСервисе.sbpPayment.paymentId);
				ОплатаСБП.Вставить("ДатаОперации",                  ДатаВUTC(ДанныеЗаказаВСервисе.sbpPayment.operationDate));
				ОплатаСБП.Вставить("ИдентификаторМерчанта",         ДанныеЗаказаВСервисе.sbpPayment.merchantId);
				ОплатаСБП.Вставить("НазначениеПлатежа",             ДанныеЗаказаВСервисе.sbpPayment.paymentPurpose);
				ОплатаСБП.Вставить(
					"Идентификатор",
					Новый УникальныйИдентификатор(ДанныеЗаказаВСервисе.sbpPayment.sbpOrderId));
					
				Если ДанныеЗаказаВСервисе.sbpPayment.paymentModel = ОнлайнЗаказыСлужебный.ИдентификаторВариантаСБПB2B() Тогда
					ОплатаСБП.Вставить("РасчетныйСчет",        ДанныеЗаказаВСервисе.sbpPayment.b2bAccount);
					ОплатаСБП.Вставить("ИдентификаторПлатежа", ДанныеЗаказаВСервисе.sbpPayment.b2bUip);
					ОплатаСБП.Вставить("СуммаНДС",             ДанныеЗаказаВСервисе.sbpPayment.b2bTotalTaxAmount);
					ОплатаСБП.Вставить("ОблагаетсяНДС",        ДанныеЗаказаВСервисе.sbpPayment.b2bTakeTax);
				КонецЕсли;
				
			КонецЕсли;
			
			Если ДанныеЗаказаВСервисе.Свойство("acquiringPayment")
				И ЗначениеЗаполнено(ДанныеЗаказаВСервисе.acquiringPayment) Тогда
				
				Эталон = Новый Структура;
				Эталон.Вставить("operationId",            "");
				Эталон.Вставить("acquiringApiProviderId", "");
				Эталон.Вставить("merchantId",             "");
				Эталон.Вставить("paymentPurpose",         "");
				Эталон.Вставить("operationDate",          "");
				Эталон.Вставить("paymentLinkId",          "");
				ЗаполнитьЗначенияСвойств(Эталон, ДанныеЗаказаВСервисе.acquiringPayment);
				
				Если Не ЗначениеЗаполнено(Эталон.paymentLinkId) Тогда
					Эталон.paymentLinkId = Эталон.operationId;
				КонецЕсли;
				
				ОплатаИнтернетЭквайринг = Новый Структура;
				ОплатаИнтернетЭквайринг.Вставить("ИдентификаторОплаты",    Эталон.paymentLinkId);
				ОплатаИнтернетЭквайринг.Вставить("ИдентификаторОперации",  Эталон.operationId);
				ОплатаИнтернетЭквайринг.Вставить("ИдентификаторУчастника", Эталон.acquiringApiProviderId);
				ОплатаИнтернетЭквайринг.Вставить("ИдентификаторМерчанта",  Эталон.merchantId);
				ОплатаИнтернетЭквайринг.Вставить("Идентификатор",          Новый УникальныйИдентификатор());
				ОплатаИнтернетЭквайринг.Вставить("НазначениеПлатежа",      Эталон.paymentPurpose);
				ОплатаИнтернетЭквайринг.Вставить("ДатаОперации",           ДатаВUTC(Эталон.operationDate));
				
			КонецЕсли;
			
			Если ДанныеЗаказаВСервисе.Свойство("fiscalization")
				И ЗначениеЗаполнено(ДанныеЗаказаВСервисе.fiscalization) Тогда
				
				ПараметрыФискализации = Новый Структура;
				ПараметрыФискализации.Вставить("НастройкаПодключения", НастройкаПодключения);
				ПараметрыФискализации.Вставить("ИдентификаторОперации", ДанныеЗаказаВСервисе.fiscalization.receiptOperationId);
				ПараметрыФискализации.Вставить(
					"СтатусОперации",
					СтатусФискализацииПоДаннымСервиса(ДанныеЗаказаВСервисе.fiscalization.status));
				ПараметрыФискализации.Вставить("ДанныеОперации", ДанныеЗаказаВСервисе.fiscalization.providerResponse);
				ПараметрыФискализации.Вставить("ОписаниеСтатусаОперации", ДанныеЗаказаВСервисе.fiscalization.comment);
			КонецЕсли;
			
			ДанныеЗаказа = Новый Структура;
			
			ДанныеЗаказа.Вставить("ИдентификаторЗаказа",     ДанныеЗаказаВСервисе.id);
			ДанныеЗаказа.Вставить("Статус",                  СтатусЗаказаПоДаннымСервиса(ДанныеЗаказаВСервисе.status));
			ДанныеЗаказа.Вставить("СуммаЗаказа",             ДанныеЗаказаВСервисе.amount);
			ДанныеЗаказа.Вставить("КонтрольнаяСумма",        ДанныеЗаказаВСервисе.orderDataChecksumValue);
			ДанныеЗаказа.Вставить("ВерсияКонтрольнойСуммы",  ДанныеЗаказаВСервисе.orderDataChecksumVersion);
			ДанныеЗаказа.Вставить("ОплатаСБП",               ОплатаСБП);
			ДанныеЗаказа.Вставить("СпособОплаты",            СпособОплаты(ДанныеЗаказаВСервисе));
			ДанныеЗаказа.Вставить("ОплатаИнтернетЭквайринг", ОплатаИнтернетЭквайринг);
			ДанныеЗаказа.Вставить("ПараметрыФискализации",   ПараметрыФискализации);
			
			ДанныеЗаказов.Добавить(ДанныеЗаказа);
			
		КонецЦикла;
		
	Исключение
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			Истина);
		
		ВызватьИсключение НСтр("ru = 'Не удалось обработать ответ. Подробности в журнале регистрации.';
								|en = 'Cannot process the response. For more information, see the event log.'");
		
	КонецПопытки;
	
	Возврат ДанныеЗаказов;
	
КонецФункции

#КонецОбласти

#Область Аутентификация

// Возвращает логин и пароль Интернет-поддержки или тикет аутентификации.
//
// Параметры:
//  URLОперации -Строка - URL операции, для которой получаются данные аутентификации.
//
// Возвращаемое значение:
//  Структура - структура, содержащая результаты определения параметров
//    аутентификации пользователя Интернет-поддержки:
//    *ДанныеАутентификации - Структура - параметры аутентификации пользователя Интернет-поддержки;
//    *ИнформацияОбОшибке   - Строка    - информация об ошибке для пользователя.
//    *Ошибка - Строка    - признак наличия ошибки.
//
Функция ДанныеАутентификации(URLОперации)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Структура;
	Результат.Вставить("ДанныеАутентификации", Новый Структура);
	Результат.Вставить("ИнформацияОбОшибке",   "");
	Результат.Вставить("Ошибка",               Ложь);
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		
		МодульИнтернетПоддержкаПользователейВМоделиСервиса =
			ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователейВМоделиСервиса");
		РезультатПолученияТикета =
			МодульИнтернетПоддержкаПользователейВМоделиСервиса.ТикетАутентификацииНаПорталеПоддержки(
				URLОперации);
		
		Если ПустаяСтрока(РезультатПолученияТикета.КодОшибки) Тогда
			Результат.ДанныеАутентификации.Вставить("Тикет", РезультатПолученияТикета.Тикет);
		Иначе
			Результат.Ошибка = Истина;
			Результат.ИнформацияОбОшибке =
				НСтр("ru = 'Ошибка аутентификации в сервисе.
					|Подробнее см. в журнале регистрации.';
					|en = 'Authentication error.
					|See the Event Log for details.'");
			ПодробнаяИнформацияОбОшибке =
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось вызвать операцию %1.
						|Не удалось выполнить аутентификацию.
						|%2';
						|en = 'Cannot call operation %1.
						|Cannot authenticate.
						|%2'"),
					URLОперации,
					РезультатПолученияТикета.ИнформацияОбОшибке);
			ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
				ПодробнаяИнформацияОбОшибке,
				Истина);
		КонецЕсли;
	Иначе
		
		Результат.ДанныеАутентификации = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
		
		Если Результат.ДанныеАутентификации = Неопределено Тогда
			Результат.Ошибка             = Истина;
			Результат.ИнформацияОбОшибке =
				НСтр("ru = 'Для работы с сервисом необходимо подключить Интернет-поддержку пользователей.';
					|en = 'To use the service, enable online user support.'");
			ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
				Результат.ИнформацияОбОшибке);
		Иначе
			Результат.ДанныеАутентификации.Вставить("Тикет", Неопределено);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует строку аутентификации из данных аутентификации
//
// Параметры:
//  ДанныеАутентификации  - Структура, Неопределено - данные аутентификации ИПП.
//
// Возвращаемое значение:
//  Строка - результат преобразования.
//
Функция СтрокаАутентификации(ДанныеАутентификации)
	
	Если ДанныеАутентификации = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеАутентификации.Тикет) Тогда
		Результат = ИнтернетПоддержкаПользователей.ЗаголовокBearerАутентификации(ДанныеАутентификации.Тикет);
	Иначе
		Результат = ИнтернетПоддержкаПользователей.ЗаголовокБазовойСхемыАутентификации(
			ДанныеАутентификации.Логин,
			ДанныеАутентификации.Пароль);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает данные аутентификации настройки онлайн-заказов.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам -
//    настройка онлайн-заказов.
//  ЭтоСозданиеНастройки - Булево - Признак операции создания настройки.
//
// Возвращаемое значение:
//  Структура - структура, содержащая результаты определения параметров
//    аутентификации онлайн-заказов:
//    *Идентификатор - Строка, Неопределено - идентификатор настройки онлайн-заказов.
//       Если Неопределено, значение настроек не обнаружено;
//    *ИнформацияОбОшибке - Строка - информация об ошибке для пользователя.
//    *КонтрольнаяСумма - Строка - контрольная сумма настройки интеграции.
//    *Ошибка - Строка - признак наличия ошибки.
//
Функция ДанныеАутентификацииНастройкиПодключения(
		НастройкаПодключения,
		ЭтоСозданиеНастройки = Ложь)
	
	Результат = Новый Структура;
	Результат.Вставить("Идентификатор",      Неопределено);
	Результат.Вставить("КонтрольнаяСумма",   Неопределено);
	Результат.Вставить("КодОшибки",          "");
	Результат.Вставить("ИнформацияОбОшибке", "");
	Результат.Вставить("Ошибка",             Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеХранилища = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		НастройкаПодключения);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДанныеХранилища <> Неопределено Тогда
		Результат.Идентификатор = ДанныеХранилища.Идентификатор;
		Результат.КонтрольнаяСумма = ДанныеХранилища.КонтрольнаяСумма;
		Возврат Результат;
	КонецЕсли;
	
	Если Не ЭтоСозданиеНастройки Тогда
		
		Результат.Ошибка = Истина;
		Результат.ИнформацияОбОшибке =
			НСтр("ru = 'Ошибка данных аутентификации.';
				|en = 'Authentication credentials error.'");
		Результат.КодОшибки = ОнлайнЗаказыСлужебный.КодОшибкиДанныеАутентификацииНеЗаполнены();
			
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			Результат.ИнформацияОбОшибке);
		
	Иначе
		
		ДанныеХранилища = НовыйОписаниеДанныхАутентификации();
		ДанныеХранилища.Идентификатор    = СокрЛП(Новый УникальныйИдентификатор);
		ДанныеХранилища.КонтрольнаяСумма = "";
		
		УстановитьПривилегированныйРежим(Истина);
		
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
			НастройкаПодключения,
			ДанныеХранилища);
		
		УстановитьПривилегированныйРежим(Ложь);
		
		Результат.Идентификатор = ДанныеХранилища.Идентификатор;
		Результат.КонтрольнаяСумма = ДанныеХранилища.КонтрольнаяСумма;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Выполняет обновление хеша настройки в безопасном хранилище.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам -
//    настройка онлайн-заказов.
//  ХешНастройки - Строка - Строка содержащая хеш настройки рассчитанный сервисом.
//
// Возвращаемое значение:
//  Структура - структура, содержащая результаты определения параметров
//    аутентификации онлайн-заказов:
//    *ИнформацияОбОшибке   - Строка    - информация об ошибке для пользователя.
//    *Ошибка - Строка    - признак наличия ошибки.
//
Функция ОбновитьХешНастройки(НастройкаПодключения, ХешНастройки)
	
	Результат = Новый Структура;
	Результат.Вставить("ИнформацияОбОшибке", "");
	Результат.Вставить("Ошибка",             Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеХранилища = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
		НастройкаПодключения);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ДанныеХранилища = Неопределено Тогда
		Результат.Ошибка = Истина;
		Результат.ИнформацияОбОшибке =
			НСтр("ru = 'Ошибка обновления хеша настройки.';
				|en = 'Error updating the hash of the setting.'");
	КонецЕсли;
	
	ДанныеХранилища.КонтрольнаяСумма = ХешНастройки;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
		НастройкаПодключения,
		ДанныеХранилища);
		
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

// Формирует новое описание данных аутентификации.
//
// Возвращаемое значение:
//  Структура - содержит описание параметров онлайн-заказа:
//    * Идентификатор - Строка - уникальный идентификатор настройки.
//    * Хеш - Строка - Хеш настройки рассчитанный сервисом.
//
Функция НовыйОписаниеДанныхАутентификации()
	
	ДанныеАутентификации = Новый Структура;
	ДанныеАутентификации.Вставить("Идентификатор",    "");
	ДанныеАутентификации.Вставить("КонтрольнаяСумма", "");
	
	Возврат ДанныеАутентификации;
	
КонецФункции

#КонецОбласти

#Область Прочие

// Формирует стандартные заголовки для вызова операции.
//
// Возвращаемое значение:
//  Соответствие - заголовки операции.
//
Функция НовыйЗаголовкиВызоваОперации() Экспорт
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type",            "application/json");
	Заголовки.Вставить("X-Correlation-ID",        Строка(Новый УникальныйИдентификатор));
	Заголовки.Вставить("X-Program-Nick",          ИнтернетПоддержкаПользователей.ИмяПрограммы());
	
	Возврат Заголовки;
	
КонецФункции

// Выполняет проверку и преобразование статуса онлайн-заказа из сервиса.
//
// Параметры:
//  ИдентификаторПоДаннымСервиса -Строка - идентификатор статуса онлайн-заказа по данным сервиса.
//
// Возвращаемое значение:
//  Строка - преобразованный идентификатор статуса онлайн-заказа.
//
Функция СтатусЗаказаПоДаннымСервиса(ИдентификаторПоДаннымСервиса)
	
	ИдентификаторПоДаннымСервиса = ВРег(ИдентификаторПоДаннымСервиса);
	
	Если ИдентификаторПоДаннымСервиса <> ОнлайнЗаказыКлиентСервер.СтатусЗаказаВыполняется()
		И ИдентификаторПоДаннымСервиса <> ОнлайнЗаказыКлиентСервер.СтатусЗаказаВыполняетсяОплата()
		И ИдентификаторПоДаннымСервиса <> ОнлайнЗаказыКлиентСервер.СтатусЗаказаИстекСрокЖизни()
		И ИдентификаторПоДаннымСервиса <> ОнлайнЗаказыКлиентСервер.СтатусЗаказаНеАктивен()
		И ИдентификаторПоДаннымСервиса <> ОнлайнЗаказыСлужебный.СтатусЗаказаОплачен() Тогда
		
		ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Передан неизвестный идентификатор статуса заказа %1';
					|en = 'Unknown order status ID %1 is passed'"),
				ИдентификаторПоДаннымСервиса);
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ПредставлениеОшибки,
			Истина);
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось обработать ответ сервиса. %1 Подробности в журнале регистрации.';
				|en = 'Cannot process the service response. %1 For more information, see the event log.'"),
			ПредставлениеОшибки);
		
	КонецЕсли;
	
	Возврат ИдентификаторПоДаннымСервиса;
	
КонецФункции

// Выполняет проверку и преобразование статуса фискализации из сервиса.
//
// Параметры:
//  ИдентификаторПоДаннымСервиса -Строка - идентификатор статуса фискализации по данным сервиса.
//
// Возвращаемое значение:
//  Строка - преобразованный идентификатор статуса фискализации.
//
Функция СтатусФискализацииПоДаннымСервиса(ИдентификаторПоДаннымСервиса)
	
	ИдентификаторПоДаннымСервиса = ВРег(ИдентификаторПоДаннымСервиса);
	
	Если ИдентификаторПоДаннымСервиса = "SUCCESS" Тогда
		Возврат ОнлайнЗаказыСлужебный.СтатусФискализацииВыполнена();
	ИначеЕсли ИдентификаторПоДаннымСервиса = "IN_PROGRESS" Тогда
		Возврат ОнлайнЗаказыСлужебный.СтатусФискализацииВыполняется();
	ИначеЕсли ИдентификаторПоДаннымСервиса = "FAILURE" Тогда
		Возврат ОнлайнЗаказыСлужебный.СтатусФискализацииОшибка();
	ИначеЕсли ИдентификаторПоДаннымСервиса = "REQUEST_RETRY"
		Или ИдентификаторПоДаннымСервиса = "REQUEST_EXPECTED" Тогда
		Возврат ОнлайнЗаказыСлужебный.СтатусФискализацииИнициализация();
	Иначе
		ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Передан неизвестный идентификатор статуса фискализации %1';
					|en = 'Unknown fiscalization status ID %1 is passed'"),
				ИдентификаторПоДаннымСервиса);
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ПредставлениеОшибки,
			Истина);
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось обработать ответ сервиса. %1 Подробности в журнале регистрации.';
				|en = 'Cannot process the service response. %1 For more information, see the event log.'"),
			ПредставлениеОшибки);
	КонецЕсли;
	
КонецФункции

// Возвращает способ оплаты по данным онлайн-заказа из сервиса.
//
// Параметры:
//  ДанныеЗаказаВСервисе -Структура - данные заказа в сервиса.
//
// Возвращаемое значение:
//  Перечисления.СпособыОплатыОнлайнЗаказов - способ оплаты онлайн-заказа.
//
Функция СпособОплаты(ДанныеЗаказаВСервисе)
	
	ИдентификаторПоДаннымСервиса = ДанныеЗаказаВСервисе.paymentType;
	
	Если Не ЗначениеЗаполнено(ИдентификаторПоДаннымСервиса) Тогда
		Возврат Неопределено;
	ИначеЕсли ИдентификаторПоДаннымСервиса = "SBP" Тогда
		Если ДанныеЗаказаВСервисе.sbpPayment.paymentModel = "C2B" Тогда
			Возврат Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b;
		ИначеЕсли ДанныеЗаказаВСервисе.sbpPayment.paymentModel = ОнлайнЗаказыСлужебный.ИдентификаторВариантаСБПB2B() Тогда
			Возврат Перечисления.СпособыОплатыОнлайнЗаказов.СБПb2b;
		Иначе
			ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Передан неизвестный идентификатор модели СБП %1';
						|en = 'Unknown ID of the %1 FPS model is passed'"),
					ДанныеЗаказаВСервисе.sbpPayment.paymentModel);
			ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
				ПредставлениеОшибки,
				Истина);
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось обработать ответ сервиса. %1 Подробности в журнале регистрации.';
					|en = 'Cannot process the service response. %1 For more information, see the event log.'"),
				ПредставлениеОшибки);
			КонецЕсли;
	ИначеЕсли ИдентификаторПоДаннымСервиса = "REQUISITES" Тогда
		Возврат Перечисления.СпособыОплатыОнлайнЗаказов.ПоРеквизитам;
	ИначеЕсли ИдентификаторПоДаннымСервиса = "ACQUIRING" Тогда
		Возврат Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг;
	ИначеЕсли ИдентификаторПоДаннымСервиса = "CASH" Тогда
		Возврат Перечисления.СпособыОплатыОнлайнЗаказов.Наличные;
	ИначеЕсли ИдентификаторПоДаннымСервиса = "OTHER" Тогда
		Возврат Перечисления.СпособыОплатыОнлайнЗаказов.Прочее;
	Иначе
		ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Передан неизвестный идентификатор статуса способа оплаты %1';
					|en = 'Unknown payment method status ID %1 is passed'"),
				ИдентификаторПоДаннымСервиса);
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ПредставлениеОшибки,
			Истина);
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось обработать ответ сервиса. %1 Подробности в журнале регистрации.';
				|en = 'Cannot process the service response. %1 For more information, see the event log.'"),
			ПредставлениеОшибки);
	КонецЕсли;
	
КонецФункции

// Преобразует текущую локальную дату в UTC.
//
// Параметры:
//  Значение - Дата - исходная дата.
//
// Возвращаемое значение:
//  Дата - результат преобразования.
//
Функция ДатаВUTC(Значение) // АПК:216 Принятое сокращение
	
	Возврат Значение - СмещениеСтандартногоВремени(ЧасовойПоясСеанса(), Значение);
	
КонецФункции

#КонецОбласти

#КонецОбласти
