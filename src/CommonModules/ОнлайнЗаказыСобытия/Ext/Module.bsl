///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ОнлайнЗаказы".
// ОбщийМодуль.ОнлайнЗаказыСобытия.
//
// Серверные процедуры обработки событий подсистемы Онлайн-заказы:
//  - заполнение параметров оплаты;
//  - запись данных оплаты;
//  - получение данных оплаты.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриИзмененииИспользованияНастройкиОплаты(
		НастройкаОплаты,
		Используется,
		ИдентификаторСпособаОплаты,
		ОбновлятьНастройки,
		РезультатОперации) Экспорт
	
	СпособОплаты = СпособОплатыПоИдентификатору(ИдентификаторСпособаОплаты);
	
	Если (СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b
		Или СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПb2b)
		И Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		Возврат;
	КонецЕсли;
	
	Если СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг
		И Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ОнлайнЗаказыСлужебный.НовыйРезультатОперации();
	Результат.Вставить("Ошибка", Ложь);
	Результат.Вставить("Токен", Неопределено);
	
	Если Используется Тогда
		ПриПолученииТокена(
			Результат,
			ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаПриемОплат(),
			НастройкаОплаты,
			СпособОплаты);
	Иначе
		ПриОтключенииТокена(
			Результат,
			ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаПриемОплат(),
			НастройкаОплаты,
			СпособОплаты);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат.СообщениеОбОшибке) Тогда
		Результат.Ошибка = Истина;
		ЗаполнитьЗначенияСвойств(РезультатОперации, Результат);
		Возврат;
	КонецЕсли;
	
	РезультатОбработки = Новый Структура;
	РезультатОбработки.Вставить("Токен", Результат.Токен);
	РезультатОбработки.Вставить("НастройкаПодключения", НастройкаОплаты);
	
	Если ОбновлятьНастройки Тогда
		
		НастройкиПодключения = Справочники.НастройкиПодключенияКОнлайнЗаказам.НастройкиПодключенияПоНастройкеОплаты(
			НастройкаОплаты);
			
		Для Каждого НастройкаПодключения Из НастройкиПодключения Цикл
			ОнлайнЗаказыСлужебный.ОбновитьНастройку(НастройкаПодключения, РезультатОбработки);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает идентификатор сервиса для которого выполняется получение данных.
//
// Возвращаемое значение:
//  Строка - Идентификатор сервиса.
//
Функция ИдентификаторСервиса() Экспорт
	
	Возврат "orders.1c.ru";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаСобытий

// Выполняет заполнение параметров оплат заказа в соответствии с допустимыми способами.
//
// Параметры:
//  ДокументЗаказа - ОпределяемыйТип.ДокументОнлайнЗаказаБИП - документ, для которого
//    формируются параметры оплат.
//  ПараметрыОплат - Структура - Содержит описание параметров оплат по способам:
//    * СБП - Структура, Неопределено - Параметры оплат СБП,
//         Неопределено если подсистема СистемаБыстрыхПлатежей не используется.
//    * ИнтернетЭквайринг - Структура, Неопределено - Параметры оплат через Интернет-эквайринг,
//         Неопределено если подсистема ИнтернетЭквайринг не используется.
//  НастройкиОплат - Соответствие из КлючИЗначение - Содержит данные используемых настроек в разрезе способов оплат:
//    * Ключ - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - способ оплаты
//    * Значение - Произвольный - Используемая настройка подключения, для данного способа оплаты.
//
Процедура ПриФормированииПараметровОплат(
		ДокументЗаказа,
		ПараметрыОплат,
		НастройкиОплат) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		
		ПараметрыСБП = Новый Структура;
		ПараметрыСБП.Вставить("c2b", Неопределено);
		ПараметрыСБП.Вставить("b2b", Неопределено);
		
		ПараметрыОплат.СБП = ПараметрыСБП;
		
	КонецЕсли;
	
	Для Каждого НастройкаОплаты Из НастройкиОплат Цикл
		Если НастройкаОплаты.Ключ = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b
			Или НастройкаОплаты.Ключ = Перечисления.СпособыОплатыОнлайнЗаказов.СБПb2b Тогда
			
			Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
				Возврат;
			КонецЕсли;
			
			МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
			МодульСистемаБыстрыхПлатежейСлужебный.ПараметрыОплаты(
				ДокументЗаказа,
				НастройкаОплаты.Значение,
				ПараметрыОплат.СБП);
			
		КонецЕсли;
		
		Если НастройкаОплаты.Ключ = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг
			И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
			
			МодульИнтернетЭквайрингСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСлужебный");
			ПараметрыОплат.ИнтернетЭквайринг = МодульИнтернетЭквайрингСлужебный.ПараметрыОплаты(
				ДокументЗаказа,
				НастройкаОплаты.Значение);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Выполняет заполнение параметров оплат заказа для сервиса.
//
// Параметры:
//  НастройкиОплатПодключения - Соответствие Из КлючИЗначение - Содержит данные используемых настроек в разрезе
//    способов оплат:
//    * Ключ - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - способ оплаты
//    * Значение - Произвольный - Используемая настройка подключения, для данного способа оплаты.
//  ПараметрыОплатЗаказа - Структура - Содержит параметры оплат заказа
//  см. ОнлайнЗаказыСлужебный.ДанныеЗаказаНаОплатуПоДокументу.ПараметрыОплат.
//  НастройкиОплатСервиса - Структура - Содержит параметры оплат для сервиса:
//    * СБП - Структура, Неопределено - Параметры оплаты СБП,
//        Неопределено в случае если настройка для оплат через СБП не выбрана.
//    * ИнтернетЭквайринг - Структура, Неопределено - Параметры оплаты через Интернет-эквайринг,
//        Неопределено в случае если настройка для оплат через Интернет-эквайринг не выбрана.
//
Процедура ПриЗаполненииНастроекОплатыФизическихЛиц(
	НастройкиОплатПодключения,
	ПараметрыОплатЗаказа,
	НастройкиОплатСервиса) Экспорт
	
	Для Каждого НастройкаОплаты Из НастройкиОплатПодключения Цикл
		Если НастройкаОплаты.Ключ = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b
			И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПc2b") Тогда
			
			НастройкиОплатыСБПc2b = Новый Структура;
			НастройкиОплатыСБПc2b.Вставить("ВариантСБП",        "C2B");
			НастройкиОплатыСБПc2b.Вставить("НазначениеПлатежа", "");
			
			Если ПараметрыОплатЗаказа.СБП.c2b <> Неопределено Тогда
				НастройкиОплатыСБПc2b.НазначениеПлатежа = ПараметрыОплатЗаказа.СБП.c2b.НазначениеПлатежа;
			КонецЕсли;
			
			НастройкиОплатСервиса.СБП = НастройкиОплатыСБПc2b;
			
		КонецЕсли;
		
		Если НастройкаОплаты.Ключ = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг
			И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
			
			НастройкиОплаты = Новый Структура;
			НастройкиОплаты.Вставить("НазначениеПлатежа", "");
			
			Если ПараметрыОплатЗаказа.ИнтернетЭквайринг <> Неопределено Тогда
				НастройкиОплаты.НазначениеПлатежа = ПараметрыОплатЗаказа.ИнтернетЭквайринг.НазначениеПлатежа;
			КонецЕсли;
			
			НастройкиОплатСервиса.ИнтернетЭквайринг = НастройкиОплаты;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Выполняет заполнение параметров оплат заказа для сервиса.
//
// Параметры:
//  НастройкиОплатПодключения - Соответствие - Содержит данные используемых настроек в разрезе способов оплат:
//    * Ключ - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - способ оплаты
//    * Значение - Произвольный - Используемая настройка подключения, для данного способа оплаты.
//  ПараметрыОплатЗаказа - Структура - Содержит параметры оплат заказа
//  см. ОнлайнЗаказыСлужебный.ДанныеЗаказаНаОплатуПоДокументу.ПараметрыОплат.
//  НастройкиОплатСервиса - Структура - Содержит параметры оплат для сервиса:
//    * СБП - Структура, Неопределено - Параметры оплаты СБП,
//        Неопределено в случае если настройка для оплат через СБП не выбрана:
//
Процедура ПриЗаполненииНастроекОплатыЮридическихЛиц(
	НастройкиОплатПодключения,
	ПараметрыОплатЗаказа,
	НастройкиОплатСервиса) Экспорт
	
	Для Каждого НастройкаОплаты Из НастройкиОплатПодключения Цикл
		Если НастройкаОплаты.Ключ = Перечисления.СпособыОплатыОнлайнЗаказов.СБПb2b
			И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПb2b") Тогда
			
			НастройкиОплатыСБПb2b = Новый Структура;
			НастройкиОплатыСБПb2b.Вставить("ВариантСБП", "B2B");
			НастройкиОплатыСБПb2b.Вставить("ОблагаетсяНДС", Ложь);
			НастройкиОплатыСБПb2b.Вставить("СуммаНДС", 0);
			НастройкиОплатыСБПb2b.Вставить("РасчетныйСчет", "");
			НастройкиОплатыСБПb2b.Вставить("ИдентификаторПлатежа", "");
			НастройкиОплатыСБПb2b.Вставить("НазначениеПлатежа", "");
			
			Если ПараметрыОплатЗаказа.СБП.b2b <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(НастройкиОплатыСБПb2b, ПараметрыОплатЗаказа.СБП.b2b);
			КонецЕсли;
			
			НастройкиОплатСервиса.СБП = НастройкиОплатыСБПb2b;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Выполняет подготовку параметров оплаты заказа для записи в соответствующие подсистемы.
//
// Параметры:
//  ДанныеЗаказа - Структура - Содержит данные заказа:
//    * ИдентификаторЗаказа - Строка - Идентификатор онлайн-заказа;
//    * Статус - Строка - Статус онлайн-заказа;
//    * СуммаЗаказа - Число - Сумма онлайн-заказа;
//    * СпособОплаты - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - Способ оплаты онлайн-заказа;
//    * ДатаОплаты - Дата - дата получения данных об оплате сервисом;
//    * КонтрольнаяСумма - Строка - Значение контрольной суммы заказа;
//    * ВерсияКонтрольнойСуммы - Число - Версия расчета контрольной суммы заказа;
//    * ОплатаСБП - Структура, Неопределено - Содержит данные операции СБП, в том случае когда
//        значение Свойства СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b:
//      ***ИдентификаторОплаты - Строка - идентификатор оплаты в СБП;
//      ***ИдентификаторПлатежнойСистемы - Строка - идентификатор оплаты в Системе быстрых платежей;
//      ***ДатаОперации - Дата - фактическая дата оплаты в UTC;
//      ***ИдентификаторМерчанта - Строка - идентификатор мерчанта в Системе быстрых платежей;
//      ***НазначениеПлатежа - Строка - назначение платежа;
//    * ОплатаИнтернетЭквайринг - Структура, Неопределено - Содержит данные операции СБП, в том случае когда
//        значение Свойства СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг:
//      ***ИдентификаторОплаты - Строка - идентификатор оплаты;
//      ***ИдентификаторОперации - Строка - идентификатор оплаты в системе банка;
//      ***ДатаОперации - Дата - фактическая дата оплаты в UTC;
//      ***ИдентификаторМерчанта - Строка - идентификатор мерчанта;
//      ***НазначениеПлатежа - Строка - назначение платежа;
//  ПараметрыОплаты - Соответствие Из КлючИЗначение - Содержит параметры оплат в разрезе ее способов
//  ПараметрыНастройкиОплаты - Структура - Содержит параметры настройки вида оплаты
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам - Настройка подключения,
//    используемая для заказа.
//
Процедура ПриЗаполненииПараметровОплаты(
		ДанныеЗаказа,
		ПараметрыОплаты,
		ПараметрыНастройкиОплаты,
		НастройкаПодключения) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеЗаказа.СпособОплаты) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаОплаты = Справочники.НастройкиПодключенияКОнлайнЗаказам.НастройкаОплаты(
		НастройкаПодключения,
		ДанныеЗаказа.СпособОплаты);
		
	Если ДанныеЗаказа.СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b
		Или ДанныеЗаказа.СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПb2b Тогда
			
		Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
			Возврат;
		КонецЕсли;
		
		МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		МодульСистемаБыстрыхПлатежейСлужебный.ЗаполнитьПараметрыОплаты(
			ДанныеЗаказа,
			НастройкаОплаты,
			ПараметрыОплаты,
			ПараметрыНастройкиОплаты,
			ИдентификаторВариантаСБП(ДанныеЗаказа.СпособОплаты));
		
	КонецЕсли;
	
	Если ДанныеЗаказа.СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг
		И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
		МодульИнтернетЭквайрингСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСлужебный");
		МодульИнтернетЭквайрингСлужебный.ЗаполнитьПараметрыОплаты(
			ДанныеЗаказа,
			НастройкаОплаты,
			ПараметрыОплаты,
			ПараметрыНастройкиОплаты);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет запись данных оплат заказа в соответствующие подсистемы.
//
// Параметры:
//  ДокументЗаказа - ОпределяемыйТип.ДокументОнлайнЗаказаБИП - документ по которому выполнена оплата.
//  СпособОплаты - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - Способ оплаты онлайн-заказа;
//  ПараметрыОплаты - Соответствие Из КлючИЗначение - Содержит данные оплат по способам:
//    * Ключ - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - способ оплаты
//    * Значение - Структура - Параметры оплат, сформированные в методе ОнлайнЗаказыСобытия.ПриЗаполненииПараметровОплаты.
//  ПараметрыНастройкиОплаты - Структура - Содержит параметры настройки вида оплаты
//
Процедура ПриЗаписиДанныхОплаты(
		ДокументЗаказа,
		СпособОплаты,
		ПараметрыОплаты,
		ПараметрыНастройкиОплаты) Экспорт
	
	Если СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b
		И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		
		ПараметрыОплатыСБПc2b = ПараметрыОплаты.Получить("СБПc2b");
		
		МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		МодульСистемаБыстрыхПлатежейСлужебный.ЗаписатьДанныеОплаты(
			ДокументЗаказа,
			ПараметрыОплатыСБПc2b,
			ПараметрыНастройкиОплаты,
			"c2b");
		
		ПараметрыОплатыСБПc2b.РезультатОбработки.ПараметрыОперации.Удалить("ИдентификаторМерчанта");
		ПараметрыОплатыСБПc2b.РезультатОбработки.ПараметрыОперации.Удалить("Идентификатор");
		ПараметрыОплатыСБПc2b.РезультатОбработки.ПараметрыОперации.Удалить("НазначениеПлатежа");
		
	ИначеЕсли СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПb2b
		И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		
		ПараметрыОплатыСБПb2b = ПараметрыОплаты.Получить("СБПb2b");
		
		МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		МодульСистемаБыстрыхПлатежейСлужебный.ЗаписатьДанныеОплаты(
			ДокументЗаказа,
			ПараметрыОплатыСБПb2b,
			ПараметрыНастройкиОплаты,
			"b2b");
		
		ПараметрыОплатыСБПb2b.РезультатОбработки.ПараметрыОперации.Удалить("ИдентификаторМерчанта");
		ПараметрыОплатыСБПb2b.РезультатОбработки.ПараметрыОперации.Удалить("Идентификатор");
		ПараметрыОплатыСБПb2b.РезультатОбработки.ПараметрыОперации.Удалить("НазначениеПлатежа");
		
	КонецЕсли;
	
	Если СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг
		И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
		
		ПараметрыОперации = ПараметрыОплаты.Получить("ИнтернетЭквайринг").РезультатОбработки.ПараметрыОперации;
		
		МодульИнтернетЭквайрингСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСлужебный");
		
		УстановитьПривилегированныйРежим(Истина);
		МодульИнтернетЭквайрингСлужебный.ЗаписатьДанныеОплаты(
			ДокументЗаказа,
			ПараметрыОперации,
			ПараметрыНастройкиОплаты);
		УстановитьПривилегированныйРежим(Ложь);
		
		ПараметрыОперации.Удалить("ИдентификаторМерчанта");
		ПараметрыОперации.Удалить("Идентификатор");
		ПараметрыОперации.Удалить("НазначениеПлатежа");
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет получение данных оплат по переданным документам.
//
// Параметры:
//  ДанныеОпераций  - Соответствие Из КлючИЗначение - содержит данные оплат для дальнейшей передачи
//    в прикладную логику для обработки отложенного статуса;
//  СпособОплаты - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - Способ оплаты онлайн-заказа;
//  ОплаченныеЗаказы - Массив Из ОпределяемыйТип.ДокументОнлайнЗаказаБИП - перечень документов,
//    по которым необходимо получить данные оплат.
//
Процедура ПриПолученииДанныхОплат(
	ДанныеОпераций,
	СпособОплаты,
	ОплаченныеЗаказы) Экспорт
	
	Если СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b
		Или СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПb2b Тогда
		
		Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
			Возврат;
		КонецЕсли;
		
		МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		МодульСистемаБыстрыхПлатежейСлужебный.ДанныеОплаты(
			ОплаченныеЗаказы,
			ДанныеОпераций,
			?(СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b,
				"c2b",
				"b2b"));
		
	КонецЕсли;
	
	Если СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг
		И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
		МодульИнтернетЭквайрингСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСлужебный");
			
		УстановитьПривилегированныйРежим(Истина);
		ДанныеОпераций = МодульИнтернетЭквайрингСлужебный.ДанныеОпераций(ОплаченныеЗаказы);
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет создание/получение токена данных оплат по переданному способу и настройке подключения.
//
// Параметры:
//  РезультатОперации - Структура - результат выполнения операции см. ОнлайнЗаказыСлужебный.НовыйРезультатОперации:
//    * Токен - Строка, Неопределено - Значение токена для создания оплаты по выбранному способу,
//      Неопределено в случае когда не удалось выполнить операцию получения токена.
//  ВидТокена - Строка - Вид получаемого токена.
//  НастройкаПодключения - Произвольный - настройка подключения перечень документов,
//    по которым необходимо получить данные оплат.
//  СпособОплаты - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - Способ оплаты для которого получается токен;
//
Процедура ПриПолученииТокена(РезультатОперации, ВидТокена, НастройкаПодключения, СпособОплаты) Экспорт
	
	Если ВидТокена = ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаОблачнаяКасса() Тогда
		
		МодульОблачныеКассыСлужебный = ОбщегоНазначения.ОбщийМодуль("ОблачныеКассыСлужебный");
		РезультатПолучения = МодульОблачныеКассыСлужебный.ТокенСервиса(
			НастройкаПодключения,
			ИдентификаторСервиса());
		ЗаполнитьЗначенияСвойств(РезультатОперации, РезультатПолучения);
		
	ИначеЕсли ЭтоОперацияСТокеномСБП(ВидТокена, СпособОплаты) Тогда
		
		Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
			Возврат;
		КонецЕсли;
		
		МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		РезультатПолучения = МодульСистемаБыстрыхПлатежейСлужебный.ТокенСпособаОплаты(
			НастройкаПодключения,
			ИдентификаторСервиса());
		ЗаполнитьЗначенияСвойств(РезультатОперации, РезультатПолучения);
		
	ИначеЕсли ВидТокена = ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаПриемОплат()
		И СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг Тогда
		
		Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
			Возврат;
		КонецЕсли;
		
		МодульИнтернетЭквайрингСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСлужебный");
		РезультатПолучения = МодульИнтернетЭквайрингСлужебный.ТокенСпособаОплаты(
			НастройкаПодключения,
			ИдентификаторСервиса());
		ЗаполнитьЗначенияСвойств(РезультатОперации, РезультатПолучения);
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОперации.СообщениеОбОшибке) Тогда
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось получить токен по способу оплаты: %1.
					|
					|%2';
					|en = 'Cannot get the token by the payment method: %1.
					|
					|%2'"),
				СпособОплаты,
				РезультатОперации.СообщениеОбОшибке),
			Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет создание/получение токена данных оплат по переданному способу и настройке подключения.
//
// Параметры:
//  СпособОплаты - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - Способ оплаты для которого получается токен;
//  НастройкаПодключения - Произвольный - настройка подключения способа оплаты,
//    по которой осуществляется отзыв токена.
//  РезультатОперации - Структура - результат выполнения операции см. ОнлайнЗаказыСлужебный.НовыйРезультатОперации
//
Процедура ПриОтключенииТокена(
		РезультатОперации,
		ВидТокена,
		НастройкаПодключения,
		СпособОплаты) Экспорт
		
	Если ВидТокена = ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаОблачнаяКасса() Тогда
		
		МодульОблачныеКассыСлужебный = ОбщегоНазначения.ОбщийМодуль("ОблачныеКассыСлужебный");
		МодульОблачныеКассыСлужебный.ОтозватьТокенСервиса(
			НастройкаПодключения,
			РезультатОперации,
			ИдентификаторСервиса());
	
	ИначеЕсли ЭтоОперацияСТокеномСБП(ВидТокена, СпособОплаты) Тогда
		
		Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
			Возврат;
		КонецЕсли;
		
		МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		МодульСистемаБыстрыхПлатежейСлужебный.ОтозватьТокенСпособаОплаты(
			НастройкаПодключения,
			РезультатОперации,
			ИдентификаторСервиса());
		
	ИначеЕсли ВидТокена = ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаПриемОплат()
		И СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг Тогда
		
		Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
			Возврат;
		КонецЕсли;
		
		МодульИнтернетЭквайрингСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСлужебный");
		МодульИнтернетЭквайрингСлужебный.ОтозватьТокенСпособаОплаты(
			НастройкаПодключения,
			РезультатОперации,
			ИдентификаторСервиса());
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(РезультатОперации.СообщениеОбОшибке) Тогда
		
		ОнлайнЗаказыСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось отозвать токен  по способу оплаты: %1.
					|
					|%2';
					|en = 'Cannot revoke the token by the payment method: %1.
					|
					|%2'"),
				СпособОплаты,
				РезультатОперации.СообщениеОбОшибке),
			Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет обработку назначения платежа при подготовке онлайн-заказа.
//
// Параметры:
//  НазначениеПлатежа - Строка - Назначение платежа для способа оплаты;
//  СпособОплаты - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - Способ оплаты для которого получается токен.
//
Процедура ПриПодготовкеНазначенияПлатежа(НазначениеПлатежа, СпособОплаты) Экспорт
	
	Если СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b
		Или СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПb2b Тогда
		
		Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
			Возврат;
		КонецЕсли;
		
		МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		МодульСистемаБыстрыхПлатежейСлужебный.ПодготовитьНазначениеПлатежа(
			НазначениеПлатежа,
			ИдентификаторВариантаСБП(СпособОплаты));
		
	КонецЕсли;

	Если СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг
		И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
		
		МодульИнтернетЭквайрингСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСлужебный");
		МодульИнтернетЭквайрингСлужебный.ПодготовитьНазначениеПлатежа(НазначениеПлатежа);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет проверку параметров оплат перед отправкой онлайн-заказа в сервис.
//
// Параметры:
//  ПараметрыОплат - Структура - Содержит параметры оплат онлайн-заказа;
//  РезультатОперации - Структура - результат выполнения операции см. ОнлайнЗаказыСлужебный.НовыйРезультатОперации
//
Процедура ПриПроверкеПараметровОплат(
		ПараметрыОплат,
		РезультатОперации) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.НастройкиПлатежныхСистем") Тогда
		Возврат;
	КонецЕсли;
	
	МодульНастройкиПлатежныхСистемСлужебный = ОбщегоНазначения.ОбщийМодуль("НастройкиПлатежныхСистемСлужебный");
	
	Если ПараметрыОплат.СБП <> Неопределено Тогда
		
		Если ПараметрыОплат.СБП.c2b <> Неопределено Тогда

			МодульНастройкиПлатежныхСистемСлужебный.ПроверитьНазначениеПлатежа(
				ПараметрыОплат.СБП.c2b.НазначениеПлатежа,
				РезультатОперации);
		
		КонецЕсли;
		
		Если ПараметрыОплат.СБП.b2b <> Неопределено Тогда
		
			МодульНастройкиПлатежныхСистемСлужебный.ПроверитьНазначениеПлатежа(
				ПараметрыОплат.СБП.b2b.НазначениеПлатежа,
				РезультатОперации);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыОплат.ИнтернетЭквайринг <> Неопределено Тогда
		
		МодульНастройкиПлатежныхСистемСлужебный.ПроверитьНазначениеПлатежа(
			ПараметрыОплат.ИнтернетЭквайринг.НазначениеПлатежа,
			РезультатОперации);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписиДанныхФискализации(ПараметрыФискализации, ДанныеФискализации) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОблачныеКассы") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыФискализации.ДокументОперации) Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнен документ операции фискализации в методе
			|ОнлайнЗаказыПереопределяемый.ПриЗагрузкеСтатусаЗаказа';
			|en = 'The fiscalization operation document is not filled in the
			|ОнлайнЗаказыПереопределяемый.ПриЗагрузкеСтатусаЗаказа method'");
	КонецЕсли;
	
	Если ДанныеФискализации.СтатусОперации = ОнлайнЗаказыСлужебный.СтатусФискализацииВыполняется() Тогда
		СтатусОперацииФискализации = ОблачныеКассыКлиентСервер.СтатусОперацииВыполняется();
	ИначеЕсли ДанныеФискализации.СтатусОперации = ОнлайнЗаказыСлужебный.СтатусФискализацииВыполнена() Тогда
		СтатусОперацииФискализации = ОблачныеКассыКлиентСервер.СтатусОперацииВыполнена();
	Иначе
		СтатусОперацииФискализации = Неопределено;
	КонецЕсли;
	
	НастройкаПодключения = Справочники.НастройкиПодключенияКОнлайнЗаказам.НастройкаОблачнойКассы(
		ДанныеФискализации.НастройкаПодключения);
	
	ДанныеОперации = Новый Структура;
	ДанныеОперации.Вставить("ДокументОперации", ПараметрыФискализации.ДокументОперации);
	ДанныеОперации.Вставить("СтатусОперации", СтатусОперацииФискализации);
	ДанныеОперации.Вставить("ИдентификаторОперации", ДанныеФискализации.ИдентификаторОперации);
	
	МодульОблачныеКассыСлужебный = ОбщегоНазначения.ОбщийМодуль("ОблачныеКассыСлужебный");
	МодульОблачныеКассыСлужебный.ЗаписатьДанныеФискализацииОнлайнЗаказа(
			НастройкаПодключения,
			ДанныеОперации,
			ДанныеФискализации.ДанныеОперации);
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыФункции

// Получает ссылку на перечисление СпособыОплатыОнлайнЗаказов по строковому идентификатору. Если значение входного
//   параметра не входит в список допустимых, то будет вызвано исключение.
// 
// Параметры:
//  ИдентификаторСпособаОплаты - Строка - строковое представление, по которому нужно получить ссылку. Допустимые
//    значения: "СБПc2b", "ИнтернетЭквайринг".
//  
//  Возвращаемое значение:
//    ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - найденная ссылка.
//
Функция СпособОплатыПоИдентификатору(ИдентификаторСпособаОплаты)
	
	Если ИдентификаторСпособаОплаты = "СБПc2b" Тогда
		Возврат Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b;
	ИначеЕсли ИдентификаторСпособаОплаты = "СБПb2b" Тогда
		Возврат Перечисления.СпособыОплатыОнлайнЗаказов.СБПb2b;
	ИначеЕсли ИдентификаторСпособаОплаты = "ИнтернетЭквайринг" Тогда
		Возврат Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг;
	КонецЕсли;
	
	ВызватьИсключение (НСтр("ru = 'Передан неизвестный идентификатор способа оплаты.';
							|en = 'An unknown payment method status ID is passed.'"));
	
КонецФункции

Функция ИдентификаторВариантаСБП(ВариантСБП)
	
	Если ВариантСБП = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b Тогда
		Возврат "c2b";
	ИначеЕсли ВариантСБП = Перечисления.СпособыОплатыОнлайнЗаказов.СБПb2b Тогда
		Возврат "b2b";
	Иначе
		ВызватьИсключение (НСтр("ru = 'Передан неизвестный идентификатор варианта СБП.';
								|en = 'Unknown ID of the FPS option is passed.'"));
	КонецЕсли;
	
КонецФункции

Функция ЭтоОперацияСТокеномСБП(ВидТокена, СпособОплаты)
	
	Если ВидТокена <> ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаПриемОплат() Тогда
		Возврат Ложь;
	ИначеЕсли СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b
		Или СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПb2b Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

