////////////////////////////////////////////////////////////////////////////////
// Клиентские процедуры и функции, управляющие настройками системы.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняется поиск настроек по панелям администрирования при глобальном поиске.
//
// Параметры:
//  СтрокаПоиска          - Строка - введенная строка в поле глобального поиска.
//  ПланГлобальногоПоиска - ПланГлобальногоПоиска - виды поиска.
//
Процедура ПриГлобальномПоиске(СтрокаПоиска, ПланГлобальногоПоиска) Экспорт
	
	Если СтрокаПоискаСоответствуетТребованиям(СтрокаПоиска) Тогда
		
		ПланГлобальногоПоиска.Добавить("ЗаполнитьРезультатГлобальногоПоискаНастроекПоРазрезамУчета",
			"НастройкиСистемы",
			Истина,
			Истина);
		ПланГлобальногоПоиска.Добавить("ЗаполнитьРезультатГлобальногоПоискаУчетныхПолитик",
			"НастройкиНалоговУчетныхПолитик",
			Истина,
			Истина);
			
		ПланПоискаНастроек = ПланГлобальногоПоиска.Найти("ЗаполнитьРезультатГлобальногоПоискаНастроек",
			"НастройкиСистемыКлиент");
		Если ПланПоискаНастроек = Неопределено Тогда
			ПланГлобальногоПоиска.Добавить("ДобавитьЗапускПоискаНастроекСистемы", 
				"НастройкиСистемыКлиент",
				Ложь,
				Ложь,
				94);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Дополняет результат глобального поиска предложением поискать по настройкам.
// 
// Параметры:
//  СтрокаПоиска                - Строка - строка поиска. 
//  РезультатыГлобальногоПоиска - РезультатГлобальногоПоиска - содержит результаты поиска.
//  ДополнительныеПараметры     - Структура - дополнительные параметры.
//
Процедура ДобавитьЗапускПоискаНастроекСистемы(СтрокаПоиска, РезультатыГлобальногоПоиска, ДополнительныеПараметры) Экспорт
	ЗаголовокПоиска = НСтр("ru = 'Найти в настройках...';
							|en = 'Find in the settings...'");
	РезультатыГлобальногоПоиска.Добавить(СтрокаПоиска, ЗаголовокПоиска, БиблиотекаКартинок.ПоискДанных,
		"ЗапускПоискаНастроекСистемы", "");
КонецПроцедуры

// Заполняет результат глобального поиска.
// 
// Параметры:
//  СтрокаПоиска                - Строка - строка поиска. 
//  РезультатыГлобальногоПоиска - РезультатГлобальногоПоиска - содержит результаты поиска.
//  ДополнительныеПараметры     - Структура - дополнительные параметры.
//
Процедура ЗаполнитьРезультатГлобальногоПоискаНастроек(СтрокаПоиска, РезультатыГлобальногоПоиска, ДополнительныеПараметры) Экспорт

	РезультатыПоиска = РезультатыГлобальногоПоискаНастроек(СтрокаПоиска);

	Если РезультатыПоиска = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Для Каждого РезультатПоиска Из РезультатыПоиска Цикл
		РезультатыГлобальногоПоиска.Добавить(РезультатПоиска.Значение, РезультатПоиска.Представление, РезультатПоиска.Картинка,
			"ПоискНастроекСистемы", РезультатПоиска.ОписаниеДействия);
	КонецЦикла;
	
	ПланГлобальногоПоиска = ГлобальныйПоиск.ПолучитьПлан();
	ПланПоискаНастроек = ПланГлобальногоПоиска.Найти("ЗаполнитьРезультатГлобальногоПоискаНастроек",
		"НастройкиСистемыКлиент");
	Если ПланПоискаНастроек <> Неопределено Тогда
		ПланГлобальногоПоиска.Удалить(ПланПоискаНастроек);
		ГлобальныйПоиск.УстановитьПлан(ПланГлобальногоПоиска);
	КонецЕсли;

КонецПроцедуры

// Выполняется при выборе результата глобального поиска по настройкам системы.
//
// Параметры:
//  ЭлементРезультата    - ЭлементРезультатаГлобальногоПоиска - выбранная строка результата поиска.
//  СтандартнаяОбработка - Булево - признак обработки по умолчанию.
//
Процедура ПриВыбореРезультатаГлобальногоПоиска(ЭлементРезультата, СтандартнаяОбработка) Экспорт
	
	Если ЭлементРезультата.ВидПоиска = "ПоискНастроекСистемы" Тогда
		ОчиститьСообщения();
		СтандартнаяОбработка = Ложь;
		
		//@skip-check use-non-recommended-method
		Форма = ПолучитьФорму(ЭлементРезультата.Описание, Неопределено);
		НайденныйЭлемент = Форма.Элементы.Найти(ЭлементРезультата.Значение);
		
		Если Форма <> Неопределено И ТипЗнч(Форма) = Тип("ФормаКлиентскогоПриложения") Тогда
			Форма.Открыть();
			Форма.ТекущийЭлемент = НайденныйЭлемент;
		КонецЕсли;
	ИначеЕсли ЭлементРезультата.ВидПоиска = "ЗапускПоискаНастроекСистемы"
		И ЗначениеЗаполнено(ЭлементРезультата.Значение) Тогда
		
		ОчиститьСообщения();
		СтандартнаяОбработка = Ложь;
		
		ПланГлобальногоПоиска = ГлобальныйПоиск.ПолучитьПлан();
		ПланГлобальногоПоиска.Добавить("ЗаполнитьРезультатГлобальногоПоискаНастроек",
			"НастройкиСистемыКлиент",
			Ложь,
			Ложь);
		ГлобальныйПоиск.УстановитьПлан(ПланГлобальногоПоиска);
		
		ГлобальныйПоиск.НачатьПоиск(ЭлементРезультата.Значение);
	КонецЕсли;
	
КонецПроцедуры

// Дополнительный индекс поиска настроек.
// 
// Возвращаемое значение:
//  Соответствие ИЗ КлючИЗначение - Дополнительный индекс поиска настроек:
//		*Ключ - Строка - Имя элемента
//		*Значение - Строка - строка поиска
Функция ДополнительныйИндексПоискаНастроек() Экспорт
	
	ИндексПоиска = Новый Соответствие();
	
#Область Закупки
	ИмяФормы = "Обработка.ПанельАдминистрированияУТ.Форма.Закупки";
	
	ЭлементыКлючевыеСлова = Новый Соответствие();
	ЭлементыКлючевыеСлова.Вставить("НазначитьОтветственныхЗаСогласованиеКоммерческихУсловийДокументовЗакупки",
		НСтр("ru = 'Ответственные за согласование коммерческих условий';
			|en = 'Persons responsible for commercial conditions'"));
	ЭлементыКлючевыеСлова.Вставить("НазначитьОтветственныхЗаСогласованиеЛогистическихУсловийДокументовЗакупки",
		НСтр("ru = 'Ответственные за согласование логистических условий';
			|en = 'Persons responsible for logistic conditions'"));
	ЭлементыКлючевыеСлова.Вставить("НазначитьОтветственныхЗаСогласованиеФинансовыхУсловийДокументовЗакупки",
		НСтр("ru = 'Ответственные за согласование финансовых условий';
			|en = 'Persons responsible for financial conditions'"));
	ЭлементыКлючевыеСлова.Вставить("НазначитьОтветственныхЗаСогласованиеЦеновыхУсловийДокументовЗакупки",
		НСтр("ru = 'Ответственные за согласование ценовых условий';
			|en = 'Persons responsible for price conditions'"));
	ЭлементыКлючевыеСлова.Вставить("ВключитьВерсионированиеДокументовЗакупки",
		НСтр("ru = 'Регистрация изменений и хранение версий документа Заказ поставщику';
			|en = 'Register changes and store the Purchase order document versions.'"));
	
	ИндексПоиска.Вставить(ИмяФормы, ЭлементыКлючевыеСлова);
	ЭлементыКлючевыеСлова.Очистить();
#КонецОбласти
	
#Область Продажи
	ИмяФормы = "Обработка.ПанельАдминистрированияУТ.Форма.Продажи";
	
	ЭлементыКлючевыеСлова = Новый Соответствие();
	ЭлементыКлючевыеСлова.Вставить("НазначитьОтветственныхЗаСогласованиеЗаявокНаВозврат",
		НСтр("ru = 'Ответственные за согласование заявок на возврат';
			|en = 'Persons responsible for return requests'"));
	ЭлементыКлючевыеСлова.Вставить("НазначитьОтветственныхЗаСогласованиеКоммерческихУсловийДокументовПродажи",
		НСтр("ru = 'Ответственные за согласование коммерческих условий';
			|en = 'Persons responsible for commercial conditions'"));
	ЭлементыКлючевыеСлова.Вставить("НазначитьОтветственныхЗаСогласованиеЛогистическихУсловийДокументовПродажи",
		НСтр("ru = 'Ответственные за согласование логистических условий';
			|en = 'Persons responsible for logistic conditions'"));
	ЭлементыКлючевыеСлова.Вставить("НазначитьОтветственныхЗаСогласованиеФинансовыхУсловийДокументовПродажи",
		НСтр("ru = 'Ответственные за согласование финансовых условий';
			|en = 'Persons responsible for financial conditions'"));
	ЭлементыКлючевыеСлова.Вставить("НазначитьОтветственныхЗаСогласованиеЦеновыхУсловийДокументовПродажи",
		НСтр("ru = 'Ответственные за согласование ценовых условий';
			|en = 'Persons responsible for price conditions'"));
	
	ИндексПоиска.Вставить(ИмяФормы, ЭлементыКлючевыеСлова);
	ЭлементыКлючевыеСлова.Очистить();
#КонецОбласти

#Область ПерсональныеНастройки
	ИмяФормы = "ОбщаяФорма.ПерсональныеНастройки";
	
	ЭлементыКлючевыеСлова = Новый Соответствие();
	ЭлементыКлючевыеСлова.Вставить("СведенияОПользователе",
		НСтр("ru = 'Сведения о пользователе';
			|en = 'User info'"));
	ЭлементыКлючевыеСлова.Вставить("ПерсональнаяНастройкаПроксиСервера",
		НСтр("ru = 'Настройка доступа к Интернету';
			|en = 'Internet connection settings'"));
	ЭлементыКлючевыеСлова.Вставить("НастройкиРаботыСПочтой",
		НСтр("ru = 'Настройки работы с почтой';
			|en = 'Account settings'"));
	ЭлементыКлючевыеСлова.Вставить("НастройкиНапоминаний",
		НСтр("ru = 'Настройки напоминаний';
			|en = 'Reminder settings'"));
	ЭлементыКлючевыеСлова.Вставить("УстановитьРасширениеРаботыСФайламиНаКлиенте",
		НСтр("ru = 'Установить расширение работы с файлами';
			|en = 'Install 1C:Enterprise Extension'"));
	ЭлементыКлючевыеСлова.Вставить("ОбновитьПараметрыСистемы",
		НСтр("ru = 'Обновить параметры системы';
			|en = 'Update system parameters'"));
	ЭлементыКлючевыеСлова.Вставить("ЗапрашиватьПодтверждениеПриЗавершенииПрограммы",
		НСтр("ru = 'Запрашивать подтверждение при завершении приложения';
			|en = 'Ask confirmation on exit'"));
	ЭлементыКлючевыеСлова.Вставить("ПоказыватьПредупреждениеОбУстановленныхОбновленияхПрограммы",
		НСтр("ru = 'Показывать предупреждение об установленных обновлениях приложения';
			|en = 'Show warning about installed application updates'"));
	ЭлементыКлючевыеСлова.Вставить("ПоказыватьЗанятыеФайлыПриЗавершенииРаботы",
		НСтр("ru = 'Показывать занятые файлы при завершении работы';
			|en = 'Show locked files before exit'"));
	ЭлементыКлючевыеСлова.Вставить("ПоказыватьИнформациюЧтоФайлНеБылИзменен",
		НСтр("ru = 'Показывать информацию, что файл не был изменен';
			|en = 'Notify that files have not been modified'"));
	ЭлементыКлючевыеСлова.Вставить("НастройкаРабочегоКаталога",
		НСтр("ru = 'Основной рабочий каталог';
			|en = 'Main working directory'"));
	ЭлементыКлючевыеСлова.Вставить("НастройкаСканирования",
		НСтр("ru = 'Настройка сканирования';
			|en = 'Configure scanning'"));
	ЭлементыКлючевыеСлова.Вставить("НастройкаЭЦП",
		НСтр("ru = 'Настройка ЭЦП';
			|en = 'Configure digital signature'"));
	ЭлементыКлючевыеСлова.Вставить("УстановитьРасширениеРаботыСКриптографией",
		НСтр("ru = 'Установить расширение работы с криптографией';
			|en = 'Install crypto extension'"));
	ЭлементыКлючевыеСлова.Вставить("ЗадатьДействиеПриВыбореМакетаПечатнойФормы",
		НСтр("ru = 'Действие при выборе макета печатной формы';
			|en = 'Action when selecting the print form template'"));
	ЭлементыКлючевыеСлова.Вставить("ЗапрашиватьПодтверждениеПриПечатиКомплектаДокументов",
		НСтр("ru = 'Запрашивать подтверждение при быстрой печати комплекта документов';
			|en = 'Request confirmation on quick document set printing'"));
	ЭлементыКлючевыеСлова.Вставить("НастроитьПараметрыАвторизацииВ1СДокументооборот",
		НСтр("ru = 'Параметры авторизации в 1С:Документооборот';
			|en = '1C:Document Management authorization parameters'"));
	ЭлементыКлючевыеСлова.Вставить("ВыключитьКонтрольОстатковТоваров",
		НСтр("ru = 'Отключить контроль остатков (на время сеанса)';
			|en = 'Disable stock balance control (for the session time)'"));
	ЭлементыКлючевыеСлова.Вставить("ВыключитьКонтрольРезервовТоваровПоЗаказам",
		НСтр("ru = 'Отключить контроль резервов (на время сеанса)';
			|en = 'Disable reserve control (for the session time)'"));
	ЭлементыКлючевыеСлова.Вставить("ВыключитьКонтрольОстатковРетроБонусовКлиентов",
		НСтр("ru = 'Отключить контроль остатков ретро-бонусов клиентов (на время сеанса)';
			|en = 'Disable customer rebate balance check (for the session time)'"));
	ЭлементыКлючевыеСлова.Вставить("ЗаполнятьРеквизитыДокументовПоСтатистике",
		НСтр("ru = 'Заполнять реквизиты документов по статистике';
			|en = 'Fill document attributes using statistical data'"));
	ЭлементыКлючевыеСлова.Вставить("ПерсональнаяНастройкаСменаПодписи",
		НСтр("ru = 'Настройки смены подписи в документах';
			|en = 'Settings for changing signature in documents'"));
	ЭлементыКлючевыеСлова.Вставить("ОчиститьНастройкиПодписанияЭЦПКадровогоЭДО",
		НСтр("ru = 'Очистить настройки подписания ЭЦП';
			|en = 'Clear digital signature settings'"));
	ЭлементыКлючевыеСлова.Вставить("НастройкиОповещений",
		НСтр("ru = 'Настройки оповещений';
			|en = 'Notification settings'"));
	ИндексПоиска.Вставить(ИмяФормы, ЭлементыКлючевыеСлова);
	ЭлементыКлючевыеСлова.Очистить();
#КонецОбласти

	НастройкиСистемыКлиентЛокализация.ДополнитьИндексПоискаНастроек(ИндексПоиска);
	Возврат ИндексПоиска
	
КонецФункции

// Конструктор свойства формы.
// 
// Параметры:
//  ИмяФормы - Неопределено, Строка - Имя формы
// 
// Возвращаемое значение:
//  Структура - Конструктор свойства формы:
// * ИмяФормыСтрокой - Строка, ФорматированнаяСтрока - 
// * Элементы - Массив Из см. КонструкторЭлементФормы
Функция КонструкторСвойстваФормы(ИмяФормы = Неопределено) Экспорт
	
	Результат = Новый Структура("ИмяФормыСтрокой, Элементы", "", Новый Массив);
	Возврат Результат
	
КонецФункции

// Конструктор элемент формы.
// 
// Возвращаемое значение:
//  Структура - Конструктор элемент формы:
// * Имя - Строка - 
// * Заголовок - Строка - 
// * Подсказка - Строка - 
// * РасширеннаяПодсказка - Строка - 
// * Тип - Неопределено, Тип - 
Функция КонструкторЭлементФормы() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Имя", "");
	Результат.Вставить("Заголовок", "");
	Результат.Вставить("Подсказка", "");
	Результат.Вставить("РасширеннаяПодсказка", "");
	Результат.Вставить("Тип", Неопределено);
	
	Возврат Результат
	
КонецФункции

// Параметры:
//  Элементы - Массив из см. КонструкторЭлементФормы
//  Элемент - ПолеФормы, ДекорацияФормы, ТаблицаФормы, КнопкаФормы, ГруппаФормы - Элемент
//  ЦветПоясняюшийТекст - Цвет - Цвет поясняюший текст
Процедура ДобавитьЭлементФормы(Элементы, Элемент, ЦветПоясняюшийТекст) Экспорт
	Если ТипЗнч(Элемент) = Тип("ДекорацияФормы")
		Или ТипЗнч(Элемент) = Тип("ПолеФормы")
		Или ТипЗнч(Элемент) = Тип("КнопкаФормы") Тогда
		
		Если ЭлементВиден(Элемент) Тогда
			
			ЭлементФормы = КонструкторЭлементФормы();
			ЭлементФормы.Имя = Элемент.Имя;
			ЭлементФормы.Заголовок = Элемент.Заголовок;
			ЭлементФормы.Тип = ТипЗнч(Элемент);
			
			//@skip-check empty-except-statement
			Попытка
				ЭлементФормы.Подсказка = Новый ФорматированнаяСтрока(Элемент.Подсказка,,ЦветПоясняюшийТекст);
			Исключение
			КонецПопытки;
			
			//@skip-check empty-except-statement
			Попытка
				ЭлементФормы.РасширеннаяПодсказка = Новый ФорматированнаяСтрока(Элемент.РасширеннаяПодсказка.Заголовок,,ЦветПоясняюшийТекст);
			Исключение
			КонецПопытки;
			
			Элементы.Добавить(ЭлементФормы);
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

// Проверяет элементы формы на совпадение со строкой поиска.
// 
// Параметры:
//  СтрокаПоиска - Строка - строка для поиска.
//  ЭлементыФормы - ЭлементыФормы - коллекция элементов формы.
//  ИмяФормы - Строка, ФорматированнаяСтрока - имя формы.
//  ИндексПоиска - Соответствие из КлючИЗначение - кэш индексов поиска по элементам формы.
//
// Возвращаемое значение:
//  Массив из см. НовыйОписаниеРезультата
//
Функция НайтиЭлементыПоСтроке(СтрокаПоиска, ЭлементыФормы, ИмяФормы, ИндексПоиска) Экспорт
	Результат = Новый Массив;
	
	Если ПустаяСтрока(СтрокаПоиска) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для каждого ЭлементФормы Из ЭлементыФормы Цикл
		НайденнаяСтрока = НайтиСтроку(ЭлементФормы, СтрокаПоиска, ИндексПоиска);
		Если НайденнаяСтрока <> Неопределено Тогда
			ДобавитьРезультатПоиска(Результат, ЭлементФормы, НайденнаяСтрока, ИмяФормы);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Новый описание результата.
// 
// Параметры:
//  ЭлементФормы - ГруппаФормы, ТаблицаФормы, ПолеФормы, КнопкаФормы, ДекорацияФормы - Элемент формы
//  НайденнаяСтрока - ФорматированнаяСтрока, Неопределено - Найденная строка
//  ИмяФормы - Строка, ФорматированнаяСтрока - Имя формы
// 
// Возвращаемое значение:
//  Структура - Новый описание результата:
// * ЭлементФормы - ГруппаФормы, ТаблицаФормы, ПолеФормы, КнопкаФормы, ДекорацияФормы - 
// * НайденнаяСтрока - ФорматированнаяСтрока, Неопределено - 
// * ИмяФормы - Строка, ФорматированнаяСтрока - 
Функция НовыйОписаниеРезультата(ЭлементФормы, НайденнаяСтрока, ИмяФормы) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ЭлементФормы", ЭлементФормы);
	Результат.Вставить("НайденнаяСтрока", НайденнаяСтрока);
	Результат.Вставить("ИмяФормы", ИмяФормы);
	Возврат Результат
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РезультатыГлобальногоПоискаНастроек(СтрокаПоиска)
	РезультатыПоиска = Новый Массив;
	
	Для каждого ИмяФормы Из ФормыДляПоиска() Цикл
		РезультатПоискаПоФорме(СтрокаПоиска, ИмяФормы, РезультатыПоиска);
	КонецЦикла;
	
	Возврат РезультатыПоиска;
	
КонецФункции

Процедура РезультатПоискаПоФорме(Знач СтрокаПоиска, ИмяФормы, РезультатыПоиска)
	
	НайденныеЭлементы = НастройкиСистемыКлиентПовтИсп.ЭлементыФормыПоСтроке(СтрокаПоиска, ИмяФормы);
	Для каждого НайденныйЭлемент Из НайденныеЭлементы Цикл
		
		ЭлементФормы = НайденныйЭлемент.ЭлементФормы; // ПолеФормы, ДекорацияФормы, КнопкаФормы - 
		
		РезультатПоиска = НастройкиСистемыКлиентСервер.НовыйРезультатГлобальногоПоиска();
		РезультатПоиска.Значение = ЭлементФормы.Имя;
		
		МассивСтрок = Новый Массив;
		МассивСтрок.Добавить(НайденныйЭлемент.НайденнаяСтрока);
		МассивСтрок.Добавить(Символы.ПС);
		МассивСтрок.Добавить(НайденныйЭлемент.ИмяФормы);
		
		РезультатПоиска.Представление = Новый ФорматированнаяСтрока(МассивСтрок);
		
		Если ЭлементФормы.Тип = Тип("ДекорацияФормы") Тогда
			РезультатПоиска.Картинка = БиблиотекаКартинок.Информация;
		Иначе
			РезультатПоиска.Картинка = БиблиотекаКартинок.Настройка;
		КонецЕсли;
		
		РезультатПоиска.ОписаниеДействия = ИмяФормы;
		
		РезультатыПоиска.Добавить(РезультатПоиска);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьРезультатПоиска(РезультатыПоиска, ЭлементФормы, НайденнаяСтрока, ИмяФормы)
	
	Результат = НовыйОписаниеРезультата(ЭлементФормы, НайденнаяСтрока, ИмяФормы);
	РезультатыПоиска.Добавить(Результат);
	
КонецПроцедуры

Функция НайтиСтроку(ЭлементФормы, СтрокаПоиска, ИндексПоиска)
	
	Результат = Неопределено;
	ИндексПоискаЭлемент = Неопределено;
	
	Если ЗначениеЗаполнено(ИндексПоиска) Тогда
		ИндексПоискаЭлемент = ИндексПоиска.Получить(ЭлементФормы.Имя);
	КонецЕсли;
	
	МассивСтрок = Новый Массив;
	СтрокаНайдена = Ложь;
	Если ЗначениеЗаполнено(ЭлементФормы.Заголовок) Тогда
		НайденнаяСтрока = СтрНайтиИВыделитьОформлением(ЭлементФормы.Заголовок, СтрокаПоиска);
		ДобавитьСтрокуВМассивСтрок(МассивСтрок, НайденнаяСтрока, ЭлементФормы.Заголовок);
		СтрокаНайдена = СтрокаНайдена Или НайденнаяСтрока <> Неопределено;
	ИначеЕсли ЭлементФормы.Тип = Тип("ПолеФормы") Или ЭлементФормы.Тип = Тип("КнопкаФормы") Тогда
		СтрокаПоискаБезПробелов = СтрЗаменить(СтрокаПоиска, " ", "");
		НайденнаяСтрока = СтрНайтиИВыделитьОформлением(ЭлементФормы.Имя, СтрокаПоискаБезПробелов);
		Если НайденнаяСтрока = Неопределено Тогда
			Если СтрНайти(НРег(ЭлементФормы.Имя), НРег(СтрокаПоискаБезПробелов)) > 0 Тогда
				НайденнаяСтрока = ЭлементФормы.Имя;
			КонецЕсли;
		КонецЕсли;
		ДобавитьСтрокуВМассивСтрок(МассивСтрок, НайденнаяСтрока, ЭлементФормы.Имя);
		СтрокаНайдена = СтрокаНайдена Или НайденнаяСтрока <> Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭлементФормы.Подсказка) Тогда
		НайденнаяСтрока = СтрНайтиИВыделитьОформлением(ЭлементФормы.Подсказка, СтрокаПоиска);
		ДобавитьСтрокуВМассивСтрок(МассивСтрок, НайденнаяСтрока, ЭлементФормы.Подсказка);
		СтрокаНайдена = СтрокаНайдена Или НайденнаяСтрока <> Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭлементФормы.РасширеннаяПодсказка) Тогда
		НайденнаяСтрока = СтрНайтиИВыделитьОформлением(ЭлементФормы.РасширеннаяПодсказка, СтрокаПоиска);
		ДобавитьСтрокуВМассивСтрок(МассивСтрок, НайденнаяСтрока, ЭлементФормы.РасширеннаяПодсказка);
		СтрокаНайдена = СтрокаНайдена Или НайденнаяСтрока <> Неопределено;
	КонецЕсли;
	
	Если НЕ СтрокаНайдена И ИндексПоискаЭлемент <> Неопределено Тогда
		НайденнаяСтрока = СтрНайтиИВыделитьОформлением(ИндексПоискаЭлемент, СтрокаПоиска);
		Если НайденнаяСтрока <> Неопределено Тогда
			СтрокаНайдена = Истина;
			
			МассивСтрок.Добавить(Символы.ПС);
			МассивСтрок.Добавить(НайденнаяСтрока);
			
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокаНайдена И МассивСтрок.Количество() > 0 Тогда
		
		Результат = Новый ФорматированнаяСтрока(МассивСтрок);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьСтрокуВМассивСтрок(МассивСтрок, НайденнаяСтрока, СтрокаПоУмолчанию)
	
	Если МассивСтрок.Количество() > 0 Тогда
		МассивСтрок.Добавить(Символы.ПС);
	КонецЕсли;
	
	Если НайденнаяСтрока <> Неопределено Тогда
		МассивСтрок.Добавить(НайденнаяСтрока);
	Иначе
		МассивСтрок.Добавить(СтрокаПоУмолчанию);
	КонецЕсли;
	
КонецПроцедуры

Функция ЭлементВиден(ЭлементФормы)
	
	Видимость = Ложь;
	
	//@skip-check empty-except-statement
	Попытка
		Видимость = ЭлементФормы.Видимость;
	Исключение
	КонецПопытки;
	
	Видимость = Видимость И ПроверитьВидимостьРодителей(ЭлементФормы);
	
	Возврат Видимость;
	
КонецФункции

// Проверить видимость родителей.
// 
// Параметры:
//  Элемент - ДекорацияФормы, КнопкаФормы, ПолеФормы, ГруппаФормы, ФормаКлиентскогоПриложения, ТаблицаФормы - Элемент
// 
// Возвращаемое значение:
//  Булево - признак видимости родителей элемента.
Функция ПроверитьВидимостьРодителей(Элемент)
	Если Элемент.Родитель = Неопределено 
		Или ТипЗнч(Элемент.Родитель) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		Возврат Истина;
	ИначеЕсли Элемент.Родитель.Видимость Тогда
		Возврат ПроверитьВидимостьРодителей(Элемент.Родитель);
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

Функция ФормыДляПоиска()
	
	МассивФорм = Новый Массив;
	Если Не ПользователиКлиент.ЭтоСеансВнешнегоПользователя() Тогда
		МассивФорм.Добавить("ОбщаяФорма.ПерсональныеНастройки");
	КонецЕсли;
	Если ПользователиКлиент.ЭтоПолноправныйПользователь()
		И Не СтандартныеПодсистемыКлиент.ЭтоБазоваяВерсияКонфигурации() Тогда
			
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУТ.Форма.БюджетированиеИПланирование");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУТ.Форма.Закупки");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУТ.Форма.Казначейство");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУТ.Форма.CRMИМаркетинг");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУТ.Форма.НастройкиНоменклатуры");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУТ.Форма.НастройкиРМКИОборудования");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУТ.Форма.НачальноеЗаполнение");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУТ.Форма.Продажи");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУТ.Форма.СкладИДоставка");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУТ.Форма.ФинансовыйРезультатИКонтроллинг");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУТ.Форма.Предприятие");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУТ.Форма.УчетНДСИмпортЭкспорт");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУТ.Форма.ОписанияПроцессов");
		
		//++ НЕ УТ
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияКА.Форма.Производство");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияКА.Форма.ВнеоборотныеАктивы");
		//-- НЕ УТ
		
		//++ НЕ УТКА
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияУП.Форма.МеждународныйФинансовыйУчет");
		//-- НЕ УТКА
		
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияБСП.Форма.Обслуживание");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияБСП.Форма.ОбщиеНастройки");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияБСП.Форма.НастройкиПользователейИПрав");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияБСП.Форма.Органайзер");
		
		Если ОбщегоНазначенияКлиент.ДоступноИспользованиеРазделенныхДанных() Тогда
			ИмяОбработки      = "ПанельАдминистрированияБСП";
			ИмяФормыОбработки = "СинхронизацияДанных";
		Иначе
			ИмяОбработки      = "ПанельАдминистрированияБСПВМоделиСервиса";
			ИмяФормыОбработки = "СинхронизацияДанныхДляАдминистратораСервиса";
		КонецЕсли;
		ИмяОткрываемойФормы = "Обработка.[ИмяОбработки].Форма.[ИмяФормыОбработки]";
		ИмяОткрываемойФормы = СтрЗаменить(ИмяОткрываемойФормы, "[ИмяОбработки]", ИмяОбработки);
		ИмяОткрываемойФормы = СтрЗаменить(ИмяОткрываемойФормы, "[ИмяФормыОбработки]", ИмяФормыОбработки);
		МассивФорм.Добавить(ИмяОткрываемойФормы);
		
		ИмяФормы = "Обработка.ПанельАдминистрированияБСП.Форма.НастройкиРаботыСФайлами";
		Если Не ОбщегоНазначенияКлиент.ДоступноИспользованиеРазделенныхДанных() Тогда
			ИмяФормы = ИмяФормы + "ВМоделиСервиса";
		КонецЕсли;
		МассивФорм.Добавить(ИмяФормы);
		
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияБСП.Форма.ПечатныеФормыОтчетыИОбработки");
		
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияБИД.Форма.НастройкаИнтеграцииС1СДокументооборотомПереопределяемый");
		
	КонецЕсли;
	
	Если ПользователиКлиент.ЭтоПолноправныйПользователь()
		И СтандартныеПодсистемыКлиент.ЭтоБазоваяВерсияКонфигурации() Тогда
		
		МассивФорм.Добавить("ОбщаяФорма.ФормаНастройкиОрганизации");
		МассивФорм.Добавить("ОбщаяФорма.ФормаНастройкиРазделаЗакупки");
		МассивФорм.Добавить("ОбщаяФорма.ФормаНастройкиРазделаПродажи");
		МассивФорм.Добавить("ОбщаяФорма.ФормаНастройкиРазделаСклад");
		МассивФорм.Добавить("Обработка.ПанельАдминистрированияБСП.Форма.НастройкиПользователейИПрав");
		
	КонецЕсли;
	
	НастройкиСистемыКлиентЛокализация.ДополнитьФормыДляПоиска(МассивФорм);
	Возврат МассивФорм
	
КонецФункции

Функция СтрокаПоискаСоответствуетТребованиям(Знач СтрокаПоиска)
	
	Если ТипЗнч(СтрокаПоиска) <> Тип("Строка")
		Или СтрДлина(СтрокаПоиска) < 4
		Или СтрНачинаетсяС(СтрокаПоиска, "e1c") Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
