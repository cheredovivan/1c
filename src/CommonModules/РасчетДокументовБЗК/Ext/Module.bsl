
#Область ПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

функция ОписаниеДокумента() Экспорт
	Описание = Новый Структура();
	Описание.Вставить("МесяцНачисленияИмя", "МесяцНачисления");
	Описание.Вставить("НачисленияПерерасчетИмя", "");
	Описание.Вставить("ДатаНачалаСобытияИмя", "");
	Описание.Вставить("ВидНачисленияВШапке", Ложь);
	Описание.Вставить("ВидНачисленияИмя", Неопределено);
	Описание.Вставить("СпособыРасчетаСреднегоЗаработка", Неопределено);
	
	Возврат Описание;
КонецФункции

Функция ОписаниеТаблицыРасчета() Экспорт
	Описание = Новый Структура();	
	Описание.Вставить("ИмяТаблицы", "");
	Описание.Вставить("НомерТаблицы", 0);
	Описание.Вставить("СодержитПолеВидРасчета", Истина);
	Описание.Вставить("ИмяРеквизитаВидРасчета", "Начисление");
	Описание.Вставить("ИмяРеквизитаПериод", "МесяцНачисления");
	Описание.Вставить("ИмяРеквизитаДокументОснование", "");
	Описание.Вставить("ИмяРеквизитаИдентификаторСтроки", "ИдентификаторСтрокиВидаРасчета");
	
	Возврат Описание;
КонецФункции

Функция ТаблицыНачисленийДокумента() Экспорт

	ТаблицыНачислений = Новый Структура(
		"Начисления,
		|НачисленияПерерасчет,
		|НачисленияПерерасчетНулевыеСторно,
		|Пособия,
		|ПособияПерерасчет,
		|Льготы,
		|ЛьготыПерерасчет,
		|УправленческиеНачисления,
		|ЗависимыеНачисления,
		|Показатели,
		|РаспределениеПоСтатьям,
		|РаспределениеПоТерриториямУсловиямТруда");
		
	Возврат ТаблицыНачислений;
	
КонецФункции

Функция ТаблицыУдержанийДокумента() Экспорт

	ТаблицыУдержаний = Новый Структура(
		"Удержания,
		|УдержанияПерерасчет,
		|РаспределениеПоСтатьям,
		|Показатели");
		
	Возврат ТаблицыУдержаний;
	
КонецФункции

Функция ТаблицыНДФЛДокумента() Экспорт

	ТаблицыУдержаний = Новый Структура(
		"НДФЛ,
		|Вычеты,
		|УчтенныеДокументы,
		|РаспределениеПоСтатьям");
		
	Возврат ТаблицыУдержаний;
	
КонецФункции

Функция ОписаниеТаблицУдержанийДокумента() Экспорт

	ТаблицыУдержаний = Новый Структура(
		"Удержания,
		|УдержанияПерерасчет");
		
	Возврат ТаблицыУдержаний;
	
КонецФункции

Функция ОписаниеТаблицНачисленийДокумента() Экспорт

	ТаблицыНачислений = Новый Структура(
		"Начисления,
		|НачисленияПерерасчет,
		|НачисленияПерерасчетНулевыеСторно,
		|Пособия,
		|ПособияПерерасчет,
		|Льготы,
		|ЛьготыПерерасчет,
		|УправленческиеНачисления,
		|ЗависимыеНачисления");
		
	Возврат ТаблицыНачислений;
	
КонецФункции

Процедура РасчетЗарплатыНачисленияВДанныеОбъекта(ТаблицыНачислений, ТаблицаИсточник, Организация, ОписанияТаблиц, МесяцНачисления = Неопределено, РежимДоначисления = Ложь, ПозицииВставки = Неопределено) Экспорт
	
	ВидыРасчета = Новый Массив;
	Для Каждого СтрокаИсточника Из ТаблицаИсточник Цикл
		ВидыРасчета.Добавить(СтрокаИсточника.Начисление);
	КонецЦикла;
	
	ВидыРасчетаИнфо = ЗарплатаКадрыРасширенный.ИнформацияОВидахРасчета(ВидыРасчета);
	
	ПозицииСотрудников = Новый Соответствие;
	СоответствиеОбходовСотрудников = Новый Соответствие;	
	
	Если ПозицииВставки <> Неопределено Тогда
		Для Каждого КлючИЗначение Из ТаблицыНачислений Цикл
			Если КлючИЗначение.Значение = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ПозицииСотрудниковТаблицы = Неопределено;
			Если Не ПозицииВставки.Свойство(КлючИЗначение.Ключ, ПозицииСотрудниковТаблицы) Тогда
				ПозицииСотрудниковТаблицы = Новый Соответствие;
				ПозицииВставки.Вставить(КлючИЗначение.Ключ, ПозицииСотрудниковТаблицы);
			КонецЕсли;
			ПозицииСотрудников.Вставить(КлючИЗначение.Ключ, ПозицииСотрудниковТаблицы);
			СоответствиеОбходовСотрудников.Вставить(КлючИЗначение.Ключ, Новый Соответствие);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого СтрокаНачисления Из ТаблицаИсточник Цикл
		
		ЭтоПерерасчет = РежимДоначисления Или РасчетЗарплатыРасширенныйФормы.ЭтоСтрокаПерерасчета(СтрокаНачисления, МесяцНачисления);
		ЭтоПособие = РасчетЗарплатыРасширенныйФормы.ЭтоСтрокаПособия(СтрокаНачисления);
		ЭтоНулевоеСторно = СтрокаНачисления.ФиксСторно
			И СтрокаНачисления.Сторно
			И СтрокаНачисления.Результат = 0
			И СтрокаНачисления.ОплаченоДней = 0
			И СтрокаНачисления.ОплаченоЧасов = 0;
			
		ЭтоЛьготы = Ложь;
		ЭтоУправленческиеНачисления = Ложь;
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников")
			И ТаблицыНачислений.Льготы <> Неопределено Тогда
			
			Модуль = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
			ЭтоЛьготы = Модуль.ЭтоСтрокаЛьготы(СтрокаНачисления, ВидыРасчетаИнфо);
		КонецЕсли;
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата")
			И ТаблицыНачислений.УправленческиеНачисления <> Неопределено Тогда
			
			Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
			ЭтоУправленческиеНачисления = Модуль.ЭтоСтрокаУправленческиеНачисления(СтрокаНачисления);
		КонецЕсли;
		

		Если ТаблицыНачислений["ПособияПерерасчет"] <> Неопределено
			И (СтрокаНачисления.Владелец = "ПособияПерерасчет"
				Или ЭтоПособие И ЭтоПерерасчет И Не ЗначениеЗаполнено(СтрокаНачисления.Владелец)) Тогда
			
			ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйФормы.ОбходТаблицыИдентификаторов(СоответствиеОбходовСотрудников["ПособияПерерасчет"], ПозицииСотрудников["ПособияПерерасчет"], СтрокаНачисления.Сотрудник);
			СтрокаТаблицы = СтрокаТаблицыПоНачальнойПозиции(ТаблицыНачислений.ПособияПерерасчет, ОписанияТаблиц.ПособияПерерасчет, ОбходТаблицыИдентификаторов);
			СтрокаПособияВДанныеОбъекта(
				Организация, СтрокаТаблицы, 
				СтрокаНачисления, 
				ТаблицыНачислений, 
				ВидыРасчетаИнфо, 
				ОписанияТаблиц.ПособияПерерасчет);
			Продолжить;
		КонецЕсли;
		
		Если ТаблицыНачислений["Пособия"] <> Неопределено
			И (СтрокаНачисления.Владелец = "Пособия"
				Или ЭтоПособие И Не ЗначениеЗаполнено(СтрокаНачисления.Владелец)) Тогда
			
			ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйФормы.ОбходТаблицыИдентификаторов(СоответствиеОбходовСотрудников["Пособия"], ПозицииСотрудников["Пособия"], СтрокаНачисления.Сотрудник);
			СтрокаТаблицы = СтрокаТаблицыПоНачальнойПозиции(ТаблицыНачислений.Пособия, ОписанияТаблиц.Пособия, ОбходТаблицыИдентификаторов);
			СтрокаПособияВДанныеОбъекта(
				Организация, СтрокаТаблицы, 
				СтрокаНачисления, 
				ТаблицыНачислений, 
				ВидыРасчетаИнфо, 
				ОписанияТаблиц.Пособия);
			Продолжить;
		КонецЕсли;
		
		Если ТаблицыНачислений["НачисленияПерерасчетНулевыеСторно"] <> Неопределено
			И (СтрокаНачисления.Владелец = "НачисленияПерерасчетНулевыеСторно"
				Или ЭтоНулевоеСторно) Тогда
		
			ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйФормы.ОбходТаблицыИдентификаторов(СоответствиеОбходовСотрудников["НачисленияПерерасчетНулевыеСторно"], ПозицииСотрудников["НачисленияПерерасчетНулевыеСторно"], СтрокаНачисления.Сотрудник);
			СтрокаТаблицы = СтрокаТаблицыПоНачальнойПозиции(ТаблицыНачислений.НачисленияПерерасчетНулевыеСторно, ОписанияТаблиц.НачисленияПерерасчетНулевыеСторно, ОбходТаблицыИдентификаторов);
			СтрокаНачисленияВДанныеОбъекта(Организация, СтрокаТаблицы, СтрокаНачисления, ТаблицыНачислений, ВидыРасчетаИнфо, ОписанияТаблиц.НачисленияПерерасчетНулевыеСторно);
			Продолжить;
		КонецЕсли;
		
		Если ТаблицыНачислений["ЛьготыПерерасчет"] <> Неопределено
			И ЭтоЛьготы	И ЭтоПерерасчет И Не ЗначениеЗаполнено(СтрокаНачисления.Владелец) Тогда
			
			ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйФормы.ОбходТаблицыИдентификаторов(СоответствиеОбходовСотрудников["ЛьготыПерерасчет"], ПозицииСотрудников["ЛьготыПерерасчет"], СтрокаНачисления.Сотрудник);
			СтрокаТаблицы = СтрокаТаблицыПоНачальнойПозиции(ТаблицыНачислений.ЛьготыПерерасчет, ОписанияТаблиц.ЛьготыПерерасчет, ОбходТаблицыИдентификаторов);
			СтрокаЛьготыВДанныеОбъекта(Организация, СтрокаТаблицы, СтрокаНачисления, ТаблицыНачислений, ВидыРасчетаИнфо, ОписанияТаблиц.ЛьготыПерерасчет);
			Продолжить;
		КонецЕсли;
		
		Если ТаблицыНачислений["Льготы"] <> Неопределено
			И ЭтоЛьготы Тогда
			
			ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйФормы.ОбходТаблицыИдентификаторов(СоответствиеОбходовСотрудников["Льготы"], ПозицииСотрудников["Льготы"], СтрокаНачисления.Сотрудник);
			СтрокаТаблицы = СтрокаТаблицыПоНачальнойПозиции(ТаблицыНачислений.Льготы, ОписанияТаблиц.Льготы, ОбходТаблицыИдентификаторов);
			СтрокаЛьготыВДанныеОбъекта(Организация, СтрокаТаблицы, СтрокаНачисления, ТаблицыНачислений, ВидыРасчетаИнфо, ОписанияТаблиц.Льготы);
			Продолжить;
		КонецЕсли;
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") 
			И ТаблицыНачислений["УправленческиеНачисления"] <> Неопределено
			И ЭтоУправленческиеНачисления Тогда
			
			ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйФормы.ОбходТаблицыИдентификаторов(СоответствиеОбходовСотрудников["УправленческиеНачисления"], ПозицииСотрудников["УправленческиеНачисления"], СтрокаНачисления.Сотрудник);
			СтрокаТаблицы = СтрокаТаблицыПоНачальнойПозиции(ТаблицыНачислений.УправленческиеНачисления, ОписанияТаблиц.УправленческиеНачисления, ОбходТаблицыИдентификаторов);
			Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
			Модуль.СтрокаНачисленияВДанныеОбъекта(СтрокаТаблицы, СтрокаНачисления, ТаблицыНачислений, ВидыРасчетаИнфо, ОписанияТаблиц.УправленческиеНачисления);
			Продолжить;
		КонецЕсли;
		
		Если ТаблицыНачислений["НачисленияПерерасчет"] <> Неопределено
			И (СтрокаНачисления.Владелец = "НачисленияПерерасчет"
				Или ЭтоПерерасчет И Не ЗначениеЗаполнено(СтрокаНачисления.Владелец)) Тогда
				
			ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйФормы.ОбходТаблицыИдентификаторов(СоответствиеОбходовСотрудников["НачисленияПерерасчет"], ПозицииСотрудников["НачисленияПерерасчет"], СтрокаНачисления.Сотрудник);
			СтрокаТаблицы = СтрокаТаблицыПоНачальнойПозиции(ТаблицыНачислений.НачисленияПерерасчет, ОписанияТаблиц.НачисленияПерерасчет, ОбходТаблицыИдентификаторов);
			СтрокаНачисленияВДанныеОбъекта(Организация, СтрокаТаблицы, СтрокаНачисления, ТаблицыНачислений, ВидыРасчетаИнфо, ОписанияТаблиц.НачисленияПерерасчет);
			Продолжить;
		КонецЕсли;
		
		Если СтрокаНачисления.РассчитыватьПоРазовымНачислениямДокумента 
			И ТаблицыНачислений.Свойство("ЗависимыеНачисления") 
			И ТаблицыНачислений.ЗависимыеНачисления <> Неопределено Тогда
			
			ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйФормы.ОбходТаблицыИдентификаторов(СоответствиеОбходовСотрудников["ЗависимыеНачисления"], ПозицииСотрудников["ЗависимыеНачисления"], СтрокаНачисления.Сотрудник);
			СтрокаТаблицы = СтрокаТаблицыПоНачальнойПозиции(ТаблицыНачислений.ЗависимыеНачисления, ОписанияТаблиц.ЗависимыеНачисления, ОбходТаблицыИдентификаторов);
			СтрокаНачисленияВДанныеОбъекта(Организация, СтрокаТаблицы, СтрокаНачисления, ТаблицыНачислений, ВидыРасчетаИнфо, ОписанияТаблиц.ЗависимыеНачисления);
			
			Продолжить;
		КонецЕсли;	
		
		ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйФормы.ОбходТаблицыИдентификаторов(СоответствиеОбходовСотрудников["Начисления"], ПозицииСотрудников["Начисления"], СтрокаНачисления.Сотрудник);
		СтрокаТаблицы = СтрокаТаблицыПоНачальнойПозиции(ТаблицыНачислений.Начисления, ОписанияТаблиц.Начисления, ОбходТаблицыИдентификаторов);
		СтрокаНачисленияВДанныеОбъекта(Организация, СтрокаТаблицы, СтрокаНачисления, ТаблицыНачислений, ВидыРасчетаИнфо, ОписанияТаблиц.Начисления);
		
	КонецЦикла;
	
	Для Каждого КлючИЗначение Из ТаблицыНачислений Цикл
		Если КлючИЗначение.Значение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		УдалитьНеактуальныеСтрокиФормы(КлючИЗначение.Значение, СоответствиеОбходовСотрудников[КлючИЗначение.Ключ], ПозицииСотрудников[КлючИЗначение.Ключ]);

	КонецЦикла;
	
КонецПроцедуры

Процедура РасчетЗарплатыУдержанияВДанныеОбъекта(ТаблицыУдержаний, ТаблицаИсточник, ОписанияТаблиц, МесяцНачисления = Неопределено, РежимДоначисления = Ложь, ПозицииВставки = Неопределено) Экспорт
	
	ВидыРасчета = Новый Массив;
	Для Каждого СтрокаИсточника Из ТаблицаИсточник Цикл
		ВидыРасчета.Добавить(СтрокаИсточника.Удержание);
	КонецЦикла;
	
	ВидыРасчетаИнфо = ЗарплатаКадрыРасширенный.ИнформацияОВидахРасчета(ВидыРасчета);
	
	ПозицииФизическихЛиц = Новый Соответствие;
	СоответствиеОбходовФизическихЛиц = Новый Соответствие;	
	
	Если ПозицииВставки <> Неопределено Тогда
		Для Каждого КлючИЗначение Из ТаблицыУдержаний Цикл
			Если КлючИЗначение.Значение = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ПозицииФизическихЛицТаблицы = Неопределено;
			Если Не ПозицииВставки.Свойство(КлючИЗначение.Ключ, ПозицииФизическихЛицТаблицы) Тогда
				ПозицииФизическихЛицТаблицы = Новый Соответствие;
				ПозицииВставки.Вставить(КлючИЗначение.Ключ, ПозицииФизическихЛицТаблицы);
			КонецЕсли;
			ПозицииФизическихЛиц.Вставить(КлючИЗначение.Ключ, ПозицииФизическихЛицТаблицы);
			СоответствиеОбходовФизическихЛиц.Вставить(КлючИЗначение.Ключ, Новый Соответствие);
		КонецЦикла;
	КонецЕсли;
			
	Для Каждого СтрокаИсточник Из ТаблицаИсточник Цикл
		ЭтоПерерасчет = РежимДоначисления 
			Или РасчетЗарплатыРасширенныйФормы.ЭтоСтрокаПерерасчетаУдержаний(СтрокаИсточник, МесяцНачисления);
		Если ТаблицыУдержаний.УдержанияПерерасчет <> Неопределено 
			И ЭтоПерерасчет
			Или СтрокаИсточник.Владелец = "УдержанияПерерасчет" Тогда
			
			ИмяТаблицы = "УдержанияПерерасчет";
		Иначе
			ИмяТаблицы = "Удержания";
		КонецЕсли;
		
		ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйФормы.ОбходТаблицыИдентификаторов(СоответствиеОбходовФизическихЛиц[ИмяТаблицы], ПозицииФизическихЛиц[ИмяТаблицы], СтрокаИсточник.ФизическоеЛицо);
		СтрокаТаблицы = СтрокаТаблицыПоНачальнойПозиции(ТаблицыУдержаний[ИмяТаблицы], ОписанияТаблиц[ИмяТаблицы], ОбходТаблицыИдентификаторов);
		СтрокаУдержанияВДанныеОбъекта(СтрокаТаблицы, СтрокаИсточник, ТаблицыУдержаний, ВидыРасчетаИнфо, ОписанияТаблиц[ИмяТаблицы])
	КонецЦикла;
	
	Для Каждого КлючИЗначение Из ТаблицыУдержаний Цикл
		Если КлючИЗначение.Значение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		УдалитьНеактуальныеСтрокиФормы(КлючИЗначение.Значение, СоответствиеОбходовФизическихЛиц[КлючИЗначение.Ключ], ПозицииФизическихЛиц[КлючИЗначение.Ключ]);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура РасчетЗарплатыНДФЛВДанныеОбъекта(ТаблицыНДФЛ, ТаблицаИсточник, ОписаниеТаблицыНДФЛ, ДокументСсылка = Неопределено, ПозицииВставки = Неопределено) Экспорт
	ФизическиеЛица = Новый Соответствие;
	
	ОписаниеКолонок = УчетНДФЛ.ОписаниеКолонокТаблицыНДФЛ(ТаблицаИсточник);
	ОписаниеКолонок.КолонкиПоиска = СтрЗаменить(ОписаниеКолонок.КолонкиПоиска,",КодВычетаЛичный","");
	СтруктураПоискаВычетов = Новый Структура(ОписаниеКолонок.КолонкиПоиска);
	ОбщегоНазначенияБЗК.ДобавитьИндексКоллекции(ТаблицаИсточник, ОписаниеКолонок.КолонкиПоиска);
	
	ИсточникСИндексами = ТаблицаИсточник.Скопировать();
	ИсточникСИндексами.Индексы.Добавить(ОписаниеКолонок.КолонкиСвязиТаблиц);
	ИсточникСИндексами.Индексы.Добавить(ОписаниеКолонок.КолонкиПоиска);
	НДФЛ = УчетНДФЛ.ТаблицаНДФЛбезМесяцаПериодаПредоставленияВычета(ИсточникСИндексами, ОписаниеКолонок);	
	
	ПозицииФизическихЛицНДФЛ = Неопределено;
	ПозицииФизическихЛицВычетов = Неопределено;
	СоответствиеОбходовФизическихЛиц = Неопределено;
	
	Если ПозицииВставки <> Неопределено Тогда
		Если Не ПозицииВставки.Свойство("НДФЛ", ПозицииФизическихЛицНДФЛ) Тогда
			ПозицииФизическихЛицНДФЛ = Новый Соответствие;
			ПозицииВставки.Вставить("НДФЛ", ПозицииФизическихЛицНДФЛ);
		КонецЕсли;
		ПозицииФизическихЛицВычетов = Новый Соответствие;
		ПозицииВставки.Вставить("ПримененныеВычетыНаДетейИИмущественные", ПозицииФизическихЛицВычетов);
		СоответствиеОбходовФизическихЛиц = Новый Соответствие;
	КонецЕсли;
	
	Для Каждого СтрокаИсточник Из НДФЛ Цикл
		ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйФормы.ОбходТаблицыИдентификаторов(СоответствиеОбходовФизическихЛиц, ПозицииФизическихЛицНДФЛ, СтрокаИсточник.ФизическоеЛицо);
		
		СтрокаНДФЛ = СтрокаТаблицыПоНачальнойПозиции(ТаблицыНДФЛ.НДФЛ, ОписаниеТаблицыНДФЛ, ОбходТаблицыИдентификаторов);
		
		ЗаполнитьЗначенияСвойств(СтруктураПоискаВычетов,СтрокаИсточник);
		СтрокиВычетов = ИсточникСИндексами.НайтиСтроки(СтруктураПоискаВычетов);
		СтрокаНДФЛВДанныеОбъекта(СтрокаНДФЛ, СтрокаИсточник, СтрокиВычетов, ТаблицыНДФЛ, ОписаниеТаблицыНДФЛ);
		
		Если ТаблицыНДФЛ.УчтенныеДокументы <> Неопределено Тогда
			ДокументыФизическихЛиц = ФизическиеЛица[СтрокаИсточник.ФизическоеЛицо];
			Если ДокументыФизическихЛиц = Неопределено Тогда 
				ДокументыФизическихЛиц = Новый Соответствие;
		        ФизическиеЛица.Вставить(СтрокаИсточник.ФизическоеЛицо, ДокументыФизическихЛиц);
			КонецЕсли;
			Для Каждого ДокументОснование Из СтрокаИсточник.УчтенныеОснования Цикл 
				Если ЗначениеЗаполнено(ДокументОснование) И ДокументОснование <> ДокументСсылка Тогда 
					ДокументыФизическихЛиц.Вставить(ДокументОснование, Истина);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла; 
	
	УдалитьНеактуальныеСтрокиФормы(ТаблицыНДФЛ.НДФЛ, СоответствиеОбходовФизическихЛиц, ПозицииФизическихЛицНДФЛ, ОписаниеТаблицыНДФЛ);
	
	Если ТаблицыНДФЛ.УчтенныеДокументы <> Неопределено Тогда
		ПозицииФизическихЛиц = Неопределено;
		Если ПозицииВставки <> Неопределено Тогда
			ПозицииФизическихЛиц = Новый Соответствие;
			ПозицииВставки.Вставить("УчтенныеПриРасчетеНДФЛДокументы", ПозицииФизическихЛиц);
		КонецЕсли;
		Для Каждого ДанныеФизическогоЛица Из ФизическиеЛица Цикл
			ФизическоеЛицо = ДанныеФизическогоЛица.Ключ;
			ДокументыФизическихЛиц = ДанныеФизическогоЛица.Значение;
			Для Каждого ДанныеДокумента Из ДокументыФизическихЛиц Цикл 
				СтрокаДокумента = ТаблицыНДФЛ.УчтенныеДокументы.Добавить();
				СтрокаДокумента.ФизическоеЛицо = ФизическоеЛицо;
				СтрокаДокумента.ДокументОснование = ДанныеДокумента.Ключ;
				Если ПозицииВставки <> Неопределено Тогда
					РасчетЗарплатыРасширенныйКлиентСервер.ДобавитьИдентификаторСтрокиСотрудника(
						ПозицииФизическихЛиц, ФизическоеЛицо, СтрокаДокумента.ПолучитьИдентификатор());
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура РасчетЗарплатыПогашениеЗаймовВДанныеОбъекта(ТаблицаПриемник, ТаблицаИсточник, ТаблицаРаспределенияПоСтатьям, ОписаниеТаблицы) Экспорт
	Для Каждого СтрокаИсточник Из ТаблицаИсточник Цикл
		СтрокаДокумента = ДобавитьСтрокуТаблицыСИдентефикатором(ТаблицаПриемник, ОписаниеТаблицы);
		СтрокаПогашениеЗаймовВДанныеОбъекта(СтрокаДокумента, СтрокаИсточник, ОписаниеТаблицы, ТаблицаРаспределенияПоСтатьям);
	КонецЦикла; 
КонецПроцедуры

Процедура РасчетЗарплатыКорректировкиВыплатыВДанныеОбъекта(ТаблицаПриемник, ТаблицаИсточник, ТаблицаРаспределенияПоСтатьям, ОписаниеТаблицы, ПозицииВставки = Неопределено) Экспорт

	ПозицииФизическихЛиц = Неопределено;
	СоответствиеОбходовФизическихЛиц = Неопределено;
	
	Если ПозицииВставки <> Неопределено Тогда
		Если Не ПозицииВставки.Свойство("КорректировкиВыплаты", ПозицииФизическихЛиц) Тогда
			ПозицииФизическихЛиц = Новый Соответствие;
			ПозицииВставки.Вставить("КорректировкиВыплаты", ПозицииФизическихЛиц);
		КонецЕсли;
		СоответствиеОбходовФизическихЛиц = Новый Соответствие;
	КонецЕсли;
			
	Для Каждого СтрокаИсточник Из ТаблицаИсточник Цикл
		ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйФормы.ОбходТаблицыИдентификаторов(СоответствиеОбходовФизическихЛиц, ПозицииФизическихЛиц, СтрокаИсточник.ФизическоеЛицо);
		СтрокаДокумента = СтрокаТаблицыПоНачальнойПозиции(ТаблицаПриемник, ОписаниеТаблицы, ОбходТаблицыИдентификаторов);
		СтрокаКорректировкиВыплатыВДанныеОбъекта(СтрокаДокумента, СтрокаИсточник, ОписаниеТаблицы, ТаблицаРаспределенияПоСтатьям);
	КонецЦикла;
	
	УдалитьНеактуальныеСтрокиФормы(ТаблицаПриемник, СоответствиеОбходовФизическихЛиц, ПозицииФизическихЛиц);
	
КонецПроцедуры

Процедура УстановитьВторогоОтветственногоВМногофункциональныхДокументах(Документ) Экспорт 
	ОписаниеДокумента = МногофункциональныеДокументыБЗК.ОписаниеМногофункциональногоДокументаПредыдущийФормат(Документ.Ссылка);

	Если Документ[ОписаниеДокумента.РеквизитСостояние] Тогда 
		Если Не ЗначениеЗаполнено(Документ[ОписаниеДокумента.ВторойОтветственный]) Тогда 
			Документ[ОписаниеДокумента.ВторойОтветственный] = Пользователи.ТекущийПользователь();
		КонецЕсли;	
	Иначе 
		Если ЗначениеЗаполнено(Документ[ОписаниеДокумента.ВторойОтветственный]) Тогда 
			Документ[ОписаниеДокумента.ВторойОтветственный] = Неопределено;
		КонецЕсли;	
	КонецЕсли;
КонецПроцедуры	

Процедура НачисленияВМенеджерРасчета(ТаблицаНачислений, 
									МенеджерРасчета, 
									ТаблицыДокумента, 
									Организация, 
									ИмяТаблицы = "Начисления", 
									ВидНачисления = Неопределено, 
									Отбор = Неопределено, 
									ПозицииВставки = Неопределено, 
									БазовыйПериод = Неопределено) Экспорт
	
	ИдентификаторыСтрокСотрудников = Новый Соответствие;
	ФизическоеЛицо = Неопределено;
	
	Если Отбор = Неопределено Тогда
		Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаНачислений, "Сотрудник");
		ФизическиеЛицаСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Сотрудники, "ФизическоеЛицо");
	Иначе
		ФизическиеЛицаСотрудников = Отбор.ФизическиеЛицаСотрудников;
	КонецЕсли;
	
	Для Каждого СтрокаНачислений Из ТаблицаНачислений Цикл
		
		ФизическоеЛицо = ФизическиеЛицаСотрудников[СтрокаНачислений.Сотрудник];
		Если Отбор <> Неопределено Тогда 
			Если ФизическоеЛицо = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Идентификатор = ТаблицаНачислений.Индекс(СтрокаНачислений);
			РасчетЗарплатыРасширенныйКлиентСервер.ДобавитьИдентификаторСтрокиСотрудника(ИдентификаторыСтрокСотрудников, СтрокаНачислений.Сотрудник, Идентификатор);
			
		КонецЕсли;

		СтрокаНачисленияВМенеджерРасчета(
			МенеджерРасчета, 
			СтрокаНачислений, 
			Организация, 
			ФизическоеЛицо, 
			ТаблицыДокумента,
			ИмяТаблицы, 
			ВидНачисления,
			БазовыйПериод);
			
	КонецЦикла;
		
	Если Отбор = Неопределено Тогда
		ТаблицаНачислений.Очистить();
	КонецЕсли;
	
	Если ПозицииВставки <> Неопределено Тогда
		ПозицииВставки.Вставить(ИмяТаблицы, ИдентификаторыСтрокСотрудников);
	КонецЕсли;
		
КонецПроцедуры

Процедура НачисленияПерерасчетСНулевымиСторноМенеджерРасчета(ТаблицаИсточник, ТаблицаДополнение, МенеджерРасчета, ТаблицыДокумента, Организация) Экспорт
	НачисленияВМенеджерРасчета(ТаблицаДополнение, МенеджерРасчета, ТаблицыДокумента, Организация, "НачисленияПерерасчетНулевыеСторно");
	НачисленияВМенеджерРасчета(ТаблицаИсточник, МенеджерРасчета, ТаблицыДокумента, Организация, "НачисленияПерерасчет");
КонецПроцедуры

Процедура УдержанияВМенеджерРасчета(ТаблицаИсточник, МенеджерРасчета, ТаблицыДокумента, ИмяТаблицы = "Удержания", Отбор = Неопределено, ПозицииВставки = Неопределено) Экспорт

	ИдентификаторыСтрокФизическихЛиц = Новый Соответствие;
	ВидыРасчета = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаИсточник, "Удержание");
	КатегорииУдержаний = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ВидыРасчета, "КатегорияУдержания");
	
	Для Каждого СтрокаТаблицы Из ТаблицаИсточник Цикл
		
		Если Отбор <> Неопределено Тогда
			ФизическоеЛицо = Отбор.ФизическиеЛицаОтбор[СтрокаТаблицы.ФизическоеЛицо];
			Если ФизическоеЛицо = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Идентификатор = ТаблицаИсточник.Индекс(СтрокаТаблицы);
			РасчетЗарплатыРасширенныйКлиентСервер.ДобавитьИдентификаторСтрокиСотрудника(ИдентификаторыСтрокФизическихЛиц, СтрокаТаблицы.ФизическоеЛицо, Идентификатор);
		КонецЕсли;

		СтрокаУдержанияВДанныеМенеджераРасчета(
			МенеджерРасчета, 
			СтрокаТаблицы, 
			КатегорииУдержаний, 
			ТаблицыДокумента, 
			ИмяТаблицы);
	КонецЦикла;
		
	Если Отбор = Неопределено Тогда
		ТаблицаИсточник.Очистить();
	КонецЕсли;
	
	Если ПозицииВставки <> Неопределено Тогда
		ПозицииВставки.Вставить(ИмяТаблицы, ИдентификаторыСтрокФизическихЛиц);
	КонецЕсли;
	
КонецПроцедуры

Процедура КорректировкиВыплатыВМенеджерРасчета(ТаблицаИсточник, МенеджерРасчета, РаспределениеПоСтатьям = Неопределено, Отбор = Неопределено, ПозицииВставки = Неопределено) Экспорт
	
	ИдентификаторыСтрокФизическихЛиц = Новый Соответствие;
	
	Для Каждого СтрокаТаблицы Из ТаблицаИсточник Цикл
		Если Отбор <> Неопределено Тогда
			ФизическоеЛицо = Отбор.ФизическиеЛицаОтбор[СтрокаТаблицы.ФизическоеЛицо];
			Если ФизическоеЛицо = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Идентификатор = ТаблицаИсточник.Индекс(СтрокаТаблицы);
			РасчетЗарплатыРасширенныйКлиентСервер.ДобавитьИдентификаторСтрокиСотрудника(ИдентификаторыСтрокФизическихЛиц, СтрокаТаблицы.ФизическоеЛицо, Идентификатор);
		КонецЕсли;
		СтрокаКорректировкиВыплатыВДанныеМенеджераРасчета(МенеджерРасчета, СтрокаТаблицы, РаспределениеПоСтатьям);
	КонецЦикла;
	
	Если Отбор = Неопределено Тогда
		ТаблицаИсточник.Очистить();
	КонецЕсли;
	
	Если ПозицииВставки <> Неопределено Тогда
		ПозицииВставки.Вставить("КорректировкиВыплаты", ИдентификаторыСтрокФизическихЛиц);
	КонецЕсли;

КонецПроцедуры

Процедура ПогашениеЗаймовВДанныеМенеджераРасчета(ТаблицаИсточник, МенеджерРасчета, РаспределениеПоСтатьям = Неопределено) Экспорт
	Для Каждого СтрокаТаблицы Из ТаблицаИсточник Цикл
		СтрокаКорректировкиВыплатыВДанныеМенеджераРасчета(МенеджерРасчета, СтрокаТаблицы, РаспределениеПоСтатьям);
	КонецЦикла;
КонецПроцедуры

Процедура НДФЛВДанныеМенеджераРасчета(ТаблицыНДФЛДокумента, МенеджерРасчета, Отбор = Неопределено, ПозицииВставки = Неопределено) Экспорт
	
	НДФЛ = МенеджерРасчета.Зарплата.НДФЛ;
	СтрокиВычетовКУдалению = Новый Массив;
	ИдентификаторыСтрокФизическихЛиц = Новый Соответствие;
	
	Для Каждого СтрокаТаблицы Из ТаблицыНДФЛДокумента.НДФЛ Цикл
		
		Если Отбор <> Неопределено Тогда
			ФизическоеЛицо = Отбор.ФизическиеЛицаОтбор[СтрокаТаблицы.ФизическоеЛицо];
			Если ФизическоеЛицо = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Идентификатор = ТаблицыНДФЛДокумента.НДФЛ.Индекс(СтрокаТаблицы);
			РасчетЗарплатыРасширенныйКлиентСервер.ДобавитьИдентификаторСтрокиСотрудника(ИдентификаторыСтрокФизическихЛиц, СтрокаТаблицы.ФизическоеЛицо, Идентификатор);
		КонецЕсли;
		
		СтруктураПоиска = Новый Структура("ИдентификаторСтрокиНДФЛ", СтрокаТаблицы.ИдентификаторСтрокиНДФЛ);
		СтрокиПримененныеВычеты = ТаблицыНДФЛДокумента.Вычеты.НайтиСтроки(СтруктураПоиска);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СтрокиВычетовКУдалению, СтрокиПримененныеВычеты);
		СтрокаНДФЛВДанныеМенеджераРасчета(
			МенеджерРасчета, 
			СтрокаТаблицы, 
			СтрокиПримененныеВычеты, 
			ТаблицыНДФЛДокумента.РаспределениеПоСтатьям);
	КонецЦикла;
		
	Если ПозицииВставки <> Неопределено Тогда	
		ПозицииВставки.Вставить("НДФЛ", ИдентификаторыСтрокФизическихЛиц);
	КонецЕсли;
		
	Если Отбор = Неопределено Тогда
		ТаблицыНДФЛДокумента.НДФЛ.Очистить();
		ТаблицыНДФЛДокумента.Вычеты.Очистить();
	Иначе
		РасчетЗарплатыРасширенныйФормы.УдалитьСтрокиРасчета(ТаблицыНДФЛДокумента.Вычеты, СтрокиВычетовКУдалению);
	КонецЕсли;
	
КонецПроцедуры

Процедура РезультатРасчетаНачисленияУдержанияВзносыВДанныеОбъекта(Объект, ДанныеРасчета) Экспорт
	
	СтрокиНачисленийФизЛиц = Новый Соответствие;
	Если ДанныеРасчета.Свойство("НачисленияУдержанияВзносы") Тогда
		Для Каждого СтрокаНачислений Из ДанныеРасчета.НачисленияУдержанияВзносы Цикл
			СтрокиНачисленийФизЛиц.Вставить(СтрокаНачислений.ФизическоеЛицо, СтрокаНачислений);
		КонецЦикла;
	КонецЕсли;
	ИменаПолейВзносов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(УчетСтраховыхВзносовКлиентСервер.РассчитываемыеВзносы(Ложь,,Ложь));  
	
	// Начисления
	Для Каждого СтрокаДокумента Из Объект.НачисленияУдержанияВзносы Цикл
		
		СтрокаНачислений = СтрокиНачисленийФизЛиц[СтрокаДокумента.ФизическоеЛицо];
		Если СтрокаНачислений = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаДокумента, СтрокаНачислений,,?(ЗначениеЗаполнено(СтрокаДокумента.ИдентификаторСтроки),"ИдентификаторСтроки",""));
		
		// Итоги
		СтрокаДокумента.КВыплате = СтрокаДокумента.Начислено - СтрокаДокумента.НДФЛ + СтрокаНачислений.ЗачтеноАвансовыхПлатежей - СтрокаДокумента.Удержано;
		
		// Распределение начислений
		Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") 
			И ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
			
			Если СтрокаНачислений.РаспределениеПоСтатьям <> Неопределено Тогда
				
				КоличествоСтрок = Объект.РаспределениеРезультатовНачислений.Количество();
				Для Сч = 1 По КоличествоСтрок Цикл
					СтрокаРаспределения = Объект.РаспределениеРезультатовНачислений[КоличествоСтрок - Сч];
					Если СтрокаДокумента.ИдентификаторСтроки = СтрокаРаспределения.ИдентификаторСтроки Тогда
						Объект.РаспределениеРезультатовНачислений.Удалить(СтрокаРаспределения);
					КонецЕсли;
				КонецЦикла;
				
				Для Каждого СтрокаРаспределения Из СтрокаНачислений.РаспределениеПоСтатьям Цикл
					СтрокаРаспределенияДокумента = Объект.РаспределениеРезультатовНачислений.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаРаспределенияДокумента, СтрокаРаспределения);
					СтрокаРаспределенияДокумента.ИдентификаторСтроки = СтрокаДокумента.ИдентификаторСтроки;
				КонецЦикла;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Очищаем НДФЛ
	Идентификаторы = Новый Соответствие;
	КоличествоСтрок = Объект.НДФЛ.Количество();
	Для Сч = 1 По КоличествоСтрок Цикл
		СтрокаДокумента = Объект.НДФЛ[КоличествоСтрок - Сч];
		Если СтрокиНачисленийФизЛиц[СтрокаДокумента.ФизическоеЛицо] <> Неопределено Тогда
			Идентификаторы.Вставить(СтрокаДокумента.ИдентификаторСтрокиНДФЛ, Истина);
			Объект.НДФЛ.Удалить(СтрокаДокумента);
		КонецЕсли;
	КонецЦикла;
	Если Идентификаторы.Количество() > 0 Тогда
		КоличествоСтрок = Объект.ПримененныеВычетыНаДетейИИмущественные.Количество();
		Для Сч = 1 По КоличествоСтрок Цикл
			СтрокаДокумента = Объект.ПримененныеВычетыНаДетейИИмущественные[КоличествоСтрок - Сч];
			Если Идентификаторы[СтрокаДокумента.ИдентификаторСтрокиНДФЛ] <> Неопределено Тогда
				Объект.ПримененныеВычетыНаДетейИИмущественные.Удалить(СтрокаДокумента);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Очищаем удержания
	Идентификаторы = Новый Соответствие;
	КоличествоСтрок = Объект.Удержания.Количество();
	Для Сч = 1 По КоличествоСтрок Цикл
		СтрокаДокумента = Объект.Удержания[КоличествоСтрок - Сч];
		Если СтрокиНачисленийФизЛиц[СтрокаДокумента.ФизическоеЛицо] <> Неопределено Тогда
			Идентификаторы.Вставить(СтрокаДокумента.ИдентификаторСтрокиВидаРасчета, Истина);
			Объект.Удержания.Удалить(СтрокаДокумента);
		КонецЕсли;
	КонецЦикла;
	Если Идентификаторы.Количество() > 0 Тогда
		КоличествоСтрок = Объект.Показатели.Количество();
		Для Сч = 1 По КоличествоСтрок Цикл
			СтрокаДокумента = Объект.Показатели[КоличествоСтрок - Сч];
			Если Идентификаторы[СтрокаДокумента.ИдентификаторСтрокиВидаРасчета] <> Неопределено Тогда
				Объект.Показатели.Удалить(СтрокаДокумента);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьНеактуальныеСтрокиФормы(ТаблицаПриемник, СоответствиеОбходовСотрудников, ПозицииСотрудников, ОписаниеТаблицы = Неопределено) Экспорт
	
	Если СоответствиеОбходовСотрудников = Неопределено Тогда
		Возврат;
	КонецЕсли;

	МассивСтрокДляУдаления = Новый Массив;
	
	Для Каждого КлючИЗначение Из ПозицииСотрудников Цикл
		
		ОбходТаблицыИдентификаторов = СоответствиеОбходовСотрудников[КлючИЗначение.Ключ];
		
		Если ОбходТаблицыИдентификаторов = Неопределено Тогда
			ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйКлиентСервер.НовыйОбходИдентификаторовТаблицы(КлючИЗначение.Значение);
		КонецЕсли;
		
		Пока ОбходТаблицыИдентификаторов.ТекущаяПозиция < ОбходТаблицыИдентификаторов.Количество Цикл
			СтрокаФормы = СтрокаТаблицыПоНачальнойПозиции(ТаблицаПриемник, ОписаниеТаблицы, ОбходТаблицыИдентификаторов);
			Если Не СтрокаФормы = Неопределено Тогда 
				МассивСтрокДляУдаления.Добавить(СтрокаФормы);
			КонецЕсли;	
			УдалитьИдентификаторТаблицыФормы(ОбходТаблицыИдентификаторов);
		КонецЦикла;
		
	КонецЦикла;	
	
	Для Каждого СтрокаДляУдаления Из МассивСтрокДляУдаления Цикл
		ТаблицаПриемник.Удалить(СтрокаДляУдаления);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьЗависимыеНачислениями(Объект, Сотрудники, ОписаниеДокумента, ОписаниеТаблицРазовыхНачислений) Экспорт	
	ПериодРегистрации = Объект[ОписаниеДокумента.МесяцНачисленияИмя];
	
	ЗависимыеНачисленияСотрудников = РасчетЗарплатыРасширенный.НайтиСтрокиОтборПоМассиву(Объект.ЗависимыеНачисления, "Сотрудник", Сотрудники);
	Для Каждого УдаляемаяСтрока Из ЗависимыеНачисленияСотрудников Цикл
		Объект.ЗависимыеНачисления.Удалить(УдаляемаяСтрока);
	КонецЦикла;
		
	МенеджерРасчета = РасчетЗарплатыРасширенный.СоздатьМенеджерРасчета(ПериодРегистрации, Объект.Организация);
	
	ТаблицаВедущихНачислений = МенеджерРасчета.НовыйВедущиеНачисленияСотрудников();
	
	СотрудникиОтбор = ОбщегоНазначенияБЗККлиентСервер.МассивВСоответствие(Сотрудники);
	
	Для Каждого ОписаниеТекущейТаблицы Из ОписаниеТаблицРазовыхНачислений Цикл
		Таблица = Объект[ОписаниеТекущейТаблицы.ИмяТаблицы];
	
		Для Каждого СтрокаПоСотруднику Из Таблица Цикл
			Если СотрудникиОтбор[СтрокаПоСотруднику.Сотрудник] <> Истина Тогда 
				Продолжить;
			КонецЕсли;
			
			СтрокаТаблицыВедущих = ТаблицаВедущихНачислений.Добавить();
			Если ОписаниеТекущейТаблицы.СодержитПолеСотрудник Тогда
				СтрокаТаблицыВедущих.Сотрудник = СтрокаПоСотруднику[ОписаниеТекущейТаблицы.ИмяРеквизитаСотрудник];
			Иначе
				СтрокаТаблицыВедущих.Сотрудник = Объект[ОписаниеТекущейТаблицы.ИмяРеквизитаСотрудник];
			КонецЕсли;
			
			Если ОписаниеТекущейТаблицы.СодержитПолеВидРасчета Тогда
				СтрокаТаблицыВедущих.Начисление = СтрокаПоСотруднику[ОписаниеТекущейТаблицы.ИмяРеквизитаВидРасчета];
			Иначе
				СтрокаТаблицыВедущих.Начисление = Объект[ОписаниеТекущейТаблицы.ИмяРеквизитаВидРасчета];
			КонецЕсли;
			
			СтрокаТаблицыВедущих.ДатаНачала = НачалоМесяца(СтрокаПоСотруднику.ПериодДействия);
			СтрокаТаблицыВедущих.ДатаОкончания = КонецМесяца(СтрокаПоСотруднику.ПериодДействия);
		КонецЦикла;
	КонецЦикла;	
	
	ЗависимыеНачисления = МенеджерРасчета.ЗависимыеНачисленияРассчитываемыеСРазовыми(ТаблицаВедущихНачислений);
	
	Если ЗависимыеНачисления.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаНачислений = МенеджерРасчета.ТаблицаИсходныеДанныеНачисленияЗарплатыПоНачислениям();
	
	Для Каждого СтрокаЗависимыхНачислений Из ЗависимыеНачисления Цикл
		СтрокаКРасчету = ТаблицаНачислений.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаКРасчету, СтрокаЗависимыхНачислений);
		СтрокаКРасчету.РассчитыватьПоРазовымНачислениямДокумента = Истина;
	КонецЦикла;

	МенеджерРасчета.ЗаполнитьНачисленияСотрудниковЗаПериод(ТаблицаНачислений.ВыгрузитьКолонку("Сотрудник"), ТаблицаНачислений);
	
	ТаблицыНачислений = РасчетДокументовБЗК.ТаблицыНачисленийДокумента();
	ТаблицыНачислений.Начисления = Объект.Начисления;
	ТаблицыНачислений.НачисленияПерерасчет = Объект.НачисленияПерерасчет;
	ТаблицыНачислений.ЗависимыеНачисления = Объект.ЗависимыеНачисления;
	ТаблицыНачислений.Показатели = Объект.Показатели;
	
	РасчетЗарплатыНачисленияВДанныеОбъекта(ТаблицыНачислений, МенеджерРасчета.Зарплата.Начисления, Объект.Организация, ОписаниеДокумента.ОписанияТаблиц);
	
КонецПроцедуры

Процедура РасчетЗарплатыВзносыВДанныеОбъекта(ТаблицыВзносов, ТаблицаИсточник, ОписаниеТаблицы, ПозицииВставки = Неопределено) Экспорт
	
	ТаблицыВзносов.Очистить();	
	Для Каждого СтрокаИсточник Из ТаблицаИсточник Цикл
		СтрокаТаблицы = ТаблицыВзносов.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаИсточник); 
	КонецЦикла; 
	
КонецПроцедуры

Процедура ЗаполнитьПозицииВставки(СтрокиРасчета, ПозицииВставки, ТаблицаНачислений, ИмяТаблицы = "Начисления") Экспорт
	
	Если ПозицииВставки = Неопределено Тогда
		ПозицииВставки = Новый Структура;
	КонецЕсли;
	Если Не ПозицииВставки.Свойство(ИмяТаблицы) Тогда
		ПозицииВставки.Вставить(ИмяТаблицы, Новый Соответствие);
	КонецЕсли;
	
	Если СтрокиРасчета = Неопределено Тогда
		Возврат;
	КонецЕсли;

	МассивИдентификаторов = Новый Массив;
	Для Каждого СтрокаРасчета Из СтрокиРасчета Цикл
		
		ИдентификаторыСтрокСотрудников = ПозицииВставки[ИмяТаблицы];
		Если ИдентификаторыСтрокСотрудников.Получить(СтрокаРасчета.Сотрудник) = Неопределено Тогда
			ИдентификаторыСтрокСотрудников.Вставить(СтрокаРасчета.Сотрудник, МассивИдентификаторов);
		КонецЕсли;			
		
		МассивИдентификаторов.Добавить(ТаблицаНачислений.Индекс(СтрокаРасчета));

	КонецЦикла;
		
КонецПроцедуры

Процедура ЗависимыеНачисленияВДанныеМенеджераРасчета(ТаблицаИсточник, МенеджерРасчета, ТаблицыДокумента, Организация, ИмяТаблицыЗависимыеНачисления = "ЗависимыеНачисления", Отбор = Неопределено, ПозицииВставки = Неопределено) Экспорт

	ИдентификаторыСтрокСотрудников = Новый Соответствие;
	ФизическоеЛицо = Неопределено;
	
	Если Отбор = Неопределено Тогда
		Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаИсточник, "Сотрудник");
		ФизическиеЛицаСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Сотрудники, "ФизическоеЛицо");
	Иначе
		ФизическиеЛицаСотрудников = Отбор.ФизическиеЛицаСотрудников;
	КонецЕсли;
	
	Для Каждого СтрокаФормы Из ТаблицаИсточник Цикл
		
		ФизическоеЛицо = ФизическиеЛицаСотрудников[СтрокаФормы.Сотрудник];
		Если Отбор <> Неопределено Тогда 
			
			ФизическоеЛицо = Отбор.ФизическиеЛицаСотрудников[СтрокаФормы.Сотрудник];
			Если ФизическоеЛицо = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Идентификатор = ТаблицаИсточник.Индекс(СтрокаФормы); 
			РасчетЗарплатыРасширенныйКлиентСервер.ДобавитьИдентификаторСтрокиСотрудника(ИдентификаторыСтрокСотрудников, СтрокаФормы.Сотрудник, Идентификатор);
		КонецЕсли;

		СтрокаНачисленияВМенеджерРасчета(
			МенеджерРасчета, 
			СтрокаФормы, 
			Организация, 
			ФизическоеЛицо, 
			ТаблицыДокумента,
			ИмяТаблицыЗависимыеНачисления);
		
	КонецЦикла;
	
	Если Отбор = Неопределено Тогда
		ТаблицаИсточник.Очистить();
	КонецЕсли;
	
	Если ПозицииВставки <> Неопределено Тогда
		ПозицииВставки.Вставить(ИмяТаблицыЗависимыеНачисления, ИдентификаторыСтрокСотрудников);
	КонецЕсли;
		
КонецПроцедуры

// Выполняет дозаполнение строк коллекции данными, 
// необходимыми для расчета: сведениями о сотрудниках, 
// показателями расчета и их значениями.
//
Процедура ДополнитьСтрокиРасчета(Объект, ОписаниеДокумента, ОписаниеТаблицы, ИдентификаторыСтрок = Неопределено, ЗаполнятьСведенияСотрудников = Истина, ЗаполнятьЗначенияПоказателей = Истина) Экспорт
	
	ИмяТаблицы = ОписаниеТаблицы.ИмяТаблицы;
	ДанныеНачислений = Объект[ОписаниеТаблицы.ИмяТаблицы];
	
	Если ИдентификаторыСтрок = Неопределено Тогда
		СтрокиРасчета = ДанныеНачислений;
	Иначе
		Если ТипЗнч(ИдентификаторыСтрок) = Тип("Массив") Тогда
			ИдентификаторыСтрок = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ИдентификаторыСтрок);
		КонецЕсли;
		СтрокиРасчета = Новый Массив;
		Для Каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
			СтрокаРасчета = ДанныеНачислений.НайтиСтроки(Новый Структура(ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки, ИдентификаторСтроки));
			Если СтрокаРасчета.Количество() = 1 Тогда
				СтрокиРасчета.Добавить(СтрокаРасчета[0]);
			КонецЕсли;	
		КонецЦикла;
	КонецЕсли;
	
	Если СтрокиРасчета.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	ВидыРасчета = Новый Массив;
	Если Не ОписаниеТаблицы.СодержитПолеВидРасчета Тогда
		ВидыРасчета.Добавить(Объект[ОписаниеТаблицы.ИмяРеквизитаВидРасчета]);
	Иначе
		ВидыРасчета = ОбщегоНазначения.ВыгрузитьКолонку(СтрокиРасчета, ОписаниеТаблицы.ИмяРеквизитаВидРасчета); 
	КонецЕсли;
	ВидыРасчетаИнфо = ЗарплатаКадрыРасширенный.ИнформацияОВидахРасчета(ВидыРасчета);
	
	МесяцНачисления = Объект[ОписаниеДокумента.МесяцНачисленияИмя];
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "Организация") Тогда
		Организация = Объект.Организация;
	КонецЕсли;
	
	МенеджерРасчета = РасчетЗарплатыРасширенный.СоздатьМенеджерРасчета(МесяцНачисления, Организация);
	Если ОписаниеДокумента.УдержанияИмя = ИмяТаблицы
		Или ОписаниеДокумента.УдержанияПерерасчетИмя = ИмяТаблицы Тогда
		ТаблицаРасчета = МенеджерРасчета.ТаблицаУдержаний();
	Иначе
		ТаблицаРасчета = МенеджерРасчета.ТаблицаНачислений();
	КонецЕсли;
	СодержитПолеСотрудник = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Объект, "Сотрудник");
	
	СоответствиеСтрок = Новый Соответствие;
	Для Каждого СтрокаРасчета Из СтрокиРасчета Цикл
		НоваяСтрока = ТаблицаРасчета.Добавить();
		СоответствиеСтрок.Вставить(НоваяСтрока, СтрокаРасчета);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета);
		НоваяСтрока.Организация = Организация;
		// Если сотрудник указан в шапке.
		Если СодержитПолеСотрудник Тогда
			НоваяСтрока.Сотрудник = Объект["Сотрудник"];
		КонецЕсли;
		// Вид расчета может быть указан в шапке.
		Если Не ОписаниеТаблицы.СодержитПолеВидРасчета Тогда
			НоваяСтрока.Начисление = Объект[ОписаниеТаблицы.ИмяРеквизитаВидРасчета];
		КонецЕсли;
		// Период может не использоваться, тогда полей нет в таблице, их нужно получать 
		// - по периоду, если он указан в шапке
		// - по периоду регистрации.
		Если ОписаниеТаблицы.ИмяРеквизитаДатаНачала = Неопределено Тогда
			Если ОписаниеТаблицы.ИмяРеквизитаПериод <> Неопределено Тогда
				НоваяСтрока.ДатаНачала = НачалоМесяца(Объект[ОписаниеТаблицы.ИмяРеквизитаПериод]);
			КонецЕсли; 
			Если ОписаниеДокумента.ПериодДействияВШапке И ОписаниеДокумента.ДатаНачалаИмя <> Неопределено Тогда
				НоваяСтрока.ДатаНачала = Объект[ОписаниеДокумента.ДатаНачалаИмя];
			КонецЕсли;
		КонецЕсли;
		Если ОписаниеТаблицы.ИмяРеквизитаДатаОкончания = Неопределено Тогда
			Если ОписаниеТаблицы.ИмяРеквизитаПериод <> Неопределено Тогда
				НоваяСтрока.ДатаОкончания = КонецМесяца(Объект[ОписаниеТаблицы.ИмяРеквизитаПериод]);
			КонецЕсли; 
			Если ОписаниеДокумента.ПериодДействияВШапке И ОписаниеДокумента.ДатаОкончанияИмя <> Неопределено Тогда
				НоваяСтрока.ДатаОкончания = Объект[ОписаниеДокумента.ДатаОкончанияИмя];
			КонецЕсли;
		КонецЕсли;
		
		Если ОписаниеДокумента.УдержанияИмя = ИмяТаблицы
			Или ОписаниеДокумента.УдержанияПерерасчетИмя = ИмяТаблицы
			Или Не ЗначениеЗаполнено(НоваяСтрока.Начисление) Тогда
			
			Продолжить;
		КонецЕсли;
		
		ЗначенияРеквизитов = ВидыРасчетаИнфо.Получить(НоваяСтрока.Начисление);
		ПериодыСтрокиРасчета = Новый Структура("ПериодДействия, ДатаНачала, ДатаОкончания");
		ЗаполнитьЗначенияСвойств(ПериодыСтрокиРасчета, НоваяСтрока);
		РасчетЗарплатыРасширенныйФормы.ЗаполнитьБазовыйПериодДанныеМенеджераРасчета(НоваяСтрока, ПериодыСтрокиРасчета, ЗначенияРеквизитов);
		
		Если ЗаполнятьЗначенияПоказателей Тогда		
			НоваяСтрока.Показатели = МенеджерРасчета.ТаблицаПоказателей();

			РасчетЗарплатыРасширенныйФормы.ПоказателиВМенеджерРасчета(НоваяСтрока, СтрокаРасчета);
		КонецЕсли;				
		//возможно где то тут нужно заполнить НоваяСтрока.ИзвестныеПоказатели из Объект.Показатели
	КонецЦикла;
	
	// Скорректируем период датами приема на работу / увольнения сотрудников.
	Если ОписаниеДокумента.ПериодДействияВШапке Тогда
		МассивСотрудников = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаРасчета, "Сотрудник", Истина);
		КадровыеДанныеСотрудников = КадровыйУчет.КадровыеДанныеСотрудников(Истина, МассивСотрудников, "ДатаПриема,ДатаЗавершенияРаботы");
		КадровыеДанныеСотрудников.Индексы.Добавить("Сотрудник");
		Для Каждого СтрокаТаблицы Из ТаблицаРасчета Цикл
			КадровыеДанныеСотрудника = КадровыеДанныеСотрудников.Найти(СтрокаТаблицы.Сотрудник, "Сотрудник");
			Если КадровыеДанныеСотрудника <> Неопределено Тогда
				Если СтрокаТаблицы.ДатаНачала < КадровыеДанныеСотрудника.ДатаПриема
					И НачалоМесяца(СтрокаТаблицы.ДатаНачала) = НачалоМесяца(КадровыеДанныеСотрудника.ДатаПриема) Тогда
					
					СтрокаТаблицы.ДатаНачала = КадровыеДанныеСотрудника.ДатаПриема;
				КонецЕсли;
				Если ЗначениеЗаполнено(КадровыеДанныеСотрудника.ДатаЗавершенияРаботы)
					И СтрокаТаблицы.ДатаОкончания > КадровыеДанныеСотрудника.ДатаЗавершенияРаботы
					И НачалоМесяца(СтрокаТаблицы.ДатаОкончания) = НачалоМесяца(КадровыеДанныеСотрудника.ДатаЗавершенияРаботы) Тогда
					
					СтрокаТаблицы.ДатаОкончания = Макс(КонецДня(СтрокаТаблицы.ДатаНачала), КадровыеДанныеСотрудника.ДатаЗавершенияРаботы);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	РазбиватьСтрокиНачислений = ОписаниеДокумента.ВидНачисленияВШапке 
								И ОписаниеДокумента.РазбиватьСтрокиНачислений
								И ВидыРасчетаИнфо[Объект[ОписаниеДокумента.ВидНачисленияИмя]] <> Неопределено 
								И ВидыРасчетаИнфо[Объект[ОписаниеДокумента.ВидНачисленияИмя]].ИспользованиеПериода = Перечисления.ВариантыИспользованияПериодаНачисления.ПериодДействия;
		
	Если ОписаниеДокумента.УдержанияИмя = ИмяТаблицы
		Или ОписаниеДокумента.УдержанияПерерасчетИмя = ИмяТаблицы Тогда
		
		Если ЗаполнятьСведенияСотрудников Тогда
			МенеджерРасчета.ЗаполнитьСведенияУдержанийДляРасчета(ТаблицаРасчета);
		КонецЕсли;
		
		Если ЗаполнятьЗначенияПоказателей Тогда
			МенеджерРасчета.ЗаполнитьЗначенияПоказателейУдержаний(ТаблицаРасчета);
		КонецЕсли;
		
		ПараметрыДляПроверкиРезультатаРаспределения = ОтражениеЗарплатыВБухучетеРасширенный.ПараметрыДляПроверкиРезультатовРаспределенияУдержаний();
		ВидыРасчета = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаРасчета, "Удержание");
		ВидыРасчетаИнфо = ЗарплатаКадрыРасширенный.ИнформацияОВидахРасчета(ВидыРасчета);
		// Переносим значения в исходную коллекцию.
		Для Каждого КлючИЗначение Из СоответствиеСтрок Цикл
			//СтрокаУдержанияВДанныеФормы(КлючИЗначение.Значение, КлючИЗначение.Ключ, ВидыРасчетаИнфо, ПараметрыДляПроверкиРезультатаРаспределения);
		КонецЦикла;
	Иначе
		Если ЗаполнятьСведенияСотрудников Тогда
			МенеджерРасчета.УстановитьТаблицуНачисления(ТаблицаРасчета);
			Если РазбиватьСтрокиНачислений Тогда
				МенеджерРасчета.РазбитьСтрокиНачислений();
			КонецЕсли;
			МенеджерРасчета.ЗаполнитьСведенияНачисленийДляРасчетаСлужебный();
		КонецЕсли;
		
		Если ЗаполнятьЗначенияПоказателей Тогда
			Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
				МодульУправленческаяЗарплатаФормы = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплатаФормы");
				МодульУправленческаяЗарплатаФормы.ПередЗаполнениемЗначенийПоказателейНачислений(ОписаниеТаблицы, ТаблицаРасчета);
			КонецЕсли;
			МенеджерРасчета.ЗаполнитьЗначенияПоказателейНачислений(ТаблицаРасчета); 
		КонецЕсли;
		
		ВидыРасчета = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаРасчета, "Начисление");
		ВидыРасчетаИнфо = ЗарплатаКадрыРасширенный.ИнформацияОВидахРасчета(ВидыРасчета);
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") 
			И ИмяТаблицы = "УправленческиеНачисления" Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
			Для Каждого КлючИЗначение Из СоответствиеСтрок Цикл
				Модуль.СтрокаУправленческиеНачисленияВДанныеФормы(КлючИЗначение.Значение, КлючИЗначение.Ключ, ВидыРасчетаИнфо);
			КонецЦикла;
		Иначе	
			Для Каждого КлючИЗначение Из СоответствиеСтрок Цикл
				СтрокаНачисленияБезОплатыВДанныеОбъекта(Объект, КлючИЗначение.Значение, КлючИЗначение.Ключ, ВидыРасчетаИнфо);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СтрокаНачисленияБезОплатыВДанныеОбъекта(Объект, СтрокаПриемник, СтрокаИсточник, ВидыРасчетаИнфо) Экспорт
	
	СтруктураОтбораПоказателей = Новый Структура("Показатель,ИдентификаторСтрокиВидаРасчета");
	
	// Заполнение Объект.Показатели
	ЗаполнитьЗначенияСвойств(СтруктураОтбораПоказателей,СтрокаПриемник);
	Для Каждого ЗначениеПоказателя Из СтрокаИсточник.Показатели Цикл
		ЗаполнитьЗначенияСвойств(СтруктураОтбораПоказателей, ЗначениеПоказателя);
		СтрокиПоказателей = Объект.Показатели.НайтиСтроки(СтруктураОтбораПоказателей);
		Если СтрокиПоказателей.Количество() > 0 Тогда
			СтрокаПоказателя = СтрокиПоказателей[0];
		Иначе
			СтрокаПоказателя = Объект.Показатели.Добавить();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаПоказателя,СтруктураОтбораПоказателей);
		ЗаполнитьЗначенияСвойств(СтрокаПоказателя,ЗначениеПоказателя);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
	
	ЗначенияРеквизитов = ВидыРасчетаИнфо.Получить(СтрокаИсточник.Начисление);
	Если ЗначенияРеквизитов <> Неопределено Тогда
		ЗначенияРеквизитов.Вставить("ДоступенВыборОснования", ЗначенияРеквизитов.ПоддерживаетНесколькоПлановыхНачислений);
		ЗаполнитьЗначенияСвойств(СтрокаПриемник, ЗначенияРеквизитов);
		ЗаполнитьБазовыйПериодДанныеОбъекта(СтрокаПриемник, СтрокаИсточник, ЗначенияРеквизитов);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьБазовыйПериодДанныеОбъекта(СтрокаПриемник, СтрокаИсточник, ЗначенияРеквизитов) Экспорт
	
	Если ЗначенияРеквизитов.ТребуетсяРасчетБазы
		И ЗначенияРеквизитов.ИспользованиеПериода = Перечисления.ВариантыИспользованияПериодаНачисления.БазовыйПериод Тогда
		
		СтрокаПриемник.ДатаНачала = СтрокаИсточник.НачалоБазовогоПериода;
		СтрокаПриемник.ДатаОкончания = СтрокаИсточник.ОкончаниеБазовогоПериода;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачисленияПерерасчетВМенеджерРасчета(ТаблицаИсточник, МенеджерРасчета, ТаблицыДокумента, Организация) Экспорт
	НачисленияВМенеджерРасчета(ТаблицаИсточник, МенеджерРасчета, ТаблицыДокумента, Организация, "НачисленияПерерасчет");
КонецПроцедуры 

Процедура СброситьФиксПризнакиСтроки(КонтролируемыеПоля, СтрокаРасчета) Экспорт

	Для Каждого ЭлементСтруктуры Из КонтролируемыеПоля Цикл
		СтрокаРасчета[ЭлементСтруктуры.Ключ] = Ложь;
	КонецЦикла;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаРасчета, "ФиксСтрока") Тогда
		СтрокаРасчета.ФиксСтрока = Ложь;
	КонецЕсли;
		
КонецПроцедуры

Процедура СортироватьПерерасчеты(НачисленияПерерасчет) Экспорт
	
	Если НачисленияПерерасчет.Количество() < 2 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаСортировки = Новый ТаблицаЗначений;
	ТаблицаСортировки.Колонки.Добавить("ИсходнаяСтрока");
	ТаблицаСортировки.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаСортировки.Колонки.Добавить("НаименованиеСотрудника", Новый ОписаниеТипов("Строка"));
	ТаблицаСортировки.Колонки.Добавить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	ТаблицаСортировки.Колонки.Добавить("ОчередностьНачисления", Новый ОписаниеТипов("Число"));
	ТаблицаСортировки.Колонки.Добавить("ПорядокНачисления", Новый ОписаниеТипов("Число"));
	ТаблицаСортировки.Колонки.Добавить("ПериодДействия", Новый ОписаниеТипов("Дата"));
	ТаблицаСортировки.Колонки.Добавить("Сторно", Новый ОписаниеТипов("Булево"));
	ТаблицаСортировки.Колонки.Добавить("ФиксСторно", Новый ОписаниеТипов("Булево"));
	ТаблицаСортировки.Колонки.Добавить("РегистраторРазовогоНачисления");
	
	НаименованияСотрудников = Неопределено;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(НачисленияПерерасчет[0], "Сотрудник") Тогда
		Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(НачисленияПерерасчет, "Сотрудник", Истина);
		Если Сотрудники.Количество() > 1 Тогда
			УстановитьПривилегированныйРежим(Истина);
			НаименованияСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Сотрудники, "Наименование");
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
	КонецЕсли;
	
	СвойстваНачислений = Неопределено;
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(НачисленияПерерасчет[0], "Начисление") Тогда
		Начисления = ОбщегоНазначения.ВыгрузитьКолонку(НачисленияПерерасчет, "Начисление", Истина);
		Если Начисления.Количество() > 1 Тогда
			УстановитьПривилегированныйРежим(Истина);
			СвойстваНачислений = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Начисления, "РеквизитДопУпорядочивания, ОчередностьРасчета");
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицыФормы Из НачисленияПерерасчет Цикл
		СтрокаТаблицыСортировки = ТаблицаСортировки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыСортировки, СтрокаТаблицыФормы);
		
		Если НаименованияСотрудников <> Неопределено Тогда
			СтрокаТаблицыСортировки.НаименованиеСотрудника = НаименованияСотрудников[СтрокаТаблицыСортировки.Сотрудник];
		КонецЕсли;
		
		Если СвойстваНачислений <> Неопределено 
			И ЗначениеЗаполнено(СтрокаТаблицыСортировки.Начисление) Тогда
			
			СтрокаТаблицыСортировки.ОчередностьНачисления = СвойстваНачислений[СтрокаТаблицыСортировки.Начисление].ОчередностьРасчета;
			СтрокаТаблицыСортировки.ПорядокНачисления = СвойстваНачислений[СтрокаТаблицыСортировки.Начисление].РеквизитДопУпорядочивания;
		КонецЕсли;
		
		СтрокаТаблицыСортировки.ИсходнаяСтрока = СтрокаТаблицыФормы;
	КонецЦикла;	
	
	ТаблицаСортировки.Сортировать("НаименованиеСотрудника, Сотрудник, ПериодДействия, РегистраторРазовогоНачисления, 
								|ОчередностьНачисления, ПорядокНачисления, Начисление, Сторно УБЫВ, ФиксСторно УБЫВ");
	
	ЦелевойПорядок = 1;
	Для Каждого ЭлементПорядка Из ТаблицаСортировки Цикл
		СтрокаТаблицыФормы = ЭлементПорядка.ИсходнаяСтрока;
		ТекущийИндексСтроки = НачисленияПерерасчет.Индекс(СтрокаТаблицыФормы);
		Сдвиг = ЦелевойПорядок - (ТекущийИндексСтроки + 1);
		
		НачисленияПерерасчет.Сдвинуть(ТекущийИндексСтроки, Сдвиг);
		ЦелевойПорядок = ЦелевойПорядок + 1;
	КонецЦикла;	
	
КонецПроцедуры	

Процедура РасчетЗарплатыНачисленияПоДоговорамВДанныеОбъекта(ТаблицыНачислений, ТаблицаИсточник, Организация, ОписанияТаблиц, ИмяТаблицы, ПозицииВставки = Неопределено) Экспорт

	ПозицииСотрудников = Новый Соответствие;
	СоответствиеОбходовСотрудников = Новый Соответствие;	
	
	Если ПозицииВставки <> Неопределено Тогда
		Для Каждого КлючИЗначение Из ТаблицыНачислений Цикл
			Если КлючИЗначение.Значение = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ПозицииСотрудниковТаблицы = Неопределено;
			Если Не ПозицииВставки.Свойство(КлючИЗначение.Ключ, ПозицииСотрудниковТаблицы) Тогда
				ПозицииСотрудниковТаблицы = Новый Соответствие;
				ПозицииВставки.Вставить(КлючИЗначение.Ключ, ПозицииСотрудниковТаблицы);
			КонецЕсли;
			ПозицииСотрудников.Вставить(КлючИЗначение.Ключ, ПозицииСотрудниковТаблицы);
			СоответствиеОбходовСотрудников.Вставить(КлючИЗначение.Ключ, Новый Соответствие);
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого СтрокаНачисления Из ТаблицаИсточник Цикл
		ОбходТаблицыИдентификаторов = РасчетЗарплатыРасширенныйФормы.ОбходТаблицыИдентификаторов(СоответствиеОбходовСотрудников["НачисленияПоДоговорам"], ПозицииСотрудников["НачисленияПоДоговорам"], СтрокаНачисления.Сотрудник);
		СтрокаТаблицы = СтрокаТаблицыПоНачальнойПозиции(ТаблицыНачислений.НачисленияПоДоговорам, ОписанияТаблиц.Начисления, ОбходТаблицыИдентификаторов);
		СтрокаНачисленияПоДоговорамВДанныеОбъекта(Организация, СтрокаТаблицы, СтрокаНачисления, ТаблицыНачислений, ОписанияТаблиц.Начисления);
	КонецЦикла; 
	
	Для Каждого КлючИЗначение Из ТаблицыНачислений Цикл
		Если КлючИЗначение.Значение = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		УдалитьНеактуальныеСтрокиФормы(КлючИЗначение.Значение, СоответствиеОбходовСотрудников[КлючИЗначение.Ключ], ПозицииСотрудников[КлючИЗначение.Ключ]);

	КонецЦикла;

КонецПроцедуры

Процедура НачисленияПоДоговорамВДанныеМенеджераРасчета(ТаблицаНачислений, 
														МенеджерРасчета,
														ТаблицыДокумента,
														Отбор = Неопределено, 
														ПозицииВставки = Неопределено, 
														ДанныеРезультатовРаспределения = Неопределено, 
														ДанныеРаспределенияПоТерриториямУсловиямТруда = Неопределено) Экспорт

	ИдентификаторыСтрокСотрудников = Новый Соответствие;
	ФизическоеЛицо = Неопределено;
	
	Если Отбор = Неопределено Тогда
		Сотрудники = ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаНачислений, "Сотрудник");
		ФизическиеЛицаСотрудников = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Сотрудники, "ФизическоеЛицо");
	Иначе
		ФизическиеЛицаСотрудников = Отбор.ФизическиеЛицаСотрудников;
	КонецЕсли;
	
	КодыДоходаДоговорРаботыУслуги = УчетНДФЛ.ДоходыНДФЛПоВидуОсобыхНачислений(Перечисления.ВидыОсобыхНачисленийИУдержаний.ДоговорРаботыУслуги);
	
	Для Каждого СтрокаНачислений Из ТаблицаНачислений Цикл
		
		ФизическоеЛицо = ФизическиеЛицаСотрудников[СтрокаНачислений.Сотрудник];
		Если Отбор <> Неопределено Тогда 
			Если ФизическоеЛицо = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Идентификатор = ТаблицаНачислений.Индекс(СтрокаНачислений);
			РасчетЗарплатыРасширенныйКлиентСервер.ДобавитьИдентификаторСтрокиСотрудника(ИдентификаторыСтрокСотрудников, 
																						СтрокаНачислений.Сотрудник, 
																						Идентификатор);
			
		КонецЕсли;

		СтрокаНачисленияПоДоговорамВДанныеМенеджераРасчета(МенеджерРасчета, 
															СтрокаНачислений, 
															ФизическоеЛицо,
															ТаблицыДокумента,
															КодыДоходаДоговорРаботыУслуги, 
															ДанныеРаспределенияПоТерриториямУсловиямТруда, 
															ДанныеРезультатовРаспределения);
			
	КонецЦикла;

	
	
	Если Отбор = Неопределено Тогда
		ТаблицаНачислений.Очистить();
	КонецЕсли;
	
	Если ПозицииВставки <> Неопределено Тогда
		ПозицииВставки.Вставить("НачисленияПоДоговорам", ИдентификаторыСтрокСотрудников);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТипыСтрокНачисленийМенеджераРасчета() Экспорт
	ТипыСтрок = Новый Структура();
	ТипыСтрок.Вставить("Начисления", "Начисления");
	ТипыСтрок.Вставить("НачисленияПерерасчет", "НачисленияПерерасчет");
	ТипыСтрок.Вставить("НачисленияПерерасчетНулевыеСторно", "НачисленияПерерасчетНулевыеСторно");
	ТипыСтрок.Вставить("Пособия", "Пособия");
	ТипыСтрок.Вставить("ПособияПерерасчет", "ПособияПерерасчет");
	ТипыСтрок.Вставить("Льготы", "Льготы");
	ТипыСтрок.Вставить("ЛьготыПерерасчет", "ЛьготыПерерасчет");
	ТипыСтрок.Вставить("УправленческиеНачисления", "УправленческиеНачисления");
	ТипыСтрок.Вставить("ЗависимыеНачисления", "ЗависимыеНачисления");
	
	Возврат ТипыСтрок;
КонецФункции

Функция ДобавитьСтрокуТаблицыСИдентефикатором(ТаблицаОбъекта, ОписаниеТаблицы, ПозицияВставки = Неопределено) Экспорт

	КоличествоЭлементовВТаблице = ТаблицаОбъекта.Количество();
	
	Если КоличествоЭлементовВТаблице = 0 Тогда
		ИдентификаторСтроки = ОписаниеТаблицы.НомерТаблицы * 1000000 + 1;
	Иначе
		ИдентификаторСтроки = ОписаниеТаблицы.НомерТаблицы * 1000000 + КоличествоЭлементовВТаблице + 1;
	КонецЕсли;
	
	Если Не ТаблицаОбъекта.Найти(ИдентификаторСтроки, ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки) = Неопределено Тогда
		ИдентификаторСтроки = ТаблицаОбъекта[КоличествоЭлементовВТаблице-1][ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки]+1;
	КонецЕсли;
	
	Если ПозицияВставки = Неопределено Тогда
		СтрокаТаблицы = ТаблицаОбъекта.Добавить();
	Иначе
		СтрокаТаблицы = ТаблицаОбъекта.Вставить(ПозицияВставки);
	КонецЕсли;
	СтрокаТаблицы[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки] = ИдентификаторСтроки;
	
	Возврат СтрокаТаблицы;

КонецФункции

Процедура СтрокаНачисленияВДанныеОбъекта(Организация, СтрокаПриемник, СтрокаИсточник, ТаблицыДокумента, ВидыРасчетаИнфо, ОписаниеТаблицы)
	
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
	
	ЗначенияРеквизитов = ВидыРасчетаИнфо.Получить(СтрокаИсточник.Начисление);
	Если ЗначенияРеквизитов <> Неопределено Тогда
		ИсходныеДанные = ОбщегоНазначения.СкопироватьРекурсивно(СтрокаИсточник.Дополнительно);
		ИсходныеДанные.Вставить("КатегорияНачисления", ЗначенияРеквизитов.КатегорияНачисленияИлиНеоплаченногоВремени);
		УчетПособийСоциальногоСтрахованияРасширенный.ПеренестиРезультатыРасчетаПособия(СтрокаПриемник, ИсходныеДанные);
		ЗначенияРеквизитов.Вставить("ДоступенВыборОснования", ЗначенияРеквизитов.ПоддерживаетНесколькоПлановыхНачислений);
		ЗаполнитьЗначенияСвойств(СтрокаПриемник, ЗначенияРеквизитов);
		
		ПоказателиВОбъект(СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], СтрокаИсточник, ТаблицыДокумента, ЗначенияРеквизитов.Показатели);
		
		РасчетЗарплатыРасширенныйФормы.ЗаполнитьБазовыйПериодДанныеФормы(СтрокаПриемник, СтрокаИсточник, ЗначенияРеквизитов);
	КонецЕсли;
	
	РаспределениеПоТерриториямУсловиямТрудаВОбъект(
		СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], 
		СтрокаИсточник, 
		Организация, 
		ТаблицыДокумента);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") 
		И ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		
		РаспределениеПоСтатьямВОбъект(
			СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], 
			СтрокаИсточник, 
			ТаблицыДокумента.РаспределениеПоСтатьям);
	КонецЕсли;	
	
КонецПроцедуры  

Процедура СтрокаУдержанияВДанныеОбъекта(СтрокаПриемник, СтрокаИсточник, ТаблицыДокумента, ВидыРасчетаИнфо, ОписаниеТаблицы)
	
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
	
	ЗначенияРеквизитов = ВидыРасчетаИнфо.Получить(СтрокаИсточник.Удержание);
	Если ЗначенияРеквизитов <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(СтрокаПриемник, ЗначенияРеквизитов);
		ПоказателиВОбъект(СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], СтрокаИсточник, ТаблицыДокумента, ЗначенияРеквизитов.Показатели);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") 
		И ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		
		РаспределениеПоСтатьямВОбъект(
			СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], 
			СтрокаИсточник, 
			ТаблицыДокумента.РаспределениеПоСтатьям);
						
	КонецЕсли;
	
КонецПроцедуры

Процедура СтрокаНДФЛВДанныеОбъекта(СтрокаНДФЛ, СтрокаИсточник, СтрокиВычетов, ТаблицыНДФЛ, ОписаниеТаблицы)
	
	ЗаполнитьЗначенияСвойств(СтрокаНДФЛ, СтрокаИсточник,, "ИдентификаторСтрокиНДФЛ");
	
	ВидыВычетов = УчетНДФЛ.ВидыВычетовПоПолямТаблицы();
	Для каждого СтрокаВычета Из СтрокиВычетов Цикл
		Для Каждого КлючИЗначение Из ВидыВычетов Цикл
			ИмяПоля = КлючИЗначение.Ключ;
			КодВычета = КлючИЗначение.Значение;
			Если ЗначениеЗаполнено(СтрокаВычета[ИмяПоля]) Тогда
				СтрокаВычетов = ТаблицыНДФЛ.Вычеты.Добавить();
				СтрокаВычетов.ИдентификаторСтрокиНДФЛ = СтрокаНДФЛ.ИдентификаторСтрокиНДФЛ;
				СтрокаВычетов.МесяцПериодаПредоставленияВычета = СтрокаВычета.МесяцПериодаПредоставленияВычета;
				СтрокаВычетов.КодВычета = КодВычета;
				СтрокаВычетов.РазмерВычета = СтрокаВычета[ИмяПоля];
				СтрокаВычетов.НалоговаяБаза = СтрокаВычета.НалоговаяБаза;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
		
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") 
		И ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		
		РаспределениеПоСтатьямВОбъект(
			СтрокаНДФЛ[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], 
			СтрокаИсточник, 
			ТаблицыНДФЛ.РаспределениеПоСтатьям);
	КонецЕсли;
		
КонецПроцедуры

Процедура ПоказателиВОбъект(ИдентификаторСтрокиДокумента, СтрокаИсточник, ТаблицыДокумента, Показатели) Экспорт

	Если СтрокаИсточник.Показатели.Количество() = 0 Тогда
		Возврат
	КонецЕсли;

	НайденныеСтрокиПоказателей = ТаблицыДокумента.Показатели.НайтиСтроки(Новый Структура("ИдентификаторСтрокиВидаРасчета",ИдентификаторСтрокиДокумента));
	Для Каждого НайденныйПоказатель Из НайденныеСтрокиПоказателей Цикл
		ТаблицыДокумента.Показатели.Удалить(НайденныйПоказатель);
	КонецЦикла;
		
	СтруктураПоиска = Новый Структура("Показатель");
	Для Каждого Показатель Из Показатели Цикл
		Если Не Показатель.ОтображатьВДокументахНачисления Тогда
			Продолжить;
		КонецЕсли;
		СтруктураПоиска.Показатель = Показатель.Показатель;
		НайденныеСтроки = СтрокаИсточник.Показатели.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(НайденныеСтроки[0].Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		ПоказательСтрокаПриемника = ТаблицыДокумента.Показатели.Добавить();
		ПоказательСтрокаПриемника.ИдентификаторСтрокиВидаРасчета = ИдентификаторСтрокиДокумента;
		ПоказательСтрокаПриемника.Показатель = Показатель.Показатель;
		ПоказательСтрокаПриемника.Значение = НайденныеСтроки[0].Значение;
		
	КонецЦикла;
КонецПроцедуры

Процедура РаспределениеПоТерриториямУсловиямТрудаВОбъект(ИдентификаторСтрокиДокумента, СтрокаИсточник, Организация, ТаблицыДокумента) Экспорт
	Если Не ЗарплатаКадрыРасширенный.ИспользоватьРаспределениеПоТерриториямУсловиямТруда(Организация) Тогда
		Возврат;	
	КонецЕсли;
	
	Если ТаблицыДокумента.РаспределениеПоТерриториямУсловиямТруда = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиУдаления = ТаблицыДокумента.РаспределениеПоТерриториямУсловиямТруда.НайтиСтроки(Новый Структура("ИдентификаторСтроки", ИдентификаторСтрокиДокумента));
	Для Каждого СтрокаУдаления Из СтрокиУдаления Цикл
		СтрокиУдаленияПоказателей = ТаблицыДокумента.Показатели.НайтиСтроки(Новый Структура("ИдентификаторСтрокиВидаРасчета", СтрокаУдаления.ИдентификаторСтрокиПоказателей));
		Для Каждого СтрокаУдаленияПоказателей Из СтрокиУдаленияПоказателей Цикл
			ТаблицыДокумента.Показатели.Удалить(СтрокаУдаленияПоказателей);
		КонецЦикла;
		ТаблицыДокумента.РаспределениеПоТерриториямУсловиямТруда.Удалить(СтрокаУдаления);
	КонецЦикла;
	
	НомераИдентификаторовСтрокПоказателей = ТаблицыДокумента.РаспределениеПоТерриториямУсловиямТруда.Выгрузить(, "ИдентификаторСтрокиПоказателей");
	НомераИдентификаторовСтрокПоказателей.Сортировать("ИдентификаторСтрокиПоказателей");
	НомерПоследнейСтроки = НомераИдентификаторовСтрокПоказателей.Количество();
	Если НомерПоследнейСтроки = 0 Тогда
		ИдентификаторСтрокиПоказателей 
			= РасчетЗарплатыРасширенныйФормы.РаспределениеПоТерриториямУсловиямТрудаНомерТаблицы() * 1000000;
	Иначе
		ИдентификаторСтрокиПоказателей 
			= НомераИдентификаторовСтрокПоказателей[НомерПоследнейСтроки - 1].ИдентификаторСтрокиПоказателей + 1;
	КонецЕсли;	
		
	Для Каждого СтрокаРаспределения Из СтрокаИсточник.ТерриторииУсловияТруда Цикл
		СтрокаРаспределенияДокумента = ТаблицыДокумента.РаспределениеПоТерриториямУсловиямТруда.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРаспределенияДокумента, СтрокаРаспределения);
		СтрокаРаспределенияДокумента.ИдентификаторСтроки = ИдентификаторСтрокиДокумента;
		СтрокаРаспределенияДокумента.ИдентификаторСтрокиПоказателей = ИдентификаторСтрокиПоказателей;
		Для Каждого СтрокаПоказателя Из СтрокаРаспределения.Показатели Цикл
			ПоказательСтрокаПриемника = ТаблицыДокумента.Показатели.Добавить();
			ПоказательСтрокаПриемника.ИдентификаторСтрокиВидаРасчета = ИдентификаторСтрокиПоказателей;
			ПоказательСтрокаПриемника.Показатель = СтрокаПоказателя.Показатель;
			ПоказательСтрокаПриемника.Значение = СтрокаПоказателя.Значение;
		КонецЦикла;
		ИдентификаторСтрокиПоказателей = ИдентификаторСтрокиПоказателей + 1;
		
	КонецЦикла; 	
КонецПроцедуры

Процедура РаспределениеПоСтатьямВОбъект(ИдентификаторСтрокиДокумента, СтрокаИсточник, ТаблицаРаспределениеПоСтатьям) Экспорт
	
	Если Не ТаблицаРаспределениеПоСтатьям = Неопределено Тогда
		
		СтрокиУдаления = ТаблицаРаспределениеПоСтатьям.НайтиСтроки(Новый Структура("ИдентификаторСтроки", ИдентификаторСтрокиДокумента));
		Для Каждого СтрокаУдаления Из СтрокиУдаления Цикл
			ТаблицаРаспределениеПоСтатьям.Удалить(СтрокаУдаления);
		КонецЦикла;
		
		Для Каждого СтрокаРаспределения Из СтрокаИсточник.РаспределениеПоСтатьям Цикл
			СтрокаРаспределенияДокумента = ТаблицаРаспределениеПоСтатьям.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРаспределенияДокумента, СтрокаРаспределения);
			СтрокаРаспределенияДокумента.ИдентификаторСтроки = ИдентификаторСтрокиДокумента;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СтрокаПогашениеЗаймовВДанныеОбъекта(СтрокаПриемник, СтрокаИсточник, ОписаниеТаблицы, ТаблицаРаспределениеПоСтатьям)
	
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
	РаспределениеПоСтатьямВОбъект(
			СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], 
			СтрокаИсточник, 
			ТаблицаРаспределениеПоСтатьям);
КонецПроцедуры

Процедура СтрокаКорректировкиВыплатыВДанныеОбъекта(СтрокаПриемник, СтрокаИсточник, ОписаниеТаблицы, ТаблицаРаспределениеПоСтатьям)
	
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник,,ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки);
	РаспределениеПоСтатьямВОбъект(
			СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], 
			СтрокаИсточник, 
			ТаблицаРаспределениеПоСтатьям);
КонецПроцедуры

Процедура СтрокаПособияВДанныеОбъекта(Организация, СтрокаПриемник, СтрокаИсточник, ТаблицыДокумента, ВидыРасчетаИнфо, ОписаниеТаблицы) Экспорт
	
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
	
	ЗначенияРеквизитов = ВидыРасчетаИнфо.Получить(СтрокаИсточник.Начисление);
	Если ЗначенияРеквизитов <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(СтрокаПриемник, ЗначенияРеквизитов);
		РасчетЗарплатыРасширенныйФормы.ДополнительныеСвойстваПособийВДанныеФормы(СтрокаПриемник, СтрокаИсточник);
		
		ПоказателиВОбъект(СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], СтрокаИсточник, ТаблицыДокумента, ЗначенияРеквизитов.Показатели);
		
		РасчетЗарплатыРасширенныйФормы.ЗаполнитьБазовыйПериодДанныеФормы(СтрокаПриемник, СтрокаИсточник, ЗначенияРеквизитов);
	КонецЕсли;
	
	РаспределениеПоТерриториямУсловиямТрудаВОбъект(
		СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], 
		СтрокаИсточник, 
		Организация, 
		ТаблицыДокумента);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") 
		И ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		
		РаспределениеПоСтатьямВОбъект(
			СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], 
			СтрокаИсточник, 
			ТаблицыДокумента.РаспределениеПоСтатьям);
	КонецЕсли;	
	
КонецПроцедуры

Процедура СтрокаНачисленияВМенеджерРасчета(МенеджерРасчета, 
											СтрокаИсточник, 
											Организация, 
											ФизическоеЛицо, 
											ТаблицыДокумента, 
											Владелец = Неопределено, 
											ВидНачисления = Неопределено, 
											БазовыйПериод = Неопределено) Экспорт
	
	СтрокаПриемник = МенеджерРасчета.СтрокаТаблицыНачислений();
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
	СтрокаПриемник.Организация = Организация;
	
	Если ВидНачисления = Неопределено Тогда
		ВидРасчетаИнфо = ЗарплатаКадрыРасширенныйПовтИсп.ПолучитьИнформациюОВидеРасчета(СтрокаИсточник.Начисление);
	Иначе
		ВидРасчетаИнфо = ЗарплатаКадрыРасширенныйПовтИсп.ПолучитьИнформациюОВидеРасчета(ВидНачисления);
		СтрокаПриемник.Начисление = ВидНачисления;
	КонецЕсли;
	
	РасчетЗарплатыРасширенныйФормы.ЗаполнитьБазовыйПериодДанныеМенеджераРасчета(
		СтрокаПриемник, 
		СтрокаИсточник, 
		ВидРасчетаИнфо,
		БазовыйПериод);
		
	СтрокаПриемник.ФизическоеЛицо = ФизическоеЛицо;
	СтрокаПриемник.Владелец = Владелец;
	РасчетЗарплатыРасширенныйФормы.ЗаполнитьДополнительныеСвойстваНачисленийМенеджераРасчета(
		СтрокаПриемник, 
		СтрокаИсточник);
	
	Если ТаблицыДокумента.Показатели <> Неопределено Тогда
		ПоказателиВМенеджерРасчета(
		СтрокаПриемник, 
		СтрокаИсточник.ИдентификаторСтрокиВидаРасчета, 
		ТаблицыДокумента.Показатели); 
	КонецЕсли;
	
	Если ТаблицыДокумента.РаспределениеПоТерриториямУсловиямТруда <> Неопределено Тогда
		РаспределениеПоТерриториямУсловиямТрудаВМенеджерРасчета(
			СтрокаПриемник,
			СтрокаИсточник.ИдентификаторСтрокиВидаРасчета,
			ТаблицыДокумента.РаспределениеПоТерриториямУсловиямТруда,
			ТаблицыДокумента.Показатели,
			МенеджерРасчета);
	КонецЕсли;
	
	Если ТаблицыДокумента.РаспределениеПоСтатьям <> Неопределено Тогда
		РезультатРаспределенияВМенеджерРасчета(
			СтрокаПриемник, 
			СтрокаИсточник.ИдентификаторСтрокиВидаРасчета, 
			ТаблицыДокумента.РаспределениеПоСтатьям);
	КонецЕсли;
		
КонецПроцедуры

Процедура СтрокаУдержанияВДанныеМенеджераРасчета(МенеджерРасчета, СтрокаИсточник, КатегорииУдержаний, ТаблицыДокумента, ИмяТаблицы) Экспорт
	СтрокаПриемник = МенеджерРасчета.СтрокаТаблицыУдержаний();
	СтрокаПриемник.Владелец = ИмяТаблицы;
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
	СтрокаПриемник.Категория = КатегорииУдержаний[СтрокаИсточник.Удержание];
	Если ТаблицыДокумента.Показатели <> Неопределено Тогда
		ПоказателиВМенеджерРасчета(СтрокаПриемник, СтрокаИсточник.ИдентификаторСтрокиВидаРасчета, ТаблицыДокумента.Показатели); 
	КонецЕсли;	
	Если ТаблицыДокумента.РаспределениеПоСтатьям <> Неопределено Тогда
		РезультатРаспределенияВМенеджерРасчета(
			СтрокаПриемник, 
			СтрокаИсточник.ИдентификаторСтрокиВидаРасчета, 
			ТаблицыДокумента.РаспределениеПоСтатьям);
	КонецЕсли;
КонецПроцедуры

Процедура СтрокаКорректировкиВыплатыВДанныеМенеджераРасчета(МенеджерРасчета, СтрокаИсточник, РаспределениеПоСтатьям = Неопределено) Экспорт
	СтрокаПриемник = МенеджерРасчета.Зарплата.КорректировкиВыплаты.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
	СтрокаПриемник.РаспределениеПоСтатьям = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
	Если РаспределениеПоСтатьям <> Неопределено Тогда
		РезультатРаспределенияВМенеджерРасчета(СтрокаПриемник, СтрокаИсточник.ИдентификаторСтроки, РаспределениеПоСтатьям);
	КонецЕсли;
КонецПроцедуры

Процедура СтрокаПогашениеЗаймовВДанныеМенеджераРасчета(МенеджерРасчета, СтрокаИсточник, РаспределениеПоСтатьям = Неопределено) Экспорт
	СтрокаПриемник = МенеджерРасчета.Зарплата.ПогашениеЗаймов.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
	СтрокаПриемник.РаспределениеПоСтатьям = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
	Если РаспределениеПоСтатьям <> Неопределено Тогда
		РезультатРаспределенияВМенеджерРасчета(СтрокаПриемник, СтрокаИсточник.ИдентификаторСтроки, РаспределениеПоСтатьям);
	КонецЕсли;
КонецПроцедуры

Процедура СтрокаНДФЛВДанныеМенеджераРасчета(МенеджерРасчета, СтрокаИсточник, СтрокиПримененныеВычеты, РаспределениеПоСтатьям = Неопределено) Экспорт
	
	СтрокаПриемник = МенеджерРасчета.Зарплата.НДФЛ.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
	
	ИменаРесурсовНалога = УчетНДФЛ.РесурсыИсчисленногоНалогаВМассиве("Налог");
	ИменаРесурсовСумма = УчетНДФЛ.РесурсыИсчисленногоНалогаВМассиве("Сумма");
	ВсегоКолонокСПревышением = ИменаРесурсовНалога.Количество();
	
	Для Сч = 1 По ВсегоКолонокСПревышением Цикл
		РесурсСумма = ИменаРесурсовСумма[Сч - 1];	
		РесурсНалога = ИменаРесурсовНалога[Сч - 1];
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаПриемник, РесурсСумма)
			И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаИсточник, РесурсНалога) Тогда

			СтрокаПриемник[РесурсСумма] = СтрокаИсточник[РесурсНалога];   
		
		КонецЕсли;
	КонецЦикла;                       
	
	ПоляТаблицы = УчетНДФЛ.ПоляТаблицыПоВидамВычетов();
	
	МесяцыПериодаПредоставленияВычета = Новый Соответствие;
	Для Каждого СтрокаВычета Из СтрокиПримененныеВычеты Цикл
		МесяцыПериодаПредоставленияВычета.Вставить(СтрокаВычета.МесяцПериодаПредоставленияВычета,СтрокаВычета.МесяцПериодаПредоставленияВычета)
	КонецЦикла;
	
	Если МесяцыПериодаПредоставленияВычета.Количество() = 0 Тогда
		
	ИначеЕсли МесяцыПериодаПредоставленияВычета.Количество() = 1 Тогда
		Для Каждого СтрокаВычета Из СтрокиПримененныеВычеты Цикл
			ИмяПоля = ПоляТаблицы[СтрокаВычета.КодВычета];
			Если СтрокаПриемник[ИмяПоля] = 0 Тогда
				СтрокаДляЗаполнения = СтрокаПриемник;
				СтрокаДляЗаполнения.НалоговаяБаза = СтрокаВычета.НалоговаяБаза;				
			Иначе
				СтрокаДляЗаполнения = МенеджерРасчета.Зарплата.НДФЛ.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДляЗаполнения, СтрокаИсточник,,"Налог,ЗачтеноАвансовыхПлатежей,ПримененныйВычетЛичный");
				СтрокаДляЗаполнения.РаспределениеПоСтатьям = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
			КонецЕсли;
			СтрокаДляЗаполнения[ИмяПоля] = СтрокаВычета.РазмерВычета;
			СтрокаДляЗаполнения.МесяцПериодаПредоставленияВычета = СтрокаВычета.МесяцПериодаПредоставленияВычета;    
			СтрокаДляЗаполнения.НалоговаяБаза = СтрокаВычета.НалоговаяБаза;			
		КонецЦикла; 
	Иначе
		ПервыйМесяцОбработан = Ложь;
		Для Каждого Элемент Из МесяцыПериодаПредоставленияВычета Цикл
			Если Не ПервыйМесяцОбработан Тогда
				СтрокаДляЗаполнения = СтрокаПриемник;
				ПервыйМесяцОбработан = Истина;
			Иначе
				СтрокаДляЗаполнения = МенеджерРасчета.Зарплата.НДФЛ.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДляЗаполнения, СтрокаИсточник,,"Налог,ЗачтеноАвансовыхПлатежей,ПримененныйВычетЛичный");
				СтрокаДляЗаполнения.РаспределениеПоСтатьям = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
			КонецЕсли;
			Для Каждого СтрокаВычета Из СтрокиПримененныеВычеты Цикл
				Если СтрокаВычета.МесяцПериодаПредоставленияВычета <> Элемент.Ключ Тогда
					Продолжить;
				КонецЕсли;
				ИмяПоля = ПоляТаблицы[СтрокаВычета.КодВычета];
				СтрокаДляЗаполнения[ИмяПоля] = СтрокаВычета.РазмерВычета;
				СтрокаДляЗаполнения.МесяцПериодаПредоставленияВычета = СтрокаВычета.МесяцПериодаПредоставленияВычета;       
				СтрокаДляЗаполнения.НалоговаяБаза = СтрокаВычета.НалоговаяБаза;
			КонецЦикла; 
		КонецЦикла;
	КонецЕсли;
	
	СтрокаПриемник.РаспределениеПоСтатьям = ОтражениеЗарплатыВУчете.НоваяТаблицаРаспределениеРезультатовУдержаний();
	РезультатРаспределенияВМенеджерРасчета(СтрокаПриемник, СтрокаИсточник.ИдентификаторСтрокиНДФЛ, РаспределениеПоСтатьям);
КонецПроцедуры

Процедура ПоказателиВМенеджерРасчета(СтрокаПриемник, ИдентификаторСтрокиДокумента, Показатели)
	СтрокиПоказателей = Показатели.НайтиСтроки(
		Новый Структура("ИдентификаторСтрокиВидаРасчета", ИдентификаторСтрокиДокумента));
		
	Для Каждого СтрокаПоказателяИсточник Из СтрокиПоказателей Цикл
		СтрокаПоказателя = СтрокаПриемник.Показатели.Добавить();
		СтрокаПоказателя.Показатель = СтрокаПоказателяИсточник.Показатель;
		СтрокаПоказателя.Значение = СтрокаПоказателяИсточник.Значение;	
	КонецЦикла;
КонецПроцедуры

Процедура РаспределениеПоТерриториямУсловиямТрудаВМенеджерРасчета(СтрокаПриемник, ИдентификаторСтрокиДокумента, 
	Распределение, Показатели, МенеджерРасчета) Экспорт
	
	СтрокиРаспределения = Распределение.НайтиСтроки(
		Новый Структура("ИдентификаторСтроки", ИдентификаторСтрокиДокумента));		

	Для Каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
		СтрокаТерриторииУсловияТруда = МенеджерРасчета.СтрокаТаблицыТерриторииУсловияТруда(СтрокаПриемник);
		ЗаполнитьЗначенияСвойств(СтрокаТерриторииУсловияТруда, СтрокаРаспределения);
		ПоказателиВМенеджерРасчета(
			СтрокаТерриторииУсловияТруда, 
			СтрокаРаспределения.ИдентификаторСтрокиПоказателей, 
			Показатели);
	КонецЦикла;
КонецПроцедуры

Процедура РезультатРаспределенияВМенеджерРасчета(СтрокаПриемник, ИдентификаторСтрокиДокумента, Распределение) Экспорт
	СтрокиРаспределения = Распределение.НайтиСтроки(
		Новый Структура("ИдентификаторСтроки", ИдентификаторСтрокиДокумента));	
	
	Для Каждого СтрокаРаспределения Из СтрокиРаспределения Цикл
		СтрокаРезультатаРаспределения = СтрокаПриемник.РаспределениеПоСтатьям.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРезультатаРаспределения, СтрокаРаспределения);
	КонецЦикла;
КонецПроцедуры

Функция СтрокаТаблицыПоНачальнойПозиции(ТаблицаИсточник, 
										ОписаниеТаблицы, 
										ОбходТаблицыИдентификаторов = Неопределено) Экспорт
	
	Если ОбходТаблицыИдентификаторов = Неопределено Тогда
		Возврат ДобавитьСтрокуТаблицыСИдентефикатором(ТаблицаИсточник, ОписаниеТаблицы);
	КонецЕсли;
	
	Если ОбходТаблицыИдентификаторов.ТекущаяПозиция >= ОбходТаблицыИдентификаторов.Количество Тогда
		СтрокаФормы = ДобавитьСтрокуТаблицыСИдентефикатором(ТаблицаИсточник, ОписаниеТаблицы);
		ОбходТаблицыИдентификаторов.Идентификаторы.Добавить(ТаблицаИсточник.Индекс(СтрокаФормы));
	Иначе
		Идентификатор = ОбходТаблицыИдентификаторов.Идентификаторы[ОбходТаблицыИдентификаторов.ТекущаяПозиция];
		СтрокаФормы = ТаблицаИсточник.Получить(Идентификатор);
	КонецЕсли;
	
	ОбходТаблицыИдентификаторов.ТекущаяПозиция = ОбходТаблицыИдентификаторов.ТекущаяПозиция + 1;
	Если СтрокаФормы = Неопределено Тогда
		УдалитьИдентификаторТаблицыФормы(ОбходТаблицыИдентификаторов);
		Возврат СтрокаТаблицыПоНачальнойПозиции(ТаблицаИсточник, ОписаниеТаблицы, ОбходТаблицыИдентификаторов);
	КонецЕсли;
	
	Возврат СтрокаФормы;
	
КонецФункции

Процедура УдалитьИдентификаторТаблицыФормы(ОбходТаблицыИдентификаторов)
	
	ОбходТаблицыИдентификаторов.ТекущаяПозиция = ОбходТаблицыИдентификаторов.ТекущаяПозиция - 1;
	ОбходТаблицыИдентификаторов.Идентификаторы.Удалить(ОбходТаблицыИдентификаторов.ТекущаяПозиция);
	ОбходТаблицыИдентификаторов.Количество = ОбходТаблицыИдентификаторов.Количество - 1;
	
КонецПроцедуры

Процедура СтрокаЛьготыВДанныеОбъекта(Организация, СтрокаПриемник, СтрокаИсточник, ТаблицыДокумента, ВидыРасчетаИнфо, ОписаниеТаблицы)

	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
	
	ЗначенияРеквизитов = ВидыРасчетаИнфо.Получить(СтрокаИсточник.Начисление);
	Если ЗначенияРеквизитов <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(СтрокаПриемник, ЗначенияРеквизитов);
		ПоказателиВОбъект(СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], СтрокаИсточник, ТаблицыДокумента, ЗначенияРеквизитов.Показатели);
	КонецЕсли;
	
	РаспределениеПоТерриториямУсловиямТрудаВОбъект(
		СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], 
		СтрокаИсточник, 
		Организация, 
		ТаблицыДокумента);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") 
		И ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		
		РаспределениеПоСтатьямВОбъект(
			СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], 
			СтрокаИсточник, 
			ТаблицыДокумента.РаспределениеПоСтатьям);
	КонецЕсли;

КонецПроцедуры 

Процедура СтрокаНачисленияВМенеджерРасчетаНачисленияВШапке(МенеджерРасчета, 
															СтрокаИсточник, 	
															Организация, 
															ФизическоеЛицо, 
															ВидНачисления, 
															БазовыйПериод = Неопределено,
															ТаблицыДокумента) Экспорт
	
	СтрокаПриемник = МенеджерРасчета.СтрокаТаблицыНачислений();
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
	СтрокаПриемник.ФизическоеЛицо = ФизическоеЛицо;
	СтрокаПриемник.Организация = Организация;
	СтрокаПриемник.Начисление = ВидНачисления;
	СтрокаПриемник.Владелец = "Начисления";
	ВидРасчетаИнфо = ЗарплатаКадрыРасширенныйПовтИсп.ПолучитьИнформациюОВидеРасчета(ВидНачисления);
	
	ЗаполнитьБазовыйПериодМенеджераРасчета(СтрокаПриемник, СтрокаИсточник, ВидРасчетаИнфо, БазовыйПериод);
	
	Если ТаблицыДокумента.Показатели <> Неопределено Тогда
		ПоказателиВМенеджерРасчета(СтрокаПриемник, СтрокаИсточник.ИдентификаторСтрокиВидаРасчета, ТаблицыДокумента.Показатели); 
	КонецЕсли;	
	
	Если ТаблицыДокумента.РаспределениеПоТерриториямУсловиямТруда <> Неопределено Тогда
		РаспределениеПоТерриториямУсловиямТрудаВМенеджерРасчета(
			СтрокаПриемник,
			СтрокаИсточник.ИдентификаторСтрокиВидаРасчета,
			ТаблицыДокумента.РаспределениеПоТерриториямУсловиямТруда,
			ТаблицыДокумента.Показатели,
			МенеджерРасчета);
	КонецЕсли;
	
	Если ТаблицыДокумента.РаспределениеПоСтатьям <> Неопределено Тогда
		РезультатРаспределенияВМенеджерРасчета(
			СтрокаПриемник, 
			СтрокаИсточник.ИдентификаторСтрокиВидаРасчета, 
			ТаблицыДокумента.РаспределениеПоСтатьям);
	КонецЕсли;

КонецПроцедуры 
  
Процедура ЗаполнитьБазовыйПериодМенеджераРасчета(СтрокаПриемник, СтрокаИсточник, ЗначенияРеквизитов, БазовыйПериод = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(СтрокаПриемник.ДатаНачала) Тогда
		СтрокаПриемник.ДатаНачала = НачалоМесяца(СтрокаИсточник.ПериодДействия);
		СтрокаПриемник.ДатаОкончания = КонецМесяца(СтрокаИсточник.ПериодДействия);
	КонецЕсли;
	
	Если ЗначенияРеквизитов.ТребуетсяРасчетБазы = Истина
		И ЗначенияРеквизитов.ИспользованиеПериода = Перечисления.ВариантыИспользованияПериодаНачисления.БазовыйПериод Тогда
		
		СтрокаПриемник.ДатаНачала = НачалоМесяца(СтрокаИсточник.ПериодДействия);
		СтрокаПриемник.ДатаОкончания = КонецМесяца(СтрокаИсточник.ПериодДействия);
		
		Если БазовыйПериод = Неопределено Тогда
			СтрокаПриемник.НачалоБазовогоПериода = СтрокаИсточник.ДатаНачала;
			СтрокаПриемник.ОкончаниеБазовогоПериода = СтрокаИсточник.ДатаОкончания;
		Иначе
			СтрокаПриемник.НачалоБазовогоПериода = БазовыйПериод.ДатаНачала;
			СтрокаПриемник.ОкончаниеБазовогоПериода = БазовыйПериод.ДатаОкончания;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СтрокаНачисленияПоДоговорамВДанныеОбъекта(Организация, СтрокаПриемник, СтрокаИсточник, ТаблицыДокумента, ОписаниеТаблицы)
	
	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник, , "ИдентификаторСтроки");
	РаспределениеПоТерриториямУсловиямТрудаВОбъект(
		СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], 
		СтрокаИсточник, 
		Организация, 
		ТаблицыДокумента);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") 
		И ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		
		РаспределениеПоСтатьямВОбъект(
			СтрокаПриемник[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки], 
			СтрокаИсточник, 
			ТаблицыДокумента.РаспределениеПоСтатьям);
	КонецЕсли;	
	
КонецПроцедуры 

Процедура СтрокаНачисленияПоДоговорамВДанныеМенеджераРасчета(МенеджерРасчета, 
											СтрокаИсточник, 
											ФизическоеЛицо,
											ТаблицыДокумента,
											КодыДоходаДоговорРаботыУслуги, 
											ДанныеРаспределенияПоТерриториямУсловиямТруда, 
											ДанныеРезультатовРаспределения) Экспорт   
											
	СтрокаПриемник = МенеджерРасчета.Зарплата.ДоговорыПодряда.Добавить();
	СтрокаПриемник.РаспределениеПоСтатьям = ОтражениеЗарплатыВБухучетеРасширенный.НоваяТаблицаРаспределениеРезультатовНачислений();

	ЗаполнитьЗначенияСвойств(СтрокаПриемник, СтрокаИсточник);
	
	Если КодыДоходаДоговорРаботыУслуги.Найти(СтрокаИсточник.КодДохода) <> Неопределено Тогда
    	СтрокаПриемник.Начисление = Перечисления.ВидыОсобыхНачисленийИУдержаний.ДоговорРаботыУслуги;
	Иначе
		СтрокаПриемник.Начисление = Перечисления.ВидыОсобыхНачисленийИУдержаний.ДоговорАвторскогоЗаказа;
	КонецЕсли;
	
	СтрокаПриемник.ФизическоеЛицо = ФизическоеЛицо;
	СтрокаПриемник.ТерриторииУсловияТруда = Обработки.МенеджерРасчетаЗарплаты.ТаблицаТерриторийУсловийТруда();
	
	Если ТаблицыДокумента.Показатели <> Неопределено Тогда
		ПоказателиВМенеджерРасчета(
		СтрокаПриемник, 
		СтрокаИсточник.ИдентификаторСтроки, 
		ТаблицыДокумента.Показатели); 
	КонецЕсли;
	
	Если ТаблицыДокумента.РаспределениеПоТерриториямУсловиямТруда <> Неопределено Тогда
		РаспределениеПоТерриториямУсловиямТрудаВМенеджерРасчета(
			СтрокаПриемник,
			СтрокаИсточник.ИдентификаторСтроки,
			ТаблицыДокумента.РаспределениеПоТерриториямУсловиямТруда,
			ТаблицыДокумента.Показатели,
			МенеджерРасчета);
	КонецЕсли;
	
	Если ТаблицыДокумента.РаспределениеПоСтатьям <> Неопределено Тогда
		РезультатРаспределенияВМенеджерРасчета(
			СтрокаПриемник, 
			СтрокаИсточник.ИдентификаторСтроки, 
			ТаблицыДокумента.РаспределениеПоСтатьям);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
