#Область ПрограммныйИнтерфейс

// Заполняет служебные реквизиты в табличной части формы зависящие от локализации
//
// Параметры:
//   Форма               - ФормаКлиентскогоПриложения - заполняемая форма
//   ПараметрыЗаполнения - Произвольный     - сценарий и параметры заполнения
//
Процедура ЗаполнитьСлужебныеРеквизиты(Форма, ПараметрыЗаполнения = Неопределено) Экспорт
	
	//++ Локализация
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Описание обработчиков строк коллекции:
// 
// Параметры:
//  ОбработчикиСтрок - СписокЗначений из Строка - Наименование обработчика строки табличной части:
//  * Представление - Строка - экспортного метода к выполнению.
//
Процедура ПриДобавленииОбработчиковСтрокКоллекции(ОбработчикиСтрок) Экспорт
	
	//++ Локализация
	
		ОбработчикиСтрок.Добавить("ЗаполнитьВидПродукцииИС",
		"ПакетнаяОбработкаТабличнойЧастиСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьПризнакМаркируемаяПродукция",
		"ПакетнаяОбработкаТабличнойЧастиСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьПризнакПродажиТовараПоРазрешению",
		"ПакетнаяОбработкаТабличнойЧастиСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьПризнакТребуетВзвешивания",
		"ПакетнаяОбработкаТабличнойЧастиСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьПризнакПроизвольнаяЕдиницаУчета",
		"ПакетнаяОбработкаТабличнойЧастиСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ");
		
	ОбработчикиСтрок.Добавить("ЗаполнитьПризнакСкоропортящаясяПродукция",
		"ПакетнаяОбработкаТабличнойЧастиСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьКодОКПД2Представление",
		"ПакетнаяОбработкаТабличнойЧастиСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьНоменклатуруЕГАИС",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ЗаполнитьНоменклатуруЕГАИС");
		
	ОбработчикиСтрок.Добавить("ЗаполнитьПризнакАвтоматическийОСУИС",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ЗаполнитьПризнакАвтоматическийОСУИС");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьАлкогольнуюПродукцию",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ЗаполнитьАлкогольнуюПродукцию");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьИндексАкцизнойМарки",
		"АкцизныеМаркиКлиентСервер.ЗаполнитьИндексАкцизнойМаркиДляСтрокиТабличнойЧасти");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьПродукциюВЕТИС",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ЗаполнитьПродукциюВЕТИС");
	
	ОбработчикиСтрок.Добавить("ПересчитатьКоличествоЕдиницВЕТИС",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ДействияПередПересчетомКоличестваЕдиницВЕТИС");
	
	ОбработчикиСтрок.Добавить("ПересчитатьКоличествоЕдиницВЕТИС",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ПересчитатьКоличествоЕдиницВЕТИСВСтрокеТЧ");
	
	ОбработчикиСтрок.Добавить("ПересчитатьКоличествоЕдиницПоВЕТИС",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ПересчитатьКоличествоЕдиницПоВЕТИСВСтрокеТЧ");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьКодОКПД2",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ЗаполнитьКодОКПД2ПоНоменклатуреВСтрокеТЧ");
	
	ОбработчикиСтрок.Добавить("ПересчитатьКоличествоЕдиницПоЗЕРНО",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ДействияПередПересчетомКоличестваЕдиницЗЕРНО");
		
	ОбработчикиСтрок.Добавить("ПересчитатьКоличествоЕдиницПоЗЕРНО",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ПересчитатьКоличествоЕдиницПоЗЕРНОВСтрокеТЧ");
	
	ОбработчикиСтрок.Добавить("СнятьПризнакМаркируемаяПродукцияАлкоголь",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.СнятьПризнакМаркируемаяПродукцияАлкоголь");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьСчетФактуруПолученнуюВОтчетеКомитентуОЗакупках",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ЗаполнитьСчетФактуруПолученнуюВОтчетеКомитентуОЗакупках");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьСчетФактуруПеревыставленнуюВОтчетеКомитентуОЗакупках",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ЗаполнитьСчетФактуруПеревыставленнуюВОтчетеКомитентуОЗакупках");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьПродукциюСАТУРН",
		"ПакетнаяОбработкаТабличнойЧастиСерверЛокализация.ЗаполнитьПродукциюСАТУРН");
	
	ОбработчикиСтрок.Добавить("ЗаполнитьЕстьВскрытыеПотребительскиеУпаковкиИС",
		"ПакетнаяОбработкаТабличнойЧастиСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ");
		
	//-- Локализация
	
КонецПроцедуры

// Описание текстов запросов, которые требуется выполнить для получения всех необходимых данных, необходимых для 
// обработки строк.
// 
// Параметры:
//  Действия - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ.СтруктураДействий.
//  ОписаниеЗапроса - см. ПакетнаяОбработкаТабличнойЧастиСервер.ОписаниеЗапроса
//  КэшированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ДополнитьТекстыЗапросовИсточниковДанных(Действия, ОписаниеЗапроса, КэшированныеЗначения) Экспорт
	
	//++ Локализация
	
	УчетНДСРФ.ДополнитьТекстЗапросаСчетФактурыПеревыставленныеВОтчетеКомитентуОЗакупках(Действия, ОписаниеЗапроса, КэшированныеЗначения);
	УчетНДСРФ.ДополнитьТекстЗапросаЗаполнитьСчетФактуруПолученнуюВОтчетеКомитентуОЗакупках(Действия, ОписаниеЗапроса, КэшированныеЗначения);
	
	ДополнитьТекстЗапросаКодОКПД2ПоНоменклатуре(Действия, ОписаниеЗапроса, КэшированныеЗначения);
	
	ДополнитьТекстЗапросаАлкогольнойПродукцииЕГАИС(Действия, ОписаниеЗапроса, КэшированныеЗначения);
	ДополнитьТекстЗапросаПродукцииВЕТИС(Действия, ОписаниеЗапроса, КэшированныеЗначения);
	ДополнитьТекстЗапросаКоэффициентыПересчетаУпаковкиВЕТИС(Действия, ОписаниеЗапроса, КэшированныеЗначения);
	ДополнитьТекстЗапросаКоэффициентыПересчетаЗЕРНО(Действия, ОписаниеЗапроса, КэшированныеЗначения);
	ДополнитьТекстЗапросаАвтоматическийОСУИС(Действия, ОписаниеЗапроса, КэшированныеЗначения);
	ДополнитьТекстЗапросаПродукцииСАТУРН(Действия, ОписаниеЗапроса, КэшированныеЗначения);
	ДополнитьТекстЗапросаДаннымиКомитентаПоТоварам(Действия, ОписаниеЗапроса, КэшированныеЗначения);
	
	//-- Локализация
	
КонецПроцедуры

Процедура ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВСтрокеТЧ(СтруктураДействий, СтруктураДействийЗаполнения) Экспорт
	
	//++ Локализация
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакМаркируемаяПродукция", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакМаркируемаяПродукция", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьВидПродукцииИС", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьВидПродукцииИС", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьЕстьВскрытыеПотребительскиеУпаковкиИС", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьЕстьВскрытыеПотребительскиеУпаковкиИС", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакПродажиТовараПоРазрешению", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакПродажиТовараПоРазрешению", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакТребуетВзвешивания", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакТребуетВзвешивания", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакПроизвольнаяЕдиницаУчета", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакПроизвольнаяЕдиницаУчета", СтруктураПараметровДействия);
	КонецЕсли;
	
	Если СтруктураДействий.Свойство("ЗаполнитьПризнакСкоропортящаясяПродукция", СтруктураПараметровДействия)
		И ЗначениеЗаполнено(СтруктураПараметровДействия) Тогда
		
		СтруктураДействийЗаполнения.Вставить("ЗаполнитьПризнакСкоропортящаясяПродукция", СтруктураПараметровДействия);
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

Процедура ПриОпределенииШаблонаПоляВыборкиПоКлючуДействия(КлючДействия, Шаблон) Экспорт
	
	//++ Локализация
	
	Если КлючДействия = "ЗаполнитьПризнакМаркируемаяПродукция" Тогда
		
		Шаблон =  ", " + ИнтеграцияИСУТ.ОпределениеПризнакаМаркируемаяПродукцияТекстаЗапроса("втТаблицаНоменклатуры.%Ключ%");
		Шаблон = Шаблон + " КАК %ЗначениеЗамены%";
		
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьВидПродукцииИС" Тогда
		
		Шаблон = ",
		|	&ОпределениеВидаПродукции КАК %ЗначениеЗамены%";
		ИнтеграцияИСУТ.ОпределитьВидПродукцииТекстаЗапроса(Шаблон, "втТаблицаНоменклатуры.%Ключ%");
		
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакПродажиТовараПоРазрешению" Тогда
		Шаблон = ",
		|	втТаблицаНоменклатуры.%Ключ%.ПродаетсяПоРазрешению КАК %ЗначениеЗамены%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакТребуетВзвешивания" Тогда
		Шаблон = ",
		|	1 НЕ В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			1
		|		ИЗ
		|			РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеИС
		|		ГДЕ
		|			втТаблицаНоменклатуры.%Ключ% = ОписаниеИС.Номенклатура
		|			Или втТаблицаНоменклатуры.%Ключ%.НаборУпаковок = ОписаниеИС.Номенклатура)
		|	И втТаблицаНоменклатуры.%Ключ%.ОсобенностьУчета В (
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияБезВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Пиво),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПивоВПотребительскихУпаковках),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.БезалкогольноеПиво),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхБезВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхВлажныеПодконтрольныеВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхВлажныеБезВЕТИС))
		|	И втТаблицаНоменклатуры.%Ключ%.ЕдиницаИзмерения.ТипИзмеряемойВеличины В(
		|		ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Вес),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Длина),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Объем),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Площадь)) КАК %ЗначениеЗамены%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакПроизвольнаяЕдиницаУчета" Тогда
		Шаблон = ",
		|	1 В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			1
		|		ИЗ
		|			РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеИС
		|		ГДЕ
		|			(втТаблицаНоменклатуры.%Ключ% = ОписаниеИС.Номенклатура
		|			Или втТаблицаНоменклатуры.%Ключ%.НаборУпаковок = ОписаниеИС.Номенклатура)
		|			И (ОписаниеИС.КоличествоВПотребительскойУпаковке <> 1
		|			Или ОписаниеИС.ЕмкостьПотребительскойУпаковки > 1))
		|	Или (
		|	1 НЕ В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			1
		|		ИЗ
		|			РегистрСведений.ОписаниеНоменклатурыИС КАК ОписаниеИС
		|		ГДЕ
		|			втТаблицаНоменклатуры.%Ключ% = ОписаниеИС.Номенклатура
		|			Или втТаблицаНоменклатуры.%Ключ%.НаборУпаковок = ОписаниеИС.Номенклатура)
		|	И втТаблицаНоменклатуры.%Ключ%.ОсобенностьУчета В (
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияБезВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.Пиво),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ПивоВПотребительскихУпаковках),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.БезалкогольноеПиво),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.МорепродуктыПодконтрольныеВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхПодконтрольныеВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхБезВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхВлажныеПодконтрольныеВЕТИС),
		|		ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.КормаДляЖивотныхВлажныеБезВЕТИС))
		|	И втТаблицаНоменклатуры.%Ключ%.ЕдиницаИзмерения.ТипИзмеряемойВеличины В(
		|		ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Вес),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Длина),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Объем),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Площадь))
		|	) КАК %ЗначениеЗамены%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьПризнакСкоропортящаясяПродукция" Тогда
		Шаблон = ",
		|	втТаблицаНоменклатуры.%Ключ%.ЕдиницаИзмеренияСрокаГодности = ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час) КАК %ЗначениеЗамены%";
	КонецЕсли;
	
	Если КлючДействия = "ЗаполнитьКодОКПД2" Тогда
		Шаблон = ",
		|	ЕСТЬNULL(втТаблицаНоменклатуры.%Ключ%.КодОКПД2.Код, """") КАК %ЗначениеЗамены%";
	КонецЕсли;   
	
	Если КлючДействия = "ЗаполнитьКодОКПД2Представление" Тогда
		Шаблон = ",
		|	ЕСТЬNULL(втТаблицаНоменклатуры.%Ключ%.КодОКПД2.Наименование, """") КАК %ЗначениеЗамены%";
	КонецЕсли;
		
	Если КлючДействия = "ЗаполнитьЕстьВскрытыеПотребительскиеУпаковкиИС" Тогда
		Шаблон =",
		|	1 В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			1
		|		ИЗ
		|			РегистрСведений.ВскрытыеПотребительскиеУпаковкиИС КАК ВскрытыеПотребительскиеУпаковкиИС
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
		|			ПО ВскрытыеПотребительскиеУпаковкиИС.КодМаркировки = ШтрихкодыУпаковокТоваров.Ссылка
		|			И ВскрытыеПотребительскиеУпаковкиИС.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыВскрытыхПотребительскихУпаковокИС.Подключено)
		|		ГДЕ
		|			ШтрихкодыУпаковокТоваров.Номенклатура = втТаблицаНоменклатуры.%Ключ%) КАК %ЗначениеЗамены%";
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Локализация

#Область ОписанияЗапросов

Процедура ДополнитьТекстЗапросаКодОКПД2ПоНоменклатуре(СтруктураДействий, ОписаниеЗапроса, КэшированныеЗначения) Экспорт
	
	Если ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
			"ЗаполнитьКодОКПД2",
			СтруктураДействий,
			КэшированныеЗначения) Тогда
				
		ТекстЗапроса = "ВЫБРАТЬ
		|	Товары.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
		|	КлассификаторОКПД2.Код КАК КодОКПД2,
		|	КлассификаторОКПД2.Наименование КАК КодОКПД2Наименование
		|ИЗ
		|	ВтИсточникДанных КАК Товары
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО СправочникНоменклатура.Ссылка = Товары.Номенклатура 
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОКПД2 КАК КлассификаторОКПД2
		|		ПО СправочникНоменклатура.КодОКПД2 = КлассификаторОКПД2.Ссылка
		|";
		
		ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "ВтКодОКПД2ПоНоменклатуре");
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьТекстЗапросаКоэффициентыПересчетаУпаковкиВЕТИС(СтруктураДействий, ОписаниеЗапроса, КэшированныеЗначения)
		
	Если Не ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
			"ПересчитатьКоличествоЕдиницВЕТИС,ПересчитатьКоличествоЕдиницПоВЕТИС",
			СтруктураДействий,
			КэшированныеЗначения) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеЕдиницИзмеренияВЕТИС.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
	|	ДанныеЕдиницИзмеренияВЕТИС.Номенклатура,
	|	ДанныеЕдиницИзмеренияВЕТИС.ЕдиницаИзмеренияВЕТИС,
	|	ЕСТЬNULL(ЕдиницыИзмеренияВЕТИС.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК
	|		Упаковка,
	|	ЕСТЬNULL(ЕдиницыИзмеренияВЕТИС.БазоваяЕдиницаИзмерения.ЕдиницаИзмерения, НЕОПРЕДЕЛЕНО) КАК Базовая,
	|	ЕСТЬNULL(ЕдиницыИзмеренияВЕТИС.Коэффициент, 1) КАК КоэффициентВЕТИС
	|ПОМЕСТИТЬ ДанныеУпаковок
	|ИЗ
	|	ВтИсточникДанных КАК ДанныеЕдиницИзмеренияВЕТИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмеренияВЕТИС КАК ЕдиницыИзмеренияВЕТИС
	|		ПО ЕдиницыИзмеренияВЕТИС.Ссылка = ДанныеЕдиницИзмеренияВЕТИС.ЕдиницаИзмеренияВЕТИС";
	
	ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "ДанныеУпаковок");
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеУпаковок.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
	|	ДанныеУпаковок.Номенклатура КАК Номенклатура,
	|	ДанныеУпаковок.Упаковка КАК Упаковка,
	|	ДанныеУпаковок.ЕдиницаИзмеренияВЕТИС КАК ЕдиницаИзмеренияВЕТИС,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.ЕдиницаИзмеренияВЕТИС = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмеренияВЕТИС.ПустаяСсылка)
	|			ТОГДА 1
	|		КОГДА ДанныеУпаковок.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА 1
	|		КОГДА ДанныеУпаковок.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)
	|		КОГДА ДанныеУпаковок.Упаковка.ТипИзмеряемойВеличины <> ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.КоличествоШтук)
	|			ТОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 0)
	|		КОГДА ДанныеУпаковок.Упаковка = СправочникНоменклатура.ЕдиницаИзмерения
	|			ТОГДА 1
	|		КОГДА ДанныеУпаковок.Базовая = СправочникНоменклатура.ЕдиницаИзмерения
	|			ТОГДА ДанныеУпаковок.КоэффициентВЕТИС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Коэффициент,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.ЕдиницаИзмеренияВЕТИС = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмеренияВЕТИС.ПустаяСсылка)
	|			ТОГДА Неопределено
	|		КОГДА ДанныеУпаковок.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА -1
	|		ИНАЧЕ ДанныеУпаковок.Упаковка.ТипИзмеряемойВеличины
	|	КОНЕЦ КАК ТипИзмеряемойВеличиныДляПроверки,
	|	ВЫБОР
	|		КОГДА ДанныеУпаковок.ЕдиницаИзмеренияВЕТИС = ЗНАЧЕНИЕ(Справочник.ЕдиницыИзмеренияВЕТИС.ПустаяСсылка)
	|			ТОГДА Неопределено
	|		КОГДА ДанныеУпаковок.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА Неопределено
	|		КОГДА ДанныеУпаковок.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА Неопределено
	|		ИНАЧЕ ЕСТЬNULL(ЕдиницыХранения.ТипИзмеряемойВеличины, НЕОПРЕДЕЛЕНО)
	|	КОНЕЦ КАК ТипИзмеряемойВеличиныНоменклатуры
	|ИЗ
	|	ДанныеУпаковок КАК ДанныеУпаковок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО СправочникНоменклатура.Ссылка = ДанныеУпаковок.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницыХранения
	|		ПО СправочникНоменклатура.ЕдиницаИзмерения = ЕдиницыХранения.Ссылка";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки", 
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки("Упаковка", "Номенклатура"));

	ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "КоэффициентыПересчетаУпаковкиВЕТИС");
	
КонецПроцедуры

Процедура ДополнитьТекстЗапросаКоэффициентыПересчетаЗЕРНО(СтруктураДействий, ОписаниеЗапроса, КэшированныеЗначения)

	Перем ПараметрыЗаполнения;
	
	Если Не ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
				"ПересчитатьКоличествоЕдиницПоЗЕРНО",
				СтруктураДействий,
				КэшированныеЗначения,
				ПараметрыЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Номенклатура,
	|	Килограмм.Значение КАК Упаковка
	|ИЗ
	|	ВтИсточникДанных КАК Товары,
	|	Константа.ЕдиницаИзмеренияКилограммИС КАК Килограмм
	|";
	
	ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "УпаковкиПересчетаЗЕРНО");
	
КонецПроцедуры

Процедура ДополнитьТекстЗапросаПродукцииВЕТИС(СтруктураДействий, ОписаниеЗапроса, КэшированныеЗначения)
	
	ПараметрыЗаполнения = Неопределено;
	
	Если Не ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
			"ЗаполнитьПродукциюВЕТИС",
			СтруктураДействий,
			КэшированныеЗначения,
			ПараметрыЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПараметрыЗаполнения.ЗаполнитьПродукциюВЕТИС Тогда
		Возврат;
	КонецЕсли;
	
	Если КэшированныеЗначения.Свойство("ЕдиницаИзмеренияВЕТИСИзменена") Тогда
		КэшированныеЗначения.Удалить("ЕдиницаИзмеренияВЕТИСИзменена");
	КонецЕсли;
	
	Отбор = Неопределено;
	ЕстьФильтрПродукции = Ложь;
	ПараметрыЗаполнения.Свойство("ОтборПродукция",Отбор);
	
	ОписаниеЗапроса.ПараметрыЗапроса.Вставить("ИсключатьПродукциюТретьегоУровня", ПараметрыЗаполнения.ИсключатьПродукциюТретьегоУровня);
	Если ЗначениеЗаполнено(Отбор) Тогда
		Если Отбор.Свойство("ХозяйствующийСубъект") Тогда
			ОписаниеЗапроса.ПараметрыЗапроса.Вставить("ХозяйствующийСубъект", Отбор.ХозяйствующийСубъект);
			ЕстьФильтрПродукции = Истина;
		КонецЕсли;
		Если Отбор.Свойство("Предприятие") Тогда
			ОписаниеЗапроса.ПараметрыЗапроса.Вставить("Предприятие", Отбор.Предприятие);
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьФильтрПродукции Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СоответствиеНоменклатурыВЕТИС.Номенклатура КАК Номенклатура,
		|	СоответствиеНоменклатурыВЕТИС.Характеристика КАК Характеристика,
		|	СоответствиеНоменклатурыВЕТИС.Серия КАК Серия,
		|	СоответствиеНоменклатурыВЕТИС.Продукция КАК Продукция
		|ПОМЕСТИТЬ ФильтрПродукцииВЕТИС
		|ИЗ
		|	Справочник.ПродукцияВЕТИС.Производители КАК Производители
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС
		|		ПО Производители.Ссылка = СоответствиеНоменклатурыВЕТИС.Продукция
		|ГДЕ
		|	Производители.Производитель = &Предприятие
		|	И Производители.Ссылка.ХозяйствующийСубъектПроизводитель = &ХозяйствующийСубъект
		|	И (НЕ &ИсключатьПродукциюТретьегоУровня ИЛИ Производители.Ссылка.Идентификатор <> """")
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Серия";
		ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "ФильтрПродукцииВЕТИС");
		
		ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
		|	ЕСТЬNULL(ПродукцияВЕТИС.Ссылка, Неопределено) КАК Продукция,
		|	ПродукцияВЕТИС.ФасовкаЕдиницаИзмерения КАК ЕдиницаИзмеренияВЕТИС,
		|	ПродукцияВЕТИС.Продукция.Идентификатор КАК ПродукцияИдентификатор,
		|	ПродукцияВЕТИС.ТипПродукции.Идентификатор КАК ТипПродукцииИдентификатор,
		|	ПродукцияВЕТИС.ТипПродукции КАК ТипПродукции,
		|	ЕСТЬNULL(ПродукцияВЕТИС.ВидПродукции.Идентификатор, ПродукцияВЕТИС.Идентификатор) КАК ВидПродукцииИдентификатор,
		|	ПродукцияВЕТИС.ФасовкаЕдиницаИзмерения КАК ФасовкаЕдиницаИзмерения
		|ИЗ
		|	ВтИсточникДанных КАК Товары
		|	ЛЕВОЕ СОЕДИНЕНИЕ ФильтрПродукцииВЕТИС КАК ФильтрПродукцииВЕТИС
		|	ПО ФильтрПродукцииВЕТИС.Номенклатура = Товары.Номенклатура
		|	И ФильтрПродукцииВЕТИС.Характеристика = Товары.Характеристика
		|	И (ФильтрПродукцииВЕТИС.Серия = Товары.Серия
		|		ИЛИ Товары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПродукцияВЕТИС КАК ПродукцияВЕТИС
		|	ПО ПродукцияВЕТИС.Ссылка = ФильтрПродукцииВЕТИС.Продукция";
		ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "СоответствиеНоменклатурыВЕТИС");
		
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
		|	ЕСТЬNULL(СоответствиеНоменклатурыВЕТИС.Продукция, Неопределено) КАК Продукция,
		|	СоответствиеНоменклатурыВЕТИС.Продукция.ФасовкаЕдиницаИзмерения КАК ЕдиницаИзмеренияВЕТИС,
		|	СоответствиеНоменклатурыВЕТИС.Продукция.Продукция.Идентификатор КАК ПродукцияИдентификатор,
		|	СоответствиеНоменклатурыВЕТИС.Продукция.ТипПродукции.Идентификатор КАК ТипПродукцииИдентификатор,
		|	СоответствиеНоменклатурыВЕТИС.Продукция.ТипПродукции КАК ТипПродукции,
		|	ЕСТЬNULL(СоответствиеНоменклатурыВЕТИС.Продукция.ВидПродукции.Идентификатор,
		|		СоответствиеНоменклатурыВЕТИС.Продукция.Идентификатор) КАК ВидПродукцииИдентификатор,
		|	СоответствиеНоменклатурыВЕТИС.Продукция.ФасовкаЕдиницаИзмерения КАК ФасовкаЕдиницаИзмерения
		|ИЗ
		|	ВтИсточникДанных КАК Товары
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыВЕТИС КАК СоответствиеНоменклатурыВЕТИС
		|	ПО СоответствиеНоменклатурыВЕТИС.Номенклатура = Товары.Номенклатура
		|	И СоответствиеНоменклатурыВЕТИС.Характеристика = Товары.Характеристика
		|	И (СоответствиеНоменклатурыВЕТИС.Серия = Товары.Серия
		|		ИЛИ Товары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
		|	И (НЕ &ИсключатьПродукциюТретьегоУровня
		|		ИЛИ СоответствиеНоменклатурыВЕТИС.Продукция.Идентификатор <> """")";
	
		ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "СоответствиеНоменклатурыВЕТИС");
	КонецЕсли;

КонецПроцедуры

Процедура ДополнитьТекстЗапросаАлкогольнойПродукцииЕГАИС(СтруктураДействий, ОписаниеЗапроса, КэшированныеЗначения)
	
	ПараметрыЗаполнения = Неопределено;
	Если Не ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
			"ЗаполнитьАлкогольнуюПродукцию",
			СтруктураДействий,
			КэшированныеЗначения,
			ПараметрыЗаполнения)
		И Не ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
			"ЗаполнитьНоменклатуруЕГАИС",
			СтруктураДействий,
			КэшированныеЗначения) Тогда
		Возврат;
	КонецЕсли;
	Если ПараметрыЗаполнения <> Неопределено И Не ПараметрыЗаполнения.ЗаполнитьАлкогольнуюПродукцию Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
	|	ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция, Неопределено) КАК АлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция.Крепость КАК Крепость
	|ИЗ
	|	ВтИсточникДанных КАК Товары
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|	ПО СоответствиеНоменклатурыЕГАИС.Номенклатура = Товары.Номенклатура
	|	И СоответствиеНоменклатурыЕГАИС.Характеристика = Товары.Характеристика
	|	И (СоответствиеНоменклатурыЕГАИС.Серия = Товары.Серия
	|		ИЛИ Товары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
	|	И &ОтборМаркируемаяПродукция
	|	И &ОтборИмпортПроизводство
	|";
	
	ОписаниеЗапроса.ПараметрыЗапроса.Вставить("ОтборМаркируемаяПродукция", Истина);
	ОписаниеЗапроса.ПараметрыЗапроса.Вставить("ОтборИмпортПроизводство", Истина);
	
	Если ПараметрыЗаполнения <> Неопределено
			И ПараметрыЗаполнения.МаркируемаяАлкогольнаяПродукция <> Неопределено Тогда
		ОписаниеЗапроса.ПараметрыЗапроса.Вставить("ОтборМаркируемаяПродукция", ПараметрыЗаполнения.МаркируемаяАлкогольнаяПродукция);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборМаркируемаяПродукция",
		"СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция.ВидПродукции.Маркируемый = &ОтборМаркируемаяПродукция");
	КонецЕсли;
	Если ПараметрыЗаполнения <> Неопределено
			И ПараметрыЗаполнения.ИмпортПроизводство <> Неопределено Тогда
		Если ПараметрыЗаполнения.ИмпортПроизводство Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборИмпортПроизводство",
			"СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция.Производитель.ТипОрганизации В(
			|	ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЕГАИС.ЮридическоеЛицоРФ),
			|	ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЕГАИС.ИндивидуальныйПредпринимательРФ))");
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборИмпортПроизводство",
			"СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция.Производитель.ТипОрганизации В(
			|	ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЕГАИС.КонтрагентТаможенногоСоюза),
			|	ЗНАЧЕНИЕ(Перечисление.ТипыОрганизацийЕГАИС.ИностранныйКонтрагент))");
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "СоответствиеНоменклатурыЕГАИС");
	
КонецПроцедуры

Процедура ДополнитьТекстЗапросаАвтоматическийОСУИС(СтруктураДействий, ОписаниеЗапроса, КэшированныеЗначения)
	
	Если Не ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
			"ЗаполнитьПризнакАвтоматическийОСУИС", СтруктураДействий, КэшированныеЗначения) Тогда
		Возврат;
	КонецЕсли;
	
	Если КэшированныеЗначения.Свойство("ЗаполненПризнакАвтоматическийОСУИС") Тогда
		КэшированныеЗначения.Удалить("ЗаполненПризнакАвтоматическийОСУИС");
	КонецЕсли;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Товары.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.ВидПродукцииИС КАК ВидПродукцииИС,
		|	ЛОЖЬ КАК АвтоматическийОСУИС
		|ИЗ
		|	ВтИсточникДанных КАК Товары";
	
	ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "ИсточникПризнакАвтоматическийОСУИС");
	
КонецПроцедуры

Процедура ДополнитьТекстЗапросаПродукцииСАТУРН(СтруктураДействий, ОписаниеЗапроса, КэшированныеЗначения)
	
	ПараметрыЗаполнения = Неопределено;
	
	Если Не ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
			"ЗаполнитьПродукциюСАТУРН",
			СтруктураДействий,
			КэшированныеЗначения,
			ПараметрыЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
		|	СоответствиеНоменклатурыСАТУРН.ПАТ КАК ПАТ
		|ИЗ
		|	ВтИсточникДанных КАК Товары
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатурыСАТУРН
		|	ПО СоответствиеНоменклатурыСАТУРН.Номенклатура = Товары.Номенклатура
		|	И СоответствиеНоменклатурыСАТУРН.Характеристика = Товары.Характеристика
		|	И СоответствиеНоменклатурыСАТУРН.Серия = Товары.Серия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
		|	СоответствиеНоменклатурыСАТУРН.ПАТ КАК ПАТ
		|ИЗ
		|	ВтИсточникДанных КАК Товары
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК ПолноеСоответствие
		|	ПО ПолноеСоответствие.Номенклатура = Товары.Номенклатура
		|	И ПолноеСоответствие.Характеристика = Товары.Характеристика
		|	И ПолноеСоответствие.Серия = Товары.Серия
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыСАТУРН КАК СоответствиеНоменклатурыСАТУРН
		|	ПО СоответствиеНоменклатурыСАТУРН.Номенклатура = Товары.Номенклатура
		|	И СоответствиеНоменклатурыСАТУРН.Характеристика = Товары.Характеристика
		|ГДЕ
		|	ПолноеСоответствие.Номенклатура ЕСТЬ NULL
		|";
	
	ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "СоответствиеНоменклатурыСАТУРН");
	
КонецПроцедуры

Процедура ДополнитьТекстЗапросаДаннымиКомитентаПоТоварам(СтруктураДействий, ОписаниеЗапроса, КэшированныеЗначения)
	
	ПараметрыЗаполнения = Неопределено;
	
	Если Не ПакетнаяОбработкаТабличнойЧастиСервер.ТребуетсяВыполнитьДействие(
			"ЗаполнитьСобственникаТовара", СтруктураДействий, КэшированныеЗначения, ПараметрыЗаполнения) 
		Или Не ЗначениеЗаполнено(ПараметрыЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеЗапросаДанных = РозничныеПродажиЛокализация.ЗапросДанныхКомитентовПоТоварам(ПараметрыЗаполнения.Организация, ПараметрыЗаполнения.Склад, "ВтИсточникДанных", "ДляДанныхКомитента");
	Если ОписаниеЗапросаДанных.ПакетЗапросов.Количество() Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ОписаниеЗапроса.ПараметрыЗапроса, ОписаниеЗапросаДанных.ПараметрыЗапроса);
		ОбщегоНазначенияКлиентСервер.ДополнитьСписок(ОписаниеЗапроса.ТекстыЗапросов, ОписаниеЗапросаДанных.ПакетЗапросов);
		
		ТекстЗапроса = "ВЫБРАТЬ 
			|	Товары.ИдентификаторСтрокиВТ КАК ИдентификаторСтрокиВТ,
			|	ЕСТЬNULL(ДанныеКомитентаПоТоварам.Комитент, Неопределено) КАК Комитент
			|ИЗ
			|	ВтИсточникДанных КАК Товары
			|	ЛЕВОЕ СОЕДИНЕНИЕ #ДанныеКомитентаПоТоварам КАК ДанныеКомитентаПоТоварам
			|	ПО ДанныеКомитентаПоТоварам.Номенклатура = Товары.Номенклатура
			|	И ДанныеКомитентаПоТоварам.Характеристика = Товары.Характеристика
			|	И ДанныеКомитентаПоТоварам.Серия = Товары.Серия";
	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ДанныеКомитентаПоТоварам", ОписаниеЗапросаДанных.ПакетЗапросов[ОписаниеЗапросаДанных.ПакетЗапросов.Количество()-1].Представление);
		ОписаниеЗапроса.ТекстыЗапросов.Добавить(ТекстЗапроса, "ДанныеКомитентаПоТоварам");
		ОписаниеЗапроса.ПривилегированныйРежим = Истина;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСтрок

Процедура ЗаполнитьСчетФактуруПеревыставленнуюВОтчетеКомитентуОЗакупках(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	ДанныеДляОбработкиСтроки = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки(
									"СчетФактурыПеревыставленныеВОтчетеКомитентуОЗакупках",
									КэшированныеЗначения);
									
	ТекущаяСтрока.СчетФактураВыданныйКомитенту = Документы.СчетФактураВыданный.ПустаяСсылка();

	Если ДанныеДляОбработкиСтроки <> Неопределено Тогда
		ТекущаяСтрока.СчетФактураВыданныйКомитенту = ДанныеДляОбработкиСтроки[0].СчетФактураВыданныйКомитенту; 
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьКодОКПД2ПоНоменклатуреВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	ПараметрыЗаполнения = Неопределено;
	
	СтруктураДействий.Свойство("ЗаполнитьКодОКПД2", ПараметрыЗаполнения);
	
	Реквизиты = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки(
		"ВтКодОКПД2ПоНоменклатуре", КэшированныеЗначения);
		
	Если Не Реквизиты = Неопределено Тогда
		ТекущаяСтрока.ОКПД2 = Реквизиты[0].КодОКПД2;
		Если ЗначениеЗаполнено(ТекущаяСтрока.ОКПД2) Тогда
			ТекущаяСтрока.ОКПД2Представление = ИнтеграцияЗЕРНОКлиентСервер.ПредставлениеОКПД2(
				Реквизиты[0].КодОКПД2Наименование, ТекущаяСтрока.ОКПД2);
		Иначе
			ТекущаяСтрока.ОКПД2Представление = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПродукциюВЕТИС(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения = Неопределено) Экспорт
	
	ПараметрыЗаполнения = Неопределено;
	Если Не СтруктураДействий.Свойство("ЗаполнитьПродукциюВЕТИС", ПараметрыЗаполнения) Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ПараметрыЗаполнения.ЗаполнитьПродукциюВЕТИС Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки("СоответствиеНоменклатурыВЕТИС", КэшированныеЗначения);
	
	ТекущаяСтрока.НоменклатураДляВыбора.Очистить();
	Если Реквизиты[0].Продукция = Неопределено Тогда
		КоличествоСопоставлено = 0;
	Иначе
		КоличествоСопоставлено = Реквизиты.Количество();
		Для Каждого ЭлементСопоставления Из Реквизиты Цикл
			ТекущаяСтрока.НоменклатураДляВыбора.Добавить(ЭлементСопоставления.Продукция);
		КонецЦикла;
	КонецЕсли;
		
	Если КоличествоСопоставлено = 0 Тогда
		ТекущаяСтрока.Продукция = Неопределено;
		ТекущаяСтрока.СопоставлениеТекст = НСтр("ru = '<Не сопоставлено>';
												|en = '<Not mapped>'");
	ИначеЕсли КоличествоСопоставлено > 1 Тогда
		ТекущаяСтрока.СопоставлениеТекст = СтрШаблон(НСтр("ru = '<Несколько позиций (%1)>';
															|en = '<Several positions (%1)>'"), КоличествоСопоставлено);
	Иначе
		ТекущаяСтрока.СопоставлениеТекст = "";
		ТекущаяСтрока.Продукция = Реквизиты[0].Продукция;
		ЕдиницаИзмеренияВЕТИСДо = ТекущаяСтрока.ЕдиницаИзмеренияВЕТИС;
		ИнтеграцияВЕТИС.ПроверитьОчиститьЕдиницуИзмеренияВЕТИС(ТекущаяСтрока, Реквизиты[0]);
	// После получения новой единицы измерения ВЕТИС, выполненный пакетный запрос для пересчета количества 
	// ВЕТИС по количеству становится неактуальным
		Если ЕдиницаИзмеренияВЕТИСДо <> ТекущаяСтрока.ЕдиницаИзмеренияВЕТИС Тогда
			Если Не КэшированныеЗначения.Свойство("ЕдиницаИзмеренияВЕТИСИзменена") Тогда
				КэшированныеЗначения.Вставить("ЕдиницаИзмеренияВЕТИСИзменена", Новый Соответствие);
				КэшированныеЗначения.ЕдиницаИзмеренияВЕТИСИзменена.Вставить("ЗаполнениеПроизведено", Ложь);
				КэшированныеЗначения.ЕдиницаИзмеренияВЕТИСИзменена.Вставить("Коэффициенты", Новый Соответствие);
				ТаблицаДозаполнения = Новый ТаблицаЗначений;
				ТаблицаДозаполнения.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
				ТаблицаДозаполнения.Колонки.Добавить("ЕдиницаИзмеренияВЕТИС", Новый ОписаниеТипов("СправочникСсылка.ЕдиницыИзмеренияВЕТИС"));
				КэшированныеЗначения.Вставить("ДанныеСтрокВЕТИС", ТаблицаДозаполнения);
			КонецЕсли;
			НоваяСтрока = КэшированныеЗначения.ДанныеСтрокВЕТИС.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияПередПересчетомКоличестваЕдиницВЕТИС(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если Не СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиницВЕТИС") Тогда
		Возврат;
	КонецЕсли;
	Если Не КэшированныеЗначения.Свойство("ЕдиницаИзмеренияВЕТИСИзменена") Тогда
		Возврат;
	КонецЕсли;
	Если КэшированныеЗначения.ЕдиницаИзмеренияВЕТИСИзменена.Получить("ЗаполнениеПроизведено") Тогда
		Возврат;
	КонецЕсли;
	КоэффициентыЕдиницИзмеренияПоВЕТИС = ИнтеграцияВЕТИСУТ.КоэффициентыЕдиницИзмеренияПоВЕТИС(
		КэшированныеЗначения.ДанныеСтрокВЕТИС);
	КэшированныеЗначения.ЕдиницаИзмеренияВЕТИСИзменена.Вставить("Коэффициенты", КоэффициентыЕдиницИзмеренияПоВЕТИС);
	КэшированныеЗначения.ЕдиницаИзмеренияВЕТИСИзменена.Вставить("ЗаполнениеПроизведено", Истина);
	
КонецПроцедуры

Процедура ДействияПередПересчетомКоличестваЕдиницЗЕРНО(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если Не СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиницПоЗЕРНО") Тогда
		Возврат;
	КонецЕсли;
	Если КэшированныеЗначения.Свойство("КоэффициентыПересчетаЗЕРНО") Тогда
		Возврат;
	КонецЕсли;
	КоэффициентыПересчетаЗЕРНО = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентыУпаковок(
		КэшированныеЗначения.ДанныеДляОбработки.УпаковкиПересчетаЗЕРНО);
	КэшированныеЗначения.Вставить("КоэффициентыПересчетаЗЕРНО", КоэффициентыПересчетаЗЕРНО);
	
КонецПроцедуры

// Пересчитывает количество товара ВЕТИС в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока			- Структура - Структура со свойствами строки документа.
//	СтруктураДействий		- Структура - Структура с действиями, которые нужно произвести.
//	КэшированныеЗначения	- Структура - Сохраненные значения параметров, используемых при обработке строки таблицы.
//
Процедура ПересчитатьКоличествоЕдиницВЕТИСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем ПараметрыПересчетаКоличества, ТекстОшибки;
	
	Если Не СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиницВЕТИС", ПараметрыПересчетаКоличества) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяКоличества = "Количество" + ПараметрыПересчетаКоличества.Суффикс;
	ИмяКоличестваВЕТИС = "Количество" + ПараметрыПересчетаКоличества.Суффикс + "ВЕТИС";
	
	ПараметрыПересчета = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.НормализоватьПараметрыПересчетаЕдиниц(ТекущаяСтрока, ПараметрыПересчетаКоличества);
	НужноОкруглять = ПараметрыПересчета.НужноОкруглять;
	
	// Если в процессе менялась единица ВетИС
	Если КэшированныеЗначения.Свойство("ЕдиницаИзмеренияВЕТИСИзменена") Тогда
		ДанныеПоНоменклатуре = КэшированныеЗначения.ЕдиницаИзмеренияВЕТИСИзменена.Получить("Коэффициенты").Получить(ТекущаяСтрока.Номенклатура);
		Если ДанныеПоНоменклатуре <> Неопределено Тогда
			ДанныеПоЕдинице = ДанныеПоНоменклатуре.Получить(ТекущаяСтрока.ЕдиницаИзмеренияВЕТИС);
			Если ДанныеПоЕдинице <> Неопределено Тогда
				Если ДанныеПоЕдинице.КодОшибки = 0 Тогда
					ТекущаяСтрока[ИмяКоличестваВЕТИС] = ТекущаяСтрока[ИмяКоличества] / ДанныеПоЕдинице.Коэффициент;
					Если НужноОкруглять И ДанныеПоЕдинице.НужноОкруглятьКоличествоВЕТИС Тогда
						ТекущаяСтрока[ИмяКоличестваВЕТИС] = Окр(ТекущаяСтрока[ИмяКоличестваВЕТИС], 0, РежимОкругления.Окр15как20);
					КонецЕсли;
				Иначе
					ТекстОшибки = ИнтеграцияВЕТИСУТКлиентСервер.ТекстОшибкиПересчетаЕдиницыИзмеренияВЕТИС(
						ДанныеПоЕдинице.КодОшибки,
						ТекущаяСтрока.Номенклатура,
						ТекущаяСтрока.ЕдиницаИзмеренияВЕТИС,
						ДанныеПоЕдинице.ТипИзмеряемойВеличины,
						"ВЕТИС");
					ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
				КонецЕсли;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Реквизиты = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки("КоэффициентыПересчетаУпаковкиВЕТИС", КэшированныеЗначения);
	Если Реквизиты = Неопределено Тогда
		ТекстОшибки = ИнтеграцияВЕТИСУТКлиентСервер.ТекстОшибкиПересчетаЕдиницыИзмеренияВЕТИС(
			ДанныеПоЕдинице.КодОшибки,
			ТекущаяСтрока.Номенклатура,
			ТекущаяСтрока.ЕдиницаИзмеренияВЕТИС,
			ДанныеПоЕдинице.ТипИзмеряемойВеличины,
			"ВЕТИС");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	МерныеТипыЕдиницИзмерений = Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений();
	ДанныеПересчета = Реквизиты[0];
	Коэффициент = 1;
	КодОшибки = 0;
	ТипИзмеряемойВеличины = Неопределено;
	Если ДанныеПересчета.ТипИзмеряемойВеличиныДляПроверки = -1 Тогда
		КодОшибки = 1;
	ИначеЕсли НЕ ЗначениеЗаполнено(ДанныеПересчета.ЕдиницаИзмеренияВЕТИС) Тогда
	Иначе
		Если ДанныеПересчета.Коэффициент = 0 Тогда
			Если МерныеТипыЕдиницИзмерений.Найти(ДанныеПересчета.ТипИзмеряемойВеличиныДляПроверки) <> Неопределено Тогда
				КодОшибки = 2;
				ТипИзмеряемойВеличины = ДанныеПересчета.ТипИзмеряемойВеличиныДляПроверки;
			Иначе
				КодОшибки = 3;
			КонецЕсли;
		Иначе
			Коэффициент = ДанныеПересчета.Коэффициент;
		КонецЕсли;
	КонецЕсли;
	
	НужноОкруглять = НужноОкруглять
		И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура)
		И ДанныеПересчета.ТипИзмеряемойВеличиныДляПроверки = Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук
		И МерныеТипыЕдиницИзмерений.Найти(ДанныеПересчета.ТипИзмеряемойВеличиныНоменклатуры) <> Неопределено;
	Если КодОшибки = 0 Тогда
		ТекущаяСтрока[ИмяКоличестваВЕТИС] = ТекущаяСтрока[ИмяКоличества] / Коэффициент;
		Если НужноОкруглять Тогда
			ТекущаяСтрока[ИмяКоличестваВЕТИС] = Окр(ТекущаяСтрока[ИмяКоличестваВЕТИС], 0, РежимОкругления.Окр15как20);
		КонецЕсли;
	Иначе
		ТекстОшибки = ИнтеграцияВЕТИСУТКлиентСервер.ТекстОшибкиПересчетаЕдиницыИзмеренияВЕТИС(
			КодОшибки, ТекущаяСтрока.Номенклатура, ТекущаяСтрока.ЕдиницаИзмеренияВЕТИС, ТипИзмеряемойВеличины, "ВЕТИС");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает количество товара (в единицах хранения) в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока			- Структура - Структура со свойствами строки документа.
//	СтруктураДействий		- Структура - Структура с действиями, которые нужно произвести.
//	КэшированныеЗначения	- Структура - Сохраненные значения параметров, используемых при обработке строки таблицы.
//
Процедура ПересчитатьКоличествоЕдиницПоВЕТИСВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем ПараметрыПересчетаКоличества, ТекстОшибки;
	
	Если Не СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиницПоВЕТИС", ПараметрыПересчетаКоличества) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПересчета = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.НормализоватьПараметрыПересчетаЕдиниц(ТекущаяСтрока, ПараметрыПересчетаКоличества);
	НужноОкруглять = ПараметрыПересчета.НужноОкруглять;
	ИмяКоличества = "Количество" + ПараметрыПересчетаКоличества.Суффикс;
	ИмяКоличестваВЕТИС = "Количество" + ПараметрыПересчетаКоличества.Суффикс + "ВЕТИС";
	
	// Если в процессе менялась единица ВетИС
	Если КэшированныеЗначения.Свойство("ЕдиницаИзмеренияВЕТИСИзменена") Тогда
		ДанныеПоНоменклатуре = КэшированныеЗначения.ЕдиницаИзмеренияВЕТИСИзменена.Получить("Коэффициенты").Получить(ТекущаяСтрока.Номенклатура);
		Если ДанныеПоНоменклатуре <> Неопределено Тогда
			ДанныеПоЕдинице = ДанныеПоНоменклатуре.Получить(ТекущаяСтрока.ЕдиницаИзмеренияВЕТИС);
			Если ДанныеПоЕдинице <> Неопределено Тогда
				Если ДанныеПоЕдинице.КодОшибки = 0 Тогда
					ТекущаяСтрока[ИмяКоличества] = ТекущаяСтрока[ИмяКоличестваВЕТИС] * ДанныеПоЕдинице.Коэффициент;
					Если НужноОкруглять
						И ДанныеПоЕдинице.НужноОкруглятьКоличество Тогда
						ТекущаяСтрока[ИмяКоличества] = Окр(ТекущаяСтрока[ИмяКоличества], 0, РежимОкругления.Окр15как20);
					КонецЕсли;
				Иначе
					ТекстОшибки = ИнтеграцияВЕТИСУТКлиентСервер.ТекстОшибкиПересчетаЕдиницыИзмеренияВЕТИС(
						ДанныеПоЕдинице.КодОшибки,
						ТекущаяСтрока.Номенклатура,
						ТекущаяСтрока.ЕдиницаИзмеренияВЕТИС,
						ДанныеПоЕдинице.ТипИзмеряемойВеличины,
						"ВЕТИС");
					ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
				КонецЕсли;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Реквизиты = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки("КоэффициентыПересчетаУпаковкиВЕТИС", КэшированныеЗначения);
	Если Реквизиты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МерныеТипыЕдиницИзмерений = Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений();
	ДанныеПересчета = Реквизиты[0];
	Коэффициент = 1;
	ТипИзмеряемойВеличины = Неопределено;
	КодОшибки = 0;
	Если ДанныеПересчета.ТипИзмеряемойВеличиныДляПроверки = -1 Тогда
		КодОшибки = 1;
	ИначеЕсли НЕ ЗначениеЗаполнено(ДанныеПересчета.ЕдиницаИзмеренияВЕТИС) Тогда
	Иначе
		Если ДанныеПересчета.Коэффициент = 0 Тогда
			Если МерныеТипыЕдиницИзмерений.Найти(ДанныеПересчета.ТипИзмеряемойВеличиныДляПроверки) <> Неопределено Тогда
				КодОшибки = 2;
				ТипИзмеряемойВеличины = ДанныеПересчета.ТипИзмеряемойВеличиныДляПроверки;
			Иначе
				КодОшибки = 3;
			КонецЕсли;
		Иначе
			Коэффициент = ДанныеПересчета.Коэффициент;
		КонецЕсли;
	КонецЕсли;
	
	НужноОкруглять = НужноОкруглять
		И ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура)
		И ДанныеПересчета.ТипИзмеряемойВеличиныНоменклатуры = Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук
		И МерныеТипыЕдиницИзмерений.Найти(ДанныеПересчета.ТипИзмеряемойВеличиныДляПроверки) <> Неопределено;
	
	Если КодОшибки = 0 Тогда
		ТекущаяСтрока[ИмяКоличества] = ТекущаяСтрока[ИмяКоличестваВЕТИС] * Коэффициент;
		Если НужноОкруглять Тогда
			ТекущаяСтрока[ИмяКоличества] = Окр(ТекущаяСтрока[ИмяКоличества], 0, РежимОкругления.Окр15как20);
		КонецЕсли;
	Иначе
		ТекстОшибки = ИнтеграцияВЕТИСУТКлиентСервер.ТекстОшибкиПересчетаЕдиницыИзмеренияВЕТИС(
			КодОшибки, ТекущаяСтрока.Номенклатура, ТекущаяСтрока.ЕдиницаИзмеренияВЕТИС, ТипИзмеряемойВеличины, "ВЕТИС");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

// Заполнить алкогольную продукцию строки табличной части.
// 
// Параметры:
//  ТекущаяСтрока - Произвольный - строка (документа), в которой происходит заполнение.
//  ПараметрыЗаполнения  - см. ИнтеграцияЕГАИСКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
//  КэшированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения.
Процедура ЗаполнитьАлкогольнуюПродукцию(ТекущаяСтрока, ПараметрыЗаполнения, КэшированныеЗначения) Экспорт
	
	Параметры = Неопределено;
	Если НЕ ПараметрыЗаполнения.Свойство("ЗаполнитьАлкогольнуюПродукцию", Параметры) Тогда
		Возврат;
	КонецЕсли;
	Если ТипЗнч(Параметры) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьПоляВыбора = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "НоменклатураДляВыбора");
	Если ЕстьПоляВыбора Тогда
		ТекущаяСтрока.НоменклатураДляВыбора.Очистить();
	КонецЕсли;
	
	Реквизиты = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки("СоответствиеНоменклатурыЕГАИС", КэшированныеЗначения);
	
	Если Реквизиты[0].АлкогольнаяПродукция = Неопределено Тогда
		КоличествоСопоставлено = 0;
	Иначе
		КоличествоСопоставлено = Реквизиты.Количество();
		Если ЕстьПоляВыбора Тогда
			Для Каждого ЭлементСопоставления Из Реквизиты Цикл
				ТекущаяСтрока.НоменклатураДляВыбора.Добавить(ЭлементСопоставления.АлкогольнаяПродукция);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если КоличествоСопоставлено = 0 Тогда
		ТекущаяСтрока.АлкогольнаяПродукция = Неопределено;
		Если ЕстьПоляВыбора Тогда
			ТекущаяСтрока.СопоставлениеАлкогольнаяПродукция = НСтр("ru = '<Не сопоставлено>';
																	|en = '<Not mapped>'");
		КонецЕсли;
	ИначеЕсли КоличествоСопоставлено = 1 Тогда
		ТекущаяСтрока.АлкогольнаяПродукция = Реквизиты[0].АлкогольнаяПродукция;
		Если ЕстьПоляВыбора Тогда
			ТекущаяСтрока.СопоставлениеАлкогольнаяПродукция = "";
		КонецЕсли;
		Если Параметры.ЗаполнитьКрепость Тогда
			ТекущаяСтрока.Крепость = Реквизиты[0].Крепость;
			Если ЕстьПоляВыбора Тогда
				ТекущаяСтрока.ПредставлениеКрепости = Реквизиты[0].Крепость;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ЕстьПоляВыбора Тогда
			ТекущаяСтрока.СопоставлениеАлкогольнаяПродукция = СтрШаблон(НСтр("ru = '<Несколько позиций (%1)>';
																			|en = '<Several positions (%1)>'"), КоличествоСопоставлено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполнить алкогольную продукцию строки табличной части.
// 
// Параметры:
//  ТекущаяСтрока - Произвольный - строка (документа), в которой происходит заполнение.
//  ПараметрыЗаполнения  - см. ИнтеграцияЕГАИСКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
//  КэшированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения.
Процедура ЗаполнитьНоменклатуруЕГАИС(ТекущаяСтрока, ПараметрыЗаполнения, КэшированныеЗначения) Экспорт
	
	Если НЕ ПараметрыЗаполнения.Свойство("ЗаполнитьНоменклатуруЕГАИС") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока.НоменклатураДляВыбора.Очистить();
	Реквизиты = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки("СоответствиеНоменклатурыЕГАИС", КэшированныеЗначения);
	
	Если Реквизиты[0].АлкогольнаяПродукция = Неопределено Тогда
		КоличествоСопоставлено = 0;
	Иначе
		КоличествоСопоставлено = Реквизиты.Количество();
		Для Каждого ЭлементСопоставления Из Реквизиты Цикл
			ТекущаяСтрока.НоменклатураДляВыбора.Добавить(ЭлементСопоставления.АлкогольнаяПродукция);
		КонецЦикла;
	КонецЕсли;
	Если ТекущаяСтрока.НоменклатураДляВыбора.НайтиПоЗначению(ТекущаяСтрока.НоменклатураЕГАИС) = Неопределено Тогда
		ТекущаяСтрока.НоменклатураЕГАИС = Неопределено;
	КонецЕсли;
	
	Если КоличествоСопоставлено = 0 Тогда
		ТекущаяСтрока.СопоставлениеАлкогольнаяПродукция = НСтр("ru = '<Не сопоставлено>';
																|en = '<Not mapped>'");
	ИначеЕсли КоличествоСопоставлено = 1 Тогда
		ТекущаяСтрока.НоменклатураЕГАИС = Реквизиты[0].АлкогольнаяПродукция;
		ТекущаяСтрока.СопоставлениеАлкогольнаяПродукция = "";
	Иначе
		ТекущаяСтрока.СопоставлениеАлкогольнаяПродукция = СтрШаблон(НСтр("ru = '<Несколько позиций (%1)>';
																		|en = '<Several positions (%1)>'"), КоличествоСопоставлено);
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает количество товара (в единицах хранения ФГИС Зерно) в текущей строке табличной части документа.
//
// Параметры:
//	ТекущаяСтрока			- Структура - Структура со свойствами строки документа.
//	СтруктураДействий		- Структура - Структура с действиями, которые нужно произвести.
//	КэшированныеЗначения	- Структура - Сохраненные значения параметров, используемых при обработке строки таблицы.
//
Процедура ПересчитатьКоличествоЕдиницПоЗЕРНОВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если Не СтруктураДействий.Свойство("ПересчитатьКоличествоЕдиницПоЗЕРНО") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не КэшированныеЗначения.Свойство("КоэффициентыПересчетаЗЕРНО") Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициент = КэшированныеЗначения.КоэффициентыПересчетаЗЕРНО.Получить(ТекущаяСтрока.Номенклатура);
	Если Коэффициент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	// в соответствии ровно 1 элемент - коэффициент для килограмма
	Для Каждого КлючИЗначение Из Коэффициент Цикл
		Коэффициент = КлючИЗначение.Значение;
		Если Не ЗначениеЗаполнено(Коэффициент) Тогда
			Коэффициент = 1;
		КонецЕсли;
	КонецЦикла;
	
	Если ТекущаяСтрока.Количество > 0 Тогда
		
		Если ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура) Тогда
			ТекущаяСтрока.КоличествоЗЕРНО = ТекущаяСтрока.Количество / Коэффициент;
		Иначе
			ТекущаяСтрока.Количество = 0;
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(ТекущаяСтрока.Номенклатура)
		И ТекущаяСтрока.КоличествоЗЕРНО > 0 Тогда
		ТекущаяСтрока.Количество = ТекущаяСтрока.КоличествоЗЕРНО * Коэффициент;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает сведения о коэффициенте пересчета единицы измерения ВЕТИС.
//
// Параметры:
//	ЕдиницаИзмеренияВЕТИС - СправочникСсылка.ЕдиницыИзмеренияВЕТИС	- Единица измерения ВЕТИС, коэффициент которой нужно 
//																		получить.
//	Номенклатура - СправочникСсылка.Номенклатура - Номенклатура для единицы хранения, которой осуществляется получение 
//		коэффициента пересчета.
//	КэшированныеЗначения - Структура - Сохраненные значения параметров, используемых при обработке строки таблицы.
//
// Возвращаемое значение:
//	см. ИнтеграцияВЕТИСУТ.КоэффициентЕдиницыИзмеренияПоВЕТИС.
//
Функция ДанныеЕдиницыИзмеренияВЕТИС(ЕдиницаИзмеренияВЕТИС, Номенклатура, КэшированныеЗначения) Экспорт
	
	КлючКоэффициента = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.КлючКэшаУпаковки(Номенклатура, ЕдиницаИзмеренияВЕТИС);
	
	ДанныеЕдиницыИзмеренияВЕТИС = КэшированныеЗначения.КоэффициентыУпаковок.Получить(КлючКоэффициента);
	Если ДанныеЕдиницыИзмеренияВЕТИС = Неопределено Тогда
		
		ДанныеЕдиницыИзмеренияВЕТИС = ИнтеграцияВЕТИСУТ.КоэффициентЕдиницыИзмеренияПоВЕТИС(
			ЕдиницаИзмеренияВЕТИС, Номенклатура);
		
		Если ДанныеЕдиницыИзмеренияВЕТИС.КэшироватьДанные Тогда
			
			ИменаКлючей = "КодОшибки, Коэффициент, ТипИзмеряемойВеличины, НужноОкруглятьКоличество,"
							+ "НужноОкруглятьКоличествоВЕТИС";
			
			ДанныеКлюча = Новый Структура(ИменаКлючей);
			ЗаполнитьЗначенияСвойств(ДанныеКлюча, ДанныеЕдиницыИзмеренияВЕТИС);
			
			КэшированныеЗначения.КоэффициентыУпаковок.Вставить(КлючКоэффициента, ДанныеКлюча);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДанныеЕдиницыИзмеренияВЕТИС;
	
КонецФункции

// Заполняет поле СчетФактураПолученный в строке табличной части найденным документом Документ.СчетФактураПолученный,
// введенным на основании указанного документа приобретения.
// Поиск осуществляется только счетов-фактур полученных, введенных на основании документов типов:
// 	1) Документ.ПриобретениеТоваровУслуг;
// 	2) Документ.ВозвратТоваровПоставщику;
// 	3) Документ.КорректировкаПриобретения.
//
// Параметры:
// 	ТекущаяСтрока - ДанныеФормыСтруктура - текущая строка для обработки.
// 	СтруктураДействий - Структура Из КлючИЗначение - структура, содержащая перечень действий со строкой.
// 	КэшированныеЗначения - Структура Из КлючИЗначение - кэшированные значения обработчика.
//
Процедура ЗаполнитьСчетФактуруПолученнуюВОтчетеКомитентуОЗакупках(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если Не СтруктураДействий.Свойство("ЗаполнитьСчетФактуруПолученнуюВОтчетеКомитентуОЗакупках") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПоСтроке = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки(
		"ЗаполнитьСчетФактуруПолученнуюВОтчетеКомитентуОЗакупках",
		КэшированныеЗначения);
	
	Если ДанныеПоСтроке <> Неопределено Тогда
		ТекущаяСтрока.СчетФактураПолученный = ДанныеПоСтроке[0].Ссылка;
	КонецЕсли;
	
КонецПроцедуры

Процедура СнятьПризнакМаркируемаяПродукцияАлкоголь(ТекущаяСтрока, СтруктураДействий = Неопределено, КэшированныеЗначения = Неопределено) Экспорт
	
	Если ТекущаяСтрока.МаркируемаяПродукция
		И ТекущаяСтрока.ВидПродукцииИС = Перечисления.ВидыПродукцииИС.Алкогольная Тогда
			ТекущаяСтрока.МаркируемаяПродукция = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПризнакАвтоматическийОСУИС(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Если Не СтруктураДействий.Свойство("ЗаполнитьПризнакАвтоматическийОСУИС") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не КэшированныеЗначения.Свойство("ЗаполненПризнакАвтоматическийОСУИС") Тогда
		Таблица = КэшированныеЗначения.ДанныеДляОбработки.ИсточникПризнакАвтоматическийОСУИС; //ТаблицаЗначений-
		ПроверкаИПодборПродукцииИСМП.ЗаполнитьПризнакАвтоматическийОСУИСВТаблице(Таблица);
		КэшированныеЗначения.Вставить("ЗаполненПризнакАвтоматическийОСУИС");
	КонецЕсли;
	
	ДанныеПоСтроке = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки("ИсточникПризнакАвтоматическийОСУИС", КэшированныеЗначения);
	Если ДанныеПоСтроке <> Неопределено Тогда
		ТекущаяСтрока.АвтоматическийОСУИС = ДанныеПоСтроке[0].АвтоматическийОСУИС;
	КонецЕсли;
	
КонецПроцедуры

// Заполнить ПАТ САТУРН строки табличной части.
// 
// Параметры:
//  ТекущаяСтрока - Произвольный - строка (документа), в которой происходит заполнение
//  ПараметрыЗаполнения - Структура - Параметры заполнения
//  КэшированныеЗначения - Неопределено - Кэшированные значения
Процедура ЗаполнитьПродукциюСАТУРН(ТекущаяСтрока, ПараметрыЗаполнения, КэшированныеЗначения) Экспорт
	
	Если Не ПараметрыЗаполнения.Свойство("ЗаполнитьПродукциюСАТУРН") Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьПоляВыбора = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, "НоменклатураДляВыбора");
	Если ЕстьПоляВыбора Тогда
		ТекущаяСтрока.НоменклатураДляВыбора.Очистить();
	КонецЕсли;
	
	Реквизиты = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки("СоответствиеНоменклатурыСАТУРН", КэшированныеЗначения);
	
	Если Реквизиты = Неопределено Тогда
		КоличествоСопоставлено = 0;
	Иначе
		КоличествоСопоставлено = Реквизиты.Количество();
		Если ЕстьПоляВыбора Тогда
			Для Каждого ЭлементСопоставления Из Реквизиты Цикл
				ТекущаяСтрока.НоменклатураДляВыбора.Добавить(ЭлементСопоставления.ПАТ);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если КоличествоСопоставлено = 0 Тогда
		ТекущаяСтрока.ПАТ = Неопределено;
		Если ЕстьПоляВыбора Тогда
			ТекущаяСтрока.СопоставлениеТекст = НСтр("ru = '<Не сопоставлено>';
													|en = '<Not mapped>'");
		КонецЕсли;
	ИначеЕсли КоличествоСопоставлено = 1 Тогда
		ТекущаяСтрока.ПАТ = Реквизиты[0].ПАТ;
		Если ЕстьПоляВыбора Тогда
			ТекущаяСтрока.СопоставлениеТекст = "";
		КонецЕсли;
	Иначе
		Если ЕстьПоляВыбора Тогда
			ТекущаяСтрока.СопоставлениеТекст = СтрШаблон(НСтр("ru = '<Несколько позиций (%1)>';
																|en = '<Several positions (%1)>'"), КоличествоСопоставлено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура, заполняет собственника товара в строке табличной части документа, используется в розничных документах.
// 
// Параметры:
//	ТекущаяСтрока - Структура - текущая строка табличной части.
//	СтруктураДействий - Структура - допустимые действия для табличной части.
//	КэшированныеЗначения - см. ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения.
//
Процедура ЗаполнитьСобственникаТовараВСтрокеТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения) Экспорт
	
	Перем СтруктураПараметровДействия;
	
	Если СтруктураДействий.Свойство("ЗаполнитьСобственникаТовара", СтруктураПараметровДействия) 
		И Не СтруктураПараметровДействия = Неопределено Тогда
			
		ТекущаяСтрока.Комитент = Неопределено;
		ДанныеКомитентаПоТоваруТекущейСтроки = ПакетнаяОбработкаТабличнойЧастиСервер.ДанныеДляОбработкиСтроки("ДанныеКомитентаПоТоварам", КэшированныеЗначения); 
		Если Не ДанныеКомитентаПоТоваруТекущейСтроки = Неопределено 
			И ДанныеКомитентаПоТоваруТекущейСтроки.Количество() Тогда
			ТекущаяСтрока.Комитент = ДанныеКомитентаПоТоваруТекущейСтроки[0].Комитент;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

//-- Локализация

#КонецОбласти
