
#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

// См. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.34.97";
	Обработчик.РежимВыполнения 	= "Отложенно";
	Обработчик.Идентификатор 	= Новый УникальныйИдентификатор("a8f3f0ad-205c-4915-8b68-6fa5254c0772");
	Обработчик.Процедура       = "Интеграция1СПерсоналРасширенный.СоздатьРешенияОПриемеНаРаботу";
	Обработчик.Комментарий = НСтр("ru = 'Регистрация решений о приеме на работу на основании офферов.';
									|en = 'Register employment decisions based on offers.'");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОчиститьДанныеПриОтключенииПриложения() Экспорт

	Параметры = Новый Структура("ИспользуетсяПриложение1СПерсонал", Ложь);
	ИнтеграцияУправлениеПерсоналомРасширенный.УстановитьИспользованиеРешенияОПриемеНаРаботу(Параметры);

КонецПроцедуры

#Область ОбновлениеИнформационнойБазы

Процедура СоздатьРешенияОПриемеНаРаботу(ПараметрыОбновления = Неопределено) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользуетсяПриложение1СПерсонал") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УдалитьОффер.Ссылка КАК Ссылка,
	|	УдалитьОффер.Дата КАК Дата,
	|	УдалитьОффер.Кандидат КАК ФизическоеЛицо,
	|	УдалитьОффер.СрокПринятия КАК СрокПринятия,
	|	УдалитьОффер.Должность КАК Должность,
	|	УдалитьОффер.Подразделение КАК Подразделение,
	|	УдалитьОффер.ДолжностьПоШтатномуРасписанию КАК ДолжностьПоШтатномуРасписанию,
	|	УдалитьОффер.ДатаВыходаНаРаботу КАК ДатаПриема,
	|	УдалитьОффер.ИдентификаторДокумента КАК ИдентификаторДокумента
	|ИЗ
	|	Документ.УдалитьОффер КАК УдалитьОффер
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РешениеОПриемеНаРаботу КАК РешениеОПриемеНаРаботу
	|		ПО УдалитьОффер.ИдентификаторДокумента = РешениеОПриемеНаРаботу.ИдентификаторДокумента
	|ГДЕ
	|	РешениеОПриемеНаРаботу.ИдентификаторДокумента ЕСТЬ NULL
	|	И НЕ УдалитьОффер.ПометкаУдаления";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	ОбработкаЗавершена = Истина;
	Пока Выборка.Следующий() Цикл
		
		ПодразделениеОрганизации 	= Неопределено;
		Подразделение 				= Неопределено;
		Организация 				= Неопределено;
		Если ТипЗнч(Выборка.Подразделение) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
			ПодразделениеОрганизации = Выборка.Подразделение;
			Если ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
				Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодразделениеОрганизации, "Владелец");
			КонецЕсли;
		Иначе
			Подразделение = Выборка.Подразделение;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Выборка.ДолжностьПоШтатномуРасписанию) Тогда
			ДанныеПозиции = Справочники.ШтатноеРасписание.ДанныеПозицииШтатногоРасписания(Выборка.ДолжностьПоШтатномуРасписанию);
			Организация = ДанныеПозиции.Организация;
		КонецЕсли;
		
		ДокументОбъект = Документы.РешениеОПриемеНаРаботу.СоздатьДокумент();
		ДокументОбъект.Дата 			= Выборка.Дата;
		ДокументОбъект.Организация 		= Организация;
		ДокументОбъект.ФизическоеЛицо 	= Выборка.ФизическоеЛицо;
		ДокументОбъект.Должность 		= Выборка.Должность;
		ДокументОбъект.ДатаПриема 		= Выборка.ДатаПриема;
		ДокументОбъект.Внешний 			= Истина;
		ДокументОбъект.ИдентификаторДокумента 			= Выборка.ИдентификаторДокумента;
		ДокументОбъект.ДолжностьПоШтатномуРасписанию 	= Выборка.ДолжностьПоШтатномуРасписанию;
		ДокументОбъект.ПодразделениеОрганизации 		= ПодразделениеОрганизации;
		ДокументОбъект.Подразделение 					= Подразделение;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект);
		
	КонецЦикла;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбработкаЗавершена);

КонецПроцедуры

#КонецОбласти


#КонецОбласти