// @strict-types

#Область ПрограммныйИнтерфейс

#Область ЦеныТоваров

// Возвращает детальную информацию по типам цен, используемым учетными записями Ozon.
//
// Возвращаемое значение:
//   ТаблицаЗначений - таблица с описанием указанных типов цен учетной записи; содержит колонки:
//     * Идентификатор          - Строка - строковое обозначение типа цен.
//     * ИдентификаторДляФормул - Строка - шаблон для формирования идентификатора вида цен.
//     * Наименование           - Строка - наименование типа цен.
//     * ДляУчетнойЗаписи       - Булево - признак отношения типа цен к учетной записи или к интеграции в целом.
//     * ОбязательноеЗаполнение - Булево - признак обязательного заполнения в формах.
//     * Значение               - СправочникСсылка.ВидыЦен - пустая ссылка вида цен.
//     * ИмяПараметраСИ         - Строка - имя параметра вида цены в запросе к сервису интеграции
//
Функция ТипыЦенWildberries() Экспорт

	ТипыЦен = ИнтеграцияСМаркетплейсамиСИ.НовыйТаблицаТипыЦен();

	НоваяСтрока = ТипыЦен.Добавить();
	НоваяСтрока.Идентификатор = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяВидЦеныДоСкидок();
	НоваяСтрока.ИдентификаторДляФормул = "";
	НоваяСтрока.Наименование = НСтр("ru = 'Цена до скидок';
									|en = 'Price before discounts'");
	НоваяСтрока.ДляУчетнойЗаписи = Истина;
	НоваяСтрока.ОбязательноеЗаполнение = Ложь;
	НоваяСтрока.ИмяПараметраСИ = "price";
	
	НоваяСтрока = ТипыЦен.Добавить();
	НоваяСтрока.Идентификатор = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяВидЦеныСоСкидкойПродавца();
	НоваяСтрока.ИдентификаторДляФормул = "";
	НоваяСтрока.Наименование = НСтр("ru = 'Цена с учетом скидки продавца';
									|en = 'Price with seller discount'");
	НоваяСтрока.ДляУчетнойЗаписи = Истина;
	НоваяСтрока.ОбязательноеЗаполнение = Истина;
	НоваяСтрока.ИмяПараметраСИ = "priceDiscounted";

	НоваяСтрока = ТипыЦен.Добавить();
	НоваяСтрока.Идентификатор = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяВидЦеныКлубная();
	НоваяСтрока.ИдентификаторДляФормул = "";
	НоваяСтрока.Наименование = НСтр("ru = 'Цена после применения скидки продавца и скидки WB Клуба';
									|en = 'Price after applying the seller discount and the WB Club discount'");
	НоваяСтрока.ДляУчетнойЗаписи = Истина;
	НоваяСтрока.ОбязательноеЗаполнение = Ложь;
	НоваяСтрока.ИмяПараметраСИ = "priceClubDiscounted";

	Возврат ТипыЦен;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет перенос всех элементов справочника "Номенклатура партнеров" по учетной записи на нового владельца
// 
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//  Партнер - СправочникСсылка.Партнеры - новый партнер.
// 
// Возвращаемое значение:
//  см. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода
Функция ИзменитьВладельцаНоменклатурыКонтрагента(УчетнаяЗапись, Партнер) Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
	Результат.КодСостояния = 200;
	
	Пока Истина Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 100
		|	НоменклатураКонтрагентов.Ссылка КАК Ссылка
		|ИЗ
		|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМП
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
		|		ПО СоответствияОбъектовМП.Объект1С = НоменклатураКонтрагентов.Ссылка
		|		И НоменклатураКонтрагентов.ВладелецНоменклатуры <> &Партнер
		|ГДЕ
		|	СоответствияОбъектовМП.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
		|	И СоответствияОбъектовМП.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)";

		Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
		Запрос.УстановитьПараметр("Партнер", Партнер);

		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Прервать;
		КонецЕсли;

		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.НоменклатураКонтрагентов");
		ЭлементБлокировкиДанных.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Ссылка", "Ссылка");
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;

		НачатьТранзакцию();

		Попытка
			БлокировкаДанных.Заблокировать();

			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл

				НоменклатураКонтрагентовОбъект = Выборка.Ссылка.ПолучитьОбъект();
				НоменклатураКонтрагентовОбъект.ВладелецНоменклатуры = Партнер;
				НоменклатураКонтрагентовОбъект.Записать();

			КонецЦикла;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
		КонецПопытки;

	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#Область СервисныеФункции

// Возвращает имя события журнала регистрации работы с маркетплейсом.
//
// Возвращаемое значение:
//   Строка - Наименование события для записей в журнале регистрации.
Функция СобытиеЖурналаРегистрации() Экспорт
	Возврат НСтр("ru = 'Интеграция с Wildberries';
				|en = 'Integration with Wildberries'", ОбщегоНазначения.КодОсновногоЯзыка());
КонецФункции

#КонецОбласти

#КонецОбласти