// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

#Область КонтрактМенеджераДействийЭДО

// Параметры:
//  МенеджерыДействийЭДО - Соответствие из КлючИЗначение:
//  * Ключ - Строка - идентификатор интеграции ЭДО.
//  * Значение - ОбщийМодуль
Процедура ПриОпределенииМенеджеровДействийЭДО(МенеджерыДействийЭДО) Экспорт
	
	МенеджерыДействийЭДО.Вставить(
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиентСервер.ИдентификаторМенеджераДействийЭДО(),
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиент);
	
КонецПроцедуры

// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения - результат: см. ЭлектронныеДокументыЭДОКлиент.НовыйРезультатОбработкиДействийИнтеграцииЭДО
//  РезультатВыполненияДействий - см. ЭлектронныеДокументыЭДОИнтеграцияОблака.РезультатВыполненияДействийЭДОДляКлиента
//  СостояниеВыполненияДействий - см. НовоеСостояниеВыполненияДействийЭДО
//  ИсходныйНаборДействийЭДО - см. ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО
//  ПаролиСертификатов - см. КриптографияБЭДКлиент.НовыеПаролиСертификатов
Процедура ОбработатьРезультатВыполненияДействийЭДО(ОповещениеОЗавершении, РезультатВыполненияДействий, СостояниеВыполненияДействий, ИсходныйНаборДействийЭДО, ПаролиСертификатов) Экспорт
	
	КонтекстОбработки = НовыйКонтекстОбработкиРезультатаВыполненияДействийЭДО();
	КонтекстОбработки.ОповещениеОЗавершении = ОповещениеОЗавершении;
	КонтекстОбработки.АсинхронныеОперации = РезультатВыполненияДействий.АсинхронныеОперации;
	КонтекстОбработки.РезультатыДействий = РезультатВыполненияДействий.РезультатыДействий;
	КонтекстОбработки.ИсходныйНаборДействийЭДО = ИсходныйНаборДействийЭДО;
	КонтекстОбработки.КонтекстДиагностики = РезультатВыполненияДействий.КонтекстДиагностики;
	КонтекстОбработки.ПаролиСертификатов = ПаролиСертификатов;
	
	Если ЗначениеЗаполнено(СостояниеВыполненияДействий) Тогда
		КонтекстОбработки.СостояниеВыполненияДействий = СостояниеВыполненияДействий;
	Иначе
		КонтекстОбработки.СостояниеВыполненияДействий = НовоеСостояниеВыполненияДействийЭДО();
	КонецЕсли;
	
	АсинхронныеОперации = КонтекстОбработки.АсинхронныеОперации;
	
	Если Не ЗначениеЗаполнено(АсинхронныеОперации) Тогда
		
		ОбработатьРезультатВыполненияДействийЭДОПослеОжиданияДействийЭДО(Новый Соответствие, КонтекстОбработки);
		Возврат;
		
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатВыполненияДействийЭДОПослеОжиданияДействийЭДО",
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиент, КонтекстОбработки);
	
	ОжидатьВыполненияДействийЭДО(Оповещение, АсинхронныеОперации, КонтекстОбработки.КонтекстДиагностики);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбщегоНазначения

// Параметры:
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  ВидОперации - Строка
//  ТекстОшибки - Строка
Процедура ДобавитьНеизвестнуюОшибку(КонтекстДиагностики, ВидОперации, ТекстОшибки) Экспорт
	Ошибка = ОбработкаНеисправностейБЭДКлиент.НоваяОшибка(ВидОперации,
		ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка(), ТекстОшибки, ТекстОшибки);
	ОбработкаНеисправностейБЭДКлиент.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
		ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ИнтеграцияОблачногоЭДО);
КонецПроцедуры

#КонецОбласти

#Область ОбработкаРезультатаВыполненияДействийЭДО

// Возвращаемое значение:
//  Структура:
//  * ОповещениеОЗавершении - ОписаниеОповещения
//  * АсинхронныеОперации - Массив из см. ИнтеграцияОблачногоЭДО.НоваяАсинхроннаяОперация
//  * РезультатыДействий - см. ЭлектронныеДокументыЭДОИнтеграцияОблака.НовыеРезультатыДействийПоУчетнымЗаписямОблачногоЭДО
//  * СостояниеВыполненияДействий - см. НовоеСостояниеВыполненияДействийЭДО
//  * ИсходныйНаборДействийЭДО - см. ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО
//  * ПаролиСертификатов - см. КриптографияБЭДКлиент.НовыеПаролиСертификатов
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  * РасшифрованныеМаркерыПоУчетнымЗаписям - см. НовыеРасшифрованныеМаркерыПоУчетнымЗаписям
//  * ПодписиДанныхПоУчетнымЗаписям - см. НовыеПодписиДанныхПоУчетнымЗаписям
Функция НовыйКонтекстОбработкиРезультатаВыполненияДействийЭДО()
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения);
	Контекст.Вставить("АсинхронныеОперации", Новый Массив);
	Контекст.Вставить("РезультатыДействий", Новый Соответствие);
	Контекст.Вставить("СостояниеВыполненияДействий", Новый Структура);
	Контекст.Вставить("ИсходныйНаборДействийЭДО", ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО());
	Контекст.Вставить("ПаролиСертификатов", КриптографияБЭДКлиент.НовыеПаролиСертификатов());
	Контекст.Вставить("КонтекстДиагностики", ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики());
	Контекст.Вставить("РасшифрованныеМаркерыПоУчетнымЗаписям", НовыеРасшифрованныеМаркерыПоУчетнымЗаписям());
	Контекст.Вставить("ПодписиДанныхПоУчетнымЗаписям", НовыеПодписиДанныхПоУчетнымЗаписям());
	Возврат Контекст;
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ВыполненныеДействияПоУчетнымЗаписям - см. НовыеВыполненныеДействияПоУчетнымЗаписям
Функция НовоеСостояниеВыполненияДействийЭДО()
	Состояние = Новый Структура;
	Состояние.Вставить("ВыполненныеДействияПоУчетнымЗаписям", НовыеВыполненныеДействияПоУчетнымЗаписям());
	Возврат Состояние;
КонецФункции

// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка - идентификатор учетной записи облачного ЭДО.
//  * Значение - Массив из ПеречислениеСсылка.ДействияПоЭДО
Функция НовыеВыполненныеДействияПоУчетнымЗаписям()
	Возврат Новый Соответствие;
КонецФункции

// Параметры:
//  РезультатыДействий - см. ЭлектронныеДокументыЭДОИнтеграцияОблака.НовыеРезультатыДействийПоУчетнымЗаписямОблачногоЭДО
//  КонтекстОбработки - см. НовыйКонтекстОбработкиРезультатаВыполненияДействийЭДО
Процедура ОбработатьРезультатВыполненияДействийЭДОПослеОжиданияДействийЭДО(РезультатыДействий, КонтекстОбработки) Экспорт
	
	Если ЗначениеЗаполнено(РезультатыДействий) Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(КонтекстОбработки.РезультатыДействий, РезультатыДействий);
	КонецЕсли;
	
	ЗашифрованныеМаркерыПоУчетнымЗаписям = НовыеЗашифрованныеМаркерыПоУчетнымЗаписям();
	
	Для Каждого РезультатДействийПоУчетнойЗаписи Из КонтекстОбработки.РезультатыДействий Цикл
		
		УчетнаяЗаписьОблачногоЭДО = РезультатДействийПоУчетнойЗаписи.Ключ;
		РезультатДействий = РезультатДействийПоУчетнойЗаписи.Значение;
		ЗашифрованныеМаркеры = РезультатДействий.ЗашифрованныеМаркеры;
		
		Если ЗначениеЗаполнено(ЗашифрованныеМаркеры) Тогда
			ЗашифрованныеМаркерыПоУчетнымЗаписям.Вставить(УчетнаяЗаписьОблачногоЭДО, ЗашифрованныеМаркеры);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ЗашифрованныеМаркерыПоУчетнымЗаписям) Тогда
		
		ОбработатьРезультатВыполненияДействийЭДОПослеРасшифровки(ЗашифрованныеМаркерыПоУчетнымЗаписям, КонтекстОбработки);
		Возврат;
		
	КонецЕсли;
		
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатВыполненияДействийЭДОПослеРасшифровки",
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиент, КонтекстОбработки);
	
	РасшифроватьМаркерыПоУчетнымЗаписям(Оповещение, ЗашифрованныеМаркерыПоУчетнымЗаписям,
		КонтекстОбработки.ПаролиСертификатов, КонтекстОбработки.КонтекстДиагностики);
	
КонецПроцедуры

// Параметры:
//  РасшифрованныеМаркерыПоУчетнымЗаписям - см. НовыеРасшифрованныеМаркерыПоУчетнымЗаписям
//  КонтекстОбработки - см. НовыйКонтекстОбработкиРезультатаВыполненияДействийЭДО
Процедура ОбработатьРезультатВыполненияДействийЭДОПослеРасшифровки(РасшифрованныеМаркерыПоУчетнымЗаписям, КонтекстОбработки) Экспорт
	
	Если ЗначениеЗаполнено(РасшифрованныеМаркерыПоУчетнымЗаписям) Тогда
		КонтекстОбработки.РасшифрованныеМаркерыПоУчетнымЗаписям = РасшифрованныеМаркерыПоУчетнымЗаписям;
	КонецЕсли;
	
	ДанныеДляВыбораСертификатов = НовыеДанныеДляПодписанияПоУчетнымЗаписям();
	
	Для Каждого РезультатДействийПоУчетнойЗаписи Из КонтекстОбработки.РезультатыДействий Цикл
		
		УчетнаяЗаписьОблачногоЭДО = РезультатДействийПоУчетнойЗаписи.Ключ;
		РезультатДействий = РезультатДействийПоУчетнойЗаписи.Значение;
		
		ДанныеДляПодписания = РезультатДействий.ДанныеДляПодписания;
		Если Не ЗначениеЗаполнено(ДанныеДляПодписания) Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ОписаниеНабораДанных Из ДанныеДляПодписания Цикл
			ОписаниеНабораДанныхПоУчетнойЗаписи = НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи();
			ЗаполнитьЗначенияСвойств(ОписаниеНабораДанныхПоУчетнойЗаписи, ОписаниеНабораДанных);
			ОписаниеНабораДанныхПоУчетнойЗаписи.УчетнаяЗаписьОблачногоЭДО = УчетнаяЗаписьОблачногоЭДО;
			ДанныеДляВыбораСертификатов.Добавить(ОписаниеНабораДанныхПоУчетнойЗаписи);
		КонецЦикла;
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ДанныеДляВыбораСертификатов) Тогда
		
		ЗавершитьОбработкуРезультатаВыполненияДействийЭДО(КонтекстОбработки);
		Возврат;
		
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатВыполненияДействийЭДОПослеВыбораСертификатов",
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиент, КонтекстОбработки);
	
	ВыбратьСертификатыДляПодписанияПоУчетнымЗаписям(Оповещение, ДанныеДляВыбораСертификатов,
		КонтекстОбработки.ПаролиСертификатов, КонтекстОбработки.КонтекстДиагностики);
	
КонецПроцедуры

// Параметры:
//  ДанныеДляЗаполненияПоУчетнымЗаписям - Массив из см. НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи
//  КонтекстОбработки - см. НовыйКонтекстОбработкиРезультатаВыполненияДействийЭДО
Процедура ОбработатьРезультатВыполненияДействийЭДОПослеВыбораСертификатов(ДанныеДляЗаполненияПоУчетнымЗаписям, КонтекстОбработки) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеДляЗаполненияПоУчетнымЗаписям) Тогда
		
		ЗавершитьОбработкуРезультатаВыполненияДействийЭДО(КонтекстОбработки);
		Возврат;
		
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатВыполненияДействийЭДОПослеЗаполненияПоСертификатам",
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиент, КонтекстОбработки);
	
	ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатам(Оповещение, ДанныеДляЗаполненияПоУчетнымЗаписям,
		КонтекстОбработки.КонтекстДиагностики);
	
КонецПроцедуры

// Параметры:
//  ДанныеДляПодписанияПоУчетнымЗаписям - Массив из см. НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи
//  КонтекстОбработки - см. НовыйКонтекстОбработкиРезультатаВыполненияДействийЭДО
Процедура ОбработатьРезультатВыполненияДействийЭДОПослеЗаполненияПоСертификатам(ДанныеДляПодписанияПоУчетнымЗаписям, КонтекстОбработки) Экспорт
	
	Если Не ЗначениеЗаполнено(ДанныеДляПодписанияПоУчетнымЗаписям) Тогда
		
		ЗавершитьОбработкуРезультатаВыполненияДействийЭДО(КонтекстОбработки);
		Возврат;
		
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатВыполненияДействийЭДОПослеСозданияПодписей",
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиент, КонтекстОбработки);
	
	СоздатьПодписиПоВыбраннымСертификатам(Оповещение, ДанныеДляПодписанияПоУчетнымЗаписям,
		КонтекстОбработки.ПаролиСертификатов, КонтекстОбработки.КонтекстДиагностики);
	
КонецПроцедуры

// Параметры:
//  ПодписиДанныхПоУчетнымЗаписям - см. НовыеПодписиДанныхПоУчетнымЗаписям
//  КонтекстОбработки - см. НовыйКонтекстОбработкиРезультатаВыполненияДействийЭДО
Процедура ОбработатьРезультатВыполненияДействийЭДОПослеСозданияПодписей(ПодписиДанныхПоУчетнымЗаписям, КонтекстОбработки) Экспорт
	
	Если ЗначениеЗаполнено(ПодписиДанныхПоУчетнымЗаписям) Тогда
		КонтекстОбработки.ПодписиДанныхПоУчетнымЗаписям = ПодписиДанныхПоУчетнымЗаписям;
	КонецЕсли;
	
	ЗавершитьОбработкуРезультатаВыполненияДействийЭДО(КонтекстОбработки);
	
КонецПроцедуры

// Параметры:
//  КонтекстОбработки - см. НовыйКонтекстОбработкиРезультатаВыполненияДействийЭДО
Процедура ЗавершитьОбработкуРезультатаВыполненияДействийЭДО(КонтекстОбработки)
	
	РезультатОбработки = РезультатОбработкиДействийИнтеграцииЭДО(КонтекстОбработки);
	
	// См. ОбработатьРезультатВыполненияДействийЭДО.ОповещениеОЗавершении
	ВыполнитьОбработкуОповещения(КонтекстОбработки.ОповещениеОЗавершении, РезультатОбработки);
	
КонецПроцедуры

#Область ОжиданиеВыполненияДействийЭДО

// Возвращаемое значение:
//  Структура:
//  * ОповещениеОЗавершении - см. РасшифроватьМаркерыПоУчетнымЗаписям.ОповещениеОЗавершении
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
Функция НовыйКонтекстОжиданияВыполненияДействийПоЭДО()
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения);
	Контекст.Вставить("КонтекстДиагностики", Новый Структура);
	Возврат Контекст;
КонецФункции

// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения - результат: см. ЭлектронныеДокументыЭДОИнтеграцияОблакаВызовСервера.РезультатыДействийЭДОАсинхронно
//  АсинхронныеОперации - Массив из см. ИнтеграцияОблачногоЭДО.НоваяАсинхроннаяОперация
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
Процедура ОжидатьВыполненияДействийЭДО(ОповещениеОЗавершении, АсинхронныеОперации, КонтекстДиагностики)
	
	КонтекстОжидания = НовыйКонтекстОжиданияВыполненияДействийПоЭДО();
	КонтекстОжидания.ОповещениеОЗавершении = ОповещениеОЗавершении;
	КонтекстОжидания.КонтекстДиагностики = КонтекстДиагностики;
	
	Оповещение = Новый ОписаниеОповещения("ОжидатьВыполненияДействийЭДОПослеАсинхронныхОпераций",
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиент, КонтекстОжидания);
	ИнтеграцияОблачногоЭДОКлиент.ОжидатьЗавершенияАсинхронныхОпераций(Оповещение, АсинхронныеОперации);
	
КонецПроцедуры

// Параметры:
//  АсинхронныеОперации - Массив из см. ИнтеграцияОблачногоЭДО.НоваяАсинхроннаяОперация
//  КонтекстОжидания - см. НовыйКонтекстОжиданияВыполненияДействийПоЭДО
Процедура ОжидатьВыполненияДействийЭДОПослеАсинхронныхОпераций(АсинхронныеОперации, КонтекстОжидания) Экспорт
	АдресаРезультатов = ИнтеграцияОблачногоЭДОКлиент.АдресаРезультатовАсинхронныхОпераций(
		АсинхронныеОперации, КонтекстОжидания.КонтекстДиагностики);
	
	РезультатыДействийЭДО = ЭлектронныеДокументыЭДОИнтеграцияОблакаВызовСервера.РезультатыДействийЭДОАсинхронно(
		АдресаРезультатов);
	
	// См. ОжидатьВыполненияДействийЭДО.ОповещениеОЗавершении
	ВыполнитьОбработкуОповещения(КонтекстОжидания.ОповещениеОЗавершении, РезультатыДействийЭДО);
	
КонецПроцедуры

#КонецОбласти

#Область РасшифроватьМаркерыПоУчетнымЗаписям

// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - СправочникСсылка.УчетныеЗаписиОблачногоЭДО
//  * Значение - см. КриптографияБЭДКлиентСервер.НовыйНаборДанныхДляРасшифровки
Функция НовыеЗашифрованныеМаркерыПоУчетнымЗаписям()
	Возврат Новый Соответствие;
КонецФункции

// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - СправочникСсылка.УчетныеЗаписиОблачногоЭДО
//  * Значение - см. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
Функция НовыеРасшифрованныеМаркерыПоУчетнымЗаписям()
	Возврат Новый Соответствие;
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ОповещениеОЗавершении - см. РасшифроватьМаркерыПоУчетнымЗаписям.ОповещениеОЗавершении
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  * ПаролиСертификатов - см. КриптографияБЭДКлиент.НовыеПаролиСертификатов
//  * ЗашифрованныеМаркерыПоУчетнымЗаписям - см. НовыеЗашифрованныеМаркерыПоУчетнымЗаписям
//  * ОбработанныеУчетныеЗаписи - Массив из СправочникСсылка.УчетныеЗаписиОблачногоЭДО
//  * УчетнаяЗаписьОблачногоЭДО - СправочникСсылка.УчетныеЗаписиОблачногоЭДО
//  * ЗашифрованныеМаркеры - см. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
//  * РасшифрованныеМаркерыПоУчетнымЗаписям - см. НовыеРасшифрованныеМаркерыПоУчетнымЗаписям
Функция НовыйКонтекстРасшифровкиМаркеровПоУчетнымЗаписям()
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения);
	Контекст.Вставить("КонтекстДиагностики", Новый Структура);
	Контекст.Вставить("ПаролиСертификатов", КриптографияБЭДКлиент.НовыеПаролиСертификатов());
	Контекст.Вставить("ЗашифрованныеМаркерыПоУчетнымЗаписям", НовыеЗашифрованныеМаркерыПоУчетнымЗаписям());
	Контекст.Вставить("ОбработанныеУчетныеЗаписи", Новый Массив);
	Контекст.Вставить("УчетнаяЗаписьОблачногоЭДО", ПредопределенноеЗначение("Справочник.УчетныеЗаписиОблачногоЭДО.ПустаяСсылка"));
	Контекст.Вставить("ЗашифрованныеМаркеры", КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных());
	Контекст.Вставить("РасшифрованныеМаркерыПоУчетнымЗаписям", НовыеРасшифрованныеМаркерыПоУчетнымЗаписям());
	Возврат Контекст;
КонецФункции

// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения - результат см. НовыеРасшифрованныеМаркерыПоУчетнымЗаписям
//  ЗашифрованныеМаркерыПоУчетнымЗаписям - см. НовыеЗашифрованныеМаркерыПоУчетнымЗаписям
//  ПаролиСертификатов - см. КриптографияБЭДКлиент.НовыеПаролиСертификатов
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
Процедура РасшифроватьМаркерыПоУчетнымЗаписям(ОповещениеОЗавершении, ЗашифрованныеМаркерыПоУчетнымЗаписям, ПаролиСертификатов, КонтекстДиагностики)
	
	КонтекстРасшифровки = НовыйКонтекстРасшифровкиМаркеровПоУчетнымЗаписям();
	КонтекстРасшифровки.ОповещениеОЗавершении = ОповещениеОЗавершении;
	КонтекстРасшифровки.ЗашифрованныеМаркерыПоУчетнымЗаписям = ЗашифрованныеМаркерыПоУчетнымЗаписям;
	КонтекстРасшифровки.ПаролиСертификатов = ПаролиСертификатов;
	КонтекстРасшифровки.КонтекстДиагностики = КонтекстДиагностики;
	
	РасшифроватьМаркерыПоУчетнымЗаписямПослеПодготовкиКонтекста(КонтекстРасшифровки);
	
КонецПроцедуры

// Параметры:
//  КонтекстРасшифровки - см. НовыйКонтекстРасшифровкиМаркеровПоУчетнымЗаписям
Процедура РасшифроватьМаркерыПоУчетнымЗаписямПослеПодготовкиКонтекста(КонтекстРасшифровки)
	
	Установлен = УстановитьЗашифрованныйМаркерПоУчетнойЗаписиДляРасшифровки(КонтекстРасшифровки);
	
	Если Не Установлен Тогда
		РасшифроватьМаркерыПоУчетнымЗаписямЗавершение(КонтекстРасшифровки);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("РасшифроватьМаркерыПоУчетнымЗаписямПослеОбработкиДанных",
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиент, КонтекстРасшифровки);
		
	ПараметрыРасшифровки = КриптографияБЭДКлиент.НовыеПараметрыРасшифровкиНабораДанных();
	ПараметрыРасшифровки.ПаролиСертификатов = КонтекстРасшифровки.ПаролиСертификатов;
	ПараметрыРасшифровки.КонтекстДиагностики = КонтекстРасшифровки.КонтекстДиагностики;
	
	КриптографияБЭДКлиент.НачатьРасшифровкуНабораДанных(Оповещение,
		КонтекстРасшифровки.ЗашифрованныеМаркеры, ПараметрыРасшифровки);
	
КонецПроцедуры

// Параметры:
//  ИтогРасшифровки - см. КриптографияБЭДКлиент.НовыйИтогРасшифровкиНабораДанных
//  КонтекстРасшифровки - см. НовыйКонтекстРасшифровкиМаркеровПоУчетнымЗаписям
Процедура РасшифроватьМаркерыПоУчетнымЗаписямПослеОбработкиДанных(ИтогРасшифровки, КонтекстРасшифровки) Экспорт
	
	КонтекстРасшифровки.РасшифрованныеМаркерыПоУчетнымЗаписям.Вставить(
		КонтекстРасшифровки.УчетнаяЗаписьОблачногоЭДО, ИтогРасшифровки.НаборРасшифрованныхДанных);
	
	РасшифроватьМаркерыПоУчетнымЗаписямПослеПодготовкиКонтекста(КонтекстРасшифровки);
	
КонецПроцедуры

// Параметры:
//  КонтекстРасшифровки - см. НовыйКонтекстРасшифровкиМаркеровПоУчетнымЗаписям
Процедура РасшифроватьМаркерыПоУчетнымЗаписямЗавершение(КонтекстРасшифровки)
	
	// См. РасшифроватьМаркерыПоУчетнымЗаписям.ОповещениеОЗавершении
	ВыполнитьОбработкуОповещения(КонтекстРасшифровки.ОповещениеОЗавершении,
		КонтекстРасшифровки.РасшифрованныеМаркерыПоУчетнымЗаписям);
	
КонецПроцедуры

// Параметры:
//  КонтекстРасшифровки - см. НовыйКонтекстРасшифровкиМаркеровПоУчетнымЗаписям
// 
// Возвращаемое значение:
//  Булево
Функция УстановитьЗашифрованныйМаркерПоУчетнойЗаписиДляРасшифровки(КонтекстРасшифровки)
	
	Установлен = Ложь;
	
	ОбработанныеУчетныеЗаписи = КонтекстРасшифровки.ОбработанныеУчетныеЗаписи;
	
	Для Каждого ЗашифрованныйМаркерПоУчетннойЗаписи Из КонтекстРасшифровки.ЗашифрованныеМаркерыПоУчетнымЗаписям Цикл
		УчетнаяЗаписьОблачногоЭДО = ЗашифрованныйМаркерПоУчетннойЗаписи.Ключ;
		Если ОбработанныеУчетныеЗаписи.Найти(УчетнаяЗаписьОблачногоЭДО) = Неопределено Тогда
			КонтекстРасшифровки.УчетнаяЗаписьОблачногоЭДО = УчетнаяЗаписьОблачногоЭДО;
			КонтекстРасшифровки.ЗашифрованныеМаркеры = ЗашифрованныйМаркерПоУчетннойЗаписи.Значение;
			ОбработанныеУчетныеЗаписи.Добавить(УчетнаяЗаписьОблачногоЭДО);
			Установлен = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Установлен;
	
КонецФункции

#КонецОбласти

#Область ВыборСертификатовДляПодписанияПоУчетнымЗаписям

// Возвращаемое значение:
//  Структура:
//  * ОповещениеОЗавершении - см. ВыбратьСертификатыДляПодписанияПоУчетнымЗаписям.ОповещениеОЗавершении
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  * ПаролиСертификатов - см. КриптографияБЭДКлиент.НовыеПаролиСертификатов
//  * ИндексНабораДанных - Число
//  * ДанныеДляВыбораСертификатов - см. НовыеДанныеДляПодписанияПоУчетнымЗаписям
//  * ДанныеДляЗаполненияПоСертификатам - см. НовыеДанныеДляПодписанияПоУчетнымЗаписям
Функция НовыйКонтекстВыбораСертификатовДляПодписанияПоУчетнымЗаписям()
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения);
	Контекст.Вставить("КонтекстДиагностики", Новый Структура);
	Контекст.Вставить("ПаролиСертификатов", КриптографияБЭДКлиент.НовыеПаролиСертификатов());
	Контекст.Вставить("ИндексНабораДанных", 0);
	Контекст.Вставить("ДанныеДляВыбораСертификатов", Новый Массив);
	Контекст.Вставить("ДанныеДляЗаполненияПоСертификатам", Новый Массив);
	Возврат Контекст;
КонецФункции

// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения - результат см. НовыеДанныеДляПодписанияВыбраннымиСертификатамиПоУчетнымЗаписям
//  ДанныеДляВыбораСертификатов - см. НовыеДанныеДляПодписанияПоУчетнымЗаписям
//  ПаролиСертификатов - см. КриптографияБЭДКлиент.НовыеПаролиСертификатов
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
Процедура ВыбратьСертификатыДляПодписанияПоУчетнымЗаписям(ОповещениеОЗавершении, ДанныеДляВыбораСертификатов, ПаролиСертификатов, КонтекстДиагностики)
	
	КонтекстВыбораСертификатов = НовыйКонтекстВыбораСертификатовДляПодписанияПоУчетнымЗаписям();
	КонтекстВыбораСертификатов.ОповещениеОЗавершении = ОповещениеОЗавершении;
	КонтекстВыбораСертификатов.ПаролиСертификатов = ПаролиСертификатов;
	КонтекстВыбораСертификатов.КонтекстДиагностики = КонтекстДиагностики;
	КонтекстВыбораСертификатов.ДанныеДляВыбораСертификатов = ДанныеДляВыбораСертификатов;
	
	ВыбратьСертификатыДляПодписанияПоУчетнымЗаписямПослеПодготовкиКонтекста(КонтекстВыбораСертификатов);
	
КонецПроцедуры

// Параметры:
//  КонтекстВыбораСертификатов - см. НовыйКонтекстВыбораСертификатовДляПодписанияПоУчетнымЗаписям
Процедура ВыбратьСертификатыДляПодписанияПоУчетнымЗаписямПослеПодготовкиКонтекста(КонтекстВыбораСертификатов)
	
	ОписаниеНабораДанныхДляВыбора = ОписаниеНабораДанныхДляВыбораСертификатаПоКонтексту(КонтекстВыбораСертификатов);
	
	Если ОписаниеНабораДанныхДляВыбора = Неопределено Тогда
		ВыбратьСертификатыДляПодписанияПоУчетнымЗаписямЗавершение(КонтекстВыбораСертификатов);
		Возврат;
	КонецЕсли;
	
	ОписаниеДанных = ОписаниеДанныхДляПодписанияЧерезБСП(ОписаниеНабораДанныхДляВыбора);
	
	ДанныеДляВыбораСертификата = Новый Структура("Данные", ПолучитьДвоичныеДанныеИзСтроки(""));
	
	ОписаниеДанных.НаборДанных = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеДляВыбораСертификата);
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьСертификатыДляПодписанияПоУчетнымЗаписямПослеВыполнения",
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиент, КонтекстВыбораСертификатов);
		
	ТипПредметаПодписания = НастройкиЭДОКлиентСервер.ТипПредметаПодписанияЭД();
	ТипПодписи = НастройкиЭДОКлиентПовтИсп.ТипПодписи(ТипПредметаПодписания);
		
	КриптографияБЭДКлиент.Подписать(ОписаниеДанных, КонтекстВыбораСертификатов.КонтекстДиагностики,,
		Оповещение, КонтекстВыбораСертификатов.ПаролиСертификатов, ТипПодписи);
	
КонецПроцедуры

// Параметры:
//  Результат               - Структура:
//  * ЗаголовокДанных       - Строка
//  * СообщитьОЗавершении   - Булево
//  * ПоказатьКомментарий   - Булево
//  * КонтекстОперации      - Неопределено,Произвольный
//  * ПрекратитьВыполнение  - Булево
//  * БезПодтверждения      - Булево
//  * ОтборСертификатов     - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  * НаборДанных           - Массив из Структура:
//    ** ПрисоединенныйФайл - СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
//    ** Данные             - ДвоичныеДанные
//    ** Представление      - Структура:
//       *** Значение       - ДокументСсылка.СообщениеЭДО
//       *** Представление  - Строка
//    ** ТребуетсяЗаполнитьПодписанта - Булево
//    ** ДоверенностиПоСертификатам - Соответствие из КлючИЗначение:
//       *** Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//       *** Значение - Строка - номер доверенности.
//    ** СертификатыТребующиеДоверенность - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//    ** ДанныеИзменены     - Булево
//    ** АдресДанныхДляОбновления - Строка
//    ** СвойстваПодписи    - Строка
//                          - См. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
//  * Операция              - Строка
//  * Успех                 - Булево
//  * Отказ                 - Булево
//  * ВыбранныйСертификат   - Структура:
//    ** Ссылка             - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//    ** Отпечаток          - Строка - отпечаток сертификата в формате строки Base64.
//    ** Данные             - Строка - адрес временного хранилища, содержащего двоичные данные сертификата.
//  * ПаролиСертификатов    - См. КриптографияБЭДКлиент.НовыеПаролиСертификатов
//  * ТекущийЭлементНабораДанных - Структура
//  * ПользовательНажалКнопкуПодписать - Булево
//  КонтекстВыбораСертификатов - См. НовыйКонтекстВыбораСертификатовДляПодписанияПоУчетнымЗаписям
Процедура ВыбратьСертификатыДляПодписанияПоУчетнымЗаписямПослеВыполнения(Результат, КонтекстВыбораСертификатов) Экспорт
	
	Если Результат = Неопределено
		ИЛИ Не Результат.Успех Тогда
		ВыбратьСледующийСертификатДляПодписанияПоУчетнымЗаписям(КонтекстВыбораСертификатов);
		Возврат;
	КонецЕсли;
	
	Сертификат = Результат.ВыбранныйСертификат.Ссылка;
	
	ДанныеДляВыбораСертификатов = КонтекстВыбораСертификатов.ДанныеДляВыбораСертификатов;
	ИндексНабораДанных = КонтекстВыбораСертификатов.ИндексНабораДанных;
	
	ОписаниеНабораДанныхДляВыбора = ДанныеДляВыбораСертификатов[ИндексНабораДанных];
	
	ОтсутствуетДоверенность = Ложь;
	
	Для Каждого ОписаниеДанных Из ОписаниеНабораДанныхДляВыбора.НаборДанных Цикл
		
		Если ОписаниеДанных.СертификатыТребующиеДоверенность.Найти(Сертификат) <> Неопределено
			И ОписаниеДанных.ДоверенностиПоСертификатам[Сертификат] = Неопределено Тогда
			ОтсутствуетДоверенность = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОтсутствуетДоверенность Тогда
		ВыбратьДругойСертификатПоУчетнымЗаписямПередПодписанием(Сертификат, КонтекстВыбораСертификатов);
		Возврат;
	КонецЕсли;
	
	ОписаниеНабораДанныхДляЗаполнения = НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи();
	ЗаполнитьЗначенияСвойств(ОписаниеНабораДанныхДляЗаполнения, ОписаниеНабораДанныхДляВыбора);
	ОписаниеНабораДанныхДляЗаполнения.Сертификаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сертификат);
	
	КонтекстВыбораСертификатов.ДанныеДляЗаполненияПоСертификатам.Добавить(ОписаниеНабораДанныхДляЗаполнения);
	
	ВыбратьСледующийСертификатДляПодписанияПоУчетнымЗаписям(КонтекстВыбораСертификатов);
	
КонецПроцедуры

// Параметры:
//  КонтекстВыбораСертификатов - см. НовыйКонтекстВыбораСертификатовДляПодписанияПоУчетнымЗаписям
Процедура ВыбратьСертификатыДляПодписанияПоУчетнымЗаписямЗавершение(КонтекстВыбораСертификатов)
	
	// См. ВыбратьСертификатыДляПодписанияПоУчетнымЗаписям.ОповещениеОЗавершении
	ВыполнитьОбработкуОповещения(КонтекстВыбораСертификатов.ОповещениеОЗавершении,
		КонтекстВыбораСертификатов.ДанныеДляЗаполненияПоСертификатам);
	
КонецПроцедуры

// Параметры:
//  КонтекстВыбораСертификатов - см. НовыйКонтекстВыбораСертификатовДляПодписанияПоУчетнымЗаписям
Процедура ВыбратьСледующийСертификатДляПодписанияПоУчетнымЗаписям(КонтекстВыбораСертификатов)
	
	КонтекстВыбораСертификатов.ИндексНабораДанных = КонтекстВыбораСертификатов.ИндексНабораДанных + 1;
	
	ВыбратьСертификатыДляПодписанияПоУчетнымЗаписямПослеПодготовкиКонтекста(КонтекстВыбораСертификатов);
	
КонецПроцедуры

// Параметры:
//  ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  КонтекстВыбораСертификатов - см. НовыйКонтекстВыбораСертификатовДляПодписанияПоУчетнымЗаписям
Процедура ВыбратьДругойСертификатПоУчетнымЗаписямПередПодписанием(ВыбранныйСертификат, КонтекстВыбораСертификатов)
	
	КонтекстВыбораСертификатов.ПаролиСертификатов.Удалить(ВыбранныйСертификат);
	
	ТекстВопроса = НСтр("ru = 'Сертификатом <a href = ""%1"">%2</a> можно подписать только по доверенности. 
		|Для выбранного сертификата в программе нет подходящей машиночитаемой доверенности. 
		|Повторите подписание, выбрав другой сертификат или передайте документ на подпись руководителю.';
		|en = 'You can sign with the <a href = ""%1"">%2</a> certificate only based on the authorization letter. 
		|No machine-readable letter of authority is available for the selected certificate. 
		|Select another certificate and try again or submit the document to the manager to sign.'");
		
	ТекстВопроса = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ТекстВопроса,
		ПолучитьНавигационнуюСсылку(ВыбранныйСертификат), ВыбранныйСертификат);
	Оповещение = Новый ОписаниеОповещения("ВыбратьДругойСертификатПоУчетнымЗаписямПередПодписаниемПослеВопроса",
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиент, КонтекстВыбораСертификатов);
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ПовторитьОтмена);
	
КонецПроцедуры

// Параметры:
//  Ответ - КодВозвратаДиалога
//  КонтекстВыбораСертификатов - см. НовыйКонтекстВыбораСертификатовДляПодписанияПоУчетнымЗаписям
Процедура ВыбратьДругойСертификатПоУчетнымЗаписямПередПодписаниемПослеВопроса(Ответ, КонтекстВыбораСертификатов) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Повторить Тогда
		ВыбратьСертификатыДляПодписанияПоУчетнымЗаписямПослеПодготовкиКонтекста(КонтекстВыбораСертификатов);
	Иначе
		ВыбратьСледующийСертификатДляПодписанияПоУчетнымЗаписям(КонтекстВыбораСертификатов);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  КонтекстВыбораСертификатов - см. НовыйКонтекстВыбораСертификатовДляПодписанияПоУчетнымЗаписям
//  
// Возвращаемое значение:
//  - Неопределено
//  - См. НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи
Функция ОписаниеНабораДанныхДляВыбораСертификатаПоКонтексту(КонтекстВыбораСертификатов)
	
	ОписаниеНабораДанныхДляВыбора = Неопределено;
	
	ДанныеДляВыбораСертификатов = КонтекстВыбораСертификатов.ДанныеДляВыбораСертификатов;
	ИндексНабораДанных = КонтекстВыбораСертификатов.ИндексНабораДанных;
	
	ПаролиСертификатов = КонтекстВыбораСертификатов.ПаролиСертификатов;
	ДанныеДляЗаполненияПоСертификатам = КонтекстВыбораСертификатов.ДанныеДляЗаполненияПоСертификатам;
	КоличествоНаборовДанныхДляВыбораСертификатов = ДанныеДляВыбораСертификатов.Количество();
	
	Пока ИндексНабораДанных < КоличествоНаборовДанныхДляВыбораСертификатов Цикл
		
		ОписаниеНабораДанных = ДанныеДляВыбораСертификатов[ИндексНабораДанных];
		
		СертификатыСПаролем = КриптографияБЭДКлиент.СертификатыСПаролемИзНабора(
			ПаролиСертификатов, ОписаниеНабораДанных.Сертификаты);
		
		Если Не ЗначениеЗаполнено(СертификатыСПаролем) Тогда
			
			ОписаниеНабораДанныхДляВыбора = ОписаниеНабораДанных;
			Прервать;
		
		ИначеЕсли ОписаниеНабораДанных.Сертификаты.Количество() = 1 Тогда
			
			ДанныеДляЗаполненияПоСертификатам.Добавить(ОписаниеНабораДанных);
			
		Иначе
			
			ОписаниеНабораДанныхДляЗаполнения = НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи();
			ЗаполнитьЗначенияСвойств(ОписаниеНабораДанныхДляЗаполнения, ОписаниеНабораДанных);
			ОписаниеНабораДанныхДляЗаполнения.Сертификаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
				СертификатыСПаролем[0]);
			ДанныеДляЗаполненияПоСертификатам.Добавить(ОписаниеНабораДанныхДляЗаполнения);
			
		КонецЕсли;
		
		ИндексНабораДанных = ИндексНабораДанных + 1;
		
	КонецЦикла;
	
	КонтекстВыбораСертификатов.ИндексНабораДанных = ИндексНабораДанных;
	
	Возврат ОписаниеНабораДанныхДляВыбора;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеДанныхДляПодписанияПоВыбраннымСертификатам

// Возвращаемое значение:
//  Структура:
//  * ОповещениеОЗавершении - см. ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатам.ОповещениеОЗавершении
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  * ДанныеДляЗаполненияПоУчетнымЗаписям - Массив из см. НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи
//  * ЗаполненныеДанныеПоУчетнымЗаписям - Соответствие из КлючИЗначение
Функция НовыйКонтекстЗаполненияДанныхДляПодписанияПоВыбраннымСертификатам()
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения);
	Контекст.Вставить("КонтекстДиагностики", Новый Структура);
	Контекст.Вставить("ДанныеДляЗаполненияПоУчетнымЗаписям", Новый Массив);
	Контекст.Вставить("ЗаполненныеДанныеПоУчетнымЗаписям", Новый Соответствие);
	Возврат Контекст;
КонецФункции

// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения - результат: см. НовыеДанныеДляПодписанияВыбраннымиСертификатамиПоУчетнымЗаписям
//  ДанныеДляЗаполненияПоУчетнымЗаписям - Массив из см. НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
Процедура ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатам(ОповещениеОЗавершении, ДанныеДляЗаполненияПоУчетнымЗаписям, КонтекстДиагностики)
	
	КонтекстЗаполнения = НовыйКонтекстЗаполненияДанныхДляПодписанияПоВыбраннымСертификатам();
	КонтекстЗаполнения.ОповещениеОЗавершении = ОповещениеОЗавершении;
	КонтекстЗаполнения.ДанныеДляЗаполненияПоУчетнымЗаписям = ДанныеДляЗаполненияПоУчетнымЗаписям;
	КонтекстЗаполнения.КонтекстДиагностики = КонтекстДиагностики;
	
	ДанныеДляЗаполненияНаСервере = ДанныеДляЗаполненияПоВыбраннымСертификатамНаСервере(
		ДанныеДляЗаполненияПоУчетнымЗаписям);
	
	Если Не ЗначениеЗаполнено(ДанныеДляЗаполненияНаСервере) Тогда
		ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатамЗавершение(КонтекстЗаполнения);
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ЭлектронныеДокументыЭДОИнтеграцияОблакаВызовСервера.ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатамВФоне(
		ДанныеДляЗаполненияНаСервере);
	
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатамПослеДлительнойОперации",
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиент, КонтекстЗаполнения);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Ложь;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

// Параметры:
//  ДлительнаяОперация - см. ДлительныеОперации.ВыполнитьФункцию
//  КонтекстЗаполнения - см. НовыйКонтекстЗаполненияДанныхДляПодписанияПоВыбраннымСертификатам
Процедура ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатамПослеДлительнойОперации(ДлительнаяОперация, КонтекстЗаполнения) Экспорт
	
	АдресРезультата = АдресРезультатаДлительнойОперации(ДлительнаяОперация, КонтекстЗаполнения.КонтекстДиагностики);
	Если ПустаяСтрока(АдресРезультата) Тогда
		ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатамЗавершение(КонтекстЗаполнения);
		Возврат;
	КонецЕсли;
	
	РезультатЗаполнения = 
		ЭлектронныеДокументыЭДОИнтеграцияОблакаВызовСервера.РезультатЗаполненияДанныхДляПодписанияПоВыбраннымСертификатамВФоне(
			АдресРезультата);
	
	ДополнитьКонтекстЗаполненияПоВыбраннымСертификатам(КонтекстЗаполнения, РезультатЗаполнения);
	
	Если Не ЗначениеЗаполнено(РезультатЗаполнения.АсинхронныеОперации) Тогда
		ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатамЗавершение(КонтекстЗаполнения);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатамПослеАсинхронныхОпераций",
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиент, КонтекстЗаполнения);
	
	ИнтеграцияОблачногоЭДОКлиент.ОжидатьЗавершенияАсинхронныхОпераций(Оповещение, РезультатЗаполнения.АсинхронныеОперации);
	
КонецПроцедуры

// Параметры:
//  АсинхронныеОперации - Массив из см. ИнтеграцияОблачногоЭДО.НоваяАсинхроннаяОперация
//  КонтекстЗаполнения - см. НовыйКонтекстЗаполненияДанныхДляПодписанияПоВыбраннымСертификатам
Процедура ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатамПослеАсинхронныхОпераций(АсинхронныеОперации, КонтекстЗаполнения) Экспорт
	
	АдресаРезультатов = ИнтеграцияОблачногоЭДОКлиент.АдресаРезультатовАсинхронныхОпераций(
		АсинхронныеОперации, КонтекстЗаполнения.КонтекстДиагностики);
	
	РезультатЗаполнения = 
		ЭлектронныеДокументыЭДОИнтеграцияОблакаВызовСервера.РезультатЗаполненияДанныхДляПодписанияПоВыбраннымСертификатамАсинхронно(
			АдресаРезультатов);
	
	ДополнитьКонтекстЗаполненияПоВыбраннымСертификатам(КонтекстЗаполнения, РезультатЗаполнения);
	
	ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатамЗавершение(КонтекстЗаполнения);
	
КонецПроцедуры

// Параметры:
//  КонтекстЗаполнения - см. НовыйКонтекстЗаполненияДанныхДляПодписанияПоВыбраннымСертификатам
Процедура ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатамЗавершение(КонтекстЗаполнения)
	
	ДанныеДляПодписанияПоУчетнымЗаписям = Новый Массив; // Массив из см. НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи
	ЗаполненныеДанныеПоУчетнымЗаписям = КонтекстЗаполнения.ЗаполненныеДанныеПоУчетнымЗаписям;
	
	Для Каждого ОписаниеНабораДанныхДляЗаполнения Из КонтекстЗаполнения.ДанныеДляЗаполненияПоУчетнымЗаписям Цикл
		
		УчетнаяЗаписьОблачногоЭДО = ОписаниеНабораДанныхДляЗаполнения.УчетнаяЗаписьОблачногоЭДО;
		
		ОписаниеНабораДанныхДляПодписания = НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи();
		ОписаниеНабораДанныхДляПодписания.УчетнаяЗаписьОблачногоЭДО = УчетнаяЗаписьОблачногоЭДО;
		ОписаниеНабораДанныхДляПодписания.Сертификаты = ОписаниеНабораДанныхДляЗаполнения.Сертификаты;
		
		НаборДанныхДляПодписания = ОписаниеНабораДанныхДляПодписания.НаборДанных;
		
		ЗаполненныеДанные = ЗаполненныеДанныеПоУчетнымЗаписям[УчетнаяЗаписьОблачногоЭДО]; // См. ЭлектронныеДокументыЭДОИнтеграцияОблака.ОбработатьРезультатЗаполненияДанныхДляПодписанияПоВыбраннымСертификатам
		
		Для Каждого ОписаниеДанных Из ОписаниеНабораДанныхДляЗаполнения.НаборДанных Цикл
			
			Если Не ОписаниеДанных.ТребуетсяЗаполнитьПодписанта Тогда
				НаборДанныхДляПодписания.Добавить(ОписаниеДанных);
				Продолжить;
			КонецЕсли;
			
			Если ЗаполненныеДанные = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Данные = ЗаполненныеДанные[ОписаниеДанных.ИдентификаторДанных];
			
			Если Данные = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ОписаниеДанных.Данные = Данные;
			
			НаборДанныхДляПодписания.Добавить(ОписаниеДанных);
			
		КонецЦикла;
		
		Если ЗначениеЗаполнено(НаборДанныхДляПодписания) Тогда
			ДанныеДляПодписанияПоУчетнымЗаписям.Добавить(ОписаниеНабораДанныхДляПодписания);
		КонецЕсли;
		
	КонецЦикла;
	
	ВыполнитьОбработкуОповещения(КонтекстЗаполнения.ОповещениеОЗавершении, ДанныеДляПодписанияПоУчетнымЗаписям);
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
//  * Сертификат - Неопределено,СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  * НомерДоверенности - Строка
//  * ИдентификаторыДанных - Массив из Строка
Функция ОписаниеНабораДанныхДляЗаполненияПоСертификатуНаСервере()
	Описание = Новый Структура;
	Описание.Вставить("Сертификат", Неопределено);
	Описание.Вставить("НомерДоверенности", "");
	Описание.Вставить("ИдентификаторыДанных", Новый Массив);
	Возврат Описание;
КонецФункции

// Параметры:
//  ДанныеДляЗаполненияПоУчетнымЗаписям - см. ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатам.ДанныеДляЗаполненияПоУчетнымЗаписям
// 
// Возвращаемое значение:
//  См. ЭлектронныеДокументыЭДОИнтеграцияОблака.ЗаполнитьДанныеДляПодписанияПоВыбраннымСертификатам.ДанныеДляЗаполненияПоУчетнымЗаписям
Функция ДанныеДляЗаполненияПоВыбраннымСертификатамНаСервере(ДанныеДляЗаполненияПоУчетнымЗаписям)
	
	ДанныеДляЗаполненияНаСервере = Новый Соответствие;
	
	ОписанияНаборовПоУчетнымЗаписям = Новый Соответствие;	
	
	Для Каждого ОписаниеНабораДанных Из ДанныеДляЗаполненияПоУчетнымЗаписям Цикл
		
		Если Не ЗначениеЗаполнено(ОписаниеНабораДанных.Сертификаты) Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ОписаниеДанных Из ОписаниеНабораДанных.НаборДанных Цикл
			
			Если Не ОписаниеДанных.ТребуетсяЗаполнитьПодписанта Тогда
				Продолжить;
			КонецЕсли;
			
			УчетнаяЗаписьОблачногоЭДО = ОписаниеНабораДанных.УчетнаяЗаписьОблачногоЭДО;
			
			ОписанияНаборовПоСертификатам = ОписанияНаборовПоУчетнымЗаписям[УчетнаяЗаписьОблачногоЭДО];
			Если ОписанияНаборовПоСертификатам = Неопределено Тогда
				ОписанияНаборовПоСертификатам = Новый Соответствие;
				ОписанияНаборовПоУчетнымЗаписям.Вставить(УчетнаяЗаписьОблачногоЭДО, ОписанияНаборовПоСертификатам);
			КонецЕсли;
			
			Сертификат = ОписаниеНабораДанных.Сертификаты[0];
			
			ОписанияНаборовСертификатаПоДоверенностям = ОписанияНаборовПоСертификатам[Сертификат];
			Если ОписанияНаборовСертификатаПоДоверенностям = Неопределено Тогда
				ОписанияНаборовСертификатаПоДоверенностям = Новый Соответствие;
				ОписанияНаборовПоСертификатам.Вставить(Сертификат, ОписанияНаборовСертификатаПоДоверенностям);
			КонецЕсли;
			
			НомерДоверенности = ОписаниеДанных.ДоверенностиПоСертификатам[Сертификат];
			Если НомерДоверенности = Неопределено Тогда
				НомерДоверенности = "";
			КонецЕсли;
			
			ОписаниеНабораДанныхДляЗаполненияНаСервере = ОписанияНаборовСертификатаПоДоверенностям[НомерДоверенности];
			Если ОписаниеНабораДанныхДляЗаполненияНаСервере = Неопределено Тогда
				ОписаниеНабораДанныхДляЗаполненияНаСервере = ОписаниеНабораДанныхДляЗаполненияПоСертификатуНаСервере();
				ОписаниеНабораДанныхДляЗаполненияНаСервере.Сертификат = Сертификат;
				ОписаниеНабораДанныхДляЗаполненияНаСервере.НомерДоверенности = НомерДоверенности;
				ОписанияНаборовСертификатаПоДоверенностям.Вставить(
					НомерДоверенности, ОписаниеНабораДанныхДляЗаполненияНаСервере);
				ДанныеДляЗаполненияНаСервере.Вставить(УчетнаяЗаписьОблачногоЭДО,
					ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОписаниеНабораДанныхДляЗаполненияНаСервере));
			КонецЕсли;
			
			ОписаниеНабораДанныхДляЗаполненияНаСервере.ИдентификаторыДанных.Добавить(ОписаниеДанных.ИдентификаторДанных);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ДанныеДляЗаполненияНаСервере;
	
КонецФункции

// Параметры:
//  КонтекстЗаполнения - см. НовыйКонтекстЗаполненияДанныхДляПодписанияПоВыбраннымСертификатам
//  РезультатЗаполнения - см. ЭлектронныеДокументыЭДОИнтеграцияОблакаВызовСервера.РезультатЗаполненияДанныхДляПодписанияПоВыбраннымСертификатамАсинхронно
Процедура ДополнитьКонтекстЗаполненияПоВыбраннымСертификатам(КонтекстЗаполнения, РезультатЗаполнения)
	
	ОбработкаНеисправностейБЭДКлиент.ДополнитьКонтекстДиагностики(
		КонтекстЗаполнения.КонтекстДиагностики,
		РезультатЗаполнения.КонтекстДиагностики);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
		КонтекстЗаполнения.ЗаполненныеДанныеПоУчетнымЗаписям,
		РезультатЗаполнения.ЗаполненныеДанныеПоУчетнымЗаписям);
	
КонецПроцедуры

// Параметры:
//  ДлительнаяОперация    - См. ДлительныеОперации.ВыполнитьФункцию
//  КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//
// Возвращаемое значение:
//  Строка - адрес временного хранилища
Функция АдресРезультатаДлительнойОперации(ДлительнаяОперация, КонтекстДиагностики)
		
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат "";
	ИначеЕсли ДлительнаяОперация.Статус <> "Выполнено" Тогда
		ТекстОшибки = НСтр("ru = 'Не удалось выполнить действия по ЭДО.';
							|en = 'Cannot perform EDI actions.'");
		ПодробныйТекстОшибки = ТекстОшибки;
		Если ЗначениеЗаполнено(ДлительнаяОперация.ПодробноеПредставлениеОшибки) Тогда
			ПодробныйТекстОшибки = ПодробныйТекстОшибки + Символы.ПС + ДлительнаяОперация.ПодробноеПредставлениеОшибки;
		КонецЕсли;
		Если ЗначениеЗаполнено(ДлительнаяОперация.КраткоеПредставлениеОшибки) Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + ДлительнаяОперация.КраткоеПредставлениеОшибки;
		КонецЕсли;
		Ошибка = ОбработкаНеисправностейБЭДКлиент.НоваяОшибка(НСтр("ru = 'Выполнение действий по ЭДО.';
																	|en = 'Perform EDI actions.'"),
			ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка(), ПодробныйТекстОшибки, ТекстОшибки);
		ПодсистемыБЭД = ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД();
		ОбработкаНеисправностейБЭДКлиент.ДобавитьОшибку(КонтекстДиагностики, Ошибка,
			ПодсистемыБЭД.ИнтеграцияОблачногоЭДО);
		Возврат "";
	КонецЕсли;
	
	Возврат ДлительнаяОперация.АдресРезультата;
	
КонецФункции

#КонецОбласти

#Область СозданиеПодписейПоВыбраннымСертификатам

// Возвращаемое значение:
//  Массив из см. НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи
Функция НовыеДанныеДляПодписанияПоУчетнымЗаписям()
	Возврат Новый Массив;
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * Сертификаты - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  * НаборДанных - Массив из см. ЭлектронныеДокументыЭДОИнтеграцияОблака.НовыеДанныеСообщенияДляИнтерактивногоПодписания
//  * УчетнаяЗаписьОблачногоЭДО - СправочникСсылка.УчетныеЗаписиОблачногоЭДО
Функция НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи()
	ОписаниеНабораДанных = ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиентСервер.НовоеОписаниеНабораДанныхДляИнтерактивногоПодписания();
	ОписаниеНабораДанных.Вставить("УчетнаяЗаписьОблачногоЭДО",
		ПредопределенноеЗначение("Справочник.УчетныеЗаписиОблачногоЭДО.ПустаяСсылка"));
	Возврат ОписаниеНабораДанных;
КонецФункции

// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка - идентификатор учетной записи облачного ЭДО.
//  * Значение - Массив из см. НоваяПодписьДанных
Функция НовыеПодписиДанныхПоУчетнымЗаписям()
	Возврат Новый Соответствие;
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ИдентификаторДанных - Строка
//  * Подпись - Неопределено,ДвоичныеДанные
//  * НомерДоверенности - Строка
Функция НоваяПодписьДанных()
	ПодписьДанных = Новый Структура;
	ПодписьДанных.Вставить("ИдентификаторДанных", "");
	ПодписьДанных.Вставить("Подпись", Неопределено);
	ПодписьДанных.Вставить("НомерДоверенности", "");
	Возврат ПодписьДанных;
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ОповещениеОЗавершении - см. ВыбратьСертификатыДляПодписанияПоУчетнымЗаписям.ОповещениеОЗавершении
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  * ПаролиСертификатов - см. КриптографияБЭДКлиент.НовыеПаролиСертификатов
//  * ИндексНабораДанных - Число
//  * ДанныеДляПодписанияПоУчетнымЗаписям - Массив из см. НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи
Функция НовыйКонтекстСозданияПодписейПоВыбраннымСертификатам()
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения);
	Контекст.Вставить("КонтекстДиагностики", Новый Структура);
	Контекст.Вставить("ПаролиСертификатов", КриптографияБЭДКлиент.НовыеПаролиСертификатов());
	Контекст.Вставить("ИндексНабораДанных", 0);
	Контекст.Вставить("ДанныеДляПодписанияПоУчетнымЗаписям", Новый Массив);
	Контекст.Вставить("ПодписиДанныхПоУчетнымЗаписям", НовыеПодписиДанныхПоУчетнымЗаписям());
	Возврат Контекст;
КонецФункции

// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения - результат: см. НовыеДанныеДляПодписанияВыбраннымиСертификатамиПоУчетнымЗаписям
//  ДанныеДляПодписанияПоУчетнымЗаписям - Массив из см. НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи
//  ПаролиСертификатов - см. КриптографияБЭДКлиент.НовыеПаролиСертификатов
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
Процедура СоздатьПодписиПоВыбраннымСертификатам(ОповещениеОЗавершении, ДанныеДляПодписанияПоУчетнымЗаписям, ПаролиСертификатов, КонтекстДиагностики)

	КонтекстСозданияПодписей = НовыйКонтекстСозданияПодписейПоВыбраннымСертификатам();
	КонтекстСозданияПодписей.ОповещениеОЗавершении = ОповещениеОЗавершении;
	КонтекстСозданияПодписей.ДанныеДляПодписанияПоУчетнымЗаписям = ДанныеДляПодписанияПоУчетнымЗаписям;
	КонтекстСозданияПодписей.ПаролиСертификатов = ПаролиСертификатов;
	КонтекстСозданияПодписей.КонтекстДиагностики = КонтекстДиагностики;
	
	СоздатьПодписиПоВыбраннымСертификатамПослеПодготовкиКонтекста(КонтекстСозданияПодписей);
	
КонецПроцедуры

// Параметры:
//  КонтекстСозданияПодписей - см. НовыйКонтекстСозданияПодписейПоВыбраннымСертификатам
Процедура СоздатьПодписиПоВыбраннымСертификатамПослеПодготовкиКонтекста(КонтекстСозданияПодписей)
	
	ДанныеДляПодписания = КонтекстСозданияПодписей.ДанныеДляПодписанияПоУчетнымЗаписям;
	ИндексНабораДанных = КонтекстСозданияПодписей.ИндексНабораДанных;
	
	Если ИндексНабораДанных > ДанныеДляПодписания.ВГраница() Тогда
		СоздатьПодписиПоВыбраннымСертификатамЗавершение(КонтекстСозданияПодписей);
		Возврат;
	КонецЕсли;
	
	ОписаниеНабораДанных = ДанныеДляПодписания[ИндексНабораДанных];
	
	ОписаниеДанных = ОписаниеДанныхДляПодписанияЧерезБСП(ОписаниеНабораДанных);
	
	Оповещение = Новый ОписаниеОповещения("СоздатьПодписиПоВыбраннымСертификатамПослеВыполнения",
		ЭлектронныеДокументыЭДОИнтеграцияОблакаКлиент, КонтекстСозданияПодписей);
	
	ТипПредметаПодписания = НастройкиЭДОКлиентСервер.ТипПредметаПодписанияЭД();
	ТипПодписи = НастройкиЭДОКлиентПовтИсп.ТипПодписи(ТипПредметаПодписания);
	
	КриптографияБЭДКлиент.Подписать(ОписаниеДанных, КонтекстСозданияПодписей.КонтекстДиагностики,,
		Оповещение, КонтекстСозданияПодписей.ПаролиСертификатов, ТипПодписи);
	
КонецПроцедуры

// Параметры:
//  РезультатПодписания     - Структура:
//  * ЗаголовокДанных       - Строка
//  * СообщитьОЗавершении   - Булево
//  * ПоказатьКомментарий   - Булево
//  * КонтекстОперации      - Неопределено,Произвольный
//  * ПрекратитьВыполнение  - Булево
//  * БезПодтверждения      - Булево
//  * ОтборСертификатов     - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  * НаборДанных           - Массив из Структура:
//    ** ИдентификаторДанных - Строка
//    ** Данные             - ДвоичныеДанные
//    ** Представление      - Структура:
//       *** Значение       - ДокументСсылка.СообщениеЭДО
//       *** Представление  - Строка
//    ** ТребуетсяЗаполнитьПодписанта - Булево
//    ** СвойстваПодписи    - Строка,
//                            См. КриптографияБЭДКлиентСервер.НовыеСвойстваПодписи
//    ** ДоверенностиПоСертификатам - Соответствие из КлючИЗначение:
//       *** Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//       *** Значение - Строка - номер доверенности.
//  * Операция              - Строка
//  * Успех                 - Булево
//  * Отказ                 - Булево
//  * ВыбранныйСертификат   - Структура:
//    ** Ссылка             - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//    ** Отпечаток          - Строка - отпечаток сертификата в формате строки Base64.
//    ** Данные             - Строка - адрес временного хранилища, содержащего двоичные данные сертификата.
//  * ПаролиСертификатов    - См. КриптографияБЭДКлиент.НовыеПаролиСертификатов
//  * ТекущийЭлементНабораДанных - Структура
//  * ПользовательНажалКнопкуПодписать - Булево
//  КонтекстСозданияПодписей - См. НовыйКонтекстСозданияПодписейПоВыбраннымСертификатам
Процедура СоздатьПодписиПоВыбраннымСертификатамПослеВыполнения(РезультатПодписания, КонтекстСозданияПодписей) Экспорт
	
	ДанныеДляПодписанияПоУчетнымЗаписям = КонтекстСозданияПодписей.ДанныеДляПодписанияПоУчетнымЗаписям;
	ИндексНабораДанных = КонтекстСозданияПодписей.ИндексНабораДанных;
	ОписаниеНабораДанных = ДанныеДляПодписанияПоУчетнымЗаписям[ИндексНабораДанных];
	
	ПодписиДанныхПоУчетнымЗаписям = КонтекстСозданияПодписей.ПодписиДанныхПоУчетнымЗаписям;
	НаборПодписейДанных = ПодписиДанныхПоУчетнымЗаписям[ОписаниеНабораДанных.УчетнаяЗаписьОблачногоЭДО];
	Если НаборПодписейДанных = Неопределено Тогда
		НаборПодписейДанных = Новый Массив; // См. Массив из см. НоваяПодписьДанных
		ПодписиДанныхПоУчетнымЗаписям.Вставить(ОписаниеНабораДанных.УчетнаяЗаписьОблачногоЭДО, НаборПодписейДанных);
	КонецЕсли;
	
	Если РезультатПодписания = Неопределено
		ИЛИ Не РезультатПодписания.Успех Тогда
		ВыбратьСледующиеДанныеДляСозданияПодписиПоВыбраннымСертификатам(КонтекстСозданияПодписей);
		Возврат;
	КонецЕсли;
	
	Сертификат = РезультатПодписания.ВыбранныйСертификат.Ссылка;
	
	Для Каждого ОписаниеДанных Из РезультатПодписания.НаборДанных Цикл
		
		Если Не ОписаниеДанных.Свойство("СвойстваПодписи") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭтоАдресВременногоХранилища(ОписаниеДанных.СвойстваПодписи) Тогда
			СвойстваПодписи = ПолучитьИзВременногоХранилища(ОписаниеДанных.СвойстваПодписи);
		Иначе
			СвойстваПодписи = ОписаниеДанных.СвойстваПодписи;
		КонецЕсли;
		
		ПодписьДанных = НоваяПодписьДанных();
		ПодписьДанных.ИдентификаторДанных = ОписаниеДанных.ИдентификаторДанных;
		ПодписьДанных.Подпись = СвойстваПодписи.Подпись;
		
		НомерДоверенности = ОписаниеДанных.ДоверенностиПоСертификатам[Сертификат];
		Если ЗначениеЗаполнено(НомерДоверенности) Тогда
			ПодписьДанных.НомерДоверенности = НомерДоверенности;
		КонецЕсли;
		
		НаборПодписейДанных.Добавить(ПодписьДанных);
		
	КонецЦикла;
	
	ВыбратьСледующиеДанныеДляСозданияПодписиПоВыбраннымСертификатам(КонтекстСозданияПодписей);
	
КонецПроцедуры

// Параметры:
//  КонтекстСозданияПодписей - см. НовыйКонтекстСозданияПодписейПоВыбраннымСертификатам
Процедура ВыбратьСледующиеДанныеДляСозданияПодписиПоВыбраннымСертификатам(КонтекстСозданияПодписей)
	
	КонтекстСозданияПодписей.ИндексНабораДанных = КонтекстСозданияПодписей.ИндексНабораДанных + 1;
	
	СоздатьПодписиПоВыбраннымСертификатамПослеПодготовкиКонтекста(КонтекстСозданияПодписей);
	
КонецПроцедуры

// Параметры:
//  КонтекстСозданияПодписей - см. НовыйКонтекстСозданияПодписейПоВыбраннымСертификатам
Процедура СоздатьПодписиПоВыбраннымСертификатамЗавершение(КонтекстСозданияПодписей)
	
	// См. СоздатьПодписиПоВыбраннымСертификатам.ОповещениеОЗавершении
	ВыполнитьОбработкуОповещения(КонтекстСозданияПодписей.ОповещениеОЗавершении,
		КонтекстСозданияПодписей.ПодписиДанныхПоУчетнымЗаписям);
	
КонецПроцедуры

// Параметры:
//  ОписаниеНабораДанных - см. НовоеОписаниеНабораДанныхДляИнтерактивногоПодписанияПоУчетнойЗаписи
// 
// Возвращаемое значение:
//  Структура -  Описание данных для подписания через БСП:
// * ЗаголовокДанных - Строка
// * СообщитьОЗавершении - Булево
// * ПоказатьКомментарий - Булево
// * КонтекстОперации - Неопределено
// * ПрекратитьВыполнение - Булево
// * БезПодтверждения - Булево
// * ОтборСертификатов - Массив из СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// * НаборДанных - Массив из см. ИнтеграцияОблачногоЭДО.НовыеДанныеСообщенияДляИнтерактивногоПодписания
// * Операция - Строка
// * ПредставлениеНабора - Строка
Функция ОписаниеДанныхДляПодписанияЧерезБСП(ОписаниеНабораДанных)
	
	Сертификаты = ОписаниеНабораДанных.Сертификаты;
	НаборДанных = ОписаниеНабораДанных.НаборДанных;
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("ЗаголовокДанных",      НСтр("ru = 'Документ';
														|en = 'Document'"));
	ОписаниеДанных.Вставить("СообщитьОЗавершении",  Ложь);
	ОписаниеДанных.Вставить("ПоказатьКомментарий",  Ложь);
	ОписаниеДанных.Вставить("КонтекстОперации",     Неопределено);
	ОписаниеДанных.Вставить("ПрекратитьВыполнение", Истина);
	ОписаниеДанных.Вставить("БезПодтверждения",     Истина);
	ОписаниеДанных.Вставить("ОтборСертификатов",    Сертификаты);
	ОписаниеДанных.Вставить("НаборДанных",          НаборДанных);
	
	КоличествоДанных = НаборДанных.Количество();
	Если КоличествоДанных = 1 Тогда
		ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Подписание электронного документа';
												|en = 'Sign electronic document'"));
	Иначе
		ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Подписание электронных документов';
												|en = 'Sign electronic documents'"));
		ОписаниеДанных.Вставить("ПредставлениеНабора",
			СтрШаблон(НСтр("ru = 'Электронные документы (%1)';
							|en = 'Electronic documents (%1)'"), КоличествоДанных));
	КонецЕсли;
	
	Возврат ОписаниеДанных;
	
КонецФункции

#КонецОбласти

#Область РезультатОбработкиДействийЭДО

// Параметры:
//  КонтекстОбработки - см. НовыйКонтекстОбработкиРезультатаВыполненияДействийЭДО
Функция РезультатОбработкиДействийИнтеграцииЭДО(КонтекстОбработки)
	
	РезультатОбработки = ЭлектронныеДокументыЭДОКлиент.НовыйРезультатОбработкиДействийИнтеграцииЭДО();
	
	РезультатОбработки.КонтекстДиагностики = КонтекстОбработки.КонтекстДиагностики;
	
	ИтогДействийЭДО = РезультатОбработки.ИтогВыполненияДействий;
	ДокументыДляОбработкиИзвещений = РезультатОбработки.ДокументыДляОбработкиИзвещений;
	ВыполненныеДействияПоУчетнымЗаписям = КонтекстОбработки.СостояниеВыполненияДействий.ВыполненныеДействияПоУчетнымЗаписям;
	ОшибкиФормирования = РезультатОбработки.ОшибкиФормирования;
	
	Для Каждого РезультатДействийПоУчетнойЗаписи Из КонтекстОбработки.РезультатыДействий Цикл
		УчетнаяЗаписьОблачногоЭДО = РезультатДействийПоУчетнойЗаписи.Ключ;
		РезультатДействий = РезультатДействийПоУчетнойЗаписи.Значение;
		
		ЭлектронныеДокументыЭДОКлиентСервер.ДополнитьИтогВыполненияДействийПоЭДО(ИтогДействийЭДО, РезультатДействий.Итог);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
			ДокументыДляОбработкиИзвещений, РезультатДействий.ДокументыДляОбработкиИзвещений, Истина);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОшибкиФормирования, РезультатДействий.ОшибкиФормирования);
		
		ВыполненныеДействия = ВыполненныеДействияПоУчетнымЗаписям[УчетнаяЗаписьОблачногоЭДО];
		Если ВыполненныеДействия = Неопределено Тогда
			ВыполненныеДействия = Новый Массив; // Массив из ПеречислениеСсылка.ДействияПоЭДО
			ВыполненныеДействияПоУчетнымЗаписям.Вставить(УчетнаяЗаписьОблачногоЭДО, ВыполненныеДействия);
		КонецЕсли;
		
		Для Каждого ОбработаноПоДействию Из РезультатДействий.Итог.ОбработаноПоДействиям Цикл
			ВыполненныеДействия.Добавить(ОбработаноПоДействию.Ключ);
		КонецЦикла;
	КонецЦикла;
	
	РезультатОбработки.СостояниеВыполненияДействий = КонтекстОбработки.СостояниеВыполненияДействий;
	
	НаборДействийДляПродолженияПоУчетнымЗаписям = НаборДействийДляПродолженияПоУчетнымЗаписям(
		КонтекстОбработки.ИсходныйНаборДействийЭДО, КонтекстОбработки.СостояниеВыполненияДействий.ВыполненныеДействияПоУчетнымЗаписям);
	
	РезультатОбработки.ПродолжитьВыполнениеДействий =
		ЗначениеЗаполнено(КонтекстОбработки.РасшифрованныеМаркерыПоУчетнымЗаписям)
		ИЛИ ЗначениеЗаполнено(КонтекстОбработки.ПодписиДанныхПоУчетнымЗаписям)
		ИЛИ ЗначениеЗаполнено(НаборДействийДляПродолженияПоУчетнымЗаписям);
	
	Если РезультатОбработки.ПродолжитьВыполнениеДействий Тогда
		РезультатОбработки.ПараметрыПродолженияДействий = ПараметрыПродолженияВыполненияДействийЭДОПоУчетнымЗаписям(
			КонтекстОбработки, НаборДействийДляПродолженияПоУчетнымЗаписям);
	КонецЕсли;
	
	Возврат РезультатОбработки;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ПодписиДанных - Неопределено,Массив из см. НоваяПодписьДанных
//  * РасшифрованныеМаркеры - Неопределено
//                          - см. КриптографияБЭДКлиентСервер.НовыйНаборРасшифрованныхДанных
//  * НаборДействий - см. ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО
Функция НовыеПараметрыПродолженияВыполненияДействийЭДО() Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("ПодписиДанных", Неопределено);
	Параметры.Вставить("РасшифрованныеМаркеры", Неопределено);
	Параметры.Вставить("НаборДействий", ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО());
	Возврат Параметры;
КонецФункции

// Параметры:
//  ИсходныйНаборДействий - см. ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО
//  ВыполненныеДействияПоУчетнымЗаписям - см. НовыеВыполненныеДействияПоУчетнымЗаписям
//
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка - идентификатор учетной записи облачного ЭДО.
//  * Значение - см. ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО
Функция НаборДействийДляПродолженияПоУчетнымЗаписям(ИсходныйНаборДействий, ВыполненныеДействияПоУчетнымЗаписям)
	
	НаборДействийПоУчетнымЗаписям = Новый Соответствие;
	
	Для Каждого ВыполненныеДействияПоУчетнойЗаписи Из ВыполненныеДействияПоУчетнымЗаписям Цикл
		
		УчетнаяЗаписьОблачногоЭДО = ВыполненныеДействияПоУчетнойЗаписи.Ключ;
		ВыполненныеДействия = ВыполненныеДействияПоУчетнойЗаписи.Значение;
		
		НаборДействийДляПродолжения = ЭлектронныеДокументыЭДОКлиент.НаборДействийПослеПодписанияРасшифровки(ИсходныйНаборДействий, ВыполненныеДействия);
		
		Если ЗначениеЗаполнено(НаборДействийДляПродолжения) Тогда
			НаборДействийПоУчетнымЗаписям.Вставить(УчетнаяЗаписьОблачногоЭДО, НаборДействийДляПродолжения);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НаборДействийПоУчетнымЗаписям;
	
КонецФункции

// Параметры:
//  КонтекстОбработки - см. НовыйКонтекстОбработкиРезультатаВыполненияДействийЭДО
//  НаборДействийДляПродолженияПоУчетнымЗаписям - см. НаборДействийДляПродолженияПоУчетнымЗаписям
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка - идентификатор учетной записи облачного ЭДО.
//  * Значение - Массив из см. НовыеПараметрыПродолженияВыполненияДействийЭДО
Функция ПараметрыПродолженияВыполненияДействийЭДОПоУчетнымЗаписям(КонтекстОбработки, НаборДействийДляПродолженияПоУчетнымЗаписям)
	
	ПродолженияВыполненияПоУчетнымЗаписям = Новый Соответствие;
	
	ПодписиДанныхПоУчетнымЗаписям = КонтекстОбработки.ПодписиДанныхПоУчетнымЗаписям;
	РасшифрованныеМаркерыПоУчетнымЗаписям = КонтекстОбработки.РасшифрованныеМаркерыПоУчетнымЗаписям;
	
	Для Каждого РезультатДействийПоУчетнойЗаписи Из КонтекстОбработки.РезультатыДействий Цикл
		
		УчетнаяЗаписьОблачногоЭДО = РезультатДействийПоУчетнойЗаписи.Ключ;
		
		ПодписиДанных = ПодписиДанныхПоУчетнымЗаписям[УчетнаяЗаписьОблачногоЭДО];
		РасшифрованныеМаркеры = РасшифрованныеМаркерыПоУчетнымЗаписям[УчетнаяЗаписьОблачногоЭДО];
		НаборДействийДляПродолжения = НаборДействийДляПродолженияПоУчетнымЗаписям[УчетнаяЗаписьОблачногоЭДО];
		
		Если Не ЗначениеЗаполнено(ПодписиДанных)
			И Не ЗначениеЗаполнено(РасшифрованныеМаркеры)
			И Не ЗначениеЗаполнено(НаборДействийДляПродолжения) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыПродолжения = НовыеПараметрыПродолженияВыполненияДействийЭДО();
		
		Если ПодписиДанных <> Неопределено Тогда
			ПараметрыПродолжения.ПодписиДанных = ПодписиДанных;
		КонецЕсли;
		
		Если РасшифрованныеМаркеры <> Неопределено Тогда
			ПараметрыПродолжения.РасшифрованныеМаркеры = РасшифрованныеМаркеры;
		КонецЕсли;
		
		Если НаборДействийДляПродолжения <> Неопределено Тогда
			ПараметрыПродолжения.НаборДействий = НаборДействийДляПродолжения;
		КонецЕсли;
		
		ПродолженияВыполненияПоУчетнымЗаписям.Вставить(УчетнаяЗаписьОблачногоЭДО, ПараметрыПродолжения);
		
	КонецЦикла;
	
	Возврат ПродолженияВыполненияПоУчетнымЗаписям;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти
