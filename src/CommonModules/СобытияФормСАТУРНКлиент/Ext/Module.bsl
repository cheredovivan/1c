#Область ПрограммныйИнтерфейс

#Область Локализация

// Выполняет переопределяемую команду
//
// Параметры:
//  Форма                   - ФормаКлиентскогоПриложения - форма, в которой расположена команда
//  Команда                 - КомандаФормы     - команда формы
//  ДополнительныеПараметры - Структура        - дополнительные параметры.
//
Процедура ВыполнитьПереопределяемуюКоманду(Форма, Команда, ДополнительныеПараметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Обрабатывает нажатие на гиперссылку со статусом обработки документа в САТУРН.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа, в которой произошло нажатие на гиперссылку,
//  НавигационнаяСсылкаФорматированнойСтроки - Строка - значение гиперссылки форматированной строки,
//  СтандартнаяОбработка - Булево - признак стандартной (системной) обработки события.
//
Процедура ОбработкаНавигационнойСсылки(Форма, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка) Экспорт
	
	Если ЭтоНавигационнаяСсылкаСАТУРН(НавигационнаяСсылкаФорматированнойСтроки) Тогда
	
		Объект = Форма[Форма.ПараметрыИнтеграцииГосИС.Получить("САТУРН").ИмяРеквизитаФормыОбъект];
		ИнтеграцияСАТУРНКлиент.ОбработкаНавигационнойСсылкиВФормеДокументаОснования(
			Форма,
			Объект,
			Неопределено,
			НавигационнаяСсылкаФорматированнойСтроки,
			СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЭтоОповещениеСАТУРН(ИмяСобытия) Тогда
		Возврат;
	КонецЕсли;
	
	МестоВызова = Новый Структура;
	МестоВызова.Вставить("Форма",  Форма);
	МестоВызова.Вставить("Объект", Форма[Форма.ПараметрыИнтеграцииГосИС.Получить("САТУРН").ИмяРеквизитаФормыОбъект]);
	
	Событие = Новый Структура;
	Событие.Вставить("Имя",        ИмяСобытия);
	Событие.Вставить("Параметр",   Параметр);
	Событие.Вставить("Источник",   Источник);
	Событие.Вставить("Обработано", Ложь);
	
	СобытияФормИСКлиентПереопределяемый.ОбработкаОповещенияВФормеДокументаОснования(МестоВызова, Событие);
	
	Если Событие.Обработано Тогда
		Возврат;
	КонецЕсли;
	
	Подсистема = Новый Структура;
	Подсистема.Вставить("Имя",ИнтеграцияСАТУРНКлиентСервер.ИмяПодсистемы());
	Подсистема.Вставить("МодульВызовСервера",ИнтеграцияСАТУРНВызовСервера);
	
	СобытияФормИСКлиент.ОбработкаОповещенияВФормеДокументаОснования(МестоВызова, Событие, Подсистема);
	
КонецПроцедуры

Процедура ПослеЗаписи(Форма, ПараметрыЗаписи) Экспорт
	
	СобытияФормСАТУРНКлиентПереопределяемый.ПослеЗаписи(Форма, ПараметрыЗаписи);
	
КонецПроцедуры

#Область СобытияЭлементовФорм

// Переопределяемая процедура, вызываемая из одноименного обработчика события элемента.
//
// Параметры:
//   Форма                   - ФормаКлиентскогоПриложения - форма, из которой происходит вызов процедуры.
//   Элемент                 - Произвольный     - элемент-источник события "При изменении"
//   ДополнительныеПараметры - Структура        - значения дополнительных параметров влияющих на обработку.
//
Процедура ПриИзмененииЭлемента(Форма, Элемент, ДополнительныеПараметры) Экспорт
	
	СобытияФормСАТУРНКлиентПереопределяемый.ПриИзмененииЭлемента(Форма, Элемент, ДополнительныеПараметры);
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая из одноименного обработчика события элемента.
//
Процедура ПриВыбореЭлемента(Форма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, ДополнительныеПараметры = Неопределено) Экспорт
	
	СобытияФормСАТУРНКлиентПереопределяемый.ПриВыбореЭлемента(Форма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка, ДополнительныеПараметры);
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая из одноименного обработчика события элемента.
//
Процедура ПриАктивизацииЯчейки(Форма, Элемент, ДополнительныеПараметры) Экспорт
	
	СобытияФормСАТУРНКлиентПереопределяемый.ПриАктивизацииЯчейки(Форма, Элемент, ДополнительныеПараметры);
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая из одноименного обработчика события элемента.
//
Процедура ПриНачалеРедактирования(Форма, Элемент, НоваяСтрока, Копирование, ДополнительныеПараметры) Экспорт
	
	СобытияФормСАТУРНКлиентПереопределяемый.ПриНачалеРедактирования(Форма, Элемент, НоваяСтрока, Копирование, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

// Вызывается при наступлении события "Выбор" в табличной части.
// Открывает форму выбранного элемента, если имя реквизита входит в массив имен.
//
// Параметры:
// Форма - ФормаКлиентскогоПриложения - форма объекта,
// ТаблицаФормы - ТаблицаФормы - таблица в которой произошло событие,
// ВыбранноеПоле - ПолеФормы
Процедура ВыборЭлементаТабличнойЧастиОткрытьФормуЭлемента(Форма, ТаблицаФормы, ВыбранноеПоле) Экспорт
	
	МассивИмен = МассивИменРеквизитовФормыОткрытия();
	
	ИмяТабличнойЧасти = ТаблицаФормы.Имя;
	
	Для Каждого ИмяЭлемента Из МассивИмен Цикл
		
		Если Форма.Элементы.Найти(ИмяТабличнойЧасти + ИмяЭлемента) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Форма.Элементы[ИмяТабличнойЧасти + ИмяЭлемента] = ВыбранноеПоле
			И ЗначениеЗаполнено(ТаблицаФормы.ТекущиеДанные[ИмяЭлемента]) Тогда
			
			НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(ТаблицаФормы.ТекущиеДанные[ИмяЭлемента]);
			ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НавигационнаяСсылка);
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#Область ОКПД2

// Вызывается при изменении на форме ОКПД2
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта
//  Элемент - ПолеФормы - элемент формы, содержащий ОКПД2
Процедура ПриИзмененииОКПД2(Форма, Элемент) Экспорт
	
	СобытияФормСАТУРНКлиентПереопределяемый.ПриИзмененииОКПД2(Форма, Элемент);
	
КонецПроцедуры

// Вызывается при начале выбора на форме ОКПД2
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта
//  Элемент - ПолеФормы - элемент формы, содержащий ОКПД2
//  ДанныеВыбора - СписокЗначений - Данные выбора
//  ТекущееЗначение - Строка - текущее значение ОКПД2
//  СтандартнаяОбработка - Булево - флаг стандартной обработки
Процедура ПриНачалеВыбораОКПД2(Форма, Элемент, ДанныеВыбора, ТекущееЗначение, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	СобытияФормСАТУРНКлиентПереопределяемый.ПриНачалеВыбораОКПД2(Форма, Элемент, ДанныеВыбора, ТекущееЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

// Вызывается при обработке выбора на форме ОКПД2
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта
//  Элемент - ПолеФормы - элемент формы, содержащий ОКПД2
//  ВыбранноеЗначение - Произвольный - Выбранное значение
//  СтандартнаяОбработка - Булево - флаг стандартной обработки
Процедура ПриОбработкеВыбораОКПД2(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка) Экспорт
	
	СобытияФормСАТУРНКлиентПереопределяемый.ПриОбработкеВыбораОКПД2(Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

// Вызывается при вводе текста и автоподборе на форме ОКПД2
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма объекта
//  Элемент - ПолеФормы - элемент формы, содержащий ОКПД2
//  Текст - Строка - текст автоподбора
//  ДанныеВыбора - СписокЗначений - Данные выбора
//  ПараметрыПолученияДанных - Структура - Параметры получения данных
//  Ожидание - Булево - флаг ожидания
//  СтандартнаяОбработка - Булево - флаг стандартной обработки
Процедура ПриАвтоподбореОКПД2(Форма, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка) Экспорт
	
	Если ЗначениеЗаполнено(Текст) Тогда
		
		СтандартнаяОбработка = Ложь;
		СобытияФормСАТУРНКлиентПереопределяемый.ПриАвтоподбореОКПД2(Форма, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область Номенклатура

// Выполняет действия при изменении номенклатуры в строке таблицы формы.
//
// Параметры:
//  Форма                  - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока          - ДанныеФормыЭлементКоллекции - текущие данные редактируемой строки таблицы товаров,
//  КэшированныеЗначения   - Структура - сохраненные значения параметров, используемых при обработке,
//  ПараметрыУказанияСерий - ФиксированнаяСтруктура - параметры указаний серий формы
//  ПараметрыЗаполнения    - Неопределено,
//                           См. ИнтеграцияСАТУРНКлиентСервер.ПараметрыЗаполненияТабличнойЧасти
Процедура ПриИзмененииНоменклатуры(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыУказанияСерий = Неопределено,
	ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ИнтеграцияСАТУРНКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
	КонецЕсли;
	
	СобытияФормСАТУРНКлиентПереопределяемый.ПриИзмененииНоменклатуры(
		Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыУказанияСерий, ПараметрыЗаполнения);
	
	ПриИзмененииУпаковки(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область Партия

Процедура ВыполнитьДействиеРазбитьСтроку(ТабличнаяЧасть, ИсходнаяСтрока, ВыделяемоеКоличествоУпаковок) Экспорт
	
	НоваяСтрока = ТабличнаяЧасть.Вставить(ТабличнаяЧасть.Индекс(ИсходнаяСтрока) + 1);
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ИсходнаяСтрока);
	Если ИсходнаяСтрока.КоличествоУпаковок <> 0 Тогда
		НоваяСтрока.КоличествоУпаковок = ВыделяемоеКоличествоУпаковок;
		НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок * ИсходнаяСтрока.Количество / ИсходнаяСтрока.КоличествоУпаковок;
		ИсходнаяСтрока.КоличествоУпаковок = ИсходнаяСтрока.КоличествоУпаковок - НоваяСтрока.КоличествоУпаковок;
		ИсходнаяСтрока.Количество         = ИсходнаяСтрока.Количество         - НоваяСтрока.Количество;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ИсходнаяСтрока, "КоличествоСАТУРН") Тогда
			НоваяСтрока.КоличествоСАТУРН = НоваяСтрока.КоличествоУпаковок;
			ИсходнаяСтрока.КоличествоСАТУРН = ИсходнаяСтрока.КоличествоУпаковок;
		КонецЕсли;
		
		ЗаполнитьКоличествоПринятоВозвращеноПриПодбореПартии(ИсходнаяСтрока);
		ЗаполнитьКоличествоПринятоВозвращеноПриПодбореПартии(НоваяСтрока);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьВыборПартииСНедостаткомКоличества(ТабличнаяЧасть, ВыбранноеЗначение, ТекущиеДанные, ДействиеВыбораПартии) Экспорт
	
	Знак = ?(ТекущиеДанные.Количество > 0, 1, -1);
	ТекстВопроса = СтрШаблон(НСтр("ru = 'Остаток по партии (%1) меньше, чем указано в строке табличной части (%2)';
									|en = 'Остаток по партии (%1) меньше, чем указано в строке табличной части (%2)'"),
		ВыбранноеЗначение.КоличествоСАТУРН, Знак*ТекущиеДанные.Количество);
	ВариантыЗаполнения = Новый СписокЗначений;
	ВариантыЗаполнения.Добавить("РазбитьСтроку", НСтр("ru = 'Разбить строку';
														|en = 'Разбить строку'"));
	ВариантыЗаполнения.Добавить("ИзменитьСтроку", НСтр("ru = 'Изменить количество';
														|en = 'Изменить количество'"));
	ВариантыЗаполнения.Добавить(КодВозвратаДиалога.Пропустить);
	ВариантыЗаполнения.Добавить(КодВозвратаДиалога.Отмена);
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ТабличнаяЧасть", ТабличнаяЧасть);
	ДополнительныеПараметры.Вставить("ВыбранноеЗначение", ВыбранноеЗначение);
	ДополнительныеПараметры.Вставить("ТекущиеДанные", ТекущиеДанные);
	ДополнительныеПараметры.Вставить("ДействиеВыбораПартии", ДействиеВыбораПартии);
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ОбработатьВыборПартииСНедостаткомКоличестваЗавершение", ЭтотОбъект, ДополнительныеПараметры),
		ТекстВопроса,
		ВариантыЗаполнения,,
		"РазбитьСтроку");
		
КонецПроцедуры

Процедура ОбработатьВыборПартииСНедостаткомКоличестваЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
		Или Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ТабличнаяЧасть         = ДополнительныеПараметры.ТабличнаяЧасть;
	ВыбранноеЗначение      = ДополнительныеПараметры.ВыбранноеЗначение;
	ТекущиеДанные          = ДополнительныеПараметры.ТекущиеДанные;
	ОбработчикВыбораПартии = ДополнительныеПараметры.ДействиеВыбораПартии;
	Знак = ?(ТекущиеДанные.Количество > 0, 1, -1);
	
	Если Результат = КодВозвратаДиалога.Пропустить Тогда
		ВыполнитьОбработкуОповещения(ОбработчикВыбораПартии, ВыбранноеЗначение);
		Возврат;
	КонецЕсли;
	
	Коэффициент = ТекущиеДанные.КоличествоУпаковок / ТекущиеДанные.Количество;
	
	Если Результат = "РазбитьСтроку" Тогда
		ВыполнитьДействиеРазбитьСтроку(
			ТабличнаяЧасть, ТекущиеДанные, (ТекущиеДанные.Количество - Знак*ВыбранноеЗначение.КоличествоСАТУРН)*Коэффициент);
		ВыполнитьОбработкуОповещения(ОбработчикВыбораПартии, ВыбранноеЗначение);
		Возврат;
	КонецЕсли;
	
	Если Результат = "ИзменитьСтроку" Тогда
		ТекущиеДанные.Количество = Знак * ВыбранноеЗначение.КоличествоСАТУРН;
		ТекущиеДанные.КоличествоУпаковок = Знак * ВыбранноеЗначение.КоличествоСАТУРН * Коэффициент;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные, "КоличествоСАТУРН") Тогда
			ТекущиеДанные.КоличествоСАТУРН = ТекущиеДанные.КоличествоУпаковок;
		КонецЕсли;
		ЗаполнитьКоличествоПринятоВозвращеноПриПодбореПартии(ТекущиеДанные);
		ВыполнитьОбработкуОповещения(ОбработчикВыбораПартии, ВыбранноеЗначение);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьВыборПартии(Форма, ВыбранноеЗначение, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения = Неопределено) Экспорт
	
	ТекущаяСтрока.Партия = ВыбранноеЗначение.Партия;
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение.ПАТ) Тогда
		ТекущаяСтрока.ПАТ = ВыбранноеЗначение.ПАТ;
	КонецЕсли;
	
	ЗаполнитьТипИзмеряемойВеличиныИКоличествоВУпаковке(ТекущаяСтрока, КэшированныеЗначения);
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ИнтеграцияСАТУРНКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
	КонецЕсли;
	
	СобытияФормСАТУРНКлиентПереопределяемый.ПриИзмененииТипаИзмеряемойВеличины(
		Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения);
	
	ПриИзмененииКоличестваУпаковок(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область Упаковки

// Выполняет действия при изменении упаковки в строке таблицы формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке.
//  ПараметрыЗаполнения  - Неопределено,
//                         См. ИнтеграцияСАТУРНКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
Процедура ПриИзмененииУпаковки(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ИнтеграцияСАТУРНКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
	КонецЕсли;
	
	ЗаполнитьТипИзмеряемойВеличиныИКоличествоВУпаковке(ТекущаяСтрока, КэшированныеЗначения);
	
	СобытияФормСАТУРНКлиентПереопределяемый.ПриИзмененииУпаковки(
		Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения);
	
	ПриИзмененииКоличестваУпаковок(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения);
	
КонецПроцедуры

// Выполняет действия при изменении типа измеряемой величины в строке таблицы формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке.
//  ПараметрыЗаполнения  - Неопределено,
//                        См. ИнтеграцияСАТУРНКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
Процедура ПриИзмененииТипаИзмеряемойВеличины(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ИнтеграцияСАТУРНКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
	КонецЕсли;
	
	ЗаполнитьТипИзмеряемойВеличиныИКоличествоВУпаковке(ТекущаяСтрока, КэшированныеЗначения, Ложь);
	
	СобытияФормСАТУРНКлиентПереопределяемый.ПриИзмененииТипаИзмеряемойВеличины(
		Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения);
	
	ПриИзмененииКоличестваУпаковок(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#Область Количество

// Выполняет действия при изменении подобранного количества (поле КоличествоУпаковок) в строке таблицы формы.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой произошло событие,
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка таблицы товаров,
//  КэшированныеЗначения - Структура - сохраненные значения параметров, используемых при обработке.
//  ПараметрыЗаполнения  - Неопределено,
//                         См. ИнтеграцияСАТУРНКлиентСервер.ПараметрыЗаполненияТабличнойЧасти.
Процедура ПриИзмененииКоличестваУпаковок(Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ИнтеграцияСАТУРНКлиентСервер.ПараметрыЗаполненияТабличнойЧасти();
	КонецЕсли;
	
	Если КэшированныеЗначения = Неопределено Тогда
		КэшированныеЗначения = Новый Структура;
	КонецЕсли;
	
	ПараметрыЗаполненияТипаИзмеряемойВеличиныИКоличества = ИнтеграцияСАТУРНКлиентСервер.ПараметрыЗаполненияТипаИзмеряемойВеличиныИКоличества(
		ТекущаяСтрока, КэшированныеЗначения);
	
	Если ПараметрыЗаполненияТипаИзмеряемойВеличиныИКоличества.ЕстьУпаковка
		И ПараметрыЗаполненияТипаИзмеряемойВеличиныИКоличества.ЕстьКоличествоУпаковок Тогда
		
		Суффиксы = СтрРазделить(ПараметрыЗаполнения.КоличествоСуффикс, ",");
		Если Суффиксы.Количество() = 0 Тогда
			Суффиксы.Добавить("");
		КонецЕсли;
		
		ДанныеУпаковки = ИнтеграцияИСКлиентСервер.КоэффициентВесОбъемУпаковки(
			ТекущаяСтрока.Номенклатура, ТекущаяСтрока.Упаковка, КэшированныеЗначения);
		
		Если ДанныеУпаковки.Коэффициент <> 0 Тогда
			Коэффициент = ДанныеУпаковки.Коэффициент;
		Иначе
			Коэффициент = 1;
		КонецЕсли;
		
		КоличествоВУпаковке = 1;
		Если ПараметрыЗаполненияТипаИзмеряемойВеличиныИКоличества.ЕстьТипИзмеряемойВеличиныСАТУРН Тогда
			Если ТекущаяСтрока.ТипИзмеряемойВеличиныСАТУРН = ПредопределенноеЗначение("Перечисление.ТипыИзмеряемыхВеличинСАТУРН.Объем") Тогда
				КоличествоВУпаковке = ДанныеУпаковки.Объем;
			ИначеЕсли ТекущаяСтрока.ТипИзмеряемойВеличиныСАТУРН = ПредопределенноеЗначение("Перечисление.ТипыИзмеряемыхВеличинСАТУРН.Вес") Тогда
				КоличествоВУпаковке = ДанныеУпаковки.Вес;
			КонецЕсли;
			Если КоличествоВУпаковке = 0 Тогда
				КоличествоВУпаковке = 1;
			КонецЕсли;
		КонецЕсли;
		
		Если ПараметрыЗаполненияТипаИзмеряемойВеличиныИКоличества.ЕстьКоличествоВУпаковкеСАТУРН
			И ТекущаяСтрока.КоличествоВУпаковкеСАТУРН <> 0 Тогда
			КоличествоВУпаковкеСАТУРН = ТекущаяСтрока.КоличествоВУпаковкеСАТУРН;
		Иначе
			КоличествоВУпаковкеСАТУРН = 1;
		КонецЕсли;
		
		Для Каждого Суффикс Из Суффиксы Цикл
			
			ИмяПоляКоличествоУпаковок = "КоличествоУпаковок" + Суффикс;
			ИмяПоляКоличествоСАТУРН   = СтрШаблон("Количество%1САТУРН", Суффикс);
			
			ТекущаяСтрока["Количество" + Суффикс] = ТекущаяСтрока[ИмяПоляКоличествоУпаковок] * Коэффициент;
			
			Если ПараметрыЗаполнения.ПересчитатьКоличествоСАТУРН
				И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущаяСтрока, ИмяПоляКоличествоСАТУРН) Тогда
				ТекущаяСтрока[ИмяПоляКоличествоСАТУРН] = ТекущаяСтрока[ИмяПоляКоличествоУпаковок] * КоличествоВУпаковке / КоличествоВУпаковкеСАТУРН;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	СобытияФормСАТУРНКлиентПереопределяемый.ПриИзмененииКоличестваУпаковок(
		Форма, ТекущаяСтрока, КэшированныеЗначения, ПараметрыЗаполнения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обрабатывает нажатие на гиперссылку объектов САТУР
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма документа, в которой произошло нажатие на гиперссылку,
//  НавигационнаяСсылкаФорматированнойСтроки - Строка - значение гиперссылки форматированной строки,
//  СтандартнаяОбработка - Булево - признак стандартной (системной) обработки события.
//
Процедура ОбработкаНавигационнойСсылкиСАТУРН(Форма, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка) Экспорт
	
	Если СтрНачинаетсяС(НавигационнаяСсылкаФорматированнойСтроки, "ОткрытьМестаХраненияОрганизации") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		НавигационнаяСсылкаМассив = СтрРазделить(НавигационнаяСсылкаФорматированнойСтроки, "_");
		ПрефиксРеквизита = ?(НавигационнаяСсылкаМассив.ВГраница() > 0, НавигационнаяСсылкаМассив[1], "");
		
		ОрганизацияСАТУРН = Форма.Объект[ПрефиксРеквизита + "ОрганизацияСАТУРН"];
		МестоХранения     = Форма.Объект[ПрефиксРеквизита + "МестоХранения"];
		
		ПараметрыФормыОрганизации = Новый Структура(
			"Ключ, ДополнительныеПараметры", 
			ОрганизацияСАТУРН, 
			Новый Структура);
			
		ПараметрыФормыОрганизации.ДополнительныеПараметры.Вставить(
			"ИмяЭлементаПозиционирования", 
			"СтраницаМестаХранения");
		ПараметрыФормыОрганизации.ДополнительныеПараметры.Вставить(
			"ЗначениеЭлементаПозиционирования", 
			МестоХранения);
			
		ОткрытьФорму(
			"Справочник.КлассификаторОрганизацийСАТУРН.ФормаОбъекта",
			ПараметрыФормыОрганизации,
			ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИмяСобытияИзмененаНастройкаАвтоматическогоОбмена() Экспорт
	Возврат "Запись_НастройкиРегламентныхЗаданийСАТУРН";
КонецФункции

Функция МассивИменРеквизитовФормыОткрытия()
	
	Массив = Новый Массив;
	Массив.Добавить("Номенклатура");
	Массив.Добавить("Характеристика");
	Массив.Добавить("Серия");
	Массив.Добавить("Партия");
	Массив.Добавить("ИмпортируемаяПартия");
	Массив.Добавить("ВыделеннаяПартия");
	Массив.Добавить("ПартияВозврат");
	Массив.Добавить("ПАТ");
	
	Возврат Массив;
	
КонецФункции

Функция ЭтоНавигационнаяСсылкаСАТУРН(НавигационнаяСсылка)
	
	Возврат СтрНайти(НавигационнаяСсылка, "ИнтеграцияИС_КомандаНавигационнойСсылки#САТУРН#") > 0;
	
КонецФункции

Функция ЭтоОповещениеСАТУРН(ИмяСобытия)
	
	Возврат СтрНайти(ИмяСобытия, "ИнтеграцияИС_СобытиеОповещения#САТУРН#") > 0;
	
КонецФункции

Процедура ЗаполнитьКоличествоПринятоВозвращеноПриПодбореПартии(ТекущиеДанные)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТекущиеДанные, "КоличествоУпаковокПринято") Тогда
		
		ТекущиеДанные.КоличествоУпаковокПринято = ТекущиеДанные.КоличествоУпаковок;
		ТекущиеДанные.КоличествоПринятоСАТУРН   = ТекущиеДанные.КоличествоУпаковок;
		ТекущиеДанные.КоличествоПринято         = ТекущиеДанные.Количество;
		ТекущиеДанные.КоличествоУпаковокВозвращено = 0;
		ТекущиеДанные.КоличествоВозвращеноСАТУРН   = 0;
		ТекущиеДанные.КоличествоВозвращено = 0;
		ТекущиеДанные.ПричинаРасхождения = "";
		
	КонецЕсли;
	
КонецПроцедуры

#Область Упаковки

Процедура ЗаполнитьТипИзмеряемойВеличиныИКоличествоВУпаковке(ТекущаяСтрока, КэшированныеЗначения, ПерезаполнятьТипИзмеряемойВеличины = Истина)
	
	Если КэшированныеЗначения = Неопределено Тогда
		КэшированныеЗначения = Новый Структура;
	КонецЕсли;
	
	ПараметрыЗаполнения = ИнтеграцияСАТУРНКлиентСервер.ПараметрыЗаполненияТипаИзмеряемойВеличиныИКоличества(
		ТекущаяСтрока, КэшированныеЗначения);
	
	ДанныеУпаковки = ИнтеграцияИСКлиентСервер.КоэффициентВесОбъемУпаковки(
		ТекущаяСтрока.Номенклатура, ТекущаяСтрока.Упаковка, КэшированныеЗначения);
	
	ДанныеДляОбработки = Новый Массив;
	ДанныеДляОбработки.Добавить(ДанныеСтрокиПоПараметрамЗаполненияТипаИзмеряемойВеличиныИКоличества(
		ТекущаяСтрока, ПараметрыЗаполнения));
	
	ИнтеграцияСАТУРНКлиентСервер.ЗаполнитьТипИзмеряемойВеличиныИКоличествоВУпаковке(
		ДанныеДляОбработки, ДанныеУпаковки, ПараметрыЗаполнения, ПерезаполнятьТипИзмеряемойВеличины);
	
	ЗаполнитьЗначенияСвойств(ТекущаяСтрока, ДанныеДляОбработки[0]);
	
КонецПроцедуры

Функция ДанныеСтрокиПоПараметрамЗаполненияТипаИзмеряемойВеличиныИКоличества(Данные, ПараметрыЗаполнения)
	
	ДанныеСтроки = Новый Структура;
	Для Каждого Параметр Из ПараметрыЗаполнения Цикл
		Если Параметр.Значение Тогда
			ИмяПоля = Сред(Параметр.Ключ, 5);
			ДанныеСтроки.Вставить(ИмяПоля, Данные[ИмяПоля]);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДанныеСтроки;
	
КонецФункции

#КонецОбласти

#КонецОбласти
