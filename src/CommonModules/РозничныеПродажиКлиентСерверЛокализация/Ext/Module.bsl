
#Область ПрограммныйИнтерфейс

#Область ПолучениеВнешнихКомпонент

// Обновляет состояние элементов интернет поддержки на форме.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ОбновитьСостояниеИнтернетПоддержки(Форма) Экспорт
	
	//++ Локализация
	
	// ИнтернетПоддержкаПользователей.ПолучениеВнешнихКомпонент
	ПодсистемаСуществуетПолучениеВнешнихКомпонент = Ложь;
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеВнешнихКомпонент") Тогда
		ПодсистемаСуществуетПолучениеВнешнихКомпонент = Истина;
	КонецЕсли;
#Иначе
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеВнешнихКомпонент") Тогда
		ПодсистемаСуществуетПолучениеВнешнихКомпонент = Истина;
	КонецЕсли;
#КонецЕсли
	
	Если ПодсистемаСуществуетПолучениеВнешнихКомпонент Тогда
		Форма.Элементы.ГруппаВнешниеКомпоненты.Видимость = Истина;
		Форма.Элементы.ДекорацияОбновлениеВнешнихКомпонентНеВыполняется.Видимость =
			(Не ОбщегоНазначенияБПОСлужебныйВызовСервера.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки()
			И Форма.НаборКонстант.ВариантОбновленияВнешнихКомпонент = 1);
		Форма.Элементы.ФайлВнешнихКомпонент.Видимость = Форма.НаборКонстант.ВариантОбновленияВнешнихКомпонент = 2;
	Иначе
		Форма.Элементы.ГруппаВнешниеКомпоненты.Видимость = Ложь;
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.ПолучениеВнешнихКомпонент
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ФискальныеОперации

// Дополняет колллекцию доступных видов оплат для вида документа
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма документа:
//  * Объект - ДокументОбъект - Основной реквизит формы
//  ДоступныеВидыОплаты - Структура -
//
Процедура ДополнитьДоступныеВидыОплаты(Форма, ДоступныеВидыОплаты) Экспорт
	
	//++ Локализация
	Если ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.ВозвратПодарочныхСертификатов")
		Или ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.РеализацияПодарочныхСертификатов") Тогда
			
		ДоступныеВидыОплаты.Вставить("СБП", Форма.ИспользоватьОплатуСБП);

	ИначеЕсли ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.ЧекККМ")
		Или ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.ЧекККМВозврат") Тогда
			
		ДополнитьДоступныеВидыОплатыВидомЭСФСС(Форма, ДоступныеВидыОплаты);
		ДоступныеВидыОплаты.Вставить("СБП", Форма.ИспользоватьОплатуСБП);
		
	ИначеЕсли ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.ОперацияПоПлатежнойКарте")
		И Форма.Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту") Тогда
		
		ЭтоОплатаСБП = (Форма.СпособПроведенияПлатежа = ПредопределенноеЗначение("Перечисление.СпособыПроведенияПлатежей.СистемаБыстрыхПлатежей"));
		ДоступныеВидыОплаты.Вставить("СБП", ЭтоОплатаСБП);
	
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Определяет, требуется ли проверка кодов маркировки средствами ККТ.
// 
// Параметры:
//  ПараметрыОперацииФискализацииЧека - Структура - Параметры операции фискализации чека
// 
// Возвращаемое значение:
//  Булево - Истина - требуется проверка кодов маркировки средствами ККТ
Функция ТребуетсяПроверкаКодовМаркировкиСредствамиККТ(ПараметрыОперацииФискализацииЧека) Экспорт
	
	ТребуетсяПроверкаКодовМаркировкиСредствамиККТ = Ложь;
	
	//++ Локализация
	ПозицииЧека = ПараметрыОперацииФискализацииЧека.ПозицииЧека;
	
	Для Каждого ПозицияЧека Из ПозицииЧека Цикл
		Если ОбщегоНазначенияУТКлиентСервер.ЕстьРеквизитОбъекта(ПозицияЧека, "РезультатРаспределенияВрем")
			И ПозицияЧека.РезультатРаспределенияВрем <> Неопределено Тогда
			
			ТребуетсяПроверкаКодовМаркировкиСредствамиККТ = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	//-- Локализация
	
	Возврат ТребуетсяПроверкаКодовМаркировкиСредствамиККТ;
	
КонецФункции

//++ Локализация

// Возвращает сумму оплаты СБП по документу
// 
// Параметры:
//  ОплатыПлатежнойКартой - ДанныеФормыКоллекция, ТаблицаЗначений, ТабличнаяЧасть - Оплаты платежной картой:
//  * ВидОплаты - ПеречислениеСсылка.ТипыПлатежнойСистемыККТ -
//  * Сумма - Число - 
//  * СтатусОплатыСБП - ПеречислениеСсылка.ТипыСтатусовОплатыСБП - 
//  ТолькоОплатаВыполнена - Булево - Истина - Выполнена
// 
// Возвращаемое значение:
//  Число - сумма оплаты СБП по документу
//
Функция СуммаОплатыСБППоДокументу(ОплатыПлатежнойКартой, ТолькоОплатаВыполнена = Ложь) Экспорт
	
	СуммаОплатыСБП = 0;
	
	Для Каждого СтрокаТЗ Из ОплатыПлатежнойКартой Цикл
		
		Если СтрокаТЗ.ВидОплаты = ПредопределенноеЗначение("Перечисление.ТипыПлатежнойСистемыККТ.СистемаБыстрыхПлатежей") Тогда
			
			Если ТолькоОплатаВыполнена 
					И (СтрокаТЗ.СтатусОплатыСБП = ПредопределенноеЗначение("Перечисление.ТипыСтатусовОплатыСБП.Выполнена") 
					ИЛИ СтрокаТЗ.СтатусОплатыСБП = ПредопределенноеЗначение("Перечисление.ТипыСтатусовОплатыСБП.ОжидаетПодтверждение")
						И ПустаяСтрока(СтрокаТЗ.СсылочныйНомер)) 
					ИЛИ Не ТолькоОплатаВыполнена Тогда
				
				СуммаОплатыСБП = СуммаОплатыСБП + СтрокаТЗ.Сумма;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СуммаОплатыСБП;
	
КонецФункции

// Возвращает массив строк оплаты СБП по документу
// 
// Параметры:
//  ОплатаПлатежнымиКартами - ДанныеФормыКоллекция, ТаблицаЗначений, ТабличнаяЧасть - Оплаты платежной картой:
//  * ВидОплаты - ПеречислениеСсылка.ТипыПлатежнойСистемыККТ -
//  * СтатусОплатыСБП - ПеречислениеСсылка.ТипыСтатусовОплатыСБП - 
//  ТолькоОплатаВыполнена - Булево - Истина - Выполнена
// 
// Возвращаемое значение:
//  Число - сумма оплаты СБП по документу
//
Функция СтрокиОплатыСБП(ОплатаПлатежнымиКартами, ТолькоОплатаВыполнена = Ложь) Экспорт
	
	ОтборОплатыСБП = Новый Структура("ВидОплаты", ПредопределенноеЗначение("Перечисление.ТипыПлатежнойСистемыККТ.СистемаБыстрыхПлатежей"));
	Если ТолькоОплатаВыполнена Тогда
		ОтборОплатыСБП.Вставить("СтатусОплатыСБП", ПредопределенноеЗначение("Перечисление.ТипыСтатусовОплатыСБП.Выполнена"));
	КонецЕсли;
	
	Возврат ОплатаПлатежнымиКартами.НайтиСтроки(ОтборОплатыСБП);
	
КонецФункции

// Возвращает структуру данных используемых для для возврата по документу оплаты СБП.
// 
// Возвращаемое значение:
//  Структура:
//   *ОснованиеПлатежа - ДокументСсылка - 
//   *Партнер - СправочникСсылка.Партнеры -
//   *Сумма - Число - 
//
Функция ДанныеДляВозвратаПоДокументуОплатыСБП() Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ОснованиеПлатежа", Неопределено);
	Результат.Вставить("Партнер", Неопределено);
	Результат.Вставить("Сумма", 0);
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру данных используемых для подключения к сервису НСПК.
//
// Возвращаемое значение:
//  Структура:
//	* АдресСервисаНСПК - Строка - Адрес сервиса НСПК
//	* КлючДоступаНСПК - Строка - Ключ доступа к НСПК
//	* ПараметрыПроксиНСПК - Структура:
//	   **Сервер - Строка
//	   **Порт - Строка
//	   **Пользователь - Строка
//	   **Пароль - Строка
//
Функция ПараметрыПодключенияНСПК() Экспорт
	
	ОбщиеПараметрыПодключенияНСПК = Новый Структура();
	ОбщиеПараметрыПодключенияНСПК.Вставить("АдресСервисаНСПК", "");
	ОбщиеПараметрыПодключенияНСПК.Вставить("КлючДоступаНСПК", "");
	ОбщиеПараметрыПодключенияНСПК.Вставить("ПараметрыПроксиНСПК", Новый Структура);
	ОбщиеПараметрыПодключенияНСПК.ПараметрыПроксиНСПК.Вставить("Сервер", "");
	ОбщиеПараметрыПодключенияНСПК.ПараметрыПроксиНСПК.Вставить("Порт", "");
	ОбщиеПараметрыПодключенияНСПК.ПараметрыПроксиНСПК.Вставить("Пользователь", "");
	ОбщиеПараметрыПодключенияНСПК.ПараметрыПроксиНСПК.Вставить("Пароль", "");
	
	Возврат ОбщиеПараметрыПодключенияНСПК;
КонецФункции

//-- Локализация

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ Локализация

// Дополняет колллекцию доступных видов оплат видом Электронный сертификат ФСС
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма документа:
//  * Объект - ДокументОбъект - Основной реквизит формы
//  ДоступныеВидыОплаты - Структура -
//
Процедура ДополнитьДоступныеВидыОплатыВидомЭСФСС(Форма, ДоступныеВидыОплаты)
	
	ВидОплатыДоступен = Ложь;
	Если Форма.ИспользоватьОплатуЭСФСС 
		И Форма.ЭквайринговыеТерминалы.Количество() > 0
		И ТипЗнч(Форма.ПараметрыКассыККМ) = Тип("ФиксированнаяСтруктура")
		И Форма.ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования = Ложь
		И ЗначениеЗаполнено(Форма.ПараметрыКассыККМ.ИдентификаторУстройства) Тогда
		
		Для Каждого ЭквайринговыйТерминал Из Форма.ЭквайринговыеТерминалы Цикл
			Если ЭквайринговыйТерминал.Значение.ИспользоватьБезПодключенияОборудования = Ложь 
				И ЭквайринговыйТерминал.Значение.ПодключаемоеОборудованиеККТ = Форма.ПараметрыКассыККМ.ИдентификаторУстройства Тогда
				ВидОплатыДоступен = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДоступныеВидыОплаты.Вставить("ЭСФСС", ВидОплатыДоступен);
КонецПроцедуры

//-- Локализация

#КонецОбласти
