////////////////////////////////////////////////////////////////////////////////
// Процедуры, используемые для локализации документа "Принятие к учету узлов и компонентов амортизации".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация
	МеханизмыДокумента.Добавить("ИмущественныеНалоги");
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- Локализация
	
КонецПроцедуры

// Добавляет в текст запроса реквизиты шапки документа.
//  Реквизиты добавляются вместо параметра "&ПринятиеКУчетуОС_РеквизитыДокумента".
//
// Параметры:
//  ТекстЗапроса - Строка				 - Исходный текст запроса.
//  ИмяТаблицы	 - Строка, Неопределено	 - Синоним таблицы документа в запросе.
//  	Для получения пустых значений нужно передать "Неопределено".
//
Процедура ДобавитьВТекстЗапросаРеквизитыДокумента(ТекстЗапроса, ИмяТаблицы = Неопределено) Экспорт
	
	ПоляЛокализации = "NULL";
	
	//++ Локализация
	
	ПоляЛокализации =
	"
	|	ДанныеДокумента.ГрафикАмортизации КАК ГрафикАмортизации
	|	,НЕОПРЕДЕЛЕНО КАК СтатьяРасходовНУ
	|	,НЕОПРЕДЕЛЕНО КАК АналитикаРасходовНУ
	|	,ЛОЖЬ КАК ОтражатьВНУ
	|	,ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ПустаяСсылка) КАК ПорядокУчетаНУ
	|";

	ПоляЛокализации = СтрЗаменить(ПоляЛокализации, "ДанныеДокумента", ИмяТаблицы);
		
	//-- Локализация
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПринятиеКУчетуОС_РеквизитыДокумента", ПоляЛокализации);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = "";
	
	//++ Локализация
	
	СписокЗапросов = Новый Массив;
	
	ВнеоборотныеАктивыЛокализация.ТекстОтраженияВРеглУчетеНачисленияАмортизацииОС(СписокЗапросов);
	
	#Область ПринятиеКУчетуОС_Дт_СчетУчета__Кт_08_04_2
	
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Принятие к учету ОС (Дт СчетУчета:: Кт 08.04.2)
	|//
	|ВЫБРАТЬ
	|	
	|	Строки.Регистратор КАК Ссылка,
	|	Строки.Период КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СтоимостьРегл КАК Сумма,
	|	Строки.Стоимость КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СтоимостьВНА) КАК ВидСчетаДт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	Строки.Арендатор КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|
	|	Строки.СтоимостьНУ КАК СуммаНУДт,
	|	Строки.СтоимостьПР КАК СуммаПРДт,
	|	Строки.СтоимостьВР КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Строки.КорСтатьяРасходов КАК АналитикаУчетаКт,
	|	Строки.КорПодразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Строки.КорПодразделение КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	Строки.КорАналитикаРасходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|
	|	Строки.СтоимостьРегл - Строки.КорПостояннаяРазница - Строки.КорВременнаяРазница КАК СуммаНУКт,
	|	Строки.КорПостояннаяРазница КАК СуммаПРКт,
	|	Строки.КорВременнаяРазница КАК СуммаВРКт,
	|
	|	""Принятие к учету узлов и компонентов амортизации"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуУзловКомпонентовАмортизации КАК Операция
	|		ПО Операция.Ссылка = ДокументыКОтражению.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьОС КАК Строки
	|		ПО Строки.Регистратор = ДокументыКОтражению.Ссылка
	|	
	|ГДЕ
	|	Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПринятиеКУчетуУзловКомпонентовАмортизации)
	|	И Строки.РасчетСтоимости
	|	И (Строки.Стоимость <> 0
	|		ИЛИ Строки.СтоимостьРегл <> 0
	|		ИЛИ Строки.СтоимостьНУ <> 0
	|		ИЛИ Строки.СтоимостьПР <> 0
	|		ИЛИ Строки.СтоимостьВР <> 0)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	#Область СписаниеНаРасходыНеПринимаяКУчету_Дт_СчетЗатрат__Кт_08_04_2
	
	ТекстЗапроса =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Включение в расходы стоимости ОС (Дт СчетЗатрат:: Кт 08.04.2)
	|//
	|ВЫБРАТЬ
	|	
	|	СтрокиДт.Регистратор КАК Ссылка,
	|	СтрокиДт.Период КАК Период,
	|	СтрокиДт.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	СтрокиДт.СуммаРегл КАК Сумма,
	|	СтрокиДт.СуммаУпр КАК СуммаУУ,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	СтрокиДт.СтатьяРасходов КАК АналитикаУчетаДт,
	|	СтрокиДт.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	СтрокиДт.Подразделение КАК ПодразделениеДт,
	|	СтрокиДт.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	СтрокиДт.СтатьяРасходов КАК СубконтоДт1,
	|	СтрокиДт.АналитикаРасходов КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	СтрокиДт.СуммаРегл - СтрокиДт.ПостояннаяРазница - СтрокиДт.ВременнаяРазница КАК СуммаНУДт,
	|	СтрокиДт.ПостояннаяРазница КАК СуммаПРДт,
	|	СтрокиДт.ВременнаяРазница КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	СтрокиКт.СтатьяРасходов КАК АналитикаУчетаКт,
	|	СтрокиКт.Подразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	СтрокиКт.Подразделение КАК ПодразделениеКт,
	|	СтрокиКт.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	СтрокиКт.АналитикаРасходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	СтрокиКт.СуммаРегл - СтрокиКт.ПостояннаяРазница - СтрокиКт.ВременнаяРазница КАК СуммаНУКт,
	|	СтрокиКт.ПостояннаяРазница КАК СуммаПРКт,
	|	СтрокиКт.ВременнаяРазница КАК СуммаВРКт,
	|
	|	""Включение в расходы стоимости ОС"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК СтрокиДт
	|		ПО СтрокиДт.Регистратор = ДокументыКОтражению.Ссылка
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК СтрокиКт
	|		ПО СтрокиКт.Регистратор = СтрокиДт.Регистратор
	|			И СтрокиКт.НастройкаХозяйственнойОперации = СтрокиДт.НастройкаХозяйственнойОперации
	|			И СтрокиКт.ИдентификаторФинЗаписи = СтрокиДт.ИдентификаторФинЗаписи
	|			И СтрокиКт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			И (СтрокиКт.СуммаРегл <> 0
	|				ИЛИ СтрокиКт.ПостояннаяРазница <> 0
	|				ИЛИ СтрокиКт.ВременнаяРазница <> 0)
	|
	|ГДЕ
	|	СтрокиДт.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНаРасходыСтоимостиОСНеПринимаяКУчету)
	|	И СтрокиДт.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	
	#КонецОбласти
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
	//-- Локализация
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, необходимых для отражения в регламентированном учете.
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = "";
	
	//++ Локализация

	СписокЗапросов = Новый Массив;
	
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаВТНачислениеАмортизацииОС(СписокЗапросов);
	
	СписокЗапросов.Добавить(Символы.ПС);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());

	//-- Локализация
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты) Экспорт

	//++ Локализация
	Запрос.УстановитьПараметр("ПредельнаяСтоимостьСписанияНаРасходы", ВнеоборотныеАктивыЛокализация.ПредельнаяСтоимостьСписанияНаРасходы());
	//-- Локализация
	
КонецПроцедуры

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса проведения.
//  Регистры - Строка, Структура - Список регистров проведения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация
	
	ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	
	//-- Локализация
	
КонецПроцедуры

Процедура ТаблицыДвиженийРасчетаСтоимости(Запрос, ТекстыЗапроса) Экспорт
	
	//++ Локализация
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Неопределено);
	//-- Локализация
	
КонецПроцедуры
	
#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// 
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапросаТаблицаПорядокУчетаОСБУ() Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Регистратор,
	|	ДанныеДокумента.Период         КАК Период,
	|	ДанныеДокумента.Организация    КАК Организация,
	|	ТаблицаОС.КомпонентАмортизации КАК ОсновноеСредство,
	|
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК СостояниеБУ,
	|
	|	ДанныеДокумента.НачислятьАмортизациюБУ      КАК НачислятьАмортизациюБУ,
	|	ДанныеДокумента.ГрафикАмортизации           КАК ГрафикАмортизации,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.СтатьяРасходовБУ <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВтСведенияВладельцев.СпособОтраженияРасходовБУ
	|	КОНЕЦ КАК СпособОтраженияРасходовБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.СтатьяРасходовБУ <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.СтатьяРасходовБУ
	|		ИНАЧЕ ВтСведенияВладельцев.СтатьяРасходовБУ
	|	КОНЕЦ КАК СтатьяРасходовБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.СтатьяРасходовБУ <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.АналитикаРасходовБУ
	|		ИНАЧЕ ВтСведенияВладельцев.АналитикаРасходовБУ
	|	КОНЕЦ КАК АналитикаРасходовБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.СтатьяРасходовБУ <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВтСведенияВладельцев.ПодразделениеБУ
	|	КОНЕЦ КАК ПодразделениеБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.СтатьяРасходовБУ <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВтСведенияВладельцев.НаправлениеДеятельностиБУ
	|	КОНЕЦ КАК НаправлениеДеятельностиБУ,
	|
	|	ВтСведенияВладельцев.АмортизационнаяГруппа  КАК АмортизационнаяГруппа,
	|	ВтСведенияВладельцев.НедвижимоеИмущество    КАК НедвижимоеИмущество,
	|	ВтСведенияВладельцев.НалогообложениеНДС     КАК НалогообложениеНДС
	|
	|ИЗ
	|	ДанныеДокументаТаблицаКомпоненты КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСведенияВладельцев КАК ВтСведенияВладельцев
	|		ПО ВтСведенияВладельцев.Ссылка = ТаблицаОС.Ссылка
	|			И ВтСведенияВладельцев.ИдентификаторСтроки = ТаблицаОС.ИдентификаторСтроки
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

// 
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапросаТаблицаПараметрыАмортизацииОСБУ() Экспорт
	
	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Регистратор,
	|	ДанныеДокумента.Период         КАК Период,
	|	ДанныеДокумента.Организация    КАК Организация,
	|	ТаблицаОС.КомпонентАмортизации КАК ОсновноеСредство,
	|
	|	ДанныеДокумента.Период                 КАК ДатаПоследнегоИзменения,
	|	ДанныеДокумента.СрокИспользованияБУ    КАК СрокПолезногоИспользованияБУ,
	|	ДанныеДокумента.КоэффициентУскоренияБУ КАК КоэффициентУскорения,
	|	ДанныеДокумента.ГрафикАмортизации      КАК ГрафикАмортизации,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|				ИЛИ ДанныеДокумента.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега)
	|			ТОГДА ТаблицаОС.ОбъемНаработки
	|	КОНЕЦ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|
	|	ЕСТЬNULL(РасчетСтоимости.СуммаПР + РасчетСтоимости.СуммаПРЦФ, 0) КАК ПРДляВычисленияАмортизации,
	|
	|	ДанныеДокумента.СрокИспользованияБУ КАК СрокИспользованияДляВычисленияАмортизации,
	|
	|	ЕСТЬNULL(РасчетСтоимости.СуммаБУ + РасчетСтоимости.СуммаЦФ, ТаблицаОС.СтоимостьБУ) КАК СтоимостьДляВычисленияАмортизации,
	|
	|	ТаблицаОС.ЛиквидационнаяСтоимостьРегл КАК ЛиквидационнаяСтоимость
	|
	|ИЗ
	|	ДанныеДокументаТаблицаКомпоненты КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК РасчетСтоимости
	|		ПО РасчетСтоимости.Ссылка = ТаблицаОС.Ссылка
	|			И РасчетСтоимости.ОбъектУчета = ТаблицаОС.КомпонентАмортизации
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтСведенияВладельцев(ТекстыЗапроса) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтМестонахождениеОС(ТекстыЗапроса, "ДанныеДокументаТаблицаКомпоненты");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОС(ТекстыЗапроса, "ДанныеДокументаТаблицаКомпоненты");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОСУУ(ТекстыЗапроса, "ДанныеДокументаТаблицаКомпоненты");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаОСБУ(ТекстыЗапроса, "ДанныеДокументаТаблицаКомпоненты");
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтСоставЕГДС(ТекстыЗапроса, "ДанныеДокументаТаблицаКомпоненты");
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаКомпоненты.Ссылка КАК Ссылка,
	|	ТаблицаКомпоненты.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|
	|	МестонахождениеОС.Местонахождение КАК Местонахождение,
	|	МестонахождениеОС.МОЛ КАК МОЛ,
	|	МестонахождениеОС.МОЛАрендатора КАК МОЛАрендатора,
	|	МестонахождениеОС.АдресМестонахождения КАК АдресМестонахождения,
	|	МестонахождениеОС.АдресМестонахожденияЗначение КАК АдресМестонахожденияЗначение,
	|	МестонахождениеОС.ПодразделениеАрендатора КАК ПодразделениеАрендатора,
	|	МестонахождениеОС.Арендатор КАК Арендатор,
	|
	|	ПорядокУчетаОСУУ.СпособОтраженияРасходов КАК СпособОтраженияРасходовУУ,
	|	ПорядокУчетаОСУУ.СтатьяРасходов КАК СтатьяРасходовУУ,
	|	ПорядокУчетаОСУУ.АналитикаРасходов КАК АналитикаРасходовУУ,
	|	ПорядокУчетаОСУУ.Подразделение КАК ПодразделениеУУ,
	|	ПорядокУчетаОСУУ.НаправлениеДеятельности КАК НаправлениеДеятельностиУУ,
	|	ПорядокУчетаОСУУ.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизациюУУ,
	|	ПорядокУчетаОСУУ.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходовУУ,
	|
	|	ПорядокУчетаОСБУ.СпособОтраженияРасходовБУ КАК СпособОтраженияРасходовБУ,
	|	ПорядокУчетаОСБУ.СтатьяРасходовБУ КАК СтатьяРасходовБУ,
	|	ПорядокУчетаОСБУ.АналитикаРасходовБУ КАК АналитикаРасходовБУ,
	|	ПорядокУчетаОСБУ.ПодразделениеБУ КАК ПодразделениеБУ,
	|	ПорядокУчетаОСБУ.НаправлениеДеятельностиБУ КАК НаправлениеДеятельностиБУ,
	|	ПорядокУчетаОСБУ.СтатьяРасходовАмортизационнойПремии КАК СтатьяРасходовАмортизационнойПремии,
	|	ПорядокУчетаОСБУ.АналитикаРасходовАмортизационнойПремии КАК АналитикаРасходовАмортизационнойПремии,
	|	ПорядокУчетаОСБУ.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ПорядокУчетаОСБУ.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	ПорядокУчетаОСБУ.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ПорядокУчетаОСБУ.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ,
	|	ПорядокУчетаОСБУ.НедвижимоеИмущество КАК НедвижимоеИмущество,
	|	ПорядокУчетаОСБУ.НалогообложениеНДС КАК НалогообложениеНДС,
	|
	|	ПорядокУчетаОС.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|
	|	СоставЕГДС.ЕГДС КАК ЕГДС
	|
	|ПОМЕСТИТЬ ВтСведенияВладельцев
	|
	|ИЗ
	|	ДанныеДокументаТаблицаКомпоненты КАК ТаблицаКомпоненты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтМестонахождениеОС КАК МестонахождениеОС
	|		ПО МестонахождениеОС.Ссылка = ТаблицаКомпоненты.Ссылка
	|			И МестонахождениеОС.ОсновноеСредство = ТаблицаКомпоненты.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПорядокУчетаОС КАК ПорядокУчетаОС
	|		ПО ПорядокУчетаОС.Ссылка = ТаблицаКомпоненты.Ссылка
	|			И ПорядокУчетаОС.ОсновноеСредство = ТаблицаКомпоненты.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПорядокУчетаОСБУ КАК ПорядокУчетаОСБУ
	|		ПО ПорядокУчетаОСБУ.Ссылка = ТаблицаКомпоненты.Ссылка
	|			И ПорядокУчетаОСБУ.ОсновноеСредство = ТаблицаКомпоненты.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПорядокУчетаОСУУ КАК ПорядокУчетаОСУУ
	|		ПО ПорядокУчетаОСУУ.Ссылка = ТаблицаКомпоненты.Ссылка
	|			И ПорядокУчетаОСУУ.ОсновноеСредство = ТаблицаКомпоненты.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСоставЕГДС КАК СоставЕГДС
	|		ПО СоставЕГДС.Ссылка = ТаблицаКомпоненты.Ссылка
	|			И СоставЕГДС.ВнеоборотныйАктив = ТаблицаКомпоненты.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	ИдентификаторСтроки
	|";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

//++ Локализация

Процедура ТекстЗапросаТаблицаСобытияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период         КАК Период,
	|	ДанныеДокумента.Организация    КАК Организация,
	|	ДанныеДокумента.СобытиеОС      КАК Событие,
	|	ТаблицаОС.КомпонентАмортизации КАК ОсновноеСредство,
	|	&НазваниеДокумента             КАК НазваниеДокумента,
	|	ДанныеДокумента.Номер          КАК НомерДокумента
	|
	|ИЗ
	|	ДанныеДокументаТаблицаКомпоненты КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СпособыОтраженияРасходовПоИмущественнымНалогам";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыЛокализация.ТекстЗапросаТаблицаВтСпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, "ДанныеДокументаТаблицаКомпоненты");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Регистратор,
	|	ДанныеДокумента.Период         КАК Период,
	|	ДанныеДокумента.Организация    КАК Организация,
	|	ТаблицаОС.КомпонентАмортизации КАК ОсновноеСредство,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество) КАК ВидНалога,
	|
	|	ВтСпособыОтраженияРасходовПоИмущественнымНалогам.СтатьяРасходов    КАК СтатьяРасходов,
	|	ВтСпособыОтраженияРасходовПоИмущественнымНалогам.АналитикаРасходов КАК АналитикаРасходов
	|
	|ИЗ
	|	ДанныеДокументаТаблицаКомпоненты КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСпособыОтраженияРасходовПоИмущественнымНалогам КАК ВтСпособыОтраженияРасходовПоИмущественнымНалогам
	|		ПО ВтСпособыОтраженияРасходовПоИмущественнымНалогам.Ссылка = ТаблицаОС.Ссылка
	|			И ВтСпособыОтраженияРасходовПоИмущественнымНалогам.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			И ВтСпособыОтраженияРасходовПоИмущественнымНалогам.ВидНалога = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество)
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Регистратор,
	|	ДанныеДокумента.Период         КАК Период,
	|	ДанныеДокумента.Организация    КАК Организация,
	|	ТаблицаОС.КомпонентАмортизации КАК ОсновноеСредство,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог) КАК ВидНалога,
	|
	|	ВтСпособыОтраженияРасходовПоИмущественнымНалогам.СтатьяРасходов    КАК СтатьяРасходов,
	|	ВтСпособыОтраженияРасходовПоИмущественнымНалогам.АналитикаРасходов КАК АналитикаРасходов
	|
	|ИЗ
	|	ДанныеДокументаТаблицаКомпоненты КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСпособыОтраженияРасходовПоИмущественнымНалогам КАК ВтСпособыОтраженияРасходовПоИмущественнымНалогам
	|		ПО ВтСпособыОтраженияРасходовПоИмущественнымНалогам.Ссылка = ТаблицаОС.Ссылка
	|			И ВтСпособыОтраженияРасходовПоИмущественнымНалогам.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			И ВтСпособыОтраженияРасходовПоИмущественнымНалогам.ВидНалога = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог)
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Регистратор,
	|	ДанныеДокумента.Период         КАК Период,
	|	ДанныеДокумента.Организация    КАК Организация,
	|	ТаблицаОС.КомпонентАмортизации КАК ОсновноеСредство,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог) КАК ВидНалога,
	|
	|	ВтСпособыОтраженияРасходовПоИмущественнымНалогам.СтатьяРасходов    КАК СтатьяРасходов,
	|	ВтСпособыОтраженияРасходовПоИмущественнымНалогам.АналитикаРасходов КАК АналитикаРасходов
	|
	|ИЗ
	|	ДанныеДокументаТаблицаКомпоненты КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСпособыОтраженияРасходовПоИмущественнымНалогам КАК ВтСпособыОтраженияРасходовПоИмущественнымНалогам
	|		ПО ВтСпособыОтраженияРасходовПоИмущественнымНалогам.Ссылка = ТаблицаОС.Ссылка
	|			И ВтСпособыОтраженияРасходовПоИмущественнымНалогам.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|			И ВтСпособыОтраженияРасходовПоИмущественнымНалогам.ВидНалога = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог)
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|	И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Регистратор,
	|	ДанныеДокумента.Ссылка КАК Документ,
	|	ДанныеДокумента.Период КАК Период,
	|	НАЧАЛОПЕРИОДА(ДанныеДокумента.Период, ДЕНЬ) КАК ДатаОтражения,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете) КАК Статус
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

//-- Локализация

Функция ТекстЗапросаТаблицаПервоначальныеСведенияОС(Запрос, ТекстыЗапроса) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияОС(ТекстыЗапроса, "ДанныеДокументаТаблицаКомпоненты");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Регистратор,
	|	ДанныеДокумента.Период         КАК Период,
	|	ТаблицаОС.КомпонентАмортизации КАК ОсновноеСредство,
	|	ДанныеДокумента.Организация    КАК Организация,
	|
	|	ПервоначальныеСведенияОСВладельца.СпособПоступления КАК СпособПоступления,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|				И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ДанныеДокумента.Период 
	|		ИНАЧЕ ВтПервоначальныеСведенияКомпонент.ДатаВводаВЭксплуатациюБУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|				И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ДанныеДокумента.Период 
	|		ИНАЧЕ ВтПервоначальныеСведенияКомпонент.ДатаВводаВЭксплуатациюУУ
	|	КОНЕЦ КАК ДатаВводаВЭксплуатациюУУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|				И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы) 
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ВтПервоначальныеСведенияКомпонент.ДокументВводаВЭксплуатациюБУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|				И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ДанныеДокумента.Ссылка 
	|		ИНАЧЕ ВтПервоначальныеСведенияКомпонент.ДокументВводаВЭксплуатациюУУ
	|	КОНЕЦ КАК ДокументВводаВЭксплуатациюУУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ДанныеДокумента.МетодНачисленияАмортизацииБУ 
	|		ИНАЧЕ ВтПервоначальныеСведенияКомпонент.МетодНачисленияАмортизацииБУ
	|	КОНЕЦ КАК МетодНачисленияАмортизацииБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ДанныеДокумента.ПорядокУчетаБУ 
	|		ИНАЧЕ ВтПервоначальныеСведенияКомпонент.ПорядокУчетаБУ
	|	КОНЕЦ КАК ПорядокУчетаБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ДанныеДокумента.ПорядокУчетаУУ 
	|		ИНАЧЕ ВтПервоначальныеСведенияКомпонент.ПорядокУчетаУУ
	|	КОНЕЦ КАК ПорядокУчетаУУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|				И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы) 
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СуммаБУ + РасчетСтоимости.СуммаЦФ, ТаблицаОС.СтоимостьБУ)
	|		ИНАЧЕ ВтПервоначальныеСведенияКомпонент.ПервоначальнаяСтоимостьБУ
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьБУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|				И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|			ТОГДА ЕСТЬNULL(РасчетСтоимости.СуммаУУ, ТаблицаОС.СтоимостьУУ)
	|		ИНАЧЕ ВтПервоначальныеСведенияКомпонент.ПервоначальнаяСтоимостьУУ
	|	КОНЕЦ КАК ПервоначальнаяСтоимостьУУ
	|
	|ИЗ
	|	ДанныеДокументаТаблицаКомпоненты КАК ТаблицаОС
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПервоначальныеСведенияКомпонент КАК ВтПервоначальныеСведенияКомпонент
	|		ПО ВтПервоначальныеСведенияКомпонент.Ссылка = ТаблицаОС.Ссылка
	|			И ВтПервоначальныеСведенияКомпонент.ОсновноеСредство = ТаблицаОС.КомпонентАмортизации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПервоначальныеСведенияОС КАК ПервоначальныеСведенияОСВладельца
	|		ПО ПервоначальныеСведенияОСВладельца.Ссылка = ТаблицаОС.Ссылка
	|			И ПервоначальныеСведенияОСВладельца.ОсновноеСредство = ТаблицаОС.ОсновноеСредство
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимостиСгруппированная КАК РасчетСтоимости
	|		ПО РасчетСтоимости.Ссылка = ТаблицаОС.Ссылка
	|			И РасчетСтоимости.ОбъектУчета = ТаблицаОС.КомпонентАмортизации
	|
	|ГДЕ
	|	(ДанныеДокумента.ОтражатьВРеглУчете
	|			И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВУпрУчете
	|			И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы))
	|";

	//-- Локализация
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСтоимостьОС(ТекстыЗапроса) Экспорт

	ТекстЗапроса = Неопределено;
	
	//++ Локализация
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)       КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                       КАК Регистратор,
	|	ДанныеДокумента.Период                       КАК Период,
	|	ДанныеДокумента.Организация                  КАК Организация,
	|	ВтСведенияВладельцев.Местонахождение         КАК Подразделение,
	|	ДанныеДокумента.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
	|	ВтСведенияВладельцев.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаОС.КомпонентАмортизации               КАК ОсновноеСредство,
	|
	|	ДанныеДокумента.ХозяйственнаяОперация        КАК ХозяйственнаяОперация,
	|
	|	ДанныеДокумента.ОтражатьВРеглУчете           КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете            КАК ОтражатьВУпрУчете,
	|	ВтСведенияВладельцев.НалогообложениеНДС      КАК НалогообложениеНДС,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыАналитикиКапитализацииРасходов.ОсновныеСредства) КАК ВидАналитикиКапитализацииРасходов,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаОС.КомпонентАмортизации.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.Узел)
	|				ИЛИ (ТаблицаОС.КомпонентАмортизации.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.КомпонентАмортизации)
	|					И ТаблицаОС.КомпонентАмортизации.УчитыватьСтоимостьЛиквидационногоОбязательства)
	|			ТОГДА ТаблицаОС.КомпонентАмортизации
	|		ИНАЧЕ ТаблицаОС.ОсновноеСредство
	|	КОНЕЦ КАК АналитикаКапитализацииРасходов,
	|
	|	ЕСТЬNULL(ВтРасчетСтоимости.Подразделение, НЕОПРЕДЕЛЕНО)                КАК КорПодразделение,
	|	ЕСТЬNULL(ВтРасчетСтоимости.НаправлениеДеятельности, НЕОПРЕДЕЛЕНО)      КАК КорНаправлениеДеятельности,
	|	ЕСТЬNULL(ВтРасчетСтоимости.СтатьяРасходов, НЕОПРЕДЕЛЕНО)               КАК КорСтатьяРасходов,
	|	ЕСТЬNULL(ВтРасчетСтоимости.АналитикаРасходов, НЕОПРЕДЕЛЕНО)            КАК КорАналитикаРасходов,
	|	НЕ ВтРасчетСтоимости.ОбъектУчета ЕСТЬ NULL                             КАК РасчетСтоимости,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВУпрУчете
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВтРасчетСтоимости.СуммаУУ, ТаблицаОС.СтоимостьУУ)
	|	КОНЕЦ КАК Стоимость,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВтРасчетСтоимости.СуммаБУ, ТаблицаОС.СтоимостьБУ)
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	0 КАК СтоимостьНУ,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВтРасчетСтоимости.СуммаПР, 0)
	|	КОНЕЦ КАК СтоимостьПР,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВтРасчетСтоимости.СуммаБУ, ТаблицаОС.СтоимостьБУ)
	|	КОНЕЦ
	|		- ВЫБОР
	|			КОГДА НЕ ДанныеДокумента.ОтражатьВРеглУчете
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(ВтРасчетСтоимости.СуммаПР, 0)
	|		КОНЕЦ КАК СтоимостьВР,
	|
	|	0 КАК АмортизационнаяПремия,
	|
	|	0 КАК ПредварительнаяСтоимость,
	|	0 КАК ПредварительнаяСтоимостьРегл,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВтРасчетСтоимости.СуммаПР, 0)
	|	КОНЕЦ КАК КорПостояннаяРазница,
	|
	|	ВЫБОР
	|		КОГДА НЕ ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВтРасчетСтоимости.СуммаБУ, ТаблицаОС.СтоимостьБУ)
	|	КОНЕЦ 
	|		- ВЫБОР
	|			КОГДА НЕ ДанныеДокумента.ОтражатьВРеглУчете
	|				ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(ВтРасчетСтоимости.СуммаПР, 0)
	|		КОНЕЦ КАК КорВременнаяРазница,
	|
	|	ВтРасчетСтоимости.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаКомпоненты КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтРасчетСтоимости КАК ВтРасчетСтоимости
	|		ПО ВтРасчетСтоимости.Ссылка = ТаблицаОС.Ссылка
	|			И ВтРасчетСтоимости.ОбъектУчета = ТаблицаОС.КомпонентАмортизации
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСведенияВладельцев КАК ВтСведенияВладельцев
	|		ПО ВтСведенияВладельцев.Ссылка = ТаблицаОС.Ссылка
	|			И ВтСведенияВладельцев.ИдентификаторСтроки = ТаблицаОС.ИдентификаторСтроки
	|
	|ГДЕ
	|	(ДанныеДокумента.ОтражатьВУпрУчете
	|		ИЛИ ДанныеДокумента.ОтражатьВРеглУчете)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Нужно отдельно записать предварительную стоимость для расчета амортизации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)          КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                          КАК Регистратор,
	|	ДанныеДокумента.Период                          КАК Период,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ВтСведенияВладельцев.Местонахождение            КАК Подразделение,
	|	ДанныеДокумента.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ВтСведенияВладельцев.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	ТаблицаОС.КомпонентАмортизации                  КАК ОсновноеСредство,
	|
	|	ДанныеДокумента.ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	ВтСведенияВладельцев.НалогообложениеНДС         КАК НалогообложениеНДС,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыАналитикиКапитализацииРасходов.ОсновныеСредства) КАК ВидАналитикиКапитализацииРасходов,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаОС.КомпонентАмортизации.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.Узел)
	|				ИЛИ (ТаблицаОС.КомпонентАмортизации.ТипОС = ЗНАЧЕНИЕ(Перечисление.ТипыОС.КомпонентАмортизации)
	|					И ТаблицаОС.КомпонентАмортизации.УчитыватьСтоимостьЛиквидационногоОбязательства)
	|			ТОГДА ТаблицаОС.КомпонентАмортизации
	|		ИНАЧЕ ТаблицаОС.ОсновноеСредство
	|	КОНЕЦ КАК АналитикаКапитализацииРасходов,
	|
	|	НЕОПРЕДЕЛЕНО                                 КАК КорПодразделение,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорНаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                                 КАК КорАналитикаРасходов,
	|	ЛОЖЬ                                         КАК РасчетСтоимости,
	|
	|	0                                            КАК Стоимость,
	|	0                                            КАК СтоимостьРегл,
	|	0                                            КАК СтоимостьНУ,
	|	0                                            КАК СтоимостьПР,
	|	0                                            КАК СтоимостьВР,
	|
	|	0                                            КАК АмортизационнаяПремия,
	|
	|	ТаблицаОС.СтоимостьУУ                        КАК ПредварительнаяСтоимость,
	|	ТаблицаОС.СтоимостьБУ                        КАК ПредварительнаяСтоимостьРегл,
	|
	|	0                                            КАК КорПостояннаяРазница,
	|	0                                            КАК КорВременнаяРазница,
	|	&ИдентификаторНеиспользуемойФинЗаписи        КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО                                 КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаКомпоненты КАК ТаблицаОС
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОС.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСведенияВладельцев КАК ВтСведенияВладельцев
	|		ПО ВтСведенияВладельцев.Ссылка = ТаблицаОС.Ссылка
	|			И ВтСведенияВладельцев.ИдентификаторСтроки = ТаблицаОС.ИдентификаторСтроки
	|
	|ГДЕ
	|	(ДанныеДокумента.ОтражатьВУпрУчете
	|			И ДанныеДокумента.ПорядокУчетаУУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НеПриниматьКУчетуВключитьВРасходы)
	|		ИЛИ ДанныеДокумента.ОтражатьВРеглУчете
	|			И ДанныеДокумента.ПорядокУчетаБУ <> ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НеПриниматьКУчетуВключитьВРасходы))
	|";
	
	//-- Локализация
	
	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти

#Область Прочее

// 
// Параметры:
//  Объект - ДокументОбъект.ПринятиеКУчетуУзловКомпонентовАмортизации - 
//  ВспомогательныеРеквизиты - Структура - 
Процедура ДополнитьВспомогательныеРеквизиты(Объект, ВспомогательныеРеквизиты) Экспорт
	
	//++ Локализация
	
	ВспомогательныеРеквизиты.Вставить(
		"ПрименяетсяФСБУ6",
		НастройкиНалоговУчетныхПолитикПовтИсп.ПрименяетсяФСБУ6(Объект.Организация, Объект.Дата));
	
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
