
#Область ПрограммныйИнтерфейс

Процедура УдалитьАрхивныеВложения(Письмо) Экспорт
	
	НаборВложений = РегистрыСведений.ВложенияНеформализованныхДокументов.СоздатьНаборЗаписей();
	НаборВложений.Отбор["НеформализованныйДокумент"].Установить(Письмо);
	НаборВложений.Прочитать();
	
	Набор = РегистрыСведений.ПризнакиАрхивированияФайловДОСКонтролирующимиОрганами.СоздатьНаборЗаписей();
	Владелец = Перечисления.ВидыАрхивируемыхМетаданныхДО.ВложенияНеформализованныхДокументов;
	Набор.Отбор["Объект"].Установить(Письмо);
	Набор.Отбор["Владелец"].Установить(Владелец);
	
	Набор.Прочитать();
	Если Набор.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
		
	ЗаписиКУдалению = Новый Массив;
	Для Каждого Запись Из Набор Цикл 
		Если Запись.Архивный Тогда 
			ЗаписиКУдалению.Добавить(Запись);
		КонецЕсли;
	КонецЦикла;
	
	Если ЗаписиКУдалению.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ЗаписатьНаборВложений = Ложь;
	Для Каждого Запись Из ЗаписиКУдалению Цикл 
		Для Каждого ЗаписьВложение Из НаборВложений Цикл 
			Если ЗаписьВложение.ИмяФайла = Запись.ИмяФайла Тогда 
				ЗаписатьНаборВложений = Истина;
				НаборВложений.Удалить(ЗаписьВложение);
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Набор.Удалить(Запись);
	КонецЦикла;

	Если ЗаписатьНаборВложений Тогда 
		НаборВложений.Записать(Истина);	
	КонецЕсли;
	
	Набор.Записать(Истина);	
		
КонецПроцедуры

Процедура ПроверитьВложения(Письмо, Всего, ВАрхиве) Экспорт
	
	КонтекстМодуля = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ВложенияОснования = КонтекстМодуля.ПолучитьВложенияНеформализованногоДокумента(Письмо,, Ложь);
	
	Всего = 0;
	ВАрхиве = 0;
	
	Для Каждого Вложение Из ВложенияОснования Цикл 
		ВАрхиве = ВАрхиве + ?(Вложение.ВАрхиве, 1, 0);
		Всего = Всего + 1;
	КонецЦикла;
	
КонецПроцедуры

Функция ПланируемыйМассивПисем(Письмо, Тип = Неопределено) Экспорт
	
	Если Тип = Неопределено Тогда
		Тип = Письмо.Тип;
	КонецЕсли;
	
	Результат = Новый Структура();
	Результат.Вставить("Допустимо", Истина);
	Результат.Вставить("НаборыФайлов", Новый Массив);
	Результат.Вставить("ПревышениеКоличества", 0);
	Результат.Вставить("ПревышениеРазмера", 0);
	Результат.Вставить("КоличествоПисем", 0);
	Результат.Вставить("КоличествоФайлов", 0);
	Результат.Вставить("МаксКоличество", 0);
	Результат.Вставить("МаксКонтейнера", 0);
	Результат.Вставить("МаксФайла", 0);
	Результат.Вставить("МаксимальныйРазмер", 0);
	
	ОдинКилобайт = 1024;
	ОдинМегабайт = Справочники.ПерепискаСКонтролирующимиОрганами.ОдинМегабайт();
	Ограничения = Справочники.ПерепискаСКонтролирующимиОрганами.МаксимальныйРазмерВложений(Тип);
	
	Результат.Вставить("МаксКоличество", Ограничения.КоличествоФайлов);
	Результат.Вставить("МаксКонтейнера", Окр(Ограничения.РазмерКонтейнера / ОдинМегабайт, 2));
	Результат.Вставить("МаксФайла", Окр(Ограничения.РазмерФайла / ОдинМегабайт, 2));
	
	КонтекстЭДОСервер = ДокументооборотСКО.ПолучитьОбработкуЭДО();
	ТаблицаВложений = КонтекстЭДОСервер.ПолучитьВложенияНеформализованногоДокумента(Письмо);
	
	ТаблицаВложений.Сортировать("Размер УБЫВ");
	ВсегоФайлов = ТаблицаВложений.Количество();
	Результат.КоличествоФайлов = ВсегоФайлов;
	
	МаксимальныйРазмер = 0;
	ВсегоРазмер = ТаблицаВложений.Итог("Размер");
	Если ТаблицаВложений.Количество() > 0 Тогда
		МаксимальныйРазмер = ТаблицаВложений[0].Размер;
	КонецЕсли;
	ВсегоРазмер = ВсегоРазмер + 2 * ОдинКилобайт * (2 + ВсегоФайлов); 
	
	Если Ограничения.РазмерФайла < МаксимальныйРазмер Тогда
		Результат.Допустимо = Ложь;
		Результат.ПревышениеРазмера = Окр(Ограничения.РазмерФайла / ОдинМегабайт, 2);
		Результат.МаксимальныйРазмер = МаксимальныйРазмер;
		Возврат Результат;
	ИначеЕсли Ограничения.РазмерКонтейнера >= ВсегоРазмер И Ограничения.КоличествоФайлов >= ВсегоФайлов Тогда
		Результат.КоличествоПисем = 1;
		Возврат Результат;
	КонецЕсли;
	
	ТаблицаФайлов = ОбщегоНазначенияЭДКО.ПрикрепленныеФайлыКОбъектуИзСправочника(
		Письмо, 
		"ПерепискаСКонтролирующимиОрганамиПрисоединенныеФайлы",
		,
		"ИсходноеИмяФайла");
	
	Для каждого СтрокаТаблицы Из ТаблицаФайлов Цикл
		СтрокаТаблицы.ИсходноеИмяФайла = ВРЕГ(СтрокаТаблицы.ИсходноеИмяФайла);
	КонецЦикла;
	
	ТаблицаВложений.Колонки.Добавить("Ссылка");
	Для каждого СтрокаРезультата Из ТаблицаВложений Цикл
		ИсходноеИмяФайла = ВРЕГ(СтрокаРезультата.ИмяФайла);
		СтрокаТаблицы = ТаблицаФайлов.Найти(ВРЕГ(СтрокаРезультата.ИмяФайла), "ИсходноеИмяФайла");
		Если СтрокаТаблицы <> Неопределено Тогда
			СтрокаРезультата.Ссылка = СтрокаТаблицы.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
	Результат.ПревышениеКоличества = Макс(0, ВсегоФайлов - Ограничения.КоличествоФайлов);
	Результат.ПревышениеРазмера = Макс(0, ВсегоРазмер - Ограничения.РазмерКонтейнера);
	Результат.ПревышениеРазмера = Окр(Результат.ПревышениеРазмера / ОдинМегабайт, 2);
	
	ТаблицаВложений.Сортировать("ИмяФайла");
	
	СчетчикРазмера = 4 * ОдинКилобайт;
	СчетчикКоличества = 0;
	СписокФайлов = Новый Массив;
	
	Для Счетчик = 1 По ВсегоФайлов Цикл
		
		ТекущийФайл = ТаблицаВложений[Счетчик - 1];
		
		НовыйФайл = Новый Структура("Ссылка, ИмяФайла", ТекущийФайл.Ссылка, ТекущийФайл.ИмяФайла);
		
		СчетчикКоличества = СчетчикКоличества + 1;
		СчетчикРазмера = СчетчикРазмера
			+ ТекущийФайл.Размер // текущий размер файла, в zip не сжимается, но шифруется
			+ 1 * ОдинКилобайт // строка описания в description.xml
			+ 3 * ОдинКилобайт; // будующая подпись + заголовок в zip
		
		Если СчетчикКоличества >= Ограничения.КоличествоФайлов
			ИЛИ СчетчикРазмера >= Ограничения.РазмерКонтейнера Тогда
			Результат.НаборыФайлов.Добавить(СписокФайлов);
			СписокФайлов = Новый Массив;
			СчетчикРазмера = 6 * ОдинКилобайт + ТекущийФайл.Размер;
			СчетчикКоличества = 1;
		КонецЕсли;
		
		СписокФайлов.Добавить(НовыйФайл);
		
	КонецЦикла;
	
	Если СписокФайлов.Количество() > 0 Тогда
		Результат.НаборыФайлов.Добавить(СписокФайлов);
	КонецЕсли;
	
	Результат.КоличествоПисем = Результат.НаборыФайлов.Количество();
		
	Возврат Результат;
	
КонецФункции

Функция ОтветНаТребованиеОтправляютКакПисьмо(Письмо) Экспорт
	
	Результат = Письмо.Тип = ПредопределенноеЗначение("Перечисление.ТипыПерепискиСКонтролирующимиОрганами.ПерепискаСФНС")
		И СтрНайти(ВРег(Письмо.Наименование), ВРег(НСтр("ru = 'ответ';
														|en = 'ответ'")))
		И СтрНайти(ВРег(Письмо.Наименование), ВРег(НСтр("ru = 'Требован';
														|en = 'Требован'")));
		
	Возврат Результат;
		
КонецФункции

Функция РазделениеПисемСозданиеНаСервере(Письмо, АнализПисем, ТекущийСчетчик, КоличествоСвязанныхПисем) Экспорт
	
	АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
	
	ПараметрыВызова = Новый Структура;
	ПараметрыВызова.Вставить("Индекс", ТекущийСчетчик + КоличествоСвязанныхПисем);
	ПараметрыВызова.Вставить("ФайлыПисьма", АнализПисем.НаборыФайлов[ТекущийСчетчик - 1]);
	ПараметрыВызова.Вставить("ТекущееПисьмо", Письмо);
	ПараметрыВызова.Вставить("ВсегоПисем", АнализПисем.КоличествоПисем + КоличествоСвязанныхПисем);
	ПараметрыВызова.Вставить("ИсходноеПисьмо", ТекущийСчетчик = 1);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Новый УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Вызов функции разбиения писем с большими вложениями';
															|en = 'Вызов функции разбиения писем с большими вложениями'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ДополнительныйРезультат = Ложь;
	
	Результат = ДлительныеОперации.ВыполнитьВФоне(
					"Справочники.ПерепискаСКонтролирующимиОрганами.РазделитьПисьма", 
					ПараметрыВызова, 
					ПараметрыВыполнения);
					
	Возврат Результат;
	
КонецФункции

Функция КоличествоСвязанныхПисем(Письмо) Экспорт
	
	СвязанныеПисьма = Справочники.ПерепискаСКонтролирующимиОрганами.ОбъектыГрупповойОтправки(Письмо);
	Возврат СвязанныеПисьма.Количество();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СобытиеНаступилоПисьмаФНС1Мб() Экспорт
	
	Возврат Истина;
	
КонецФункции

Функция СсылкаНаЛичныйКабинетНалогоплательщикаПоВидуОрганизации(Организация) Экспорт
	
	Результат = Неопределено;
	
	Если ТипЗнч(Организация) = Тип("СправочникСсылка.Организации") Тогда
		
		ЭтоЮридическоеЛицо = РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(Организация);
		
		Если ЭтоЮридическоеЛицо Тогда
			Результат = "https://lkul.nalog.ru/";
		Иначе
			Результат = "https://lkip2.nalog.ru";
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РеквизитыПерепискиПриОтправке(Письмо) Экспорт
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Письмо, "Тип, Отправитель");
	
КонецФункции

#КонецОбласти