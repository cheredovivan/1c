///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ИнтернетЭквайринг".
// ОбщийМодуль.ИнтернетЭквайрингКлиент.
// 
// Клиентские служебные процедуры работы с интернет-эквайрингом:
//  - создание новой настройки;
//  - обработка выбора настройки в форме списка;
//  - проверка заполнения реквизитов форм, создаваемых программно;
//  - открытие формы настройки шаблонов назначения платежа и журнала регистрации.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Открывает форму настройки подключения к Интернет-эквайрингу.
// 
// Параметры:
//  ПараметрыПодключения - Структура - настройки открытия формы подключения:
//    * БИК - Строка, Неопределено - идентификатор банка. Используется для автоматического
//      выбора в мастере подключения.
//    * НастройкаДляКопирования - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка, будет создана
//                                  копированием существующей.
//                              - Неопределено - будет создана с нуля.
//    * ДополнительныеПараметры - Неопределено, Произвольный - любые дополнительные параметры, которые могут
//      использоваться в переопределяемом методе
//      ИнтернетЭквайрингПереопределяемый.ПриНастройкеЭлементовФормыПодключения.
//  ОписаниеОповещения - ОписаниеОповещения, Неопределено - оповещение, которое
//    необходимо вызвать после завершения настройки подключения. В случае успешного
//    завершения настройки подключения в результате оповещения будет возвращено Истина.
//
Процедура ПодключитьИнтернетЭквайрингСлужебный(
		ПараметрыПодключения,
		ОписаниеОповещения = Неопределено) Экспорт

	ОткрытьФорму(
		"Обработка.ПодключениеКИнтернетЭквайрингу.Форма",
		ПараметрыПодключения,
		,
		,
		,
		,
		ОписаниеОповещения);
	
КонецПроцедуры

// Открывает мастер создания новой настройки на основании существующей.
// 
// Параметры:
//  ДанныеСтроки - ДанныеФормыЭлементКоллекции - данные выделенной строки.
//  ОписаниеОповещения - ОписаниеОповещения, Неопределено - оповещение, которое
//    необходимо вызвать после завершения настройки подключения.
//
Процедура СкопироватьНастройку(ДанныеСтроки, ОписаниеОповещения) Экспорт
	
	Если ДанныеСтроки.ЭтоГруппа Тогда
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru = 'Создание групп запрещено.';
				|en = 'Cannot create groups.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыПодключения = НастройкиПлатежныхСистемКлиент.НовыйПараметрыПодключения();
	ПараметрыПодключения.Вставить("НастройкаДляКопирования", ДанныеСтроки.Ссылка);
	
	ПодключитьИнтернетЭквайрингСлужебный(
		ПараметрыПодключения,
		ОписаниеОповещения);
	
КонецПроцедуры

// Выполняет обработку выбора строки в общем списке настроек
// 
// Параметры:
//  ПараметрыВыбора - см. НастройкиПлатежныхСистемКлиент.НовыйПараметрыОбработкиВыбораНастройки
// 
Процедура ПриОбработкеВыбораНастройки(ПараметрыВыбора) Экспорт

	ПараметрыВыбора.ИмяОткрываемойФормы = "Справочник.НастройкиПодключенияКИнтернетЭквайрингу.ФормаОбъекта";

	Если ПараметрыВыбора.РежимВыбора Тогда
		
		ЭтоГруппа = ПараметрыВыбора.ТекущиеДанные.ЭтоГруппа;
		
		ТекстОшибки = "";
		
		Если ЭтоГруппа И ПараметрыВыбора.ТолькоЭлементы Тогда
			ТекстОшибки = НСтр("ru = 'Выбор групп запрещен.';
								|en = 'Cannot select groups.'");
		ИначеЕсли Не ЭтоГруппа И ПараметрыВыбора.ТолькоГруппы Тогда
			ТекстОшибки = НСтр("ru = 'Разрешено выбрать только группу.';
								|en = 'You can select only groups.'");
		КонецЕсли;
		
		Если Не ПустаяСтрока(ТекстОшибки) Тогда
			ПараметрыВыбора.Отказ = Истина;
			ПараметрыВыбора.СообщениеОбОшибке = ТекстОшибки;
		КонецЕсли;
		
	Иначе
		
		Если ПараметрыВыбора.ТекущиеДанные.ЭтоГруппа Тогда
			ПараметрыВыбора.Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет обработку выбранного значения в общем списке настроек при нажатии кнопки контекстного меню "Изменить".
// 
// Параметры:
//  ПараметрыВыбора - см. НастройкиПлатежныхСистемКлиент.НовыйПараметрыОбработкиВыбораНастройкиДляИзменения
// 
Процедура ПриОбработкеВыбораНастройкиДляИзменения(ПараметрыВыбора) Экспорт
	
	Если ПараметрыВыбора.ТекущиеДанные.ЭтоГруппа Тогда
		
		ПараметрыВыбора.Отказ = Истина;
		ПараметрыВыбора.СообщениеОбОшибке =
			НСтр("ru = 'Изменение групп запрещено.';
				|en = 'Cannot edit groups.'");
		
	Иначе
		
		ПараметрыВыбора.ИмяФормы = "Справочник.НастройкиПодключенияКИнтернетЭквайрингу.ФормаОбъекта";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет проверку заполненности всех полей аутентификации, которые были программно созданы на форме.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма на которой необходимо проверить введенные данные. На формы должен быть
//    реквизит ДинамическиеРеквизиты.
//  ТипПолей - Строка - тип полей, которые необходимо проверить. Одно из следующих значений:
//    -- "НастройкиАутентификации" - поля для ввода параметров подключения с сервисам банка.
//    -- "ПрикладныеНастройки" - поля для ввода прикладных настроек.
// 
// Возвращаемое значение:
//  Булево - Истина, если заполнены все поля. Ложь в противном случае. Если поля не созданы, то возвращается Ложь.
//    Если в параметре ТипПоля передано недопустимое значение, то возвращается Ложь.
//
Функция ДинамическиеРеквизитыЗаполнены(Форма, ТипПолей) Экспорт
	
	РезультатПроверки = ИнтернетЭквайрингКлиентСервер.ДинамическиеРеквизитыЗаполнены(
		Форма,
		ТипПолей);
	
	Если Не РезультатПроверки.ВсеЗаполнены Тогда
		
		Если ЗначениеЗаполнено(РезультатПроверки.СообщениеОбОшибке) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				РезультатПроверки.СообщениеОбОшибке);
		КонецЕсли;
		
		Для каждого ОписаниеПоля Из РезультатПроверки.НезаполненныеПоля Цикл
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Поле ""%1"" не заполнено';
						|en = 'Field %1 is required'"),
					ОписаниеПоля.Представление),
				,
				ОписаниеПоля.ИмяРеквизита);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат РезультатПроверки.ВсеЗаполнены;
	
КонецФункции

// Определяет какой текст сообщения должен использоваться в зависимости от прав текущего пользователя.
// 
// Параметры:
//  ТекстОбычныйПользователь - Строка - Текст сообщения, который необходимо вывести, если у пользователя нет полных
//    прав.
//  ТекстАдминистратор - Строка - Текст Сообщения, который необходимо вывести, если у пользователя полные права.
//
// Возвращаемое значение:
//  Строка - текст сообщения.
//
Функция ТекстСообщенияПоПравамПользователя(
		ТекстОбычныйПользователь,
		ТекстАдминистратор) Экспорт
		
	Если ПользователиКлиент.ЭтоПолноправныйПользователь(Истина) Тогда
		Возврат ТекстАдминистратор;
	Иначе
		Возврат ТекстОбычныйПользователь;
	КонецЕсли;
	
КонецФункции

// Открывает форму журнала регистрации с отбором
// по событию см. ИмяСобытияЖурналаРегистрации.
//
Процедура ОткрытьЖурналРегистрации() Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("СобытиеЖурналаРегистрации", ИмяСобытияЖурналаРегистрации());
	ЖурналРегистрацииКлиент.ОткрытьЖурналРегистрации(Отбор);
	
КонецПроцедуры

// Возвращает имя события для журнала регистрации, которое используется
// для записи событий загрузки данных из внешних систем.
//
// Возвращаемое значение:
//  Строка - имя события.
//
Функция ИмяСобытияЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Интернет-эквайринг';
				|en = 'Online acquiring'",
		ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	
КонецФункции

// Описание дополнительных параметров.
// 
// Параметры:
//  РезультатОперации - Структура - результат операции получения токена:
//    * ТребуемыеНастройки - Массив Из Структура - требуемые настройки.
//    * ДополнительныеПараметры - Соответствие Из КлючИЗначение - дополнительные настройки, установка значения для
//      которых не требуется.
//  ТекущиеЗначения - Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации - текущее
//      значение дополнительных настроек.
// 
// Возвращаемое значение:
//  Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации.
//
Функция ОписаниеДополнительныхПараметров(РезультатОперации, ТекущиеЗначения) Экспорт
	
	Результат = Новый Массив;
	
	Для Каждого ОписаниеНастройки Из РезультатОперации.ТребуемыеНастройки Цикл
		ОписаниеПараметра = ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации();
		ОписаниеПараметра.Идентификатор     = ОписаниеНастройки.Идентификатор;
		ОписаниеПараметра.Представление     = ОписаниеНастройки.Представление;
		ОписаниеПараметра.ДоступныеЗначения = ОписаниеНастройки.Значения;
		ОписаниеПараметра.ТребуемыйПараметр = Истина;
		Результат.Добавить(ОписаниеПараметра);
	КонецЦикла;
	
	Для Каждого КлючЗначение Из РезультатОперации.ДополнительныеПараметры Цикл
		ОписаниеПараметра = ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации();
		ОписаниеПараметра.Идентификатор         = КлючЗначение.Ключ;
		ОписаниеПараметра.Представление         = КлючЗначение.Ключ;
		ОписаниеПараметра.Значение              = КлючЗначение.Значение;
		ОписаниеПараметра.ПредставлениеЗначения = КлючЗначение.Значение;
		Для Каждого ТекущееОписание Из ТекущиеЗначения Цикл
			Если ТекущееОписание.Идентификатор = КлючЗначение.Ключ Тогда
				ЗаполнитьЗначенияСвойств(ОписаниеПараметра, ТекущееОписание, "Представление");
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Результат.Добавить(ОписаниеПараметра);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти