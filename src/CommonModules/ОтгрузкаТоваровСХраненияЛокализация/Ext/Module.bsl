
#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	МеханизмыДокумента.Добавить("РегламентированныйУчет");
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый документ.
//  Отказ - Булево - Признак проведения документа.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то проведение документа выполнено не будет.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ОбработкаПроведения(Объект, Отказ, РежимПроведения) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект.
//  ДанныеЗаполнения - Произвольный - Значение, которое используется как основание для заполнения.
//  СтандартнаяОбработка - Булево - В данный параметр передается признак выполнения стандартной (системной) обработки события.
//
Процедура ОбработкаЗаполнения(Объект, ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	//++ Локализация

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.ИсходящаяТранспортнаяОперацияВЕТИС") Тогда
		
		ИнтеграцияВЕТИСУТ.ЗаполнитьОтгрузкуТоваровСХраненияНаОснованииИсходящейТранспортнойОперацииВЕТИС(
			Объект, 
			ДанныеЗаполнения);
			
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ОформлениеСДИЗЗЕРНО") Тогда
		
		ИнтеграцияЗЕРНОУТ.ЗаполнитьОтгрузкуТоваровСХраненияНаОснованииОформленияСДИЗЗЕРНО(
			Объект, 
			ДанныеЗаполнения);
		
	КонецЕсли;
	
	ОбменСКонтрагентамиУТ.ОбновитьКодСпециальныхОбстоятельств(Объект);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")
		И ДанныеЗаполнения.Свойство("АктОРасхождениях")
		И ДанныеЗаполнения.Свойство("ОснованиеАкта") Тогда
		
		ИнтеграцияИСУТ.ЗаполнитьШтрихкодыУпаковокПоАктуОРасхождениях(Объект, ДанныеЗаполнения.АктОРасхождениях);
		
	КонецЕсли;
	
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//
Процедура ОбработкаУдаленияПроведения(Объект, Отказ) Экспорт
	
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то запись выполнена не будет и будет вызвано исключение.
//  РежимЗаписи - РежимЗаписиДокумента - В параметр передается текущий режим записи документа. Позволяет определить в теле процедуры режим записи.
//  РежимПроведения - РежимПроведенияДокумента - В данный параметр передается текущий режим проведения.
//
Процедура ПередЗаписью(Объект, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	//++ Локализация
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		УчетПрослеживаемыхТоваровЛокализация.ПроверитьДанныеПрослеживаемостиНомеровГТД(Объект, Объект.ВидыЗапасов, Объект.Дата, "ВидыЗапасов");
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Признак отказа от записи.
//                   Если в теле процедуры-обработчика установить данному параметру значение Истина, то запись выполнена не будет и будет вызвано исключение.
//
Процедура ПриЗаписи(Объект, Отказ) Экспорт
	
	//++ Локализация
	Документы.ТранспортнаяНакладная.ОбновитьРеквизитыТранспортныхНакладныхПриЗаписиДокументаОснования(Объект, Отказ);
	//-- Локализация
	
	Возврат;
	
КонецПроцедуры

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  ОбъектКопирования - ДокументОбъект - Исходный документ, который является источником копирования.
//
Процедура ПриКопировании(Объект, ОбъектКопирования) Экспорт
	
//++ Локализация

	Объект.ШтрихкодыУпаковок.Очистить();

//-- Локализация

	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	//++ Локализация
	ОбменСГИСЭПДПереопределяемый.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	//-- Локализация
	
КонецПроцедуры

// Добавляет команду создания документа "Авансовый отчет".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
Процедура ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт


КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	//++ Локализация
	
	// МХ-3
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати                 = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор                  = "МХ3";
	КомандаПечати.Представление                  = НСтр("ru = 'Акт о возврате ТМЦ, сданных на хранение (МХ-3)';
														|en = 'Certificate of stored inventory return (MH-3)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	//++ НЕ УТКА
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(
		КомандаПечати,
		"ХозяйственнаяОперация",
		Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5,
		ВидСравнения.НеРавно);
		
		
	// Накладная на передачу готовой продукции (МХ-18)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "МХ18";
	КомандаПечати.Представление = НСтр("ru = 'Накладная на передачу готовой продукции (МХ-18)';
										|en = 'Invoice for finished product transfer (MH-18)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(
		КомандаПечати,
		"ХозяйственнаяОперация",
		Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5,
		ВидСравнения.Равно);
		
		
	// Товарная накладная (ТОРГ-12)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "ТОРГ12";
	КомандаПечати.Представление = НСтр("ru = 'Товарная накладная (ТОРГ-12)';
										|en = 'Invoice (TORG-12)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("ВыводитьУслуги", Ложь);
	КомандаПечати.Порядок = 17;
		
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(
		КомандаПечати,
		"ХозяйственнаяОперация",
		Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5,
		ВидСравнения.Равно);
		
		
	СписокОпераций = Новый СписокЗначений;
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5);
	СписокОпераций.Добавить(Перечисления.ХозяйственныеОперации.ВозвратДавальцу2_5);
		
	// Накладная (М-15)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "М15";
	КомандаПечати.Представление = НСтр("ru = 'Накладная (М-15)';
										|en = 'Invoice (M-15)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Порядок = 25;
		
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(
		КомандаПечати,
		"ХозяйственнаяОперация",
		СписокОпераций,
		ВидСравнения.ВСписке);
	//-- НЕ УТКА
	
	Документы.ТранспортнаяНакладная.ДобавитьКомандыПечати(КомандыПечати);
	
	// Универсальный передаточный документ (УПД)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "УПД";
	КомандаПечати.Представление = НСтр("ru = 'Универсальный передаточный документ (УПД)';
										|en = 'Universal transfer document (UTD)'");
	КомандаПечати.Порядок = 40;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнительныеПараметры.Вставить("Выданный", Ложь);
	
	// Универсальный корректировочный документ (УКД)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьОбщихФорм";
	КомандаПечати.Идентификатор = "УКД";
	КомандаПечати.Представление = НСтр("ru = 'Универсальный корректировочный документ (УКД)';
										|en = 'Universal adjustment document (UAD)'");
	КомандаПечати.Порядок = 42;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	УправлениеПечатью.ДобавитьУсловиеВидимостиКоманды(
		КомандаПечати,
		"Исправление",
		Истина,
		ВидСравнения.Равно);
		
	//-- Локализация

КонецПроцедуры

#КонецОбласти

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	
КонецПроцедуры

Процедура КомплектПечатныхФорм(КомплектПечатныхФорм) Экспорт

	//++ Локализация
	
	РегистрыСведений.НастройкиПечатиОбъектов.ДобавитьПечатнуюФормуВКомплект(КомплектПечатныхФорм,
																			"МХ3",
																			НСтр("ru = 'Акт о возврате ТМЦ, сданных на хранение (МХ-3)';
																				|en = 'Certificate of stored inventory return (MH-3)'"),
																			1);
	//-- Локализация
	
КонецПроцедуры

Процедура СформироватьКомплектПечатныхФорм(МассивОбъектов,
										ПараметрыПечати,
										КоллекцияПечатныхФорм,
										ОбъектыПечати,
										КомплектПечатныхФорм) Экспорт
	
	//++ Локализация
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(КоллекцияПечатныхФорм,
																									КомплектПечатныхФорм,
																									МассивОбъектов,
																									"МХ3");
	
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтгрузкаТоваровСХранения", КомплектПечати.Объекты);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуМХ3(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;
	
	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(
		КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"УПД");
		
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтгрузкаТоваровСХранения", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуУПД(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;

	КомплектыПечатиПоОбъектам = РегистрыСведений.НастройкиПечатиОбъектов.КомплектыПечатиПоОбъектам(
		КоллекцияПечатныхФорм,
		КомплектПечатныхФорм,
		МассивОбъектов,
		"УКД");
		
	Для Каждого КомплектПечати Из КомплектыПечатиПоОбъектам Цикл
		СтруктураТипов = Новый Соответствие;
		СтруктураТипов.Вставить("Документ.ОтгрузкаТоваровСХранения", КомплектПечати.Объекты);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			КомплектПечати.Имя,
			КомплектПечати.Представление,
			Обработки.ПечатьОбщихФорм.СформироватьПечатнуюФормуУКД(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
	КонецЦикла;
	
		
	//-- Локализация
	
КонецПроцедуры

//++ Локализация

// Возвращает данные для формирования печатной формы МХ - 3.
//
// Параметры:
//	ПараметрыПечати	- Структура -дополнительные настройки печати.
//	МассивОбъектов	- Массив из ДокументСсылка.ОтгрузкаТоваровСХранения - коллекция значений ссылок на документы,
//																			по которым необходимо получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемых для печати, содержащая следующие следующие свойства:
//		* РезультатПоШапке			- РезультатЗапроса - данные шапки документа.
//		* РезультатПоСкладам		- РезультатЗапроса - данные о складе ответственного хранения.
//		* РезультатПоТабличнойЧасти	- РезультатЗапроса - данные табличной части документа.
//		* РезультатПоОшибкам		- Неорпеделено - данные об ошибках, возникающих при печати документа.
//
Функция ПолучитьДанныеДляПечатнойФормыМХ3(ПараметрыПечати, МассивОбъектов) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка               КАК Ссылка,
	|	ДанныеДокументов.ИсправляемыйДокумент КАК ИсправляемыйДокумент
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВыводитьСклад = Истина;
	
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПериодыКурсовВалютПоДокументам.Ссылка,
	|	КурсыВалют.Валюта,
	|	КурсыВалют.КурсЧислитель,
	|	КурсыВалют.КурсЗнаменатель
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	(ВЫБРАТЬ
	|		Документы.Ссылка КАК Ссылка,
	|		МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|		КурсыВалют.Валюта КАК Валюта,
	|		КурсыВалют.БазоваяВалюта КАК БазоваяВалюта
	|	ИЗ
	|		Документ.ОтгрузкаТоваровСХранения КАК Документы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|			ПО Документы.Валюта = КурсыВалют.Валюта
	|				И Документы.Ссылка.Организация.ВалютаРегламентированногоУчета = КурсыВалют.БазоваяВалюта
	|				И Документы.Дата >= КурсыВалют.Период
	|	ГДЕ
	|		Документы.Ссылка В(&МассивДокументов)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Документы.Ссылка,
	|		КурсыВалют.Валюта,
	|		КурсыВалют.БазоваяВалюта) КАК ПериодыКурсовВалютПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО ПериодыКурсовВалютПоДокументам.Период = КурсыВалют.Период
	|			И ПериодыКурсовВалютПоДокументам.Валюта = КурсыВалют.Валюта
	|			И ПериодыКурсовВалютПоДокументам.БазоваяВалюта = КурсыВалют.БазоваяВалюта
	|
	|;
	|
	|ВЫБРАТЬ
	|	ДокументОтгрузки.Ссылка            КАК Ссылка,
	|	ЕСТЬNULL(ДокументОтгрузки.ИсправляемыйДокумент.Номер, ДокументОтгрузки.Номер) КАК Номер,
	|	ЕСТЬNULL(ДокументОтгрузки.ИсправляемыйДокумент.Дата, ДокументОтгрузки.Дата) КАК Дата,
	|	ЕСТЬNULL(ДокументОтгрузки.ИсправляемыйДокумент.Дата, ДокументОтгрузки.Дата) КАК ДатаДокумента,
	|	ДокументОтгрузки.Дата              КАК ДатаПолученияСебестоимости,
	|	ДокументОтгрузки.Организация       КАК Организация,
	|	ДокументОтгрузки.Контрагент        КАК Контрагент,
	|	ДокументОтгрузки.Отпустил          КАК Сдал,
	|	ДокументОтгрузки.ОтпустилДолжность КАК СдалДолжность,
	|	ДокументОтгрузки.Договор.Дата      КАК ДоговорДата,
	|	ДокументОтгрузки.Договор.Номер     КАК ДоговорНомер,
	|	ДокументОтгрузки.Валюта            КАК Валюта
	|ПОМЕСТИТЬ ВтШапка
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ДокументОтгрузки
	|ГДЕ
	|	ДокументОтгрузки.Ссылка В(&МассивДокументов)
	|	И ДокументОтгрузки.Проведен
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ВтШапка.Ссылка          КАК Ссылка,
	|	ВтШапка.Номер           КАК Номер,
	|	ВтШапка.Дата            КАК Дата,
	|	ВтШапка.Дата            КАК ДатаДокумента,
	|	ВтШапка.Контрагент     	КАК Организация,
	|	ВтШапка.Контрагент      КАК Поклажедатель,
	|	ВтШапка.Сдал            КАК Сдал,
	|	ВтШапка.СдалДолжность   КАК СдалДолжность,
	|	ВтШапка.ДоговорДата     КАК ДоговорДата,
	|	ВтШапка.ДоговорНомер    КАК ДоговорНомер
	|ИЗ
	|	ВтШапка КАК ВтШапка
	|;
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыОтгрузки.Ссылка                КАК Ссылка,
	|	ТоварыОтгрузки.Склад                 КАК Склад,
	|	ЛОЖЬ                                 КАК ПредварительныйРасчет,
	|	НЕОПРЕДЕЛЕНО                         КАК ИсточникИнформацииОЦенахДляПечати,
	|	ПРЕДСТАВЛЕНИЕ(ТоварыОтгрузки.Склад)  КАК ПредставлениеСклада,
	|	ПРЕДСТАВЛЕНИЕ(Склады.Подразделение)  КАК ПредставлениеПодразделения,
	|	ВтШапка.Организация                  КАК Поклажедержатель,
	|	""""                                 КАК ОсобыеОтметки,
	|	""""                                 КАК УсловияХранения,
	|	ВтШапка.Сдал                         КАК МОЛ,
	|	ВтШапка.СдалДолжность                КАК ДолжностьМОЛ
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения.Товары КАК ТоварыОтгрузки
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК ВтШапка
	|		ПО ВтШапка.Ссылка = ТоварыОтгрузки.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО ТоварыОтгрузки.Склад = Склады.Ссылка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ТоварыОтгрузки.Ссылка                            КАК Ссылка,
	|	ТоварыОтгрузки.Склад                             КАК Склад,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТоварыОтгрузки.Номенклатура.Артикул
	|		ИНАЧЕ ТоварыОтгрузки.Номенклатура.Код
	|	КОНЕЦ                                            КАК НоменклатураКод,
	|	ТоварыОтгрузки.Номенклатура                      КАК Номенклатура,
	|	ТоварыОтгрузки.Номенклатура.НаименованиеПолное   КАК ПредставлениеНоменклатуры,
	|	ТоварыОтгрузки.Характеристика.НаименованиеПолное КАК ПредставлениеХарактеристики,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения        КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения                 КАК ЕдиницаИзмеренияКод,
	|	ТоварыОтгрузки.КоличествоУпаковок                КАК Количество,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения        КАК ВидУпаковки,
	|	ВЫБОР
	|		КОГДА ТоварыОтгрузки.СуммаБезНДС > 0
	|			ТОГДА ВЫРАЗИТЬ(ТоварыОтгрузки.СуммаБезНДС / ТоварыОтгрузки.КоличествоУпаковок КАК ЧИСЛО(31,2))
	|		ИНАЧЕ ВЫРАЗИТЬ(ТоварыОтгрузки.Цена * ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА ТоварыОтгрузки.СуммаБезНДС > 0
	|			ТОГДА ТоварыОтгрузки.СуммаБезНДС
	|		ИНАЧЕ ВЫРАЗИТЬ(ТоварыОтгрузки.СуммаБезНДС * ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	ОтгрузкаТоваровСХраненияТаблицаТоваров КАК ТоварыОтгрузки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтШапка КАК ОтгрузкаТоваровСХранения
	|		ПО ОтгрузкаТоваровСХранения.Ссылка = ТоварыОтгрузки.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО Организации.Ссылка = ОтгрузкаТоваровСХранения.Организация
	|	ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО ТоварыОтгрузки.Ссылка = КурсыВалют.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|
	|ИТОГИ ПО
	|	Ссылка,
	|	Склад
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "ТоварыОтгрузки.Упаковка", "ТоварыОтгрузки.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "ТоварыОтгрузки.Упаковка", "ТоварыОтгрузки.Номенклатура"));
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("КолонкаКодов",     КолонкаКодов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[2];
	РезультатПоСкладам        = МассивРезультатов[3];
	РезультатПоТабличнойЧасти = МассивРезультатов[4];
	
	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке",          РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоСкладам",        РезультатПоСкладам);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти", РезультатПоТабличнойЧасти);
	СтруктураДанныхДляПечати.Вставить("РезультатПоОшибкам",        Неопределено);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Формирует временную таблицу, содержащую табличную часть по таблице данных документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц, содержащий таблицу
//		ТаблицаДанныхДокументов с полями:
//			* Ссылка - ДокументСсылка.ОтгрузкаТоваровСхранения - ссылка на документ отгрузки товров с хранения.
//	ПараметрыЗаполнения     - Структура               - структура, возвращаемая функцией
//		ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров.
//
Процедура ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения = Неопределено) Экспорт
	
	Если ПараметрыЗаполнения = Неопределено Тогда
		ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Ссылка               КАК Документ,
	|	Таблица.ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	Таблица.Дата                 КАК ДатаДокумента,
	|	Таблица.МоментВремени        КАК МоментВремени
	|ПОМЕСТИТЬ ЦепочкаДокументовБезПредыдущего
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровСХранения КАК Таблица
	|		ПО ДанныеДокументов.Ссылка = Таблица.Ссылка
	|ГДЕ
	|	НЕ Таблица.Ссылка ЕСТЬ NULL
	|	И Таблица.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ссылка               КАК Документ,
	|	Таблица.ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	Таблица.Дата                 КАК ДатаДокумента,
	|	Таблица.МоментВремени        КАК МоментВремени
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровСХранения КАК Таблица
	|		ПО ДанныеДокументов.Ссылка.ИсправляемыйДокумент = Таблица.ИсправляемыйДокумент
	|ГДЕ
	|	ДанныеДокументов.Ссылка.ИсправляемыйДокумент <> ЗНАЧЕНИЕ(Документ.ОтгрузкаТоваровСХранения.ПустаяСсылка)
	|	И НЕ Таблица.Ссылка ЕСТЬ NULL
	|	И Таблица.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ссылка               КАК Документ,
	|	Таблица.ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	Таблица.Дата                 КАК ДатаДокумента,
	|	Таблица.МоментВремени        КАК МоментВремени
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровСХранения КАК Таблица
	|		ПО ДанныеДокументов.Ссылка.ИсправляемыйДокумент = Таблица.Ссылка
	|ГДЕ
	|	НЕ Таблица.Ссылка ЕСТЬ NULL
	|	И Таблица.Проведен
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦепочкаДокументовБезПредыдущего.Документ                                  КАК Документ,
	|	ЦепочкаДокументовБезПредыдущего.ИсправляемыйДокумент                      КАК ИсправляемыйДокумент,
	|	ЦепочкаДокументовБезПредыдущего.ДатаДокумента                             КАК ДатаДокумента,
	|	МАКСИМУМ(ЕСТЬNULL(ПредыдущиеДокументы.ДатаДокумента, ДАТАВРЕМЯ(1, 1, 1))) КАК ДатаДокументаПредыдущегоДокумента
	|ПОМЕСТИТЬ ЦепочкаДокументов
	|ИЗ
	|	ЦепочкаДокументовБезПредыдущего КАК ЦепочкаДокументовБезПредыдущего
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ЦепочкаДокументовБезПредыдущего КАК ПредыдущиеДокументы
	|ПО
	|	ЦепочкаДокументовБезПредыдущего.МоментВремени > ПредыдущиеДокументы.МоментВремени
	|
	|СГРУППИРОВАТЬ ПО
	|	ЦепочкаДокументовБезПредыдущего.Документ,
	|	ЦепочкаДокументовБезПредыдущего.ИсправляемыйДокумент,
	|	ЦепочкаДокументовБезПредыдущего.ДатаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЦепочкаДокументов.Документ                                  КАК Основной,
	|	ЕСТЬNULL(ЦепочкаПредыдущихДокументов.Документ, ЗНАЧЕНИЕ(Документ.ОтгрузкаТоваровСХранения.ПустаяСсылка)) КАК Предыдущий
	|ПОМЕСТИТЬ ПредыдущиеДокументы
	|ИЗ
	|	ЦепочкаДокументов КАК ЦепочкаДокументов
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|ПО
	|	ЦепочкаДокументов.Документ = ДанныеДокументов.Ссылка
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ЦепочкаДокументов КАК ЦепочкаПредыдущихДокументов
	|ПО
	|	ЦепочкаДокументов.ДатаДокументаПредыдущегоДокумента = ЦепочкаПредыдущихДокументов.ДатаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка                     КАК Ссылка,
	|	ЛОЖЬ                                   КАК ДоКорректировки,
	|	ВидыЗапасов.НомерСтроки                КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.Упаковка                   КАК Упаковка,
	|	ВидыЗапасов.Цена                       КАК Цена,
	|	ВидыЗапасов.ВидЗапасов                 КАК ВидЗапасов,
	|	ВидыЗапасов.НомерГТД                   КАК НомерГТД,
	|	ВидыЗапасов.ИдентификаторСтроки        КАК ИдентификаторСтроки,
	|	ВидыЗапасов.КоличествоУпаковок         КАК КоличествоУпаковок,
	|	ВидыЗапасов.Количество                 КАК Количество,
	|	ВидыЗапасов.КоличествоПоРНПТ           КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, 0) КАК СуммаБезНДС,
	|	ЕСТЬNULL(СуммыРегл.СтавкаНДС, 0)       КАК СтавкаНДС,
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, 0)    КАК СуммаНДС,
	|	ВидыЗапасов.ЗаказКлиента               КАК ЗаказКлиента
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения.ВидыЗапасов КАК ВидыЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ВидыЗапасов.Ссылка = ДанныеДокументов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|		И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|		И СуммыРегл.Активность
	|		И &ПересчитыватьВВалютуРегл
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка                                                КАК Ссылка,
	|	ИСТИНА                                                                 КАК ДоКорректировки,
	|	ЕСТЬNULL(ВидыЗапасов.НомерСтроки, 0)                                   КАК НомерСтроки,
	|	ЕСТЬNULL(ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|		ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)) КАК АналитикаУчетаНоменклатуры,
	|	ЕСТЬNULL(ВидыЗапасов.Упаковка,
	|		ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка))        КАК Упаковка,
	|	ЕСТЬNULL(ВидыЗапасов.Цена, 0)                                          КАК Цена,
	|	ЕСТЬNULL(ВидыЗапасов.ВидЗапасов,
	|		ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка))                     КАК ВидЗапасов,
	|	ЕСТЬNULL(ВидыЗапасов.НомерГТД,
	|		ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка))                       КАК НомерГТД,
	|	ЕСТЬNULL(ВидыЗапасов.ИдентификаторСтроки, """")                        КАК ИдентификаторСтроки,
	|	ЕСТЬNULL(ВидыЗапасов.КоличествоУпаковок, 0)                            КАК КоличествоУпаковок,
	|	ЕСТЬNULL(ВидыЗапасов.Количество, 0)                                    КАК Количество,
	|	ЕСТЬNULL(ВидыЗапасов.КоличествоПоРНПТ, 0)                              КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(СуммыРегл.СуммаБезНДСРегл, 0)                                 КАК СуммаБезНДС,
	|	ЕСТЬNULL(СуммыРегл.СтавкаНДС, 0)                                       КАК СтавкаНДС,
	|	ЕСТЬNULL(СуммыРегл.СуммаНДСРегл, 0)                                    КАК СуммаНДС,
	|	ЕСТЬNULL(ВидыЗапасов.ЗаказКлиента, НЕОПРЕДЕЛЕНО)                       КАК ЗаказКлиента
	|ИЗ
	|	ЦепочкаДокументов КАК ЦепочкаДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ЦепочкаДокументов.Документ = ДанныеДокументов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровСХранения.ВидыЗапасов КАК ВидыЗапасов
	|		ПО (ЦепочкаДокументов.ИсправляемыйДокумент = ВидыЗапасов.Ссылка.ИсправляемыйДокумент
	|			ИЛИ ЦепочкаДокументов.ИсправляемыйДокумент = ВидыЗапасов.Ссылка)
	|		И ЦепочкаДокументов.ДатаДокументаПредыдущегоДокумента = ВидыЗапасов.Ссылка.Дата
	|		И ВидыЗапасов.Ссылка.Проведен
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыРегл
	|		ПО ВидыЗапасов.Ссылка = СуммыРегл.Регистратор
	|		И ВидыЗапасов.ИдентификаторСтроки = СуммыРегл.ИдентификаторСтроки
	|		И СуммыРегл.Активность
	|		И &ПересчитыватьВВалютуРегл
	|ГДЕ
	|	&ВключаяДоКорректировки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка						КАК Ссылка,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Упаковка						КАК Упаковка,
	|	МАКСИМУМ(ТаблицаТоваров.НомерСтроки)		КАК НомерСтроки
	|ПОМЕСТИТЬ СтрокиТоваров
	|ИЗ
	|Документ.ОтгрузкаТоваровСХранения.Товары КАК ТаблицаТоваров
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|ПО
	|	ТаблицаТоваров.Ссылка = ДанныеДокументов.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Упаковка
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	АналитикаУчетаНоменклатуры,
	|	Упаковка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999)                 КАК НомерСтроки,
	|	ТаблицаДокумента.Ссылка                                    КАК Ссылка,
	|	ЛОЖЬ                                                       КАК ЭтоКомплектующие,
	|	ЛОЖЬ                                                       КАК ЭтоНабор,
	|	0                                                          КАК НомерСтрокиНаборы,
	|	ЛОЖЬ                                                       КАК ПолныйНабор,
	|	ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка) КАК НоменклатураПартнера,
	|	КлючиАналитикиУчетаНоменклатуры.Номенклатура			   КАК Номенклатура,
	|	КлючиАналитикиУчетаНоменклатуры.Характеристика             КАК Характеристика,
	|	КлючиАналитикиУчетаНоменклатуры.Серия                      КАК Серия,
	|	ТаблицаДокумента.Упаковка                                  КАК Упаковка,
	|	ВЫБОР
	|		КОГДА &ВключаяНомераГТД
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ &ПустаяГТД
	|	КОНЕЦ                                                      КАК НомерГТД,
	|	ВЫБОР
	|		КОГДА СУММА(ТаблицаДокумента.Количество) > 0
	|			ТОГДА СУММА(ТаблицаДокумента.Количество)
	|		ИНАЧЕ -СУММА(ТаблицаДокумента.Количество)
	|	КОНЕЦ                                                      КАК Количество,
	|	ВЫБОР
	|		КОГДА СУММА(ТаблицаДокумента.КоличествоУпаковок) > 0
	|			ТОГДА СУММА(ТаблицаДокумента.КоличествоУпаковок)
	|		ИНАЧЕ -СУММА(ТаблицаДокумента.КоличествоУпаковок)
	|	КОНЕЦ                                                      КАК КоличествоУпаковок,
	|	ВЫБОР
	|		КОГДА НЕ &ВключаяНомераГТД
	|			ТОГДА 0
	|		КОГДА СУММА(ТаблицаДокумента.КоличествоПоРНПТ) > 0
	|			ТОГДА СУММА(ТаблицаДокумента.КоличествоПоРНПТ)
	|		ИНАЧЕ -СУММА(ТаблицаДокумента.КоличествоПоРНПТ)
	|	КОНЕЦ                                                      КАК КоличествоПоРНПТ,
	|	ВЫБОР
	|		КОГДА СУММА(ТаблицаДокумента.СуммаБезНДС) > 0
	|			ТОГДА СУММА(ТаблицаДокумента.СуммаБезНДС)
	|		ИНАЧЕ -СУММА(ТаблицаДокумента.СуммаБезНДС)
	|	КОНЕЦ                                                      КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА СУММА(ТаблицаДокумента.КоличествоУпаковок) = 0
	|			ТОГДА 0
	|		КОГДА СУММА(ТаблицаДокумента.СуммаБезНДС) / СУММА(ТаблицаДокумента.КоличествоУпаковок) > 0
	|			ТОГДА СУММА(ТаблицаДокумента.СуммаБезНДС) / СУММА(ТаблицаДокумента.КоличествоУпаковок)
	|		ИНАЧЕ
	|			-СУММА(ТаблицаДокумента.СуммаБезНДС) / СУММА(ТаблицаДокумента.КоличествоУпаковок)
	|	КОНЕЦ                                                      КАК Цена,
	|	ТаблицаДокумента.СтавкаНДС                                 КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА СУММА(ТаблицаДокумента.СуммаНДС) > 0
	|			ТОГДА СУММА(ТаблицаДокумента.СуммаНДС)
	|		ИНАЧЕ -СУММА(ТаблицаДокумента.СуммаНДС)
	|	КОНЕЦ                                                      КАК СуммаНДС,
	|	ИСТИНА                                                     КАК ЭтоТовар,
	|	ВЫБОР
	|		КОГДА &ВыводитьСклад
	|			ТОГДА КлючиАналитикиУчетаНоменклатуры.МестоХранения
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                                      КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.КлассификаторТНВЭД.ПустаяСсылка)       КАК КодТНВЭД,
	|	ТаблицаДокумента.ДоКорректировки                           КАК ДоКорректировки,
	|	&ТекстЗапросаВесНоменклатуры                               КАК МассаНеттоНоменклатуры
	|ПОМЕСТИТЬ ОтгрузкаТоваровСХраненияТаблицаТоваров
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаДокумента
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	СтрокиТоваров КАК СтрокиТоваров
	|ПО
	|	ТаблицаДокумента.Ссылка = СтрокиТоваров.Ссылка
	|	И ТаблицаДокумента.АналитикаУчетаНоменклатуры = СтрокиТоваров.АналитикаУчетаНоменклатуры
	|	И ТаблицаДокумента.Упаковка = СтрокиТоваров.Упаковка
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
	|ПО
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры = КлючиАналитикиУчетаНоменклатуры.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(СтрокиТоваров.НомерСтроки, 99999),
	|	ТаблицаДокумента.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка),
	|	КлючиАналитикиУчетаНоменклатуры.Номенклатура,
	|	КлючиАналитикиУчетаНоменклатуры.Характеристика,
	|	КлючиАналитикиУчетаНоменклатуры.Серия,
	|	КлючиАналитикиУчетаНоменклатуры.МестоХранения,
	|	ТаблицаДокумента.Упаковка,
	|	ВЫБОР
	|		КОГДА &ВключаяНомераГТД
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ &ПустаяГТД
	|	КОНЕЦ,
	|	ТаблицаДокумента.СтавкаНДС,
	|	ТаблицаДокумента.ДоКорректировки
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаДокумента.Количество) <> 0
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ЦепочкаДокументовБезПредыдущего
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ЦепочкаДокументов
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтВидыЗапасов
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СтрокиТоваров
	|";

	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВесНоменклатуры",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"КлючиАналитикиУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения",
			"КлючиАналитикиУчетаНоменклатуры.Номенклатура"));
	
	Запрос.УстановитьПараметр("ПересчитыватьВВалютуРегл", ПараметрыЗаполнения.ПересчитыватьВВалютуРегл);
	Запрос.УстановитьПараметр("ВключаяНомераГТД",         ПараметрыЗаполнения.ВключаяНомераГТД);
	Запрос.УстановитьПараметр("ВключаяДоКорректировки",   ПараметрыЗаполнения.ВключаяДоКорректировки);
	Запрос.УстановитьПараметр("ПустаяГТД",                Справочники.НомераГТД.ПустаяСсылка());
	Запрос.УстановитьПараметр("ВыводитьСклад",            ПараметрыЗаполнения.ВыводитьСклад);
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

//++ НЕ УТКА

// Формирует данные для печатной формы МХ18
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ОтгрузкаТоваровСХранения - Массив ссылок на документы, 
//															по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке          - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыМХ18(ПараметрыПечати, МассивОбъектов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПериодыКурсовВалютПоДокументам.Ссылка,
	|	КурсыВалют.Валюта,
	|	КурсыВалют.КурсЧислитель,
	|	КурсыВалют.КурсЗнаменатель
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	(ВЫБРАТЬ
	|		Документы.Ссылка КАК Ссылка,
	|		МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|		КурсыВалют.Валюта КАК Валюта,
	|		КурсыВалют.БазоваяВалюта КАК БазоваяВалюта
	|	ИЗ
	|		Документ.ОтгрузкаТоваровСХранения КАК Документы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|			ПО Документы.Валюта = КурсыВалют.Валюта
	|				И Документы.Ссылка.Организация.ВалютаРегламентированногоУчета = КурсыВалют.БазоваяВалюта
	|				И Документы.Дата >= КурсыВалют.Период
	|	ГДЕ
	|		Документы.Ссылка В(&МассивДокументов)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Документы.Ссылка,
	|		КурсыВалют.Валюта,
	|		КурсыВалют.БазоваяВалюта) КАК ПериодыКурсовВалютПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО ПериодыКурсовВалютПоДокументам.Период = КурсыВалют.Период
	|			И ПериодыКурсовВалютПоДокументам.Валюта = КурсыВалют.Валюта
	|			И ПериодыКурсовВалютПоДокументам.БазоваяВалюта = КурсыВалют.БазоваяВалюта
	|
	|;
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка														КАК Ссылка,
	|	Товары.Номенклатура													КАК Номенклатура,
	|	Товары.Характеристика												КАК Характеристика,
	|	Товары.Серия														КАК Серия,
	|	Товары.Упаковка														КАК Упаковка,
	|	СУММА(Товары.КоличествоУпаковок)									КАК КоличествоУпаковок,
	|	СУММА(Товары.Сумма)													КАК Сумма,
	|	Товары.Ссылка.Контрагент.Представление								КАК Получатель
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения.Товары КАК Товары
	|
	|ГДЕ
	|	Товары.Ссылка В(&МассивДокументов)
	|	
	|СГРУППИРОВАТЬ ПО
	|	Товары.Ссылка,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Серия,
	|	Товары.Упаковка,
	|	Товары.Ссылка.Контрагент.Представление
	|
	|;
	|////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ШАПКЕ
	|
	|ВЫБРАТЬ
	|	ПередачаДавальцу.Ссылка							КАК Ссылка,
	|	ПередачаДавальцу.Номер							КАК Номер,
	|	ПередачаДавальцу.Дата							КАК Дата,
	|	ПередачаДавальцу.Дата							КАК ДатаДокумента,
	|	ПередачаДавальцу.Контрагент.Представление		КАК Получатель,
	|	ПередачаДавальцу.Подразделение.Представление	КАК ПредставлениеПодразделения
	|
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ПередачаДавальцу
	|
	|ГДЕ
	|	ПередачаДавальцу.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// ЗАПРОС ПО ТАБЛИЧНЫМ ЧАСТЯМ
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка														КАК Ссылка,
	|	Товары.Ссылка.Организация											КАК Организация,
	|	Товары.Ссылка.Организация.Префикс									КАК Префикс,
	|	Товары.Номенклатура													КАК Номенклатура,
	|	Товары.Номенклатура.НаименованиеПолное								КАК НоменклатураНаименование,
	|	&ПолеНоменклатураКод												КАК НоменклатураКод,
	|	Товары.Характеристика.НаименованиеПолное							КАК ХарактеристикаНаименование,
	|	Товары.Характеристика												КАК Характеристика,
	|	Товары.Серия.Наименование											КАК СерияНаименование,
	|	Товары.Серия														КАК Серия,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения 							КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения 									КАК ЕдиницаИзмеренияКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения 							КАК ВидУпаковки,
	|	ВЫБОР КОГДА Товары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) ТОГДА
	|		1
	|	ИНАЧЕ
	|		&ТекстЗапросаКоэффициентУпаковки
	|	КОНЕЦ																КАК КоличествоВОдномМесте,
	|	ВЫБОР КОГДА (НЕ Товары.Упаковка.Вес ЕСТЬ NULL) ТОГДА
	|		Товары.КоличествоУпаковок * &ТекстЗапросаВес
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ																КАК МассаБрутто,
	|
	|	Товары.КоличествоУпаковок											КАК Количество,
	|	Товары.КоличествоУпаковок											КАК КоличествоМест,
	|	0																	КАК НомерСтроки,
	|	ЛОЖЬ																КАК ЭтоВозвратнаяТара,
	|	ВЫРАЗИТЬ(Товары.Сумма / Товары.КоличествоУпаковок * ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК ЧИСЛО(31,2))	КАК Цена,
	|	ВЫРАЗИТЬ(Товары.Сумма * ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК ЧИСЛО(31,2))	КАК Сумма,
	|	Товары.Получатель													КАК Получатель
	|
	|ИЗ
	|	ТаблицаТовары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО Товары.Ссылка = КурсыВалют.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|
	|ИТОГИ ПО
	|	Ссылка,
	|	Организация,
	|	Получатель";
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если НЕ ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолеНоменклатураКод", "Товары.Номенклатура." + КолонкаКодов);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"Товары.Упаковка",
			"Товары.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВес",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("Товары.Упаковка", "Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование", "Товары.Упаковка", "Товары.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код", "Товары.Упаковка", "Товары.Номенклатура"));
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке = МассивРезультатов[2];
	РезультатПоТабличнойЧасти = МассивРезультатов[3];
	
	СтруктураДанныхДляПечати = Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПоШапке,
		РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Функция получает данные для формирования печатной формы М15
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ОтгрузкаТоваровСХранения - Массив ссылок на документы, 
//															по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке          - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыМ15(ПараметрыПечати, МассивОбъектов) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка               КАК Ссылка,
	|	ДанныеДокументов.ИсправляемыйДокумент КАК ИсправляемыйДокумент
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();

	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(
		МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПериодыКурсовВалютПоДокументам.Ссылка,
	|	КурсыВалют.Валюта,
	|	КурсыВалют.КурсЧислитель,
	|	КурсыВалют.КурсЗнаменатель
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	(ВЫБРАТЬ
	|		Документы.Ссылка КАК Ссылка,
	|		МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|		КурсыВалют.Валюта КАК Валюта,
	|		КурсыВалют.БазоваяВалюта КАК БазоваяВалюта
	|	ИЗ
	|		Документ.ОтгрузкаТоваровСХранения КАК Документы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|			ПО Документы.Валюта = КурсыВалют.Валюта
	|				И Документы.Ссылка.Организация.ВалютаРегламентированногоУчета = КурсыВалют.БазоваяВалюта
	|				И Документы.Дата >= КурсыВалют.Период
	|	ГДЕ
	|		Документы.Ссылка В(&МассивОбъектов)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Документы.Ссылка,
	|		КурсыВалют.Валюта,
	|		КурсыВалют.БазоваяВалюта) КАК ПериодыКурсовВалютПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО ПериодыКурсовВалютПоДокументам.Период = КурсыВалют.Период
	|			И ПериодыКурсовВалютПоДокументам.Валюта = КурсыВалют.Валюта
	|			И ПериодыКурсовВалютПоДокументам.БазоваяВалюта = КурсыВалют.БазоваяВалюта
	|
	|;
	|
	|ВЫБРАТЬ
	|	ОтгрузкаТоваровСХранения.Ссылка КАК Ссылка,
	|	ОтгрузкаТоваровСХранения.Номер КАК Номер,
	|	ОтгрузкаТоваровСХранения.Дата КАК Дата,
	|	ОтгрузкаТоваровСХранения.Дата КАК ДатаСоставления,
	|	ОтгрузкаТоваровСХранения.Контрагент КАК Контрагент,
	|	ОтгрузкаТоваровСХранения.Организация КАК Организация,
	|	ОтгрузкаТоваровСХранения.Организация.Префикс КАК Префикс,
	|	ОтгрузкаТоваровСХранения.ОснованиеДляПечати КАК Основание,
	|	ОтгрузкаТоваровСХранения.Склад КАК Склад,
	|	ОтгрузкаТоваровСХранения.Склад.Наименование КАК СкладНаименование,
	|	ОтгрузкаТоваровСХранения.Подразделение КАК СтруктурноеПодразделение,
	|	ОтгрузкаТоваровСХранения.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	ОтгрузкаТоваровСХранения.Валюта КАК Валюта,
	|	"""" КАК ЦенаВключаетНДС,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ОтгрузкаТоваровСХранения.Отпустил КАК Кладовщик,
	|	ОтгрузкаТоваровСХранения.ОтпустилДолжность КАК ДолжностьКладовщика
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ОтгрузкаТоваровСХранения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ОтгрузкаТоваровСХранения.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ОтгрузкаТоваровСХранения.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ДанныеПоТовару.НаименованиеПолное КАК НоменклатураНаименование,
	|	ДанныеПоТовару.Код КАК НоменклатураКод,
	|	&ТекстЗапросаНаименованиеЕдиницыИзмерения КАК ЕдиницаИзмеренияНаименование,
	|	&ТекстЗапросаКодЕдиницыИзмерения КАК ЕдиницаИзмеренияКод,
	|	ХарактеристикиНоменклатуры.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	СерииНоменклатуры.Наименование КАК СерияНаименование,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаТоваров.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ТаблицаТоваров.КоличествоУпаковок КАК Количество,
	|	0 КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СуммаБезНДС > 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок КАК ЧИСЛО(31,2))
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТоваров.Цена * ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СуммаБезНДС > 0
	|			ТОГДА ТаблицаТоваров.СуммаБезНДС
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТоваров.СуммаБезНДС * ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаБезНДС,
	|	0																														КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СуммаБезНДС > 0
	|			ТОГДА ТаблицаТоваров.СуммаБезНДС
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТоваров.СуммаБезНДС * ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаСНДС,
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА
	|			ДанныеПоТовару.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ОтгрузкаТоваровСХраненияТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО ТаблицаТоваров.Ссылка = КурсыВалют.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ДанныеПоТовару
	|		ПО ДанныеПоТовару.Ссылка = ТаблицаТоваров.Номенклатура
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО СерииНоменклатуры.Ссылка = ТаблицаТоваров.Номенклатура
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО ХарактеристикиНоменклатуры.Ссылка = ТаблицаТоваров.Характеристика
	|
	|ГДЕ
	|	ТаблицаТоваров.ЭтоТовар
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	РезультатПоШапке = МассивРезультатов[1];
	РезультатПоТабличнойЧасти = МассивРезультатов[2];
	
	СтруктураДанныхДляПечати = Новый Структура(
		"РезультатПоШапке, РезультатПоТабличнойЧасти",
		РезультатПоШапке,
		РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

//-- НЕ УТКА

// Функция получает данные для формирования печатной формы ТОРГ - 12
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ОтгрузкаТоваровСХранения - Массив ссылок на документы, 
//															по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке          - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
//
Функция ПолучитьДанныеДляПечатнойФормыТОРГ12(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка               КАК Ссылка,
	|	ДанныеДокументов.ИсправляемыйДокумент КАК ИсправляемыйДокумент
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Выполнить();
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	Если ПараметрыПечати.Свойство("ВыводитьГТД") Тогда
		ПараметрыЗаполнения.ВключаяНомераГТД = ПараметрыПечати.ВыводитьГТД;
	КонецЕсли;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	ПродажиСервер.ПоместитьВременнуюТаблицуКоэффициентыУпаковок(
		МенеджерВременныхТаблиц, "ОтгрузкаТоваровСХраненияТаблицаТоваров");
		
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(
		МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПериодыКурсовВалютПоДокументам.Ссылка,
	|	КурсыВалют.Валюта,
	|	КурсыВалют.КурсЧислитель,
	|	КурсыВалют.КурсЗнаменатель
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	(ВЫБРАТЬ
	|		Документы.Ссылка КАК Ссылка,
	|		МАКСИМУМ(КурсыВалют.Период) КАК Период,
	|		КурсыВалют.Валюта КАК Валюта,
	|		КурсыВалют.БазоваяВалюта КАК БазоваяВалюта
	|	ИЗ
	|		Документ.ОтгрузкаТоваровСХранения КАК Документы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|			ПО Документы.Валюта = КурсыВалют.Валюта
	|				И Документы.Ссылка.Организация.ВалютаРегламентированногоУчета = КурсыВалют.БазоваяВалюта
	|				И Документы.Дата >= КурсыВалют.Период
	|	ГДЕ
	|		Документы.Ссылка В(&МассивОбъектов)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Документы.Ссылка,
	|		КурсыВалют.Валюта,
	|		КурсыВалют.БазоваяВалюта) КАК ПериодыКурсовВалютПоДокументам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют КАК КурсыВалют
	|		ПО ПериодыКурсовВалютПоДокументам.Период = КурсыВалют.Период
	|			И ПериодыКурсовВалютПоДокументам.Валюта = КурсыВалют.Валюта
	|			И ПериодыКурсовВалютПоДокументам.БазоваяВалюта = КурсыВалют.БазоваяВалюта
	|
	|;
	|
	|ВЫБРАТЬ
	|	ОтгрузкаТоваровСХранения.Ссылка КАК Ссылка,
	|	ОтгрузкаТоваровСХранения.Номер КАК Номер,
	|	ОтгрузкаТоваровСХранения.Дата КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК Статус,
	|	ОтгрузкаТоваровСХранения.Партнер КАК Партнер,
	|	ОтгрузкаТоваровСХранения.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ОтгрузкаТоваровСХранения.Организация.ОбособленноеПодразделение
	|			ТОГДА ОтгрузкаТоваровСХранения.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ ОтгрузкаТоваровСХранения.Организация
	|	КОНЕЦ КАК Организация,
	|	ТаблицаОтветственныеЛица.РуководительНаименование  КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ОтгрузкаТоваровСХранения.Отпустил КАК Кладовщик,
	|	ОтгрузкаТоваровСХранения.ОтпустилДолжность КАК ДолжностьКладовщика,
	|	ОтгрузкаТоваровСХранения.Организация.Префикс КАК Префикс,
	|	ОтгрузкаТоваровСХранения.ОснованиеДляПечати КАК Основание,
	|	ОтгрузкаТоваровСХранения.ОснованиеДата КАК ОснованиеДата,
	|	ОтгрузкаТоваровСХранения.ОснованиеНомер КАК ОснованиеНомер,
	|	ВЫБОР
	|		КОГДА ОтгрузкаТоваровСХранения.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ОтгрузкаТоваровСХранения.Контрагент
	|		ИНАЧЕ ОтгрузкаТоваровСХранения.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА ОтгрузкаТоваровСХранения.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ОтгрузкаТоваровСХранения.Организация
	|		ИНАЧЕ ОтгрузкаТоваровСХранения.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	ОтгрузкаТоваровСХранения.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	ОтгрузкаТоваровСХранения.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	ОтгрузкаТоваровСХранения.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	ОтгрузкаТоваровСХранения.БанковскийСчетГрузополучателя КАК БанковскийСчетГрузополучателя,
	|	ОтгрузкаТоваровСХранения.АдресДоставки КАК АдресДоставки,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	"""" КАК Валюта,
	|	ОтгрузкаТоваровСХранения.ДоверенностьНомер КАК ДоверенностьНомер,
	|	ОтгрузкаТоваровСХранения.ДоверенностьДата КАК ДоверенностьДата,
	|	ОтгрузкаТоваровСХранения.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	ОтгрузкаТоваровСХранения.ДоверенностьЛицо КАК ДоверенностьЛицо,
	|	&ЕдиницаИзмеренияВеса КАК ЕдиницаИзмеренияВеса
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ОтгрузкаТоваровСХранения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ОтгрузкаТоваровСХранения.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ОтгрузкаТоваровСХранения.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ДанныеПоТовару.НаименованиеПолное КАК НоменклатураНаименование,
	|	ДанныеПоТовару.Наименование КАК НоменклатураНаименованиеКраткое,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ДанныеПоТовару.Артикул
	|		ИНАЧЕ ДанныеПоТовару.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ДанныеПоТовару.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ДанныеПоТовару.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ДанныеПоТовару.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ХарактеристикиНоменклатуры.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	СерииНоменклатуры.Наименование КАК СерияНаименование,
	|	ТаблицаТоваров.Упаковка КАК Упаковка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 1
	|			ТОГДА """"
	|		ИНАЧЕ УпаковкиЕдиницыИзмерения.Наименование
	|	КОНЕЦ КАК УпаковкаНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения ТОГДА
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|		ИНАЧЕ
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения2
	|	КОНЕЦ КАК ВидУпаковки,
	|	0 КАК СтавкаНДС,
	|	"""" КАК НомерГТД,
	|	"""" КАК СтранаПроисхождения,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ ТаблицаТоваров.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаТоваров.КоличествоУпаковок
	|		ИНАЧЕ КоэффициентыУпаковок.Количество / КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|	КОНЕЦ КАК КоличествоМест,
	|	ВЫБОР
	|		КОГДА НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ВЫБОР
	|					КОГДА КоэффициентыУпаковок.Количество < КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|						ТОГДА КоэффициентыУпаковок.Количество
	|					ИНАЧЕ КоэффициентыУпаковок.КоэффициентВложеннойУпаковки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|					ТОГДА 1
	|				ИНАЧЕ &ТекстЗапросаКоэффициентУпаковки
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоВОдномМесте,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СуммаБезНДС > 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТоваров.СуммаБезНДС / ТаблицаТоваров.КоличествоУпаковок КАК ЧИСЛО(31,2))
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТоваров.Цена * ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СуммаБезНДС > 0
	|			ТОГДА ТаблицаТоваров.СуммаБезНДС
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТоваров.СуммаБезНДС * ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаБезНДС,
	|	0 КАК СуммаНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаТоваров.СуммаБезНДС > 0
	|			ТОГДА ТаблицаТоваров.СуммаБезНДС
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаТоваров.СуммаБезНДС * ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) / ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаСНДС,
	|
	|	ТаблицаТоваров.Количество * &ТекстЗапросаВесНоменклатуры КАК МассаНетто,
	|
	|	ВЫБОР КОГДА &ЗаполненаЕдиницаИзмеренияВеса ТОГДА
	|		ВЫБОР КОГДА УпаковкиЕдиницыИзмерения.Вес ЕСТЬ NULL ТОГДА
	|			ТаблицаТоваров.Количество * &ТекстЗапросаВесУпаковки
	|		ИНАЧЕ
	|			ТаблицаТоваров.КоличествоУпаковок * &ТекстЗапросаВесУпаковки
	|		КОНЕЦ
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ                                                      КАК МассаБрутто,
	|
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА
	|			ДанныеПоТовару.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ОтгрузкаТоваровСХраненияТаблицаТоваров КАК ТаблицаТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО ТаблицаТоваров.Ссылка = КурсыВалют.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыУпаковок КАК КоэффициентыУпаковок
	|		ПО ТаблицаТоваров.Ссылка = КоэффициентыУпаковок.Ссылка
	|			И ТаблицаТоваров.НомерСтроки = КоэффициентыУпаковок.НомерСтроки
	|			И ТаблицаТоваров.КоличествоУпаковок = КоэффициентыУпаковок.КоличествоУпаковок 
	|			И НЕ &ВыводитьБазовыеЕдиницыИзмерения
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
	|		ПО УпаковкиЕдиницыИзмерения.Ссылка = ТаблицаТоваров.Упаковка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ДанныеПоТовару
	|		ПО ДанныеПоТовару.Ссылка = ТаблицаТоваров.Номенклатура
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО СерииНоменклатуры.Ссылка = ТаблицаТоваров.Номенклатура
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО ХарактеристикиНоменклатуры.Ссылка = ТаблицаТоваров.Характеристика
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВесУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст, 
		"&ТекстЗапросаВесНоменклатуры",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения",
			"ТаблицаТоваров.Номенклатура"));
		
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"КоэффициентыУпаковок.ВидУпаковки",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
			
	Запрос.УстановитьПараметр("КолонкаКодов",                    КолонкаКодов);
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияВеса",            Константы.ЕдиницаИзмеренияВеса.Получить());
	Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса",   ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения",
		Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить() ИЛИ ПараметрыЗаполнения.ВключаяНомераГТД);
	
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	РезультатПоШапке          = МассивРезультатов[1];
	РезультатПоТабличнойЧасти = МассивРезультатов[2];
	
	СтруктураДанныхДляПечати 	= Новый Структура("РезультатПоШапке, РезультатПоТабличнойЧасти",
	                                               РезультатПоШапке, РезультатПоТабличнойЧасти);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

// Возвращает данные для формирования печатной формы УПД
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.ОтгрузкаТоваровСХранения - ссылки на документы, по которым необходимо
//																			получить данные.
//
// Возвращаемое значение:
//	Структура - коллекция данных, используемая при печати УПД, содержащая следующие свойства:
//		* РезультатПоШапке - РезультатЗапроса - общие данные накладной.
//		* РезультатПоТабличнойЧасти - РезультатЗапроса - данные табличной части накладной.
//
Функция ПолучитьДанныеДляПечатнойФормыУПД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	ДанныеДокументов.Исправление КАК Исправление,
	|	ДанныеДокументов.ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	ДанныеДокументов.Валюта КАК Валюта,
	|	ДанныеДокументов.Организация КАК Организация,
	|	ДанныеДокументов.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	ДанныеДокументов.Подразделение КАК Подразделение,
	|	ДанныеДокументов.Склад КАК Склад,
	|	ДанныеДокументов.Дата КАК Период
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ДанныеДокументов
	|
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка КАК Ссылка,
	|	КОЛИЧЕСТВО(РеестрДокументов.Ссылка) КАК НомерИсправления	// Номер исправления - номер по порядку в цепочке исправлений
	|ПОМЕСТИТЬ НомераИсправлений
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ДанныеДокументов
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|ПО
	|	ДанныеДокументов.ИсправляемыйДокумент = ВЫРАЗИТЬ(РеестрДокументов.ИсправляемыйДокумент КАК Документ.ОтгрузкаТоваровСХранения)
	|	И ДанныеДокументов.Ссылка.МоментВремени >= ВЫРАЗИТЬ(РеестрДокументов.Ссылка КАК Документ.ОтгрузкаТоваровСХранения).МоментВремени
	|ГДЕ
	|	ДанныеДокументов.Исправление
	|	И НЕ РеестрДокументов.ДополнительнаяЗапись
	|	И РеестрДокументов.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокументов.Ссылка
	|";

	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВключаяНомераГТД = Истина;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос.Текст =
		"ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НЕ ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьУслуги,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаТоваров.ЭтоТовар
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьТовары,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ТаблицаТоваров.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьПрослеживаемыеТовары
	|ПОМЕСТИТЬ НоменклатураДокументов
	|ИЗ
	|	ОтгрузкаТоваровСХраненияТаблицаТоваров КАК ТаблицаТоваров
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеДокумента,
	|	2 КАК СтатусУПД,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Исправление
	|				И НЕ ДанныеИсправления.Ссылка ЕСТЬ NULL
	|			ТОГДА ДанныеИсправления.Номер
	|		ИНАЧЕ ДанныеДокумента.Номер
	|	КОНЕЦ КАК Номер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Исправление
	|				И НЕ ДанныеИсправления.Ссылка ЕСТЬ NULL
	|			ТОГДА ДанныеИсправления.Дата
	|		ИНАЧЕ ДанныеДокумента.Дата
	|	КОНЕЦ КАК Дата,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Исправление
	|			ТОГДА ЕСТЬNULL(НомераИсправлений.НомерИсправления, 1)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НомерИсправления,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Исправление
	|			ТОГДА ДанныеДокумента.Дата
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДатаИсправления,
	|	ДанныеДокумента.Исправление КАК Исправление,
	|	ДанныеДокумента.ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК НомерСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО КАК ДатаИсправленияСчетаФактуры,
	|	ЛОЖЬ КАК КорректировочныйСчетФактура,
	|	НЕОПРЕДЕЛЕНО КАК СтрокаПоДокументу,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаСчетаФактуры,
	|	ДанныеДокумента.Партнер КАК Партнер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Контрагент.ОбособленноеПодразделение
	|			ТОГДА ДанныеДокумента.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ДанныеДокумента.Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК НалогообложениеНДС,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Организация.ОбособленноеПодразделение
	|			ТОГДА ДанныеДокумента.Организация.ГоловнаяОрганизация
	|		ИНАЧЕ ДанныеДокумента.Организация
	|	КОНЕЦ КАК Организация,
	|	ДанныеДокумента.Организация.Префикс КАК Префикс,
	|	0 КАК ИндексПодразделения,
	|	ТаблицаОтветственныеЛица.РуководительНаименование КАК Руководитель,
	|	ТаблицаОтветственныеЛица.РуководительДолжность КАК ДолжностьРуководителя,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.Контрагент
	|		ИНАЧЕ ДанныеДокумента.Грузополучатель
	|	КОНЕЦ КАК Грузополучатель,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ДанныеДокумента.Организация
	|		ИНАЧЕ ДанныеДокумента.Грузоотправитель
	|	КОНЕЦ КАК Грузоотправитель,
	|	ДанныеДокумента.БанковскийСчетОрганизации КАК БанковскийСчетОрганизации,
	|	ДанныеДокумента.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
	|	ДанныеДокумента.БанковскийСчетГрузоотправителя КАК БанковскийСчетГрузоотправителя,
	|	ДанныеДокумента.БанковскийСчетГрузополучателя КАК БанковскийСчетГрузополучателя,
	|	ДанныеДокумента.Организация.КПП КАК КПППоставщика,
	|	""""                           КАК КПППокупателя,
	|	ДанныеДокумента.Контрагент.ИНН КАК ИННПокупателя,
	|	ДанныеДокумента.АдресДоставки КАК АдресДоставки,
	|	ДанныеДокумента.Валюта КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код КАК ВалютаКод,
	|	ВЫБОР
	|		КОГДА НоменклатураДокументов.ЕстьУслуги
	|				И НЕ НоменклатураДокументов.ЕстьТовары
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТолькоУслуги,
	|	НоменклатураДокументов.ЕстьПрослеживаемыеТовары КАК ЕстьПрослеживаемыеТовары,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоПередачаНаКомиссию,
	|	ДанныеДокумента.ОснованиеДляПечати КАК Основание,
	|	ДанныеДокумента.Основание КАК ДокументОснование,
	|	ДанныеДокумента.ОснованиеДата КАК ОснованиеДата,
	|	ДанныеДокумента.ОснованиеНомер КАК ОснованиеНомер,
	|	ДанныеДокумента.ДоверенностьНомер КАК ДоверенностьНомер,
	|	ДанныеДокумента.ДоверенностьДата КАК ДоверенностьДата,
	|	ДанныеДокумента.ДоверенностьВыдана КАК ДоверенностьВыдана,
	|	ДанныеДокумента.ДоверенностьЛицо КАК ДоверенностьЛицо,
	|	ДанныеДокумента.Отпустил КАК Кладовщик,
	|	ДанныеДокумента.ОтпустилДолжность КАК ДолжностьКладовщика,
	|	ДанныеДокумента.КодСпециальныхОбстоятельств КАК КодСпециальныхОбстоятельств,
	|	ИСТИНА КАК ТребуетсяНаличиеСФ
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураДокументов КАК НоменклатураДокументов
	|		ПО ДанныеДокумента.Ссылка = НоменклатураДокументов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровСХранения КАК ДанныеИсправления
	|		ПО ДанныеДокумента.ИсправляемыйДокумент = ДанныеИсправления.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ НомераИсправлений КАК НомераИсправлений
	|		ПО ДанныеДокумента.Ссылка = НомераИсправлений.Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Номенклатура.НаименованиеПолное КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ &ТекстЗапросаЕдиницаИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ &ТекстЗапросаНаименованиеЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ &ТекстЗапросаКодЕдиницыИзмерения
	|	КОНЕЦ КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения               КАК ЕдиницаИзмеренияТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияТНВЭДНаименование,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Код           КАК ЕдиницаИзмеренияТНВЭДКод,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.Серия.Наименование КАК СерияНаименование,
	|	ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС) КАК СтавкаНДС,
	|	ТаблицаДокумента.НомерГТД КАК НомерГТДСсылка,
	|	ЕСТЬNULL(ТаблицаДокумента.НомерГТД.ТипНомераГТД, НЕОПРЕДЕЛЕНО) КАК ТипНомераГТД,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НомерГТД.РегистрационныйНомер = """"
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ ТаблицаДокумента.НомерГТД.РегистрационныйНомер
	|	КОНЕЦ КАК НомерГТД,
	|	ТаблицаДокумента.КодТНВЭД КАК КодТНВЭД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Количество
	|		ИНАЧЕ ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Количество,
	|	ТаблицаДокумента.Количество КАК КоличествоБазовыхЕдиниц,
	|	ТаблицаДокумента.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество
	|		ИНАЧЕ ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ КАК Цена,
	|	ТаблицаДокумента.СуммаБезНДС КАК СуммаБезНДС,
	|	0                            КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаБезНДС КАК СуммаСНДС,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Серия КАК Серия,
	|	ТаблицаДокумента.Упаковка КАК Упаковка,
	|	ЛОЖЬ КАК ЭтоВозвратнаяТара
	|ИЗ
	|	ОтгрузкаТоваровСХраненияТаблицаТоваров КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|ИТОГИ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения.ШтрихкодыУпаковок КАК ТаблицаДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДокумента.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|		И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В(&МассивОбъектов)
	|	И ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка								КАК Ссылка,
	|	ТаблицаДокумента.НомерГТД							КАК НомерГТД,
	|	Комплектующие.НомерРНПТ								КАК НомерРНПТ,
	|	ВЫБОР
	|		КОГДА Комплектующие.НомерРНПТ.РегистрационныйНомер = """"
	|			ТОГДА Комплектующие.НомерРНПТ
	|		ИНАЧЕ Комплектующие.НомерРНПТ.РегистрационныйНомер
	|	КОНЕЦ												КАК НомерТовара,
	|	НомераГТД.СуммаПоРНПТ								КАК ОбщаяСуммаПоКомплекту,
	|	Комплектующие.СуммаПоРНПТ							КАК СуммаПоРНПТ,
	|	Комплектующие.КоличествоПоРНПТ						КАК Количество,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД					КАК ЕдиницаИзмерения,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Код				КАК ЕдиницаИзмеренияКод,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Представление	КАК ЕдиницаИзмеренияНаименование
	|ИЗ
	|	ОтгрузкаТоваровСХраненияТаблицаТоваров КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД.ПрослеживаемыеКомплектующие КАК Комплектующие
	|		ПО ТаблицаДокумента.НомерГТД = Комплектующие.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|		ПО ТаблицаДокумента.НомерГТД = НомераГТД.Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТИПЗНАЧЕНИЯ(Таблица.ЗаказКлиента) КАК ТипДокумента,
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА Таблица.ЗаказКлиента ССЫЛКА Документ.ЗаказДавальца2_5
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказДавальца2_5).НомерПоДаннымПартнера <> """"
	|							И ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказДавальца2_5).ДатаПоДаннымПартнера <> ДАТАВРЕМЯ(1,1,1)
	|						ТОГДА ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказДавальца2_5).ДатаПоДаннымПартнера		
	|					ИНАЧЕ ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказДавальца2_5).Дата
	|				КОНЕЦ
	//-- НЕ УТКА
	|		КОГДА Таблица.ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказКлиента).НомерПоДаннымКлиента <> """"
	|							И ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказКлиента).ДатаПоДаннымКлиента <> ДАТАВРЕМЯ(1,1,1)
	|						ТОГДА ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказКлиента).ДатаПоДаннымКлиента		
	|					ИНАЧЕ ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказКлиента).Дата
	|				КОНЕЦ
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
	|	КОНЕЦ КАК Дата,
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА Таблица.ЗаказКлиента ССЫЛКА Документ.ЗаказДавальца2_5
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказДавальца2_5).НомерПоДаннымПартнера <> """"
	|							И ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказДавальца2_5).ДатаПоДаннымПартнера <> ДАТАВРЕМЯ(1,1,1)
	|						ТОГДА ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказДавальца2_5).НомерПоДаннымПартнера		
	|					ИНАЧЕ ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказДавальца2_5).Номер
	|				КОНЕЦ
	//-- НЕ УТКА
	|		КОГДА Таблица.ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказКлиента).НомерПоДаннымКлиента <> """"
	|							И ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказКлиента).ДатаПоДаннымКлиента <> ДАТАВРЕМЯ(1,1,1)
	|						ТОГДА ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказКлиента).НомерПоДаннымКлиента		
	|					ИНАЧЕ ВЫРАЗИТЬ(Таблица.ЗаказКлиента КАК Документ.ЗаказКлиента).Номер
	|				КОНЕЦ
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Номер
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения.Товары КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка В (&МассивОбъектов)
	|";
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	
	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов			= Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	РезультатПоШапке			= МассивРезультатов[1];
	РезультатПоТабличнойЧасти	= МассивРезультатов[2];
	ШтрихкодыУпаковок			= МассивРезультатов[3].Выгрузить();
	РезультатПоЗаказам			= МассивРезультатов[5];
	
	Если ПараметрыПечати.Свойство("ЗаполнениеЭлектронногоДокумента") Тогда
		ТаблицаТоварыДляЭДО = ИнтеграцияИСМПУТ.ТаблицаТоварыДляЭДО(РезультатПоТабличнойЧасти.Выгрузить(), "КоличествоБазовыхЕдиниц");
		Если ТаблицаТоварыДляЭДО.Количество() Тогда
			ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования(ТаблицаТоварыДляЭДО[0].Ссылка);
		КонецЕсли;
		Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(
			ШтрихкодыУпаковок,
			ТаблицаТоварыДляЭДО,
			ПараметрыСканирования);
	Иначе
		Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(ШтрихкодыУпаковок);
	КонецЕсли;
	
	ПрослеживаемыеКомплектующие	= УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных(
									МассивРезультатов[4]);
	
	ДанныеДляПечати = Новый Структура;
	ДанныеДляПечати.Вставить("РезультатПоШапке",			РезультатПоШапке);
	ДанныеДляПечати.Вставить("РезультатПоТабличнойЧасти",	РезультатПоТабличнойЧасти);
	ДанныеДляПечати.Вставить("РезультатПоЗаказам",			РезультатПоЗаказам);
	ДанныеДляПечати.Вставить("Маркировка",					Маркировка);
	ДанныеДляПечати.Вставить("ПрослеживаемыеКомплектующие",	ПрослеживаемыеКомплектующие);
	
	Возврат ДанныеДляПечати;
	
КонецФункции

// Функция получает данные для формирования печатной формы УКД
//
// Параметры:
//	ПараметрыПечати - Структура
//	МассивОбъектов - Массив из ДокументСсылка.КорректировкаРеализации - Массив ссылок на документы, 
//																	по которым необходимо получить данные.
//
// Возвращаемое значение:
// 	Структура:
// 		* РезультатПоШапке - РезультатЗапроса
// 		* РезультатПоИсходнымДанным - РезультатЗапроса
// 		* РезультатПоТабличнойЧасти - РезультатЗапроса
// 		* ПрослеживаемыеКомплектующие - см. УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных
//
Функция ПолучитьДанныеДляПечатнойФормыУКД(ПараметрыПечати, МассивОбъектов) Экспорт
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	Если Не ЗначениеЗаполнено(КолонкаКодов) Тогда
		КолонкаКодов = "Код";
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка               КАК Ссылка,
	|	ДанныеДокументов.Исправление          КАК Исправление,
	|	ДанныеДокументов.Подразделение        КАК Подразделение,
	|	ДанныеДокументов.Организация          КАК Организация,
	|	ДанныеДокументов.Дата                 КАК Период,
	|	ДанныеДокументов.Склад                КАК Склад,
	|	ДанныеДокументов.ИсправляемыйДокумент КАК ИсправляемыйДокумент
	|ПОМЕСТИТЬ ТаблицаДанныхВсехДокументов
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Ссылка В (&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.Ссылка               КАК Ссылка,
	|	ДанныеДокументов.Подразделение        КАК Подразделение,
	|	ДанныеДокументов.Организация          КАК Организация,
	|	ДанныеДокументов.Период               КАК Период,
	|	ДанныеДокументов.Склад                КАК Склад,
	|	ДанныеДокументов.ИсправляемыйДокумент КАК ИсправляемыйДокумент
	|ПОМЕСТИТЬ ТаблицаДанныхДокументов
	|ИЗ
	|	ТаблицаДанныхВсехДокументов КАК ДанныеДокументов
	|ГДЕ
	|	ДанныеДокументов.Исправление
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка                КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаДанныхДокументов.Ссылка) КАК ПредставлениеСсылки
	|ИЗ
	|	ТаблицаДанныхВсехДокументов КАК ТаблицаДанныхДокументов
	|ГДЕ
	|	НЕ ТаблицаДанныхДокументов.Исправление
	|";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаОшибки = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока ВыборкаОшибки.Следующий() Цикл
		
		ТекстСообщения = НСтр("ru = 'Документ %1 не является исправлением. Печать универсального корректировочного документа (УКД) не требуется.';
								|en = 'The %1 document is not a correction document. Printing the universal adjustment document (UAD) is not required.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстСообщения,
			ВыборкаОшибки.ПредставлениеСсылки);
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстСообщения,
			ВыборкаОшибки.Ссылка);
		
	КонецЦикла;
	
	ПараметрыЗаполнения = ПродажиСервер.ПараметрыЗаполненияВременнойТаблицыТоваров();
	ПараметрыЗаполнения.ВключаяНомераГТД       = Истина;
	ПараметрыЗаполнения.ВключаяДоКорректировки = Истина;
	ПоместитьВременнуюТаблицуТоваров(МенеджерВременныхТаблиц, ПараметрыЗаполнения);
	
	Обработки.ПечатьОбщихФорм.ПоместитьВременнуюТаблицуДанныхПоставщика(МенеджерВременныхТаблиц);
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов,
		МенеджерВременныхТаблиц);
	СформироватьВременнуюТаблицуНоменклатураДокументов(МенеджерВременныхТаблиц);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка КАК Ссылка,
	|	ДанныеОснования.Номер          КАК НомерСчетаФактуры,
	|	ДанныеОснования.Дата           КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                   КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                   КАК ДатаИсправленияСчетаФактуры
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровСХранения КАК ДанныеОснования
	|		ПО ТаблицаДанныхДокументов.Ссылка.ИсправляемыйДокумент = ДанныеОснования.Ссылка
	|ИТОГИ ПО 
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                                КАК Ссылка,
	|	2                                                                     КАК СтатусУПД,
	|	ДанныеДокумента.Номер                                                 КАК Номер,
	|	ДанныеДокумента.Дата                                                  КАК Дата,
	|	НЕОПРЕДЕЛЕНО                                                          КАК НомерИсправления,
	|	НЕОПРЕДЕЛЕНО                                                          КАК ДатаИсправления,
	|	ДанныеДокумента.Исправление                                           КАК Исправление,
	|	ДанныеДокумента.ИсправляемыйДокумент                                  КАК ИсправляемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                                                          КАК НомерСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                                          КАК ДатаСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                                          КАК НомерИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                                          КАК ДатаИсправленияСчетаФактуры,
	|	НЕОПРЕДЕЛЕНО                                                          КАК ВалютаСчетаФактуры,
	|	ДанныеДокумента.Партнер                                               КАК Партнер,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.Контрагент.ОбособленноеПодразделение
	|			ТОГДА ДанныеДокумента.Контрагент.ГоловнойКонтрагент
	|		ИНАЧЕ ДанныеДокумента.Контрагент
	|	КОНЕЦ                                                                 КАК Контрагент,
	|	ЕСТЬNULL(ДанныеПоставщика.ГоловнаяОрганизация,
	|		ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))                    КАК Организация,
	|	ДанныеДокумента.Организация.Префикс                                   КАК Префикс,
	|	ДанныеДокумента.БанковскийСчетОрганизации                             КАК БанковскийСчетОрганизации,
	|	ДанныеДокумента.БанковскийСчетКонтрагента                             КАК БанковскийСчетКонтрагента,
	|	ДанныеДокумента.БанковскийСчетГрузоотправителя                        КАК БанковскийСчетГрузоотправителя,
	|	ДанныеДокумента.БанковскийСчетГрузополучателя                         КАК БанковскийСчетГрузополучателя,
	|	0                                                                     КАК ИндексПодразделения,
	|	ЕСТЬNULL(ТаблицаОтветственныеЛица.РуководительНаименование, """")     КАК Руководитель,
	|	ЕСТЬNULL(ТаблицаОтветственныеЛица.РуководительДолжность, """")        КАК ДолжностьРуководителя,
	|	ЕСТЬNULL(ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование, """") КАК ГлавныйБухгалтер,
	|	ЕСТЬNULL(ДанныеПоставщика.КПППоставщика, """")                        КАК КПППоставщика,
	|	ДанныеДокумента.Контрагент.КПП                                        КАК КПППокупателя,
	|	ДанныеДокумента.Валюта                                                КАК Валюта,
	|	ДанныеДокумента.Валюта.НаименованиеПолное                             КАК ВалютаНаименованиеПолное,
	|	ДанныеДокумента.Валюта.Код                                            КАК ВалютаКод,
	|	ДанныеДокумента.ОснованиеДляПечати                                    КАК Основание,
	|	ДанныеДокумента.Основание                                             КАК ДокументОснование,
	|	ДанныеДокумента.ОснованиеДата                                         КАК ОснованиеДата,
	|	ДанныеДокумента.ОснованиеНомер                                        КАК ОснованиеНомер,
	|	ЛОЖЬ                                                                  КАК ПечатьНеТребуется,
	|	ЛОЖЬ                                                                  КАК РеализацияЧерезКомиссионера,
	|	НЕОПРЕДЕЛЕНО                                                          КАК НалогообложениеНДС,
	|	ЕСТЬNULL(НоменклатураДокументов.ЕстьПрослеживаемыеТовары, ЛОЖЬ)       КАК ЕстьПрослеживаемыеТовары
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ДанныеДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокументов КАК ДанныеДокументов
	|		ПО ДанныеДокумента.Ссылка = ДанныеДокументов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоставщика КАК ДанныеПоставщика
	|		ПО ДанныеДокумента.Ссылка = ДанныеПоставщика.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураДокументов КАК НоменклатураДокументов
	|		ПО ДанныеДокумента.Ссылка = НоменклатураДокументов.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка                                        КАК Ссылка,
	|	ТаблицаДокумента.ЭтоНабор                                      КАК ЭтоНабор,
	|	ТаблицаДокумента.ЭтоКомплектующие                              КАК ЭтоКомплектующие,
	|	ТаблицаДокумента.Номенклатура                                  КАК Номенклатура,
	|	ТаблицаДокумента.Номенклатура.НаименованиеПолное               КАК НоменклатураНаименование,
	|	ВЫБОР
	|		КОГДА &КолонкаКодов = ""Артикул""
	|			ТОГДА ТаблицаДокумента.Номенклатура.Артикул
	|		ИНАЧЕ 
	|			ТаблицаДокумента.Номенклатура.Код
	|	КОНЕЦ                                                          КАК НоменклатураКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ
	|			&ТекстЗапросаЕдиницаИзмерения1
	|	КОНЕЦ                                                          КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Представление
	|		ИНАЧЕ
	|			&ТекстЗапросаНаименованиеЕдиницыИзмерения1
	|	КОНЕЦ                                                          КАК ЕдиницаИзмеренияНаименование,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Номенклатура.ЕдиницаИзмерения.Код
	|		ИНАЧЕ
	|			&ТекстЗапросаКодЕдиницыИзмерения1
	|	КОНЕЦ                                                          КАК ЕдиницаИзмеренияКод,
	|	ТаблицаДокумента.Характеристика                                КАК Характеристика,
	|	ТаблицаДокумента.Характеристика.НаименованиеПолное             КАК ХарактеристикаНаименование,
	|	ТаблицаДокумента.Упаковка                                      КАК Упаковка,
	|	ТаблицаДокумента.СтавкаНДС                                     КАК СтавкаНДС,
	|	ТаблицаДокумента.НомерГТД                                      КАК НомерГТДСсылка,
	|	ЕСТЬNULL(ТаблицаДокумента.НомерГТД.ТипНомераГТД, НЕОПРЕДЕЛЕНО) КАК ТипНомераГТД,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НомерГТД.РегистрационныйНомер = """"
	|			ТОГДА ТаблицаДокумента.НомерГТД
	|		ИНАЧЕ
	|			ТаблицаДокумента.НомерГТД.РегистрационныйНомер
	|	КОНЕЦ                                                          КАК НомерГТД,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения                  КАК СтранаПроисхождения,
	|	ТаблицаДокумента.НомерГТД.СтранаПроисхождения.Код              КАК СтранаПроисхожденияКод,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ТаблицаДокумента.Количество
	|		ИНАЧЕ
	|			ТаблицаДокумента.КоличествоУпаковок
	|	КОНЕЦ                                                          КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0)
	|		ИНАЧЕ
	|			ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0)
	|	КОНЕЦ                                                          КАК КоличествоДо,
	|	ТаблицаДокумента.КоличествоПоРНПТ                              КАК КоличествоПоРНПТ,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)         КАК КоличествоПоРНПТДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КоличествоПоРНПТ >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|			ТОГДА ТаблицаДокумента.КоличествоПоРНПТ - ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК КоличествоПрослежУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КоличествоПоРНПТ < ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0)
	|			ТОГДА -(ТаблицаДокумента.КоличествоПоРНПТ - ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоПоРНПТ, 0))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК КоличествоПрослежУменьшение,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА
	|				ВЫБОР 
	|					КОГДА ТаблицаДокумента.Количество <> 0
	|						ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.Количество
	|					ИНАЧЕ 
	|						0
	|				КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР 
	|				КОГДА ТаблицаДокумента.КоличествоУпаковок <> 0
	|					ТОГДА ТаблицаДокумента.СуммаБезНДС / ТаблицаДокумента.КоличествоУпаковок
	|				ИНАЧЕ
	|					0
	|			КОНЕЦ
	|	КОНЕЦ                                                          КАК Цена,
	|	ВЫБОР
	|		КОГДА &ВыводитьБазовыеЕдиницыИзмерения
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0) <> 0
	|						ТОГДА ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.Количество
	|					ИНАЧЕ
	|						0
	|				КОНЕЦ
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА ЕСТЬNULL(ТаблицаТоваровПредыдущие.КоличествоУпаковок, 0) <> 0
	|					ТОГДА ТаблицаТоваровПредыдущие.СуммаБезНДС / ТаблицаТоваровПредыдущие.КоличествоУпаковок
	|				ИНАЧЕ
	|					0
	|			КОНЕЦ
	|	КОНЕЦ                                                          КАК ЦенаДо,
	|	ТаблицаДокумента.СуммаБезНДС                                   КАК СуммаБезНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)              КАК СуммаБезНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК РазницаБезНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК РазницаБезНДСУменьшение,
	|	ТаблицаДокумента.СуммаНДС                                      КАК СуммаНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)                 КАК СуммаНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС >= ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК РазницаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаНДС < ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаНДС - ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК РазницаНДСУменьшение,
	|	ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС       КАК СуммаСНДС,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0)
	|		+ ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)           КАК СуммаСНДСДо,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС >=
	|				ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС -
	|				(ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК РазницаСНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС <
	|				ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)
	|			ТОГДА -(ТаблицаДокумента.СуммаБезНДС + ТаблицаДокумента.СуммаНДС -
	|				(ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаБезНДС, 0) + ЕСТЬNULL(ТаблицаТоваровПредыдущие.СуммаНДС, 0)))
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ                                                          КАК РазницаСНДСУменьшение,
	|	ТаблицаДокумента.НомерСтроки                                   КАК НомерСтроки,
	|	ЛОЖЬ                                                           КАК ЭтоВозвратнаяТара,
	|	1                                                              КАК Порядок,
	|	ТаблицаДокумента.КодТНВЭД                                      КАК КодТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения                     КАК ЕдиницаИзмеренияТНВЭД,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Представление       КАК ЕдиницаИзмеренияТНВЭДНаименование,
	|	ТаблицаДокумента.НомерГТД.ЕдиницаИзмерения.Код                 КАК ЕдиницаИзмеренияТНВЭДКод,
	|	ТаблицаДокумента.Количество                                    КАК КоличествоБазовыхЕдиниц,
	|	ЕСТЬNULL(ТаблицаТоваровПредыдущие.Количество, 0)               КАК КоличествоБазовыхЕдиницДо
	|ИЗ
	|	ОтгрузкаТоваровСХраненияТаблицаТоваров КАК ТаблицаДокумента
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	ОтгрузкаТоваровСХраненияТаблицаТоваров КАК ТаблицаТоваровПредыдущие
	|ПО
	|	ТаблицаТоваровПредыдущие.ДоКорректировки
	|	И ТаблицаДокумента.Ссылка = ТаблицаТоваровПредыдущие.Ссылка
	|	И ТаблицаДокумента.Номенклатура = ТаблицаТоваровПредыдущие.Номенклатура
	|	И ТаблицаДокумента.Характеристика = ТаблицаТоваровПредыдущие.Характеристика
	|	И ТаблицаДокумента.Упаковка = ТаблицаТоваровПредыдущие.Упаковка
	|	И ТаблицаДокумента.НомерГТД = ТаблицаТоваровПредыдущие.НомерГТД
	|	И ТаблицаДокумента.СтавкаНДС = ТаблицаТоваровПредыдущие.СтавкаНДС
	|ГДЕ
	|	НЕ ТаблицаДокумента.ДоКорректировки
	|	И (ТаблицаДокумента.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИЛИ ТаблицаДокумента.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокументов.Ссылка     КАК Ссылка,
	|	ТаблицаШтрихкодов.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	ТаблицаДанныхДокументов КАК ТаблицаДанныхДокументов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровСХранения.ШтрихкодыУпаковок КАК ТаблицаШтрихкодов
	|		ПО ТаблицаДанныхДокументов.Ссылка = ТаблицаШтрихкодов.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаДанныхДокументов.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|		И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредыдущиеДокументы.Основной         КАК Ссылка,
	|	ТаблицаШтрихкодовДо.ШтрихкодУпаковки КАК Штрихкод
	|ИЗ
	|	ПредыдущиеДокументы КАК ПредыдущиеДокументы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровСХранения.ШтрихкодыУпаковок КАК ТаблицаШтрихкодовДо
	|		ПО ПредыдущиеДокументы.Предыдущий = ТаблицаШтрихкодовДо.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровИСМП КАК ДокументыПрямогоОбмена
	|		ПО ТаблицаШтрихкодовДо.Ссылка = ДокументыПрямогоОбмена.ДокументОснование
	|		И НЕ ДокументыПрямогоОбмена.ПометкаУдаления
	|ГДЕ
	|	ДокументыПрямогоОбмена.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка                           КАК Ссылка,
	|	ТаблицаДокумента.НомерГТД                         КАК НомерГТД,
	|	Комплектующие.НомерРНПТ                           КАК НомерРНПТ,
	|	ВЫБОР
	|		КОГДА Комплектующие.НомерРНПТ.РегистрационныйНомер = """"
	|			ТОГДА Комплектующие.НомерРНПТ
	|		ИНАЧЕ
	|			Комплектующие.НомерРНПТ.РегистрационныйНомер
	|	КОНЕЦ                                             КАК НомерТовара,
	|	НомераГТД.СуммаПоРНПТ                             КАК ОбщаяСуммаПоКомплекту,
	|	Комплектующие.СуммаПоРНПТ                         КАК СуммаПоРНПТ,
	|	Комплектующие.КоличествоПоРНПТ                    КАК Количество,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД               КАК ЕдиницаИзмерения,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Код           КАК ЕдиницаИзмеренияКод,
	|	Комплектующие.ЕдиницаИзмеренияТНВЭД.Представление КАК ЕдиницаИзмеренияНаименование
	|ИЗ
	|	ОтгрузкаТоваровСХраненияТаблицаТоваров КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД.ПрослеживаемыеКомплектующие КАК Комплектующие
	|		ПО ТаблицаДокумента.НомерГТД = Комплектующие.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НомераГТД КАК НомераГТД
	|		ПО ТаблицаДокумента.НомерГТД = НомераГТД.Ссылка";
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаДокумента.Упаковка",
			"ТаблицаДокумента.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаЕдиницаИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Ссылка",
			"ТаблицаТоваровПредыдущие.Упаковка",
			"ТаблицаТоваровПредыдущие.Номенклатура"));
			
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаНаименованиеЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Наименование",
			"ТаблицаТоваровПредыдущие.Упаковка",
			"ТаблицаТоваровПредыдущие.Номенклатура"));
	
	Запрос.Текст = СтрЗаменить(
		Запрос.Текст,
		"&ТекстЗапросаКодЕдиницыИзмерения2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
			"Код",
			"ТаблицаТоваровПредыдущие.Упаковка",
			"ТаблицаТоваровПредыдущие.Номенклатура"));
	
	Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
	Запрос.УстановитьПараметр("КолонкаКодов", КолонкаКодов);
	Запрос.УстановитьПараметр("ПустаяУпаковкаНоменклатуры", Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	
	ВидыМаркируемойПродукции = ИнтеграцияИСМП.ИспользуемыеВидыМаркируемойПродукцииИСМП();
	ИнтеграцияИСМПУТ.УстановитьУсловиеПоВидуМаркируемойПродукции(
		Запрос.Текст,
		ВидыМаркируемойПродукции,
		"ТаблицаДокумента.Номенклатура");

	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов         = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	РезультатПоИсходнымДанным = МассивРезультатов[0];
	РезультатПоШапке          = МассивРезультатов[1];
	РезультатПоТабличнойЧасти = МассивРезультатов[2];
	
	Если ПараметрыПечати.Свойство("ЗаполнениеЭлектронногоДокумента") Тогда
		
		ТаблицаТоварыДляЭДО = ИнтеграцияИСМПУТ.ТаблицаТоварыДляЭДО(РезультатПоТабличнойЧасти.Выгрузить(),
			"КоличествоБазовыхЕдиницДо");
		
		Если ТаблицаТоварыДляЭДО.Количество() Тогда
			ПараметрыСканирования = ШтрихкодированиеОбщегоНазначенияИС.ПараметрыСканирования(ТаблицаТоварыДляЭДО[0].Ссылка);
		КонецЕсли;
		
		МаркировкаДо = МассивРезультатов[4].Выгрузить();
		МаркировкаДо = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(
			МаркировкаДо,
			ТаблицаТоварыДляЭДО,
			ПараметрыСканирования);
		
		Если МаркировкаДо.Количество() = 0 Тогда // Коды переданы через прямой обмен с ИС МП
			Маркировка = МаркировкаДо.СкопироватьКолонки();
		Иначе
			
			ТаблицаТоварыДляЭДО = ИнтеграцияИСМПУТ.ТаблицаТоварыДляЭДО(РезультатПоТабличнойЧасти.Выгрузить(),
				"КоличествоБазовыхЕдиниц");
			
			Маркировка = МассивРезультатов[3].Выгрузить();
			Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимоеИКодыОСУ(
				Маркировка,
				ТаблицаТоварыДляЭДО,
				ПараметрыСканирования);
			
		КонецЕсли;
		
	Иначе
		
		МаркировкаДо = МассивРезультатов[4].Выгрузить();
		МаркировкаДо = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(МаркировкаДо);
		
		Если МаркировкаДо.Количество() = 0 Тогда // Коды переданы через прямой обмен с ИС МП
			Маркировка = МаркировкаДо.СкопироватьКолонки();
		Иначе
			
			Маркировка = МассивРезультатов[3].Выгрузить();
			Маркировка = ЭлектронноеВзаимодействиеИСМП.ЧастичноеСодержимое(Маркировка);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПрослеживаемыеКомплектующие = УчетПрослеживаемыхТоваровЛокализация.ПрослеживаемыеКомплектующиеДляПечатиДанных(
		МассивРезультатов[5]);

	СтруктураДанныхДляПечати = Новый Структура;
	СтруктураДанныхДляПечати.Вставить("РезультатПоШапке",            РезультатПоШапке);
	СтруктураДанныхДляПечати.Вставить("РезультатПоТабличнойЧасти",   РезультатПоТабличнойЧасти);
	СтруктураДанныхДляПечати.Вставить("РезультатПоИсходнымДанным",   РезультатПоИсходнымДанным);
	СтруктураДанныхДляПечати.Вставить("Маркировка",                  Маркировка);
	СтруктураДанныхДляПечати.Вставить("МаркировкаДо",                МаркировкаДо);
	СтруктураДанныхДляПечати.Вставить("ПрослеживаемыеКомплектующие", ПрослеживаемыеКомплектующие);
	
	Возврат СтруктураДанныхДляПечати;
	
КонецФункции

//-- Локализация

#КонецОбласти

//++ НЕ УТ
#Область ПроводкиРегУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	ТекстыОтражения = Новый Массив;
	
	//++ Локализация
	
	ТекстыОтражения.Добавить(ТекстОтгрузкаТоваровПринятыхНаОтветХранение()); // Отгрузка товаров забалансом (Дт :: Кт 002, 003, 004)
	
	//-- Локализация
	
	Возврат СтрСоединить(ТекстыОтражения, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц,
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - сформированный текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	//++ Локализация
	//-- Локализация
	Возврат "";
	
КонецФункции

#КонецОбласти
//-- НЕ УТ

//++ Локализация

// Правила получения значений реквизитов ТТН
// 
// Возвращаемое значение:
//  см. Документы.ТранспортнаяНакладная.ПараметрыФормированияТранспортныхНакладных
//
Функция ПараметрыФормированияТранспортныхНакладных() Экспорт
	
	ПараметрыФормированияТранспортныхНакладных =
		Документы.ТранспортнаяНакладная.ПараметрыФормированияТранспортныхНакладных();
		
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.Грузоотправитель = 
	"	ВЫБОР
	|		КОГДА ОснованиеТранспортнойНакладной.Грузоотправитель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ОснованиеТранспортнойНакладной.Организация
	|		ИНАЧЕ ОснованиеТранспортнойНакладной.Грузоотправитель
	|	КОНЕЦ";
	
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.Грузополучатель = 
	"		ВЫБОР
	|			КОГДА ОснованиеТранспортнойНакладной.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ОснованиеТранспортнойНакладной.Контрагент
	|			ИНАЧЕ ОснованиеТранспортнойНакладной.Грузополучатель
	|		КОНЕЦ";
	
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.ЗаказчикПеревозки                 = "ОснованиеТранспортнойНакладной.Организация";
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.БанковскийСчетЗаказчикаПеревозки  = "ОснованиеТранспортнойНакладной.БанковскийСчетОрганизации";
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.Плательщик                        = "ОснованиеТранспортнойНакладной.Организация";
	ПараметрыФормированияТранспортныхНакладных.Реквизиты.БанковскийСчетПлательщика         = "ОснованиеТранспортнойНакладной.БанковскийСчетОрганизации";
	
	Возврат	ПараметрыФормированияТранспортныхНакладных;
	
КонецФункции

// Правила получения значений реквизитов ЭТН
// 
// Возвращаемое значение:
//  см. ОбменСГИСЭПДПереопределяемый.ПараметрыФормированияЭлектронныхТранспортныхНакладных
//
Функция ПараметрыФормированияЭлектронныхТранспортныхНакладных() Экспорт
	
	ПараметрыФормирования =
		ОбменСГИСЭПДПереопределяемый.ПараметрыФормированияЭлектронныхТранспортныхНакладных();

	ПараметрыФормирования.Реквизиты.СсылкаТитулГрузоотправителяГрузополучатель = 
	"		ВЫБОР
	|			КОГДА ОснованиеТранспортнойНакладной.Грузополучатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ОснованиеТранспортнойНакладной.Контрагент
	|			ИНАЧЕ ОснованиеТранспортнойНакладной.Грузополучатель
	|		КОНЕЦ";
	
	ПараметрыФормирования.ИмяПоляСуммы = "Сумма";
	
	Возврат	ПараметрыФормирования;
	
КонецФункции

// Правила получения значений реквизитов ЭЗН
// 
// Возвращаемое значение:
//  см. ОбменСГИСЭПДПереопределяемый.ПараметрыФормированияЭлектронныхЗаказНарядов
//
Функция ПараметрыФормированияЭлектронныхЗаказНарядов() Экспорт
	
	ПараметрыФормирования =
		ОбменСГИСЭПДПереопределяемый.ПараметрыФормированияЭлектронныхЗаказНарядов();
		
	Возврат	ПараметрыФормирования;
	
КонецФункции

//-- Локализация

#Область МаркируемаяПродукция

// Получить распоряжения.
// 
// Параметры:
//  ПроверяемыйДокумент - ДокументСсылка.ОтгрузкаТоваровСХранения - Документ распоряжение
// 
// Возвращаемое значение:
//  Массив из ДокументСсылка - документы распоряжения
Функция ПолучитьРаспоряженияПоДокументу(ПроверяемыйДокумент) Экспорт
	
	МассивРаспоряженийНаОтгрузку = Новый Массив;

//++ Локализация

	МассивРаспоряженийНаОтгрузку.Добавить(ПроверяемыйДокумент);
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ОтгрузкаТоваровСХраненияТовары.ЗаказКлиента КАК Распоряжение
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения.Товары КАК ОтгрузкаТоваровСХраненияТовары
	|ГДЕ
	|	ОтгрузкаТоваровСХраненияТовары.Ссылка = &ПроверяемыйДокумент
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	ОтгрузкаТоваровСХранения.Основание
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения КАК ОтгрузкаТоваровСХранения
	|ГДЕ
	|	ОтгрузкаТоваровСХранения.Ссылка = &ПроверяемыйДокумент
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("ПроверяемыйДокумент", ПроверяемыйДокумент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Распоряжение) Тогда
			МассивРаспоряженийНаОтгрузку.Добавить(Выборка.Распоряжение);
		КонецЕсли;
	КонецЦикла;

//-- Локализация

	Возврат МассивРаспоряженийНаОтгрузку;
	
КонецФункции

#Область ПодборМаркируемойПродукцииИзОрдеров

// Заполнить штрихкоды упаковок.
// 
// Параметры:
//  РаспоряженияДляОрдеров - Массив Из ДокументСсылка - Распоряжения для ордеров
//  Товары - ТаблицаЗначений - Товары
//  ШтрихкодыУпаковок - ТаблицаЗначений - Штрихкоды упаковок
Процедура ЗаполнитьШтрихкодыУпаковок(РаспоряженияДляОрдеров, Товары, ШтрихкодыУпаковок) Экспорт

//++ Локализация

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", РаспоряженияДляОрдеров);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтгрузкаТоваровСХраненияТовары.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ
	|	ПотребителиШтрихкодовУпаковок
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения.Товары КАК ОтгрузкаТоваровСХраненияТовары
	|ГДЕ
	|	ОтгрузкаТоваровСХраненияТовары.ЗаказКлиента В (&Ссылка)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтгрузкаТоваровСХраненияШтрихкодыУпаковок.ШтрихкодУпаковки
	|ИЗ
	|	Документ.ОтгрузкаТоваровСХранения.ШтрихкодыУпаковок КАК ОтгрузкаТоваровСХраненияШтрихкодыУпаковок
	|ГДЕ
	|	ОтгрузкаТоваровСХраненияШтрихкодыУпаковок.Ссылка В (ВЫБРАТЬ Ссылка ИЗ ПотребителиШтрихкодовУпаковок)";
	ИспользованныеШтрихкодыУпаковок = Запрос.Выполнить().Выгрузить();
	ИнтеграцияИСУТ.ЗаполнитьШтрихкодыУпаковок(РаспоряженияДляОрдеров, Товары, ШтрихкодыУпаковок, ИспользованныеШтрихкодыУпаковок);

//-- Локализация

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

// Процедура дополняет тексты запросов проведения документа.
//
// Параметры:
//  Запрос - Запрос - Общий запрос проведения документа.
//  ТекстыЗапроса - СписокЗначений - Список текстов запроса провведения.
//  Регистры - Строка, Структура - Список регистров продения документа через запятую или в ключах структуры.
//
Процедура ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры) Экспорт
	
	//++ Локализация

	//++ НЕ УТ
	РеглУчетПроведениеСервер.ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	//-- НЕ УТ

	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

//++ Локализация

//++ НЕ УТ

#Область ФрагментыПроводокРеглУчета

Функция ТекстОтгрузкаТоваровПринятыхНаОтветХранение()
	
	ТекстЗапроса = ПроизводствоБезЗаказаЛокализация.ТекстСписаниеТоваровЗаБалансом(Ложь, Ложь);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Содержание", "ВЫБОР
	|		КОГДА Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаДавальцу2_5)
	|		ТОГДА ""Передача продукции давальцу""
	|		КОГДА Строки.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДавальцу2_5)
	|		ТОГДА ""Возврат материалов давальца""
	|		ИНАЧЕ ""Отгрузка товаров, принятых на ответственное хранение""
	|	КОНЕЦ");
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

//-- НЕ УТ

Процедура СформироватьВременнуюТаблицуНоменклатураДокументов(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаТоваров.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ТаблицаТоваров.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьПрослеживаемыеТовары
	|ПОМЕСТИТЬ
	|	НоменклатураДокументов
	|ИЗ
	|	ОтгрузкаТоваровСХраненияТаблицаТоваров КАК ТаблицаТоваров
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Ссылка
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	Запрос.Выполнить();
	
КонецПроцедуры

//-- Локализация

#КонецОбласти
