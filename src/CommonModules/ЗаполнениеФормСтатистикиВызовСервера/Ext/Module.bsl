
#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание формы которую нужно открыть для настройки поля формы статистики
// подробнее см. РегистрыСведений.НастройкаЗаполненияФормСтатистики.ОписаниеФормыНастройкиПоля
//
Функция ОписаниеФормыНастройкиПоля(ИмяОтчета, ИмяФормы, ИмяПоля, Организация, ДатаНачала, ДатаОкончания) Экспорт
	
	Возврат РегистрыСведений.НастройкаЗаполненияФормСтатистики.ОписаниеФормыНастройкиПоля(
		ИмяОтчета, ИмяФормы, ИмяПоля, Организация, ДатаНачала, ДатаОкончания);
	
КонецФункции

// Проверяет выполнялась ли настройка заполнения для формы статистики
// Прараметры:
//	- ИмяРегламентированногоОтчета - Строка    - идентификатор отчета (имя отчета в метаданных).
//	- ИдентификаторФормы           - Строка    - идентификатор редакции отчета (имя формы в метаданных).
//	- Организация                  - СправочникСсылка.Организации - организация для которой нужно проверить настройки.
// Возвращаемое значение:
//	- Структура - содержит ключи
//		* ФормаСтатистики - СправочникСсылка.ФормыСтатистики.
//		* ВыполнялисьНастройкиФормы - Булево, истина - для этой формы настройки уже выполнялись, в противном случае ложь.
//
Функция ПроверитьНастройкиЗаполненияОтчета(ИмяРегламентированногоОтчета, ИдентификаторФормы, Организация) Экспорт
	
	ДоступноРедактированиеНастроек = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкаЗаполненияФормСтатистики);
	Если Не ДоступноРедактированиеНастроек Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяОтчета", ИмяРегламентированногоОтчета);
	Отбор.Вставить("ИмяФормы",  ИдентификаторФормы);
	ОписаниеФормы = ЗаполнениеФормСтатистики.НастраиваемыеФормыСтатистики(Отбор);
	Если ОписаниеФормы.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ФормаСтатистики = ОписаниеФормы[0].ФормаСтатистики;
		
	ВыполнялисьНастройкиФормы = РегистрыСведений.СостояниеНастройкиЗаполненияФормСтатистики.ВыполнялисьНастройкиФормы(
		ФормаСтатистики, 
		Организация);
	
	Результат = Новый Структура;
	Результат.Вставить("ФормаСтатистики",           ФормаСтатистики);
	Результат.Вставить("ВыполнялисьНастройкиФормы", ВыполнялисьНастройкиФормы);
	Возврат Результат;
	
КонецФункции

// Устанавливает признак того что для формы статистики выполнялась настройка
// подробнее см. РегистрыСведений.СостояниеНастройкиЗаполненияФормСтатистики.ЗаписатьНастройкиВыполнены
//
Процедура ЗаписатьНастройкиВыполнены(ФормаСтатистики, Организация) Экспорт
	
	РегистрыСведений.СостояниеНастройкиЗаполненияФормСтатистики.ЗаписатьНастройкиВыполнены(ФормаСтатистики, Организация);
	
КонецПроцедуры

Функция ФормаЗаполняетсяОтдельноПоКаждомуОбособленномуПодразделению(ИмяОтчета) Экспорт

	ФормыЗаполняемыеПоКаждомуОП = Новый Массив;
	ЗаполнениеФормСтатистикиПоОбособленнымПодразделениямПереопределяемый.ДобавитьФормыЗаполняемыеОтдельноПоОбособленнымПодразделениям(ФормыЗаполняемыеПоКаждомуОП);
	
	Возврат ФормыЗаполняемыеПоКаждомуОП.Найти(ИмяОтчета) <> Неопределено;

КонецФункции

// Получить объект наблюдения.
// 
// Параметры:
//  ДополнительныеПараметры - Структура:
//  			Организация - СправочникСсылка.Организации.
//  			ДатаОкончания - Дата.
//  			УникальныйИдентификатор - Строка.
//  			ИмяОтчета - Строка.
//  			ИмяФормы - Строка.
// 
// Возвращаемое значение:
//  СправочникСсылка.ОбъектыСтатистическогоНаблюдения - Получить объект наблюдения
//
Функция ПолучитьОбъектНаблюдения(ДополнительныеПараметры) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяОтчета", ДополнительныеПараметры.ИмяОтчета);
	Отбор.Вставить("ИмяФормы",  ДополнительныеПараметры.ИмяФормы);
	НастраиваемыеФормыСтатистики = ЗаполнениеФормСтатистики.НастраиваемыеФормыСтатистики(Отбор);
	
	Если Не ЗначениеЗаполнено(НастраиваемыеФормыСтатистики) Тогда
		Возврат Справочники.ОбъектыСтатистическогоНаблюдения.ПустаяСсылка();
	КонецЕсли;
	ФормаСтатистики = НастраиваемыеФормыСтатистики[0].ФормаСтатистики;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФормаСтатистики", ФормаСтатистики);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоляФормСтатистики.СтатистическийПоказатель.Владелец КАК ОбъектНаблюдения
	|ПОМЕСТИТЬ НастраиваемыеОбъектыНаблюдения
	|ИЗ
	|	Справочник.ПоляФормСтатистики КАК ПоляФормСтатистики
	|ГДЕ
	|	ПоляФормСтатистики.Владелец = &ФормаСтатистики
	|	И НЕ ПоляФормСтатистики.ПометкаУдаления
	|	И НЕ ПоляФормСтатистики.ЭтоГруппа
	|	И ПоляФормСтатистики.СтатистическийПоказатель <> ЗНАЧЕНИЕ(Справочник.СтатистическиеПоказатели.ПустаяСсылка)
	|	И ПоляФормСтатистики.СтатистическийПоказатель.Детализировать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбъектыСтатистическогоНаблюдения.Ссылка КАК ОбъектНаблюдения
	|ИЗ
	|	НастраиваемыеОбъектыНаблюдения КАК НастраиваемыеОбъектыНаблюдения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыСтатистическогоНаблюдения КАК ОбъектыСтатистическогоНаблюдения
	|		ПО НастраиваемыеОбъектыНаблюдения.ОбъектНаблюдения = ОбъектыСтатистическогоНаблюдения.Ссылка
	|ГДЕ
	|	ОбъектыСтатистическогоНаблюдения.Детализация = ЗНАЧЕНИЕ(Перечисление.ВидыСвободныхСтрокФормСтатистики.ВидыДеятельности)
	|	И НастраиваемыеОбъектыНаблюдения.ОбъектНаблюдения.ТребуетНастройки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Тогда
		Возврат Справочники.ОбъектыСтатистическогоНаблюдения.ПустаяСсылка();
	КонецЕсли;
	
	// Предполагается только один объект наблюдения для конкретного отчета.
	Возврат Выборка.ОбъектНаблюдения;
		
КонецФункции

// Обработать распределение выручки по ОКВЭД.
// 
// Параметры:
//  ДополнительныеПараметры - Структура:
//  			Организация - СправочникСсылка.Организации.
//  			ДатаОкончания - Дата.
//  			УникальныйИдентификатор - Строка.
//  
Процедура ОбработатьРаспределениеВыручкиПоОКВЭД(ДополнительныеПараметры) Экспорт
	
	ОбъектНаблюдения = ПолучитьОбъектНаблюдения(ДополнительныеПараметры);
	РегистрыСведений.НастройкиКлассификацииВыручкиПоОКВЭД.ЗаполнитьРаспределениеВыручкиПоОКВЭД(
														ДополнительныеПараметры, ОбъектНаблюдения);
		
КонецПроцедуры

// Определяет список возможных действий расшифровки.
//
// Параметры:
//  АдресРасшифровки		 - Строка										 - Адрес во временном хранилище куда помещены данные расшифровки.
//  ИдентификаторРасшифровки - ИдентификаторРасшифровкиКомпоновкиДанных 	 - Идентификатор расшифровки из ячейки для которой вызвана расшифровка.
// 
// Возвращаемое значение:
//   СписокЗначений - Возможные действия расшифровки отчета.
//
Функция ВозможныеРасшифровки(Знач АдресРасшифровки, Знач ИдентификаторРасшифровки) Экспорт

	ВозможныеРасшифровки = Новый СписокЗначений;
	
	Если Не ЭтоАдресВременногоХранилища(АдресРасшифровки) Тогда
		Возврат ВозможныеРасшифровки;
	КонецЕсли;
		
	Расшифровка = ПолучитьИзВременногоХранилища(АдресРасшифровки);
	
	ТекущийЭлементРасшифровки = Расшифровка.Элементы.Получить(ИдентификаторРасшифровки);
	ПоляТекущейРасшифровки = ТекущийЭлементРасшифровки.ПолучитьПоля();
	
	Если ПоляТекущейРасшифровки.Количество() > 0
		И (ТекущийЭлементРасшифровки.ОсновноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение 
		ИЛИ ПоляТекущейРасшифровки.Количество() = 1) Тогда
		
			ЗначениеПоля = ПоляТекущейРасшифровки[0].Значение;
			Если ЗначениеЗаполнено(ЗначениеПоля) И ОбщегоНазначения.ЭтоСсылка(ТипЗнч(ЗначениеПоля)) Тогда
				ВозможныеРасшифровки.Добавить(ПоляТекущейРасшифровки[0].Значение, СтрШаблон(НСтр("ru = 'Открыть ""%1""';
																								|en = 'Open ""%1""'"), ПоляТекущейРасшифровки[0].Значение));
			КонецЕсли;
			
	КонецЕсли;
	
	ВозможныеРасшифровки.Добавить("УстановитьКодОКВЭД", НСтр("ru = 'Установить код ОКВЭД';
															|en = 'Установить код ОКВЭД'"));
	
	Возврат ВозможныеРасшифровки;
	
КонецФункции

// Получить компоновщик настроек.
// 
// Параметры:
//  	ПараметрыФормы - Структура:
//  			Организация - СправочникСсылка.Организации.
//  			ДатаОкончания - Дата.
//  			УникальныйИдентификатор - Строка.
//  			ИмяОтчета - Строка.
//  			ИмяФормы - Строка.
// 
// Возвращаемое значение:
//  КомпоновщикНастроекКомпоновкиДанных - Получить компоновщик настроек
Функция ПолучитьКомпоновщикНастроек(ПараметрыФормы) Экспорт
	
	КомпоновщикНастроек = Отчеты.КлассификацияВыручкиПоОКВЭД.Создать().КомпоновщикНастроек;
	Настройки = КомпоновщикНастроек.Настройки;
	
	Ключ     = СтрЗаменить(Строка(ПараметрыФормы.ИмяОтчета = "РегламентированныйОтчетСтатистикаФорма1П") + ПараметрыФормы.Организация.УникальныйИдентификатор(), "-", "");
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Настройки, "Организация", Ключ);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек.ПользовательскиеНастройки, "Организация", Ключ);
	
	ОбъектНаблюдения = ПолучитьОбъектНаблюдения(ПараметрыФормы);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Настройки, "ОбъектНаблюдения", ОбъектНаблюдения);
	
	ПараметрПериод = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(Настройки, "Период");
	ПараметрПериод.Значение.ДатаНачала = ПараметрыФормы.ДатаНачала;
	ПараметрПериод.Значение.ДатаОкончания = ПараметрыФормы.ДатаОкончания;
	ПараметрПериод.Использование = Истина;
	
	Возврат КомпоновщикНастроек;
	
КонецФункции

#КонецОбласти