////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции редактора производственного процесса
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

#Область ЭтапПроизводства

// Возвращает параметры Стандартного техпроцесс этапа.
// 
// Параметры:
//  Этап - ДокументСсылка.ЭтапПроизводства2_2
// 
// Возвращаемое значение:
//  см. РедакторПроизводственногоПроцессаКлиентСервер.ПараметрыТехпроцессаКонструктор 
Функция СтандартныйТехпроцессЭтапа(Этап) Экспорт
	
	Возврат ПараметрыТехпроцессаЭтапа(Этап, Истина);
	
КонецФункции

Функция ПараметрыТехпроцессаЭтапа(Этап, СтандартныйТехпроцесс = Ложь) Экспорт
	
	ПараметрыТехпроцесса = РедакторПроизводственногоПроцессаКлиентСервер.ПараметрыТехпроцессаКонструктор();
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&ПараметрыТехпроцесса
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК РеквизитыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТехнологическиеПроцессыЭтаповПроизводства КАК Техпроцесс
	|		ПО РеквизитыДокумента.Ссылка = Техпроцесс.Этап
	|			И НЕ &СтандартныйТехпроцесс
	|ГДЕ
	|	РеквизитыДокумента.Ссылка = &Этап
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ПараметрыТехпроцесса",
		РедакторПроизводственногоПроцесса.ТекстЗапросаПараметрыТехпроцессаЭтапа());
		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Этап", Этап);
	Запрос.УстановитьПараметр("СтандартныйТехпроцесс", СтандартныйТехпроцесс);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыТехпроцесса, Выборка);
	КонецЕсли;
	
	Возврат ПараметрыТехпроцесса;
	
КонецФункции

Процедура ПереключитьНаИндивидуальныйТехпроцесс(Этап, Отказ) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Документ.ПроизводственнаяОперация2_2 КАК Т
	|ГДЕ
	|	Т.Проведен
	|	И Т.Этап = &Этап
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Т
	|ГДЕ
	|	Т.Этап = &Этап
	|	И Т.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Этап", Этап);
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'У этапа есть назначенные операции. Переключение в индивидуальный режим невозможно.';
				|en = 'The stage has assigned operations. Cannot switch to individual mode.'"),,,,Отказ);
		Возврат;
	КонецЕсли;
	
	ДанныеЭтапа = Документы.ЭтапПроизводства2_2.ДанныеДляРасчетаОчередиОпераций(Этап, Ложь)[Этап];
	
	Если ДанныеЭтапа.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Индивидуальный Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Технологический процесс этапа уже находится в индивидуальном режиме.';
				|en = 'The technological process of the stage is already in individual mode.'"),,,,Отказ);
		Возврат;
	КонецЕсли;
	
	ДанныеЭтапа.Вставить("Операции", РегистрыСведений.ОчередьПроизводственныхОпераций.СписокОперацийПоДаннымЭтапа(ДанныеЭтапа));
	
	НачатьТранзакцию();
	Попытка
		
		Запись = РегистрыСведений.ТехнологическиеПроцессыЭтаповПроизводства.СоздатьМенеджерЗаписи();
		Запись.Этап = Этап;
		Запись.Прочитать();
		Если НЕ Запись.Выбран() Тогда
			Запись.Этап = Этап;
		КонецЕсли;
		Запись.ТехнологическийПроцесс = Неопределено;
		Запись.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Индивидуальный;
		Запись.КоэффициентТехнологическогоПроцесса = 1;
		Запись.Записать();
		
		РегистрыСведений.ОчередьПроизводственныхОпераций.РассчитатьОчередьПоДаннымЭтапа(
			Этап, ДанныеЭтапа, РегистрыСведений.ОчередьПроизводственныхОпераций.ПараметрыРасчетаОчереди());
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Ошибка расчета очереди операций этапа. %1';
										|en = 'An error occurred while calculating the stage operation queue. %1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Отказ = Истина;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТКА

#Область ТехнологическаяОперация

// Данные выбора операции по классификатору.
// 
// Параметры:
//  ПолеПоиска - Строка
//  Текст - Строка
// 
// Возвращаемое значение:
//  СписокЗначений из СправочникСсылка.КлассификаторТехнологическихОпераций
Функция ДанныеВыбораОперацииПоКлассификатору(ПолеПоиска, Текст) Экспорт
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Если НЕ ПустаяСтрока(Текст) Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 51
		|	Классификатор.Ссылка       КАК Ссылка,
		|	Классификатор.Код          КАК Код,
		|	Классификатор.Наименование КАК Наименование
		|ИЗ
		|	Справочник.КлассификаторТехнологическихОпераций КАК Классификатор
		|ГДЕ
		|	&ПолеПоиска ПОДОБНО &ТекстПоиска
		|	И НЕ Классификатор.ЭтоГруппа
		|	И НЕ Классификатор.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	&ПолеПоиска";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ПолеПоиска",
			СтрШаблон("Классификатор.%1", ПолеПоиска));
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("ТекстПоиска", Текст+"%");
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ДанныеВыбора.Добавить(Выборка.Ссылка, СтрШаблон("%1 (%2)", Выборка.Наименование, Выборка.Код));
			
		КонецЦикла;
	
	КонецЕсли;
	
	Возврат ДанныеВыбора;
	
КонецФункции

#КонецОбласти

//-- НЕ УТКА

//++ НЕ УТКА

#Область Редактор

// Начать запись данных техпроцесса.
// 
// Параметры:
//  ДанныеДляЗавершенияРедактирования - Структура - данные для создания
//  УИД - УникальныйИдентификатор
// 
// Возвращаемое значение:
//  см. ДлительныеОперации.ВыполнитьВФоне
Функция НачатьЗаписьДанныхТехпроцесса(ДанныеДляЗавершенияРедактирования, УИД) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УИД);
	ПараметрыВыполнения.ЗапуститьВФоне    = Истина;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = РедакторПроизводственногоПроцессаКлиентСервер.ФоноваяОперацияЗаписьДанных();
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.РедакторПроизводственногоПроцесса.ЗаписатьДанныеРедактораВФоне",
		ПолучитьИзВременногоХранилища(ДанныеДляЗавершенияРедактирования.АдресПараметровЗаписиТехпроцесса),
		ПараметрыВыполнения);
	
КонецФункции

// Начать создание типового техпроцесса.
// 
// Параметры:
//  ДанныеДляСозданияТехпроцесса - Структура - данные для создания
//  УИД - УникальныйИдентификатор
// 
// Возвращаемое значение:
//  см. ДлительныеОперации.ВыполнитьВФоне
Функция НачатьСозданиеТиповогоТехпроцесса(ДанныеДляСозданияТехпроцесса, УИД) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УИД);
	ПараметрыВыполнения.ЗапуститьВФоне    = Истина;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = РедакторПроизводственногоПроцессаКлиентСервер.ФоноваяОперацияСозданиеТехпроцесса();
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.РедакторПроизводственногоПроцесса.СоздатьТиповойТехпроцессВФоне",
		ПолучитьИзВременногоХранилища(ДанныеДляСозданияТехпроцесса.АдресПараметровСозданияТиповогоТехпроцесса),
		ПараметрыВыполнения);
	
КонецФункции

#КонецОбласти

//-- НЕ УТКА

#КонецОбласти