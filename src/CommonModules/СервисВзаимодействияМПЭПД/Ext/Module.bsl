
#Область СлужебныйПрограммныйИнтерфейс

#Область ОбменДаннымиССервером

// Проверяет работоспособность сервиса.
// 
// Возвращаемое значение:
//  Булево - Признак работоспособности сервера.
Функция СервисПроверитьРаботоспособность() Экспорт
	
	Результат = Ложь;
	
	ПараметрыОперации = ПараметрыВыполненияОперацииСервиса();
	ПараметрыОперации.АдресРесурса = "common/heartbeat";
	ПараметрыОперации.Метод = "GET";
	ПараметрыОперации.Таймаут = 7;
	
	ОтветСервиса = ВыполнитьОперацию(ПараметрыОперации);
	Если ОтветСервиса.Успех Тогда
		ДанныеОтвета = СтруктураИзJson(ОтветСервиса.Ответ.ПолучитьТелоКакСтроку(), Истина);
		Результат = ЗначениеЗаполнено(ДанныеОтвета["Date"]);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Региструет организацию.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
//  Организация - ОпределяемыйТип.Организация 
//  Маркер - Строка - Маркер Хаба.
// 
// Возвращаемое значение:
//  Булево - Признак успешности операции.
Функция СервисРегистрацияОрганизации(ИдентификаторЭДО, Организация, Маркер) Экспорт
	
	Результат = Ложь;
	
	ДанныеОрганизации = ИнтеграцияЭДО.РегистрационныеДанныеОрганизации(Организация);
	АдресПочты = ЗначениеКонтактнойИнформации(Организация, "Справочник.ВидыКонтактнойИнформации.EmailОрганизации");

	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("INN", СвойствоСтруктуры(ДанныеОрганизации, "ИНН"));
	ПараметрыЗапроса.Вставить("KPP", СвойствоСтруктуры(ДанныеОрганизации, "КПП"));
	ПараметрыЗапроса.Вставить("OGRN", СвойствоСтруктуры(ДанныеОрганизации, "ОГРН"));
	ПараметрыЗапроса.Вставить("Title", СвойствоСтруктуры(ДанныеОрганизации, "Наименование"));
	ПараметрыЗапроса.Вставить("AbonentEDO", ИдентификаторЭДО);
	ПараметрыЗапроса.Вставить("OperatorEDO", Лев(ИдентификаторЭДО, 3));
	ПараметрыЗапроса.Вставить("Email", АдресПочты);
	ПараметрыЗапроса.Вставить("Marker", Маркер);
	ПараметрыЗапроса.Вставить("Phone", СвойствоСтруктуры(ДанныеОрганизации, "Телефон"));
	ПараметрыЗапроса.Вставить("Ticket", ПолучитьСтруктуруДанныхТикетаПодпискиИТС());
	ПараметрыЗапроса.Вставить("version", ВерсияБиблиотеки());
	
	ПараметрыОперации = ПараметрыВыполненияОперацииСервиса(ИдентификаторЭДО);
	ПараметрыОперации.АдресРесурса = "service/registrationEDO";
	ПараметрыОперации.Метод = "POST";
	ПараметрыОперации.ТелоЗапроса = СтруктураВJSON(ПараметрыЗапроса);

	ОтветСервиса = ВыполнитьОперацию(ПараметрыОперации);
	Если ОтветСервиса.Успех Тогда
		ДанныеОтвета = СтруктураИзJson(ОтветСервиса.Ответ.ПолучитьТелоКакСтроку(), Истина, Новый Структура("ИменаСвойствДаты", "expiration"));
		
		ЗаписьРегистра = РегистрыСведений.НастройкиВзаимодействияМПЭПД.ПараметрыЗаписиРегистра(ИдентификаторЭДО);
		ЗаписьРегистра.Организация = Организация;
		ЗаписьРегистра.СрокДействияТокена = СвойствоСтруктуры(ДанныеОтвета, "expiration"); 
		ЗаписьРегистра.ТокенДоступа = СвойствоСтруктуры(ДанныеОтвета, "token");
		ЗаписьРегистра.ТокенОбновления = СвойствоСтруктуры(ДанныеОтвета, "refresh_token");
		
		РегистрыСведений.НастройкиВзаимодействияМПЭПД.РегистрироватьЗапись(ЗаписьРегистра);
		Результат = Истина;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Удаляет подключение по идентификатору ЭДО.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО
// 
// Возвращаемое значение:
//  Булево - Признак успешности операции.
Функция СервисУдалениеОрганизации(ИдентификаторЭДО) Экспорт
	
	Результат = Ложь;
	
	ТокенДоступа = ПолучитьТокенДоступа(ИдентификаторЭДО);
	
	ПараметрыОперации = ПараметрыВыполненияОперацииСервиса(ИдентификаторЭДО);
	ПараметрыОперации.АдресРесурса = "service/deleteEDO";
	ПараметрыОперации.Метод = "POST";
	ПараметрыОперации.Токен = ТокенДоступа.Токен;
	ПараметрыОперации.ТокенОбновления = ТокенДоступа.ТокенОбновления;

	ОтветСервиса = ВыполнитьОперацию(ПараметрыОперации);
	Если ОтветСервиса.Успех Тогда
		ЗаписьРегистра = РегистрыСведений.НастройкиВзаимодействияМПЭПД.ПараметрыЗаписиРегистра(ИдентификаторЭДО);
		РегистрыСведений.НастройкиВзаимодействияМПЭПД.УдалитьЗапись(ЗаписьРегистра);
		Результат = Истина;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Добавляет пользователя.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
//  ДанныеВодителя - Структура - Данные водителя.
// 
// Возвращаемое значение:
//  Строка - Код регистрации.
Функция СервисДобавитьПользователя(ИдентификаторЭДО, ДанныеВодителя) Экспорт
	
	Результат = Неопределено;
	
	ТокенДоступа = ПолучитьТокенДоступа(ИдентификаторЭДО);

	ПараметрыЗапроса = Новый Структура;
	Если ДанныеВодителя.Свойство("ИдентификаторМП")
		И Не ПустаяСтрока(СвойствоСтруктуры(ДанныеВодителя, "ИдентификаторМП")) Тогда
		ПараметрыЗапроса.Вставить("Number", СвойствоСтруктуры(ДанныеВодителя, "ИдентификаторМП"));
		ПараметрыЗапроса.Вставить("repeat", Истина);
	КонецЕсли;
	
	ЕстьБумажнаяДоверенность = СвойствоСтруктуры(ДанныеВодителя, "ЕстьБумажнаяДоверенность", Ложь);
	
	СтруктураДанныхДоверенности = Неопределено;
	Если ЕстьБумажнаяДоверенность И ЗначениеЗаполнено(ДанныеВодителя.БумажнаяДоверенностьНомер) Тогда
		СтруктураДанныхДоверенности = Новый Структура;
		СтруктураДанныхДоверенности.Вставить("БумажнаяДоверенность", Истина);
		СтруктураДанныхДоверенности.Вставить("Номер", ДанныеВодителя.БумажнаяДоверенностьНомер);
		СтруктураДанныхДоверенности.Вставить("Дата", ДанныеВодителя.БумажнаяДоверенностьДата);
		
	ИначеЕсли Не ЕстьБумажнаяДоверенность И ЗначениеЗаполнено(ДанныеВодителя.МЧД) Тогда
		// Полный список полей см. ТранспортныеКонтейнерыЭДО.НовоеОписаниеДанныхДоверенности
		ДанныеДоверенности = МашиночитаемыеДоверенности.ДанныеДоверенностиДляКонтейнера(ДанныеВодителя.МЧД);
		СтруктураДанныхДоверенности = Новый Структура;
		СтруктураДанныхДоверенности.Вставить("БумажнаяДоверенность", Ложь);
		СтруктураДанныхДоверенности.Вставить("Номер", ДанныеДоверенности.НомерДоверенности);
		СтруктураДанныхДоверенности.Вставить("Дата", ДанныеДоверенности.ДатаВыдачи);
		СтруктураДанныхДоверенности.Вставить("ДатаОкончания", ДанныеДоверенности.ДатаОкончания);
		СтруктураДанныхДоверенности.Вставить("СсылкаНаДоверенностьВРеестре", ДанныеДоверенности.СсылкаНаДоверенностьВРеестре);
		
	КонецЕсли;
	
	Если СтруктураДанныхДоверенности <> Неопределено Тогда
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		НастройкаСериализации = Новый НастройкиСериализацииJSON();
		НастройкаСериализации.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
		ЗаписатьJSON(ЗаписьJSON, СтруктураДанныхДоверенности, НастройкаСериализации);
		ДанныеДоверенности = ЗаписьJSON.Закрыть();

		ПараметрыЗапроса.Вставить("Attorney", ДанныеДоверенности);
	КонецЕсли;
	
	ПараметрыЗапроса.Вставить("FIO", СвойствоСтруктуры(ДанныеВодителя, "Наименование"));
	ПараметрыЗапроса.Вставить("SNILS", СвойствоСтруктуры(ДанныеВодителя, "СНИЛС"));
	ПараметрыЗапроса.Вставить("INN", СвойствоСтруктуры(ДанныеВодителя, "ИНН"));
	ПараметрыЗапроса.Вставить("Role", СвойствоСтруктуры(ДанныеВодителя, "РольСУчетомДокументов"));
	ПараметрыЗапроса.Вставить("Marker", СвойствоСтруктуры(ДанныеВодителя, "Маркер"));
	ПараметрыЗапроса.Вставить("Phone", СвойствоСтруктуры(ДанныеВодителя, "Телефон"));
	ПараметрыЗапроса.Вставить("ExtID", СвойствоСтруктуры(ДанныеВодителя, "Идентификатор"));
	
	СтруктураВУ = Новый Структура;
	СтруктураВУ.Вставить("Series", СвойствоСтруктуры(ДанныеВодителя, "СерияВУ"));
	СтруктураВУ.Вставить("Number", СвойствоСтруктуры(ДанныеВодителя, "НомерВУ"));
	СтруктураВУ.Вставить("Date", СвойствоСтруктуры(ДанныеВодителя, "ДатаВыдачиВУ"));
	
	ПараметрыЗапроса.Вставить("DriverLicence", СтруктураВУ);
	
	ПараметрыЗапроса.Вставить("KindSign", ОпределитьВидПодписи(СвойствоСтруктуры(ДанныеВодителя, "ВидЭП")));
	
	ПараметрыЗапроса.Вставить("DirectExchange", СвойствоСтруктуры(ДанныеВодителя, "ПрямойОбмен", Ложь));
	ПараметрыЗапроса.Вставить("PaperAttorney", ЕстьБумажнаяДоверенность);
	ПараметрыЗапроса.Вставить("IdentificationDocuments", СвойствоСтруктуры(ДанныеВодителя, "ЛичныеДокументыДляОтправкиВМП"));
	
	ПараметрыОперации = ПараметрыВыполненияОперацииСервиса(ИдентификаторЭДО);
	ПараметрыОперации.АдресРесурса = "service/registrationMA";
	ПараметрыОперации.Метод = "POST";
	ПараметрыОперации.ТелоЗапроса = СтруктураВJSON(ПараметрыЗапроса);
	ПараметрыОперации.Токен = ТокенДоступа.Токен;
	ПараметрыОперации.ТокенОбновления = ТокенДоступа.ТокенОбновления;

	ОтветСервиса = ВыполнитьОперацию(ПараметрыОперации);
	Если ОтветСервиса.Успех Тогда
		ДанныеОтвета = СтруктураИзJson(ОтветСервиса.Ответ.ПолучитьТелоКакСтроку(), Истина, Новый Структура("ИменаСвойствДаты", "expiration"));
		Результат = СвойствоСтруктуры(ДанныеОтвета, "code"); 
		
		Идентификатор = СвойствоСтруктуры(ДанныеВодителя, "Идентификатор");
		
		НоваяЗапись = РегистрыСведений.ПодключенныеМПЭПД.ПараметрыЗаписиРегистра(ИдентификаторЭДО, Идентификатор);
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ДанныеВодителя);
		НоваяЗапись.Состояние = Перечисления.СостояниеМПЭПД.ОжидаетсяПодключение;
		НоваяЗапись.Телефоны = СвойствоСтруктуры(ДанныеВодителя, "Телефон");
		НоваяЗапись.ВидПодписи = СвойствоСтруктуры(ДанныеВодителя, "ВидЭП");
		НоваяЗапись.ДатаВремяПолученияКодаРегистрации = ТекущаяУниверсальнаяДата();
		Если ДанныеВодителя.Свойство("МЧД") Тогда
			НоваяЗапись.МЧД = ДанныеВодителя.МЧД;
		КонецЕсли;
		НоваяЗапись.КодРегистрации = Результат;
		НоваяЗапись.ПрямойОбмен =  СвойствоСтруктуры(ДанныеВодителя, "ПрямойОбмен", Ложь);
		НоваяЗапись.ЕстьБумажнаяДоверенность =  СвойствоСтруктуры(ДанныеВодителя, "ЕстьБумажнаяДоверенность", Ложь);
		
		ЛичныеДокументыДляОтправкиВМП = СвойствоСтруктуры(ДанныеВодителя, "ЛичныеДокументыДляОтправкиВМП");
		ХранилищеЛичныеДокументыДляОтправкиВМП = Новый ХранилищеЗначения(ЛичныеДокументыДляОтправкиВМП, Новый СжатиеДанных(6));
		НоваяЗапись.ЛичныеДокументыДляОтправкиВМП = ХранилищеЛичныеДокументыДляОтправкиВМП;
		
		РегистрыСведений.ПодключенныеМПЭПД.РегистрироватьЗапись(НоваяЗапись);
		
		ИзменитьИспользованиеЗаданияПолучениеНовыхУведомлений(Истина);
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Получает пользователей МП и записывает их данные в регистр.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
// 
// Возвращаемое значение:
//  Структура - Сервис получить пользователей:
// * УспешностьЗагрузки - Булево - 
// * ЕстьУстройстваОжидающиеПодключение - Булево - 
Функция СервисПолучитьПользователей(ИдентификаторЭДО) Экспорт
	
	ТекущаяУниверсальнаяДатаПередЗапросом = ТекущаяУниверсальнаяДата();
	
	СтруктураРезультата = НовыйРезультатПолученияСпискаПользователей();
	СтруктураРезультата.УспешностьЗагрузки = Истина;
	
	МассивИдентификаторов = Новый Массив;
	Если ТипЗнч(ИдентификаторЭДО) = Тип("Строка") Тогда
		МассивИдентификаторов.Добавить(ИдентификаторЭДО);
	Иначе
		МассивИдентификаторов = ИдентификаторЭДО;
	КонецЕсли;                         
	
	Для Каждого СтрокаЭДО Из МассивИдентификаторов Цикл
		ТокенДоступа = ПолучитьТокенДоступа(СтрокаЭДО);
		
		Если ТокенДоступа.Токен = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыОперации = ПараметрыВыполненияОперацииСервиса(СтрокаЭДО);
		ПараметрыОперации.АдресРесурса = "service/listMA";
		ПараметрыОперации.Метод = "GET";
		ПараметрыОперации.Токен = ТокенДоступа.Токен;
		ПараметрыОперации.ТокенОбновления = ТокенДоступа.ТокенОбновления;

		ОтветСервиса = ВыполнитьОперацию(ПараметрыОперации);
		СтруктураРезультата.УспешностьЗагрузки = СтруктураРезультата.УспешностьЗагрузки И ОтветСервиса.Успех;
		
		Если ОтветСервиса.Успех Тогда
			
			ДанныеОтвета = СтруктураИзJson(ОтветСервиса.Ответ.ПолучитьТелоКакСтроку(), Истина);
			
			НаборЗаписей = Новый Массив;
			Для Каждого СтрокаМассива Из ДанныеОтвета Цикл
				НоваяЗапись = РегистрыСведений.ПодключенныеМПЭПД.ПараметрыЗаписиРегистра(
					СтрокаЭДО, СтрокаМассива["ExtID"]);
				НоваяЗапись.ИдентификаторМП = СтрокаМассива["Number"];
				НоваяЗапись.ИНН = СтрокаМассива["INN"];
				НоваяЗапись.СНИЛС = СтрокаМассива["SNILS"];
				
				СтруктураВУ = СтрокаМассива["DriverLicence"];
				Если СтруктураВУ <> Неопределено Тогда
					НоваяЗапись.СерияВУ = СтруктураВУ["Series"];
					НоваяЗапись.НомерВУ = СтруктураВУ["Number"];
					НоваяЗапись.ДатаВыдачиВУ = XMLЗначение(Тип("Дата"), СтруктураВУ["Date"]);
				КонецЕсли;
				
				НоваяЗапись.Состояние = СоответствиеСостояниеМП(СтрокаМассива["Status"]);
				НоваяЗапись.Наименование = СтрокаМассива["Title"];
				НоваяЗапись.Телефоны = СтрокаМассива["Phones"];
				НаборЗаписей.Добавить(НоваяЗапись);
			КонецЦикла;
			
			РезультатРегистрации = РегистрыСведений.ПодключенныеМПЭПД.РегистрироватьНабор(
					СтрокаЭДО, НаборЗаписей, ТекущаяУниверсальнаяДатаПередЗапросом);
			
			Если РезультатРегистрации.ЕстьУстройстваОжидающиеПодключение Тогда
				СтруктураРезультата.ЕстьУстройстваОжидающиеПодключение = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла; // По массиву идентификаторов

	Возврат СтруктураРезультата;
	
КонецФункции

// Сервис удалить пользователя.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
//  ИдентификаторМП - Строка - Идентификатор МП.
//  Идентификатор - Строка - Идентификатор.
// 
// Возвращаемое значение:
//  Булево - Признак успешности удаления пользователя.
Функция СервисУдалитьПользователя(ИдентификаторЭДО, ИдентификаторМП, Идентификатор) Экспорт
	
	Результат = Ложь;
	
	Если Не ЗначениеЗаполнено(ИдентификаторМП) Тогда
		Результат = Истина;
		ПараметрыЗаписи = РегистрыСведений.ПодключенныеМПЭПД.ПараметрыЗаписиРегистра(ИдентификаторЭДО, Идентификатор);
		РегистрыСведений.ПодключенныеМПЭПД.УдалитьЗапись(ПараметрыЗаписи);
		Возврат Результат;
	КонецЕсли;
	
	ТокенДоступа = ПолучитьТокенДоступа(ИдентификаторЭДО);
	
	ПараметрыЗапроса = Новый Структура("Number", ИдентификаторМП);
	
	ПараметрыОперации = ПараметрыВыполненияОперацииСервиса(ИдентификаторЭДО);
	ПараметрыОперации.АдресРесурса = "service/deleteMA";
	ПараметрыОперации.Метод = "POST";
	ПараметрыОперации.ТелоЗапроса = СтруктураВJSON(ПараметрыЗапроса);
	ПараметрыОперации.Токен = ТокенДоступа.Токен;
	ПараметрыОперации.ТокенОбновления = ТокенДоступа.ТокенОбновления;

	ОтветСервиса = ВыполнитьОперацию(ПараметрыОперации);
	Если ОтветСервиса.Успех Тогда
		ДанныеОтвета = СтруктураИзJson(ОтветСервиса.Ответ.ПолучитьТелоКакСтроку());
		Результат = ДанныеОтвета.success;
		
		Если Результат = Истина Тогда 
			УдалитьЗаписьПодключенныхМПЭПД(ИдентификаторЭДО, Идентификатор);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Удаляет список пользователей.
// 
// Параметры:
//  УчетныеЗаписи - Массив из Строка - Учетные записи.
// 
// Возвращаемое значение:
//  Булево - Признак успешности выполнения операции.
Функция СервисУдалитьСписокПользователей(УчетныеЗаписи) Экспорт
	
	Всего = УчетныеЗаписи.Количество();
	
	Для Каждого СтрокаМассива Из УчетныеЗаписи Цикл
		Если СервисУдалитьПользователя(СтрокаМассива.ИдентификаторЭДО, СтрокаМассива.ИдентификаторМП, СтрокаМассива.Идентификатор) = Истина Тогда
			Всего = Всего - 1;
		КонецЕсли;
	КонецЦикла;

	Возврат Всего = 0;
	
КонецФункции

// Приостановливает работу пользователя.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
//  ИдентификаторМП - Строка - Идентификатор МП.
// 
// Возвращаемое значение:
//  Булево - Признак успешности выполнения операции.
Функция СервисПриостановитьРаботуПользователя(ИдентификаторЭДО, ИдентификаторМП) Экспорт
	
	Результат = Ложь;
	
	ТокенДоступа = ПолучитьТокенДоступа(ИдентификаторЭДО);
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Number", ИдентификаторМП);
	ПараметрыЗапроса.Вставить("status", "Недоступен");
	       
	ПараметрыОперации = ПараметрыВыполненияОперацииСервиса(ИдентификаторЭДО);
	ПараметрыОперации.АдресРесурса = "service/status_ma";
	ПараметрыОперации.Метод = "POST";
	ПараметрыОперации.ТелоЗапроса = СтруктураВJSON(ПараметрыЗапроса);
	ПараметрыОперации.Токен = ТокенДоступа.Токен;
	ПараметрыОперации.ТокенОбновления = ТокенДоступа.ТокенОбновления;

	ОтветСервиса = ВыполнитьОперацию(ПараметрыОперации);
	Если ОтветСервиса.Успех Тогда
		ДанныеОтвета = СтруктураИзJson(ОтветСервиса.Ответ.ПолучитьТелоКакСтроку());
		Если ДанныеОтвета.Свойство("status")
			И ДанныеОтвета.status = "Недоступен" Тогда
			Результат = Истина;
		Иначе
			Результат = Ложь;
		КонецЕсли;
		Если Результат = Истина Тогда
			Состояние = Перечисления.СостояниеМПЭПД.Приостановлен;
			РегистрыСведений.ПодключенныеМПЭПД.ИзменитьСостояниеМП(ИдентификаторМП, Состояние);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возбновляет работу пользователя.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
//  ИдентификаторМП - Строка - Идентификатор МП.
// 
// Возвращаемое значение:
//  Неопределено, Булево - Признак успешности выполнения операции. Неопределено, если запрос к серверу не был выполнен.
Функция СервисВозобновитьРаботуПользователя(ИдентификаторЭДО, ИдентификаторМП) Экспорт
	
	Результат = Неопределено;
	
	ТокенДоступа = ПолучитьТокенДоступа(ИдентификаторЭДО);
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Number", ИдентификаторМП);
	ПараметрыЗапроса.Вставить("status", "Свободен");
	       
	ПараметрыОперации = ПараметрыВыполненияОперацииСервиса(ИдентификаторЭДО);
	ПараметрыОперации.АдресРесурса = "service/status_ma";
	ПараметрыОперации.Метод = "POST";
	ПараметрыОперации.ТелоЗапроса = СтруктураВJSON(ПараметрыЗапроса);
	ПараметрыОперации.Токен = ТокенДоступа.Токен;
	ПараметрыОперации.ТокенОбновления = ТокенДоступа.ТокенОбновления;

	ОтветСервиса = ВыполнитьОперацию(ПараметрыОперации);
	Если ОтветСервиса.Успех Тогда
		ДанныеОтвета = СтруктураИзJson(ОтветСервиса.Ответ.ПолучитьТелоКакСтроку());
		Если ДанныеОтвета.Свойство("status")
			И ДанныеОтвета.status = "Свободен" Тогда
			Результат = Истина;
		Иначе
			Результат = Ложь;
		КонецЕсли;
		Если Результат = Истина Тогда
			Состояние = Перечисления.СостояниеМПЭПД.Подключен;
			РегистрыСведений.ПодключенныеМПЭПД.ИзменитьСостояниеМП(ИдентификаторМП, Состояние);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Изменяет данные пользователя.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО. 
//  ДанныеВодителя - Структура - Данные водителя.
// 
// Возвращаемое значение:
//  Неопределено, Булево - Признак успешности выполнения операции. Неопределено, если запрос к серверу не был выполнен.
Функция СервисИзменитьПользователя(ИдентификаторЭДО, ДанныеВодителя) Экспорт
	
	Результат = Неопределено;
	
	ТокенДоступа = ПолучитьТокенДоступа(ИдентификаторЭДО);
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Number", ДанныеВодителя.ИдентификаторМП);
	ПараметрыЗапроса.Вставить("KindSign", ОпределитьВидПодписи(СвойствоСтруктуры(ДанныеВодителя, "ВидЭП")));
	ПараметрыЗапроса.Вставить("SNILS", СвойствоСтруктуры(ДанныеВодителя, "СНИЛС"));
	ПараметрыЗапроса.Вставить("INN", СвойствоСтруктуры(ДанныеВодителя, "ИНН"));
	ПараметрыЗапроса.Вставить("Role", СвойствоСтруктуры(ДанныеВодителя, "Роль"));
	
	СтруктураВУ = Новый Структура;
	СтруктураВУ.Вставить("Series", СвойствоСтруктуры(ДанныеВодителя, "СерияВУ"));
	СтруктураВУ.Вставить("Number", СвойствоСтруктуры(ДанныеВодителя, "НомерВУ"));
	СтруктураВУ.Вставить("Date", СвойствоСтруктуры(ДанныеВодителя, "ДатаВыдачиВУ"));
	
	ПараметрыЗапроса.Вставить("DriverLicence", СтруктураВУ);
	
	Телефон = СвойствоСтруктуры(ДанныеВодителя, "Телефоны");
	Если СтрНачинаетсяС(Телефон, "+") Тогда
		Телефон = Сред(Телефон, 2);
	КонецЕсли;
	ПараметрыЗапроса.Вставить("PhoneList", Телефон);

	ЕстьБумажнаяДоверенность = СвойствоСтруктуры(ДанныеВодителя, "ЕстьБумажнаяДоверенность", Ложь)
		И ЗначениеЗаполнено(ДанныеВодителя.БумажнаяДоверенностьНомер);
	
	СтруктураДанныхДоверенности = Неопределено;
	Если ЕстьБумажнаяДоверенность И ЗначениеЗаполнено(ДанныеВодителя.БумажнаяДоверенностьНомер) Тогда
		СтруктураДанныхДоверенности = Новый Структура;
		СтруктураДанныхДоверенности.Вставить("БумажнаяДоверенность", Истина);
		СтруктураДанныхДоверенности.Вставить("Номер", ДанныеВодителя.БумажнаяДоверенностьНомер);
		СтруктураДанныхДоверенности.Вставить("Дата", ДанныеВодителя.БумажнаяДоверенностьДата);
		
	ИначеЕсли Не ЕстьБумажнаяДоверенность И ЗначениеЗаполнено(ДанныеВодителя.МЧД) Тогда
		// Полный список полей см. ТранспортныеКонтейнерыЭДО.НовоеОписаниеДанныхДоверенности
		ДанныеДоверенности = МашиночитаемыеДоверенности.ДанныеДоверенностиДляКонтейнера(ДанныеВодителя.МЧД);
		СтруктураДанныхДоверенности = Новый Структура;
		СтруктураДанныхДоверенности.Вставить("БумажнаяДоверенность", Ложь);
		СтруктураДанныхДоверенности.Вставить("Номер", ДанныеДоверенности.НомерДоверенности);
		СтруктураДанныхДоверенности.Вставить("Дата", ДанныеДоверенности.ДатаВыдачи);
		СтруктураДанныхДоверенности.Вставить("ДатаОкончания", ДанныеДоверенности.ДатаОкончания);
		СтруктураДанныхДоверенности.Вставить("СсылкаНаДоверенностьВРеестре", ДанныеДоверенности.СсылкаНаДоверенностьВРеестре);
		
	КонецЕсли;
	
	Если СтруктураДанныхДоверенности <> Неопределено Тогда
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		НастройкаСериализации = Новый НастройкиСериализацииJSON();
		НастройкаСериализации.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
		ЗаписатьJSON(ЗаписьJSON, СтруктураДанныхДоверенности, НастройкаСериализации);
		ДанныеДоверенности = ЗаписьJSON.Закрыть();
		
		ПараметрыЗапроса.Вставить("Attorney", ДанныеДоверенности);
	КонецЕсли;
	
	ПараметрыЗапроса.Вставить("DirectExchange", СвойствоСтруктуры(ДанныеВодителя, "ПрямойОбмен", Ложь));
	ПараметрыЗапроса.Вставить("PaperAttorney", ЕстьБумажнаяДоверенность);
	ПараметрыЗапроса.Вставить("IdentificationDocuments", СвойствоСтруктуры(ДанныеВодителя, "ЛичныеДокументыДляОтправкиВМП"));
		
	ПараметрыОперации = ПараметрыВыполненияОперацииСервиса(ИдентификаторЭДО);
	ПараметрыОперации.АдресРесурса = "service/changeMA";
	ПараметрыОперации.Метод = "POST";
	ПараметрыОперации.ТелоЗапроса = СтруктураВJSON(ПараметрыЗапроса);
	ПараметрыОперации.Токен = ТокенДоступа.Токен;
	ПараметрыОперации.ТокенОбновления = ТокенДоступа.ТокенОбновления;

	ОтветСервиса = ВыполнитьОперацию(ПараметрыОперации);
	Если ОтветСервиса.Успех Тогда
		ДанныеОтвета = СтруктураИзJson(ОтветСервиса.Ответ.ПолучитьТелоКакСтроку());
		Результат = ДанныеОтвета.status; 
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Получает уведомления.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
//  ТочкаНачала - Число - Точка начала.
// 
// Возвращаемое значение:
//  Структура - Сервис получить уведомления:
// * УведомленияСвязанныеСДокументами - Массив из Строка 
// * Точка - Число, Неопределено
//
//@skip-check doc-comment-field-type
//@skip-check doc-comment-description-ends-on-dot
//@skip-check doc-comment-field-type-strict
Функция СервисПолучитьУведомления(ИдентификаторЭДО, ТочкаНачала) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("УведомленияСвязанныеСДокументами", Новый Массив);
	Результат.Вставить("Точка");
	
	ТокенДоступа = ПолучитьТокенДоступа(ИдентификаторЭДО);
	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("point", Формат(ТочкаНачала, "ЧН=0; ЧГ=0"));

	ПараметрыОперации = ПараметрыВыполненияОперацииСервиса(ИдентификаторЭДО);
	ПараметрыОперации.АдресРесурса = "service/notifications";
	ПараметрыОперации.Метод = "GET";
	ПараметрыОперации.Токен = ТокенДоступа.Токен;
	ПараметрыОперации.ТокенОбновления = ТокенДоступа.ТокенОбновления;
	ПараметрыОперации.ПараметрыЗапроса = ПараметрыЗапроса;

	ОтветСервиса = ВыполнитьОперацию(ПараметрыОперации);
	Если ОтветСервиса.Успех Тогда
		СоответствиеТитуловТипамМП = СервисВзаимодействияМПЭПДКлиентСервер.СоответствиеТитуловТипамМП();
		
		ДанныеОтвета = СтруктураИзJson(ОтветСервиса.Ответ.ПолучитьТелоКакСтроку(), Ложь, Новый Структура("ИменаСвойствДаты", "Date"));
		Для Каждого СтрокаМассива Из ДанныеОтвета.List Цикл
			ТекущаяЗапись = РегистрыСведений.КэшВзаимодействияМПЭПД.ПараметрыЗаписиРегистра();
			ТекущаяЗапись.ИдентификаторЭДО = ИдентификаторЭДО;
			ТекущаяЗапись.Вставить("ВнешнийИД", СтрокаМассива.ID);
			ТекущаяЗапись.Вставить("Дата", СтрокаМассива.Date);
			ТекущаяЗапись.Вставить("ИдентификаторДокумента", СтрокаМассива.IDobject);
			ТекущаяЗапись.Вставить("Содержимое", СтрокаМассива.Content);
			ТекущаяЗапись.Вставить("ДанныеСообщения", СтрокаМассива.ServiceData);
			ТекущаяЗапись.Вставить("ИдентификаторМП", СтрокаМассива.IDrecipient);
			ТекущаяЗапись.Вставить("ВидСообщения", СоответствиеУведомлений(СтрокаМассива.Kind));
			ТекущаяЗапись.Вставить("ИдентификаторРодителя", СтрокаМассива.IDparent);
			
			ВыполнитьЗапись = Истина;
			Если Не ПустаяСтрока(СтрокаМассива.ServiceData) Тогда
				СтруктураДанныхСообщения = СтруктураИзJson(СтрокаМассива.ServiceData);
				
				Если СервисВзаимодействияМПЭПДКлиентСервер.ЭтоТипСообщенияОбОтправкеТитулаИзМП(СтруктураДанныхСообщения.Type)
					Или СоответствиеТитуловТипамМП.Получить(СтруктураДанныхСообщения.Type) <> Неопределено Тогда
					Результат.УведомленияСвязанныеСДокументами.Добавить(ТекущаяЗапись);
					ВыполнитьЗапись = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			Если ВыполнитьЗапись Тогда
				РегистрыСведений.КэшВзаимодействияМПЭПД.РегистрироватьЗапись(ТекущаяЗапись);
			КонецЕсли;
		КонецЦикла;
		
		Результат.Точка = ДанныеОтвета.LastPoint;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Получаеть события.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
//  ТочкаНачала - Число - Точка начала.
// 
// Возвращаемое значение:
//  Структура - Сервис получить события:
// * События - Массив из Структура - см.  РегистрыСведений.КэшВзаимодействияМПЭПД.ПараметрыЗаписиРегистра().
// * Точка - Неопределено, Число
// 
//@skip-check function-return-types
//@skip-check doc-comment-field-type-strict
//@skip-check doc-comment-description-ends-on-dot
//@skip-check doc-comment-field-type
Функция СервисПолучитьСобытия(ИдентификаторЭДО, ТочкаНачала) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("События", Новый Массив);
	Результат.Вставить("Точка");
	
	ТокенДоступа = ПолучитьТокенДоступа(ИдентификаторЭДО);
	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("point", Формат(ТочкаНачала, "ЧН=0; ЧГ=0"));
	ПараметрыЗапроса.Вставить("details", "true");

	ПараметрыОперации = ПараметрыВыполненияОперацииСервиса(ИдентификаторЭДО);
	ПараметрыОперации.АдресРесурса = "common/events";
	ПараметрыОперации.Метод = "GET";
	ПараметрыОперации.Токен = ТокенДоступа.Токен;
	ПараметрыОперации.ТокенОбновления = ТокенДоступа.ТокенОбновления;

	ПараметрыОперации.ПараметрыЗапроса = ПараметрыЗапроса;

	ОтветСервиса = ВыполнитьОперацию(ПараметрыОперации);
	Если ОтветСервиса.Успех Тогда
		ДанныеОтвета = СтруктураИзJson(ОтветСервиса.Ответ.ПолучитьТелоКакСтроку(), Ложь, Новый Структура("ИменаСвойствДаты", "Date"));
		ВсеСобытия = Новый Массив;
		Для Каждого СтрокаМассива Из ДанныеОтвета.events Цикл
			ВидСобытия = СоответствиеУведомлений(СтрокаМассива.IDEvent);
			Если ЗначениеЗаполнено(ВидСобытия) Тогда
				ТекущаяЗапись = РегистрыСведений.КэшВзаимодействияМПЭПД.ПараметрыЗаписиРегистра();
				ТекущаяЗапись.ИдентификаторЭДО = ИдентификаторЭДО;
				ТекущаяЗапись.Дата = СтрокаМассива.Date;
				ТекущаяЗапись.Содержимое = СтрокаМассива.NameEvent;
				ТекущаяЗапись.ВидСообщения = ВидСобытия;
				ТекущаяЗапись.ИдентификаторДокумента = СтрокаМассива.IDEPD;
				Если СтрокаМассива.Свойство("IDrecipient") Тогда
					ТекущаяЗапись.ИдентификаторМП = СтрокаМассива.IDrecipient;
				КонецЕсли;
				ВсеСобытия.Добавить(ТекущаяЗапись);
			КонецЕсли;
		КонецЦикла;
		Результат.Вставить("События", ВсеСобытия);
		Результат.Вставить("Точка", ДанныеОтвета.point);
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Отправляет уведомления.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
//  Параметры - Произвольный, Структура - Параметры:
// * ИдентификаторДокумента - Строка 
// * Содержимое - Строка 
// * ДанныеСообщения - Строка 
// * ИдентификаторМП - Строка 
// 
// Возвращаемое значение:
//  Неопределено, Структура - Сервис отправить уведомления:
// * Идентификатор - Строка
// * Дата - Дата 
Функция СервисОтправитьУведомления(ИдентификаторЭДО, Параметры) Экспорт
	
	Результат = Неопределено;
	
	ТокенДоступа = ПолучитьТокенДоступа(ИдентификаторЭДО);
	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("content", СвойствоСтруктуры(Параметры, "Содержимое", ""));
	ПараметрыЗапроса.Вставить("service_data",  СвойствоСтруктуры(Параметры, "ДанныеСообщения", ""));
	ПараметрыЗапроса.Вставить("kind", СоответствиеУведомлений(СвойствоСтруктуры(Параметры, "ВидСообщения", Перечисления.ТипыВзаимодействияМПЭПД.Сообщение)));
	ПараметрыЗапроса.Вставить("idobject", СвойствоСтруктуры(Параметры, "ИдентификаторДокумента"));
	ПараметрыЗапроса.Вставить("recipient", СвойствоСтруктуры(Параметры, "ИдентификаторМП"));

	ПараметрыОперации = ПараметрыВыполненияОперацииСервиса(ИдентификаторЭДО);
	ПараметрыОперации.АдресРесурса = "service/notification";
	ПараметрыОперации.Метод = "POST";
	ПараметрыОперации.Токен = ТокенДоступа.Токен;
	ПараметрыОперации.ТокенОбновления = ТокенДоступа.ТокенОбновления;
	ПараметрыОперации.ТелоЗапроса = СтруктураВJSON(ПараметрыЗапроса);

	ОтветСервиса = ВыполнитьОперацию(ПараметрыОперации);
	Если ОтветСервиса.Успех Тогда
		ДанныеОтвета = СтруктураИзJson(ОтветСервиса.Ответ.ПолучитьТелоКакСтроку(), Ложь, Новый Структура("ИменаСвойствДаты", "Date"));
		Результат = Новый Структура;
		Результат.Вставить("Идентификатор", ДанныеОтвета.ID);
		Результат.Вставить("Дата", ДанныеОтвета.Date);
	КонецЕсли;

	//@skip-check constructor-function-return-section
	Возврат Результат;
	
КонецФункции

// Изменяет свойства документа на сервере МП.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО
//  ИдентификаторДокумента - Строка - Идентификатор документа.
//  ТипВзаимодействияМПЭПД - ПеречислениеСсылка.ТипыВзаимодействияМПЭПД
//  Документ - ОпределяемыйТип.ДокументыЭПД
//  СтруктураДанныхПоРаботеМП - Неопределено, Структура - Структура данных по работе МП.
//  СтатусДокумента - Неопределено, Строка - Статус документа.
//  НаименованиеМП - Неопределено, Строка - Наименование МП.
// 
// Возвращаемое значение:
//  Булево, Неопределено - Признак успешности выполнения операции. Неопределено, если запрос к серверу не был выполнен.
//  
Функция СервисИзменитьДокумент(ИдентификаторЭДО, ИдентификаторДокумента, ТипВзаимодействияМПЭПД, Документ,
		СтруктураДанныхПоРаботеМП = Неопределено, СтатусДокумента = Неопределено, НаименованиеМП = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Если СтруктураДанныхПоРаботеМП = Неопределено Тогда
		ИдентификаторЗаписиМП = Неопределено;
		ИдентификаторМП = Неопределено;
		НаименованиеМП = Неопределено;
	Иначе
		ИдентификаторЗаписиМП = СтруктураДанныхПоРаботеМП.ИдентификаторЗаписиМП;
		ИдентификаторМП = СтруктураДанныхПоРаботеМП.ИдентификаторМП;
	КонецЕсли;
	
	ТокенДоступа = ПолучитьТокенДоступа(ИдентификаторЭДО);
	РаботаВыполняемаяПользователем = РаботаВыполняемаяПользователем(ТипВзаимодействияМПЭПД, Документ);
	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("DocFlow", ИдентификаторДокумента);
	ПараметрыЗапроса.Вставить("DocType", СервисВзаимодействияМПЭПДКлиентСервер.ТипДокументаМП(Документ));
	ПараметрыЗапроса.Вставить("IDMA", ИдентификаторМП);
	ПараметрыЗапроса.Вставить("Work", РаботаВыполняемаяПользователем);
	Если СтатусДокумента <> Неопределено Тогда
		ПараметрыЗапроса.Вставить("Status", СоответствиеСостояниеЭПД(СтатусДокумента));
	КонецЕсли;

	ПараметрыОперации = ПараметрыВыполненияОперацииСервиса(ИдентификаторЭДО);
	ПараметрыОперации.АдресРесурса = "edo/SetEPD";
	ПараметрыОперации.Метод = "POST";
	ПараметрыОперации.Токен = ТокенДоступа.Токен;
	ПараметрыОперации.ТокенОбновления = ТокенДоступа.ТокенОбновления;
	ПараметрыОперации.ТелоЗапроса = СтруктураВJSON(ПараметрыЗапроса);

	ОтветСервиса = ВыполнитьОперацию(ПараметрыОперации);
	Если ОтветСервиса.Успех Тогда
		Результат = Истина;
		
		ТекущаяЗапись = РегистрыСведений.КэшВзаимодействияМПЭПД.ПараметрыЗаписиРегистра();
		ТекущаяЗапись.ИдентификаторЭДО = ИдентификаторЭДО;
		ТекущаяЗапись.Дата = ТекущаяДатаСеанса();
		ТекущаяЗапись.Содержимое = "Смена пользователя";
		ТекущаяЗапись.Исходящее = Истина;
		ТекущаяЗапись.ВидСообщения = ТипВзаимодействияМПЭПД;
		ТекущаяЗапись.ИдентификаторДокумента = ИдентификаторДокумента;
		ТекущаяЗапись.ИдентификаторМП = ИдентификаторМП;
		ТекущаяЗапись.ДокументЭПД = Документ;
		ТекущаяЗапись.Автор = Пользователи.ТекущийПользователь();
		
		РегистрыСведений.КэшВзаимодействияМПЭПД.РегистрироватьЗапись(ТекущаяЗапись);
		
		ЗаписьРеестраЭПД = РегистрыСведений.РеестрЭПД.СоздатьМенеджерЗаписи();
		ЗаписьРеестраЭПД.Ссылка = Документ;
		ЗаписьРеестраЭПД.Прочитать();
		Если ЗаписьРеестраЭПД.Выбран() Тогда
			СоответствиеТитуловИШагов = ОбменСГИСЭПДКлиентСервер.СоответствиеТитуловИШагов(Документ);
			СоответствиеТитуловИШагов = СервисВзаимодействияМПЭПДКлиентСервер.ПеревернутьСоответствие(СоответствиеТитуловИШагов);
			ТитулРеестра = СоответствиеТитуловИШагов.Получить(ЗаписьРеестраЭПД.ТекущийШаг);
			МассивДоступныхВидовВзаимодействия = СервисВзаимодействияМПЭПДКлиентСервер.МассивДоступныхВидовВзаимодействияПоТитулу(ТитулРеестра);
			Если МассивДоступныхВидовВзаимодействия.Найти(ТипВзаимодействияМПЭПД) <> Неопределено Тогда
				ЗаписьРеестраЭПД.МобильноеПриложение = ИдентификаторМП;
				ЗаписьРеестраЭПД.ИдентификаторЗаписиМП = ИдентификаторЗаписиМП;
				ЗаписьРеестраЭПД.НаименованиеМП = НаименованиеМП;
				ЗаписьРеестраЭПД.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

// Отвязывает документ.
// 
// Параметры:
//  Документ - ОпределяемыйТип.ДокументыЭПД
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО
//  ТипВзаимодействияМПЭПД - ПеречислениеСсылка.ТипыВзаимодействияМПЭПД
// 
// Возвращаемое значение:
//  Булево, Неопределено - Сервис отвязать документ
Функция СервисОтвязатьДокумент(Документ, ИдентификаторЭДО, ТипВзаимодействияМПЭПД) Экспорт
	
	ЭлектронныйДокумент = ИнтеграцияЭДО.ОсновнойЭлектронныйДокументОбъектаУчета(Документ);
	Результат = СервисИзменитьДокумент(ИдентификаторЭДО, ЭлектронныйДокумент.ИдентификаторДокументооборота,
		ТипВзаимодействияМПЭПД, Документ);
		
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область РегламентныеОперации

// Возвращает регламентное задание получения событий МПЭПД.
// 
// Возвращаемое значение:
//  ОбъектМетаданныхРегламентноеЗадание - Регламентное задание получения событий МПЭПД.
Функция РегламентноеЗаданиеПолученияСобытийМПЭПД() Экспорт
	
	Возврат Метаданные.РегламентныеЗадания.ПолучениеСобытийМПЭПД;
	
КонецФункции

Процедура ИзменитьИспользованиеЗаданияПолучениеНовыхУведомлений(Использование) Экспорт
	
	Если ПривилегированныйРежим() = Ложь Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Использование", Использование);
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Если ОбщегоНазначения.ПодсистемаСуществует("ТехнологияСервиса.ОчередьЗаданий") Тогда
			МодульРаботаВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("РаботаВМоделиСервиса");
			МодульОчередьЗаданий = ОбщегоНазначения.ОбщийМодуль("ОчередьЗаданий");
			ПараметрыПоиска = Новый Структура;
			Если ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
				ОбластьДанных = МодульРаботаВМоделиСервиса.ЗначениеРазделителяСеанса();
				ПараметрыПоиска.Вставить("ОбластьДанных", ОбластьДанных);
			КонецЕсли;
			Шаблон = МодульОчередьЗаданий.ШаблонПоИмени("ПолучениеСобытийМПЭПД");
			Если ЗначениеЗаполнено(Шаблон) Тогда
				ПараметрыПоиска.Вставить("Шаблон", Шаблон);
				СписокЗаданий = МодульОчередьЗаданий.ПолучитьЗадания(ПараметрыПоиска);
				Для Каждого Задание Из СписокЗаданий Цикл
					МодульОчередьЗаданий.ИзменитьЗадание(Задание.Идентификатор, ПараметрыЗадания);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	Иначе
		РегламентныеЗаданияСервер.ИзменитьЗадание(Метаданные.РегламентныеЗадания.ПолучениеСобытийМПЭПД, ПараметрыЗадания);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает признак необходимости выполнять регламентное задание получения новых уведомлений.
// Если МП нет, отключает использование регламентного задания.
// 
// Возвращаемое значение:
//  Булево - Признак необходимости выполнять регламентное задание.
Функция НужноВыполнятьРегламентноеЗаданиеПолученияНовыхУведомлений() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПодключенныеМПЭПД.Идентификатор
	|ИЗ
	|	РегистрСведений.ПодключенныеМПЭПД КАК ПодключенныеМПЭПД";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ИзменитьИспользованиеЗаданияПолучениеНовыхУведомлений(Ложь);
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

Процедура ОбслуживаниеПолучениеНовыхУведомлений(Организация = Неопределено) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ПолучениеСобытийМПЭПД);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НужноВыполнятьРегламентноеЗаданиеПолученияНовыхУведомлений() = Ложь Тогда
		Возврат;
	КонецЕсли;

	СтрокаОтбора = Неопределено;
	Если Организация <> Неопределено Тогда
		СтрокаОтбора = Новый Структура("Организация", Организация);
	КонецЕсли;
	
	ВсеУчетныеЗаписи = СписокПодключенныхУчетныхЗаписейЭДО(Ложь, СтрокаОтбора);
	
	Для Каждого СтрокаТаблицы Из ВсеУчетныеЗаписи Цикл
		Если СтрокаТаблицы.ВсегоМП = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеУведомлений = СервисПолучитьУведомления(СтрокаТаблицы.УчетнаяЗаписьЭДО, СтрокаТаблицы.ТочкаОпросаУведомлений);
		НоваяТочкаУведомлений = ДанныеУведомлений.Точка;
		
		ДанныеСобытий = СервисПолучитьСобытия(СтрокаТаблицы.УчетнаяЗаписьЭДО, СтрокаТаблицы.ТочкаОпросаСобытий);
		НоваяТочкаСобытий = ДанныеСобытий.Точка;
		
		Если ДанныеСобытий.События.Количество() = 0 
			И ДанныеУведомлений.УведомленияСвязанныеСДокументами.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		МассивСобытийИУведомлений = ДанныеСобытий.События;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСобытийИУведомлений,
			ДанныеУведомлений.УведомленияСвязанныеСДокументами);
		
		МассивИдентификаторовМП = Новый Массив;
		МассивИдентификаторовДокументооборота = Новый Массив;
		Для Каждого СтрокаМассива Из МассивСобытийИУведомлений Цикл
			СервисВзаимодействияМПЭПДКлиентСервер.ДобавитьНеПустоеЗначениеВМассив(МассивИдентификаторовМП,
				СтрокаМассива.ИдентификаторМП);
			СервисВзаимодействияМПЭПДКлиентСервер.ДобавитьНеПустоеЗначениеВМассив(МассивИдентификаторовДокументооборота,
				СтрокаМассива.ИдентификаторДокумента);
		КонецЦикла;
		
		Запрос = Новый Запрос;
		//@skip-check ql-temp-table-index
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭлектронныйДокументВходящийЭДО.Ссылка КАК ЭлектронныйДокумент,
		|	ЭлектронныйДокументВходящийЭДО.ИдентификаторДокументооборота КАК ИдентификаторДокументооборота
		|ПОМЕСТИТЬ ВТ_ЭлектронныеДокументы
		|ИЗ
		|	Документ.ЭлектронныйДокументВходящийЭДО КАК ЭлектронныйДокументВходящийЭДО
		|ГДЕ
		|	ЭлектронныйДокументВходящийЭДО.ИдентификаторДокументооборота В (&МассивИдентификаторовДокументооборота)
		|	И ЭлектронныйДокументВходящийЭДО.ИдентификаторОрганизации = &ИдентификаторОрганизации
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЭлектронныйДокументИсходящийЭДО.Ссылка,
		|	ЭлектронныйДокументИсходящийЭДО.ИдентификаторДокументооборота
		|ИЗ
		|	Документ.ЭлектронныйДокументИсходящийЭДО КАК ЭлектронныйДокументИсходящийЭДО
		|ГДЕ
		|	ЭлектронныйДокументИсходящийЭДО.ИдентификаторДокументооборота В (&МассивИдентификаторовДокументооборота)
		|	И ЭлектронныйДокументИсходящийЭДО.ИдентификаторОрганизации = &ИдентификаторОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ЭлектронныеДокументы.ИдентификаторДокументооборота КАК ИдентификаторДокументооборота,
		|	ОбъектыУчетаДокументовЭДО.ОбъектУчета КАК ОбъектУчета,
		|	ЭлектроннаяТранспортнаяНакладная.ТитулПереадресовкаИдентификаторФайла,
		|	ЭлектроннаяТранспортнаяНакладная.ДопТитулПереадресовкаИдентификаторФайла,
		|	ЭлектроннаяТранспортнаяНакладная.ТитулПеревозчикаЗаменыИдентификаторФайла,
		|	ЭлектроннаяТранспортнаяНакладная.ДопТитулПеревозчикаЗаменыИдентификаторФайла
		|ИЗ
		|	ВТ_ЭлектронныеДокументы КАК ВТ_ЭлектронныеДокументы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
		|		ПО ВТ_ЭлектронныеДокументы.ЭлектронныйДокумент = ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент
		|		И (ОбъектыУчетаДокументовЭДО.Актуальный = ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭлектроннаяТранспортнаяНакладная КАК ЭлектроннаяТранспортнаяНакладная
		|		ПО ОбъектыУчетаДокументовЭДО.ОбъектУчета = ЭлектроннаяТранспортнаяНакладная.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|выбрать
		|	ПодключенныеМПЭПД.Идентификатор как ИдентификаторЗаписиМП,
		|	ПодключенныеМПЭПД.ИдентификаторМП как ИдентификаторМП,
		|	ПодключенныеМПЭПД.Наименование как НаименованиеМП
		|из
		|	РегистрСведений.ПодключенныеМПЭПД как ПодключенныеМПЭПД
		|где
		|	ПодключенныеМПЭПД.ИдентификаторМП В (&МассивИдентификаторовМП)";
		
		Запрос.УстановитьПараметр("ИдентификаторОрганизации", СтрокаТаблицы.УчетнаяЗаписьЭДО);
		Запрос.УстановитьПараметр("МассивИдентификаторовДокументооборота", МассивИдентификаторовДокументооборота);
		Запрос.УстановитьПараметр("МассивИдентификаторовМП", МассивИдентификаторовМП);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		ВыборкаДокументов = МассивРезультатов[1].Выбрать();
		ВыборкаДанныхМП = МассивРезультатов[2].Выбрать();
		
		СтруктураПоискаДокумента = Новый Структура;
		СтруктураПоискаДокумента.Вставить("ИдентификаторДокументооборота");
		
		СтруктураПоискаМП = Новый Структура;
		СтруктураПоискаМП.Вставить("ИдентификаторМП");
		
		СоответствиеТитуловТипамМП = СервисВзаимодействияМПЭПДКлиентСервер.СоответствиеТитуловТипамМП();
		
		Для Каждого СтрокаМассива Из МассивСобытийИУведомлений Цикл
		
			// В регистр записываются события, которые необходимо отражать в БД, а так же сообщения (уведомления),
			// т.к. здесь обрабатываются только те сообщения, которые связаны с документами, остальное игнорируются.
			Если СтрокаМассива.ВидСообщения <> Перечисления.ТипыВзаимодействияМПЭПД.ПовторнаяРегистрацияМП
				И СтрокаМассива.ВидСообщения <> Перечисления.ТипыВзаимодействияМПЭПД.ИзменениеСведенийМП
				И СтрокаМассива.ВидСообщения <> Перечисления.ТипыВзаимодействияМПЭПД.ОтвязкаВодителя
				И СтрокаМассива.ВидСообщения <> Перечисления.ТипыВзаимодействияМПЭПД.ПривязкаВодителя
				И СтрокаМассива.ВидСообщения <> Перечисления.ТипыВзаимодействияМПЭПД.УдалениеМП
				И СтрокаМассива.ВидСообщения <> Перечисления.ТипыВзаимодействияМПЭПД.Сообщение Тогда

				Продолжить;
			КонецЕсли;

			Если Не ЗначениеЗаполнено(СтрокаМассива.ИдентификаторДокумента) Тогда
				ЕстьДокумент = Ложь;
			Иначе
				ВыборкаДокументов.Сбросить();
				СтруктураПоискаДокумента.ИдентификаторДокументооборота = СтрокаМассива.ИдентификаторДокумента;
				ЕстьДокумент = ВыборкаДокументов.НайтиСледующий(СтруктураПоискаДокумента);
			КонецЕсли;

			Если ЕстьДокумент Тогда
				СтрокаМассива.ДокументЭПД = ВыборкаДокументов.ОбъектУчета;
			КонецЕсли;
			РегистрыСведений.КэшВзаимодействияМПЭПД.РегистрироватьЗапись(СтрокаМассива);

			Если Не ЗначениеЗаполнено(СтрокаМассива.ИдентификаторМП) Или Не ЕстьДокумент Тогда
				Продолжить;
			КонецЕсли;
			
			НачатьТранзакцию();

			Попытка

				Блокировка = Новый БлокировкаДанных;

				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РеестрЭПД");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыборкаДокументов.ОбъектУчета);
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;

				Блокировка.Заблокировать();

				МенеджерЗаписиРеестраЭПД = РегистрыСведений.РеестрЭПД.СоздатьМенеджерЗаписи();
				МенеджерЗаписиРеестраЭПД.Ссылка = ВыборкаДокументов.ОбъектУчета;
				МенеджерЗаписиРеестраЭПД.Прочитать();

				Если Не МенеджерЗаписиРеестраЭПД.Выбран() Тогда
					ОтменитьТранзакцию();
					Продолжить;
				КонецЕсли;

				СтруктураДанныхСообщения = СтруктураИзJson(СтрокаМассива.ДанныеСообщения);
				Тип = Неопределено;
				СтруктураДанныхСообщения.Свойство("Type", Тип);
				Принято = Неопределено;
				СтруктураДанныхСообщения.Свойство("Принято", Принято);

				СоответствиеТитуловИШагов = ОбменСГИСЭПДКлиентСервер.СоответствиеТитуловИШагов(
							ВыборкаДокументов.ОбъектУчета);
				СоответствиеТитуловИШагов = СервисВзаимодействияМПЭПДКлиентСервер.ПеревернутьСоответствие(
							СоответствиеТитуловИШагов);
				ТитулИзРеестра = СоответствиеТитуловИШагов.Получить(МенеджерЗаписиРеестраЭПД.ТекущийШаг);

				ЭтоТипСообщенияОбОтправкеТитула = СервисВзаимодействияМПЭПДКлиентСервер.ЭтоТипСообщенияОбОтправкеТитулаИзМП(Тип);
				Если ЭтоТипСообщенияОбОтправкеТитула Тогда
					ТипДляОпределенияТитула = СервисВзаимодействияМПЭПДКлиентСервер.ПривестиТипСообщенияКВидуПолученияТитулаБЭД(Тип);
				Иначе
					ТипДляОпределенияТитула = Тип;
				КонецЕсли;
				ТитулСобытияУведомления = СоответствиеТитуловТипамМП.Получить(ТипДляОпределенияТитула);

				ЭтоПривязкаДокумента = СервисВзаимодействияМПЭПДКлиентСервер.ЭтоПривязкаДокумента(
							СтрокаМассива.ВидСообщения);
				ЭтоСообщение = СервисВзаимодействияМПЭПДКлиентСервер.ЭтоТипВзаимодействияСообщение(
							СтрокаМассива.ВидСообщения);
							
				Если ТитулСобытияУведомления <> ТитулИзРеестра И Не ЭтоСообщение Тогда
					ОтменитьТранзакцию();
					Продолжить;
				КонецЕсли;

				ВыборкаДанныхМП.Сбросить();
				СтруктураПоискаМП.ИдентификаторМП = СтрокаМассива.ИдентификаторМП;
				ЕстьДанныеМП = ВыборкаДанныхМП.НайтиСледующий(СтруктураПоискаМП);
				
				Если ЭтоСообщение И ТитулСобытияУведомления <> ТитулИзРеестра Тогда
					Если ЭтоТипСообщенияОбОтправкеТитула Тогда
						МенеджерЗаписиРеестраЭПД.ТекущийШагМП = Тип;
					ИначеЕсли Тип <> Неопределено Тогда
						МенеджерЗаписиРеестраЭПД.ТекущийШагМП = Тип + ?(Принято = Ложь, "_reject", "");
					КонецЕсли;
				
				ИначеЕсли ЭтоСообщение И ЕстьДанныеМП Тогда
					МенеджерЗаписиРеестраЭПД.МобильноеПриложение = ВыборкаДанныхМП.ИдентификаторМП;
					МенеджерЗаписиРеестраЭПД.ИдентификаторЗаписиМП = ВыборкаДанныхМП.ИдентификаторЗаписиМП;
					МенеджерЗаписиРеестраЭПД.НаименованиеМП = ВыборкаДанныхМП.НаименованиеМП;

					Если ЭтоТипСообщенияОбОтправкеТитула Тогда
						МенеджерЗаписиРеестраЭПД.ТекущийШагМП = Тип;
					ИначеЕсли Тип <> Неопределено Тогда
						МенеджерЗаписиРеестраЭПД.ТекущийШагМП = Тип + ?(Принято = Ложь, "_reject", "");
					КонецЕсли;

					Если МенеджерЗаписиРеестраЭПД.ОжидаемПросмотрМП Тогда
						МенеджерЗаписиРеестраЭПД.ОжидаемПросмотрМП = ОжидаемПросмотрМП(МенеджерЗаписиРеестраЭПД,
							СтруктураДанныхСообщения, ВыборкаДокументов);
					КонецЕсли;

				ИначеЕсли ЭтоПривязкаДокумента И ЕстьДанныеМП Тогда
					МенеджерЗаписиРеестраЭПД.МобильноеПриложение = ВыборкаДанныхМП.ИдентификаторМП;
					МенеджерЗаписиРеестраЭПД.ИдентификаторЗаписиМП = ВыборкаДанныхМП.ИдентификаторЗаписиМП;
					МенеджерЗаписиРеестраЭПД.НаименованиеМП = ВыборкаДанныхМП.НаименованиеМП;

				ИначеЕсли СтрокаМассива.ВидСообщения <> Перечисления.ТипыВзаимодействияМПЭПД.ПовторнаяРегистрацияМП
					И СтрокаМассива.ВидСообщения <> Перечисления.ТипыВзаимодействияМПЭПД.ИзменениеСведенийМП Тогда
					МенеджерЗаписиРеестраЭПД.МобильноеПриложение = "";
					МенеджерЗаписиРеестраЭПД.ИдентификаторЗаписиМП = "";
					МенеджерЗаписиРеестраЭПД.НаименованиеМП = "";
					МенеджерЗаписиРеестраЭПД.ТекущийШагМП = "";
				Иначе
					ОтменитьТранзакцию();
					Продолжить;
				КонецЕсли;

				МенеджерЗаписиРеестраЭПД.Записать(Истина);
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ИнформацияОбОшибке = ИнформацияОбОшибке();
				РегистрацияВЖурнале(УровеньЖурналаРегистрации.Ошибка, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));				
			КонецПопытки;
			
		КонецЦикла; // По событиям и уведомлениям
		
		Если ЗначениеЗаполнено(НоваяТочкаУведомлений) ИЛИ ЗначениеЗаполнено(НоваяТочкаСобытий) Тогда
			ТекущаяЗапись = РегистрыСведений.НастройкиВзаимодействияМПЭПД.ПараметрыЗаписиРегистра(СтрокаТаблицы.УчетнаяЗаписьЭДО);
			Если ЗначениеЗаполнено(НоваяТочкаУведомлений) Тогда
				ТекущаяЗапись.ТочкаОпросаУведомлений = НоваяТочкаУведомлений;
			КонецЕсли;
			Если ЗначениеЗаполнено(НоваяТочкаСобытий) Тогда
				ТекущаяЗапись.ТочкаОпросаСобытий = НоваяТочкаСобытий;
			КонецЕсли;
			РегистрыСведений.НастройкиВзаимодействияМПЭПД.РегистрироватьЗапись(ТекущаяЗапись);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура ОтправитьСообщениеОПереадресовке(Документ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭлектронныйДокументВходящийЭДО.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
	|	ЭлектронныйДокументВходящийЭДО.ИдентификаторДокументооборота КАК ИдентификаторДокументооборота,
	|	ВЫРАЗИТЬ(ОбъектыУчетаДокументовЭДО.ОбъектУчета КАК Документ.ЭлектроннаяТранспортнаяНакладная).ТитулГрузоотправителяТранспортнаяНакладнаяНомер КАК Номер,
	|	ВЫРАЗИТЬ(ОбъектыУчетаДокументовЭДО.ОбъектУчета КАК Документ.ЭлектроннаяТранспортнаяНакладная).ТитулГрузоотправителяТранспортнаяНакладнаяДата КАК Дата,
	|	ВЫРАЗИТЬ(ОбъектыУчетаДокументовЭДО.ОбъектУчета КАК Документ.ЭлектроннаяТранспортнаяНакладная).ТитулПереадресовкаИдентификаторФайла КАК ИдентификаторФайла,
	|	ВЫРАЗИТЬ(ОбъектыУчетаДокументовЭДО.ОбъектУчета КАК Документ.ЭлектроннаяТранспортнаяНакладная).ДопТитулПереадресовкаИдентификаторФайла КАК ДопТитулИдентификаторФайла,
	|	РеестрЭПД.МобильноеПриложение КАК ИдентификаторМП
	|ИЗ
	|	РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭлектронныйДокументВходящийЭДО КАК ЭлектронныйДокументВходящийЭДО
	|		ПО ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент = ЭлектронныйДокументВходящийЭДО.Ссылка
	|			И (ОбъектыУчетаДокументовЭДО.ОбъектУчета = &Документ)
	|			И (ОбъектыУчетаДокументовЭДО.Актуальный = ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВзаимодействияМПЭПД КАК НастройкиВзаимодействияМПЭПД
	|		ПО (ЭлектронныйДокументВходящийЭДО.ИдентификаторОрганизации = НастройкиВзаимодействияМПЭПД.ИдентификаторЭДО)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрЭПД КАК РеестрЭПД
	|		ПО ОбъектыУчетаДокументовЭДО.ОбъектУчета = РеестрЭПД.Ссылка";
	Запрос.УстановитьПараметр("Документ", Документ);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторДокументооборота = Выборка.ИдентификаторДокументооборота;
	ИдентификаторЭДО = Выборка.ИдентификаторОрганизации;
	Номер = Выборка.Номер;
	Дата = Формат(Выборка.Дата, "ДФ=dd.MM.yyyy");
	ИдентификаторМП = Выборка.ИдентификаторМП;
	
	Шаблон = НСтр("ru = 'Переадресовка сформирована по документу %1 от %2';
					|en = 'Readdressing is generated based on the %1 document dated %2'");
	Содержимое = СтрШаблон(Шаблон, Номер, Дата);
	
	Если ЗначениеЗаполнено(Выборка.ДопТитулИдентификаторФайла) Тогда
		ИдентификаторФайла = Выборка.ДопТитулИдентификаторФайла;
	Иначе
		ИдентификаторФайла = Выборка.ИдентификаторФайла;
	КонецЕсли;
	
	ДанныеСообщения = Новый Структура;
	ДанныеСообщения.Вставить("FileId", ИдентификаторФайла);
	ДанныеСообщения.Вставить("Type", ТипСообщенияДляМППереадресовка());
	
	ДанныеСообщенияJSON = СтруктураВJSON(ДанныеСообщения);
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("ИдентификаторДокумента", ИдентификаторДокументооборота);
	ПараметрыОперации.Вставить("Содержимое", Содержимое);
	ПараметрыОперации.Вставить("ДанныеСообщения", ДанныеСообщенияJSON);
	ПараметрыОперации.Вставить("ИдентификаторМП", ИдентификаторМП);
	
	СервисОтправитьУведомления(ИдентификаторЭДО, ПараметрыОперации);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ОтправитьСообщениеОЗаменеТС(Документ) Экспорт
	
	МассивТитулов = Новый Массив;
	МассивТитулов.Добавить(Перечисления.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул8);
	МассивТитулов.Добавить(Перечисления.ТипыЭлементовРегламентаЭДО.ЭТрН_ДопТитул8);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗначенияРеквизитовДокументовЭПД.ЗначениеРеквизитаСтрока КАК СодержаниеОперации
	|ИЗ
	|	РегистрСведений.ЗначенияРеквизитовДокументовЭПД КАК ЗначенияРеквизитовДокументовЭПД
	|ГДЕ
	|	ЗначенияРеквизитовДокументовЭПД.Документ = &Документ
	|	И ЗначенияРеквизитовДокументовЭПД.Титул В(&МассивТитулов)
	|	И (ЗначенияРеквизитовДокументовЭПД.ИмяРеквизита = ""ТитулПеревозчикаЗаменыСодержаниеОперации""
	|			ИЛИ ЗначенияРеквизитовДокументовЭПД.ИмяРеквизита = ""ДопТитулПеревозчикаЗаменыСодержаниеОперации"")
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗначенияРеквизитовДокументовЭПД.НомерВерсии УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭлектронныйДокументВходящийЭДО.ИдентификаторОрганизации КАК ИдентификаторОрганизации,
	|	ЭлектронныйДокументВходящийЭДО.ИдентификаторДокументооборота КАК ИдентификаторДокументооборота,
	|	ВЫРАЗИТЬ(ОбъектыУчетаДокументовЭДО.ОбъектУчета КАК Документ.ЭлектроннаяТранспортнаяНакладная).ТитулГрузоотправителяТранспортнаяНакладнаяНомер КАК Номер,
	|	ВЫРАЗИТЬ(ОбъектыУчетаДокументовЭДО.ОбъектУчета КАК Документ.ЭлектроннаяТранспортнаяНакладная).ТитулГрузоотправителяТранспортнаяНакладнаяДата КАК Дата,
	|	ВЫРАЗИТЬ(ОбъектыУчетаДокументовЭДО.ОбъектУчета КАК Документ.ЭлектроннаяТранспортнаяНакладная).ТитулПеревозчикаЗаменыИдентификаторФайла КАК ИдентификаторФайла,
	|	ВЫРАЗИТЬ(ОбъектыУчетаДокументовЭДО.ОбъектУчета КАК Документ.ЭлектроннаяТранспортнаяНакладная).ДопТитулПеревозчикаЗаменыИдентификаторФайла КАК ДопТитулИдентификаторФайла,
	|	РеестрЭПД.МобильноеПриложение КАК ИдентификаторМП
	|ИЗ
	|	РегистрСведений.ОбъектыУчетаДокументовЭДО КАК ОбъектыУчетаДокументовЭДО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭлектронныйДокументВходящийЭДО КАК ЭлектронныйДокументВходящийЭДО
	|		ПО ОбъектыУчетаДокументовЭДО.ЭлектронныйДокумент = ЭлектронныйДокументВходящийЭДО.Ссылка
	|			И (ОбъектыУчетаДокументовЭДО.ОбъектУчета = &Документ)
	|			И (ОбъектыУчетаДокументовЭДО.Актуальный = ИСТИНА)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВзаимодействияМПЭПД КАК НастройкиВзаимодействияМПЭПД
	|		ПО (ЭлектронныйДокументВходящийЭДО.ИдентификаторОрганизации = НастройкиВзаимодействияМПЭПД.ИдентификаторЭДО)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрЭПД КАК РеестрЭПД
	|		ПО ОбъектыУчетаДокументовЭДО.ОбъектУчета = РеестрЭПД.Ссылка";
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("МассивТитулов", МассивТитулов);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	Выборка = РезультатыЗапросов[1].Выбрать();
	ВыборкаСодержанияОперации = РезультатыЗапросов[0].Выбрать();
	
	Если Не Выборка.Следующий()
		Или Не ВыборкаСодержанияОперации.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	// Если в содержании операции есть замена водителя, значит в моб. устройстве документооборот завершается,
	// в этом случае отправлять дополнительное сообщение не нужно
	СодержаниеОперации = ВыборкаСодержанияОперации.СодержаниеОперации;
	Если СтрНайти(СодержаниеОперации, "Замена водителя") <> 0
		Или СтрНайти(СодержаниеОперации, "Замена водителей") <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторДокументооборота = Выборка.ИдентификаторДокументооборота;
	ИдентификаторЭДО = Выборка.ИдентификаторОрганизации;
	Номер = Выборка.Номер;
	Дата = Формат(Выборка.Дата, "ДФ=dd.MM.yyyy");
	ИдентификаторМП = Выборка.ИдентификаторМП;
	
	Шаблон = НСтр("ru = 'Замена ТС сформирована по документу %1 от %2';
					|en = 'Vehicle change is generated based on the %1 document dated %2'");
	Содержимое = СтрШаблон(Шаблон, Номер, Дата);
	
	Если ЗначениеЗаполнено(Выборка.ДопТитулИдентификаторФайла) Тогда
		ИдентификаторФайла = Выборка.ДопТитулИдентификаторФайла;
	Иначе
		ИдентификаторФайла = Выборка.ИдентификаторФайла;
	КонецЕсли;
	
	ДанныеСообщения = Новый Структура;
	ДанныеСообщения.Вставить("FileId", ИдентификаторФайла);
	ДанныеСообщения.Вставить("Type", ТипСообщенияДляМПЗаменаТС());
	
	ДанныеСообщенияJSON = СтруктураВJSON(ДанныеСообщения);
	
	ПараметрыОперации = Новый Структура;
	ПараметрыОперации.Вставить("ИдентификаторДокумента", ИдентификаторДокументооборота);
	ПараметрыОперации.Вставить("Содержимое", Содержимое);
	ПараметрыОперации.Вставить("ДанныеСообщения", ДанныеСообщенияJSON);
	ПараметрыОперации.Вставить("ИдентификаторМП", ИдентификаторМП);
	
	СервисОтправитьУведомления(ИдентификаторЭДО, ПараметрыОперации);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура СервисОтправитьНастройкиДополнительныхПолей(МассивНастроек) Экспорт
	
	МассивМП = СервисВзаимодействияМПЭПДВызовСервера.СписокПодключенныхМП(, , Истина);
	
	СоответствиеОтправки = Новый Соответствие;
	Для Каждого СтруктураМП Из МассивМП Цикл
		ПараметрыПоИдентификаторуЭДО = СоответствиеОтправки.Получить(СтруктураМП.ИдентификаторЭДО); 
		Если ПараметрыПоИдентификаторуЭДО = Неопределено Тогда
			ПараметрыПоИдентификаторуЭДО = Новый Соответствие;
			СоответствиеОтправки.Вставить(СтруктураМП.ИдентификаторЭДО, ПараметрыПоИдентификаторуЭДО);
		КонецЕсли;
		ПараметрыОтправки = ПараметрыПоИдентификаторуЭДО.Получить(СтруктураМП.ИдентификаторМП);
		Если ПараметрыОтправки = Неопределено Тогда
			ПараметрыОтправки = Новый Структура;
			ПараметрыПоИдентификаторуЭДО.Вставить(СтруктураМП.ИдентификаторМП, ПараметрыОтправки);
		КонецЕсли;
		
		ПараметрыОтправки.Вставить("ВидСообщения", Перечисления.ТипыВзаимодействияМПЭПД.НастройкиДополнительныхПолей);
		ПараметрыОтправки.Вставить("ИдентификаторМП", СтруктураМП.ИдентификаторМП);
		
		Для Каждого СтруктураНастройки Из МассивНастроек Цикл
			СодержимоеСтруктура = Новый Структура;
			Если ЗначениеЗаполнено(СтруктураНастройки.Контрагент) Тогда
				ИНН = СтруктураНастройки.Контрагент.ИНН;
			Иначе
				ИНН = "";
			КонецЕсли;
			СодержимоеСтруктура.Вставить("INN", ИНН);
			СодержимоеСтруктура.Вставить("DocType", СервисВзаимодействияМПЭПДКлиентСервер.ТипДокументаМП(СтруктураНастройки.ТипДокумента));
			СодержимоеСтруктура.Вставить("Id", СтруктураНастройки.ИдентификаторНастройки);
			СодержимоеСтруктура.Вставить("DataGroup", СтруктураНастройки.ГруппаДанных);
			СодержимоеСтруктура.Вставить("DataId", СтруктураНастройки.ИдентификаторИнформации);
			СодержимоеСтруктура.Вставить("DataType", СтруктураНастройки.ТипДанных);
			СодержимоеСтруктура.Вставить("Qualifiers", СтруктураНастройки.Квалификаторы);
			СодержимоеСтруктура.Вставить("Enums", СтруктураНастройки.Перечисления);
			СодержимоеСтруктура.Вставить("Required", СтруктураНастройки.Обязательный);
			СодержимоеСтруктура.Вставить("Caption", СтруктураНастройки.Заголовок);
		КонецЦикла;
		
		ПараметрыОтправки.Вставить("Содержимое", СтруктураВJSON(СодержимоеСтруктура));
	КонецЦикла;
	
	Для Каждого ПараметрыПоИдентификаторуЭДО Из СоответствиеОтправки Цикл
		Для Каждого ПараметрыОтправки Из ПараметрыПоИдентификаторуЭДО.Значение Цикл
			СервисОтправитьУведомления(ПараметрыПоИдентификаторуЭДО.Ключ, ПараметрыОтправки.Значение);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область УчетныеЗаписиЭДО

// Возвращает представление учетной записи ЭДО.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
//  Организация - Неопределено, ОпределяемыйТип.Организация - Организация.
// 
// Возвращаемое значение:
//  Строка - Представление учетной записи ЭДО.
Функция ПредставлениеУчетнойЗаписиЭДО(ИдентификаторЭДО = "", Организация = Неопределено) Экспорт
	
	Результат = НСтр("ru = 'Неизвестно';
					|en = 'Unknown'");
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Результат = СокрЛП(Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЭДО) И ЗначениеЗаполнено(Организация) Тогда
		Результат = Результат + " (" + ИдентификаторЭДО  + ")";
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторЭДО) Тогда
		Результат = ИдентификаторЭДО;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает список подключенных учетных записей ЭДО.
// 
// Параметры:
//  ДанныеПоОрганизациям - Булево
//  Отборы - Неопределено, Структура - Произвольные отборы.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список подключенных учетных записей ЭДО:
//	* ИдентификаторЭДО - Строка
//	* Организация - ОпределяемыйТип.Организация
//	* УчетнаяЗаписьЭДО - Строка
//	* СтатусСертификата - Число
//	* ВсегоМП - Число
//	* Настроено - Число
//	* ПодключеноЭДО - Булево
//	* Всего - Число
//	* ТочкаОпросаУведомлений - Число
//	* ТочкаОпросаСобытий - Число
//	* ДатаОпросаДокументов - Дата 
Функция СписокПодключенныхУчетныхЗаписейЭДО(ДанныеПоОрганизациям = Ложь, Отборы = Неопределено) Экспорт
	
	//@skip-check ql-temp-table-index
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	МАКСИМУМ(СертификатыУчетныхЗаписейЭДО.ДействителенДо) КАК СертификатДействителенДо
	|ПОМЕСТИТЬ ДатыСертификатов
	|ИЗ
	|	РегистрСведений.СертификатыУчетныхЗаписейЭДО КАК СертификатыУчетныхЗаписейЭДО
	|
	|СГРУППИРОВАТЬ ПО
	|	СертификатыУчетныхЗаписейЭДО.ИдентификаторЭДО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПодключенныеМПЭПД.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	СУММА(1) КАК ВсегоМП
	|ПОМЕСТИТЬ ВсеМП
	|ИЗ
	|	РегистрСведений.ПодключенныеМПЭПД КАК ПодключенныеМПЭПД
	|ГДЕ
	|	ПодключенныеМПЭПД.ИдентификаторМП <> """"
	|	И ПодключенныеМПЭПД.Состояние В (ЗНАЧЕНИЕ(Перечисление.СостояниеМПЭПД.Приостановлен), ЗНАЧЕНИЕ(Перечисление.СостояниеМПЭПД.ОжидаетсяПодключение), ЗНАЧЕНИЕ(Перечисление.СостояниеМПЭПД.Подключен), ЗНАЧЕНИЕ(Перечисление.СостояниеМПЭПД.ПодключениеНеВыполнено))
	|	И ПодключенныеМПЭПД.Удален = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	ПодключенныеМПЭПД.ИдентификаторЭДО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	УчетныеЗаписиЭДО.Организация КАК Организация,
	|	УчетныеЗаписиЭДО.СпособОбменаЭД КАК СпособОбменаЭД
	|ПОМЕСТИТЬ ВсеУчетныеЗаписиЭДО
	|ИЗ
	|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
	|ГДЕ
	|	УчетныеЗаписиЭДО.СпособОбменаЭД = &СпособОбменаЭД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КэшВзаимодействияМПЭПД.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	1 КАК ВсегоУведомлений
	|ПОМЕСТИТЬ ВсеУведомления
	|ИЗ
	|	РегистрСведений.КэшВзаимодействияМПЭПД КАК КэшВзаимодействияМПЭПД
	|ГДЕ
	|	КэшВзаимодействияМПЭПД.Прочитано = ЛОЖЬ
	|	И КэшВзаимодействияМПЭПД.Исходящее = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВсеУчетныеЗаписиЭДО.Организация КАК Организация,
	|	ВсеУчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	МАКСИМУМ(ВсеУчетныеЗаписиЭДО.ИдентификаторЭДО) КАК УчетнаяЗаписьЭДО,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ДатыСертификатов.СертификатДействителенДо ЕСТЬ NULL
	|					ИЛИ РАЗНОСТЬДАТ(&ТекущаяДата, ДатыСертификатов.СертификатДействителенДо, ДЕНЬ) > 30
	|				ТОГДА 0
	|			КОГДА РАЗНОСТЬДАТ(&ТекущаяДата, ДатыСертификатов.СертификатДействителенДо, МИНУТА) <= 0
	|				ТОГДА 2
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК СтатусСертификата,
	|	СУММА(ЕСТЬNULL(ПодключенныеМПЭПД.ВсегоМП, 0)) КАК ВсегоМП,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ НастройкиВзаимодействияМПЭПД.Организация ЕСТЬ NULL
	|					И НЕ НастройкиВзаимодействияМПЭПД.СрокДействияТокена = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Настроено,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА НЕ НастройкиВзаимодействияМПЭПД.Организация ЕСТЬ NULL
	|					И НЕ НастройкиВзаимодействияМПЭПД.СрокДействияТокена = ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ПодключеноЭДО,
	|	СУММА(1) КАК Всего,
	|	СУММА(ЕСТЬNULL(ВсеУведомления.ВсегоУведомлений, 0)) КАК ВсегоУведомлений,
	|	МАКСИМУМ(НастройкиВзаимодействияМПЭПД.ТочкаОпросаУведомлений) КАК ТочкаОпросаУведомлений,
	|	МАКСИМУМ(НастройкиВзаимодействияМПЭПД.ТочкаОпросаСобытий) КАК ТочкаОпросаСобытий,
	|	МАКСИМУМ(НастройкиВзаимодействияМПЭПД.ДатаОпросаДокументов) КАК ДатаОпросаДокументов
	|{ВЫБРАТЬ
	|	ИдентификаторЭДО,
	|	Организация,
	|	УчетнаяЗаписьЭДО,
	|	СтатусСертификата,
	|	ВсегоМП,
	|	Настроено,
	|	ПодключеноЭДО,
	|	Всего,
	|	ТочкаОпросаУведомлений,
	|	ТочкаОпросаСобытий,
	|	ДатаОпросаДокументов}
	|ИЗ
	|	ВсеУчетныеЗаписиЭДО КАК ВсеУчетныеЗаписиЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВзаимодействияМПЭПД КАК НастройкиВзаимодействияМПЭПД
	|		ПО ВсеУчетныеЗаписиЭДО.ИдентификаторЭДО = НастройкиВзаимодействияМПЭПД.ИдентификаторЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеМП КАК ПодключенныеМПЭПД
	|		ПО ВсеУчетныеЗаписиЭДО.ИдентификаторЭДО = ПодключенныеМПЭПД.ИдентификаторЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДатыСертификатов КАК ДатыСертификатов
	|		ПО ВсеУчетныеЗаписиЭДО.ИдентификаторЭДО = ДатыСертификатов.ИдентификаторЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеУведомления КАК ВсеУведомления
	|		ПО ВсеУчетныеЗаписиЭДО.ИдентификаторЭДО = ВсеУведомления.ИдентификаторЭДО
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеУчетныеЗаписиЭДО.ИдентификаторЭДО,
	|	ВсеУчетныеЗаписиЭДО.Организация";
	
	НовыйЗапрос = Новый ПостроительЗапроса;
	НовыйЗапрос.Текст = ТекстЗапроса;
	НовыйЗапрос.ЗаполнитьНастройки();
	
	Если Отборы <> Неопределено Тогда
		Счетчик = 1;
		Для Каждого СтрокаКлюча Из Отборы Цикл
			НовыйОтбор = НовыйЗапрос.Отбор.Добавить(СтрокаКлюча.Ключ, "П" + Счетчик);
			НовыйОтбор.ВидСравнения = ВидСравнения.Равно;
			НовыйОтбор.Значение = СтрокаКлюча.Значение;
			НовыйОтбор.Использование = Истина;
		КонецЦикла;
	КонецЕсли;
	
	Если ДанныеПоОрганизациям Тогда
		НовыйЗапрос.ВыбранныеПоля.Удалить(НовыйЗапрос.ВыбранныеПоля[0]);
	КонецЕсли;
	
	Запрос = НовыйЗапрос.ПолучитьЗапрос();
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("СпособОбменаЭД", Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

// Возвращает массив идентификаторов ЭДО.
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация
// 
// Возвращаемое значение:
//  Массив из Строка - Учетные записи организации
Функция УчетныеЗаписиОрганизации(Организация) Экспорт
	
	ОписаниеОтбора = УчетныеЗаписиЭДО.НовыйОтборУчетныхЗаписей();
	ОписаниеОтбора.Организация = "&Организация";
	ОписаниеОтбора.СпособОбмена = "&СпособОбмена"; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО
		|ИЗ
		|	УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО";	
	Запросы = Новый Массив;
	Запросы.Добавить(УчетныеЗаписиЭДО.ЗапросУчетныхЗаписей("УчетныеЗаписиЭДО", ОписаниеОтбора));
	ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
	ИтоговыйЗапрос.УстановитьПараметр("Организация", Организация);
	ИтоговыйЗапрос.УстановитьПараметр("СпособОбмена", Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = ИтоговыйЗапрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	Выборка = РезультатЗапроса.Выбрать();
	
	УчетныеЗаписи = Новый Массив;
	Пока Выборка.Следующий() Цикл
		УчетныеЗаписи.Добавить(Выборка.ИдентификаторЭДО);
	КонецЦикла;
	
	Возврат УчетныеЗаписи;
	
КонецФункции

// Возвращает организацию соответствующую идентификатору ЭДО.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
// 
// Возвращаемое значение:
//  Неопределено, СправочникСсылка.Организации - Организация по идентификатору ЭДО.
Функция ОрганизацияПоИдентификаторуЭДО(ИдентификаторЭДО) Экспорт
	
	ОписаниеОтбора = УчетныеЗаписиЭДО.НовыйОтборУчетныхЗаписей();
	ОписаниеОтбора.УчетныеЗаписи = "&ИдентификаторЭДО";
	ОписаниеОтбора.СпособОбмена = "&СпособОбмена"; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	УчетныеЗаписиЭДО.Организация КАК Организация
	|ИЗ
	|	УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО";	
	Запросы = Новый Массив;
	Запросы.Добавить(УчетныеЗаписиЭДО.ЗапросУчетныхЗаписей("УчетныеЗаписиЭДО", ОписаниеОтбора));
	ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
	ИтоговыйЗапрос.УстановитьПараметр("УчетныеЗаписи", ИдентификаторЭДО);
	ИтоговыйЗапрос.УстановитьПараметр("СпособОбмена", Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО);
	
	РезультатЗапроса = ИтоговыйЗапрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Организация;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Признак подключения учетной записи.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
// 
// Возвращаемое значение:
//  Булево - Учетная запись подключена.
Функция УчетнаяЗаписьПодключена(ИдентификаторЭДО) Экспорт

	Результат = Ложь;
	
	Если ЗначениеЗаполнено(ИдентификаторЭДО) Тогда
		ВсеЭДО = СписокПодключенныхУчетныхЗаписейЭДО();
		НашлиСтроки = ВсеЭДО.НайтиСтроки(Новый Структура("УчетнаяЗаписьЭДО, ПодключеноЭДО", ИдентификаторЭДО, Истина));
		Если НашлиСтроки.Количество() > 0 Тогда
			Результат = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает данные для регистрации учетной записи.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
// 
// Возвращаемое значение:
//  Структура - Необходимые данные для регистрации учетной записи:
// * УчетнаяЗаписьПодключена - Булево
// * НужноВвестиДанныеИнтернетПоддержки - Булево 
Функция НеобходимыеДанныеДляРегистрацииУчетнойЗаписи(ИдентификаторЭДО) Экспорт
	
	УчетнаяЗаписьПодключена = УчетнаяЗаписьПодключена(ИдентификаторЭДО);
	НужноВвестиДанныеИнтернетПоддержки = НужноВвестиДанныеИнтернетПоддержки();
	
	Структура = НоваяСтруктураНеобходимыхДанныхДляРегистрацииУчетнойЗаписи();
	
	Структура.УчетнаяЗаписьПодключена = УчетнаяЗаписьПодключена;
	Структура.НужноВвестиДанныеИнтернетПоддержки = НужноВвестиДанныеИнтернетПоддержки;
	
	//@skip-check constructor-function-return-section
	Возврат Структура;
	
КонецФункции

// Признак наличия МП по учетной записи есть подключенные МП.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
// 
// Возвращаемое значение:
//  Булево
Функция ПоУчетнойЗаписиЕстьПодключенныеМП(ИдентификаторЭДО) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПодключенныеМПЭПД.Идентификатор КАК Идентификатор
	|ИЗ
	|	РегистрСведений.ПодключенныеМПЭПД КАК ПодключенныеМПЭПД
	|ГДЕ
	|	ПодключенныеМПЭПД.ИдентификаторЭДО = &ИдентификаторЭДО
	|	И ПодключенныеМПЭПД.Состояние В (ЗНАЧЕНИЕ(Перечисление.СостояниеМПЭПД.Приостановлен), ЗНАЧЕНИЕ(Перечисление.СостояниеМПЭПД.ОжидаетсяПодключение), ЗНАЧЕНИЕ(Перечисление.СостояниеМПЭПД.Подключен))";
	Запрос.УстановитьПараметр("ИдентификаторЭДО", ИдентификаторЭДО);
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти

#Область ВизуальноеОформлениеМПНаФормахДокументов

// Добавляет в масси реквизиты МП.
//  
// Параметры:
//  МассивРеквизитовДобавления - Массив из РеквизитФормы
//  Форма - ФормаКлиентскогоПриложения - Форма
//  
//@skip-check bsl-legacy-check-type-environments
//@skip-check doc-comment-description-ends-on-dot
Процедура ДобавитьРеквизитыМП(МассивРеквизитовДобавления, Форма) Экспорт
	
	Документ = Форма.Объект.Ссылка;
	
	// Реквизиты проектов титулов
	МассивРеквизитовДобавления.Добавить(Новый РеквизитФормы("МПЕстьРазличия", Новый ОписаниеТипов("Булево")));
	МассивРеквизитовДобавления.Добавить(Новый РеквизитФормы("ДанныеДокументовМП", Новый ОписаниеТипов()) );
	
	МассивИндексовРаботПоДокументу = СервисВзаимодействияМПЭПДКлиентСервер.МассивИндексовРаботПоДокументу(Документ);
	
	// Реквизиты мобильного приложения
	МассивРеквизитовДобавления.Добавить(Новый РеквизитФормы("ДанныеМП", Новый ОписаниеТипов()) );	
	
	Для Каждого ИндексРаботы из МассивИндексовРаботПоДокументу Цикл
		ИмяЭлемента = ИмяСИндексом(ИндексРаботы, "МПНаименование");
		МассивРеквизитовДобавления.Добавить(Новый РеквизитФормы(ИмяЭлемента, Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(100)) ));
		
		ИмяЭлемента = ИмяСИндексом(ИндексРаботы, "МПФото");
		МассивРеквизитовДобавления.Добавить(Новый РеквизитФормы(ИмяЭлемента, Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки()) ));
		
		ИмяЭлемента = ИмяСИндексом(ИндексРаботы, "МПДобавленКакАвторТитула");
		МассивРеквизитовДобавления.Добавить(Новый РеквизитФормы(ИмяЭлемента, Новый ОписаниеТипов("Булево") ));
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьЭлементыМобильныхПриложений(Форма) Экспорт
	
	#Если НЕ ВнешнееСоединение Тогда
		
		Элементы = Форма.Элементы;
		Документ = Форма.Объект.Ссылка;
		РольУчастника = Форма.Объект.РольУчастника;
		
		ЭлементРодитель = Элементы.Найти("ГруппаПраво");
		
		Если ЭлементРодитель = Неопределено Тогда
			Возврат;	
		КонецЕсли;
		
		Если ЭлементРодитель.ПодчиненныеЭлементы.Количество() > 0 Тогда
			ГруппаВыравнивание = Элементы.Вставить("ГруппаМПВыравниваниеВправо", Тип("ГруппаФормы"), ЭлементРодитель,
				ЭлементРодитель.ПодчиненныеЭлементы[0]);
		Иначе
			ГруппаВыравнивание = Элементы.Добавить("ГруппаМПВыравниваниеВправо", Тип("ГруппаФормы"), ЭлементРодитель);
		КонецЕсли;
		ГруппаВыравнивание.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаВыравнивание.Отображение = ОтображениеОбычнойГруппы.Нет;
		ГруппаВыравнивание.ОтображатьЗаголовок = Ложь;
		ГруппаВыравнивание.РастягиватьПоГоризонтали = Истина;
		ГруппаВыравнивание.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Право;
		ГруппаВыравнивание.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		
		ГруппаПанельМПОбщая = Элементы.Добавить("ГруппаПанельМПОбщая", Тип("ГруппаФормы"), ГруппаВыравнивание); 
		ГруппаПанельМПОбщая.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаПанельМПОбщая.Отображение = ОтображениеОбычнойГруппы.Нет;
		ГруппаПанельМПОбщая.ОтображатьЗаголовок = Ложь;
		ГруппаПанельМПОбщая.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Право;
		ГруппаПанельМПОбщая.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		ГруппаПанельМПОбщая.РастягиватьПоГоризонтали = Ложь;
		ГруппаПанельМПОбщая.Ширина = ШиринаГруппЭлементовФормыМП();
		
		// Верхняя часть панели моб. устройств, заголовок "Мобильные устройства"
		ДекорацияВерхнийОтступ = Элементы.Добавить("ДекорацияВерхнийОтступМП", Тип("ДекорацияФормы"), ГруппаПанельМПОбщая);
		ДекорацияВерхнийОтступ.Вид = ВидДекорацииФормы.Надпись;
		ДекорацияВерхнийОтступ.РастягиватьПоГоризонтали = Ложь;
		//@skip-check new-font
		ДекорацияВерхнийОтступ.Заголовок = Новый ФорматированнаяСтрока(" ", Новый Шрифт( , , , , , , 5), , ,);
		
		ГруппаЗаголовокПанелиМП = Элементы.Добавить("ГруппаЗаголовокПанелиМП", Тип("ГруппаФормы"), ГруппаПанельМПОбщая);
		ГруппаЗаголовокПанелиМП.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаЗаголовокПанелиМП.Отображение = ОтображениеОбычнойГруппы.Нет;
		ГруппаЗаголовокПанелиМП.ОтображатьЗаголовок = Ложь;
		ГруппаЗаголовокПанелиМП.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Лево;
		ГруппаЗаголовокПанелиМП.ВертикальноеПоложениеПодчиненных = ВертикальноеПоложениеЭлемента.Центр;
		ГруппаЗаголовокПанелиМП.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
		ГруппаЗаголовокПанелиМП.Ширина = ШиринаГруппЭлементовФормыМП();
				
		КартинкаПанелиМП = Элементы.Добавить("КартинкаПанелиМП", Тип("ДекорацияФормы"), ГруппаЗаголовокПанелиМП);
		КартинкаПанелиМП.Вид = ВидДекорацииФормы.Картинка;
		КартинкаПанелиМП.РазмерКартинки = РазмерКартинки.Пропорционально;
		КартинкаПанелиМП.Высота = 1;
		КартинкаПанелиМП.Картинка = БиблиотекаКартинок.МобильноеУстройствоЭПД;
		
		ЗаголовокПанелиМП = Элементы.Добавить("ЗаголовокПанелиМП", Тип("ДекорацияФормы"), ГруппаЗаголовокПанелиМП);
		ЗаголовокПанелиМП.Вид = ВидДекорацииФормы.Надпись;
		ЗаголовокПанелиМП.РастягиватьПоГоризонтали = Истина;
		//@skip-check new-font
		ЗаголовокПанелиМП.Шрифт = Новый Шрифт(, 12, Истина, , , , );
		ЗаголовокПанелиМП.Заголовок = НСтр("ru = 'Мобильные устройства';
											|en = 'Mobile devices'");
		
		ИндексДекорацииРазделителя = 0;
		ДобавитьДекорациюРазделитель(ИндексДекорацииРазделителя, Элементы, ГруппаПанельМПОбщая);
		
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист")
			Или (ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") И РольУчастника = 1) Тогда
			ДобавитьКнопкуОткрытияНастроекМПНаФормах(Форма);
		КонецЕсли;
		
		// Элементы управления мобильных устройств
		МассивИндексовРаботПоДокументу = СервисВзаимодействияМПЭПДКлиентСервер.МассивИндексовРаботПоДокументу(Документ);
		Для Каждого ИндексРабот Из МассивИндексовРаботПоДокументу Цикл
			ДобавитьЭлементыМобильногоПриложения(Форма, ГруппаПанельМПОбщая, ИндексРабот);
		КонецЦикла;
				
	#КонецЕсли
	
КонецПроцедуры

// Возвращает настройки форм для оформления МП.
// 
// Параметры:
//  Отправитель - Неопределено, ОпределяемыйТип.Организация - Отправитель.
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
//  МассивПолучателей - Массив из Строка - Массив получателей.
//  ВидДокумента - СправочникСсылка.ВидыДокументовЭДО - Вид документа
// 
// Возвращаемое значение:
//  Структура - Настройки форм:
//  * ЭлектроннаяТранспортнаяНакладная - Структура:
//  ** ИндексыРаботОтображаемыеМП - Соответствие Из Число
//  * ЭлектронныйПутевойЛист - Структура:
//  ** ИндексыРаботОтображаемыеМП - Соответствие Из Число 
Функция ПолучитьНастройкиФормДляОформленияМП(Отправитель, ИдентификаторЭДО, МассивПолучателей, ВидДокумента) Экспорт
	
	НастройкиФорм = Неопределено;
	
	Если ЗначениеЗаполнено(ИдентификаторЭДО) Тогда
		НастройкиФорм = РегистрыСведений.НастройкиВзаимодействияМПЭПД.НастройкиФорм(ИдентификаторЭДО);
		
	Иначе
		НастройкиОтправки = Неопределено;
		Для Каждого Получатель Из МассивПолучателей Цикл
			КлючНастроекОтправки = НастройкиЭДОКлиентСервер.НовыйКлючНастроекОтправки();
			КлючНастроекОтправки.ВидДокумента = ВидДокумента;
			КлючНастроекОтправки.Отправитель = Отправитель;
			КлючНастроекОтправки.Получатель = Получатель;
			
			НастройкиОтправки = НастройкиОтправкиЭДОСлужебный.НастройкиОтправки(КлючНастроекОтправки);
			Если НастройкиОтправки <> Неопределено Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НастройкиОтправки <> Неопределено Тогда
			ИдентификаторЭДО = НастройкиОтправки.ИдентификаторОтправителя;
			НастройкиФорм = РегистрыСведений.НастройкиВзаимодействияМПЭПД.НастройкиФорм(ИдентификаторЭДО);
		КонецЕсли;
	КонецЕсли;
	
	//@skip-check constructor-function-return-section
	ВОзврат НастройкиФорм;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийСтороннихПодсистем

// Заполняет в строку списка документов дополнительные данные ЭПД.
//
// Параметры:
//  ЧтениеXML	 - ЧтениеXML
//  СтрТЗ		 - СтрокаТаблицыЗначений - СервисЭДО.НовыйРезультатПолученияСпискаДокументов
//
Процедура ЗаполнитьВСтрокуСпискаДокументовДополнительныеДанныеЭПД(ЧтениеXML, СтрТЗ) Экспорт
	
	Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента И ЧтениеXML.ЛокальноеИмя = "Filename" Тогда
		
		ЧтениеXML.Прочитать();
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			СтрТЗ.ИдФайл = ЧтениеXML.Значение;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при получении списка исходящих документов. Исключает из списка исходящих документов,
// полученных из сервиса ЭДО, записи с документами которые не нужно загружать.
//
// Параметры:
//  СтруктураСоСпискомВходящих 	- Структура - входящие документы, СервисЭДО.НовыйРезультатПолученияСпискаДокументов
//  СтруктураСоСпискомИсходящих - Структура - исходящие документы, СервисЭДО.НовыйРезультатПолученияСпискаДокументов
//	ИдентификаторУчетнойЗаписи  - Строка
//
Процедура ПриПолученииСпискаИсходящихДокументовСервисаЭДО(СтруктураСоСпискомВходящих, СтруктураСоСпискомИсходящих, ИдентификаторУчетнойЗаписи) Экспорт
	
	ТаблицаВходящихДокументов = СтруктураСоСпискомВходящих.ЭлектронныеДокументы;
	ТаблицаИсходящихДокументов = СтруктураСоСпискомИсходящих.ЭлектронныеДокументы;
	КоличествоСтрок = ТаблицаИсходящихДокументов.Количество();
	
	Если КоличествоСтрок = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Массив кодов, по которым загружаются исходящие документы (по остальным пропускаются).
	МассивЗагружаемыхКодов = Новый Массив;
	МассивЗагружаемыхКодов.Добавить("BeforeMedicalCheck");
	МассивЗагружаемыхКодов.Добавить("UOUMedicalCheck");
	МассивЗагружаемыхКодов.Добавить("BeforeTechnicalCheck");
	МассивЗагружаемыхКодов.Добавить("BeforeOdometerCheck");
	МассивЗагружаемыхКодов.Добавить("AfterOdometerCheck");
	МассивЗагружаемыхКодов.Добавить("AfterMedicalCheck");
	
	Для й = 1 По КоличествоСтрок Цикл
		Индекс = КоличествоСтрок - й;
		КодТранзакции = ТаблицаИсходящихДокументов[Индекс].КодТранзакции;
		ИДДокумента = ТаблицаИсходящихДокументов[Индекс].ИДДокумента;
		
		ДокументЗагружается = МассивЗагружаемыхКодов.Найти(КодТранзакции) <> Неопределено
			И ТаблицаВходящихДокументов.Найти(ИДДокумента, "ИДДокумента") = Неопределено;
		
		Если Не ДокументЗагружается Тогда
			ТаблицаИсходящихДокументов.Удалить(Индекс);
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаИсходящихДокументов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОтбора = УчетныеЗаписиЭДО.НовыйОтборУчетныхЗаписей();
	ОписаниеОтбора.УчетныеЗаписи = "&ИдентификаторЭДО";
	ОписаниеОтбора.СпособОбмена = "&СпособОбмена"; 
	
	Запрос = Новый Запрос;
	//@skip-check ql-temp-table-index
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТранспортныйКонтейнерЭДО.Ссылка КАК Ссылка,
	|	ТранспортныйКонтейнерЭДО.ВнешнийУИД КАК ВнешнийУИД
	|ПОМЕСТИТЬ ВТ_НайденныеТранспортныеКонтейнерыПоИДДокументооборота 
	|ИЗ
	|	Документ.ТранспортныйКонтейнерЭДО КАК ТранспортныйКонтейнерЭДО
	|ГДЕ
	|	ТранспортныйКонтейнерЭДО.ИдентификаторДокументооборота В (&МассивИдентификаторовДокументооборота)
	|	И ТранспортныйКонтейнерЭДО.Организация В
	|			(ВЫБРАТЬ
	|				ВТ_УчетныеЗаписиЭДО.Организация
	|			ИЗ
	|				ВТ_УчетныеЗаписиЭДО)
	|	И ТранспортныйКонтейнерЭДО.Направление В(&Исходящий)
	|	И НЕ ТранспортныйКонтейнерЭДО.ВнешнийУИД В(&МассивВнешнихУИД)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НайденныеТранспортныеКонтейнерыПоИДДокументооборота.Ссылка КАК Ссылка,
	|	ВТ_НайденныеТранспортныеКонтейнерыПоИДДокументооборота.ВнешнийУИД КАК ВнешнийУИД,
	|	ОбъектыТранспортныхКонтейнеровЭДО.Объект КАК СсылкаНаСообщениеЭДО
	|ПОМЕСТИТЬ ВТ_ОбъектыТранспортныхКонтейнеровЭДО
	|ИЗ
	|	ВТ_НайденныеТранспортныеКонтейнерыПоИДДокументооборота КАК ВТ_НайденныеТранспортныеКонтейнерыПоИДДокументооборота
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыТранспортныхКонтейнеровЭДО КАК ОбъектыТранспортныхКонтейнеровЭДО
	|		ПО ВТ_НайденныеТранспортныеКонтейнерыПоИДДокументооборота.Ссылка = ОбъектыТранспортныхКонтейнеровЭДО.ТранспортныйКонтейнер
	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОбъектыТранспортныхКонтейнеровЭДО.Ссылка КАК Ссылка,
	|	ВТ_ОбъектыТранспортныхКонтейнеровЭДО.ВнешнийУИД КАК ВнешнийУИД,
	|	ВЫРАЗИТЬ(СообщениеЭДОПрисоединенныеФайлы.ПолноеИмяФайла КАК СТРОКА(500)) КАК ИдФайл
	|ПОМЕСТИТЬ ВТ_НайденныеТранспортныеКонтейнерыПоИдФайл
	|ИЗ
	|	ВТ_ОбъектыТранспортныхКонтейнеровЭДО КАК ВТ_ОбъектыТранспортныхКонтейнеровЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СообщениеЭДО КАК СообщениеЭДО
	|		ПО ВТ_ОбъектыТранспортныхКонтейнеровЭДО.СсылкаНаСообщениеЭДО = СообщениеЭДО.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СообщениеЭДОПрисоединенныеФайлы КАК СообщениеЭДОПрисоединенныеФайлы
	|		ПО (СообщениеЭДО.ОсновнойФайл = СообщениеЭДОПрисоединенныеФайлы.Ссылка)
	|ГДЕ
	|	СообщениеЭДО.Направление = ЗНАЧЕНИЕ(Перечисление.НаправленияЭДО.Исходящий)
	|	И НЕ СообщениеЭДО.ПометкаУдаления
	|	И НЕ СообщениеЭДОПрисоединенныеФайлы.ПометкаУдаления
	|	И (ВЫРАЗИТЬ(СообщениеЭДОПрисоединенныеФайлы.ПолноеИмяФайла КАК СТРОКА(500))) В (&МассивИдФайл)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТранспортныйКонтейнерЭДО.Ссылка КАК Ссылка,
	|	ТранспортныйКонтейнерЭДО.ВнешнийУИД КАК ВнешнийУИД,
	|	"""" КАК ИдФайл,
	|	ЛОЖЬ КАК ЭтоОтборПоИдФайл
	|ИЗ
	|	Документ.ТранспортныйКонтейнерЭДО КАК ТранспортныйКонтейнерЭДО
	|ГДЕ
	|	ТранспортныйКонтейнерЭДО.ВнешнийУИД В(&МассивВнешнихУИД)
	|	И ТранспортныйКонтейнерЭДО.Направление В(&Исходящий)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_НайденныеТранспортныеКонтейнерыПоИдФайл.Ссылка КАК Ссылка,
	|	ВТ_НайденныеТранспортныеКонтейнерыПоИдФайл.ВнешнийУИД КАК ВнешнийУИД,
	|	ВТ_НайденныеТранспортныеКонтейнерыПоИдФайл.ИдФайл КАК ИдФайл,
	|	ИСТИНА КАК ЭтоОтборПоИдФайл
	|ИЗ
	|	ВТ_НайденныеТранспортныеКонтейнерыПоИдФайл КАК ВТ_НайденныеТранспортныеКонтейнерыПоИдФайл";
		
	МассивИдФайл = ТаблицаИсходящихДокументов.ВыгрузитьКолонку("ИдФайл");
	УдалитьИзМассиваВсеПустыеЗначения(МассивИдФайл);
	МассивВнешнихУИД = ТаблицаИсходящихДокументов.ВыгрузитьКолонку("ИДДокумента");	
	МассивИдентификаторовДокументооборота = ТаблицаИсходящихДокументов.ВыгрузитьКолонку("ИДДокументооборота");
		
	Запросы = Новый Массив;
	Запросы.Добавить(УчетныеЗаписиЭДО.ЗапросУчетныхЗаписей("ВТ_УчетныеЗаписиЭДО", ОписаниеОтбора));
	ИтоговыйЗапрос = ОбщегоНазначенияБЭД.СоединитьЗапросы(Запрос, Запросы);
	ИтоговыйЗапрос.УстановитьПараметр("ИдентификаторЭДО", ИдентификаторУчетнойЗаписи);
	ИтоговыйЗапрос.УстановитьПараметр("СпособОбмена", Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО);
	ИтоговыйЗапрос.УстановитьПараметр("МассивИдФайл", МассивИдФайл);
	ИтоговыйЗапрос.УстановитьПараметр("МассивИдентификаторовДокументооборота", МассивИдентификаторовДокументооборота);
	ИтоговыйЗапрос.УстановитьПараметр("МассивВнешнихУИД", МассивВнешнихУИД);
	ИтоговыйЗапрос.УстановитьПараметр("Исходящий", Перечисления.НаправленияЭДО.Исходящий);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = ИтоговыйЗапрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ЭтоОтборПоИдФайл Тогда
			ИдФайл = СокрЛП(Выборка.ИдФайл);
			СтрокаТаблицы = ТаблицаИсходящихДокументов.Найти(ИдФайл, "ИдФайл");
			ВнешнийУИД = СтрокаТаблицы.ИДДокумента;
			ТаблицаИсходящихДокументов.Удалить(СтрокаТаблицы);
			
			ТранспортныйКонтейнер = Выборка.Ссылка;
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Документ.ТранспортныйКонтейнерЭДО");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ТранспортныйКонтейнер);
			
			НачатьТранзакцию();
			Попытка
				Блокировка.Заблокировать();
				ТранспортныйКонтейнерОбъект = ТранспортныйКонтейнер.ПолучитьОбъект();
				ТранспортныйКонтейнерОбъект.ВнешнийУИД = ВнешнийУИД;
				ТранспортныйКонтейнерОбъект.Записать();
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ТекстСообщения = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
				ЭлектронноеВзаимодействие.ОбработатьОшибку(НСтр("ru = 'Создание нового транспортного контейнера ЭДО';
																|en = 'Create EDI transport container'"),
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), ТекстСообщения);
				ВызватьИсключение;
			КонецПопытки;
			
		Иначе
			ЗагруженныйВнешнийУИД = Выборка.ВнешнийУИД;
			СтрокаТаблицы = ТаблицаИсходящихДокументов.Найти(ЗагруженныйВнешнийУИД, "ИДДокумента");
			ТаблицаИсходящихДокументов.Удалить(СтрокаТаблицы);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТаблицаИсходящихДокументов.Количество() <> 0 Тогда
		ТаблицаИсходящихДокументов.Колонки.Добавить("ЭтоИсходящийДокумент", Новый ОписаниеТипов("Булево"));
		ТаблицаИсходящихДокументов.ЗаполнитьЗначения(Истина, "ЭтоИсходящийДокумент");
		СтруктураСоСпискомВходящих.Вставить("ЭлектронныеДокументыЭПДИсходящие", ТаблицаИсходящихДокументов);
	КонецЕсли;

КонецПроцедуры

// Вызывается при исключении загруженных контейнеров. СинхронизацияЭДОСлужебный.ПолучитьОписаниеДокументов.
// Добавляет в список документов в сервисе исходящие документы ЭПД.
//
// Параметры:
//	ДокументыДляЗагрузки - см. СервисЭДО.НоваяТаблицаКонтейнеров
//	ДокументыВСервисе - см. СервисЭДО.НоваяТаблицаКонтейнеров
//  СпискиДокументовЭПД - Структура:
// * ДатаПоследнегоЗапроса - Дата
// * ЭлектронныеДокументы - см. СервисЭДО.НоваяТаблицаКонтейнеров 
// * ЭлектронныеДокументыЭПДИсходящие - см. СервисЭДО.НоваяТаблицаКонтейнеров 
//
Процедура ПослеИсключенияКонтейнеровИзСпискаДокументовСервисаЭДО(ДокументыДляЗагрузки, ДокументыВСервисе, СпискиДокументовЭПД) Экспорт
	                                            
	Если СпискиДокументовЭПД.Свойство("ЭлектронныеДокументыЭПДИсходящие") Тогда
		ИсходящиеЭПД = СпискиДокументовЭПД.ЭлектронныеДокументыЭПДИсходящие;
		
		Если ДокументыДляЗагрузки.Колонки.Найти("ЭтоИсходящийДокумент") = Неопределено Тогда
			ДокументыДляЗагрузки.Колонки.Добавить("ЭтоИсходящийДокумент", Новый ОписаниеТипов("Булево"));
		КонецЕсли;
		
		Если ДокументыДляЗагрузки.Колонки.Найти("ИдФайл") = Неопределено Тогда
			ДокументыДляЗагрузки.Колонки.Добавить("ИдФайл", Новый ОписаниеТипов("Строка"));
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ИсходящиеЭПД, ДокументыДляЗагрузки);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ИсходящиеЭПД, ДокументыВСервисе);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается после инициализации основных параметров входящего транспортного контейнера.
// 
// Параметры:
// 	ПараметрыКонтейнера - см. ТранспортныеКонтейнерыЭДО.ПараметрыКонтейнера
// 	ОписаниеКонтейнера - СтрокаТаблицыЗначений из см. СервисЭДО.НоваяТаблицаКонтейнеров
//
Процедура ПослеИнициализацииОсновныхПараметровВходящегоКонтейнера(ПараметрыКонтейнера, ОписаниеКонтейнера) Экспорт

	Если ОписаниеКонтейнера.Владелец().Колонки.Найти("ЭтоИсходящийДокумент") = Неопределено
		Или ОписаниеКонтейнера.ЭтоИсходящийДокумент = Ложь Тогда
		Возврат;
	КонецЕсли;
		
	ДополнительныеПараметрыЭПД = Новый Структура;
	ДополнительныеПараметрыЭПД.Вставить("ЭтоИсходящийКонтейнер", Истина);
	ПараметрыКонтейнера.Вставить("ДополнительныеПараметрыЭПД", ДополнительныеПараметрыЭПД);
	
КонецПроцедуры

// Вызывается при заполнении контрагента при распаковке входящего транспортного контейнера.
//
// Параметры:
//  ПараметрыКонтейнера	 - см. ТранспортныеКонтейнерыЭДО.ПараметрыКонтейнера
//  РезультатРаспаковки	 - см. ТранспортныеКонтейнерыЭДО.РаспаковатьФайлКонтейнера
//  ПараметрыПриглашения - см. ПриглашенияЭДОКлиентСервер.КлючПриглашения
//  Контрагент			 - СправочникСсылка.Контрагенты	 - Контрагент, которого необходимо заполнить.
// 
Процедура ПриЗаполненииКонтрагентаПриРаспаковкеКонтейнера(ПараметрыКонтейнера, РезультатРаспаковки, ПараметрыПриглашения, Контрагент) Экспорт

	Если РезультатРаспаковки.Метаданные.Документы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка выполняется по имени файла, т.к. распаковка может происходить не в процессе загрузки контейнера,
	// а после (например из формы документа)
	ИмяФайла = РезультатРаспаковки.Метаданные.Документы[0].ОписаниеДанных.ИмяФайла;
	ИдентификаторОрганизации = ПараметрыКонтейнера.Получатель;
	ЭтоИсходящийКонтейнер = ЭтоИмяФайлаИсходящегоКонтейнераМП(ИмяФайла, ИдентификаторОрганизации);
	
	Если ЭтоИсходящийКонтейнер Тогда
		ПараметрыПриглашения.ИдентификаторОрганизации = РезультатРаспаковки.Карточка.ИдентификаторОтправителя;
		ПараметрыПриглашения.ИдентификаторКонтрагента = РезультатРаспаковки.Карточка.ИдентификаторПолучателя;
		ДанныеПриглашения = ПриглашенияЭДО.ДанныеПриглашенияПоПараметрам(ПараметрыПриглашения);
		Контрагент = ДанныеПриглашения.Контрагент;
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при заполнении параметров транспортного контейнера по данным файла. 
// Используется в ТранспортныеКонтейнерыЭДО.ЗаполнитьПараметрыКонтейнера.
// 
// Параметры:
// 	ПараметрыКонтейнера - см. ТранспортныеКонтейнерыЭДО.ПараметрыКонтейнера
// 	РезультатРаспаковки - см. ТранспортныеКонтейнерыЭДО.РаспаковатьФайлКонтейнера
//
Процедура ПриЗаполненииПараметровВходящегоКонтейнера(ПараметрыКонтейнера, РезультатРаспаковки) Экспорт

	Если ПараметрыКонтейнера.Свойство("ДополнительныеПараметрыЭПД")
		Или РезультатРаспаковки.Метаданные.Документы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка выполняется по имени файла, т.к. распаковка может происходить не в процессе загрузки контейнера,
	// а после (например из формы документа)
	ИмяФайла = РезультатРаспаковки.Метаданные.Документы[0].ОписаниеДанных.ИмяФайла;
	ИдентификаторОрганизации = ПараметрыКонтейнера.Получатель;
	ЭтоИсходящийКонтейнер = ЭтоИмяФайлаИсходящегоКонтейнераМП(ИмяФайла, ИдентификаторОрганизации);
	
	Если ЭтоИсходящийКонтейнер Тогда
		ДополнительныеПараметрыЭПД = Новый Структура;
		ДополнительныеПараметрыЭПД.Вставить("ЭтоИсходящийКонтейнер", Истина);
		ПараметрыКонтейнера.Вставить("ДополнительныеПараметрыЭПД", ДополнительныеПараметрыЭПД);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при заполнении и записи транспортного контейнера. см. ТранспортныеКонтейнерыЭДО.ЗаполнитьТранспортныйКонтейнер.
// 
// Параметры:
// 	ПараметрыКонтейнера - см. ТранспортныеКонтейнерыЭДО.ПараметрыКонтейнера
//  ДанныеЗаполнения - Структура - Данные контейнера для заполнения.
//
Процедура ПриЗаполненииВходящегоКонтейнера(ПараметрыКонтейнера, ДанныеЗаполнения) Экспорт

	Если Не ПараметрыКонтейнера.Свойство("ДополнительныеПараметрыЭПД") 
		Или Не ПараметрыКонтейнера.ДополнительныеПараметрыЭПД.ЭтоИсходящийКонтейнер Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЗаполнения.Вставить("Направление", Перечисления.НаправленияЭДО.Исходящий);
	ДанныеЗаполнения.Вставить("ВидСервисаЭДО", Перечисления.ВидыСервисовЭДО.ЭПД);
	
КонецПроцедуры

// Вызывается перед записью сообщения ЭДО. Изменяет реквизиты документа с учетом того, что титул отправлен МП.
//
// Параметры:
//  СообщениеОбъект		 		- ДокументОбъект.СообщениеЭДО
//  ОписаниеСообщения 			- Структура - см. ЭлектронныеДокументыЭДО.НовоеОписаниеСообщения
//  ЭлектронныйДокумент 		- ДокументСсылка.ЭлектронныйДокументВходящийЭДО, ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  НастройкиДокумента 			- Структура:
//  * ТипДокумента          	- ПеречислениеСсылка.ТипыДокументовЭДО
//  * ТипРегламента          	- ПеречислениеСсылка.ТипыРегламентовЭДО
//  * ТребуетсяПодтверждение	- Булево
//  * ОбменБезПодписи       	- Булево
//  * СпособОбмена          	- ПеречислениеСсылка.СпособыОбменаЭД
//
//@skip-check doc-comment-field-type
//@skip-check doc-comment-complex-type-with-link
//@skip-check doc-comment-description-ends-on-dot
//@skip-check doc-comment-field-type-strict
Процедура ПередЗаписьюВходящегоСообщенияЭДО(СообщениеОбъект, ОписаниеСообщения, ЭлектронныйДокумент, НастройкиДокумента) Экспорт
	
	ВозможныИзмененияВДокументеПоСвойствамСообщения =
		Не ЭлектронныйДокумент.Пустая()
		И ОписаниеСообщения.Направление = Перечисления.НаправленияЭДО.Входящий
		И (ОписаниеСообщения.ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_ОткМ
			Или ОписаниеСообщения.ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул2
			Или ОписаниеСообщения.ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул3
			Или ОписаниеСообщения.ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул4
			Или ОписаниеСообщения.ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул5
			Или ОписаниеСообщения.ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ЭПЛ_Титул6);
		
	Если Не ВозможныИзмененияВДокументеПоСвойствамСообщения Тогда
		Возврат;
	КонецЕсли;
	
	ИмяФайла = ОписаниеСообщения.Данные.Документ.ИмяФайла;
	ИдентификаторОрганизации = НастройкиДокумента.ИдентификаторОрганизации;
	ЭтоИсходящийКонтейнер = ЭтоИмяФайлаИсходящегоКонтейнераМП(ИмяФайла, ИдентификаторОрганизации);
	
	Если ЭтоИсходящийКонтейнер Тогда
		// Внесение изменений в сообщение.
		СообщениеОбъект.Направление = Перечисления.НаправленияЭДО.Исходящий;
	    СообщениеОбъект.Статус = Перечисления.СтатусыСообщенийЭДО.Отправлен;
		МодульРегламентаЭПД = ОбменСГИСЭПД.РегламентЭПД(НастройкиДокумента.ТипРегламента);
		ИспользоватьУтверждение = Ложь;
		Состояние = МодульРегламентаЭПД.СостояниеСообщения(СообщениеОбъект, НастройкиДокумента, ИспользоватьУтверждение);
		СообщениеОбъект.Состояние = Состояние;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДанныеМобильногоПриложения

// Возвращает список подключенных устройств.
// 
// Параметры:
//  Отборы - Неопределено, Структура - Отборы
//  ВключаяНеактивные - Булево - Включая неактивные
//  ВернутьМассив - Булево - Вернуть массив
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Список подключенных МП:
//  * ИдентификаторЭДО - Строка
//  * ИдентификаторМП - Строка
//  * Идентификатор - Строка
//  * Состояние - ПеречислениеСсылка.СостояниеМПЭПД
//  * ИндексКартинки - Число
//  * Фамилия - Строка
//  * Имя - Строка
//  * Отчество - Строка
//  * ИНН - Строка
//  * СНИЛС - Строка
//  * СерияВУ - Строка
//  * НомерВУ - Строка
//  * ДатаВыдачиВУ - Дата
//  * Наименование - Строка
//  * ВидПодписи - ПеречислениеСсылка.ВидыПодписиМПЭПД
//  * Телефоны - Строка
//  * МЧД - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//  * Фото - ХранилищеЗначения
//  * Роль - Число
//  * ВидыДокументов - Число
//  * ПредставлениеРоли - Строка
//  * Организация - ОпределяемыйТип.Организация
//  
//@skip-check module-query-syntax
//@skip-check bsl-ql-hub
//@skip-check doc-comment-description-ends-on-dot
Функция СписокПодключенныхМП(Отборы = Неопределено, ВключаяНеактивные = Ложь) Экспорт
	
	МетаданныеПеречисления = Перечисления.СостояниеМПЭПД.ПустаяСсылка().Метаданные();
	ПервоеЗначение = Истина;
	МассивЧастейСостояний = Новый Массив;
	Для Каждого ЗначениеПеречисления Из МетаданныеПеречисления.ЗначенияПеречисления Цикл
		СостояниеТекстом = ЗначениеПеречисления.Имя;
		Состояние = Перечисления.СостояниеМПЭПД[СостояниеТекстом];
		ИндексКартинки = Перечисления.СостояниеМПЭПД.ПолучитьИндексКартинки(Состояние);
		Если ПервоеЗначение Тогда
			Шаблон = "ВЫБРАТЬ
			|ЗНАЧЕНИЕ(Перечисление.СостояниеМПЭПД.%1) КАК Состояние,
			|%2 КАК ИндексКартинки
			|ПОМЕСТИТЬ ВТ_ИндексыКартинокСостояний";
			ПервоеЗначение = Ложь;
		Иначе
			Шаблон = "ВЫБРАТЬ
			|ЗНАЧЕНИЕ(Перечисление.СостояниеМПЭПД.%1) КАК Состояние,
			|%2 КАК ИндексКартинки";
		КонецЕсли;
		ТекстВставки = СтрШаблон(Шаблон, СостояниеТекстом, ИндексКартинки);
		МассивЧастейСостояний.Добавить(ТекстВставки);
	КонецЦикла;
	
	ПредставленияРолейМП = СервисВзаимодействияМПЭПДКлиентСервер.ПредставленияРолейМП();
	МассивЧастейПредставленийРолей = Новый Массив;
	ПервоеЗначение = Истина;
	Для Каждого КиЗ Из ПредставленияРолейМП Цикл
		Если ПервоеЗначение Тогда
			ШаблонПредставленияРолей = "ВЫБРАТЬ
			|%1 КАК ЧисловоеЗначение,
			|""%2"" КАК Представление
			|ПОМЕСТИТЬ ВТ_ПредставленияРолей";
			ПервоеЗначение = Ложь;
		Иначе
			ШаблонПредставленияРолей = "ВЫБРАТЬ
			|%1 КАК ЧисловоеЗначение,
			|""%2"" КАК Представление";
		КонецЕсли;
		ТекстВставки = СтрШаблон(ШаблонПредставленияРолей, КиЗ.Ключ, КиЗ.Значение);
		МассивЧастейПредставленийРолей.Добавить(ТекстВставки);	
	КонецЦикла;
	
	СтрокаОбъединения = "
	|ОБЪЕДИНИТЬ ВСЕ
	|";
	ТекстНачалаЗапроса = СтрСоединить(МассивЧастейСостояний, СтрокаОбъединения);
	
	ТекстНачалаЗапроса = ТекстНачалаЗапроса +
	";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|" + СтрСоединить(МассивЧастейПредставленийРолей, СтрокаОбъединения);
	
	ТекстЗапроса = ТекстНачалаЗапроса +
	";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиВзаимодействияМПЭПД.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	НастройкиВзаимодействияМПЭПД.Организация КАК Организация
	|ПОМЕСТИТЬ ВсеУчетныеЗаписи
	|ИЗ
	|	РегистрСведений.НастройкиВзаимодействияМПЭПД КАК НастройкиВзаимодействияМПЭПД
	|{ГДЕ
	|	НастройкиВзаимодействияМПЭПД.Организация,
	|	НастройкиВзаимодействияМПЭПД.ИдентификаторЭДО}
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодключенныеМПЭПД.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	ПодключенныеМПЭПД.ИдентификаторМП КАК ИдентификаторМП,
	|	ПодключенныеМПЭПД.Идентификатор КАК Идентификатор,
	|	ПодключенныеМПЭПД.Состояние КАК Состояние,
	|	ЕСТЬNULL(ВТ_ИндексыКартинокСостояний.ИндексКартинки, 1) КАК ИндексКартинки,
	|	ПодключенныеМПЭПД.Фамилия КАК Фамилия,
	|	ПодключенныеМПЭПД.Имя КАК Имя,
	|	ПодключенныеМПЭПД.Отчество КАК Отчество,
	|	ПодключенныеМПЭПД.ИНН КАК ИНН,
	|	ПодключенныеМПЭПД.СНИЛС КАК СНИЛС,
	|	ПодключенныеМПЭПД.СерияВУ КАК СерияВУ,
	|	ПодключенныеМПЭПД.НомерВУ КАК НомерВУ,
	|	ПодключенныеМПЭПД.ДатаВыдачиВУ КАК ДатаВыдачиВУ,
	|	ПодключенныеМПЭПД.Наименование КАК Наименование,
	|	ПодключенныеМПЭПД.ВидПодписи КАК ВидПодписи,
	|	ПодключенныеМПЭПД.Телефоны КАК Телефоны,
	|	ПодключенныеМПЭПД.МЧД КАК МЧД,
	|	ПодключенныеМПЭПД.Фото КАК Фото,
	|	ПодключенныеМПЭПД.Роль КАК Роль,
	|	ПодключенныеМПЭПД.ВидыДокументов КАК ВидыДокументов,
	|	ВТ_ПредставленияРолей.Представление КАК ПредставлениеРоли,
	|	ВсеУчетныеЗаписи.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ПодключенныеМПЭПД КАК ПодключенныеМПЭПД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеУчетныеЗаписи КАК ВсеУчетныеЗаписи
	|		ПО ПодключенныеМПЭПД.ИдентификаторЭДО = ВсеУчетныеЗаписи.ИдентификаторЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИндексыКартинокСостояний КАК ВТ_ИндексыКартинокСостояний
	|		ПО ПодключенныеМПЭПД.Состояние = ВТ_ИндексыКартинокСостояний.Состояние
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПредставленияРолей КАК ВТ_ПредставленияРолей
	|		ПО ПодключенныеМПЭПД.Роль = ВТ_ПредставленияРолей.ЧисловоеЗначение
	|{ГДЕ
	|	ПодключенныеМПЭПД.ИНН,
	|	ПодключенныеМПЭПД.СНИЛС,
	|	ПодключенныеМПЭПД.СерияВУ,
	|	ПодключенныеМПЭПД.НомерВУ,
	|	ПодключенныеМПЭПД.ДатаВыдачиВУ,
	|	ПодключенныеМПЭПД.Телефоны,
	|	ПодключенныеМПЭПД.ИдентификаторМП}";
	
	НовыйЗапрос = Новый ПостроительЗапроса;
	НовыйЗапрос.Текст = ТекстЗапроса;
	НовыйЗапрос.ЗаполнитьНастройки();
	
	Счетчик = 1;
	Если Отборы <> Неопределено Тогда
		Для Каждого КлючЗначение Из Отборы Цикл
			Если ТипЗнч(КлючЗначение.Значение) = Тип("СписокЗначений") Тогда
				НовыйОтбор = НовыйЗапрос.Отбор.Добавить(КлючЗначение.Ключ, "П" + Счетчик);
				НовыйОтбор.ВидСравнения = ВидСравнения.ВСписке;
				НовыйОтбор.Значение = КлючЗначение.Значение;
				НовыйОтбор.Использование = Истина;
			Иначе
				НовыйОтбор = НовыйЗапрос.Отбор.Добавить(КлючЗначение.Ключ, "П" + Счетчик);
				НовыйОтбор.ВидСравнения = ВидСравнения.Равно;
				НовыйОтбор.Значение = КлючЗначение.Значение;
				НовыйОтбор.Использование = Истина;
			КонецЕсли;
			Счетчик = Счетчик + 1;
		КонецЦикла;
	КонецЕсли;

	Если Не ВключаяНеактивные Тогда
		СписокСостояний = Новый СписокЗначений;
		СписокСостояний.Добавить(Перечисления.СостояниеМПЭПД.Недоступен);
		СписокСостояний.Добавить(Перечисления.СостояниеМПЭПД.ОжидаетсяПодключение);
		СписокСостояний.Добавить(Перечисления.СостояниеМПЭПД.Подключен);
		СписокСостояний.Добавить(Перечисления.СостояниеМПЭПД.ПодключениеНеВыполнено);
		СписокСостояний.Добавить(Перечисления.СостояниеМПЭПД.Черновик);
		НовыйОтбор = НовыйЗапрос.Отбор.Добавить("Состояние", "П" + Счетчик);
		НовыйОтбор.ВидСравнения = ВидСравнения.ВСписке;
		НовыйОтбор.Значение = СписокСостояний;
		НовыйОтбор.Использование = Истина;
	КонецЕсли;
	
	Запрос = НовыйЗапрос.ПолучитьЗапрос();
	РезультатТаблица = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатТаблица;
	
КонецФункции

// Осуществляет поиск МП по идентификатору или ключевым полям. При поиске по ключевым полям
// запись ищется либо по ИНН, либо полям ВУ, либо по ФИО.
//
// Параметры:
//  ИдентификаторЭДО		 - Строка	 - Идентификатор ЭДО
//  Роль					 - Число	 - Числовое представление роли пользователя
//  Идентификатор			 - Неопределено, Строка	 - Если Неопределено тогда поиск осуществляется по "Идентификатор"
//  СтруктураКлючевыхПолей	 - Неопределено, Структура - Если Неопределено тогда поиск осуществляется по "СтруктураКлючевыхПолей"
// 
// Возвращаемое значение:
//  Неопределено, Структура - Если запись найдена возвращается структура:
//  * ИдентификаторЭДО - Строка
//  * ИдентификаторМП - Строка
//  * Идентификатор - Строка
//  * Состояние - ПеречислениеСсылка.СостояниеМПЭПД
//  * ИндексКартинки - Число
//  * Фамилия - Строка
//  * Имя - Строка
//  * Отчество - Строка
//  * ИНН - Строка
//  * СНИЛС - Строка
//  * СерияВУ - Строка
//  * НомерВУ - Строка
//  * ДатаВыдачиВУ - Дата
//  * Наименование - Строка
//  * ВидПодписи - ПеречислениеСсылка.ВидыПодписиМПЭПД
//  * Телефоны - Строка
//  * МЧД - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//  * Фото - ХранилищеЗначения
//  * Роль - Число
//  * ВидыДокументов - Число
//  * ПредставлениеРоли - Строка
//  * Организация - ОпределяемыйТип.Организация
//
//@skip-check module-query-syntax
//@skip-check bsl-ql-hub
//@skip-check doc-comment-description-ends-on-dot
Функция СтруктураДанныхМП(ИдентификаторЭДО, Роль, Идентификатор = Неопределено, СтруктураКлючевыхПолей = Неопределено) Экспорт
	
	ПредставленияРолейМП = СервисВзаимодействияМПЭПДКлиентСервер.ПредставленияРолейМП();
	МассивЧастейПредставленийРолей = Новый Массив;
	ПервоеЗначение = Истина;
	Для Каждого КиЗ Из ПредставленияРолейМП Цикл
		Если ПервоеЗначение Тогда
			
			ШаблонПредставленияРолей = "ВЫБРАТЬ
			|%1 КАК ЧисловоеЗначение,
			|""%2"" КАК Представление
			|ПОМЕСТИТЬ ВТ_ПредставленияРолей";
			ПервоеЗначение = Ложь;
		Иначе
			ШаблонПредставленияРолей = "ВЫБРАТЬ
			|%1 КАК ЧисловоеЗначение,
			|""%2"" КАК Представление";
		КонецЕсли;
		ТекстВставки = СтрШаблон(ШаблонПредставленияРолей, КиЗ.Ключ, КиЗ.Значение);
		МассивЧастейПредставленийРолей.Добавить(ТекстВставки);	
	КонецЦикла;
	СтрокаОбъединения = "
	|ОБЪЕДИНИТЬ ВСЕ
	|";
	ТекстЗапросаРолей = СтрСоединить(МассивЧастейПредставленийРолей, СтрокаОбъединения);
	
	РазделительПакетаЗапросов = ОбщегоНазначения.РазделительПакетаЗапросов();
	
    ТекстЗапроса = ТекстЗапросаРолей + РазделительПакетаЗапросов + 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиВзаимодействияМПЭПД.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	НастройкиВзаимодействияМПЭПД.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_УчетныеЗаписи
	|ИЗ
	|	РегистрСведений.НастройкиВзаимодействияМПЭПД КАК НастройкиВзаимодействияМПЭПД
	|ГДЕ
	|	НастройкиВзаимодействияМПЭПД.ИдентификаторЭДО = &ИдентификаторЭДО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодключенныеМПЭПД.ИдентификаторЭДО КАК ИдентификаторЭДО,
	|	ПодключенныеМПЭПД.ИдентификаторМП КАК ИдентификаторМП,
	|	ПодключенныеМПЭПД.Идентификатор КАК Идентификатор,
	|	ПодключенныеМПЭПД.Состояние КАК Состояние,
	|	ПодключенныеМПЭПД.Фамилия КАК Фамилия,
	|	ПодключенныеМПЭПД.Имя КАК Имя,
	|	ПодключенныеМПЭПД.Отчество КАК Отчество,
	|	ПодключенныеМПЭПД.ИНН КАК ИНН,
	|	ПодключенныеМПЭПД.СНИЛС КАК СНИЛС,
	|	ПодключенныеМПЭПД.СерияВУ КАК СерияВУ,
	|	ПодключенныеМПЭПД.НомерВУ КАК НомерВУ,
	|	ПодключенныеМПЭПД.ДатаВыдачиВУ КАК ДатаВыдачиВУ,
	|	ПодключенныеМПЭПД.Наименование КАК Наименование,
	|	ПодключенныеМПЭПД.ВидПодписи КАК ВидПодписи,
	|	ПодключенныеМПЭПД.Телефоны КАК Телефоны,
	|	ПодключенныеМПЭПД.МЧД КАК МЧД,
	|	ПодключенныеМПЭПД.Фото КАК Фото,
	|	ПодключенныеМПЭПД.Роль КАК Роль,
	|	ПодключенныеМПЭПД.ВидыДокументов КАК ВидыДокументов,
	|	ВТ_ПредставленияРолей.Представление КАК ПредставлениеРоли,
	|	ВТ_УчетныеЗаписи.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.ПодключенныеМПЭПД КАК ПодключенныеМПЭПД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_УчетныеЗаписи КАК ВТ_УчетныеЗаписи
	|		ПО ПодключенныеМПЭПД.ИдентификаторЭДО = ВТ_УчетныеЗаписи.ИдентификаторЭДО
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПредставленияРолей КАК ВТ_ПредставленияРолей
	|		ПО ПодключенныеМПЭПД.Роль = ВТ_ПредставленияРолей.ЧисловоеЗначение
	|ГДЕ
	|	&ТекстУсловийПоиска";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторЭДО", ИдентификаторЭДО);
	
	Если ЗначениеЗаполнено(Идентификатор) Тогда
		
		Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
		ТекстУсловийПоиска = "Идентификатор = &Идентификатор";
		
	Иначе
		МассивТекстовУсловий = Новый Массив;
		
		РазделительУсловийИ = "
		|	И ";
		РазделительУсловийИли = "
		|	ИЛИ ";
		
		// Условия поиска по ФИО
		МассивУсловий = Новый Массив;
		ТекстУсловияЗапроса = СформироватьТекстУсловияЗапроса(СтруктураКлючевыхПолей, "Фамилия", Запрос);
		СервисВзаимодействияМПЭПДКлиентСервер.ДобавитьНеПустоеЗначениеВМассив(МассивУсловий, ТекстУсловияЗапроса);
		ТекстУсловияЗапроса = СформироватьТекстУсловияЗапроса(СтруктураКлючевыхПолей, "Имя", Запрос);
		СервисВзаимодействияМПЭПДКлиентСервер.ДобавитьНеПустоеЗначениеВМассив(МассивУсловий, ТекстУсловияЗапроса);
		ТекстУсловияЗапроса = СформироватьТекстУсловияЗапроса(СтруктураКлючевыхПолей, "Отчество", Запрос);
		СервисВзаимодействияМПЭПДКлиентСервер.ДобавитьНеПустоеЗначениеВМассив(МассивУсловий, ТекстУсловияЗапроса);
		
		ТекстУсловияЗапроса = СтрШаблон("(%1)", СтрСоединить(МассивУсловий, РазделительУсловийИ));
		СервисВзаимодействияМПЭПДКлиентСервер.ДобавитьНеПустоеЗначениеВМассив(МассивТекстовУсловий, ТекстУсловияЗапроса);
		
		// Условия поиска по ВУ
		МассивУсловий = Новый Массив;
		ТекстУсловияЗапроса = СформироватьТекстУсловияЗапроса(СтруктураКлючевыхПолей, "СерияВУ", Запрос);
		СервисВзаимодействияМПЭПДКлиентСервер.ДобавитьНеПустоеЗначениеВМассив(МассивУсловий, ТекстУсловияЗапроса);
		ТекстУсловияЗапроса = СформироватьТекстУсловияЗапроса(СтруктураКлючевыхПолей, "НомерВУ", Запрос);
		СервисВзаимодействияМПЭПДКлиентСервер.ДобавитьНеПустоеЗначениеВМассив(МассивУсловий, ТекстУсловияЗапроса);
		ТекстУсловияЗапроса = СформироватьТекстУсловияЗапроса(СтруктураКлючевыхПолей, "ДатаВыдачиВУ", Запрос);
		СервисВзаимодействияМПЭПДКлиентСервер.ДобавитьНеПустоеЗначениеВМассив(МассивУсловий, ТекстУсловияЗапроса);
		
		ТекстУсловияЗапроса = СтрШаблон("(%1)", СтрСоединить(МассивУсловий, РазделительУсловийИ));
		СервисВзаимодействияМПЭПДКлиентСервер.ДобавитьНеПустоеЗначениеВМассив(МассивТекстовУсловий, ТекстУсловияЗапроса);
		
		// Прочие условия
		МассивИсключенныхПолей = Новый Массив;
		МассивИсключенныхПолей.Добавить("Фамилия");
		МассивИсключенныхПолей.Добавить("Имя");
		МассивИсключенныхПолей.Добавить("Отчество");
		МассивИсключенныхПолей.Добавить("СерияВУ");
		МассивИсключенныхПолей.Добавить("НомерВУ");
		МассивИсключенныхПолей.Добавить("ДатаВыдачиВУ");
				
		Для Каждого КлючЗначение Из СтруктураКлючевыхПолей Цикл
			Если МассивИсключенныхПолей.Найти(КлючЗначение.Ключ) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстУсловияЗапроса = СформироватьТекстУсловияЗапроса(СтруктураКлючевыхПолей, КлючЗначение.Ключ, Запрос);
			СервисВзаимодействияМПЭПДКлиентСервер.ДобавитьНеПустоеЗначениеВМассив(МассивТекстовУсловий, ТекстУсловияЗапроса);
		КонецЦикла;
		
		// Выполнение запроса, получение результата
		Если МассивТекстовУсловий.Количество() = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		МассивВключающихРолей = ОбменСГИСЭПДКлиентСервер.ВсеВключающиеРоли(Роль, 5);
		Запрос.УстановитьПараметр("МассивВключающихРолей", МассивВключающихРолей);
		ТекстУсловийПоиска = СтрШаблон("ПодключенныеМПЭПД.Роль В(&МассивВключающихРолей) И (%1)",
			СтрСоединить(МассивТекстовУсловий, РазделительУсловийИли));
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловийПоиска", ТекстУсловийПоиска);
	Запрос.Текст = ТекстЗапроса;
	
	ТаблицаУстройств = Запрос.Выполнить().Выгрузить();
	Если ТаблицаУстройств.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаУстройств[0]);
	//@skip-check constructor-function-return-section
	Возврат Результат;
	
КонецФункции

Процедура УдалитьЗаписьПодключенныхМПЭПД(ИдентификаторЭДО, Идентификатор) Экспорт
	
	ПараметрыЗаписи = РегистрыСведений.ПодключенныеМПЭПД.ПараметрыЗаписиРегистра(ИдентификаторЭДО, Идентификатор);
	РегистрыСведений.ПодключенныеМПЭПД.УдалитьЗапись(ПараметрыЗаписи);
	
КонецПроцедуры

// Возвращает данные мобильного приложения для записи в реестр ЭПД.
//
// Параметры:
//  КоллекцияЗаписейРегистра - Произвольный	 - Коллекция с данными регистра и возможностью изменения. См. ОбменСГИСЭПДКлиентСервер.НоваяСтруктураДляРеестраЭПД.
//  ЗаписьИзФормы			 - Булево		 - Запись документа из формы означает прочтение сообщения от МП.
//
Процедура ДобавитьДанныеМобильногоПриложенияДляЗаписиВРеестр(КоллекцияЗаписейРегистра, ЗаписьИзФормы) Экспорт
	
	Если КоллекцияЗаписейРегистра.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаДоступныхВзаимодействий = Новый ТаблицаЗначений;
	ОписаниеТиповДокумент = Новый ОписаниеТипов("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная, ДокументСсылка.ЭлектронныйПутевойЛист");
	ТаблицаДоступныхВзаимодействий.Колонки.Добавить("Документ", ОписаниеТиповДокумент); 
	ТаблицаДоступныхВзаимодействий.Колонки.Добавить("ВидВзаимодействия", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыВзаимодействияМПЭПД"));
	
	НужноРассчитатьДанныеОПросмотреМП = Ложь;
	Для Каждого ЗаписьРегистра Из КоллекцияЗаписейРегистра Цикл
		Документ = ЗаписьРегистра.Ссылка;
		РольУчастника = ЗаписьРегистра.РольУчастника;
		
		// Входящее событие о просмотре титула в МП будет расчитываться только для ЭТрН, для других типов документов будет проигнорировано
		Если Не НужноРассчитатьДанныеОПросмотреМП
			И ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
			НужноРассчитатьДанныеОПросмотреМП = Истина;
		КонецЕсли;
		
		СоответствиеТитуловИШагов = ОбменСГИСЭПДКлиентСервер.СоответствиеТитуловИШагов(Документ);
		СоответствиеТитуловИШагов = СервисВзаимодействияМПЭПДКлиентСервер.ПеревернутьСоответствие(СоответствиеТитуловИШагов);
		
		Титул = СоответствиеТитуловИШагов.Получить(ЗаписьРегистра.ТекущийШаг);
		ИндексРаботыПоТитулу = СервисВзаимодействияМПЭПДКлиентСервер.ИндексРаботыПоТитулу(Титул);
		ИндексРаботыДоступен = СервисВзаимодействияМПЭПДКлиентСервер.ИндексРаботыДоступенДляРолиУчастника(Документ,
			РольУчастника, ИндексРаботыПоТитулу);
		Если Не ИндексРаботыДоступен Тогда
			Продолжить;
		КонецЕсли;
		
		МассивДоступныхВидовВзаимодействия = СервисВзаимодействияМПЭПДКлиентСервер.МассивДоступныхВидовВзаимодействияПоТитулу(Титул);
		Для Каждого ВидВзаимодействия Из МассивДоступныхВидовВзаимодействия Цикл
			НоваяСтрока = ТаблицаДоступныхВзаимодействий.Добавить();
			НоваяСтрока.Документ = Документ;
			НоваяСтрока.ВидВзаимодействия = ВидВзаимодействия;
		КонецЦикла;
	КонецЦикла;
	
	Если ТаблицаДоступныхВзаимодействий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	//@skip-check ql-temp-table-index
	//@skip-check bsl-ql-hub
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДоступныхВзаимодействий.Документ КАК Документ,
	|	ТаблицаДоступныхВзаимодействий.ВидВзаимодействия КАК ВидВзаимодействия
	|ПОМЕСТИТЬ ВТ_ТаблицаДоступныхВзаимодействий
	|ИЗ
	|	&ТаблицаДоступныхВзаимодействий КАК ТаблицаДоступныхВзаимодействий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КэшВзаимодействияМПЭПД.ДокументЭПД КАК Документ,
	|	КэшВзаимодействияМПЭПД.ДанныеСообщения КАК ДанныеСообщения,
	|	КэшВзаимодействияМПЭПД.ИдентификаторМП КАК ИдентификаторМП,
	|	КэшВзаимодействияМПЭПД.ВидСообщения КАК ВидСообщения,
	|	ПодключенныеМПЭПД.Идентификатор КАК ИдентификаторЗаписиМП,
	|	ПодключенныеМПЭПД.Наименование КАК НаименованиеМП
	|ИЗ
	|	ВТ_ТаблицаДоступныхВзаимодействий КАК ВТ_ТаблицаДоступныхВзаимодействий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КэшВзаимодействияМПЭПД КАК КэшВзаимодействияМПЭПД
	|		ПО ВТ_ТаблицаДоступныхВзаимодействий.Документ = КэшВзаимодействияМПЭПД.ДокументЭПД
	|			И ВТ_ТаблицаДоступныхВзаимодействий.ВидВзаимодействия = КэшВзаимодействияМПЭПД.ВидСообщения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПодключенныеМПЭПД КАК ПодключенныеМПЭПД
	|		ПО КэшВзаимодействияМПЭПД.ИдентификаторМП = ПодключенныеМПЭПД.ИдентификаторМП
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ
	|ИТОГИ ПО
	|	ДокументЭПД";
	
	Если НужноРассчитатьДанныеОПросмотреМП Тогда
		ТекстЗапросаДанныхОпросмотреМП = 
		"ВЫБРАТЬ
		|	ЭлектроннаяТранспортнаяНакладная.Ссылка КАК Документ,
		|	ЭлектроннаяТранспортнаяНакладная.ТитулПереадресовкаИдентификаторФайла КАК ТитулПереадресовкаИдентификаторФайла,
		|	ЭлектроннаяТранспортнаяНакладная.ДопТитулПереадресовкаИдентификаторФайла КАК ДопТитулПереадресовкаИдентификаторФайла,
		|	ЭлектроннаяТранспортнаяНакладная.ТитулПеревозчикаЗаменыИдентификаторФайла КАК ТитулПеревозчикаЗаменыИдентификаторФайла,
		|	ЭлектроннаяТранспортнаяНакладная.ДопТитулПеревозчикаЗаменыИдентификаторФайла КАК ДопТитулПеревозчикаЗаменыИдентификаторФайла
		|ИЗ
		|	Документ.ЭлектроннаяТранспортнаяНакладная КАК ЭлектроннаяТранспортнаяНакладная
		|ГДЕ
		|	ЭлектроннаяТранспортнаяНакладная.Ссылка В
		|			(ВЫБРАТЬ
		|				ВТ_ТаблицаДоступныхВзаимодействий.Документ
		|			ИЗ
		|				ВТ_ТаблицаДоступныхВзаимодействий)";
		РазделительПакетаЗапросов = ОбщегоНазначения.РазделительПакетаЗапросов();
		Запрос.Текст = Запрос.Текст + РазделительПакетаЗапросов + ТекстЗапросаДанныхОпросмотреМП;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТаблицаДоступныхВзаимодействий", ТаблицаДоступныхВзаимодействий);
	
	Если НужноРассчитатьДанныеОПросмотреМП Тогда
		МассивРезультатов = Запрос.ВыполнитьПакет();
		ВыборкаДокументов = МассивРезультатов[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ВыборкаДанныхОПросмотреДокумента = МассивРезультатов[2].Выбрать();
	Иначе
		ВыборкаДокументов = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	КонецЕсли;
	
	СтруктураПоискаПоДокументу = Новый Структура;
	СтруктураПоискаПоДокументу.Вставить("Документ");
	
	СоответствиеТитуловТипамМП = СервисВзаимодействияМПЭПДКлиентСервер.СоответствиеТитуловТипамМП();
	
	Для Каждого ЗаписьРегистра Из КоллекцияЗаписейРегистра Цикл
		ВыборкаДокументов.Сбросить();
		Документ = ЗаписьРегистра.Ссылка;
		СоответствиеТитуловИШагов = ОбменСГИСЭПДКлиентСервер.СоответствиеТитуловИШагов(Документ);
		СоответствиеТитуловИШагов = СервисВзаимодействияМПЭПДКлиентСервер.ПеревернутьСоответствие(СоответствиеТитуловИШагов);
		ТитулЗаписиРегистра = СоответствиеТитуловИШагов.Получить(ЗаписьРегистра.ТекущийШаг);
		
		СтруктураПоискаПоДокументу.Документ = Документ;
		Если Не ВыборкаДокументов.НайтиСледующий(СтруктураПоискаПоДокументу) Тогда
			// Нет доступных взаимодействий по титулу.
			ЗаписьРегистра.МобильноеПриложение = "";
			ЗаписьРегистра.ИдентификаторЗаписиМП = "";
			ЗаписьРегистра.НаименованиеМП = "";
			ЗаписьРегистра.ТекущийШагМП = "";
			Продолжить;
		КонецЕсли;
		
		ВыборкаДанныхПоДокументу = ВыборкаДокументов.Выбрать();
		ШагиТребующиеПросмотрМП = ШагиТребующиеПросмотрМП();
		
		Пока ВыборкаДанныхПоДокументу.Следующий() Цикл
			ЭтоСообщение = СервисВзаимодействияМПЭПДКлиентСервер.ЭтоТипВзаимодействияСообщение(ВыборкаДанныхПоДокументу.ВидСообщения);
			Если Не ЭтоСообщение Тогда
				Прервать;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ВыборкаДанныхПоДокументу.ДанныеСообщения) Тогда
				СтруктураДанныхСообщения = СтруктураИзJson(ВыборкаДанныхПоДокументу.ДанныеСообщения);
				Тип = Неопределено;
				СтруктураДанныхСообщения.Свойство("Type", Тип);
				
				ЭтоТипСообщенияОбОтправкеТитула = СервисВзаимодействияМПЭПДКлиентСервер.ЭтоТипСообщенияОбОтправкеТитулаИзМП(Тип);
				Если ЭтоТипСообщенияОбОтправкеТитула Тогда
					ТипДляОпределенияТитула = СервисВзаимодействияМПЭПДКлиентСервер.ПривестиТипСообщенияКВидуПолученияТитулаБЭД(Тип);
				Иначе
					ТипДляОпределенияТитула = Тип;
				КонецЕсли;
				
				ТитулВСообщении = СоответствиеТитуловТипамМП.Получить(ТипДляОпределенияТитула);
				Если ТитулВСообщении = ТитулЗаписиРегистра Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
			
			ЭтоСообщение = Ложь;
		КонецЦикла;
		
		ЭтоПривязкаДокумента = СервисВзаимодействияМПЭПДКлиентСервер.ЭтоПривязкаДокумента(ВыборкаДанныхПоДокументу.ВидСообщения);
		
		Если Не ЭтоПривязкаДокумента И Не ЭтоСообщение Тогда
			// Обработка отвязки документа от МП.
			ЗаписьРегистра.МобильноеПриложение = "";
			ЗаписьРегистра.ИдентификаторЗаписиМП = "";
			ЗаписьРегистра.НаименованиеМП = "";
			ЗаписьРегистра.ТекущийШагМП = "";
			ЗаписьРегистра.ОжидаемПросмотрМП = Ложь;
		
		ИначеЕсли ЭтоСообщение Тогда
			Принято = Неопределено;
			СтруктураДанныхСообщения.Свойство("Принято", Принято);
			
			// Обработка событий с сообщениями и непосредственно сообщений (уведомлений).
			ЗаписьРегистра.МобильноеПриложение = ВыборкаДанныхПоДокументу.ИдентификаторМП;
			ЗаписьРегистра.ИдентификаторЗаписиМП = ВыборкаДанныхПоДокументу.ИдентификаторЗаписиМП;
			ЗаписьРегистра.НаименованиеМП = ВыборкаДанныхПоДокументу.НаименованиеМП;
			
			Если ЭтоТипСообщенияОбОтправкеТитула Тогда
				ЗаписьРегистра.ТекущийШагМП = Тип;
			ИначеЕсли Тип <> Неопределено Тогда
				ЗаписьРегистра.ТекущийШагМП = Тип + ?(Принято = Ложь, "_reject", "");	
			КонецЕсли;		
			
			Если НужноРассчитатьДанныеОПросмотреМП И ЗаписьРегистра.ОжидаемПросмотрМП = Истина Тогда
				ВыборкаДанныхОПросмотреДокумента.Сбросить();
				Если ВыборкаДанныхОПросмотреДокумента.НайтиСледующий(СтруктураПоискаПоДокументу) Тогда
					ЗаписьРегистра.ОжидаемПросмотрМП = ОжидаемПросмотрМП(ЗаписьРегистра, СтруктураДанныхСообщения,
						ВыборкаДанныхОПросмотреДокумента);
				Иначе
					ЗаписьРегистра.ОжидаемПросмотрМП = Ложь;
				КонецЕсли;
			Иначе
				ЗаписьРегистра.ОжидаемПросмотрМП = Ложь;
			КонецЕсли;
			
		Иначе
			// Обработка взаимодействий без сообщений (например вызываных из БЭД или событий привзяки).
			ЗаписьРегистра.МобильноеПриложение = ВыборкаДанныхПоДокументу.ИдентификаторМП;
			ЗаписьРегистра.ИдентификаторЗаписиМП = ВыборкаДанныхПоДокументу.ИдентификаторЗаписиМП;
			ЗаписьРегистра.НаименованиеМП = ВыборкаДанныхПоДокументу.НаименованиеМП;
			
			Если ШагиТребующиеПросмотрМП.Найти(ЗаписьРегистра.ТекущийШаг) <> Неопределено Тогда
				ЗаписьРегистра.ОжидаемПросмотрМП = ЗаписьРегистра.ТекущийШагВыполнен = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		// Если метод вызван интерактивно (из формы документа), необходимо очистить текущий шаг МП.
		// Очищенный шаг МП означает реакцию пользователя на событие МП.
		Если ЗаписьИзФормы Тогда
			ЗаписьРегистра.ТекущийШагМП = "";
		КонецЕсли;
		
	КонецЦикла; // По коллекции записей регистра
	
КонецПроцедуры

#КонецОбласти

// Ожидает выполнение фоновой функции.
// 
// Параметры:
//  ИмяФункции - Строка - Имя функции.
//  ИдентификаторФормы - Строка - Идентификатор формы.
//  ПараметрыФункции - Неопределено, Массив из Произвольный - Параметры функции.
// 
// Возвращаемое значение:
//  Структура - Ожидать выполнение фоновой функции:
// * Статус - Строка - 
// * ИдентификаторЗадания - Неопределено 
// * АдресРезультата - Строка
// * АдресДополнительногоРезультата - Строка 
// * ИнформацияОбОшибке - Неопределено
// * КраткоеПредставлениеОшибки - Строка 
// * ПодробноеПредставлениеОшибки - Строка 
// * Сообщения - ФиксированныйМассив из СообщениеПользователю
//
//@skip-check constructor-function-return-section
Функция ОжидатьВыполнениеФоновойФункции(ИмяФункции, ИдентификаторФормы, ПараметрыФункции = Неопределено) Экспорт
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Сервис взаимодействия МП ЭПД.';
			|en = 'Electronic shipment document MA collaboration service.'") + ИмяФункции;
		
	Если ПараметрыФункции = Неопределено Тогда	
		Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяФункции);
	ИначеЕсли ПараметрыФункции.Количество() = 1 Тогда
		Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяФункции,
			ПараметрыФункции[0]);
	ИначеЕсли ПараметрыФункции.Количество() = 2 Тогда
		Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяФункции,
			ПараметрыФункции[0], ПараметрыФункции[1]);
	ИначеЕсли ПараметрыФункции.Количество() = 3 Тогда
		Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяФункции,
			ПараметрыФункции[0], ПараметрыФункции[1], ПараметрыФункции[2]);
	ИначеЕсли ПараметрыФункции.Количество() = 4 Тогда
		Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяФункции,
			ПараметрыФункции[0], ПараметрыФункции[1], ПараметрыФункции[2], ПараметрыФункции[3]);
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьЗначенияОбъекта(ТекущийОбъект, СвойстваОбъекта) Экспорт

	ТолькоИзмененные = Новый Структура;
	Для Каждого СтрокаСвойства Из СвойстваОбъекта Цикл
		Если СтрокаСвойства.Значение <> Неопределено Тогда
			ТолькоИзмененные.Вставить(СтрокаСвойства.Ключ, СтрокаСвойства.Значение);
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(ТекущийОбъект, ТолькоИзмененные);
	
КонецПроцедуры

// Возвращает реквизит формы по имени.
// 
// Параметры:
//  ТекущаяФорма - ФормаКлиентскогоПриложения
//  ИмяРеквизита - Строка
// 
// Возвращаемое значение:
//  Неопределено, РеквизитФормы - Реквизит формы.
//  
//@skip-check bsl-legacy-check-dynamic-feature-access
//@skip-check doc-comment-description-ends-on-dot
Функция РеквизитФормы(ТекущаяФорма, ИмяРеквизита) Экспорт
	
	Результат = Неопределено;
	
	ВсеРеквизиты = ТекущаяФорма.ПолучитьРеквизиты();
	Для Каждого СтрокаМассива Из ВсеРеквизиты Цикл
		Если СтрокаМассива.Имя = ИмяРеквизита Тогда
			Результат = СтрокаМассива;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает данные физического лица.
// 
// Параметры:
//  СсылкаСправочника - СправочникСсылка
// 
// Возвращаемое значение:
//  Структура - Получить данные физического лица:
// * ИНН - Строка 
// * СтраховойНомерПФР - Строка 
// * Фамилия - Строка
// * Имя - Строка
// * Отчество - Строка 
// * Телефон - Строка, ТаблицаЗначений - Телефон в виде таблицы или строкой:
// ** Объект - ЛюбаяСсылка 
// ** Вид - СправочникСсылка.ВидыКонтактнойИнформации 
// ** Тип - ПеречислениеСсылка.ТипыКонтактнойИнформации 
// ** Значение - Строка 
// ** Представление - Строка 
// ** Дата - Дата
// ** ИдентификаторСтрокиТабличнойЧасти - Число
// ** ЗначенияПолей - Строка
Функция ПолучитьДанныеФизическогоЛица(СсылкаСправочника) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИНН", "");
	Результат.Вставить("СтраховойНомерПФР", "");
	Результат.Вставить("Фамилия", "");
	Результат.Вставить("Имя", "");
	Результат.Вставить("Отчество", "");
	
	ОбъектВыбора = СсылкаСправочника.ПолучитьОбъект();
	ЗаполнитьЗначенияСвойств(Результат, ОбъектВыбора);
	Результат.Вставить("Телефон", ЗначениеКонтактнойИнформации(СсылкаСправочника, "Справочник.ВидыКонтактнойИнформации.ТелефонРабочийФизлица"));

	Возврат Результат;
	
КонецФункции

// Возвращает время активности кода регистрации в секундах.
// 
// Возвращаемое значение:
//  Число - Время активности кода регистрации в секундах.
Функция ВремяАктивностиКодаРегистрацииВСекундах() Экспорт
	
	Возврат 1800;
	
КонецФункции

// Возвращает организацию по умолчанию.
// 
// Возвращаемое значение:
//  Неопределено, ОпределяемыйТип.Организация - Организация по умолчанию, если ее нет то Неопределено. 
Функция ОрганизацияПоУмолчанию() Экспорт
	
	Если Не Метаданные.ОпределяемыеТипы.Организация.Тип.СодержитТип(Тип("Строка")) Тогда
		ПолноеИмя = Метаданные.НайтиПоТипу(Метаданные.ОпределяемыеТипы.Организация.Тип.Типы()[0]).ПолноеИмя();
		ИмяСправочникаОрганизации = "Справочники." + СтрРазделить(ПолноеИмя, ".")[1];
		МодульОрганизации = ОбщегоНазначения.ОбщийМодуль(ИмяСправочникаОрганизации);
		ОрганизацияПоУмолчанию = МодульОрганизации.ОрганизацияПоУмолчанию();
	Иначе
		ОрганизацияПоУмолчанию = Неопределено;
	КонецЕсли;
	
	Возврат ОрганизацияПоУмолчанию;
	
КонецФункции

// Возвращается ссылка на инструкцию.
// 
// Возвращаемое значение:
//  Строка - Ссылка на инструкцию
Функция СсылкаНаИнструкцию() Экспорт
	
	Возврат "https://mobile.1c.ru/1c-epd/";
	
КонецФункции

Процедура ДобавитьКнопкуОтбораНовыхСообщений(Форма, ГруппаКнопокОтбор) Экспорт
	
	КомандаОтбор = Форма.Команды.Добавить("ОтборНовыеСообщения");
	КомандаОтбор.Действие = "Подключаемый_ОтборНовыеСообщения";
	КомандаОтбор.Заголовок = НСтр("ru = 'Новые сообщения';
									|en = 'New messages'");

	КнопкаОтборНовыеСообщения = Форма.Элементы.Добавить("КнопкаОтборНовыеСообщения", Тип("КнопкаФормы"), ГруппаКнопокОтбор);
	КнопкаОтборНовыеСообщения.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	КнопкаОтборНовыеСообщения.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КнопкаОтборНовыеСообщения.Картинка = СервисВзаимодействияМПЭПДКлиентСервер.КартинкаМобильноеУстройствоЭПД();
	КнопкаОтборНовыеСообщения.ИмяКоманды = "ОтборНовыеСообщения";

КонецПроцедуры

Процедура РегистрацияВЖурнале(Уровень, ПолноеПредставлениеОшибки, ИмяПодсобытияСистемы = "") Экспорт
	
	Если ЗначениеЗаполнено(ИмяПодсобытияСистемы) Тогда
		Шаблон = НСтр("ru = 'Сервис взаимодействия МПЭПД.%1';
						|en = 'Electronic shipment document MA collaboration service.%1'", ОбщегоНазначения.КодОсновногоЯзыка());
		ИмяСобытия = СтрШаблон(Шаблон, ИмяПодсобытияСистемы);
	Иначе
		ИмяСобытия =  НСтр("ru = 'Сервис взаимодействия МПЭПД';
							|en = 'Electronic shipment document MA collaboration service'", ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	ЗаписьЖурналаРегистрации(ИмяСобытия, Уровень, , , ПолноеПредставлениеОшибки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСJson

// Служебная функция, предназначенная для использования в объекте ЗаписатьJSON
// Служит функцией преобразования двоичных данных в base64 и содержит обязательные 
// для такого случая параметры.
// 
// Параметры: 
// 	Свойство 				- Строка - в параметр передается имя свойства, если выполняется запись структуры или соответствия
// 	Значение 				- Произвольный - ожидается тип ДвоичныеДанные
// 	ДополнительныеПараметры - Структура - дополнительные параметры, которые указаны в вызове метода ЗаписатьJSON:
//   * ЗаменятьДвоичныеДанные - Булево - заменяет двоичные данные
// 	Отказ - Булево	- отказ от записи свойства
// 
// Возвращаемое значение: 
//  Строка
//
Функция ПреобразоватьДвоичныеДанныеВBase64(Свойство, Значение, ДополнительныеПараметры, Отказ) Экспорт
	
	Если ТипЗнч(Значение) = Тип("ДвоичныеДанные") Тогда
		Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
			И ДополнительныеПараметры.Свойство("ЗаменятьДвоичныеДанные")
			И ДополнительныеПараметры.ЗаменятьДвоичныеДанные Тогда
			Возврат Значение.Размер();
		Иначе
			Возврат Base64Строка(Значение);
		КонецЕсли;
	КонецЕсли;
		
КонецФункции

// Служебная функция, предназначенная для использования в объекте ПрочитатьJSON
// Служит функцией восстановления строки base64 в ДвоичныеДанные и содержит обязательные 
// для такого случая параметры.
// 
// Параметры: 
//	Свойство 				- Строка - указывается только при чтении объектов JSON,
//	Значение 				- Произвольный - значение допустимого для сериализации типа
//	ДополнительныеПараметры - Произвольный - содержит дополнительные параметры 
// 
// Возвращаемое значение: 
//  ДвоичныеДанные
//
Функция ПреобразоватьСвойстваОбъекта(Свойство, Значение, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Строка") Тогда
		Возврат Base64Значение(Значение);
	ИначеЕсли ТипЗнч(Значение) = Тип("Массив") Тогда
		Для Индекс = 0 По Значение.ВГраница() Цикл
			Значение[Индекс] = Base64Значение(Значение[Индекс]);	
		КонецЦикла;
		Возврат Значение;
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

// Преобразует строку в формате JSON в структуру. 
//
// Параметры:
//  СтрокаJSON              - Строка - строка в формате JSON.
//  КакСоответствие - Булево
//  ПараметрыПреобразования - Структура - дополнительные параметры для настройки преобразования:
//    * ИменаСвойствДляВосстановления - Массив из Строка, ФиксированныйМассив из Строка - список свойств, 
//                                      которые необходимо преобразовать из Base64 в двоичные данные.
// Возвращаемое значение:
//	Соответствие из Строка, Структура - Структура или соответствие с полями.
//
//@skip-check doc-comment-type
//@skip-check doc-comment-collection-item-type
//@skip-check doc-comment-description-ends-on-dot
//@skip-check function-return-types
Функция СтруктураИзJson(СтрокаJSON, КакСоответствие = Ложь, ПараметрыПреобразования = Неопределено) Экспорт
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	
	ИменаСвойствДаты = СвойствоСтруктуры(ПараметрыПреобразования, "ИменаСвойствДаты", Неопределено);
	Если ИменаСвойствДаты <> Неопределено Тогда
		ИменаСвойствДаты = СтрРазделить(ИменаСвойствДаты, ",");
	КонецЕсли;
	ИменаСвойствВосстановления = СвойствоСтруктуры(ПараметрыПреобразования, "ИменаСвойствВосстановления", Неопределено);
	Если ИменаСвойствВосстановления <> Неопределено Тогда
		ИменаСвойствВосстановления = СтрРазделить(ИменаСвойствВосстановления, ",");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИменаСвойствВосстановления) Тогда
		Объект = ПрочитатьJSON(
			ЧтениеJSON, КакСоответствие,
			ИменаСвойствДаты,
			ФорматДатыJSON.ISO, 
			"ПреобразоватьСвойстваОбъекта", 
			СервисВзаимодействияМПЭПД, 
			ПараметрыПреобразования,
			ИменаСвойствВосстановления);
	Иначе
		Объект = ПрочитатьJSON(
			ЧтениеJSON, КакСоответствие,
			ИменаСвойствДаты,
			ФорматДатыJSON.ISO);
	КонецЕсли;
	
	ЧтениеJSON.Закрыть();
	
	Возврат Объект;
	
КонецФункции

// Преобразует структура в строку JSON. Двоичные данные преобразуются в строки Base64.
//
// Параметры:
//  Объект - Структура - Структура, которую необходимо преобразовать в строку JSON.
//  ПараметрыПреобразования - Структура - Дополнительные параметры, которые будут переданы в функцию восстановления значений.
//  ФорматДатыISO - Булево - Если установлено значение Истина, будут созданы параметры сериализации
//
// Возвращаемое значение:
//	Строка - в формате JSON.
//
Функция СтруктураВJSON(Объект, ПараметрыПреобразования = Неопределено, ФорматДатыISO = Ложь) Экспорт
	
	Если ФорматДатыISO Тогда
		НастройкиСериализации = Новый НастройкиСериализацииJSON;
		НастройкиСериализации.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
	Иначе
		НастройкиСериализации = Неопределено;
	КонецЕсли;
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(
		ЗаписьJSON, 
		Объект,
		НастройкиСериализации, 
		"ПреобразоватьДвоичныеДанныеВBase64", 
		СервисВзаимодействияМПЭПД, 
		ПараметрыПреобразования);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

#КонецОбласти

#Область ТекстыЗапросов

//@skip-check string-literal-types
Функция СформироватьТекстУсловияЗапроса(Структура, Поле, Запрос)
	
	Значение = Неопределено;
	Если Не Структура.Свойство(Поле, Значение) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат "";
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("СписокЗначений")
		Или ТипЗнч(Значение) = Тип("Массив") Тогда
		ШаблонДобавленияУсловия = "%1 В (&%1)";
	Иначе
		ШаблонДобавленияУсловия = "%1 = &%1";
	КонецЕсли;
	
	Запрос.УстановитьПараметр(Поле, Значение);
	ТекстУсловия = СтрШаблон(ШаблонДобавленияУсловия, Поле); 
	
	Возврат ТекстУсловия;
	
КонецФункции
	
#КонецОбласти

#Область ВизуальноеОформлениеМПНаФормахДокументов

//@skip-check undefined-variable
//@skip-check bsl-legacy-check-dynamic-feature-access
//@skip-check doc-comment-description-ends-on-dot
//@skip-check new-font
Процедура ДобавитьЭлементыМобильногоПриложения(Форма, ГруппаПанельМПОбщая, ИндексЭлемента)
	
	Элементы = Форма.Элементы;
	НаименованиеРаботПоИндексу = СервисВзаимодействияМПЭПДКлиентСервер.НаименованиеРаботПоИндексу(ИндексЭлемента);
	
	// Общая группа вертикальная
	ГруппаМП = Элементы.Добавить(ИмяСИндексом(ИндексЭлемента, "ГруппаМобильноеПриложение"),
		Тип("ГруппаФормы"), ГруппаПанельМПОбщая);
	ГруппаМП.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМП.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМП.ОтображатьЗаголовок = Ложь;
	ГруппаМП.РастягиватьПоГоризонтали = Ложь;
	ГруппаМП.РастягиватьПоВертикали = Ложь;
	ГруппаМП.Ширина = ШиринаГруппЭлементовФормыМП();
	ГруппаМП.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	// Общая группа горизонтальная
	ГруппаМПОбщаяГоризонтальная = Элементы.Добавить(ИмяСИндексом(ИндексЭлемента, "ГруппаМПОбщаяДляОдногоМПГоризонтальная"),
		Тип("ГруппаФормы"), ГруппаМП);
	ГруппаМПОбщаяГоризонтальная.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМПОбщаяГоризонтальная.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМПОбщаяГоризонтальная.ОтображатьЗаголовок = Ложь;
	ГруппаМПОбщаяГоризонтальная.РастягиватьПоГоризонтали = Ложь;
	ГруппаМПОбщаяГоризонтальная.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаМПОбщаяГоризонтальная.ГоризонтальноеПоложениеПодчиненных = ГоризонтальноеПоложениеЭлемента.Лево;
	
	// Фото
	МПФото = Элементы.Добавить(ИмяСИндексом(ИндексЭлемента, "МПФото"), Тип("ПолеФормы"), ГруппаМПОбщаяГоризонтальная);
	МПФото.Вид = ВидПоляФормы.ПолеКартинки;
	МПФото.РазмерКартинки = РазмерКартинки.Пропорционально;
	МПФото.ПутьКДанным = ИмяСИндексом(ИндексЭлемента, "МПФото");
	МПФото.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	МПФото.ТекстНевыбраннойКартинки = НСтр("ru = 'Фото
		|отсутствует';
		|en = 'The photo
		|is missing'");
	МПФото.ЦветТекста = ЦветаСтиля.ЦветТекстаНеВыполненныйШагЭПД;
	МПФото.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	МПФото.Шрифт = ШрифтыСтиля.ШрифтУменьшенныйЭПД;
	МПФото.Ширина = 6;
	МПФото.Высота = 3;
	МПФото.РастягиватьПоГоризонтали = Ложь;
	МПФото.РастягиватьПоВертикали = Ложь;
	
	// Заголовок МП, элементы управления
	ГруппаМППанельПравая = Элементы.Добавить(ИмяСИндексом(ИндексЭлемента, "ГруппаМППанельПравая"),
		Тип("ГруппаФормы"),  ГруппаМПОбщаяГоризонтальная);
	ГруппаМППанельПравая.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМППанельПравая.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМППанельПравая.ОтображатьЗаголовок = Ложь;
	ГруппаМППанельПравая.РастягиватьПоГоризонтали = Ложь;
	ГруппаМППанельПравая.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	
	ГруппаМППанельЗаголовка = Элементы.Добавить(ИмяСИндексом(ИндексЭлемента, "ГруппаМППанельЗаголовка"),
		Тип("ГруппаФормы"),  ГруппаМППанельПравая);
	ГруппаМППанельЗаголовка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМППанельЗаголовка.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМППанельЗаголовка.ОтображатьЗаголовок = Ложь;
	ГруппаМППанельЗаголовка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаМППанельЗаголовка.РастягиватьПоГоризонтали = Ложь;
	ГруппаМППанельЗаголовка.Ширина = ШиринаПанелиЗаголовкаМП();
	ГруппаМППанельЗаголовка.ГоризонтальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
	
	МПОтступЗаголовка = Элементы.Добавить(ИмяСИндексом(ИндексЭлемента, "МПОтступЗаголовка"),
		Тип("ДекорацияФормы"), ГруппаМППанельЗаголовка);
	МПОтступЗаголовка.Вид = ВидДекорацииФормы.Надпись;
	МПОтступЗаголовка.Заголовок = " ";
	МПОтступЗаголовка.РастягиватьПоГоризонтали = Ложь;
	МПОтступЗаголовка.Шрифт = Новый Шрифт(, 6);
	
	МПЗаголовок = Элементы.Добавить(ИмяСИндексом(ИндексЭлемента, "МПЗаголовок"),
		Тип("ДекорацияФормы"), ГруппаМППанельЗаголовка);
	МПЗаголовок.Вид = ВидДекорацииФормы.Надпись;
	МПЗаголовок.Заголовок = НаименованиеРаботПоИндексу;
	МПЗаголовок.РастягиватьПоГоризонтали = Ложь;
	МПЗаголовок.Ширина = ШиринаВидаРаботМП();
	
	КомандаВыбратьМП = Элементы.Добавить(ИмяСИндексом(ИндексЭлемента, "КомандаВыбратьМП"),
		Тип("ДекорацияФормы"), ГруппаМПОбщаяГоризонтальная);
	КомандаВыбратьМП.Вид = ВидДекорацииФормы.Картинка;
	КомандаВыбратьМП.Картинка = СервисВзаимодействияМПЭПДКлиентСервер.КартинкаИзменитьМПЭПД();
	КомандаВыбратьМП.РазмерКартинки = РазмерКартинки.ПоРазмеруШрифта;
	КомандаВыбратьМП.Шрифт = Новый Шрифт(, 16);
	КомандаВыбратьМП.Ширина = 3;
	КомандаВыбратьМП.Гиперссылка = Истина;
	КомандаВыбратьМП.УстановитьДействие("Нажатие", "Подключаемый_КомандаВыбратьМПНажатие");
	
	ВставкаВместоКомандыВыбратьМП = Элементы.Добавить(ИмяСИндексом(ИндексЭлемента, "ВставкаВместоКомандыВыбратьМП"),
		Тип("ДекорацияФормы"), ГруппаМПОбщаяГоризонтальная);
	ВставкаВместоКомандыВыбратьМП.Вид = ВидДекорацииФормы.Надпись;
	ВставкаВместоКомандыВыбратьМП.Шрифт = Новый Шрифт(, 16);
	ВставкаВместоКомандыВыбратьМП.Ширина = 3;
	ВставкаВместоКомандыВыбратьМП.Видимость = Ложь;
	
	// Наименование МП
	ГруппаМПНаименование = Элементы.Добавить(ИмяСИндексом(ИндексЭлемента, "ГруппаМПНаименование"),
		Тип("ГруппаФормы"),  ГруппаМППанельПравая);
	ГруппаМПНаименование.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаМПНаименование.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаМПНаименование.ОтображатьЗаголовок = Ложь;
	ГруппаМПНаименование.РастягиватьПоГоризонтали = Ложь;
	ГруппаМПНаименование.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	КомандаОткрытьМП = Форма.Команды.Добавить(ИмяСИндексом(ИндексЭлемента, "Подключаемый_ОткрытьМП"));
	КомандаОткрытьМП.Действие = "Подключаемый_ОткрытьМП";
	КомандаОткрытьМП.Заголовок = НСтр("ru = 'Открыть мобильное устройство';
										|en = 'Open the mobile device'");
	
	КнопкаОткрытьМП = Форма.Элементы.Добавить(ИмяСИндексом(ИндексЭлемента, "Подключаемый_ОткрытьМП"),
		Тип("КнопкаФормы"), ГруппаМПНаименование);
	КнопкаОткрытьМП.ИмяКоманды = ИмяСИндексом(ИндексЭлемента, "Подключаемый_ОткрытьМП");
	КнопкаОткрытьМП.Отображение = ОтображениеКнопки.Текст;
	КнопкаОткрытьМП.ОтображениеФигуры = ОтображениеФигурыКнопки.Нет;
	КнопкаОткрытьМП.РастягиватьПоГоризонтали = Ложь;
	КнопкаОткрытьМП.ЦветТекста = ЦветаСтиля.ЦветТекстаНаименованияВыбранногоМПЭПД;
	
	ДобавитьДекорациюРазделитель(ИндексЭлемента, Элементы, ГруппаМП);
	
КонецПроцедуры

Функция ШиринаГруппЭлементовФормыМП()
	
	Возврат 44;
	
КонецФункции

Функция ШиринаПанелиЗаголовкаМП()
	
	Возврат 33;
	
КонецФункции

Функция ШиринаВидаРаботМП()
	
	Возврат 30;
	
КонецФункции

Функция ШиринаРазделителяМП()
	
	Возврат 44;
	
КонецФункции

Функция ИмяСИндексом(Индекс, ИмяЭлемента)
	
	Возврат СервисВзаимодействияМПЭПДКлиентСервер.ИмяСИндексом(Индекс, ИмяЭлемента);
	
КонецФункции

Процедура ДобавитьКнопкуОткрытияНастроекМПНаФормах(Форма)
	
	Команда = Форма.Команды.Добавить("ПодключаемыйМП_ОткрытьФормуНастроек");
	Команда.Действие = "Подключаемый_ОткрытьФормуНастроекМПДляФорм";
	Команда.Заголовок = НСтр("ru = 'Мобильные устройства';
							|en = 'Mobile devices'");
	
	Кнопка = Форма.Элементы.Добавить("ПодключаемыйМП_ОткрытьФормуНастроек", Тип("КнопкаФормы"), Форма.КоманднаяПанель);
	Кнопка.ИмяКоманды = "ПодключаемыйМП_ОткрытьФормуНастроек";
	Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	Кнопка.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанелиИВДополнительномПодменю;
	Кнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Кнопка.Картинка = СервисВзаимодействияМПЭПДКлиентСервер.КартинкаМобильноеУстройствоЭПД();
	
КонецПроцедуры

//@skip-check new-font
//@skip-check undefined-variable
//@skip-check doc-comment-description-ends-on-dot
Процедура ДобавитьДекорациюРазделитель(ИндексДекорацииРазделителя, Элементы, ГруппаДобавления)
	
	Если ОбщегоНазначения.ЭтоВебКлиент() Тогда
		Возврат;
	КонецЕсли;
		
	ДекорацияРазделитель = Элементы.Добавить(ИмяСИндексом(ИндексДекорацииРазделителя, "ДекорацияРазделительМП"),
		Тип("ДекорацияФормы"), ГруппаДобавления);
	ДекорацияРазделитель.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияРазделитель.Заголовок = Новый ФорматированнаяСтрока(" ", Новый Шрифт( , , , , , , 3), , ,);
	ДекорацияРазделитель.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Подчеркивание, 1);
	ДекорацияРазделитель.Ширина = ШиринаРазделителяМП();
	
КонецПроцедуры

#КонецОбласти

// Возвращает текстовое представление работы используемое на сервере МП.
// Работа определяется с учетом титула и документа.
// Пример 1 - медработник проводящий медосмотр перед рейсом.
// Пример 2 - ответственный за показания одометра вносящий показания после рейса.
//
// Параметры:
//  ТипВзаимодействияМПЭПД	 - ПеречислениеСсылка.ТипыВзаимодействияМПЭПД - 
//  Документ				 - ДокументСсылка - 
// 
// Возвращаемое значение:
//  Строка - текстовое представление роли пользователя.
//
Функция РаботаВыполняемаяПользователем(ТипВзаимодействияМПЭПД, Документ)
	
	РаботаВыполняемаяПользователем = "";
	
	Если ТипВзаимодействияМПЭПД = Перечисления.ТипыВзаимодействияМПЭПД.ПривязкаВодителя Тогда
		Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектронныйПутевойЛист") Тогда
			РаботаВыполняемаяПользователем = "EPL_Driver";
		ИначеЕсли ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
			РаботаВыполняемаяПользователем = "ETrN_Driver";
		КонецЕсли;
	
	ИначеЕсли ТипВзаимодействияМПЭПД = Перечисления.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаДоРейса Тогда
		РаботаВыполняемаяПользователем = "EPL_MedicBeforeShift";
	
	ИначеЕсли ТипВзаимодействияМПЭПД = Перечисления.ТипыВзаимодействияМПЭПД.ПривязкаМедработникаПослеРейса Тогда
		РаботаВыполняемаяПользователем = "EPL_MedicAfterShift";
	
	ИначеЕсли ТипВзаимодействияМПЭПД = Перечисления.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрДоРейса Тогда
		РаботаВыполняемаяПользователем = "EPL_OdometrBeforeShift";
	
	ИначеЕсли ТипВзаимодействияМПЭПД = Перечисления.ТипыВзаимодействияМПЭПД.ПривязкаОтветственногоЗаОдометрПослеРейса Тогда
		РаботаВыполняемаяПользователем = "EPL_OdometrAfterShift";
	
	ИначеЕсли ТипВзаимодействияМПЭПД = Перечисления.ТипыВзаимодействияМПЭПД.ПривязкаМеханика Тогда
		РаботаВыполняемаяПользователем = "EPL_Mechanic";
		
	КонецЕсли;
	
	Возврат РаботаВыполняемаяПользователем;
	
КонецФункции

Функция ОжидаемПросмотрМП(ЗаписьРеестра, ДанныеСообщения, РеквизитыДокумента)
	
	Результат = Ложь;
	
	ТекущийТип = Неопределено;
	ДанныеСообщения.Свойство("Type", ТекущийТип);
	
	ФайлТекущегоШагаМП = Неопределено;
	ДанныеСообщения.Свойство("fileid", ФайлТекущегоШагаМП);
	Если Не ЗначениеЗаполнено(ФайлТекущегоШагаМП) Тогда
		Возврат Результат;
	КонецЕсли;
	
	СоответствиеШагов = ОбменСГИСЭПДКлиентСервер.СоответствиеТитуловИШагов();
	СоответствиеТитулов = ПеревернутьСоответствие(СоответствиеШагов);
	
	ТекущийШаг = ЗаписьРеестра.ТекущийШаг;
	ТекущийШагВыполнен = ЗаписьРеестра.ТекущийШагВыполнен;	
	
	Если ТекущийШагВыполнен = Истина Тогда
		Если СоответствиеТитулов.Получить(ТекущийШаг) = Перечисления.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул7 Тогда
			РеквизитИмениФайлаТитула = "ТитулПереадресовкаИдентификаторФайла";
			ИмяТипаПросмотрМП = "readressing_viewed";
		ИначеЕсли СоответствиеТитулов.Получить(ТекущийШаг) = Перечисления.ТипыЭлементовРегламентаЭДО.ЭТрН_ДопТитул7 Тогда
			РеквизитИмениФайлаТитула = "ДопТитулПереадресовкаИдентификаторФайла";
			ИмяТипаПросмотрМП = "readressing_viewed";
		ИначеЕсли СоответствиеТитулов.Получить(ТекущийШаг) = Перечисления.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул8 Тогда
			РеквизитИмениФайлаТитула = "ТитулПеревозчикаЗаменыИдентификаторФайла";
			ИмяТипаПросмотрМП = "replacement_viewed";
		ИначеЕсли СоответствиеТитулов.Получить(ТекущийШаг) = Перечисления.ТипыЭлементовРегламентаЭДО.ЭТрН_ДопТитул8 Тогда
			РеквизитИмениФайлаТитула = "ДопТитулПеревозчикаЗаменыИдентификаторФайла";
			ИмяТипаПросмотрМП = "replacement_viewed";	
		Иначе
			Возврат Результат;
		КонецЕсли;
		
		Результат = Истина;
		Если ТекущийТип = ИмяТипаПросмотрМП Тогда
			ИмяФайлаТитула = РеквизитыДокумента[РеквизитИмениФайлаТитула];
			Если ИмяФайлаТитула = ФайлТекущегоШагаМП Тогда
				Результат = Ложь;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ШагиТребующиеПросмотрМП()
	
	СоответствиеТитуловИШагов = ОбменСГИСЭПДКлиентСервер.СоответствиеТитуловИШагов();
	
	Результат = Новый Массив;
	Результат.Добавить(СоответствиеТитуловИШагов.Получить(Перечисления.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул7));
	Результат.Добавить(СоответствиеТитуловИШагов.Получить(Перечисления.ТипыЭлементовРегламентаЭДО.ЭТрН_ДопТитул7));
	Результат.Добавить(СоответствиеТитуловИШагов.Получить(Перечисления.ТипыЭлементовРегламентаЭДО.ЭТрН_Титул8));
	Результат.Добавить(СоответствиеТитуловИШагов.Получить(Перечисления.ТипыЭлементовРегламентаЭДО.ЭТрН_ДопТитул8));
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрыВыполненияОперацииСервиса(ИдентификаторЭДО = "")
	
	Параметры = Новый Структура;
	Параметры.Вставить("АдресРесурса", "");
	Параметры.Вставить("ВерсияИнтерфейса", 1);
	Параметры.Вставить("ПредставлениеОперации", "");
	Параметры.Вставить("Метод", "");
	Параметры.Вставить("Токен", Неопределено);
	Параметры.Вставить("ТокенОбновления", Неопределено);
	Параметры.Вставить("ПараметрыЗапроса", Новый Структура);
	Параметры.Вставить("Заголовки", Новый Соответствие);
	Параметры.Вставить("ТелоЗапроса", Неопределено);
	Параметры.Вставить("Таймаут", 60);
	Параметры.Вставить("ИдентификаторЭДО", ИдентификаторЭДО);
	
	Возврат Параметры;
	
КонецФункции

Функция ЗначениеКонтактнойИнформации(СсылкаВладельца, ВидИнформации)
	
	Результат = "";
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
		Попытка
			СсылкаВида = ОбщегоНазначения.ПредопределенныйЭлемент(ВидИнформации);
			МодульУправлениеКонтактнойИнформацией = ОбщегоНазначения.ОбщийМодуль("УправлениеКонтактнойИнформацией");
			Результат = МодульУправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(СсылкаВладельца, СсылкаВида);
		Исключение
			Результат = "";
		КонецПопытки;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СоответствиеВидаПодписи(ТекущееЗначение)
	
	ВсеПредставления = Новый Соответствие;
	ВсеПредставления.Вставить(Перечисления.ВидыПодписиМПЭПД.НеИспользуется, "Empty");
	ВсеПредставления.Вставить(Перечисления.ВидыПодписиМПЭПД.УНЭПГосключ, "UES");
	ВсеПредставления.Вставить(Перечисления.ВидыПодписиМПЭПД.УКЭПГосключ, "QES");
	ВсеПредставления.Вставить(Перечисления.ВидыПодписиМПЭПД.ПростаяПодпись, "Simple");
	ВсеПредставления.Вставить(Перечисления.ВидыПодписиМПЭПД.СервисDSS, "DSS");
	ВсеПредставления.Вставить(Перечисления.ВидыПодписиМПЭПД.УКЭПРутокен, "RutokenQES");
	
	Возврат ПеревернутьСоответствие(ВсеПредставления)[ТекущееЗначение];
	
КонецФункции

Функция ОпределитьВидПодписи(ВидПодписи)
	
	Возврат СоответствиеВидаПодписи(ВидПодписи);
	
КонецФункции

Функция СоответствиеСостояниеМП(ТекущееЗначение)
	
	ВсеПредставления = Новый Соответствие;
	ВсеПредставления.Вставить(Перечисления.СостояниеМПЭПД.Приостановлен, "Недоступен");
	ВсеПредставления.Вставить(Перечисления.СостояниеМПЭПД.Подключен, "Свободен");
	ВсеПредставления.Вставить(Перечисления.СостояниеМПЭПД.ОжидаетсяПодключение, "ОжидаетПеререгистрации");
	
	Возврат ПеревернутьСоответствие(ВсеПредставления)[ТекущееЗначение];
	
КонецФункции

Функция СоответствиеСостояниеЭПД(ТекущееЗначение)
	
	ВсеПредставления = Новый Соответствие;
	ВсеПредставления.Вставить("Title1", "Титул 1");
	ВсеПредставления.Вставить("Title2", "Титул 2");
	ВсеПредставления.Вставить("Title3", "Титул 3");
	ВсеПредставления.Вставить("Title4", "Титул 4");
	ВсеПредставления.Вставить("Title5", "Титул 5");
	ВсеПредставления.Вставить("Title6", "Титул 6");
	ВсеПредставления.Вставить("Title7", "Титул 7");
	ВсеПредставления.Вставить("Title8", "Титул 8");
	ВсеПредставления.Вставить("Title2Work", "В работе титул 2");
	ВсеПредставления.Вставить("Title4Work", "В работе титул 4");
	ВсеПредставления.Вставить("Close", "Закрыт");
	
	Возврат ПеревернутьСоответствие(ВсеПредставления)[ТекущееЗначение];
	
КонецФункции

Функция СоответствиеУведомлений(ТекущееЗначение)
	
	ВсеПредставления = Новый Соответствие;
	ВсеПредставления.Вставить(Перечисления.ТипыВзаимодействияМПЭПД.Сообщение, "Comment");
	ВсеПредставления.Вставить(Перечисления.ТипыВзаимодействияМПЭПД.УдалениеМП, "DeleteMA");
	ВсеПредставления.Вставить(Перечисления.ТипыВзаимодействияМПЭПД.ИзменениеСведенийМП, "ChangeInfoМА");
	ВсеПредставления.Вставить(Перечисления.ТипыВзаимодействияМПЭПД.ПовторнаяРегистрацияМП, "RepeatRegistration");
	ВсеПредставления.Вставить(Перечисления.ТипыВзаимодействияМПЭПД.ОтвязкаВодителя, "CancelDoc");
	ВсеПредставления.Вставить(Перечисления.ТипыВзаимодействияМПЭПД.ПривязкаВодителя, "DesignatedDoc");
	ВсеПредставления.Вставить(Перечисления.ТипыВзаимодействияМПЭПД.НастройкиДополнительныхПолей, "AdditionalParametersSetup");
	
	Возврат ПеревернутьСоответствие(ВсеПредставления)[ТекущееЗначение];
	
КонецФункции

Функция ПеревернутьСоответствие(ТекущееСоответствие)
	
	Возврат СервисВзаимодействияМПЭПДКлиентСервер.ПеревернутьСоответствие(ТекущееСоответствие);
	
КонецФункции

Функция ПолучитьТокенДоступа(ИдентификаторЭДО)
	
	Результат = Новый Структура;
	
	ЗаписьРегистра = РегистрыСведений.НастройкиВзаимодействияМПЭПД.ПараметрыЗаписиРегистра(ИдентификаторЭДО);
	ЗаписьРегистра = РегистрыСведений.НастройкиВзаимодействияМПЭПД.НайтиЗапись(ЗаписьРегистра);

	Результат.Вставить("Токен", ЗаписьРегистра.ТокенДоступа);
	Результат.Вставить("ТокенОбновления", ЗаписьРегистра.ТокенОбновления);
	
	Возврат Результат;
	
КонецФункции

Функция СвойствоСтруктуры(ТекущееХранилище, ИмяСвойства, ЗначениеПоУмолчанию = "")
	
	Результат = ЗначениеПоУмолчанию;
	
	Если ТипЗнч(ТекущееХранилище) = Тип("Структура") ИЛИ ТипЗнч(ТекущееХранилище) = Тип("ФиксированнаяСтруктура") Тогда
		Результат = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ТекущееХранилище, ИмяСвойства, ЗначениеПоУмолчанию);
	ИначеЕсли ТекущееХранилище <> Неопределено Тогда
		Результат = ТекущееХранилище[ИмяСвойства];
		Если Результат = Неопределено Тогда
			Результат = ЗначениеПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСтруктуруДанныхТикетаПодпискиИТС()
	
	Результат = Новый Структура;
	Результат.Вставить("Login", "");
	Результат.Вставить("Password", "");
	Результат.Вставить("Ticket", "");
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		ВладелецТикета = "MAEPD";
		МодульИнтернетПоддержкаПользователей = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
		
		УстановитьПривилегированныйРежим(Истина);
		
		Если Не ОбщегоНазначения.РазделениеВключено() Тогда
			РезультатЗапроса = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
			
			Если РезультатЗапроса <> Неопределено Тогда
				Результат.Login = РезультатЗапроса.Логин;
				Результат.Password = РезультатЗапроса.Пароль;
			КонецЕсли;
		КонецЕсли;
		
		РезультатЗапроса = МодульИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки(ВладелецТикета);
		Если ЗначениеЗаполнено(РезультатЗапроса.Тикет) Тогда
			Результат.Ticket = РезультатЗапроса.Тикет;
		Иначе
			ИмяПодсобытияСистемы = НСтр("ru = 'Получение тикета ИТС';
										|en = 'Receive the ITS ticket'", ОбщегоНазначения.КодОсновногоЯзыка());
			РегистрацияВЖурнале(УровеньЖурналаРегистрации.Ошибка, РезультатЗапроса.ИнформацияОбОшибке, ИмяПодсобытияСистемы);
		КонецЕсли; 
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВерсияБиблиотеки()
	
	Возврат "LED_" + ОбновлениеИнформационнойБазыБЭД.ВерсияБиблиотеки();
	
КонецФункции

// Получение объекта соединение.
//
// Параметры:
//  ПараметрыОперации - См. ПараметрыВыполненияОперацииСервиса
//
// Возвращаемое значение:
//  См. ИнтернетСоединениеБЭД.ОписаниеHTTPСоединения
//
Функция СоединениеССервисом(ПараметрыОперации)
	
	Возврат ИнтернетСоединениеБЭД.ОписаниеHTTPСоединения(
				АдресСервисаВзаимодействия(),
				ПараметрыОперации.Таймаут);

КонецФункции

// Возвращает подготовленный HTTP запрос к сервису.
// 
// Параметры:
//  ПараметрыОперации - см. ПараметрыВыполненияОперацииСервиса
// 
// Возвращаемое значение:
//  HTTPЗапрос
Функция ЗапросКСервису(ПараметрыОперации)
	
	HTTPЗапрос = Новый HTTPЗапрос;

	АдресРесурса = АдресВерсииИнтерфейса(ПараметрыОперации.ВерсияИнтерфейса) + "/" + ПараметрыОперации.АдресРесурса;
	
	СимволРазделителя = "?";
	Для Каждого Параметр Из ПараметрыОперации.ПараметрыЗапроса Цикл
		ЗначениеПараметра = КодироватьСтроку(Параметр.Значение, СпособКодированияСтроки.КодировкаURL);
		АдресРесурса = АдресРесурса + СимволРазделителя + Параметр.Ключ + "=" + ЗначениеПараметра;
		СимволРазделителя = "&";
	КонецЦикла;
	
	Если ТипЗнч(ПараметрыОперации.ТелоЗапроса) = Тип("ДвоичныеДанные") Тогда
		HTTPЗапрос.УстановитьТелоИзДвоичныхДанных(ПараметрыОперации.ТелоЗапроса);
	ИначеЕсли ТипЗнч(ПараметрыОперации.ТелоЗапроса) = Тип("Строка") Тогда
		HTTPЗапрос.УстановитьТелоИзСтроки(ПараметрыОперации.ТелоЗапроса);
	КонецЕсли;

	Заголовки = Новый Соответствие;
	Токен = ПараметрыОперации.Токен;
	
	Если ЗначениеЗаполнено(Токен) Тогда
		ТокенДоступа = "";
		Если ТипЗнч(Токен) = Тип("ДвоичныеДанные") Тогда
			ТокенДоступа = ПолучитьСтрокуИзДвоичныхДанных(Токен);
		ИначеЕсли ТипЗнч(Токен) = Тип("Строка") Тогда 
			ТокенДоступа = Токен;
		КонецЕсли;
		Заголовки.Вставить("X-Auth-Token", ТокенДоступа);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(Заголовки, ПараметрыОперации.Заголовки);
	
	HTTPЗапрос.АдресРесурса = АдресРесурса;
	HTTPЗапрос.Заголовки = Заголовки;

	Возврат HTTPЗапрос;
	
КонецФункции

// Вызывает HTTP метод и возвращает его результат в виде структуры, при ошибках формирует запись в журнале регистрации.
// 
// Параметры:
//  ОписаниеСоединения - См. СоединениеССервисом
//  HTTPЗапрос - HTTPЗапрос
//  Метод - Строка
//  ВидОперации - Строка
// 
// Возвращаемое значение:
//  Структура
// * Успех - Булево
// * Ответ - HTTPОтвет, Неопределено - Неопределено в случае ошибки
Функция ВызватьHTTPМетод(ОписаниеСоединения, HTTPЗапрос, Метод, ВидОперации)
	
	РезультатВызова = Новый Структура;
	РезультатВызова.Вставить("Успех", Ложь);
	РезультатВызова.Вставить("Ответ", Неопределено);
	
	Попытка
		Ответ = ОписаниеСоединения.HTTPСоединение.ВызватьHTTPМетод(Метод, HTTPЗапрос);
		РезультатВызова.Ответ = Ответ;
		РезультатВызова.Успех = Истина;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		//@skip-check Undefined variable
		РегистрацияВЖурнале(УровеньЖурналаРегистрации.Ошибка, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
	КонецПопытки;
	
	Возврат РезультатВызова;
	
КонецФункции

Процедура ОбновитьТокенДоступа(ОтветСервиса, ПараметрыОперации, ОписаниеСоединения, ТекущийШаг)
	
	Если ОтветСервиса.КодСостояния <> 531 
		ИЛИ ТекущийШаг > 1 
		ИЛИ ПустаяСтрока(ПараметрыОперации.ТокенОбновления) Тогда // закончился срок действия токена
		ТекущийШаг = 0;
		Возврат;
	КонецЕсли;
	
	ПараметрыОбновления = ПараметрыВыполненияОперацииСервиса();
	ПараметрыОбновления.ПараметрыЗапроса.Вставить("refresh_token", ПараметрыОперации.ТокенОбновления);
	ПараметрыОбновления.ПараметрыЗапроса.Вставить("version", ВерсияБиблиотеки());
	ПараметрыОбновления.АдресРесурса = "common/token";
	ПараметрыОбновления.Метод = "GET";
	ПараметрыОбновления.ПредставлениеОперации = "Обновление токена доступа";
	
	HTTPЗапросОбновления = ЗапросКСервису(ПараметрыОбновления);
	РезультатЗапроса = ВызватьHTTPМетод(ОписаниеСоединения, HTTPЗапросОбновления, ПараметрыОбновления.Метод, ПараметрыОбновления.ПредставлениеОперации);
	
	Если РезультатЗапроса.Успех и РезультатЗапроса.Ответ.КодСостояния = 200 Тогда
		ДанныеОтвета = СтруктураИзJson(ОтветСервиса.ПолучитьТелоКакСтроку(), Истина);
		ПараметрыОперации.Токен = СвойствоСтруктуры(ДанныеОтвета, "token");
		ПараметрыОперации.ТокенОбновления = СвойствоСтруктуры(ДанныеОтвета, "refresh_token");
		ИдентификаторЭДО = СвойствоСтруктуры(ПараметрыОперации, "ИдентификаторЭДО");

		Если НЕ ПустаяСтрока(ИдентификаторЭДО) Тогда
			ЗаписьРегистра = РегистрыСведений.НастройкиВзаимодействияМПЭПД.ПараметрыЗаписиРегистра(ИдентификаторЭДО);
			ЗаписьРегистра.ТокенДоступа = ПараметрыОперации.Токен;
			ЗаписьРегистра.ТокенОбновления = ПараметрыОперации.ТокенОбновления;
			ЗаписьРегистра.СрокДействияТокена = СвойствоСтруктуры(ДанныеОтвета, "expiration");
			РегистрыСведений.НастройкиВзаимодействияМПЭПД.РегистрироватьЗапись(ЗаписьРегистра);
		КонецЕсли;
		
	КонецЕсли;

	ТекущийШаг = ТекущийШаг + 1;
	
КонецПроцедуры

// Описание
// 
// Параметры:
// 	ПараметрыОперации - см. ПараметрыВыполненияОперацииСервиса
// Возвращаемое значение:
// 	Структура:
//		* Успех - Булево
//		* Ответ - HTTPОтвет, Неопределено - Неопределено при ошибках
//		* Ошибка - Строка - Описание ошибки
Функция ВыполнитьОперацию(ПараметрыОперации)
	
	РезультатОперации = Новый Структура("Успех, Ответ, Ошибка", Ложь, Неопределено, "");
	ОписаниеСоединения = СоединениеССервисом(ПараметрыОперации);
	
	HTTPЗапрос = ЗапросКСервису(ПараметрыОперации);
	
	ТекущийШаг = 1;
	
	Пока ТекущийШаг Цикл
		РезультатЗапроса = ВызватьHTTPМетод(ОписаниеСоединения, HTTPЗапрос, ПараметрыОперации.Метод, ПараметрыОперации.ПредставлениеОперации);
		
		Если Не РезультатЗапроса.Успех Тогда
			Возврат РезультатОперации;
		КонецЕсли;
		
		ОбновитьТокенДоступа(РезультатЗапроса.Ответ, ПараметрыОперации, ОписаниеСоединения, ТекущийШаг);
		
	КонецЦикла;
	
	РезультатОперации.Успех = РезультатЗапроса.Ответ.КодСостояния = 200;
	РезультатОперации.Ответ = РезультатЗапроса.Ответ;
	Если Не РезультатОперации.Успех Тогда
		ОшибкаОператора = ПрочитатьОписаниеОшибкиОператора(РезультатЗапроса.Ответ);
		РезультатОперации.Ошибка = ОшибкаОператора.Описание;
		РегистрацияВЖурнале(УровеньЖурналаРегистрации.Ошибка, ОшибкаОператора.Журнал);
	КонецЕсли;

	Возврат РезультатОперации;

КонецФункции

Функция ПрочитатьОписаниеОшибкиОператора(ОтветСервиса)
	
	Результат = Новый Структура();
	Результат.Вставить("Описание");
	Результат.Вставить("Журнал");
	
	Описание = НСтр("ru = 'Ошибка на сервере взаимодействия с мобильными устройствами';
					|en = 'Error on the mobile device collaboration server'", ОбщегоНазначения.КодОсновногоЯзыка());
	ОшибкаВЖурнале = НСтр("ru = 'Ошибка на сервере взаимодействия с мобильными устройствами';
							|en = 'Error on the mobile device collaboration server'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	Попытка
		ДанныеОтвета = СтруктураИзJson(ОтветСервиса.ПолучитьТелоКакСтроку(), Истина);
		Результат.Описание = СвойствоСтруктуры(ДанныеОтвета, "title");
		Результат.Журнал = ОшибкаВЖурнале + Символы.ПС				
				+ СвойствоСтруктуры(ДанныеОтвета, "title") + Символы.ПС
				+ СвойствоСтруктуры(ДанныеОтвета, "code") + Символы.ПС
				+ СвойствоСтруктуры(ДанныеОтвета, "details") + Символы.ПС
				+ СвойствоСтруктуры(ДанныеОтвета, "id");
	Исключение
		Результат.Описание = Описание;
		Результат.Журнал = ОшибкаВЖурнале;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция АдресСервисаВзаимодействия()
	
	Результат = "https://app-492902.1cmycloud.com";
	Возврат Результат;
	
КонецФункции

Функция АдресВерсииИнтерфейса(ВерсияВзаимодействия = 1)
	
	Результат = "applications/REDMS";
	Если ВерсияВзаимодействия = 1 Тогда
		Результат = Результат + "/api/v1";
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

Функция НовыйРезультатПолученияСпискаПользователей()
	
	Структура = Новый Структура;
	Структура.Вставить("УспешностьЗагрузки", Ложь);
	Структура.Вставить("ЕстьУстройстваОжидающиеПодключение", Ложь);
	
	Возврат Структура;
	
КонецФункции

Функция НужноВвестиДанныеИнтернетПоддержки()
	
	ЗаполненыДанныеАутентификации = ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	Возврат Не ЗаполненыДанныеАутентификации;
	
КонецФункции

Функция НоваяСтруктураНеобходимыхДанныхДляРегистрацииУчетнойЗаписи()
	
	Структура = Новый Структура;
	Структура.Вставить("УчетнаяЗаписьПодключена");
	Структура.Вставить("НужноВвестиДанныеИнтернетПоддержки");
	
	Возврат Структура;
	
КонецФункции

Функция ТипСообщенияДляМППереадресовка()
	
	Возврат "readressing";
	
КонецФункции

Функция ТипСообщенияДляМПЗаменаТС()
	
	Возврат "replacement";
	
КонецФункции

Функция ФайлаМожетБытьОтправленСМП(ИдентификаторФайла)
	
	ФайлаМожетБытьОтправленСМП = 
		СтрНачинаетсяС(ИдентификаторФайла, "DP_UVUTOCH")
		Или СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSPRMO")
		Или СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSVIPTS")
		Или СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSODVZD")
		Или СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSODPARK")
		Или СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSPOSMO");
		
	Возврат ФайлаМожетБытьОтправленСМП;
	
КонецФункции

Функция ПолучитьИДОтправителяДокументаПоИдФайл(ИдентификаторФайла)
	
	МассивЧастейФайла = СтрРазделить(ИдентификаторФайла, "_", Истина);
	
	// УОУ.  Отправитель медик.
	Если СтрНачинаетсяС(ИдентификаторФайла, "DP_UVUTOCH") Тогда
		ИдентификаторОтправителя = МассивЧастейФайла[3];
		
	// ЭПЛ Титул2. Отправитель медик.
	ИначеЕсли СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSPRMO") Тогда
		ИдентификаторОтправителя = МассивЧастейФайла[4];
		
	// ЭПЛ Титул3. Отправитель техник.
	ИначеЕсли СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSVIPTS") Тогда
		ИдентификаторОтправителя = МассивЧастейФайла[4];
		
	// ЭПЛ Титул4. Отправитель ответственный за одометр.
	ИначеЕсли СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSODVZD") Тогда
		ИдентификаторОтправителя = МассивЧастейФайла[3];
		
	// ЭПЛ Титул5. Отправитель ответственный за одометр.
	ИначеЕсли СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSODPARK") Тогда
		ИдентификаторОтправителя = МассивЧастейФайла[4];
		
	// ЭПЛ Титул6. Отправитель медик.
	ИначеЕсли СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSPOSMO") Тогда
		ИдентификаторОтправителя = МассивЧастейФайла[3];
		
	КонецЕсли;
	
	Возврат ИдентификаторОтправителя;
	
КонецФункции

Функция ПолучитьМассивИДПолучателейПоИдФайл(ИдентификаторФайла)
	
	МассивПолучателей = Новый Массив;
	МассивЧастейФайла = СтрРазделить(ИдентификаторФайла, "_", Истина);
	
	// УОУ.  Отправитель медик.
	Если СтрНачинаетсяС(ИдентификаторФайла, "DP_UVUTOCH") Тогда
		// Оформитель
		МассивПолучателей.Добавить(МассивЧастейФайла[2]);
		
	// ЭПЛ Титул2. Отправитель медик.
	ИначеЕсли СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSPRMO") Тогда
		// Оформитель
		МассивПолучателей.Добавить(МассивЧастейФайла[2]);
		// Техконтроль
		МассивПолучателей.Добавить(МассивЧастейФайла[3]);
		
	// ЭПЛ Титул3. Отправитель техник.
	ИначеЕсли СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSVIPTS") Тогда
		// Оформитель
		МассивПолучателей.Добавить(МассивЧастейФайла[2]);
		//Одометр
		МассивПолучателей.Добавить(МассивЧастейФайла[3]);
		
	// ЭПЛ Титул4. Отправитель ответственный за одометр.
	ИначеЕсли СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSODVZD") Тогда
		// Оформитель
		МассивПолучателей.Добавить(МассивЧастейФайла[2]);
		
	// ЭПЛ Титул5. Отправитель ответственный за одометр.
	ИначеЕсли СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSODPARK") Тогда
		// Оформитель
		МассивПолучателей.Добавить(МассивЧастейФайла[2]);
		//Медосмотр
		МассивПолучателей.Добавить(МассивЧастейФайла[3]);
		
	// ЭПЛ Титул6. Отправитель медик.
	ИначеЕсли СтрНачинаетсяС(ИдентификаторФайла, "ON_PTLSPOSMO") Тогда
		// Оформитель
		МассивПолучателей.Добавить(МассивЧастейФайла[2]);
		
	КонецЕсли;
	
	Возврат МассивПолучателей;
	
КонецФункции

Процедура УдалитьИзМассиваВсеПустыеЗначения(Массив)

	Количество = Массив.Количество();
	Для й = 1 По Количество Цикл
		
		Индекс = Количество - й;
		
		Если Не ЗначениеЗаполнено(Массив[Индекс]) Тогда
			Массив.Удалить(Индекс);
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Функция ЭтоИмяФайлаИсходящегоКонтейнераМП(ИмяФайла, ИдентификаторОрганизации)
	
	СтруктураИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ИмяФайла);
	ИдентификаторФайла = СтруктураИмениФайла.ИмяБезРасширения;
	
	ФайлаМожетБытьОтправленСМП = ФайлаМожетБытьОтправленСМП(ИдентификаторФайла);
	Если Не ФайлаМожетБытьОтправленСМП Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИдентификаторОтправителя = ПолучитьИДОтправителяДокументаПоИдФайл(ИдентификаторФайла);
	МассивИдентификаторвПолучателей = ПолучитьМассивИДПолучателейПоИдФайл(ИдентификаторФайла);
	
	ОтправительЭтоТекущаяОрганизация = ИдентификаторОрганизации = ИдентификаторОтправителя;
	ВПолучателяхЕстьТекущаяОрганизация = МассивИдентификаторвПолучателей.Найти(ИдентификаторОрганизации) <> Неопределено;
	
	// Контейнер является исходящим если выполняются условия:
	// 1. Текущая организация является отправителем.
	// 2. В получателях нет текущей организации. В ином случае контейнер отправляет организация сама себе
	// и он обрабатывается по правилам входящего документа.
	Возврат ОтправительЭтоТекущаяОрганизация И Не ВПолучателяхЕстьТекущаяОрганизация;
	
КонецФункции

#КонецОбласти
