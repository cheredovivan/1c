////////////////////////////////////////////////////////////////////////////////
// Подсистема "Сервис доставки".
// ОбщийМодуль.СервисДоставкиКлиентПереопределяемый.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Обработчик события АвтоПодбор поля формы контактной информации для подбора вариантов адреса по введенной строке.
//
// Параметры:
//     Элемент                  - ПолеФормы      - элемент формы, содержащий представление контактной информации.
//     Текст                    - Строка         - строка текста, введенная пользователем в поле контактной информации.
//     ДанныеВыбора             - СписокЗначений Из Строка - содержит список значений, который будет использован при стандартной
//                                                 обработке события.
//     ПараметрыПолученияДанных - Структура
//                              - Неопределено - содержит параметры поиска, которые будут переданы
//                                в метод ПолучитьДанныеВыбора. Подробнее см. описание расширения поля формы для
//                                поля ввода АвтоПодбор в синтакс-помощнике.
//     Ожидание -   Число       - интервал в секундах после ввода текста, через который произошло событие.
//                                Если 0, то это означает, что событие было вызвано не по поводу ввода текста,
//                                а для формирования списка быстрого выбора. 
//     СтандартнаяОбработка     - Булево         - в данный параметр передается признак выполнения стандартной
//                                системной) обработки события. Если в теле процедуры-обработчика
//                                установить данному параметру значение Ложь, стандартная обработка события
//                                производиться не будет.
//
Процедура АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка) Экспорт
	
	УправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

// Устанавливает имя формы выбора объекта, если она отличается от формы выбора по умолчанию.
//
// Параметры:
//  ИмяОбъекта - Строка - имя объекта метаданных.
//  ИмяФормыВыбора - Строка - полное имя формы выбора объекта. Например, "Документ.ЗаказПокупателя.ФормаВыбора".
//
Процедура УстановитьИмяФормыВыбораОбъектаПоИмени(ИмяОбъекта, ИмяФормыВыбора) Экспорт
	
КонецПроцедуры

// Открытие формы нового заказа на доставку, создаваемого на основании переданного объекта
// в параметре "МассивСсылок". Вызывается из подсистемы "ПодключаемыеКоманды".
//
// Параметры:
//  ДокументыОснования - Массив из ЛюбаяСсылка - объект или список объектов.
//  ПараметрыВыполнения - см. ПодключаемыеКомандыКлиентСервер.ПараметрыВыполненияКоманды
//
Процедура ЗаказНаДоставкуСоздатьНаОсновании(ДокументыОснования, ПараметрыВыполнения) Экспорт
	
	ПараметрыВыполненияКоманды = Новый Структура;
	ПараметрыВыполненияКоманды.Вставить("Источник");
	ПараметрыВыполненияКоманды.Вставить("Уникальность");
	ПараметрыВыполненияКоманды.Вставить("Окно");
	ПараметрыВыполненияКоманды.Вставить("НавигационнаяСсылка");
	ОписаниеКоманды = ПараметрыВыполнения.ОписаниеКоманды; // Структура
	ЗаполнитьЗначенияСвойств(ПараметрыВыполненияКоманды, ОписаниеКоманды.ДополнительныеПараметры); 
	
	ПараметрыОткрытия = СервисДоставкиКлиент.НовыйПараметрыОткрытьФормуСервисаДоставки();
	ПараметрыОткрытия.Вставить("ПараметрыВыполненияКоманды", ПараметрыВыполненияКоманды);
	
	Если ТипЗнч(ДокументыОснования) = Тип("Массив") Тогда
		ПараметрыОткрытия.ДокументыОснования.ЗагрузитьЗначения(ДокументыОснования);
	Иначе
		ПараметрыОткрытия.ДокументыОснования.Добавить(ДокументыОснования);
	КонецЕсли;
	
	ТипГрузоперевозки = 0;
	
	Если ОписаниеКоманды.Идентификатор = "СоздатьНовыйЗаказНаДоставку" Тогда
		ТипГрузоперевозки = 1;
	ИначеЕсли ОписаниеКоманды.Идентификатор = "СоздатьНовыйЗаказНаКурьерскуюДоставку" Тогда
		ТипГрузоперевозки = 2;
	ИначеЕсли ОписаниеКоманды.Идентификатор = "СоздатьНовыйЗаказНаДоставкуКурьерика" Тогда
		ТипГрузоперевозки = 3;
	ИначеЕсли ОписаниеКоманды.Идентификатор = "СоздатьНовыйЗаказНаДоставкуСДЭК" Тогда
		ТипГрузоперевозки = 4;
	КонецЕсли;
	
	Если ТипГрузоперевозки <> 0 Тогда
		ПараметрыОткрытия.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	КонецЕсли;

	ДокументОснование = ПараметрыОткрытия.ДокументыОснования[0].Значение;

	ПараметрыПроверки = СервисДоставкиКлиент.ПараметрыПроверкиОснований();
	ПараметрыПроверки.ПроверитьСпособДоставки = ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг");
	ПараметрыПроверки.ПолучитьКоличествоЗаказов = 
		Не (ТипГрузоперевозки = 3 И ПараметрыОткрытия.ДокументыОснования.Количество() > 1);

	Если ПараметрыПроверки.ПроверитьСпособДоставки Или ПараметрыПроверки.ПолучитьКоличествоЗаказов Тогда
		ПроверитьОснованияЗаказаНаДоставкуИПродолжить(ПараметрыОткрытия, ПараметрыПроверки);
		Возврат;
	КонецЕсли;
	
	СервисДоставкиКлиент.ОткрытьФормуСервисаДоставки(ПараметрыОткрытия);
	
КонецПроцедуры

// Открытие формы нового заказа на доставку, создаваемого на основании документа.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма документа.
//
Процедура ОткрытьФормуНовогоЗаказаНаДоставку(Форма) Экспорт
	
	// Запись в форме объекта.
	Если Не Форма.Объект.Проведен Или Форма.Модифицированность Тогда
			
		ТекстВопроса = НСтр("ru = 'Для создания заказа на доставку
			|документ будет проведен. Продолжить?';
			|en = 'The document will be posted
			|to generate devliery order. Continue?'");
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Провести и продолжить';
													|en = 'Post and continue'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		
		Обработчик = Новый ОписаниеОповещения("ПродолжитьВыполнениеКомандыОткрытьФормуНовогоЗаказаНаДоставку", ЭтотОбъект, Форма);
		
		ПоказатьВопрос(Обработчик, ТекстВопроса, Кнопки);
		Возврат;
		
	КонецЕсли;

	ПроверитьОснованияЗаказаНаДоставкуИПродолжить(Форма);

КонецПроцедуры

// Обработка выбора заказа на доставку из списка выбора заказов.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма документа.
//  ВыбранныйЗаказ - ДанныеФормыЭлементКоллекции, Неопределено - Строка списка заказов.
//  ИмяПроцедурыОбработки - Строка - Имя процедуры обработки выбора заказа в списке заказов.
//
Процедура ОбработатьРезультатВыбораЗаказаНаДоставку(Форма, ВыбранныйЗаказ, ИмяПроцедурыОбработки) Экспорт
	
КонецПроцедуры

// Открытие списка платежных документов
// 
// Параметры:
//  ПараметрыСписка - Структура - Параметры списка. Содержит свойства:
//   * Организация - ОпределяемыйТип.ОрганизацияСервисДоставки - организация-заказчик перевозки,
//   * ДоговорыЭквайринга - массив из СправочникСсылка.ДоговорыЭквайринга - договора эквайринга, указанные в настройках
//    сервиса доставки.
//
Процедура ПерейтиКСпискуПлатежныхДокументов(ПараметрыСписка) Экспорт
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Организация", ПараметрыСписка.Организация);
	ПараметрыОтбора.Вставить("ДоговорЭквайринга", ПараметрыСписка.ДоговорыЭквайринга);
		
	ПараметрыФормы = Новый Структура("Отбор", ПараметрыОтбора);
	
	ОткрытьФорму("Документ.ОперацияПоПлатежнойКарте.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры

// Открывает форму настроек, если требуется
//
Процедура ОткрытьФормуНастройкиСозданиеПлатежныхДокументов() Экспорт
	
	ОткрытьФорму("РегистрСведений.НастройкиЗагрузкиНаложенныхПлатежейСервисДоставки.ФормаСписка");
	
КонецПроцедуры

#Область НаложенныеПлатежи

// Заполняет перечень типов документов-оснований, для которых можно оформить оплату доставки наложенным платежом.
//  
// Параметры:
//	КоллекцияТиповДокументов - Массив из ОписаниеТипов
Процедура ТипыОснованийДляНаложенногоПлатежа(КоллекцияТиповДокументов) Экспорт
	
	КоллекцияТиповДокументов.Добавить(Тип("ДокументСсылка.ЗаказКлиента"));
	КоллекцияТиповДокументов.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
//	Ответ - КодВозвратаДиалога
//	Форма - ФормаКлиентскогоПриложения - форма
Процедура ПродолжитьВыполнениеКомандыОткрытьФормуНовогоЗаказаНаДоставку(Ответ, Форма) Экспорт
	
	Если Ответ = КодВозвратаДиалога.ОК Тогда
		
		ОчиститьСообщения();
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Форма", Форма);
		ДействиеПослеЗаписи = Новый ОписаниеОповещения("ПослеПроведенияДокумента", ЭтотОбъект, ДополнительныеПараметры);
		
		ПараметрыЗаписи = Новый Структура;
		ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
		ПараметрыЗаписи.Вставить("ДействиеПослеЗаписи", ДействиеПослеЗаписи);
		
		Форма.Записать(ПараметрыЗаписи);
		
	КонецЕсли;
	
КонецПроцедуры

// После проведения документа.
// 
// Параметры:
//  Результат - Булево - Результат проведения документа
//  ДополнительныеПараметры - Структура:
//   * Форма - ФормаКлиентскогоПриложения - 
Процедура ПослеПроведенияДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		Форма = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеПараметры, "Форма");
		Если Форма <> Неопределено Тогда
			ПроверитьОснованияЗаказаНаДоставкуИПродолжить(Форма);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//	Контекст - ФормаКлиентскогоПриложения - форма:
//		* Объект - Структура - коллекция реквизитов заказа на доставку:
//		** Ссылка - ДокументСсылка, Неопределено - ссылка на документ,
//			 - Структура - Контекст открытия формы.
Процедура ЗавершитьВыполнениеКомандыОткрытьФормуНовогоЗаказаНаДоставку(Контекст)
	
	Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		ПараметрыОткрытия = СервисДоставкиКлиент.НовыйПараметрыОткрытьФормуСервисаДоставки();
		
		ПараметрыОткрытия.ОрганизацияБизнесСети = Контекст.Объект["Организация"];
		Если ЗначениеЗаполнено(Контекст.Объект["Ссылка"]) Тогда
			ПараметрыОткрытия.ДокументыОснования.Добавить(Контекст.Объект["Ссылка"]);
		КонецЕсли;
		
		ДанныеВладельцаФормы = Новый Структура;
		ДанныеВладельцаФормы.Вставить("Форма", Контекст);
		ДанныеВладельцаФормы.Вставить("Элемент", Контекст.ТекущийЭлемент);
		
	Иначе
		
		ПараметрыОткрытия = Контекст;
		ДанныеВладельцаФормы = Неопределено;
		
	КонецЕсли;
	
	СервисДоставкиКлиент.ОткрытьФормуСервисаДоставки(ПараметрыОткрытия, ДанныеВладельцаФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// При необходимости проверяет способ доставки и количество уже введенных заказов на доставку 
// после чего  открывает форму нового Заказа на доставку, если это возможно.
// 
// Параметры:
//	Контекст - ФормаКлиентскогоПриложения - форма:
//		* Объект - Структура - коллекция реквизитов заказа на доставку:
//		** Ссылка - ДокументСсылка, Неопределено - ссылка на документ,
//			 - Структура - см. СервисДоставкиКлиент.НовыйПараметрыОткрытьФормуСервисаДоставки
//  ПараметрыПроверки - Неопределено, Структура - см. СервисДоставкиКлиент.ПараметрыПроверкиОснований
Процедура ПроверитьОснованияЗаказаНаДоставкуИПродолжить(Контекст, ПараметрыПроверки = Неопределено)
	
	Если ТипЗнч(Контекст) = Тип("Структура") Тогда
		ДокументыОснования = Контекст.ДокументыОснования.ВыгрузитьЗначения();
	Иначе
		ДокументыОснования = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Контекст.Объект.Ссылка);
	КонецЕсли;
	Если ПараметрыПроверки = Неопределено Тогда
		ПараметрыПроверки = СервисДоставкиКлиент.ПараметрыПроверкиОснований();
		ПараметрыПроверки.ПолучитьКоличествоЗаказов = Истина;
	КонецЕсли;
	
	ДанныеПроверки = 
		СервисДоставкиВызовСервера.ДанныеПроверкиОснованияЗаказаНаДоставку(ДокументыОснования, ПараметрыПроверки);
		
	Если ПараметрыПроверки.ПроверитьСпособДоставки И Не ДанныеПроверки.СпособДоставкиПрименим Тогда
		ТекстПредупреждения = СтрШаблон(
			НСтр("ru = 'Ввод Заказа на доставку возможен только для способа доставки  ""Перевозчик (с нашего склада)"".
				 |По документам %1 способ доставки отличается от указанного.';
				 |en = 'You can create a delivery order only for the ""Outsourced carrier"" delivery method.
				 |The delivery method of the %1 documents differs from the specified one.'"), ДанныеПроверки.ТекстПояснения);
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если ПараметрыПроверки.ПолучитьКоличествоЗаказов И ДанныеПроверки.КоличествоЗаказовПоОснованиям.Количество() > 0 
		И ДанныеПроверки.КоличествоЗаказовПоОснованиям[0].Значение > 0 Тогда
		
		ТекстВопроса = Стршаблон(НСтр("ru = 'На основании %1 введено заказов на доставку: %2.
			|Продолжить?';
			|en = '%2 delivery orders are generated based on %1.
			|Continue?'"), ДанныеПроверки.КоличествоЗаказовПоОснованиям[0].Представление,
			ДанныеПроверки.КоличествоЗаказовПоОснованиям[0].Значение);
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Продолжить';
													|en = 'Continue'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена);
		
		Обработчик = Новый ОписаниеОповещения("ПродолжитьПроверитьСуществованиеЗаказовНаДоставкуПоОснованию", 
			ЭтотОбъект, Контекст);
		
		ПоказатьВопрос(Обработчик, ТекстВопроса, Кнопки);
		Возврат;
		
	КонецЕсли;
	
	ЗавершитьВыполнениеКомандыОткрытьФормуНовогоЗаказаНаДоставку(Контекст);
КонецПроцедуры

// Параметры:
//	Ответ - КодВозвратаДиалога
//	Контекст - ФормаКлиентскогоПриложения - форма:
//		* Объект - Структура - коллекция реквизитов заказа на доставку:
//		** Ссылка - ДокументСсылка, Неопределено - ссылка на документ,
//			 - Структура - Контекст открытия формы.
Процедура ПродолжитьПроверитьСуществованиеЗаказовНаДоставкуПоОснованию(Ответ, Контекст) Экспорт
	
	Если Ответ = КодВозвратаДиалога.ОК Тогда
		ОчиститьСообщения();
		ЗавершитьВыполнениеКомандыОткрытьФормуНовогоЗаказаНаДоставку(Контекст);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти