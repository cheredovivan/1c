///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.ОнлайнЗаказы".
// ОбщийМодуль.ОнлайнЗаказы.
//
// Серверные процедуры подготовки и отправки данных в сервис онлайн-заказов:
//  - определение доступности настроек подключения, а также получения настроек;
//  - определение доступности создания новых заказов и получения статусов созданных заказов;
//  - получение описания новой колонки таблицы товаров заказа.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет доступность использования функциональности создания/обновления онлайн-заказа
// на основании прав доступа пользователя.
//
// Возвращаемое значение:
//  Булево - если Истина, создание/обновление заказа доступно.
//
Функция ОнлайнЗаказыДоступны() Экспорт
	
	Возврат ОнлайнЗаказыСлужебный.ОнлайнЗаказыДоступны();
	
КонецФункции

// Определяет доступность использования функциональности настройки
// онлайн-заказов на основании прав доступа пользователя.
//
// Возвращаемое значение:
//  Булево - если Истина, настройка онлайн-заказов доступна.
//
Функция НастройкаОнлайнЗаказовДоступна() Экспорт
	
	Возврат ОнлайнЗаказыСлужебный.НастройкаОнлайнЗаказовДоступна();
	
КонецФункции

// Создает новый онлайн-заказ или возвращает ссылку на существующий.
//
// Параметры:
//  ДокументЗаказа - ОпределяемыйТип.ДокументОнлайнЗаказаБИП - документ в информационной базе,
//    являющийся основанием для заказа.
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам - настройка выполнения операции.
//
// Возвращаемое значение:
//  Структура - результат получения ссылки онлайн-заказа:
//    * URLЗаказа - Строка - URL онлайн-заказа;
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом:
//        - <Пустая строка> - получение URL заказа выполнено успешно;
//        - "НеверныйФорматЗапроса" - передан некорректный запрос или настройка подключения;
//        - "НеверныйЛогинИлиПароль" - некорректный логин/пароль ИПП;
//        - "СервисВременноНеДоступен" - временная недоступность сервиса;
//        - "ПревышеноКоличествоПопыток" - превышено количество попыток
//          обращения к сервису с некорректным логином и паролем;
//        - "ОшибкаПодключения" - ошибка при подключении к сервису;
//        - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция URLЗаказа(
		ДокументЗаказа,
		НастройкаПодключения) Экспорт
	
	ИмяФункции = "ОнлайнЗаказы.URLЗаказа";
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"ДокументЗаказа",
		ДокументЗаказа,
		Метаданные.ОпределяемыеТипы.ДокументОнлайнЗаказаБИП.Тип);
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"НастройкаПодключения",
		НастройкаПодключения,
		Тип("СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам"));
	
	Возврат ОнлайнЗаказыСлужебный.URLЗаказа(
		ДокументЗаказа,
		НастройкаПодключения);
	
КонецФункции

// Выполняет загрузку статусов онлайн-заказов, по которым было
// отложено получение результата.
//
// Возвращаемое значение:
//  Массив из ОпределяемыйТип.ДокументОнлайнЗаказаБИП - перечень обработанных документов.
//
Функция СтатусыЗаказов() Экспорт
	
	Возврат ОнлайнЗаказыСлужебный.АктуализироватьСтатусыЗаказов();
	
КонецФункции

// Получает данные онлайн-заказов для переданных документов оснований.
//
// Параметры:
//  ДокументыЗаказов - Массив Из ОпределяемыйТип.ДокументОнлайнЗаказаБИП - документы в информационной базе,
//   являющиеся основанием для заказа.
//
// Возвращаемое значение:
//  Соответствие - данные онлайн-заказов:
//    * Ключ - ОпределяемыйТип.ДокументОнлайнЗаказаБИП - документ основание онлайн-заказа.
//    * Значение - Структура, Неопределено - Данные онлайн-заказа, Неопределено в случае, если онлайн-заказ не был создан:
//      ** СтатусЗаказа - Строка - Статус онлайн-заказа, может принимать следующие значения:
//        - "Выполняется" - подтверждение оплаты не получено;
//        - "Отменен" - онлайн-заказ отменен;
//        - "Выполнен" - онлайн-заказ оплачен;
//        - "Ошибка" - не удалось выполнить проверку оплаты из-за ошибки;
//      ** ПараметрыЗаказа - Структура - дополнительные данные по заказу:
//        *** URLЗаказа - Строка - URL онлайн-заказа;
//        *** СпособОплаты - Строка - Идентификатор способа оплаты, пустая если СтатусЗаказа <> "Выполнен".
//            Может принимать следующие значения: "СБПc2b", "СБПb2b", "Наличные", "ПоРеквизитам", "Эквайринг", "Прочее".
//        *** СуммаОплаты - Число - Сумма оплаты онлайн-заказа, пустая если СтатусЗаказа <> "Выполнен".
//
Функция ДанныеЗаказов(ДокументыЗаказов) Экспорт
	
	ИмяФункции = "ОнлайнЗаказы.ДанныеЗаказов";
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"ДокументыЗаказов",
		ДокументыЗаказов,
		Тип("Массив"));
	
	Возврат ОнлайнЗаказыСлужебный.ДанныеЗаказов(ДокументыЗаказов);
	
КонецФункции

// Выполняет установку статуса у существующего онлайн-заказа.
//
// Параметры:
//  ДокументЗаказа - ОпределяемыйТип.ДокументОнлайнЗаказаБИП - документ в информационной базе,
//   являющийся основанием для заказа.
//  Статус - Строка - Статус который должен быть установлен у онлайн-заказа. Может принимать значения:
//   "Оплачен", "Отменен"
//  ПараметрыОплаты - Структура - содержит параметры оплаты обязательна для заполнения когда Статус принимает значение "Оплачен"
//    * СпособОплаты - Строка - Идентификатор способа оплаты.
//      Может принимать значения: "Наличные", "Эквайринг", "ПоРеквизитам", "Прочее"
//  ПривилегированныйРежим - Булево - признак выполнения операции установки статуса привилегированном режиме,
//  Ложь по умолчанию
//
// Возвращаемое значение:
//  Структура - результат установки статуса онлайн-заказа:
//    * КодОшибки - Строка - строковый код возникшей ошибки, который
//      может быть обработан вызывающим методом:
//        - <Пустая строка> - установка статуса выполнена успешно;
//        - "ИдентификаторНеНайден" - у преданного документа отсутствует онлайн-заказ;
//        - "УжеОплачен" - онлайн-заказ уже оплачен;
//        - "НеверныйФорматЗапроса" - передан некорректный запрос или настройка подключения;
//        - "НеверныйЛогинИлиПароль" - некорректный логин/пароль ИПП;
//        - "СервисВременноНеДоступен" - временная недоступность сервиса;
//        - "ПревышеноКоличествоПопыток" - превышено количество попыток
//          обращения к сервису с некорректным логином и паролем;
//        - "ОшибкаПодключения" - ошибка при подключении к сервису;
//        - "ОшибкаСервиса" - внутренняя ошибка сервиса;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя;
//    * ИнформацияОбОшибке - Строка, ФорматированнаяСтрока - сообщение об ошибке для администратора.
//
Функция УстановитьСтатусЗаказа(
	ДокументЗаказа,
	Статус,
	ПараметрыОплаты,
	ПривилегированныйРежим = Ложь) Экспорт
	
	Результат = ОнлайнЗаказыСлужебный.НовыйРезультатОперации();
	
	ИмяФункции = "ОнлайнЗаказы.УстановитьСтатусЗаказа";
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"ДокументЗаказа",
		ДокументЗаказа,
		Метаданные.ОпределяемыеТипы.ДокументОнлайнЗаказаБИП.Тип);
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"Статус",
		Статус,
		Тип("Строка"),
		,
		"Оплачен,Отменен");
		
	Если Статус = "Оплачен" Тогда
		
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
			ИмяФункции,
			"ПараметрыОплаты",
			ПараметрыОплаты,
			Тип("Структура"),
			Новый Структура(
				"СпособОплаты",
				Тип("Строка")));
		
		ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
			ИмяФункции,
			"ПараметрыОплаты.СпособОплаты",
			ПараметрыОплаты.СпособОплаты,
			Тип("Строка"),
			,
			"Наличные,Эквайринг,ПоРеквизитам,Прочее");
		
	КонецЕсли;
	
	Если ПривилегированныйРежим Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Если Статус = "Отменен" Тогда
		РезультатОперации = ОнлайнЗаказыСлужебный.ДеактивироватьЗаказ(ДокументЗаказа);
	ИначеЕсли Статус = "Оплачен" Тогда
		РезультатОперации = ОнлайнЗаказыСлужебный.УстановитьСтатусОплачен(ДокументЗаказа, ПараметрыОплаты);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Результат, РезультатОперации);
	
	Возврат Результат;
	
КонецФункции

// Определяет параметры функциональности настройки онлайн-заказов
// на основании прав доступа пользователя и введенных данных.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - Организация для которой планируется создание настройки онлайн-заказов.
//
// Возвращаемое значение:
//  Структура - содержит параметры настройки онлайн-заказов:
//    * НастройкиПодключения - Массив из Справочник.НастройкиПодключенияКОнлайнЗаказам -
//      перечень настроек подключения по заданной организации.
//
Функция НастройкиПодключения(Организация) Экспорт
	
	ИмяФункции = "ОнлайнЗаказы.НастройкиПодключения";
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"Организация",
		Организация,
		Метаданные.ОпределяемыеТипы.Организация.Тип);
	
	Если Не ОнлайнЗаказыДоступны()
		И Не НастройкаОнлайнЗаказовДоступна() Тогда
		ВызватьИсключение НСтр("ru = 'Нарушение прав доступа. Пользователю запрещено чтение настроек подключения Онлайн-заказов.
			|Обратитесь к администратору.';
			|en = 'Access violation. The user cannot read settings of online order connection.
			|Contact the administrator.'");
	КонецЕсли;
	
	НастройкиОрганизации = Справочники.НастройкиПодключенияКОнлайнЗаказам.НастройкиОрганизации(
		Организация);
	
	Результат = Новый Структура;
	Результат.Вставить(
		"НастройкиПодключения",
		НастройкиОрганизации.ИспользуемыеНастройки);
	
	Возврат Результат;
	
КонецФункции

// Формирует новое описание колонки товаров онлайн-заказа.
//
// Возвращаемое значение:
//  Структура - содержит описание онлайн-заказа:
//    * Идентификатор - Строка - уникальный идентификатор колонки.
//    * Представление - Строка - представление колонки пользователю (шапка табличной части).
//    * ПорядокОтображения - Число - Порядок отображения колонок пользователю, упорядочивание по возрастанию.
//    * Видимость - Булево - признак видимости колонки пользователю.
//
Функция НоваяКолонкаТовары() Экспорт
	
	Возврат ОнлайнЗаказыСлужебный.НоваяКолонкаТовары();
	
КонецФункции

// Возвращает список команд печати для переданного объекта метаданных.
//
// Параметры:
//  ОснованиеЗаказа - ОбъектМетаданных - объект метаданных документа в информационной базе,
//    являющегося основанием для заказа, для которого необходимо получить перечень команд печати.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - перечень команд печати:
//   * Идентификатор - Строка - идентификатор команды печати, по которому менеджер печати определяет печатную форму,
//     которую необходимо сформировать. Например, "СчетЗаказ".
//   * Представление - Строка - представление команды в меню Печать. Например, "Счет на оплату".
//   * МенеджерПечати - Строка - имя объекта, в модуле менеджера которого располагается
//     процедура Печать, формирующая табличные документы для этой команды.
//   * ТипыОбъектовПечати - Массив - список типов объектов, для которых предназначена команда
//     печати.
//   * Обработчик - Строка - клиентский обработчик команды, в который необходимо передать
//     управление вместо стандартного обработчика команды Печать.
//   * Порядок - Число - Значение от 1 до 100, указывающее порядок размещения команды
//     по отношению к другим командам.
//   * Картинка - Картинка - Картинка, которая отображается возле команды в меню Печать.
//     Например, БиблиотекаКартинок.ФорматPDF.
//   * СписокФорм - Строка - Имена форм через запятую, в которых отображается
//     команда.
//   * МестоРазмещения - Строка - Имя группы формы, в которой размещается
//     команда печати.
//   * ЗаголовокФормы - Строка - Произвольная строка, переопределяющая стандартный заголовок
//     формы "Печать документов". Например, "Настраиваемый комплект".
//   * ФункциональныеОпции - Строка - Имена функциональных опций через запятую, от которых зависит
//     доступность команды печати.
//   * УсловияВидимости - Массив -  Коллекция условий видимости команды в зависимости от
//     контекста.
//   * ПроверкаПроведенияПередПечатью - Булево - Признак необходимости проверки проведенности
//     документов перед печатью.
//   * СразуНаПринтер - Булево - Признак необходимости печати документов без предварительного
//     просмотра, сразу на принтер.
//   * ФорматСохранения - Строка - Применяется для быстрого сохранения печатной
//     формы (без дополнительных действий) в различные форматы, отличные от mxl.
//   * ПереопределитьПользовательскиеНастройкиКоличества - Булево - (необязательный) Признак необходимости отключения в
//     форме ПечатьДокументов механизма сохранения/восстановления выбранного
//     пользователем количества экземпляров на печать.
//   * ДополнитьКомплектВнешнимиПечатнымиФормами - Булево - (необязательный) Признак необходимости дополнить комплект
//     документов всеми подключенными к объекту внешними печатными формами
//     (подсистема ДополнительныеОтчетыИОбработки).
//   * ФиксированныйКомплект - Булево -  Признак необходимости блокировки от изменения пользователем
//     состава комплекта документов.
//   * ДополнительныеПараметры - Структура - произвольные параметры для передачи в менеджер печати.
//   * НеВыполнятьЗаписьВФорме - Булево  - Признак необходимости отключения механизма записи объекта
//     перед выполнением команды печати.
//   * ТребуетсяРасширениеРаботыСФайлами - Булево - Признак необходимости подключения расширения для работы 
//     с 1С:Предприятием перед выполнением команды.
//
Функция КомандыПечатиОбъектаДоступныеДляВложений(ОснованиеЗаказа) Экспорт
	
	ИмяФункции = "ОнлайнЗаказы.КомандыПечатиОбъектаДоступныеДляВложений";
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		ИмяФункции,
		"ОснованиеЗаказа",
		ОснованиеЗаказа,
		Тип("ОбъектМетаданных"));
	
	Возврат УправлениеПечатью.КомандыПечатиОбъектаДоступныеДляВложений(ОснованиеЗаказа);
	
КонецФункции

#КонецОбласти
