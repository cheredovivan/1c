
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("СтруктураОтбора") И Параметры.СтруктураОтбора.Свойство("Организация")
		И ЗначениеЗаполнено(Параметры.СтруктураОтбора.Организация) И Не Параметры.Отбор.Свойство("Организация") Тогда
		// Отбор по организации будет устанавливаться только тогда, когда нет отбора, прописанного через стандартные
		// механизмы платформы (он более приоритетный)
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Организация",
			Параметры.СтруктураОтбора.Организация, ВидСравненияКомпоновкиДанных.Равно,, Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	КонецЕсли;
	
	// Отбор устанавливающий по умолчанию старое поведения для формы (когда выбирались документы продажи только тогда,
	// когда они были оформлены вне заказа
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "НеЗаполненЗаказ",
		Истина, ВидСравненияКомпоновкиДанных.Равно, НСтр("ru = 'Не показывать реализации по заказам';
														|en = 'Do not show sales for orders'"), Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ, "РеализацииПоЗаказам");
	
	Для каждого НастраиваемыйДокумент Из ПланыВидовХарактеристик.СтатьиРасходов.ИдентификаторыДокументовСтатейРаспределяемыхНаПродажу() Цикл
		ТипыДокументов.Добавить(НастраиваемыйДокумент, НастраиваемыйДокумент.Синоним, Истина);
	КонецЦикла;
	
	Список.Параметры.УстановитьЗначениеПараметра("ТипыСсылок", ТипыДокументов.ВыгрузитьЗначения());
	ТипыДокументовСтрокой = СписокНастраиваемыхДокументовСтрокой(ТипыДокументов);
	
	Список.Параметры.УстановитьЗначениеПараметра("ТипыСсылок", ПродажиСервер.ТипыСсылокДокументыРеализации(Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьВыборДокументов" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ОграничиватьВыборУказаннымиЗначениями", Истина);
		ПараметрыФормы.Вставить("ОписаниеТипов", Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных"));
		ПараметрыФормы.Вставить("ЗначенияДляВыбора", ТипыДокументов);
		ПараметрыФормы.Вставить("Представление", НСтр("ru = 'Фильтр по типам документов';
														|en = 'Filter by document types'"));
		СписокОтмеченных = Новый СписокЗначений;
		СписокОтмеченных.ЗагрузитьЗначения(ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(ПараметрыФормы.ЗначенияДляВыбора));
		ПараметрыФормы.Вставить("Отмеченные", СписокОтмеченных);
		ОповещениеОЗавершенииОтметки = Новый ОписаниеОповещения("УстановитьСписок", ЭтотОбъект);
		ОткрытьФорму("ОбщаяФорма.ВводЗначенийСпискомСФлажками", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор,,, ОповещениеОЗавершенииОтметки);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	ОповеститьОВыборе(СтрокаТаблицы.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьДокумент(Команда)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		ОповеститьОВыборе(СтрокаТаблицы.Ссылка);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокумент(Команда)
	
	СтрокаТаблицы = Элементы.Список.ТекущиеДанные;
	Если СтрокаТаблицы <> Неопределено Тогда
		ПоказатьЗначение(Неопределено, СтрокаТаблицы.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, "Список.Дата", "Дата");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеСписка(СписокНастраиваемыхДокументов, КоличествоВыводимых = 2)
	
	КоличествоОтмеченных = ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(СписокНастраиваемыхДокументов).Количество();
	Если КоличествоОтмеченных = СписокНастраиваемыхДокументов.Количество() Тогда
		Возврат НСтр("ru = '< показаны все >';
					|en = '< shown all >'");
	КонецЕсли;
	
	Если КоличествоОтмеченных = 0 Тогда
		Возврат НСтр("ru = '< не выбраны >';
					|en = '<not selected>'");
	КонецЕсли;
	
	МассивПредставлений = Новый Массив;
	Для каждого Стр Из СписокНастраиваемыхДокументов Цикл
		Если МассивПредставлений.Количество() = КоличествоВыводимых Тогда
			Прервать;
		КонецЕсли;
		Если Стр.Пометка Тогда
			МассивПредставлений.Добавить(Стр.Представление);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивПредставлений.Количество() = 1 Тогда
		Возврат МассивПредставлений.Получить(0);
	КонецЕсли;
	
	Если КоличествоОтмеченных > КоличествоВыводимых Тогда
		Возврат СтрСоединить(МассивПредставлений, ", ") + " " + СтрШаблон(НСтр("ru = 'и еще %1';
																				|en = 'and also %1'"), КоличествоОтмеченных - КоличествоВыводимых);
	КонецЕсли;
	
	Если МассивПредставлений.Количество() = КоличествоВыводимых Тогда
		Возврат СтрСоединить(МассивПредставлений, ", ");
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура УстановитьСписок(СписокДокументов, ДополнительныеПараметры = Неопределено) Экспорт

	Если ТипЗнч(СписокДокументов) <> Тип("СписокЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	ТипыДокументов = СписокДокументов;
	Список.Параметры.УстановитьЗначениеПараметра("ТипыСсылок", ОбщегоНазначенияКлиентСервер.ОтмеченныеЭлементы(ТипыДокументов));
	ТипыДокументовСтрокой = СписокНастраиваемыхДокументовСтрокой(ТипыДокументов);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СписокНастраиваемыхДокументовСтрокой(СписокНастраиваемыхДокументов)
	
	СтрокаВозврата = Новый ФорматированнаяСтрока(ПредставлениеСписка(СписокНастраиваемыхДокументов),,,, "ОткрытьВыборДокументов");
	Возврат СтрокаВозврата;
	
КонецФункции

#КонецОбласти
