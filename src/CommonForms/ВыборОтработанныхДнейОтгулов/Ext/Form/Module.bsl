#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Сотрудник = Параметры.Сотрудник;
	Документ = Параметры.Документ;
	ИсправленныйДокумент = Параметры.ИсправленныйДокумент;
	КоличествоДней = Параметры.КоличествоДней;
	КоличествоЧасов = Параметры.КоличествоЧасов;
	НачалоОтгула = Параметры.НачалоОтгула;
	Компенсация = Параметры.Компенсация;
	РасходДнейДоНачалаУчетаОтгулов = Параметры.РасходДнейДоНачалаУчетаОтгулов;
	ВыборОтработанныхЧасов = Параметры.ВыборОтработанныхЧасов;
	ВыбранныеДни.ЗагрузитьЗначения(Параметры.ВыбранныеДни);
	
	Если ЗначениеЗаполнено(Параметры.ИспользованныеДни) Тогда 
		ИспользованныеДни.ЗагрузитьЗначения(Параметры.ИспользованныеДни);
	КонецЕсли;
	
	ОтработанныеДниНачало = ДобавитьМесяц(НачалоОтгула, -12);
	
	ЗаполнитьОтработанныеДни();
	УстановитьКоличествоИспользованныхДнейДоНачалаУчетаОтгулов(ЭтаФорма);
	
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыОтработанныеДни

&НаКлиенте
Процедура ОтработанныеДниВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элемент.ТекущиеДанные.Выбран = Не Элемент.ТекущиеДанные.Выбран;
	ЭлементСписка = ВыбранныеДни.НайтиПоЗначению(Элемент.ТекущиеДанные.Дата);
	
	Если ЭлементСписка <> Неопределено Тогда
		ВыбранныеДни.Удалить(ЭлементСписка);
	Иначе 
		ВыбранныеДни.Добавить(Элемент.ТекущиеДанные.Дата);
		ВыбранныеДни.СортироватьПоЗначению();
		Если Не Компенсация И Элемент.ТекущиеДанные.Дата < ОтработанныеДниНачало Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Между отработанным днем %1 и началом отгула прошло более года';
					|en = 'More than one year passed between the worked day %1 and the day off start'"), 
				Формат(Элемент.ТекущиеДанные.Дата, "ДЛФ=Д"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
	УстановитьКоличествоИспользованныхДнейДоНачалаУчетаОтгулов(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ВыбраноДней = 0;
	ВыбраноЧасов = 0;
	Если ВыборОтработанныхЧасов Тогда 
		Для Каждого ОтработанныйДень Из ОтработанныеДни Цикл
			Если ВыбранныеДни.НайтиПоЗначению(ОтработанныйДень.Дата) <> Неопределено Тогда 
				ВыбраноЧасов = ВыбраноЧасов + ОтработанныйДень.Часы;
			КонецЕсли;
		КонецЦикла;
	Иначе 
		ВыбраноДней = ИспользованоДоНачалаУчетаОтработанныхДней + ВыбранныеДни.Количество();
	КонецЕсли;
	
	Если Не ВыборОтработанныхЧасов И ВыбраноДней <> КоличествоДней Тогда 
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Выбранное количество дней (%1) не соответствует требуемому (%2). Продолжить?';
				|en = 'The selected number of days (%1) does not match the required number (%2). Continue?'"), 
			ВыбраноДней, КоличествоДней);
		Оповещение = Новый ОписаниеОповещения("ОКЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	ИначеЕсли ВыборОтработанныхЧасов И ВыбраноЧасов <> КоличествоЧасов Тогда
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Выбранное количество часов (%1) не соответствует требуемому (%2). Продолжить?';
				|en = 'The selected number of hours (%1) does not match the required number (%2). Continue?'"), 
			ВыбраноЧасов, КоличествоЧасов);
		Оповещение = Новый ОписаниеОповещения("ОКЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
	Иначе	
		ОКЗавершение(КодВозвратаДиалога.ОК, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОКЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.ОК Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеСотрудника = Новый Структура;
	ДанныеСотрудника.Вставить("Сотрудник", Сотрудник);
	ДанныеСотрудника.Вставить("НачалоОтгула", НачалоОтгула);
	ДанныеСотрудника.Вставить("ВыбранныеДни", ВыбранныеДни.ВыгрузитьЗначения());
	ДанныеСотрудника.Вставить("ВыборОтработанныхЧасов", ВыборОтработанныхЧасов);
	
	ОтработанныеЧасы = Новый Соответствие;
	Если ВыборОтработанныхЧасов Тогда
		Для Каждого ОтработанныйДень Из ОтработанныеДни Цикл 
			ОтработанныеЧасы.Вставить(ОтработанныйДень.Дата, ОтработанныйДень.Часы);
		КонецЦикла;
	КонецЕсли;
	ДанныеСотрудника.Вставить("ОтработанныеЧасы", ОтработанныеЧасы);
	
	ОповеститьОВыборе(ДанныеСотрудника);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОтработанныеДни()
	
	Если ВыборОтработанныхЧасов Тогда 
		ДанныеСотрудников = УчетОтгулов.СведенияОбОтработанныхЧасах(Сотрудник, НачалоОтгула, Документ, ИсправленныйДокумент);
	Иначе 
		ДанныеСотрудников = УчетОтгулов.СведенияОбОтработанныхДнях(Сотрудник, НачалоОтгула, Документ, ИсправленныйДокумент);
	КонецЕсли;
	
	ДанныеСотрудника = ДанныеСотрудников.Получить(Сотрудник);
	Если ДанныеСотрудника = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого ОтработанныйДень Из ДанныеСотрудника.НеиспользованныеОтработанныеДни Цикл  
		Если ОтработанныйДень >= НачалоОтгула Тогда 
			Прервать;
		КонецЕсли;
		Если ИспользованныеДни.НайтиПоЗначению(ОтработанныйДень) <> Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ОтработанныеДни.Добавить();
		НоваяСтрока.Дата = ОтработанныйДень; 
		НоваяСтрока.Выбран = ВыбранныеДни.НайтиПоЗначению(НоваяСтрока.Дата) <> Неопределено;
		Если ВыборОтработанныхЧасов Тогда
			НоваяСтрока.Часы = ДанныеСотрудника.ОтработанныеЧасы[ОтработанныйДень];
		КонецЕсли;
	КонецЦикла;
	
	ОстатокДнейДоНачалаУчетаОтгулов = ДанныеСотрудника.ОстатокДоНачалаУчетаОтгулов;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьКоличествоИспользованныхДнейДоНачалаУчетаОтгулов(Форма) 
	
	ИспользованоДоНачалаУчета = Мин(Форма.КоличествоДней - Форма.ВыбранныеДни.Количество(), Макс(Форма.ОстатокДнейДоНачалаУчетаОтгулов - Форма.РасходДнейДоНачалаУчетаОтгулов, 0));
	Форма.ИспользованоДоНачалаУчетаОтработанныхДней = Макс(ИспользованоДоНачалаУчета, 0);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ИспользованоДоНачалаУчетаОтработанныхДней", "Видимость", ОстатокДнейДоНачалаУчетаОтгулов > 0);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
		"ОтработанныеДниЧасы", "Видимость", ВыборОтработанныхЧасов);
	
	Если ВыборОтработанныхЧасов Тогда 
		Заголовок = НСтр("ru = 'Часы работы в выходные и праздники';
						|en = 'Working hours on weekends and holidays'");
		Элементы.ОтработанныеДниРасширеннаяПодсказка.Заголовок =
			НСтр("ru = 'Часы работы в выходные и праздники после 1 марта 2025 г., за которые предоставляется день отдыха';
				|en = 'Working hours on weekends and holidays after March 1, 2025, for which a leave day is provided'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
