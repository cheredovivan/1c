
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьНесколькоОрганизаций = СервисСверкиРасчетовПереопределяемый.ИспользоватьНесколькоОрганизаций(); 
	НастройкаОбсужденийДоступна      = СервисСверкиРасчетовПереопределяемый.НастройкаОбсужденийДоступна();
	
	СервисПодключен = ПолучитьФункциональнуюОпцию("ИспользуетсяСервисСверкиРасчетов");
	
	ОрганизацияПоУмолчанию = СервисСверкиРасчетовПереопределяемый.ОрганизацияПоУмолчанию();
	Организация            = ОрганизацияПоУмолчанию;
	РеквизитыОрганизацииДляПодключения = СервисСверкиРасчетовПереопределяемый.РеквизитыОрганизацииДляПодключения(Организация);
	
	НаименованиеОрганизации = РеквизитыОрганизацииДляПодключения.Наименование;
	ЭтоЮрЛицо = РеквизитыОрганизацииДляПодключения.ЭтоЮрЛицо;
	ЭтоПолныйИнтерфейс = СервисСверкиРасчетовПереопределяемый.ЭтоПолныйИнтерфейс();
	ОтобразитьРелевантныхКонтрагентовСервиса();
	
	Если СервисСверкиРасчетов.ЭтоИнтерфейсТакси() Тогда
		
		Элементы.ДекорацияКраткаяИнформацияОСервис.Ширина = 65;
		Элементы.ДекорацияПользовательскоеСоглашение.Ширина = 70;
		Элементы.ДекорацияНеобходимостьУведомлений.Ширина = 70;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "БизнесСеть_РегистрацияОрганизаций" Тогда
		УправлениеФормой(ЭтотОбъект);
		СтатусыОрганизаций = СтатусыРегистрацииОрганизацийВСервисе(Ложь);
		Если СтатусыОрганизаций.КоличествоПодключенных = 0 Тогда
			ОтключитьСервисНаКлиенте();
		ИначеЕсли ЭтотОбъект.СервисПодключен Тогда
			ВыполнитьДействияПоПодключениюСервисаВФоне();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ОбсужденияПодключены" Тогда 
		ОповещенияСверкиВключены = СервисСверкиРасчетовВызовСервера.ВключитьОповещениеСверки();
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = "ФормированиеОтчетаПоРасхождениям" Тогда
		ОтобразитьРезультатыСверки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("Подключаемый_ОтобразитьРезультатыСверки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы И Не ЭтотОбъект.ЭтоПолныйИнтерфейс Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияКраткаяИнформацияОСервисОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "open:Description" Тогда
		СтандартнаяОбработка = Ложь;
		СервисСверкиРасчетовКлиент.ОткрытьНавигационнуюСсылкуПодробнееОСервисе();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПользовательскоеСоглашениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "open:userAgreement" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("ОбщаяФорма.СервисСверкиРасчетовПользовательскоеСоглашение");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНеУдалосьПодключитьОрганизацииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("ОбщаяФорма.СервисСверкиРасчетовПодключенныеОрганизации");

КонецПроцедуры 

&НаКлиенте
Процедура ДекорацияОрганизацияЗарегистрированаРанееОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "НастройкиИнтернетПоддержки" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыВыполненияКоманды = Новый Структура;
		ПараметрыВыполненияКоманды.Вставить("Окно", Неопределено);
		ПараметрыВыполненияКоманды.Вставить("Источник", ЭтотОбъект);
		НастройкиПрограммыБИПКлиент.ОткрытьНастройкиИнтернетПоддержкаИСервисы(ПараметрыВыполненияКоманды);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКоличествоПодключенныхНажатие(Элемент)
	
	ОткрытьФорму("ОбщаяФорма.СервисСверкиРасчетовПодключенныеОрганизации");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНавигацияПоРасхождениямОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СервисСверкиРасчетовКлиент.ОбработатьПереходПользователяПоСсылкеСОтбормПоПериоду(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодключитьИнтернетПоддержку(Команда)
	
	ПодключитьИнтернетПоддержкуПользователей();
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьСервис(Команда)

	РегистрацияОрганизацийВСервисеВФоне();
	ОтобразитьРелевантныхКонтрагентовСервиса();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьСервис(Команда)
	
	ОтключитьСервисНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОбсуждения(Команда)
	
	Если НЕ ОбсужденияПодключены Тогда
		МодульОбсужденияКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбсужденияКлиент");
		МодульОбсужденияКлиент.ПоказатьПодключение();
	Иначе 
		ОповещенияСверкиВключены = СервисСверкиРасчетовВызовСервера.ВключитьОповещениеСверки();
		Если ОповещенияСверкиВключены Тогда
			УправлениеФормой(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьОбсуждения(Команда)
	
	ОповещенияСверкиВключены = НЕ СервисСверкиРасчетовВызовСервера.ВыключитьОповещениеСверки();
	Если НЕ ОповещенияСверкиВключены Тогда
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Форма.ОбсужденияПодключены     = СервисСверкиРасчетовВызовСервера.ОбсужденияПодключены();
	Форма.ОповещенияСверкиВключены = СервисСверкиРасчетовВызовСервера.ОповещенияСверкиВключены();
	
	// Интернет-поддержка
	ИнтернетПоддержкаПодключена = ИнтернетПоддержкаПодключена();

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ИнтернетПоддержка",
		"Видимость",
		Не ИнтернетПоддержкаПодключена);
		
	// Включение сервиса
	СтатусыОрганизаций = СтатусыРегистрацииОрганизацийВСервисе();
	НесколькоОрганизацийПодключены = Форма.ИспользоватьНесколькоОрганизаций И СтатусыОрганизаций.КоличествоПодключенных > 0;
	НесколькоОрганизацийНеПодключены = Форма.ИспользоватьНесколькоОрганизаций И СтатусыОрганизаций.КоличествоПодключенных = 0;
	ЕдинственнаяОрганизацияПодключена = Не Форма.ИспользоватьНесколькоОрганизаций И СтатусыОрганизаций.КоличествоПодключенных > 0;
	ЕдинственнаяОрганизацияНеПодключена = Не Форма.ИспользоватьНесколькоОрганизаций И СтатусыОрганизаций.КоличествоПодключенных = 0;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВключениеСервисаСверки",
		"Видимость",
		ИнтернетПоддержкаПодключена);
			
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаПодключитьСервис",
		"Видимость",
		Не Форма.СервисПодключен);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаОтключитьСервис",
		"Видимость",
		Форма.СервисПодключен);

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДекорацияНавигацияПоРасхождениямПростойИнтерфейс",
		"Видимость",
		Не Форма.ЭтоПолныйИнтерфейс);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДекорацияНавигацияПоРасхождениямПолныйИнтерфейс",
		"Видимость",
		Форма.ЭтоПолныйИнтерфейс);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДекорацияПодключенныеОрганизации",
		"Видимость",
		НесколькоОрганизацийПодключены); 
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДекорацияКоличествоПодключенных",
		"Видимость",
		НесколькоОрганизацийПодключены);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДекорацияОрганизацияПодключена",
		"Видимость",
		ЕдинственнаяОрганизацияПодключена);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДекорацияНеУдалосьПодключитьОрганизации",
		"Видимость",
		НесколькоОрганизацийНеПодключены);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДекорацияОрганизацияНеПодключена",
		"Видимость",
		ЕдинственнаяОрганизацияНеПодключена);
		
	Элементы.ДекорацияОрганизацияЗарегистрированаРанее.Видимость = Ложь;
	
	Если Форма.ИспользоватьНесколькоОрганизаций Тогда
		Элементы.ДекорацияКоличествоПодключенных.Заголовок = СтрШаблон(
			"%1 из %2",
			СтатусыОрганизаций.КоличествоПодключенных,
			СтатусыОрганизаций.Количество);
	Иначе
		Если СтатусыОрганизаций.КоличествоПодключенных > 0 Тогда
			Элементы.ДекорацияОрганизацияПодключена.Заголовок = 
				?(Форма.ЭтоЮрЛицо,
				СтрШаблон("Организация %1 подключена к сервису", Форма.НаименованиеОрганизации),
				СтрШаблон("%1 подключен к сервису", Форма.НаименованиеОрганизации)); 
		ИначеЕсли СтатусыОрганизаций.КоличествоПодключенных = 0
			И ОрганизацияЗарегистрированаПодИнымЛогиномИТС(Форма.ОрганизацияПоУмолчанию) Тогда
			Элементы.ДекорацияОрганизацияЗарегистрированаРанее.Видимость = Истина;
			Элементы.ДекорацияОрганизацияЗарегистрированаРанее.Заголовок = 
				"Обратитесь в службу поддержки по электронной почте sverka@1c.ru.";
			Элементы.ДекорацияОрганизацияНеПодключена.Заголовок =
				?(Форма.ЭтоЮрЛицо,
				СтрШаблон("Организация %1 уже зарегистрирована в сервисе под другой учетной записью пользователя ИТС.", Форма.НаименованиеОрганизации),
				СтрШаблон("%1 уже зарегистрирован в сервисе под другой учетной записью пользователя ИТС.", Форма.НаименованиеОрганизации));
		Иначе
			Элементы.ДекорацияОрганизацияНеПодключена.Заголовок =
				?(Форма.ЭтоЮрЛицо,
				СтрШаблон("Организация %1 не подключена к сервису.", Форма.НаименованиеОрганизации),
				СтрШаблон("%1 не подключен к сервису.", Форма.НаименованиеОрганизации));
		КонецЕсли;
	КонецЕсли;
		
	// Обсуждения
	МожноПодключитьОбсуждения = СервисСверкиРасчетовВызовСервера.МожноПодключитьОбсуждения()
		И ИнтернетПоддержкаПодключена;
		
	Элементы.ОбратитесьКАдминистратору.Видимость =
		МожноПодключитьОбсуждения
		И Не Форма.НастройкаОбсужденийДоступна
		И Не Форма.ОбсужденияПодключены;

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"УведомленияОРасхождениях",
		"Видимость",
		МожноПодключитьОбсуждения ИЛИ Форма.ОбсужденияПодключены);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"УведомленияОРасхождениях",
		"Доступность",
		Форма.СервисПодключен
		Или Форма.ОповещенияСверкиВключены);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаПодключитьУведомления",
		"Видимость",
		Не Форма.ОповещенияСверкиВключены);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаОтключитьУведомления",
		"Видимость",
		Форма.ОповещенияСверкиВключены);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПодключитьОбсуждения",
		"Видимость",
		Форма.НастройкаОбсужденийДоступна ИЛИ Форма.ОбсужденияПодключены И НЕ Форма.ОповещенияСверкиВключены);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ОтключитьОбсуждения",
		"Видимость",
		Форма.НастройкаОбсужденийДоступна ИЛИ Форма.ОповещенияСверкиВключены);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИнтернетПоддержкаПодключена()

	МодульИПП = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
	Возврат МодульИПП.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();

КонецФункции 

&НаКлиенте
Процедура ПодключитьИнтернетПоддержкуПользователей()

	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.БазоваяФункциональностьБИП") Тогда
		Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтернетПоддержкаПользователейКлиент");
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПодключитьИнтернетПоддержкуЗавершение", ЭтотОбъект);
		Модуль.ПодключитьИнтернетПоддержкуПользователей(ОповещениеОЗакрытии, ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодключитьИнтернетПоддержкуЗавершение(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция СтатусыРегистрацииОрганизацийВСервисе(ТолькоДоступные = Истина)
	
	Возврат СервисСверкиРасчетов.СтатусыРегистрацииОрганизацийВСервисе(ТолькоДоступные);
	
КонецФункции

&НаСервереБезКонтекста
Функция ОрганизацияЗарегистрированаПодИнымЛогиномИТС(ОрганизацияПоУмолчанию)
	
	Возврат СервисСверкиРасчетов.ОрганизацияЗарегистрированаПодИнымЛогиномИТС(ОрганизацияПоУмолчанию);
	
КонецФункции

&НаСервереБезКонтекста
Функция ИспользуетсяСервисСверкиРасчетов()
	
	Возврат СервисСверкиРасчетов.ИспользуетсяСервисСверкиРасчетов();
	
КонецФункции

&НаКлиенте
Процедура ОтключитьСервисНаКлиенте()
	
	УстановитьПризнакПодключенияСервисаНаСервере(Ложь);
	
	ОповещенияСверкиВключены = НЕ СервисСверкиРасчетовВызовСервера.ВыключитьОповещениеСверки();
	
	Если ЭтотОбъект.ЭтоПолныйИнтерфейс Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура УстановитьПризнакПодключенияСервисаНаСервере(СервисПодключен)
	
	УстановитьПризнакПодключенияСервиса(СервисПодключен, ЭтотОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьПризнакПодключенияСервиса(СервисПодключен, Форма)
	
	Элементы = Форма.Элементы;
	
	Форма.СервисПодключен = СервисПодключен;
	Константы.ИспользуетсяСервисСверкиРасчетов.Установить(СервисПодключен);
	Если Не Форма.СервисПодключен Тогда
		Элементы.РезультатыСверки.Видимость = Ложь;
		Элементы.ПодключенныеОрганизации.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияОрганизацийВСервисеВФоне()

	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	РегистрацияОрганизацийВСервисе = ЗарегистрироватьОрганизацииВСервисеВФоне(УникальныйИдентификатор, АдресХранилища);
	Если РегистрацияОрганизацийВСервисе.Статус = "Выполнено" Тогда
		ПослеЗавершенияРегистрацииОрганизацийВСервисеВФоне(РегистрацияОрганизацийВСервисе);
		Возврат;
	КонецЕсли;
	
	Элементы.ПодключенныеОрганизации.Видимость = Ложь;
	Элементы.ГруппаОбновление.Видимость = Истина;
	Если ЭтотОбъект.ИспользоватьНесколькоОрганизаций Тогда
		ТекстОрганизация = НСтр("ru = 'организаций';
								|en = 'companies'");
	Иначе
		ТекстОрганизация = НСтр("ru = 'организации';
								|en = 'companies'");
	КонецЕсли;
	Элементы.НадписьОбновление.Заголовок = СтрШаблон(НСтр("ru = 'Регистрация %1 в сервисе...';
															|en = '%1 registration in the service...'"), ТекстОрганизация);
	
	ОповещениеОЗавершении   = Новый ОписаниеОповещения("ПослеЗавершенияРегистрацииОрганизацийВСервисеВФоне", ЭтотОбъект);
	ПараметрыОжидания       = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РегистрацияОрганизацийВСервисе, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияРегистрацииОрганизацийВСервисеВФоне(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Элементы.ГруппаОбновление.Видимость = Ложь;
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	ПослеЗавершенияРегистрацииОрганизацийВСервисеВФонеФрагмент(Результат);
	
	Элементы.ПодключенныеОрганизации.ЦветФона = Новый Цвет;
	
	Если Не УспешнаяРегистрацияОрганизаций Тогда
		Элементы.ПодключенныеОрганизации.ЦветФона = Новый Цвет(251, 212, 212);
		Элементы.РезультатыВключенияСервиса.ЦветФона = Новый Цвет;
		Возврат;
	КонецЕсли;
	
	УстановитьПризнакПодключенияСервисаНаСервере(Истина);
	
	Если ЭтотОбъект.СервисПодключен Тогда
		ВыполнитьДействияПоПодключениюСервисаВФоне();
	КонецЕсли;

	Если ЭтотОбъект.ЭтоПолныйИнтерфейс Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗавершенияРегистрацииОрганизацийВСервисеВФонеФрагмент(Знач Результат)
	
	УспешнаяРегистрацияОрганизаций = Ложь;
	
	ТаблицаРезультатовРегистрации = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(ТаблицаРезультатовРегистрации) <> Тип("ТаблицаЗначений") Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоОшибокПодключения = 0;
	
	Для каждого СтрокаРегистрации Из ТаблицаРезультатовРегистрации Цикл
		Если ЗначениеЗаполнено(СтрокаРегистрации.ТекстОшибки) Тогда
			КоличествоОшибокПодключения = КоличествоОшибокПодключения + 1;
		КонецЕсли;
	КонецЦикла;
	
	СтатусыОрганизаций = СтатусыРегистрацииОрганизацийВСервисе(Ложь);
	УспешнаяРегистрацияОрганизаций = КоличествоОшибокПодключения < СтатусыОрганизаций.Количество;
	
	Элементы.ПодключенныеОрганизации.Видимость = Истина;
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗарегистрироватьОрганизацииВСервисеВФоне(УникальныйИдентификатор, АдресХранилища)
	
	НаименованиеЗадания = СтрШаблон("%1. Регистрация организаций в сервисе 1С:Бизнес-сеть", СервисСверкиРасчетов.ИмяСервиса());
	ИмяМетода           = "СервисСверкиРасчетов.ЗарегистрироватьОрганизацииВСервисе";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяМетода, АдресХранилища);
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьДействияПоПодключениюСервисаВФоне()
	
	РегистрацияДокументовКСверке = ЗарегистрироватьКСверкеРанееВведенныеДокументыВФоне(УникальныйИдентификатор);
	Если РегистрацияДокументовКСверке.Статус = "Выполнено" Тогда
		ПослеЗавершенияРегистрацииДокументовКСверке(РегистрацияДокументовКСверке);
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаОбновление.Видимость = Истина;
	Элементы.НадписьОбновление.Заголовок = НСтр("ru = 'Подготовка документов к сверке...';
												|en = 'Preparing documents for reconciliation...'");
	
	ОповещениеОЗавершении   = Новый ОписаниеОповещения("ПослеЗавершенияРегистрацииДокументовКСверке", ЭтотОбъект);
	ПараметрыОжидания       = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РегистрацияДокументовКСверке, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция АктуализироватьКэшДанныхКонтрагентовВФоне(УникальныйИдентификатор)
	
	СервисСверкиРасчетовПереопределяемый.СброситьКэшДанныхКонтрагентов();
	
	НаименованиеЗадания = СтрШаблон("%1. Актуализация данных о контрагентах", СервисСверкиРасчетов.ИмяСервиса());
	ИмяМетода           = "БизнесСеть.АктуализироватьКэшДанныхКонтрагентов";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяМетода);
	
КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияАктуализацииКэшаКонтрагентов(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Элементы.ГруппаОбновление.Видимость = Ложь;
	
	Если Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	ОбменССервисом = ОбменятьсяССервисомВФоне(УникальныйИдентификатор);
	
	Если ОбменССервисом.Статус = "Выполнено" Тогда
		ПослеЗавершенияОбменаССервисом(ОбменССервисом);
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаОбновление.Видимость = Истина;
	Элементы.НадписьОбновление.Заголовок = НСтр("ru = 'Сверка документов...';
												|en = 'Reconciling documents...'");
	
	ОповещениеОЗавершении   = Новый ОписаниеОповещения("ПослеЗавершенияОбменаССервисом", ЭтотОбъект);
	ПараметрыОжидания       = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ОбменССервисом, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗарегистрироватьКСверкеРанееВведенныеДокументыВФоне(УникальныйИдентификатор)
	
	НаименованиеЗадания = СтрШаблон("%1. Регистрация к сверке ранее введенных документов", СервисСверкиРасчетов.ИмяСервиса());
	ИмяМетода           = "СервисСверкиРасчетов.ЗарегистрироватьКСверкеРанееВведенныеДокументы";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяМетода);
	
КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияРегистрацииДокументовКСверке(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Элементы.ГруппаОбновление.Видимость = Ложь;
	
	Если Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	АктуализацияКэшаКонтрагентов = АктуализироватьКэшДанныхКонтрагентовВФоне(УникальныйИдентификатор);
	Если АктуализацияКэшаКонтрагентов.Статус = "Выполнено" Тогда
		ПослеЗавершенияАктуализацииКэшаКонтрагентов(АктуализацияКэшаКонтрагентов);
		Возврат
	КонецЕсли;
	
	Элементы.ГруппаОбновление.Видимость = Истина;
	Элементы.НадписьОбновление.Заголовок = НСтр("ru = 'Получаем информацию о контрагентах...';
												|en = 'Receiving information about counterparties...'");
	
	ОповещениеОЗавершении   = Новый ОписаниеОповещения("ПослеЗавершенияАктуализацииКэшаКонтрагентов", ЭтотОбъект);
	ПараметрыОжидания       = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(АктуализацияКэшаКонтрагентов, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбменятьсяССервисомВФоне(УникальныйИдентификатор)
	
	НаименованиеЗадания = СтрШаблон("%1. Обмен с сервисом", СервисСверкиРасчетов.ИмяСервиса());
	ИмяМетода           = "СервисСверкиРасчетов.ОбменятьсяССервисом";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания; 
	ПараметрыПроцедуры = Новый Структура("РегистрироватьДокументыПередОбменом", Ложь);
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяМетода);
	
КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияОбменаССервисом(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Элементы.ГруппаОбновление.Видимость = Ложь;
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	ОтобразитьРезультатыСверки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьРезультатыСверки()
	
	Если Не ЭтотОбъект.СервисПодключен Тогда
		Возврат;
	КонецЕсли;
	
	ПолучениеИтоговСверки = ПолучитьИтогиСверкиВФоне(УникальныйИдентификатор);
	
	Если ПолучениеИтоговСверки.Статус = "Выполнено" Тогда
		ПослеПолученияИтоговСверки(ПолучениеИтоговСверки);
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении   = Новый ОписаниеОповещения("ПослеПолученияИтоговСверки", ЭтотОбъект);
	ПараметрыОжидания       = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ПолучениеИтоговСверки, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьРезультатыСверкиНаСервере(АдресРезультата) 
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	КоличествоНеподключенныхКонтрагентов = Результат.КоличествоНеподключенныхКонтрагентов;
	КоличествоНеотправленныхДокументов   = Результат.КоличествоНеотправленныхДокументов;
	КоличествоОтправленныхДокументов     = Результат.КоличествоОтправленныхДокументов;
	КоличествоДокументовСРасхождениями   = Результат.КоличествоДокументовСРасхождениями;
	
	Элементы.РезультатыСверки.Видимость = Истина;
	Элементы.ПодключенныеОрганизации.Видимость = Истина;
	
	ТекстСверено = НСтр("ru = 'Сверено';
						|en = 'Reconciled'");
	Если Прав(Строка(КоличествоОтправленныхДокументов), 1) = "1"
		И Прав(Строка(КоличествоОтправленныхДокументов), 2) <> "11" Тогда
		ТекстСверено = Лев(ТекстСверено, 6);
	КонецЕсли;
	
	СвереноДокументов = ПолучитьСклоненияСтрокиПоЧислу("документ", КоличествоОтправленныхДокументов,,,"ПД=Винительный");
	ТекстСвереноДокументов = СтрШаблон(НСтр("ru = '%1 %2';
											|en = '%1 %2'"), ТекстСверено, СвереноДокументов[0]); 
	
	ТекстПодготовлен = НСтр("ru = 'подготовлены';
							|en = 'prepared'");
	Если Прав(Строка(КоличествоНеотправленныхДокументов), 1) = "1"
		И Прав(Строка(КоличествоНеотправленныхДокументов), 2) <> "11" Тогда
		ТекстПодготовлен = Лев(ТекстПодготовлен, 11);
	КонецЕсли;
	СклоненияКоличествоНеотправленныхДокументов = ПолучитьСклоненияСтрокиПоЧислу("документ", КоличествоНеотправленныхДокументов,,,"ПД=Винительный");
	СклоненияКоличествоНеподключенныхКонтрагентов = ПолучитьСклоненияСтрокиПоЧислу("контрагент", КоличествоНеподключенныхКонтрагентов,,,"ПД=Именительный");
	
	ТекстПодключен = НСтр("ru = 'подключены';
							|en = 'connected'");
	Если Прав(Строка(КоличествоНеподключенныхКонтрагентов), 1) = "1"
		И Прав(Строка(КоличествоНеподключенныхКонтрагентов), 2) <> "11" Тогда
		ТекстПодключен = Лев(ТекстПодключен, 9);
	КонецЕсли;
	
	Если КоличествоОтправленныхДокументов = 0 Тогда
		
		// Сверку произвести не удалсь, ни один контрагент не подключен к сервису
		Элементы.РезультатыСверки.ЦветФона = Новый Цвет(251, 237, 158);
		Если КоличествоНеотправленныхДокументов > 0 Тогда
			Элементы.ДекорацияРезультатСверки.Заголовок = СтрШаблон(НСтр("ru = 'Сверка не производилась.
				|К сверке %1 %2, но %3 еще не %4 к сервису.';
				|en = 'No reconciliation was performed.
				|To reconcile %1 %2, but %3 is not yet %4 to the service.'"),
				ТекстПодготовлен, СклоненияКоличествоНеотправленныхДокументов[0], СклоненияКоличествоНеподключенныхКонтрагентов[0], ТекстПодключен);
		Иначе
			// Нет документов для сверки
			Элементы.ДекорацияРезультатСверки.Заголовок = НСтр("ru = 'Сверка не производилась.
				|Отсутствуют документы для сверки.';
				|en = 'No reconciliation was performed.
				|There are no documents for reconciliation.'");
		КонецЕсли;
			
	ИначеЕсли КоличествоДокументовСРасхождениями > 0 Тогда
		
		// Сверка произведена, найдены расхождения
		Элементы.РезультатыСверки.ЦветФона = Новый Цвет(251, 237, 158);
		
		ОбнаруженоРасхождений = ПолучитьСклоненияСтрокиПоЧислу("расхождение", КоличествоДокументовСРасхождениями,,,"ПД=Винительный");
		Если КоличествоНеотправленныхДокументов > 0 Тогда
			Элементы.ДекорацияРезультатСверки.Заголовок = СтрШаблон(НСтр("ru = '%1: обнаружено %2.
				|К сверке %3 %4, но %5 еще не %6 к сервису.';
				|en = '%1: %2 found.
				|To reconcile %3 %4, but %5 is not yet %6 to the service.'"),
				ТекстСвереноДокументов, ОбнаруженоРасхождений[0],
				ТекстПодготовлен, СклоненияКоличествоНеотправленныхДокументов[0], СклоненияКоличествоНеподключенныхКонтрагентов[0], ТекстПодключен);
		Иначе
			Элементы.ДекорацияРезультатСверки.Заголовок = СтрШаблон(НСтр("ru = '%1: обнаружено %2.';
																		|en = '%1: %2 found.'"),
				ТекстСвереноДокументов, ОбнаруженоРасхождений[0]);
		КонецЕсли;
		
	Иначе
		
		// Сверка произведена, расхождений нет
		Элементы.РезультатыСверки.ЦветФона = ЦветаСтиля.СервисСверкиРасчетовЦветФонаБаннер;
		
		Если КоличествоНеотправленныхДокументов > 0 Тогда
			Элементы.ДекорацияРезультатСверки.Заголовок = СтрШаблон(НСтр("ru = '%1: расхождений не обнаружено.
				|К сверке %2 %3, но %4 еще не %5 к сервису.';
				|en = '%1: no discrepancies are found. 
				|To reconcile %2 %3, but %4 is not yet %5 to the service.'"),
				ТекстСвереноДокументов,
				ТекстПодготовлен, СклоненияКоличествоНеотправленныхДокументов[0], СклоненияКоличествоНеподключенныхКонтрагентов[0], ТекстПодключен);
		Иначе
			Элементы.ДекорацияРезультатСверки.Заголовок = СтрШаблон(НСтр("ru = '%1: расхождений не обнаружено.';
																		|en = '%1: discrepancies are not found.'"),
				ТекстСвереноДокументов);
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.РезультатыВключенияСервиса.ЦветФона = Элементы.РезультатыСверки.ЦветФона;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИтогиСверкиВФоне(УникальныйИдентификатор)
	
	НаименованиеЗадания = СтрШаблон("%1. Получение итогов сверки", СервисСверкиРасчетов.ИмяСервиса());
	ИмяМетода           = "СервисСверкиРасчетов.ИтогиСверки";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОтобразитьРезультатыСверки()
	
	ОтобразитьРезультатыСверки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияИтоговСверки(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	ОтобразитьРезультатыСверкиНаСервере(Результат.АдресРезультата);
	
КонецПроцедуры

&НаСервере
Функция НаименованиеКонтрагентаДляБаннера(Контрагент)
	
	НаименованиеКонтрагента = Строка(Контрагент);
	// Ограничим длину наименования контрагента 20 символами, чтобы поместилось в баннер.
	НаименованиеКонтрагента = ?(СтрДлина(НаименованиеКонтрагента) > 20,
		СтрШаблон("%1...", Лев(НаименованиеКонтрагента, 18)),
		НаименованиеКонтрагента);
		
	Возврат НаименованиеКонтрагента;
	
КонецФункции

&НаСервере
Процедура ОтобразитьРелевантныхКонтрагентовСервиса()
	
	Элементы.РелевантныеКонтрагентыСервиса.Видимость = Не СервисПодключен;
	
	Если СервисПодключен Тогда
		Возврат;
	КонецЕсли;
	
	КэшДанныхОРелевантныхКонтрагентах = СервисСверкиРасчетов.КэшДанныхОРелевантныхКонтрагентах();
	Если Не ЗначениеЗаполнено(КэшДанныхОРелевантныхКонтрагентах) Тогда
		Элементы.РелевантныеКонтрагентыСервиса.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	КартинкаЛоготипаПомощника = СервисСверкиРасчетовПереопределяемый.КартинкаЛоготипаПомощника();
	Элементы.РелевантныеКонтрагентыСервиса.ЦветФона = СервисСверкиРасчетов.ЦветФонаБаннеровСервисаСверкиРасчетов();
	Если ЗначениеЗаполнено(КартинкаЛоготипаПомощника) Тогда
		Элементы.ДекорацияПомощник.Видимость  = Истина;
		Элементы.ДекорацияПомощник.Картинка   = БиблиотекаКартинок[КартинкаЛоготипаПомощника];
	Иначе
		Элементы.ДекорацияПомощник.Видимость  = Ложь;
	КонецЕсли;
	
	РелевантныеКонтрагенты = КэшДанныхОРелевантныхКонтрагентах.РелевантныеКонтрагенты;
	КоличествоПодключенныхКонтрагентов = КэшДанныхОРелевантныхКонтрагентах.КоличествоПодключенныхКонтрагентов;
	
	ФорматнаяСтрока = "Л = ru_RU; ЧДЦ=0";
	
	ПредметКонтрагент = "контрагент,контрагента,контрагентов,м,,,,0";
	ПрописьЧислаКонтрагент = ЧислоПрописью(КоличествоПодключенныхКонтрагентов, ФорматнаяСтрока, ПредметКонтрагент);
	ПрописьЧислаЕщеКонтрагенты = ЧислоПрописью(КоличествоПодключенныхКонтрагентов - 3, ФорматнаяСтрока, ПредметКонтрагент);

	ИндексПредметаКонтрагент = СтрНайти(ПрописьЧислаКонтрагент, "контрагент");
	ИндексПредметаЕщеКонтрагенты = СтрНайти(ПрописьЧислаЕщеКонтрагенты, "контрагент");
	НадписьКонтрагент = Строка(Сред(ПрописьЧислаКонтрагент, ИндексПредметаКонтрагент, СтрДлина(ПрописьЧислаКонтрагент)- ИндексПредметаКонтрагент - 3));
	
	Если НадписьКонтрагент = "контрагент" Тогда
		ТекстСтроки = СтрШаблон("%1 ваш %2 уже включил сервис", Строка(КоличествоПодключенныхКонтрагентов), НадписьКонтрагент);
	Иначе
		ТекстСтроки = СтрШаблон("%1 ваших %2 уже включили сервис", Строка(КоличествоПодключенныхКонтрагентов), НадписьКонтрагент);
	КонецЕсли;
	
	ТекстЗаголовка = Новый ФорматированнаяСтрока(ТекстСтроки, ШрифтыСтиля.ОплатаСервисаКрупныйПолужирныйШрифт);
	
	КоличествоДокументовИтог = РелевантныеКонтрагенты.Итог("КоличествоДокументов");
	ОборотИтог = РелевантныеКонтрагенты.Итог("Оборот");
	
	ПредметДокумент = "документ,документа,документов,м,,,,0";
	ПрописьЧислаДокумент = ЧислоПрописью(КоличествоДокументовИтог, ФорматнаяСтрока, ПредметДокумент);
	ИндексПредметаДокумент = СтрНайти(ПрописьЧислаДокумент, "документ");
	
	ПредметРубль = "рубль,рубля,рублей,м,,,,0";
	ПрописьЧислаРубль = ЧислоПрописью(ОборотИтог, ФорматнаяСтрока, ПредметРубль);
	ИндексПредметаРубль = СтрНайти(ПрописьЧислаРубль, "рубл");
	
	Если Год(СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов()) = Год(ТекущаяДата()) Тогда
		ПериодСверкиДокументов = Формат(Год(СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов()), "ЧДЦ=0; ЧН=0; ЧГ=;");
	Иначе
		ПериодСверкиДокументов = СтрШаблон("%1-%2",
			Формат(Год(СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов()), "ЧДЦ=0; ЧН=0; ЧГ=;"),
			Формат(Год(ТекущаяДата()), "ЧДЦ=0; ЧН=0; ЧГ=;"));
	КонецЕсли;
	
	ТекстПодзаголовка = Новый ФорматированнаяСтрока(
		СтрШаблон("Можно сверить %1", Строка(КоличествоДокументовИтог)),
		" ",
		Строка(Сред(ПрописьЧислаДокумент, ИндексПредметаДокумент, СтрДлина(ПрописьЧислаДокумент)- ИндексПредметаДокумент - 3)),
		" ",
		СтрШаблон("за %1 год на сумму %2", ПериодСверкиДокументов, Строка(ОборотИтог)),
		" ",
		Строка(Сред(ПрописьЧислаРубль, ИндексПредметаРубль, СтрДлина(ПрописьЧислаРубль)- ИндексПредметаРубль - 3)));
		
	Элементы.ОбъемДокументов.Заголовок = Новый ФорматированнаяСтрока(
		ТекстЗаголовка,
		Символы.ПС,
		ТекстПодзаголовка);
		
	КонтрагентыСтрока = "";
	КоличествоДокументовСтрока = "";
	ОборотСтрока = "";
	КоличествоКонтрагентовЕще = 0;
	КоличествоДокументовЕще = 0;
	ОборотыЕще = 0;
	
	Для НомерСтроки = 0 По РелевантныеКонтрагенты.Количество() - 1 Цикл
		
		Если НомерСтроки < 3 Тогда
			Если ЗначениеЗаполнено(КонтрагентыСтрока) Тогда
				КонтрагентыСтрока = Новый ФорматированнаяСтрока(
					КонтрагентыСтрока,
					Символы.ПС,
					НаименованиеКонтрагентаДляБаннера(РелевантныеКонтрагенты[НомерСтроки].Контрагент));
				КоличествоДокументовСтрока = Новый ФорматированнаяСтрока(
					КоличествоДокументовСтрока,
					Символы.ПС,
					Строка(РелевантныеКонтрагенты[НомерСтроки].КоличествоДокументов));
				ОборотСтрока = Новый ФорматированнаяСтрока(
					ОборотСтрока,
					Символы.ПС,
					Строка(РелевантныеКонтрагенты[НомерСтроки].Оборот));
			Иначе
				КонтрагентыСтрока          = НаименованиеКонтрагентаДляБаннера(РелевантныеКонтрагенты[НомерСтроки].Контрагент);
				КоличествоДокументовСтрока = Строка(РелевантныеКонтрагенты[НомерСтроки].КоличествоДокументов);
				ОборотСтрока               = Строка(РелевантныеКонтрагенты[НомерСтроки].Оборот);
			КонецЕсли;
		Иначе
			КоличествоКонтрагентовЕще = КоличествоКонтрагентовЕще + 1;
			КоличествоДокументовЕще   = КоличествоДокументовЕще + РелевантныеКонтрагенты[НомерСтроки].КоличествоДокументов;
			ОборотыЕще                = ОборотыЕще + РелевантныеКонтрагенты[НомерСтроки].Оборот;
		КонецЕсли;
		
	КонецЦикла;

	Элементы.Контрагент.Заголовок = КонтрагентыСтрока;
	Элементы.КоличествоДокументов.Заголовок = КоличествоДокументовСтрока;
	Элементы.Обороты.Заголовок = ОборотСтрока;
	
	Элементы.КонтрагентЕще.Заголовок = СтрШаблон("и еще %1 %2",
		Строка(КоличествоПодключенныхКонтрагентов - 3),
		Сред(ПрописьЧислаЕщеКонтрагенты, ИндексПредметаЕщеКонтрагенты, СтрДлина(ПрописьЧислаЕщеКонтрагенты)- ИндексПредметаЕщеКонтрагенты - 3));
	Элементы.КоличествоДокументовЕще.Заголовок = Строка(КоличествоДокументовЕще);
	Элементы.ОборотыЕще.Заголовок = Строка(ОборотыЕще);
	
	Элементы.КонтрагентЕще.Видимость = ЗначениеЗаполнено(КоличествоКонтрагентовЕще);
	Элементы.КонтрагентОтступ.Видимость = ЗначениеЗаполнено(КоличествоКонтрагентовЕще);
	
	Элементы.КоличествоДокументовЕще.Видимость = ЗначениеЗаполнено(КоличествоКонтрагентовЕще);
	Элементы.КоличествоДокументовОтступ.Видимость = ЗначениеЗаполнено(КоличествоКонтрагентовЕще);
	
	Элементы.ОборотыЕще.Видимость = ЗначениеЗаполнено(КоличествоКонтрагентовЕще);
	Элементы.ОборотыОтступ.Видимость = ЗначениеЗаполнено(КоличествоКонтрагентовЕще);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастроитьОповещенияНажатие(Элемент)
	
	КлючГлобальнойНастройкиОповещений = СервисСверкиРасчетовВызовСервера.КлючГлобальнойНастройкиОповещений();
	Если КлючГлобальнойНастройкиОповещений <> Неопределено Тогда
		ОткрытьФорму("Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей.ФормаОбъекта", 
		Новый Структура("Ключ, ПерсональнаяНастройка", КлючГлобальнойНастройкиОповещений, Ложь));
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти
