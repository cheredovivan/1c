&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Организации = ЭлектронныйДокументооборотСФСС.ОрганизацииИспользующиеОбменФСС();
	
	Если Организации.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РезультатАнализа = ЭлектронныйДокументооборотСФСС.ПроанализироватьОрганизацииПоОбменуСЭДО(Организации);
	
	Для каждого Организация Из РезультатАнализа.ОрганизацииОбменНапрямую Цикл
		Если НЕ РезультатАнализа.ОрганизацииСЗаявлениями.Найти(Организация) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Строка = ТаблицаОрганизаций.Добавить();
		Строка.Организация = Организация;
		Строка.ТекущееСостояние = НСтр("ru = '1С-Отчетность не подключена';
										|en = '1С-Отчетность не подключена'");
		Строка.Ссылка = ИмяСсылкиПодключитьТарифСФРБесплатно();
	КонецЦикла;
	
	Для каждого Организация Из РезультатАнализа.ОрганизацииБезНаправленияФСС Цикл
		Если НЕ РезультатАнализа.ОрганизацииСЗаявлениями.Найти(Организация) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Строка = ТаблицаОрганизаций.Добавить();
		Строка.Организация = Организация;
		Строка.ТекущееСостояние = НСтр("ru = '1С-Отчетность подключена без направления СФР';
										|en = '1С-Отчетность подключена без направления СФР'");
		Строка.Ссылка = ИмяСсылкиДобавитьНаправлениеСФР();
	КонецЦикла;
	
	Для каждого ЗаявлениеОрганизация Из РезультатАнализа.ЗаявленияОрганизаций Цикл
		ДанныеЗаявления = ЭлектронныйДокументооборотСФССВызовСервера.ДанныеЗаявленияНаПодключение1СО(ЗаявлениеОрганизация.Заявление);
		Если ДанныеЗаявления.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Одобрено") Тогда
			Продолжить;
		КонецЕсли;
		Строка = ТаблицаОрганизаций.Добавить();
		Строка.Организация = ЗаявлениеОрганизация.Организация;
		Строка.ТекущееСостояние = НСтр("ru = 'Заявление на подключение отправлено';
										|en = 'Заявление на подключение отправлено'");
		Строка.Ссылка = ИмяСсылкиЕстьОтправленноеЗаявление();
	КонецЦикла;
	
	// Вывод баннера с текстом.
	Организации = Неопределено;
	ПараметрыПанели = ЭлектронныйДокументооборотСФСС.НовыеПараметрыПанелиРекомендацийПоПереходуНаОператораСЭДО();
	ПараметрыПанели.ВыводитьДействия = Ложь;
	ПараметрыПанели.ВыводитьСсылкуЗакрытия = Ложь;
	ПараметрыПанели.ОтобразитьИконку = Ложь;
	
	ПараметрыПанели.ЗаголовокПанели = "";
	ТекстПанели = НСтр("ru = 'Организации из списка пока еще не подключены к обмену с СЭДО СФР через 1С-Отчетность.';
						|en = 'Организации из списка пока еще не подключены к обмену с СЭДО СФР через 1С-Отчетность.'")
		+ Символы.ПС + НСтр("ru = 'Для подключения выполните рекомендации ниже.';
							|en = 'Для подключения выполните рекомендации ниже.'");
	ПараметрыПанели.ТекстПанели = СтроковыеФункции.ФорматированнаяСтрока(ТекстПанели);
	
	Панель = ЭлектронныйДокументооборотСФСС.ДобавитьПанельПереходаНаОбменЧерезОператора(
		ЭтотОбъект, Организации, , Элементы.ИнформационнаяПанельБРО, ПараметрыПанели);
	
	Если НЕ Панель = Неопределено Тогда
		Элементы.ИнформационнаяПанельБРО.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НадписьПереключениеНаОператораСЭДООбработкаНавигационнойСсылки(
			Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
			
	ЭлектронныйДокументооборотСФССКлиент.ПереключениеНаОператораСЭДООбработкаНавигационнойСсылки(
		ЭтотОбъект, Элемент, НавигационнаяСсылкаФорматированнойСтроки);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяСсылкиПодключитьТарифСФРБесплатно()
	
	Возврат НСтр("ru = 'Подключить 1С-Отчетность';
				|en = 'Подключить 1С-Отчетность'");
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяСсылкиДобавитьНаправлениеСФР()
	
	Возврат НСтр("ru = 'Добавить направление СФР в 1С-Отчетности';
				|en = 'Добавить направление СФР в 1С-Отчетности'");
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяСсылкиЗаявлениеНаМультирежим()
	
	Возврат НСтр("ru = 'Отправьте заявление на подключение многопользовательского режима';
				|en = 'Отправьте заявление на подключение многопользовательского режима'");
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяСсылкиЕстьОтправленноеЗаявление()
	
	Возврат НСтр("ru = 'Заявление на подключение 1С-Отчетности отправлено, ожидайте';
				|en = 'Заявление на подключение 1С-Отчетности отправлено, ожидайте'");;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяСсылкиЗявлениеПринято()
	
	Возврат НСтр("ru = 'Подключение завершено';
				|en = 'Подключение завершено'");;
	
КонецФункции

&НаКлиенте
Процедура ТаблицаОрганизацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Если ТекущиеДанные.Ссылка = ИмяСсылкиПодключитьТарифСФРБесплатно() Тогда
		ЭлектронныйДокументооборотСФССКлиент.ОткрытьФормуПередПереключениемНаОператораСЭДО(
			ЭтотОбъект, ТекущиеДанные.Организация, Ложь);
	ИначеЕсли ТекущиеДанные.Ссылка = ИмяСсылкиДобавитьНаправлениеСФР()
			ИЛИ ТекущиеДанные.Ссылка = ИмяСсылкиЗаявлениеНаМультирежим() Тогда
		ЭлектронныйДокументооборотСФССКлиент.ОткрытьЗаявлениеНаДобавлениеНаправленияСФР(ТекущиеДанные.Организация);
	ИначеЕсли ТекущиеДанные.Ссылка = ИмяСсылкиЕстьОтправленноеЗаявление()
		ИЛИ ТекущиеДанные.Ссылка = ИмяСсылкиЗявлениеПринято() Тогда
		ЭлектронныйДокументооборотСФССКлиент.ОткрытьСуществующегоЗаявлениеНаПодключение1СОтчетности(ТекущиеДанные.Организация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ЭлектронныйДокументооборотСФССКлиент.ИмяСобытияУспешнойОтправкиЗаявленияНаПодключение()
		ИЛИ ИмяСобытия = ЭлектронныйДокументооборотСФССКлиент.ИмяСобытияУспешнойОбработкиЗаявленияНаПодключение()
		И ТипЗнч(Источник) = Тип("ДокументСсылка.ЗаявлениеАбонентаСпецоператораСвязи") Тогда
		
		Организация = ЭлектронныйДокументооборотСФССВызовСервера.ОрганизацияЗаявленияНаПодключение1СО(Источник);
		
		Для каждого Строка Из ТаблицаОрганизаций Цикл
			Если Строка.Организация = Организация Тогда
				Если ИмяСобытия = ЭлектронныйДокументооборотСФССКлиент.ИмяСобытияУспешнойОбработкиЗаявленияНаПодключение() тогда
					Если Параметр.Свойство("РеквизитыДокумента")
						И Параметр.РеквизитыДокумента.Свойство("Статус")
						И Параметр.РеквизитыДокумента.Статус
							= ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Одобрено") Тогда
						Строка.ТекущееСостояние = НСтр("ru = 'Подключение завершено';
														|en = 'Подключение завершено'");
						Строка.Ссылка = ИмяСсылкиЗявлениеПринято();
					КонецЕсли;
				Иначе
					Строка.ТекущееСостояние = НСтр("ru = 'Заявление на подключение отправлено';
													|en = 'Заявление на подключение отправлено'");
					Строка.Ссылка = ИмяСсылкиЕстьОтправленноеЗаявление();
				КонецЕсли;
				Строка.Заявление = Источник;
			КонецЕсли;
		КонецЦикла;
		
		Элементы.ТаблицаОрганизаций.Обновить();
		
		ЭлектронныйДокументооборотСФССКлиент.ОбработатьОповещенияПереходаНаОператораСЭДО(ЭтотОбъект,
				ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	
КонецПроцедуры

