#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РедактируемыйДокументСсылка = Параметры.РедактируемыйДокументСсылка;
	ДатаРедактируемогоДокумента = Параметры.ДатаРедактируемогоДокумента;
	Организация = Параметры.Организация;
	ФизическоеЛицо = Параметры.ФизическоеЛицо;
	
	ДанныеИзВременногоХранилищаВДанныеФормы(Параметры.АдресВоВременномХранилище);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеДанныхФизическогоЛица" И Не ТолькоПросмотр Тогда 
		ЗаполнитьИзменившиесяДанныеФизическогоЛица(Параметр);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЛистокСообщенияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ЛистокСообщения) Тогда 
		ЛистокСообщенияПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РедактироватьДанныеФизическогоЛица(Команда)
	
	ПараметрыФормы = Новый Структура("Ключ, АктивнаяСтраница, РедактируемоеПоле", ФизическоеЛицо, "ЛичныеДанные");
	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ВыбратьИЗакрыть();	
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДанныеИзВременногоХранилищаВДанныеФормы(АдресВоВременномХранилище)
	
	Модифицированность = Ложь;
	
	ДанныеДокументаПоСотруднику = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	
	Если ДанныеДокументаПоСотруднику = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Не удалось получить редактируемые данные.';
								|en = 'Не удалось получить редактируемые данные.'");	
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, ДанныеДокументаПоСотруднику);
	
	ДанныеСотрудника = НовыеДанныеСотрудника();
	Для Каждого КлючИЗначение Из ДанныеСотрудника Цикл 
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
			КлючИЗначение.Ключ, "Видимость", ДанныеДокументаПоСотруднику.Свойство(КлючИЗначение.Ключ));
	КонецЦикла;
	
	Если ДанныеДокументаПоСотруднику.Свойство("ЛистокСообщения") Тогда 
		УстановитьПараметрыВыбораЛисткаСообщения();
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ВыбратьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ДанныеСотрудника = НовыеДанныеСотрудника();
	ЗаполнитьЗначенияСвойств(ДанныеСотрудника, ЭтаФорма);
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДанныеСотрудника);
	
	ПараметрыОповещения = Новый Структура("РедактируемыйДокументСсылка, ФизическоеЛицо, АдресВоВременномХранилище");
	ПараметрыОповещения.РедактируемыйДокументСсылка = РедактируемыйДокументСсылка;
	ПараметрыОповещения.ФизическоеЛицо = ФизическоеЛицо;
	ПараметрыОповещения.АдресВоВременномХранилище = АдресВоВременномХранилище;	
	
	Оповестить("ИзменениеЛичныхДанныхДляВоенкомата", ПараметрыОповещения, ЭтаФорма);
	
	Модифицированность = Ложь;

	Закрыть();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НовыеДанныеСотрудника()
	
	ДанныеСотрудника = Новый Структура(
		"Фамилия,
		|Имя,
		|Отчество,
		|ДатаРождения,
		|Пол,
		|СтраховойНомерСФР,
		|МестоРождения,
		|Образование,
		|СемейноеПоложение,
		|СерияПаспорта,
		|НомерПаспорта,
		|ДатаВыдачиПаспорта,
		|Военкомат,
		|Звание,
		|Состав,
		|КатегорияЗапаса,
		|Годность,
		|ВУС,
		|НомерКомандыПартии,
		|Забронирован,
		|ДатаСобытия,
		|ВидСобытия,
		|НомерПриказа,
		|ДатаПриказа,
		|НомерТрудовогоДоговора,
		|ДатаТрудовогоДоговора,
		|Должность,
		|Подразделение,
		|Категория,
		|АдресРегистрацииПоМестуЖительства,
		|ДатаРегистрацииПоМестуЖительства,
		|АдресРегистрацииПоМестуПребывания,
		|ДатаРегистрацииПоМестуПребывания,
		|АдресФактическогоПроживания,
		|ДатаФактическогоПроживания,
		|НомерТелефона,
		|ЛистокСообщения,
		|ВидИзменений,
		|СодержаниеИзменений"); 
	
	Возврат ДанныеСотрудника;

КонецФункции

&НаСервере
Процедура ЗаполнитьИзменившиесяДанныеФизическогоЛица(ДанныеФизическогоЛица)	
	
	ОсновныеСотрудники = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(ФизическоеЛицо, Истина, Организация, ДатаРедактируемогоДокумента);
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(РедактируемыйДокументСсылка);
	КадровыеДанныеФизическихЛиц = МенеджерОбъекта.КадровыеДанныеСотрудников(ОсновныеСотрудники);
	
	Если КадровыеДанныеФизическихЛиц.Количество() > 0 Тогда 
		МенеджерОбъекта.ЗаполнитьДанныеСтрокиСотрудника(ЭтаФорма, КадровыеДанныеФизическихЛиц[0], ДатаРедактируемогоДокумента);		
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры	

&НаСервере
Процедура УстановитьПараметрыВыбораЛисткаСообщения()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЛистокСообщенияДляВоенкомата.Ссылка КАК ЛистокСообщения
		|ИЗ
		|	Документ.ЛистокСообщенияДляВоенкомата КАК ЛистокСообщенияДляВоенкомата
		|ГДЕ
		|	ЛистокСообщенияДляВоенкомата.Организация = &Организация
		|	И ЛистокСообщенияДляВоенкомата.Проведен = ИСТИНА
		|	И ЛистокСообщенияДляВоенкомата.Сотрудник.ФизическоеЛицо = &ФизическоеЛицо";
	
	ЛисткиСообщения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЛистокСообщения");
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(ЛисткиСообщения)));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ЛистокСообщения", "ПараметрыВыбора", Новый ФиксированныйМассив(ПараметрыВыбора));
	
КонецПроцедуры

&НаСервере
Процедура ЛистокСообщенияПриИзмененииНаСервере()
	
	ДанныеЛисткаСообщения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЛистокСообщения, "ВидИзменений, ОписаниеИзменений", Истина);
	ВидИзменений = ДанныеЛисткаСообщения.ВидИзменений;
	СодержаниеИзменений = ДанныеЛисткаСообщения.ОписаниеИзменений;
	
КонецПроцедуры

#КонецОбласти

