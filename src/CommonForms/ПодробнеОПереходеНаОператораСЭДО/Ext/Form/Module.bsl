&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	РезультатАнализа = Параметры.РезультатАнализа;
	Если РезультатАнализа = Неопределено Тогда
		РезультатАнализа = ЭлектронныйДокументооборотСФСС.ПроанализироватьОрганизацииПоОбменуСЭДО();
	КонецЕсли;
	
	ОднаОрганизация = РезультатАнализа.ОднаОрганизация;
	Если НЕ ЗначениеЗаполнено(Организация) И ОднаОрганизация Тогда
		Организация = РезультатАнализа.ОрганизацииДляПереходаНаОператора[0];
	КонецЕсли;
	
	Элементы.ГруппаКнопок.Видимость = НЕ Параметры.НеВыводитьДействия;
	
	ЗаголовокПрямойОбмен = НСтр("ru = 'Для подключения 1С-Отчетности доступен промо-тариф <a href = ""https://1c-report.ru/stocks/promo-kadrovye-resheniya/"">""СФР бесплатно""</a> на 3 месяца и другие <a href = ""https://1c-report.ru/stocks/"">акции</a>.';
								|en = 'Для подключения 1С-Отчетности доступен промо-тариф <a href = ""https://1c-report.ru/stocks/promo-kadrovye-resheniya/"">""СФР бесплатно""</a> на 3 месяца и другие <a href = ""https://1c-report.ru/stocks/"">акции</a>.'")
		+ Символы.ПС + НСтр("ru = 'После завершения действия промо-тарифа вы можете воспользоваться скидкой 50% при подключении тарифа ""Кадровые решения"" или выбрать другой <a href = ""https://1c-report.ru/tarifs/#0"">тариф</a>.';
							|en = 'После завершения действия промо-тарифа вы можете воспользоваться скидкой 50% при подключении тарифа ""Кадровые решения"" или выбрать другой <a href = ""https://1c-report.ru/tarifs/#0"">тариф</a>.'")
		+ Символы.ПС + НСтр("ru = 'Также в рамках договора 1С:ИТС ПРОФ подключение 1С-Отчетности по одной организации производится без дополнительной платы.';
							|en = 'Также в рамках договора 1С:ИТС ПРОФ подключение 1С-Отчетности по одной организации производится без дополнительной платы.'");
	ЗаголовокБезНаправленияФССКомбо = НСтр("ru = 'Если организация уже использует 1С-Отчетность, но без направления СФР, отправьте заявление на изменение настроек, в котором подключите направление.';
											|en = 'Если организация уже использует 1С-Отчетность, но без направления СФР, отправьте заявление на изменение настроек, в котором подключите направление.'");
	ЗаголовокБезНаправленияФСС = НСтр("ru = 'Для подключения направления СФР необходимо отправить заявление на изменения настроек, в котором указать это направление.';
										|en = 'Для подключения направления СФР необходимо отправить заявление на изменения настроек, в котором указать это направление.'");
	
	ВидимостьОтправитьЗаявление = Ложь;
	ВидимостьПодключитьТариф = Ложь;
	ВидимостьНачатьПодключение = Истина;
	КнопкаПоУмолчаниюНачатьПодключение = Истина;
	
	Декорация3Заголовок = "";
	Декорация4Заголовок = "";
	Если РезультатАнализа.ЕстьОрганизацииБезНаправленияФСС
		И РезультатАнализа.ЕстьОрганизацииОбменНапрямую Тогда
		// Несколько организаций, с разными вариантами подключения.
		Декорация3Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокПрямойОбмен);
		Декорация4Заголовок = ЗаголовокБезНаправленияФССКомбо;
	ИначеЕсли ОднаОрганизация И РезультатАнализа.ЕстьОрганизацииОбменНапрямую Тогда
		// Одна организация без 1С-Отчетности.
		Декорация3Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокПрямойОбмен);
		ВидимостьОтправитьЗаявление = Истина;
		ВидимостьПодключитьТариф = Истина;
		ВидимостьНачатьПодключение = Ложь;
		КнопкаПоУмолчаниюНачатьПодключение = Ложь;
	ИначеЕсли РезультатАнализа.ЕстьОрганизацииОбменНапрямую Тогда
		// Прямой обмен без 1С-Отчетности.
		Декорация3Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокПрямойОбмен);
	Иначе
		// Одна или несколько организаций без направления СФР.
		Декорация3Заголовок = СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокБезНаправленияФСС);
	КонецЕсли;
	
	Элементы.Декорация3.Заголовок = Декорация3Заголовок;
	Если НЕ ПустаяСтрока(Декорация4Заголовок) Тогда
		Элементы.Декорация4.Заголовок = Декорация4Заголовок;
	Иначе
		Элементы.Декорация4.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ОтправитьЗаявлениеСФРБесплатно.Видимость = ВидимостьОтправитьЗаявление;
	Элементы.ПодключитьСДругимТарифом.Видимость = ВидимостьПодключитьТариф;
	Элементы.НачатьПодключение.Видимость = ВидимостьНачатьПодключение;
	Если ВидимостьНачатьПодключение Тогда
		Элементы.НачатьПодключение.КнопкаПоУмолчанию = КнопкаПоУмолчаниюНачатьПодключение;
	Иначе
		Элементы.НачатьПодключение.КнопкаПоУмолчанию = Ложь;
	КонецЕсли;
	
	Элементы.ГруппаКнопок.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗаявлениеСФРБесплатно(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОтправитьЗаявлениеПослеЗакрытияЗаявления", ЭтотОбъект);
	ЭлектронныйДокументооборотСФССКлиент.ОткрытьЗаявлениеСТарифомОператораЭДО(Организация, ЭтотОбъект.ВладелецФормы, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьСДругимТарифом(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОтправитьЗаявлениеПослеЗакрытияЗаявления", ЭтотОбъект);
	ЭлектронныйДокументооборотСФССКлиент.ОткрытьЗаявлениеНаПодключение1СОтчетности(Организация, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПодключение(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОтправитьЗаявлениеПослеЗакрытияЗаявления", ЭтотОбъект);
	ОднаОрганизация = РезультатАнализа.ОднаОрганизация;
	Если НЕ ОднаОрганизация Тогда
		ЭлектронныйДокументооборотСФССКлиент.ОткрытьФормуПереключенияОрганизацийНаОператораСЭДО(ЭтотОбъект.ВладелецФормы);
		Закрыть();
	Иначе
		// Тут может быть случай только когда нет направления СФР.
		ЭлектронныйДокументооборотСФССКлиент.ОткрытьЗаявлениеНаДобавлениеНаправленияСФР(Организация, Оповещение);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ОтправитьЗаявлениеПослеЗакрытияЗаявления(Результат, ДополнительныеПараметры) Экспорт
	
	СделатьАнализИПодключитьДругиеОрганизацииПриНеобходимости(Результат);
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура СделатьАнализИПодключитьДругиеОрганизацииПриНеобходимости(Результат)
	
	Если НЕ ПредлагатьПодключатьДругиеОрганизации Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат = Неопределено
		ИЛИ НЕ ТипЗнч(Результат) = Тип("Структура") 
		ИЛИ НЕ Результат.Свойство("ОтправленоУспешно")
		ИЛИ НЕ Результат.ОтправленоУспешно Тогда
		Возврат;
	КонецЕсли;
	
	ИсключитьОрганизации = Неопределено;
	Если ЗначениеЗаполнено(Организация) Тогда
		ИсключитьОрганизации = Новый Массив;
		ИсключитьОрганизации.Добавить(Организация);
	КонецЕсли;
	РезультатАнализа = ЭлектронныйДокументооборотСФССВызовСервера.ПроанализироватьОрганизацииПоОбменуСЭДО(, ИсключитьОрганизации);
	Организации = РезультатАнализа.ОрганизацииДляПереходаНаОператора;
	КоличествоОрганизаций = Организации.Количество();
	Если КоличествоОрганизаций > 0 Тогда
		Если КоличествоОрганизаций = 1 Тогда
			Если РезультатАнализа.ЕстьОрганизацииОбменНапрямую Тогда
				Шаблон = НСтр("ru = 'Для работы с СЭДО через оператора подключить 1С-Отчетность для организации %1?';
								|en = 'Для работы с СЭДО через оператора подключить 1С-Отчетность для организации %1?'");
			Иначе
				Шаблон = НСтр("ru = 'Для работы с СЭДО через оператора подключить направление СФР для организации %1?';
								|en = 'Для работы с СЭДО через оператора подключить направление СФР для организации %1?'");
			КонецЕсли;
			ТекстВопроса = СтрШаблон(Шаблон, Организации[0]);
		Иначе
			Если РезультатАнализа.ЕстьОрганизацииОбменНапрямую
				И РезультатАнализа.ЕстьОрганизацииБезНаправленияФСС Тогда
				ТекстВопроса = НСтр("ru = 'Для работы с СЭДО через оператора подключить 1С-Отчетность или направление СФР для других организаций?';
									|en = 'Для работы с СЭДО через оператора подключить 1С-Отчетность или направление СФР для других организаций?'");
			ИначеЕсли РезультатАнализа.ЕстьОрганизацииОбменНапрямую Тогда
				ТекстВопроса = НСтр("ru = 'Для работы с СЭДО через оператора подключить направление СФР в 1С-Отчетности для других организаций?';
									|en = 'Для работы с СЭДО через оператора подключить направление СФР в 1С-Отчетности для других организаций?'");
			Иначе
				ТекстВопроса = НСтр("ru = 'Для работы с СЭДО через оператора подключить 1С-Отчетность для других организаций?';
									|en = 'Для работы с СЭДО через оператора подключить 1С-Отчетность для других организаций?'");
			КонецЕсли;
		КонецЕсли;
		ОтветНаВопрос = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0, КодВозвратаДиалога.Да);
		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			Если КоличествоОрганизаций = 1 Тогда
				Организация = Организации[0];
				Если НЕ РезультатАнализа.ОрганизацииОбменНапрямую.Найти(Организация) = Неопределено Тогда
					ЭлектронныйДокументооборотСФССКлиент.ОткрытьЗаявлениеСТарифомОператораЭДО(Организация, ЭтотОбъект.ВладелецФормы);
				Иначе
					ЭлектронныйДокументооборотСФССКлиент.ОткрытьЗаявлениеНаДобавлениеНаправленияСФР(Организация);
				КонецЕсли;
			Иначе
				ЭлектронныйДокументооборотСФССКлиент.ОткрытьФормуПереключенияОрганизацийНаОператораСЭДО(ЭтотОбъект.ВладелецФормы);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
