////////////////////////////////////////////////////////////////////////////////
//
// Параметры формы
// 		МасштабФормы 			- Число - масштаб формы.
//		УникальностьФормы		- УИД формы, из которой вызывается данная форма.
//

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
	    Возврат;
	КонецЕсли;
	
	МасштабФормыИзПараметров = Параметры.Масштаб;
	
	Если РегламентированнаяОтчетностьАЛКОКлиентСервер.ЭтоИнтерфейс_8_5() Тогда
	
		Элементы.МасштабФормы.МинимальноеЗначение = 60;
		Элементы.МасштабФормы.МаксимальноеЗначение = 200;
		Элементы.МасштабФормы.Шаг = 1;
		Элементы.МасштабФормы.БольшойШаг = 5;
		Элементы.МасштабФормы.ШагРазметки = 10;
		
		// Для случая интерфейса 8.5 масштабируемая форма будет масштабироваться через
		// процедуру масштабирования, скорость работы будет зависеть от качества соединения 
		// с Сервером, по умолчанию отключаем интерактивность.
		Элементы.УстановитьМасштаб.Видимость = Истина;
		Интерактивно = Ложь;
		
	Иначе
		
		Элементы.МасштабФормы.МинимальноеЗначение = 40;
		Элементы.МасштабФормы.МаксимальноеЗначение = 400;
		Элементы.МасштабФормы.Шаг = 1;
		Элементы.МасштабФормы.БольшойШаг = 5;
		Элементы.МасштабФормы.ШагРазметки = 20;
		
		// Для случая Такси масштабируемая форма будет масштабироваться через
		// свойство Масштаб Формы, это работает очень быстро, 
		// нет смысла отключать интерактивность.
		Элементы.УстановитьМасштаб.Видимость = Ложь;
		Интерактивно = Истина;
		
	КонецЕсли;
		
	МасштабФормыИзПараметров = ?(МасштабФормыИзПараметров < Элементы.МасштабФормы.МинимальноеЗначение,
									Элементы.МасштабФормы.МинимальноеЗначение, МасштабФормыИзПараметров);
	МасштабФормыИзПараметров = ?(МасштабФормыИзПараметров > Элементы.МасштабФормы.МаксимальноеЗначение,
									Элементы.МасштабФормы.МаксимальноеЗначение, МасштабФормыИзПараметров);
									
	МасштабФормы = МасштабФормыИзПараметров;
	СтрокаМасштаба = РегламентированнаяОтчетностьАЛКОКлиентСервер.СтрокаЧГ0(МасштабФормы) + " %";
	
	Параметры.Свойство("ИмяТаблицы", ИмяТаблицы);
	Параметры.Свойство("УникальностьФормы", УникальностьФормы);
	
	Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
	
		Заголовок = "Установка масштаба таблицы данных";
	
	КонецЕсли;
	
	РегламентированнаяОтчетностьАЛКО.ПодготовкаИнтерфейсаФормыАЛКО(ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТекстПредупреждения = НСтр("ru = 'Данная форма вспомогательная, предназначена для установки масштаба
										|в формах регламентированных отчетов!';
										|en = 'Данная форма вспомогательная, предназначена для установки масштаба
										|в формах регламентированных отчетов!'");
		
	Если ВладелецФормы = Неопределено Тогда
		
		Отказ = Истина;		
		ПоказатьПредупреждение(, ТекстПредупреждения, , НСтр("ru = 'Установка масштаба.';
															|en = 'Установка масштаба.'"));
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьИнтерактивноПоУмолчанию();
	
КонецПроцедуры

&НаКлиенте
Процедура МасштабФормыПриИзменении(Элемент)
	
	СтрокаМасштаба = РегламентированнаяОтчетностьАЛКОКлиентСервер.СтрокаЧГ0(МасштабФормы) + " %";
	
	Если Интерактивно Тогда
		
		Параметр = Новый Структура("Масштаб, ИмяТаблицы", МасштабФормы, ИмяТаблицы);
		Оповестить("ИзменениеМасштаба", Параметр, УникальностьФормы);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтерактивноПриИзменении(Элемент = Неопределено)
	
	Элементы.УстановитьМасштаб.Видимость = НЕ Интерактивно;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьМасштаб(Команда)
	
	Закрыть(МасштабФормы);
	
КонецПроцедуры

// Передается с контекстом, потому что масштабируемая форма 
// будет менять масштаб именно в режиме &НаСервере
&НаСервере
Процедура ВызватьСервер(ВремяЗадержкиНаСервереМсек)

	РегламентированнаяОтчетностьАЛКОКлиентСервер.Пауза(ВремяЗадержкиНаСервереМсек);

КонецПроцедуры


&НаКлиенте
Процедура УстановитьИнтерактивноПоУмолчанию()

	ВремяЗадержкиНаСервереМсек = 100;
	ВремяСтарта = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ВызватьСервер(ВремяЗадержкиНаСервереМсек); 
	ВремяФиниша = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	ВремяСвязиССервером = (ВремяФиниша - ВремяСтарта - ВремяЗадержкиНаСервереМсек)/2;
	
	Интерактивно = (ВремяСвязиССервером <= 50); // 0.05 сек.
	
	ИнтерактивноПриИзменении();
	
КонецПроцедуры


