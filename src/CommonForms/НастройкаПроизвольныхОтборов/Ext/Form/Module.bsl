
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализироватьФорму();
	ИнициализироватьКомпоновщикНастроек();
	УстановитьОтборыПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкиПоУмолчанию(Команда)
	НастройкиПоУмолчаниюНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	Закрыть(ПолучитьНастройки());
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьФорму()
	
	АдресПараметров = Параметры.АдресПараметров;

	Если Параметры.Свойство("ЗаголовокФормы") Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = Параметры.ЗаголовокФормы;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаголовокЗавершитьРедактирование") Тогда
		Элементы.ФормаЗавершитьРедактирование.Заголовок = Параметры.ЗаголовокЗавершитьРедактирование;
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") Тогда
		Для Каждого КлючИЗначение Из Параметры.Отбор Цикл
			ДанныеСтроки = ОтборПоУмолчанию.Добавить();
			ДанныеСтроки.ИмяПоля = КлючИЗначение.Ключ;
			Если ТипЗнч(КлючИЗначение.Значение) = Тип("Структура") Тогда
				ДанныеСтроки.Значение = КлючИЗначение.Значение.Значение;
				ДанныеСтроки.ВидСравнения = КлючИЗначение.Значение.ВидСравнения;
			Иначе
				ДанныеСтроки.Значение = КлючИЗначение.Значение;
				ДанныеСтроки.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек()
	
	ПараметрыНастройки = ПолучитьИзВременногоХранилища(АдресПараметров);
	
	СхемаКомпоновкиДанных = ПараметрыНастройки.СхемаКомпоновкиДанных;
	
	АдресСКД = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСКД));
	
	Если ПараметрыНастройки.Свойство("Настройки")
		И ЗначениеЗаполнено(ПараметрыНастройки.Настройки) Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(ПараметрыНастройки.Настройки);
	Иначе	
		КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	КонецЕсли;
	
	КомпоновщикНастроек.Восстановить();

КонецПроцедуры

&НаСервере
Процедура НастройкиПоУмолчаниюНаСервере()
	
	ПараметрыНастройки = ПолучитьИзВременногоХранилища(АдресПараметров);
	СхемаКомпоновкиДанных = ПараметрыНастройки.СхемаКомпоновкиДанных;
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	УстановитьОтборыПоУмолчанию();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборыПоУмолчанию()
	
	Для Каждого ДанныеСтроки Из ОтборПоУмолчанию Цикл
		
		ЭлементОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(
			КомпоновщикНастроек, 
			ДанныеСтроки.ИмяПоля, 
			ДанныеСтроки.Значение,
			ДанныеСтроки.ВидСравнения,,);
		
		ДанныеСтроки.Идентификатор = ЭлементОтбора.ИдентификаторПользовательскойНастройки;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьНастройки()
	Возврат КомпоновщикНастроек.ПолучитьНастройки();
КонецФункции

#КонецОбласти
