
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ФайлыДокументовЭДОБЗК) Тогда
		ФайлыДокументовЭДОБЗК.ЗагрузитьЗначения(Параметры.ФайлыДокументовЭДОБЗК);
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отказать(Команда)
	
	ЗарегистрироватьОтказ();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗарегистрироватьОтказ()
	
	Если ПроверитьЗаполнение() Тогда
		
		ЗарегистрироватьОтказНаСервере();
		Если ВладелецФормы <> Неопределено И ВладелецФормы.Открыта() Тогда
			ВладелецФормы.Закрыть();
		КонецЕсли;
		Если Открыта() Тогда
			Закрыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗарегистрироватьОтказНаСервере()
	
	Для Каждого ЭлементКоллекции Из ФайлыДокументовЭДОБЗК Цикл
		РегистрыСведений.КомментарииФайловДокументовЭДОБЗК.ДобавитьКомментарийФайла(
			ЭлементКоллекции.Значение, ПричинаОтказа, Пользователи.ТекущийПользователь());
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
