
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	ОбновитьСписокОрганизаций();

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ОповещатьФормуПодключения Тогда
		Оповестить("БизнесСеть_РегистрацияОрганизаций",, ВладелецФормы);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "БизнесСеть_РегистрацияОрганизаций" Тогда
		ОбновитьСписокОрганизаций();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодключенныеОрганизации

&НаКлиенте
Процедура ПодключенныеОрганизацииПриАктивизацииСтроки(Элемент)

	ТекущиеДанные = Элементы.ПодключенныеОрганизации.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ФормаПодключить.Доступность = ТекущиеДанные.СтатусПодключения <> 0;
	Элементы.ФормаОтключить.Доступность = ТекущиеДанные.СтатусПодключения = 0;
	Элементы.ДекорацияОрганизацияЗарегистрированаРанее.Видимость = ТекущиеДанные.СтатусПодключения = 1;
	Элементы.ДекорацияТребуетсяПовторноеПодключение.Видимость = ТекущиеДанные.ТребуетсяПовторноеПодключение;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключить(Команда)
	
	ТекущиеДанные = Элементы.ПодключенныеОрганизации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Организация = ТекущиеДанные.Организация;
	ТребуетсяПовторноеПодключение = ТекущиеДанные.ТребуетсяПовторноеПодключение;
	ПодключитьОрганизациюНаСервере(Организация, ТребуетсяПовторноеПодключение);
	
КонецПроцедуры

&НаКлиенте
Процедура Отключить(Команда)

	ТекущиеДанные = Элементы.ПодключенныеОрганизации.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Организация = ТекущиеДанные.Организация;
	СписокОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	
	Отказ = Ложь;
	БизнесСетьВызовСервера.ОтключитьОрганизации(СписокОрганизаций, Отказ);
	ОбновитьСписокОрганизаций();
	ОповещатьФормуПодключения = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСписокОрганизаций()
	
	ПодключенныеОрганизации.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПодключенныеОрганизации", БизнесСеть.ПодключенныеОрганизации());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Организация КАК Организация,
	|	Организации.ТребуетсяПовторноеПодключение КАК ТребуетсяПовторноеПодключение
	|ПОМЕСТИТЬ ПодключенныеОрганизации
	|ИЗ
	|	&ПодключенныеОрганизации КАК Организации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Организация,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ПодключенныеОрганизации.Организация ЕСТЬ NULL
	|				ТОГДА ""Не подключена""
	|			КОГДА ПодключенныеОрганизации.ТребуетсяПовторноеПодключение
	|				ТОГДА ""Требуется повторное подключение""
	|			ИНАЧЕ ""Подключена""
	|		КОНЕЦ КАК СТРОКА(50)) КАК Пояснение,
	|	ВЫБОР
	|		КОГДА ПодключенныеОрганизации.Организация ЕСТЬ NULL
	|				ИЛИ ПодключенныеОрганизации.ТребуетсяПовторноеПодключение
	|			ТОГДА 2
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтатусПодключения,
	|	ЕСТЬNULL(ПодключенныеОрганизации.ТребуетсяПовторноеПодключение, ЛОЖЬ) КАК ТребуетсяПовторноеПодключение
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПодключенныеОрганизации КАК ПодключенныеОрганизации
	|		ПО Организации.Ссылка = ПодключенныеОрганизации.Организация
	|ГДЕ
	|	НЕ Организации.ПометкаУдаления
	|	И Организации.Ссылка <> &УправленческаяОрганизация";
	Запрос.УстановитьПараметр("УправленческаяОрганизация", СервисСверкиРасчетовПереопределяемый.УправленческаяОрганизация());
	Организации = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаОрганизации Из Организации Цикл
		Если СтрокаОрганизации.СтатусПодключения = 2
		И Не СтрокаОрганизации.ТребуетсяПовторноеПодключение
		   И СервисСверкиРасчетов.ОрганизацияЗарегистрированаПодИнымЛогиномИТС(СтрокаОрганизации.Организация) Тогда
			СтрокаОрганизации.Пояснение = НСтр("ru = 'Зарегистрирована под другой учетной записью ИТС';
												|en = 'Registered under another ITS account'");
			СтрокаОрганизации.СтатусПодключения = 1;
		КонецЕсли;
	КонецЦикла;
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(Организации, ПодключенныеОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОрганизацияЗарегистрированаРанееОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "НастройкиИнтернетПоддержки" Тогда 
		СтандартнаяОбработка = Ложь;
		ПараметрыВыполненияКоманды = Новый Структура;
		ПараметрыВыполненияКоманды.Вставить("Окно", Неопределено);
		ПараметрыВыполненияКоманды.Вставить("Источник", ЭтотОбъект);
		НастройкиПрограммыБИПКлиент.ОткрытьНастройкиИнтернетПоддержкаИСервисы(ПараметрыВыполненияКоманды);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПодключитьОрганизациюНаСервере(Организация, ТребуетсяПовторноеПодключение)
	
	Если ТребуетсяПовторноеПодключение Тогда
		СписокОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
		Отказ = Ложь;
		БизнесСетьВызовСервера.ОтключитьОрганизации(СписокОрганизаций, Отказ);
	КонецЕсли;
	
	РезультатПодключенияОрганизациюКСервису = СервисСверкиРасчетовМенеджерОбмена.РезультатПодключенияОрганизациюКСервису(Организация);
	Если Не ЗначениеЗаполнено(РезультатПодключенияОрганизациюКСервису.ТекстОшибки) Тогда
		ОбновитьСписокОрганизаций();
		ОповещатьФормуПодключения = Истина;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти
