&НаКлиенте
Перем ОтправитьУведомление;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.УведомлениеОВвозеПрослеживаемыхТоваров");
	ИдентификаторУведомлениеОПеремещенииПрослеживаемыхТоваров = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.УведомлениеОПеремещенииПрослеживаемыхТоваров");
	
	УстановитьУсловноеОформление();
	
	// Возврат при получении формы для анализа.
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ТипУведомления.СписокВыбора.Очистить();
	Элементы.ТипУведомления.СписокВыбора.Добавить(ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров, СинонимыСтрокОформления().УведомлениеОВвозе);
	Элементы.ТипУведомления.СписокВыбора.Добавить(ИдентификаторУведомлениеОПеремещенииПрослеживаемыхТоваров, СинонимыСтрокОформления().УведомлениеОВывозе);
	
	Организация = Параметры.Организация;
	Основание = Параметры.Основание;
	КодТНВЭД = Параметры.КодТНВЭД;
	КорректировочноеУведомление = Параметры.КорректировочноеУведомление;
	
	Если Параметры.ТипУведомления = Тип("ДокументСсылка.УведомлениеОВвозеПрослеживаемыхТоваров") Тогда
		ТипУведомления = ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров;	
	ИначеЕсли Параметры.ТипУведомления = Тип("ДокументСсылка.УведомлениеОПеремещенииПрослеживаемыхТоваров") Тогда
		ТипУведомления = ИдентификаторУведомлениеОПеремещенииПрослеживаемыхТоваров;
	КонецЕсли;
	
	КОформлению.Параметры.УстановитьЗначениеПараметра("КорректировочноеУведомление", КорректировочноеУведомление);
	УстановитьОтборПоТипуДокументаИмпорта();
	УстановитьОтборПоОрганизации();
	УстановитьОтборПоКодуТНВЭД();
	УстановитьОтборПоТипуУведомления();
	УстановитьОтборПоОснованию();
	
	ОбновитьЗаголовкиГиперссылокНаСервере();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);  
	
	УстановитьВидимость();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	//++ НЕ УТ
	Если ИмяСобытия = "Завершение отправки в контролирующий орган"
		ИЛИ ИмяСобытия = "Завершение отправки"
		ИЛИ ИмяСобытия = "Получены новые сообщения 1С-Отчетности без смены страницы" Тогда

		Если (ИмяСобытия = "Завершение отправки в контролирующий орган" ИЛИ ИмяСобытия = "Завершение отправки")
			И ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("Ссылка")
			И ЭтоОтправленныйОтчет(Параметр.Ссылка)
			И УведомленияДляОтправкиВФНС.Количество() > 1
			И УведомленияДляОтправкиВФНС.НайтиПоЗначению(Параметр.Ссылка) <> Неопределено
			И УведомленияДляОтправкиВФНС.Индекс(УведомленияДляОтправкиВФНС.НайтиПоЗначению(Параметр.Ссылка)) 
				< (УведомленияДляОтправкиВФНС.Количество()-1) Тогда

			Если (ТекущийИндексДокументаДляОтправкиВФНС + 1) < УведомленияДляОтправкиВФНС.Количество() Тогда

				ТекущийИндексДокументаДляОтправкиВФНС = ТекущийИндексДокументаДляОтправкиВФНС + 1;
				СледующийЭлементСписка = УведомленияДляОтправкиВФНС.Получить(ТекущийИндексДокументаДляОтправкиВФНС);
				ОрганизацияДокумента = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЗначениеРеквизитаОбъекта(СледующийЭлементСписка.Значение, "Организация");
				ОбновитьСчетчикОтправкиУведомлений(,ТекущийИндексДокументаДляОтправкиВФНС);
				РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтаФорма, "ФНС", ,
					Истина, СледующийЭлементСписка.Значение, ОрганизацияДокумента);

			КонецЕсли;

		Иначе
				
			ОбновитьСчетчикОтправкиУведомлений(Истина);
			Элементы.КОформлению.Обновить();
			ОбновитьЗаголовкиГиперссылокНаСервере();

		КонецЕсли;

	КонецЕсли;
	//-- НЕ УТ

	Если ИмяСобытия = "Запись_ПриобретениеТоваровУслуг"
		Или ИмяСобытия = "Запись_РеализацияТоваровУслуг"
		Или ИмяСобытия = "Запись_УведомлениеОПрослеживаемыхТоваров" Тогда
		Элементы.КОформлению.Обновить();
	КонецЕсли;

	Если ИмяСобытия = "Запись_УведомлениеОПрослеживаемыхТоваров" Тогда
		ОбновитьЗаголовкиГиперссылокНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ ЗавершениеРаботы Тогда
		//++ НЕ УТ
		ОбновитьСчетчикОтправкиУведомлений(Истина);
		//-- НЕ УТ
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если НужноУстановитьОтборДинамическогоСписка(КОформлению.Отбор.Элементы, Элементы.Организация, Организация) Тогда	
		УстановитьОтборПоОрганизации();
		ОбновитьЗаголовкиГиперссылокНаСервере();
	КонецЕсли; 
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура КодТНВЭДПриИзменении(Элемент)
	
	Если НужноУстановитьОтборДинамическогоСписка(КОформлению.Отбор.Элементы, Элементы.КодТНВЭД, КодТНВЭД) Тогда	
		УстановитьОтборПоКодуТНВЭД();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипУведомленияПриИзменении(Элемент)
	
	Если НужноУстановитьОтборДинамическогоСписка(КОформлению.Отбор.Элементы, Элементы.ТипУведомления, ТипУведомления) Тогда
		УстановитьОтборПоТипуУведомления();	
		ОбновитьЗаголовкиГиперссылокНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеПриИзменении(Элемент)
	
	Если НужноУстановитьОтборДинамическогоСписка(КОформлению.Отбор.Элементы, Элементы.Основание, Основание) Тогда
		УстановитьОтборПоОснованию();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаОтправитьНажатие(Элемент)
	
	ОткрытьФормуУведомлений("ОжидаютОтправки");
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаОжидатьНажатие(Элемент)
	
	ОткрытьФормуУведомлений("ОжидаютПолучения");
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаАрхивНажатие(Элемент)
	
	ОткрытьФормуУведомлений("Завершены");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьИОтправитьУведомление(Команда)  

	ОтправитьУведомление = Истина;
	СоздатьИОтправитьУведомленияНаКлиенте(); 
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьУведомление(Команда) 
	
	ОтправитьУведомление = Ложь;
   	СоздатьИОтправитьУведомленияНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПриИзмененииРеквизитов

&НаСервере
Процедура УстановитьОтборПоОрганизации()
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		КОформлению,
		"Организация",
		Организация,
		ВидСравненияКомпоновкиДанных.Равно,
		"Организация",
		ЗначениеЗаполнено(Организация));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоКодуТНВЭД()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		КОформлению,
		"АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД",
		КодТНВЭД,
		ВидСравненияКомпоновкиДанных.Равно,
		"КодТНВЭД",
		ЗначениеЗаполнено(КодТНВЭД));
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		КОформлению,
		"Номенклатура.КодТНВЭД",
		КодТНВЭД,
		ВидСравненияКомпоновкиДанных.Равно,
		"КодТНВЭД",
		ЗначениеЗаполнено(КодТНВЭД));
		
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоТипуУведомления()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		КОформлению,
		"ТипУведомления",
		ТипУведомления,
		ВидСравненияКомпоновкиДанных.Равно,
		"ТипУведомления",
		ЗначениеЗаполнено(ТипУведомления));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоОснованию()
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		КОформлению,
		"Основание",
		Основание,
		ВидСравненияКомпоновкиДанных.Равно,
		"Основание",
		ЗначениеЗаполнено(Основание));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоТипуДокументаИмпорта()
	
	СписокТиповДокументов = Новый СписокЗначений;
	СписокТиповДокументов.Добавить(ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров);
	КОформлению.Параметры.УстановитьЗначениеПараметра("ТипДокументаИмпорта", СписокТиповДокументов);
	КОформлению.Параметры.УстановитьЗначениеПараметра("ИдентификаторУведомлениеОПеремещенииПрослеживаемыхТоваров", ИдентификаторУведомлениеОПеремещенииПрослеживаемыхТоваров);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КОформлениюТипУведомления.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КОформлению.ТипУведомления");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров;
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", СинонимыСтрокОформления().УведомлениеОВвозе);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КОформлениюТипУведомления.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КОформлению.ТипУведомления");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ИдентификаторУведомлениеОПеремещенииПрослеживаемыхТоваров;
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", СинонимыСтрокОформления().УведомлениеОВывозе);
		
КонецПроцедуры

&НаКлиенте
Функция НужноУстановитьОтборДинамическогоСписка(ЭлементыОтбора, ЭлементФормы, ПроверяемоеЗначение)
	Результат = Ложь;
	ЭлементОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементОтбораПоПредставлению(КОформлению.Отбор.Элементы, ЭлементФормы.Имя);
	Если ЭлементОтбора = Неопределено
		Или ЭлементОтбора.Использование И ЭлементОтбора.ЛевоеЗначение <> ПроверяемоеЗначение
		Или Не ЭлементОтбора.Использование И ЗначениеЗаполнено(ПроверяемоеЗначение) Тогда
		Результат = Истина;	
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура СоздатьИОтправитьУведомленияНаКлиенте()  
	
	ВыделенныеСтроки  = Элементы.КОформлению.ВыделенныеСтроки;
	ДанныеКОформлению = Новый Массив;
	
	Для Каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		ВыделеннаяСтрока = Элементы.КОформлению.ДанныеСтроки(ИдентификаторСтроки);
		СтруктураДанныхКОформлению = СтруктураДанныхКОформлению();
		ЗаполнитьЗначенияСвойств(СтруктураДанныхКОформлению,ВыделеннаяСтрока);
		СтруктураДанныхКОформлению.КорректировочноеУведомление = КорректировочноеУведомление;
		ДанныеКоформлению.Добавить(СтруктураДанныхКОформлению);
	КонецЦикла;
	
	Если ДанныеКОформлению.Количество() > 0 Тогда 
				
		ДлительнаяОперация = СоздатьУведомленияНаСервереВФоне(ДанныеКОформлению);
		
		Если ДлительнаяОперация = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ДлительнаяОперация.Статус <> "Ошибка" Тогда
			НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			НастройкиОжидания.ВыводитьОкноОжидания = Истина;
			
			Обработчик = Новый ОписаниеОповещения("ПослеСозданияУведомлений", ЭтотОбъект);
			
			ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
		Иначе
			ПоказатьОшибкуСозданияУведомления(ДлительнаяОперация);			
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость() 
	
	ПодключенДО = Ложь;
	
	//++ НЕ УТ
	ПодключенДО = НЕ ПолучитьФункциональнуюОпцию("УправлениеТорговлей")
			И ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом(Организация, Перечисления.ТипыКонтролирующихОрганов.ФНС);
	//-- НЕ УТ
		
	Элементы.КОформлениюСоздатьИОтправитьУведомление.Видимость = ПодключенДО;
			
	Элементы.КОформлениюСоздатьИОтправитьУведомление.КнопкаПоУмолчанию = ПодключенДО;
	Элементы.КОформлениюСоздатьУведомление.КнопкаПоУмолчанию = НЕ ПодключенДО;
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураДанныхКОформлению()
	
	СтруктураДанных = Новый Структура();
	СтруктураДанных.Вставить("Организация");
	СтруктураДанных.Вставить("КодТНВЭД");
	СтруктураДанных.Вставить("ТипУведомления");
	СтруктураДанных.Вставить("Основание");
	СтруктураДанных.Вставить("КорректировочноеУведомление");
	Возврат СтруктураДанных;
	
КонецФункции	
	
&НаКлиенте
Процедура ПослеСозданияУведомлений(ДлительнаяОперация, ДополнительныеПараметры) Экспорт

	Если ДлительнаяОперация = Неопределено Тогда
		Возврат; // Отменено
	КонецЕсли;

	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		Оповестить("Запись_УведомлениеОПрослеживаемыхТоваров");
		
		УведомленияДляОтправкиВФНС.ЗагрузитьЗначения(ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата));  
		
		//++ НЕ УТ
		Если ОтправитьУведомление Тогда 			  
			 НачатьОтправкуУведомленийВФНС();
		КонецЕсли;
		//-- НЕ УТ
		 
	Иначе      
		
		ПоказатьОшибкуПолученияДанных(ДлительнаяОперация);

	КонецЕсли;
	
КонецПроцедуры 

//++ НЕ УТ
&НаКлиенте
Процедура НачатьОтправкуУведомленийВФНС()
	
	Если УведомленияДляОтправкиВФНС.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	Обработчик = Новый ОписаниеОповещения("ПослеПодтвержденияОтправки", ЭтотОбъект);
	
	КнопкиВопроса = Новый СписокЗначений;
	КнопкиВопроса.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Продолжить';
														|en = 'Continue'"));
	КнопкиВопроса.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена';
															|en = 'Cancel'"));
	ПоказатьВопрос(
		Обработчик,
		НСтр("ru = 'Сейчас начнется отправка отчетов.
		|В процессе отправки могут быть запрошены пароли от закрытых ключей.';
		|en = 'Report sending will now begin.
		|Passwords from private keys may be requested during the sending.'"),
		КнопкиВопроса);   
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодтвержденияОтправки(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийИндексДокументаДляОтправкиВФНС = 0;
	ТекущийЭлементСписка = УведомленияДляОтправкиВФНС[ТекущийИндексДокументаДляОтправкиВФНС];
	
	ОрганизацияДокумента = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЗначениеРеквизитаОбъекта(ТекущийЭлементСписка.Значение, "Организация");
	
	ОбновитьСчетчикОтправкиУведомлений(,ТекущийИндексДокументаДляОтправкиВФНС);
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтаФорма, "ФНС", ,
		Истина, ТекущийЭлементСписка.Значение, ОрганизацияДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСчетчикОтправкиУведомлений(ЭтоОчистка = Ложь, ТекущийНомерОтправки = Неопределено)
	
	Если ЭтоОчистка Тогда
		ДлительнаяОтправкаКлиент.КонтекстДлительнойОперации("БРО.ПодтверждатьОтправку");
		ДлительнаяОтправкаКлиент.КонтекстДлительнойОперации("БРО.ПодтверждатьЗавершениеОтправки");
		ДлительнаяОтправкаКлиент.КонтекстДлительнойОперации("БРО.ХарактеристикаОтправляемогоОтчета");
		Возврат;
	КонецЕсли;
	
	НомерИКоличество = "";
	
	Если ТекущийНомерОтправки <> Неопределено Тогда
		НомерСтроки = ТекущийНомерОтправки + 1;
		Если НомерСтроки > 0 Тогда
			КоличествоСтрок = УведомленияДляОтправкиВФНС.Количество();
			Если КоличествоСтрок > 1 Тогда
				НомерИКоличество = СтрШаблон(
					НСтр("ru = '%1 из %2';
						|en = '%1 out of %2'"),
					Строка(НомерСтроки),
					Строка(КоличествоСтрок));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДлительнаяОтправкаКлиент.ЗапомнитьКонтекстДлительнойОперации(Ложь,
		"БРО.ПодтверждатьОтправку");
	ДлительнаяОтправкаКлиент.ЗапомнитьКонтекстДлительнойОперации(Ложь,
		"БРО.ПодтверждатьЗавершениеОтправки");
	ДлительнаяОтправкаКлиент.ЗапомнитьКонтекстДлительнойОперации(НомерИКоличество,
		"БРО.ХарактеристикаОтправляемогоОтчета");
	
КонецПроцедуры 

&НаСервереБезКонтекста
Функция ЭтоОтправленныйОтчет(Ссылка)
	
	Возврат ИнтерфейсыВзаимодействияБРО.СостояниеДокумента(Ссылка).Отправлено;
	
КонецФункции
//-- НЕ УТ

&НаКлиенте
Процедура ПоказатьОшибкуПолученияДанных(ДлительнаяОперация)
	
	МассивИзДвухСтрок = Новый Массив;
	МассивИзДвухСтрок.Добавить(НСтр("ru = 'Ошибка при получении данных:';
									|en = 'An error occurred when receiving data:'"));
	МассивИзДвухСтрок.Добавить(ДлительнаяОперация.КраткоеПредставлениеОшибки);
	
	ВызватьИсключение СтрСоединить(МассивИзДвухСтрок, Символы.ПС);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкуСозданияУведомления(ДлительнаяОперация)
	
	МассивИзДвухСтрок = Новый Массив;
	МассивИзДвухСтрок.Добавить(НСтр("ru = 'Ошибка при создании уведомления..';
									|en = 'An error occurred when creating the notification..'"));
	МассивИзДвухСтрок.Добавить(ДлительнаяОперация.КраткоеПредставлениеОшибки);
	
	ВызватьИсключение СтрСоединить(МассивИзДвухСтрок, Символы.ПС);
	
КонецПроцедуры

&НаСервере
Функция СоздатьУведомленияНаСервереВФоне(ДанныеКОформлению)
	
	ПараметрыОбработчика = Новый Структура;
	ПараметрыОбработчика.Вставить("ДанныеКОформлению", ДанныеКОформлению); 
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Создание уведомлений';
														|en = 'Notification creation'");
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"ПрослеживаемостьУП.СоздатьУведомленияВФоне",        
	    ПараметрыОбработчика,
		НастройкиЗапуска);
	
	Возврат ДлительнаяОперация;
	
КонецФункции

&НаСервереБезКонтекста
Функция СинонимыСтрокОформления()
	
	Результат = Новый Структура;
	Результат.Вставить("УведомлениеОВвозе", НСтр("ru = 'Уведомление о ввозе';
												|en = 'Import notification'"));
	Результат.Вставить("УведомлениеОВывозе", НСтр("ru = 'Уведомление о перемещении';
													|en = 'Transfer notification'"));
	Возврат Результат;
	
КонецФункции

#Область ОбменСКО

&НаКлиенте
Процедура ОткрытьФормуУведомлений(Состояние)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Состояние", Состояние);
	
	Если ЗначениеЗаполнено(ТипУведомления) Тогда
		
		Если ТипУведомления = ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров Тогда
			ПараметрыФормы.Вставить("ТипУведомления", Тип("ДокументСсылка.УведомлениеОВвозеПрослеживаемыхТоваров"));
		Иначе
			ПараметрыФормы.Вставить("ТипУведомления", Тип("ДокументСсылка.УведомлениеОПеремещенииПрослеживаемыхТоваров"));
		КонецЕсли;
		
	КонецЕсли;
	
	ИмяФормыУведомлений = "ЖурналДокументов.ПрослеживаемостьУведомления.ФормаСписка";
	
	ОткрытьФорму(
		ИмяФормыУведомлений,
		ПараметрыФормы,
		,,,,,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовкиГиперссылок(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбновитьЗаголовкиГиперссылокНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаголовкиГиперссылокНаСервере()
	
	МассивОрганизаций = ?(ЗначениеЗаполнено(Организация), ОбщегоНазначенияУТКлиентСервер.Массив(Организация), Неопределено);

	ВидимостьЭлементаГиперссылкаОжидать = Истина;
			
	ТипыУведомлений = Новый Массив;
	Если ЗначениеЗаполнено(ТипУведомления) Тогда
		Если ТипУведомления = ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров Тогда 
			ТипыУведомлений.Добавить(Тип("ДокументСсылка.УведомлениеОВвозеПрослеживаемыхТоваров"));			
		Иначе
			ТипыУведомлений.Добавить(Тип("ДокументСсылка.УведомлениеОПеремещенииПрослеживаемыхТоваров"));  
			ВидимостьЭлементаГиперссылкаОжидать = Ложь;	
		КонецЕсли;
	Иначе
		ТипыУведомлений.Добавить(Тип("ДокументСсылка.УведомлениеОВвозеПрослеживаемыхТоваров"));
		ТипыУведомлений.Добавить(Тип("ДокументСсылка.УведомлениеОПеремещенииПрослеживаемыхТоваров"));
	КонецЕсли;
	
	КоличестваУведомленийКОформлению =
		ЖурналыДокументов.ПрослеживаемостьУведомления.СтатистикаОтправкиУведомлений(ТипыУведомлений, МассивОрганизаций);
	
	ШаблонЗаголовокЭлементаОтправить = НСтр("ru = 'Отправить %1';
											|en = 'Send %1'");
	ШаблонЗаголовокЭлементаОжидать = НСтр("ru = 'Ожидать получения РНПТ %1';
											|en = 'Await receiving goods batch registration number %1'");
	ШаблонЗаголовокЭлементаАрхив = НСтр("ru = 'Архив %1';
										|en = 'Archive %1'");
	
	КоличествоКОформлениюОтправить = 0;
	КоличествоКОформлениюОжидать = 0;
	КоличествоКОформлениюАрхив = 0;
	
	Для Каждого КлючЗначение Из КоличестваУведомленийКОформлению Цикл
		
		КоличествоКОформлениюОтправить = КоличествоКОформлениюОтправить + КлючЗначение.Значение.ОжидаютОтправки;
		КоличествоКОформлениюОжидать = КоличествоКОформлениюОжидать + КлючЗначение.Значение.ОжидаютПолучения;
		КоличествоКОформлениюАрхив = КоличествоКОформлениюАрхив + КлючЗначение.Значение.Завершены;
	
	КонецЦикла;
	
	ЗаголовокЭлементаОтправить =
		СтрШаблон(
			ШаблонЗаголовокЭлементаОтправить,
			?(ЗначениеЗаполнено(КоличествоКОформлениюОтправить), "(" + Строка(КоличествоКОформлениюОтправить) + ")", ""));
	
	ЗаголовокЭлементаОжидать =
		СтрШаблон(
			ШаблонЗаголовокЭлементаОжидать,
			?(ЗначениеЗаполнено(КоличествоКОформлениюОжидать), "(" + Строка(КоличествоКОформлениюОжидать) + ")", ""));
	
	ЗаголовокЭлементаАрхив =
		СтрШаблон(
			ШаблонЗаголовокЭлементаАрхив,
			?(ЗначениеЗаполнено(КоличествоКОформлениюАрхив), "(" + Строка(КоличествоКОформлениюАрхив) + ")", ""));
	
	Элементы.ГиперссылкаОтправить.Заголовок = СокрЛП(ЗаголовокЭлементаОтправить);
	Элементы.ГиперссылкаОжидать.Заголовок 	= СокрЛП(ЗаголовокЭлементаОжидать);
	Элементы.ГиперссылкаАрхив.Заголовок 	= СокрЛП(ЗаголовокЭлементаАрхив);
	
	Элементы.ГиперссылкаОжидать.Видимость = ВидимостьЭлементаГиперссылкаОжидать;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
