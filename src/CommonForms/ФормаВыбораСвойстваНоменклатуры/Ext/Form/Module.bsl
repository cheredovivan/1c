//@strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокРеквизитов();
	УстановитьТекущуюСтрокуДерева();
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРеквизитыСвойстваНоменклатуры

&НаКлиенте
Процедура РеквизитыСвойстваНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ЗавершитьВыбор();
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыСвойстваНоменклатурыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.РеквизитыСвойстваНоменклатуры.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстКомментарияОбщийРеквизит = НСтр("ru = 'Дополнительный реквизит для всех видов номенклатуры';
										|en = 'Additional attribute for all item kinds'");
	ШаблонДопРеквизитОдинВидНоменклатуры = НСтр("ru = 'Дополнительный реквизит вида номенклатуры ""%1""';
												|en = 'Additional attribute of the ""%1"" item kind'");
	ШаблонДопРеквизитНесколькоВидовНоменклатуры = НСтр("ru = 'Дополнительный реквизит входит в %1 %2 номенклатуры';
														|en = 'An additional attribute is included in %1 %2 item'");
	
	ДанныеСтроки = РеквизитыСвойстваНоменклатуры.НайтиПоИдентификатору(ТекущиеДанные.ПолучитьИдентификатор());
	КоличествоВидовНоменклатуры = ДанныеСтроки.КоличествоВидовНоменклатуры;
	ЭтоДополнительныйРеквизит = ДанныеСтроки.ДополнительныйРеквизит;
	
	ТекстКомментария = НСтр("ru = 'Реквизит для всех видов номенклатуры';
							|en = 'Attribute for all item kinds'");
	
	Если ЭтоДополнительныйРеквизит Тогда
		
		Если КоличествоВидовНоменклатуры > 1 Тогда
			
			ПараметрыПредметаИсчисления = НСтр("ru = 'вид,вида,видов,,,,,,0';
												|en = 'kind, kinds,,,,,,0'");
			ПрописьЧисла = ЧислоПрописью(КоличествоВидовНоменклатуры,, НСтр("ru = ',,,,,,,,0';
																			|en = ',,,,,,,,0'"));
			ПрописьЧислаИПредмета = ЧислоПрописью(КоличествоВидовНоменклатуры,, ПараметрыПредметаИсчисления);
			СтрокаВидыНоменклатуры = СтрЗаменить(ПрописьЧислаИПредмета, ПрописьЧисла, "");
			
			ТекущийШаблон = ШаблонДопРеквизитНесколькоВидовНоменклатуры ;
			ТекстКомментария = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекущийШаблон,
				Формат(КоличествоВидовНоменклатуры, "ЧГ="),
				СтрокаВидыНоменклатуры);
			
		ИначеЕсли КоличествоВидовНоменклатуры = 1 Тогда
			
			ТекущийШаблон = ШаблонДопРеквизитОдинВидНоменклатуры;
			ТекстКомментария = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекущийШаблон,
				ДанныеСтроки.ПредставлениеВидаНоменклатуры);
			
		Иначе
			
			ТекстКомментария = ТекстКомментарияОбщийРеквизит;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.Комментарий.Заголовок = ТекстКомментария;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьРеквизит(Команда)
	
	ЗавершитьВыбор();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗавершитьВыбор()
	
	Результат = Неопределено;
	ТекущиеДанные = Элементы.РеквизитыСвойстваНоменклатуры.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		ДанныеСтроки = РеквизитыСвойстваНоменклатуры.НайтиПоИдентификатору(ТекущиеДанные.ПолучитьИдентификатор());
		Результат = Новый Структура;
		Результат.Вставить("ИдентификаторРеквизита", ДанныеСтроки.ИдентификаторРеквизита);
		Результат.Вставить("ПредставлениеРеквизита", ДанныеСтроки.ПредставлениеРеквизита);
		
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокРеквизитов()
	
	ДеревоРеквизитов = РеквизитФормыВЗначение("РеквизитыСвойстваНоменклатуры");
	
	СвойстваНоменклатуры = Параметры.СвойстваНоменклатуры;
	Для Каждого ТекущийЭлементСписка Из СвойстваНоменклатуры Цикл
		
		НоваяСтрока = ДеревоРеквизитов.Строки.Добавить();
		ИдентификаторРеквизита = ТекущийЭлементСписка.Значение; // Строка
		НоваяСтрока.ИдентификаторРеквизита = ИдентификаторРеквизита;
		НоваяСтрока.ПредставлениеРеквизита = ТекущийЭлементСписка.Представление;
		
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения") Тогда
		
		ДополнительныеРеквизиты = ДополнительныеРеквизитыПоВидамНоменклатуры();
		
		Если ДополнительныеРеквизиты.Количество() > 0 Тогда
			
			ЗаполнитьСписокДополнительныхРеквизитовСвойств(ДеревоРеквизитов, ДополнительныеРеквизиты);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ДеревоРеквизитов, "РеквизитыСвойстваНоменклатуры");
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокДополнительныхРеквизитовСвойств(ДеревоРеквизитов, ДополнительныеРеквизитыПоВидамНоменклатуры)
	
	ДополнительныеРеквизиты = ДополнительныеРеквизитыПоВидамНоменклатуры.Скопировать();
	Колонки = "ИдентификаторДляФормул, Общий, ПредставлениеРеквизита, ТипЗначения";
	ДополнительныеРеквизиты.Свернуть(Колонки);
	
	ПрефиксВидаХарактеристик = "";
	МетаданныеХарактеристики = Метаданные.ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения;
	СвойстваХарактеристик = РаботаСФормуламиПовтИсп.СвойстваПоддерживаемыхИсточниковХарактеристик();
	ОписаниеСвойств = СвойстваХарактеристик.Найти(МетаданныеХарактеристики, "МетаданныеИсточника");
	Если ОписаниеСвойств <> Неопределено Тогда
		ПрефиксВидаХарактеристик = ОписаниеСвойств.ПрефиксТипаВидаХарактеристик;
	КонецЕсли;
	
	ШаблонИдентификатора = "{%1%2}";
	ТипыСвойствНоменклатуры = Метаданные.ОпределяемыеТипы.ТипыСвойствНоменклатурыРетроБонусов.Тип;
	
	Для Каждого ТекущийРеквизит Из ДополнительныеРеквизиты Цикл
		
		Если НЕ РетроБонусыСервер.РазрешенныйТипЗначенияДляСвойстваНоменклатуры(
			ТекущийРеквизит.ТипЗначения,
			ТипыСвойствНоменклатуры) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		НоваяСтрока = ДеревоРеквизитов.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущийРеквизит);
		НоваяСтрока.ДополнительныйРеквизит = Истина;
		
		Идентификатор = ТекущийРеквизит.ИдентификаторДляФормул;
		ПолныйИдентификаторРеквизита = СтрШаблон(ШаблонИдентификатора, ПрефиксВидаХарактеристик, Идентификатор);
		
		НоваяСтрока.ПредставлениеРеквизита = ТекущийРеквизит.ПредставлениеРеквизита;
		НоваяСтрока.ИдентификаторРеквизита = ПолныйИдентификаторРеквизита;
		
		Отбор = Новый Структура;
		Отбор.Вставить("ИдентификаторДляФормул", Идентификатор);
		НайденныеСтроки = ДополнительныеРеквизитыПоВидамНоменклатуры.НайтиСтроки(Отбор);
		КоличествоНайденныхСтрок = НайденныеСтроки.Количество();
		
		Если КоличествоНайденныхСтрок = 1 Тогда
			
			НоваяСтрока.ПредставлениеВидаНоменклатуры = НайденныеСтроки[0].ПредставлениеВидаНоменклатуры;
			Если НЕ ПустаяСтрока(НоваяСтрока.ПредставлениеВидаНоменклатуры) Тогда
				НоваяСтрока.КоличествоВидовНоменклатуры = 1;
			КонецЕсли;
			
		Иначе
			
			НоваяСтрока.КоличествоВидовНоменклатуры = КоличествоНайденныхСтрок;
			Для Каждого ТекущаяСтрокаВида Из НайденныеСтроки Цикл
				
				НоваяСтрокаПоВиду = НоваяСтрока.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаПоВиду, НоваяСтрока);
				НоваяСтрокаПоВиду.ПредставлениеВидаНоменклатуры = ТекущаяСтрокаВида.ПредставлениеВидаНоменклатуры;
				НоваяСтрокаПоВиду.КоличествоВидовНоменклатуры = 1;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращаемое значение:
//  ТаблицаЗначений - Таблица доп реквизитов сведений номенклатуры:
// * ИдентификаторДляФормул - Строка - 
// * Общий - Булево - 
// * ПредставлениеВидаНоменклатуры - Строка 
// * ПредставлениеРеквизита - Строка
// * ТипЗначения - ОписаниеТипов
//
&НаСервере
Функция ДополнительныеРеквизитыПоВидамНоменклатуры()
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщийНабор = УправлениеСвойствами.НаборСвойствПоИмени("Справочник_Номенклатура_Общие");
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВидыНоменклатуры.НаборСвойств КАК НаборСвойств,
	|	ВидыНоменклатуры.Представление КАК ПредставлениеВидаНоменклатуры
	|ПОМЕСТИТЬ ВТ_НаборыСвойств
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|ГДЕ
	|	НЕ ВидыНоменклатуры.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НаборыСвойств.ПредставлениеВидаНоменклатуры КАК ПредставлениеВидаНоменклатуры,
	|	ДополнительныеРеквизитыНабора.Свойство КАК Свойство,
	|	ЛОЖЬ КАК Общий
	|ПОМЕСТИТЬ ВТ_СвойстваВидовНоменклатуры
	|ИЗ
	|	ВТ_НаборыСвойств КАК ВТ_НаборыСвойств
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК
	|			ДополнительныеРеквизитыНабора
	|		ПО ВТ_НаборыСвойств.НаборСвойств = ДополнительныеРеквизитыНабора.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений КАК НаборыДополнительныхРеквизитовИСведений
	|		ПО ВТ_НаборыСвойств.НаборСвойств = НаборыДополнительныхРеквизитовИСведений.Ссылка
	|		И НаборыДополнительныхРеквизитовИСведений.Используется
	|ГДЕ
	|	НЕ ДополнительныеРеквизитыНабора.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	"""",
	|	ДополнительныеРеквизитыНабора.Свойство,
	|	ИСТИНА
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК ДополнительныеРеквизитыНабора
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений КАК НаборыДополнительныхРеквизитовИСведений
	|		ПО ДополнительныеРеквизитыНабора.Ссылка = НаборыДополнительныхРеквизитовИСведений.Ссылка
	|		И НаборыДополнительныхРеквизитовИСведений.Используется
	|ГДЕ
	|	ДополнительныеРеквизитыНабора.Ссылка = &ОбщийНабор
	|	И НЕ ДополнительныеРеквизитыНабора.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СвойстваВидовНоменклатуры.ПредставлениеВидаНоменклатуры КАК ПредставлениеВидаНоменклатуры,
	|	&ВыражениеИдентификаторДляФормул КАК ИдентификаторДляФормул,
	|	ДополнительныеРеквизитыИСведения.Представление КАК ПредставлениеРеквизита,
	|	ВТ_СвойстваВидовНоменклатуры.Общий КАК Общий,
	|	ДополнительныеРеквизитыИСведения.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ВТ_СвойстваВидовНоменклатуры КАК ВТ_СвойстваВидовНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|		ПО ВТ_СвойстваВидовНоменклатуры.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
	|ГДЕ
	|	НЕ ДополнительныеРеквизитыИСведения.ПометкаУдаления";
	
	ВыражениеИдентификаторДляФормул = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"ДополнительныеРеквизитыИСведения.%1",
		РаботаСФормулами.ПолеИдентификатораДополнительныхРеквизитовИСведений());
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеИдентификаторДляФормул", ВыражениеИдентификаторДляФормул);
	Запрос.Текст = ТекстЗапроса; 
	Запрос.УстановитьПараметр("ОбщийНабор", ОбщийНабор);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	УстановитьОформлениеРеквизитДляВсехВидовНоменклатуры();
	УстановитьОформлениеРеквизитДляНесколькихВидовНоменклатуры();
	
КонецПроцедуры 

&НаСервере
Процедура УстановитьОформлениеРеквизитДляВсехВидовНоменклатуры()
	
	ТекстДляВсех = НСтр("ru = 'для всех';
						|en = 'for all'");
	ТипЭлементОтбораКомпоновкиДанных = Тип("ЭлементОтбораКомпоновкиДанных");
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле =
		Новый ПолеКомпоновкиДанных(Элементы.РеквизитыСвойстваНоменклатурыПредставлениеВидаНоменклатуры.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(ТипЭлементОтбораКомпоновкиДанных);
	ОтборЭлемента.ЛевоеЗначение =
		Новый ПолеКомпоновкиДанных("РеквизитыСвойстваНоменклатуры.ПредставлениеВидаНоменклатуры");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(ТипЭлементОтбораКомпоновкиДанных);
	ОтборЭлемента.ЛевоеЗначение =
		Новый ПолеКомпоновкиДанных("РеквизитыСвойстваНоменклатуры.КоличествоВидовНоменклатуры");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ТекстДляВсех);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеРеквизитДляНесколькихВидовНоменклатуры()
	
	ТипЭлементОтбораКомпоновкиДанных = Тип("ЭлементОтбораКомпоновкиДанных");
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле =
		Новый ПолеКомпоновкиДанных(Элементы.РеквизитыСвойстваНоменклатурыПредставлениеВидаНоменклатуры.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(ТипЭлементОтбораКомпоновкиДанных);
	ОтборЭлемента.ЛевоеЗначение =
		Новый ПолеКомпоновкиДанных("РеквизитыСвойстваНоменклатуры.ПредставлениеВидаНоменклатуры");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(ТипЭлементОтбораКомпоновкиДанных);
	ОтборЭлемента.ЛевоеЗначение =
		Новый ПолеКомпоновкиДанных("РеквизитыСвойстваНоменклатуры.КоличествоВидовНоменклатуры");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
	ОтборЭлемента.ПравоеЗначение = 1;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'несколько';
																|en = 'several'"));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущуюСтрокуДерева()
	
	ИдентификаторРеквизита = Параметры.ИдентификаторРеквизита;
	Если ПустаяСтрока(ИдентификаторРеквизита) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из РеквизитыСвойстваНоменклатуры.ПолучитьЭлементы() Цикл
		
		Если Элемент.ИдентификаторРеквизита = ИдентификаторРеквизита Тогда
			
			Элементы.РеквизитыСвойстваНоменклатуры.ТекущаяСтрока = Элемент.ПолучитьИдентификатор();
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти