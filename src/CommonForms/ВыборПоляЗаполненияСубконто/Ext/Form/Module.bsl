
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ВидСубконто = Параметры.ВидСубконто;
	АдресСхемыКомпоновкиДанных = Параметры.АдресСхемыКомпоновкиДанных;
	ТекущееВыражение = Параметры.ТекущееВыражение;
	СкрытьДополнительныеРеквизиты = Параметры.СкрытьДополнительныеРеквизиты;
	
	ТипЗначения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидСубконто, "ТипЗначения");
	
	МаксимальныйУровеньРазверткиСтрок = 2;
	ФинансоваяОтчетностьСервер.ЗаполнитьДоступныеПоляПоСхемеКомпоновкиДанных(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДеревоВыбораПередРазворачиванием(Элемент, Строка, Отказ)
	
	ТекущиеДанные = ДеревоВыбора.НайтиПоИдентификатору(Строка);
	Если ТекущиеДанные.РазворачиватьДоРеквизитов
		И НЕ ТекущиеДанные.ПодчиненныеСтрокиРазворачивались Тогда
		// Если есть подчиненные реквизиты и для них не были получены реквизиты следующего уровня.
		РазвернутьПодчиненныеСтрокиДереваОперандов(Строка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РазвернутьПодчиненныеСтрокиДереваОперандов(ИдентификаторТекущейСтрокиДереваОперандов)
	
	РаботаСФормулами.РазвернутьСтрокуОперандаДереваФормы(ДеревоВыбора,
		ИдентификаторТекущейСтрокиДереваОперандов,
		МаксимальныйУровеньРазверткиСтрок);
	
	// Обработка ограничений по типу значения или идентификатору доп.реквизита на форме.
	ТекущиеДанные = ДеревоВыбора.НайтиПоИдентификатору(ИдентификаторТекущейСтрокиДереваОперандов);
	Для Каждого ДобавленнаяСтрока Из ТекущиеДанные.ПолучитьЭлементы() Цикл
		Если ДобавленнаяСтрока.РазрешаетсяВыборОперанда Тогда
			ДобавленнаяСтрока.РазрешаетсяВыборОперанда = ФинансоваяОтчетностьСервер.ПодходящееПолеВыбора(ДобавленнаяСтрока,
				ТипЗначения, ДополнительноеСвойствоИдентификатор);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьВыбор(ВыбраннаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОбработатьВыбор(Элементы.ДеревоВыбора.ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВыбораПредставление.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВыбораИдентификатор.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВыбораТипЗначения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВыбора.РазрешаетсяВыборОперанда");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеактуальногоСписка);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВыбора.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВыбораПредставление.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВыбораИдентификатор.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВыбораТипЗначения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВыбора.ТипЭлементаДерева");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "ДополнительныйРеквизит";
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СкрытьДополнительныеРеквизиты");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыбор(ВыбраннаяСтрока)
	
	СтрокаДерева = ДеревоВыбора.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если Не СтрокаДерева.РазрешаетсяВыборОперанда Тогда
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(СтрокаДерева.ПолныйИдентификаторСтроки);
	
КонецПроцедуры


#КонецОбласти