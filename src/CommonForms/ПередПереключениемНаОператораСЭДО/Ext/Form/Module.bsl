&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	ПредлагатьПодключатьДругиеОрганизации = Параметры.ПредлагатьПодключатьДругиеОрганизации;
	
	ЭтотОбъект.Заголовок = НСтр("ru = 'Подключение 1С-Отчетности для ';
								|en = 'Подключение 1С-Отчетности для '") 
		+ Организация;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗаявлениеСФРБесплатно(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОтправитьЗаявлениеСФРБесплатноПослеЗаявления", ЭтотОбъект);
	ЭлектронныйДокументооборотСФССКлиент.ОткрытьЗаявлениеСТарифомОператораЭДО(Организация, ЭтотОбъект.ВладелецФормы, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьСДругимТарифом(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПодключитьСДругимТарифомПослеЗаявления", ЭтотОбъект);
	ЭлектронныйДокументооборотСФССКлиент.ОткрытьЗаявлениеНаПодключение1СОтчетности(Организация, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗаявлениеСФРБесплатноПослеЗаявления(Результат, ДополнительныеПараметры) Экспорт
	
	СделатьАнализИПодключитьДругиеОрганизацииПриНеобходимости(Результат);
	
	Если ЭтотОбъект.Открыта() Тогда
		ЭтотОбъект.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьСДругимТарифомПослеЗаявления(Результат, ДополнительныеПараметры) Экспорт
	
	СделатьАнализИПодключитьДругиеОрганизацииПриНеобходимости(Результат);
	
	Если ЭтотОбъект.Открыта() Тогда
		ЭтотОбъект.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура СделатьАнализИПодключитьДругиеОрганизацииПриНеобходимости(Результат)
	
	Если НЕ ПредлагатьПодключатьДругиеОрганизации Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат = Неопределено
		ИЛИ НЕ ТипЗнч(Результат) = Тип("Структура") 
		ИЛИ НЕ Результат.Свойство("ОтправленоУспешно")
		ИЛИ НЕ Результат.ОтправленоУспешно Тогда
		Возврат;
	КонецЕсли;
	
	ИсключитьОрганизации = Неопределено;
	Если ЗначениеЗаполнено(Организация) Тогда
		ИсключитьОрганизации = Новый Массив;
		ИсключитьОрганизации.Добавить(Организация);
	КонецЕсли;
	РезультатАнализа = ЭлектронныйДокументооборотСФССВызовСервера.ПроанализироватьОрганизацииПоОбменуСЭДО(, ИсключитьОрганизации);
	Организации = РезультатАнализа.ОрганизацииДляПереходаНаОператора;
	КоличествоОрганизаций = Организации.Количество();
	Если КоличествоОрганизаций > 0 Тогда
		Шаблон = НСтр("ru = 'Подключить 1С-Отчетность с направлением СФР по оставшимся организациям (%1)?';
						|en = 'Подключить 1С-Отчетность с направлением СФР по оставшимся организациям (%1)?'");
		ТекстВопроса = СтрШаблон(Шаблон, КоличествоОрганизаций);
		ОтветНаВопрос = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, 0, КодВозвратаДиалога.Да);
		Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
			ЭлектронныйДокументооборотСФССКлиент.ОткрытьФормуПереключенияОрганизацийНаОператораСЭДО(ЭтотОбъект.ВладелецФормы);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры