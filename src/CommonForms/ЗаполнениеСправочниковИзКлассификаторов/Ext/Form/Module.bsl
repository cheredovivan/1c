
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("Code", "Код");
	СоответствиеПолей.Вставить("Name", "Наименование");

	ЗаголовкиПолей = Новый Соответствие;
	ЗначенияЗамены = Новый Соответствие;
	
	Параметры.Свойство("Заголовок", Заголовок);
	Параметры.Свойство("ИмяСправочника", ИмяСправочника);
	Параметры.Свойство("ПолныйПутьКМакету", ПолныйПутьКМакету);
	Параметры.Свойство("ЗаполнятьДополнительныеСведения", ЗаполнятьДополнительныеСведения);
	Параметры.Свойство("ЗагружатьИерархию", ЗагружатьИерархию);
	
	Если Параметры.Свойство("СоответствиеПолей") Тогда
		
		Для каждого ЭлементСоответствия Из Параметры.СоответствиеПолей Цикл
			СоответствиеПолей.Вставить(ЭлементСоответствия.Ключ, ЭлементСоответствия.Значение);
		КонецЦикла;
		
	КонецЕсли;
	
	СоответствиеПолейКлассификатораРеквизитам = Новый ФиксированноеСоответствие(СоответствиеПолей);

	Если Параметры.Свойство("ЗаголовкиПолей") Тогда
		
		Для каждого ЭлементСоответствия Из Параметры.ЗаголовкиПолей Цикл
			ЗаголовкиПолей.Вставить(ЭлементСоответствия.Ключ, ЭлементСоответствия.Значение);
		КонецЦикла;
		
	КонецЕсли;

	ЗаголовкиПолейКлассификатора = Новый ФиксированноеСоответствие(ЗаголовкиПолей);
	
	Если Параметры.Свойство("ЗначенияЗамены") Тогда
		
		Для каждого ЭлементСоответствия Из Параметры.ЗначенияЗамены Цикл
			ЗначенияЗамены.Вставить(ЭлементСоответствия.Ключ, ОбщегоНазначения.СкопироватьРекурсивно(ЭлементСоответствия.Значение));
		КонецЦикла;
		
	КонецЕсли;
	
	ЗначенияЗаменыПолейКлассификатора = Новый ФиксированноеСоответствие(ЗначенияЗамены);
	
	ЗаполнитьКлассификатор(ПолныйПутьКМакету, ИмяСправочника);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗагруженВесьКлассификатор() Тогда
		
		Отказ = Истина;
		СообщитьОЗагруженностиВсегоКлассификатора();
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы Тогда
		
		Если ЗначениеЗаполнено(ДобавленныеОбъекты) Тогда
			Отказ = Истина;
			ДобавленныеСсылки = Новый Соответствие;
			Для Каждого ДанныеОбъекта Из ДобавленныеОбъекты Цикл
				ДобавленныеСсылки.Вставить(ДанныеОбъекта.Значение, ДанныеОбъекта.Представление);
			КонецЦикла;
			ДобавленныеОбъекты.Очистить();
			Закрыть(ДобавленныеСсылки);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКлассификатор

// Загружает Выбранную строку классификатора.
//
&НаКлиенте
Процедура КлассификаторВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элемент.ТекущиеДанные.ПолучитьЭлементы().Количество() > 0 Тогда
		Если Элементы.Классификатор.Развернут(Элемент.ТекущаяСтрока) Тогда
			Элементы.Классификатор.Свернуть(Элемент.ТекущаяСтрока);
		Иначе
			Элементы.Классификатор.Развернуть(Элемент.ТекущаяСтрока);
		КонецЕсли;
	Иначе
		ЗагрузитьСтрокуКлассификатораПоИдентификатору(Элемент.ТекущаяСтрока);
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	ЗагрузитьТекущий();
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьВсе(Команда)
	ЗагрузитьВсе();
	СообщитьОЗагруженностиВсегоКлассификатора();
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СообщитьОЗагруженностиВсегоКлассификатора()
	ТекстПредупреждения = НСтр("ru = 'Загружен весь классификатор';
								|en = 'All classifier is imported'");
	ПоказатьПредупреждение(, ТекстПредупреждения, , НСтр("ru = 'Загрузка классификатора';
														|en = 'Import classifier'"));
КонецПроцедуры

// Загружает классификатор из макета в дерево значений.
//
&НаСервере
Процедура ЗаполнитьКлассификатор(ПолныйПутьКМакету, ИмяЗагружаемогоСправочника)
	
	ОтображатьЭлементыУправлениеИерархией = Ложь;
	
	РанееЗагруженные  = Новый Соответствие;
	Запрос = Новый Запрос;
	
	ОписаниеПолейЗапроса = "";
	
	РеквизитКод = СоответствиеПолейКлассификатораРеквизитам.Получить("Code");
	ЕстьКод = НЕ ПустаяСтрока(РеквизитКод);
	Если ЕстьКод Тогда
		ОписаниеПолейЗапроса = "Классификатор." + РеквизитКод + " КАК Код";
	КонецЕсли;
	
	РеквизитНаименование = СоответствиеПолейКлассификатораРеквизитам.Получить("Name");
	ЕстьНаименование = НЕ ПустаяСтрока(РеквизитНаименование);
	Если ЕстьНаименование Тогда
		ОписаниеПолейЗапроса = ?(ПустаяСтрока(ОписаниеПолейЗапроса), "", ОписаниеПолейЗапроса + "," + Символы.ПС) +
			"Классификатор." + РеквизитНаименование + " КАК Наименование";
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ Классификатор.Ссылка Как СсылкаНаЭлементКлассификатора,
	|	&ОписаниеПолейЗапроса
	|ИЗ
	|	Справочник." + ИмяЗагружаемогоСправочника + " КАК Классификатор";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОписаниеПолейЗапроса", ОписаниеПолейЗапроса);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокаИдентификатор = "";
		
		Если ЕстьКод Тогда
			СтрокаИдентификатор = СокрЛП(Выборка.Код);
		КонецЕсли; 
		
		Если ЕстьНаименование Тогда
			СтрокаИдентификатор = СтрокаИдентификатор + СокрЛП(Выборка.Наименование);
		КонецЕсли; 
		
		Если ПустаяСтрока(СтрокаИдентификатор) Тогда
			Продолжить;
		КонецЕсли;
		
		РанееЗагруженные.Вставить(СтрокаИдентификатор, Выборка.СсылкаНаЭлементКлассификатора);
		
	КонецЦикла;
	
	КлассификаторXML = ПолучитьМакетКлассификатора(ПолныйПутьКМакету).ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	ЕстьПолеКод = КлассификаторТаблица.Колонки.Найти("Code") <> Неопределено;
	ЕстьПолеУровень = КлассификаторТаблица.Колонки.Найти("Level") <> Неопределено;
	ЕстьПолеНаименование = КлассификаторТаблица.Колонки.Найти("Name") <> Неопределено;
	
	ДобавляемыеКолонки = Новый Массив;
	Для Каждого Колонка Из КлассификаторТаблица.Колонки Цикл
		
		Если Колонка.Имя = "Code"
			Или Колонка.Имя = "Level"
			Или Колонка.Имя = "Name" Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		ДобавляемыеКолонки.Добавить(Колонка.Имя);
		
	КонецЦикла;
	
	ИменаДобавляемыхКолонок = Новый ФиксированныйМассив(ДобавляемыеКолонки);
	
	Если ИменаДобавляемыхКолонок.Количество() > 0 Тогда
		
		ДобавляемыеРеквизиты = Новый Массив;
		Для Каждого ИмяКолонки Из ИменаДобавляемыхКолонок Цикл
			ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяКолонки, Новый ОписаниеТипов("Строка"), "Классификатор", ЗаголовкиПолейКлассификатора.Получить(ИмяКолонки)));
		КонецЦикла;
		
		СуществующиеРеквизиты = Новый Массив;
		ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтотОбъект, СуществующиеРеквизиты);
		
		ЗарплатаКадры.ИзменитьРеквизитыФормы(ЭтотОбъект, ДобавляемыеРеквизиты, СуществующиеРеквизиты);
		
		Для Каждого ИмяКолонки Из ИменаДобавляемыхКолонок Цикл
			
			Элемент = Элементы.Добавить("Классификатор" + ИмяКолонки, Тип("ПолеФормы"), Элементы.Классификатор);
			Элемент.Вид = ВидПоляФормы.ПолеВвода;
			Элемент.ТолькоПросмотр = Истина;
			Элемент.ПутьКДанным = "Классификатор." + ИмяКолонки;
			
		КонецЦикла;
		
	КонецЕсли;
	
	МассивРодителей = Новый Массив(50);
	ТекущийУровень = 0;
	
	Для Каждого СтрокаКлассификаторТаблица Из КлассификаторТаблица Цикл
		
		Если ЕстьПолеУровень Тогда
			
			ТекущийУровень = Число(СтрокаКлассификаторТаблица.Level);
			Если ТекущийУровень = 0 Тогда
				КоллекцияРодителя = Классификатор.ПолучитьЭлементы();
			Иначе 
				
				Родитель = МассивРодителей[ТекущийУровень - 1];
				Родитель.ИндексКартинки = 0;
				КоллекцияРодителя = Родитель.ПолучитьЭлементы();
				ОтображатьЭлементыУправлениеИерархией = Истина;
				
			КонецЕсли;
			
		Иначе
			КоллекцияРодителя = Классификатор.ПолучитьЭлементы();
		КонецЕсли; 
		
		СтрокаИдентификатор = "";
		НоваяЗаписьКлассификатора = КоллекцияРодителя.Добавить();
		
		Для каждого ЭлементСоответствия Из СоответствиеПолейКлассификатораРеквизитам Цикл
			
			Если НЕ ПустаяСтрока(ЭлементСоответствия.Значение) Тогда
				Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаКлассификаторТаблица, ЭлементСоответствия.Ключ) Тогда
					НоваяЗаписьКлассификатора.ЗначенияПолейСтрокиКлассификатора.Добавить(
						СтрокаКлассификаторТаблица[ЭлементСоответствия.Ключ],
						ЭлементСоответствия.Значение);
				КонецЕсли;
			КонецЕсли;
				
		КонецЦикла;
		
		Если ЕстьПолеКод Тогда
			
			НоваяЗаписьКлассификатора.Код = СокрЛП(СтрокаКлассификаторТаблица.Code);
			Если ЕстьКод Тогда
				СтрокаИдентификатор = НоваяЗаписьКлассификатора.Код;
			КонецЕсли; 
			
		КонецЕсли;
		
		Если ЕстьПолеНаименование Тогда
			
			НоваяЗаписьКлассификатора.Наименование = СокрЛП(СтрокаКлассификаторТаблица.Name);
			СтрокаИдентификатор = СтрокаИдентификатор + НоваяЗаписьКлассификатора.Наименование;
			
		КонецЕсли;
		
		НоваяЗаписьКлассификатора.ИндексКартинки = 2;
		
		СсылкаНаЭлементКлассификатора = РанееЗагруженные.Получить(СтрокаИдентификатор);
		Если СсылкаНаЭлементКлассификатора <> Неопределено Тогда
			НоваяЗаписьКлассификатора.Загружен = Истина;
			НоваяЗаписьКлассификатора.СсылкаНаЭлементКлассификатора = СсылкаНаЭлементКлассификатора;
		КонецЕсли;
		
		Для Каждого ИмяКолонки Из ИменаДобавляемыхКолонок Цикл
			НоваяЗаписьКлассификатора[ИмяКолонки] = СтрокаКлассификаторТаблица[ИмяКолонки];
		КонецЦикла;
		
		МассивРодителей[ТекущийУровень] = НоваяЗаписьКлассификатора;
		
	КонецЦикла;
	
	Элементы.КлассификаторДерево.Видимость = ОтображатьЭлементыУправлениеИерархией;
	Элементы.КлассификаторСписок.Видимость = ОтображатьЭлементыУправлениеИерархией;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМакетКлассификатора(ПолныйПутьКМакету)
	
	ЧастиПути = СтрЗаменить(ПолныйПутьКМакету, ".", Символы.ПС);
	
	Если СтрЧислоСтрок(ЧастиПути) = 3 Тогда
		ПутьКМетаданным = СтрПолучитьСтроку(ЧастиПути, 1) + "." + СтрПолучитьСтроку(ЧастиПути, 2);
		ПутьКОбъектуМетаданных = СтрПолучитьСтроку(ЧастиПути, 3);
	ИначеЕсли СтрЧислоСтрок(ЧастиПути) = 2 Тогда
		ПутьКМетаданным = СтрПолучитьСтроку(ЧастиПути, 1);
		ПутьКОбъектуМетаданных = СтрПолучитьСтроку(ЧастиПути, 2);
	Иначе
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Макет классификатора ""%1"" не найден. Операция прервана.';
																						|en = 'Template of the ""%1"" classifier is not found. The operation is stopped.'"), ПолныйПутьКМакету);
	КонецЕсли;
	
	Если СтрЧислоСтрок(ЧастиПути) = 3 Тогда
		Макет = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПутьКМетаданным).ПолучитьМакет(ПутьКОбъектуМетаданных);
	Иначе
		Макет = ПолучитьОбщийМакет(ПутьКОбъектуМетаданных);
	КонецЕсли;
	
    Возврат Макет;
	
КонецФункции

// Загружает весь классификатор.
&НаСервере
Процедура ЗагрузитьВсе()
	ЗагрузитьСтрокуКлассификатора(Классификатор);
КонецПроцедуры

// Загружает отмеченные строки классификатора.
//
&НаСервере
Процедура ЗагрузитьТекущий()
	Для Каждого ВыделеннаяСтрока Из Элементы.Классификатор.ВыделенныеСтроки Цикл
		ЗагрузитьСтрокуКлассификатораПоИдентификатору(ВыделеннаяСтрока);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСтрокуКлассификатораПоИдентификатору(ИдентификаторТекущейСтроки)
	НайденнаяСтрока = Классификатор.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
	Если НайденнаяСтрока <> Неопределено Тогда
		ЗагрузитьСтрокуКлассификатора(НайденнаяСтрока);
	КонецЕсли; 
КонецПроцедуры

// Загружает строку классификатора, если передана строка, содержащая строки
// производит рекурсивную загрузку этих строк.
&НаСервере
Процедура ЗагрузитьСтрокуКлассификатора(ЗагружаемыйЭлемент, ЗагружатьВложенные = Истина, ЭтоГруппа = Ложь)
	
	Если ЗагружатьВложенные И ЗагружаемыйЭлемент.ПолучитьЭлементы().Количество() > 0 Тогда
		
		Для Каждого СтрокаКоллекции Из ЗагружаемыйЭлемент.ПолучитьЭлементы() Цикл
			ЗагрузитьСтрокуКлассификатора(СтрокаКоллекции);
		КонецЦикла;
		
	ИначеЕсли НЕ ЗагружаемыйЭлемент.Загружен Тогда
		
		ПроверкаКодаНаименования = Новый Структура("Код,Наименование", "ДлинаКода", "ДлинаНаименования");
		
		ИспользованиеРеквизитов = Новый Массив;
		ИспользованиеРеквизитов.Добавить(Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппыИЭлемента);
		Если ЭтоГруппа Тогда
			НовыйОбъект = Справочники[ИмяСправочника].СоздатьГруппу();
			ИспользованиеРеквизитов.Добавить(Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляГруппы);
		Иначе
			НовыйОбъект = Справочники[ИмяСправочника].СоздатьЭлемент();
			ИспользованиеРеквизитов.Добавить(Метаданные.СвойстваОбъектов.ИспользованиеРеквизита.ДляЭлемента);
		КонецЕсли;
		
		Если ЗагружатьИерархию Тогда
			ЭлементРодитель = ЗагружаемыйЭлемент.ПолучитьРодителя();
			Если Не ЭлементРодитель = Неопределено Тогда
				СсылкаНаЭлементРодителя = ЭлементРодитель.СсылкаНаЭлементКлассификатора;
				Если Не ЗначениеЗаполнено(СсылкаНаЭлементРодителя) Тогда
					ЗагрузитьСтрокуКлассификатора(ЭлементРодитель, Ложь, Истина);
					СсылкаНаЭлементРодителя = ЭлементРодитель.СсылкаНаЭлементКлассификатора;
				КонецЕсли;
				НовыйОбъект.Родитель = ЭлементРодитель.СсылкаНаЭлементКлассификатора;
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого ЭлементСписка Из ЗагружаемыйЭлемент.ЗначенияПолейСтрокиКлассификатора Цикл
			
			Если ПроверкаКодаНаименования.Свойство(ЭлементСписка.Представление)
				И Метаданные.Справочники[ИмяСправочника][ПроверкаКодаНаименования[ЭлементСписка.Представление]] > 0 Тогда
				
				НовыйОбъект[ЭлементСписка.Представление] = ЭлементСписка.Значение;
				
			ИначеЕсли Не Метаданные.Справочники[ИмяСправочника].Реквизиты.Найти(ЭлементСписка.Представление) = Неопределено
				И Не ИспользованиеРеквизитов.Найти(Метаданные.Справочники[ИмяСправочника].Реквизиты[ЭлементСписка.Представление].Использование) = Неопределено Тогда
				
				Если Не ЗначенияЗаменыПолейКлассификатора.Получить(ЭлементСписка.Представление) = Неопределено 
					И Не ЗначенияЗаменыПолейКлассификатора[ЭлементСписка.Представление].Получить(ЭлементСписка.Значение) = Неопределено Тогда
					НовыйОбъект[ЭлементСписка.Представление] = ЗначенияЗаменыПолейКлассификатора[ЭлементСписка.Представление].Получить(ЭлементСписка.Значение);
				Иначе
					НовыйОбъект[ЭлементСписка.Представление] = ЭлементСписка.Значение;
				КонецЕсли;
					
			КонецЕсли;
		КонецЦикла;
		
		Если ЗаполнятьДополнительныеСведения И Не ЭтоГруппа Тогда
			
			ДополнительныеСведения = Новый Структура;
			Для Каждого ИмяКолонки Из ИменаДобавляемыхКолонок Цикл
				ДополнительныеСведения.Вставить(ИмяКолонки, ЗагружаемыйЭлемент[ИмяКолонки]);
			КонецЦикла;
			
			Для каждого ЭлементСоответствия Из СоответствиеПолейКлассификатораРеквизитам Цикл
				
				Если ПустаяСтрока(ЭлементСоответствия.Значение) Тогда
					
					Если ЭлементСоответствия.Ключ = "Code" Тогда
						ДополнительныеСведения.Вставить("Код", ЗагружаемыйЭлемент["Код"]);
					ИначеЕсли ЭлементСоответствия.Ключ = "Name" Тогда
						ДополнительныеСведения.Вставить("Наименование", ЗагружаемыйЭлемент["Наименование"]);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если ДополнительныеСведения.Количество() > 0 Тогда
				НовыйОбъект.ЗаполнитьДополнительныеСведенияКлассификатора(ДополнительныеСведения);
			КонецЕсли;
			
		КонецЕсли;
		
		НовыйОбъект.ДополнительныеСвойства.Вставить("ПодборИзКлассификатора");
		
		Попытка
			НовыйОбъект.Записать();
			ЗагружаемыйЭлемент.Загружен = Истина;
			ЗагружаемыйЭлемент.СсылкаНаЭлементКлассификатора = НовыйОбъект.Ссылка;
			Если ЗначениеЗаполнено(ЗагружаемыйЭлемент.Код) Тогда
				ДобавленныеОбъекты.Добавить(ЗагружаемыйЭлемент.СсылкаНаЭлементКлассификатора, ЗагружаемыйЭлемент.Код);
			Иначе
				ДобавленныеОбъекты.Добавить(ЗагружаемыйЭлемент.СсылкаНаЭлементКлассификатора, ЗагружаемыйЭлемент.Наименование);
			КонецЕсли;
		Исключение
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось загрузить элемент %1 (код: %2)';
					|en = 'Cannot import item %1 (code: %2)'"),
				ЗагружаемыйЭлемент.Наименование,
				ЗагружаемыйЭлемент.Код);
		КонецПопытки;
			
	КонецЕсли;
	
КонецПроцедуры

// Проверяет, на предмет загрузки всего классификатора.
//
&НаКлиенте
Функция ЗагруженВесьКлассификатор()
	Возврат ЗагруженыСтроки(Классификатор.ПолучитьЭлементы());
КонецФункции

// Проверяет коллекцию строк на предмет загруженности.
//
&НаКлиенте
Функция ЗагруженыСтроки(КоллекцияСтрок)
	Для Каждого СтрокаКоллекции Из КоллекцияСтрок Цикл
		Если СтрокаКоллекции.ПолучитьЭлементы().Количество() > 0 Тогда
			РезультатПроверки = ЗагруженыСтроки(СтрокаКоллекции.ПолучитьЭлементы());
		Иначе
			РезультатПроверки = СтрокаКоллекции.Загружен;
		КонецЕсли;
		Если НЕ РезультатПроверки Тогда
			Возврат Ложь;
		КонецЕсли; 
	КонецЦикла;
	Возврат Истина;
КонецФункции

#КонецОбласти
