#Область ОбработчикиСобытий

Функция pingGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаПроверкаСоединения(СтруктураОтвета);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	Если ИспользоватьСерверЛояльности Тогда
		
		СтруктураОтвета.Connection = Истина;
		
	Иначе
		
		СтруктураОтвета.Error = Истина;
		СтруктураОтвета.ErrorMessage = НСтр("ru = 'Использование сервера лояльности отключено в настройках программы.';
											|en = 'Loyalty server is disabled in the application settings.'");
		
	КонецЕсли;
	
	СтруктураОтветаJSON = Новый ЗаписьJSON;
	СтруктураОтветаJSON.УстановитьСтроку();
	ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
	
	Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьБонусыКлиентаGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаБонусыКлиента(СтруктураОтвета);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		
		Возврат Ответ;
		
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
		
		Если КартаКлиента <> Неопределено Тогда
			
			СтруктураОтвета.ClientNotFound = Ложь;
			БонусыЗаблокированы = СерверЛояльностиПоставщикДанных.БонусыЗаблокированы(КартаКлиента);
			
			Если ПараметрыМетода.ТребуетсяБлокировкаБонусов
				И Не БонусыЗаблокированы Тогда
				СерверЛояльностиПоставщикДанныхПереопределяемый.ЗаполнитьДанныеОБонусах(КартаКлиента, СтруктураОтвета);
				Если СтруктураОтвета.BonusCount > 0 Тогда
					СерверЛояльностиПоставщикДанных.ЗаблокироватьБонусы(КартаКлиента, ТекущаяДатаСеанса());
				КонецЕсли;
			КонецЕсли;
			
			СерверЛояльностиПоставщикДанныхПереопределяемый.ЗаполнитьСтруктуруОтветаНомерамиКарты(КартаКлиента, СтруктураОтвета);
			
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());

		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ПолучитьБонусыКлиента");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция РазблокироватьБонусыКлиентаPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	Попытка
		
		ИспользоватьСерверЛояльности = Ложь;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
		Если Не ИспользоватьСерверЛояльности Тогда
			Возврат Ответ;
		КонецЕсли;
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
		
		Если КартаКлиента <> Неопределено Тогда
			СерверЛояльностиПоставщикДанных.РазблокироватьБонусы(КартаКлиента);
			ТекстСообщения = НСтр("ru = 'Бонусы разблокированы';
									|en = 'Bonus points are unlocked'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Карта клиента не найдена';
									|en = 'Customer card is not found'");
		КонецЕсли;
		
		Ответ.УстановитьТелоИзСтроки(ТекстСообщения);
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "РазблокироватьБонусыКлиента");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция СписатьБонусыКлиентаPOST(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанных.ДополнитьПараметрамиМетодаБонусы(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
		
		Если КартаКлиента <> Неопределено Тогда 
			Если ЗначениеЗаполнено(ПараметрыМетода.КоличествоБонусныхБаллов) Тогда
				СерверЛояльностиПоставщикДанных.СписатьНачислитьБонусы(КартаКлиента, ПараметрыМетода.КоличествоБонусныхБаллов, 0);
				ТекстСообщения = НСтр("ru = 'Бонусы успешно списаны';
										|en = 'Bonus points are written off'");
			Иначе
				ТекстСообщения = НСтр("ru = 'Количество баллов не указано';
										|en = 'Number of bonus points is not specified'");
			КонецЕсли;
		Иначе 
			ТекстСообщения = НСтр("ru = 'Карта клиента не найдена';
									|en = 'Customer card is not found'");
		КонецЕсли;
		
		Ответ.УстановитьТелоИзСтроки(ТекстСообщения);
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке,
			"СписатьБонусыКлиента");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция ВыдатьКартуКлиентуPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаКартаКлиента(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанных.ДополнитьПараметрамиМетодаВыдатьКарту(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПроверитьДанныеКартыКлиента(ПараметрыМетода, СтруктураОтвета);
		
		Если СтруктураОтвета.Error = Ложь Тогда
			
			СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
			КартаСуществует = ЗначениеЗаполнено(КартаКлиента);
			ДругойКлиент = Неопределено;
			
			Если Не КартаСуществует Тогда
				КартаКлиента = Неопределено;
				ОписаниеОшибки = "";
				СерверЛояльностиПоставщикДанныхПереопределяемый.СоздатьКартуКлиента(ПараметрыМетода, КартаКлиента, ДругойКлиент, ОписаниеОшибки);
			КонецЕсли;

			Если ЗначениеЗаполнено(КартаКлиента) Тогда
				СерверЛояльностиПоставщикДанныхПереопределяемый.ЗаполнитьСтруктуруОтветаДаннымиКарты(КартаКлиента, СтруктураОтвета, КартаСуществует);
			ИначеЕсли ЗначениеЗаполнено(ДругойКлиент) Тогда
				СерверЛояльностиПоставщикДанныхПереопределяемый.ЗаполнитьСтруктуруОтветаДаннымиКлиента(ДругойКлиент, СтруктураОтвета);
			Иначе
				СтруктураОтвета.Error = Истина;
				Если ПустаяСтрока(ОписаниеОшибки) Тогда
					СтруктураОтвета.ErrorMessage = НСтр("ru = 'Не удалось создать карту клиента';
														|en = 'Cannot create a customer card'");
				Иначе
					СтруктураОтвета.ErrorMessage = ОписаниеОшибки;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ВыдатьКартуКлиенту");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ИнформацияОЗапретахПродажGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаИнформацияОЗапретеПродаж(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	ПараметрыМетода = Неопределено;
	СерверЛояльностиПоставщикДанных.ПараметрыМетодаЗапретыПродаж(Запрос, ПараметрыМетода);
	
	Попытка
		
		СтруктураОтвета = Неопределено;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ДанныеОЗапретахПродаж(ПараметрыМетода, СтруктураОтвета);
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ИнформацияОЗапретахПродаж");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьДанныеПодарочногоСертификатаGET(Запрос)
	
	Попытка
		
		Ответ = Новый HTTPСервисОтвет(200);
		
		ИспользоватьСерверЛояльности = Ложь;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
		
		СтруктураОтвета 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаДанныеПодарочногоСертификата(СтруктураОтвета);
		
		Если Не ИспользоватьСерверЛояльности Тогда
			СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
			Возврат Ответ;
		КонецЕсли;
		
		ПараметрыМетода 	= Неопределено;
		ДанныеСертификата 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
	
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиПодарочныйСертификат(ПараметрыМетода, ДанныеСертификата);
		
		Если ДанныеСертификата.ПодарочныйСертификат <> Неопределено Тогда
			
			СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьБалансПодарочногоСертификата(ДанныеСертификата, СтруктураОтвета);
			
		Иначе
			
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = НСтр("ru = 'Подарочный сертификат не найден';
												|en = 'Gift card is not found'");
		КонецЕсли;
		
		// Возвращаем структуру ответа 
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ПолучитьДанныеПодарочногоСертификата");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьДанныеПодарочногоСертификатаPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
		
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаДанныеПодарочногоСертификата(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	
	ВходящиеДанные = Запрос.ПолучитьТелоКакСтроку();
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ЕстьОшибки", Ложь);
	ПараметрыМетода.Вставить("ОписаниеОшибки", "");
	
	Попытка
		
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПараметрыМетодаИзЗапросаПолучитьДанныеПодарочногоСертификата(ВходящиеДанные, ПараметрыМетода);
		
		Если ПараметрыМетода.ЕстьОшибки Тогда
			
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = ПараметрыМетода.ОписаниеОшибки;
			
		Иначе
			
			СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьБалансПодарочногоСертификата(ПараметрыМетода, СтруктураОтвета);
			
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке,
			"ПараметрыМетодаИзЗапросаПолучитьДанныеПодарочногоСертификата");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьДанныеПромокодаGET(Запрос)
	
	Попытка
		
		Ответ = Новый HTTPСервисОтвет(200);
		
		ИспользоватьСерверЛояльности = Ложь;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
		
		СтруктураОтвета 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаДанныеПромокода(СтруктураОтвета);
		
		Если Не ИспользоватьСерверЛояльности Тогда
			СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
			Возврат Ответ;
		КонецЕсли;
		
		ПараметрыМетода 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаПромокода(Запрос, ПараметрыМетода);
		
		Если ПараметрыМетода.Промокод <> Неопределено Тогда
			СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьСостояниеПромокода(ПараметрыМетода, СтруктураОтвета, Истина);
		КонецЕсли;
		
		// Возвращаем структуру ответа 
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ПолучитьДанныеПромокода");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция РазблокироватьПодарочныйСертификатPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода   = Неопределено;
		ДанныеСертификата = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиПодарочныйСертификат(ПараметрыМетода, ДанныеСертификата);
		
		Если ДанныеСертификата.ПодарочныйСертификат <> Неопределено Тогда
			СерверЛояльностиПоставщикДанных.РазблокироватьПодарочныйСертификат(ДанныеСертификата);
			ТекстСообщения = НСтр("ru = 'Подарочный сертификат разблокирован';
									|en = 'Gift card is unlocked'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Подарочный сертификат не найден';
									|en = 'Gift card is not found'");
		КонецЕсли;
		
		Ответ.УстановитьТелоИзСтроки(ТекстСообщения);
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "РазблокироватьПодарочныйСертификат");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции 

Функция РазблокироватьПромокодPOST(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода   = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
		
		СерверЛояльностиПоставщикДанных.СнятьРезервПримененияПромокода(ПараметрыМетода.Промокод);
		ТекстСообщения = НСтр("ru = 'Промокод разблокирован.';
								|en = 'Promo code is unlocked.'");
			
		Ответ.УстановитьТелоИзСтроки(ТекстСообщения);
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке,
			"РазблокироватьПромокод");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция СписатьПодарочныйСертификатPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода   = Неопределено;
		ДанныеСертификата = Неопределено;
		
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанных.ДополнитьПараметрамиМетодаСписатьПодарочныйСертификат(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиПодарочныйСертификат(ПараметрыМетода, ДанныеСертификата);
		
		Если ДанныеСертификата.ПодарочныйСертификат <> Неопределено Тогда
			СерверЛояльностиПоставщикДанных.СкорректироватьБалансПодарочногоСертификата(ДанныеСертификата,
				ПараметрыМетода.СуммаСписания, 0);
			ТекстСообщения = НСтр("ru = 'Подарочный сертификат успешно списан на указанную сумму';
									|en = 'Gift card is written off for the specified amount'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Подарочный сертификат не найден';
									|en = 'Gift card is not found'");
		КонецЕсли;
		Ответ.УстановитьТелоИзСтроки(ТекстСообщения);
	
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке,
			"СписатьПодарочныйСертификат");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ИспользоватьАвтоматическиеСкидкиGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаИспользоватьАвтоматическиеСкидки(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьАвтоматическиеСкидки(СтруктураОтвета.UseAutomaticDiscounts);
		СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьБонусыКонтрагентов(СтруктураОтвета.UseCounterpartyBonuses);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
	
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
	
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ЗапросИспользованияАвтоматическихСкидок");
		
	КонецПопытки;
		
	Возврат Ответ;
	
КонецФункции

Функция РассчитатьАвтоматическиеСкидкиGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаРасчетСкидок(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	ИспользоватьАвтоматическиеСкидки = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьАвтоматическиеСкидки(ИспользоватьАвтоматическиеСкидки);
	
	Если Не ИспользоватьАвтоматическиеСкидки Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветАвтоматическиеСкидки(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	ВходящиеДанные		= Запрос.ПолучитьТелоКакСтроку();
	ПараметрыРасчета 	= Неопределено;
 	СерверЛояльностиПоставщикДанныхПереопределяемый.ДесериализоватьПараметрыРасчета(ВходящиеДанные, ПараметрыРасчета);
	
	Попытка
	
		Если ПараметрыРасчета.ЕстьОшибки Тогда
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = ПараметрыРасчета.ОписаниеОшибки;
		Иначе
			СерверЛояльностиПоставщикДанныхПереопределяемый.РассчитатьАвтоматическиеСкидки(ПараметрыРасчета, СтруктураОтвета);
			
			Если ПараметрыРасчета.ВернутьДанныеКарты И ПараметрыРасчета.БлокироватьКарту = "1"
					И Не СтруктураОтвета.LoyaltyCardDescription.CardNotFound Тогда
					
				ДанныеКарты = СтруктураОтвета.LoyaltyCardDescription.LoyaltyCard;
				Если ДанныеКарты.BonusData.BonusCount > 0 И Не ДанныеКарты.IsBlocked Тогда
					СерверЛояльностиПоставщикДанных.ЗаблокироватьБонусы(ПараметрыРасчета.ДисконтнаяКарта, ТекущаяДатаСеанса());
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
	
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке,
			"РассчитатьАвтоматическиеСкидки");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция РассчитатьАвтоматическиеСкидкиPOST(Запрос)
	
	Ответ = РассчитатьАвтоматическиеСкидкиGET(Запрос);
	
	Возврат Ответ;
	
КонецФункции

Функция ПроверитьПодарочныйСертификатGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаПроверкаПодарочногоСертификат(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПроверитьПодарочныйСертификат(ПараметрыМетода, СтруктураОтвета);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
	
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке,
			"ПроверкаПодарочногоСертификата");
		
	КонецПопытки;
		
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьУправляемыеСкидкиGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыРасчета 	= Неопределено;
		СтруктураОтвета = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаУправляемыеСкидки(Запрос, ПараметрыРасчета);
		
		СерверЛояльностиПоставщикДанных.СписокУправляемыхСкидок(СтруктураОтвета, ПараметрыРасчета);
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке,
			"СписокУправляемыхСкидок");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьСкидкиПоВидамОплатыGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		СтруктураОтвета = Неопределено;
		СерверЛояльностиПоставщикДанных.СписокСкидокПоВидамОплаты(СтруктураОтвета);
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке,
			"СписокСкидокПоВидамОплаты");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьОстаткиGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаОстатки(СтруктураОтвета);
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ВходящиеДанные = Запрос.ПолучитьТелоКакСтроку();
		ПараметрыПолученияОстатков = Неопределено;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПараметрыИзЗапросаПолучитьОстатки(ВходящиеДанные, ПараметрыПолученияОстатков);
		
		
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьОстатки(ПараметрыПолученияОстатков, СтруктураОтвета);
		
		// Возвращаем структуру ответа 
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ПолучитьОстатки");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьОстаткиPOST(Запрос)
	
	Ответ = ПолучитьОстаткиGET(Запрос);
	
	Возврат Ответ;
	
КонецФункции

Функция НайтиКартуКлиентаGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаСписокКартЛояльности(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		КартаКлиента = Неопределено;
		
		СерверЛояльностиПоставщикДанных.ПараметрыМетодаИзЗапросаНайтиКартуКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартыЛояльности(ПараметрыМетода, СтруктураОтвета);
		
		Если ПараметрыМетода.БлокироватьКарту = "1" И СтруктураОтвета.LoyaltyCardsList.Количество() = 1 
				И СтруктураОтвета.LoyaltyCardsList[0].BonusData.BonusCount > 0
				И Не СтруктураОтвета.LoyaltyCardsList[0].IsBlocked Тогда
			
			ГУИДКартыСтрокой = СтруктураОтвета.LoyaltyCardsList[0].CardGUID;
			СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьКартуКлиентаПоУникальномуИдентификатору(ГУИДКартыСтрокой,
				КартаКлиента);
			СерверЛояльностиПоставщикДанных.ЗаблокироватьБонусы(КартаКлиента, ТекущаяДатаСеанса());
			
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "НайтиКартуКлиента");
		
	КонецПопытки;
		
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьВидыКартGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
		
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		СтруктураОтвета = Неопределено;
		
		ПараметрыРасчета 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаВидыДисконтныхКарт(Запрос, ПараметрыРасчета);
		
		СерверЛояльностиПоставщикДанных.СписокВидовДисконтныхКарт(СтруктураОтвета, ПараметрыРасчета);
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ПолучитьВидыКарт");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьОборотыПродажGET(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаОборотыПродаж(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
		
		Если КартаКлиента <> Неопределено Тогда
			
			СтруктураОтвета.ClientNotFound = Ложь;
			
			СерверЛояльностиПоставщикДанныхПереопределяемый.ЗаполнитьДанныеОбОборотахПродаж(КартаКлиента, СтруктураОтвета);
			
			СерверЛояльностиПоставщикДанныхПереопределяемый.ЗаполнитьСтруктуруОтветаНомерамиКарты(КартаКлиента, СтруктураОтвета);
			
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ПолучитьОборотыПродаж");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция ПолучитьДанныеПродавцовGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	СтруктураОтвета = Неопределено;
	
	Попытка
		
		ПараметрыРасчета 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаСпискаПродавцов(Запрос, ПараметрыРасчета);
		СерверЛояльностиПоставщикДанных.СписокСотрудников(ПараметрыРасчета, СтруктураОтвета);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "СписокПродавцов");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьСерииПоОтборуGET(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	
	СтруктураОтвета = Неопределено;

	Попытка
		
		ПараметрыРасчета 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаСпискаСерий(Запрос, ПараметрыРасчета);
		СерверЛояльностиПоставщикДанных.ЗаполнитьСписокСерий(ПараметрыРасчета, СтруктураОтвета);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ПолучитьСписокСерийПоТовару");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция ПолучитьДанныеСерииGET(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаДанныеСерии(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаДанныеСерии(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьДанныеСерии(ПараметрыМетода, СтруктураОтвета);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
	
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ПолучитьДанныеСерии");
		
	КонецПопытки;
		
	Возврат Ответ;
КонецФункции

// ++ Локализация
Функция ОбработатьКодМаркировкиPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ВходящиеДанные = Запрос.ПолучитьТелоКакСтроку();
		ПараметрыОбработкиКодаМаркировки = Неопределено;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПараметрыИзЗапросаОбработатьКодМаркировки(ВходящиеДанные, ПараметрыОбработкиКодаМаркировки);
		
		РезультатОтвета = Неопределено;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ОбработатьКодМаркировки(ПараметрыОбработкиКодаМаркировки, РезультатОтвета);
		
		// Возвращаем структуру ответа 
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, РезультатОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ОбработатьКодМаркировки");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьТокенАвторизацииГосИСPOST(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ВходящиеДанные = Запрос.ПолучитьТелоКакСтроку();
		ПараметрыПолученияТокенаАвторизации = Неопределено;
		СерверЛояльностиПоставщикДанных.ПолучитьПараметрыТокенаАвторизацииГосИС(ПараметрыПолученияТокенаАвторизации);
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПараметрыИзЗапросаПолучитьТокенАвторизацииГосИС(ВходящиеДанные, ПараметрыПолученияТокенаАвторизации);
		
		РезультатОтвета = Неопределено;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьТокенАвторизацииГосИС(ПараметрыПолученияТокенаАвторизации, РезультатОтвета);
		
		// Возвращаем структуру ответа 
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, РезультатОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ПолучитьТокенАвторизацииГосИС");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции
// -- Локализация

Функция ПолучитьАктуальнуюЦенуGET(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаЗапросаЦены(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаАктуальнойЦены(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПолучитьАктуальнуюЦену(ПараметрыМетода, СтруктураОтвета);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ПолучитьАктуальнуюЦену");
		
	КонецПопытки;
		
	Возврат Ответ;
	
КонецФункции

Функция ОбъединитьБонусныеКартыPOST(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаТекстовоеСообщение(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	ВходящиеДанные = Запрос.ПолучитьТелоКакСтроку();
	ПараметрыМетода = Неопределено;
	
	Попытка
		СерверЛояльностиПоставщикДанныхПереопределяемый.ПараметрыМетодаИзЗапросаОбъединитьБонусныеКарты(ВходящиеДанные, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.ОбъединитьБонусныеКарты(ПараметрыМетода, СтруктураОтвета);
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "ОбъединитьБонусныеКарты");
		
	КонецПопытки;

	
	Возврат Ответ;
КонецФункции

#Область СерверЛояльности_3_0_11

Функция РазблокироватьБонусыКлиентаPOST3_0(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	Попытка
		
		ИспользоватьСерверЛояльности = Ложь;
		СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
		
		СтруктураОтвета = Неопределено;
		СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаТекстовоеСообщение(СтруктураОтвета);
		
		Если Не ИспользоватьСерверЛояльности Тогда
			СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
			Возврат Ответ;
		КонецЕсли;
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
		
		Если КартаКлиента <> Неопределено Тогда
			СерверЛояльностиПоставщикДанных.РазблокироватьБонусы(КартаКлиента);
			ТекстСообщения = НСтр("ru = 'Бонусы разблокированы';
									|en = 'Bonus points are unlocked'");
			СтруктураОтвета.TextMessage = ТекстСообщения;
		Иначе
			ТекстСообщения = НСтр("ru = 'Карта клиента не найдена';
									|en = 'Customer card is not found'");
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = ТекстСообщения;
		КонецЕсли;
		
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "РазблокироватьБонусыКлиента");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция СписатьБонусыКлиентаPOST3_0(Запрос)
		
	Ответ = Новый HTTPСервисОтвет(200);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаТекстовоеСообщение(СтруктураОтвета);
		
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанных.ДополнитьПараметрамиМетодаБонусы(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
		
		Если КартаКлиента <> Неопределено Тогда 
			Если ЗначениеЗаполнено(ПараметрыМетода.КоличествоБонусныхБаллов) Тогда
				СерверЛояльностиПоставщикДанных.СписатьНачислитьБонусы(КартаКлиента, ПараметрыМетода.КоличествоБонусныхБаллов, 0);
				ТекстСообщения = НСтр("ru = 'Бонусы успешно списаны';
										|en = 'Bonus points are written off'");
				СтруктураОтвета.TextMessage = ТекстСообщения;
			Иначе
				ТекстСообщения = НСтр("ru = 'Количество баллов не указано';
										|en = 'Number of bonus points is not specified'");
				СтруктураОтвета.Error = Истина;
				СтруктураОтвета.ErrorMessage = ТекстСообщения;
			КонецЕсли;
		Иначе
			ТекстСообщения = НСтр("ru = 'Карта клиента не найдена';
									|en = 'Customer card is not found'");
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = ТекстСообщения;
		КонецЕсли;
		
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке,
			"СписатьБонусыКлиента");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция РазблокироватьПодарочныйСертификатPOST3_0(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаТекстовоеСообщение(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода   = Неопределено;
		ДанныеСертификата = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиПодарочныйСертификат(ПараметрыМетода, ДанныеСертификата);
		
		Если ДанныеСертификата.ПодарочныйСертификат <> Неопределено Тогда
			СерверЛояльностиПоставщикДанных.РазблокироватьПодарочныйСертификат(ДанныеСертификата);
			СтруктураОтвета.TextMessage = НСтр("ru = 'Подарочный сертификат разблокирован.';
												|en = 'Gift card is unlocked.'");
		Иначе
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = НСтр("ru = 'Подарочный сертификат не найден.';
												|en = 'Gift card is not found.'");
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "РазблокироватьПодарочныйСертификат");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция СписатьПодарочныйСертификатPOST3_0(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаТекстовоеСообщение(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода   = Неопределено;
		ДанныеСертификата = Неопределено;
		
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанных.ДополнитьПараметрамиМетодаСписатьПодарочныйСертификат(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиПодарочныйСертификат(ПараметрыМетода, ДанныеСертификата);
		
		Если ДанныеСертификата.ПодарочныйСертификат <> Неопределено Тогда
			СерверЛояльностиПоставщикДанных.СкорректироватьБалансПодарочногоСертификата(ДанныеСертификата, ПараметрыМетода.СуммаСписания, 0);
			СтруктураОтвета.TextMessage = НСтр("ru = 'Подарочный сертификат успешно списан на указанную сумму.';
												|en = 'Gift card is written off for the specified amount.'");
		Иначе
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = НСтр("ru = 'Подарочный сертификат не найден.';
												|en = 'Gift card is not found.'");
			
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
	
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "СписатьПодарочныйСертификат");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция РазблокироватьПромокодPOST3_0(Запрос)
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаТекстовоеСообщение(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода   = Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыЗапросаПромокода(Запрос, ПараметрыМетода);
		
		СерверЛояльностиПоставщикДанных.СнятьРезервПримененияПромокода(ПараметрыМетода.Промокод);
		
		СтруктураОтвета.TextMessage = НСтр("ru = 'Промокод разблокирован.';
											|en = 'Promo code is unlocked.'");
			
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке,
			"РазблокироватьПромокод");
		
	КонецПопытки;
	
	Возврат Ответ;
	
КонецФункции

Функция НачислитьБонусыКлиентаPOST(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаТекстовоеСообщение(СтруктураОтвета);
		
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		
		ПараметрыМетода = Неопределено;
		КартаКлиента 	= Неопределено;
		СерверЛояльностиПоставщикДанных.ПараметрыДанныхКартыКлиента(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанных.ДополнитьПараметрамиМетодаБонусы(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиКартуКлиента(ПараметрыМетода, КартаКлиента);
		
		Если КартаКлиента <> Неопределено Тогда
			Если ЗначениеЗаполнено(ПараметрыМетода.КоличествоБонусныхБаллов) Тогда
				СерверЛояльностиПоставщикДанных.СписатьНачислитьБонусы(КартаКлиента, 0, ПараметрыМетода.КоличествоБонусныхБаллов);
				ТекстСообщения = НСтр("ru = 'Бонусы успешно начислины';
										|en = 'Bonus points are accrued'");
				СтруктураОтвета.TextMessage = ТекстСообщения;
			Иначе
				ТекстСообщения = НСтр("ru = 'Количество баллов не указано';
										|en = 'Number of bonus points is not specified'");
				СтруктураОтвета.Error = Истина;
				СтруктураОтвета.ErrorMessage = ТекстСообщения;
			КонецЕсли;
		Иначе
			ТекстСообщения = НСтр("ru = 'Карта клиента не найдена';
									|en = 'Customer card is not found'");
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = ТекстСообщения;
		КонецЕсли;
		
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
		
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки, СообщениеОбОшибке, "НачислитьБонусыКлиента");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции

Функция НачислитьПодарочныйСертификатPOST(Запрос)
	Ответ = Новый HTTPСервисОтвет(200);
	
	ИспользоватьСерверЛояльности = Ложь;
	СерверЛояльностиПоставщикДанныхПереопределяемый.ИспользоватьСерверЛояльности(Ответ, ИспользоватьСерверЛояльности);
	
	СтруктураОтвета = Неопределено;
	СерверЛояльностиПоставщикДанных.ПолучитьПараметрыОтветаТекстовоеСообщение(СтруктураОтвета);
	
	Если Не ИспользоватьСерверЛояльности Тогда
		СерверЛояльностиПоставщикДанных.ЗаполнитьОтветОтключенныйСерверЛояльности(СтруктураОтвета, Ответ);
		Возврат Ответ;
	КонецЕсли;
	
	Попытка
		ПараметрыМетода   = Неопределено;
		ДанныеСертификата = Неопределено;
		
		СерверЛояльностиПоставщикДанных.ПараметрыПодарочногоСертификата(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанных.ДополнитьПараметрамиМетодаНачислитьПодарочныйСертификат(Запрос, ПараметрыМетода);
		СерверЛояльностиПоставщикДанныхПереопределяемый.НайтиПодарочныйСертификат(ПараметрыМетода, ДанныеСертификата);
		
		Если ДанныеСертификата.ПодарочныйСертификат <> Неопределено Тогда
			СерверЛояльностиПоставщикДанных.СкорректироватьБалансПодарочногоСертификата(ДанныеСертификата, 0, ПараметрыМетода.СуммаНачисления);
			СерверЛояльностиПоставщикДанныхПереопределяемый.УстановитьСтатусПодарочного(ДанныеСертификата, ПараметрыМетода.СуммаНачисления > 0);
			СтруктураОтвета.TextMessage = НСтр("ru = 'Подарочный сертификат успешно начислен на указанную сумму.';
												|en = 'The specified amount is accrued on the gift card'");
		Иначе
			СтруктураОтвета.Error = Истина;
			СтруктураОтвета.ErrorMessage = НСтр("ru = 'Подарочный сертификат не найден.';
												|en = 'Gift card is not found.'");
			
		КонецЕсли;
		
		// Возвращаем структуру ответа
		СтруктураОтветаJSON = Новый ЗаписьJSON;
		СтруктураОтветаJSON.УстановитьСтроку();
		ЗаписатьJSON(СтруктураОтветаJSON, СтруктураОтвета);
		
		Ответ.УстановитьТелоИзСтроки(СтруктураОтветаJSON.Закрыть());
	
	Исключение
		
		ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		СообщениеОбОшибке = ОбработкаОшибок.СообщениеОбОшибкеДляПользователя(ИнформацияОбОшибке());
		Ответ = СерверЛояльностиПоставщикДанных.ЗафиксироватьОшибкуСервисаЛояльности(ОписаниеОшибки,
			СообщениеОбОшибке, "НачислитьПодарочныйСертификат");
		
	КонецПопытки;
	
	Возврат Ответ;
КонецФункции
#КонецОбласти

#КонецОбласти
