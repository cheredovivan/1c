
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ЭтоПредопределенныйЭлемент") Тогда
		Если Параметры.ЭтоПредопределенныйЭлемент Тогда
			ЭтоПредопределенныйЭлемент = Параметры.ЭтоПредопределенныйЭлемент;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("ЭтоПользовательскаяСтатья") Тогда
		Если Параметры.ЭтоПользовательскаяСтатья Тогда
			ЭтоПользовательскаяСтатья = Параметры.ЭтоПользовательскаяСтатья;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("ЭтоОбъектФинансовогоУчета") Тогда
		Если Параметры.ЭтоОбъектФинансовогоУчета Тогда
			ЭтоОбъектФинансовогоУчета = Параметры.ЭтоОбъектФинансовогоУчета;
		КонецЕсли;
	КонецЕсли;
	
	РазрешитьРедактированиеАналитики = НЕ ЭтоПредопределенныйЭлемент И НЕ ЭтоОбъектФинансовогоУчета;
	РазрешитьРедактированиеСпособаУказанияСчетов = Ложь;
	
	Если ЭтоОбъектФинансовогоУчета Тогда
		Элементы.РазрешитьРедактирование.Видимость = Ложь;
		Элементы.ОписаниеДействияРедактированиеАналитики.Видимость = Ложь;
		Элементы.РазрешитьРедактированиеАналитики.Видимость = Ложь;
		Элементы.ОписаниеДействияРедактированиеСпособаУказанияСчетов.Видимость = Ложь;
		Элементы.РазрешитьРедактированиеСпособаУказанияСчетов.Видимость = Ложь;
		Элементы.ОписаниеДействияОбъектФинансовогоУчета.Видимость = Истина;
	Иначе
		Элементы.РазрешитьРедактирование.Видимость = Истина;
		Элементы.ОписаниеДействияРедактированиеАналитики.Видимость = Истина;
		Элементы.РазрешитьРедактированиеАналитики.Видимость = Истина;
		Элементы.ОписаниеДействияРедактированиеСпособаУказанияСчетов.Видимость = ЭтоПользовательскаяСтатья;
		Элементы.РазрешитьРедактированиеСпособаУказанияСчетов.Видимость = ЭтоПользовательскаяСтатья;
		Элементы.ОписаниеДействияОбъектФинансовогоУчета.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЭтоПредопределенныйЭлемент Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Свойства предопределенного элемента изменять нельзя!';
													|en = 'Cannot change predefined item properties.'"));
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РазрешитьРедактирование(Команда)

	Результат = Новый Массив;
	
	Если РазрешитьРедактированиеАналитики Тогда
		Результат.Добавить("АктивПассив");
		Результат.Добавить("ТипЗначения");
		Результат.Добавить("Аналитика");
	КонецЕсли;
	
	Если РазрешитьРедактированиеСпособаУказанияСчетов Тогда
		Результат.Добавить("СчетаУчетаВНастройкахПроводок");
	КонецЕсли;
	
	Закрыть(Результат);

КонецПроцедуры

#КонецОбласти
