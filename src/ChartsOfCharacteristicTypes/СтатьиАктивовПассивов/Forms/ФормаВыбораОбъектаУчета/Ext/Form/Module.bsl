#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	НастройкиУчета = ПланыВидовХарактеристик.СтатьиАктивовПассивов.НастройкиФинансовогоУчета();
	
	//++ НЕ УТКА
	
	НастройкиПроводок = Справочники.НастройкиФормированияПроводокМеждународногоУчета.НастройкиФормированияПроводок(
		Параметры.НастройкаФормированияПроводок);
	
	Если НастройкиПроводок.ВыручкаБезНДС = Неопределено Тогда
		НастройкиПроводок.ВыручкаБезНДС = Истина;
	КонецЕсли;
	
	НастройкиУчета = МеждународныйУчетПоДаннымФинансовыхРегистров.НастройкиУчетаПоНастройкамФормированияПроводок(
		НастройкиПроводок);
	
	//-- НЕ УТКА
	
	НесобственныеПодконтрольныеЦенности = Ложь;
	Если НЕ Параметры.Свойство("Отбор") Тогда
	ИначеЕсли НЕ Параметры.Отбор.Свойство("НесобственныеПодконтрольныеЦенности", НесобственныеПодконтрольныеЦенности) Тогда
	ИначеЕсли НастройкиУчета.УчетНесобственныхЦенностей Тогда
		НастройкиУчета.УчетНесобственныхЦенностей = НесобственныеПодконтрольныеЦенности;
	КонецЕсли;
	
	СчетаУчетаВНастройкахПроводок = Ложь;
	Если НЕ Параметры.Свойство("Отбор") Тогда
	ИначеЕсли НЕ Параметры.Отбор.Свойство("СчетаУчетаВНастройкахПроводок", СчетаУчетаВНастройкахПроводок) Тогда
		СчетаУчетаВНастройкахПроводок = Ложь;
	КонецЕсли;
	
	ОбщегоНазначения.ЗаполнитьКоллекциюЭлементовДереваДанныхФормы(
		ДеревоОбъектовУчета.ПолучитьЭлементы(),
		ПланыВидовХарактеристик.СтатьиАктивовПассивов.ДеревоОбъектовФинансовогоУчета(НастройкиУчета, СчетаУчетаВНастройкахПроводок));
	
	РегистрыНакопленияМетаданные = Метаданные.РегистрыНакопления;
	
	Для каждого Группа Из ДеревоОбъектовУчета.ПолучитьЭлементы() Цикл
		Для каждого СтрокаОбъектУчета Из Группа.ПолучитьЭлементы() Цикл
			Если СтрокаОбъектУчета.ОбъектУчета = Параметры.ТекущаяСтрока Тогда
				Элементы.ДеревоОбъектовУчета.ТекущаяСтрока = СтрокаОбъектУчета.ПолучитьИдентификатор();
			КонецЕсли;
			
			МетаданныеРегистра = РегистрыНакопленияМетаданные.Найти(СтрокаОбъектУчета.ИсточникДанных);
			Если МетаданныеРегистра <> Неопределено Тогда
				СтрокаОбъектУчета.ИсточникДанныхПредставление = МетаданныеРегистра.Синоним;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоОбъектовУчета

&НаКлиенте
Процедура ДеревоОбъектовУчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = ДеревоОбъектовУчета.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(ТекущиеДанные.ОбъектУчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОбъектовУчетаВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ТекущиеДанные = ДеревоОбъектовУчета.НайтиПоИдентификатору(Значение);
	Если ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(ТекущиеДанные.ОбъектУчета);

КонецПроцедуры

#КонецОбласти