#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив Из Строка- список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетНДС");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//	СписокЗначений Из Строка - список значений, содержащий тексты запросов и их имена.
//						Возвращается в случае, если свойство 'ПолучитьТекстыЗапроса' параметра ДопПараметры
//						имеет значение Истина.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ПодтверждениеНулевойСтавкиНДС") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса

		ТекстЗапросаТаблицаНДСЗаписиКнигиПродаж(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаНДССостояниеРеализации0(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Производит заполнение документа
//
// Параметры:
//   СтруктураПараметров - Структура - структура входных параметров с ключами:
//     * Организация           - СправочникСсылка.Организации - организация, для которой будут получены данные для заполнения документа.
//     * Дата                  - Дата - дата, на которую будут получены данные для заполнения документа.
//     * Событие               - ПеречислениеСсылка.НДССостоянияРеализация0
//     * ДокументыОтгрузки - Массив из ОпределяемыйТип.ОснованиеСчетФактураВыданный
//     * ТаблицаКодовОпераций0 - ТаблицаЗначений - содержит значения общего макета ПереченьКодовОпераций0.
//   АдресХранилища - Строка - адрес временного хранилища для помещения результата выполнения.
Процедура ЗаполнитьДокумент(СтруктураПараметров, АдресХранилища) Экспорт
	
	Организация = СтруктураПараметров.Организация;
	Дата = СтруктураПараметров.Дата;
	Событие = СтруктураПараметров.Событие;
	ДокументыОтгрузки = СтруктураПараметров.ДокументыОтгрузки;
	БезОтбораПоДокументуОтгрузки = ?(ЗначениеЗаполнено(ДокументыОтгрузки), Ложь, Истина);

	Состав = Новый ТаблицаЗначений();
	Для Каждого КолонкаТабличнойЧасти Из Метаданные.Документы.ПодтверждениеНулевойСтавкиНДС.ТабличныеЧасти.Состав.Реквизиты Цикл
		Состав.Колонки.Добавить(КолонкаТабличнойЧасти.Имя, КолонкаТабличнойЧасти.Тип);
	КонецЦикла;
	
	ДанныеДляЗаполнения = Новый Структура;
	
	Если УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(Дата) >= 3 Тогда
		
		КодыОперацийНалоговаяБаза = Новый ТаблицаЗначений();
		Для Каждого КолонкаТабличнойЧасти Из Метаданные.Документы.ПодтверждениеНулевойСтавкиНДС.ТабличныеЧасти.КодыОперацийНалоговаяБаза.Реквизиты Цикл
			КодыОперацийНалоговаяБаза.Колонки.Добавить(КолонкаТабличнойЧасти.Имя, КолонкаТабличнойЧасти.Тип);
		КонецЦикла;
		
		ТаблицаКодовОпераций0 = СтруктураПараметров.ТаблицаКодовОпераций0;
		
		ДатаЗапроса = Дата;
		
		Если НЕ ЗначениеЗаполнено(ДатаЗапроса) Тогда
			
			ДатаЗапроса = КонецДня(ТекущаяДатаСеанса());
			
		КонецЕсли;
		
		ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		
		СостоянияПредположения0 = Новый СписокЗначений();
		СостоянияПредположения0.Добавить(Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение);
		СостоянияПредположения0.Добавить(Перечисления.НДССостоянияРеализация0.ЧастичноПодтвержденаРеализация0);
		
		СписокОфшоров = Новый Массив();
		//++ НЕ УТ
		СписокОфшоров = УчетОфшоров.СписокОфшоров(ДатаЗапроса);
		//-- НЕ УТ
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("КонецПериода",            Новый Граница(КонецДня(ДатаЗапроса), ВидГраницы.Включая));
		Запрос.УстановитьПараметр("Организация",             Организация);
		Запрос.УстановитьПараметр("ДокументОтгрузки",        ДокументыОтгрузки);
		Запрос.УстановитьПараметр("СостояниеПредположения0", СостоянияПредположения0);
		Запрос.Параметры.Вставить("ДатаЗапроса",             ДатаЗапроса);
		Запрос.Параметры.Вставить("Офшоры",                  СписокОфшоров);
		
		Запрос.Параметры.Вставить("ВалютаРеглУчета",         ВалютаРеглУчета);
		Запрос.Параметры.Вставить("ТаблицаКодовОпераций0",   ТаблицаКодовОпераций0);
		Запрос.Параметры.Вставить("КодОперацииПоУмолчанию",  ""); // Значение в случае неопределения конкретной операции НДС 0%
		Запрос.Параметры.Вставить("РФ",                      Справочники.СтраныМира.Россия);
		Запрос.Параметры.Вставить("СтраныЕАЭС",              УправлениеКонтактнойИнформацией.СтраныУчастникиЕАЭС());
		Запрос.Параметры.Вставить("ТекстЗаПределамиЕАЭС",    НСтр("ru = 'За пределами ЕАЭС';
																	|en = 'Outside the EAEU'"));
		Запрос.Параметры.Вставить("ТекстЗаПределамиРФ",      НСтр("ru = 'За пределами РФ';
																	|en = 'Outside the RF'"));
		Запрос.УстановитьПараметр("БезОтбораПоДокументуОтгрузки", БезОтбораПоДокументуОтгрузки);
		Запрос.УстановитьПараметр("СобытиеЧастичноПодтверждена",  Перечисления.НДССостоянияРеализация0.ЧастичноПодтвержденаРеализация0);
		Запрос.УстановитьПараметр("СобытиеПодтвержденаСтавка0",   Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0);
		
		Если ЗначениеЗаполнено(Организация) Тогда
			
			Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "ОбособленноеПодразделение, ГоловнаяОрганизация");
			
			Если Реквизиты.ОбособленноеПодразделение Тогда
				Запрос.УстановитьПараметр("ГоловнаяОрганизация", Реквизиты.ГоловнаяОрганизация);
			Иначе
				Запрос.УстановитьПараметр("ГоловнаяОрганизация", Организация);
			КонецЕсли;
			
		Иначе
			Запрос.УстановитьПараметр("ГоловнаяОрганизация", Организация);
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапросаРасчетКодовОпераций0();
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Выборка.Следующий() Тогда
			
			Ном = 0;
			ВыборкаЗакончилась = Ложь;
			
			Пока Ном < Выборка.Количество() И НЕ ВыборкаЗакончилась Цикл
				
				СтрокаСостава = Состав.Добавить();
				КлючСтроки = Новый УникальныйИдентификатор;
				
				СтрокаСостава.ДокументОтгрузки = Выборка.ДокументОтгрузки;
				СтрокаСостава.ИсправленныйСчетФактура = Выборка.ИсправленныйСчетФактура;
				СтрокаСостава.Контрагент = Выборка.Покупатель;
				СтрокаСостава.Событие = ?(Выборка.Событие <> Неопределено, Выборка.Событие, Событие);
				СтрокаСостава.ПродажиСНДС0 = Выборка.НалоговаяБазаДокументаОтгрузки;
				СтрокаСостава.КлючСтроки = КлючСтроки;
				
				КолвоКодовДокументаОтгрузки = 0;
				
				Пока СтрокаСостава.ДокументОтгрузки = Выборка.ДокументОтгрузки
					И СтрокаСостава.Контрагент = Выборка.Покупатель Цикл
					
					СтрокаКодыОпераций = КодыОперацийНалоговаяБаза.Добавить();
					СтрокаКодыОпераций.КлючСтроки = КлючСтроки;
					СтрокаКодыОпераций.КодОперации = Выборка.Код;
					СтрокаКодыОпераций.НесырьевыеТовары = Выборка.НесырьевыеТовары;
					СтрокаКодыОпераций.ПродажиСНДС0 = Выборка.НалоговаяБазаКодаОперации;
					СтрокаКодыОпераций.АналитикаУчетаНоменклатуры = Выборка.АналитикаУчетаНоменклатуры;
					
					Если КолвоКодовДокументаОтгрузки < 2 Тогда
						
						СтрокаСостава.КодыОперацийПредставление = СтрокаСостава.КодыОперацийПредставление
									+ ?(ЗначениеЗаполнено(СтрокаСостава.КодыОперацийПредставление), ";", "") + Выборка.Код;
						
					КонецЕсли;
					
					Ном = Ном + 1;
					КолвоКодовДокументаОтгрузки = КолвоКодовДокументаОтгрузки + 1;
					
					Если Не Выборка.Следующий() Тогда
						
						ВыборкаЗакончилась = Истина;
						Прервать;
						
					КонецЕсли;
					
				КонецЦикла;
				
				Если КолвоКодовДокументаОтгрузки > 2 Тогда
					
					СтрокаСостава.КодыОперацийПредставление = СтрокаСостава.КодыОперацийПредставление
									+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru = ' и еще %1';
												|en = ' and %1 more'"),
											КолвоКодовДокументаОтгрузки - 2);
				КонецЕсли;
				
			КонецЦикла;
		
		КонецЕсли;
		
		ДанныеДляЗаполнения.Вставить("КодыОперацийНалоговаяБаза",КодыОперацийНалоговаяБаза);
		
	Иначе
		
		// Старый алгоритм, в котором 1 реализации соответствует 1 код операции НДС 0%
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("КонецПериода",            Новый Граница(КонецДня(Дата), ВидГраницы.Включая));
		Запрос.УстановитьПараметр("Организация",             Организация);
		Запрос.УстановитьПараметр("ДокументОтгрузки",        ДокументыОтгрузки);
		Запрос.УстановитьПараметр("СостояниеПредположения0", Перечисления.НДССостоянияРеализация0.ОжидаетсяПодтверждение);
		Запрос.УстановитьПараметр("БезОтбораПоДокументуОтгрузки", БезОтбораПоДокументуОтгрузки);
		
		ТекстПостановление = 
		"ВЫБРАТЬ
		|	НДССостояниеРеализации0.ДокументРеализации КАК ДокументОтгрузки,
		|	НДССостояниеРеализации0.Контрагент КАК Покупатель
		|ПОМЕСТИТЬ НДССостояниеРеализации0
		|ИЗ
		|	РегистрСведений.НДССостояниеРеализации0.СрезПоследних(
		|			&КонецПериода,
		|			Организация = &Организация
		|				И (&БезОтбораПоДокументуОтгрузки
		|					ИЛИ ДокументРеализации В (&ДокументОтгрузки))) КАК НДССостояниеРеализации0
		|ГДЕ
		|	 Состояние В (&СостояниеПредположения0)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументОтгрузки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НДССостояниеРеализации0.ДокументОтгрузки КАК ДокументОтгрузки,
		|	НДССостояниеРеализации0.Покупатель КАК Контрагент,
		|	ВыручкаИСебестоимостьПродажОбороты.СуммаВыручкиРеглОборот КАК ПродажиСНДС0,
		|	ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0) КАК Событие
		|ИЗ
		|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(, &КонецПериода, Регистратор, АналитикаУчетаПоПартнерам.Организация = &Организация) КАК ВыручкаИСебестоимостьПродажОбороты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НДССостояниеРеализации0 КАК НДССостояниеРеализации0
		|		ПО ВыручкаИСебестоимостьПродажОбороты.Регистратор = НДССостояниеРеализации0.ДокументОтгрузки";

		Запрос.Текст = ТекстПостановление;
		
		РезультатЗапроса = Запрос.Выполнить();
			
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
				
			Пока Выборка.Следующий() Цикл
				
				СтрокаСостава = Состав.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаСостава, Выборка);
				
			КонецЦикла;
				
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеДляЗаполнения.Вставить("Состав",Состав);
	
	ПоместитьВоВременноеХранилище(ДанныеДляЗаполнения, АдресХранилища);

КонецПроцедуры

// Добавляет команду создания документа "Подтверждение ставки НДС 0%".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
// Возвращаемое значение:  
//	 СтрокаТаблицыЗначений, Неопределено - команда для вывода в подменю.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПодтверждениеНулевойСтавкиНДС) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.ПодтверждениеНулевойСтавкиНДС.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.ПодтверждениеНулевойСтавкиНДС);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьПродажиНаЭкспорт";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// При заполнении ограничения доступа.
// 
// Параметры:
//  Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных;

КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Организация.ВалютаРегламентированногоУчета  КАК ВалютаРегламентированногоУчета,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Комментарий КАК Комментарий,
	|	ДанныеДокумента.Ответственный КАК Ответственный,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	Документ.ПодтверждениеНулевойСтавкиНДС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПодтверждениеНулевойСтавкиНДС"));
	Запрос.УстановитьПараметр("ТипыНоменклатурыТовар", УчетНДСУПСлужебный.ТипыНоменклатурыТовар());
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаНДСЗаписиКнигиПродаж(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НДСЗаписиКнигиПродаж";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	&Дата КАК Период,
	|	&Ссылка КАК Регистратор,
	|	&Организация КАК Организация,
	|	Состав.ДокументОтгрузки КАК СчетФактура,
	|	Состав.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СпрНоменклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В (&ТипыНоменклатурыТовар)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	КОНЕЦ КАК ВидЦенности,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0) КАК СтавкаНДС,
	|	Состав.Контрагент КАК Покупатель,
	|	ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0) КАК Состояние,
	|	&Дата КАК ДатаСобытия,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ПодтвержденаСтавка0) КАК Событие,
	|	ЕСТЬNULL(КодыОперацийНалоговаяБаза.НесырьевыеТовары, ЛОЖЬ) КАК НесырьевыеТовары,
	|	ЕСТЬNULL(КодыОперацийНалоговаяБаза.ПродажиСНДС0, Состав.ПродажиСНДС0) КАК СуммаБезНДС,
	|	0 КАК НДС,
	|	ЕСТЬNULL(КодыОперацийНалоговаяБаза.КодОперации, """") КАК КодОперации0,
	|	ЕСТЬNULL(СпрНоменклатура.Ссылка, НЕОПРЕДЕЛЕНО) КАК Номенклатура,
	|	Состав.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ПодготовленныеЗаписиКнигиПродаж
	|ИЗ
	|	Документ.ПодтверждениеНулевойСтавкиНДС.Состав КАК Состав
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПодтверждениеНулевойСтавкиНДС.КодыОперацийНалоговаяБаза КАК КодыОперацийНалоговаяБаза
	|		ПО Состав.КлючСтроки = КодыОперацийНалоговаяБаза.КлючСтроки
	|			И Состав.Ссылка = КодыОперацийНалоговаяБаза.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК СпрКлючиАналитикиУчетаНоменклатуры
	|		ПО (КодыОперацийНалоговаяБаза.АналитикаУчетаНоменклатуры = СпрКлючиАналитикиУчетаНоменклатуры.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО (СпрКлючиАналитикиУчетаНоменклатуры.Номенклатура = СпрНоменклатура.Ссылка)
	|ГДЕ
	|	Состав.Ссылка = &Ссылка
	|	И Состав.Событие В (ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0), ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ЧастичноПодтвержденаРеализация0))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодготовленныеЗаписиКнигиПродаж.Период КАК Период,
	|	ПодготовленныеЗаписиКнигиПродаж.Регистратор КАК Регистратор,
	|	ПодготовленныеЗаписиКнигиПродаж.Организация КАК Организация,
	|	ПодготовленныеЗаписиКнигиПродаж.СчетФактура КАК СчетФактура,
	|	ПодготовленныеЗаписиКнигиПродаж.ВидЦенности КАК ВидЦенности,
	|	ПодготовленныеЗаписиКнигиПродаж.СтавкаНДС КАК СтавкаНДС,
	|	ПодготовленныеЗаписиКнигиПродаж.Покупатель КАК Покупатель,
	|	ПодготовленныеЗаписиКнигиПродаж.Состояние КАК Состояние,
	|	ПодготовленныеЗаписиКнигиПродаж.ДатаСобытия КАК ДатаСобытия,
	|	ПодготовленныеЗаписиКнигиПродаж.Событие КАК Событие,
	|	ПодготовленныеЗаписиКнигиПродаж.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	ВЫБОР
	|		КОГДА ПодготовленныеЗаписиКнигиПродаж.ИсправленныйСчетФактура = ЗНАЧЕНИЕ(Документ.СчетФактураВыданный.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ КАК Исправление,
	|	ПодготовленныеЗаписиКнигиПродаж.НесырьевыеТовары КАК НесырьевыеТовары,
	|	ПодготовленныеЗаписиКнигиПродаж.СуммаБезНДС КАК СуммаБезНДС,
	|	ПодготовленныеЗаписиКнигиПродаж.НДС КАК НДС,
	|	ПодготовленныеЗаписиКнигиПродаж.КодОперации0 КАК КодОперации0,
	|	ПодготовленныеЗаписиКнигиПродаж.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ПодготовленныеЗаписиКнигиПродаж КАК ПодготовленныеЗаписиКнигиПродаж";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаНДССостояниеРеализации0(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НДССостояниеРеализации0";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Дата КАК Период,
	|	&Организация КАК Организация,
	|	&Ссылка КАК Регистратор,
	|	Состав.ДокументОтгрузки КАК ДокументРеализации,
	|	ДанныеРеестра.ДатаПервичногоДокумента КАК ДатаРеализации,
	|	КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ) КАК ДатаПодтверждения,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0) КАК СтавкаНДС,
	|	Состав.Контрагент КАК Контрагент,
	|	Состав.Событие КАК Состояние
	|ИЗ
	|	Документ.ПодтверждениеНулевойСтавкиНДС.Состав КАК Состав
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК ДанныеРеестра
	|		ПО Состав.ДокументОтгрузки = ДанныеРеестра.Ссылка
	|ГДЕ
	|	Состав.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Состав.НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаДанныхОснований(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаДанныхОснований";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеРеестра.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеРеестра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРеестра.Подразделение КАК Подразделение,
	|	СУММА(ДанныеДокумента.ПродажиСНДС0) КАК ПродажиСНДС0
	|ПОМЕСТИТЬ ВтТаблицаДанныхОснований
	|ИЗ
	|	Документ.ПодтверждениеНулевойСтавкиНДС.Состав КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК ДанныеРеестра
	|		ПО ДанныеДокумента.ДокументОтгрузки = ДанныеРеестра.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	ДанныеРеестра.НаправлениеДеятельности,
	|	ДанныеРеестра.ХозяйственнаяОперация,
	|	ДанныеРеестра.Подразделение
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаДанныхОснований", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаДанныхОснований(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Дата                                   КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	ТаблицаОснований.ХозяйственнаяОперация  КАК ХозяйственнаяОперация,
	|	ТаблицаОснований.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО                            КАК МестоХранения,
	|	ТаблицаОснований.Подразделение          КАК Подразделение,
	|	&Ответственный                          КАК Ответственный,
	|	&Комментарий                            КАК Комментарий,
	|	ДанныеДокумента.Организация.ВалютаРегламентированногоУчета КАК Валюта,
	|	ТаблицаОснований.ПродажиСНДС0           КАК Сумма,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	НЕОПРЕДЕЛЕНО                            КАК РазделительЗаписи,
	|	НЕОПРЕДЕЛЕНО                            КАК Дополнительно,
	|	&Дата                                   КАК ДатаПервичногоДокумента,
	|	&Номер                                  КАК НомерПервичногоДокумента,
	|	ЛОЖЬ                                    КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                            КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                            КАК ИсправляемыйДокумент,
	|	&Дата                                   КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                            КАК Приоритет
	|ИЗ
	|	Документ.ПодтверждениеНулевойСтавкиНДС КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаДанныхОснований КАК ТаблицаОснований
	|		ПО ДанныеДокумента.Ссылка = ТаблицаОснований.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента      = "Документ.ПодтверждениеНулевойСтавкиНДС";
	ВЗапросеЕстьИсточник    = Истина;
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	ПереопределениеРасчетаПараметров.Вставить("ИнформацияПоИсправлению", """""");
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.ПодтверждениеНулевойСтавкиНДС"));
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ТекстыЗапросаВременныхТаблиц.Вставить("ВтТаблицаДанныхОснований", ТекстЗапросаВтТаблицаДанныхОснований(Запрос, ТекстыЗапроса));
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", 	ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", 		ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
		ТекстЗапроса,
		ПолноеИмяДокумента,
		СинонимТаблицыДокумента,
		ВЗапросеЕстьИсточник,
		ПереопределениеРасчетаПараметров,
		ТекстыЗапросаВременныхТаблиц);
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Функция ТекстЗапросаРасчетКодовОпераций0()
	
	Разделитель =";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	ТекстЗапроса = ТекстЗапросаОбщиеДанныеКлассификацииКодовОпераций() + Разделитель;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУведомленияОКонтролируемыхСделках") Тогда
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.Классификация КАК Классификация,
		|	ВЫБОР
		|		КОГДА ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.СтранаРегистрации В (&Офшоры)
		|			ТОГДА ""Да""
		|		ИНАЧЕ ""Нет""
		|	КОНЕЦ КАК КонтрагентИЗОфшора,
		|	ВЫБОР
		|		КОГДА ЛОЖЬ
		|			ТОГДА """"
		//++ НЕ УТ
		|		КОГДА ЕСТЬNULL(ВзаимозависимыеЛицаСрезПоследних.ТипВзаимозависимости, НЕОПРЕДЕЛЕНО) В
		|						(ЗНАЧЕНИЕ(Перечисление.ТипыВзаимозависимости.ВзаимозависимыПоКодексу),
		|						ЗНАЧЕНИЕ(Перечисление.ТипыВзаимозависимости.СамостоятельноеПризнаниеВзаимозависимости),
		|						ЗНАЧЕНИЕ(Перечисление.ТипыВзаимозависимости.ВзаимозависимостьПоРешениюСуда))
		|			ТОГДА ""Да""
		//-- НЕ УТ
		|		ИНАЧЕ ""Нет""
		|	КОНЕЦ КАК КонтрагентВзаимозависимоеЛицо,
		|	ВЫБОР
		|		КОГДА ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.СтранаРеализации В (&СтраныЕАЭС)
		|			ТОГДА ""ЕАЭС""
		|		КОГДА ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.СтранаРеализации <> &РФ
		|				И НЕ ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.СтранаРеализации В (&СтраныЕАЭС)
		|			ТОГДА &ТекстЗаПределамиЕАЭС
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК СтранаРеализацииЕАЭСЗаПределамиЕАЭС,
		|	ВЫБОР
		|		КОГДА ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.СтранаРеализации <> &РФ
		|			ТОГДА &ТекстЗаПределамиРФ
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК СтранаРеализацииЗаПределамиРФ,
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.Покупатель КАК Покупатель,
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.ПродажиНДС0 КАК ПродажиСНДС0,
		|	СуммыДокументовВВалютахУчетаИтоговые.НалоговаяБазаДокументаОтгрузки КАК НалоговаяБазаДокументаОтгрузки,
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.Событие КАК Событие
		|ПОМЕСТИТЬ ОтгрузкиДляОпределенияКодовОпераций
		|ИЗ
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости КАК ОтгрузкиДляОпределенияОфшоровИВзаимозависимости
		//++ НЕ УТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВзаимозависимыеЛица.СрезПоследних(&ДатаЗапроса, Организация = &ГоловнаяОрганизация) КАК ВзаимозависимыеЛицаСрезПоследних
		|		ПО ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.Покупатель = ВзаимозависимыеЛицаСрезПоследних.Контрагент
		//-- НЕ УТ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СуммыДокументовВВалютахУчетаИтоговые КАК СуммыДокументовВВалютахУчетаИтоговые
		|		ПО ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.ДокументОтгрузки = СуммыДокументовВВалютахУчетаИтоговые.ДокументОтгрузки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Классификация";
		
	Иначе
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.Классификация КАК Классификация,
		|	ВЫБОР
		|		КОГДА ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.СтранаРегистрации В (&Офшоры)
		|			ТОГДА ""Да""
		|		ИНАЧЕ ""Нет""
		|	КОНЕЦ КАК КонтрагентИЗОфшора,
		|	""Нет"" КАК КонтрагентВзаимозависимоеЛицо,
		|	ВЫБОР
		|		КОГДА ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.СтранаРеализации В (&СтраныЕАЭС)
		|			ТОГДА ""ЕАЭС""
		|		КОГДА ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.СтранаРеализации <> &РФ
		|				И НЕ ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.СтранаРеализации В (&СтраныЕАЭС)
		|			ТОГДА &ТекстЗаПределамиЕАЭС
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК СтранаРеализацииЕАЭСЗаПределамиЕАЭС,
		|	ВЫБОР
		|		КОГДА ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.СтранаРеализации <> &РФ
		|			ТОГДА &ТекстЗаПределамиРФ
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК СтранаРеализацииЗаПределамиРФ,
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.Покупатель КАК Покупатель,
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.ПродажиНДС0 КАК ПродажиСНДС0,
		|	СуммыДокументовВВалютахУчетаИтоговые.НалоговаяБазаДокументаОтгрузки КАК НалоговаяБазаДокументаОтгрузки,
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.Событие КАК Событие
		|ПОМЕСТИТЬ ОтгрузкиДляОпределенияКодовОпераций
		|ИЗ
		|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости КАК ОтгрузкиДляОпределенияОфшоровИВзаимозависимости
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СуммыДокументовВВалютахУчетаИтоговые КАК СуммыДокументовВВалютахУчетаИтоговые
		|		ПО ОтгрузкиДляОпределенияОфшоровИВзаимозависимости.ДокументОтгрузки = СуммыДокументовВВалютахУчетаИтоговые.ДокументОтгрузки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Классификация";

	КонецЕсли;

	ТекстЗапроса = ТекстЗапроса + Разделитель +
	"УНИЧТОЖИТЬ ОтгрузкиДляОпределенияОфшоровИВзаимозависимости
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКодовОпераций0.Код КАК Код,
	|	ТаблицаКодовОпераций0.ДатаНачала КАК ДатаНачала,
	|	ТаблицаКодовОпераций0.ДатаОкончания КАК ДатаОкончания,
	|	ТаблицаКодовОпераций0.Классификация КАК Классификация,
	|	ТаблицаКодовОпераций0.КонтрагентИЗОфшора КАК КонтрагентИЗОфшора,
	|	ТаблицаКодовОпераций0.КонтрагентВзаимозависимоеЛицо КАК КонтрагентВзаимозависимоеЛицо,
	|	ТаблицаКодовОпераций0.СтранаРеализации КАК СтранаРеализации,
	|	ТаблицаКодовОпераций0.НесырьевыеТовары КАК НесырьевыеТовары,
	|	ТаблицаКодовОпераций0.СырьевыеТовары КАК СырьевыеТовары
	|ПОМЕСТИТЬ КодыОпераций0
	|ИЗ
	|	&ТаблицаКодовОпераций0 КАК ТаблицаКодовОпераций0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Классификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодтверждениеНулевойСтавкиНДССостав.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ПодтверждениеНулевойСтавкиНДСКодыОперацийНалоговаяБаза.КодОперации КАК КодОперации0,
	|	ПодтверждениеНулевойСтавкиНДСКодыОперацийНалоговаяБаза.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	СУММА(ПодтверждениеНулевойСтавкиНДСКодыОперацийНалоговаяБаза.ПродажиСНДС0) КАК ПродажиСНДС0
	|ПОМЕСТИТЬ ЧастичноПодтвержденныеОтгрузкиВРазрезеКодовОпераций
	|ИЗ
	|	Документ.ПодтверждениеНулевойСтавкиНДС.КодыОперацийНалоговаяБаза КАК ПодтверждениеНулевойСтавкиНДСКодыОперацийНалоговаяБаза
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПодтверждениеНулевойСтавкиНДС.Состав КАК ПодтверждениеНулевойСтавкиНДССостав
	|		ПО (ПодтверждениеНулевойСтавкиНДСКодыОперацийНалоговаяБаза.Ссылка = ПодтверждениеНулевойСтавкиНДССостав.Ссылка
	|				И ПодтверждениеНулевойСтавкиНДСКодыОперацийНалоговаяБаза.КлючСтроки = ПодтверждениеНулевойСтавкиНДССостав.КлючСтроки)
	|ГДЕ
	|	ПодтверждениеНулевойСтавкиНДССостав.Ссылка.Проведен
	|	И ПодтверждениеНулевойСтавкиНДССостав.Ссылка.Дата МЕЖДУ ДОБАВИТЬКДАТЕ(&ДатаЗапроса, ГОД, -1) И &ДатаЗапроса
	|	И ПодтверждениеНулевойСтавкиНДССостав.Событие = &СобытиеЧастичноПодтверждена
	|
	|СГРУППИРОВАТЬ ПО
	|	ПодтверждениеНулевойСтавкиНДССостав.ДокументОтгрузки,
	|	ПодтверждениеНулевойСтавкиНДСКодыОперацийНалоговаяБаза.КодОперации,
	|	ПодтверждениеНулевойСтавкиНДСКодыОперацийНалоговаяБаза.АналитикаУчетаНоменклатуры
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки,
	|	КодОперации0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтгрузкиДляОпределенияКодовОпераций.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ОтгрузкиДляОпределенияКодовОпераций.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	ЕСТЬNULL(КодыОпераций0.Код, &КодОперацииПоУмолчанию) КАК Код,
	|	ЕСТЬNULL(КодыОпераций0.НесырьевыеТовары, ИСТИНА) КАК НесырьевыеТовары,
	|	ЕСТЬNULL(КодыОпераций0.СырьевыеТовары, ИСТИНА) КАК СырьевыеТовары,
	|	СУММА(ОтгрузкиДляОпределенияКодовОпераций.ПродажиСНДС0) КАК НалоговаяБазаКодаОперации,
	|	ОтгрузкиДляОпределенияКодовОпераций.Покупатель КАК Покупатель,
	|	ОтгрузкиДляОпределенияКодовОпераций.НалоговаяБазаДокументаОтгрузки КАК НалоговаяБазаДокументаОтгрузки,
	|	ОтгрузкиДляОпределенияКодовОпераций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ОтгрузкиДляОпределенияКодовОпераций.Событие КАК Событие
	|ПОМЕСТИТЬ ПодготовленныеОтгрузкиДляОпределенияКодовОпераций
	|ИЗ
	|	ОтгрузкиДляОпределенияКодовОпераций КАК ОтгрузкиДляОпределенияКодовОпераций
	|		ЛЕВОЕ СОЕДИНЕНИЕ КодыОпераций0 КАК КодыОпераций0
	|		ПО ОтгрузкиДляОпределенияКодовОпераций.Классификация = КодыОпераций0.Классификация
	|		И (КодыОпераций0.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	|		И КодыОпераций0.ДатаНачала <= &ДатаЗапроса
	|		ИЛИ &ДатаЗапроса МЕЖДУ КодыОпераций0.ДатаНачала И КодыОпераций0.ДатаОкончания)
	|		И КодыОпераций0.КонтрагентИЗОфшора В ("""", ОтгрузкиДляОпределенияКодовОпераций.КонтрагентИЗОфшора)
	|		И КодыОпераций0.КонтрагентВзаимозависимоеЛицо В ("""",
	|			ОтгрузкиДляОпределенияКодовОпераций.КонтрагентВзаимозависимоеЛицо)
	|		И КодыОпераций0.СтранаРеализации В ("""", ОтгрузкиДляОпределенияКодовОпераций.СтранаРеализацииЕАЭСЗаПределамиЕАЭС,
	|			ОтгрузкиДляОпределенияКодовОпераций.СтранаРеализацииЗаПределамиРФ)
	|СГРУППИРОВАТЬ ПО
	|	ОтгрузкиДляОпределенияКодовОпераций.ДокументОтгрузки,
	|	ОтгрузкиДляОпределенияКодовОпераций.ИсправленныйСчетФактура,
	|	ОтгрузкиДляОпределенияКодовОпераций.Покупатель,
	|	ОтгрузкиДляОпределенияКодовОпераций.НалоговаяБазаДокументаОтгрузки,
	|	ЕСТЬNULL(КодыОпераций0.Код, &КодОперацииПоУмолчанию),
	|	ЕСТЬNULL(КодыОпераций0.НесырьевыеТовары, ИСТИНА),
	|	ЕСТЬNULL(КодыОпераций0.СырьевыеТовары, ИСТИНА),
	|	ОтгрузкиДляОпределенияКодовОпераций.АналитикаУчетаНоменклатуры,
	|	ОтгрузкиДляОпределенияКодовОпераций.Событие
	|
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки,
	|	Код
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтгрузкиДляОпределенияКодовОпераций.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ОтгрузкиДляОпределенияКодовОпераций.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	ОтгрузкиДляОпределенияКодовОпераций.Код КАК Код,
	|	ОтгрузкиДляОпределенияКодовОпераций.НесырьевыеТовары КАК НесырьевыеТовары,
	|	ОтгрузкиДляОпределенияКодовОпераций.СырьевыеТовары КАК СырьевыеТовары,
	|	ОтгрузкиДляОпределенияКодовОпераций.НалоговаяБазаКодаОперации - ЕСТЬNULL(ЧастичноПодтвержденныеОтгрузкиВРазрезеКодовОпераций.ПродажиСНДС0, 0) КАК
	|		НалоговаяБазаКодаОперации,
	|	ОтгрузкиДляОпределенияКодовОпераций.Покупатель КАК Покупатель,
	|	ОтгрузкиДляОпределенияКодовОпераций.НалоговаяБазаДокументаОтгрузки КАК НалоговаяБазаДокументаОтгрузки,
	|	ОтгрузкиДляОпределенияКодовОпераций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ОтгрузкиДляОпределенияКодовОпераций.Событие КАК Событие,
	|	ЕСТЬNULL(СчетФактураВыданный.Ссылка, НЕОПРЕДЕЛЕНО) КАК СчетФактураНаНеподтвержденнуюРеализацию0
	|ИЗ
	|	ПодготовленныеОтгрузкиДляОпределенияКодовОпераций КАК ОтгрузкиДляОпределенияКодовОпераций
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЧастичноПодтвержденныеОтгрузкиВРазрезеКодовОпераций
	|		ПО ЕСТЬNULL(ОтгрузкиДляОпределенияКодовОпераций.Код, &КодОперацииПоУмолчанию) = ЧастичноПодтвержденныеОтгрузкиВРазрезеКодовОпераций.КодОперации0
	|		И ЧастичноПодтвержденныеОтгрузкиВРазрезеКодовОпераций.ДокументОтгрузки = ОтгрузкиДляОпределенияКодовОпераций.ДокументОтгрузки
	|		И ЧастичноПодтвержденныеОтгрузкиВРазрезеКодовОпераций.АналитикаУчетаНоменклатуры = ОтгрузкиДляОпределенияКодовОпераций.АналитикаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураНаНеподтвержденнуюРеализацию0 КАК СчетФактураВыданный
	|		ПО ОтгрузкиДляОпределенияКодовОпераций.ДокументОтгрузки = СчетФактураВыданный.ДокументОснование
	|		И НЕ СчетФактураВыданный.ПометкаУдаления
	|ГДЕ
	|	ОтгрузкиДляОпределенияКодовОпераций.НалоговаяБазаКодаОперации - ЕСТЬNULL(ЧастичноПодтвержденныеОтгрузкиВРазрезеКодовОпераций.ПродажиСНДС0, 0) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументОтгрузки,
	|	Покупатель,
	|	Код
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОтгрузкиДляОпределенияКодовОпераций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ КодыОпераций0";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаОбщиеДанныеКлассификацииКодовОпераций()
	Возврат
	"ВЫБРАТЬ
	|	НДССостояниеРеализации0.ДокументРеализации КАК ДокументОтгрузки,
	|	НДССостояниеРеализации0.Контрагент КАК Покупатель,
	|	НДССостояниеРеализации0.Контрагент.СтранаРегистрации КАК СтранаРегистрации,
	|	НДССостояниеРеализации0.Состояние КАК Состояние
	|ПОМЕСТИТЬ НДССостояниеРеализации0
	|ИЗ
	|	РегистрСведений.НДССостояниеРеализации0.СрезПоследних(
	|			&КонецПериода,
	|			Организация = &Организация
	|				И (&БезОтбораПоДокументуОтгрузки
	|					ИЛИ ДокументРеализации В (&ДокументОтгрузки))) КАК НДССостояниеРеализации0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСПродажи0.СчетФактура КАК ДокументОтгрузки,
	|	НДСПродажи0.СуммаБезНДСОборот КАК ПодтвержденнаяСуммаПоДокументуОтгрузки
	|ПОМЕСТИТЬ НДСПродажи0
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			ДОБАВИТЬКДАТЕ(&ДатаЗапроса, ГОД, -1),
	|			&КонецПериода,
	|			,
	|			Организация = &Организация
	|				И Событие = &СобытиеПодтвержденаСтавка0
	|				И СчетФактура В
	|					(ВЫБРАТЬ
	|						Т.ДокументОтгрузки
	|					ИЗ
	|						НДССостояниеРеализации0 КАК Т)) КАК НДСПродажи0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(СуммыДокументовВВалютахУчетаСрезПоследних.БазаНДСРегл) КАК БазаНДСРегл,
	|	СуммыДокументовВВалютахУчетаСрезПоследних.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ СуммыДокументовВВалютахУчета
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета.СрезПоследних(
	|			,
	|			Регистратор В
	|				(ВЫБРАТЬ
	|					Т.ДокументОтгрузки
	|				ИЗ
	|					НДССостояниеРеализации0 КАК Т)) КАК СуммыДокументовВВалютахУчетаСрезПоследних
	|
	|СГРУППИРОВАТЬ ПО
	|	СуммыДокументовВВалютахУчетаСрезПоследних.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДССостояниеРеализации0.ДокументОтгрузки КАК ДокументОтгрузки,
	|	НДССостояниеРеализации0.Покупатель КАК Покупатель,
	|	НДССостояниеРеализации0.СтранаРегистрации КАК СтранаРегистрации,
	|	СуммыДокументовВВалютахУчета.БазаНДСРегл - ЕСТЬNULL(НДСПродажи0.ПодтвержденнаяСуммаПоДокументуОтгрузки, 0) КАК НалоговаяБазаДокументаОтгрузки
	|ПОМЕСТИТЬ ДокументыРеализации0
	|ИЗ
	|	НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|		ЛЕВОЕ СОЕДИНЕНИЕ НДСПродажи0 КАК НДСПродажи0
	|		ПО НДССостояниеРеализации0.ДокументОтгрузки = НДСПродажи0.ДокументОтгрузки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|		ПО НДССостояниеРеализации0.ДокументОтгрузки = СуммыДокументовВВалютахУчета.Регистратор
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &БезОтбораПоДокументуОтгрузки
	|				ТОГДА НДССостояниеРеализации0.Состояние В (&СостояниеПредположения0)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВозвратТоваровОтКлиентаВидыЗапасов.Ссылка КАК ДокументРегистратор,
	|	ВозвратТоваровОтКлиентаВидыЗапасов.ДокументРеализации КАК ДокументОтгрузки,
	|	ДокументыРеализации0.Покупатель КАК Покупатель,
	|	ДокументыРеализации0.СтранаРегистрации КАК СтранаРегистрации,
	|	ДокументыРеализации0.НалоговаяБазаДокументаОтгрузки КАК НалоговаяБазаДокументаОтгрузки,
	|	ВозвратТоваровОтКлиентаВидыЗапасов.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ДокументыКорректировкиИВозвраты
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента.ВидыЗапасов КАК ВозвратТоваровОтКлиентаВидыЗапасов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыРеализации0 КАК ДокументыРеализации0
	|		ПО ВозвратТоваровОтКлиентаВидыЗапасов.ДокументРеализации = ДокументыРеализации0.ДокументОтгрузки
	|			И ВозвратТоваровОтКлиентаВидыЗапасов.Ссылка.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииВидыЗапасовОприходование.Ссылка,
	|	КорректировкаРеализацииВидыЗапасовОприходование.Ссылка.ДокументОснование,
	|	ДокументыРеализации0.Покупатель КАК Покупатель,
	|	ДокументыРеализации0.СтранаРегистрации КАК СтранаРегистрации,
	|	ДокументыРеализации0.НалоговаяБазаДокументаОтгрузки КАК НалоговаяБазаДокументаОтгрузки,
	|	КорректировкаРеализацииВидыЗапасовОприходование.ИдентификаторСтроки
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовОприходование КАК КорректировкаРеализацииВидыЗапасовОприходование
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыРеализации0 КАК ДокументыРеализации0
	|		ПО КорректировкаРеализацииВидыЗапасовОприходование.Ссылка.ДокументОснование = ДокументыРеализации0.ДокументОтгрузки
	|			И КорректировкаРеализацииВидыЗапасовОприходование.Ссылка.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаРеализацииВидыЗапасовСписание.Ссылка,
	|	КорректировкаРеализацииВидыЗапасовСписание.Ссылка.ДокументОснование,
	|	ДокументыРеализации0.Покупатель КАК Покупатель,
	|	ДокументыРеализации0.СтранаРегистрации КАК СтранаРегистрации,
	|	ДокументыРеализации0.НалоговаяБазаДокументаОтгрузки КАК НалоговаяБазаДокументаОтгрузки,
	|	КорректировкаРеализацииВидыЗапасовСписание.ИдентификаторСтроки
	|ИЗ
	|	Документ.КорректировкаРеализации.ВидыЗапасовСписание КАК КорректировкаРеализацииВидыЗапасовСписание
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыРеализации0 КАК ДокументыРеализации0
	|		ПО КорректировкаРеализацииВидыЗапасовСписание.Ссылка.ДокументОснование = ДокументыРеализации0.ДокументОтгрузки
	|			И КорректировкаРеализацииВидыЗапасовСписание.Ссылка.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыРеализации0.Покупатель КАК Покупатель,
	|	ДокументыРеализации0.СтранаРегистрации КАК СтранаРегистрации,
	|	ВЫБОР
	|		КОГДА ДокументыРеализации0.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ВЫРАЗИТЬ(ДокументыРеализации0.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).СтранаРеализации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(ДокументыРеализации0.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).СтранаРеализации
	|		КОГДА ДокументыРеализации0.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|				И ВЫРАЗИТЬ(ДокументыРеализации0.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).СтранаРеализации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(ДокументыРеализации0.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).СтранаРеализации
	|		ИНАЧЕ ДокументыРеализации0.СтранаРегистрации
	|	КОНЕЦ КАК СтранаРеализации,
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ДокументыРеализации0.НалоговаяБазаДокументаОтгрузки КАК НалоговаяБазаДокументаОтгрузки,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|			ТОГДА """"
	//++ НЕ УТ
	|		КОГДА ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.Операции0, ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка)
	|			ТОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.Операции0.Код
	//-- НЕ УТ
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Классификация,
	|	ДокументыРеализации0.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ВыручкаИСебестоимостьПродаж.Регистратор КАК ДокументРегистратор,
	|	ВыручкаИСебестоимостьПродаж.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи
	|ПОМЕСТИТЬ ВыручкаИСебестоимостьПродажАналитикаУчетаНоменклатуры
	|ИЗ
	|	ДокументыРеализации0 КАК ДокументыРеализации0
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|		ПО ДокументыРеализации0.ДокументОтгрузки = ВыручкаИСебестоимостьПродаж.ЗаказКлиента
	|		И НЕ ВыручкаИСебестоимостьПродаж.РасчетСебестоимости
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыРеализации0.Покупатель,
	|	ДокументыРеализации0.СтранаРегистрации,
	|	ВЫБОР
	|		КОГДА ДокументыРеализации0.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ВЫРАЗИТЬ(ДокументыРеализации0.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).СтранаРеализации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(ДокументыРеализации0.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).СтранаРеализации
	|		ИНАЧЕ ДокументыРеализации0.СтранаРегистрации
	|	КОНЕЦ,
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры,
	|	ДокументыРеализации0.НалоговаяБазаДокументаОтгрузки,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|			ТОГДА """"
	//++ НЕ УТ
	|		КОГДА ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.Операции0, ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка)
	|			ТОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.Операции0.Код
	//-- НЕ УТ
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ДокументыРеализации0.ДокументОтгрузки,
	|	ВыручкаИСебестоимостьПродаж.Регистратор,
	|	ВыручкаИСебестоимостьПродаж.ИдентификаторФинЗаписи
	|ИЗ
	|	ДокументыКорректировкиИВозвраты КАК ДокументыРеализации0
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|		ПО ДокументыРеализации0.ДокументРегистратор = ВыручкаИСебестоимостьПродаж.Регистратор
	|			И ДокументыРеализации0.ДокументОтгрузки <> ВыручкаИСебестоимостьПродаж.ЗаказКлиента
	|			И ДокументыРеализации0.ИдентификаторСтроки = ВыручкаИСебестоимостьПродаж.ИдентификаторФинЗаписи
	|			И НЕ ВыручкаИСебестоимостьПродаж.РасчетСебестоимости
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыРеализации0.Покупатель КАК Покупатель,
	|	ДокументыРеализации0.СтранаРегистрации КАК СтранаРегистрации,
	|	ВЫБОР
	|		КОГДА ДокументыРеализации0.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ВЫРАЗИТЬ(ДокументыРеализации0.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).СтранаРеализации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(ДокументыРеализации0.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).СтранаРеализации
	|		ИНАЧЕ ДокументыРеализации0.СтранаРегистрации
	|	КОНЕЦ КАК СтранаРеализации,
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ДокументыРеализации0.НалоговаяБазаДокументаОтгрузки КАК НалоговаяБазаДокументаОтгрузки,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|			ТОГДА """"
	//++ НЕ УТ
	|		КОГДА ЕСТЬNULL(ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.Операции0, ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка)
	|			ТОГДА ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры.Номенклатура.Операции0.Код
	//-- НЕ УТ
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Классификация,
	|	ДокументыРеализации0.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ВыручкаИСебестоимостьПродаж.Регистратор КАК ДокументРегистратор,
	|	ВыручкаИСебестоимостьПродаж.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ДокументыРеализации0 КАК ДокументыРеализации0
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|		ПО ДокументыРеализации0.ДокументОтгрузки = ВыручкаИСебестоимостьПродаж.Регистратор
	|			И ДокументыРеализации0.ДокументОтгрузки <> ВыручкаИСебестоимостьПродаж.ЗаказКлиента
	|			И НЕ ВыручкаИСебестоимостьПродаж.РасчетСебестоимости
	|;
	//++ НЕ УТ
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки КАК ДокументОтгрузки
	|ПОМЕСТИТЬ ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки
	|ИЗ
	|	ДокументыРеализации0 КАК ДокументыРеализации0
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеОВвозеТоваровПолученное.ДокументыОтгрузки КАК ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки
	|		ПО ДокументыРеализации0.ДокументОтгрузки = ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки
	|		И ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.Ссылка.Проведен
	|		И ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.Ссылка.КодПричины = ""1""
	|;
	//-- НЕ УТ
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыРеализации0.ДокументОтгрузки КАК ДокументОтгрузки,
	|	СчетФактураВыданный.Ссылка КАК ИсходныйСчетФактура
	|ПОМЕСТИТЬ СчетаФактурыИсходные
	|ИЗ
	|	ДокументыРеализации0 КАК ДокументыРеализации0
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДО
	|		ПО ДокументыРеализации0.ДокументОтгрузки = СчетФактураВыданныйДО.ДокументОснование
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ПО СчетФактураВыданныйДО.Ссылка = СчетФактураВыданный.Ссылка
	|			И СчетФактураВыданный.Проведен
	|			И НЕ СчетФактураВыданный.Корректировочный
	|			И НЕ СчетФактураВыданный.Исправление
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ДокументОтгрузки КАК ДокументОтгрузки,
	|	МАКСИМУМ(ВложенныйЗапрос.НомерИсправления) КАК НомерИсправления
	|ПОМЕСТИТЬ СчетФактураАктуальноеИсправление
	|ИЗ
	|	(ВЫБРАТЬ
	|		СчетаФактурыИсходные.ДокументОтгрузки КАК ДокументОтгрузки,
	|		"""" КАК НомерИсправления
	|	ИЗ
	|		СчетаФактурыИсходные КАК СчетаФактурыИсходные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		СчетаФактурыИсходные.ДокументОтгрузки КАК ДокументОтгрузки,
	|		СчетФактураВыданныйИсправление.НомерИсправления КАК НомерИсправления
	|	ИЗ
	|		СчетаФактурыИсходные КАК СчетаФактурыИсходные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданныйИсправление
	|		ПО СчетаФактурыИсходные.ИсходныйСчетФактура = СчетФактураВыданныйИсправление.СчетФактураОснование
	|			И СчетФактураВыданныйИсправление.Проведен
	|			И СчетФактураВыданныйИсправление.Исправление
	|			И НЕ СчетФактураВыданныйИсправление.Корректировочный
	|			И СчетФактураВыданныйИсправление.Дата <= &ДатаЗапроса) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаФактурыИсходные.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ВЫБОР
	|		КОГДА СчетФактураАктуальноеИсправление.НомерИсправления = """"
	|			ТОГДА СчетаФактурыИсходные.ИсходныйСчетФактура
	|		ИНАЧЕ
	|			ЕСТЬNULL(СчетФактураВыданныйИсправление.Ссылка, ЗНАЧЕНИЕ(Документ.СчетФактураВыданный.ПустаяСсылка))
	|	КОНЕЦ КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА СчетФактураАктуальноеИсправление.НомерИсправления = """"
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ
	|			ИСТИНА
	|	КОНЕЦ КАК Исправление
	|ПОМЕСТИТЬ АктуальныеСчетаФактуры
	|ИЗ
	|	СчетаФактурыИсходные КАК СчетаФактурыИсходные
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ СчетФактураАктуальноеИсправление КАК СчетФактураАктуальноеИсправление
	|	ПО СчетаФактурыИсходные.ДокументОтгрузки = СчетФактураАктуальноеИсправление.ДокументОтгрузки
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданныйИсправление
	|	ПО СчетаФактурыИсходные.ИсходныйСчетФактура = СчетФактураВыданныйИсправление.СчетФактураОснование
	|		И СчетФактураВыданныйИсправление.Проведен
	|		И СчетФактураВыданныйИсправление.Исправление
	|		И СчетФактураВыданныйИсправление.НомерИсправления = СчетФактураАктуальноеИсправление.НомерИсправления
	|		И НЕ СчетФактураВыданныйИсправление.Корректировочный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыручкаИСебестоимостьПродаж.Покупатель КАК Покупатель,
	|	ВыручкаИСебестоимостьПродаж.СтранаРегистрации КАК СтранаРегистрации,
	|	ВыручкаИСебестоимостьПродаж.СтранаРеализации КАК СтранаРеализации,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВыручкаИСебестоимостьПродаж.ДокументРегистратор) = ТИП(Документ.ВозвратТоваровОтКлиента)
	|			ТОГДА -1
	|		ИНАЧЕ
	|			1
	|	КОНЕЦ * ЕСТЬNULL(СуммыДокументовВВалютахУчета.БазаНДСРегл, 0) КАК ПродажиНДС0,
	|	ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВыручкаИСебестоимостьПродаж.НалоговаяБазаДокументаОтгрузки КАК НалоговаяБазаДокументаОтгрузки,
	|	ВыручкаИСебестоимостьПродаж.Классификация КАК Классификация,
	|	ВыручкаИСебестоимостьПродаж.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ЕСТЬNULL(ИсправленныеСчетаФактуры.СчетФактура, НЕОПРЕДЕЛЕНО) КАК ИсправленныйСчетФактура,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	//++ НЕ УТ
	|		КОГДА ВыручкаИСебестоимостьПродаж.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			И ВыручкаИСебестоимостьПродаж.СтранаРегистрации.УчастникЕАЭС
	|			И ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки ЕСТЬ NULL
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.НеПодтвержденаРеализация0)
	//-- НЕ УТ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Событие
	|ПОМЕСТИТЬ ОтгрузкиДляОпределенияОфшоровИВзаимозависимости
	|ИЗ
	|	ВыручкаИСебестоимостьПродажАналитикаУчетаНоменклатуры КАК ВыручкаИСебестоимостьПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|		ПО (ВыручкаИСебестоимостьПродаж.ДокументРегистратор = СуммыДокументовВВалютахУчета.Регистратор)
	|			И (ВыручкаИСебестоимостьПродаж.ИдентификаторФинЗаписи = СуммыДокументовВВалютахУчета.ИдентификаторСтроки)
	//++ НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки КАК ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки
	|		ПО ВыручкаИСебестоимостьПродаж.ДокументОтгрузки = ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки
	//-- НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ АктуальныеСчетаФактуры КАК ИсправленныеСчетаФактуры
	|		ПО ВыручкаИСебестоимостьПродаж.ДокументОтгрузки = ИсправленныеСчетаФактуры.ДокументОтгрузки
	|			И ИсправленныеСчетаФактуры.Исправление
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыРеализации0.Покупатель,
	|	ДокументыРеализации0.СтранаРегистрации,
	|	РеализацияТоваровИУслугТоварыВПути.Ссылка.СтранаРеализации,
	|	СуммыДокументовВВалютахУчета.БазаНДСРегл,
	|	РеализацияТоваровИУслугТоварыВПути.АналитикаУчетаНоменклатуры,
	|	ДокументыРеализации0.НалоговаяБазаДокументаОтгрузки,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|			ТОГДА """"
	//++ НЕ УТ
	|		КОГДА ЕСТЬNULL(РеализацияТоваровИУслугТоварыВПути.АналитикаУчетаНоменклатуры.Номенклатура.Операции0, ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка)
	|			ТОГДА РеализацияТоваровИУслугТоварыВПути.АналитикаУчетаНоменклатуры.Номенклатура.Операции0.Код
	//-- НЕ УТ
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ДокументыРеализации0.ДокументОтгрузки,
	|	ЕСТЬNULL(ИсправленныеСчетаФактуры.СчетФактура, НЕОПРЕДЕЛЕНО),
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	//++ НЕ УТ
	|		КОГДА ДокументыРеализации0.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			И ДокументыРеализации0.СтранаРегистрации.УчастникЕАЭС
	|			И ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки ЕСТЬ NULL
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.НеПодтвержденаРеализация0)
	//-- НЕ УТ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|ИЗ
	|	ДокументыРеализации0 КАК ДокументыРеализации0
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК РеализацияТоваровИУслугТоварыВПути
	|		ПО ДокументыРеализации0.ДокументОтгрузки = РеализацияТоваровИУслугТоварыВПути.Ссылка
	|			И РеализацияТоваровИУслугТоварыВПути.Ссылка.ХозяйственнаяОперация В (
	|									ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
	|									ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))
	|			И РеализацияТоваровИУслугТоварыВПути.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРеализацийТоваровУслуг.ВПути)
	|			И (РеализацияТоваровИУслугТоварыВПути.Ссылка.Проведен)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|		ПО (РеализацияТоваровИУслугТоварыВПути.Ссылка = СуммыДокументовВВалютахУчета.Регистратор)
	|			И (РеализацияТоваровИУслугТоварыВПути.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки)
	//++ НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки КАК ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки
	|		ПО ДокументыРеализации0.ДокументОтгрузки = ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки
	//-- НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ АктуальныеСчетаФактуры КАК ИсправленныеСчетаФактуры
	|		ПО ДокументыРеализации0.ДокументОтгрузки = ИсправленныеСчетаФактуры.ДокументОтгрузки
	|			И ИсправленныеСчетаФактуры.Исправление
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыРеализации0.Покупатель,
	|	ДокументыРеализации0.СтранаРегистрации,
	|	ДокументыРеализации0.СтранаРегистрации,
	|	СуммыДокументовВВалютахУчета.БазаНДСРегл,
	|	ОтгрузкаТоваровКлиенту.АналитикаУчетаНоменклатуры,
	|	ДокументыРеализации0.НалоговаяБазаДокументаОтгрузки,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|			ТОГДА """"
	//++ НЕ УТ
	|		КОГДА ЕСТЬNULL(СчетФактураВыданныйТовары.Номенклатура.Операции0, ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.Операции0.ПустаяСсылка)
	|			ТОГДА СчетФактураВыданныйТовары.Номенклатура.Операции0.Код
	//-- НЕ УТ
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ДокументыРеализации0.ДокументОтгрузки,
	|	ВЫБОР
	|		КОГДА АктуальныеСчетаФактуры.Исправление
	|			ТОГДА АктуальныеСчетаФактуры.СчетФактура
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	//++ НЕ УТ
	|		КОГДА ДокументыРеализации0.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|				И ДокументыРеализации0.СтранаРегистрации.УчастникЕАЭС
	|				И ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.НеПодтвержденаРеализация0)
	//-- НЕ УТ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|ИЗ
	|	ДокументыРеализации0 КАК ДокументыРеализации0
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АктуальныеСчетаФактуры КАК АктуальныеСчетаФактуры
	|		ПО ДокументыРеализации0.ДокументОтгрузки = АктуальныеСчетаФактуры.ДокументОтгрузки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.Товары КАК СчетФактураВыданныйТовары
	|		ПО АктуальныеСчетаФактуры.СчетФактура = СчетФактураВыданныйТовары.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|		ПО (СчетФактураВыданныйТовары.Ссылка = СуммыДокументовВВалютахУчета.Регистратор)
	|			И (СчетФактураВыданныйТовары.ИдентификаторСтроки = СуммыДокументовВВалютахУчета.ИдентификаторСтроки)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтгрузкаТоваровКлиенту.ВидыЗапасов КАК ОтгрузкаТоваровКлиенту
	|		ПО СчетФактураВыданныйТовары.Ссылка.ДокументОснование = ОтгрузкаТоваровКлиенту.Ссылка
	|			И СчетФактураВыданныйТовары.КодСтроки = ОтгрузкаТоваровКлиенту.КодСтроки
	|			И СчетФактураВыданныйТовары.Номенклатура = ОтгрузкаТоваровКлиенту.АналитикаУчетаНоменклатуры.Номенклатура
	|			И СчетФактураВыданныйТовары.Характеристика = ОтгрузкаТоваровКлиенту.АналитикаУчетаНоменклатуры.Характеристика
	|			И ОтгрузкаТоваровКлиенту.Ссылка.Проведен
	//++ НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки КАК ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки
	|		ПО ДокументыРеализации0.ДокументОтгрузки = ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки
	//-- НЕ УТ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыРеализации0.Покупатель,
	|	ДокументыРеализации0.СтранаРегистрации,
	|	ДокументыРеализации0.СтранаРегистрации,
	|	СуммыДокументовВВалютахУчета.БазаНДСРегл,
	|	НЕОПРЕДЕЛЕНО,
	|	ДокументыРеализации0.НалоговаяБазаДокументаОтгрузки,
	|	"""",
	|	ДокументыРеализации0.ДокументОтгрузки,
	|	ЕСТЬNULL(ИсправленныеСчетаФактуры.СчетФактура, НЕОПРЕДЕЛЕНО),
	|	ВЫБОР
	|		КОГДА ЛОЖЬ
	|			ТОГДА НЕОПРЕДЕЛЕНО
	//++ НЕ УТ
	|		КОГДА ДокументыРеализации0.СтранаРегистрации <> ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			И ДокументыРеализации0.СтранаРегистрации.УчастникЕАЭС
	|			И ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки ЕСТЬ NULL
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.НеПодтвержденаРеализация0)
	//-- НЕ УТ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|ИЗ
	|	ДокументыРеализации0 КАК ДокументыРеализации0
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочаяВыручка КАК ПрочаяВыручка
	|		ПО ДокументыРеализации0.ДокументОтгрузки = ПрочаяВыручка.Регистратор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|		ПО (ПрочаяВыручка.Регистратор = СуммыДокументовВВалютахУчета.Регистратор)
	|			И (ПрочаяВыручка.ИдентификаторФинЗаписи = СуммыДокументовВВалютахУчета.ИдентификаторСтроки)
	//++ НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки КАК ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки
	|		ПО ДокументыРеализации0.ДокументОтгрузки = ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки
	//-- НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ АктуальныеСчетаФактуры КАК ИсправленныеСчетаФактуры
	|		ПО ДокументыРеализации0.ДокументОтгрузки = ИсправленныеСчетаФактуры.ДокументОтгрузки
	|			И ИсправленныеСчетаФактуры.Исправление
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ДокументыОтгрузки.ПродажиНДС0) КАК НалоговаяБазаДокументаОтгрузки,
	|	ДокументыОтгрузки.ДокументОтгрузки КАК ДокументОтгрузки
	|ПОМЕСТИТЬ СуммыДокументовВВалютахУчетаСУчетомКорректировок
	|ИЗ
	|	ОтгрузкиДляОпределенияОфшоровИВзаимозависимости КАК ДокументыОтгрузки
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыОтгрузки.ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыОтгрузки.НалоговаяБазаДокументаОтгрузки - ЕСТЬNULL(НДСПродажи0.ПодтвержденнаяСуммаПоДокументуОтгрузки, 0) КАК НалоговаяБазаДокументаОтгрузки,
	|	ДокументыОтгрузки.ДокументОтгрузки КАК ДокументОтгрузки
	|ПОМЕСТИТЬ СуммыДокументовВВалютахУчетаИтоговые
	|ИЗ
	|	СуммыДокументовВВалютахУчетаСУчетомКорректировок КАК ДокументыОтгрузки
	|		ЛЕВОЕ СОЕДИНЕНИЕ НДСПродажи0 КАК НДСПродажи0
	|		ПО ДокументыОтгрузки.ДокументОтгрузки = НДСПродажи0.ДокументОтгрузки
	|ГДЕ
	|	ДокументыОтгрузки.НалоговаяБазаДокументаОтгрузки - ЕСТЬNULL(НДСПродажи0.ПодтвержденнаяСуммаПоДокументуОтгрузки, 0) > 0
	|";
	
КонецФункции

#КонецОбласти

#КонецЕсли
