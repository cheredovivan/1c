#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ТипДанныхЗаполнения = Тип("Структура") И ДанныеЗаполнения.Свойство("ДокументыРеализации") Тогда
	
		КлассификаторXML = ПолучитьОбщийМакет("ПереченьКодовОпераций0").ПолучитьТекст();
		Чтение = Новый ЧтениеXML;
		Чтение.УстановитьСтроку(КлассификаторXML);
		ТаблицаКодовОпераций0 = СериализаторXDTO.ПрочитатьXML(Чтение);
		
		ПараметрыЗначений = Новый Структура();
		ПараметрыЗначений.Вставить("Организация", ДанныеЗаполнения.Организация);
		ПараметрыЗначений.Вставить("Дата", ДанныеЗаполнения.Дата);
		ПараметрыЗначений.Вставить("Событие", ДанныеЗаполнения.Событие);
		ПараметрыЗначений.Вставить("ДокументыОтгрузки", ДанныеЗаполнения.ДокументыРеализации);
		ПараметрыЗначений.Вставить("ТаблицаКодовОпераций0", ТаблицаКодовОпераций0);
	
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор());
		Документы.ПодтверждениеНулевойСтавкиНДС.ЗаполнитьДокумент(ПараметрыЗначений, АдресХранилища);
		СтруктураДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
	
		Если ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
		
		Состав.Загрузить(СтруктураДанных.Состав);
		
		Если СтруктураДанных.Свойство("КодыОперацийНалоговаяБаза") Тогда
			 КодыОперацийНалоговаяБаза.Загрузить(СтруктураДанных.КодыОперацийНалоговаяБаза);
		КонецЕсли;
		УдалитьИзВременногоХранилища(АдресХранилища);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
	Состав.Очистить();
	КодыОперацийНалоговаяБаза.Очистить();
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	СформироватьСчетаФактуры(РежимЗаписи);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьСчетаФактуры(РежимЗаписи)
	
	НеобходимоСоставитьСчетФактуру = УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(Дата) >= 3;
	
	Для Каждого СтрокаТабличнойЧасти Из Состав Цикл
		Если СтрокаТабличнойЧасти.Событие = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0
			И НеобходимоСоставитьСчетФактуру
			И Не ЗначениеЗаполнено(СтрокаТабличнойЧасти.СчетФактураНаНеподтвержденнуюРеализацию0)
			И РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения Тогда
				ДанныеЗаполнения = Новый Структура;
				ДанныеЗаполнения.Вставить("ДокументОснование", СтрокаТабличнойЧасти.ДокументОтгрузки);
				ДанныеЗаполнения.Вставить("Организация",       Организация);
				
				НоменклатураИКодОперации = Новый Соответствие;
				КодыОпераций = КодыОперацийНалоговаяБаза.НайтиСтроки(Новый Структура("КлючСтроки", СтрокаТабличнойЧасти.КлючСтроки));
				Для Каждого Строка Из КодыОпераций Цикл
					Номенклатура = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.АналитикаУчетаНоменклатуры, "Номенклатура");
					НоменклатураИКодОперации.Вставить(Номенклатура, Строка.КодОперации);
				КонецЦикла;
				ДанныеЗаполнения.Вставить("КодыОпераций0", НоменклатураИКодОперации);
				
				Если Дата >= Дата(2024, 1, 1) Тогда
					ДанныеЗаполнения.Вставить("Дата", Дата);
				Иначе
					ДанныеЗаполнения.Вставить("Дата", Мин(Дата, ТекущаяДатаСеанса()));
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.СчетФактураНаНеподтвержденнуюРеализацию0) Тогда
					СчетФактураВыданный = СтрокаТабличнойЧасти.СчетФактураНаНеподтвержденнуюРеализацию0.ПолучитьОбъект();
				Иначе
					СчетФактураВыданный = Документы.СчетФактураНаНеподтвержденнуюРеализацию0.СоздатьДокумент();
				КонецЕсли;
				СчетФактураВыданный.Заполнить(ДанныеЗаполнения);
				СчетФактураВыданный.Записать(РежимЗаписиДокумента.Проведение);
				СтрокаТабличнойЧасти.СчетФактураНаНеподтвержденнуюРеализацию0 = СчетФактураВыданный.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли