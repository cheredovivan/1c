#Область ОписаниеПеременных

&НаКлиенте
Перем СтароеЗначениеСоставПродажиСНДС0;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ИнтеграцияС1СДокументооборотом
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// СтандартныеПодсистемы.РаботаСФайлами
	ПараметрыГиперссылки = РаботаСФайлами.ГиперссылкаФайлов();
	ПараметрыГиперссылки.Размещение = "КоманднаяПанель";
	РаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыГиперссылки);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Ключ.Пустая() Тогда
		ЗаполнитьРеквизитыИзПараметровФормы(ЭтотОбъект);
	КонецЕсли;
	
	ТекущаяДатаДокумента = Объект.Дата;
	ТекущаяОрганизация = Объект.Организация;
	ВерсияПостановления1137 = УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(Объект.Дата);
	
	ОбновитьПодвал(ЭтотОбъект);
	
	//++ НЕ УТ
	// Загружаем полную таблицу кодов операций, потом будет фильтрация, согласно дате документа
	КлассификаторXML = ПолучитьОбщийМакет("ПереченьКодовОпераций0").ПолучитьТекст();
	Чтение = Новый ЧтениеXML;
	Чтение.УстановитьСтроку(КлассификаторXML);
	ТаблицаКодовОпераций0.Загрузить(СериализаторXDTO.ПрочитатьXML(Чтение));
	//-- НЕ УТ
	
	НеПодтвержденаСтавка0 = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0;
	
	Если ПолучитьФункциональнуюОпцию("УправлениеТорговлей") Тогда
		Элементы.СоставПродажиСНДС0.ТолькоПросмотр = Истина;
		Элементы.СоставСобытие.СписокВыбора.Удалить(1);
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "РедактированиеСпискаКодовОпераций" Тогда 
		
		КлючПоиска = Новый Структура("КлючСтроки", Параметр.КлючСтроки);
		
		МассивСтрокКодыОперацийНалоговаяБаза = Объект.КодыОперацийНалоговаяБаза.НайтиСтроки(КлючПоиска);
		
		Для Каждого СтрокаТЧ Из МассивСтрокКодыОперацийНалоговаяБаза Цикл
			Объект.КодыОперацийНалоговаяБаза.Удалить(СтрокаТЧ);
		КонецЦикла;
		
		АдресТаблицыКодыОперацийНалоговаяБаза = Параметр.АдресТаблицыКодыОперацийНалоговаяБаза;
		ЗагрузитьКодыОперацийИзХранилища(Параметр.КлючСтроки);
		
	КонецЕсли;
	
	СобытияФормКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПроверкаЗаполнения() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СобытияФормКлиент.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	СобытияФорм.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;

	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата, 
		ТекущаяДатаДокумента);
		
	// Если определили, что изменение даты может повлиять на какие-либо параметры, 
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера Тогда
		Если Объект.Состав.Количество() > 0 Тогда
			ТекстВопроса = НСтр("ru = 'Табличная часть будет очищена. Продолжить?';
								|en = 'The table will be cleared. Do you want to continue?'");
			Оповещение = Новый ОписаниеОповещения("ВопросОчиститьТабличнуюЧастьЗавершение", ЭтотОбъект, ТекущаяДатаДокумента);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			ДатаПриИзмененииНаСервере();
		КонецЕсли;
	Иначе
		// Запомним новую дату документа.
		ТекущаяДатаДокумента = Объект.Дата;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если Объект.Состав.Количество() <> 0 Тогда 
		ТекстВопроса = НСтр("ru = 'Необходимо очистить табличную часть. Продолжить?';
							|en = 'The tabular section must be cleared. Continue?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПриИзмененииОрганизацииЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	ИначеЕсли ТекущаяОрганизация <> Объект.Организация Тогда
		ТекущаяОрганизация = Объект.Организация;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСостав

&НаКлиенте
Процедура СоставПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекДанныеСостав = Элементы.Состав.ТекущиеДанные;
	
	Если ТекДанныеСостав <> Неопределено Тогда
	
		СтароеЗначениеСоставПродажиСНДС0 = ТекДанныеСостав.ПродажиСНДС0;
		
		Если НоваяСтрока Тогда
			
			ТекДанныеСостав.КлючСтроки = Новый УникальныйИдентификатор;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоставПриИзменении(Элемент)
	
	ТекДанные = Элементы.Состав.ТекущиеДанные;
	
	Если ТекДанные <> Неопределено Тогда
		
		Если ТекДанные.КлючСтроки = "" Тогда
			
			ТекДанные.КлючСтроки = Новый УникальныйИдентификатор;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьПодвал(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СоставКодыОперацийПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Элементы.Состав.ТекущиеДанные <> Неопределено Тогда
		Если Элементы.Состав.ТекущиеДанные.КлючСтроки = "" Тогда
				
			ТекДанныеСостав = Элементы.Состав.ТекущиеДанные;
			ТекДанныеСостав.КлючСтроки = Новый УникальныйИдентификатор;
			
		КонецЕсли;
	КонецЕсли;
	
	ВыбраннаяСтрока = Элементы.Состав.ТекущаяСтрока;
	
	ОткрытьФормуКодыОпераций(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура СоставВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекДанныеСостав = Элементы.Состав.ТекущиеДанные;
	
	Если ТекДанныеСостав <> Неопределено Тогда
	
		Если Поле.Имя = "СоставКодыОперацийПредставление" Тогда
			
			Если Элементы.Состав.ТекущиеДанные.КлючСтроки = "" Тогда
				
				ТекДанныеСостав.КлючСтроки = Новый УникальныйИдентификатор;
			
			КонецЕсли;
		
			ОткрытьФормуКодыОпераций(ВыбраннаяСтрока);
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоставПередУдалением(Элемент, Отказ)
	
	ВыделенныеСтроки = Элементы.Состав.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Для Каждого ИдСтроки Из ВыделенныеСтроки Цикл
		ТекДанные = Объект.Состав.НайтиПоИдентификатору(ИдСтроки);
		Если ТекДанные <> Неопределено Тогда
			// Необходимо очистить дополнительные сведения
			НоваяСтрока = ТаблицаУдаленныхКлючей.Добавить();
			НоваяСтрока.КлючСтроки = ТекДанные.КлючСтроки;
		КонецЕсли;
	КонецЦикла;
	ПодключитьОбработчикОжидания("Подключаемый_УдалитьСвязанныеЗаписи", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УдалитьСвязанныеЗаписи()
	
	УдалитьСвязанныеЗаписиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СоставПродажиСНДС0ПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(СтароеЗначениеСоставПродажиСНДС0) Тогда
		Если Элемент.Родитель.ТекущиеДанные.ПродажиСНДС0 < СтароеЗначениеСоставПродажиСНДС0 Тогда
			Элемент.Родитель.ТекущиеДанные.Событие = 
				ПредопределенноеЗначение("Перечисление.НДССостоянияРеализация0.ЧастичноПодтвержденаРеализация0");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоставСобытиеПриИзменении(Элемент)
	
	Если Элемент.Родитель.ТекущиеДанные.Событие = ПредопределенноеЗначение("Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0")
		Или Элемент.Родитель.ТекущиеДанные.Событие = ПредопределенноеЗначение("Перечисление.НДССостоянияРеализация0.ЧастичноПодтвержденаРеализация0") Тогда
			Элемент.Родитель.ТекущиеДанные.СчетФактураНаНеподтвержденнуюРеализацию0 = ПредопределенноеЗначение("Документ.СчетФактураНаНеподтвержденнуюРеализацию0.ПустаяСсылка");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если Объект.Проведен Тогда
		ТекстВопроса = НСтр("ru = 'Проведенный документ не может быть заполнен автоматически. 
			|Отменить проведение документа для заполнения?';
			|en = 'Cannot fill in the posted document automatically. 
			|Cancel document posting for filling?'"); 
		Оповещение = Новый ОписаниеОповещения("ВопросЗаполнитьЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	Иначе
		ЗаполнитьДокумент();
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.РаботаСФайлам

&НаКлиенте
Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)
	РаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	РаботаСФайламиКлиент.ПолеПредпросмотраПроверкаПеретаскивания(ЭтотОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	РаботаСФайламиКлиент.ПолеПредпросмотраПеретаскивание(ЭтотОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраНажатие(Элемент, СтандартнаяОбработка)
	РаботаСФайламиКлиент.ПолеПредпросмотраНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

// Конец СтандартныеПодсистемы.РаботаСФайлам

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыИзПараметровФормы(Форма)
	
	ПараметрыЗаполненияФормы = Неопределено;
	
	Если Форма.Параметры.Свойство("ПараметрыЗаполненияФормы",ПараметрыЗаполненияФормы) Тогда
		ЗаполнитьЗначенияСвойств(Форма.Объект,ПараметрыЗаполненияФормы);
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПодвал(Форма)
	
	Объект   			  = Форма.Объект;
	Форма.НадписьВсего 	  = Объект.Состав.Итог("ПродажиСНДС0");

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДокумент(ОтменитьПроведение = Ложь)
	
	// 1. Запуск фонового задания на сервере.
	ДлительнаяОперация = ЗаполнитьДокументНаСервере();
	
	// 2. Подключение обработчика завершения фонового задания.
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьДокументЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаполнитьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьДокумент();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьДокументНаСервере()
	
	Если Объект.Проведен Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения));
	КонецЕсли;
	
	Объект.Состав.Очистить();
	Объект.КодыОперацийНалоговаяБаза.Очистить();
	
	ПараметрыЗначений = Новый Структура();
	ПараметрыЗначений.Вставить("Организация", Объект.Организация);
	ПараметрыЗначений.Вставить("Дата", Объект.Дата);
	ПараметрыЗначений.Вставить("Событие", Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0);
	ПараметрыЗначений.Вставить("ТаблицаКодовОпераций0", ТаблицаКодовОпераций0.Выгрузить());
	ПараметрыЗначений.Вставить("ДокументыОтгрузки", Неопределено);
			
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, 
												"Документы.ПодтверждениеНулевойСтавкиНДС.ЗаполнитьДокумент",
												ПараметрыЗначений, АдресХранилища);

КонецФункции

&НаСервере
Процедура ЗагрузитьПодготовленныеДанные()
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	Если ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Состав.Загрузить(СтруктураДанных.Состав);
	
	Если СтруктураДанных.Свойство("КодыОперацийНалоговаяБаза") Тогда
		 Объект.КодыОперацийНалоговаяБаза.Загрузить(СтруктураДанных.КодыОперацийНалоговаяБаза);
	КонецЕсли;
	
	ОбновитьПодвал(ЭтотОбъект);
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДокументЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда // Пользователь отменил задание.
		Возврат;
	КонецЕсли;

	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
	КонецЕсли;

	ЗагрузитьПодготовленныеДанные();
	УдалитьИзВременногоХранилища(АдресХранилища);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОчиститьТабличнуюЧастьЗавершение(Результат, ТекущаяДатаДокумента) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ДатаПриИзмененииНаСервере();
		Объект.Состав.Очистить();
	Иначе
		Объект.Дата = ТекущаяДатаДокумента;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверкаЗаполнения()
	
	Отказ = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаСостав", Объект.Состав.Выгрузить(,"КлючСтроки,НомерСтроки,ПродажиСНДС0"));
	Запрос.УстановитьПараметр("ТаблицаКодыОпераций", Объект.КодыОперацийНалоговаяБаза.Выгрузить(,"КлючСтроки,КодОперации,НомерСтроки,ПродажиСНДС0"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаСостав.КлючСтроки КАК КлючСтроки,
	|	ТаблицаСостав.НомерСтроки КАК НомерСтроки,
	|	ТаблицаСостав.ПродажиСНДС0 КАК ПродажиСНДС0
	|ПОМЕСТИТЬ СоставПроверкиСумм
	|ИЗ
	|	&ТаблицаСостав КАК ТаблицаСостав
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКодыОпераций.КлючСтроки КАК КлючСтроки,
	|	ТаблицаКодыОпераций.ПродажиСНДС0 КАК ПродажиСНДС0,
	|	ТаблицаКодыОпераций.КодОперации КАК КодОперации
	|ПОМЕСТИТЬ КодыОперацийСтрокиСумм
	|ИЗ
	|	&ТаблицаКодыОпераций КАК ТаблицаКодыОпераций
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(КодыОперацийСтрокиСумм.ПродажиСНДС0) КАК ПродажиСНДС0,
	|	КодыОперацийСтрокиСумм.КлючСтроки КАК КлючСтроки
	|ПОМЕСТИТЬ КодыОперацийПроверкиСумм
	|ИЗ
	|	КодыОперацийСтрокиСумм КАК КодыОперацийСтрокиСумм
	|
	|СГРУППИРОВАТЬ ПО
	|	КодыОперацийСтрокиСумм.КлючСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоставПроверкиСумм.НомерСтроки КАК НомерСтроки,
	|	""ПродажиСНДС0"" КАК Ошибка,
	|	СоставПроверкиСумм.ПродажиСНДС0 КАК СоставСумма,
	|	КодыОперацийПроверкиСумм.ПродажиСНДС0 КАК КодыОперацийСумма
	|ИЗ
	|	СоставПроверкиСумм КАК СоставПроверкиСумм
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КодыОперацийПроверкиСумм КАК КодыОперацийПроверкиСумм
	|		ПО СоставПроверкиСумм.КлючСтроки = КодыОперацийПроверкиСумм.КлючСтроки
	|			И СоставПроверкиСумм.ПродажиСНДС0 <> КодыОперацийПроверкиСумм.ПродажиСНДС0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоставПроверкиСумм.НомерСтроки КАК НомерСтроки,
	|	""КодОперации"" КАК Ошибка
	|ИЗ
	|	СоставПроверкиСумм КАК СоставПроверкиСумм
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КодыОперацийСтрокиСумм КАК КодыОперацийСтрокиСумм
	|		ПО СоставПроверкиСумм.КлючСтроки = КодыОперацийСтрокиСумм.КлючСтроки
	|			И КодыОперацийСтрокиСумм.КодОперации = """"
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СоставПроверкиСумм
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ КодыОперацийСтрокиСумм
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ КодыОперацийПроверкиСумм";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	КоличествоРезультатов = МассивРезультатов.Количество();
	
	Выборка = МассивРезультатов[КоличествоРезультатов - 5].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Текст = СтрШаблон(НСтр("ru = 'В строке %1 значение %2 %3 по документу отгрузки не совпадает с суммарным значением %4 по кодам операций';
								|en = 'In the %1 line, the %2 %3 value of the shipping document does not match the %4 total value of transaction codes'"),
			Выборка.НомерСтроки,
			Выборка.Ошибка,
			Выборка.СоставСумма,
			Выборка.КодыОперацийСумма);
			
		ОбщегоНазначения.СообщитьПользователю(
			Текст,
			,
			"Состав[" + Выборка.НомерСтроки + "]." + Выборка.Ошибка,
			,
			Отказ);
			
	КонецЦикла;
	
	//++ НЕ УТ
	Выборка = МассивРезультатов[КоличествоРезультатов -4].Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Текст = СтрШаблон(НСтр("ru = 'В строке %1 не заполнен код операции';
								|en = 'Transaction code in line %1 is not filled in'"),
			Выборка.НомерСтроки);
			
		ОбщегоНазначения.СообщитьПользователю(
			Текст,
			,
			"Состав[" + Выборка.НомерСтроки + "]." + Выборка.Ошибка,
			,
			Отказ);
			
	КонецЦикла;
	//-- НЕ УТ
	
	Возврат Отказ;
	
КонецФункции

&НаСервере
Процедура ДатаПриИзмененииНаСервере()

	ВерсияПостановления1137 = УчетНДСПереопределяемый.ВерсияПостановленияНДС1137(Объект.Дата);
	ТекущаяДатаДокумента = Объект.Дата;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	// СоставКодыОперацийПредставление

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставКодыОперацийПредставление");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Состав.КодыОперацийПредставление", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", "<...>");
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);

	// СоставСчетФактураНаНеподтвержденнуюРеализацию0

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставСчетФактураНаНеподтвержденнуюРеализацию0");

	СостоянияПодтвержденаРеализация0 = Новый СписокЗначений;
	СостоянияПодтвержденаРеализация0.Добавить(Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0);
	СостоянияПодтвержденаРеализация0.Добавить(Перечисления.НДССостоянияРеализация0.ЧастичноПодтвержденаРеализация0);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Состав.Событие", ВидСравненияКомпоновкиДанных.ВСписке, СостоянияПодтвержденаРеализация0);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не требуется';
																	|en = 'Not required'"));

	// СоставВидЦенности

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставВидЦенности");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ВерсияПостановления1137", ВидСравненияКомпоновкиДанных.БольшеИлиРавно, 3);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	// СоставПродажиСНДС0

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставПродажиСНДС0");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Состав.Событие", ВидСравненияКомпоновкиДанных.Равно, Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// Событие
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставСобытие");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Состав.Контрагент", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	// СоставПродажиСНДС0ДляУТ

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СоставПродажиСНДС0");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Состав.ПродажиСНДС0", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Ложь);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьКодыОпераций(СтрокаТабличнойЧасти, КодыОперацийПоСтроке)

	КодыОперацийПредставление = "";
	КоличествоКодовОпераций = КодыОперацийПоСтроке.Количество();
	Для Инд = 0 По КоличествоКодовОпераций - 1 Цикл
		
		КодОперации = КодыОперацийПоСтроке[Инд].КодОперации;
		Если Инд < 2 Тогда
			КодыОперацийПредставление = КодыОперацийПредставление + ?(ЗначениеЗаполнено(КодыОперацийПредставление), ";", "") + КодОперации;
		Иначе
			КодыОперацийПредставление = КодыОперацийПредставление + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = ' и еще %1';
					|en = ' and %1 more'"),
				КоличествоКодовОпераций - Инд);
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтрокаТабличнойЧасти.Свойство("КодыОперацийПредставление") Тогда
		СтрокаТабличнойЧасти.КодыОперацийПредставление = КодыОперацийПредставление;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКодыОпераций(ВыбранноеЗначение)
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = Объект.Состав.НайтиПоИдентификатору(ВыбранноеЗначение);
	
	Если Не ТолькоПросмотр Тогда
		ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресТаблицыКодыОперацийНалоговаяБаза", ПоместитьКодыОперацийНалоговаяБазаВХранилище(ДанныеСтроки.КлючСтроки));
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыФормы.Вставить("КлючСтроки", ДанныеСтроки.КлючСтроки);
	ПараметрыФормы.Вставить("Дата", Объект.Дата);
	ПараметрыФормы.Вставить("ДокументОтгрузкиПродажиСНДС0", ДанныеСтроки.ПродажиСНДС0);
	ПараметрыФормы.Вставить("Событие", ДанныеСтроки.Событие);
	ПараметрыФормы.Вставить("Покупатель", ДанныеСтроки.Контрагент);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("ДокументОтгрузки", ДанныеСтроки.ДокументОтгрузки);
	ПараметрыФормы.Вставить("ВерсияПостановления1137", ВерсияПостановления1137);
	
	ОткрытьФорму("Документ.ПодтверждениеНулевойСтавкиНДС.Форма.ФормаКодовОпераций", ПараметрыФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьКодыОперацийНалоговаяБазаВХранилище(КлючСтроки)
	
	СтруктураОтбора = Новый Структура("КлючСтроки", КлючСтроки);
	ТаблицаКодыОперацийНалоговаяБаза = Объект.КодыОперацийНалоговаяБаза.Выгрузить(СтруктураОтбора);
	АдресТаблицыКодыОперацийНалоговаяБаза = ПоместитьВоВременноеХранилище(ТаблицаКодыОперацийНалоговаяБаза, УникальныйИдентификатор);
	
	Возврат АдресТаблицыКодыОперацийНалоговаяБаза;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьКодыОперацийИзХранилища(КлючСтроки)
	
	ТаблицаКодыОперацийНалоговаяБаза = ПолучитьИзВременногоХранилища(АдресТаблицыКодыОперацийНалоговаяБаза);
	
	Для Каждого СтрокаТЧ Из ТаблицаКодыОперацийНалоговаяБаза Цикл
		НоваяСтрока = Объект.КодыОперацийНалоговаяБаза.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
	КонецЦикла;
	
	КлючПоиска = Новый Структура("КлючСтроки", КлючСтроки);
	МассивСтрокСостав = Объект.Состав.НайтиСтроки(КлючПоиска);
	Если МассивСтрокСостав.Количество() > 0 Тогда 
		СтрокаТаблицы = МассивСтрокСостав[0];
		Если ТаблицаКодыОперацийНалоговаяБаза.Количество() > 0 Тогда 
			ЗаполнитьКодыОпераций(СтрокаТаблицы, ТаблицаКодыОперацийНалоговаяБаза);
		Иначе
			СтрокаТаблицы.КодыОперацийПредставление = "<...>";
		КонецЕсли;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПриИзмененииОрганизацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Объект.Состав.Очистить();
		Объект.КодыОперацийНалоговаяБаза.Очистить();
		
		Если ТекущаяОрганизация <> Объект.Организация Тогда
			
			ТекущаяОрганизация = Объект.Организация;
			
		КонецЕсли;
		
	Иначе
		
		Объект.Организация = ТекущаяОрганизация;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСвязанныеЗаписиНаСервере()
	
	Для Каждого СтрокаКлюча Из ТаблицаУдаленныхКлючей Цикл
		
		КлючПоиска = Новый Структура("КлючСтроки", СтрокаКлюча.КлючСтроки);
		
		МассивСтрок = Объект.КодыОперацийНалоговаяБаза.НайтиСтроки(КлючПоиска);
		Для Каждого СтрокаТЧ Из МассивСтрок Цикл
			Объект.КодыОперацийНалоговаяБаза.Удалить(СтрокаТЧ);
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаУдаленныхКлючей.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

#КонецОбласти
