
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры,
		"КлючСтроки,АдресТаблицыКодыОперацийНалоговаяБаза,
		|ДокументОтгрузкиПродажиСНДС0,,
		|Событие,Дата,Организация,
		|ВерсияПостановления1137,Покупатель,ДокументОтгрузки");

	ИсключитьПустоеЗначение = Новый Массив;
	ИсключитьПустоеЗначение.Добавить("");
	//++ НЕ УТ
	ТаблицаВыбора.Загрузить(УчетНДС.КлассификаторКодовОперацийПоСтавке0(Параметры.Дата,,ИсключитьПустоеЗначение));
	//-- НЕ УТ
	
	ТаблицаКодыОперацийНалоговаяБаза = ПолучитьИзВременногоХранилища(АдресТаблицыКодыОперацийНалоговаяБаза);
	
	Объект.КодыОперацийНалоговаяБаза.Очистить();
	Объект.КодыОперацийНалоговаяБаза.Загрузить(ТаблицаКодыОперацийНалоговаяБаза);
	
	ОбновитьПодвал(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗавершениеРаботы И (Модифицированность ИЛИ ПеренестиВДокумент) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И НЕ ПеренестиВДокумент Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?';
							|en = 'The data has changed. Do you want to save the changes?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
	Если ПеренестиВДокумент И НЕ Отказ Тогда
		Если Не ПроверитьЗаполнениеНаКлиенте() Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		ПеренестиВДокумент = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ПеренестиВДокумент Тогда
		
		СтруктураРезультат = ПриЗакрытииНаСервере();
		
		Оповестить("РедактированиеСпискаКодовОпераций", СтруктураРезультат, ВладелецФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКодыОперацийНалоговаяБаза

&НаКлиенте
Процедура КодыОперацийПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ДанныеСтроки = Элементы.КодыОперацийНалоговаяБаза.ТекущиеДанные;
	
	Если НоваяСтрока Тогда
		ДанныеСтроки.КлючСтроки = КлючСтроки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодыОперацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	//++ НЕ УТ
	Если Поле.Имя = "КодыОперацийНалоговаяБазаКодОперации" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Заголовок",          "Выбор кода операции");
		ПараметрыФормы.Вставить("ТаблицаЗначений",    ТаблицаВыбора);
		ПараметрыФормы.Вставить("СтруктураДляПоиска", Новый Структура("Код", Элементы.КодыОперацийНалоговаяБаза.ТекущиеДанные.КодОперации));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВвестиКодОперацииНалоговаяБазаЗавершение", ЭтотОбъект, Элементы.КодыОперацийНалоговаяБаза.ТолькоПросмотр);
		
		ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВыбораЗначенияИзТаблицы",
		ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	//-- НЕ УТ
	
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура КодыОперацийПриИзменении(Элемент)
	
	ОбновитьПодвал(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПеренестиВДокумент = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВопросПередЗакрытиемФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполнениеНаКлиенте()
	
	Отказ = Ложь;
	
	#Область ПроверкаКодовОперацийНалоговаяБаза

		ИтогоПродажиСНДС0 = Объект.КодыОперацийНалоговаяБаза.Итог("ПродажиСНДС0");
		
		Если ДокументОтгрузкиПродажиСНДС0 <> 0 И ДокументОтгрузкиПродажиСНДС0 <> ИтогоПродажиСНДС0 Тогда
			
			Текст = НСтр("ru = 'Итоговая сумма по кодам операций в колонке ""Продажи с НДС 0%""';
						|en = 'Total amount by transaction codes in the ""Sales including 0% VAT"" column'") +
			СтрШаблон(НСтр("ru = ' %1 не совпадает со значением %2 по документу отгрузки.';
							|en = ' %1 doesn''t match the ""%2"" value of the shipping document.'"),
			ИтогоПродажиСНДС0,
			ДокументОтгрузкиПродажиСНДС0);
				ОбщегоНазначенияКлиент.СообщитьПользователю(
					Текст,
					,
					"ДокументОтгрузкиПродажиСНДС0",
					,
					Отказ);
			
		КонецЕсли;

		ИмяСписка = НСтр("ru = 'Налоговая база';
						|en = 'Tax base'");
		
		МассивКодовОпераций = Новый Массив;
		
		Для Индекс = 0 По Объект.КодыОперацийНалоговаяБаза.Количество() - 1 Цикл
			
			СтрокаКодыОпераций = Объект.КодыОперацийНалоговаяБаза[Индекс];
			Префикс = "Объект.КодыОперацийНалоговаяБаза[%1]";
			Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Префикс, Формат(Индекс, "ЧН=0; ЧГ="));
			
			Если Не ЗначениеЗаполнено(СтрокаКодыОпераций.КодОперации) Тогда
				Поле = Префикс + ".КодОперации";
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Код операции';
																									|en = 'Transaction code'"),
					Индекс + 1, ИмяСписка);
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , Поле, , Отказ);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаКодыОпераций.ПродажиСНДС0) Тогда
				Поле = Префикс + ".ПродажиСНДС0";
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Продажи с НДС 0%';
																									|en = 'Sales including 0% VAT'"),
					Индекс + 1, ИмяСписка);
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , Поле, , Отказ);
			КонецЕсли;
			
			Отбор = Новый Структура();
			Отбор.Вставить("КодОперации", СтрокаКодыОпераций.КодОперации);
			Отбор.Вставить("НесырьевыеТовары", СтрокаКодыОпераций.НесырьевыеТовары);
			Отбор.Вставить("АналитикаУчетаНоменклатуры", СтрокаКодыОпераций.АналитикаУчетаНоменклатуры);
			СтрокиТаблицы = Объект.КодыОперацийНалоговаяБаза.НайтиСтроки(Отбор);

			Если СтрокиТаблицы.Количество() > 1 И ЗначениеЗаполнено(СтрокаКодыОпераций.КодОперации) Тогда
				Текст = СтрШаблон(НСтр("ru = 'В строке %1 повторно указана комбинация кода операции %2 и признака ""Несырьевые товары "" %3.';
										|en = 'In line %1, the combination of transaction code %2 and the %3 ""Non-resource goods"" flag is repeated.'"),
					Индекс + 1,
					СтрокаКодыОпераций.КодОперации,
					СтрокаКодыОпераций.НесырьевыеТовары);
				ОбщегоНазначенияКлиент.СообщитьПользователю(
					Текст,
					,
					"КодыОперацийНалоговаяБаза["+Индекс+"].КодОперации",
					,
					Отказ);
			КонецЕсли;
			
			МассивКодовОпераций.Добавить(СтрокаКодыОпераций.КодОперации);
			
		КонецЦикла;
	
	#КонецОбласти
	
	Возврат Не Отказ;
	
КонецФункции

&НаСервере
Функция ПриЗакрытииНаСервере()
	
	ТаблицаКодыОперацийНалоговаяБаза = Объект.КодыОперацийНалоговаяБаза.Выгрузить();
	АдресТаблицыКодыОперацийНалоговаяБаза = ПоместитьВоВременноеХранилище(ТаблицаКодыОперацийНалоговаяБаза, УникальныйИдентификатор);
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("КлючСтроки", КлючСтроки);
	СтруктураРезультат.Вставить("АдресТаблицыКодыОперацийНалоговаяБаза", АдресТаблицыКодыОперацийНалоговаяБаза);
	
	Возврат СтруктураРезультат;
	
КонецФункции

&НаКлиенте
Процедура ВвестиКодОперацииНалоговаяБазаЗавершение(РезультатВыбора, ТолькоПросмотр) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТолькоПросмотр Тогда
		Элементы.КодыОперацийНалоговаяБаза.ТекущиеДанные.КодОперации = РезультатВыбора.Код;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьПодвал(Форма)
	
	Объект = Форма.Объект;
	Форма.НадписьВсегоНалоговаяБаза = Объект.КодыОперацийНалоговаяБаза.Итог("ПродажиСНДС0");
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.КодыОперацийНалоговаяБаза);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.КодыОперацийНалоговаяБаза, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.КодыОперацийНалоговаяБаза);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти
