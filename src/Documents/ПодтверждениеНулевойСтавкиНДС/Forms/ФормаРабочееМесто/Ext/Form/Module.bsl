
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
		
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ОсновнаяТаблица = "РегистрСведений.НДССостояниеРеализации0.СрезПоследних";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	СвойстваСписка.ТекстЗапроса = ПолучитьТекстЗапросаСписок();
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
	УстановитьЗначенияПоПараметрамФормы(Параметры);
	УстановитьУсловноеОформление();
	ОбновитьДанныеФормы();
	УстановитьОтборыПоОрганизации();
	УстановитьОтборыПоПериоду();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.Источники = Новый ОписаниеТипов("ДокументОбъект.ПодтверждениеНулевойСтавкиНДС");
	ПараметрыРазмещения.КоманднаяПанель = Элементы.СписокДокументовКоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СчетФактураНаНеподтвержденнуюРеализацию0" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	НачалоПериода = НачалоКвартала(НачалоПериода);
	КонецПериода = КонецКвартала(НачалоПериода);

	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)

	КонецПериода = КонецКвартала(КонецПериода);
	НачалоПериода = НачалоКвартала(КонецПериода);
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеОтборПриИзменении(Элемент)
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОтборПриИзменении(Элемент)
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры
 
&НаКлиенте
Процедура ОрганизацияОтборПриИзменении(Элемент)
	
	Если ОрганизацияСохраненноеЗначение <> ОрганизацияОтбор Тогда
		
		УстановитьОтборыПоОрганизации();
		ОрганизацияСохраненноеЗначение = ОрганизацияОтбор;
		
	КонецЕсли;
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДокументов

&НаКлиенте
Процедура СписокДокументовПриАктивизацииСтроки(Элемент)
	
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Поле.Имя = "СписокДокументРеализации" Тогда
		ОткрытьТекущийДокументРеализации();
	ИначеЕсли Поле.Имя = "СписокСчетФактураНаНеподтвержденнуюРеализацию0" Тогда
		ОткрытьТекущийСчетФактуруНаНеподтвержденнуюСтавку0();
	ИначеЕсли Поле.Имя = "ТаможеннаяДекларация" Тогда
		ОткрытьТекущийТаможеннаяДекларацияЭкспорт();
	ИначеЕсли Поле.Имя = "ЗаявлениеОВвозеТоваровОтПокупателя" Тогда
		ОткрытьТекущийЗаявлениеОВвозеТоваровОтПокупателя();
	Иначе
		ОткрытьТекущийДокументПодтверждения();
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", НачалоПериода, КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвержденаСтавкаНДС(Команда)
	
	ТекущееСостояние = ПредопределенноеЗначение("Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0");
	ПодтверждениеСтавкиНДС(ТекущееСостояние);
	
КонецПроцедуры

&НаКлиенте
Процедура НеподтвержденаСтавкаНДС(Команда)
	
	ТекущееСостояние = ПредопределенноеЗначение("Перечисление.НДССостоянияРеализация0.НеПодтвержденаРеализация0");
	ПодтверждениеСтавкиНДС(ТекущееСостояние);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокументПодтверждения(Команда)
	
	ОткрытьТекущийДокументПодтверждения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокументРеализации(Команда)
	
	ОткрытьТекущийДокументРеализации();
	
КонецПроцедуры

&НаКлиенте
Процедура СборВедется(Команда)
	
//++ НЕ УТ
	УстановитьСостояниеСбораДокументов(Ложь);
//-- НЕ УТ
	Возврат;// в УТ11 обработчик пустой
	
КонецПроцедуры

&НаКлиенте
Процедура СборЗавершен(Команда)
	
//++ НЕ УТ
	УстановитьСостояниеСбораДокументов(Истина);
//-- НЕ УТ
	Возврат;// в УТ11 обработчик пустой
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПриИзмененииРеквизитов

&НаСервере
Процедура ОбновитьДанныеФормы()
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "КонецПериода", КонецДня(КонецПериода));
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,"Состояние",
		Состояние, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Состояние));
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Организация",
		ОрганизацияОтбор, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(ОрганизацияОтбор));
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Контрагент",
		Контрагент, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Контрагент));
		
	//++ НЕ УТ
	Элементы.ГруппаСборДокументов.Видимость = Истина;
	//-- НЕ УТ
	
	УстановитьОтборыПоПериоду();
	
КонецПроцедуры

#КонецОбласти

#Область ПриРегистрацииПодтверждения

&НаКлиенте
Процедура ПодтверждениеСтавкиНДС(ТекущееСостояние)
	
	ОчиститьСообщения();
	
	ПодготовленныйСписокДокументов = Элементы.Список.ВыделенныеСтроки;
	
	Если ПодготовленныйСписокДокументов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументыРеализации = Новый Массив;

	Для Каждого Строка Из ПодготовленныйСписокДокументов Цикл
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(Строка);
		ДокументыРеализации.Добавить(ДанныеСтроки.ДокументРеализации);
		ОрганизацияДанныеСтроки = ?(ЗначениеЗаполнено(ОрганизацияОтбор), ОрганизацияОтбор, ДанныеСтроки.Организация);
	КонецЦикла;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Дата", КонецПериода);
	ЗначенияЗаполнения.Вставить("Организация", ОрганизацияДанныеСтроки);
	ЗначенияЗаполнения.Вставить("Событие", ТекущееСостояние);
	ЗначенияЗаполнения.Вставить("ДокументыРеализации", ДокументыРеализации);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ПодтверждениеНулевойСтавкиНДС.Форма.ФормаДокумента", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
#Область ИзменениеСостоянияСбораПодтверждающихДокументов

&НаКлиенте
Процедура УстановитьСостояниеСбораДокументов(НовоеСостояние)
	
	ОчиститьСообщения();
	
	ПодготовленныйСписокДокументов = Элементы.Список.ВыделенныеСтроки;
	
	Если ПодготовленныйСписокДокументов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из ПодготовленныйСписокДокументов Цикл
		
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(Строка);
		Если ЗначениеЗаполнено(ДанныеСтроки.ТаможеннаяДекларация) Тогда
			УстановитьСостояниеСбораДокументовСервер(ДанныеСтроки.ТаможеннаяДекларация, НовоеСостояние);
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьСостояниеСбораДокументовСервер(ТаможеннаяДекларация, НовоеСостояние)
	
	Документы.ТаможеннаяДекларацияЭкспорт.УстановитьСостояниеСбораСопроводительныхДокументов(
		ТаможеннаяДекларация,
		НовоеСостояние);
	
КонецПроцедуры
#КонецОбласти
//-- НЕ УТ

#Область Прочее

&НаСервере
Процедура УстановитьЗначенияПоПараметрамФормы(Параметры)
	
	СтруктураБыстрогоОтбора = Неопределено;
	Параметры.Свойство("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	Если СтруктураБыстрогоОтбора <> Неопределено Тогда
		СтруктураБыстрогоОтбора.Свойство("ОрганизацияОтбор", ОрганизацияОтбор);
		СтруктураБыстрогоОтбора.Свойство("НачалоПериода", НачалоПериода);
		СтруктураБыстрогоОтбора.Свойство("КонецПериода", КонецПериода);
		СтруктураБыстрогоОтбора.Свойство("Состояние", Состояние);
	Иначе
		НачалоПериода = НачалоКвартала(ТекущаяДатаСеанса());
		КонецПериода = КонецКвартала(ТекущаяДатаСеанса());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокСтавкаНДС.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Состояние");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.НДССостоянияРеализация0.ПодтвержденаРеализация0;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.СтавкаНДС");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Справочники.СтавкиНДС.ПустаяСсылка();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '0%';
																|en = '0%'"));
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокСчетФактураНаНеподтвержденнуюРеализацию0.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.СчетФактураНаНеподтвержденнуюРеализацию0");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборГруппаИЛИ = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппаИЛИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ОтборГруппаИЛИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Состояние");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0;
	
	ОтборЭлемента = ОтборГруппаИЛИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДатаПодтверждения");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = Дата(2014, 10, 01);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>';
																|en = '<not required>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокСчетФактураНаНеподтвержденнуюРеализацию0.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.СчетФактураНаНеподтвержденнуюРеализацию0");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Состояние");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.НДССостоянияРеализация0.НеПодтвержденаРеализация0;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.ДатаПодтверждения");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ОтборЭлемента.ПравоеЗначение = Дата(2014, 10, 01);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<требуется выписать>';
																|en = '<it is required to issue>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПросроченныеДанныеЦвет);
	
	//

КонецПроцедуры

&НаСервере
Процедура УстановитьОтборыПоОрганизации()
	
	СписокОрганизаций = Новый СписокЗначений;
	СписокОрганизаций.Добавить(ОрганизацияОтбор);
	
	Если ЗначениеЗаполнено(ОрганизацияОтбор)
		И ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеПодразделенияВыделенныеНаБаланс") Тогда
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ОбособленноеПодразделение
		|	И Организации.ГоловнаяОрганизация = &Организация
		|	И Организации.ДопускаютсяВзаиморасчетыЧерезГоловнуюОрганизацию");
		Запрос.УстановитьПараметр("Организация", ОрганизацияОтбор);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СписокОрганизаций.Добавить(Выборка.Ссылка);
		КонецЦикла;
	КонецЕсли;
		
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокДокументов,
		"Организация",
		СписокОрганизаций,
		ВидСравненияКомпоновкиДанных.ВСписке,
		,
		ЗначениеЗаполнено(ОрганизацияОтбор));
	
	Элементы.Организация.Видимость = Не ЗначениеЗаполнено(ОрганизацияОтбор);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборыПоПериоду()
	
	ГруппаЭлементовОтбора = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		СписокДокументов.Отбор.Элементы,
		Нстр("ru = 'Период';
			|en = 'Period'"),
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаЭлементовОтбора,
		"Дата",
		ВидСравненияКомпоновкиДанных.БольшеИлиРавно,
		НачалоДня(НачалоПериода),
		Нстр("ru = 'Начало периода';
			|en = 'Period start'"),
		ЗначениеЗаполнено(НачалоПериода));
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаЭлементовОтбора,
		"Дата",
		ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,
		КонецДня(КонецПериода),
		Нстр("ru = 'Конец периода';
			|en = 'Period end'"),
		ЗначениеЗаполнено(КонецПериода));
		
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатВыбора, "НачалоПериода,КонецПериода");
	НачалоПериода = НачалоКвартала(НачалоПериода);
	КонецПериода = КонецКвартала(НачалоПериода);
	
	ОбновитьДанныеФормы();
	
	Если ПроверитьЗаполнение() Тогда
		УстановитьОтборыПоПериоду();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТекущийДокументПодтверждения()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ПоказатьЗначение(Неопределено, ТекущиеДанные.Регистратор);
	
КонецПроцедуры	

&НаКлиенте
Процедура ОткрытьТекущийДокументРеализации()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ПоказатьЗначение(Неопределено, ТекущиеДанные.ДокументРеализации);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.СписокДокументов);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.СписокДокументов, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.СписокДокументов);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ОткрытьТекущийСчетФактуруНаНеподтвержденнуюСтавку0()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ПоказатьЗначение(Неопределено, ТекущиеДанные.СчетФактураНаНеподтвержденнуюРеализацию0);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТекущийТаможеннаяДекларацияЭкспорт()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ПоказатьЗначение(Неопределено, ТекущиеДанные.ТаможеннаяДекларация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТекущийЗаявлениеОВвозеТоваровОтПокупателя()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат
	КонецЕсли;
	
	ПоказатьЗначение(Неопределено, ТекущиеДанные.ЗаявлениеОВвозеТоваровОтПокупателя);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТекстЗапросаСписок()
	
	Возврат 
	"ВЫБРАТЬ
	|	НДССостояниеРеализации0.Регистратор,
	|	НДССостояниеРеализации0.Период,
	|	НДССостояниеРеализации0.Организация,
	|	НДССостояниеРеализации0.ДокументРеализации,
	|	НДССостояниеРеализации0.ДатаРеализации,
	|	НДССостояниеРеализации0.Контрагент,
	//++ НЕ УТ
	|	ТаможеннаяДекларацияОснования.Ссылка КАК ТаможеннаяДекларация,
	|	ТаможеннаяДекларацияДокумент.СборСопроводительныхДокументовЗавершен КАК СопроводительныеДокументы,
	|	МАКСИМУМ(ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.Ссылка) КАК ЗаявлениеОВвозеТоваровОтПокупателя,
	//-- НЕ УТ
	|	НДССостояниеРеализации0.Состояние,
	|	НДССостояниеРеализации0.СтавкаНДС,
	|	НДССостояниеРеализации0.ДатаПодтверждения,
	|	ВЫРАЗИТЬ(НДССостояниеРеализации0.Комментарий КАК СТРОКА(1000)) КАК Комментарий,
	|	СчетФактураВыданный.Ссылка КАК СчетФактураНаНеподтвержденнуюРеализацию0,
	|	СчетФактураВыданный.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.НДССостояниеРеализации0.СрезПоследних(&КонецПериода) КАК НДССостояниеРеализации0
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураНаНеподтвержденнуюРеализацию0 КАК СчетФактураВыданный
	|		ПО НДССостояниеРеализации0.ДокументРеализации = СчетФактураВыданный.ДокументОснование
	|		И НЕ СчетФактураВыданный.ПометкаУдаления
	//++ НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияЭкспорт.ДокументыОснования КАК ТаможеннаяДекларацияОснования
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияЭкспорт КАК ТаможеннаяДекларацияДокумент
	|			ПО ТаможеннаяДекларацияОснования.Ссылка = ТаможеннаяДекларацияДокумент.Ссылка
	|			И ТаможеннаяДекларацияДокумент.Проведен
	|		ПО ТаможеннаяДекларацияОснования.ДокументОснование = НДССостояниеРеализации0.ДокументРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявлениеОВвозеТоваровПолученное.ДокументыОтгрузки КАК
	|			ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеОВвозеТоваровПолученное КАК ЗаявлениеОВвозеТоваровПолученноеДокумент
	|			ПО ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.Ссылка = ЗаявлениеОВвозеТоваровПолученноеДокумент.Ссылка
	|			И ЗаявлениеОВвозеТоваровПолученноеДокумент.Проведен
	|		ПО ЗаявлениеОВвозеТоваровПолученноеДокументыОтгрузки.ДокументОтгрузки = НДССостояниеРеализации0.ДокументРеализации
	|СГРУППИРОВАТЬ ПО
	|	НДССостояниеРеализации0.Регистратор,
	|	НДССостояниеРеализации0.Период,
	|	НДССостояниеРеализации0.Организация,
	|	НДССостояниеРеализации0.ДокументРеализации,
	|	НДССостояниеРеализации0.ДатаРеализации,
	|	НДССостояниеРеализации0.Контрагент,
	|	ТаможеннаяДекларацияОснования.Ссылка,
	|	ТаможеннаяДекларацияДокумент.СборСопроводительныхДокументовЗавершен,
	|	НДССостояниеРеализации0.Состояние,
	|	НДССостояниеРеализации0.СтавкаНДС,
	|	НДССостояниеРеализации0.ДатаПодтверждения,
	|	ВЫРАЗИТЬ(НДССостояниеРеализации0.Комментарий КАК СТРОКА(1000)),
	|	СчетФактураВыданный.Ссылка
	//-- НЕ УТ
	|";
	
КонецФункции

#КонецОбласти

#КонецОбласти