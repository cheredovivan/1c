#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(Сотрудники.ФизическоеЛицо, NULL КАК ИСТИНА)
	|	) И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ВоинскийУчет.ДляОтбораПоОрганизации(Настройки);
	ВоинскийУчет.ДляОбъектаСПрисоединеннымиФайлами(Настройки);
КонецПроцедуры

#КонецОбласти

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.ИзвещениеОПриемеИлиУвольненииДляВоенкомата;
	ОписаниеСоставаОбъекта = ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	ОписаниеСоставаОбъекта.ЗаполнятьФизическиеЛицаПоСотрудникам = Ложь;
	Возврат ОписаниеСоставаОбъекта;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Идентификатор = "ПФ_MXL_ИзвещениеОПриемеУвольнении";
	КомандаПечати.Представление = НСтр("ru = 'Извещение о приеме или увольнении';
										|en = 'Извещение о приеме или увольнении'");
	КомандаПечати.Порядок = 10;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// ВХОДЯЩИЕ:
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать.
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы.
//   ОшибкиПечати          - Список значений  - Ошибки печати  (значение - ссылка на объект, представление - текст
//                           ошибки).
//   ОбъектыПечати         - Список значений  - Объекты печати (значение - ссылка на объект, представление - имя
//                           области в которой был выведен объект).
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_ИзвещениеОПриемеУвольнении") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ПФ_MXL_ИзвещениеОПриемеУвольнении", 
			НСтр("ru = 'Извещение о приеме или увольнении';
				|en = 'Извещение о приеме или увольнении'"), 
			ПечатнаяФормаИзвещенияОПриемеУвольнении(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;					
						
КонецПроцедуры								

Функция ПечатнаяФормаИзвещенияОПриемеУвольнении(МассивОбъектов, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИзвещениеОПриемеИлиУвольненииДляВоенкомата";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	ВыборкаПоШапкеДокумента = ЗапросПоШапкеДляПечати(МассивОбъектов).Выбрать();
	ВыборкаПоРаботникам = ЗапросПоСотрудникамДляПечати(МассивОбъектов).Выбрать();
	
	Пока ВыборкаПоШапкеДокумента.СледующийПоЗначениюПоля("Ссылка") Цикл 
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Отчет.ВоинскийУчетОбщий.ПФ_MXL_ИзвещениеОПриемеУвольнении2023");
		
		Извещение = Макет.ПолучитьОбласть("Извещение");
		
		ВыборкаПоРаботникам.Сбросить();
		
		Если ВыборкаПоРаботникам.НайтиСледующий(Новый Структура("Ссылка", ВыборкаПоШапкеДокумента.Ссылка)) Тогда
			
			ВыборкаПоРаботникам.СледующийПоЗначениюПоля("Ссылка");
			
			Пока ВыборкаПоРаботникам.Следующий() Цикл
				
				НачалоФормы = ДокументРезультат.ВысотаТаблицы + 1;
				
				Извещение.Параметры.ВоенкоматСотрудника = ВыборкаПоРаботникам.Военкомат;
				Извещение.Параметры.НаименованиеВоенкомата = Строка(ВыборкаПоРаботникам.Военкомат);
				Извещение.Параметры.АдресВоенкомата = ВыборкаПоРаботникам.АдресВоенкомата;
				Извещение.Параметры.ФизическоеЛицо = ВыборкаПоРаботникам.ФизическоеЛицо;
				Извещение.Параметры.ФИОПолные = ВыборкаПоРаботникам.Фамилия + " " + ВыборкаПоРаботникам.Имя + " " + ВыборкаПоРаботникам.Отчество;
				Извещение.Параметры.ДатаРождения = ВыборкаПоРаботникам.ДатаРождения;
				Извещение.Параметры.Звание = ВыборкаПоРаботникам.Звание;
				Извещение.Параметры.ДокументСерия = ВыборкаПоРаботникам.СерияПаспорта;
				Извещение.Параметры.ДокументНомер = ВыборкаПоРаботникам.НомерПаспорта;
				Извещение.Параметры.СтраховойНомерПФР = ВыборкаПоРаботникам.СтраховойНомерСФР;
			    Извещение.Параметры.ПолноеНаименованиеОрганизации = ВыборкаПоШапкеДокумента.НаименованиеОрганизации;				
				Извещение.Параметры.ФактАдресОрганизации = ВыборкаПоШапкеДокумента.ФактическийАдресОрганизации;
				Извещение.Параметры.Должность = Строка(ВыборкаПоРаботникам.Должность);
				Извещение.Параметры.НомерПриказа = ЗарплатаКадрыОтчеты.НомерНаПечать(ВыборкаПоРаботникам.НомерПриказа);
				Извещение.Параметры.ДатаПриказа = ВыборкаПоРаботникам.ДатаПриказа;
				
				Если ЗначениеЗаполнено(ВыборкаПоРаботникам.АдресРегистрацииПоМестуПребывания) Тогда 
			    	Извещение.Параметры.АдресМестаПроживанияПредставление = ВыборкаПоРаботникам.АдресРегистрацииПоМестуПребывания;
				ИначеЕсли ЗначениеЗаполнено(ВыборкаПоРаботникам.АдресРегистрацииПоМестуЖительства) Тогда 
			    	Извещение.Параметры.АдресМестаПроживанияПредставление = ВыборкаПоРаботникам.АдресРегистрацииПоМестуЖительства;
				Иначе 
			    	Извещение.Параметры.АдресМестаПроживанияПредставление = ВыборкаПоРаботникам.АдресФактическогоПроживания;
				КонецЕсли;
				
				Если ВыборкаПоРаботникам.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием
					И Не ЗначениеЗаполнено(ВыборкаПоРаботникам.ДатаПриказа) Тогда 
					Извещение.Параметры.НомерПриказа = СокрЛП(ВыборкаПоРаботникам.НомерТрудовогоДоговора);
					Извещение.Параметры.ДатаПриказа = ВыборкаПоРаботникам.ДатаТрудовогоДоговора;
				КонецЕсли;
				
				Если ВыборкаПоРаботникам.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием Тогда 
					Извещение.Области.Принят.Шрифт = Новый Шрифт(Извещение.Области.Принят.Шрифт, , , , , , Ложь);
					Извещение.Области.Уволен.Шрифт = Новый Шрифт(Извещение.Области.Уволен.Шрифт, , , , , , Истина);
				Иначе 
					Извещение.Области.Принят.Шрифт = Новый Шрифт(Извещение.Области.Принят.Шрифт, , , , , , Истина);
					Извещение.Области.Уволен.Шрифт = Новый Шрифт(Извещение.Области.Уволен.Шрифт, , , , , , Ложь);
				КонецЕсли;
				
			    Извещение.Параметры.ДолжностьРуководителя = Строка(ВыборкаПоШапкеДокумента.ДолжностьРуководителя);				
				Извещение.Параметры.РуководительРасшифровкаПодписи = ВыборкаПоШапкеДокумента.РуководительРасшифровкаПодписи;
			    Извещение.Параметры.ДолжностьОтветственногоЗаВУР = Строка(ВыборкаПоШапкеДокумента.ДолжностьОтветственногоЗаВУР);				
				Извещение.Параметры.ОтветственныйЗаВУРРасшифровкаПодписи = ВыборкаПоШапкеДокумента.ОтветственныйЗаВУРРасшифровкаПодписи;
				
				ДокументРезультат.Вывести(Извещение);
				
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, НачалоФормы, ОбъектыПечати, ВыборкаПоШапкеДокумента.Ссылка);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДокументРезультат;
	
КонецФункции

Функция ЗапросПоШапкеДляПечати(МассивСсылок)
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИзвещениеОПриемеИлиУвольненииДляВоенкомата.Дата КАК Дата,
		|	ИзвещениеОПриемеИлиУвольненииДляВоенкомата.Номер КАК Номер,
		|	ИзвещениеОПриемеИлиУвольненииДляВоенкомата.НомерПриказа КАК НомерПриказа,
		|	ИзвещениеОПриемеИлиУвольненииДляВоенкомата.Организация КАК Организация,
		|	ИзвещениеОПриемеИлиУвольненииДляВоенкомата.ФактическийАдресОрганизации КАК ФактическийАдресОрганизации,
		|	ИзвещениеОПриемеИлиУвольненииДляВоенкомата.Ссылка КАК Ссылка,
		|	ИзвещениеОПриемеИлиУвольненииДляВоенкомата.Руководитель КАК Руководитель,
		|	ИзвещениеОПриемеИлиУвольненииДляВоенкомата.ДолжностьРуководителя КАК ДолжностьРуководителя,
		|	ИзвещениеОПриемеИлиУвольненииДляВоенкомата.ОтветственныйЗаВУР КАК ОтветственныйЗаВУР,
		|	ИзвещениеОПриемеИлиУвольненииДляВоенкомата.ДолжностьОтветственногоЗаВУР КАК ДолжностьОтветственногоЗаВУР
		|ПОМЕСТИТЬ ВТДанныеДокументов
		|ИЗ
		|	Документ.ИзвещениеОПриемеИлиУвольненииДляВоенкомата КАК ИзвещениеОПриемеИлиУвольненииДляВоенкомата
		|ГДЕ
		|	ИзвещениеОПриемеИлиУвольненииДляВоенкомата.Ссылка В(&МассивСсылок)";
	
	Запрос.Выполнить();
	
	ИменаПолейОтветственныхЛиц = Новый Массив;
	ИменаПолейОтветственныхЛиц.Добавить("Руководитель");
	ИменаПолейОтветственныхЛиц.Добавить("ОтветственныйЗаВУР");
	ЗарплатаКадры.СоздатьВТФИООтветственныхЛиц(Запрос.МенеджерВременныхТаблиц, Истина, ИменаПолейОтветственныхЛиц, "ВТДанныеДокументов");
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеДокументов.Ссылка КАК Ссылка,
		|	ДанныеДокументов.Дата КАК Дата,
		|	ДанныеДокументов.Номер КАК Номер,
		|	ДанныеДокументов.НомерПриказа КАК НомерПриказа,
		|	ДанныеДокументов.Организация КАК Организация,
		|	ДанныеДокументов.ФактическийАдресОрганизации КАК ФактическийАдресОрганизации,
		|	ДанныеДокументов.Руководитель КАК Руководитель,
		|	ДанныеДокументов.ДолжностьРуководителя КАК ДолжностьРуководителя,
		|	ДанныеДокументов.ОтветственныйЗаВУР КАК ОтветственныйЗаВУР,
		|	ДанныеДокументов.ДолжностьОтветственногоЗаВУР КАК ДолжностьОтветственногоЗаВУР,
		|	ВЫБОР
		|		КОГДА ПОДСТРОКА(Организации.НаименованиеПолное, 1, 10) <> """"
		|			ТОГДА Организации.НаименованиеПолное
		|		ИНАЧЕ Организации.Наименование
		|	КОНЕЦ КАК НаименованиеОрганизации,
		|	Организации.НаименованиеПолное КАК НаименованиеПолное,
		|	Организации.НаименованиеСокращенное КАК НаименованиеСокращенное,
		|	ФИОРуководителя.РасшифровкаПодписи КАК РуководительРасшифровкаПодписи,
		|	ФИООтветственногоЗаВУР.РасшифровкаПодписи КАК ОтветственныйЗаВУРРасшифровкаПодписи
		|ИЗ
		|	ВТДанныеДокументов КАК ДанныеДокументов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО ДанныеДокументов.Организация = Организации.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИОРуководителя
		|		ПО ДанныеДокументов.Руководитель = ФИОРуководителя.ФизическоеЛицо
		|			И ДанныеДокументов.Ссылка = ФИОРуководителя.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИООтветственногоЗаВУР
		|		ПО ДанныеДокументов.ОтветственныйЗаВУР = ФИООтветственногоЗаВУР.ФизическоеЛицо
		|			И ДанныеДокументов.Ссылка = ФИООтветственногоЗаВУР.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеДокументов.Дата,
		|	Ссылка";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапросПоСотрудникамДляПечати(МассивСсылок)
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Сотрудники.Ссылка КАК Ссылка,
		|	Сотрудники.НомерСтроки КАК НомерСтроки,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Сотрудники.Военкомат КАК Военкомат,
		|	Сотрудники.Военкомат.Наименование КАК НаименованиеВоенкомата,
		|	ВЫБОР
		|		КОГДА Сотрудники.Военкомат.НаименованиеДляЭлектронногоОбмена = """"
		|			ТОГДА Сотрудники.Военкомат.Наименование
		|		ИНАЧЕ Сотрудники.Военкомат.НаименованиеДляЭлектронногоОбмена
		|	КОНЕЦ КАК ВоенкоматНаименованиеДляЭлектронногоОбмена,
		|	Сотрудники.Военкомат.Адрес КАК АдресВоенкомата,
		|	Сотрудники.Фамилия КАК Фамилия,
		|	Сотрудники.Имя КАК Имя,
		|	Сотрудники.Отчество КАК Отчество,
		|	Сотрудники.СтраховойНомерСФР КАК СтраховойНомерСФР,
		|	Сотрудники.ДатаРождения КАК ДатаРождения,
		|	Сотрудники.Пол КАК Пол,
		|	Сотрудники.СерияПаспорта КАК СерияПаспорта,
		|	Сотрудники.НомерПаспорта КАК НомерПаспорта,
		|	Сотрудники.ДатаВыдачиПаспорта КАК ДатаВыдачиПаспорта,
		|	Сотрудники.ДатаСобытия КАК ДатаСобытия,
		|	Сотрудники.ВидСобытия КАК ВидСобытия,
		|	Сотрудники.ДатаРегистрацииПоМестуЖительства КАК ДатаРегистрацииПоМестуЖительства,
		|	Сотрудники.АдресРегистрацииПоМестуЖительства КАК АдресРегистрацииПоМестуЖительства,
		|	Сотрудники.ДатаРегистрацииПоМестуПребывания КАК ДатаРегистрацииПоМестуПребывания,
		|	Сотрудники.АдресРегистрацииПоМестуПребывания КАК АдресРегистрацииПоМестуПребывания,
		|	Сотрудники.ДатаФактическогоПроживания КАК ДатаФактическогоПроживания,
		|	Сотрудники.АдресФактическогоПроживания КАК АдресФактическогоПроживания,
		|	Сотрудники.Категория КАК Категория,
		|	Сотрудники.Должность КАК Должность,
		|	Сотрудники.НомерПриказа КАК НомерПриказа,
		|	Сотрудники.ДатаПриказа КАК ДатаПриказа,
		|	Сотрудники.НомерТрудовогоДоговора КАК НомерТрудовогоДоговора,
		|	Сотрудники.ДатаТрудовогоДоговора КАК ДатаТрудовогоДоговора,
		|	Сотрудники.Звание КАК Звание
		|ИЗ
		|	Документ.ИзвещениеОПриемеИлиУвольненииДляВоенкомата.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&МассивСсылок)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	НомерСтроки";
	
	Возврат Запрос.Выполнить();
	
КонецФункции

Функция ЗапрашиваемыеКадровыеДанныеСотрудников() Экспорт

	ЗапрашиваемыеДанные = "Фамилия, Имя, Отчество, СтраховойНомерПФР, Пол, ДатаРождения, Должность, 
		|ПриказОПриемеДата, ПриказОПриемеНомер, ПриказОбУвольненииДата, ПриказОбУвольненииНомер,
		|ТрудовойДоговорДата, ТрудовойДоговорНомер, ДокументСерия, ДокументНомер, ДокументДатаВыдачи, 
		|ВоинскийУчетВоенкомат, АдресПоПропискеПредставление, АдресМестаПроживанияПредставление, 
		|АдресДляИнформированияПредставление, ДатаРегистрации, ДатаВременнойРегистрации, 
		|ДатаФактическогоПроживания, ДатаПриема, ДатаУвольнения, ВоинскийУчетЗвание";
	
	Возврат ЗапрашиваемыеДанные;
	
КонецФункции

Функция КадровыеДанныеСотрудников(ДанныеСотрудников) Экспорт
	
	ЗапрашиваемыеДанные = ЗапрашиваемыеКадровыеДанныеСотрудников();
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудниковПоТаблицеЗначений(Истина, ДанныеСотрудников, ЗапрашиваемыеДанные);
	
	Возврат КадровыеДанные;
	
КонецФункции

Процедура ЗаполнитьДанныеСтрокиСотрудника(СтрокаСотрудника, КадровыеДанные, ДатаДокумента) Экспорт 
	
	ЗаполнитьЗначенияСвойств(СтрокаСотрудника, КадровыеДанные, , "ДатаФактическогоПроживания");
	
	СтрокаСотрудника.Военкомат = КадровыеДанные.ВоинскийУчетВоенкомат; 
	СтрокаСотрудника.СтраховойНомерСФР = КадровыеДанные.СтраховойНомерПФР;
	СтрокаСотрудника.СерияПаспорта = СтрЗаменить(КадровыеДанные.ДокументСерия, " ", "");
	СтрокаСотрудника.НомерПаспорта = СтрЗаменить(КадровыеДанные.ДокументНомер, " ", "");
	СтрокаСотрудника.ДатаВыдачиПаспорта = КадровыеДанные.ДокументДатаВыдачи;
	СтрокаСотрудника.ДатаРегистрацииПоМестуЖительства = КадровыеДанные.ДатаРегистрации;
	СтрокаСотрудника.АдресРегистрацииПоМестуЖительства = КадровыеДанные.АдресПоПропискеПредставление;
	
	Если Не ПолучитьФункциональнуюОпцию("ЗаполнятьТолькоАдресПоПропискеВСведенияхДляВоенкоматов") Тогда 
		СтрокаСотрудника.ДатаРегистрацииПоМестуПребывания = КадровыеДанные.ДатаВременнойРегистрации;
		СтрокаСотрудника.АдресРегистрацииПоМестуПребывания = КадровыеДанные.АдресМестаПроживанияПредставление;
		СтрокаСотрудника.ДатаФактическогоПроживания = КадровыеДанные.ДатаФактическогоПроживания;
		СтрокаСотрудника.АдресФактическогоПроживания = КадровыеДанные.АдресДляИнформированияПредставление;
	КонецЕсли;
	
	СтрокаСотрудника.Категория = Перечисления.КатегорииПринятыхУволенныхГражданДляВоенкомата.Работник;
	СтрокаСотрудника.НомерТрудовогоДоговора = КадровыеДанные.ТрудовойДоговорНомер;
	СтрокаСотрудника.ДатаТрудовогоДоговора = КадровыеДанные.ТрудовойДоговорДата;
	СтрокаСотрудника.Звание = КадровыеДанные.ВоинскийУчетЗвание;
	
	Если Не ЗначениеЗаполнено(СтрокаСотрудника.ВидСобытия) Тогда
		Если Не ЗначениеЗаполнено(КадровыеДанные.ПриказОбУвольненииДата)
			Или КадровыеДанные.ДатаУвольнения > ДатаДокумента Тогда 
			СтрокаСотрудника.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием;
			СтрокаСотрудника.ДатаСобытия = КадровыеДанные.ДатаПриема;
		Иначе 
			СтрокаСотрудника.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение;
			СтрокаСотрудника.ДатаСобытия = КадровыеДанные.ДатаУвольнения;
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокаСотрудника.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием 
		Или Не ЗначениеЗаполнено(КадровыеДанные.ПриказОбУвольненииДата) Тогда 
		СтрокаСотрудника.НомерПриказа = КадровыеДанные.ПриказОПриемеНомер;
		СтрокаСотрудника.ДатаПриказа = КадровыеДанные.ПриказОПриемеДата;
	Иначе 
		СтрокаСотрудника.НомерПриказа = КадровыеДанные.ПриказОбУвольненииНомер;
		СтрокаСотрудника.ДатаПриказа = КадровыеДанные.ПриказОбУвольненииДата;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаФормированияФайла(Объект) Экспорт
	
	ТекстФайла = СформироватьВыходнойФайл(Объект.Ссылка);
	ВоинскийУчет.ЗаписатьФайлОтчетности(Объект.Ссылка, Объект.ИмяФайлаОтчетности, ТекстФайла, "UTF-8");
	
КонецПроцедуры

Функция СформироватьВыходнойФайл(Ссылка)
	
	СписокДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка);
	ВыборкаПоШапкеДокумента = ЗапросПоШапкеДляПечати(СписокДокументов).Выбрать();
	ВыборкаПоРаботникам = ЗапросПоСотрудникамДляПечати(СписокДокументов).Выбрать();
	
	ВыборкаПоШапкеДокумента.Следующий();
	
	ТекстФайла = "Наименование военного комиссариата;Фамилия;Имя;Отчество (при наличии);Дата рождения;Пол;Серия паспорта;Номер паспорта;Дата выдачи паспорта;СНИЛС;Дата регистрации по месту жительства;Адрес регистрации по месту жительства;Дата регистрации по месту пребывания;Адрес регистрации по месту пребывания;Дата фактического проживания;Адрес фактического проживания;Работник/Учащийся;Принят/зачислен/уволен/отчислен;Дата приема на работу/поступления в образовательную организацию;Дата увольнения с работы/отчисления из образовательной организации;Должность/наименование профессии/специальности/направления подготовки;Фактический адрес организации;Номер приказа о приеме/зачислении/увольнении/отчислении;Дата приказа о приеме/зачислении/увольнении/отчислении;Номер трудового договора;Дата трудового договора" + Символы.ПС;
	
	Пока ВыборкаПоРаботникам.Следующий() Цикл 
		ДанныеСтроки = Новый Массив;
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.ВоенкоматНаименованиеДляЭлектронногоОбмена);
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.Фамилия);
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.Имя);
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.Отчество);
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаРождения, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(Строка(ВыборкаПоРаботникам.Пол));
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.СерияПаспорта);
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.НомерПаспорта);
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаВыдачиПаспорта, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.СтраховойНомерСФР);
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаРегистрацииПоМестуЖительства, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(ВоинскийУчет.ФорматАдресаДляЭлектронногоОбмена(ВыборкаПоРаботникам.АдресРегистрацииПоМестуЖительства));
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаРегистрацииПоМестуПребывания, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(ВоинскийУчет.ФорматАдресаДляЭлектронногоОбмена(ВыборкаПоРаботникам.АдресРегистрацииПоМестуПребывания));
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаФактическогоПроживания, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(ВоинскийУчет.ФорматАдресаДляЭлектронногоОбмена(ВыборкаПоРаботникам.АдресФактическогоПроживания));
		ДанныеСтроки.Добавить(Строка(ВыборкаПоРаботникам.Категория)); 
		ДанныеСтроки.Добавить(?(ВыборкаПоРаботникам.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием, "Принят", "Уволен"));
		ДанныеСтроки.Добавить(?(ВыборкаПоРаботникам.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Прием, Формат(ВыборкаПоРаботникам.ДатаСобытия, "ДЛФ=Д"), ""));
		ДанныеСтроки.Добавить(?(ВыборкаПоРаботникам.ВидСобытия = Перечисления.ВидыКадровыхСобытий.Увольнение, Формат(ВыборкаПоРаботникам.ДатаСобытия, "ДЛФ=Д"), ""));
		ДанныеСтроки.Добавить(Строка(ВыборкаПоРаботникам.Должность));
		ДанныеСтроки.Добавить(ВоинскийУчет.ФорматАдресаДляЭлектронногоОбмена(ВыборкаПоШапкеДокумента.ФактическийАдресОрганизации));
		ДанныеСтроки.Добавить(ЗарплатаКадрыОтчеты.НомерНаПечать(ВыборкаПоРаботникам.НомерПриказа));
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаПриказа, "ДЛФ=Д"));
		ДанныеСтроки.Добавить(ВыборкаПоРаботникам.НомерТрудовогоДоговора);
		ДанныеСтроки.Добавить(Формат(ВыборкаПоРаботникам.ДатаТрудовогоДоговора, "ДЛФ=Д"));
		ТекстФайла = ТекстФайла + СтрСоединить(ДанныеСтроки, ";") + Символы.ПС;
	КонецЦикла;
	
	Возврат ТекстФайла;
		
КонецФункции

#КонецОбласти

#КонецЕсли