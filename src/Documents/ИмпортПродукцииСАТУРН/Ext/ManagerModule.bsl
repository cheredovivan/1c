
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Статусы

// Возвращает статус по умолчанию.
// 
// Параметры:
//   СтруктураПараметров - Структура - Входящие данные.
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН - Статус по-умолчанию.
//
Функция СтатусПоУмолчанию(СтруктураПараметров = Неопределено) Экспорт
	
	Если ТипЗнч(СтруктураПараметров) = Тип("Структура")
		И СтруктураПараметров.Свойство("Операция")
		И ЗначениеЗаполнено(СтруктураПараметров.Операция) Тогда
		ЭтоПриемкаИзЕАЭС              = (СтруктураПараметров.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзЕАЭС);
		ЭтоПредварительноеУведомление = (СтруктураПараметров.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомление);
	ИначеЕсли ТипЗнч(СтруктураПараметров) = Тип("Структура")
		И СтруктураПараметров.Свойство("ОбъектРасчета") Тогда
		Операция = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктураПараметров.ОбъектРасчета, "Операция");
		ЭтоПриемкаИзЕАЭС              = (Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзЕАЭС);
		ЭтоПредварительноеУведомление = (Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомление);
	Иначе
		ЭтоПриемкаИзЕАЭС              = Ложь;
		ЭтоПредварительноеУведомление = Ложь;
	КонецЕсли;
	
	Если ЭтоПриемкаИзЕАЭС Или ЭтоПредварительноеУведомление Тогда
		Возврат Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Черновик;
	Иначе
		Возврат Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ввезено;
	КонецЕсли;
	
КонецФункции

// Возвращает статусы ошибок.
//
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН - Статусы ошибок.
//
Функция СтатусыОшибок() Экспорт
	
	Статусы = Новый Массив;
	
	Статусы.Добавить(Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка);
	
	Возврат Статусы;
	
КонецФункции

// Возвращает конечные статусы документа.
//
// Параметры:
//   ТребуетсяПовторноеОформление - Булево.
//
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН - Конечные статусы.
//
Функция КонечныеСтатусы(ТребуетсяПовторноеОформление = Истина) Экспорт
	
	Статусы = Новый Массив;
	
	Если Не ТребуетсяПовторноеОформление Тогда
		Статусы.Добавить(Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено);
		Статусы.Добавить(Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Отменен);
		Статусы.Добавить(Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПроведенСАТУРН);
	КонецЕсли;
	
	Возврат Статусы;
	
КонецФункции

// Возвращает дальнейшее действие по умолчанию.
// 
// Параметры:
//   СтруктураПараметров - Структура - Входящие данные.
//
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие по-умолчанию.
//
Функция ДальнейшееДействиеПоУмолчанию(СтруктураПараметров = Неопределено) Экспорт
	
	МассивДействий = Новый Массив;
	
	Если ТипЗнч(СтруктураПараметров) = Тип("Структура")
		И СтруктураПараметров.Свойство("Операция")
		И ЗначениеЗаполнено(СтруктураПараметров.Операция) Тогда
		ЭтоПриемкаИзЕАЭС              = (СтруктураПараметров.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзЕАЭС);
		ЭтоПредварительноеУведомление = (СтруктураПараметров.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомление);
	ИначеЕсли ТипЗнч(СтруктураПараметров) = Тип("Структура")
		И СтруктураПараметров.Свойство("ОбъектРасчета") Тогда
		Операция = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктураПараметров.ОбъектРасчета, "Операция");
		ЭтоПриемкаИзЕАЭС              = (Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзЕАЭС);
		ЭтоПредварительноеУведомление = (Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомление);
	Иначе
		ЭтоПриемкаИзЕАЭС              = Ложь;
		ЭтоПредварительноеУведомление = Ложь;
	КонецЕсли;
	
	Если ЭтоПриемкаИзЕАЭС Или ЭтоПредварительноеУведомление Тогда
		
		МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПередайтеДанные);
		
	Иначе
		
		МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПодтвердитеИмпорт);
		
	КонецЕсли;
	
	Возврат МассивДействий;
	
КонецФункции

#КонецОбласти

#Область ДействияПриОбмене

// Статус после подготовки к передаче данных.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ИмпортПродукцииСАТУРН - Ссылка на документ.
//  Операция - ПеречислениеСсылка.ВидыОперацийСАТУРН - Операция.
//  ДополнительныеПараметры - Структура - Дополнительные параметры.
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие 3.
//
Функция СтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатусовПоДокументу
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПодтверждениеПриемки
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииОтменаПриемки
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭССозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСПринятиеКУчету
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеПринятиеКУчету Тогда
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусыКПередаче(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается);
		
	Иначе
		ВызватьИсключение ОбщегоНазначенияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Статус после передачи данных.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ИмпортПродукцииСАТУРН - Ссылка на документ.
//  Операция - ПеречислениеСсылка.ВидыОперацийСАТУРН - Операция.
//  СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийСАТУРН - Статус обработки сообщения.
//
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие 3.
//
Функция СтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки) Экспорт
	
	Если СтатусОбработки = Неопределено Тогда
		СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийСАТУРН.ЗаявкаПринята;
	КонецЕсли;
	
	Если Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеИзменениеДокумента Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
		
		Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, Статусы);
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПодтверждениеПриемки Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
		
		Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеПартииИмпорта);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, Статусы);
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииОтменаПриемки Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
		
		Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеПартииИмпорта);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, Статусы);
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
		
		Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеПартииИмпорта);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, Статусы);
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭССозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеСозданиеДокумента Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
		
		Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, Статусы);
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСПринятиеКУчету
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеПринятиеКУчету Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
		
		Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеПартииИмпорта);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(ДокументСсылка, СтатусОбработки, Статусы);
		
	Иначе
		ВызватьИсключение ОбщегоНазначенияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Статус после получения данных.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ИмпортПродукцииСАТУРН - Ссылка на документ.
//  Операция - ПеречислениеСсылка.ВидыОперацийСАТУРН - Операция.
//  ДополнительныеПараметры - Структура - Структура со свойствами:
//   * СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийСАТУРН - Статус обработки сообщения.
//   * ОперацияКвитанции - ПеречислениеСсылка.ВидыОперацийСАТУРН - Операция, на которую получена квитанция.
//   * ДополнительныеПараметры - Неопределено, Структура - 
// Возвращаемое значение:
//  Структура - Структура со свойствами:
//   * НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие 3.
//
Функция СтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСИзменениеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеИзменениеДокумента Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
		
		Статусы.Обрабатывается = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.КПередаче;
		Статусы.ОбрабатываетсяДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
		
		Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(
			ДокументСсылка,
			ДополнительныеПараметры.СтатусОбработки,
			Статусы);
			
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПодтверждениеПриемки Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.НеТребуется);
		
		Статусы.Обрабатывается = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается;
		Статусы.ОбрабатываетсяДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
		
		Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеПартииИмпорта);
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(
			ДокументСсылка,
			ДополнительныеПараметры.СтатусОбработки,
			Статусы);
			
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииОтменаПриемки Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Отменен;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.НеТребуется);
		
		Статусы.Обрабатывается = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.КПередаче;
		Статусы.ОбрабатываетсяДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
		
		Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(
			ДокументСсылка,
			ДополнительныеПараметры.СтатусОбработки,
			Статусы);
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		РезультирующийСтатусДокумента = РезультирующийСтатусДокумента(ДокументСсылка);
		
		Статусы.Принят = РезультирующийСтатусДокумента.Статус;
		Статусы.ПринятДействия = РезультирующийСтатусДокумента.ДальнейшееДействие;
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(
			ДокументСсылка,
			Перечисления.СтатусыОбработкиСообщенийСАТУРН.ЗаявкаВыполнена,
			Статусы);
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		Если ДополнительныеПараметры.Свойство("ИдентификаторСтроки")
			И ЗначениеЗаполнено(ДополнительныеПараметры.ИдентификаторСтроки) Тогда
	
			РезультирующийСтатусДокумента = СтатусПоСтрокеИмпортируемойПартии(ДокументСсылка, ДополнительныеПараметры.ИдентификаторСтроки);
			Статусы.Принят = РезультирующийСтатусДокумента.Статус;
			Статусы.ПринятДействия = РезультирующийСтатусДокумента.ДальнейшееДействие;
			
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(
				ДокументСсылка,
				Перечисления.СтатусыОбработкиСообщенийСАТУРН.ЗаявкаВыполнена,
				Статусы);
			
		Иначе
			
			Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
			Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено;
			
			Статусы.Обрабатывается = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается;
			Статусы.ОбрабатываетсяДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
			
			Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
			Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПередайтеДанные);
			Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
			
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(
				ДокументСсылка,
				ДополнительныеПараметры.СтатусОбработки,
				Статусы);
			
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатусовПоДокументу Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		ОперацияДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Операция");
		
		Если ОперацияДокумента = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзЕАЭС
			Или ОперацияДокумента = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомление Тогда
			
			Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
			
			Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПроведенСАТУРН;
			
			Статусы.Обрабатывается = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается;
			Статусы.ОбрабатываетсяДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
			
			Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
			Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПередайтеДанные);
			Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
			
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(
				ДокументСсылка,
				ДополнительныеПараметры.СтатусОбработки,
				Статусы);
			
		Иначе
			
			РезультирующийСтатусДокумента = РезультирующийСтатусДокумента(ДокументСсылка);
			Статусы.Принят = РезультирующийСтатусДокумента.Статус;
			Статусы.ПринятДействия = РезультирующийСтатусДокумента.ДальнейшееДействие;
			
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(
				ДокументСсылка,
				Перечисления.СтатусыОбработкиСообщенийСАТУРН.ЗаявкаВыполнена,
				Статусы);
			
		КонецЕсли;
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭССозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеСозданиеДокумента
		Или Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСПринятиеКУчету Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
		
		Статусы.Обрабатывается = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.КПередаче;
		Статусы.ОбрабатываетсяДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
		
		Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(
			ДокументСсылка,
			ДополнительныеПараметры.СтатусОбработки,
			Статусы);
		
	ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеПринятиеКУчету Тогда
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПроведенСАТУРН;
		Статусы.ПринятДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.НеТребуется);
		
		Статусы.Обрабатывается = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается;
		Статусы.ОбрабатываетсяДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
		
		Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеПартииИмпорта);
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		
		ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(
			ДокументСсылка,
			ДополнительныеПараметры.СтатусОбработки,
			Статусы);
		
	Иначе
		ВызватьИсключение ОбщегоНазначенияИС.ТекстИсключенияОбработкиСтатуса(ДокументСсылка, Операция);
	КонецЕсли;
	
	Возврат ПараметрыОбновления;
	
КонецФункции

// Обновить статус после подготовки к передаче данных.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ИмпортПродукцииСАТУРН - Ссылка на документ.
//  Операция - ПеречислениеСсылка.ВидыОперацийСАТУРН - Операция.
//  ДополнительныеПараметры - Структура - Дополнительные параметры.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН - Новый статус.
//
Функция ОбновитьСтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОбновления = СтатусПослеПодготовкиКПередачеДанных(ДокументСсылка, Операция);

	Если ПараметрыОбновления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Если ДополнительныеПараметры <> Неопределено Тогда
		ПараметрыОбновления.ИдентификаторСтроки = ДополнительныеПараметры.ИдентификаторСтроки;
	КонецЕсли;

	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);

	Возврат НовыйСтатусПослеОбновления;
	
КонецФункции

// Обновить статус после передачи данных.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ИмпортПродукцииСАТУРН - Ссылка на документ.
//  Операция - ПеречислениеСсылка.ВидыОперацийСАТУРН - Операция.
//  СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийСАТУРН - Статус обработки сообщения.
//  ДополнительныеПараметры - Структура - Дополнительные параметры.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН - Новый статус.
//
Функция ОбновитьСтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОбновления = СтатусПослеПередачиДанных(ДокументСсылка, Операция, СтатусОбработки);

	Если ПараметрыОбновления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		ПараметрыОбновления.ИдентификаторСтроки = ДополнительныеПараметры.ИдентификаторСтроки;
		НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.ОбновитьСтатус(
			ДокументСсылка, ПараметрыОбновления, ДополнительныеПараметры);
		ПараметрыОбновления.ИдентификаторСтроки = "";
	КонецЕсли;

	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);

	Возврат НовыйСтатусПослеОбновления;
	
КонецФункции

// Обновить статус после получения данных.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ИмпортПродукцииСАТУРН - Ссылка на документ.
//  Операция - ПеречислениеСсылка.ВидыОперацийСАТУРН - Операция.
//  ДополнительныеПараметры - Неопределено, Структура - Структура со свойствами:
//   * СтатусОбработки - ПеречислениеСсылка.СтатусыОбработкиСообщенийСАТУРН - Статус обработки сообщения.
//   * ОперацияКвитанции - ПеречислениеСсылка.ВидыОперацийСАТУРН - Операция, на которую получена квитанция.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН - Новый статус.
//
Функция ОбновитьСтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОбновления = СтатусПослеПолученияДанных(ДокументСсылка, Операция, ДополнительныеПараметры);

	Если ПараметрыОбновления = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(ДополнительныеПараметры.ИдентификаторСтроки) Тогда
			
			ПараметрыОбновления.ИдентификаторСтроки = ДополнительныеПараметры.ИдентификаторСтроки;
			НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.ОбновитьСтатус(
				ДокументСсылка, ПараметрыОбновления, ДополнительныеПараметры);
			
			ПараметрыОбновления.ИдентификаторСтроки = "";
			
		КонецЕсли;
		
	КонецЕсли;
	
	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);

	Возврат НовыйСтатусПослеОбновления;
	
КонецФункции

// Изменяет и возвращает статус документа.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ИмпортПродукцииСАТУРН - Ссылка на документ.
//  ПараметрыОбновления - Структура - Структура со свойствами:
//   * НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиНакладнойСАТУРН - Новый статус.
//   * ДальнейшееДействие1 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие 1.
//   * ДальнейшееДействие2 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие 2.
//   * ДальнейшееДействие3 - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие 3.
//  ДополнительныеПараметры - Структура - Дополнительные параметры.
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН - Новый статус.
//
Функция ОбновитьСтатус(ДокументСсылка, ПараметрыОбновления, ДополнительныеПараметры) Экспорт
	
	НовыйСтатусПослеОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.ОбновитьСтатус(
		ДокументСсылка,
		ПараметрыОбновления, ДополнительныеПараметры);
	
	Возврат НовыйСтатусПослеОбновления;
	
КонецФункции

// Получить последовательность операций в течении жизненного цикла документа.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ИмпортПродукцииСАТУРН - Ссылка на документ.
//  ЛинейныйСписок - Булево - Не используется.
//
// Возвращаемое значение:
//  ТаблицаЗначений - см. функцию ПротоколОбменаИС.ПустаяТаблицаПоследовательностьОпераций().
//
Функция ПоследовательностьОпераций(ДокументСсылка, ЛинейныйСписок = Ложь) Экспорт
	
	Операция = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Операция");
	
	ЭтоИмпортИзЕАЭС               = Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзЕАЭС;
	ЭтоПредварительноеУведомление = Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомление;
	
	Таблица = ПротоколОбменаИС.ПустаяТаблицаПоследовательностьОпераций();
	
	Исходящий = Перечисления.ТипыЗапросовИС.Исходящий;
	
	Если ЭтоИмпортИзЕАЭС Тогда
		
		АбстрактнаяОперация = ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 11,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса);
		АбстрактнаяОперация.АбстрактнаяОперация = Истина;
		
		ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 12,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭССозданиеДокумента);
			
		ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 13,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСИзменениеДокумента);
			
		ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 14,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСПринятиеКУчету);
			
		ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 15,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий);
		
	ИначеЕсли ЭтоПредварительноеУведомление Тогда
		
		АбстрактнаяОперация = ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 11,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса);
		АбстрактнаяОперация.АбстрактнаяОперация = Истина;
		
		ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 12,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеСозданиеДокумента);
			
		ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 13,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеИзменениеДокумента);
			
		ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 14,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеПринятиеКУчету);
			
	Иначе
		
		АбстрактнаяОперация = ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 11,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса);
		АбстрактнаяОперация.АбстрактнаяОперация = Истина;
	
		АбстрактнаяОперация = ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 12,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатусовПоДокументу);
		АбстрактнаяОперация.АбстрактнаяОперация = Истина;
		
		ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 13,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзменениеДокумента);
			
		ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 14,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПодтверждениеПриемки);
		
		ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 15,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий);
		
		АбстрактнаяОперация = ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 21,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса);
		АбстрактнаяОперация.АбстрактнаяОперация = Истина;
	
		АбстрактнаяОперация = ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 22,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатусовПоДокументу);
		АбстрактнаяОперация.АбстрактнаяОперация = Истина;
		
		ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 23,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииОтменаПриемки);
			
		ПротоколОбменаИС.ДобавитьОперациюВПоследовательность(Таблица, 24,
			Исходящий,
			Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий);
		
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

// Обработчик изменения статуса документа.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ИмпортПродукцииСАТУРН - Ссылка на документ.
//  ПредыдущийСтатус - ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН - Предыдущий статус.
//  НовыйСтатус - ПеречислениеСсылка.СтатусыОбработкиИмпортаПродукцииСАТУРН - Новый статус.
//  ПараметрыОбновленияСтатуса - См. ИнтеграцияСАТУРНСлужебныйКлиентСервер.ПараметрыОбновленияСтатуса
Процедура ПриИзмененииСтатусаДокумента(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ПараметрыОбновленияСтатуса) Экспорт
	
	ИнтеграцияСАТУРНПереопределяемый.ПриИзмененииСтатусаДокумента(ДокументСсылка, ПредыдущийСтатус, НовыйСтатус, ПараметрыОбновленияСтатуса);
	
	//Получение конечного статуса требующего повторного оформления
	Если КонечныеСтатусы().Найти(НовыйСтатус) <> Неопределено
		И КонечныеСтатусы().Найти(ПредыдущийСтатус) = Неопределено Тогда
		РасчетСтатусовОформленияСАТУРН.РассчитатьСтатусыОформленияДокументов(ДокументСсылка);
	КонецЕсли;
	
	Если СтатусыПересчетаДвижений().Найти(НовыйСтатус) <> Неопределено И НовыйСтатус <> ПредыдущийСтатус Тогда
		РегистрыСведений.МестаХраненияПартийСАТУРН.ОбновитьДвиженияПриИзмененииСтатусаДокумента(ДокументСсылка);
		РегистрыНакопления.ОстаткиПартийСАТУРН.ОбновитьДвиженияПриИзмененииСтатусаДокумента(ДокументСсылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПанельОбменСАТУРН

// Все требующие действия.
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Все требующие действия
Функция ВсеТребующиеДействия() Экспорт
	
	МассивДействий = Новый Массив;
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитьВвоз);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПодтвердитеИмпорт);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеПартииИмпорта);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ВыполнитеОбмен);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПередайтеДанные);
	
	Возврат МассивДействий;
	
КонецФункции

// Все требующие ожидания.
// 
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Все требующие ожидания
Функция ВсеТребующиеОжидания() Экспорт
	
	МассивДействий = Новый Массив;
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеПередачуДанныхРегламентнымЗаданием);
	МассивДействий.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
	
	Возврат МассивДействий;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

#Область Отчеты

// Заполняет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//   Параметры - Структура - Вспомогательные параметры. См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры.
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт

	ИнтеграцияИСПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	ИнтеграцияИСПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);
	
	КомандаОтчет = Отчеты.АнализРасхожденийПриДвиженииСАТУРН.ДобавитьКомандуОтчета(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.Порядок = 1;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Сформировать печатные формы объектов.
//
// Параметры:
//   МассивОбъектов        - Массив из Произвольный         - ссылки на объекты, которые нужно распечатать
//   ПараметрыПечати       - Структура                      - дополнительные настройки печати
//   КоллекцияПечатныхФорм - ТаблицаЗначений                - сформированные табличные документы (выходной параметр)
//   ОбъектыПечати         - СписокЗначений из Произвольный - имя области, в которой был выведен объект (выходной параметр)
//   ПараметрыВывода       - Структура                      - дополнительные параметры сформированных табличных документов (выходной параметр)
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
//
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
	Возврат;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	УправлениеДоступомИСПереопределяемый.ПриЗаполненииОграниченияДоступа(
		Метаданные.Документы.ИмпортПродукцииСАТУРН, Ограничение);

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	ОбновлениеИнформационнойБазыСАТУРН.ДобавитьОбработчикПриПереходеНаНовуюВерсиюAPI(
		ПустаяСсылка().Метаданные(),
		Новый УникальныйИдентификатор("be826f01-2ec2-46a1-bc46-37de46caef93"),
		Обработчики);
	
КонецПроцедуры

// Регистрирует данные для обработчика обновления перехода на новую версию API
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ОбъектМетаданных = ПустаяСсылка().Метаданные();
	ОбновлениеИнформационнойБазыСАТУРН.ЗарегистрироватьДанныеДляАрхивированияПриПереходеНаНовоеAPI(
		ОбъектМетаданных, Параметры);
	
КонецПроцедуры

// Обработка данных перехода на новую версию API
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ОбъектМетаданных = ПустаяСсылка().Метаданные();
	ОбновлениеИнформационнойБазыСАТУРН.АрхивироватьДанныеПриПереходеНаНовоеAPI(
		ОбъектМетаданных, Параметры);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Серии

//Имена реквизитов, от значений которых зависят параметры указания серий
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	
	Возврат ИнтеграцияИС.ИменаРеквизитовДляЗаполненияПараметровУказанияСерий(Метаданные.Документы.ИмпортПродукцииСАТУРН);
	
КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе.
//
// Параметры:
//  Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
// 
// Возвращаемое значение:
//  Произвольный - (См. ИнтеграцияИСПереопределяемый.ЗаполнитьПараметрыУказанияСерий) - особенности указания серий.
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	Возврат ИнтеграцияИС.ПараметрыУказанияСерий(Метаданные.Документы.ИмпортПродукцииСАТУРН, Объект);
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий
// Параметры:
//   ПараметрыУказанияСерий - См. ПараметрыУказанияСерий
// Возвращаемое значение:
//   Строка - текст запроса
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	Возврат ИнтеграцияИС.ТекстЗапросаЗаполненияСтатусовУказанияСерий(Метаданные.Документы.ИмпортПродукцииСАТУРН, ПараметрыУказанияСерий);

КонецФункции

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Ссылка");
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	Поля.Добавить("Операция");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = ПредставлениеДокументаИмпорта(
		Ложь, Данные.Операция, Данные.Номер, Данные.Дата);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область Обмен

Процедура ОбработкаЗагрузкиПолученныхДанных(ЭлементОчереди, ПараметрыОбмена, ПолученныеДанные, ИзмененныеОбъекты) Экспорт
	
	РеквизитыИсходящегоСообщения          = ЭлементОчереди.РеквизитыИсходящегоСообщения;
	
	Если ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий Тогда
		
		ВходящиеДанные = ИнтеграцияСАТУРНСлужебный.ОбработатьРезультатЗапросаСпискаОбъектов(
			Метаданные.Справочники.ИмпортируемаяПартияСАТУРН, ПолученныеДанные, ПараметрыОбмена);
		ИнтеграцияСАТУРНСлужебный.СсылкиПоИдентификаторам(ПараметрыОбмена, ИзмененныеОбъекты);
		
		ЭлементОчередиОснование               = ИнтеграцияСАТУРНСлужебный.ЭлементОчередиСообщенияОснования(ЭлементОчереди, ПараметрыОбмена);
		РеквизитыИсходящегоСообщенияОснования = ЭлементОчередиОснование.РеквизитыИсходящегоСообщения;
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(Метаданные.Документы.ИмпортПродукцииСАТУРН.ПолноеИмя());
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ЭлементОчереди.Документ);
		
		Если ТипЗнч(РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса) = Тип("Структура")
			И РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.Свойство("ЭтоПолучениеИдентификаторовСтрок")
			И РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.ЭтоПолучениеИдентификаторовСтрок Тогда
			
			РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.ЭтоПолучениеИдентификаторовСтрок = Ложь;
			
			СообщенияJSON = Новый Массив;
			
			СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
			ИнтеграцияСАТУРНСлужебный.ЗаполнитьСообщениеПоИсточнику(СообщениеJSON, РеквизитыИсходящегоСообщения);
			
			СообщениеJSON.ИдентификаторЗаявки = РеквизитыИсходящегоСообщенияОснования.ИдентификаторЗаявки;
			СообщениеJSON.ПараметрыЗапроса    = РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса;
			СообщениеJSON.АргументыОперации   = РеквизитыИсходящегоСообщенияОснования.АргументыОперации;
			
			ИнтерфейсСАТУРН.ДобавитьАргументШапки(СообщениеJSON.АргументыОперации, "id", СообщениеJSON.ИдентификаторЗаявки);
			
			СообщенияJSON.Добавить(СообщениеJSON);
			
			ОперацияДокумента = РеквизитыИсходящегоСообщения.ПараметрыЗапроса.Операция;
			
			Если ОперацияДокумента = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзЕАЭС Тогда
			
				Если РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.КоличествоСтрок > 1 Тогда
					
					СообщениеJSON.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСИзменениеДокумента;
					
					НомерСтроки = 0;
					Для Каждого СтрокаТаблицы Из ВходящиеДанные[0]._tbrs Цикл
						
						ИдентификаторСтроки = Формат(СтрокаТаблицы.id, "ЧРГ=; ЧГ=;");
						РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.ИдентификаторПоСтрокам.Вставить(ИдентификаторСтроки, НомерСтроки);
						
						СообщениеJSON.АргументыОперации.theCard.patList[НомерСтроки].Вставить("id", ИдентификаторСтроки);
						НомерСтроки = НомерСтроки + 1;
						
					КонецЦикла;
					
				Иначе
					СообщениеJSON.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСПринятиеКУчету;
				КонецЕсли;
				
			Иначе
				
				Если РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.КоличествоСтрок > 1 Тогда
					
					СообщениеJSON.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзменениеДокумента;
					
					НомерСтроки = 0;
					Для Каждого СтрокаТаблицы Из ВходящиеДанные[0]._tbrs Цикл
						
						ИдентификаторСтроки = Формат(СтрокаТаблицы.id, "ЧРГ=; ЧГ=;");
						РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.ИдентификаторПоСтрокам.Вставить(ИдентификаторСтроки, НомерСтроки);
						
						СообщениеJSON.АргументыОперации.theCard.patList[НомерСтроки].Вставить("id", ИдентификаторСтроки);
						НомерСтроки = НомерСтроки + 1;
						
					КонецЦикла;
					
				Иначе
					СообщениеJSON.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПодтверждениеПриемки;
				КонецЕсли;
				
			КонецЕсли;
			
			СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция, СообщениеJSON.Документ);
			ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
			
		Иначе
		
			Попытка
				
				ДокументОбъект = ЭлементОчереди.Документ.ПолучитьОбъект();
				Блокировка.Заблокировать();
				
				МетаданныеПартии = Метаданные.Справочники.ПартииСАТУРН;
				
				Для Каждого СтрокаДанныхИмпортируемыхПартий Из ВходящиеДанные Цикл
					
					ДанныеОбъекта   = Справочники.ИмпортируемаяПартияСАТУРН.ДанныеОбъекта(СтрокаДанныхИмпортируемыхПартий);
					Справочники.ИмпортируемаяПартияСАТУРН.ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена,,, ЭлементОчереди.ОрганизацияСАТУРН);
					
					Для Каждого СтрокаДанныхИмпортируемойПартии Из ДанныеОбъекта.Товары Цикл
						
						Если Не ЗначениеЗаполнено(СтрокаДанныхИмпортируемойПартии.ИдентификаторПартии) Тогда
							Продолжить;
						КонецЕсли;
						
						Если ТипЗнч(РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса) = Тип("Структура")
							И РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.Свойство("КоличествоСтрок")
							И РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.КоличествоСтрок > 1 Тогда
							
							НомерСтрокиТоваров = РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.ИдентификаторПоСтрокам.Получить(СтрокаДанныхИмпортируемойПартии.ИдентификаторСтроки);
							
							Если НомерСтрокиТоваров = Неопределено Тогда
								Продолжить;
							КонецЕсли;
							
							СтрокаТовары = ДокументОбъект.Товары[НомерСтрокиТоваров];
							
						Иначе
							СтрокаТовары = ДокументОбъект.Товары[0];
						КонецЕсли;
						
						ИдентификаторПартии = Формат(СтрокаДанныхИмпортируемойПартии.ИдентификаторПартии, "ЧРД=; ЧРГ=; ЧГ=;");
						
						ИнтеграцияСАТУРН.ДобавитьРеквизитСозданияОбъекта(
							"ПАТ", СтрокаТовары.ПАТ, ИдентификаторПартии, МетаданныеПартии, ПараметрыОбмена);
						СтрокаТовары.Партия = Справочники.ПартииСАТУРН.Партия(
							ИдентификаторПартии, ПараметрыОбмена, ЭлементОчереди.ОрганизацияСАТУРН);
						
						ИзмененныеОбъекты.Добавить(СтрокаТовары.Партия);
						
						Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
						
						Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПроведенСАТУРН;
						
						Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
						Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
						
						СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийСАТУРН.ЗаявкаВыполнена;
						
						ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(
							ЭлементОчереди.Документ, СтатусОбработки, Статусы);
						ПараметрыОбновления.ИдентификаторСтроки = СтрокаДанныхИмпортируемойПартии.ИдентификаторСтроки;
						
						РегистрыСведений.СтатусыДокументовСАТУРН.ОбновитьСтатус(
							ЭлементОчереди.Документ,
							ПараметрыОбновления);
						
					КонецЦикла;
					
				КонецЦикла;
				
				ДокументОбъект.Записать();
				
			Исключение
				ВызватьИсключение;
			КонецПопытки;
			
		КонецЕсли;
		
	ИначеЕсли ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатусовПоДокументу Тогда
		
		СообщенияJSON = Новый Массив;
		
		ЭлементОчередиОснование               = ИнтеграцияСАТУРНСлужебный.ЭлементОчередиСообщенияОснования(ЭлементОчереди, ПараметрыОбмена);
		РеквизитыИсходящегоСообщенияОснования = ЭлементОчередиОснование.РеквизитыИсходящегоСообщения;
		
		СообщениеЧтениеПартийJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
		ИнтеграцияСАТУРНСлужебный.ЗаполнитьСообщениеПоИсточнику(СообщениеЧтениеПартийJSON, РеквизитыИсходящегоСообщения);
		
		СообщениеЧтениеПартийJSON.ПараметрыЗапроса    = РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса;
		
		СообщениеЧтениеПартийJSON.ИдентификаторЗаявки = РеквизитыИсходящегоСообщения.ИдентификаторЗаявки;
		СообщениеЧтениеПартийJSON.ИдентификаторСтроки = РеквизитыИсходящегоСообщения.ИдентификаторСтроки;
		
		СообщениеЧтениеПартийJSON.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий;
		СообщениеЧтениеПартийJSON.Описание = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеЧтениеПартийJSON.Операция, СообщениеЧтениеПартийJSON.Документ);
		
		СообщениеЧтениеПартийJSON.АргументыОперации = ИнтерфейсСАТУРН.АргументыОперации(Истина, Истина);
		ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(СообщениеЧтениеПартийJSON.АргументыОперации, "id", РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.ИдентификаторыПартий);
		
		СообщенияJSON.Добавить(СообщениеЧтениеПартийJSON);
		
		ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
		
	ИначеЕсли ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭССозданиеДокумента Тогда
		
		СообщенияJSON = Новый Массив;
		
		Идентификатор = ПолученныеДанные.id;
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(Метаданные.Документы.ИмпортПродукцииСАТУРН.ПолноеИмя());
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ЭлементОчереди.Документ);
		
		Попытка
			
			Блокировка.Заблокировать();
			
			ДокументОбъект               = ЭлементОчереди.Документ.ПолучитьОбъект();
			ДокументОбъект.Идентификатор = Идентификатор;
			
			ДокументОбъект.Записать();
			
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
		ЭлементОчередиОснование               = ИнтеграцияСАТУРНСлужебный.ЭлементОчередиСообщенияОснования(ЭлементОчереди, ПараметрыОбмена);
		РеквизитыИсходящегоСообщенияОснования = ЭлементОчередиОснование.РеквизитыИсходящегоСообщения;
		РеквизитыИсходящегоСообщения          = ЭлементОчереди.РеквизитыИсходящегоСообщения;
		
		РеквизитыИсходящегоСообщенияОснования.ИдентификаторЗаявки = Идентификатор;
		
		СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
		
		ИнтеграцияСАТУРНСлужебный.ЗаполнитьСообщениеПоИсточнику(СообщениеJSON, РеквизитыИсходящегоСообщения);
		
		СообщениеJSON.ПараметрыЗапроса    = РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса;
		
		Если РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.Свойство("КоличествоСтрок") И РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.КоличествоСтрок > 1 Тогда
			
			СообщениеJSON.Операция          = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий;
			СообщениеJSON.АргументыОперации = ИнтерфейсСАТУРН.АргументыОперации(Истина, Истина);
			
			СообщениеJSON.ПараметрыЗапроса.Вставить("ЭтоПолучениеИдентификаторовСтрок", Истина);
			
			ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(СообщениеJSON.АргументыОперации, "id", РеквизитыИсходящегоСообщенияОснования.ИдентификаторЗаявки);
			
		Иначе
			
			СообщениеJSON.Операция          = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСПринятиеКУчету;
			СообщениеJSON.АргументыОперации = ИнтерфейсСАТУРН.АргументыОперации();
			
			ИнтерфейсСАТУРН.ДобавитьАргументtheCard(СообщениеJSON.АргументыОперации, "id",   Идентификатор);
			ИнтерфейсСАТУРН.ДобавитьАргументtheCard(СообщениеJSON.АргументыОперации, "date", РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.ДатаПолучения);
			
		КонецЕсли;
		
		СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция, СообщениеJSON.Документ);
		
		СообщенияJSON.Добавить(СообщениеJSON);
		
		ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
		
	ИначеЕсли ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеСозданиеДокумента Тогда
		
		СообщенияJSON = Новый Массив;
		
		Идентификатор = ПолученныеДанные.id;
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(Метаданные.Документы.ИмпортПродукцииСАТУРН.ПолноеИмя());
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ЭлементОчереди.Документ);
		
		Попытка
			
			Блокировка.Заблокировать();
			
			ДокументОбъект               = ЭлементОчереди.Документ.ПолучитьОбъект();
			ДокументОбъект.Идентификатор = Идентификатор;
			
			ДокументОбъект.Записать();
			
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
		ЭлементОчередиОснование               = ИнтеграцияСАТУРНСлужебный.ЭлементОчередиСообщенияОснования(ЭлементОчереди, ПараметрыОбмена);
		РеквизитыИсходящегоСообщенияОснования = ЭлементОчередиОснование.РеквизитыИсходящегоСообщения;
		РеквизитыИсходящегоСообщения          = ЭлементОчереди.РеквизитыИсходящегоСообщения;
		
		РеквизитыИсходящегоСообщенияОснования.ИдентификаторЗаявки = Идентификатор;
		
		СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
		
		ИнтеграцияСАТУРНСлужебный.ЗаполнитьСообщениеПоИсточнику(СообщениеJSON, РеквизитыИсходящегоСообщения);
		
		СообщениеJSON.ПараметрыЗапроса    = Новый Структура;
		СообщениеJSON.Операция            = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеПринятиеКУчету;
		СообщениеJSON.ИдентификаторЗаявки = Идентификатор;
		
		СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция, СообщениеJSON.Документ);
		
		СообщенияJSON.Добавить(СообщениеJSON);
		
		ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
		
	ИначеЕсли ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСИзменениеДокумента Тогда
		
		СообщенияJSON = Новый Массив;
		
		ЭлементОчередиОснование               = ИнтеграцияСАТУРНСлужебный.ЭлементОчередиСообщенияОснования(ЭлементОчереди, ПараметрыОбмена);
		РеквизитыИсходящегоСообщенияОснования = ЭлементОчередиОснование.РеквизитыИсходящегоСообщения;
		РеквизитыИсходящегоСообщения          = ЭлементОчереди.РеквизитыИсходящегоСообщения;
		
		СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
		ИнтеграцияСАТУРНСлужебный.ЗаполнитьСообщениеПоИсточнику(СообщениеJSON, РеквизитыИсходящегоСообщения);
		СообщениеJSON.ИдентификаторЗаявки = РеквизитыИсходящегоСообщенияОснования.ИдентификаторЗаявки;
		
		Если РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.ЭтоПолучениеИдентификаторовСтрок Тогда
			
			СообщениеJSON.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий;
			
			РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.ЭтоПолучениеИдентификаторовСтрок = Истина;
			СообщениеJSON.ПараметрыЗапроса = РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса;
			
		Иначе
			
			СообщениеJSON.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСПринятиеКУчету;
			СообщениеJSON.АргументыОперации = ИнтерфейсСАТУРН.АргументыОперации();
			
			ИнтерфейсСАТУРН.ДобавитьАргументtheCard(СообщениеJSON.АргументыОперации, "id",   РеквизитыИсходящегоСообщенияОснования.ИдентификаторЗаявки);
			ИнтерфейсСАТУРН.ДобавитьАргументtheCard(СообщениеJSON.АргументыОперации, "date", РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.ДатаПолучения);
			
		КонецЕсли;
		
		СообщениеJSON.Описание = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция, СообщениеJSON.Документ);
		
		СообщенияJSON.Добавить(СообщениеJSON);
		
		ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
		
	ИначеЕсли ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеИзменениеДокумента Тогда
		
		СообщенияJSON = Новый Массив;
		
		ЭлементОчередиОснование               = ИнтеграцияСАТУРНСлужебный.ЭлементОчередиСообщенияОснования(ЭлементОчереди, ПараметрыОбмена);
		РеквизитыИсходящегоСообщенияОснования = ЭлементОчередиОснование.РеквизитыИсходящегоСообщения;
		РеквизитыИсходящегоСообщения          = ЭлементОчереди.РеквизитыИсходящегоСообщения;
		
		СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
		ИнтеграцияСАТУРНСлужебный.ЗаполнитьСообщениеПоИсточнику(СообщениеJSON, РеквизитыИсходящегоСообщения);
		СообщениеJSON.ИдентификаторЗаявки = РеквизитыИсходящегоСообщенияОснования.ИдентификаторЗаявки;
		
		СообщениеJSON.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеПринятиеКУчету;
		СообщениеJSON.ИдентификаторЗаявки = Идентификатор;
		
		СообщениеJSON.Описание = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция, СообщениеJSON.Документ);
		
		СообщенияJSON.Добавить(СообщениеJSON);
		
		ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
		
	ИначеЕсли ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭСПринятиеКУчету Тогда
		
		СообщенияJSON = Новый Массив;
		
		ЭлементОчередиОснование               = ИнтеграцияСАТУРНСлужебный.ЭлементОчередиСообщенияОснования(ЭлементОчереди, ПараметрыОбмена);
		РеквизитыИсходящегоСообщенияОснования = ЭлементОчередиОснование.РеквизитыИсходящегоСообщения;
		
		СообщениеЧтениеПартийJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
		ИнтеграцияСАТУРНСлужебный.ЗаполнитьСообщениеПоИсточнику(СообщениеЧтениеПартийJSON, РеквизитыИсходящегоСообщения);
		
		СообщениеЧтениеПартийJSON.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий;
		СообщениеЧтениеПартийJSON.Описание = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеЧтениеПартийJSON.Операция, СообщениеЧтениеПартийJSON.Документ);
		
		РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.ПринятКУчету = Истина;
		СообщениеЧтениеПартийJSON.ПараметрыЗапроса = РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса;
		
		СообщениеЧтениеПартийJSON.АргументыОперации = ИнтерфейсСАТУРН.АргументыОперации(Истина, Истина);
		ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(СообщениеЧтениеПартийJSON.АргументыОперации, "id", РеквизитыИсходящегоСообщенияОснования.ИдентификаторЗаявки);
		
		СообщенияJSON.Добавить(СообщениеЧтениеПартийJSON);
		
		ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
		
	ИначеЕсли ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса Тогда
		
		ЭлементОчередиОснование               = ИнтеграцияСАТУРНСлужебный.ЭлементОчередиСообщенияОснования(ЭлементОчереди, ПараметрыОбмена);
		РеквизитыИсходящегоСообщенияОснования = ЭлементОчередиОснование.РеквизитыИсходящегоСообщения;
		
		Статусы = РегистрыСведений.СтатусыДокументовСАТУРН.СтруктураСтатусы();
		
		Статусы.Принят = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПроведенСАТУРН;
		
		Статусы.Ошибка = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		Статусы.ОшибкаДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		
		Если ТипЗнч(РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса)  = Тип("Структура")
			И РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.Свойство("ПринятКУчету")
			И РеквизитыИсходящегоСообщенияОснования.ПараметрыЗапроса.ПринятКУчету Тогда
			СтатусОбработки = Перечисления.СтатусыОбработкиСообщенийСАТУРН.ЗаявкаВыполнена;
		КонецЕсли;
		
		Если СтатусОбработки <> Неопределено Тогда
			
			ПараметрыОбновления = РегистрыСведений.СтатусыДокументовСАТУРН.РассчитатьСтатусы(
				ЭлементОчереди.Документ, СтатусОбработки, Статусы);
			
			РегистрыСведений.СтатусыДокументовСАТУРН.ОбновитьСтатус(
				ЭлементОчереди.Документ,
				ПараметрыОбновления);
				
			ИзмененныеОбъекты.Добавить(ЭлементОчереди.Документ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Сообщения

// Сообщение к передаче JSON.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ИмпортПродукцииСАТУРН - Ссылка на документ итмпорта.
//  ДальнейшееДействие - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие.
//  ДополнительныеПараметры - Структура, Неопределено - Дополнительные параметры.
//
// Возвращаемое значение:
//  Массив Из См. ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON - Сообщения к передаче.
//
Функция СообщениеКПередачеJSON(ДокументСсылка, ДальнейшееДействие, ДополнительныеПараметры = Неопределено) Экспорт
	
	Операция = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Операция");
	
	СообщенияJSON = Новый Массив();
	
	Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПодтвердитеИмпорт Тогда
		
		СообщенияJSON = ПодтверждениеПолученияИмпортаПродукцииСАТУРН(ДокументСсылка, ДополнительныеПараметры);
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеПартииИмпорта Тогда
		
		СообщенияJSON = ОбновитьДанныеПартийИмпортаПродукцииСАТУРН(ДокументСсылка, ДополнительныеПараметры);
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитьВвоз Тогда
		
		СообщенияJSON = ОтменитьВвозИмпортаПродукцииСАТУРН(ДокументСсылка, ДополнительныеПараметры);
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПередайтеДанные Тогда
		
		Если Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзЕАЭС Тогда
			
			СообщенияJSON = ОформитьИмпортПродукцииИзЕАЭССАТУРН(ДокументСсылка, ДополнительныеПараметры);
			
		ИначеЕсли Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомление Тогда
			
			СообщенияJSON = ПередатьДанныеПредварительногоУведомленияОбИмпортеСАТУРН(ДокументСсылка, ДополнительныеПараметры);
			
		КонецЕсли;
		
	КонецЕсли;

	Возврат СообщенияJSON;
	
КонецФункции

#КонецОбласти

#Область Проведение

Функция ПредставлениеДокументаИмпорта(НовыйДокумент, Операция, Номер, Дата) Экспорт
	
	Если Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийСАТУРН.ИмпортПродукцииИзЕАЭС") Тогда
		ПредставлениеОбъекта = НСтр("ru = 'Импорт продукции из ЕАЭС';
									|en = 'Импорт продукции из ЕАЭС'");
	ИначеЕсли Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомление") Тогда
		ПредставлениеОбъекта = НСтр("ru = 'Предварительное уведомление об импорте продукции';
									|en = 'Предварительное уведомление об импорте продукции'");
	Иначе
		ПредставлениеОбъекта = НСтр("ru = 'Импорт продукции';
									|en = 'Импорт продукции'");
	КонецЕсли;
	
	Если НовыйДокумент Тогда
		Заголовок = СтрШаблон(НСтр("ru = '%1 (создание)';
									|en = '%1 (создание)'"), ПредставлениеОбъекта);
	Иначе
		Заголовок = СтрШаблон(НСтр("ru = '%1 %2 от %3';
									|en = '%1 %2 от %3'"), ПредставлениеОбъекта, Номер, Дата);
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных;

КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаОстаткиПартийСАТУРН(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаМестаХраненияПартийСАТУРН(Запрос, ТекстыЗапроса, Регистры);
	ИнтеграцияИСПереопределяемый.ТекстыЗапросовТаблицыДвижения(Запрос, ТекстыЗапроса, Регистры, ДокументСсылка, ДополнительныеСвойства);
	
	ИнтеграцияИС.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОтменитьВвозИмпортаПродукцииСАТУРН(ДокументСсылка, ДополнительныеПараметры)
	
	СообщенияJSON = Новый Массив;
	
	ДанныеИсходногоДокумента = ДанныеДокументаИмпортаПродукции(ДокументСсылка);
	
	Шапка  = ДанныеИсходногоДокумента.Шапка;
	Данные = ДанныеИсходногоДокумента.Данные;
	
	МассивИдентификаторов = Новый Массив;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	
	АбстрактноеСообщениеПоДокументуJSON = ИнтеграцияСАТУРНСлужебный.ИнициализироватьСообщениеJSON(
		Шапка, ДокументСсылка, Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатусовПоДокументу, НомерВерсии);
	АбстрактноеСообщениеПоДокументуJSON.АргументыОперации = Новый Структура;
	АбстрактноеСообщениеПоДокументуJSON.ПараметрыЗапроса  = Новый Структура;
	
	СообщенияJSON.Добавить(АбстрактноеСообщениеПоДокументуJSON);

	Пока Данные.Следующий() Цикл
	
		АбстрактноеСообщениеJSON = ИнтеграцияСАТУРНСлужебный.ИнициализироватьСообщениеJSON(
			Шапка, ДокументСсылка, Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса, НомерВерсии);
		АбстрактноеСообщениеJSON.АргументыОперации   = Новый Структура;
		АбстрактноеСообщениеJSON.ПараметрыЗапроса    = Новый Структура;
		АбстрактноеСообщениеJSON.ИдентификаторСтроки = Данные.ИдентификаторСтроки;
		АбстрактноеСообщениеJSON.ИдентификаторЗаявки = Данные.ИдентификаторСтроки;
		
		АбстрактноеСообщениеJSON.ЗагружатьДо = АбстрактноеСообщениеПоДокументуJSON.Идентификатор;
		
		АбстрактноеСообщениеJSON.ЯвляетсяОснованиемСообщений = Истина;
		
		СообщенияJSON.Добавить(АбстрактноеСообщениеJSON);
	
		СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
		СообщениеJSON.Документ            = ДокументСсылка;
		СообщениеJSON.Версия              = НомерВерсии;
		СообщениеJSON.ИдентификаторСтроки = Данные.ИдентификаторСтроки;
		СообщениеJSON.Операция            = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииОтменаПриемки;
		СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция);
		СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации();
		СообщениеJSON.ИдентификаторЗаявки = Данные.ИдентификаторСтроки;
		СообщениеJSON.ЗагружатьДо         = АбстрактноеСообщениеJSON.Идентификатор;
		СообщениеJSON.СообщениеОснование  = АбстрактноеСообщениеJSON.Идентификатор;
		
		АргументыОперации = СообщениеJSON.АргументыОперации;
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "_id", Данные.Идентификатор);
		
		МассивИдентификаторов.Добавить(Данные.Идентификатор);
		
		СообщенияJSON.Добавить(СообщениеJSON);
		
	КонецЦикла;
	
	АбстрактноеСообщениеПоДокументуJSON.ПараметрыЗапроса.Вставить("ИдентификаторыПартий", МассивИдентификаторов);

	Возврат СообщенияJSON;
	
КонецФункции

Функция ПодтверждениеПолученияИмпортаПродукцииСАТУРН(ДокументСсылка, ДополнительныеПараметры)
	
	СообщенияJSON = Новый Массив;
	
	ДанныеИсходногоДокумента = ДанныеДокументаИмпортаПродукции(ДокументСсылка);
	
	Шапка  = ДанныеИсходногоДокумента.Шапка;
	Данные = ДанныеИсходногоДокумента.Данные;
	
	МассивИдентификаторов = Новый Массив;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	
	АбстрактноеСообщениеПоДокументуJSON = ИнтеграцияСАТУРНСлужебный.ИнициализироватьСообщениеJSON(
		Шапка, ДокументСсылка, Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатусовПоДокументу, НомерВерсии);
	АбстрактноеСообщениеПоДокументуJSON.АргументыОперации = Новый Структура;
	АбстрактноеСообщениеПоДокументуJSON.ПараметрыЗапроса  = Новый Структура;
	АбстрактноеСообщениеПоДокументуJSON.ПараметрыЗапроса.Вставить("КоличествоСтрок",        Данные.Количество());
	АбстрактноеСообщениеПоДокументуJSON.ПараметрыЗапроса.Вставить("Операция",               Шапка.Операция);
	АбстрактноеСообщениеПоДокументуJSON.ПараметрыЗапроса.Вставить("ИдентификаторПоСтрокам", Новый Соответствие());
	
	СообщенияJSON.Добавить(АбстрактноеСообщениеПоДокументуJSON);
	
	Пока Данные.Следующий() Цикл
		
		АбстрактноеСообщениеJSON = ИнтеграцияСАТУРНСлужебный.ИнициализироватьСообщениеJSON(
			Шапка, ДокументСсылка, Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса, НомерВерсии);
		АбстрактноеСообщениеJSON.АргументыОперации   = Новый Структура;
		АбстрактноеСообщениеJSON.ПараметрыЗапроса    = Новый Структура;
		АбстрактноеСообщениеJSON.ИдентификаторСтроки = Данные.Идентификатор;
		АбстрактноеСообщениеJSON.ЗагружатьДо         = АбстрактноеСообщениеПоДокументуJSON.Идентификатор;
		
		АбстрактноеСообщениеJSON.ЯвляетсяОснованиемСообщений = Истина;
		
		СообщенияJSON.Добавить(АбстрактноеСообщениеJSON);
	
		СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
		СообщениеJSON.Документ            = ДокументСсылка;
		СообщениеJSON.Версия              = НомерВерсии;
		СообщениеJSON.ИдентификаторСтроки = Данные.Идентификатор;
		СообщениеJSON.Операция            = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПодтверждениеПриемки;
		СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция);
		СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации();
		СообщениеJSON.ЗагружатьДо         = АбстрактноеСообщениеJSON.Идентификатор;
		СообщениеJSON.СообщениеОснование  = АбстрактноеСообщениеJSON.Идентификатор;
		
		ОписаниеТипаЧисло = ОбщегоНазначения.ОписаниеТипаЧисло(50);
		АргументыОперации = СообщениеJSON.АргументыОперации;
		
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "id",          Данные.ИдентификаторИмпортируемойПартии);
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "warehouseId", ОписаниеТипаЧисло.ПривестиЗначение(Данные.ИдентификаторСклада));
		
		Если ЗначениеЗаполнено(Данные.ДатаПолучения) Тогда
			ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "receiveDate", Данные.ДатаПолучения);
		КонецЕсли;
		
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "userDocNum", Шапка.НомерДокумента);
		
		ИдентификаторПартии   = Данные.ИдентификаторИмпортируемойПартии;
		МассивИдентификаторов.Добавить(ИдентификаторПартии);
		
		СообщенияJSON.Добавить(СообщениеJSON);
		
		АбстрактноеСообщениеПоДокументуJSON.ПараметрыЗапроса.ИдентификаторПоСтрокам.Вставить(Данные.Идентификатор, Данные.НомерСтроки - 1);
		
	КонецЦикла;
	
	АбстрактноеСообщениеПоДокументуJSON.ПараметрыЗапроса.Вставить("ИдентификаторыПартий", МассивИдентификаторов);
	
	Возврат СообщенияJSON;
	
КонецФункции

Функция ОформитьИмпортПродукцииИзЕАЭССАТУРН(ДокументСсылка, ДополнительныеПараметры)
	
	СообщенияJSON = Новый Массив;
	
	ДанныеИсходногоДокумента = ДанныеДокументаИмпортаПродукцииИзЕАЭС(ДокументСсылка);
	
	Шапка  = ДанныеИсходногоДокумента.Шапка;
	Данные = ДанныеИсходногоДокумента.Данные;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	
	АбстрактноеСообщениеJSON = ИнтеграцияСАТУРНСлужебный.ИнициализироватьСообщениеJSON(
		Шапка, ДокументСсылка, Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса, НомерВерсии);
	АбстрактноеСообщениеJSON.АргументыОперации = Новый Структура;
	АбстрактноеСообщениеJSON.ПараметрыЗапроса  = Новый Структура;
	АбстрактноеСообщениеJSON.ПараметрыЗапроса.Вставить("ДатаПолучения",          Шапка.ДатаПолучения);
	АбстрактноеСообщениеJSON.ПараметрыЗапроса.Вставить("КоличествоСтрок",        Данные.Количество());
	АбстрактноеСообщениеJSON.ПараметрыЗапроса.Вставить("Операция",               Шапка.Операция);
	АбстрактноеСообщениеJSON.ПараметрыЗапроса.Вставить("ИдентификаторПоСтрокам", Новый Соответствие());
	АбстрактноеСообщениеJSON.ПараметрыЗапроса.Вставить("ПринятКУчету",           Ложь);
	
	СообщенияJSON.Добавить(АбстрактноеСообщениеJSON);
	
	Если Данные.Количество() = 0 Тогда
		СообщениеJSON = ИнтеграцияСАТУРНСлужебный.ИнициализироватьСообщениеJSON(
			Шапка, ДокументСсылка, Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭССозданиеДокумента, НомерВерсии);
		СообщениеJSON.ЗагружатьДо = АбстрактноеСообщениеJSON.Идентификатор;
		СообщениеJSON.АргументыОперации = АбстрактноеСообщениеJSON.АргументыОперации;
		ИнтеграцияСАТУРНСлужебный.УстановитьСообщениеОснование(СообщениеJSON, АбстрактноеСообщениеJSON);
		СообщениеJSON.ТекстОшибки = НСтр("ru = 'Нет данных для выгрузки.';
										|en = 'Нет данных для выгрузки.'");
		СообщенияJSON.Добавить(СообщениеJSON);
		Возврат СообщенияJSON;
	КонецЕсли;
	
	Для Каждого СтрокаТовары Из Данные Цикл
		Если Не ЗначениеЗаполнено(СтрокаТовары.Упаковка) Тогда
			Данные.Колонки.Добавить("ЕдиницаИзмерения", Метаданные.ОпределяемыеТипы.Упаковка.Тип);
			ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(Неопределено, Данные);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = ДокументСсылка;
	СообщениеJSON.Версия              = НомерВерсии;
	СообщениеJSON.Операция            = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПриемкаИзЕАЭССозданиеДокумента;
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция);
	СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации();
	СообщениеJSON.ЗагружатьДо         = АбстрактноеСообщениеJSON.Идентификатор;
	
	ИнтеграцияСАТУРНСлужебный.УстановитьСообщениеОснование(СообщениеJSON, АбстрактноеСообщениеJSON);
	
	ОписаниеТипаЧисло = ОбщегоНазначения.ОписаниеТипаЧисло(50);
	АргументыОперации = СообщениеJSON.АргументыОперации;
	
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "docNum",                 Шапка.НомерДокумента);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "countryIdExporter",      ОписаниеТипаЧисло.ПривестиЗначение(Шапка.СтранаЭкспортерИдентификатор));
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "destinationWarehouseId", ОписаниеТипаЧисло.ПривестиЗначение(Шапка.МестоХраненияИдентификатор));
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "typeTransport",          ОписаниеТипаЧисло.ПривестиЗначение(Шапка.ТипТранспортаИдентификатор));
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "truckNumber",            Шапка.НомерТранспортногоСредства);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "trailerNumber",          Шапка.НомерТранспортногоПрицепа);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "containerNumber",        Шапка.НомерТранспортногоКонтейнера);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "flight",                 Шапка.НомерТранспортногоРейса);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "invoiceNumber",          Шапка.НомерДокумента);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "invoiceDate",            Шапка.ДатаДокумента);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "sealNumber",             Шапка.НомерПломбы);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "sender",                 Шапка.ГрузоотправительПредставление);
	
	СтрокиТела = Новый Массив;
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "patList", СтрокиТела);
	
	Для Каждого СтрокаТоваров Из Данные Цикл
		
		Если ЗначениеЗаполнено(СтрокаТоваров.Упаковка) Тогда
			НаименованиеУпаковки = СтрокаТоваров.УпаковкаПредставление;
		Иначе
			НаименованиеУпаковки = Строка(СтрокаТоваров.ЕдиницаИзмерения);
		КонецЕсли;
		
		ЭлементДанных = Новый Структура;
		ЭлементДанных.Вставить("tnved",             СтрЗаменить(СтрокаТоваров.КодТНВЭД, ".", ""));
		ЭлементДанных.Вставить("patProductId",      ОписаниеТипаЧисло.ПривестиЗначение(СтрокаТоваров.ПАТИДентификатор));
		ЭлементДанных.Вставить("batchCodes",        СтрокаТоваров.НомерПартии);
		ЭлементДанных.Вставить("puUnit",            НаименованиеУпаковки);
		ЭлементДанных.Вставить("puKgWeight",        СтрокаТоваров.КоличествоВУпаковкеСАТУРН);
		ЭлементДанных.Вставить("baseUnitType",      ИнтерфейсСАТУРН.ТипИзмеряемойВеличины(СтрокаТоваров.ТипИзмеряемойВеличиныСАТУРН));
		ЭлементДанных.Вставить("countPu",           СтрокаТоваров.КоличествоУпаковок);
		ЭлементДанных.Вставить("manufacturingName", СтрокаТоваров.ПредставлениеПроизводителя);
		ЭлементДанных.Вставить("expirationDate",    СтрокаТоваров.СрокГодности);
		
		СтрокиТела.Добавить(ЭлементДанных);
		
	КонецЦикла;
	
	АбстрактноеСообщениеJSON.АргументыОперации = АргументыОперации;
	СообщенияJSON.Добавить(СообщениеJSON);
	
	Возврат СообщенияJSON;
	
КонецФункции

Функция ПередатьДанныеПредварительногоУведомленияОбИмпортеСАТУРН(ДокументСсылка, ДополнительныеПараметры)
	
	СообщенияJSON = Новый Массив;
	
	ДанныеИсходногоДокумента = ДанныеДокументаИмпортаПродукцииПредварительноеУведомление(ДокументСсылка);
	
	Шапка  = ДанныеИсходногоДокумента.Шапка;
	Данные = ДанныеИсходногоДокумента.Данные;
	
	НомерВерсии = Шапка.ПоследнийНомерВерсии + 1;
	
	АбстрактноеСообщениеJSON = ИнтеграцияСАТУРНСлужебный.ИнициализироватьСообщениеJSON(
		Шапка, ДокументСсылка, Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса, НомерВерсии);
	АбстрактноеСообщениеJSON.АргументыОперации = Новый Структура;
	АбстрактноеСообщениеJSON.ПараметрыЗапроса  = Новый Структура;
	
	СообщенияJSON.Добавить(АбстрактноеСообщениеJSON);
	
	Если Данные.Количество() = 0 Тогда
		СообщениеJSON = ИнтеграцияСАТУРНСлужебный.ИнициализироватьСообщениеJSON(
			Шапка, ДокументСсылка, Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеСозданиеДокумента, НомерВерсии);
		СообщениеJSON.ЗагружатьДо = АбстрактноеСообщениеJSON.Идентификатор;
		СообщениеJSON.АргументыОперации = АбстрактноеСообщениеJSON.АргументыОперации;
		ИнтеграцияСАТУРНСлужебный.УстановитьСообщениеОснование(СообщениеJSON, АбстрактноеСообщениеJSON);
		СообщениеJSON.ТекстОшибки = НСтр("ru = 'Нет данных для выгрузки.';
										|en = 'Нет данных для выгрузки.'");
		СообщенияJSON.Добавить(СообщениеJSON);
		Возврат СообщенияJSON;
	КонецЕсли;
	
	Для Каждого СтрокаТовары Из Данные Цикл
		Если Не ЗначениеЗаполнено(СтрокаТовары.Упаковка) Тогда
			Данные.Колонки.Добавить("ЕдиницаИзмерения", Метаданные.ОпределяемыеТипы.Упаковка.Тип);
			ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(Неопределено, Данные);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = ДокументСсылка;
	СообщениеJSON.Версия              = НомерВерсии;
	СообщениеJSON.Операция            = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомлениеСозданиеДокумента;
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция);
	СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации();
	СообщениеJSON.ЗагружатьДо         = АбстрактноеСообщениеJSON.Идентификатор;
	
	ИнтеграцияСАТУРНСлужебный.УстановитьСообщениеОснование(СообщениеJSON, АбстрактноеСообщениеJSON);
	
	ОписаниеТипаЧисло = ОбщегоНазначения.ОписаниеТипаЧисло(50);
	АргументыОперации = СообщениеJSON.АргументыОперации;
	
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "docNum",                         Шапка.НомерДокумента);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "docDate",                        Шапка.ДатаДокумента);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "countryIdExporter",              ОписаниеТипаЧисло.ПривестиЗначение(Шапка.СтранаЭкспортерИдентификатор));
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "ppBorderId",                     ОписаниеТипаЧисло.ПривестиЗначение(Шапка.ТаможенныйПунктИдентификатор));
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "tuId",                           ОписаниеТипаЧисло.ПривестиЗначение(Шапка.ТерриториальноеУправлениеИдентификатор));
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "destinationWarehouseId",         ОписаниеТипаЧисло.ПривестиЗначение(Шапка.МестоХраненияИдентификатор));
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "typeTransport",                  ОписаниеТипаЧисло.ПривестиЗначение(Шапка.ТипТранспортаИдентификатор));
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "expectedDateToPvkp",             ИнтеграцияИС.ДатаUTC(Шапка.ДатаПолучения));
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "countryIdTransportRegistration", ОписаниеТипаЧисло.ПривестиЗначение(Шапка.СтранаПеревозчикИдентификатор));
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "truckNumber",                    Шапка.НомерТранспортногоСредства);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "trailerNumber",                  Шапка.НомерТранспортногоПрицепа);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "containerNumber",                Шапка.НомерТранспортногоКонтейнера);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "flight",                         Шапка.НомерТранспортногоРейса);
	
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "invoiceNumber",          Шапка.НомерДокумента);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "invoiceDate",            Шапка.ДатаДокумента);
	
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "sealNumber",                     Шапка.НомерПломбы);
	ИнтерфейсСАТУРН.ДобавитьАргументШапки(АргументыОперации, "sender",                         Шапка.ГрузоотправительПредставление);
	
	СтрокиТела = Новый Массив;
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "patList", СтрокиТела);
	
	Для Каждого СтрокаТоваров Из Данные Цикл
		
		Если ЗначениеЗаполнено(СтрокаТоваров.Упаковка) Тогда
			НаименованиеУпаковки = СтрокаТоваров.УпаковкаПредставление;
		Иначе
			НаименованиеУпаковки = Строка(СтрокаТоваров.ЕдиницаИзмерения);
		КонецЕсли;
		
		ЭлементДанных = Новый Структура;
		ЭлементДанных.Вставить("tnved",             СтрЗаменить(СтрокаТоваров.КодТНВЭД, ".", ""));
		ЭлементДанных.Вставить("patProductId",      ОписаниеТипаЧисло.ПривестиЗначение(СтрокаТоваров.ПАТИДентификатор));
		ЭлементДанных.Вставить("batchId",           0);
		ЭлементДанных.Вставить("batchCodes",        СтрокаТоваров.НомерПартии);
		ЭлементДанных.Вставить("puUnit",            НаименованиеУпаковки);
		ЭлементДанных.Вставить("puKgWeight",        СтрокаТоваров.КоличествоВУпаковкеСАТУРН);
		ЭлементДанных.Вставить("baseUnitType",      ИнтерфейсСАТУРН.ТипИзмеряемойВеличины(СтрокаТоваров.ТипИзмеряемойВеличиныСАТУРН));
		ЭлементДанных.Вставить("countPu",           СтрокаТоваров.КоличествоУпаковок);
		ЭлементДанных.Вставить("manufacturingName", СтрокаТоваров.ПредставлениеПроизводителя);
		
		СтрокиТела.Добавить(ЭлементДанных);
		
	КонецЦикла;
	
	АбстрактноеСообщениеJSON.АргументыОперации = АргументыОперации;
	СообщенияJSON.Добавить(СообщениеJSON);
	
	Возврат СообщенияJSON;
	
КонецФункции

Функция ОбновитьДанныеПартийИмпортаПродукцииСАТУРН(ДокументСсылка, ДополнительныеПараметры)
	
	Операция      = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииЧтениеПартий;
	СообщенияJSON = Новый Массив;
	
	ДанныеИсходногоДокумента = ДанныеДокументаИмпортаПродукции(ДокументСсылка);
	
	АбстрактноеСообщениеJSON = ИнтеграцияСАТУРНСлужебный.ИнициализироватьСообщениеJSON(
		ДанныеИсходногоДокумента.Шапка, ДокументСсылка, Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса, ДанныеИсходногоДокумента.Шапка.ПоследнийНомерВерсии);
	АбстрактноеСообщениеJSON.АргументыОперации = Новый Структура;
	АбстрактноеСообщениеJSON.ПараметрыЗапроса  = Новый Структура;
	
	СообщенияJSON.Добавить(АбстрактноеСообщениеJSON);
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = ДокументСсылка;
	СообщениеJSON.Операция            = Операция;
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция);
	СообщениеJSON.ПараметрыЗапроса    = Новый Структура();
	
	ИнтеграцияСАТУРНСлужебный.УстановитьСообщениеОснование(СообщениеJSON, АбстрактноеСообщениеJSON);
	
	МассивИдентификаторов = Новый Массив;
	
	Пока ДанныеИсходногоДокумента.Данные.Следующий() Цикл
		
		ИдентификаторПартии   = ДанныеИсходногоДокумента.Данные.ИдентификаторИмпортируемойПартии;
		МассивИдентификаторов.Добавить(ИдентификаторПартии);
		
		НомерВерсии = ДанныеИсходногоДокумента.Шапка.ПоследнийНомерВерсии + 1;
		СообщениеJSON.Версия              = НомерВерсии;
		
	КонецЦикла;
	
	СообщениеJSON.АргументыОперации = ИнтерфейсСАТУРН.АргументыОперации(Истина, Истина);
	ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(СообщениеJSON.АргументыОперации, "id", МассивИдентификаторов);
	
	СообщенияJSON.Добавить(СообщениеJSON);
	
	Возврат СообщенияJSON;
	
КонецФункции

Функция ДанныеДокументаИмпортаПродукции(ДокументИмпорт)
	
	Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса;
	
	РезультатВозврата = Новый Структура("Шапка, Данные");
	СписокЗапросов = Новый СписокЗначений;
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ПрисоединенныеФайлы.Документ                      КАК Ссылка,
	|	МАКСИМУМ(ЕСТЬNULL(ПрисоединенныеФайлы.Версия, 0)) КАК ПоследнийНомерВерсии
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.САТУРНПрисоединенныеФайлы КАК ПрисоединенныеФайлы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН КАК Шапка
	|		ПО Шапка.Ссылка                 = &ДокументИмпорт
	|		 И Шапка.Ссылка                 = ПрисоединенныеФайлы.Документ
	|		 И ПрисоединенныеФайлы.Операция = &Операция
	|ГДЕ
	|	ПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|СГРУППИРОВАТЬ ПО
	|	ПрисоединенныеФайлы.Документ
	|");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ЕСТЬNULL(Версии.ПоследнийНомерВерсии, 0) КАК ПоследнийНомерВерсии,
	|	ДокументСписок.ОрганизацияСАТУРН         КАК ОрганизацияСАТУРН,
	|	ДокументСписок.Операция                  КАК Операция,
	|	ДокументСписок.ДокументОснование         КАК ДокументОснование,
	|	ДокументСписок.ДатаПолучения             КАК ДатаПолучения,
	|	ДокументСписок.Номер                     КАК НомерДокумента,
	|	ДокументСписок.Комментарий               КАК Комментарий
	|ИЗ
	|	Документ.ИмпортПродукцииСАТУРН КАК ДокументСписок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|			ПО ДокументСписок.Ссылка = Версии.Ссылка
	|ГДЕ
	|	ДокументСписок.Ссылка = &ДокументИмпорт",
	"Шапка");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ИмпортПродукцииСАТУРНТовары.НомерСтроки КАК                       НомерСтроки,
	|	ИмпортПродукцииСАТУРНТовары.ИмпортируемаяПартия.Идентификатор КАК ИдентификаторИмпортируемойПартии,
	|	ИмпортПродукцииСАТУРНТовары.Идентификатор КАК                     Идентификатор,
	|	ДокументСписок.МестоХранения.Идентификатор КАК                    ИдентификаторСклада,
	|	ДокументСписок.ДатаПолучения КАК                                  ДатаПолучения,
	|	ДокументСписок.Комментарий КАК                                    Комментарий
	|ИЗ
	|	Документ.ИмпортПродукцииСАТУРН.Товары КАК ИмпортПродукцииСАТУРНТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН КАК ДокументСписок
	|		ПО ИмпортПродукцииСАТУРНТовары.Ссылка = ДокументСписок.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО ИмпортПродукцииСАТУРНТовары.Ссылка = СтатусыДокументовСАТУРН.Документ
	|		И ИмпортПродукцииСАТУРНТовары.Идентификатор = СтатусыДокументовСАТУРН.ИдентификаторСтроки
	|ГДЕ
	|	ДокументСписок.Ссылка = &ДокументИмпорт
	|	И НЕ ИмпортПродукцииСАТУРНТовары.Идентификатор = """"
	|	И НЕ ЕСТЬNULL(СтатусыДокументовСАТУРН.Статус,
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено)
	|	И НЕ ЕСТЬNULL(СтатусыДокументовСАТУРН.Статус,
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.Отменен)",
	"Данные");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументИмпорт", ДокументИмпорт);
	Запрос.УстановитьПараметр("Операция",       Операция);
	
	РезультатЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);
	
	//@skip-warning
	Данные  = РезультатЗапроса["Данные"].Выбрать();
	//@skip-warning
	Шапка   = РезультатЗапроса["Шапка"].Выбрать();
	Шапка.Следующий();
	
	РезультатВозврата.Шапка  = Шапка;
	РезультатВозврата.Данные = Данные;
	
	Возврат РезультатВозврата;
	
КонецФункции

Функция ДанныеДокументаИмпортаПродукцииИзЕАЭС(ДокументИмпорт)
	
	Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса;
	
	РезультатВозврата = Новый Структура("Шапка, Данные");
	СписокЗапросов = Новый СписокЗначений;
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ПрисоединенныеФайлы.Документ                      КАК Ссылка,
	|	МАКСИМУМ(ЕСТЬNULL(ПрисоединенныеФайлы.Версия, 0)) КАК ПоследнийНомерВерсии
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.САТУРНПрисоединенныеФайлы КАК ПрисоединенныеФайлы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН КАК Шапка
	|		ПО Шапка.Ссылка                 = &ДокументИмпорт
	|		 И Шапка.Ссылка                 = ПрисоединенныеФайлы.Документ
	|		 И ПрисоединенныеФайлы.Операция = &Операция
	|ГДЕ
	|	ПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|СГРУППИРОВАТЬ ПО
	|	ПрисоединенныеФайлы.Документ
	|");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ЕСТЬNULL(Версии.ПоследнийНомерВерсии, 0)         КАК ПоследнийНомерВерсии,
	|	ДокументСписок.ОрганизацияСАТУРН                 КАК ОрганизацияСАТУРН,
	|	ДокументСписок.Операция                          КАК Операция,
	|	ДокументСписок.ДокументОснование                 КАК ДокументОснование,
	|	ДокументСписок.ДатаПолучения                     КАК ДатаПолучения,
	|	ДокументСписок.Номер                             КАК НомерДокумента,
	|	ДокументСписок.Дата                              КАК ДатаДокумента,
	|	ДокументСписок.МестоХранения                     КАК МестоХранения,
	|	ДокументСписок.МестоХранения.Идентификатор       КАК МестоХраненияИдентификатор,
	|	ДокументСписок.СтранаЭкспортер                   КАК СтранаЭкспортер,
	|	ДокументСписок.СтранаЭкспортер.Идентификатор     КАК СтранаЭкспортерИдентификатор,
	|	ДокументСписок.ГрузоотправительОрганизацияСАТУРН КАК Грузоотправитель,
	|	ДокументСписок.Грузоотправитель                  КАК ГрузоотправительПредставление,
	|	ДокументСписок.ТипТранспорта                     КАК ТипТранспорта,
	|	ДокументСписок.ТипТранспорта.Идентификатор       КАК ТипТранспортаИдентификатор,
	|	ДокументСписок.НомерТранспортногоСредства        КАК НомерТранспортногоСредства,
	|	ДокументСписок.НомерТранспортногоПрицепа         КАК НомерТранспортногоПрицепа,
	|	ДокументСписок.НомерТранспортногоКонтейнера      КАК НомерТранспортногоКонтейнера,
	|	ДокументСписок.НомерТранспортногоРейса           КАК НомерТранспортногоРейса,
	|	ДокументСписок.НомерПломбы                       КАК НомерПломбы,
	|	ДокументСписок.Комментарий                       КАК Комментарий
	|ИЗ
	|	Документ.ИмпортПродукцииСАТУРН КАК ДокументСписок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|			ПО ДокументСписок.Ссылка = Версии.Ссылка
	|ГДЕ
	|	ДокументСписок.Ссылка = &ДокументИмпорт",
	"Шапка");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ИмпортПродукцииСАТУРНТовары.НомерСтроки                      КАК НомерСтроки,
	|	ИмпортПродукцииСАТУРНТовары.Идентификатор                    КАК Идентификатор,
	|	ИмпортПродукцииСАТУРНТовары.КодТНВЭД                         КАК КодТНВЭД,
	|	ИмпортПродукцииСАТУРНТовары.Номенклатура                     КАК Номенклатура,
	|	ИмпортПродукцииСАТУРНТовары.ПАТ                              КАК ПАТ,
	|	ИмпортПродукцииСАТУРНТовары.ПАТ.Идентификатор                КАК ПАТИдентификатор,
	|	ПРЕДСТАВЛЕНИЕ(ИмпортПродукцииСАТУРНТовары.ПАТ.Производитель) КАК ПредставлениеПроизводителя,
	|	ИмпортПродукцииСАТУРНТовары.Упаковка                         КАК Упаковка,
	|	Представление(ИмпортПродукцииСАТУРНТовары.Упаковка)          КАК УпаковкаПредставление,
	|	ИмпортПродукцииСАТУРНТовары.ТипИзмеряемойВеличиныСАТУРН      КАК ТипИзмеряемойВеличиныСАТУРН,
	|	ИмпортПродукцииСАТУРНТовары.НомерПартии                      КАК НомерПартии,
	|	ИмпортПродукцииСАТУРНТовары.ТорговоеНаименование             КАК ТорговоеНаименование,
	|	ИмпортПродукцииСАТУРНТовары.АртикулПроизводителя             КАК АртикулПроизводителя,
	|	ИмпортПродукцииСАТУРНТовары.СрокГодности                     КАК СрокГодности,
	|	ИмпортПродукцииСАТУРНТовары.Количество                       КАК Количество,
	|	ИмпортПродукцииСАТУРНТовары.КоличествоУпаковок               КАК КоличествоУпаковок,
	|	ИмпортПродукцииСАТУРНТовары.КоличествоВУпаковкеСАТУРН        КАК КоличествоВУпаковкеСАТУРН,
	|	ИмпортПродукцииСАТУРНТовары.КоличествоСАТУРН                 КАК КоличествоСАТУРН
	|ИЗ
	|	Документ.ИмпортПродукцииСАТУРН.Товары КАК ИмпортПродукцииСАТУРНТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН КАК ДокументСписок
	|		ПО ИмпортПродукцииСАТУРНТовары.Ссылка = ДокументСписок.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО ИмпортПродукцииСАТУРНТовары.Ссылка = СтатусыДокументовСАТУРН.Документ
	|		И ИмпортПродукцииСАТУРНТовары.Идентификатор = СтатусыДокументовСАТУРН.ИдентификаторСтроки
	|ГДЕ
	|	ДокументСписок.Ссылка = &ДокументИмпорт
	|	И НЕ ЕСТЬNULL(СтатусыДокументовСАТУРН.Статус,
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено)
	|	И НЕ ЕСТЬNULL(СтатусыДокументовСАТУРН.Статус,
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.Отменен)",
	"Данные");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументИмпорт", ДокументИмпорт);
	Запрос.УстановитьПараметр("Операция",       Операция);
	
	РезультатЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);
	
	//@skip-warning
	Данные  = РезультатЗапроса["Данные"].Выгрузить();
	//@skip-warning
	Шапка   = РезультатЗапроса["Шапка"].Выбрать();
	Шапка.Следующий();
	
	РезультатВозврата.Шапка  = Шапка;
	РезультатВозврата.Данные = Данные;
	
	Возврат РезультатВозврата;
	
КонецФункции

Функция ДанныеДокументаИмпортаПродукцииПредварительноеУведомление(ДокументИмпорт)
	
	Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииРасчетСтатуса;
	
	РезультатВозврата = Новый Структура("Шапка, Данные");
	СписокЗапросов = Новый СписокЗначений;
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ПрисоединенныеФайлы.Документ                      КАК Ссылка,
	|	МАКСИМУМ(ЕСТЬNULL(ПрисоединенныеФайлы.Версия, 0)) КАК ПоследнийНомерВерсии
	|ПОМЕСТИТЬ Версии
	|ИЗ
	|	Справочник.САТУРНПрисоединенныеФайлы КАК ПрисоединенныеФайлы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН КАК Шапка
	|		ПО Шапка.Ссылка                 = &ДокументИмпорт
	|		 И Шапка.Ссылка                 = ПрисоединенныеФайлы.Документ
	|		 И ПрисоединенныеФайлы.Операция = &Операция
	|ГДЕ
	|	ПрисоединенныеФайлы.ТипСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыЗапросовИС.Исходящий)
	|СГРУППИРОВАТЬ ПО
	|	ПрисоединенныеФайлы.Документ
	|");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ЕСТЬNULL(Версии.ПоследнийНомерВерсии, 0)               КАК ПоследнийНомерВерсии,
	|	ДокументСписок.ОрганизацияСАТУРН                       КАК ОрганизацияСАТУРН,
	|	ДокументСписок.ДокументОснование                       КАК ДокументОснование,
	|	ДокументСписок.ДатаПолучения                           КАК ДатаПолучения,
	|	ДокументСписок.Номер                                   КАК НомерДокумента,
	|	ДокументСписок.Дата                                    КАК ДатаДокумента,
	|	ДокументСписок.МестоХранения                           КАК МестоХранения,
	|	ДокументСписок.МестоХранения.Идентификатор             КАК МестоХраненияИдентификатор,
	|	ДокументСписок.СтранаЭкспортер                         КАК СтранаЭкспортер,
	|	ДокументСписок.СтранаЭкспортер.Идентификатор           КАК СтранаЭкспортерИдентификатор,
	|	ДокументСписок.ГрузоотправительОрганизацияСАТУРН       КАК Грузоотправитель,
	|	ДокументСписок.Грузоотправитель                        КАК ГрузоотправительПредставление,
	|	ДокументСписок.ТипТранспорта                           КАК ТипТранспорта,
	|	ДокументСписок.ТипТранспорта.Идентификатор             КАК ТипТранспортаИдентификатор,
	|	ДокументСписок.НомерТранспортногоСредства              КАК НомерТранспортногоСредства,
	|	ДокументСписок.НомерТранспортногоПрицепа               КАК НомерТранспортногоПрицепа,
	|	ДокументСписок.НомерТранспортногоКонтейнера            КАК НомерТранспортногоКонтейнера,
	|	ДокументСписок.НомерТранспортногоРейса                 КАК НомерТранспортногоРейса,
	|	ДокументСписок.НомерПломбы                             КАК НомерПломбы,
	|	ДокументСписок.ТаможенныйПункт                         КАК ТаможенныйПункт,
	|	ДокументСписок.ТаможенныйПункт.Идентификатор           КАК ТаможенныйПунктИдентификатор,
	|	ДокументСписок.ТерриториальноеУправление               КАК ТерриториальноеУправление,
	|	ДокументСписок.ТерриториальноеУправление.Идентификатор КАК ТерриториальноеУправлениеИдентификатор,
	|	ДокументСписок.СтранаПеревозчик                        КАК СтранаПеревозчик,
	|	ДокументСписок.СтранаПеревозчик.Идентификатор          КАК СтранаПеревозчикИдентификатор,
	|	ДокументСписок.Комментарий                             КАК Комментарий
	|ИЗ
	|	Документ.ИмпортПродукцииСАТУРН КАК ДокументСписок
	|		ЛЕВОЕ СОЕДИНЕНИЕ Версии КАК Версии
	|			ПО ДокументСписок.Ссылка = Версии.Ссылка
	|ГДЕ
	|	ДокументСписок.Ссылка = &ДокументИмпорт",
	"Шапка");
	
	СписокЗапросов.Добавить(
	"ВЫБРАТЬ
	|	ИмпортПродукцииСАТУРНТовары.НомерСтроки                      КАК НомерСтроки,
	|	ИмпортПродукцииСАТУРНТовары.Идентификатор                    КАК Идентификатор,
	|	ИмпортПродукцииСАТУРНТовары.КодТНВЭД                         КАК КодТНВЭД,
	|	ИмпортПродукцииСАТУРНТовары.Номенклатура                     КАК Номенклатура,
	|	ИмпортПродукцииСАТУРНТовары.ПАТ                              КАК ПАТ,
	|	ИмпортПродукцииСАТУРНТовары.ПАТ.Идентификатор                КАК ПАТИдентификатор,
	|	ПРЕДСТАВЛЕНИЕ(ИмпортПродукцииСАТУРНТовары.ПАТ.Производитель) КАК ПредставлениеПроизводителя,
	|	ИмпортПродукцииСАТУРНТовары.Упаковка                         КАК Упаковка,
	|	Представление(ИмпортПродукцииСАТУРНТовары.Упаковка)          КАК УпаковкаПредставление,
	|	ИмпортПродукцииСАТУРНТовары.ТипИзмеряемойВеличиныСАТУРН      КАК ТипИзмеряемойВеличиныСАТУРН,
	|	ИмпортПродукцииСАТУРНТовары.НомерПартии                      КАК НомерПартии,
	|	ИмпортПродукцииСАТУРНТовары.ТорговоеНаименование             КАК ТорговоеНаименование,
	|	ИмпортПродукцииСАТУРНТовары.АртикулПроизводителя             КАК АртикулПроизводителя,
	|	ИмпортПродукцииСАТУРНТовары.СрокГодности                     КАК СрокГодности,
	|	ИмпортПродукцииСАТУРНТовары.Количество                       КАК Количество,
	|	ИмпортПродукцииСАТУРНТовары.КоличествоУпаковок               КАК КоличествоУпаковок,
	|	ИмпортПродукцииСАТУРНТовары.КоличествоВУпаковкеСАТУРН        КАК КоличествоВУпаковкеСАТУРН,
	|	ИмпортПродукцииСАТУРНТовары.КоличествоСАТУРН                 КАК КоличествоСАТУРН
	|ИЗ
	|	Документ.ИмпортПродукцииСАТУРН.Товары КАК ИмпортПродукцииСАТУРНТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН КАК ДокументСписок
	|		ПО ИмпортПродукцииСАТУРНТовары.Ссылка = ДокументСписок.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
	|		ПО ИмпортПродукцииСАТУРНТовары.Ссылка = СтатусыДокументовСАТУРН.Документ
	|		И ИмпортПродукцииСАТУРНТовары.Идентификатор = СтатусыДокументовСАТУРН.ИдентификаторСтроки
	|ГДЕ
	|	ДокументСписок.Ссылка = &ДокументИмпорт
	|	И НЕ ЕСТЬNULL(СтатусыДокументовСАТУРН.Статус,
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено)
	|	И НЕ ЕСТЬNULL(СтатусыДокументовСАТУРН.Статус,
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.Отменен)",
	"Данные");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументИмпорт", ДокументИмпорт);
	Запрос.УстановитьПараметр("Операция",       Операция);
	
	РезультатЗапроса = ОбщегоНазначенияИС.ВыполнитьПакетЗапросов(Запрос, СписокЗапросов);
	
	//@skip-warning
	Данные  = РезультатЗапроса["Данные"].Выгрузить();
	//@skip-warning
	Шапка   = РезультатЗапроса["Шапка"].Выбрать();
	Шапка.Следующий();
	
	РезультатВозврата.Шапка  = Шапка;
	РезультатВозврата.Данные = Данные;
	
	Возврат РезультатВозврата;
	
КонецФункции

Функция СтатусПоСтрокеИмпортируемойПартии(ДокументСсылка, ИдентификаторСтроки)
	
	Результат = Новый Структура;
	Результат.Вставить("Статус",             Неопределено);
	Результат.Вставить("ДальнейшееДействие", Новый Массив);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СписокИмпортируемаяПартияСАТУРН.Статус КАК Статус
		|ИЗ
		|	Документ.ИмпортПродукцииСАТУРН.Товары КАК ИмпортПродукцииСАТУРНТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИмпортируемаяПартияСАТУРН КАК СписокИмпортируемаяПартияСАТУРН
		|		ПО ИмпортПродукцииСАТУРНТовары.ИмпортируемаяПартия = СписокИмпортируемаяПартияСАТУРН.Ссылка
		|ГДЕ
		|	ИмпортПродукцииСАТУРНТовары.Ссылка = &Ссылка
		|	И ИмпортПродукцииСАТУРНТовары.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Ссылка",        ДокументСсылка);
	Запрос.УстановитьПараметр("Идентификатор", ИдентификаторСтроки);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Если ВыборкаДетальныеЗаписи.Статус = Перечисления.СтатусыОбъектовСАТУРН.Актуально Тогда
			
			Результат.Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено;
			Результат.ДальнейшееДействие.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.НеТребуется);
			
		ИначеЕсли ВыборкаДетальныеЗаписи.Статус = Перечисления.СтатусыОбъектовСАТУРН.Отменен Тогда
			
			Результат.Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Отменен;
			Результат.ДальнейшееДействие.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.НеТребуется);
			
		ИначеЕсли ВыборкаДетальныеЗаписи.Статус = Перечисления.СтатусыОбъектовСАТУРН.Архив Тогда
			
			Результат.Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Архив;
			Результат.ДальнейшееДействие.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.НеТребуется);
			
		Иначе
			
			Результат.Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ввезено;
			Результат.ДальнейшееДействие = ДальнейшееДействиеПоУмолчанию();
			
		КонецЕсли;

	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РезультирующийСтатусДокумента(ДокументСсылка)
	
	Результат = Новый Структура;
	Результат.Вставить("Статус",             Неопределено);
	Результат.Вставить("ДальнейшееДействие", Новый Массив);
	
	РезультатПоОперации = РезультирующийСтатусДокументаПоОперацииИмпорт(ДокументСсылка);
	
	Результат.Статус             = РезультатПоОперации.Статус;
	Результат.ДальнейшееДействие = РезультатПоОперации.ДальнейшееДействие;
	
	Возврат Результат;
	
КонецФункции

Функция РезультирующийСтатусДокументаПоОперацииИмпорт(ДокументСсылка)
	
	Результат = Новый Структура;
	Результат.Вставить("Статус",             Неопределено);
	Результат.Вставить("ДальнейшееДействие", Новый Массив);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТоварыИмпорта.Идентификатор                            КАК Идентификатор,
		|	ДокументСписок.Операция                                КАК Операция,
		|	ЕСТЬNULL(СтатусыДокументовСАТУРН.Статус, НЕОПРЕДЕЛЕНО) КАК Статус
		|ИЗ
		|	Документ.ИмпортПродукцииСАТУРН.Товары КАК ТоварыИмпорта
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
		|		ПО ТоварыИмпорта.Ссылка = СтатусыДокументовСАТУРН.Документ
		|		И СтатусыДокументовСАТУРН.ИдентификаторСтроки = ТоварыИмпорта.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН КАК ДокументСписок
		|		ПО ТоварыИмпорта.Ссылка = ДокументСписок.Ссылка
		|ГДЕ
		|	ТоварыИмпорта.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ЕстьСформировано = Ложь;
	ЕстьОшибки       = Ложь;
	ЕстьВвезенные    = Ложь;
	ЕстьОтмененные   = Ложь;
	
	ДальнейшееДействие = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено
			Или Выборка.Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПроведенСАТУРН Тогда
			
			ЕстьСформировано = Истина;
			
		ИначеЕсли Выборка.Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается Тогда
			
			Результат.Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Обрабатывается;
			Результат.ДальнейшееДействие.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОжидайтеЗавершенияОбработкиДанных);
	
			Возврат Результат;
			
		ИначеЕсли Выборка.Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ввезено Тогда
			
			ЕстьВвезенные = Истина;
			
		ИначеЕсли Выборка.Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Отменен Тогда
			
			ЕстьОтмененные = Истина;
			
		Иначе
			
			ЕстьОшибки = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДальнейшееДействие = Новый Массив;
	
	Если ЕстьСформировано И ЕстьОшибки Тогда
		
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПолученоЧастично;
		ДальнейшееДействие = ДальнейшееДействиеПоУмолчанию();
		
	ИначеЕсли ЕстьСформировано И Не ЕстьВвезенные
		И (Выборка.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииИзЕАЭС Или Выборка.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомление) Тогда
		
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПроведенСАТУРН;
		
	ИначеЕсли ЕстьСформировано И Не ЕстьВвезенные Тогда
		
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено;
		
	ИначеЕсли ЕстьОтмененные И ЕстьОшибки Тогда
		
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		ДальнейшееДействие.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		ДальнейшееДействие.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеПартииИмпорта);
		
	ИначеЕсли ЕстьОтмененные Тогда
		
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Отменен;
		
	ИначеЕсли ЕстьСформировано И ЕстьВвезенные Тогда
		
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПолученоЧастично;
		ДальнейшееДействие = ДальнейшееДействиеПоУмолчанию();
		
	ИначеЕсли ЕстьВвезенные Тогда
		
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ввезено;
		ДальнейшееДействие = ДальнейшееДействиеПоУмолчанию();
		
	Иначе
		
		Статус = Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка;
		ДальнейшееДействие.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеОперацию);
		ДальнейшееДействие.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеПартииИмпорта);
		
	КонецЕсли;
	
	Результат.Статус             = Статус;
	Результат.ДальнейшееДействие = ДальнейшееДействие;
	
	Возврат Результат;
	
КонецФункции

#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеШапки.Дата         КАК Период,
	|	ДанныеШапки.Ссылка       КАК Ссылка,
	|	СтатусыДокументов.Статус КАК СтатусОбработки
	|ИЗ
	|	Документ.ИмпортПродукцииСАТУРН КАК ДанныеШапки
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументов
	|		ПО СтатусыДокументов.Документ = &Ссылка
	|		И СтатусыДокументов.ИдентификаторСтроки = """"
	|ГДЕ
	|	ДанныеШапки.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",          Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка",          Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("СтатусОбработки", Реквизиты.СтатусОбработки);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаОстаткиПартийСАТУРН(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОстаткиПартийСАТУРН";
	
	Если НЕ ИнтеграцияИС.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Шапка.ДатаПолучения                    КАК Период,
	|	0                                      КАК ВОбработкеСАТУРН,
	|	ТаблицаТовары.КоличествоСАТУРН         КАК КоличествоСАТУРН,
	|	ТаблицаТовары.Партия                   КАК Партия
	|ИЗ
	|	Документ.ИмпортПродукцииСАТУРН КАК Шапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН.Товары КАК ТаблицаТовары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК Статусы
	|			ПО ТаблицаТовары.Ссылка = Статусы.Документ
	|			И ТаблицаТовары.Идентификатор = Статусы.ИдентификаторСтроки
	|		ПО Шапка.Ссылка = ТаблицаТовары.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|	И Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено)
	|	И ТаблицаТовары.Партия <> ЗНАЧЕНИЕ(Справочник.ПартииСАТУРН.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)        КАК ВидДвижения,
	|	Шапка.ДатаПолучения                           КАК Период,
	|	0                                             КАК ВОбработкеСАТУРН,
	|	ТаблицаТовары.КоличествоУпаковок 
	|		* ТаблицаТовары.КоличествоВУпаковкеСАТУРН КАК КоличествоСАТУРН,
	|	ТаблицаТовары.Партия                          КАК Партия
	|ИЗ
	|	Документ.ИмпортПродукцииСАТУРН КАК Шапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН.Товары КАК ТаблицаТовары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК Статусы
	|			ПО ТаблицаТовары.Ссылка = Статусы.Документ
	|			И ТаблицаТовары.Идентификатор = Статусы.ИдентификаторСтроки
	|		ПО Шапка.Ссылка = ТаблицаТовары.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|	И Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПроведенСАТУРН)
	|	И ТаблицаТовары.Партия <> ЗНАЧЕНИЕ(Справочник.ПартииСАТУРН.ПустаяСсылка)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаМестаХраненияПартийСАТУРН(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МестаХраненияПартийСАТУРН";
	
	Если НЕ ИнтеграцияИС.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Шапка.ДатаПолучения        КАК Период,
	|	Шапка.ОрганизацияСАТУРН    КАК ОрганизацияСАТУРН,
	|	Шапка.МестоХранения        КАК МестоХранения,
	|	ТаблицаТовары.Партия       КАК Партия
	|ИЗ
	|	Документ.ИмпортПродукцииСАТУРН КАК Шапка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК Статусы
	|			ПО ТаблицаТовары.Ссылка = Статусы.Документ
	|			И ТаблицаТовары.Идентификатор = Статусы.ИдентификаторСтроки
	|		ПО Шапка.Ссылка = ТаблицаТовары.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|	И Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено)
	|	И ТаблицаТовары.Партия <> ЗНАЧЕНИЕ(Справочник.ПартииСАТУРН.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Шапка.ДатаПолучения        КАК Период,
	|	Шапка.ОрганизацияСАТУРН    КАК ОрганизацияСАТУРН,
	|	Шапка.МестоХранения        КАК МестоХранения,
	|	ТаблицаТовары.Партия       КАК Партия
	|ИЗ
	|	Документ.ИмпортПродукцииСАТУРН КАК Шапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИмпортПродукцииСАТУРН.Товары КАК ТаблицаТовары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК Статусы
	|			ПО ТаблицаТовары.Ссылка = Статусы.Документ
	|			И ТаблицаТовары.Идентификатор = Статусы.ИдентификаторСтроки
	|		ПО Шапка.Ссылка = ТаблицаТовары.Ссылка
	|ГДЕ
	|	Шапка.Ссылка = &Ссылка
	|	И Статусы.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПроведенСАТУРН)
	|	И ТаблицаТовары.Партия <> ЗНАЧЕНИЕ(Справочник.ПартииСАТУРН.ПустаяСсылка)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает статусы документа в которых требуется пересчет движений.
//
// Возвращаемое значение:
//  Массив из ПеречислениеСсылка.СтатусыОбработкиАктаПримененияСАТУРН- статусы пересчета движений.
//
Функция СтатусыПересчетаДвижений()
	
	Статусы = Новый Массив;
	
	Статусы.Добавить(Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Получено);
	Статусы.Добавить(Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.ПроведенСАТУРН);
	
	Возврат Статусы;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
