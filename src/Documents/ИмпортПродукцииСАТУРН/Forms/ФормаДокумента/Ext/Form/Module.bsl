#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЭтоИмпортПродукции = (Объект.Операция = Перечисления.ВидыОперацийСАТУРН.ИмпортПродукции);
	
	Если Объект.Ссылка.Пустая() И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) И ЭтоИмпортПродукции Тогда
		ВызватьИсключение НСтр("ru = 'Создание нового документа с операцией Импорт продукции копированием запрещено.
			|
			|Новый документ необходимо создать из списка К оформлению.';
			|en = 'Создание нового документа с операцией Импорт продукции копированием запрещено.
			|
			|Новый документ необходимо создать из списка К оформлению.'");
	КонецЕсли;
	
	ИнтеграцияИС.НастроитьВидимостьДокументаОснования(ЭтотОбъект);
	Элементы.ДокументОснование.ДоступныеТипы = Метаданные.ОпределяемыеТипы.ОснованиеИмпортПродукцииСАТУРН.Тип;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыИС.ПриСозданииНаСервере(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВерсионированиеОбъектов") Тогда
		МодульВерсионированиеОбъектов = ОбщегоНазначения.ОбщийМодуль("ВерсионированиеОбъектов");
		МодульВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(ЭтотОбъект,   "ТоварыХарактеристика");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(ЭтотОбъект,   "ТоварыСерия");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСХарактеристикой(ЭтотОбъект, "ТоварыСерия");
	СобытияФормИСПереопределяемый.УстановитьСвязиПараметровВыбораСНоменклатурой(ЭтотОбъект,   "ТоварыУпаковка");
	
	СобытияФормСАТУРН.УстановитьСвязиПараметровВыбораСОрганизациейМестомХранения(ЭтотОбъект, "ТоварыПартия");
	СобытияФормСАТУРН.УстановитьСвязиПараметровВыбораСОрганизациейМестомХранения(ЭтотОбъект, "МестоХранения",, "");
	СобытияФормСАТУРН.УстановитьПараметрыВыбораОрганизации(ЭтотОбъект);
	
	ВидыПродукции = Новый Массив;
	ВидыПродукции.Добавить(Перечисления.ВидыПродукцииИС.ПодконтрольнаяПродукцияСАТУРН);
	
	СобытияФормИСКлиентСерверПереопределяемый.УстановитьПараметрыВыбораНоменклатуры(ЭтотОбъект, ВидыПродукции);
	
	Если Объект.Ссылка.Пустая() Тогда
		
		ПриСозданииЧтенииНаСервере();
		ИнтеграцияИСПереопределяемый.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
		
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
	СобытияФормИС.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка, Неопределено);
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриСозданииЧтенииНаСервере(Ложь);
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыИС.ПриЧтенииНаСервере(ЭтотОбъект);
	
	СобытияФормИС.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если НовыйОбъект = Элементы.ДокументОснование.ДоступныеТипы.ПривестиЗначение(НовыйОбъект) Тогда
		Объект.ДокументОснование = НовыйОбъект;
		Модифицированность = Истина;
		Записать();
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриВыбореНоменклатуры", ЭтотОбъект);
	СобытияФормСАТУРНКлиентПереопределяемый.ОбработкаВыбораНоменклатуры(ОписаниеОповещения, НовыйОбъект, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ИмяПодсистемы = ИнтеграцияСАТУРНКлиентСервер.ИмяПодсистемы();
	
	Если ИмяСобытия = ОбменДаннымиИСКлиентСервер.ИмяСобытияИзмененоСостояние(ИмяПодсистемы)
		И Параметр.Ссылка = Объект.Ссылка Тогда
		
		Если Параметр.Свойство("ОбъектИзменен")
			И Параметр.ОбъектИзменен Тогда
			ОбновитьПредставленияНаФорме(Истина);
		Иначе
			ОбновитьПредставленияНаФорме(Ложь);
		КонецЕсли;
		
		НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "СтатусСАТУРН");
		
	КонецЕсли;
	
	Если ИмяСобытия = ОбменДаннымиИСКлиентСервер.ИмяСобытияВыполненОбмен(ИмяПодсистемы)
	 И (Параметр = Неопределено
		Или (ТипЗнч(Параметр) = Тип("Структура") И Параметр.ОбновлятьСтатусВФормахДокументов)) Тогда
		
		ОбновитьПредставленияНаФорме(Истина);
		
	КонецЕсли;
	
	Если ИмяСобытия = "Закрытие_ПерейтиКСтрокеОшибки" И Источник = "Справочник.САТУРНПрисоединенныеФайлы.Форма.ФормаОшибки" Тогда
		ТекущийЭлемент = Элементы.Товары;
		Элементы.Товары.ТекущаяСтрока = Параметр;
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_КлассификаторОрганизацийСАТУРН"
			И Параметр = Объект.ОрганизацияСАТУРН
		Или ИмяСобытия = "Запись_МестаХраненияСАТУРН"
			И Параметр = Объект.МестоХранения
		Или ИмяСобытия = "Запись_КлассификаторОрганизацийСАТУРН"
			И Параметр = Объект.ГрузоотправительОрганизацияСАТУРН Тогда
		
		ЗаполнитьГиперссылкиРеквизитов();
		
	КонецЕсли;
	
	ОбщегоНазначенияСобытияФормИСКлиентПереопределяемый.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	СобытияФормСАТУРНКлиентПереопределяемый.ОбработкаВыбораСерии(
		ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора, ПараметрыУказанияСерий);
	
	СобытияФормСАТУРНКлиентПереопределяемый.ОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение, ИсточникВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СобытияФормСАТУРНКлиент.ОбработкаНавигационнойСсылкиСАТУРН(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	СобытияФормИСКлиент.ОбработкаНавигационнойСсылки(ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ОбщегоНазначенияСобытияФормИСКлиентПереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ИнтеграцияИСКлиент.ПослеЗаписиВФормеОбъектаДокументаИС(
		ЭтотОбъект,
		Объект,
		ИнтеграцияСАТУРНКлиентСервер.ИмяПодсистемы(),
		ПараметрыЗаписи);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Подключаемый_ОбновитьКоманды();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	НовыйДокумент = Не ЗначениеЗаполнено(Объект.Ссылка);
	
	ИнтеграцияСАТУРН.ЗаполнитьСопоставленныеТовары(Объект.Товары, НоменклатураДляВыбора, НовыйДокумент);
	ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(ЭтотОбъект, Объект.Товары);
	
	СформироватьПредставлениеДанныхНовойПартии(ЭтотОбъект, Объект.Товары);
	
	ОбновитьПредставленияНаФорме();
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
	ИнтеграцияИС.ПослеЗаписиНаСервереВФормеОбъектаДокументаИС(
		ЭтотОбъект,
		ТекущийОбъект,
		ИнтеграцияСАТУРНКлиентСервер.ИмяПодсистемы(),
		ПараметрыЗаписи);
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатусПредставлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОчиститьСообщения();
	
	Если (Не ЗначениеЗаполнено(Объект.Ссылка))
		Или (ДействияНавигационнойСсылкиБезПроверкиПроведения().Найти(НавигационнаяСсылкаФорматированнойСтроки) = Неопределено И Не Объект.Проведен) Тогда
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение",
		                                                    ЭтотОбъект,
		                                                    Новый Структура("НавигационнаяСсылкаФорматированнойСтроки", НавигационнаяСсылкаФорматированнойСтроки));
		ТекстВопроса = НСтр("ru = '""Импорт продукции"" не проведен. Провести?';
							|en = '""Импорт продукции"" не проведен. Провести?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	ИначеЕсли Модифицированность Тогда
		
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение",
		                                                    ЭтотОбъект,
		                                                    Новый Структура("НавигационнаяСсылкаФорматированнойСтроки", НавигационнаяСсылкаФорматированнойСтроки));
		
		Если ДействияНавигационнойСсылкиБезПроверкиПроведения().Найти(НавигационнаяСсылкаФорматированнойСтроки) = Неопределено Тогда
			ТекстВопроса = НСтр("ru = '""Импорт продукции"" был изменен. Провести?';
								|en = '""Импорт продукции"" был изменен. Провести?'");
		Иначе
			ТекстВопроса = НСтр("ru = '""Импорт продукции"" был изменен. Записать?';
								|en = '""Импорт продукции"" был изменен. Записать?'");
		КонецЕсли;
		
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ОбработатьНажатиеНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ДокументОснование, Операция");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияСАТУРНПриИзменении(Элемент)
	
	ЗаполнитьГиперссылкиРеквизитов();

КонецПроцедуры

&НаКлиенте
Процедура МестоХраненияПриИзменении(Элемент)
	
	ЗаполнитьГиперссылкиРеквизитов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузоотправительОрганизацияСАТУРНПриИзменении(Элемент)
	
	ЗаполнитьГиперссылкиРеквизитов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеОчистка(Элемент, СтандартнаяОбработка)
	ЗаполнитьОтборыПоОснованию(Неопределено, ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ТипТранспортаПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ТипТранспорта");
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоПриИзменении(Элемент)
	
	РеквизитыТранспортногоСредства      = ЗначенияРеквизитовТранспортногоСредства(Объект.ТранспортноеСредство);
	Объект.НомерТранспортногоСредства   = РеквизитыТранспортногоСредства.НомерТранспортногоСредства;
	Объект.НомерТранспортногоКонтейнера = РеквизитыТранспортногоСредства.НомерТранспортногоКонтейнера;
	Объект.НомерТранспортногоПрицепа    = РеквизитыТранспортногоСредства.НомерТранспортногоПрицепа;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если РедактированиеФормыНедоступно
		Или Не ПравоИзменения Тогда
			
		СобытияФормСАТУРНКлиент.ВыборЭлементаТабличнойЧастиОткрытьФормуЭлемента(ЭтотОбъект, Элемент, Поле);
		
	ИначеЕсли Поле.Имя = Элементы.ТоварыИмпортируемаяПартия.Имя Тогда
		
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПоказатьЗначение(, ТекущиеДанные.ИмпортируемаяПартия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	
	Если РедактированиеФормыНедоступно Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент = Элементы.ТоварыНоменклатура Тогда
		
		ЗаполнитьСпискиВыбораНоменклатуры(ТекущиеДанные);
		
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент = Элементы.ТоварыХарактеристика Тогда
		
		ЗаполнитьСпискиВыбораХарактеристика(ТекущиеДанные);
		
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент = Элементы.ТоварыИмпортируемаяПартияПАТ Тогда
		
		Элементы.ТоварыИмпортируемаяПартияПАТ.СписокВыбора.ЗагрузитьЗначения(ТекущиеДанные.НоменклатураДляВыбора.ВыгрузитьЗначения());
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СобытияФормИСКлиентПереопределяемый.ПриНачалеВыбораНоменклатуры(
		Элемент,
		ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПодконтрольнаяПродукцияСАТУРН"),
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	НоменклатураПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураСоздание(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	ПараметрыНоменклатуры = ИнтеграцияСАТУРНВызовСервера.ПараметрыСозданияНоменклатуры(
		ТекущиеДанные.ПАТ);
	
	СобытияФормСАТУРНКлиентПереопределяемый.ОткрытьФормуСозданияНоменклатуры(ЭтотОбъект, ПараметрыНоменклатуры);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоУпаковокПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормСАТУРНКлиент.ПриИзмененииКоличестваУпаковок(
		ЭтотОбъект, ТекущиеДанные, КэшированныеЗначения);
	
	ПриИзмененииКоличестваУпаковок(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыУпаковкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормСАТУРНКлиент.ПриИзмененииУпаковки(
		ЭтотОбъект, ТекущиеДанные, КэшированныеЗначения);
	
	ПриИзмененииКоличестваВУпаковкеСАТУРН(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормСАТУРНКлиентПереопределяемый.ПриНачалеВыбораХарактеристики(
		Элемент, ТекущиеДанные, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыХарактеристикаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормСАТУРНКлиентПереопределяемый.ПриИзмененииХарактеристики(ЭтотОбъект, ТекущиеДанные, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСерияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтеграцияИСКлиент.ОткрытьПодборСерий(ЭтотОбъект,, Элемент.ТекстРедактирования, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСерияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	СобытияФормСАТУРНКлиентПереопределяемый.ПриИзмененииСерии(
		ЭтотОбъект, ТекущиеДанные, КэшированныеЗначения, ПараметрыУказанияСерий);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКодТНВЭДНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияСАТУРНКлиент.ОткрытьФормуПодбораТНВЭД(ЭтотОбъект, ТекущиеДанные, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыДанныеНовойПартииОчистка(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	ОчиститьДанныеНовойПартии(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыДанныеНовойПартииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	НередактируемыеРеквизиты = Новый Массив;
	НередактируемыеРеквизиты.Добавить("ДатаПроизводства");
	
	ОбязательныеРеквизиты = Новый Массив;
	ОбязательныеРеквизиты.Добавить("СрокГодности");
	
	ДанныеПартии = Новый Структура;
	ДанныеПартии.Вставить("НомерПартии",          ТекущиеДанные.НомерПартии);
	ДанныеПартии.Вставить("ТорговоеНаименование", ТекущиеДанные.ТорговоеНаименование);
	ДанныеПартии.Вставить("АртикулПроизводителя", ТекущиеДанные.АртикулПроизводителя);
	ДанныеПартии.Вставить("ДатаПроизводства",     Объект.Дата);
	ДанныеПартии.Вставить("СрокГодности",         ТекущиеДанные.СрокГодности);
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ДанныеПартии",             ДанныеПартии);
	ПараметрыОткрытияФормы.Вставить("НередактируемыеРеквизиты", НередактируемыеРеквизиты);
	ПараметрыОткрытияФормы.Вставить("ОбязательныеРеквизиты",    ОбязательныеРеквизиты);
	
	ОткрытьФорму(
		"Справочник.ПартииСАТУРН.Форма.ДанныеПартии",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,,,,
		Новый ОписаниеОповещения("ДанныеНовойПартииОкончаниеВыбора", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыТипИзмеряемойВеличиныСАТУРННачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Данные = Новый Структура;
	Данные.Вставить("Партия",       ТекущиеДанные.Партия);
	Данные.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
	Данные.Вставить("Упаковка",     ТекущиеДанные.Упаковка);
	
	ДанныеВыбора = Новый СписокЗначений;
	ДанныеВыбора.ЗагрузитьЗначения(
		ИнтеграцияСАТУРНКлиент.ДоступныеТипыИзмеряемыхВеличин(Данные, КэшированныеЗначения));
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыТипИзмеряемойВеличиныСАТУРНПриИзменении(Элемент)
	
	ПриИзмененииТипаИзмеряемойВеличины();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ИмпортПродукцииСАТУРН.Форма.ФормаДокумента.Провести",,Истина);
	
	ОчиститьСообщения();
	ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ИмпортПродукцииСАТУРН.Форма.ФормаДокумента.Записать",,Истина);
	
	ОчиститьСообщения();
	Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ИмпортПродукцииСАТУРН.Форма.ФормаДокумента.ПровестиИЗакрыть",,Истина);
	
	ОчиститьСообщения();
	ПараметрыЗаписи = Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	
	Если Записать(ПараметрыЗаписи) Тогда
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьПоОснованию(Команда)
	ОбработчикПерезаполненияПоОснованию();
КонецПроцедуры

&НаКлиенте
Процедура АрхивироватьДокумент(Команда)
	
	ОбщегоНазначенияИСКлиент.АрхивироватьДокументы(ЭтотОбъект, Объект.Ссылка, ИнтеграцияСАТУРНКлиент);
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

//@skip-warning
&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюКомандПодключенныхКОбъекту(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормИСКлиентПереопределяемый.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ВыполнитьОбменОбработкаОжидания()
	
	ИнтеграцияСАТУРНСлужебныйКлиент.ПродолжитьВыполнениеОбмена(ЭтотОбъект, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриЗавершенииОперации(Результат, ДополнительныеПараметры) Экспорт
	
	Прочитать();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПослеВыбораОснования(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если ДанныеВыбора = Элементы.ДокументОснование.ДоступныеТипы.ПривестиЗначение(ДанныеВыбора) Тогда
		Объект.ДокументОснование = ДанныеВыбора;
		Модифицированность       = Истина;
	КонецЕсли;
	
	ЗаполнитьТовары = (ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ОбработатьПерезаполнение"));
	Если ЗаполнитьТовары Тогда
		ОбработчикПерезаполненияПоОснованию(Ложь);
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ДокументОснование");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Оформление поля Партия
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыПартия.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.Партия");
	ОтборЭлемента.ВидСравнения  = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста",     ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст",          НСтр("ru = '<будет создана автоматически>';
																			|en = '<будет создана автоматически>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// Оформление неустановленного статуса обработки строки
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСтатусОбработки.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(Элементы.ТоварыСтатусОбработки.ПутьКДанным);
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Отсутствует>';
																					|en = '<Отсутствует>'"));
	
	// ДанныеНовойПартии
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыДанныеНовойПартии.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(Элементы.ТоварыДанныеНовойПартии.ПутьКДанным);
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст",
		НСтр("ru = '<укажите данные создаваемой партии>';
			|en = '<укажите данные создаваемой партии>'"));
	
	СобытияФормИСПереопределяемый.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтотОбъект);
	СобытияФормИСПереопределяемый.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтотОбъект);
	СобытияФормИСПереопределяемый.УстановитьУсловноеОформлениеСерийНоменклатуры(ЭтотОбъект);
	
	СобытияФормИС.УстановитьУсловноеОформлениеПоляНоменклатура(ЭтотОбъект);
	СобытияФормИС.УстановитьУсловноеОформлениеПоляХарактеристика(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииЧтенииНаСервере(ОбновитьКомандыОснования = Истина)
	
	ПравоИзменения = ПравоДоступа("Изменение", Метаданные.Документы.ИмпортПродукцииСАТУРН);
	ЗаполнитьОтборыПоОснованию(Объект.ДокументОснование, ЭтотОбъект, ОбновитьКомандыОснования);
	
	НовыйДокумент = Не ЗначениеЗаполнено(Объект.Ссылка);
	
	ИнтеграцияСАТУРН.ЗаполнитьСопоставленныеТовары(Объект.Товары, НоменклатураДляВыбора, НовыйДокумент);
	ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(ЭтотОбъект, Объект.Товары);
	
	СформироватьПредставлениеДанныхНовойПартии(ЭтотОбъект, Объект.Товары);
	ПараметрыУказанияСерий = ИнтеграцияИС.ПараметрыУказанияСерийФормыОбъекта(Объект, Документы.ИмпортПродукцииСАТУРН);
	
	СобытияФормИСКлиентСерверПереопределяемый.УстановитьПараметрыВыбораНоменклатуры(
		ЭтотОбъект,
		Перечисления.ВидыПродукцииИС.ПодконтрольнаяПродукцияСАТУРН);
	
	ЗаполнитьГиперссылкиРеквизитов();
	ОбновитьПредставленияНаФорме();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставленияНаФорме(Прочитать = Ложь)

	Если Прочитать Тогда
		Прочитать();
	КонецЕсли;
	
	ПараметрыСтатуса = ОбновитьСтатусСАТУРН();
		
	Для Каждого СтрокаТовары Из Объект.Товары Цикл
		
		ДанныеПоСтроке = ПараметрыСтатуса.СтатусыПоСтрокам[СтрокаТовары.Идентификатор];
		Если ДанныеПоСтроке = Неопределено Тогда
			СтрокаТовары.СтатусОбработки = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ввезено");
		Иначе
			СтрокаТовары.СтатусОбработки = ДанныеПоСтроке.СтатусСАТУРН;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	ЗависимыеОтСтатусаСАТУРН = Новый Массив;
	
	ЗависимыеОтСтатусаСАТУРН.Добавить(Элементы.ГруппаНередактируемыеПослеОтправкиРеквизитыОсновное.Имя);
	ЗависимыеОтСтатусаСАТУРН.Добавить(Элементы.НомерТТНАРГУС.Имя);
	ЗависимыеОтСтатусаСАТУРН.Добавить(Элементы.ГруппаПервичныйДокументНомерДата.Имя);
	ЗависимыеОтСтатусаСАТУРН.Добавить(Элементы.СтраницаТовары.Имя);
	ЗависимыеОтСтатусаСАТУРН.Добавить(Элементы.СтраницаДополнительно.Имя);
	
	ОбщегоНазначенияИСКлиентСервер.УстановитьДоступностьЭлементовФормы(Форма,
		ЗависимыеОтСтатусаСАТУРН, Не Форма.РедактированиеФормыНеДоступно);
	
	ВидимостьСтатусаСтроки = Форма.СтатусСАТУРН <> ПредопределенноеЗначение("Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ввезено")
		                         И Форма.СтатусСАТУРН <> ПредопределенноеЗначение("Перечисление.СтатусыОбработкиИмпортаПродукцииСАТУРН.Отменен");
	
	СтруктураПоиска = Новый Структура("СтатусОбработки", Форма.СтатусСАТУРН);
	КоличествоСтрокСоСтатусомКакИВШапке = Объект.Товары.НайтиСтроки(СтруктураПоиска).Количество();
	
	Если Инициализация Или СтруктураРеквизитов.Свойство("ДокументОснование") Тогда
		ПодключаемыеКомандыИСКлиентСервер.УправлениеВидимостьюКомандПодключенныхКОбъекту(Форма);
	КонецЕсли;
	
	ЭтоИмпортИзЕАЭС               = Объект.Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийСАТУРН.ИмпортПродукцииИзЕАЭС");
	ЭтоПредварительноеУведомление = Объект.Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомление");
	
	Элементы.ТоварыСтатусОбработки.Видимость = ВидимостьСтатусаСтроки
		И Объект.Товары.Количество() > 1
		И Не КоличествоСтрокСоСтатусомКакИВШапке = Объект.Товары.Количество()
		И Не ЭтоИмпортИзЕАЭС И Не ЭтоПредварительноеУведомление;
	
	Элементы.ТоварыПартия.Видимость            = Форма.РедактированиеФормыНедоступно И Не ЭтоПредварительноеУведомление;
	Элементы.ТоварыДанныеНовойПартии.Видимость = Не Форма.РедактированиеФормыНедоступно И (ЭтоИмпортИзЕАЭС Или ЭтоПредварительноеУведомление);
	
	Если Инициализация Или СтруктураРеквизитов.Свойство("Операция") Или СтруктураРеквизитов.Свойство("ТипТранспорта") Тогда
		
		ЭтоАвтоперевозка = Объект.ТипТранспорта = ПредопределенноеЗначение("Справочник.КлассификаторТранспортныхСредствСАТУРН.АвтомобильныйТранспорт");
		ЭтоАвиаперевозка = Объект.ТипТранспорта = ПредопределенноеЗначение("Справочник.КлассификаторТранспортныхСредствСАТУРН.АвиационноеСообщение");
		
		Элементы.НомерТранспортногоСредства.Видимость   = (ЭтоИмпортИзЕАЭС Или ЭтоПредварительноеУведомление) И ЭтоАвтоперевозка;
		Элементы.НомерТранспортногоПрицепа.Видимость    = (ЭтоИмпортИзЕАЭС Или ЭтоПредварительноеУведомление) И ЭтоАвтоперевозка;
		Элементы.НомерТранспортногоКонтейнера.Видимость = ЭтоИмпортИзЕАЭС Или ЭтоПредварительноеУведомление;
		Элементы.НомерТранспортногоРейса.Видимость      = (ЭтоИмпортИзЕАЭС Или ЭтоПредварительноеУведомление) И ЭтоАвиаперевозка;
		Элементы.НомерПломбы.Видимость                  = ЭтоИмпортИзЕАЭС Или ЭтоПредварительноеУведомление;
		Элементы.ТранспортноеСредство.Видимость         = (ЭтоИмпортИзЕАЭС Или ЭтоПредварительноеУведомление) И ЭтоАвтоперевозка;
		
	КонецЕсли;
	
	Если Инициализация Или СтруктураРеквизитов.Свойство("Операция") Тогда
		
		УстановитьЗаголовокДокумента(Форма);
		
		Элементы.ДанныеИмпортаИзЕАЭС.Видимость               = ЭтоИмпортИзЕАЭС Или ЭтоПредварительноеУведомление;
		Элементы.ДанныеПредварительногоУведомления.Видимость = ЭтоПредварительноеУведомление;
		Элементы.ТоварыИмпортируемаяПартия.Видимость         = Не ЭтоИмпортИзЕАЭС И Не ЭтоПредварительноеУведомление;
		Элементы.ТоварыКодТНВЭД.Видимость                    = ЭтоИмпортИзЕАЭС Или ЭтоПредварительноеУведомление;
		Элементы.ГруппаГрузоотправитель.Видимость            = ЭтоИмпортИзЕАЭС Или ЭтоПредварительноеУведомление;
		Элементы.ТоварыКоличествоВУпаковкеСАТУРН.Видимость   = ЭтоИмпортИзЕАЭС Или ЭтоПредварительноеУведомление;
		Элементы.ТоварыКоличествоСАТУРН.Видимость            = Не ЭтоИмпортИзЕАЭС И Не ЭтоПредварительноеУведомление;
		
		Элементы.Товары.ИзменятьСоставСтрок  = ЭтоИмпортИзЕАЭС Или ЭтоПредварительноеУведомление;
		Элементы.Товары.ИзменятьПорядокСтрок = ЭтоИмпортИзЕАЭС Или ЭтоПредварительноеУведомление;
		
		Элементы.ТоварыИмпортируемаяПартияПАТ.ТолькоПросмотр      = Не ЭтоИмпортИзЕАЭС И Не ЭтоПредварительноеУведомление;
		Элементы.ТоварыКоличествоСАТУРН.ТолькоПросмотр            = Не ЭтоИмпортИзЕАЭС И Не ЭтоПредварительноеУведомление;
		Элементы.ТоварыТипИзмеряемойВеличиныСАТУРН.ТолькоПросмотр = Не ЭтоИмпортИзЕАЭС И Не ЭтоПредварительноеУведомление;
		
		Элементы.Грузоотправитель.АвтоОтметкаНезаполненного = ЭтоИмпортИзЕАЭС;
		
		ПараметрОтбора = Новый ПараметрВыбора("Отбор.ЕАЭС", ЭтоИмпортИзЕАЭС);
		МассивПараметровВыбора = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрОтбора);
		
		Элементы.СтранаЭкспортер.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
		
		Элементы.ДатаПолучения.Заголовок = ?(ЭтоПредварительноеУведомление, НСтр("ru = 'Планируемая дата получения';
																				|en = 'Планируемая дата получения'"), НСтр("ru = 'Дата получения';
																															|en = 'Дата получения'"));
		Элементы.ДатаПолучения.АвтоОтметкаНезаполненного = Не ЭтоПредварительноеУведомление;
		
		Если ЭтоПредварительноеУведомление Тогда
			Элементы.ДатаПолучения.ОтметкаНезаполненного = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#Область ПерезаполнениеПоОснованию

&НаКлиенте
Процедура ОбработчикПерезаполненияПоОснованию(ЗадаватьВопрос = Истина)
	
	ОчиститьСообщения();
	
	Если Объект.Товары.Количество() > 0 И ЗадаватьВопрос Тогда
		
		ТекстВопроса = НСтр("ru = 'Данные документа будут перезаполнены. Продолжить?';
							|en = 'Данные документа будут перезаполнены. Продолжить?'");
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ВопросОПерезаполнениииПоОснованиюПриЗавершении", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ПерезаполнитьПоОснованиюСервер();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОПерезаполнениииПоОснованиюПриЗавершении(Результат, Параметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ПерезаполнитьПоОснованиюСервер();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьПоОснованиюСервер()
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	ТекущийОбъект.Заполнить(Объект.ДокументОснование);
	
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	
	ПриСозданииЧтенииНаСервере();
	ИнтеграцияИСПереопределяемый.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область Статус

&НаСервере
Функция ОбновитьСтатусСАТУРН()
	
	Результат = Новый Структура;
	Результат.Вставить("СтатусыПоСтрокам", Новый Соответствие());
	
	МенеджерОбъекта = ОбщегоНазначенияИС.МенеджерОбъектаПоСсылке(Объект.Ссылка);
	
	ПараметрыОбъекта = Новый Структура("ОбъектРасчета, Операция", Объект.Ссылка, Объект.Операция);
	СтатусСАТУРН = МенеджерОбъекта.СтатусПоУмолчанию(ПараметрыОбъекта);
	
	ДальнейшееДействие = МенеджерОбъекта.ДальнейшееДействиеПоУмолчанию(ПараметрыОбъекта);
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Статусы.Статус              КАК Статус,
		|	Статусы.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ВЫБОР
		|		КОГДА Статусы.ДальнейшееДействие1 В (&МассивДальнейшиеДействия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюСАТУРН.НеТребуется)
		|		ИНАЧЕ Статусы.ДальнейшееДействие1
		|	КОНЕЦ КАК ДальнейшееДействие1,
		|	ВЫБОР
		|		КОГДА Статусы.ДальнейшееДействие2 В (&МассивДальнейшиеДействия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюСАТУРН.НеТребуется)
		|		ИНАЧЕ Статусы.ДальнейшееДействие2
		|	КОНЕЦ КАК ДальнейшееДействие2,
		|	ВЫБОР
		|		КОГДА Статусы.ДальнейшееДействие3 В (&МассивДальнейшиеДействия)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ДальнейшиеДействияПоВзаимодействиюСАТУРН.НеТребуется)
		|		ИНАЧЕ Статусы.ДальнейшееДействие3
		|	КОНЕЦ КАК ДальнейшееДействие3
		|ИЗ
		|	РегистрСведений.СтатусыДокументовСАТУРН КАК Статусы
		|ГДЕ
		|	Статусы.Документ = &Документ");
		
		Запрос.УстановитьПараметр("Документ",                 Объект.Ссылка);
		Запрос.УстановитьПараметр("МассивДальнейшиеДействия", ИнтеграцияСАТУРН.НеотображаемыеВДокументахДальнейшиеДействия());
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если ЗначениеЗаполнено(Выборка.ИдентификаторСтроки) Тогда
				
				ДанныеПоСтроке = Новый Структура;
				ДанныеПоСтроке.Вставить("СтатусСАТУРН", Выборка.Статус);
				Результат.СтатусыПоСтрокам.Вставить(Выборка.ИдентификаторСтроки, ДанныеПоСтроке);
				
			Иначе
			
				СтатусСАТУРН = Выборка.Статус;
				
				ДальнейшееДействие = Новый Массив;
				ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие1);
				ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие2);
				ДальнейшееДействие.Добавить(Выборка.ДальнейшееДействие3);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

	ДопустимыеДействия = Новый Массив;
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитьВвоз);
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПодтвердитеИмпорт);
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитеПередачуДанных);
	ДопустимыеДействия.Добавить(Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОформитеОтгрузкуИмпортаИзЕЭАС);
	
	СтатусПредставление = ИнтеграцияСАТУРН.ПредставлениеСтатуса(СтатусСАТУРН, ДальнейшееДействие, ДопустимыеДействия);
	
	РедактированиеФормыНеДоступно = СтатусСАТУРН <> Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ввезено
	                              И СтатусСАТУРН <> Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Ошибка
	                              И СтатусСАТУРН <> Перечисления.СтатусыОбработкиИмпортаПродукцииСАТУРН.Черновик;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура СтатусПредставлениеОбработкаНавигационнойСсылкиЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	ПроверятьОбъектПроведен = Истина;
	Если ДействияНавигационнойСсылкиБезПроверкиПроведения().Найти(ДополнительныеПараметры.НавигационнаяСсылкаФорматированнойСтроки) <> Неопределено Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись));
		РазблокироватьДанныеФормыДляРедактирования();
		ПроверятьОбъектПроведен = Ложь;
	ИначеЕсли ПроверитьЗаполнение() Тогда
		Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
		РазблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;

	Если Не Модифицированность И (Не ПроверятьОбъектПроведен Или Объект.Проведен) Тогда
		ОбработатьНажатиеНавигационнойСсылки(ДополнительныеПараметры.НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНажатиеНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПередатьДанные" Тогда
		
		ПараметрыОбработкиДокументов = ИнтеграцияСАТУРНСлужебныйКлиентСервер.ПараметрыОбработкиДокументов();
		ПараметрыОбработкиДокументов.Ссылка             = Объект.Ссылка;
		ПараметрыОбработкиДокументов.ОрганизацияСАТУРН  = Объект.ОрганизацияСАТУРН;
		ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПередайтеДанные");
		
		ОписаниеПриЗавершении = Новый ОписаниеОповещения(
			"Подключаемый_ПриЗавершенииОперации", ЭтотОбъект, ПараметрыОбработкиДокументов);
		
		ИнтеграцияСАТУРНКлиент.ПодготовитьКПередаче(
			ЭтотОбъект,
			ПараметрыОбработкиДокументов,
			ОписаниеПриЗавершении);
			
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПодтвердитьИмпорт" Тогда
		
		ПараметрыОбработкиДокументов = ИнтеграцияСАТУРНСлужебныйКлиентСервер.ПараметрыОбработкиДокументов();
		ПараметрыОбработкиДокументов.Ссылка             = Объект.Ссылка;
		ПараметрыОбработкиДокументов.ОрганизацияСАТУРН  = Объект.ОрганизацияСАТУРН;
		ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПодтвердитеИмпорт");
		
		ОписаниеПриЗавершении = Новый ОписаниеОповещения(
			"Подключаемый_ПриЗавершенииОперации", ЭтотОбъект, ПараметрыОбработкиДокументов);
		
		ИнтеграцияСАТУРНКлиент.ПодготовитьКПередаче(
			ЭтотОбъект,
			ПараметрыОбработкиДокументов,
			ОписаниеПриЗавершении);
			
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОтменитьИмпорт" Тогда
		
		ПараметрыОбработкиДокументов = ИнтеграцияСАТУРНСлужебныйКлиентСервер.ПараметрыОбработкиДокументов();
		ПараметрыОбработкиДокументов.Ссылка             = Объект.Ссылка;
		ПараметрыОбработкиДокументов.ОрганизацияСАТУРН  = Объект.ОрганизацияСАТУРН;
		ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ОтменитьВвоз");
		
		ОписаниеПриЗавершении = Новый ОписаниеОповещения(
			"Подключаемый_ПриЗавершенииОперации", ЭтотОбъект, ПараметрыОбработкиДокументов);
		
		ИнтеграцияСАТУРНКлиент.ПодготовитьКПередаче(
			ЭтотОбъект,
			ПараметрыОбработкиДокументов,
			ОписаниеПриЗавершении);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ЗагрузитьПартииИмпорта" Тогда
		
		ПараметрыОбработкиДокументов = ИнтеграцияСАТУРНСлужебныйКлиентСервер.ПараметрыОбработкиДокументов();
		ПараметрыОбработкиДокументов.Ссылка             = Объект.Ссылка;
		ПараметрыОбработкиДокументов.ОрганизацияСАТУРН  = Объект.ОрганизацияСАТУРН;
		ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеПартииИмпорта");
		
		ОписаниеПриЗавершении = Новый ОписаниеОповещения(
			"Подключаемый_ПриЗавершенииОперации", ЭтотОбъект, ПараметрыОбработкиДокументов);
		
		ИнтеграцияСАТУРНКлиент.ПодготовитьКПередаче(
			ЭтотОбъект,
			ПараметрыОбработкиДокументов,
			ОписаниеПриЗавершении);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОтменитьОперацию" Тогда
		
		ИнтеграцияСАТУРНКлиент.ОтменитьПоследнююОперацию(Объект.Ссылка);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОтменитьПередачуДанных" Тогда
		
		ИнтеграцияСАТУРНКлиент.ОтменитьПередачу(Объект.Ссылка);
	
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПоказатьПричинуОшибки" Тогда
		
		ПараметрыОткрытияФормы = Новый Структура;
		ПараметрыОткрытияФормы.Вставить("Документ", Объект.Ссылка);
		
		ОткрытьФорму(
			"Справочник.САТУРНПрисоединенныеФайлы.Форма.ФормаОшибки",
			ПараметрыОткрытияФормы,
			ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокДокумента(Форма)
	
	Объект   = Форма.Объект;
	НовыйДокумент = Не ЗначениеЗаполнено(Объект.Ссылка);
	
	Форма.Заголовок = СформироватьЗаголовокДокументаПоПараметрам(НовыйДокумент, Объект.Операция, Объект.Номер, Объект.Дата);
	Форма.АвтоЗаголовок = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьЗаголовокДокументаПоПараметрам(НовыйДокумент, Операция, Номер, Дата)
	
	Заголовок = Документы.ИмпортПродукцииСАТУРН.ПредставлениеДокументаИмпорта(НовыйДокумент, Операция, Номер, Дата);
	Возврат Заголовок;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьГиперссылкиРеквизитов()
	
	ПараметрыПолученияСопоставлений = Новый Массив;
	
	ПараметрПредставления = ИнтеграцияСАТУРН.НовыеПараметрыФормированияПредставленияСопоставлений();
	ПараметрПредставления.ОрганизацияСАТУРН                       = Объект.ОрганизацияСАТУРН;
	ПараметрПредставления.ОрганизацияСАТУРНОбязательноеЗаполнение = Ложь;
	ПараметрПредставления.ОрганизацияКонтрагент                   = ОрганизацияИзОснованияДляОтбораОрганизацииСАТУРН;

	ПараметрПредставления.МестоХранения                       = Объект.МестоХранения;
	ПараметрПредставления.МестоХраненияОбязательноеЗаполнение = Ложь;
	ПараметрПредставления.Склад                               = СкладИзОснованияДляОтбораМестаХранения;
	
	ПараметрПредставления.Грузоотправитель                             = Объект.ГрузоотправительОрганизацияСАТУРН;
	ПараметрПредставления.ГрузоотправительСАТУРНОбязательноеЗаполнение = Ложь;
	ПараметрПредставления.Контрагент                                   = ГрузоотправительИзОснованияДляОтбораОрганизацииСАТУРН;
	
	ПараметрыПолученияСопоставлений.Добавить(ПараметрПредставления);
	
	СобытияФормСАТУРН.ЗаполнитьГиперссылкиРеквизитов(ЭтотОбъект, ПараметрыПолученияСопоставлений);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначенияРеквизитовТранспортногоСредства(ТранспортноеСредство)
	
	РеквизитыТранспортногоСредства = ИнтеграцияСАТУРН.ИнициализироватьСтруктуруРеквизитовТранспортногоСредства();
	ИнтеграцияСАТУРНПереопределяемый.ПриОпределенииРеквизитовТранспортногоСредства(РеквизитыТранспортногоСредства, ТранспортноеСредство);
	Возврат РеквизитыТранспортногоСредства;
	
КонецФункции

#Область Товары

&НаКлиенте
Процедура ПриИзмененииТипаИзмеряемойВеличины()
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СобытияФормСАТУРНКлиент.ПриИзмененииТипаИзмеряемойВеличины(
		ЭтотОбъект, ТекущиеДанные, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеНовойПартииОкончаниеВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, Результат);
	
	СформироватьПредставлениеДанныхНовойПартии(ЭтотОбъект, ТекущиеДанные);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьДанныеНовойПартии(Форма, ТекущиеДанные)
	
	ТекущиеДанные.НомерПартии          = "";
	ТекущиеДанные.ТорговоеНаименование = "";
	ТекущиеДанные.АртикулПроизводителя = "";
	ТекущиеДанные.ДатаПроизводства     = Дата(1, 1, 1);
	ТекущиеДанные.СрокГодности         = Дата(1, 1, 1);
	
	СформироватьПредставлениеДанныхНовойПартии(Форма, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьПредставлениеДанныхНовойПартии(Форма, Данные)
	
	Если Данные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Данные) = Тип("ДанныеФормыКоллекция") Тогда
		ДанныеДляОбработки = Данные;
	Иначе
		ДанныеДляОбработки = Новый Массив;
		ДанныеДляОбработки.Добавить(Данные);
	КонецЕсли;
	
	Для Каждого Строка Из ДанныеДляОбработки Цикл
		Если Не ЗначениеЗаполнено(Строка.НомерПартии) Тогда
			Если ЗначениеЗаполнено(Строка.ДатаПроизводства) Тогда
				Строка.ДатаПроизводства = "";
			КонецЕсли;
		Иначе
			Строка.ДатаПроизводства = Форма.Объект.Дата;
		КонецЕсли;
	КонецЦикла;
	
	ИнтеграцияСАТУРНКлиентСервер.СформироватьПредставлениеДанныхНовойПартии(Данные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКоличестваВУпаковкеСАТУРН(ТекущиеДанные)
	
	Если ТекущиеДанные.КоличествоВУпаковкеСАТУРН > 0 Тогда
		ТекущиеДанные.КоличествоУпаковок = ТекущиеДанные.КоличествоСАТУРН / ТекущиеДанные.КоличествоВУпаковкеСАТУРН;
	Иначе
		ТекущиеДанные.КоличествоУпаковок = 0;
	КонецЕсли;
	
	СобытияФормСАТУРНКлиент.ПриИзмененииКоличестваУпаковок(
		ЭтотОбъект, ТекущиеДанные, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКоличестваУпаковок(ТекущиеДанные)
	
	Если Объект.Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийСАТУРН.ИмпортПродукцииИзЕАЭС")
		Или Объект.Операция = ПредопределенноеЗначение("Перечисление.ВидыОперацийСАТУРН.ИмпортПродукцииПредварительноеУведомление") Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.КоличествоУпаковок > 0 Тогда
		ТекущиеДанные.КоличествоВУпаковкеСАТУРН = ТекущиеДанные.КоличествоСАТУРН / ТекущиеДанные.КоличествоУпаковок;
	Иначе
		ТекущиеДанные.КоличествоВУпаковкеСАТУРН = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСпискиВыбораНоменклатуры(ТекущаяСтрока)
	
	СписокВыбораНоменклатура = Элементы.ТоварыНоменклатура.СписокВыбора;
	СписокВыбораНоменклатура.Очистить();
	
	НайденныеСтроки = НоменклатураДляВыбора.НайтиСтроки(Новый Структура("ПАТ", ТекущаяСтрока.ПАТ));
	НоменклатураКэш = Неопределено;
	Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
		Если СтрокаТЧ.Номенклатура <> НоменклатураКэш Тогда
			СписокВыбораНоменклатура.Добавить(СтрокаТЧ.Номенклатура);
			НоменклатураКэш = СтрокаТЧ.Номенклатура;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСпискиВыбораХарактеристика(ТекущаяСтрока)
	
	СписокВыбораХарактеристика = Элементы.ТоварыХарактеристика.СписокВыбора;
	СписокВыбораХарактеристика.Очистить();
	
	НайденныеСтроки = НоменклатураДляВыбора.НайтиСтроки(Новый Структура("ПАТ, Номенклатура",
		ТекущаяСтрока.ПАТ,
		ТекущаяСтрока.Номенклатура));
	Для Каждого СтрокаТЧ Из НайденныеСтроки Цикл
		СписокВыбораХарактеристика.Добавить(СтрокаТЧ.Характеристика);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореНоменклатуры(Номенклатура, ДополнительныеПараметры) Экспорт
	
	Если Номенклатура = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.Номенклатура = Номенклатура;
	
	НоменклатураПриИзменении(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(ТекущиеДанные = Неопределено)
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	КонецЕсли;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СобытияФормСАТУРНКлиентПереопределяемый.ПриИзмененииНоменклатуры(
		ЭтотОбъект, ТекущиеДанные, КэшированныеЗначения, ПараметрыУказанияСерий);
	
	ПриИзмененииКоличестваВУпаковкеСАТУРН(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьОтборыПоОснованию(ДокументОснование, Форма, НастроитьЗависимыеЭлементыФормы = Истина)
	
	ОтборыПоОснованию = Неопределено;
	Если ДокументОснование <> Неопределено Тогда 
		ОтборыПоОснованию = ИнтеграцияСАТУРНВызовСервера.ОтборыДляРеквизитовДокументаПоОснованию(Тип("ДокументСсылка.ИмпортПродукцииСАТУРН"), ДокументОснование);
	КонецЕсли;
	
	Если ОтборыПоОснованию <> Неопределено Тогда
		Форма.ОрганизацияИзОснованияДляОтбораОрганизацииСАТУРН      = ОтборыПоОснованию.ОрганизацияСАТУРН;
		Форма.СкладИзОснованияДляОтбораМестаХранения                = ОтборыПоОснованию.МестоХранения;
		Форма.ГрузоотправительИзОснованияДляОтбораОрганизацииСАТУРН = ОтборыПоОснованию.Грузоотправитель;
	Иначе 
		Форма.ОрганизацияИзОснованияДляОтбораОрганизацииСАТУРН      = Неопределено;
		Форма.СкладИзОснованияДляОтбораМестаХранения                = Неопределено;
		Форма.ГрузоотправительИзОснованияДляОтбораОрганизацииСАТУРН = Неопределено;
	КонецЕсли;
	
	Если НастроитьЗависимыеЭлементыФормы Тогда
		НастроитьЗависимыеЭлементыФормы(Форма, "ДокументОснование, Операция");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ДействияНавигационнойСсылкиБезПроверкиПроведения()
	
	Результат = Новый Массив;
	Результат.Добавить("ПоказатьПричинуОшибки");
	Результат.Добавить("ОтменитьОперацию");
	Результат.Добавить("ОтменитьПередачуДанных");
	Результат.Добавить("ОтменитьИмпорт");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти