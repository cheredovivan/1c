#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ПрочитатьНастройки();
		ЗначенияДляЗаполнения = Новый Структура("Организация, Ответственный, ДатаСобытия", 
			"Объект.Организация", "Объект.Ответственный", "Объект.ДатаПриема");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтотОбъект, ЗначенияДляЗаполнения);
		Если ИспользоватьАдаптациюУвольнение И ИспользоватьШтатноеРасписание И ЗначениеЗаполнено(Объект.ДолжностьПоШтатномуРасписанию) Тогда
			ЗаполнитьМероприятияНаСервере();
		КонецЕсли;
		ПриПолученииДанных();
	КонецЕсли;
	
	УстановитьУсловноеОформление(УсловноеОформление);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ДляРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ДляРазмещения.КоманднаяПанель = Элементы.КомандыФормыПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ДляРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	ДополнительныеПараметры.Вставить("ИмяЭлементаКоманднойПанели", "КомандыФормыПанель");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	КонецЕсли;
	// Конец ПроцессыОбработкиДокументов
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	// СтандартныеПодсистемы.РаботаСФайлами
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайлами = ОбщегоНазначения.ОбщийМодуль("РаботаСФайлами");
		ГиперссылкаФайлов = МодульРаботаСФайлами.ГиперссылкаФайлов();
		ГиперссылкаФайлов.Размещение = Элементы.КомандыФормыПанель.Имя;
		МодульРаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ГиперссылкаФайлов);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.РаботаСФайлами
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайламиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСФайламиКлиент");
		МодульРаботаСФайламиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.РаботаСФайлами
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайламиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСФайламиКлиент");
		МодульРаботаСФайламиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	Если ИмяСобытия = "Запись_ПриемНаРаботу" Тогда
		ОбновитьДанныеФормы();
	ИначеЕсли ИмяСобытия = "ДанныеДляОформленияНаРаботуЗаписанДокумент" И Источник = ЭтотОбъект  Тогда
		ОбновитьДанныеДляОформленияНаРаботу();
	КонецЕсли;
	
	ОбработатьОповещениеРабочегоМеста(ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПрочитатьНастройки();
	ПриПолученииДанных(ТекущийОбъект);
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Если СвойствоСтруктуры(ПараметрыЗаписи, "ИскатьПринятогоСотрудника", Истина) 
			И Не ЕстьСвязанныйСРешениемСотрудник() Тогда
			Отказ = Истина;
			НачатьПоискСотрудника(ПараметрыЗаписи);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОповеститьКандидата = ПрочитанныйКандидат;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи, Отказ);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность = ОбщегоНазначения.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональность");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(
			ЭтотОбъект,
			ТекущийОбъект,
			ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	Если ПараметрыЗаписи.Свойство("ГруппаДоступаФизическогоЛица") Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("ГруппаДоступа",
			ПараметрыЗаписи.ГруппаДоступаФизическогоЛица);
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("СведенияОСотруднике") Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("СведенияОСотруднике", ПараметрыЗаписи.СведенияОСотруднике);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	КадровыеРешенияФормы.ФормаДокументаПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	СинхронизацияДанныхЗарплатаКадры.ЗапуститьОтложеннуюОбработкуЗаполненияДанныхПоФизическимЛицам(ТекущийОбъект);
	
	ПриПолученииДанных(ТекущийОбъект);
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.ПередЗакрытием(ЭтотОбъект, Объект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если Не Объект.Внешний И ИспользоватьПодборПерсонала Тогда
		ПараметрыОповещения = Новый Структура();
		ПараметрыОповещения.Вставить("Кандидат", Объект.Кандидат);
		ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(Объект.Ссылка, ПараметрыОповещения);
		Если ЗначениеЗаполнено(ОповеститьКандидата) И ОповеститьКандидата <> Объект.Кандидат Тогда
			ПараметрыОповещения.Кандидат = ОповеститьКандидата;
			ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(Объект.Ссылка, ПараметрыОповещения);
		КонецЕсли;
	КонецЕсли;
	
	Пока ПослеЗаписи.Количество() > 0 Цикл
		ИмяОбработчика = ПослеЗаписи.Получить(0).Значение;
		ПослеЗаписи.Удалить(0);
		ВыполнитьОбработкуОповещения(Новый ОписаниеОповещения(ИмяОбработчика, ЭтотОбъект));
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Объект.МероприятияАдаптацииУвольнения.Очистить();
	Объект.Подразделение = Неопределено;
	УстановитьВидимостьОплатыТруда();
	ЗаполнитьДанныеПоПозицииНаКлиенте();
	УстановитьФункциональныеОпцииФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура КандидатПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Кандидат) Тогда
		Объект.ФизическоеЛицо = НайтиФизическоеЛицо(Объект.Ссылка, Объект.Кандидат);
		Объект.ЛичныеДанныеКандидата = ЛичныеДанныеКандидата(Объект.Кандидат);
	Иначе
		Объект.ФизическоеЛицо = Неопределено;
		Объект.ЛичныеДанныеКандидата = Неопределено;
	КонецЕсли;
	
	ЗаполнитьМероприятияНаКлиенте();
	ЗаполнитьДанныеПоКандидатуНаКлиенте();
	
	УстановитьПредупреждениеОДругомРешении(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КандидатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ТекущийДокумент", Объект.Ссылка);
	ПараметрыОткрытияФормы.Вставить("Организация", Объект.Организация);
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КадровыеРешения") Тогда
		МодульКадровыеРешенияКлиентРасширенный = ОбщегоНазначенияКлиент.ОбщийМодуль("КадровыеРешенияКлиентРасширенный");
		МодульКадровыеРешенияКлиентРасширенный.ОткрытьФормуВыбораКандидатаРешенияОПриемеНаРаботу(ПараметрыОткрытияФормы, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриемаПриИзменении(Элемент)
	
	НачатьПерезаполнениеМероприятий();
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьПоШтатномуРасписаниюПриИзменении(Элемент)
	
	ЗаполнитьМероприятияНаКлиенте();
	ЗаполнитьДанныеПоПозицииНаКлиенте();
	УстановитьВидимостьОплатыТруда();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапланироватьПрограммуОбученияПриИзменении(Элемент)
	УстановитьДоступностьПрограммаОбучения(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура НоваяПозицияПриИзменении(Элемент)
	
	УстановитьВидимостьДолжности(ЭтаФорма);
	УстановитьВидимостьРабочегоМеста();
	УстановитьВидимостьОплатыТруда();
	ЗаполнитьМероприятияНаКлиенте();
	ЗаполнитьДанныеПоПозицииНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьПриИзменении(Элемент)
	ЗаполнитьМероприятияНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	ЗаполнитьМероприятияНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ПрофильДолжностиПриИзменении(Элемент)
	
	ЗаполнитьИспытательныйСрокНаКлиенте();
	
	Если Объект.ПрограммаОбучения.Количество() = 0 Тогда
		ЗаполнитьПрограммуОбученияИзПрофиля();
	Иначе
		ПоказатьВопросЗаполненияПрограммыОбученияНачало();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспытательныйСрокПриИзменении(Элемент)
	УстановитьСклонениеИспытательногоСрока(Объект.ИспытательныйСрок, Элементы.ИспытательныйСрок);
	УстановитьВидимостьПредупреждения(Объект.ИспытательныйСрок, Элементы.ИспытательныйСрокПредупреждение);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования,
		ЭтотОбъект,
		"Объект.Комментарий");
	
КонецПроцедуры

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраНажатие(Элемент, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайламиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСФайламиКлиент");
		МодульРаботаСФайламиКлиент.ПолеПредпросмотраНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайламиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСФайламиКлиент");
		МодульРаботаСФайламиКлиент.ПолеПредпросмотраПроверкаПеретаскивания(ЭтотОбъект, Элемент,
			ПараметрыПеретаскивания, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайламиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСФайламиКлиент");
		МодульРаботаСФайламиКлиент.ПолеПредпросмотраПеретаскивание(ЭтотОбъект, Элемент,
			ПараметрыПеретаскивания, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборФизическихЛицПриИзменении(Элемент)
	УстановитьВидимостьПоляВыбораКандидата(ЭтаФорма);
	УстановитьДоступностьДанныхДляОформленияНаРаботу();
КонецПроцедуры

&НаКлиенте
Процедура ФизическоеЛицоПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Кандидат) Тогда
		Объект.Кандидат = Неопределено;
		Объект.ЛичныеДанныеКандидата = Неопределено;
	КонецЕсли;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНазначаемыеМероприятия

&НаКлиенте
Процедура НазначаемыеМероприятияМероприятиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнениеКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("АдаптацияУвольнениеКлиент");
		МодульАдаптацияУвольнениеКлиент.НазначаемыеМероприятияМероприятиеНачалоВыбора(ЭтотОбъект, Элемент, СтандартнаяОбработка, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НазначаемыеМероприятияМероприятиеОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура НазначаемыеМероприятияМероприятиеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнениеКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("АдаптацияУвольнениеКлиент");
		ДопПараметры = ПараметрыПолученияСвойствНазначаемогоМероприятия();
		МодульАдаптацияУвольнениеКлиент.НазначаемыеМероприятияМероприятиеОбработкаВыбора(ЭтотОбъект, Элемент, ВыбранноеЗначение, ДопПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НазначаемыеМероприятияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НазначаемыеМероприятия.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Мероприятие) И Не Объект.Ссылка.Пустая() Тогда
		РезультатыМероприятия = РезультатыМероприятия(Объект.Ссылка, ТекущиеДанные.Мероприятие);
		Если РезультатыМероприятия <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(ТекущиеДанные, РезультатыМероприятия);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НазначаемыеМероприятияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле = Элементы.НазначаемыеМероприятияРезультатВыполнения
		И ЗначениеЗаполнено(ТекущиеДанные.ЗаданиеАдаптацииУвольнения) Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекущиеДанные.ЗаданиеАдаптацииУвольнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПрограммаОбучения

&НаКлиенте
Процедура ПрограммаОбученияМероприятиеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ПрограммаОбучения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗависимыеОтМероприятияОбученияРеквизиты(ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами") Тогда
		МодульРаботаСФайламиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСФайламиКлиент");
		МодульРаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);
	КонецЕсли;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

#Область ОбработчикиСобытийПроцессыОбработкиДокументов

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокумента(Команда)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.ВыполнитьЗадачу(ЭтотОбъект, Команда, Объект)
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьЗадачуПоОбработкеДокументаОповещение(Контекст, ДополнительныеПараметры) Экспорт
	ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры, Контекст);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗадачуПоОбработкеДокументаНаСервере(Контекст)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ВыполнитьЗадачу(ЭтотОбъект, Контекст, Объект);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийНаправившегоОткрытие(Элемент, СтандартнаяОбработка)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.КомментарийНаправившегоОткрытие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийСледующемуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда	
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначенияКлиент.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплатаКлиент");
		МодульПроцессыОбработкиДокументовЗарплата.КомментарийСледующемуНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	КонецЕсли;		
	// Конец ПроцессыОбработкиДокументов
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ЗаполнитьМероприятия(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьМероприятияПослеПроверкиСпискаМероприятий", ЭтотОбъект);
	ЗарплатаКадрыРасширенныйКлиент.ПроверитьВозможностьОчисткиТабличныхЧастей(ЭтотОбъект, "МероприятияАдаптацииУвольнения", Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПринятьНаРаботу(Команда)
	НачатьПриемНаРаботу();
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент");
		МодульИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
			Команда,
			ЭтотОбъект,
			Объект);
	КонецЕсли;
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Провести(Команда)
	Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
КонецПроцедуры

&НаКлиенте
Процедура ПровестиЗакрыть(Команда)
	ПослеЗаписи.Добавить("ЗакрытьФормуПослеЗаписи");
	Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриПолученииДанных(ТекущийОбъект = Неопределено)
	
	Если ТекущийОбъект = Неопределено Тогда
		ТекущийОбъект = Объект;
	КонецЕсли;
	
	ОбновитьДанныеФормы();
	
	ПрочитанныйКандидат = ТекущийОбъект.Кандидат;
	УстановитьПредупреждениеОДругомРешении(ЭтотОбъект);
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура УстановитьДоступностьЭлементов()
	
	ПриказОформлен = ЗначениеЗаполнено(КадровыйПриказИнфоНадпись);
	Элементы.ПринятьНаРаботу.Доступность = (Не ПриказОформлен И ПравоДоступа("Добавление",
		Метаданные.Документы.ПриемНаРаботу));
	
	Если ПриказОформлен Тогда
		Элементы.ГруппаОрганизацияДокумент.Доступность = Ложь;
		Элементы.ГруппаПринятНаРаботу.Доступность = Ложь;
		Элементы.ДатаПриема.Доступность = Ложь;
		Элементы.ГлавноеГруппа.Доступность = Ложь;
		Элементы.ГруппаСборДанных.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияОффер", "Видимость", Объект.Внешний);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "НоваяПозиция", "Видимость", Не Объект.Внешний И ИспользоватьШтатноеРасписание);
	
	Если Объект.Внешний Тогда
		
		ТекстОписания = НСтр("ru = 'Документ создан на основании оффера, полученного из приложения 1С:Персонал';
							|en = 'The document is created based on the offer received from 1C:Human Resources'");
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияОффер", "Заголовок", ТекстОписания);
		
		ВыборФизическихЛиц = Истина;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВыборФизическихЛиц", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрофильДолжности", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтраницыФормы", "ОтображениеСтраниц", ОтображениеСтраницФормы.Нет);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаИспытательныйСрок", "Видимость", Не Объект.Внешний);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОплатаТруда", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДистанционнаяРабота", "Видимость", Не Объект.Внешний);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СогласованныйДоход", "Видимость", Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Должность", "Видимость", Не ИспользоватьШтатноеРасписание);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДолжностьПоШтатномуРасписанию", "Видимость", ИспользоватьШтатноеРасписание);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "РабочееМесто", "Видимость", ИспользоватьШтатноеРасписание И РабочиеМестаОхраныТрудаИспользуются);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Подразделение", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодразделениеОрганизации", "Видимость", Истина);
		
		// Блокируем реквизиты полученные при загрузке из оффера.
		ИменаЭлементов = "Организация,ФизическоеЛицо,Подразделение,ПодразделениеОрганизации,ДолжностьПоШтатномуРасписанию,Должность,ДатаПриема";
		ИменаБлокируемыхЭлементов = СтрРазделить(ИменаЭлементов, ",");
		Для каждого ИмяЭлемента Из ИменаБлокируемыхЭлементов Цикл
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, ИмяЭлемента, "ТолькоПросмотр", Истина);
		КонецЦикла;
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Подразделение", "Видимость", ИспользоватьПодборПерсонала);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПодразделениеОрганизации", "Видимость", Не ИспользоватьПодборПерсонала);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЗадачиАдаптацииСтраница", "Видимость", ИспользоватьАдаптациюУвольнение);
		Если Не ИспользоватьОбучениеРазвитиеСотрудников И Не ИспользоватьАдаптациюУвольнение Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтраницыФормы", "ОтображениеСтраниц", ОтображениеСтраницФормы.Нет);
		КонецЕсли;
		
		ПрофилиДолжностейДоступны = Ложь;
		Если ИспользоватьПодборПерсонала И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
			МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
			ПрофилиДолжностейДоступны = МодульПодборПерсонала.ЕстьДоступКПрофилямДолжностей("Чтение")
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПрофильДолжности", "Видимость", ПрофилиДолжностейДоступны);
		
		Если Не ИспользоватьПодборПерсонала Тогда
			ВыборФизическихЛиц = Истина;
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВыборФизическихЛиц", "Видимость", Ложь);
		ИначеЕсли Не ЗначениеЗаполнено(Объект.Кандидат) И ЗначениеЗаполнено(Объект.ФизическоеЛицо) Тогда
			ВыборФизическихЛиц = Истина;
		Иначе
			УстановитьВидимостьКандидата(ЭтотОбъект);
		КонецЕсли;
		
		УстановитьДоступностьПрограммаОбучения(ЭтотОбъект);
		УстановитьВидимостьОплатыТруда();
		УстановитьВидимостьДолжности(ЭтотОбъект);
		УстановитьВидимостьРабочегоМеста();
		УстановитьДоступностьРабочегоМеста(ЭтаФорма);
		
	КонецЕсли;
	
	УстановитьВидимостьПоляВыбораКандидата(ЭтаФорма);
	УстановитьСклонениеИспытательногоСрока(Объект.ИспытательныйСрок, Элементы.ИспытательныйСрок);
	УстановитьВидимостьПредупреждения(Объект.ИспытательныйСрок, Элементы.ИспытательныйСрокПредупреждение);
	УстановитьДоступностьДанныхДляОформленияНаРаботу();
	УстановитьФункциональныеОпцииФормы();
	
	Если ИспользоватьШтатноеРасписание И РабочиеМестаОхраныТрудаИспользуются Тогда
		ДанныеПозиции = ДанныеПозицииШтатногоРасписания(Объект.ДолжностьПоШтатномуРасписанию);
		СписокРабочихМест = СписокРабочихМестПоДаннымПозиции(ДанныеПозиции);
		Элементы.РабочееМесто.СписокВыбора.ЗагрузитьЗначения(СписокРабочихМест.ВыгрузитьЗначения());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПерезаполнениеМероприятий()
	
	Если Объект.МероприятияАдаптацииУвольнения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗаголовокВопроса = НСтр("ru = 'Задачи адаптации';
							|en = 'Adaptation tasks'");
	ТекстВопроса = НСтр("ru = 'Перезаполнить список мероприятий?';
						|en = 'Refill the list of activities?'");
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьПерезаполнениеМероприятий", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,, ЗаголовокВопроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПерезаполнениеМероприятий(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьМероприятияНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМероприятияНаКлиенте()
	
	Если Не ИспользоватьАдаптациюУвольнение Или Не ИспользоватьШтатноеРасписание Тогда
		Возврат;
	КонецЕсли;
	
	Объект.МероприятияАдаптацииУвольнения.Очистить();
	
	Если Не Объект.НоваяПозиция И Не ЗначениеЗаполнено(Объект.ДолжностьПоШтатномуРасписанию) Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.НоваяПозиция Тогда
		Если Не ЗначениеЗаполнено(Объект.Должность) И Не ЗначениеЗаполнено(Объект.Подразделение) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьМероприятияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМероприятияПослеПроверкиСпискаМероприятий(Отказ, ДополнительныеПараметры = Неопределено) Экспорт 
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьМероприятияНаКлиенте();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМероприятияНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.ФизическоеЛицо) Тогда
		Объект.ЗапланироватьПрограммуОбучения = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнениеФормы = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнениеФормы");
		ПараметрыЗаполнения = Документы.РешениеОПриемеНаРаботу.ПараметрыЗаполненияМероприятий(Объект, Объект.ФизическоеЛицо);
		МодульАдаптацияУвольнениеФормы.ЗаполнитьНазначаемыеМероприятия(ЭтотОбъект, ПараметрыЗаполнения);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОбучениеРазвитие") Тогда
		МодульОбучениеРазвитиеФормы = ОбщегоНазначения.ОбщийМодуль("ОбучениеРазвитиеФормы");
		МодульОбучениеРазвитиеФормы.ЗаполнитьПрограммуОбученияИзПозицииШР(Объект.ПрограммаОбучения,
			Объект.ДолжностьПоШтатномуРасписанию, Объект.ДатаПриема);
	КонецЕсли;
	
	Объект.ЗапланироватьПрограммуОбучения = Объект.ПрограммаОбучения.Количество() > 0;  
	УстановитьДоступностьПрограммаОбучения(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыПолученияСвойствНазначаемогоМероприятия()
	
	ПараметрыПолучения = Новый Структура;
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнениеКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("АдаптацияУвольнениеКлиент");
		ПараметрыПолучения = МодульАдаптацияУвольнениеКлиент.ПараметрыПолученияСвойствНазначаемогоМероприятия();
		ПараметрыПолучения.ФизическоеЛицо = Объект.ФизическоеЛицо;
		ПараметрыПолучения.ДатаСобытия = Объект.ДатаПриема;
		ПараметрыПолучения.Подразделение = Объект.Подразделение;
		ПараметрыПолучения.Позиция = Объект.ДолжностьПоШтатномуРасписанию;
		ПараметрыПолучения.Ответственный = Объект.Ответственный;
	КонецЕсли;
	
	Возврат ПараметрыПолучения;
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиСотрудникаПоКандидату(Кандидат)
	
	Сведения = КадровыеРешенияКлиентСервер.СведенияОСотруднике();
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		Сведения.Сотрудник = МодульПодборПерсонала.СотрудникКандидата(Кандидат);
	КонецЕсли;
	Сведения.ПриказОПриеме = ПриказОПриемеСотрудника(Сведения.Сотрудник);
	Возврат Сведения;
	
КонецФункции

&НаСервереБезКонтекста
Функция СведенияОСотрудникеСвязанномСРешением(Решение)
	
	Сведения = КадровыеРешенияКлиентСервер.СведенияОСотруднике();
	Сведения.Сотрудник = Документы.РешениеОПриемеНаРаботу.СотрудникПоРешениюОПриемеНаРаботу(Решение);
	Сведения.ПриказОПриеме = ПриказОПриемеСотрудника(Сведения.Сотрудник);
	Возврат Сведения;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПриказОПриемеСотрудника(Сотрудник)

	Если Сотрудник = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаПоРабочимМестам = КадровыйУчет.КадровыеДанныеСотрудников(Ложь, Сотрудник, "ПриказОПриеме");
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ТаблицаПоРабочимМестам.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат ?(ЗначениеЗаполнено(ТаблицаПоРабочимМестам[0].ПриказОПриеме), 
		ТаблицаПоРабочимМестам[0].ПриказОПриеме, Неопределено);
	
КонецФункции

&НаКлиенте
Процедура НачатьПриемНаРаботу()
	
	Если Не ЗначениеЗаполнено(Объект.ФизическоеЛицо) Тогда
		Объект.ФизическоеЛицо = НайтиФизическоеЛицо(Объект.Ссылка, Объект.Кандидат);
	КонецЕсли;
	
	Если Объект.Внешний Или Не ИспользоватьПодборПерсонала Тогда
		ТекстСообщения = НСтр("ru = 'Не указано физическое лицо. Прием на работу невозможен.';
								|en = 'The person is not specified. Hiring is impossible.'");
	Иначе
		ТекстСообщения = НСтр("ru = 'Не найдено связанное с кандидатом физическое лицо. Прием на работу невозможен.';
								|en = 'The person linked to the candidate is not found. Hiring is impossible.'");
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.Проверить(ЗначениеЗаполнено(Объект.ФизическоеЛицо), ТекстСообщения);
	
	Если Не Параметры.Ключ.Пустая() И Объект.Проведен И Не Модифицированность Тогда
		ЗавершитьПриемНаРаботу();
		Возврат;
	КонецЕсли;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ПродолжитьПриемНаРаботу", ЭтотОбъект), 
		НСтр("ru = 'Перед приемом на работу документ требуется провести. Продолжить?';
			|en = 'Post the document before hiring. Continue?'"), 
		РежимДиалогаВопрос.ДаНет, , , 
		НСтр("ru = 'Документ не проведен';
			|en = 'Document is not posted'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПриемНаРаботу(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;

	ПослеЗаписи.Добавить("ЗавершитьПриемНаРаботуПослеЗаписи");
	ПараметрыЗаписи = ПараметрыЗаписиДляПроведения();
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПриемНаРаботуПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт // АПК:78 используется как обработчик оповещения после записи
	ЗавершитьПриемНаРаботу();
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт // АПК:78 используется как обработчик оповещения после записи

	Если Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыЗаписиДляПроведения()
	ПараметрыЗаписи = Новый Структура();
	ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	ПараметрыЗаписи.Вставить("ИскатьПринятогоСотрудника", Истина);
	Возврат ПараметрыЗаписи;
КонецФункции

&НаКлиенте
Функция ЕстьСвязанныйСРешениемСотрудник()
	
	Если Объект.Кандидат <> ПрочитанныйКандидат Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Не Параметры.Ключ.Пустая() И СвязанныйСРешениемСотрудник(Объект.Ссылка) <> Неопределено;
	
КонецФункции

&НаСервереБезКонтекста
Функция СвязанныйСРешениемСотрудник(Решение)
	УстановитьПривилегированныйРежим(Истина);
	Возврат Документы.РешениеОПриемеНаРаботу.СотрудникПоРешениюОПриемеНаРаботу(Решение);
КонецФункции

&НаКлиенте
Процедура НачатьПоискСотрудника(ПараметрыЗаписи)
	
	Если Не ЗначениеЗаполнено(Объект.Кандидат) Тогда
		ПараметрыЗаписи.Вставить("ИскатьПринятогоСотрудника", Ложь);
		Записать(ПараметрыЗаписи);
		Возврат;
	КонецЕсли;
	
	Сведения = НайтиСотрудникаПоКандидату(Объект.Кандидат);
	
	ПараметрыЗаписи.Вставить("ИскатьПринятогоСотрудника", Ложь);
	
	Если Не ЗначениеЗаполнено(Сведения.Сотрудник) Тогда
		Записать(ПараметрыЗаписи);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Сведения.ПриказОПриеме) Тогда
		ТекстВопроса = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
			НСтр("ru = 'Найден(а) <a href=""%1"">%2</a>';
				|en = '<a href=""%1"">%2</a> is found'"), 
			ПолучитьНавигационнуюСсылку(Сведения.Сотрудник),
			Строка(Сведения.Сотрудник));
	Иначе
		ТекстВопроса = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
			НСтр("ru = 'Найден(а) <a href=""%1"">%2</a>, оформленный(ая) приказом <a href=""%3"">%4</a>';
				|en = '<a href=""%1"">%2</a> is found, registered by the <a href=""%3"">%4</a> order'"), 
			ПолучитьНавигационнуюСсылку(Сведения.Сотрудник),
			Строка(Сведения.Сотрудник),
			ПолучитьНавигационнуюСсылку(Сведения.ПриказОПриеме),
			Строка(Сведения.ПриказОПриеме));
	КонецЕсли;
	
	КнопкиОтвета = Новый СписокЗначений;
	КнопкиОтвета.Добавить(КодВозвратаДиалога.Прервать, НСтр("ru = 'Связать найденного сотрудника с этим документом';
															|en = 'Link the found employee to this document'"));
	КнопкиОтвета.Добавить(КодВозвратаДиалога.Повторить, НСтр("ru = 'Принять на работу еще одного сотрудника';
															|en = 'Hire one more employee'"));
	КнопкиОтвета.Добавить(КодВозвратаДиалога.Отмена);
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ПараметрыЗаписи", ПараметрыЗаписи);
	ДополнительныеПараметры.Вставить("СведенияОСотруднике", Сведения);
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ПродолжитьПослеВопросаОНайденномСотруднике", ЭтотОбъект, ДополнительныеПараметры), 
		ТекстВопроса, КнопкиОтвета, , , НСтр("ru = 'Найден похожий сотрудник';
											|en = 'A similar employee is found'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПослеВопросаОНайденномСотруднике(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = Неопределено Или Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи = ДополнительныеПараметры.ПараметрыЗаписи;

	Если Ответ = КодВозвратаДиалога.Прервать Тогда
		// Пробросить в модуль объекта в проведение приказ и сотрудника, которых нужно привязать
		ПараметрыЗаписи.Вставить("СведенияОСотруднике", ДополнительныеПараметры.СведенияОСотруднике);
	ИначеЕсли Ответ = КодВозвратаДиалога.Повторить Тогда
		// Ничего не делаем, продолжаем запись-проведение, создастся новый сотрудник
	Иначе
		ОбщегоНазначенияКлиентСервер.Проверить(Ложь, 
			НСтр("ru = 'Неожиданный ответ на вопрос о создании приказа о приеме на работу';
				|en = 'Unexpected answer to the question about creating a hiring order'"),
			"Документы.РешениеОПриемеНаРаботу.Формы.ФормаДокумента");
	КонецЕсли;

	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПриемНаРаботу()
	
	СведенияОСотруднике = СведенияОСотрудникеСвязанномСРешением(Объект.Ссылка);
	ОбщегоНазначенияКлиентСервер.Проверить(ЗначениеЗаполнено(СведенияОСотруднике.Сотрудник), 
		НСтр("ru = 'Не обнаружен сотрудник, связанный с документом, для приема на работу.';
			|en = 'The employee linked to the document is not found for hiring.'"), 
		"Документы.РешениеОПриемеНаРаботу.Формы.ФормаДокумента");
	
	Если Не ЗначениеЗаполнено(СведенияОСотруднике.ПриказОПриеме) Тогда
		СоздатьПриказОПриеме();
		Возврат;
	КонецЕсли;

	ПриказПоЭтомуРешению = СвязанныйСРешениемПриказ(Объект.Ссылка);
	Если ПриказПоЭтомуРешению = СведенияОСотруднике.ПриказОПриеме Тогда
		// Нужный приказ уже привязан. 
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
		НСтр("ru = '<a href=""%1"">%2</a>, уже принят(а) на работу приказом <a href=""%3"">%4</a>.';
			|en = '<a href=""%1"">%2</a>, is already hired by the <a href=""%3"">%4</a> order.'"), 
		ПолучитьНавигационнуюСсылку(СведенияОСотруднике.Сотрудник),
		Строка(СведенияОСотруднике.Сотрудник),
		ПолучитьНавигационнуюСсылку(СведенияОСотруднике.ПриказОПриеме),
		Строка(СведенияОСотруднике.ПриказОПриеме));
	
	КнопкиОтвета = Новый СписокЗначений;
	КнопкиОтвета.Добавить(КодВозвратаДиалога.ОК, НСтр("ru = 'Связать приказ с этим решением';
														|en = 'Link the order to this decision'"));
	КнопкиОтвета.Добавить(КодВозвратаДиалога.Отмена);
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ПродолжитьСозданиеПриемаНаРаботу", ЭтотОбъект, СведенияОСотруднике.ПриказОПриеме), 
		ТекстВопроса, КнопкиОтвета, , , НСтр("ru = 'Найден похожий сотрудник';
											|en = 'A similar employee is found'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьСозданиеПриемаНаРаботу(Ответ, ПриказОПриеме) Экспорт
	
	Если Ответ = Неопределено Или Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если Ответ = КодВозвратаДиалога.ОК Тогда
		СвязатьПриказСРешением(ПриказОПриеме);
	Иначе
		ОбщегоНазначенияКлиентСервер.Проверить(Ложь, 
			НСтр("ru = 'Неожиданный ответ на вопрос о создании приказа о приеме на работу';
				|en = 'Unexpected answer to the question about creating a hiring order'"),
			"Документы.РешениеОПриемеНаРаботу.Формы.ФормаДокумента");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПриказОПриеме()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения,ПриемНаПозицию");
	ПараметрыФормы.ЗначенияЗаполнения = РеквизитыНовогоПриказа(Объект.ФизическоеЛицо);
	Если ИспользоватьШтатноеРасписание Тогда
		ПараметрыФормы.ПриемНаПозицию = Объект.ДолжностьПоШтатномуРасписанию;
	КонецЕсли;
	
	ОткрытьФорму("Документ.ПриемНаРаботу.Форма.ФормаДокумента", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция РеквизитыНовогоПриказа(ФизическоеЛицо)
	Возврат Документы.РешениеОПриемеНаРаботу.ДанныеЗаполненияПриемаНаРаботуПоДокументуРешения(
		РеквизитФормыВЗначение("Объект"), СотрудникДляПриемаНаРаботу(Объект.Ссылка), ФизическоеЛицо);
КонецФункции

&НаСервереБезКонтекста
Функция СотрудникДляПриемаНаРаботу(РешениеСсылка)
	Возврат Документы.РешениеОПриемеНаРаботу.СотрудникПоРешениюОПриемеНаРаботу(РешениеСсылка);
КонецФункции

// Связать приказ с решением.
// 
// Параметры:
//  ПриказОПриеме - ДокументСсылка.ПриемНаРаботу
&НаСервере
Процедура СвязатьПриказСРешением(ПриказОПриеме)
	
	ПриказОбъект = ПриказОПриеме.ПолучитьОбъект();
	
	ОбщегоНазначенияКлиентСервер.Проверить(Не ЗначениеЗаполнено(ПриказОбъект.Решение), 
		СтрШаблон(НСтр("ru = 'Приказ о приеме %1 уже связан с другим решением %2';
						|en = 'The %1 hiring order is already linked to another decision (%2)'"), ПриказОПриеме, ПриказОбъект.Решение), 
		Метаданные.Документы.РешениеОПриемеНаРаботу.Формы.ФормаДокумента.ПолноеИмя());
	
	ПриказОбъект.Решение = Объект.Ссылка;
	ПриказОбъект.Записать(РежимЗаписиДокумента.Запись);
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СвязанныйСРешениемПриказ(Решение)
	Возврат КадровыеРешения.СвязанныйСРешениемПриказ(Решение);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьПрограммаОбучения(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ПрограммаОбучения",
		"Доступность",
		Форма.Объект.ЗапланироватьПрограммуОбучения);

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеФормы()
	
	УстановитьПривилегированныйРежим(Истина);
	КадровыеРешенияФормы.ФормаДокументаПриПолученииДанных(ЭтотОбъект);
	ПрочитатьДанныеДляОформленияНаРаботу();
	УстановитьПривилегированныйРежим(Ложь);
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗависимыеОтМероприятияОбученияРеквизиты(ИдентификаторСтроки)

	ТекущиеДанные = Объект.ПрограммаОбучения.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Мероприятие) Тогда
		ТекущиеДанные.Ответственный = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка");
		ТекущиеДанные.КоличествоУчебныхЧасов = 0;
		ТекущиеДанные.СуммаРасходов = 0;
		Возврат;
	КонецЕсли;
	
	РеквизитыМероприятия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущиеДанные.Мероприятие, "КоличествоУчебныхЧасов, Ответственный, СпособОпределенияСтоимости, Стоимость, КоличествоСлушателей");
	
	ТекущиеДанные.Ответственный = РеквизитыМероприятия.Ответственный;
	ТекущиеДанные.КоличествоУчебныхЧасов = РеквизитыМероприятия.КоличествоУчебныхЧасов;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОбучениеРазвитие") Тогда
		МодульОбучениеРазвитиеКлиентСервер = ОбщегоНазначения.ОбщийМодуль("ОбучениеРазвитиеКлиентСервер");
		ТекущиеДанные.СуммаРасходов = МодульОбучениеРазвитиеКлиентСервер.СтоимостьОбученияДляСотрудника(РеквизитыМероприятия);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИспытательныйСрокНаСервере()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		ДанныеПрофиляДолжности = МодульПодборПерсонала.ДанныеПрофиляДолжности(Объект.ПрофильДолжности);
		Объект.ИспытательныйСрок = ДанныеПрофиляДолжности.ИспытательныйСрок;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПоПозицииНаСервере()
	
	Если Не ИспользоватьШтатноеРасписание Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПозиции = Справочники.ШтатноеРасписание.ДанныеПозицииШтатногоРасписания(Объект.ДолжностьПоШтатномуРасписанию);
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = ДанныеПозиции.Организация;
	КонецЕсли;
	
	Объект.Подразделение = ДанныеПозиции.МестоВСтруктуреПредприятия;
	Объект.Должность = ДанныеПозиции.Должность;
	Объект.ВариантОплатыТруда = Перечисления.ВариантыОплатыТруда.ФОТ;
	
	УстановитьДоступностьРабочегоМеста(ЭтаФорма);
	СписокРабочихМест = СписокРабочихМестПоДаннымПозиции(ДанныеПозиции);
	Элементы.РабочееМесто.СписокВыбора.ЗагрузитьЗначения(СписокРабочихМест.ВыгрузитьЗначения());
	ЗаполнитьРабочееМесто(ЭтаФорма, СписокРабочихМест);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПоКандидатуНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Кандидат)
		Или ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Кандидат, "Состояние") <>
			Перечисления.СостоянияСогласования.Согласовано Тогда
		Возврат;
	КонецЕсли;
	
	ИменаСвойств = "ОжидаемыйДоход,ВалютаДохода,ПериодичностьДохода,Вакансия,Подразделение,Позиция";
	ДанныеКандидата = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Кандидат, ИменаСвойств);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		МодульПодборПерсоналаКлиентСервер = ОбщегоНазначения.ОбщийМодуль("ПодборПерсоналаКлиентСервер");
		СуммаВВалютеПоУмолчанию = МодульПодборПерсоналаКлиентСервер.СуммаВВалютеПоУмолчанию(
			ДанныеКандидата.ОжидаемыйДоход, ДанныеКандидата.ВалютаДохода);
		Объект.СогласованныйДоход = МодульПодборПерсоналаКлиентСервер.ОжидаемыйДоходВМесяц(
			СуммаВВалютеПоУмолчанию, ДанныеКандидата.ПериодичностьДохода);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеКандидата.Вакансия) Тогда
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
			МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
			ДанныеВакансии = МодульПодборПерсонала.ДанныеВакансии(ДанныеКандидата.Вакансия);
			Объект.Подразделение = ДанныеВакансии.Подразделение;
			Объект.Должность = ДанныеВакансии.Должность;
			Объект.ПрофильДолжности = ДанныеВакансии.ПрофильДолжности;
			Объект.ИспытательныйСрок = ДанныеВакансии.ИспытательныйСрок;
			Объект.НоваяПозиция = ДанныеВакансии.НоваяПозиция;
			Объект.ДолжностьПоШтатномуРасписанию = ДанныеВакансии.Позиция;
			ЗаполнитьПрограммуОбученияИзПрофиля();
		КонецЕсли;
	Иначе
		Объект.Подразделение = ДанныеКандидата.Подразделение;
		Объект.ДолжностьПоШтатномуРасписанию = ДанныеКандидата.Позиция;
		Объект.НоваяПозиция = Ложь;
	КонецЕсли;
	
	УстановитьСклонениеИспытательногоСрока(Объект.ИспытательныйСрок, Элементы.ИспытательныйСрок);
	УстановитьВидимостьПредупреждения(Объект.ИспытательныйСрок, Элементы.ИспытательныйСрокПредупреждение);
	УстановитьВидимостьОплатыТруда();
	УстановитьВидимостьРабочегоМеста();
	УстановитьВидимостьДолжности(ЭтаФорма);
	
	УстановитьДоступностьРабочегоМеста(ЭтаФорма);
	ДанныеПозиции = ДанныеПозицииШтатногоРасписания(Объект.ДолжностьПоШтатномуРасписанию);
	СписокРабочихМест = СписокРабочихМестПоДаннымПозиции(ДанныеПозиции);
	Элементы.РабочееМесто.СписокВыбора.ЗагрузитьЗначения(СписокРабочихМест.ВыгрузитьЗначения());
	ЗаполнитьРабочееМесто(ЭтаФорма, СписокРабочихМест);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьРабочегоМеста()
	
	Если Не РабочиеМестаОхраныТрудаИспользуются Тогда
		Элементы.РабочееМесто.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Если Объект.НоваяПозиция Тогда
		Элементы.РабочееМесто.Видимость = Ложь;
		РабочееМестоПредыдущееЗначение = Объект.РабочееМесто;
		Объект.РабочееМесто = Неопределено;
		Возврат;
	КонецЕсли;
	
	Элементы.РабочееМесто.Видимость = ИспользоватьШтатноеРасписание;
	Если ЗначениеЗаполнено(РабочееМестоПредыдущееЗначение) Тогда
		Объект.РабочееМесто = РабочееМестоПредыдущееЗначение;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьОплатыТруда()
	
	Если Объект.Внешний Или Не ИспользоватьПодборПерсонала Тогда
		Элементы.СогласованныйДоход.Видимость = Ложь;
		Элементы.ОплатаТруда.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Документы.РешениеОПриемеНаРаботу.Реквизиты.СогласованныйДоход) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Пользователи.РолиДоступны("ЧтениеНачисленийШтатногоРасписания", , Ложь) Тогда
		Элементы.СогласованныйДоход.Видимость = Истина;
		Элементы.ОплатаТруда.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ДолжностьПоШтатномуРасписанию) Тогда
		Элементы.ОплатаТруда.Видимость = Истина;
		Элементы.СогласованныйДоход.Видимость = Ложь;
		ФОТ = Объект.ДолжностьПоШтатномуРасписанию.ФОТ;
	Иначе
		Элементы.ОплатаТруда.Видимость = Ложь;
		Элементы.СогласованныйДоход.Видимость = Истина;
		ФОТ = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ПараметрыФО = Новый Структура("Организация", Объект.Организация);
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИспытательныйСрокНаКлиенте()
	
	Если ЗначениеЗаполнено(Объект.ФизическоеЛицо)
		Или ЗначениеЗаполнено(Объект.ИспытательныйСрок)
		Или Не ЗначениеЗаполнено(Объект.ПрофильДолжности) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьИспытательныйСрокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеПоПозицииНаКлиенте()
	
	Если Не ИспользоватьШтатноеРасписание Или Объект.Внешний Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДолжностьПоШтатномуРасписанию) Тогда
		Объект.ВариантОплатыТруда = ПредопределенноеЗначение("Перечисление.ВариантыОплатыТруда.СогласованныйДоход");
		Если Не Объект.НоваяПозиция Тогда
			Объект.Должность = Неопределено;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ЗаполнитьДанныеПоПозицииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеПоКандидатуНаКлиенте()
	
	Если Не ЗначениеЗаполнено(Объект.Кандидат) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьДанныеПоКандидатуНаСервере();
	
КонецПроцедуры

// Установить видимость кандидата.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
&НаСервереБезКонтекста
Процедура УстановитьВидимостьКандидата(Форма)
	
	ДоступноЧтениеКандидатов = Ложь;
	ДоступноЧтениеЛичныхДанныхКандидатов = Ложь;
	Если Форма.ИспользоватьПодборПерсонала И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		ДоступноЧтениеКандидатов = МодульПодборПерсонала.ЕстьДоступККандидатам("Чтение");
		ДоступноЧтениеЛичныхДанныхКандидатов = МодульПодборПерсонала.ЕстьДоступКЛичнымДаннымКандидата("Чтение");
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	
	ЭлементЛичныеДанныеКандидата = Элементы.Найти("ЛичныеДанныеКандидата");
	Если ЭлементЛичныеДанныеКандидата = Неопределено Тогда
		Если ДоступноЧтениеЛичныхДанныхКандидатов Тогда
			НовыйЭлемент = Элементы.Добавить("ЛичныеДанныеКандидата", Тип("ПолеФормы"), Элементы.ОсновноеГруппа);
			НовыйЭлемент.ПутьКДанным = "Объект.ЛичныеДанныеКандидата";
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеНадписи;
			НовыйЭлемент.Заголовок = НСтр("ru = 'Принят на работу';
											|en = 'Hired'");
		Иначе
			НовыйЭлемент = Элементы.Добавить("ЛичныеДанныеКандидата", Тип("ДекорацияФормы"), Элементы.ОсновноеГруппа);
			НовыйЭлемент.Заголовок = НСтр("ru = 'Принят на работу: Данные недоступны';
											|en = 'Hired: Data is unavailable'");
		КонецЕсли;
	КонецЕсли;
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Кандидат", "Видимость",
		ДоступноЧтениеКандидатов);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЛичныеДанныеКандидата", "Видимость",
		Не ДоступноЧтениеКандидатов);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДолжности(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Если Не Форма.ИспользоватьШтатноеРасписание Тогда
		Элементы.Должность.Видимость = Истина;
		Элементы.ДолжностьПоШтатномуРасписанию.Видимость = Ложь;
	Иначе
		Элементы.Должность.Видимость = Объект.НоваяПозиция;
		Элементы.ДолжностьПоШтатномуРасписанию.Видимость = Не Объект.НоваяПозиция;
		Элементы.ДолжностьПоШтатномуРасписанию.АвтоОтметкаНеЗаполненного = Не Объект.НоваяПозиция;
		Элементы.ДолжностьПоШтатномуРасписанию.ОтметкаНеЗаполненного =
			Не ЗначениеЗаполнено(Объект.ДолжностьПоШтатномуРасписанию) И Не Объект.НоваяПозиция;
		
		Если Объект.НоваяПозиция Тогда
			Форма.ПозицияПредыдущееЗначение = Объект.ДолжностьПоШтатномуРасписанию;
			Объект.ДолжностьПоШтатномуРасписанию = Неопределено;
		ИначеЕсли ЗначениеЗаполнено(Форма.ПозицияПредыдущееЗначение) Тогда
			Объект.ДолжностьПоШтатномуРасписанию = Форма.ПозицияПредыдущееЗначение;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСклонениеИспытательногоСрока(ИспытательныйСрок, Элемент)
	Элемент.Подсказка = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		НСтр("ru = ';месяц;;месяца;месяцев;месяца';
			|en = ';month;;;;months'"), ИспытательныйСрок);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьПредупреждения(ИспытательныйСрок, Предупреждение)
	
	Если ИспытательныйСрок > 3 Тогда
		Предупреждение.Видимость = Истина;
	Иначе
		Предупреждение.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиФизическоеЛицо(ДокументСсылка, Кандидат)
	Возврат Документы.РешениеОПриемеНаРаботу.НайтиФизическоеЛицо(ДокументСсылка, Кандидат);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьРабочегоМеста(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Не ЗначениеЗаполнено(Объект.ДолжностьПоШтатномуРасписанию) Тогда
		Элементы.РабочееМесто.Доступность = Ложь;
		Объект.РабочееМесто = Неопределено;
	Иначе
		Элементы.РабочееМесто.Доступность = Истина;
	КонецЕсли
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеПозицииШтатногоРасписания(Позиция)
	Возврат Справочники.ШтатноеРасписание.ДанныеПозицииШтатногоРасписания(Позиция);
КонецФункции

#Область РабочееМесто

&НаКлиенте
Процедура ОбработатьОповещениеРабочегоМеста(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия <> "Запись_РабочиеМестаОхраныТруда" Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПозиции = ДанныеПозицииШтатногоРасписания(Объект.ДолжностьПоШтатномуРасписанию);
	СписокРабочихМест = СписокРабочихМестПоДаннымПозиции(ДанныеПозиции);
	Элементы.РабочееМесто.СписокВыбора.ЗагрузитьЗначения(СписокРабочихМест.ВыгрузитьЗначения());
	
	Если СписокРабочихМест.НайтиПоЗначению(Источник) <> Неопределено Тогда
		Объект.РабочееМесто = Источник;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокРабочихМестПоДаннымПозиции(ДанныеПозиции);
	
	СписокРабочихМест = Новый СписокЗначений;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.РабочиеМестаОхраныТруда") Тогда
		МодульРабочиеМестаОхраныТруда = ОбщегоНазначения.ОбщийМодуль("РабочиеМестаОхраныТруда");
		СписокРабочихМест = МодульРабочиеМестаОхраныТруда.РабочиеМестаПоПодразделениюИДолжности(
			ДанныеПозиции.Подразделение,
			ДанныеПозиции.Должность,
			ДанныеПозиции.Организация);
	КонецЕсли;
	
	Возврат СписокРабочихМест;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьРабочееМесто(Форма, СписокРабочихМест)
	
	Если Не ЗначениеЗаполнено(СписокРабочихМест) Тогда
		Возврат;
	КонецЕсли;
	
	Если СписокРабочихМест.Количество() = 1 Тогда
		Форма.Объект.РабочееМесто = СписокРабочихМест[0].Значение;
	Иначе
		Форма.Объект.РабочееМесто = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Функция РезультатыМероприятия(Предмет, Мероприятие)
	
	Мероприятия = Новый Массив;
	Мероприятия.Добавить(Мероприятие);

	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнение = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнение");
		РезультатыМероприятий = МодульАдаптацияУвольнение.РезультатыМероприятий(Предмет, Мероприятия);
		Возврат РезультатыМероприятий.Получить(Мероприятие);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление(УсловноеОформление)
	
	УсловноеОформление.Элементы.Очистить();
	
	Если ИспользоватьАдаптациюУвольнение И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнениеФормы = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнениеФормы");
		МодульАдаптацияУвольнениеФормы.РешениеУстановитьУсловноеОформлениеРезультатаВыполнения(УсловноеОформление);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЛичныеДанныеКандидата(Кандидат)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		Возврат МодульПодборПерсонала.ДанныеКандидата(Кандидат).ЛичныеДанныеКандидата;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СвойствоСтруктуры(Структура, Ключ, ЗначениеПоУмолчанию = Неопределено)
	Возврат ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Структура, Ключ, ЗначениеПоУмолчанию);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПредупреждениеОДругомРешении(Форма)

	Форма.Элементы.КандидатРасширеннаяПодсказка.Заголовок = "";
	Если Форма.Объект.Проведен Или Не ЗначениеЗаполнено(Форма.Объект.Кандидат) Тогда
		Возврат;
	КонецЕсли;
	
	ДругоеРешение = РешениеПоКандидату(Форма.Объект.Кандидат, Форма.Объект.Ссылка);
	Если ЗначениеЗаполнено(ДругоеРешение) Тогда
		МодульСтроковыеФункции = Неопределено;
		#Если Клиент Тогда
			МодульСтроковыеФункции = СтроковыеФункцииКлиент;
		#Иначе
			МодульСтроковыеФункции = СтроковыеФункции;
		#КонецЕсли
		Форма.Элементы.КандидатРасширеннаяПодсказка.Заголовок = 
			МодульСтроковыеФункции.ФорматированнаяСтрока(
				НСтр("ru = 'По этому кандидату уже есть <a href=""%1"">документ решение о приеме на работу</a>';
					|en = 'The <a href=""%1"">employment decision</a> document for this candidate already exists'"), 
				ПолучитьНавигационнуюСсылку(ДругоеРешение));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция РешениеПоКандидату(Кандидат, ИсключаяРешение = Неопределено)
	Возврат Документы.РешениеОПриемеНаРаботу.РешениеПоКандидату(Кандидат, ИсключаяРешение);
КонецФункции

#Область ПрограммаОбучения

&НаКлиенте
Процедура ПоказатьВопросЗаполненияПрограммыОбученияНачало()
	
	Оповещение = Новый ОписаниеОповещения("ПоказатьВопросЗаполненияПрограммыОбученияЗавершение", ЭтотОбъект);
	
	ТекстВопроса = НСтр("ru = 'Программа обучения уже содержит мероприятия.
		|Обновить программу обучения из профиля должности?.';
		|en = 'The training program already contains activities.
		|Update the training program from the job title profile?'");
	
	КнопкиВопроса = Новый СписокЗначений;
	КнопкиВопроса.Добавить(КодВозвратаДиалога.Да);
	КнопкиВопроса.Добавить(КодВозвратаДиалога.Нет);
	
	ДопПараметры = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
	ДопПараметры.Заголовок = НСтр("ru = 'Программа обучения';
									|en = 'Training program'");
	ДопПараметры.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
	
	СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, ТекстВопроса, КнопкиВопроса, ДопПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросЗаполненияПрограммыОбученияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатВопроса.Значение <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПрограммуОбученияИзПрофиля();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПрограммуОбученияИзПрофиля()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОбучениеРазвитие") Тогда
		МодульОбучениеРазвитиеФормы = ОбщегоНазначения.ОбщийМодуль("ОбучениеРазвитиеФормы");
		МодульОбучениеРазвитиеФормы.ЗаполнитьПрограммуОбученияИзПрофиляДолжности(
			Объект.ПрограммаОбучения, Объект.ПрофильДолжности, Объект.ДатаПриема);
	КонецЕсли;
	
	Если Объект.ПрограммаОбучения.Количество() > 0 Тогда
		Объект.ЗапланироватьПрограммуОбучения = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьПоляВыбораКандидата(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы, "Кандидат", "Видимость", Не Форма.ВыборФизическихЛиц);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы, "ФизическоеЛицо", "Видимость", Форма.ВыборФизическихЛиц);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройки()
	
	ИспользоватьСборДанныхДляОформленияНаРаботу = ПолучитьФункциональнуюОпцию("ИспользоватьСборДанныхДляОформленияНаРаботу");
	КорпоративнаяВерсияКонфигурации = ПолучитьФункциональнуюОпцию("ИспользоватьЗарплатаКадрыКорпоративнаяПодсистемы");
	ИспользоватьПодборПерсонала = Ложь;
	ИспользоватьАдаптациюУвольнение = Ложь;
	ИспользоватьОбучениеРазвитиеСотрудников = Ложь;
	Если КорпоративнаяВерсияКонфигурации Тогда
		ИмяОпции = "ИспользоватьПодборПерсонала";
		ФункциональнаяОпцияИспользуется = (Метаданные.ФункциональныеОпции.Найти(ИмяОпции) <> Неопределено);
		ИспользоватьПодборПерсонала = ФункциональнаяОпцияИспользуется И ПолучитьФункциональнуюОпцию(ИмяОпции);
		ИмяОпции = "ИспользоватьАдаптациюУвольнение";
		ФункциональнаяОпцияИспользуется = (Метаданные.ФункциональныеОпции.Найти(ИмяОпции) <> Неопределено);
		ИспользоватьАдаптациюУвольнение = ФункциональнаяОпцияИспользуется И ПолучитьФункциональнуюОпцию(ИмяОпции);
		ИмяОпции = "ИспользоватьОбучениеРазвитиеСотрудников";
		ФункциональнаяОпцияИспользуется = (Метаданные.ФункциональныеОпции.Найти(ИмяОпции) <> Неопределено);
		ИспользоватьОбучениеРазвитиеСотрудников = ФункциональнаяОпцияИспользуется И ПолучитьФункциональнуюОпцию(ИмяОпции);
	КонецЕсли;
	ИспользоватьШтатноеРасписание = ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание");
	
	РабочиеМестаОхраныТрудаИспользуются = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.РабочиеМестаОхраныТруда") Тогда
		МодульРабочиеМестаОхраныТруда = ОбщегоНазначения.ОбщийМодуль("РабочиеМестаОхраныТруда");
		РабочиеМестаОхраныТрудаИспользуются = МодульРабочиеМестаОхраныТруда.РабочиеМестаОхраныТрудаИспользуются(Объект.Организация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапросДанныхДляОформленияНаРаботу(Команда)
	
	СоздатьЗапросДанныхДляОформленияНаРаботу();
	
КонецПроцедуры

&НаКлиенте
Функция ДокументГотовДляЗапросаДанных()
	
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Не указана организация';
								|en = 'Company is not specified'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"Организация","Объект.Организация");
		Отказ = Истина;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.ФизическоеЛицо) Тогда
		ТекстСообщения = НСтр("ru = 'Не указано физическое лицо';
								|en = 'The person is not specified'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ФизическоеЛицо","Объект.ФизическоеЛицо");
		Отказ = Истина;
	КонецЕсли;
		
	Если Отказ Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

&НаКлиенте
Процедура СоздатьЗапросДанныхДляОформленияНаРаботу()
	
	Если ДокументГотовДляЗапросаДанных() Тогда
		
		Если Не Параметры.Ключ.Пустая() И Объект.Проведен И Не Модифицированность Тогда
			СоздатьЗапросДанныхДляОформленияНаРаботуЗавершение();
			Возврат;
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("СоздатьЗапросДанныхДляОформленияНаРаботуПродолжение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Перед созданием запроса данных для оформления на работу документ требуется провести. Продолжить?';
							|en = 'Before creating a request for employment registration data, post the document. Continue?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , , НСтр("ru = 'Документ не проведен';
																							|en = 'Document is not posted'"));
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗапросДанныхДляОформленияНаРаботуПродолжение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;

	ПослеЗаписи.Добавить("СоздатьЗапросДанныхДляОформленияНаРаботуПослеЗаписи");
	ПараметрыЗаписи = Новый Структура();
	ПараметрыЗаписи.Вставить("РежимЗаписи", РежимЗаписиДокумента.Проведение);
	Записать(ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗапросДанныхДляОформленияНаРаботуПослеЗаписи(Результат, ДополнительныеПараметры) Экспорт // АПК:78 используется как обработчик оповещения после записи
	СоздатьЗапросДанныхДляОформленияНаРаботуЗавершение();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗапросДанныхДляОформленияНаРаботуЗавершение()
	
	ДанныеЗаполнения = Новый Структура("Организация,ФизическоеЛицо,ОснованиеДокумента");
	ДанныеЗаполнения.Организация 		= Объект.Организация;
	ДанныеЗаполнения.ФизическоеЛицо 	= Объект.ФизическоеЛицо;
	ДанныеЗаполнения.ОснованиеДокумента = Объект.Ссылка;
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения");
	ПараметрыФормы.ЗначенияЗаполнения = ДанныеЗаполнения;
	
	ОткрытьФорму("Документ.ДанныеДляОформленияНаРаботу.Форма.ФормаДокумента", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеДляОформленияНаРаботу()
	
	Если Не ИспользоватьСборДанныхДляОформленияНаРаботу Или Объект.Ссылка.Пустая()
		Или Не ИнтеграцияУправлениеПерсоналом.ДоступноЧтениеДанныхДляОформленияНаРаботу() Тогда
		ДанныеДляОформленияИнфоНадпись = "";
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОснованиеДокумента", Объект.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДляОформленияНаРаботу.Ссылка КАК Ссылка,
	|	ДанныеДляОформленияНаРаботу.Состояние КАК Состояние
	|ИЗ
	|	Документ.ДанныеДляОформленияНаРаботу КАК ДанныеДляОформленияНаРаботу
	|ГДЕ
	|	ДанныеДляОформленияНаРаботу.ОснованиеДокумента = &ОснованиеДокумента
	|	И НЕ ДанныеДляОформленияНаРаботу.ПометкаУдаления
	|	И ДанныеДляОформленияНаРаботу.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияДанныхДляОформленияНаРаботу.Аннулирован)";
	ДанныеДляОформленияНаРаботу = Запрос.Выполнить().Выгрузить();
	
	Если ДанныеДляОформленияНаРаботу.Количество() = 0 Тогда
		ДанныеДляОформленияИнфоНадпись = "";
	Иначе
		ДокументСсылка = ДанныеДляОформленияНаРаботу[0].Ссылка;
		СостояниеДанных = ДанныеДляОформленияНаРаботу[0].Состояние;
		Если СостояниеДанных = Перечисления.СостоянияДанныхДляОформленияНаРаботу.Новый Тогда
			ТекстСостояние = "";
		Иначе
			ТекстСостояние = СтрШаблон("(%1)", Строка(СостояниеДанных));
		КонецЕсли;
		ИнфоНадпись = СтрШаблон("%1 %2", Строка(ДокументСсылка), ТекстСостояние);
		ДанныеДляОформленияИнфоНадпись = Новый ФорматированнаяСтрока(ИнфоНадпись, , , , ПолучитьНавигационнуюСсылку(ДокументСсылка));
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьДанныхДляОформленияНаРаботу()
	
	ЧтениеДоступно = ИнтеграцияУправлениеПерсоналом.ДоступноЧтениеДанныхДляОформленияНаРаботу();
	СборДанныхДоступен = ЧтениеДоступно И ИспользоватьСборДанныхДляОформленияНаРаботу И ВыборФизическихЛиц;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСборДанных", "Видимость", СборДанныхДоступен);
	
	Если Не СборДанныхДоступен Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьСборДанных 		= Не ПустаяСтрока(ДанныеДляОформленияИнфоНадпись);
	ИзмененияДоступны 	= ИнтеграцияУправлениеПерсоналом.ДоступноИзменениеДанныхДляОформленияНаРаботу();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СборДанныхДляОформленияНаРаботу",
		"Доступность", Объект.ИспользоватьЗапросДанныхДляОформленияНаРаботу);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СборДанныхДляОформленияНаРаботу", "Видимость", Не ЕстьСборДанных);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДанныеДляОформленияИнфоНадпись", "Видимость", ЕстьСборДанных);

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЗапросДанныхДляОформленияНаРаботуПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СборДанныхДляОформленияНаРаботу",
		"Доступность", Объект.ИспользоватьЗапросДанныхДляОформленияНаРаботу);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеДляОформленияНаРаботу()

	ПрочитатьДанныеДляОформленияНаРаботу();
	УстановитьДоступностьДанныхДляОформленияНаРаботу();

КонецПроцедуры

#КонецОбласти