#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		Ограничение.Текст =
		"РазрешитьЧтениеИзменение
		|ГДЕ
		|	ЗначениеРазрешено(Организация)
		|	И ЗначениеРазрешено(ЛичныеДанныеКандидата)
		|	И ЗначениеРазрешено(ФизическоеЛицо)";
	Иначе
		Ограничение.Текст =
		"РазрешитьЧтениеИзменение
		|ГДЕ
		|	ЗначениеРазрешено(Организация)
		|	И ЗначениеРазрешено(ФизическоеЛицо)";
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ДанныеЗаполненияПриемаНаРаботуПоДокументуРешения(РешениеОПриемеОбъект, Сотрудник, ФизическоеЛицо) Экспорт
	
	РеквизитыПриказа = НовыйРеквизитыПриказа();
	
	ЗаполнитьЗначенияСвойств(РеквизитыПриказа, РешениеОПриемеОбъект);
	
	Если ЗначениеЗаполнено(РешениеОПриемеОбъект.ПодразделениеОрганизации) Тогда
		РеквизитыПриказа.Подразделение = РешениеОПриемеОбъект.ПодразделениеОрганизации;
	Иначе
		РеквизитыПриказа.Подразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			РешениеОПриемеОбъект.ДолжностьПоШтатномуРасписанию, "Подразделение");
	КонецЕсли;
	РеквизитыПриказа.ФизическоеЛицо = ФизическоеЛицо;
	РеквизитыПриказа.Сотрудник = Сотрудник;
	РеквизитыПриказа.Решение = РешениеОПриемеОбъект.Ссылка;
	РеквизитыПриказа.РаботаетДистанционно = РешениеОПриемеОбъект.ДистанционнаяРабота;
	РеквизитыПриказа.ДлительностьИспытательногоСрока = РешениеОПриемеОбъект.ИспытательныйСрок;
	
	ОбщегоНазначенияКлиентСервер.Проверить(ЗначениеЗаполнено(РеквизитыПриказа.ФизическоеЛицо), 
		НСтр("ru = 'При формировании реквизитов нового приказа о приеме на работу не заполнено физическое лицо.';
			|en = 'When generating attributes of a new hiring  order, a person is not filled in.'"), 
		Метаданные.Документы.РешениеОПриемеНаРаботу.ПолноеИмя());
	ОбщегоНазначенияКлиентСервер.Проверить(ЗначениеЗаполнено(РеквизитыПриказа.Сотрудник),
		НСтр("ru = 'При формировании реквизитов нового приказа о приеме на работу не заполнен сотрудник.';
			|en = 'When generating attributes of a new hiring  order, an employee is not filled in.'"), 
		Метаданные.Документы.РешениеОПриемеНаРаботу.ПолноеИмя());
	
	Возврат РеквизитыПриказа;
	
КонецФункции

Функция НайтиФизическоеЛицо(ДокументСсылка, Кандидат) Экспорт
	
	ФизическоеЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(ДокументСсылка) Тогда
		УстановитьПривилегированныйРежим(Истина);
		Приказ = КадровыеРешения.СвязанныйСРешениемПриказ(ДокументСсылка);
		УстановитьПривилегированныйРежим(Ложь);
		Если Приказ <> Неопределено Тогда
			ФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Приказ, "ФизическоеЛицо");
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Возврат ФизическоеЛицо;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Кандидат) Тогда
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
			МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
			ФизическоеЛицо = МодульПодборПерсонала.ФизическоеЛицоКандидата(Кандидат, Ложь, Истина);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ФизическоеЛицо;
	
КонецФункции

// Находит решение о приеме на работу по кандидату, исключая заданное решение.
// 
// Параметры:
//  Кандидат - СправочникСсылка.Кандидаты -
//  ИсключаяРешение - Неопределено, ДокументСсылка.РешениеОПриемеНаРаботу -
// 
// Возвращаемое значение:
//  ДокументСсылка.РешениеОПриемеНаРаботу, Произвольный - Решение по кандидату
Функция РешениеПоКандидату(Кандидат, ИсключаяРешение = Неопределено) Экспорт
	Возврат РешенияПоКандидатам(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Кандидат), ИсключаяРешение)[Кандидат];
КонецФункции

// Находит решения о приеме на работу по кандидатам.
// 
// Параметры:
//  Кандидаты - Массив из СправочникСсылка.Кандидаты -
//  ИсключаяРешение - Неопределено, ДокументСсылка.РешениеОПриемеНаРаботу -
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
// 		* Ключ - СправочникСсылка.Кандидаты
// 		* Значение - ДокументСсылка.РешениеОПриемеНаРаботу
Функция РешенияПоКандидатам(Кандидаты, ИсключаяРешение = Неопределено) Экспорт
	
	РешенияПоКандидатам = Новый Соответствие();
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Кандидаты", Кандидаты);
	Запрос.УстановитьПараметр("ИсключаяРешение", ИсключаяРешение);
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Решение.Кандидат КАК Кандидат,
		|	Решение.Ссылка КАК Решение,
		|	Решение.Дата КАК Дата
		|ИЗ
		|	Документ.РешениеОПриемеНаРаботу КАК Решение
		|ГДЕ
		|	Решение.Кандидат В(&Кандидаты)
		|	И НЕ Решение.ПометкаУдаления
		|	И Решение.Ссылка <> &ИсключаяРешение
		|
		|УПОРЯДОЧИТЬ ПО
		|	Кандидат,
		|	Дата УБЫВ";
		
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Кандидат") Цикл
		РешенияПоКандидатам.Вставить(Выборка.Кандидат, Выборка.Решение);
	КонецЦикла;
	
	Возврат РешенияПоКандидатам;
	
КонецФункции

// Находит кандидатов по решениям о приеме на работу.
// 
// Параметры:
//  Решения - Массив из СправочникСсылка.РешениеОПриемеНаРаботу
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
// 		* Ключ - СправочникСсылка.РешениеОПриемеНаРаботу
// 		* Значение - ДокументСсылка.Кандидаты
Функция КандидатыПоРешениям(Решения) Экспорт
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Решения, "Кандидат", Истина);
	
КонецФункции

// Сотрудник по решению о приеме на работу.
// 
// Параметры:
//  Сотрудник - СправочникСсылка.Сотрудники
// 
// Возвращаемое значение:
//  ДокументСсылка.РешениеОПриемеНаРаботу, Неопределено - Решение о приеме на работу по сотруднику
Функция РешениеОПриемеНаРаботуПоСотруднику(Сотрудник) Экспорт
	Возврат РегистрыСведений.СотрудникиПоРешениямОПриемеНаРаботу.РешенияОПриемеНаРаботуПоСотрудникам(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник))[Сотрудник];
КонецФункции

// Сотрудник по решению о приеме на работу.
// 
// Параметры:
//  Решение - ДокументСсылка.РешениеОПриемеНаРаботу
// 
// Возвращаемое значение:
//  СправочникСсылка.Сотрудники, Неопределено - Сотрудник по решению о приеме на работу
Функция СотрудникПоРешениюОПриемеНаРаботу(Решение) Экспорт
	Возврат РегистрыСведений.СотрудникиПоРешениямОПриемеНаРаботу.СотрудникиПоРешениямОПриемеНаРаботу(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Решение))[Решение];
КонецФункции

// Связать сотрудника с решением о приеме на работу.
// 
// Параметры:
//  Решение - ДокументСсылка.РешениеОПриемеНаРаботу
//  Сотрудник - СправочникСсылка.Сотрудники
Процедура СвязатьСотрудникаСРешениемОПриемеНаРаботу(Решение, Сотрудник) Экспорт
	СотрудникиПоРешениям = Новый Соответствие();
	СотрудникиПоРешениям.Вставить(Решение, Сотрудник);
	РегистрыСведений.СотрудникиПоРешениямОПриемеНаРаботу.СвязатьСотрудниковСРешениямиОПриемеНаРаботу(СотрудникиПоРешениям);
КонецПроцедуры

// Отвязать сотрудника от решения о приеме на работу.
// 
// Параметры:
//  Решение - ДокументСсылка.РешениеОПриемеНаРаботу
Процедура ОтвязатьСотрудникаОтРешенияОПриемеНаРаботу(Решение) Экспорт
	
	СотрудникиПоРешениям = Новый Соответствие();
	СотрудникиПоРешениям.Вставить(Решение, Неопределено);
	РегистрыСведений.СотрудникиПоРешениямОПриемеНаРаботу.СвязатьСотрудниковСРешениямиОПриемеНаРаботу(СотрудникиПоРешениям);
	
КонецПроцедуры

// Заполняет данные для направления на медицинский осмотр.
// 
// Параметры:
//  ДанныеЗаполнения - см. Документы.НаправлениеНаМедицинскийОсмотр.ОписаниеДляЗаполнения
Процедура ЗаполнитьДанныеДляНаправленияНаМедицинскийОсмотр(ДанныеЗаполнения) Экспорт

	Основание = ДанныеЗаполнения.Основание;
	Если ТипЗнч(Основание) <> Тип("ДокументСсылка.РешениеОПриемеНаРаботу") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеРешения = ДанныеОПриемеНаРаботу(Основание);
	ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, ДанныеРешения);
	
КонецПроцедуры

#Область ОбработчикиРегистрацииФизическихЛиц

Функция ПринадлежностиОбъекта() Экспорт
	Возврат ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Организация");
КонецФункции

#КонецОбласти

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнение = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнение");
		ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляОтбораПоПодразделению(Настройки, ,
			МодульАдаптацияУвольнение.РазделыДанныхОбъектовПодсистемы());
	КонецЕсли;
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляОбъектаСПрисоединеннымиФайлами(Настройки);
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляРегистрацииДвижений(Настройки);
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляСовместноРегистрируемыхОбъектов(Настройки);
	
КонецПроцедуры

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	МетаданныеДокумента = Метаданные.Документы.РешениеОПриемеНаРаботу;
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаПоМетаданнымФизическиеЛицаВТабличныхЧастях(МетаданныеДокумента);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПараметрыЗаполненияМероприятий(Объект, ФизическоеЛицо) Экспорт
	
	ПараметрыЗаполнения = Новый Структура;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнение = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнение");
		ПараметрыЗаполнения = МодульАдаптацияУвольнение.ПараметрыЗаполненияМероприятий();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыЗаполнения) Тогда
		Возврат ПараметрыЗаполнения;
	КонецЕсли;
	
	ПараметрыЗаполнения.Организация = Объект.Организация;
	ПараметрыЗаполнения.ФизическоеЛицо = ФизическоеЛицо;
	ПараметрыЗаполнения.ВидСобытия = ВидСобытияАдаптацииУвольнения();
	ПараметрыЗаполнения.ДатаСобытия = Объект.ДатаПриема;
	ПараметрыЗаполнения.Ответственный = Объект.Ответственный;
	
	Если Не Объект.НоваяПозиция Тогда
		ПараметрыЗаполнения.Позиция = Объект.ДолжностьПоШтатномуРасписанию;
	Иначе
		ПараметрыЗаполнения.Должность = Объект.Должность;
		ПараметрыЗаполнения.Подразделение = Объект.Подразделение;
	КонецЕсли;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Функция ВидСобытияАдаптацииУвольнения() Экспорт
	
	ВидСобытия = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнение = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнение");
		ВидСобытия = МодульАдаптацияУвольнение.СобытиеПрием();
	КонецЕсли;
	
	Возврат ВидСобытия;
	
КонецФункции

Функция ПоляПроверкиУникальности() Экспорт
	
	ПоляПроверки = Новый Массив();
	ПоляПроверки.Добавить("Кандидат");
	ПоляПроверки.Добавить("ФизическоеЛицо");
	Возврат ПоляПроверки;
	
КонецФункции

Функция НовыйРеквизитыПриказа()
	
	РеквизитыПриказа = Новый Структура;
	РеквизитыПриказа.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	РеквизитыПриказа.Вставить("Сотрудник", Справочники.Сотрудники.ПустаяСсылка());
	РеквизитыПриказа.Вставить("ФизическоеЛицо", Справочники.ФизическиеЛица.ПустаяСсылка());
	РеквизитыПриказа.Вставить("Подразделение", Справочники.ПодразделенияОрганизаций.ПустаяСсылка());
	РеквизитыПриказа.Вставить("Территория", Справочники.ТерриторииВыполненияРабот.ПустаяСсылка());
	РеквизитыПриказа.Вставить("Должность", Справочники.Должности.ПустаяСсылка());
	РеквизитыПриказа.Вставить("ДолжностьПоШтатномуРасписанию", Справочники.ШтатноеРасписание.ПустаяСсылка());
	РеквизитыПриказа.Вставить("ДатаПриема", Дата(1, 1, 1));
	РеквизитыПриказа.Вставить("Решение", ПустаяСсылка());
	РеквизитыПриказа.Вставить("РабочееМесто");
	РеквизитыПриказа.Вставить("РаботаетДистанционно", Ложь);
	РеквизитыПриказа.Вставить("ДлительностьИспытательногоСрока", 0);
	
	Возврат РеквизитыПриказа;
	
КонецФункции

Функция ДанныеОПриемеНаРаботу(ДокументСсылка)
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументРешение.Ссылка КАК Ссылка,
	|	ДокументРешение.Организация КАК Организация,
	|	ДокументРешение.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДокументРешение.ЛичныеДанныеКандидата КАК ЛичныеДанныеКандидата,
	|	ДокументРешение.ДолжностьПоШтатномуРасписанию КАК Позиция,
	|	ДокументРешение.ДатаПриема КАК ДатаПриема,
	|	ДокументРешение.РабочееМесто КАК РабочееМесто,
	|	ДокументРешение.Подразделение КАК Подразделение,
	|	ДокументРешение.Должность КАК Должность
	|ИЗ
	|	Документ.РешениеОПриемеНаРаботу КАК ДокументРешение
	|ГДЕ
	|	ДокументРешение.Ссылка = &ДокументСсылка";
	РезультатЗапроса = Запрос.Выполнить();
	Данные = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(РезультатЗапроса.Выгрузить()[0]);
	Если Не ЗначениеЗаполнено(Данные.ФизическоеЛицо) Тогда
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
			МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
			Данные.ФизическоеЛицо = МодульПодборПерсонала.ФизическоеЛицоЛичныхДанныхКандидата(Данные.ЛичныеДанныеКандидата);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли