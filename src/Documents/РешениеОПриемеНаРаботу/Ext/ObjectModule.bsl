#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьПраваНаПроведение(Отказ, РежимЗаписи, РежимПроведения);
	ПроверитьЛичныеДанныеКандидата();
	ПроверитьФизическоеЛицо(РежимЗаписи);
	
	ЗаполнитьПрежниеДанные();

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Внешний И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнение = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнение");
		МодульАдаптацияУвольнение.ПриЗаписи(ЭтотОбъект, Отказ);
	КонецЕсли;

	ОбновитьСотрудника();

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ДанныеОффера") Тогда
		ЗаполнитьНаОснованииОффера(ДанныеЗаполнения.ДанныеОффера);
	Иначе
		
		ДатаПриема = ТекущаяДатаСеанса();
		ВариантОплатыТруда = Перечисления.ВариантыОплатыТруда.СогласованныйДоход;
		
		Если ДанныеЗаполнения = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.Конкурсы") Тогда
			МодульКонкурсы = ОбщегоНазначения.ОбщийМодуль("Конкурсы");
			МодульКонкурсы.ЗаполнитьРешениеОПриемеНаРаботуПоКонкурсу(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Кандидат) Тогда
			Возврат;
		КонецЕсли;
		
		ЗаполнитьПоДаннымКандидата(ДанныеЗаполнения, СтандартнаяОбработка);
		ЗаполнитьПоДаннымЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если Внешний Или Не ПолучитьФункциональнуюОпцию("ИспользоватьЗарплатаКадрыКорпоративнаяПодсистемы") Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнение = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнение");
		МодульАдаптацияУвольнение.СформироватьДвиженияМероприятияАдаптацииУвольненияПоДокументу(Движения,
			ДанныеДляПроведения.ДанныеМероприятийАдаптацииУвольнения);
		МодульАдаптацияУвольнение.ЗарегистрироватьДокументДляОбновленияЗаданийАдаптацииУвольнения(Движения);
	КонецЕсли;
	
	Если ЗапланироватьПрограммуОбучения И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОбучениеРазвитие") Тогда
		МодульОбучениеРазвитие = ОбщегоНазначения.ОбщийМодуль("ОбучениеРазвитие");
		МодульОбучениеРазвитие.СформироватьДвиженияПланаПоСотрудникам(Движения,
			ДанныеДляПроведения.ПлановыеДвиженияСотрудников);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаПриема, "Объект.ДатаПриема", Отказ, НСтр("ru = 'Дата приема';
																								|en = 'Hiring date'"),
		, , Ложь);
		
	Если Не Внешний И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнение = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнение");
		МодульАдаптацияУвольнение.ПроверитьЗаполнениеМероприятийАдаптацииВРешенииОПриеме(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	КонецЕсли;
		
	Если Не Отказ Тогда
		КадровыеРешения.ПроверитьНаличиеДубляДокумента(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	Если ЗапланироватьПрограммуОбучения И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОбучениеРазвитие") Тогда
		МодульОбучениеРазвитие = ОбщегоНазначения.ОбщийМодуль("ОбучениеРазвитие");
		МодульОбучениеРазвитие.ПроверитьЗаполнениеПрограммыОбученияВРешенииОПриеме(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	КонецЕсли;
	
	Если НоваяПозиция Или Не ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание") Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДолжностьПоШтатномуРасписанию");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если Не Внешний И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.АдаптацияУвольнение") Тогда
		МодульАдаптацияУвольнение = ОбщегоНазначения.ОбщийМодуль("АдаптацияУвольнение");
		МодульАдаптацияУвольнение.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения() Экспорт
	
	Если Не Внешний И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КадровыеРешения") Тогда
		МодульКадровыеРешенияРасширенный = ОбщегоНазначения.ОбщийМодуль("КадровыеРешенияРасширенный");
		ФизическоеЛицо = ДополнительныеСвойства.ФизическоеЛицо;
		Возврат МодульКадровыеРешенияРасширенный.ДанныеДляПроведенияРешениеОПриемеНаРаботу(Ссылка, ФизическоеЛицо, ДолжностьПоШтатномуРасписанию);
	КонецЕсли;
		
	Возврат Новый Структура("ДанныеМероприятийАдаптацииУвольнения,ПлановыеДвиженияСотрудников");
	
КонецФункции

Процедура ПроверитьПраваНаПроведение(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Не ПравоДоступа("Добавление", Метаданные.Документы.РешениеОПриемеНаРаботу)  Тогда
		
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение И Не Проведен Тогда
			Отказ = Истина;
			ВызватьИсключение(НСтр("ru = 'Нет прав на проведение непроведенного документа.';
									|en = 'There are no rights to post an unposted document.'"));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЛичныеДанныеКандидата()

	ИмяОпции = "ИспользоватьПодборПерсонала";
	ФункциональнаяОпцияИспользуется = (Метаданные.ФункциональныеОпции.Найти(ИмяОпции) <> Неопределено);
	ИспользоватьПодборПерсонала = ФункциональнаяОпцияИспользуется И ПолучитьФункциональнуюОпцию(ИмяОпции);
	
	Если Не Внешний И ИспользоватьПодборПерсонала И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		Если Не ЗначениеЗаполнено(ЛичныеДанныеКандидата) И ЗначениеЗаполнено(Кандидат) Тогда
			ЗаполнитьЛичныеДанныеКандидата(Кандидат);
			ОбщегоНазначенияКлиентСервер.Проверить(ЗначениеЗаполнено(ЛичныеДанныеКандидата),
				СтрШаблон(НСтр("ru = 'Для кандидата %1 не найдены личные данные.';
								|en = 'No personal data is found for the %1 candidate.'"), Кандидат));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьФизическоеЛицо(РежимЗаписи)
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		ФизическоеЛицо = Документы.РешениеОПриемеНаРаботу.НайтиФизическоеЛицо(Ссылка, Кандидат);
	КонецЕсли;
	
	ИмяОпции = "ИспользоватьПодборПерсонала";
	ФункциональнаяОпцияИспользуется = (Метаданные.ФункциональныеОпции.Найти(ИмяОпции) <> Неопределено);
	ИспользоватьПодборПерсонала = ФункциональнаяОпцияИспользуется И ПолучитьФункциональнуюОпцию(ИмяОпции);
	
	Если Внешний Или Не ИспользоватьПодборПерсонала Тогда
		ТекстСообщения = НСтр("ru = 'Не указано физическое лицо.';
								|en = 'The person is not specified.'");
	Иначе
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Для кандидата %1 не найдено физическое лицо.';
										|en = 'No person is found for the %1 candidate.'"), Кандидат);
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.Проверить(ЗначениеЗаполнено(ФизическоеЛицо),ТекстСообщения);
	
	ДополнительныеСвойства.Вставить("ФизическоеЛицо", ФизическоеЛицо);
	
КонецПроцедуры

Процедура ЗаполнитьПоДаннымЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если ДанныеЗаполнения.Свойство("Кандидат") Тогда
		ЗаполнитьПоДаннымКандидата(ДанныеЗаполнения.Кандидат, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоДаннымКандидата(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.КадровыеРешения") Тогда
		МодульКадровыеРешенияРасширенный = ОбщегоНазначения.ОбщийМодуль("КадровыеРешенияРасширенный");
		МодульКадровыеРешенияРасширенный.ЗаполнитьРешениеОПриемеНаРаботуПоДаннымКандидата(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПрежниеДанные()
	
	Если ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	ПрежниеДанные = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДополнительныеСвойства, 
		"ПрежниеДанные", Новый Структура);

	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Кандидат, Проведен");
	ПрежниеДанные.Вставить("Кандидат", ЗначенияРеквизитов.Кандидат);
	ПрежниеДанные.Вставить("Проведен", ЗначенияРеквизитов.Проведен);

	ДополнительныеСвойства.Вставить("ПрежниеДанные", ПрежниеДанные);
	
КонецПроцедуры

Процедура ОбновитьСотрудника()

	ПрежниеДанные = СвойствоСтруктуры(ДополнительныеСвойства, "ПрежниеДанные", Новый Структура());
	
	Если СвойствоСтруктуры(ПрежниеДанные, "Кандидат") = Кандидат 
		И СвойствоСтруктуры(ПрежниеДанные, "Проведен", Ложь) = Проведен Тогда
		Возврат;
	КонецЕсли;

	ПрежнийСотрудник = Неопределено;
	Если СвойствоСтруктуры(ПрежниеДанные, "Проведен", Ложь) Тогда
		УстановитьПривилегированныйРежим(Истина);
		ПрежнийСотрудник = Документы.РешениеОПриемеНаРаботу.СотрудникПоРешениюОПриемеНаРаботу(Ссылка);
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;

	Если Не Проведен Тогда
		ОтвязатьСотрудника(ПрежнийСотрудник);
		Возврат;
	КонецЕсли;

	Если СвойствоСтруктуры(ПрежниеДанные, "Кандидат") <> Кандидат Тогда
		ОтвязатьСотрудника(ПрежнийСотрудник);
	КонецЕсли;

	СведенияОСотруднике = СвойствоСтруктуры(ДополнительныеСвойства, "СведенияОСотруднике", Новый Структура());
	
	// Из формы пользователь выбрал существующего сотрудника.
	ПривязатьСотрудника(СвойствоСтруктуры(СведенияОСотруднике, "Сотрудник"), 
		СвойствоСтруктуры(СведенияОСотруднике, "ПриказОПриеме"));

КонецПроцедуры

Процедура ПривязатьСотрудника(Сотрудник, ПриказОПриеме)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Сотрудник = Неопределено Тогда
		// Новый сотрудник
		Если Не ЗначениеЗаполнено(ФизическоеЛицо) Тогда
			Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
				МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
				ФизическоеЛицо = МодульПодборПерсонала.ФизическоеЛицоКандидата(Кандидат, Ложь, Ложь);
			КонецЕсли;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.Проверить(ЗначениеЗаполнено(ФизическоеЛицо), 
			НСтр("ru = 'Физическое лицо не обнаружено, проведение документа невозможно';
				|en = 'The person is not found. Cannot post the document'"), 
			Метаданные.Документы.РешениеОПриемеНаРаботу.ПолноеИмя());
		Сотрудник = КадровыйУчет.НовыйСотрудникФизическогоЛица(ФизическоеЛицо, Организация);
	КонецЕсли;
	
	Документы.РешениеОПриемеНаРаботу.СвязатьСотрудникаСРешениемОПриемеНаРаботу(Ссылка, Сотрудник);
	Если ПриказОПриеме <> Неопределено Тогда
		ПриказОбъект = ПриказОПриеме.ПолучитьОбъект();
		ПриказОбъект.Решение = Ссылка;
		ПриказОбъект.Записать(РежимЗаписиДокумента.Запись);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтвязатьСотрудника(Сотрудник)

	Если Сотрудник = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудника(Ложь, 
		Сотрудник, "ДатаНачалаУчета, ВАрхиве", ТекущаяДатаСеанса());
	УстановитьПривилегированныйРежим(Ложь);
	
	ОбщегоНазначенияКлиентСервер.Проверить(
		Сотрудник = Неопределено Или Не ЗначениеЗаполнено(ДанныеСотрудника.ДатаНачалаУчета), 
		СтрШаблон(НСтр("ru = 'С документом связан сотрудник %1, по которому зарегистрированы трудовые отношения. 
			 |Операции с решением о приеме на работу более и невозможны, и не имеют смысла.';
			 |en = 'The document is linked to employee %1, for whom employment is registered.
			 |Further operations with employment decision are no longer possible or meaningful.'"), Сотрудник));

	УстановитьПривилегированныйРежим(Истина);
	КадровыйУчет.ПоместитьСотрудникаВАрхив(Сотрудник, Истина);
	Документы.РешениеОПриемеНаРаботу.ОтвязатьСотрудникаОтРешенияОПриемеНаРаботу(Ссылка);
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

Функция СвойствоСтруктуры(Структура, Ключ, ЗначениеПоУмолчанию = Неопределено)
	Возврат ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Структура, Ключ, ЗначениеПоУмолчанию);
КонецФункции

Процедура ЗаполнитьЛичныеДанныеКандидата(Кандидат)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		МодульПодборПерсонала = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		ЛичныеДанныеКандидата = МодульПодборПерсонала.ДанныеКандидата(Кандидат).ЛичныеДанныеКандидата;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьНаОснованииОффера(ДанныеОффера)

	Если ТипЗнч(ДанныеОффера.Подразделение) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
		ПодразделениеОрганизации = ДанныеОффера.Подразделение;
		Если ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
			Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодразделениеОрганизации, "Владелец");
		КонецЕсли;
	Иначе
		Подразделение = ДанныеОффера.Подразделение;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеОффера.ДолжностьПоШтатномуРасписанию) И (Не ЗначениеЗаполнено(Организация) Или Не ЗначениеЗаполнено(ПодразделениеОрганизации)) Тогда
		ДанныеПозиции = Справочники.ШтатноеРасписание.ДанныеПозицииШтатногоРасписания(ДанныеОффера.ДолжностьПоШтатномуРасписанию);
		Если Не ЗначениеЗаполнено(Организация) Тогда
			Организация = ДанныеПозиции.Организация;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ПодразделениеОрганизации) Тогда
			ПодразделениеОрганизации = ДанныеПозиции.Подразделение;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПодразделениеОрганизации) И ЗначениеЗаполнено(Подразделение) Тогда
		ПодразделениеИсточник = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Подразделение, "Источник");
		Если ЗначениеЗаполнено(ПодразделениеИсточник) И ТипЗнч(ПодразделениеИсточник) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
			ПодразделениеОрганизации = ПодразделениеИсточник;
			Если Не ЗначениеЗаполнено(Организация) Тогда
				Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПодразделениеОрганизации, "Владелец");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ФизическоеЛицо 					= ДанныеОффера.ФизическоеЛицо;
	Должность 						= ДанныеОффера.Должность;
	ДатаПриема 						= ДанныеОффера.ДатаПриема;
	Внешний 						= Истина;
	ИдентификаторДокумента 			= ДанныеОффера.ИдентификаторДокумента;
	ДолжностьПоШтатномуРасписанию 	= ДанныеОффера.ДолжностьПоШтатномуРасписанию;
	ИспользоватьЗапросДанныхДляОформленияНаРаботу = ПолучитьФункциональнуюОпцию("ИспользоватьСборДанныхДляОформленияНаРаботу");

КонецПроцедуры


#Область ОбменДанными

Функция СовместноРегистрируемыеОбъекты() Экспорт
	Если ЗначениеЗаполнено(Кандидат) Тогда
		Возврат ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Кандидат);
	Иначе
		Возврат Новый Массив;
	КонецЕсли;
КонецФункции

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли