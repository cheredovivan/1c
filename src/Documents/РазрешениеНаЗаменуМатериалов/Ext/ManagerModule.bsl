#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует описание реквизитов объекта, заполняемых по статистике их использования.
// 
// Параметры:
//  ОписаниеРеквизитов - Структура - описание реквизитов, для которых необходимо получить значения по статистике
//
//
Процедура ЗадатьОписаниеЗаполняемыхРеквизитовПоСтатистике(ОписаниеРеквизитов) Экспорт
	
	Параметры = ЗаполнениеОбъектовПоСтатистике.ПараметрыЗаполняемыхРеквизитов();
	Параметры.РазрезыСбораСтатистики.ИспользоватьВсегда = "Ответственный";
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов,
		"ВариантЗамены", Параметры);
			
КонецПроцедуры

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("НСИПроизводства");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.РазрешениеНаЗаменуМатериалов") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаАналогиМатериалов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаОбластиДействияАналоговМатериалов(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	
	
КонецПроцедуры

// Добавляет команду создания документа "Разрешение на замену материалов".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//  СтрокаТаблицыЗначений, Неопределено -
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РазрешениеНаЗаменуМатериалов) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РазрешениеНаЗаменуМатериалов.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.РазрешениеНаЗаменуМатериалов);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьАналогиМатериалов";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	
	
КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ОбластиДействия.ЗаказНаПроизводство В (ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство2_2.ПустаяСсылка), 
	//++ Устарело_Производство21
	|														 ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка), 
	//-- Устарело_Производство21
	|														 НЕОПРЕДЕЛЕНО)
	|				ТОГДА 0
	|			ИНАЧЕ 10000000
	|		КОНЕЦ + ВЫБОР
	|			КОГДА ОбластиДействия.ЗаказКлиента = ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ 1000000
	|		КОНЕЦ + ВЫБОР
	|			КОГДА ОбластиДействия.Спецификация = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ 100000
	|		КОНЕЦ + ВЫБОР
	|			КОГДА ОбластиДействия.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ 10000
	|		КОНЕЦ + ВЫБОР
	|			КОГДА ОбластиДействия.ХарактеристикаИзделия = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|				ТОГДА ВЫБОР
	|						КОГДА ОбластиДействия.Изделие = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|							ТОГДА 0
	|						ИНАЧЕ 100
	|					КОНЕЦ
	|			ИНАЧЕ 1000
	|		КОНЕЦ + ВЫБОР
	|			КОГДА ОбластиДействия.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ 10
	|		КОНЕЦ + ВЫБОР
	|			КОГДА ОбластиДействия.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК Приоритет
	|ИЗ
	|	Документ.РазрешениеНаЗаменуМатериалов.ОбластиДействия КАК ОбластиДействия
	|ГДЕ
	|	ОбластиДействия.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РазрешениеНаЗаменуМатериалов.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	РазрешениеНаЗаменуМатериалов.ДатаОкончанияДействия КАК ДатаОкончанияДействия,
	|	РазрешениеНаЗаменуМатериалов.Статус КАК Статус,
	|	РазрешениеНаЗаменуМатериалов.ВариантЗамены КАК ВариантЗамены,
	|	РазрешениеНаЗаменуМатериалов.ВзаимозаменяемыеАналоги КАК ВзаимозаменяемыеАналоги,
	|	РазрешениеНаЗаменуМатериалов.Проведен КАК Проведен
	|ИЗ
	|	Документ.РазрешениеНаЗаменуМатериалов КАК РазрешениеНаЗаменуМатериалов
	|ГДЕ
	|	РазрешениеНаЗаменуМатериалов.Ссылка = &Ссылка";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	Индекс = МассивРезультатов.ВГраница();
	
	РеквизитыШапки = МассивРезультатов[Индекс].Выбрать();
	РеквизитыТЧ = МассивРезультатов[Индекс - 1].Выбрать();
	
	РеквизитыШапки.Следующий();
	РеквизитыТЧ.Следующий();
	
	Запрос.УстановитьПараметр("Приоритет",                 РеквизитыТЧ.Приоритет);
	Запрос.УстановитьПараметр("Статус",                    РеквизитыШапки.Статус);
	Запрос.УстановитьПараметр("ДатаНачалаДействия",        РеквизитыШапки.ДатаНачалаДействия);
	Запрос.УстановитьПараметр("ДатаОкончанияДействия",     РеквизитыШапки.ДатаОкончанияДействия);
	Запрос.УстановитьПараметр("ВариантЗамены",             РеквизитыШапки.ВариантЗамены);
	Запрос.УстановитьПараметр("ВзаимозаменяемыеАналоги",   РеквизитыШапки.ВзаимозаменяемыеАналоги);
	Запрос.УстановитьПараметр("Проведен",                  РеквизитыШапки.Проведен);
		
КонецПроцедуры

Функция ТекстЗапросаТаблицаАналогиМатериалов(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "АналогиМатериалов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка КАК Разрешение,
	|	&ДатаНачалаДействия КАК Период,
	|	&ДатаОкончанияДействия КАК ПериодЗавершения,
	|	Материалы.Номенклатура КАК Номенклатура,
	|	Материалы.Характеристика КАК Характеристика,
	|	Материалы.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Материалы.КоличествоУпаковок КАК КоличествоУпаковок,
	|	Материалы.Упаковка КАК Упаковка,
	|	ВЫБОР
	|		КОГДА &ВариантЗамены = ЗНАЧЕНИЕ(Перечисление.ВариантЗаменыАналогов.СписокАналогов)
	|				ИЛИ &ВзаимозаменяемыеАналоги
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РолиАналогов.ВзаимозаменяемыйАналог)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РолиАналогов.Материал)
	|	КОНЕЦ КАК РольНоменклатуры,
	|	&Приоритет КАК МаксимальныйПриоритетОбластиДействия
	|ИЗ
	|	Документ.РазрешениеНаЗаменуМатериалов.Материалы КАК Материалы
	|ГДЕ
	|	Материалы.Ссылка = &Ссылка
	|	И &Проведен
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРазрешенийНаЗаменуМатериалов.Утверждено)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Ссылка,
	|	&ДатаНачалаДействия,
	|	&ДатаОкончанияДействия,
	|	Аналоги.Номенклатура,
	|	Аналоги.Характеристика,
	|	Аналоги.ИдентификаторСтроки,
	|	Аналоги.КоличествоУпаковок,
	|	Аналоги.Упаковка,
	|	ВЫБОР
	|		КОГДА &ВзаимозаменяемыеАналоги
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РолиАналогов.ВзаимозаменяемыйАналог)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РолиАналогов.Аналог)
	|	КОНЕЦ,
	|	&Приоритет
	|ИЗ
	|	Документ.РазрешениеНаЗаменуМатериалов.Аналоги КАК Аналоги
	|ГДЕ
	|	Аналоги.Ссылка = &Ссылка
	|	И &Проведен
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРазрешенийНаЗаменуМатериалов.Утверждено)
	|	И &ВариантЗамены = ЗНАЧЕНИЕ(Перечисление.ВариантЗаменыАналогов.НаборНаНабор)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаОбластиДействияАналоговМатериалов(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ОбластиДействияАналоговМатериалов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбластиДействия.Ссылка КАК Разрешение,
	|	ОбластиДействия.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ОбластиДействия.Подразделение КАК Подразделение,
	|	ОбластиДействия.Изделие КАК Изделие,
	|	ОбластиДействия.ХарактеристикаИзделия КАК ХарактеристикаИзделия,
	|	ОбластиДействия.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ОбластиДействия.Спецификация КАК Спецификация,
	|	ОбластиДействия.Этап КАК Этап,
	|	ОбластиДействия.ЗаказКлиента КАК ЗаказКлиента
	|ИЗ
	|	Документ.РазрешениеНаЗаменуМатериалов.ОбластиДействия КАК ОбластиДействия
	|ГДЕ
	|	ОбластиДействия.Ссылка = &Ссылка
	|	И &Проведен
	|	И &Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРазрешенийНаЗаменуМатериалов.Утверждено)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция ТекстЗапросаДинамическогоСпискаРазрешенийНаЗаменуМатериалов() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОсновнаяТаблица.Ссылка КАК Ссылка,
	|	ОсновнаяТаблица.ВерсияДанных КАК ВерсияДанных,
	|	ОсновнаяТаблица.ПометкаУдаления КАК ПометкаУдаления,
	|	ОсновнаяТаблица.Номер КАК Номер,
	|	ОсновнаяТаблица.Дата КАК Дата,
	|	ОсновнаяТаблица.Проведен КАК Проведен,
	|	ОсновнаяТаблица.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	ОсновнаяТаблица.ДатаОкончанияДействия КАК ДатаОкончанияДействия,
	|	ОсновнаяТаблица.Статус КАК Статус,
	|	ОсновнаяТаблица.УказаниеПоПрименению КАК УказаниеПоПрименению,
	|	ОсновнаяТаблица.Ответственный КАК Ответственный
	|ИЗ
	|	Документ.РазрешениеНаЗаменуМатериалов КАК ОсновнаяТаблица";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Устанавливает отбор по номенклатуре в динамическом списке.
//
// Параметры:
//  Список       - ДинамическийСписок - оформляемый динамический список.
//  Номенклатура - СправочникСсылка.Номенклатура - номенклатура отбора.
//  Назначение   - ПеречислениеСсылка.ИспользованиеНоменклатурыВНСИПроизводства - назначение отбора.
//
Процедура УстановитьОтборПоНоменклатуреВДинамическомСпискеРазрешенийНаЗаменуМатериалов(Список, Номенклатура, Назначение = Неопределено) Экспорт
	
	ОтборПоНоменклатуре = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы,
		НСтр("ru = 'Отбор по номенклатуре';
			|en = 'Filter by items'"),
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ОтборПоНоменклатуре,
		"Ссылка.ОбластиДействия.Изделие",
		ВидСравненияКомпоновкиДанных.Равно,
		Номенклатура,
		,
		ЗначениеЗаполнено(Номенклатура) И (НЕ ЗначениеЗаполнено(Назначение) ИЛИ Назначение = Перечисления.ИспользованиеНоменклатурыВНСИПроизводства.Изделие));
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ОтборПоНоменклатуре,
		"Ссылка.Материалы.Номенклатура",
		ВидСравненияКомпоновкиДанных.Равно,
		Номенклатура,
		,
		ЗначениеЗаполнено(Номенклатура) И (НЕ ЗначениеЗаполнено(Назначение) ИЛИ Назначение = Перечисления.ИспользованиеНоменклатурыВНСИПроизводства.Материал));
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ОтборПоНоменклатуре,
		"Ссылка.Аналоги.Номенклатура",
		ВидСравненияКомпоновкиДанных.Равно,
		Номенклатура,
		,
		ЗначениеЗаполнено(Номенклатура) И (НЕ ЗначениеЗаполнено(Назначение) ИЛИ Назначение = Перечисления.ИспользованиеНоменклатурыВНСИПроизводства.Аналог));
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.РазрешениеНаЗаменуМатериалов";
	СинонимТаблицыДокумента = "РазрешениеНаЗаменуМатериаловМатериалы";
	
	Если ИмяРегистра = "АналогиМатериалов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаАналогиМатериалов(Запрос, ТекстыЗапроса, ИмяРегистра);
		
	Иначе
		
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
									ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента);
	
	Возврат Результат;
	
КонецФункции	

#КонецОбласти

#КонецОбласти

#КонецЕсли
