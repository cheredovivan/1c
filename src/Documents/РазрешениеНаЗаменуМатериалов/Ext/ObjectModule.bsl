#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокумент();
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством2_2") Тогда
		
		Для Каждого СтрокаОбластиДействия Из ОбластиДействия Цикл
		
			Если ЗначениеЗаполнено(СтрокаОбластиДействия.ЗаказКлиента) Тогда
				СтрокаОбластиДействия.ЗаказКлиента = Документы.ЗаказКлиента.ПустаяСсылка();
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаОбластиДействия.ЗаказНаПроизводство) 
				И ТипЗнч(СтрокаОбластиДействия.ЗаказНаПроизводство) <> Тип("ДокументСсылка.ЗаказНаПроизводство2_2") Тогда
				СтрокаОбластиДействия.ЗаказНаПроизводство = Неопределено;
			КонецЕсли;
		
		КонецЦикла;
		
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из Материалы Цикл
		СтрокаТаблицы.ИдентификаторСтроки = Новый УникальныйИдентификатор;
	КонецЦикла;
	
	Для каждого СтрокаТаблицы Из Аналоги Цикл
		СтрокаТаблицы.ИдентификаторСтроки = Новый УникальныйИдентификатор;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	ПропуститьЗаполнениеПоСтатистике = Ложь;
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Аналог") Тогда
			НоваяСтрока = Аналоги.Добавить();
			НоваяСтрока.Номенклатура = ДанныеЗаполнения.Аналог;
			НоваяСтрока.ИдентификаторСтроки = Новый УникальныйИдентификатор;
			
			ПропуститьЗаполнениеПоСтатистике = Истина;
			ВариантЗамены = Перечисления.ВариантЗаменыАналогов.НаборНаНабор;
			
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Материалы") Тогда
			
			Для каждого СтрокаМатериал Из ДанныеЗаполнения.Материалы Цикл
				
				СтрокаТаблицы = Материалы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаМатериал);
				
				Если Не ЗначениеЗаполнено(СтрокаТаблицы.ИдентификаторСтроки) Тогда
					СтрокаТаблицы.ИдентификаторСтроки = Новый УникальныйИдентификатор;
				КонецЕсли;
				
			КонецЦикла;
			
			ПропуститьЗаполнениеПоСтатистике = Истина;
			ВариантЗамены = Перечисления.ВариантЗаменыАналогов.НаборНаНабор;
			
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Спецификация") Тогда
			СтрокаОбластиДействия = ОбластиДействия.Добавить();
			СтрокаОбластиДействия.Спецификация = ДанныеЗаполнения.Спецификация;
			
			ПропуститьЗаполнениеПоСтатистике = Истина;
			ВариантЗамены = Перечисления.ВариантЗаменыАналогов.НаборНаНабор;
			
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("ВзаимозаменяемыеАналоги")
		   И ДанныеЗаполнения.Свойство("ДокументРазрешения")
		   И ДанныеЗаполнения.ВзаимозаменяемыеАналоги Тогда
		   
			Реквизиты = "ВариантЗамены, Материалы, Аналоги, ОбластиДействия";
			СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения.ДокументРазрешения, Реквизиты);
			ВариантЗамены = СтруктураРеквизитов.ВариантЗамены;
			Материалы.Загрузить(СтруктураРеквизитов.Аналоги.Выгрузить());
			Аналоги.Загрузить(СтруктураРеквизитов.Материалы.Выгрузить());
			ОбластиДействия.Загрузить(СтруктураРеквизитов.ОбластиДействия.Выгрузить());
			
			ПропуститьЗаполнениеПоСтатистике = Истина;
			
		КонецЕсли;
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
		
		СтрокаОбластиДействия = ОбластиДействия.Добавить();
		СтрокаОбластиДействия.Спецификация = ДанныеЗаполнения;
		
		ПропуститьЗаполнениеПоСтатистике = Истина;
		ВариантЗамены = Перечисления.ВариантЗаменыАналогов.НаборНаНабор;
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказНаПроизводство2_2")
			//++ Устарело_Производство21
				ИЛИ ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказНаПроизводство")
			//-- Устарело_Производство21 
			Тогда
			
		СтрокаОбластиДействия = ОбластиДействия.Добавить();
		СтрокаОбластиДействия.ЗаказНаПроизводство = ДанныеЗаполнения;
		
		ПропуститьЗаполнениеПоСтатистике = Истина;
		ВариантЗамены = Перечисления.ВариантЗаменыАналогов.НаборНаНабор;
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		
		ИспользоватьОбособленноеОбеспечениеЗаказов = ПолучитьФункциональнуюОпцию("ИспользоватьОбособленноеОбеспечениеЗаказов");
		
		Если Не ИспользоватьОбособленноеОбеспечениеЗаказов Тогда
			ТекстИсключения = НСтр("ru = 'Ввод разрешений на замену возможен при использовании обособленного обеспечения заказов.';
									|en = 'You can enter the substitution permissions when using assigned order fulfillment.'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		Если Не ЕстьТоварыПодЗаказ(ДанныеЗаполнения) Тогда
			ТекстИсключения = НСтр("ru = 'Ввод разрешений на замену возможен при наличии товаров с действием ""К обеспечению"" или ""Резервировать по мере поступления"" и с установленным флагом ""Обособленно"".';
									|en = 'You can enter the substitution permissions if there are goods with the ""To supply"" or ""Reserve upon receipt"" action and with the ""Assigned"" check box selected.'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		СтрокаОбластиДействия = ОбластиДействия.Добавить();
		СтрокаОбластиДействия.ЗаказКлиента = ДанныеЗаполнения;
		
		ПропуститьЗаполнениеПоСтатистике = Истина;
		ВариантЗамены = Перечисления.ВариантЗаменыАналогов.НаборНаНабор;
		
	КонецЕсли;
	
	Если ОбластиДействия.Количество() = 0 Тогда
		ОбластиДействия.Добавить();
	КонецЕсли;
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
	Если Не ПропуститьЗаполнениеПоСтатистике Тогда
	
		ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	
		Если Не ЗначениеЗаполнено(ВариантЗамены) Тогда
			ВариантЗамены = Перечисления.ВариантЗаменыАналогов.НаборНаНабор;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	// Дата окончания действия соглашения должна быть не меньше, чем дата начала действия.
	Если ЗначениеЗаполнено(ДатаНачалаДействия) И ЗначениеЗаполнено(ДатаОкончанияДействия) Тогда
		
		Если ДатаНачалаДействия > ДатаОкончанияДействия Тогда
			
			ТекстОшибки = НСтр("ru = 'Дата окончания действия разрешения должна быть не меньше даты начала';
								|en = 'Permission validity expiration date must be greater than the start date'");
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект, 
				"ДатаОкончанияДействия",
				,
				Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Статус <> Перечисления.СтатусыРазрешенийНаЗаменуМатериалов.Прекращено Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаОкончанияДействия");
	КонецЕсли;
	
	Если ВариантЗамены = Перечисления.ВариантЗаменыАналогов.СписокАналогов Тогда

		МассивНепроверяемыхРеквизитов.Добавить("Аналоги");
		МассивНепроверяемыхРеквизитов.Добавить("Аналоги.Номенклатура");
		МассивНепроверяемыхРеквизитов.Добавить("Аналоги.Характеристика");
		МассивНепроверяемыхРеквизитов.Добавить("Материалы.КоличествоУпаковок");
		
		ПроверитьДублиНабора("Материалы", НСтр("ru = 'Обнаружено дублирование строк в наборе материалов.';
												|en = 'Duplicate lines are found in the material set.'"), Отказ);
		
		Если Материалы.Количество() < 2 Тогда
		
			ТекстОшибки = НСтр("ru = 'В списке аналогов должно быть указано более одной строки';
								|en = 'The list of substitutes must have more than one line'");
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект, 
				"Материалы",
				,
				Отказ);
		
		КонецЕсли;
		
	ИначеЕсли ВариантЗамены = Перечисления.ВариантЗаменыАналогов.НаборНаНабор Тогда 
		
		ПроверитьДублиНабора("Материалы", НСтр("ru = 'Обнаружено дублирование строк в наборе материалов.';
												|en = 'Duplicate lines are found in the material set.'"), Отказ);
		ПроверитьДублиНабора("Аналоги", НСтр("ru = 'Обнаружено дублирование строк в наборе аналогов.';
											|en = 'Duplicate lines are found in the list of substitutes.'"), Отказ);
		
	КонецЕсли;
	
	Если ВариантЗамены <> Перечисления.ВариантЗаменыАналогов.СписокАналогов Тогда
		ПроверитьИдентичностьНаборов(Отказ);
		ПроверитьДублиОбластиДействия(Отказ);
		ПроверитьЗаполнениеКоличестваВТабЧасти("Материалы", Отказ);
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Материалы.КоличествоУпаковок");
	МассивНепроверяемыхРеквизитов.Добавить("Аналоги.КоличествоУпаковок");
		
	ПроверитьЗаполнениеКоличестваВТабЧасти("Аналоги", Отказ);
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИмяТЧ = "Материалы";
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
	ПараметрыПроверки.ИмяТЧ = "Аналоги";
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПустойУникальныйИдентификатор = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
	НайденныеСтроки = Материалы.НайтиСтроки(Новый Структура("ИдентификаторСтроки", ПустойУникальныйИдентификатор));
	Для каждого СтрокаТаблицы Из НайденныеСтроки Цикл
		СтрокаТаблицы.ИдентификаторСтроки = Новый УникальныйИдентификатор;
	КонецЦикла;
	
	НайденныеСтроки = Аналоги.НайтиСтроки(Новый Структура("ИдентификаторСтроки", ПустойУникальныйИдентификатор));
	Для каждого СтрокаТаблицы Из НайденныеСтроки Цикл
		СтрокаТаблицы.ИдентификаторСтроки = Новый УникальныйИдентификатор;
	КонецЦикла;
	
	Если ВариантЗамены = Перечисления.ВариантЗаменыАналогов.СписокАналогов Тогда
		
		ПустаяУпаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
		Аналоги.Очистить();
		
		Для Каждого СтрокаТаблицы Из Материалы Цикл
			
			СтрокаТаблицы.КоличествоУпаковок = 1;
			СтрокаТаблицы.Упаковка = ПустаяУпаковка;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ВзаимозаменяемыеАналоги
	   И (Материалы.Количество() > 1
	 ИЛИ Аналоги.Количество() > 1) Тогда
	 	
	 	ВзаимозаменяемыеАналоги = Ложь;
	 	
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Ответственный			= Пользователи.АвторизованныйПользователь();
	ДатаНачалаДействия		= НачалоДня(ТекущаяДатаСеанса());
	Статус					= Перечисления.СтатусыРазрешенийНаЗаменуМатериалов.Создано;
		
КонецПроцедуры

Функция ЕстьТоварыПодЗаказ(Ссылка)
	
	СтруктураОтбора = Новый Структура("ВариантОбеспечения, Обособленно", 
									   Перечисления.ВариантыОбеспечения.РезервироватьПоМереПоступления, Истина);
	
	НайденныеСтроки = Ссылка.Товары.НайтиСтроки(СтруктураОтбора);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		СтруктураОтбора = Новый Структура("ВариантОбеспечения, Обособленно", 
										   Перечисления.ВариантыОбеспечения.КОбеспечению, Истина);
		
		НайденныеСтроки = Ссылка.Товары.НайтиСтроки(СтруктураОтбора);
	КонецЕсли;

	Если НайденныеСтроки.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ПроверитьДублиНабора(ИмяНабора, Сообщение, Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Набор.Номенклатура,
	|	Набор.Характеристика,
	|	Набор.НомерСтроки
	|ПОМЕСТИТЬ ВТНабор
	|ИЗ
	|	&Набор КАК Набор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Набор.Номенклатура,
	|	Набор.Характеристика,
	|	МАКСИМУМ(Набор.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	ВТНабор КАК Набор
	|
	|СГРУППИРОВАТЬ ПО
	|	Набор.Номенклатура,
	|	Набор.Характеристика
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Набор.НомерСтроки) > 1";
	
	Запрос.УстановитьПараметр("Набор", ЭтотОбъект[ИмяНабора]);
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОбщегоНазначения.СообщитьПользователю(
			Сообщение,
			ЭтотОбъект,
			ИмяНабора + "[" + (Выборка.НомерСтроки - 1) + "].Номенклатура",
			"Объект",
			Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеКоличестваВТабЧасти(ИмяТЧ, Отказ)
	
	ТаблицаОбъекта = ЭтотОбъект[ИмяТЧ]; // ДокументТабличнаяЧасть.РазрешениеНаЗаменуМатериалов.Материалы, ДокументТабличнаяЧасть.РазрешениеНаЗаменуМатериалов.Аналоги - 
	
	Для каждого Строка Из ТаблицаОбъекта Цикл
		
		Если Строка.КоличествоУпаковок = 0 Тогда
			
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
				"Колонка", "Заполнение", НСтр("ru = 'Количество';
												|en = 'Quantity'"), Строка.НомерСтроки, ИмяТЧ);
				
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТЧ, Строка.НомерСтроки, "КоличествоУпаковок"),,
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьИдентичностьНаборов(Отказ)
	
	Если Не Отказ
	   И Материалы.Количество() = Аналоги.Количество() Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Материалы.Номенклатура КАК Номенклатура,
		|	Материалы.Характеристика КАК Характеристика,
		|	Материалы.Упаковка КАК Упаковка,
		|	Материалы.КоличествоУпаковок КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_Материалы
		|ИЗ
		|	&Материалы КАК Материалы
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Аналоги.Номенклатура КАК Номенклатура,
		|	Аналоги.Характеристика КАК Характеристика,
		|	Аналоги.Упаковка КАК Упаковка,
		|	Аналоги.КоличествоУпаковок КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_Аналоги
		|ИЗ
		|	&Аналоги КАК Аналоги
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИСТИНА КАК Флаг,
		|	&ТекстЗапросаКоэффициентУпаковкиМатериала Как Поле1
		|ИЗ
		|	ВТ_Материалы КАК ВТ_Материалы
		|ГДЕ
		|	НЕ ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					ВТ_Аналоги КАК ВТ_Аналоги
		|				ГДЕ
		|					ВТ_Аналоги.Номенклатура = ВТ_Материалы.Номенклатура
		|					И ВТ_Аналоги.Характеристика = ВТ_Материалы.Характеристика
		|					И ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковкиАналога, 1) * ВТ_Аналоги.КоличествоУпаковок 
		|						= ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковкиМатериала, 1) * ВТ_Материалы.КоличествоУпаковок)";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ТекстЗапросаКоэффициентУпаковкиАналога",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ВТ_Аналоги.Упаковка",
			"ВТ_Аналоги.Номенклатура"));
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ТекстЗапросаКоэффициентУпаковкиМатериала",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ВТ_Материалы.Упаковка",
			"ВТ_Материалы.Номенклатура"));
		
		Запрос.УстановитьПараметр("Материалы", Материалы);
		Запрос.УстановитьПараметр("Аналоги", Аналоги);
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			
			ТекстСообщения = НСтр("ru = 'Обнаружено дублирование наборов материалов и аналогов.';
									|en = 'Duplicate substitute and material sets are found.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,, Отказ);
		
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьДублиОбластиДействия(Отказ)
	
	ТекстСообщения = НСтр("ru = 'Обнаружено дублирование строк в области действия.';
							|en = 'Duplicate lines are found in the scope.'");
	ИмяНабора = "ОбластиДействия";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Набор.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Набор.Подразделение КАК Подразделение,
	|	Набор.Изделие КАК Изделие,
	|	Набор.ХарактеристикаИзделия КАК ХарактеристикаИзделия,
	|	Набор.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Набор.Спецификация КАК Спецификация,
	|	Набор.Этап КАК Этап,
	|	Набор.ЗаказКлиента КАК ЗаказКлиента,
	|	Набор.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТНабор
	|ИЗ
	|	&Набор КАК Набор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(Набор.НомерСтроки) КАК НомерСтроки,
	|	Набор.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Набор.Подразделение КАК Подразделение,
	|	Набор.Изделие КАК Изделие,
	|	Набор.ХарактеристикаИзделия КАК ХарактеристикаИзделия,
	|	Набор.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Набор.Спецификация КАК Спецификация,
	|	Набор.Этап КАК Этап,
	|	Набор.ЗаказКлиента КАК ЗаказКлиента
	|ИЗ
	|	ВТНабор КАК Набор
	|
	|СГРУППИРОВАТЬ ПО
	|	Набор.ЗаказНаПроизводство,
	|	Набор.НаправлениеДеятельности,
	|	Набор.ЗаказКлиента,
	|	Набор.Этап,
	|	Набор.Спецификация,
	|	Набор.ХарактеристикаИзделия,
	|	Набор.Изделие,
	|	Набор.Подразделение
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Набор.НомерСтроки) > 1";
	
	Запрос.УстановитьПараметр("Набор", ЭтотОбъект[ИмяНабора]);
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстСообщения,
			ЭтотОбъект,
			ИмяНабора + "[" + (Выборка.НомерСтроки - 1) + "].НаправлениеДеятельности",
			"Объект",
			Отказ);
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
