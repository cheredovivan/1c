
#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения; //используется механизмом обработки изменения реквизитов ТЧ

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);

	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ПриЧтенииСозданииНаСервере();
		
	КонецЕсли;
	
	УправлениеВидимостью(ЭтотОбъект);
	
	УстановитьДоступностьКомандБуфераОбмена(ЭтотОбъект, РаботаСТабличнымиЧастями.ЕстьСтрокиВБуфереОбмена());
	
	НастроитьВидимостьОбластиДействия(ЭтотОбъект);
	НастроитьВидимостьСтраницыЗамены(ЭтотОбъект);
	
	//++ Устарело_Производство21
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством2_2") Тогда
	//-- Устарело_Производство21	
	
		ЕстьУстаревшийОбъект = Ложь;
		
		Для каждого СтрокаОбластиДействия Из Объект.ОбластиДействия Цикл
			
			Если ЗначениеЗаполнено(СтрокаОбластиДействия.ЗаказКлиента) 
			 ИЛИ ЗначениеЗаполнено(СтрокаОбластиДействия.ЗаказНаПроизводство) 
			   И ТипЗнч(СтрокаОбластиДействия.ЗаказНаПроизводство) <> Тип("ДокументСсылка.ЗаказНаПроизводство2_2") Тогда
			
				ЕстьУстаревшийОбъект = Истина;
				Прервать;
			
			КонецЕсли;	
		
		КонецЦикла;
		
		Если ЕстьУстаревшийОбъект Тогда
			Элементы.ОбластиДействия.ТолькоПросмотр = Истина;
			Элементы.ОбластиДействияПодменюЗаполнить.Доступность = Ложь;
		Иначе
			Элементы.ОбластиДействияЗаказНаПроизводство.ОграничениеТипа = Новый ОписаниеТипов("ДокументСсылка.ЗаказНаПроизводство2_2");
		КонецЕсли;
		
	//++ Устарело_Производство21
	Иначе
		Элементы.ГруппаМножественнаяОбластьДействия.Видимость = Ложь;
		Элементы.МножественноеУказание.Видимость = Ложь;
		Элементы.ГруппаВариантЗамены.Видимость = Ложь;
		Элементы.ГруппаЗаменаСписок.Видимость = Ложь;
		Элементы.ГруппаЗаменаНабор.Видимость = Истина;
	КонецЕсли;
	//-- Устарело_Производство21
	
	ВариантЗаменыДоИзменения = Объект.ВариантЗамены;
	МножественноеУказание = НСтр("ru = 'Настроить множественное указание области действия';
								|en = 'Configure specifying multiple scopes'");
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ИнтеграцияС1СДокументооборотом

	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Справочник.РесурсныеСпецификации.Форма.ПодборМатериаловПоСпецификации" Тогда
		
		Если Не ЭтоАдресВременногоХранилища(ВыбранноеЗначение) Тогда
			Возврат;
		КонецЕсли;
		
		Модифицированность = Истина;
		
		ОбработатьПодборМатериаловПоСпецификации(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.РазрешениеНаЗаменуМатериалов.Форма.ФормаВводаОбластиДействия" Тогда
		
		Модифицированность = Истина;
		
		ЗначениеВыбора = ВыбранноеЗначение.ЗначениеВыбора;
		
		Если Не ЗначениеВыбора.МножественноеУказание Тогда
	
			СписокСвойств = "НаправлениеДеятельности,"
						+ "ЗаказНаПроизводство,"
						+ "ЗаказКлиента,"
						+ "Подразделение,"
						+ "Спецификация,"
						+ "Этап,"
						+ "Изделие,"
						+ "ХарактеристикаИзделия";
						
			Если Объект.ОбластиДействия.Количество() > 0 Тогда
				
				СпецификацияДоВыбора = Объект.ОбластиДействия[0].Спецификация;
				ЭтапДоВыбора = Объект.ОбластиДействия[0].Этап;
				
				Если СпецификацияДоВыбора <> ЗначениеВыбора.Спецификация Или ЭтапДоВыбора <> ЗначениеВыбора.Этап Тогда
					Объект.Материалы.Очистить();
				КонецЕсли;
				
				Объект.ОбластиДействия.Очистить();
				
			КонецЕсли;
			
			СтрокаОбластиДействия = Объект.ОбластиДействия.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаОбластиДействия, ЗначениеВыбора, СписокСвойств);
			
		Иначе
			
			ЕстьОтличиеВНастройке = Ложь;
			Для Каждого СтрокаТЧ Из Объект.ОбластиДействия Цикл
				
				Если СтрокаТЧ.Этап <> ЗначениеВыбора.Этап
				   И Не ЕстьОтличиеВНастройке Тогда
				
					ЕстьОтличиеВНастройке = Истина;
					Прервать;
				
				КонецЕсли;
				
				Если СтрокаТЧ.Спецификация <> ЗначениеВыбора.Спецификация
				   И Не ЕстьОтличиеВНастройке Тогда
				
					ЕстьОтличиеВНастройке = Истина;
					Прервать;
				
				КонецЕсли;

			КонецЦикла;

			Если ЕстьОтличиеВНастройке Тогда
				Объект.Материалы.Очистить();
			КонецЕсли;
			
			СписокСвойств = "Спецификация, Этап, Изделие, ХарактеристикаИзделия";
			ТекущиеДанные = Элементы.ОбластиДействия.ТекущиеДанные;
			ЗаполнитьЗначенияСвойств(ТекущиеДанные, ЗначениеВыбора, СписокСвойств);
			ЗаполнитьПредставлениеИзделия(ТекущиеДанные);
		
		КонецЕсли;
		
		ОбновитьПредставлениеОбластиДействия();
		
		УправлениеВидимостью(ЭтотОбъект);
		ОпределитьДоступностьВариантаЗамены(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "КопированиеСтрокВБуферОбмена" Тогда
		УстановитьДоступностьКомандБуфераОбмена(ЭтотОбъект, Истина);
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьСлужебныеРеквизиты();
	НастроитьЭлементыФормы();

	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтотОбъект, ПараметрыЗаписи);

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредставлениеОбластиДействияНажатие(Элемент, СтандартнаяОбработка)
	
	ОчиститьСообщения();
	СтандартнаяОбработка = Ложь;
	
	Если Объект.ВариантЗамены = ПредопределенноеЗначение("Перечисление.ВариантЗаменыАналогов.СписокАналогов") Тогда
	
		ТекстСообщения = НСтр("ru = 'Указание области действия недоступно для варианта замены ""Список аналогов""';
								|en = 'Cannot specify the scope for the ""Substitute list"" replacement option'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	
	КонецЕсли;
	
	Если Объект.ОбластиДействия.Количество() = 0 Тогда
		СтрокаОбластиДействия = Объект.ОбластиДействия.Добавить();
	Иначе
		СтрокаОбластиДействия = Объект.ОбластиДействия[0];
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("НаправлениеДеятельности", СтрокаОбластиДействия.НаправлениеДеятельности);
	ПараметрыФормы.Вставить("ЗаказНаПроизводство", СтрокаОбластиДействия.ЗаказНаПроизводство);
	ПараметрыФормы.Вставить("ЗаказКлиента", СтрокаОбластиДействия.ЗаказКлиента);
	ПараметрыФормы.Вставить("Подразделение", СтрокаОбластиДействия.Подразделение);
	ПараметрыФормы.Вставить("Спецификация", СтрокаОбластиДействия.Спецификация);
	ПараметрыФормы.Вставить("Этап", СтрокаОбластиДействия.Этап);
	ПараметрыФормы.Вставить("Изделие", СтрокаОбластиДействия.Изделие);
	ПараметрыФормы.Вставить("ХарактеристикаИзделия", СтрокаОбластиДействия.ХарактеристикаИзделия);
	ПараметрыФормы.Вставить("КоличествоМатериалов", Объект.Материалы.Количество());
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыФормы.Вставить("МножественноеУказаниеОбластиДействия", Ложь);
	
	ОткрытьФорму("Документ.РазрешениеНаЗаменуМатериалов.Форма.ФормаВводаОбластиДействия", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОснованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования,
		ЭтотОбъект,
		"Объект.УказаниеПоПрименению",
		НСтр("ru = 'Указание по применению';
			|en = 'Usage note'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	
	Если Объект.Статус 
			= ПредопределенноеЗначение("Перечисление.СтатусыРазрешенийНаЗаменуМатериалов.Утверждено")
		И Не ЗначениеЗаполнено(Объект.ДатаНачалаДействия) Тогда
		
		Объект.ДатаНачалаДействия = ОбщегоНазначенияКлиент.ДатаСеанса();
		
	ИначеЕсли Объект.Статус 
			= ПредопределенноеЗначение("Перечисление.СтатусыРазрешенийНаЗаменуМатериалов.Прекращено") Тогда
			
		Объект.ДатаОкончанияДействия = ОбщегоНазначенияКлиент.ДатаСеанса();
		
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "Статус");
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВариантЗаменыПриИзменении(Элемент)
	
	ОчиститьСообщения();
	ЗадатьВопрос = Ложь;
	ИзменениеДоступно = Истина;
	ВариантСписок = ПредопределенноеЗначение("Перечисление.ВариантЗаменыАналогов.СписокАналогов");
	
	Если ВариантЗаменыДоИзменения <> ВариантСписок
	   И Объект.ВариантЗамены = ВариантСписок Тогда
	
		ЕстьСтрокиАналогов = Объект.Аналоги.Количество() > 0;
		
		Если ЕстьСтрокиАналогов Тогда
			
			ТекстВопроса = НСтр("ru = 'Изменение варианта замены приведет к невозможности ввода области действия и потере данных набора аналогов. Продолжить?';
								|en = 'If you change the replacement option, you will not be able to enter the scope and will lose the substitute set data. Continue?'");
			ЗадатьВопрос = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗадатьВопрос Тогда
	
		Обещание = ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Результат = Ждать Обещание;
		
		Если Результат = КодВозвратаДиалога.Да Тогда
			ВыполнитьДействияПриИзмененииВариантаЗамены();
		Иначе
			Объект.ВариантЗамены = ВариантЗаменыДоИзменения;
		КонецЕсли;
		
	ИначеЕсли ИзменениеДоступно Тогда
		
		ВыполнитьДействияПриИзмененииВариантаЗамены();
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МножественноеУказаниеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Элементы.ГруппаМножественнаяОбластьДействия.Видимость = Истина;
	Элементы.МножественноеУказание.Видимость = Ложь;
	Элементы.ПредставлениеОбластиДействия.Видимость = Ложь;
	
	Если Объект.ОбластиДействия.Количество() > 0 Тогда
		
		СтрокаОбластиДействия = Объект.ОбластиДействия[0];
		ЗаполнитьПредставлениеИзделия(СтрокаОбластиДействия);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбластиДействияИзделиеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ОбластиДействия.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("НаправлениеДеятельности", ТекущиеДанные.НаправлениеДеятельности);
	ПараметрыФормы.Вставить("ЗаказНаПроизводство", ТекущиеДанные.ЗаказНаПроизводство);
	ПараметрыФормы.Вставить("ЗаказКлиента", ТекущиеДанные.ЗаказКлиента);
	ПараметрыФормы.Вставить("Подразделение", ТекущиеДанные.Подразделение);
	ПараметрыФормы.Вставить("Спецификация", ТекущиеДанные.Спецификация);
	ПараметрыФормы.Вставить("Этап", ТекущиеДанные.Этап);
	ПараметрыФормы.Вставить("Изделие", ТекущиеДанные.Изделие);
	ПараметрыФормы.Вставить("ХарактеристикаИзделия", ТекущиеДанные.ХарактеристикаИзделия);
	ПараметрыФормы.Вставить("КоличествоМатериалов", Объект.Материалы.Количество());
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыФормы.Вставить("МножественноеУказаниеОбластиДействия", Истина);
	
	ОткрытьФорму("Документ.РазрешениеНаЗаменуМатериалов.Форма.ФормаВводаОбластиДействия", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМатериалы

&НаКлиенте
Процедура МатериалыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		ЗаполнитьИдентификаторСтроки(Элементы.Материалы.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		ЗаполнитьИдентификаторСтроки(Элементы.Материалы.ТекущиеДанные);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Материалы.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	ДобавитьВСтруктуруДействияПриИзмененииНоменклатуры(СтруктураДействий, ТекущаяСтрока, "Материалы");
	
	ПакетнаяОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура Материалы_ПодобратьПоСпецификации(Команда)
	
	ОткрытьФорму(
		"Справочник.РесурсныеСпецификации.Форма.ПодборМатериаловПоСпецификации",
		ПараметрыПодбораПоСпецификации(),
		ЭтотОбъект, 
		УникальныйИдентификатор,,,, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Материалы_ВставитьСтроки(Команда)
	
	ПолучитьСтрокиИзБуфераОбмена("Материалы");
	
КонецПроцедуры

&НаКлиенте
Процедура Материалы_СкопироватьСтроки(Команда)
	
	СкопироватьСтрокиТЧ("Материалы", "Материалы");
	
КонецПроцедуры

&НаКлиенте
Процедура Материалы_ЗагрузитьИзВнешнегоФайла(Команда)
	
	ПараметрыЗагрузки = РаботаСТабличнымиЧастямиКлиент.ПараметрыЗагрузкиНоменклатуры();
	ПараметрыЗагрузки.ЗагружатьКоличество = Истина;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗаполнитьМатериалы", Истина);
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьИзВнешнегоФайлаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РаботаСТабличнымиЧастямиКлиент.ПоказатьФормуЗагрузкиНоменклатуры(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыПриИзменении(Элемент)
	
	УстановитьДоступностьФлагаВзаимозамены(ЭтотОбъект);
	
	Если Объект.Материалы.Количество() > 1
	 ИЛИ Объект.Аналоги.Количество() > 1 Тогда
	
		Объект.ВзаимозаменяемыеАналоги = Ложь;
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАналоги

&НаКлиенте
Процедура АналогиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		ЗаполнитьИдентификаторСтроки(Элементы.Аналоги.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АналогиПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		ЗаполнитьИдентификаторСтроки(Элементы.Аналоги.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АналогиНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Аналоги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	ДобавитьВСтруктуруДействияПриИзмененииНоменклатуры(СтруктураДействий, ТекущаяСтрока, "Аналоги");
	
	ПакетнаяОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура Аналоги_ВставитьСтроки(Команда)
	
	ПолучитьСтрокиИзБуфераОбмена("Аналоги");
	
КонецПроцедуры

&НаКлиенте
Процедура Аналоги_СкопироватьСтроки(Команда)
	
	СкопироватьСтрокиТЧ("Аналоги", "Аналоги");
	
КонецПроцедуры

&НаКлиенте
Процедура Аналоги_ЗагрузитьИзВнешнегоФайла(Команда)
	
	ПараметрыЗагрузки = РаботаСТабличнымиЧастямиКлиент.ПараметрыЗагрузкиНоменклатуры();
	ПараметрыЗагрузки.ЗагружатьКоличество = Истина;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗаполнитьМатериалы", Ложь);
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьИзВнешнегоФайлаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РаботаСТабличнымиЧастямиКлиент.ПоказатьФормуЗагрузкиНоменклатуры(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура АналогиПриИзменении(Элемент)

	УстановитьДоступностьФлагаВзаимозамены(ЭтотОбъект);
	
	Если Объект.Материалы.Количество() > 1
	 ИЛИ Объект.Аналоги.Количество() > 1 Тогда
	
		Объект.ВзаимозаменяемыеАналоги = Ложь;
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМатериалыСписокАналогов

&НаКлиенте
Процедура МатериалыСписокАналоговНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.МатериалыСписокАналогов.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	
	ДобавитьВСтруктуруДействияПриИзмененииНоменклатуры(СтруктураДействий, ТекущаяСтрока, "Материалы");
	
	ПакетнаяОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыСписокАналоговПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		
		ТекущиеДанные = Элементы.МатериалыСписокАналогов.ТекущиеДанные;
		ТекущиеДанные.КоличествоУпаковок = 1;
		ЗаполнитьИдентификаторСтроки(ТекущиеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыСписокАналоговПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		ЗаполнитьИдентификаторСтроки(Элементы.МатериалыСписокАналогов.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыСписокАналогов_СкопироватьСтроки(Команда)
	
	СкопироватьСтрокиТЧ("Материалы", "МатериалыСписокАналогов");
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыСписокАналогов_ВставитьСтроки(Команда)
	
	ПолучитьСтрокиИзБуфераОбмена("Материалы");
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыСписокАналогов_ЗагрузитьИзВнешнегоФайла(Команда)
	
	ПараметрыЗагрузки = РаботаСТабличнымиЧастямиКлиент.ПараметрыЗагрузкиНоменклатуры();
	ПараметрыЗагрузки.ЗагружатьКоличество = Ложь;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗаполнитьМатериалы", Истина);
		
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьИзВнешнегоФайлаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РаботаСТабличнымиЧастямиКлиент.ПоказатьФормуЗагрузкиНоменклатуры(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбластиДействия

&НаКлиенте
Процедура ОбластиДействияПослеУдаления(Элемент)
	
	Если Объект.ОбластиДействия.Количество() = 0 Тогда
		Объект.ОбластиДействия.Добавить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбластиДействияПриИзменении(Элемент)
	
	ОпределитьДоступностьВариантаЗамены(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовСтраницыДополнительно

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "Комментарий");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервалВыполнить(Команда)
	
	ОбщегоНазначенияУТКлиент.РедактироватьПериод(Объект, 
		Новый Структура("ДатаНачала, ДатаОкончания", "ДатаНачалаДействия", "ДатаОкончанияДействия"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбластиДействия_ПодобратьЗаказыНаПроизводство(Команда)
	
	ТипыОбъекта = Новый Массив;
	ТипыОбъекта.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство2_2"));
	
	НастройкиПодбора = Новый Структура;
	НастройкиПодбора.Вставить("ТипыОбъекта", ТипыОбъекта);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодборЗаказовНаПроизводство", Истина);
	
	ОткрытьФорму(
		"Документ.РазрешениеНаЗаменуМатериалов.Форма.ПодборЗаказов",
		НастройкиПодбора,
		ЭтотОбъект,
		,
		,
		,
		Новый ОписаниеОповещения("ПодобратьЗаказыЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбластиДействия_ЗаполнитьНаправлениеДеятельности(Команда)
	
	ВыделенныеСтроки = Элементы.ОбластиДействия.ВыделенныеСтроки;
	
	ОткрытьФорму(
		"Справочник.НаправленияДеятельности.ФормаВыбора",
		,
		ЭтотОбъект,
		,
		,
		,
		Новый ОписаниеОповещения("ЗаполнитьНаправлениеДеятельностиЗавершение",
		ЭтотОбъект,
		Новый Структура("ВыделенныеСтроки", ВыделенныеСтроки)),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбластиДействия_ЗаполнитьПодразделение(Команда)
	
	ВыделенныеСтроки = Элементы.ОбластиДействия.ВыделенныеСтроки;
	
	ОткрытьФорму(
		"Справочник.СтруктураПредприятия.ФормаВыбора",
		,
		ЭтотОбъект,
		,
		,
		,
		Новый ОписаниеОповещения("ЗаполнитьПодразделениеЗавершение",
		ЭтотОбъект,
		Новый Структура("ВыделенныеСтроки", ВыделенныеСтроки)),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбластиДействия_ЗаполнитьИзделие(Команда)
	
	ВыделенныеСтроки = Элементы.ОбластиДействия.ВыделенныеСтроки;
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("НаправлениеДеятельности", Неопределено);
	ПараметрыФормы.Вставить("ЗаказНаПроизводство", Неопределено);
	ПараметрыФормы.Вставить("ЗаказКлиента", Неопределено);
	ПараметрыФормы.Вставить("Подразделение", Неопределено);
	ПараметрыФормы.Вставить("Спецификация", Неопределено);
	ПараметрыФормы.Вставить("Этап", Неопределено);
	ПараметрыФормы.Вставить("Изделие", Неопределено);
	ПараметрыФормы.Вставить("ХарактеристикаИзделия", Неопределено);
	ПараметрыФормы.Вставить("КоличествоМатериалов", Объект.Материалы.Количество());
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	ПараметрыФормы.Вставить("МножественноеУказаниеОбластиДействия", Истина);
	
	ОткрытьФорму(
		"Документ.РазрешениеНаЗаменуМатериалов.Форма.ФормаВводаОбластиДействия",
		ПараметрыФормы,
		ЭтотОбъект,
		,
		,
		,
		Новый ОписаниеОповещения("ЗаполнитьИзделиеЗавершение",
		ЭтотОбъект,
		Новый Структура("ВыделенныеСтроки", ВыделенныеСтроки)),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбластиДействия_ЗакрытьМножественнуюНастройку(Команда)
	
	ОчиститьСообщения();
	
	Если Объект.ОбластиДействия.Количество() = 1 Тогда
		НастроитьВидимостьОбластиДействия(ЭтотОбъект);
		ОбновитьПредставлениеОбластиДействия();
		ОпределитьДоступностьВариантаЗамены(ЭтотОбъект);
	Иначе	
		Текст = НСтр("ru = 'Для закрытия множественной настройки области действия необходимо, чтобы в табличной части осталась только одна строка.';
					|en = 'To close the multiple scope setting, leave only one row in the table.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(Текст);
	КонецЕсли;
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	#Область СтандартноеОформление
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтотОбъект, 
																			 "МатериалыХарактеристика",
																		     "Объект.Материалы.ХарактеристикиИспользуются");

	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтотОбъект, 
																			 "АналогиХарактеристика",
																		     "Объект.Аналоги.ХарактеристикиИспользуются");
	
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтотОбъект, 
																		 "МатериалыСписокАналоговХарактеристика",
																	     "Объект.Материалы.ХарактеристикиИспользуются");

	ПараметрыУсловногоОформления = НоменклатураСервер.НовыеПараметрыУсловногоОформленияЕдиницИзмерения();
	ПараметрыУсловногоОформления.ИмяПоляЕдиницаИзмерения = "МатериалыНоменклатураЕдиницаИзмерения";
	ПараметрыУсловногоОформления.ПутьКПолюУпаковка = "Объект.Материалы.Упаковка";
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтотОбъект, ПараметрыУсловногоОформления);
																   
	ПараметрыУсловногоОформления = НоменклатураСервер.НовыеПараметрыУсловногоОформленияЕдиницИзмерения();
	ПараметрыУсловногоОформления.ИмяПоляЕдиницаИзмерения = "АналогиНоменклатураЕдиницаИзмерения";
	ПараметрыУсловногоОформления.ПутьКПолюУпаковка = "Объект.Аналоги.Упаковка";
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтотОбъект, ПараметрыУсловногоОформления);
	#КонецОбласти
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДатаОкончанияДействия.Имя);
	
	ОтборГруппаИли = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппаИли.Использование = Истина;
	ОтборГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	                                                                                
	ОтборЭлемента = ОтборГруппаИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Статус");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СтатусыРазрешенийНаЗаменуМатериалов.Прекращено;
	
	ОтборЭлемента = ОтборГруппаИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДатаОкончанияДействия");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбластиДействияПодразделение.Имя);
	
	ОтборГруппаИли = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппаИли.Использование = Истина;
	ОтборГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	                                                                                
	ОтборЭлемента = ОтборГруппаИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОбластиДействия.Подразделение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Справочники.СтруктураПредприятия.ПустаяСсылка();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<все подразделения>';
																|en = '<all business units>'"));
    Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Серый);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбластиДействияИзделиеПредставление.Имя);
	
	ОтборГруппаИли = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппаИли.Использование = Истина;
	ОтборГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	                                                                                
	ОтборЭлемента = ОтборГруппаИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОбластиДействия.ИзделиеПредставление");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "";
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<все изделия>';
																|en = '<all finished products>'"));
    Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Серый);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбластиДействияЗаказНаПроизводство.Имя);
	
	ОтборГруппаИли = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппаИли.Использование = Истина;
	ОтборГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	                                                                                
	ОтборЭлемента = ОтборГруппаИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОбластиДействия.ЗаказНаПроизводство");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<все заказы>';
																|en = '<all orders>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Серый);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбластиДействияНаправлениеДеятельности.Имя);
	
	ОтборГруппаИли = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппаИли.Использование = Истина;
	ОтборГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	                                                                                
	ОтборЭлемента = ОтборГруппаИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОбластиДействия.НаправлениеДеятельности");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Справочники.НаправленияДеятельности.ПустаяСсылка();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<все направления деятельности>';
																|en = '<all lines of business>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Серый);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ЗаполнитьСлужебныеРеквизиты();
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	УстановитьДоступностьФлагаВзаимозамены(ЭтотОбъект);
	ОбновитьПредставлениеОбластиДействия();
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
	НастроитьВидимостьОбластиДействия(ЭтотОбъект);
	НастроитьВидимостьСтраницыЗамены(ЭтотОбъект);
	ОпределитьДоступностьВариантаЗамены(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если СтруктураРеквизитов.Свойство("Комментарий")
		ИЛИ Инициализация Тогда
		
		Элементы.СтраницаКомментарий.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Объект.Комментарий);
		
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("Статус")
		ИЛИ Инициализация Тогда
		
		Элементы.ДатаОкончанияДействия.АвтоОтметкаНезаполненного = 
			(Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыРазрешенийНаЗаменуМатериалов.Прекращено"));
			
		ДоступноИзменение = Истина;
			
		Если Объект.Проведен
		   И (Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыРазрешенийНаЗаменуМатериалов.Утверждено")
				ИЛИ Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыРазрешенийНаЗаменуМатериалов.Прекращено")) Тогда
		
			ДоступноИзменение = Ложь;
		
		КонецЕсли;
		
		ИменаЭлементов = Новый Массив; // Массив Из Строка
		ИменаЭлементов.Добавить("ОбластиДействия");
		ИменаЭлементов.Добавить("ГруппаЗаменаНабор");
		ИменаЭлементов.Добавить("ГруппаЗаменаСписок");
			
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(
			Форма.Элементы, ИменаЭлементов, "ТолькоПросмотр", НЕ ДоступноИзменение);
			
		ИменаЭлементов = Новый Массив; // Массив Из Строка
		ИменаЭлементов.Добавить("КонтекстноеМенюАналоги_ВставитьСтроки");
		ИменаЭлементов.Добавить("Материалы_СкопироватьСтроки");
		ИменаЭлементов.Добавить("Материалы_ВставитьСтроки");
		ИменаЭлементов.Добавить("Материалы_ЗагрузитьИзВнешнегоФайла");
		ИменаЭлементов.Добавить("Материалы_ПодобратьПоСпецификации");
		ИменаЭлементов.Добавить("КонтекстноеМенюМатериалы_СкопироватьСтроки");
		ИменаЭлементов.Добавить("КонтекстноеМенюМатериалы_ВставитьСтроки");
		ИменаЭлементов.Добавить("Аналоги_СкопироватьСтроки");
		ИменаЭлементов.Добавить("Аналоги_ВставитьСтроки");
		ИменаЭлементов.Добавить("Аналоги_ЗагрузитьИзВнешнегоФайла");
		ИменаЭлементов.Добавить("КонтекстноеМенюАналоги_СкопироватьСтроки");
		ИменаЭлементов.Добавить("МатериалыСписокАналогов_СкопироватьСтроки");
		ИменаЭлементов.Добавить("МатериалыСписокАналогов_ВставитьСтроки");
		ИменаЭлементов.Добавить("МатериалыСписокАналогов_ЗагрузитьИзВнешнегоФайла");
		ИменаЭлементов.Добавить("КонтекстноеМенюМатериалы_СкопироватьСтрокиСписок");
		ИменаЭлементов.Добавить("КонтекстноеМенюМатериалы_ВставитьСтрокиСписок");
		ИменаЭлементов.Добавить("ОбластиДействия_ПодобратьЗаказыНаПроизводство");
		ИменаЭлементов.Добавить("ОбластиДействия_ЗаполнитьНаправлениеДеятельности");
		ИменаЭлементов.Добавить("ОбластиДействия_ЗаполнитьПодразделение");
		ИменаЭлементов.Добавить("ОбластиДействия_ЗаполнитьИзделие");
		ИменаЭлементов.Добавить("ОбластиДействия_ЗакрытьМножественнуюНастройку");
		ИменаЭлементов.Добавить("ВариантЗамены");
		ИменаЭлементов.Добавить("ПредставлениеОбластиДействия");
		ИменаЭлементов.Добавить("МножественноеУказание");
		
		ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(
			Форма.Элементы, ИменаЭлементов, "Доступность", ДоступноИзменение);
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизиты()
	
	ЗаполнитьПризнакХарактеристикиИспользуются = Новый Структура("Номенклатура", "ХарактеристикиИспользуются");
	ЗаполнитьПризнакТипНоменклатуры = Новый Структура("Номенклатура", "ТипНоменклатуры");
	
	СтруктураДействий = Новый Структура("ЗаполнитьПризнакХарактеристикиИспользуются, ЗаполнитьПризнакТипНоменклатуры",
		ЗаполнитьПризнакХарактеристикиИспользуются,
		ЗаполнитьПризнакТипНоменклатуры);
		
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		Объект.Материалы, СтруктураДействий);
		
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		Объект.Аналоги, СтруктураДействий);
		
	Для Каждого СтрокаТЧ Из Объект.ОбластиДействия Цикл
		ЗаполнитьПредставлениеИзделия(СтрокаТЧ);
	КонецЦикла;	
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьВСтруктуруДействияПриИзмененииНоменклатуры(СтруктураДействий, ТекущаяСтрока, ИмяТЧ)

	СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущаяСтрока.Упаковка);
	СтруктураДействий.Вставить("НоменклатураПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти", "ФормаДокумента", ИмяТЧ));
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеОбластиДействия()
	
	Если Объект.ОбластиДействия.Количество() = 0 Тогда
		
		ПредставлениеОбластиДействия = НСтр("ru = 'По предприятию в целом';
											|en = 'By the entire enterprise'");
		Возврат;
		
	КонецЕсли;
	
	СтрокаОбластиДействия = Объект.ОбластиДействия[0];
	ПредставлениеОбластиДействия = "";
	МассивПодстрок = Новый Массив;
	
	Если ЗначениеЗаполнено(СтрокаОбластиДействия.ЗаказНаПроизводство) Тогда
		
		РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаОбластиДействия.ЗаказНаПроизводство, "Номер, Дата");
		
		Шаблон = НСтр("ru = 'Заказ на производство: №%1 от %2';
						|en = 'Production order: No.%1 dated %2'");
		МассивПодстрок.Добавить(СтрШаблон(Шаблон,
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыОбъекта.Номер),
			Формат(РеквизитыОбъекта.Дата, "ДЛФ=D")));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаОбластиДействия.ЗаказКлиента) Тогда
		
		РеквизитыОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаОбластиДействия.ЗаказКлиента, "Номер, Дата");
		
		Шаблон = НСтр("ru = 'Заказ клиента: №%1 от %2';
						|en = 'Sales order: No.%1 dated %2'");
		МассивПодстрок.Добавить(СтрШаблон(Шаблон,
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(РеквизитыОбъекта.Номер),
			Формат(РеквизитыОбъекта.Дата, "ДЛФ=D")));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаОбластиДействия.Спецификация) Тогда
		
		Шаблон = НСтр("ru = 'Спецификация: %1';
						|en = 'Bill of materials: %1'");
		МассивПодстрок.Добавить(СтрШаблон(Шаблон, СтрокаОбластиДействия.Спецификация));
		
	КонецЕсли;
		
	Если ЗначениеЗаполнено(СтрокаОбластиДействия.Этап) Тогда
		
		Шаблон = НСтр("ru = 'Этап: %1';
						|en = 'Stage: %1'");
		МассивПодстрок.Добавить(СтрШаблон(Шаблон, СтрокаОбластиДействия.Этап));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаОбластиДействия.Изделие) Тогда
		
		Шаблон = НСтр("ru = 'Изделие: %1';
						|en = 'Finished product: %1'");
		МассивПодстрок.Добавить(СтрШаблон(Шаблон, СтрокаОбластиДействия.Изделие));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаОбластиДействия.ХарактеристикаИзделия) Тогда
		
		Шаблон = НСтр("ru = 'Характеристика: %1';
						|en = 'Variant: %1'");
		МассивПодстрок.Добавить(СтрШаблон(Шаблон, СтрокаОбластиДействия.ХарактеристикаИзделия));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаОбластиДействия.Подразделение) Тогда
		
		Шаблон = НСтр("ru = 'Подразделение: %1';
						|en = 'Business unit: %1'");
		МассивПодстрок.Добавить(СтрШаблон(Шаблон, СтрокаОбластиДействия.Подразделение));
		
	КонецЕсли;
		
	Если ЗначениеЗаполнено(СтрокаОбластиДействия.НаправлениеДеятельности) Тогда
		
		Шаблон = НСтр("ru = 'Направление деятельности: %1';
						|en = 'Line of business: %1'");
		МассивПодстрок.Добавить(СтрШаблон(Шаблон, СтрокаОбластиДействия.НаправлениеДеятельности));
		
	КонецЕсли;
	
	ПредставлениеОбластиДействия = СтрСоединить(МассивПодстрок, "; ");
	ДлинаСтроки = СтрДлина(ПредставлениеОбластиДействия);
	
	Если ДлинаСтроки = 0 Тогда
		ПредставлениеОбластиДействия = НСтр("ru = 'По предприятию в целом';
											|en = 'By the entire enterprise'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостью(Форма)
	
	ВидимостьКомандРаботыСБуфером = Истина;
	
	Для Каждого СтрокаТЧ Из Форма.Объект.ОбластиДействия Цикл
	
		Если ЗначениеЗаполнено(СтрокаТЧ.Спецификация) Тогда
			ВидимостьКомандРаботыСБуфером = Ложь;
			Прервать;
		КонецЕсли;
	
	КонецЦикла;
	
	Форма.Элементы.Материалы_ВставитьСтроки.Видимость = ВидимостьКомандРаботыСБуфером;
	Форма.Элементы.КонтекстноеМенюМатериалы_ВставитьСтроки.Видимость = ВидимостьКомандРаботыСБуфером;
	Форма.Элементы.Материалы_ПодобратьПоСпецификации.Видимость = Не ВидимостьКомандРаботыСБуфером;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИдентификаторСтроки(СтрокаТаблицы)
	
	СтрокаТаблицы.ИдентификаторСтроки = Новый УникальныйИдентификатор;
	
КонецПроцедуры	

#Область РаботаСБуферомОбмена

&НаКлиенте
Процедура СкопироватьСтрокиТЧ(ОбъектИмяТЧ, ЭлементыИмяТЧ)

	Если РаботаСТабличнымиЧастямиКлиент.ВыбранаСтрокаДляВыполненияКоманды(Элементы[ЭлементыИмяТЧ]) Тогда
		СкопироватьСтрокиНаСервере(ОбъектИмяТЧ, ЭлементыИмяТЧ);
		РаботаСТабличнымиЧастямиКлиент.ОповеститьПользователяОКопированииСтрок(Элементы[ЭлементыИмяТЧ].ВыделенныеСтроки.Количество());
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СкопироватьСтрокиНаСервере(ОбъектИмяТЧ, ЭлементыИмяТЧ)
	
	РаботаСТабличнымиЧастями.СкопироватьСтрокиВБуферОбмена(Объект[ОбъектИмяТЧ], Элементы[ЭлементыИмяТЧ].ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСтрокиИзБуфераОбмена(ИмяТЧ)
	
	КоличествоСтрокДоВставки = Объект[ИмяТЧ].Количество();
	
	ПолучитьСтрокиИзБуфераОбменаНаСервере(ИмяТЧ);
	
	КоличествоВставленных = Объект[ИмяТЧ].Количество() - КоличествоСтрокДоВставки;
	РаботаСТабличнымиЧастямиКлиент.ОповеститьПользователяОВставкеСтрок(КоличествоВставленных);
	
	УстановитьДоступностьФлагаВзаимозамены(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСтрокиИзБуфераОбменаНаСервере(ИмяТЧ)
	
	МассивТиповНоменклатуры = Новый Массив;
	МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар"));
	МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара"));
	МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа"));
	
	ПараметрыОтбора = Новый Соответствие;
	ПараметрыОтбора.Вставить("Номенклатура.ТипНоменклатуры", МассивТиповНоменклатуры);
	
	Колонки = "Номенклатура,Характеристика,Упаковка,КоличествоУпаковок";
	
	СтрокиИзБуфера = РаботаСТабличнымиЧастями.СтрокиИзБуфераОбмена(ПараметрыОтбора, Колонки);
	
	Если Не ЗначениеЗаполнено(СтрокиИзБуфера) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДействий = Новый Структура;
	ОбрабатываемыеСтроки = Новый Массив;
	Таблица = Объект[ИмяТЧ];//ДанныеФормыКоллекция - 
	
	ВариантЗаменыСписком = Объект.ВариантЗамены = Перечисления.ВариантЗаменыАналогов.СписокАналогов;
	ПустаяУпаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
	
	Для каждого СтрокаИзБуфера Из СтрокиИзБуфера Цикл
		
		ТекущаяСтрока = Таблица.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаИзБуфера);
		
		Если ВариантЗаменыСписком Тогда
			
			ТекущаяСтрока.КоличествоУпаковок = 1;
			ТекущаяСтрока.Упаковка = ПустаяУпаковка;
			
		КонецЕсли;
		
		ДобавитьВСтруктуруДействияПриИзмененииНоменклатуры(СтруктураДействий, ТекущаяСтрока, ИмяТЧ);
		ОбрабатываемыеСтроки.Добавить(ТекущаяСтрока);
		
	КонецЦикла;

	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
		ОбрабатываемыеСтроки,
		СтруктураДействий,
		Таблица);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКомандБуфераОбмена(Форма, ДоступностьРеквизитов)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Если Объект.Проведен
	   И (Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыРазрешенийНаЗаменуМатериалов.Утверждено")
			ИЛИ Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыРазрешенийНаЗаменуМатериалов.Прекращено")) Тогда
	
		ДоступностьРеквизитов = Ложь;
	
	КонецЕсли;
	
	Элементы.Материалы_ВставитьСтроки.Доступность = ДоступностьРеквизитов;
	Элементы.КонтекстноеМенюМатериалы_ВставитьСтроки.Доступность = ДоступностьРеквизитов;
	
	Элементы.Аналоги_ВставитьСтроки.Доступность = ДоступностьРеквизитов;
	Элементы.КонтекстноеМенюАналоги_ВставитьСтроки.Доступность = ДоступностьРеквизитов;
	
	Элементы.МатериалыСписокАналогов_ВставитьСтроки.Доступность = ДоступностьРеквизитов;
	Элементы.КонтекстноеМенюМатериалы_ВставитьСтрокиСписок.Доступность = ДоступностьРеквизитов;
	
КонецПроцедуры

#КонецОбласти

#Область ПодбораПоСпецификации

&НаСервере
Функция ПараметрыПодбораПоСпецификации()
	
	ДанныеПоНоменклатуре = Справочники.РесурсныеСпецификации.ДанныеПоНоменклатуреРасширенный();
	
	Спецификация = Справочники.РесурсныеСпецификации.ПустаяСсылка();
	Этап = Справочники.ЭтапыПроизводства.ПустаяСсылка();
	
	Для Каждого СтрокаТЧ Из Объект.ОбластиДействия Цикл
		
		Если ЗначениеЗаполнено(СтрокаТЧ.Спецификация) Тогда
			Спецификация = СтрокаТЧ.Спецификация;
			Этап = СтрокаТЧ.Этап;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеПоНоменклатуре.Спецификация = Спецификация;
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Спецификация, "ОсновноеИзделиеНоменклатура, ОсновноеИзделиеУпаковка, ОсновноеИзделиеКоличествоУпаковок");
		
	ДанныеСпецификации = Новый Структура;
	ДанныеСпецификации.Вставить("Номенклатура",       ЗначенияРеквизитов.ОсновноеИзделиеНоменклатура);
	ДанныеСпецификации.Вставить("Упаковка",           ЗначенияРеквизитов.ОсновноеИзделиеУпаковка);
	ДанныеСпецификации.Вставить("КоличествоУпаковок", ЗначенияРеквизитов.ОсновноеИзделиеКоличествоУпаковок);
	ДанныеСпецификации.Вставить("Количество",         0);
		
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	
	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ДанныеСпецификации, СтруктураДействий, Неопределено);
	
	ДанныеПоНоменклатуре.Количество = ДанныеСпецификации.Количество;
	
	СтруктураПолейТаблицы = ПроизводствоСервер.СтруктураПолейТаблицы();
	СтруктураПолейТаблицы.ОсновныеПоля = "Номенклатура, Характеристика, КоличествоУпаковок, Упаковка";
	СтруктураПолейТаблицы.ДополнительныеПоля.Вставить("Этап", Новый ОписаниеТипов("СправочникСсылка.ЭтапыПроизводства"));
	СтруктураПолейТаблицы.ЗначенияПоУмолчанию.Вставить("Этап", Этап);
	
	АдресПодобранныеМатериалы = ПроизводствоСервер.ПоместитьВоВременноеХранилищеДанныеСтрок(
									ЭтотОбъект, "Объект", "Материалы", Ложь, СтруктураПолейТаблицы);
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("ДанныеПоНоменклатуре", ДанныеПоНоменклатуре);
	ПараметрыПодбора.Вставить("Этап", Этап);
	ПараметрыПодбора.Вставить("ПоказыватьКоличествоПодобрано", Истина);
	ПараметрыПодбора.Вставить("ЗаголовокКолонкиКоличествоПодобрано", НСтр("ru = 'В документе';
																			|en = 'In document'"));
	ПараметрыПодбора.Вставить("СтруктураПоискаПодобранныеМатериалы", Новый Структура("Номенклатура, Характеристика, Этап"));
	ПараметрыПодбора.Вставить("АдресПодобранныеМатериалы", АдресПодобранныеМатериалы);
	ПараметрыПодбора.Вставить("Основание", Объект.Ссылка);
	
	Возврат ПараметрыПодбора;
	
КонецФункции

&НаСервере
Процедура ОбработатьПодборМатериаловПоСпецификации(АдресВХранилище)
	
	ТаблицаДанных = ПолучитьИзВременногоХранилища(АдресВХранилище);
	Для каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		
		СтрокаМатериалы = Объект.Материалы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаМатериалы, СтрокаТаблицы);
		СтрокаМатериалы.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		
	КонецЦикла;
	
	УстановитьДоступностьФлагаВзаимозамены(ЭтотОбъект);
	
КонецПроцедуры	

#КонецОбласти

// СтандартныеПодсистемы.Свойства 

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()

	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);

КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства


&НаКлиенте
Процедура ВыполнитьДействияПриИзмененииВариантаЗамены(ОчиститьОбластиДействия = Ложь)

	ОбработатьДанныеТабличныхЧастейПриИзмененииВариантаЗамены();
	НастроитьВидимостьСтраницыЗамены(ЭтотОбъект);
	НастроитьВидимостьОбластиДействия(ЭтотОбъект);
	
	ВариантЗаменыДоИзменения = Объект.ВариантЗамены;
	Если ОчиститьОбластиДействия Тогда
	
		Объект.ОбластиДействия.Очистить();
		Объект.ОбластиДействия.Добавить();
	
	КонецЕсли;
	
	ОбновитьПредставлениеОбластиДействия();
	
	Если Объект.ВариантЗамены = ПредопределенноеЗначение("Перечисление.ВариантЗаменыАналогов.СписокАналогов")
	   И Объект.ВзаимозаменяемыеАналоги Тогда
	
		Объект.ВзаимозаменяемыеАналоги = Ложь;
	
	КонецЕсли;

КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьВидимостьСтраницыЗамены(Форма)
	
	Элементы = Форма.Элементы;
	ВариантЗамены = Форма.Объект.ВариантЗамены;
	
	Если ВариантЗамены = ПредопределенноеЗначение("Перечисление.ВариантЗаменыАналогов.НаборНаНабор") Тогда
		
		Элементы.ГруппаЗаменаНабор.Видимость = Истина;
		Элементы.ГруппаЗаменаСписок.Видимость = Ложь;
		
	Иначе
		
		Элементы.ГруппаЗаменаНабор.Видимость = Ложь;
		Элементы.ГруппаЗаменаСписок.Видимость = Истина;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьДанныеТабличныхЧастейПриИзмененииВариантаЗамены()
	
	Если Объект.ВариантЗамены = ПредопределенноеЗначение("Перечисление.ВариантЗаменыАналогов.СписокАналогов") Тогда
		
		Объект.Аналоги.Очистить();
		
		Для Каждого СтрокаТЧ Из Объект.Материалы Цикл
			СтрокаТЧ.КоличествоУпаковок = 1;
			СтрокаТЧ.Упаковка = ПредопределенноеЗначение("Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка");
		КонецЦикла;
			
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзВнешнегоФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(АдресЗагруженныхДанных) Тогда
		ОчиститьСообщения();
		ЗаполнитьМатериалы = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
								ДополнительныеПараметры, "ЗаполнитьМатериалы", Ложь);
		ПолучитьЗагруженныеТоварыИзХранилища(АдресЗагруженныхДанных, ЗаполнитьМатериалы);
	КонецЕсли;
	
	УстановитьДоступностьФлагаВзаимозамены(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗагруженныеТоварыИзХранилища(АдресЗагруженныхДанных, ЗаполнитьМатериалы)

	ТоварыИзХранилища = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	Если ЗаполнитьМатериалы Тогда
		ТабличнаяЧасть = Объект.Материалы;
	Иначе
		ТабличнаяЧасть = Объект.Аналоги;
	КонецЕсли;
	
	Для Каждого СтрокаТовары Из ТоварыИзХранилища Цикл
	
		НоваяСтрока = ТабличнаяЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовары);
		НоваяСтрока.ИдентификаторСтроки = Новый УникальныйИдентификатор;
		
		Если НоваяСтрока.КоличествоУпаковок = 0 Тогда
			НоваяСтрока.КоличествоУпаковок = 1;
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьВидимостьОбластиДействия(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	ЗаменаСписком = Объект.ВариантЗамены = ПредопределенноеЗначение("Перечисление.ВариантЗаменыАналогов.СписокАналогов");
	
	ВидимостьТЧ = Объект.ОбластиДействия.Количество() > 1 И НЕ ЗаменаСписком;
	Элементы.ГруппаМножественнаяОбластьДействия.Видимость = ВидимостьТЧ;
	Элементы.МножественноеУказание.Видимость = НЕ ВидимостьТЧ И НЕ ЗаменаСписком;
	Элементы.ПредставлениеОбластиДействия.Видимость = НЕ ВидимостьТЧ;
	
	Если ВидимостьТЧ Тогда
	
		Для Каждого СтрокаТЧ Из Объект.ОбластиДействия Цикл
			ЗаполнитьПредставлениеИзделия(СтрокаТЧ);
		КонецЦикла;
	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаправлениеДеятельностиЗавершение(Результат, ДополнительныеПараметры) Экспорт

	ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		Для Каждого ТекСтрока Из ВыделенныеСтроки Цикл
			
			СтрокаТаблицы = Объект.ОбластиДействия.НайтиПоИдентификатору(ТекСтрока);
			СтрокаТаблицы.НаправлениеДеятельности = Результат;
			
		КонецЦикла;
		
		ОпределитьДоступностьВариантаЗамены(ЭтотОбъект);
		
		ТекстОповещения = НСтр("ru = 'Выделенные строки заполнены';
								|en = 'Selected lines are filled in'");
		ПоказатьОповещениеПользователя(ТекстОповещения,,, БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПодразделениеЗавершение(Результат, ДополнительныеПараметры) Экспорт

	ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		Для Каждого ТекСтрока Из ВыделенныеСтроки Цикл
			
			СтрокаТаблицы = Объект.ОбластиДействия.НайтиПоИдентификатору(ТекСтрока);
			СтрокаТаблицы.Подразделение = Результат;
			
		КонецЦикла;
		
		ОпределитьДоступностьВариантаЗамены(ЭтотОбъект);
		
		ТекстОповещения = НСтр("ru = 'Выделенные строки заполнены';
								|en = 'Selected lines are filled in'");
		ПоказатьОповещениеПользователя(ТекстОповещения,,, БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИзделиеЗавершение(Результат, ДополнительныеПараметры) Экспорт

	ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		Для Каждого ТекСтрока Из ВыделенныеСтроки Цикл
			
			СтрокаТаблицы = Объект.ОбластиДействия.НайтиПоИдентификатору(ТекСтрока);
			СписокСвойств = "Спецификация, Этап, Изделие, ХарактеристикаИзделия";
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Результат.ЗначениеВыбора, СписокСвойств);
			
		КонецЦикла;
		
		ОпределитьДоступностьВариантаЗамены(ЭтотОбъект);
		
		ТекстОповещения = НСтр("ru = 'Выделенные строки заполнены';
								|en = 'Selected lines are filled in'");
		ПоказатьОповещениеПользователя(ТекстОповещения,,, БиблиотекаКартинок.Информация32);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьЗаказыЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если ЗначениеЗаполнено(Результат) Тогда
		
		ПодборЗаказовНаПроизводство = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
									ДополнительныеПараметры, "ПодборЗаказовНаПроизводство", Ложь);
		
		МассивУникальныхСсылок = Новый Массив;
		
		Если ПодборЗаказовНаПроизводство Тогда
			
			Для каждого СтрокаСписка Из Результат Цикл
				
				Если МассивУникальныхСсылок.Найти(СтрокаСписка.Значение) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = Объект.ОбластиДействия.Добавить();
				НоваяСтрока.ЗаказНаПроизводство = СтрокаСписка.Значение;
				
				МассивУникальныхСсылок.Добавить(СтрокаСписка.Значение);
			
			КонецЦикла;
			
		КонецЕсли;
		
		ОпределитьДоступностьВариантаЗамены(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеИзделия(СтрокаТЧ)
	
	ИзделиеПредставление = "";
	МассивПодстрок = Новый Массив;
	
	Если ЗначениеЗаполнено(СтрокаТЧ.Спецификация) Тогда
		
		Шаблон = НСтр("ru = 'Спецификация: %1';
						|en = 'Bill of materials: %1'");
		МассивПодстрок.Добавить(СтрШаблон(Шаблон, СтрокаТЧ.Спецификация));
		
	КонецЕсли;
		
	Если ЗначениеЗаполнено(СтрокаТЧ.Этап) Тогда
		
		Шаблон = НСтр("ru = 'Этап: %1';
						|en = 'Stage: %1'");
		МассивПодстрок.Добавить(СтрШаблон(Шаблон, СтрокаТЧ.Этап));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТЧ.Изделие) Тогда
		
		Шаблон = НСтр("ru = 'Изделие: %1';
						|en = 'Finished product: %1'");
		МассивПодстрок.Добавить(СтрШаблон(Шаблон, СтрокаТЧ.Изделие));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТЧ.ХарактеристикаИзделия) Тогда
		
		Шаблон = НСтр("ru = 'Характеристика: %1';
						|en = 'Variant: %1'");
		МассивПодстрок.Добавить(СтрШаблон(Шаблон, СтрокаТЧ.ХарактеристикаИзделия));
		
	КонецЕсли;
	
	ИзделиеПредставление = СтрСоединить(МассивПодстрок, ";");
		
	СтрокаТЧ.ИзделиеПредставление = ИзделиеПредставление;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьФлагаВзаимозамены(Форма)

	Форма.Элементы.ВзаимозаменяемыеАналоги.ТолькоПросмотр = Форма.Объект.Материалы.Количество() > 1 ИЛИ Форма.Объект.Аналоги.Количество() > 1;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОпределитьДоступностьВариантаЗамены(Форма)

	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ОбластьДействияЗаполнена = Ложь;
	
	Для Каждого СтрокаТЧ Из Объект.ОбластиДействия Цикл
	
		Если ЗначениеЗаполнено(СтрокаТЧ.НаправлениеДеятельности)
		 ИЛИ ЗначениеЗаполнено(СтрокаТЧ.Подразделение)
		 ИЛИ ЗначениеЗаполнено(СтрокаТЧ.Изделие)
		 ИЛИ ЗначениеЗаполнено(СтрокаТЧ.ХарактеристикаИзделия)
		 ИЛИ ЗначениеЗаполнено(СтрокаТЧ.ЗаказНаПроизводство)
		 ИЛИ ЗначениеЗаполнено(СтрокаТЧ.Спецификация)
		 ИЛИ ЗначениеЗаполнено(СтрокаТЧ.Этап)
		 ИЛИ ЗначениеЗаполнено(СтрокаТЧ.ЗаказКлиента) Тогда
		
			ОбластьДействияЗаполнена = Истина;
			Прервать;
		
		КонецЕсли;
	
	КонецЦикла;
	
	Элементы.ВариантЗамены.ТолькоПросмотр = ОбластьДействияЗаполнена;

КонецПроцедуры

#КонецОбласти
