
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОчиститьСообщения();
	ВводДоступен = ДоступноСозданиеДокумента(ПараметрКоманды);
	
	Если Не ВводДоступен Тогда
		
		ТекстСообщения = НСтр("ru = 'Создание взаимозаменяемых аналогов доступно только для режима ""Набор на набор""';
								|en = 'You can create interchangeable substitutes only in ""Set to set"" mode'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	
	КонецЕсли;
	
	ЭлементыОтбора = Новый Структура("ДокументРазрешения, ВзаимозаменяемыеАналоги", ПараметрКоманды, Истина);
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЭлементыОтбора);
	
	ОткрытьФорму("Документ.РазрешениеНаЗаменуМатериалов.Форма.ФормаДокумента", 
		ПараметрыФормы, 
		ПараметрыВыполненияКоманды.Источник, 
		ПараметрыВыполненияКоманды.Уникальность, 
		ПараметрыВыполненияКоманды.Окно, 
		ПараметрыВыполненияКоманды.НавигационнаяСсылка);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДоступноСозданиеДокумента(Разрешение)

	Статус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Разрешение, "ВариантЗамены");
	Возврат Статус <> Перечисления.ВариантЗаменыАналогов.СписокАналогов;

КонецФункции 

#КонецОбласти
