#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//	Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд создания на основании.
//
// Параметры:
//	КомандыСозданияНаОсновании - См. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//	Параметры - Структура - Вспомогательные параметры. Для чтения. См. описание 2 параметра процедуры
//							СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании().
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	ИсправлениеДокументов.ДобавитьКомандуИсправление(КомандыСозданияНаОсновании, ПустаяСсылка().Метаданные());
	
КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//	КомандыОтчетов - ТаблицаЗначений - Таблица с командами отчетов. Для изменения. См. описание 1 параметра процедуры
//										ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//	Параметры - Структура - Вспомогательные параметры. Для чтения. См. описание 2 параметра процедуры
//							ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов().
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//	МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена регистрация в механизме 
//									проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("ОперативныйУчетТоваровОрганизаций");
	МеханизмыДокумента.Добавить("УчетИмпорта");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("ИсправлениеДокументов");
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов - таблиц значений - данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.УведомлениеОВвозеПрослеживаемыхТоваров") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаТоварыКОформлениюДокументовИмпорта(Запрос, ТекстыЗапроса, Регистры); 
		ПроведениеДокументов.ДобавитьЗапросыСторноДвижений(Запрос, ТекстыЗапроса, Регистры, ПустаяСсылка().Метаданные());
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

// Возвращает данные для заполнения табличной части Товары по основанию.
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ПриобретениеТоваровУслуг - основание
//	КодТНВЭД - СправочникСсылка.КлассификаторТНВЭД
//	ЕдиницаИзмерения - ОпределяемыйТип.ЕдиницаИзмерения
//	Ссылка - ДокументСсылка.УведомлениеОбОстаткахПрослеживаемыхТоваров
//
// Возвращаемое значение:
//	ТаблицаЗначений
//
Функция ДанныеТЧТоварыПоОснованию(ДокументОснование, КодТНВЭД, ЕдиницаИзмерения = Неопределено, Ссылка = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("КодТНВЭД", КодТНВЭД);
	Запрос.УстановитьПараметр("ЕдиницаИзмерения", ЕдиницаИзмерения);
	Запрос.УстановитьПараметр("Ссылка", Ссылка); 
	Запрос.УстановитьПараметр("ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.УведомлениеОВвозеПрослеживаемыхТоваров"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТоварыОстатки.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ТоварыОстатки.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТоварыОстатки.АналитикаУчетаНоменклатуры.МестоХранения КАК МестоХранения,
	|	ТоварыОстатки.АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
	|	ТоварыОстатки.АналитикаУчетаНоменклатуры.ТипМестаХранения КАК ТипМестаХранения,
	|	ТоварыОстатки.ВидЗапасов КАК ВидЗапасов,
	|	ТоварыОстатки.КоличествоОстаток КАК Количество,
	|	ТоварыОстатки.КоличествоПоРНПТОстаток КАК КоличествоПоРНПТ,
	|	ТоварыОстатки.СуммаОстаток КАК Сумма,
	|	ТоварыОстатки.АналитикаУчетаНоменклатуры.СкладскаяТерритория КАК Склад,
	|	ТоварыОстатки.АналитикаУчетаНоменклатуры.Подразделение КАК Подразделение,
	|	ТоварыОстатки.АналитикаУчетаНоменклатуры.Договор КАК Договор,
	|	ТоварыОстатки.АналитикаУчетаНоменклатуры.Партнер КАК Хранитель,
	|	ТоварыОстатки.АналитикаУчетаНоменклатуры.Контрагент КАК Контрагент,
	|	ТоварыОстатки.АналитикаУчетаНоменклатуры.Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюДокументовИмпорта.Остатки(, ДокументПоступления = &ДокументОснование
	|								И ТипДокументаИмпорта = &ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров) КАК ТоварыОстатки
	|ГДЕ
	|	ТоварыОстатки.КоличествоОстаток > 0
	|	И ТоварыОстатки.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД = &КодТНВЭД
	|	И (ТоварыОстатки.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения = &ЕдиницаИзмерения
	|	ИЛИ &ЕдиницаИзмерения = НЕОПРЕДЕЛЕНО)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыРасход.АналитикаУчетаНоменклатуры,
	|	ТоварыРасход.АналитикаУчетаНоменклатуры.Номенклатура,
	|	ТоварыРасход.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения,
	|	ТоварыРасход.АналитикаУчетаНоменклатуры.МестоХранения,
	|	ТоварыРасход.АналитикаУчетаНоменклатуры.Назначение,
	|	ТоварыРасход.АналитикаУчетаНоменклатуры.ТипМестаХранения,
	|	ТоварыРасход.ВидЗапасов,
	|	ТоварыРасход.Количество,
	|	ТоварыРасход.КоличествоПоРНПТ,
	|	ТоварыРасход.Сумма,
	|	ТоварыРасход.АналитикаУчетаНоменклатуры.СкладскаяТерритория,
	|	ТоварыРасход.АналитикаУчетаНоменклатуры.Подразделение,
	|	ТоварыРасход.АналитикаУчетаНоменклатуры.Договор,
	|	ТоварыРасход.АналитикаУчетаНоменклатуры.Партнер,
	|	ТоварыРасход.АналитикаУчетаНоменклатуры.Контрагент,
	|	ТоварыРасход.АналитикаУчетаНоменклатуры.Номенклатура.СтранаПроисхождения
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюДокументовИмпорта КАК ТоварыРасход
	|ГДЕ
	|	ТоварыРасход.Регистратор = &Ссылка
	|	И ТоварыРасход.Активность
	|	И ТоварыРасход.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) 
	|	И ТоварыРасход.ТипДокументаИмпорта = &ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров
	|	И ТоварыРасход.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД = &КодТНВЭД
	|	И (ТоварыРасход.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения = &ЕдиницаИзмерения
	|	ИЛИ &ЕдиницаИзмерения = НЕОПРЕДЕЛЕНО)";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает данные для заполнения табличной части Товары по Заявлению.
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ПриобретениеТоваровУслуг - основание
//	Уведомление - ДокументСсылка.УведомлениеОВвозеПрослеживаемыхТоваров - исправляемое уведомление
//	КодТНВЭД - СправочникСсылка.КлассификаторТНВЭД
//	ЕдиницаИзмерения - ОпределяемыйТип.ЕдиницаИзмерения
//	Ссылка - ДокументСсылка.УведомлениеОбОстаткахПрослеживаемыхТоваров
//
// Возвращаемое значение:
//	ТаблицаЗначений
//
Функция ДанныеТЧТоварыПоЗаявлению(ДокументОснование, Уведомление, КодТНВЭД, ЕдиницаИзмерения = Неопределено, Ссылка = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("Уведомление", Уведомление);
	Запрос.УстановитьПараметр("КодТНВЭД", КодТНВЭД);
	Запрос.УстановитьПараметр("ЕдиницаИзмерения", ЕдиницаИзмерения);
	Запрос.УстановитьПараметр("Ссылка", Ссылка); 
	Запрос.УстановитьПараметр("ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.УведомлениеОВвозеПрослеживаемыхТоваров"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.МестоХранения КАК МестоХранения,
	|	ВложенныйЗапрос.Назначение КАК Назначение,
	|	ВложенныйЗапрос.ТипМестаХранения КАК ТипМестаХранения,
	|	ВложенныйЗапрос.ВидЗапасов КАК ВидЗапасов,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ВложенныйЗапрос.КоличествоПоРНПТ) КАК КоличествоПоРНПТ,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	ВложенныйЗапрос.Склад КАК Склад,
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ВложенныйЗапрос.Договор КАК Договор,
	|	ВложенныйЗапрос.Хранитель КАК Хранитель,
	|	ВложенныйЗапрос.Контрагент КАК Контрагент,
	|	МАКСИМУМ(ВложенныйЗапрос.СтранаПроисхождения) КАК СтранаПроисхождения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТоварыОстатки.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|		ТоварыОстатки.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ТоварыОстатки.АналитикаУчетаНоменклатуры.МестоХранения КАК МестоХранения,
	|		ТоварыОстатки.АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
	|		ТоварыОстатки.АналитикаУчетаНоменклатуры.ТипМестаХранения КАК ТипМестаХранения,
	|		ТоварыОстатки.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыОстатки.КоличествоОстаток КАК Количество,
	|		ТоварыОстатки.КоличествоПоРНПТОстаток КАК КоличествоПоРНПТ,
	|		ТоварыОстатки.СуммаОстаток КАК Сумма,
	|		ТоварыОстатки.АналитикаУчетаНоменклатуры.СкладскаяТерритория КАК Склад,
	|		ТоварыОстатки.АналитикаУчетаНоменклатуры.Подразделение КАК Подразделение,
	|		ТоварыОстатки.АналитикаУчетаНоменклатуры.Договор КАК Договор,
	|		ТоварыОстатки.АналитикаУчетаНоменклатуры.Партнер КАК Хранитель,
	|		ТоварыОстатки.АналитикаУчетаНоменклатуры.Контрагент КАК Контрагент,
	|		ТоварыОстатки.АналитикаУчетаНоменклатуры.Номенклатура.СтранаПроисхождения КАК СтранаПроисхождения
	|	ИЗ
	|		РегистрНакопления.ТоварыКОформлениюДокументовИмпорта.Остатки(
	|				,
	|				ДокументПоступления = &ДокументОснование
	|					И ТипДокументаИмпорта = &ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров) КАК ТоварыОстатки
	|	ГДЕ
	|		ТоварыОстатки.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД = &КодТНВЭД
	|		И (ТоварыОстатки.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения = &ЕдиницаИзмерения
	|				ИЛИ &ЕдиницаИзмерения = НЕОПРЕДЕЛЕНО)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыРасход.АналитикаУчетаНоменклатуры,
	|		ТоварыРасход.АналитикаУчетаНоменклатуры.Номенклатура,
	|		ТоварыРасход.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения,
	|		ТоварыРасход.АналитикаУчетаНоменклатуры.МестоХранения,
	|		ТоварыРасход.АналитикаУчетаНоменклатуры.Назначение,
	|		ТоварыРасход.АналитикаУчетаНоменклатуры.ТипМестаХранения,
	|		ТоварыРасход.ВидЗапасов,
	|		ТоварыРасход.Количество,
	|		ТоварыРасход.КоличествоПоРНПТ,
	|		ТоварыРасход.Сумма,
	|		ТоварыРасход.АналитикаУчетаНоменклатуры.СкладскаяТерритория,
	|		ТоварыРасход.АналитикаУчетаНоменклатуры.Подразделение,
	|		ТоварыРасход.АналитикаУчетаНоменклатуры.Договор,
	|		ТоварыРасход.АналитикаУчетаНоменклатуры.Партнер,
	|		ТоварыРасход.АналитикаУчетаНоменклатуры.Контрагент,
	|		ТоварыРасход.АналитикаУчетаНоменклатуры.Номенклатура.СтранаПроисхождения
	|	ИЗ
	|		РегистрНакопления.ТоварыКОформлениюДокументовИмпорта КАК ТоварыРасход
	|	ГДЕ
	|		ТоварыРасход.Регистратор = &Ссылка
	|		И ТоварыРасход.Активность
	|		И ТоварыРасход.ТипДокументаИмпорта = &ИдентификаторУведомлениеОВвозеПрослеживаемыхТоваров
	|		И ТоварыРасход.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД = &КодТНВЭД
	|		И (ТоварыРасход.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения = &ЕдиницаИзмерения
	|				ИЛИ &ЕдиницаИзмерения = НЕОПРЕДЕЛЕНО)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеУведомления.АналитикаУчетаНоменклатуры,
	|		ДанныеУведомления.Номенклатура,
	|		ДанныеУведомления.Номенклатура.ЕдиницаИзмерения,
	|		ДанныеУведомления.Склад,
	|		ДанныеУведомления.Назначение,
	|		ДанныеУведомления.ТипМестаХранения,
	|		ДанныеУведомления.ВидЗапасов,
	|		ДанныеУведомления.Количество,
	|		ДанныеУведомления.КоличествоПоРНПТ,
	|		ДанныеУведомления.Сумма,
	|		ДанныеУведомления.Склад,
	|		ДанныеУведомления.Подразделение,
	|		ДанныеУведомления.Договор,
	|		ДанныеУведомления.Хранитель,
	|		ДанныеУведомления.Контрагент,
	|		ДанныеУведомления.СтранаПроисхождения
	|	ИЗ
	|		Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК ДанныеУведомления
	|	ГДЕ
	|		ДанныеУведомления.Ссылка = &Уведомление) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.МестоХранения,
	|	ВложенныйЗапрос.Контрагент,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры,
	|	ВложенныйЗапрос.ЕдиницаИзмерения,
	|	ВложенныйЗапрос.ТипМестаХранения,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Склад,
	|	ВложенныйЗапрос.Хранитель,
	|	ВложенныйЗапрос.Договор,
	|	ВложенныйЗапрос.ВидЗапасов,
	|	ВложенныйЗапрос.Подразделение
	|
	|ИМЕЮЩИЕ СУММА(ВложенныйЗапрос.Количество) <> 0
	|";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

// Формирует описание реквизитов объекта, заполняемых по статистике их использования.
//
// Параметры:
//	ОписаниеРеквизитов - Структура - описание реквизитов, для которых необходимо получить значения по статистике 
//										см. ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов.
//
Процедура ЗадатьОписаниеЗаполняемыхРеквизитовПоСтатистике(ОписаниеРеквизитов) Экспорт
	
	ЗаполнениеОбъектовПоСтатистике.ДобавитьОписаниеЗаполняемыхРеквизитов(ОписаниеРеквизитов, "Организация");
	
КонецПроцедуры

// Возвращает представление уведомления о ввозе прослеживаемых товаров с учетом состояния проведения.
//
// Параметры:
// 	Номер - Строка - Номер уведомления о ввозе.
// 	Дата - Дата - Дата уведомления о ввозе.
// 	Проведен - Булево - Признак проведения документа.
//
// Возвращаемое значение:
//	Строка - представление уведомления о ввозе прослеживаемых товаров.
//
Функция ПредставлениеУведомленияОВвозеПрослеживаемыхТоваров(Номер, Дата, Проведен) Экспорт
	
	ДанныеШапки = Новый Структура();
	ДанныеШапки.Вставить("Номер", Номер);
	ДанныеШапки.Вставить("Дата", Дата);
	
	МассивПодстрок = Новый Массив;
	МассивПодстрок.Добавить(ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(ДанныеШапки, НСтр("ru = 'Уведомление по ПТ';
																											|en = 'TG notification'")));
	Если Не Проведен Тогда
		МассивПодстрок.Добавить(НСтр("ru = '(не проведено)';
									|en = '(not posted)'"));
	КонецЕсли;
	
	Возврат СтрСоединить(МассивПодстрок, " ");
	
КонецФункции

// Функция находит распоряжения на оформление уведомлений о ввозе прослеживаемых товаров для заданного документа поступления.
//
// Параметры:
//	ДокументПоступления - ДокументСсылка - Документ, для которого необходимо найти распоряжения на оформление уведомлений о ввозе прослеживаемых товаров.
//	КорректировочноеУведомление - Булево - определяет, нужно ли получать все данные из регистра с учетом корректировок, в том числе и отрицательные.
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица с найденными распоряжениями на оформление.
//
Функция РаспоряженияКОформлениюУведомленийОВвозеПоОснованию(ДокументПоступления, КорректировочноеУведомление = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ТоварыКОформлениюДокументовИмпортаОстатки.Организация КАК Организация,
	|	ТоварыКОформлениюДокументовИмпортаОстатки.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД КАК КодТНВЭД,
	|	ТоварыКОформлениюДокументовИмпортаОстатки.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения КАК
	|		ЕдиницаИзмерения,
	|	ТоварыКОформлениюДокументовИмпортаОстатки.ДокументПоступления КАК ДокументПоступления,
	|	СУММА(ЕСТЬNULL(ТоварыКОформлениюДокументовИмпортаОстатки.СуммаОстаток, 0)) КАК Поле1
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюДокументовИмпорта.Остатки(, ДокументПоступления = &ДокументПоступления
	|	И ТипДокументаИмпорта = &ИдентификаторМетаданныхЗаявлениеОВвозе) КАК ТоварыКОформлениюДокументовИмпортаОстатки
	|ГДЕ
	|	ТоварыКОформлениюДокументовИмпортаОстатки.КоличествоОстаток > 0
	|	ИЛИ &КорректировочноеУведомление
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлениюДокументовИмпортаОстатки.Организация,
	|	ТоварыКОформлениюДокументовИмпортаОстатки.АналитикаУчетаНоменклатуры.Номенклатура.КодТНВЭД,
	|	ТоварыКОформлениюДокументовИмпортаОстатки.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения,
	|	ТоварыКОформлениюДокументовИмпортаОстатки.ДокументПоступления";
	
	Запрос.УстановитьПараметр("ДокументПоступления", ДокументПоступления);
	Запрос.УстановитьПараметр("КорректировочноеУведомление", КорректировочноеУведомление);
	Запрос.УстановитьПараметр("ИдентификаторМетаданныхЗаявлениеОВвозе", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.УведомлениеОВвозеПрослеживаемыхТоваров"));
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаРаспоряжений = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаРаспоряжений;
	
КонецФункции

// Функция находит уведомления о ввозе прослеживаемых товаров заданного первичного документа.
//
// Параметры:
//	ПервичныйДокумент - ДокументСсылка - Документ, для которого необходимо найти уведомления о ввозе прослеживаемых товаров.
//	Проведен - Булево - Признак того, что необходимо получить проведенные документы.
//
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица найденных уведомления о ввозе прослеживаемых товаров.
//
Функция УведомленияОВвозеПрослеживаемыхТоваровПоОснованию(ПервичныйДокумент, Проведен = Истина, КодТНВЭД = НЕОПРЕДЕЛЕНО) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Ссылка             КАК Ссылка,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Проведен           КАК Проведен,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Номер              КАК Номер,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Дата               КАК Дата,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Организация        КАК Организация,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Контрагент         КАК Контрагент,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД           КАК КодТНВЭД,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.Склад              КАК Склад,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент  КАК ДокументОснование,
	|	УведомлениеОВвозеПрослеживаемыхТоваров.НомерКорректировки КАК НомерКорректировки
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК УведомлениеОВвозеПрослеживаемыхТоваров
	|ГДЕ
	|	УведомлениеОВвозеПрослеживаемыхТоваров.ПервичныйДокумент = &ДокументОснование
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.ПометкаУдаления = ЛОЖЬ 
	|	И УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД = &КодТНВЭД
	|";
	
	Если Проведен Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПометкаУдаления = ЛОЖЬ", "Проведен");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДокументОснование", ПервичныйДокумент);
	
	Если ЗначениеЗаполнено(КодТНВЭД) Тогда
		Запрос.УстановитьПараметр("КодТНВЭД", КодТНВЭД);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И УведомлениеОВвозеПрослеживаемыхТоваров.КодТНВЭД = &КодТНВЭД", "");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаУведомлений = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаУведомлений;
	
КонецФункции

// Инициализирует параметры, обслуживающие выбор назначений в формах документа.
//
// Возвращаемое значение:
//	См. Справочники.Назначения.МакетФормыВыбораНазначений
//
Функция МакетФормыВыбораНазначений() Экспорт
	
	МакетФормы = Справочники.Назначения.МакетФормыВыбораНазначений();
	
	ШаблонНазначения = Справочники.Назначения.ДобавитьШаблонНазначений(МакетФормы);
	ШаблонНазначения.ДвиженияПоСкладскимРегистрам = "ИСТИНА";
	//++ НЕ УТКА
	ШаблонНазначения.ТипыНазначений.Удалить(ШаблонНазначения.ТипыНазначений.Найти(Перечисления.ТипыНазначений.ДавальческоеПродукция22));
	//-- НЕ УТКА
	
	// В наличии на складе
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы,
																	"ОбеспечениеЗаказов",
																	Истина,
																	"Объект.Товары.Назначение",
																	"ОбеспечениеПоСкладу");
	ОписаниеКолонок.Колонки.НайтиПоЗначению("ВНаличии").Пометка = Истина;
	ОписаниеКолонок.КолонкиПоУмолчанию.Добавить("ВНаличии");
	
	ОписаниеКолонок.УсловиеИспользования = "Объект.Товары.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)";
	
	ОписаниеКолонок.ПутиКДанным.Номенклатура	= "Объект.Товары.Номенклатура";
	ОписаниеКолонок.ПутиКДанным.Характеристика	= "Объект.Товары.АналитикаУчетаНоменклатуры.Характеристика";
	ОписаниеКолонок.ПутиКДанным.Склад			= "Объект.Товары.Склад";
		
	// В наличии у хранителя
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы,
																	"ВсеНазначения",
																	Истина,
																	"Объект.Товары.Назначение",
																	"ОбеспечениеПоДоговору");
	
	ОписаниеКолонок.УсловиеИспользования = "Объект.Товары.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.ДоговорКонтрагента)";
	
	Возврат МакетФормы;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ НЕ УТ

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УведомлениеОВвозеПрослеживаемыхТоваров") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"УведомлениеОВвозеПрослеживаемыхТоваров", 
			НСтр("ru = 'Уведомление о ввозе прослеживаемых товаров';
				|en = 'Notification of traceable goods import'"),
			ПрослеживаемостьБРУ.ПечатьУведомленияОВвозеПрослеживаемыхТоваров(МассивОбъектов, ОбъектыПечати));
			
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "УведомлениеОВвозеПрослеживаемыхТоваров";
	КомандаПечати.Представление  = НСтр("ru = 'Печать';
										|en = 'Print'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Уведомление о ввозе прослеживаемых товаров';
										|en = 'Notification of traceable goods import'");
	КомандаПечати.Обработчик     = "ПрослеживаемостьФормыКлиент.ВыполнитьКомандуПечатиУведомленияОВвозеПрослеживаемыхТоваров";
	КомандаПечати.СписокФорм     = "ФормаДокумента,ФормаСписка";
	
КонецПроцедуры

#КонецОбласти

//-- НЕ УТ

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                    КАК Ссылка,
	|	ДанныеДокумента.Номер                     КАК Номер,
	|	ДанныеДокумента.Дата                      КАК Период,
	|	ДанныеДокумента.Организация               КАК Организация,
	|	ДанныеДокумента.Контрагент                КАК Контрагент,
	|	ДанныеДокумента.Проведен                  КАК Проведен,
	|	ДанныеДокумента.РНПТ                      КАК РНПТ,
	|	ДанныеДокумента.Комментарий               КАК Комментарий,
	|	ДанныеДокумента.ПервичныйДокумент         КАК ДокументПоступления,
	|	ДанныеДокумента.Исправление               КАК Исправление, 
	|	ДанныеДокумента.Ответственный             КАК Ответственный,
	|	ДанныеДокумента.СуммаДокумента            КАК СуммаДокумента,
	|	ДанныеДокумента.СторнируемыйДокумент      КАК СторнируемыйДокумент,
	|	ДанныеДокумента.ИсправляемыйДокумент      КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.ПервичныйДокумент.Договор КАК Договор,
	|	ДанныеДокумента.ПометкаУдаления           КАК ПометкаУдаления, 
	|	ДанныеДокумента.ПервичныйДокумент.Партнер КАК Поставщик,
	|	ДанныеДокумента.ПервичныйДокумент.Склад   КАК МестоХранения, 
	|	ДанныеДокумента.ПервичныйДокумент.Валюта  КАК Валюта,
	|	ДанныеДокумента.ПервичныйДокумент.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.ПервичныйДокумент.Подразделение           КАК Подразделение
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Реквизиты = РезультатЗапроса.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ИдентификаторМетаданных = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяОбъекта());
	УчетПрослеживаемыхТоваровЛокализация.УстановитьПараметрыИспользованияУчетаПрослеживаемыхТоваров(Запрос);
	
	Запрос.УстановитьПараметр("НомерНаПечать",           ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер,Истина));
	Запрос.УстановитьПараметр("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяОбъекта()));
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",   Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС);;
	Запрос.УстановитьПараметр("ТипДокументаИмпорта",     ИдентификаторМетаданных);  
	
	ИнформацияПоДоговору = "";
	Если ЗначениеЗаполнено(Реквизиты.Договор) Тогда
		ШаблонСтроки = НСтр("ru = 'По договору ""%1""';
							|en = 'Under the ""%1"" contract'"); // @НСтр
		ИнформацияПоДоговору = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтроки, Реквизиты.Договор);
	КонецЕсли;
	Запрос.УстановитьПараметр("ИнформацияПоДоговору", ИнформацияПоДоговору);
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаКорректировки(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаКорректировки";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВложенныйЗапрос.ВидЗапасов КАК ВидЗапасов,
	|	ВложенныйЗапрос.НомерГТД КАК НомерГТД,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ВложенныйЗапрос.КоличествоПоРНПТ) КАК КоличествоПоРНПТ
	|ПОМЕСТИТЬ ВтТаблицаКорректировки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТоварыСторно.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаТоварыСторно.ВидЗапасов КАК ВидЗапасов,
	|		ТаблицаТоварыСторно.НомерГТД КАК НомерГТД,
	|		ТаблицаТоварыСторно.Количество КАК Количество,
	|		ТаблицаТоварыСторно.КоличествоПоРНПТ КАК КоличествоПоРНПТ
	|	ИЗ
	|		Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК ТаблицаТоварыСторно
	|	ГДЕ
	|		ТаблицаТоварыСторно.Ссылка = &СторнируемыйДокумент
	|		И ТаблицаТоварыСторно.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|		И &РНПТ <> """"
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаТовары.АналитикаУчетаНоменклатуры,
	|		ТаблицаТовары.ВидЗапасов,
	|		ТаблицаТовары.НомерГТД,
	|		-ТаблицаТовары.Количество,
	|		-ТаблицаТовары.КоличествоПоРНПТ
	|	ИЗ
	|		Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|		И ТаблицаТовары.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|		И &РНПТ <> """") КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры,
	|	ВложенныйЗапрос.ВидЗапасов,
	|	ВложенныйЗапрос.НомерГТД
	|	
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.КоличествоПоРНПТ) > 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыОрганизаций";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаКорректировки", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаКорректировки(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки					КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)		КАК ВидДвижения,
	|	&Период										КАК Период,
	|	&Организация								КАК Организация,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.ВидЗапасов					КАК ВидЗапасов,
	|	ТаблицаТовары.НомерГТД						КАК НомерГТД,
	|	ТаблицаТовары.Количество					КАК Количество,
	|	ТаблицаТовары.КоличествоПоРНПТ				КАК КоличествоПоРНПТ,
	|	ЛОЖЬ										КАК Первичное
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|	И &РНПТ <> """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки					КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)		КАК ВидДвижения,
	|	&Период										КАК Период,
	|	&Организация								КАК Организация,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.ВидЗапасов					КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)	КАК НомерГТД,
	|	ТаблицаТовары.Количество					КАК Количество,
	|	0											КАК КоличествоПоРНПТ,
	|	ЛОЖЬ										КАК Первичное
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|	И &РНПТ <> """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0											КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)		КАК ВидДвижения,
	|	&Период										КАК Период,
	|	&Организация								КАК Организация,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.ВидЗапасов					КАК ВидЗапасов,
	|	ТаблицаТовары.НомерГТД						КАК НомерГТД,
	|	ТаблицаТовары.Количество					КАК Количество,
	|	ТаблицаТовары.КоличествоПоРНПТ				КАК КоличествоПоРНПТ,
	|	ЛОЖЬ										КАК Первичное
	|ИЗ
	|	ВтТаблицаКорректировки КАК ТаблицаТовары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0											КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)		КАК ВидДвижения,
	|	&Период										КАК Период,
	|	&Организация								КАК Организация,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.ВидЗапасов					КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)	КАК НомерГТД,
	|	ТаблицаТовары.Количество					КАК Количество,
	|	0											КАК КоличествоПоРНПТ,
	|	ЛОЖЬ										КАК Первичное
	|ИЗ
	|	ВтТаблицаКорректировки КАК ТаблицаТовары
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыКОформлениюДокументовИмпорта(Запрос, ТекстыЗапроса, Регистры)
	
	
	ИмяРегистра = "ТоварыКОформлениюДокументовИмпорта";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
		
	ТекстЗапроса = 
	"
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки					КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)		КАК ВидДвижения,
	|	&Период										КАК Период,
	|	&Организация								КАК Организация,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.ВидЗапасов					КАК ВидЗапасов,
	|	&РНПТ										КАК НомерГТД,
	|	ТаблицаТовары.Количество					КАК Количество,
	|	ТаблицаТовары.КоличествоПоРНПТ				КАК КоличествоПоРНПТ,
	|	&Поставщик									КАК Поставщик,
	|	&ДокументПоступления						КАК ДокументПоступления,
	|	&ТипДокументаИмпорта						КАК ТипДокументаИмпорта,
	|	Аналитика.Номенклатура						КАК Номенклатура,
	|	Аналитика.Характеристика					КАК Характеристика,
	|	ТаблицаТовары.Сумма							КАК Сумма,
	|	ЛОЖЬ										КАК Первичное
	|ИЗ
	|	Документ.УведомлениеОВвозеПрослеживаемыхТоваров.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                    КАК Ссылка,
	|	&Период                    КАК ДатаДокументаИБ,
	|	&Номер                     КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных   КАК ТипСсылки,
	|	&Организация               КАК Организация,
	|	&ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО               КАК Партнер,
	|	&Контрагент                КАК Контрагент,
	|	&Договор                   КАК Договор,
	|	&НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	&МестоХранения             КАК МестоХранения,
	|	&Подразделение             КАК Подразделение,
	|	&Ответственный             КАК Ответственный,
	|	&Комментарий               КАК Комментарий,
	|	&Валюта                    КАК Валюта,
	|	&СуммаДокумента            КАК Сумма,
	|	НЕОПРЕДЕЛЕНО               КАК Статус,
	|	&Проведен                  КАК Проведен,
	|	&ПометкаУдаления           КАК ПометкаУдаления,
	|	ЛОЖЬ                       КАК ДополнительнаяЗапись,
	|	&ИнформацияПоДоговору      КАК Дополнительно,
	|	&Период                    КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать             КАК НомерПервичногоДокумента,
	|	&Исправление               КАК СторноИсправление,
	|	&СторнируемыйДокумент      КАК СторнируемыйДокумент,
	|	&ИсправляемыйДокумент      КАК ИсправляемыйДокумент,
	|	&Период                    КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО               КАК Приоритет
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента					= ПолноеИмяОбъекта();
	СинонимТаблицыДокумента				= "";
	ПереопределениеРасчетаПараметров	= Новый Структура;
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
		
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных",
								ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	ЗначенияПараметров.Вставить("ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров",
								УчетПрослеживаемыхТоваровЛокализация.ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров());
	ЗначенияПараметров.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС);
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ПереопределениеРасчетаПараметров.Вставить("Договор", "ДанныеДокумента.ПервичныйДокумент.Договор");
		ПереопределениеРасчетаПараметров.Вставить("Поставщик", "ДанныеДокумента.ПервичныйДокумент.Партнер");
		ПереопределениеРасчетаПараметров.Вставить("МестоХранения", "ДанныеДокумента.ПервичныйДокумент.Склад");
		ПереопределениеРасчетаПараметров.Вставить("Валюта", "ДанныеДокумента.ПервичныйДокумент.Валюта");
		ПереопределениеРасчетаПараметров.Вставить("НаправлениеДеятельности", "ДанныеДокумента.ПервичныйДокумент.НаправлениеДеятельности");
		ПереопределениеРасчетаПараметров.Вставить("Подразделение", "ДанныеДокумента.ПервичныйДокумент.Подразделение");
		ПереопределениеРасчетаПараметров.Вставить("ИнформацияПоДоговору", """""");
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		ВЗапросеЕстьИсточник = Ложь;
		
	ИначеЕсли ИмяРегистра = "ТоварыОрганизаций" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ТаблицаТовары";
		
	Иначе
		
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", 	ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", 		ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;

	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ВЗапросеЕстьИсточник,
			ПереопределениеРасчетаПараметров);
		
	Иначе
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ПереопределениеРасчетаПараметров,
			ТекстыЗапросаВременныхТаблиц);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция ПолноеИмяОбъекта()
	
	Возврат "Документ.УведомлениеОВвозеПрослеживаемыхТоваров";
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли