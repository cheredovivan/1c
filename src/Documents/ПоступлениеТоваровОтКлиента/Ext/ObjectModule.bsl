#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область УсловияПродаж

// Заполняет условия продаж в поступлении товаров от клиента.
//
// Параметры:
//	УсловияПродаж - Структура - Данные для заполнения.
//
Процедура ЗаполнитьУсловияПродаж(Знач УсловияПродаж) Экспорт
	
	Если УсловияПродаж = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УсловияПродаж.Валюта) Тогда
		Валюта = УсловияПродаж.Валюта;
	КонецЕсли;
	НаправлениеДеятельности = УсловияПродаж.НаправлениеДеятельности;
	
	Если ЗначениеЗаполнено(УсловияПродаж.Организация) Тогда
		Организация = УсловияПродаж.Организация;
	КонецЕсли;
	
	Если УсловияПродаж.Типовое <> Неопределено И Не УсловияПродаж.Типовое Тогда
		Если ЗначениеЗаполнено(УсловияПродаж.Контрагент) Тогда
			Контрагент = УсловияПродаж.Контрагент;
		КонецЕсли;
	КонецЕсли;
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
	
	Договор = Документы.ПоступлениеТоваровОтКлиента.ПолучитьДоговорПоУмолчанию(ЭтотОбъект);
	РеквизитыДоговора = Новый Структура("Валюта, НаправлениеДеятельности, Подразделение", "ВалютаВзаиморасчетов");
	Справочники.ДоговорыКонтрагентов.ЗаполнитьРеквизитыДокумента(ЭтотОбъект, Договор, РеквизитыДоговора);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоходовПоНаправлениямДеятельности") Тогда
		НаправленияДеятельностиСервер.ЗаполнитьНаправлениеПоУмолчанию(НаправлениеДеятельности, Соглашение, Договор);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УсловияПродаж.Склад) Тогда
		Склад = УсловияПродаж.Склад;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет условия продаж по умолчанию в поступлении товаров от клиента.
//
Процедура ЗаполнитьУсловияПродажПоУмолчанию() Экспорт
	
	ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	
	Если ЗначениеЗаполнено(Партнер)
		Или Не ИспользоватьСоглашенияСКлиентами Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ВыбранноеСоглашение",   Соглашение);
		ПараметрыОтбора.Вставить("ПустаяСсылкаДокумента", Документы.ПоступлениеТоваровОтКлиента.ПустаяСсылка());
		ПараметрыОтбора.Вставить("ХозяйственныеОперации",
			Документы.ПоступлениеТоваровОтКлиента.ХозяйственнаяОперацияДоговора());
		
		УсловияПродажПоУмолчанию = ПродажиСервер.ПолучитьУсловияПродажПоУмолчанию(
										Партнер, ПараметрыОтбора);
		
		Если УсловияПродажПоУмолчанию <> Неопределено Тогда
			
			Если Не ИспользоватьСоглашенияСКлиентами
				Или (Соглашение <> УсловияПродажПоУмолчанию.Соглашение
					И ЗначениеЗаполнено(УсловияПродажПоУмолчанию.Соглашение)) Тогда
				
				Соглашение = УсловияПродажПоУмолчанию.Соглашение;
				ЗаполнитьУсловияПродаж(УсловияПродажПоУмолчанию);
				
				Если ИспользоватьСоглашенияСКлиентами Тогда
					ЗаполнитьЦеныПоУсловиямПродаж();
				КонецЕсли;
				
			Иначе
				Соглашение = УсловияПродажПоУмолчанию.Соглашение;
				
				ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
			КонецЕсли;
			
		Иначе
			Соглашение = Неопределено;
			
			ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет условия продаж по соглашению в поступлении товаров от клиента.
//
// Параметры:
//	ПересчитатьЦены - Булево - Пересчитывать цены по соглашению.
//
Процедура ЗаполнитьУсловияПродажПоСоглашению(ПересчитатьЦены = Истина) Экспорт
	
	Если Не ЗначениеЗаполнено(Соглашение) Тогда
		Возврат;
	КонецЕсли;
	
	УсловияПродаж = ПродажиСервер.ПолучитьУсловияПродаж(Соглашение, Истина, Истина);
	
	ЗаполнитьУсловияПродаж(УсловияПродаж);
	Если ПересчитатьЦены = Истина Тогда
		ЗаполнитьЦеныПоУсловиямПродаж();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Формирует временные таблицы с данными документа.
//
// Возвращаемое значение:
//	МенеджерВременныхТаблиц - менеджер временных таблиц.
//
Функция ВременныеТаблицыДанныхДокумента() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Дата        КАК Дата,
	|	НЕОПРЕДЕЛЕНО КАК Партнер,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка) КАК Соглашение,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка) КАК НалогообложениеНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) КАК ТипЗапасов
	|ПОМЕСТИТЬ ТаблицаДанныхДокумента
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки         КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура        КАК Номенклатура,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК ОсновнаяАналитика,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатурыТоварыУПартнеров КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Характеристика      КАК Характеристика,
	|	ТаблицаТоваров.Назначение          КАК Назначение,
	|	ТаблицаТоваров.НоменклатураОприходование   КАК НоменклатураОприходование,
	|	ТаблицаТоваров.ХарактеристикаОприходование КАК ХарактеристикаОприходование,
	|	ТаблицаТоваров.Серия               КАК Серия,
	|	ТаблицаТоваров.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ТаблицаТоваров.Количество          КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|				ИЛИ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТоваров.КоличествоПоРНПТ
	|	КОНЕЦ КАК КоличествоПоРНПТ,
	|	&Договор                           КАК Склад,
	|	ТаблицаТоваров.НомерГТД            КАК НомерГТД
	|ПОМЕСТИТЬ ВтТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки            КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура           КАК Номенклатура,
	|	ТаблицаТоваров.ОсновнаяАналитика      КАК ОсновнаяАналитика,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Характеристика         КАК Характеристика,
	|	ТаблицаТоваров.Назначение             КАК Назначение,
	|	ТаблицаТоваров.НоменклатураОприходование КАК НоменклатураОприходование,
	|	ТаблицаТоваров.ХарактеристикаОприходование КАК ХарактеристикаОприходование,
	|	ТаблицаТоваров.Серия                  КАК Серия,
	|	ТаблицаТоваров.СтатусУказанияСерий	  КАК СтатусУказанияСерий,
	|	ТаблицаТоваров.Количество             КАК Количество,
	|	ТаблицаТоваров.КоличествоПоРНПТ       КАК КоличествоПоРНПТ,
	|	ТаблицаТоваров.Склад                  КАК Склад,
	|	ТаблицаТоваров.НомерГТД               КАК НомерГТД,
	|	ИСТИНА                                КАК ПодбиратьВидыЗапасов
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	ВтТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК ОсновнаяАналитика,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатурыТоварыУПартнеров КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.НоменклатураОприходование КАК НоменклатураОприходование, 
	|	ТаблицаВидыЗапасов.ХарактеристикаОприходование КАК ХарактеристикаОприходование,
	|	ТаблицаВидыЗапасов.ВидЗапасов  КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.Количество  КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|				ИЛИ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.КоличествоПоРНПТ
	|	КОНЕЦ                          КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.НомерГТД    КАК НомерГТД
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	&ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////// 4
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки       КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.ВидЗапасов        КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.ОсновнаяАналитика КАК ОсновнаяАналитика,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура               КАК Номенклатура,
	|	Аналитика.Характеристика             КАК Характеристика,
	|	ТаблицаВидыЗапасов.НоменклатураОприходование КАК НоменклатураОприходование,
	|	ТаблицаВидыЗапасов.ХарактеристикаОприходование КАК ХарактеристикаОприходование,
	|	ТаблицаВидыЗапасов.Количество        КАК Количество,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ  КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.НомерГТД          КАК НомерГТД,
	|	&ВидыЗапасовУказаныВручную           КАК ВидыЗапасовУказаныВручную
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ТаблицаТоваров",        Товары);
	Запрос.УстановитьПараметр("ТаблицаВидыЗапасов",    ВидыЗапасов);
	Запрос.УстановитьПараметр("Ссылка",                Ссылка);
	Запрос.УстановитьПараметр("Дата",                  Дата);
	Запрос.УстановитьПараметр("Партнер",               Партнер);
	Запрос.УстановитьПараметр("Контрагент",            Контрагент);
	Запрос.УстановитьПараметр("Договор",               Договор);
	Запрос.УстановитьПараметр("Организация",           Организация);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("Склад",                 Склад);
	Запрос.УстановитьПараметр("ВидыЗапасовУказаныВручную", ВидыЗапасовУказаныВручную);
	
	УчетПрослеживаемыхТоваровЛокализация.УстановитьПараметрыИспользованияУчетаПрослеживаемыхТоваров(Запрос);
	
	ЗапасыСервер.ДополнитьВременныеТаблицыОбязательнымиКолонками(Запрос);
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

// Инициализирует параметры заполнения видов запасов дополнительных свойств документа, используемых при записи документа
// в режиме 'Проведения' или 'Отмены проведения'.
//
// Параметры:
//	ДокументОбъект - ДокументОбъект.ПоступлениеТоваровОтКлиента - документ, для которого выполняется инициализация параметров.
//	РежимЗаписи - РежимЗаписиДокумента - режим записи документа.
//
Процедура ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ДокументОбъект, РежимЗаписи) Экспорт
	
	ПараметрыЗаполнения = ПараметрыЗаполненияВидовЗапасов();
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ПараметрыЗаполнения.ДокументДелаетИПриходИРасход = Ложь;
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		ПараметрыЗаполнения = ЗапасыСервер.ПараметрыЗаполненияВидовЗапасов();
	КонецЕсли;
	
	ДокументОбъект.ДополнительныеСвойства.Вставить("ПараметрыЗаполненияВидовЗапасов", ПараметрыЗаполнения);
	
КонецПроцедуры

// Заполняет реквизиты, хранящие информацию о видах запасов и аналитиках учета номенклатуры в табличной части 'Товары'
// документа, а также заполняет табличную часть 'ВидыЗапасов'.
//
// Параметры:
//	Отказ - Булево - признак того, что не удалось заполнить данные.
//	ТаблицыДокумента - см. Документы.ПоступлениеТоваровОтКлиента.КоллекцияТабличныхЧастейТоваров.
//
Процедура ЗаполнитьВидыЗапасовПриОбмене(Отказ, ТаблицыДокумента) Экспорт
	
	ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров();
	ЗаполнитьВидыЗапасов(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары.НоменклатураОприходование");
	
	ПараметрыПроверкиКоличества = Документы.ПоступлениеТоваровОтКлиента.ПараметрыТЧДляОкругления(Склад);
	
	НоменклатураСервер.ПроверитьЗаполнениеКоличества(
		ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверкиКоличества["Товары"]);
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ПоступлениеТоваровОтКлиента);
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИменаПолейССуффиксом.Вставить("Номенклатура", "НоменклатураОприходование");
	ПараметрыПроверки.ИменаПолейССуффиксом.Вставить("Характеристика", "ХарактеристикаОприходование");
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,
		МассивНепроверяемыхРеквизитов,
		Отказ,
		ПараметрыПроверки);

	НоменклатураСервер.ПроверитьВидНоменклатурыОприходования(ЭтотОбъект,Отказ);

	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект, ПараметрыУказанияСерий, Отказ, МассивНепроверяемыхРеквизитов);
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары.КоличествоПоРНПТ");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.НомерГТД");
	
	ЭтоПрослеживаемыйДокумент = УчетПрослеживаемыхТоваровЛокализация.ЭтоПрослеживаемыйДокумент(Товары, Дата);
	
	Если (ПолучитьФункциональнуюОпцию("ЗапретитьПоступлениеТоваровБезНомеровГТД")
			Или ЭтоПрослеживаемыйДокумент) Тогда
		ЗапасыСервер.ПроверитьЗаполнениеНомеровГТД(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		
		АдресОшибки = " " + НСтр("ru = 'в строке %НомерСтроки% списка ""Товары""';
								|en = 'in line %НомерСтроки% of the ""Goods"" list'");
		АдресОшибки =  СтрЗаменить(АдресОшибки, "%НомерСтроки%", СтрокаТЧ.НомерСтроки);
		
		ПолеНоменклатураОприходование = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
			"Товары",
			СтрокаТЧ.НомерСтроки,
			"НоменклатураОприходование");
		
		Если СтрокаТЧ.Порча Тогда
			
			Если Не ЗначениеЗаполнено(СтрокаТЧ.НоменклатураОприходование) Тогда
			
				ТекстОшибки = НСтр("ru = 'Не заполнена колонка ""Номенклатура (оприходование)""';
									|en = 'Column ""Items (recording as received)"" is not filled in'");
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки + АдресОшибки,
															ЭтотОбъект,
															ПолеНоменклатураОприходование,
															,
															Отказ);
			
			ИначеЕсли СтрокаТЧ.Номенклатура = СтрокаТЧ.НоменклатураОприходование Тогда
			
				ТекстОшибки = НСтр("ru = 'Товар исходного качества совпадает с товаром другого качества';
									|en = 'Goods of original quality matches with goods of different quality'");
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки + АдресОшибки,
															ЭтотОбъект,
															ПолеНоменклатураОприходование,
															,
															Отказ);
			
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Распоряжение)
			И Не ЗначениеЗаполнено(СтрокаТЧ.Распоряжение) Тогда
			
			ТекстОшибки = НСтр("ru = 'Не заполнено поле ""Распоряжение"" в строке %НомерСтроки% списка ""Товары""';
								|en = 'The ""Reference"" field is not filled in the %НомерСтроки% line of the ""Goods"" list'");
			ТекстОшибки =  СтрЗаменить(ТекстОшибки, "%НомерСтроки%", СтрокаТЧ.НомерСтроки);
			ПолеОшибки  = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары",
																			СтрокаТЧ.НомерСтроки,
																			"Распоряжение");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, ПолеОшибки, Неопределено, Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ИсправлениеДокументов.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
	Если Не Отказ
		И ОбщегоНазначенияУТ.ПроверитьЗаполнениеРеквизитовОбъекта(ЭтотОбъект, ПроверяемыеРеквизиты) Тогда
		
		Отказ = Истина;
		
	КонецЕсли;
	
	ПродажиСервер.ПроверитьКорректностьЗаполненияДокументаПродажи(ЭтотОбъект, Отказ);
	
	ПоступлениеТоваровОтКлиентаЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ИнициализироватьДокументПередЗаполнением();
	
	ЗаполненНаОснованииДокумента = Ложь;
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("АктОРасхождениях") Тогда
			ЗаполнитьДокументНаОснованииАктаОРасхождениях(ДанныеЗаполнения);
			ЗаполненНаОснованииДокумента = Истина;
		
		ИначеЕсли ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
			
			ДокОснование = ДанныеЗаполнения.ДокументОснование;
			МассивОснований = Неопределено;
			Если ЗначениеЗаполнено(ДокОснование) Тогда
				Если ТипЗнч(ДокОснование) = Тип("Массив") Тогда
					МассивОснований = ДокОснование;
				Иначе
					МассивОснований = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДокОснование);
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(МассивОснований) Тогда
				ЗаполнитьДокументПоРаспоряжениям(МассивОснований);
				ЗаполненНаОснованииДокумента = Истина;
			КонецЕсли;
		КонецЕсли;
	
	ИначеЕсли ТипДанныхЗаполнения = Тип("Массив") И ДанныеЗаполнения.Количество() > 0 Тогда

		ЗаполнитьДокументПоРаспоряжениям(ДанныеЗаполнения);
	
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ПоступлениеТоваровОтКлиента") Тогда

		ИсправлениеДокументов.ЗаполнитьИсправление(ЭтотОбъект, ДанныеЗаполнения);
		ЗаполненНаОснованииДокумента = Истина;

	КонецЕсли;
	
	ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	
	Если Не ИспользоватьСоглашенияСКлиентами
		И Не ЗаполненНаОснованииДокумента Тогда
		ЗаполнитьУсловияПродажПоУмолчанию();
	КонецЕсли;
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
	ПараметрыРасчета = УчетПрослеживаемыхТоваровКлиентСерверЛокализация.ПараметрыПолученияКоэффициентаРНПТ(
						ЭтотОбъект,
						"Договор");
	УчетПрослеживаемыхТоваровЛокализация.ЗаполнитьКоличествоПоРНПТВТабличнойЧасти(ПараметрыРасчета, Товары);
	
	ПоступлениеТоваровОтКлиентаЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Распоряжение) Тогда
		Для Каждого ТекСтрока Из Товары Цикл
			Если Не ЗначениеЗаполнено(ТекСтрока.Распоряжение) Тогда
				ТекСтрока.Распоряжение = Распоряжение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Склад) Тогда
		
		СкладГруппа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "ЭтоГруппа");
		
		Если Не СкладГруппа Тогда
			Для Каждого ТекСтрока Из Товары Цикл
				Если Не ЗначениеЗаполнено(ТекСтрока.Склад) Тогда
					ТекСтрока.Склад = Склад;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ПараметрыОкругления = Документы.ПоступлениеТоваровОтКлиента.ПараметрыТЧДляОкругления(Склад);
	НоменклатураСервер.ОкруглитьКоличествоШтучныхТоваров(ЭтотОбъект, РежимЗаписи,
		ПараметрыОкругления["Товары"]);
	
	СуммаДокумента         = ПолучитьСуммуДокумента();
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект,
		Документы.ПоступлениеТоваровОтКлиента);
	
	НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект, ПараметрыУказанияСерий);
	
	ПоступлениеПоЗаказу = Ложь;
	
	Для Каждого СтрокаТоваров Из Товары Цикл
		Если ТипЗнч(СтрокаТоваров) <> Тип("ДокументСсылка.КорректировкаРеализации") Тогда
			ПоступлениеПоЗаказу = Истина;
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПоступлениеПоЗаказу
		И УменьшитьОтгрузкуПоРаспоряжению Тогда
		
		УменьшитьОтгрузкуПоРаспоряжению = Ложь;
		
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров();
		ЗаполнитьВидыЗапасов(Отказ);
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Если Не ВидыЗапасовУказаныВручную Тогда
			ВидыЗапасов.Очистить();
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ЭтотОбъект, "Товары,ВидыЗапасов");
	
	ИсправлениеДокументов.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	ПоступлениеТоваровОтКлиентаЛокализация.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
	Документы.ПоступлениеТоваровОтКлиента.ДобавитьДополнительноеСвойствоДляКонтроляПриПроведении(ЭтотОбъект);
	
	ПоступлениеТоваровОтКлиентаЛокализация.ПриЗаписи(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ЭтотОбъект, РежимЗаписиДокумента.Проведение);
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ПоступлениеТоваровОтКлиентаЛокализация.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ИнициализироватьПараметрыЗаполненияВидовЗапасовДляПроведения(ЭтотОбъект, РежимЗаписиДокумента.ОтменаПроведения);
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ПоступлениеТоваровОтКлиентаЛокализация.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИсправлениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
	ВидыЗапасовУказаныВручную = Ложь;
	ДокументОснование = Неопределено;
	
	Для Каждого ТекСтрока Из Товары Цикл
		ТекСтрока.ИдентификаторСтроки = "";
	КонецЦикла;
	
	Серии.Очистить();
	ВидыЗапасов.Очистить();
	
	ИнициализироватьДокумент();
	
	ПараметрыРасчета = УчетПрослеживаемыхТоваровКлиентСерверЛокализация.ПараметрыПолученияКоэффициентаРНПТ(
						ЭтотОбъект,
						"Договор");
	УчетПрослеживаемыхТоваровЛокализация.ЗаполнитьКоличествоПоРНПТВТабличнойЧасти(ПараметрыРасчета, Товары);
	
	ОбщегоНазначенияУТ.ОчиститьИдентификаторыДокумента(ЭтотОбъект, "Товары,ВидыЗапасов");
	
	ПоступлениеТоваровОтКлиентаЛокализация.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокументПередЗаполнением()
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Автор         = Пользователи.ТекущийПользователь();
	Организация   = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Склад         = ЗначениеНастроекПовтИсп.ПолучитьСкладПоУмолчанию(Склад);
	Если Не ЗначениеЗаполнено(Валюта) Тогда
		Валюта = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	КонецЕсли;
	
	ОтветственныеЛицаСервер.ЗаполнитьМенеджера(ЭтотОбъект, Ложь);
	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	Если Не ЗначениеЗаполнено(Менеджер) Тогда
		Менеджер = Пользователи.ТекущийПользователь();
	КонецЕсли;
	Подразделение = ЗначениеНастроекПовтИсп.ПодразделениеПользователя(Менеджер, Подразделение);
	
	СкладГруппа = Справочники.Склады.ЭтоГруппаИСкладыИспользуютсяВТЧДокументовПродажи(Склад);
	СкладыСервер.ЗаполнитьСкладыВТабличнойЧасти(Склад, СкладГруппа, Товары, Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоРаспоряжениям(МассивРаспоряжений)
	
	РаспоряжениеЗаполнения = МассивРаспоряжений[0];
	
	ОперацияРаспоряжения = Неопределено;
	Если ТипЗнч(РаспоряжениеЗаполнения) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
		
		ОснованиеКорректировки =
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РаспоряжениеЗаполнения, "ДокументОснование");
		
		ХозяйственнаяОперацияОснованияКорректировки =
			Документы.КорректировкаРеализации.ХозяйственнаяОперацияДокументаОснования(ОснованиеКорректировки);
		
		Если КомиссионнаяТорговляСервер.РеализацияЧерезКомиссионера(ХозяйственнаяОперацияОснованияКорректировки) Тогда
			ОперацияРаспоряжения = ХозяйственнаяОперацияОснованияКорректировки;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОперацияРаспоряжения = Неопределено Тогда 
		ОперацияРаспоряжения =
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РаспоряжениеЗаполнения, "ХозяйственнаяОперация");
	КонецЕсли;
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОснованииПоОперации(РаспоряжениеЗаполнения,
									Метаданные().Имя,
									ОперацияРаспоряжения);
	
	РеквизитыШапки = Новый Структура();
	ПродажиВызовСервера.СформироватьДанныеЗаполненияРеализации(МассивРаспоряжений, Метаданные().Имя, РеквизитыШапки);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыШапки);
	Валюта = РеквизитыШапки.ВалютаВзаиморасчетов;
	ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеТоваровОтКлиента;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
	|				И ВЫРАЗИТЬ(ТаблицаТовары.ЗаказКлиента КАК Документ.ЗаказКлиента).ЭтоЗаказКакСчет
	|				И ТаблицаТовары.Ссылка.Исправление
	|			ТОГДА ТаблицаТовары.Ссылка.ИсправляемыйДокумент
	|		КОГДА ТаблицаТовары.ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
	|				И ВЫРАЗИТЬ(ТаблицаТовары.ЗаказКлиента КАК Документ.ЗаказКлиента).ЭтоЗаказКакСчет
	|			ТОГДА ТаблицаТовары.Ссылка
	|		КОГДА ТаблицаТовары.ЗаказКлиента ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|				И ВЫРАЗИТЬ(ТаблицаТовары.ЗаказКлиента КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ЭтоЗаказКакСчет
	|				И ТаблицаТовары.Ссылка.Исправление
	|			ТОГДА ТаблицаТовары.Ссылка.ИсправляемыйДокумент
	|		КОГДА ТаблицаТовары.ЗаказКлиента ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|				И ВЫРАЗИТЬ(ТаблицаТовары.ЗаказКлиента КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ЭтоЗаказКакСчет
	|			ТОГДА ТаблицаТовары.Ссылка
	|		ИНАЧЕ
	|			ТаблицаТовары.ЗаказКлиента
	|	КОНЕЦ КАК Распоряжение
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В (&МассивРаспоряжений)
	|	И ТаблицаТовары.Ссылка.ОтгрузкаПоЗаказам
	|	И НЕ ТаблицаТовары.ЗаказКлиента В(
	|	                  ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|	                  ЗНАЧЕНИЕ(Документ.ОтгрузкаТоваровКлиенту.ПустаяСсылка),
	|	                  ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка),
	|	                  НЕОПРЕДЕЛЕНО
	|	                  )
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Ссылка.Исправление
	|			ТОГДА ТаблицаТовары.Ссылка.ИсправляемыйДокумент
	|		ИНАЧЕ
	|			ТаблицаТовары.Ссылка
	|	КОНЕЦ КАК Распоряжение
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка В (&МассивРаспоряжений)
	|	И НЕ ТаблицаТовары.Ссылка.ОтгрузкаПоЗаказам
	|	И ТаблицаТовары.ЗаказКлиента В(
	|	                  ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|	                  ЗНАЧЕНИЕ(Документ.ОтгрузкаТоваровКлиенту.ПустаяСсылка),
	|	                  ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка),
	|	                  НЕОПРЕДЕЛЕНО
	|	                  )
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ссылка КАК Распоряжение
	|ИЗ
	|	Документ.ЗаказКлиента КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка В (&МассивРаспоряжений)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ссылка КАК Распоряжение
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка В (&МассивРаспоряжений)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ссылка КАК Распоряжение
	|ИЗ
	|	Документ.ПервичныйДокумент КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка В (&МассивРаспоряжений)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Ссылка КАК Распоряжение
	|ИЗ
	|	Документ.КорректировкаРеализации КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка В (&МассивРаспоряжений)
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("МассивРаспоряжений", МассивРаспоряжений);
	
	Распоряжения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Распоряжение");
	
	МенеджерНакладной = Документы.ПоступлениеТоваровОтКлиента;
	
	ПараметрыЗаполненияДокумента = МенеджерНакладной.ПараметрыЗаполненияДокумента();
	МенеджерНакладной.ИнициализироватьПараметрыЗаполнения(ПараметрыЗаполненияДокумента,
		РеквизитыШапки, Распоряжения);
	
	ТаблицаНакладная = МенеджерНакладной.ДанныеТаблицыТоварыДокумента(Ссылка);
	
	МенеджерНакладной.ЗаполнитьПоЗаказамОрдерам(ТаблицаНакладная,
		Неопределено, ПараметрыЗаполненияДокумента);
	
	КолонкаТаблицы = ТаблицаНакладная.Колонки.Количество; // КолонкаТаблицыЗначений
	КолонкаТаблицы.Имя = "КоличествоДоИзменения";
	
	КолонкаТаблицы = ТаблицаНакладная.Колонки.КоличествоВЗаказе; // КолонкаТаблицыЗначений
	КолонкаТаблицы.Имя = "Количество";

	Товары.Загрузить(ТаблицаНакладная);

	МенеджерНакладной.ОбновитьЗависимыеРеквизитыТабличнойЧасти(Товары, ПараметрыЗаполненияДокумента);
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, МенеджерНакладной);
		
	НоменклатураСервер.ЗаполнитьСерииПоFEFO(ЭтотОбъект, ПараметрыУказанияСерий, Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьДокументНаОснованииАктаОРасхождениях(ДанныеЗаполнения)

	АктОРасхождениях = ДанныеЗаполнения.АктОРасхождениях;
	ОснованиеАктаОРасхождениях = ДанныеЗаполнения.ОснованиеАкта;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Партнер                 КАК Партнер,
	|	Таблица.Контрагент              КАК Контрагент,
	|	Таблица.Соглашение              КАК Соглашение,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеТоваровОтКлиента) КАК ХозяйственнаяОперация,
	|	Таблица.Организация             КАК Организация,
	|	Таблица.Договор                 КАК Договор,
	|	Таблица.Склад                   КАК Склад,
	|	Таблица.Валюта                  КАК Валюта,
	|	Таблица.Сделка                  КАК Сделка,
	|	Таблица.Подразделение           КАК Подразделение,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ИСТИНА                          КАК ПоРаспоряжениям,
	|	Таблица.Ссылка                  КАК ОснованиеАктаОРасхождениях
	|ИЗ
	|	Документ.ОтгрузкаТоваровКлиенту КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка = &ОснованиеАктаОРасхождениях
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктОРасхожденияхТовары.Номенклатура       КАК Номенклатура,
	|	АктОРасхожденияхТовары.Характеристика     КАК Характеристика,
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяОприходованиеВозврат)
	|			ТОГДА 0
	|		ИНАЧЕ
	|			АктОРасхожденияхТовары.КодСтроки
	|	КОНЕЦ                                     КАК КодСтроки,
	|	АктОРасхожденияхТовары.Склад              КАК Склад,
	|	АктОРасхожденияхТовары.Назначение         КАК Назначение,
	|	АктОРасхожденияхТовары.Количество - АктОРасхожденияхТовары.КоличествоПоДокументу КАК Количество,
	|	АктОРасхожденияхТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	АктОРасхожденияхТовары.Цена               КАК Цена,
	|	АктОРасхожденияхТовары.Серия              КАК Серия,
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхТовары.ЗаказКлиента В (
	|				ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка),
	|				ЗНАЧЕНИЕ(Документ.ОтгрузкаТоваровКлиенту.ПустаяСсылка),
	|				ЗНАЧЕНИЕ(Документ.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка),
	|				НЕОПРЕДЕЛЕНО
	|				)
	|			ТОГДА АктОРасхожденияхТовары.Реализация
	|		ИНАЧЕ
	|			АктОРасхожденияхТовары.ЗаказКлиента
	|	КОНЕЦ                                     КАК Распоряжение,
	|	АктОРасхожденияхТовары.Упаковка           КАК Упаковка
	|ИЗ
	|	Документ.АктОРасхожденияхПослеОтгрузки.Товары КАК АктОРасхожденияхТовары
	|ГДЕ
	|	АктОРасхожденияхТовары.Ссылка = &АктОРасхождениях
	|	И АктОРасхожденияхТовары.Реализация = &ОснованиеАктаОРасхождениях
	|	И АктОРасхожденияхТовары.Действие В (
	|	                         ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ВозвратПерепоставленного),
	|	                         ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеОтгрузки.ТребуетсяОприходованиеВозврат)
	|	                         )
	|";

	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("АктОРасхождениях", АктОРасхождениях);
	Запрос.УстановитьПараметр("ОснованиеАктаОРасхождениях", ОснованиеАктаОРасхождениях);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = РезультатыЗапроса[РезультатыЗапроса.ВГраница()-1].Выбрать();
	ТаблицаТовары = РезультатыЗапроса[РезультатыЗапроса.ВГраница()].Выгрузить();
	
	// Заполняем шапку
	ВыборкаШапка.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаШапка);

	// Заполняем табличную часть Товары
	Товары.Загрузить(ТаблицаТовары);
	Документы.ПоступлениеТоваровОтКлиента.ОбновитьЗависимыеРеквизитыТабличнойЧасти(Товары, Новый Структура);
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект,
		Документы.ПоступлениеТоваровОтКлиента);
		
	НоменклатураСервер.ЗаполнитьСерииПоFEFO(ЭтотОбъект, ПараметрыУказанияСерий, Ложь);

КонецПроцедуры

#КонецОбласти

#Область ВидыЗапасов

Процедура ЗаполнитьВидыЗапасов(Отказ)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерВременныхТаблиц = ВременныеТаблицыДанныхДокумента();
	
	Если Не Проведен
		Или ЗапасыСервер.ПроверитьНеобходимостьПерезаполненияВидовЗапасовДокумента(ЭтотОбъект)
		Или ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц)
		Или ПроверитьИзменениеТоваров(МенеджерВременныхТаблиц) Тогда
		
		ПараметрыЗаполнения = ПараметрыЗаполненияВидовЗапасов();
		
		ЗапасыСервер.ЗаполнитьВидыЗапасовПоТоварамОрганизаций(ЭтотОбъект,
																МенеджерВременныхТаблиц,
																Отказ,
																ПараметрыЗаполнения);
		
		ВидыЗапасов.Свернуть("АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД",
							"Количество, КоличествоУпаковок, КоличествоПоРНПТ");
		
		Если Не Отказ Тогда
			ЗаполнитьДополнительныеКолонкиВидовЗапасов();
			ЗаполнитьНоменклатуруОприходованиеВидовЗапасов();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц)
	
	ИменаРеквизитов = "Дата, Организация, ХозяйственнаяОперация, Партнер, Договор";
	
	Возврат ЗапасыСервер.ПроверитьИзменениеРеквизитовДокумента(МенеджерВременныхТаблиц, Ссылка, ИменаРеквизитов);
	
КонецФункции

Функция ПроверитьИзменениеТоваров(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.НоменклатураОприходование	КАК НоменклатураОприходование,
	|	ТаблицаТоваров.ХарактеристикаОприходование	КАК ХарактеристикаОприходование
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТоваров.ОсновнаяАналитика	КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаТоваров.НоменклатураОприходование КАК НоменклатураОприходование,
	|		ТаблицаТоваров.ХарактеристикаОприходование КАК ХарактеристикаОприходование,
	|		ТаблицаТоваров.Количество			КАК Количество,
	|		ТаблицаТоваров.КоличествоПоРНПТ		КАК КоличествоПоРНПТ,
	|		ТаблицаТоваров.НомерГТД				КАК НомерГТД
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаВидыЗапасов.ОсновнаяАналитика	КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаВидыЗапасов.НоменклатураОприходование КАК НоменклатураОприходование,
	|		ТаблицаВидыЗапасов.ХарактеристикаОприходование КАК ХарактеристикаОприходование,
	|		-ТаблицаВидыЗапасов.Количество			КАК Количество,
	|		-ТаблицаВидыЗапасов.КоличествоПоРНПТ	КАК КоличествоПоРНПТ,
	|		ТаблицаВидыЗапасов.НомерГТД				КАК НомерГТД
	|	ИЗ
	|		ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|	
	|	) КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.НоменклатураОприходование,
	|	ТаблицаТоваров.ХарактеристикаОприходование
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаТоваров.Количество) <> 0
	|	ИЛИ СУММА(ТаблицаТоваров.КоличествоПоРНПТ) <> 0";
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапрос = Запрос.Выполнить();
	
	Возврат Не РезультатЗапрос.Пустой();
	
КонецФункции

Функция ПараметрыЗаполненияВидовЗапасов()
	
	ПараметрыЗаполнения = ЗапасыСервер.ПараметрыЗаполненияВидовЗапасов();
	ПараметрыЗаполнения.СторнируемыйДокумент = СторнируемыйДокумент;
	ПараметрыЗаполнения.ДокументДелаетИПриходИРасход = Истина;
	
	ПараметрыЗаполнения.ОтборыВидовЗапасов.Организация = Организация;
	
	//++ НЕ УТ
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПодакцизныеТовары") Тогда
		ПараметрыЗаполнения.ОтборыВидовЗапасов.ОблагаетсяАкцизом.Очистить()
	КонецЕсли;
	//-- НЕ УТ
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Процедура ЗаполнитьДополнительныеКолонкиВидовЗапасов()
	
	СтруктураПоиска = Новый Структура("АналитикаУчетаНоменклатуры");
	
	Для Каждого СтрокаТоваров Из Товары Цикл
		
		КоличествоТоваров = СтрокаТоваров.Количество;
		
		СтруктураПоиска.АналитикаУчетаНоменклатуры = СтрокаТоваров.АналитикаУчетаНоменклатурыТоварыУПартнеров;
		
		НайденныеСтроки = ВидыЗапасов.НайтиСтроки(СтруктураПоиска);
		
		Для Каждого СтрокаЗапасов Из НайденныеСтроки Цикл
			
			Если СтрокаЗапасов.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Количество = Мин(КоличествоТоваров, СтрокаЗапасов.Количество);
			
			ЗаполняемыеСвойства = "АналитикаУчетаНоменклатуры, АналитикаУчетаНоменклатурыТоварыУПартнеров";
			
			НоваяСтрока = ВидыЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗапасов);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТоваров, ЗаполняемыеСвойства);
			
			НоваяСтрока.Количество = Количество;
			НоваяСтрока.КоличествоПоРНПТ = Количество * СтрокаЗапасов.КоличествоПоРНПТ / СтрокаЗапасов.Количество;
			
			СтрокаЗапасов.Количество		= СтрокаЗапасов.Количество - НоваяСтрока.Количество;
			СтрокаЗапасов.КоличествоПоРНПТ	= СтрокаЗапасов.КоличествоПоРНПТ - НоваяСтрока.КоличествоПоРНПТ;
			
			КоличествоТоваров = КоличествоТоваров - НоваяСтрока.Количество;
			
			Если КоличествоТоваров = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПараметрыОтбора = Новый Структура("Количество", 0);
	УдаляемыеСтроки = ВидыЗапасов.НайтиСтроки(ПараметрыОтбора);
	
	Для Каждого СтрокаТаблицы Из УдаляемыеСтроки Цикл
		ВидыЗапасов.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьНоменклатуруОприходованиеВидовЗапасов()
	
	Если ВозвратПорчи Тогда
		
		ОтборСтрок	= Новый Структура("Порча", Истина);
		СтрокиПорчи	= Товары.НайтиСтроки(ОтборСтрок);
		
		СтруктураПоиска	= Новый Структура("АналитикаУчетаНоменклатуры");
		
		Для Каждого СтрокаТоваров Из СтрокиПорчи Цикл
			
			КоличествоТоваров = СтрокаТоваров.Количество;
			
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТоваров);
			
			Для Каждого СтрокаЗапасов Из ВидыЗапасов.НайтиСтроки(СтруктураПоиска) Цикл
				
				Если СтрокаЗапасов.Количество = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Количество = Мин(КоличествоТоваров, СтрокаЗапасов.Количество);
				
				НоваяСтрока = ВидыЗапасов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗапасов);
				
				НоваяСтрока.НоменклатураОприходование	= СтрокаТоваров.НоменклатураОприходование;
				НоваяСтрока.ХарактеристикаОприходование	= СтрокаТоваров.ХарактеристикаОприходование;
				
				НоваяСтрока.Количество = Количество;
				
				СтрокаЗапасов.Количество = СтрокаЗапасов.Количество - НоваяСтрока.Количество;
				
				КоличествоТоваров = КоличествоТоваров - НоваяСтрока.Количество;
				
				Если КоличествоТоваров = 0 Тогда
					Прервать;
				КонецЕсли;
			
			КонецЦикла;
			
		КонецЦикла;
		
		ПараметрыОтбора = Новый Структура("Количество", 0);
		УдаляемыеСтроки = ВидыЗапасов.НайтиСтроки(ПараметрыОтбора);
		
		Для Каждого СтрокаТаблицы Из УдаляемыеСтроки Цикл
			ВидыЗапасов.Удалить(СтрокаТаблицы);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет аналитики учета номенклатуры в табличных частях документа, хранящих информацию о товарах.
// Если параметр не передан, тогда будет выполнено заполнение данных в табличных частях документа.
//
// Параметры:
//	ТаблицыДокумента - см. Документы.ПоступлениеТоваровОтКлиента.КоллекцияТабличныхЧастейТоваров.
//
Процедура ЗаполнитьАналитикиУчетаНоменклатурыВТабличныхЧастяхТоваров(ТаблицыДокумента = Неопределено)
	
	Если ТаблицыДокумента = Неопределено Тогда
		ТаблицыДокумента = Документы.ПоступлениеТоваровОтКлиента.КоллекцияТабличныхЧастейТоваров();
		
		ЗаполнитьЗначенияСвойств(ТаблицыДокумента, ЭтотОбъект);
	КонецЕсли;
	
	ТаблицаТовары = ТаблицыДокумента.Товары;
	
	МестаУчета = РегистрыСведений.АналитикаУчетаНоменклатуры.МестаУчета(Неопределено,
																		Склад,
																		Подразделение,
																		Партнер,
																		Договор);
	
	ИменаПолей = РегистрыСведений.АналитикаУчетаНоменклатуры.ИменаПолейКоллекцииПоУмолчанию();
	ИменаПолей.СтатусУказанияСерий = "СтатусУказанияСерийНаСкладах";
	
	// Если Склад - группа, то для аналитики учета номенклатуры склад берем из ТЧ.
	РеквизитыСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Склад, "ЭтоГруппа, ВыборГруппы");
	
	Если ЗначениеЗаполнено(Склад)
		И РеквизитыСклада.ЭтоГруппа
		И РеквизитыСклада.ВыборГруппы = Перечисления.ВыборГруппыСкладов.РазрешитьВЗаказахИНакладных Тогда
		
		ИменаПолей.Вставить("Произвольный", "Склад");
		
	КонецЕсли;
	
	РегистрыСведений.АналитикаУчетаНоменклатуры.ЗаполнитьВКоллекции(ТаблицаТовары, МестаУчета, ИменаПолей);
	
	МестаУчетаУПартнеров = РегистрыСведений.АналитикаУчетаНоменклатуры.МестаУчета(ХозяйственнаяОперация,
																					Договор,
																					Подразделение,
																					Партнер,
																					Договор);
	
	ИменаПолей = РегистрыСведений.АналитикаУчетаНоменклатуры.ИменаПолейКоллекцииПоУмолчанию();
	ИменаПолей.АналитикаУчетаНоменклатуры = "АналитикаУчетаНоменклатурыТоварыУПартнеров";
	ИменаПолей.СтатусУказанияСерий        = "СтатусУказанияСерийПереданныхТоваров";
	
	РегистрыСведений.АналитикаУчетаНоменклатуры.ЗаполнитьВКоллекции(ТаблицаТовары, МестаУчетаУПартнеров, ИменаПолей);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ЗаполнитьЦеныПоУсловиямПродаж()
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("ПоляЗаполнения", "Цена");
	ПараметрыЗаполнения.Вставить("Дата",           Дата);
	ПараметрыЗаполнения.Вставить("Организация",    Организация);
	ПараметрыЗаполнения.Вставить("Валюта",         Валюта);
	ПараметрыЗаполнения.Вставить("Соглашение",     Соглашение);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСумму",     "КоличествоУпаковок");
	
	ЦеныПредприятияЗаполнениеСервер.ЗаполнитьЦены(Товары, Неопределено, ПараметрыЗаполнения, СтруктураДействий);
	
КонецПроцедуры

Функция ПолучитьСуммуДокумента()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Сумма    КАК Сумма
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(Товары.Сумма), 0) КАК Сумма
	|ИЗ
	|	Товары КАК Товары";
	
	ТоварыДокумента = Товары.Выгрузить(,"Номенклатура, Сумма");
	
	Запрос.УстановитьПараметр("Товары", ТоварыДокумента);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	СуммаИтого       = РезультатЗапроса[0].Сумма; // ОпределяемыйТип.ДенежнаяСуммаЛюбогоЗнака - 
	
	Возврат СуммаИтого;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
