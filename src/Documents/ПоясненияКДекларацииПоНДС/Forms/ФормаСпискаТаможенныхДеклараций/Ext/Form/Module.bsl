
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Наступило = ДокументооборотСКОВызовСервера.СобытиеНаступилоНовыйФорматПояснДеклНДС();
	
	Для Каждого ТаможеннаяДекларация Из Параметры.ТаможенныеДекларации Цикл
		ТаможенныеДекларации.Добавить().Номер = ТаможеннаяДекларация;
	КонецЦикла;
	
	Если ТолькоПросмотр Тогда
		Элементы.ТаможенныеДекларации.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
		Элементы.ФормаКомандаОК.Видимость = Ложь;
	КонецЕсли;
	
	Если Наступило Тогда
		
		ЭтаФорма.Заголовок = НСтр("ru = 'Декларации или партии товара';
									|en = 'Декларации или партии товара'");
		
		Элементы.ТаможенныеДекларацииНомер.Заголовок = НСтр("ru = 'Регистрационный номер декларации или партии товара';
															|en = 'Регистрационный номер декларации или партии товара'");
		
		Элементы.ТаможенныеДекларацииНомер.Подсказка = 
			НСтр("ru = 'Регистрационный номер декларации на товары
			|или регистрационный номер партии товара,
			|подлежащего прослеживаемости';
			|en = 'Регистрационный номер декларации на товары
			|или регистрационный номер партии товара,
			|подлежащего прослеживаемости'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда 
		
		Если НЕ ПеренестиВДокумент Тогда
			
			ТекстПредупреждения = НСтр("ru = 'Отменить изменения?';
										|en = 'Отменить изменения?'");
	
			ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияПроизвольнойФормы(
				ЭтотОбъект, 
				Отказ, 
				ЗавершениеРаботы,
				ТекстПредупреждения, 
				"ПеренестиВДокумент");
			
		ИначеЕсли Не Отказ Тогда
				
			Отказ = НЕ ПроверитьЗаполнениеНаКлиенте();
			Если Отказ Тогда
				ПеренестиВДокумент = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПеренестиВДокумент = Истина;
	Если ПроверитьЗаполнениеНаКлиенте() Тогда 
		РезультатЗакрытия = МассивТаможенныхДеклараций();
		ОповеститьОВыборе(РезультатЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	ПеренестиВДокумент = Ложь;
	Модифицированность = Ложь;
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция МассивТаможенныхДеклараций()
	
	МассивТаможенныхДеклараций = Новый Массив();
	
	Для Каждого ТаможеннаяДекларация Из ТаможенныеДекларации Цикл
		МассивТаможенныхДеклараций.Добавить(ТаможеннаяДекларация.Номер);
	КонецЦикла;
	
	Возврат МассивТаможенныхДеклараций;
	
КонецФункции

&НаКлиенте
Функция ПроверитьЗаполнениеНаКлиенте()

	Отказ = Ложь;
	
	Для Каждого ТаможеннаяДекларация Из ТаможенныеДекларации Цикл
		
		Если Не ЗначениеЗаполнено(ТаможеннаяДекларация.Номер) Тогда
			
			Если Наступило Тогда
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле",
					"Заполнение",
					НСтр("ru = 'Регистрационный номер декларации или партии товара';
						|en = 'Регистрационный номер декларации или партии товара'"));
				
			Иначе
				
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
					"Поле",
					"Заполнение",
					НСтр("ru = 'Номер таможенной декларации';
						|en = 'Номер таможенной декларации'"));
				
			КонецЕсли;
			
			Поле = "ТаможенныеДекларации["+ Формат(ТаможенныеДекларации.Индекс(ТаможеннаяДекларация), "ЧДЦ=; ЧГ=0") +"].Номер";
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , Поле, "", Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Не Отказ;

КонецФункции

#КонецОбласти