#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьГоловнуюОрганизацию();
	ДополнительныеСвойства.Вставить("ЗначенияРеквизитовДоЗаписи", ЗначенияРеквизитовДоЗаписи());
	Если ОбменДанными.Загрузка Тогда
		// Режим записи "БЗК".
		// При этом не срабатыват штатное проведение документа, поэтому движения необходимо записать "вручную".
		Если ДокументБудетПроведен(РежимЗаписи, РежимПроведения) Тогда
			// Наполнение наборов записей.
			ОбработкаПроведения(Отказ, РежимПроведения);
		Иначе
			// Включение флажка записи пустых наборов (при записи наборы будут очищены).
			Для Каждого НаборЗаписей Из Движения Цикл
				НаборЗаписей.Записывать = Истина;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	РегистрыСведений.СведенияОбЭЛН.ПриЗаписиДанныхСФРДляРасчетаПособия(ЭтотОбъект);
	РегистрыСведений.СНИЛСВходящихСообщенийСЭДО.ОбновитьПоДокументу(
		Ссылка,
		Страхователь,
		ИдентификаторСообщения,
		СотрудникСНИЛС,
		ФизическоеЛицо,
		ДатаСообщения,
		СокрЛП(СотрудникФамилия + " " + СотрудникИмя + " " + СотрудникОтчество),
		Документы.ДанныеСФРДляРасчетаПособия.ТипСообщения(ЭтотОбъект));
	Если ОбменДанными.Загрузка Тогда
		// Режим записи "БЗК".
		// При этом не срабатыват штатное проведение документа, поэтому движения необходимо записать "вручную".
		Для Каждого НаборЗаписей Из Движения Цикл
			НаборЗаписей.Записать(Истина);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	ВключитьБизнесЛогику = Не ОбменДанными.Загрузка;
	РегистрироватьНаУзлахПлановОбмена = Неопределено;
	ДополнительныеСвойства.Свойство("РегистрироватьНаУзлахПлановОбменаПриОбновленииИБ", РегистрироватьНаУзлахПлановОбмена);
	Если РегистрироватьНаУзлахПлановОбмена = Неопределено Тогда
		РегистрироватьНаУзлахПлановОбмена = ОбменДанными.Получатели.АвтоЗаполнение;
	КонецЕсли;
	
	СтруктураДокумента = Новый Структура("Ссылка, Дата, ФизическоеЛицо, Страхователь, СНИЛС, ДанныеОЗаработке");
	ЗаполнитьЗначенияСвойств(СтруктураДокумента, ЭтотОбъект);
	СтруктураДокумента.Дата = ДатаСообщения;
	СтруктураДокумента.СНИЛС = СотрудникСНИЛС;
	СтруктураДокумента.ДанныеОЗаработке = ДанныеОЗаработкеВТаблицу();
	Для Каждого НаборЗаписей Из Движения Цикл
		НаборЗаписей.Записывать = Истина;
		НаборЗаписей.ОбработкаПроведенияРегистратора(СтруктураДокумента, Отказ, РежимПроведения);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Истина, РегистрироватьНаУзлахПлановОбмена, ВключитьБизнесЛогику);
	КонецЦикла;
	
	// ЗарплатаКадрыРасширеннаяПодсистемы.ПособияСоциальногоСтрахования.СЭДО
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		МодульСЭДОФССРасширенный = ОбщегоНазначения.ОбщийМодуль("СЭДОФССРасширенный");
		МодульСЭДОФССРасширенный.ОбработкаПроведенияДокументаДанныеСФРДляРасчетаПособия(ЭтотОбъект, Отказ, РежимПроведения);
	КонецЕсли;
	// Конец ЗарплатаКадрыРасширеннаяПодсистемы.ПособияСоциальногоСтрахования.СЭДО
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПриИзменении

// Вызывается из формы при изменении соответствующего реквизита перед ОбновитьВторичныеДанные.
Процедура ОрганизацияПриИзменении() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Если ЗначениеЗаполнено(Организация) Тогда
		ДатаНачалаСобытия = ДатаНачалаСобытия(СведенияОбЭЛН());
		ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(ЭтотОбъект, "Страхователь");
		ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(ЭтотОбъект, "ГоловнаяОрганизация");
		Страхователь = СЭДОФСС.СтраховательОрганизации(Организация, ДатаНачалаСобытия);
		ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Страхователь);
		Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
			ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(ЭтотОбъект, "Сотрудник");
			Сотрудник = КадровыйУчет.ОсновнойСотрудникФизическогоЛица(ФизическоеЛицо, Организация, ДатаНачалаСобытия);
		КонецЕсли;
	Иначе
		ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитаШапки(ЭтотОбъект, "Сотрудник");
		ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитаШапки(ЭтотОбъект, "Страхователь");
		ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитаШапки(ЭтотОбъект, "ГоловнаяОрганизация");
		ВходящееСообщение = СЭДОФСС.ВходящееСообщение(ИдентификаторСообщения);
		Если ВходящееСообщение <> Неопределено И ЗначениеЗаполнено(ВходящееСообщение.Организация) Тогда
			Страхователь = ВходящееСообщение.Организация;
			ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Страхователь);
		Иначе
			Страхователь = Справочники.Организации.ПустаяСсылка();
			ГоловнаяОрганизация = Справочники.Организации.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Вызывается из формы при изменении соответствующего реквизита перед ОбновитьВторичныеДанные.
Процедура СотрудникПриИзменении() Экспорт
	УстановитьПривилегированныйРежим(Истина);
	СведенияОбЭЛН = СведенияОбЭЛН();
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		ДатаНачалаСобытия = ДатаНачалаСобытия(СведенияОбЭЛН);
		ИменаПолей = "Организация, ФизическоеЛицо";
		КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудника(Ложь, Сотрудник, ИменаПолей, ДатаНачалаСобытия);
		Если КадровыеДанные = Неопределено Тогда
			НовоеФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "ФизическоеЛицо");
		Иначе
			НовоеФизическоеЛицо = КадровыеДанные.ФизическоеЛицо;
			ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(ЭтотОбъект, "Организация");
			ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(ЭтотОбъект, "Страхователь");
			ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(ЭтотОбъект, "ГоловнаяОрганизация");
			Организация = КадровыеДанные.Организация;
			Страхователь = СЭДОФСС.СтраховательОрганизации(Организация, ДатаНачалаСобытия);
			ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Страхователь);
		КонецЕсли;
		Если ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(ФизическоеЛицо, НовоеФизическоеЛицо) Тогда
			ФиксацияВторичныхДанныхВДокументах.ЗафиксироватьРеквизитШапки(ЭтотОбъект, "ФизическоеЛицо");
		КонецЕсли;
	Иначе
		ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитаШапки(ЭтотОбъект, "ФизическоеЛицо");
		ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитаШапки(ЭтотОбъект, "Организация");
		ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитаШапки(ЭтотОбъект, "Страхователь");
		ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитаШапки(ЭтотОбъект, "ГоловнаяОрганизация");
	КонецЕсли;
КонецПроцедуры

// Вызывается из формы при изменении соответствующего реквизита перед ОбновитьВторичныеДанные.
Процедура НомерЛНПриИзменении() Экспорт
	ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитаШапки(ЭтотОбъект, "ФизическоеЛицо");
	ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитаШапки(ЭтотОбъект, "Организация");
	ФиксацияВторичныхДанныхВДокументах.ОтменитьФиксациюРеквизитаШапки(ЭтотОбъект, "Сотрудник");
КонецПроцедуры

#КонецОбласти

#Область ФиксацияВторичныхДанныхВДокументах

Функция ОбновитьВторичныеДанные(ПараметрыФиксации = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Модифицирован = Ложь;
	
	Если Не Документы.ДанныеСФРДляРасчетаПособия.ОбновлятьВторичныеДанные(ЭтотОбъект) Тогда
		Возврат Модифицирован;
	КонецЕсли;
	
	Если ПараметрыФиксации = Неопределено Тогда
		ПараметрыФиксации = Документы.ДанныеСФРДляРасчетаПособия.ПараметрыФиксацииВторичныхДанных();
	КонецЕсли;
	
	Если ЗаполнитьГоловнуюОрганизацию() Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьПодтверждениеПолучения(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьМаксимальнуюДатуПодтвержденияПолучения() Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьФизическоеЛицо() Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьСотрудникаИОрганизацию() Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьФлажокОбработан() Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Возврат Модифицирован;
КонецФункции

Функция ЗаполнитьГоловнуюОрганизацию()
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "ГоловнаяОрганизация") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Страхователь) Тогда
		НовоеЗначение = ЗарплатаКадры.ГоловнаяОрганизация(Страхователь);
	Иначе
		НовоеЗначение = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(ГоловнаяОрганизация, НовоеЗначение);
КонецФункции

Функция ЗаполнитьПодтверждениеПолучения(ПараметрыФиксации) Экспорт
	Реквизиты = Новый Структура("ТребуетсяПодтверждение, ДатаОтправкиПодтверждения, ПодтверждениеПолученоСФР");
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитыШапкиЗафиксированы(ЭтотОбъект, Реквизиты) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Подтверждение = СЭДОФСС.СостояниеПодтверждения(Страхователь, ИдентификаторСообщения, ДатаОтправкиПодтверждения);
	Если Подтверждение = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Реквизиты.ТребуетсяПодтверждение    = Подтверждение.Требуется;
	Реквизиты.ДатаОтправкиПодтверждения = Подтверждение.ДатаОтправки;
	Реквизиты.ПодтверждениеПолученоСФР  = Подтверждение.Доставлено;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьМаксимальнуюДатуПодтвержденияПолучения()
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "МаксимальнаяДатаПодтверждения") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаСообщения) Тогда
		НовоеЗначение = СЭДОФСС.СледующийРабочийДень(ДатаСообщения, 3);
	Иначе
		НовоеЗначение = Неопределено;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(МаксимальнаяДатаПодтверждения, НовоеЗначение);
КонецФункции

Функция ЗаполнитьФизическоеЛицо()
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "ФизическоеЛицо") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НовоеЗначение = Неопределено;
	
	СведенияОбЭЛН = СведенияОбЭЛН();
	Если ЗначениеЗаполнено(СведенияОбЭЛН.ФизическоеЛицо) И ЗначениеЗаполнено(СведенияОбЭЛН.Больничный) Тогда
		НовоеЗначение = СведенияОбЭЛН.ФизическоеЛицо;
	Иначе
		РезультатПоиска = ФизическиеЛицаЗарплатаКадры.ФизическоеЛицоПоСНИЛСИлиФИО(
			СотрудникСНИЛС,
			СотрудникФамилия,
			СотрудникИмя,
			СотрудникОтчество);
		Если ЗначениеЗаполнено(РезультатПоиска.ФизическоеЛицо) Тогда
			НовоеЗначение = РезультатПоиска.ФизическоеЛицо;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(ФизическоеЛицо, НовоеЗначение);
КонецФункции

Функция ЗаполнитьСотрудникаИОрганизацию()
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "Сотрудник")
		Или ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "Организация") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Реквизиты = Новый Структура("Сотрудник, Организация");
	
	СведенияОбЭЛН = СведенияОбЭЛН();
	
	Если ЗначениеЗаполнено(СведенияОбЭЛН.Больничный) Или ЗначениеЗаполнено(СведенияОбЭЛН.ОтветНаЗапрос) Тогда
		Реквизиты.Сотрудник = СведенияОбЭЛН.Сотрудник;
		Реквизиты.Организация = СведенияОбЭЛН.Организация;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Реквизиты.Сотрудник) Тогда
		КадровыеДанные = КадровыйУчет.КадровыеДанныеОсновногоСотрудникаФизическогоЛица(
			Страхователь,
			ФизическоеЛицо,
			"Сотрудник, Организация",
			ДатаНачалаСобытия(СведенияОбЭЛН),
			Ложь);
		Если КадровыеДанные <> Неопределено Тогда
			Реквизиты.Сотрудник = КадровыеДанные.Сотрудник;
			Реквизиты.Организация = КадровыеДанные.Организация;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Реквизиты.Организация) Тогда
		Реквизиты.Организация = Страхователь;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапкиБезУчетаФиксации(ЭтотОбъект, Реквизиты);
КонецФункции

Функция ЗаполнитьФлажокОбработан()
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "Обработан") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СведенияОбЭЛН = СведенияОбЭЛН();
	
	НовоеЗначение = ЗначениеЗаполнено(СведенияОбЭЛН.Больничный) И СведенияОбЭЛН.БольничныйПроведен;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(Обработан, НовоеЗначение);
КонецФункции

#КонецОбласти

#Область ПередЗаписью

Функция ЗначенияРеквизитовДоЗаписи()
	ИменаРеквизитов = "ГоловнаяОрганизация, НомерЛН, ПометкаУдаления";
	Если ЭтоНовый() Тогда
		Возврат ОбщегоНазначенияБЗК.ЗначенияСвойств(ЭтотОбъект, ИменаРеквизитов);
	Иначе
		Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов);
	КонецЕсли;
КонецФункции

Функция ДокументБудетПроведен(РежимЗаписи, РежимПроведения)
	Если ПометкаУдаления Тогда
		Возврат Ложь;
	КонецЕсли;
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Возврат Истина;
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Возврат Ложь;
	Иначе
		Возврат Проведен;
	КонецЕсли;
КонецФункции

#КонецОбласти

#Область ОбработкаПроведения

Функция ДанныеОЗаработкеВТаблицу()
	МетаданныеРеквизитов = Метаданные().Реквизиты;
	
	ДанныеОЗаработке = Новый ТаблицаЗначений;
	ДанныеОЗаработке.Колонки.Добавить("РасчетныйГод", МетаданныеРеквизитов.ПервыйРасчетныйГод.Тип);
	ДанныеОЗаработке.Колонки.Добавить("ЗаработокПоВсем", МетаданныеРеквизитов.ПервыйРасчетныйГодЗаработокПоВсем.Тип);
	ДанныеОЗаработке.Колонки.Добавить("ЗаработокПоВсемГПХ", МетаданныеРеквизитов.ПервыйРасчетныйГодЗаработокПоВсемГПХ.Тип);
	ДанныеОЗаработке.Колонки.Добавить("ЗаработокПоТекущему", МетаданныеРеквизитов.ПервыйРасчетныйГодЗаработокПоТекущему.Тип);
	ДанныеОЗаработке.Колонки.Добавить("ЗаработокПоТекущемуГПХ", МетаданныеРеквизитов.ПервыйРасчетныйГодЗаработокПоТекущемуГПХ.Тип);
	
	Если ЗначениеЗаполнено(ПервыйРасчетныйГод) Тогда
		СтрокаТаблицы = ДанныеОЗаработке.Добавить();
		СтрокаТаблицы.РасчетныйГод = ПервыйРасчетныйГод;
		СтрокаТаблицы.ЗаработокПоВсем = ПервыйРасчетныйГодЗаработокПоВсем;
		СтрокаТаблицы.ЗаработокПоВсемГПХ = ПервыйРасчетныйГодЗаработокПоВсемГПХ;
		СтрокаТаблицы.ЗаработокПоТекущему = ПервыйРасчетныйГодЗаработокПоТекущему;
		СтрокаТаблицы.ЗаработокПоТекущемуГПХ = ПервыйРасчетныйГодЗаработокПоТекущемуГПХ;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВторойРасчетныйГод) Тогда
		СтрокаТаблицы = ДанныеОЗаработке.Добавить();
		СтрокаТаблицы.РасчетныйГод = ВторойРасчетныйГод;
		СтрокаТаблицы.ЗаработокПоВсем = ВторойРасчетныйГодЗаработокПоВсем;
		СтрокаТаблицы.ЗаработокПоВсемГПХ = ВторойРасчетныйГодЗаработокПоВсемГПХ;
		СтрокаТаблицы.ЗаработокПоТекущему = ВторойРасчетныйГодЗаработокПоТекущему;
		СтрокаТаблицы.ЗаработокПоТекущемуГПХ = ВторойРасчетныйГодЗаработокПоТекущемуГПХ;
	КонецЕсли;
	
	Возврат ДанныеОЗаработке;
КонецФункции

#КонецОбласти

#Область СведенияОбЭЛН

Функция ДатаНачалаСобытия(СведенияОбЭЛН)
	Возврат ?(ЗначениеЗаполнено(СведенияОбЭЛН.ДатаНачалаСобытия), СведенияОбЭЛН.ДатаНачалаСобытия, ДатаСообщения);
КонецФункции

Функция СведенияОбЭЛН() Экспорт
	Результат = Неопределено;
	Если ДополнительныеСвойства.Свойство("СведенияОбЭЛН", Результат) Тогда
		Возврат Результат;
	КонецЕсли;
	Результат = Новый Структура("ВходящийЗапрос, ОтветНаЗапрос, Больничный, БольничныйПроведен,
	|Сотрудник, Организация, ФизическоеЛицо, ДатаНачалаСобытия");
	ДополнительныеСвойства.Вставить("СведенияОбЭЛН", Результат);
	
	Если Не ЗначениеЗаполнено(ГоловнаяОрганизация) Или Не ЗначениеЗаполнено(НомерЛН) Тогда
		Возврат Результат;
	КонецЕсли;
	
	СведенияОбЭЛН = РегистрыСведений.СведенияОбЭЛН.СведенияОбЭЛН(НомерЛН, ГоловнаяОрганизация);
	Если СведенияОбЭЛН = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Результат, СведенияОбЭЛН);
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли