///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
// 
// Параметры:
//  КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной
//                                                            параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной
//                                            параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ФизическоеЛицо)
	|	И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// СтандартныеПодсистемы.ТекущиеДела

// См. ТекущиеДелаПереопределяемый.ПриОпределенииОбработчиковТекущихДел.
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	МетаданныеДокумента = Метаданные();
	Если Не СЭДОФСС.ДоступенОбменЧерезСЭДО()
		Или Не ПравоДоступа("Изменение", МетаданныеДокумента) Тогда
		Возврат; // Нет прав.
	КонецЕсли;
	
	МодульТекущиеДелаСервер = ОбщегоНазначения.ОбщийМодуль("ТекущиеДелаСервер");
	Разделы = МодульТекущиеДелаСервер.РазделыДляОбъекта(МетаданныеДокумента.ПолноеИмя());
	Если Разделы.Количество() = 0 Тогда
		Возврат; // Некорректное внедрение.
	КонецЕсли;
	
	НачалоТекущегоДня = НачалоДня(ТекущаяДатаСеанса());
	НачалоРабочегоДня = НачалоДня(СЭДОФСС.БлижайшийРабочийДень(НачалоТекущегоДня));
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Шапка.Ссылка КАК Ссылка,
	|	НЕ Шапка.Загружен КАК ТребуетсяЗагрузить,
	|	Шапка.ТребуетсяПодтверждение
	|		И Шапка.ДатаОтправкиПодтверждения = ДАТАВРЕМЯ(1, 1, 1) КАК ТребуетсяПодтвердить
	|ИЗ
	|	Документ.ДанныеСФРДляРасчетаПособия КАК Шапка
	|ГДЕ
	|	НЕ Шапка.Обработан
	|	И НЕ Шапка.ПометкаУдаления";
	Запрос.УстановитьПараметр("НачалоРабочегоДня", НачалоРабочегоДня);
	
	Требования = Запрос.Выполнить().Выгрузить();
	ВРаботе                  = Требования.Количество();
	ТребуетсяЗагрузить       = Требования.НайтиСтроки(Новый Структура("ТребуетсяЗагрузить", Истина)).Количество();
	ТребуетсяПодтвердить     = Требования.НайтиСтроки(Новый Структура("ТребуетсяПодтвердить", Истина)).Количество();
	ИмяДокумента             = МетаданныеДокумента.Имя;
	ПредставлениеСписка      = МетаданныеДокумента.ПредставлениеСписка;
	
	Для Каждого Раздел Из Разделы Цикл
		
		ПолноеИмяРаздела = СтрЗаменить(Раздел.ПолноеИмя(), ".", "_");
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = ИмяДокумента + "_ВРаботе_" + ПолноеИмяРаздела;
		Дело.ЕстьДела       = (ВРаботе > 0);
		Дело.Важное         = Ложь;
		Дело.Владелец       = Раздел;
		Дело.Представление  = ПредставлениеСписка;
		Дело.Количество     = ВРаботе;
		Дело.ПараметрыФормы = Новый Структура("Отбор", Новый Структура("Обработан", Ложь));
		Дело.Форма          = "Документ.ДанныеСФРДляРасчетаПособия.ФормаСписка";
		
		ИдентификаторРодителя = Дело.Идентификатор;
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = ИмяДокумента + "_Загрузить_" + ПолноеИмяРаздела;
		Дело.ЕстьДела       = (ТребуетсяЗагрузить > 0);
		Дело.Важное         = Истина;
		Дело.Владелец       = ИдентификаторРодителя;
		Дело.Представление  = НСтр("ru = 'Получить новые';
									|en = 'Get new to-do list items'");
		Дело.Количество     = ТребуетсяЗагрузить;
		
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор  = ИмяДокумента + "_Подтвердить_" + ПолноеИмяРаздела;
		Дело.ЕстьДела       = (ТребуетсяПодтвердить > 0);
		Дело.Важное         = Истина;
		Дело.Владелец       = ИдентификаторРодителя;
		Дело.Представление  = НСтр("ru = 'Подтвердить получение';
									|en = 'Confirm receipt'");
		Дело.Количество     = ТребуетсяПодтвердить;
		
	КонецЦикла;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ТекущиеДела

// СтандартныеПодсистемы.ЗащитаПерсональныхДанных

// См. ЗащитаПерсональныхДанныхПереопределяемый.ЗаполнитьСведенияОПерсональныхДанных.
Процедура ЗаполнитьСведенияОПерсональныхДанных(ТаблицаСведений) Экспорт
	НовыеСведения = ТаблицаСведений.Добавить();
	НовыеСведения.Объект          = "Документ.ДанныеСФРДляРасчетаПособия";
	НовыеСведения.ПоляРегистрации = "Сотрудник,ФизическоеЛицо";
	НовыеСведения.ПоляДоступа     = "ПервыйРасчетныйГодЗаработокПоВсем,ПервыйРасчетныйГодЗаработокПоВсемБезГПХ,ПервыйРасчетныйГодЗаработокПоВсемГПХ,ПервыйРасчетныйГодЗаработокПоТекущему,ПервыйРасчетныйГодЗаработокПоТекущемуБезГПХ,ПервыйРасчетныйГодЗаработокПоТекущемуГПХ,ВторойРасчетныйГодЗаработокПоВсем,ВторойРасчетныйГодЗаработокПоВсемБезГПХ,ВторойРасчетныйГодЗаработокПоВсемГПХ,ВторойРасчетныйГодЗаработокПоТекущему,ВторойРасчетныйГодЗаработокПоТекущемуБезГПХ,ВторойРасчетныйГодЗаработокПоТекущемуГПХ";
	НовыеСведения.ОбластьДанных   = "Доходы";
КонецПроцедуры

// Конец СтандартныеПодсистемы.ЗащитаПерсональныхДанных

#КонецОбласти

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

#Область СЭДО

Функция ТипСообщения(Документ = Неопределено) Экспорт
	Возврат 322;
КонецФункции

Функция Метаданные() Экспорт
	Возврат Метаданные.Документы.ДанныеСФРДляРасчетаПособия;
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.34.125";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("c8ec81c7-ab2e-11f0-8168-c84d4423fb75");
	Обработчик.Процедура       = "Документы.ДанныеСФРДляРасчетаПособия.ПовторноОбработатьСообщения322";
	Обработчик.Комментарий     = НСтр("ru = 'Создание документов по сообщениям СЭДО СФР 322.';
										|en = 'Create documents based on Social Insurance Fund EDI 322 messages.'");
	
КонецПроцедуры

// Создание документов по сообщениям СЭДО.
//
// Параметры:
//   ПараметрыОбновления - Структура - Параметры отложенного обновления.
//
Процедура ПовторноОбработатьСообщения322(ПараметрыОбновления = Неопределено) Экспорт
	СЭДОФСС.ПовторноОбработатьВходящиеСообщенияСЭДО(ПараметрыОбновления, 322);
КонецПроцедуры

#КонецОбласти

#Область СоставДокументов

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
//
Функция ОписаниеСоставаОбъекта() Экспорт
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаФизическоеЛицоВШапке("ФизическоеЛицо", "Сотрудник");
КонецФункции

#КонецОбласти

#Область МногофункциональныеДокументы

// Возвращает метаданные разделов документа.
//
// Возвращаемое значение:
//   Соответствие, Неопределено - Описание разделов документа.
//
Функция ОписаниеРазделовДанных() Экспорт
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("МногофункциональныеДокументыБЗККлиентСервер");
		ВсеРазделы = Модуль.РазделыДанных();
		
		ОписаниеРазделовДанных = Новый Соответствие();
		
		ОписаниеРаздела = Модуль.НовыйОписаниеРазделаДанных();
		ОписаниеРазделовДанных.Вставить(ВсеРазделы.КадровыеДанные, ОписаниеРаздела);
		ОписаниеРаздела.РеквизитСостояние    = "Проведен";
		ОписаниеРаздела.РеквизитОтветсвенный = "Ответственный";
		
		ОписаниеРаздела = Модуль.НовыйОписаниеРазделаДанных();
		ОписаниеРазделовДанных.Вставить(ВсеРазделы.НачисленнаяЗарплата, ОписаниеРаздела);
		Возврат ОписаниеРазделовДанных;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

// Возвращает значения по которым будут проверяться права на документ.
//
// Параметры:
//   ДокументОбъект - ДокументОбъект, ДанныеФормыСтруктура
//
// Возвращаемое значение:
//   Структура - Значения доступа по которым будут проверяться права на документ
//
Функция ЗначенияДоступа(ДокументОбъект) Экспорт
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		МодульМногофункциональныеДокументыБЗК = ОбщегоНазначения.ОбщийМодуль("МногофункциональныеДокументыБЗК");
		Возврат МодульМногофункциональныеДокументыБЗК.ЗначенияДоступаПоСоставуДокумента(
			ДокументОбъект,
			ДокументОбъект.Организация);
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

#КонецОбласти

#Область ВводНаОсновании

// Формирует структуру данных для заполнения документа "Больничный лист".
//
// Параметры:
//   Ссылка - ДокументСсылка.ДанныеСФРДляРасчетаПособия
//
Функция ДанныеДляЗаполненияБольничного(Ссылка) Экспорт
	ИменаРеквизитов = "Организация, Сотрудник, ФизическоеЛицо, НомерЛН, ПервыйРасчетныйГод, ВторойРасчетныйГод";
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов);
	ЗначенияРеквизитов.Вставить("НомерЛисткаНетрудоспособности", ЗначенияРеквизитов.НомерЛН);
	Возврат ЗначенияРеквизитов;
КонецФункции

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область ФиксацияВторичныхДанныхВДокументах

Функция ОбновлятьВторичныеДанные(Документ) Экспорт
	Возврат Документ.Загружен И Не Документ.ПометкаУдаления И Не Документ.Обработан;
КонецФункции

Функция ПараметрыФиксацииВторичныхДанных() Экспорт
	Возврат ФиксацияВторичныхДанныхВДокументах.ПараметрыФиксации(ФиксируемыеРеквизиты());
КонецФункции

Функция ФиксируемыеРеквизиты()
	ФиксируемыеРеквизиты = Новый Соответствие;
	
	// При помощи механизмов фиксации описываются только механизмы обновления вторичных данных.
	// Механизмы заполнения первичных данных при этом могут существенно отличаться.
	
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.ИмяГруппы = "ПодтверждениеПолучения";
	Шаблон.ОснованиеЗаполнения = "ИдентификаторСообщения";
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ТребуетсяПодтверждение");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ДатаОтправкиПодтверждения");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ПодтверждениеПолученоСФР");
	
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.ИмяГруппы = "МаксимальнаяДатаПодтверждения";
	Шаблон.ОснованиеЗаполнения = "ДатаСообщения";
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "МаксимальнаяДатаПодтверждения");
	
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.ИмяГруппы = "КадровыеДанные";
	Шаблон.ОснованиеЗаполнения = "СотрудникСНИЛС";
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ФизическоеЛицо");
	
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.ИмяГруппы = "Сотрудник";
	Шаблон.ФиксацияГруппы = Истина;
	Шаблон.ОснованиеЗаполнения = "ФизическоеЛицо";
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "Организация");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "Сотрудник");
	
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.ИмяГруппы = "Прочее";
	Шаблон.ОснованиеЗаполнения = "НомерЛН";
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "Обработан");
	
	// Добавление всех оставшихся реквизитов.
	РеквизитыРучногоЗаполнения = Новый Структура("Ответственный, Комментарий");
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.ИмяГруппы = "СлужебныеРеквизиты";
	Шаблон.ОтображатьПредупреждение = Ложь;
	Для Каждого Реквизит Из Метаданные().Реквизиты Цикл
		Если Не РеквизитыРучногоЗаполнения.Свойство(Реквизит.Имя)
			И ФиксируемыеРеквизиты[Реквизит.Имя] = Неопределено Тогда
			ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, Реквизит.Имя);
		КонецЕсли;
	КонецЦикла;
	ТаблицыРучногоЗаполнения = Новый Структура("ДополнительныеРеквизиты, ФиксацияИзменений");
	Для Каждого ТабличнаяЧасть Из Метаданные().ТабличныеЧасти Цикл
		Если ТаблицыРучногоЗаполнения.Свойство(ТабличнаяЧасть.Имя) Тогда
			Продолжить;
		КонецЕсли;
		Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
		Шаблон.Путь = ТабличнаяЧасть.Имя;
		Шаблон.ИмяГруппы = "СлужебныеРеквизиты";
		Шаблон.ОтображатьПредупреждение = Ложь;
		Для Каждого Реквизит Из ТабличнаяЧасть.Реквизиты Цикл
			Если Не РеквизитыРучногоЗаполнения.Свойство(ТабличнаяЧасть.Имя + Реквизит.Имя)
				И ФиксируемыеРеквизиты[Шаблон.Путь + Реквизит.Имя] = Неопределено Тогда
				ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, Реквизит.Имя);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(ФиксируемыеРеквизиты);
КонецФункции

#КонецОбласти

#Область СЭДО

Процедура ЗагрузитьУведомлениеОНаличииСообщения(ТипСообщения, Страхователь, Уведомление, ЕстьИзменения) Экспорт
	ПолучаемыеРеквизиты = "Ссылка, ПометкаУдаления, Страхователь, ТребуетсяПодтверждение";
	Документ = НайтиПоИдентификаторуСообщения(Уведомление.Идентификатор, ПолучаемыеРеквизиты);
	Если Документ <> Неопределено Тогда
		Если Документ.Страхователь = Страхователь
			И Не Документ.ПометкаУдаления
			И Документ.ТребуетсяПодтверждение = Уведомление.ТребуетсяПодтверждение Тогда
			Возврат;
		КонецЕсли;
		ДокументОбъект = Документ.Ссылка.ПолучитьОбъект();
		Если ДокументОбъект.ПометкаУдаления Тогда
			ДокументОбъект.УстановитьПометкуУдаления(Ложь);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ДокументОбъект.Организация) Тогда
			ДокументОбъект.Организация = Страхователь;
		КонецЕсли;
	Иначе
		ДокументОбъект = Документы.ДанныеСФРДляРасчетаПособия.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ДокументОбъект.Организация = Страхователь;
		ДокументОбъект.ИдентификаторСообщения = Уведомление.Идентификатор;
	КонецЕсли;
	
	ДокументОбъект.Страхователь = Страхователь;
	ДокументОбъект.ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Страхователь);
	ДокументОбъект.ТребуетсяПодтверждение = Уведомление.ТребуетсяПодтверждение;
	СЭДОФСС.ОбновитьДатуСообщенияВДокументе(ДокументОбъект, СЭДОФСС.ДатаСообщения(ДокументОбъект.ИдентификаторСообщения));
	СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Запись);
	
	ЕстьИзменения = Истина;
КонецПроцедуры

Процедура ЗагрузитьСообщение322(Страхователь, ИдентификаторСообщения, ТекстXML, ТребуетсяПодтверждение, Результат, Кэш) Экспорт
	СтруктураДокумента = СтруктураСообщения322(ТекстXML, Страхователь);
	СтруктураДокумента.Вставить("ИдентификаторСообщения", ИдентификаторСообщения);
	СтруктураДокумента.Вставить("ТребуетсяПодтверждение", ТребуетсяПодтверждение);
	Если Не ЗначениеЗаполнено(СтруктураДокумента.ДатаСообщения) Тогда
		СтруктураДокумента.ДатаСообщения = СЭДОФСС.ДатаСообщения(ИдентификаторСообщения, Кэш);
	КонецЕсли;
	
	Документ = НайтиПоИдентификаторуСообщения(ИдентификаторСообщения);
	Если Документ <> Неопределено Тогда
		ДокументОбъект = Документ.Ссылка.ПолучитьОбъект();
	Иначе
		ДокументОбъект = СоздатьДокумент();
		ДокументОбъект.Организация = Страхователь;
		ДокументОбъект.Страхователь = Страхователь;
		ДокументОбъект.ИдентификаторСообщения = ИдентификаторСообщения;
		ДокументОбъект.ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Страхователь);
	КонецЕсли;
	
	ЕстьИзменения = Не ДокументОбъект.Проведен;
	
	Если Не ДокументОбъект.Загружен Тогда
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ДокументОбъект.Загружен = Истина;
		ЕстьИзменения = Истина;
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из СтруктураДокумента Цикл
		Если ТипЗнч(КлючИЗначение.Значение) = Тип("ТаблицаЗначений") Тогда
			ЗначениеДоИзменения = ДокументОбъект[КлючИЗначение.Ключ].Выгрузить();
			ДокументОбъект[КлючИЗначение.Ключ].Загрузить(КлючИЗначение.Значение);
			ЗначениеПослеИзменения = ДокументОбъект[КлючИЗначение.Ключ].Выгрузить();
			Если Не ОбщегоНазначенияБЗК.ЗначенияСовпадают(ЗначениеДоИзменения, ЗначениеПослеИзменения) Тогда
				ЕстьИзменения = Истина;
			КонецЕсли;
		Иначе
			ЗначениеДоИзменения = ДокументОбъект[КлючИЗначение.Ключ];
			ДокументОбъект[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
			Если ЗначениеДоИзменения <> ДокументОбъект[КлючИЗначение.Ключ] Тогда
				ЕстьИзменения = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ДокументОбъект.ОбновитьВторичныеДанные() Тогда
		ЕстьИзменения = Истина;
	КонецЕсли;
	
	Если ЕстьИзменения Тогда
		СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Проведение);
	КонецЕсли;
	
	Результат.Обработано = Истина;
	
КонецПроцедуры

Процедура ЗагрузитьОтветНаПодтверждениеПолученияСообщения(Страхователь, ИсходноеСообщение, Результат, ЕстьИзменения) Экспорт
	// Вызывается при получении сообщения с типом 11 в ответ на подтверждение о прочтении входящего сообщения.
	//   Страхователь - СправочникСсылка.Организации - организация, получатель сообщения.
	//   ИсходноеСообщение - Структура:
	//     * ИдентификаторСообщения - Строка - идентификатор исходного сообщения СЭДО, по которому отправлялось подтверждение.
	//     * Тип                    - Число  - тип исходного сообщения СЭДО, по которому отправлялось подтверждение.
	//     * ТекстОшибки            - Строка - ошибка приема подтверждения.
	//     * ТекстПредупреждения    - Строка - предупреждение приема подтверждения.
	//   Результат - Структура - результат обработки сообщения:
	//     * Обработано      - Булево - признак того, что сообщение было успешно обработано.
	//     * ОшибкаОбработки - Булево - признак того, что при обработке сообщения возникла ошибка.
	//     * ОписаниеОшибки  - Строка - описание ошибки обработки.
	
	// Поиск документа по идентификатору сообщения.
	Документ = НайтиПоИдентификаторуСообщения(ИсходноеСообщение.ИдентификаторСообщения);
	Если Документ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение подтверждения получения.
	ДокументОбъект = Документ.Ссылка.ПолучитьОбъект();
	Если ДокументОбъект.ЗаполнитьПодтверждениеПолучения(Неопределено) Тогда
		СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Запись);
		ЕстьИзменения = Истина;
	КонецЕсли;
	
КонецПроцедуры

Функция НайтиПоИдентификаторуСообщения(ИдентификаторСообщения, ВыбираемыеПоля = "Ссылка")
	НастройкиЗапроса = ЗапросыБЗК.НастройкиЗапросаКТаблице();
	НастройкиЗапроса.Количество = 1;
	НастройкиЗапроса.Порядок = "ПометкаУдаления, Дата УБЫВ";
	ЗапросыБЗК.ДобавитьОтбор(НастройкиЗапроса.Отбор, "ИдентификаторСообщения", , ИдентификаторСообщения);
	Запрос = ЗапросыБЗК.ЗапросКТаблице(Метаданные(), ВыбираемыеПоля, НастройкиЗапроса);
	Таблица = Запрос.Выполнить().Выгрузить();
	Если Таблица.Количество() = 1 Тогда
		Возврат Таблица[0];
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция СтруктураСообщения322(ТекстXML, Страхователь)
	#Область Пример_Сообщения_322
	//<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
	//<proactiveSalaryNotification messageDateTime="2025-08-29T17:43:11.834+03:00"
	//                             xmlns="urn:ru:fss:integration:types:proactive:notifications:v01"
	//                             xmlns:ns2="http://www.fss.ru/integration/types/person/v02"
	//                             xmlns:ns4="http://www.fss.ru/integration/types/organization/v01"
	//                             xmlns:ns3="http://www.fss.ru/integration/types/common/v01">
	//	<elnInfo>
	//		<lnNumber>920000206738</lnNumber>
	//	</elnInfo>
	//	<insurerInfo>
	//		<regNum>3103275205</regNum>
	//		<regNumSFR>1000865343</regNumSFR>
	//	</insurerInfo>
	//	<insuredInfo>
	//		<snils>00122615672</snils>
	//		<fullName>
	//			<ns2:firstName>СВЕТЛАНА</ns2:firstName>
	//			<ns2:lastName>МУКАШЕВА</ns2:lastName>
	//			<ns2:middleName>ПЕТРОВНА</ns2:middleName>
	//		</fullName>
	//		<birthDate>1985-02-24+03:00</birthDate>
	//	</insuredInfo>
	//	<insuredWorksSeveralInsurers>true</insuredWorksSeveralInsurers>
	//	<paymentMethod>severalTDInsurersAssignment</paymentMethod>
	//	<salaryInfo>
	//		<yearSum>
	//			<year>2023</year>
	//			<salaryByCurrentInsurer>
	//				<sumOPS>1917000</sumOPS>
	//				<sumGPH>0</sumGPH>
	//			</salaryByCurrentInsurer>
	//			<salaryByAllInsurers>
	//				<sumOPS>1917000</sumOPS>
	//				<sumGPH>0</sumGPH>
	//			</salaryByAllInsurers>
	//		</yearSum>
	//		<yearSum>
	//			<year>2024</year>
	//			<salaryByCurrentInsurer>
	//				<sumOPS>2225000</sumOPS>
	//				<sumGPH>0</sumGPH>
	//			</salaryByCurrentInsurer>
	//			<salaryByAllInsurers>
	//				<sumOPS>2225000</sumOPS>
	//				<sumGPH>0</sumGPH>
	//			</salaryByAllInsurers>
	//		</yearSum>
	//	</salaryInfo>
	//</proactiveSalaryNotification>
	#КонецОбласти
	
	СтруктураDOM = СериализацияБЗК.СтруктураDOM(ТекстXML);
	КореньDOM = СериализацияБЗК.НайтиУзелDOMПоИмени(СтруктураDOM, "proactiveSalaryNotification");
	Если КореньDOM = Неопределено Тогда
		КореньDOM = СтруктураDOM.ДокументDOM.ЭлементДокумента;
	КонецЕсли;
	Необработанные = Новый Массив;
	ТекстыОшибок = Новый Массив;
	
	// Подготовка структуры документа.
	Документ = Новый Структура;
	Документ.Вставить("Страхователь", Страхователь);
	
	// Чтение атрибутов XML.
	АтрибутыКорня = СериализацияБЗК.АтрибутыЭлементаDOM(КореньDOM, "messageDateTime");
	Документ.Вставить("ДатаСообщения", СериализацияБЗК.ДатаИзXML(АтрибутыКорня.messageDateTime));
	
	// Чтение реквизитов XML.
	ИменаУзлов = "elnInfo, insurerInfo, insuredInfo,
	|insuredWorksSeveralInsurers, paymentMethod, salaryInfo, mobilDaysAmount";
	УзлыКорня = УзлыЭлементаDOM(КореньDOM, ИменаУзлов, Необработанные);
	
	Если ТипЗнч(УзлыКорня.elnInfo) = Тип("ЭлементDOM") Тогда
		ЭЛНDOM = УзлыЭлементаDOM(УзлыКорня.elnInfo, "lnNumber", Необработанные);
		Документ.Вставить("НомерЛН", СериализацияБЗК.СтрокаИзXML(ЭЛНDOM.lnNumber));
	КонецЕсли;
	Если ТипЗнч(УзлыКорня.insurerInfo) = Тип("ЭлементDOM") Тогда
		СтраховательDOM = УзлыЭлементаDOM(УзлыКорня.insurerInfo, "regNum, regNumSFR", Необработанные);
		Документ.Вставить("РегистрационныйНомерФСС", СериализацияБЗК.СтрокаИзXML(СтраховательDOM.regNum));
		Документ.Вставить("РегистрационныйНомерСФР", СериализацияБЗК.СтрокаИзXML(СтраховательDOM.regNumSFR));
	КонецЕсли;
	Если ТипЗнч(УзлыКорня.insuredInfo) = Тип("ЭлементDOM") Тогда
		СотрудникDOM = УзлыЭлементаDOM(УзлыКорня.insuredInfo, "snils, fullName, birthDate", Необработанные);
		ФИО = СЭДОФСС.ФИОИзXML(СотрудникDOM.fullName, Необработанные);
		Документ.Вставить("СотрудникСНИЛС", СЭДОФСС.СНИЛСИзXML(СотрудникDOM.snils));
		Документ.Вставить("СотрудникФамилия", ФИО.Фамилия);
		Документ.Вставить("СотрудникИмя", ФИО.Имя);
		Документ.Вставить("СотрудникОтчество", ФИО.Отчество);
		Документ.Вставить("СотрудникДатаРождения", СериализацияБЗК.ДатаИзXML(СотрудникDOM.birthDate));
	КонецЕсли;
	Документ.Вставить("ЧислитсяУНесколькихСтрахователей",
		СериализацияБЗК.БулевоИзXML(УзлыКорня.insuredWorksSeveralInsurers));
	Документ.Вставить("МетодНазначенияИВыплатыПособия",
		Перечисления.МетодыНазначенияИВыплатыПособийСФР.ЗначениеИзXML(
			СериализацияБЗК.СтрокаИзXML(УзлыКорня.paymentMethod)));
	Если ТипЗнч(УзлыКорня.salaryInfo) = Тип("ЭлементDOM") Тогда
		СредниеDOM = СериализацияБЗК.СписокDOM(УзлыКорня.salaryInfo, "yearSum", Необработанные);
		НомерГода = 0;
		Для Каждого СреднийDOM Из СредниеDOM Цикл
			ИменаУзлов = "year, salaryByCurrentInsurer, salaryByAllInsurers";
			ЗаработокDOM = УзлыЭлементаDOM(СреднийDOM, ИменаУзлов, Необработанные);
			ИменаУзлов = "sumOPS, sumGPH, sumOPSGPH";
			ЗаработокТекущегоСтрахователяDOM = УзлыЭлементаDOM(ЗаработокDOM.salaryByCurrentInsurer, ИменаУзлов, Необработанные, Истина);
			ЗаработокВсехСтрахователейDOM = УзлыЭлементаDOM(ЗаработокDOM.salaryByAllInsurers, ИменаУзлов, Необработанные, Истина);
			РасчетныйГод = СериализацияБЗК.ЧислоИзXML(ЗаработокDOM.year);
			ЗаработокПоТекущему = СериализацияБЗК.ЧислоИзXML(ЗаработокТекущегоСтрахователяDOM.sumOPS);
			ЗаработокПоТекущемуГПХ = СериализацияБЗК.ЧислоИзXML(ЗаработокТекущегоСтрахователяDOM.sumGPH);
			ЗаработокПоТекущемуБезГПХ = СериализацияБЗК.ЧислоИзXML(ЗаработокТекущегоСтрахователяDOM.sumOPSGPH);
			ЗаработокПоВсем = СериализацияБЗК.ЧислоИзXML(ЗаработокВсехСтрахователейDOM.sumOPS);
			ЗаработокПоВсемГПХ = СериализацияБЗК.ЧислоИзXML(ЗаработокВсехСтрахователейDOM.sumGPH);
			ЗаработокПоВсемБезГПХ = СериализацияБЗК.ЧислоИзXML(ЗаработокВсехСтрахователейDOM.sumOPSGPH);
			НомерГода = НомерГода + 1;
			Если НомерГода = 1 Тогда
				Документ.Вставить("ПервыйРасчетныйГод", РасчетныйГод);
				Документ.Вставить("ПервыйРасчетныйГодЗаработокПоТекущему", ЗаработокПоТекущему);
				Документ.Вставить("ПервыйРасчетныйГодЗаработокПоТекущемуГПХ", ЗаработокПоТекущемуГПХ);
				Документ.Вставить("ПервыйРасчетныйГодЗаработокПоТекущемуБезГПХ", ЗаработокПоТекущемуБезГПХ);
				Документ.Вставить("ПервыйРасчетныйГодЗаработокПоВсем", ЗаработокПоВсем);
				Документ.Вставить("ПервыйРасчетныйГодЗаработокПоВсемГПХ", ЗаработокПоВсемГПХ);
				Документ.Вставить("ПервыйРасчетныйГодЗаработокПоВсемБезГПХ", ЗаработокПоВсемБезГПХ);
			ИначеЕсли НомерГода = 2 Тогда
				Документ.Вставить("ВторойРасчетныйГод", РасчетныйГод);
				Документ.Вставить("ВторойРасчетныйГодЗаработокПоТекущему", ЗаработокПоТекущему);
				Документ.Вставить("ВторойРасчетныйГодЗаработокПоТекущемуГПХ", ЗаработокПоТекущемуГПХ);
				Документ.Вставить("ВторойРасчетныйГодЗаработокПоТекущемуБезГПХ", ЗаработокПоТекущемуБезГПХ);
				Документ.Вставить("ВторойРасчетныйГодЗаработокПоВсем", ЗаработокПоВсем);
				Документ.Вставить("ВторойРасчетныйГодЗаработокПоВсемГПХ", ЗаработокПоВсемГПХ);
				Документ.Вставить("ВторойРасчетныйГодЗаработокПоВсемБезГПХ", ЗаработокПоВсемБезГПХ);
			Иначе
				ТекстОшибки = ТекстОшибки + Символы.ПС + СтрШаблон(
					НСтр("ru = '- Расчетный период № %1: год ""%2"" - превышено максимальное количество расчетных лет (=2).';
						|en = '- Payroll period No. %1: %2 - the maximum number of payroll years is exceeded (=2).'"),
					НомерГода,
					РасчетныйГод);
				ТекстыОшибок.Добавить(ТекстОшибки);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Документ.Вставить("ПриостановлениеТДКалендарныхДней", СериализацияБЗК.ЧислоИзXML(УзлыКорня.mobilDaysAmount));
	Документ.Вставить("НеобработанныеУзлы", СЭДОФСС.ТаблицаЭлементовDOM(Необработанные));
	Возврат Документ;
КонецФункции

Функция УзлыЭлементаDOM(ЭлементDOM, ИменаУзлов = "", НеобработанныеУзлы = Неопределено, ТребуетсяАдаптацияИмен = Ложь)
	Возврат СериализацияБЗК.УзлыЭлементаDOM(ЭлементDOM, ИменаУзлов, НеобработанныеУзлы, ТребуетсяАдаптацияИмен);
КонецФункции

#КонецОбласти

#Область Интеграция

Процедура ПриИзмененииСНИЛСФизическогоЛица(ФизическоеЛицо, СтарыйСНИЛС, НовыйСНИЛС) Экспорт
	// В сообщениях 322 Фонд явно указывает СНИЛС и появление сообщений 322 явно связано с тем,
	// к какому РНС относятся сведения о застрахованном лице "по версии Фонда",
	// поэтому в них требуется актуализировать ссылки физлиц.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Шапка.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ДанныеСФРДляРасчетаПособия КАК Шапка
	|ГДЕ
	|	Шапка.СотрудникСНИЛС = &НовыйСНИЛС
	|	И Шапка.ФизическоеЛицо <> &ФизическоеЛицо
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Шапка.Ссылка
	|ИЗ
	|	Документ.ДанныеСФРДляРасчетаПособия КАК Шапка
	|ГДЕ
	|	Шапка.ФизическоеЛицо = &ФизическоеЛицо
	|	И Шапка.СотрудникСНИЛС <> &НовыйСНИЛС";
	Если ЗначениеЗаполнено(НовыйСНИЛС) Тогда
		Запрос.УстановитьПараметр("НовыйСНИЛС", НовыйСНИЛС);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Шапка.СотрудникСНИЛС = &НовыйСНИЛС", "ЛОЖЬ");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Шапка.СотрудникСНИЛС <> &НовыйСНИЛС", "ИСТИНА");
	КонецЕсли;
	Запрос.УстановитьПараметр("ФизическоеЛицо", ФизическоеЛицо);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаВыборки Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаВыборки.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		ДокументОбъект = СтрокаВыборки.Ссылка.ПолучитьОбъект();
		Если ДокументОбъект.СотрудникСНИЛС = НовыйСНИЛС И ЗначениеЗаполнено(НовыйСНИЛС) Тогда
			ДокументОбъект.ФизическоеЛицо = ФизическоеЛицо;
		Иначе
			ДокументОбъект.ФизическоеЛицо = Неопределено;
		КонецЕсли;
		ДокументОбъект.Организация = ДокументОбъект.Страхователь;
		ДокументОбъект.Сотрудник   = Справочники.Сотрудники.ПустаяСсылка();
		ДокументОбъект.ОбновитьВторичныеДанные();
		СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, "БЗК", РежимЗаписиДокумента.Запись);
	КонецЦикла;
КонецПроцедуры

Процедура ПриИзмененииСведенийОбЭЛН(НаборЗаписей, Замещение) Экспорт
	Если НаборЗаписей.Количество() = 0 Или РегистрыБЗК.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Возврат;
	КонецЕсли;
	Для Каждого Запись Из НаборЗаписей Цикл
		Если ЗначениеЗаполнено(Запись.ДанныеСФРДляРасчетаПособия) Тогда
			ДокументОбъект = Запись.ДанныеСФРДляРасчетаПособия.ПолучитьОбъект();
			Если ДокументОбъект.ОбновитьВторичныеДанные() Тогда
				СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, Истина, РежимЗаписиДокумента.Запись);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
