///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ВызватьИсключение НСтр("ru = 'Входящие документы от СФР загружаются в автоматическом режиме';
								|en = 'Incoming Social Insurance Fund documents are imported automatically'");
	КонецЕсли;
	
	Элементы.ПолученныеСведенияГруппа.ТолькоПросмотр = Истина;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПриПолученииДанныхНаСервере(ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	Если ИмяСобытия = "Запись_БольничныйЛист"
		Или ИмяСобытия = "Запись_ОтветНаЗапросФССДляРасчетаПособия"
		Или ИмяСобытия = СЭДОФССКлиент.ИмяСобытияПослеПолученияСообщенийОтФСС()
		Или ИмяСобытия = СЭДОФССКлиент.ИмяСобытияПослеОтправкиПодтвержденияПолучения() Тогда
		ПодключитьОбработчикОжиданияПрочитатьОбновитьВторичныеДанные();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ОбновитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ДанныеСФРДляРасчетаПособия", ПараметрыЗаписи, Объект.Ссылка);
	СЭДОФССКлиент.ОповеститьОНеобходимостиОбновитьТекущиеДела();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьТребуетсяПодтверждениеОбработкаНавигационнойСсылки(Элемент, Адрес, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если Не Записать() Тогда
		Возврат;
	КонецЕсли;
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(Объект.Ссылка);
	СЭДОФССКлиент.ОтправитьПодтверждениеПолучения(МассивСсылок);
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьВторичныеДанные(Команда)
	Если ВозможностьОбновленияВторичныхДанных() Тогда
		ОбновитьВторичныеДанныеНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИсправления(Команда)
	Если ВозможностьОбновленияВторичныхДанных() Тогда
		ОтменитьВсеИсправленияНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВопросВПоддержку(Команда)
	СЭДОФССКлиент.ОткрытьВопросВПоддержку(ПодготовитьВопросВПоддержку());
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если Записать() Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВключитьВозможностьРедактирования(Команда)
	ВключитьВозможностьРедактированияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТекстXML(Команда)
	СЭДОФССКлиент.ПоказатьТекстXML(Объект.ИдентификаторСообщения);
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область Свойства

// СтандартныеПодсистемы.Свойства
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СвертываемыеГруппы

&НаСервере
Процедура ПриОпределенииСвертываемыхГрупп(Группы) Экспорт
	Группы.Добавить(Элементы.ЗастрахованноеЛицоГруппа);
	Группы.Добавить(Элементы.ДанныеДляРасчетаГруппа);
	Группы.Добавить(Элементы.ПараметрыНазначенияГруппа);
	Группы.Добавить(Элементы.НеобработанныеУзлыГруппа);
	Группы.Добавить(Элементы.СтраховательГруппа);
КонецПроцедуры

&НаСервере
Процедура ПослеДобавленияЭлементовСвертываемыхГрупп() Экспорт
	// Обработка не требуется.
КонецПроцедуры

&НаСервере
Процедура ПриОпределенииСвойствСвертываемойГруппы(Группа, Представление, Заполнена, Развернута, Видимость) Экспорт
	
	Если Группа = Элементы.ЗастрахованноеЛицоГруппа Тогда
		ОбновитьЭлементыГруппыЗастрахованноеЛицо(Группа, Представление, Заполнена, Развернута, Видимость);
		
	ИначеЕсли Группа = Элементы.ДанныеДляРасчетаГруппа Тогда
		ОбновитьЭлементыГруппыДанныеДляРасчета(Группа, Представление, Заполнена, Развернута, Видимость);
		
	ИначеЕсли Группа = Элементы.ПараметрыНазначенияГруппа Тогда
		ОбновитьЭлементыГруппыПараметрыНазначения(Группа, Представление, Заполнена, Развернута, Видимость);
		
	ИначеЕсли Группа = Элементы.НеобработанныеУзлыГруппа Тогда
		ОбновитьЭлементыГруппыНеобработанныеУзлы(Группа, Представление, Заполнена, Развернута, Видимость);
		
	ИначеЕсли Группа = Элементы.СтраховательГруппа Тогда
		ОбновитьЭлементыГруппыСтрахователь(Группа, Представление, Заполнена, Развернута, Видимость);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СвернутьРазвернутьГруппу(Элемент)
	СвернутьРазвернутьГруппу(Элемент.Имя);
КонецПроцедуры

&НаСервере
Процедура СвернутьРазвернутьГруппу(ИмяЭлемента)
	СЭДОФСС.СвернутьРазвернутьГруппу(ЭтотОбъект, ИмяЭлемента);
КонецПроцедуры

#КонецОбласти

#Область Форма

&НаСервере
Процедура ПриПолученииДанныхНаСервере(ТекущийОбъект)
	ФиксацияВторичныхДанныхВДокументахФормы.ИнициализироватьМеханизмФиксацииРеквизитов(ЭтотОбъект, ТекущийОбъект);
	ФиксацияВторичныхДанныхВДокументахФормы.ПодключитьОбработчикиФиксацииИзмененийРеквизитов(
		ЭтотОбъект,
		ЭтотОбъект["ПараметрыФиксацииВторичныхДанных"].ОписаниеФормы.БыстрыйПоискОписаний);
	ОбновитьВторичныеДанныеНаСервере();
	ТолькоПросмотр = Не Объект.Загружен;
	
	Элементы.ФормаОбновитьВторичныеДанные.Видимость = Объект.Загружен;
	Элементы.ФормаОтменитьИзменения.Видимость = Объект.Загружен;
	
	ОбновитьВидимостьЭлементовПоПравам();
	ОбновитьЭлементыФормы();
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьЭлементовПоПравам()
	Если ПараметрыПодключаемыхКоманд = Неопределено Тогда // Первичная инициализация формы.
		ЭтоРасчетчик = Элементы.ПервыйРасчетныйГодЗаработокПоВсем.Видимость;
	КонецЕсли;
	ЕстьИзменения = ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(
		ЭтоРасчетчик,
		СЭДОФСС.ЕстьПравоПросмотраФактическихНачисленийВДокументе(Объект));
	Если ЕстьИзменения Или Не ЭтоРасчетчик Тогда
		СЭДОФСС.ИзменитьВидимостьПодчиненныхРекурсивно(Элементы.ДанныеДляРасчетаГруппа, ЭтоРасчетчик);
		Элементы.ПервыйРасчетныйГод.Видимость = Истина;
		Элементы.ВторойРасчетныйГод.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыФормы()
	
	ПравоИзменения   = (Элементы.Найти("ФормаПровести") <> Неопределено);
	ПравоПолученияЭД = СЭДОФСС.ЕстьПравоПолучения();
	ПравоОтправкиЭД  = СЭДОФСС.ЕстьПравоОтправки();
	
	// Кнопки командной панели.
	Если ПравоИзменения Тогда
		Если Объект.Загружен Тогда
			Элементы.ФормаПровестиИЗакрыть.КнопкаПоУмолчанию = Истина;
		Иначе
			Элементы.ФормаПровестиИЗакрыть.КнопкаПоУмолчанию = Ложь;
		КонецЕсли;
	КонецЕсли;
	Кнопка = Элементы.Найти("ФормаПовторноПолучитьИзФСС");
	Если Кнопка <> Неопределено Тогда
		Кнопка.Видимость = ПравоИзменения И ПравоПолученияЭД;
		Если Объект.Загружен Тогда
			Кнопка.Заголовок = "";
			Кнопка.КнопкаПоУмолчанию = Ложь;
			Кнопка.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
		Иначе
			Кнопка.Заголовок = НСтр("ru = 'Получить из СФР';
									|en = 'Get from the Social Insurance Fund of Russia'");
			Кнопка.КнопкаПоУмолчанию = Истина;
			Кнопка.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.Авто;
		КонецЕсли;
	КонецЕсли;
	
	// Подтверждение получения.
	Если Не Объект.Загружен Тогда
		ТекущаяСтраница = Элементы.ГруппаТребуетсяПолучить;
	ИначеЕсли Не Объект.ТребуетсяПодтверждение Тогда
		ТекущаяСтраница = Элементы.ГруппаНетПодтвержденияПолучения;
	ИначеЕсли ЗначениеЗаполнено(Объект.ДатаОтправкиПодтверждения) Тогда
		Если Объект.ПодтверждениеПолученоСФР Тогда
			ТекущаяСтраница = Элементы.ГруппаПодтверждениеОтправленоИПолучено;
			Элементы.НадписьПодтверждениеОтправленоИПолучено.Заголовок = СтрШаблон(
				НСтр("ru = 'Подтверждение получения отправлено %1 и получено СФР.';
					|en = 'Receipt confirmation was sent on %1 and received by the Social Insurance Fund of Russia.'"),
				Формат(Объект.ДатаОтправкиПодтверждения, "ДЛФ=D"));
		Иначе
			ТекущаяСтраница = Элементы.ГруппаПодтверждениеОтправленоНоНеПолучено;
			Элементы.НадписьПодтверждениеОтправленоНоНеПолучено.Заголовок = СтрШаблон(
				НСтр("ru = 'Подтверждение получения отправлено %1.';
					|en = 'Receipt confirmation was sent on %1.'"),
				Формат(Объект.ДатаОтправкиПодтверждения, "ДЛФ=D"));
		КонецЕсли;
	Иначе
		ТекущаяСтраница = Элементы.ГруппаТребуетсяПодтверждение;
	КонецЕсли;
	Элементы.СтраницыПодтвержденияПолучения.ТекущаяСтраница = ТекущаяСтраница;
	
	// Обновление фиксируемых реквизитов.
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбновитьФорму(ЭтотОбъект);
	
	// Обновление свертываемых групп.
	СЭДОФСС.ОбновитьСвертываемыеГруппы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСсылкиСвязанныхДокументов(ДокументОбъект)
	
	СведенияОбЭЛН = ДокументОбъект.СведенияОбЭЛН();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СведенияОбЭЛН);
	
	Элементы.БольничныйНеНайденНадпись.Видимость = ЗначениеЗаполнено(Объект.НомерЛН) И Не ЗначениеЗаполнено(Больничный);
	Элементы.ВходящийЗапросНеНайденНадпись.Видимость = ЗначениеЗаполнено(Объект.НомерЛН)
		И Не ЗначениеЗаполнено(ВходящийЗапрос)
		И Не ЗначениеЗаполнено(ОтветНаЗапрос);
	Элементы.ВходящийЗапрос.Видимость = ЗначениеЗаполнено(ВходящийЗапрос);
	Элементы.ОтветНаЗапрос.Видимость = ЗначениеЗаполнено(ОтветНаЗапрос);
	
КонецПроцедуры

#КонецОбласти

#Область ФиксацияВторичныхДанныхВДокументах

&НаКлиенте
Функция ВозможностьОбновленияВторичныхДанных()
	Если Объект.ПометкаУдаления Тогда
		Текст = НСтр("ru = 'Необходимо снять пометку удаления документа';
					|en = 'Clear the document deletion mark'");
		СообщенияБЗККлиентСервер.СообщитьВФорме(Текст, "", "", ЭтотОбъект);
		Возврат Ложь;
	КонецЕсли;
	Если Объект.Обработан Тогда
		Текст = НСтр("ru = 'Необходимо отключить флажок ""Документ обработан""';
					|en = 'Clear the ""Document is processed"" checkbox'");
		СообщенияБЗККлиентСервер.СообщитьВФорме(Текст, "Обработан", "Объект", ЭтотОбъект);
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
КонецФункции

&НаСервере
Функция ОбъектЗафиксирован() Экспорт
	Возврат Не Документы.ДанныеСФРДляРасчетаПособия.ОбновлятьВторичныеДанные(Объект);
КонецФункции

&НаСервере
Функция ФиксацияОписаниеФормы(ПараметрыФиксации) Экспорт
	ОписаниеФормы = ФиксацияВторичныхДанныхВДокументахФормы.ОписаниеФормы();
	ОписаниеФормы.Вставить("ФормаРедактируетсяПослеФиксации", Ложь);
	
	// ОписаниеЭлементовФормы - Соответствие
	//     * Ключ - Имя фиксации (имя таблицы + имя реквизита).
	//     * Значение - КлючИЗначение.Значение описания фиксации реквизита.
	ОписаниеЭлементовФормы = Новый Соответствие;
	ОписаниеФормы.Вставить("ОписаниеЭлементовФормы", ОписаниеЭлементовФормы);
	
	// БыстрыйПоискОписаний - Соответствие
	//     * Ключ - Имя элемента формы.
	//     * Значение - КлючИЗначение описания фиксации реквизита.
	БыстрыйПоискОписаний = Новый Соответствие;
	ОписаниеФормы.Вставить("БыстрыйПоискОписаний", БыстрыйПоискОписаний);
	
	ОписаниеЭлементаФормы = ФиксацияВторичныхДанныхВДокументахФормы.ОписаниеЭлементаФормы();
	ОписаниеЭлементаФормы.ПрефиксПути = "Объект";
	Для Каждого КлючИЗначение Из ПараметрыФиксации.ОписаниеФиксацииРеквизитов Цикл
		ОписаниеЭлементовФормы.Вставить(КлючИЗначение.Ключ, ОписаниеЭлементаФормы);
		Элемент = ЭлементСвязанныйСФиксируемымРеквизитом(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		Если Элемент <> Неопределено Тогда
			БыстрыйПоискОписаний.Вставить(Элемент.Имя, КлючИЗначение);
			// "Только просмотр" для служебных реквизитов в случае, если не включен режим редактирования.
			Если Не РежимРедактирования И КлючИЗначение.Значение.ИмяГруппы = "СлужебныеРеквизиты" Тогда
				Элемент.ТолькоПросмотр = истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОписаниеФормы;
КонецФункции

&НаКлиенте
Процедура ПодключитьОбработчикОжиданияПрочитатьОбновитьВторичныеДанные()
	ОтключитьОбработчикОжидания("ПрочитатьИлиОбновитьВторичныеДанныеНаКлиенте");
	ПодключитьОбработчикОжидания("ПрочитатьИлиОбновитьВторичныеДанныеНаКлиенте", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьИлиОбновитьВторичныеДанныеНаКлиенте()
	ПрочитатьИлиОбновитьВторичныеДанные();
КонецПроцедуры

&НаСервере
Процедура ПрочитатьИлиОбновитьВторичныеДанные()
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если Не Модифицированность
			Или ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбъектФормыЗафиксирован(ЭтотОбъект)
			Или Объект.ВерсияДанных <> ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ВерсияДанных") Тогда
			Прочитать();
			Возврат;
		КонецЕсли;
	КонецЕсли;
	ОбновитьВторичныеДанныеОбъектаИЭлементыФормы();
КонецПроцедуры

&НаСервере
Процедура ОбновитьВторичныеДанныеОбъектаИЭлементыФормы()
	ОбновитьВторичныеДанныеНаСервере();
	ОбновитьЭлементыФормы();
КонецПроцедуры

&НаСервере
Процедура ОбновитьВторичныеДанныеНаСервере(ИмяЭлементаПриИзменении = "")
	Если ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОбъектФормыЗафиксирован(ЭтотОбъект) Тогда
		ОбновитьСсылкиСвязанныхДокументов(РеквизитФормыВЗначение("Объект"));
		Возврат;
	КонецЕсли;
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗаполнитьИдентификаторыСтрок(ЭтотОбъект);
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.СохранитьРеквизитыФормыФикс(ЭтотОбъект, ДокументОбъект);
	
	Если ИмяЭлементаПриИзменении <> "" Тогда
		Если ИмяЭлементаПриИзменении = Элементы.Организация.Имя Тогда
			ДокументОбъект.ОрганизацияПриИзменении();
		ИначеЕсли ИмяЭлементаПриИзменении = Элементы.Сотрудник.Имя Тогда
			ДокументОбъект.СотрудникПриИзменении();
		ИначеЕсли ИмяЭлементаПриИзменении = Элементы.НомерЛН.Имя Тогда
			ДокументОбъект.НомерЛНПриИзменении();
		КонецЕсли;
	КонецЕсли;
	
	ДокументИзменен = ДокументОбъект.ОбновитьВторичныеДанные(ЭтотОбъект["ПараметрыФиксацииВторичныхДанных"]);
	
	ОбновитьСсылкиСвязанныхДокументов(ДокументОбъект);
	
	Если ДокументИзменен Тогда
		Если ТолькоПросмотр Или Не ПравоДоступа("Изменение", ДокументОбъект.Метаданные()) Тогда
			ФиксацияВторичныхДанныхВДокументахФормы.ВывестиПредупреждениеОНаличииИзмененийВИсходныхДанныхКоторыеНельзяПрименить(
				ЭтотОбъект);
		Иначе
			Если Не ДокументОбъект.ЭтоНовый() Тогда
				ФиксацияВторичныхДанныхВДокументахФормы.УстановитьМодифицированность(ЭтотОбъект, Истина);
			КонецЕсли;
			ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
			ФиксацияВторичныхДанныхВДокументахФормы.ЗаполнитьРеквизитыФормыФикс(ЭтотОбъект, Объект);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ЭлементСвязанныйСФиксируемымРеквизитом(Ключ, Значение)
	Элемент = Элементы.Найти(Ключ);
	Если Элемент = Неопределено Тогда
		Элемент = Элементы.Найти(Значение.ИмяРеквизита);
	КонецЕсли;
	Возврат Элемент;
КонецФункции

&НаСервере
Процедура ОтменитьВсеИсправленияНаСервере()
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ОчиститьФиксациюИзменений(ЭтотОбъект, Объект);
	ФиксацияВторичныхДанныхВДокументахФормы.ЗаполнитьРеквизитыФормыФикс(ЭтотОбъект, Объект);
	ОбновитьВторичныеДанныеОбъектаИЭлементыФормы();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗафиксироватьИзменениеРеквизитаВФорме(Элемент, СтандартнаяОбработка = Ложь) Экспорт
	Подключаемый_ЗафиксироватьИзменениеРеквизитаНаСервере(Элемент.Имя, Элементы.НеобработанныеУзлы.ТекущаяСтрока);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ЗафиксироватьИзменениеРеквизитаНаСервере(ИмяЭлемента, ИдентификаторСтроки = 0)
	БыстрыйПоискОписаний = ЭтотОбъект["ПараметрыФиксацииВторичныхДанных"].ОписаниеФормы.БыстрыйПоискОписаний;
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.ЗафиксироватьИзменениеРеквизита(
		ЭтотОбъект,
		БыстрыйПоискОписаний[ИмяЭлемента],
		ИдентификаторСтроки);
	ФиксацияВторичныхДанныхВДокументахКлиентСервер.СброситьФиксациюИзмененийРеквизитовПоОснованиюЗаполнения(
		ЭтотОбъект,
		ИмяЭлемента);
	ОбновитьВторичныеДанныеНаСервере(ИмяЭлемента);
	ОбновитьЭлементыФормы();
КонецПроцедуры

#КонецОбласти

#Область Организация

&НаСервере
Процедура ОбновитьЭлементыГруппыСтрахователь(Группа, Представление, Заполнена, Развернута, Видимость)
	Видимость = Объект.Загружен;
	Если Не Видимость Тогда
		Возврат;
	КонецЕсли;
	ЕстьСтрахователь = ЗначениеЗаполнено(Объект.Страхователь);
	ЕстьРегномерСФР = ЗначениеЗаполнено(Объект.РегистрационныйНомерСФР);
	ЕстьРегномерФСС = ЗначениеЗаполнено(Объект.РегистрационныйНомерФСС);
	Заполнена = ЕстьСтрахователь;
	Если Развернута Тогда
		Элементы.РегистрационныйНомерФСС.Видимость = ЕстьРегномерФСС Или РежимРедактирования;
		Элементы.РегистрационныйНомерСФР.Видимость = ЕстьРегномерСФР Или РежимРедактирования;
	Иначе
		Представления = Новый Массив;
		Если ЕстьСтрахователь Тогда
			Представления.Добавить(Строка(Объект.Страхователь));
		Иначе
			Представления.Добавить(НСтр("ru = '<Страхователь не заполнен>';
										|en = '<Insurant is not filled in>'"));
		КонецЕсли;
		Если ЕстьРегномерСФР Тогда
			Представления.Добавить(НСтр("ru = 'регистрационный номер СФР';
										|en = 'registration number in Social Insurance Fund of Russia'") + " " + Объект.РегистрационныйНомерСФР);
		ИначеЕсли ЕстьРегномерФСС Тогда
			Представления.Добавить(НСтр("ru = 'регистрационный номер ФСС';
										|en = 'SSF registration number'") + " " + Объект.РегистрационныйНомерФСС);
		КонецЕсли;
		Представление = СтрСоединить(Представления, ", ");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область Сотрудник

&НаСервере
Процедура ОбновитьЭлементыГруппыЗастрахованноеЛицо(Группа, Представление, Заполнена, Развернута, Видимость)
	Видимость = Объект.Загружен;
	Если Не Видимость Тогда
		Возврат;
	КонецЕсли;
	ЕстьФизическоеЛицо = ЗначениеЗаполнено(Объект.ФизическоеЛицо);
	Заполнена = ЕстьФизическоеЛицо;
	Если Развернута Тогда
		// В группе нет элементов для которых требуется обновлять видимость или доступность.
	Иначе
		Если ЕстьФизическоеЛицо Тогда
			Представление = Строка(Объект.ФизическоеЛицо);
		Иначе
			Представление = СтрШаблон(
				НСтр("ru = '<Не найдено физическое лицо %1 со СНИЛС %2>';
					|en = '<Individual %1 with SNILS %2 is not found>'"),
				ТРег(СокрЛП(Объект.СотрудникФамилия +" "+ Объект.СотрудникИмя +" "+ Объект.СотрудникОтчество)),
				Объект.СотрудникСНИЛС);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ДанныеДляРасчета

&НаСервере
Процедура ОбновитьЭлементыГруппыДанныеДляРасчета(Группа, Представление, Заполнена, Развернута, Видимость)
	Видимость = Объект.Загружен;
	Если Не Видимость Тогда
		Возврат;
	КонецЕсли;
	ЕстьПервыйРасчетныйГод = ЗначениеЗаполнено(Объект.ПервыйРасчетныйГод);
	ЕстьВторойРасчетныйГод = ЗначениеЗаполнено(Объект.ВторойРасчетныйГод);
	ЕстьЗаработокЗаПервыйРасчетныйГод = Объект.ПервыйРасчетныйГодЗаработокПоВсем > 0
		Или Объект.ПервыйРасчетныйГодЗаработокПоТекущему > 0;
	ЕстьЗаработокЗаВторойРасчетныйГод = Объект.ВторойРасчетныйГодЗаработокПоВсем > 0
		Или Объект.ВторойРасчетныйГодЗаработокПоТекущему > 0;
	Заполнена = (ЕстьПервыйРасчетныйГод И ЕстьЗаработокЗаПервыйРасчетныйГод)
		Или (ЕстьВторойРасчетныйГод И ЕстьЗаработокЗаВторойРасчетныйГод);
	Если Развернута Тогда
		// Видимость элементов устанавливается в ОбновитьВидимостьЭлементовПоПравам.
	Иначе
		Если ЕстьПервыйРасчетныйГод И ЕстьВторойРасчетныйГод Тогда
			Если ЕстьЗаработокЗаПервыйРасчетныйГод И ЕстьЗаработокЗаВторойРасчетныйГод Тогда
				Шаблон = НСтр("ru = 'Есть данные о заработке за %1 и %2 годы';
								|en = 'There is data on earnings for %1 and %2'");
			ИначеЕсли ЕстьЗаработокЗаПервыйРасчетныйГод Тогда
				Шаблон = НСтр("ru = 'Есть данные о заработке за %1 год, а за %2 год данных нет';
								|en = 'There is data on earnings for %1, but there is no data for %2'");
			ИначеЕсли ЕстьЗаработокЗаВторойРасчетныйГод Тогда
				Шаблон = НСтр("ru = 'Нет данных о заработке за %1 год, а за %2 год данные есть';
								|en = 'There is no data on earnings for %1, but there is data for %2'");
			Иначе
				Шаблон = НСтр("ru = 'Нет данных о заработке за %1 и %2 годы';
								|en = 'There is no data on earnings for %1 and %2'");
			КонецЕсли;
			Представление = СтрШаблон(Шаблон,
				Формат(Объект.ПервыйРасчетныйГод, "ЧН=; ЧГ="),
				Формат(Объект.ВторойРасчетныйГод, "ЧН=; ЧГ="));
		ИначеЕсли ЕстьПервыйРасчетныйГод Тогда
			Представление = НСтр("ru = '<Отсутствует второй расчетный год>';
								|en = '<The second payroll year is missing>'");
		ИначеЕсли ЕстьВторойРасчетныйГод Тогда
			Представление = НСтр("ru = '<Отсутствует первый расчетный год>';
								|en = '<The first payroll year is missing>'");
		Иначе
			Представление = НСтр("ru = '<Отсутствуют расчетные годы>';
								|en = '<Target years are missing>'");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ПараметрыНазначения

&НаСервере
Процедура ОбновитьЭлементыГруппыПараметрыНазначения(Группа, Представление, Заполнена, Развернута, Видимость)
	Видимость = Объект.Загружен;
	Если Не Видимость Тогда
		Возврат;
	КонецЕсли;
	ЕстьПриостановленияТД = ЗначениеЗаполнено(Объект.ПриостановлениеТДКалендарныхДней);
	ЕстьМетодНазначения = ЗначениеЗаполнено(Объект.МетодНазначенияИВыплатыПособия);
	Заполнена = ЕстьПриостановленияТД Или Объект.ЧислитсяУНесколькихСтрахователей Или ЕстьМетодНазначения;
	Если Развернута Тогда
		// В группе нет элементов для которых требуется обновлять видимость или доступность.
	Иначе
		Представления = Новый Массив;
		Если ЕстьПриостановленияТД Тогда
			Дней = ЗарплатаКадрыКлиентСервер.ПредставлениеДней(Объект.ПриостановлениеТДКалендарныхДней);
			Представления.Добавить(СтрШаблон(НСтр("ru = '%1 приостановления ТД';
													|en = 'Employment contract suspension %1'"), Дней));
		КонецЕсли;
		Если ЕстьМетодНазначения Тогда
			ПредставлениеЗначения = Строка(Объект.МетодНазначенияИВыплатыПособия);
			ПредставлениеЗначения = НРег(Лев(ПредставлениеЗначения, 1)) + Сред(ПредставлениеЗначения, 2);
			Представления.Добавить(НСтр("ru = 'Назначить пособие';
										|en = 'Grant allowance'") + " " + ПредставлениеЗначения);
		КонецЕсли;
		Если Объект.ЧислитсяУНесколькихСтрахователей Тогда
			Представления.Добавить(НСтр("ru = 'числится у нескольких страхователей';
										|en = 'registered with multiple insurants'"));
		КонецЕсли;
		Представление = СтрСоединить(Представления, ", ");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область НеобработанныеУзлы

&НаСервере
Процедура ОбновитьЭлементыГруппыНеобработанныеУзлы(Группа, Представление, Заполнена, Развернута, Видимость)
	ЕстьНеобработанныеУзлы = ЗначениеЗаполнено(Объект.НеобработанныеУзлы);
	Видимость = Объект.Загружен И (ЕстьНеобработанныеУзлы Или РежимРедактирования);
	Если Не Видимость Тогда
		Возврат;
	КонецЕсли;
	Заполнена = ЕстьНеобработанныеУзлы;
	Если Развернута Тогда
		// В группе нет элементов для которых требуется обновлять видимость или доступность.
	Иначе
		Представление = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = ';%1 узел;;%1 узла;%1 узлов;';
				|en = ';%1 node;;;;%1 nodes'"),
			Объект.НеобработанныеУзлы.Количество());
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область РежимРедактирования

&НаСервере
Процедура ВключитьВозможностьРедактированияНаСервере()
	РежимРедактирования = Истина;
	Элементы.ВключитьВозможностьРедактирования.Пометка = Истина;
	Элементы.ВключитьВозможностьРедактирования.Доступность = Ложь;
	
	Для Каждого Элемент Из Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ПолеФормы")
			И Элемент.ТолькоПросмотр Тогда
			Элемент.ТолькоПросмотр = Ложь;
		ИначеЕсли ТипЗнч(Элемент) = Тип("ГруппаФормы")
			И Элемент.ТолькоПросмотр Тогда
			Элемент.ТолькоПросмотр = Ложь;
		ИначеЕсли ТипЗнч(Элемент) = Тип("ТаблицаФормы")
			И (Элемент.ТолькоПросмотр Или Не Элемент.ИзменятьСоставСтрок) Тогда
			Элемент.ТолькоПросмотр = Ложь;
			Элемент.ИзменятьПорядокСтрок = Истина;
			Элемент.ИзменятьСоставСтрок = Истина;
			Элемент.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Авто;
			Элемент.РастягиватьПоВертикали = Истина;
			Элемент.Шапка = Истина;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьЭлементыФормы();
КонецПроцедуры

#КонецОбласти

#Область Поддержка

&НаСервере
Функция ПодготовитьВопросВПоддержку()
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Вложения = Новый Массив;
	Возврат СЭДОФСС.ПодготовитьВопросВПоддержку(ДокументОбъект, Вложения);
КонецФункции

#КонецОбласти

#КонецОбласти
