#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Документы.НазначениеРабочихМестСотрудникам.ЗаполнитьДатуЗапретаРедактирования(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ШтатноеРасписание") Тогда
		Должность = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "Должность");
		Подразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "Подразделение");
		Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "Владелец");
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Действие") И ДанныеЗаполнения.Действие = "Исправить" Тогда
			ИсправлениеДокументовЗарплатаКадры.СкопироватьДокумент(ЭтотОбъект, ДанныеЗаполнения.Ссылка);
			ИсправленныйДокумент = ДанныеЗаполнения.Ссылка;
		ИначеЕсли ДанныеЗаполнения.Свойство("Организация") Тогда
			Организация = ДанныеЗаполнения.Организация;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект, , , ЗначениеЗаполнено(ИсправленныйДокумент));
	ИсправлениеПериодическихСведений.ИсправлениеПериодическихСведений(ЭтотОбъект, Отказ, РежимПроведения);
	
	ДанныеДляПроведения = ДанныеДляПроведения();
	
	ЗарплатаКадрыРасширенный.УстановитьВремяРегистрацииДокумента(Движения, ДанныеДляПроведения.РабочиеМестаСотрудников.Скопировать(, "Сотрудник, Период"), Ссылка, "Период");
	
	РабочиеМестаОхраныТруда.СформироватьДвиженияПоРабочимМестам(Движения, ДанныеДляПроведения);
	
	УчетСтажаПФР.ЗарегистрироватьПериодыВУчетеСтажаПФР(Движения, ДанныеДляРегистрацииВУчетаСтажаПФР());
	
	Если ДанныеДляПроведения.Свойство("ЗначенияПоказателей") И ДанныеДляПроведения.ЗначенияПоказателей.Количество() > 0 Тогда	
		СтруктураПлановыхНачислений = Новый Структура;
		СтруктураПлановыхНачислений.Вставить("ДанныеОПлановыхНачислениях", ДанныеДляПроведения.ПлановыеНачисления);
		СтруктураПлановыхНачислений.Вставить("ЗначенияПоказателей", ДанныеДляПроведения.ЗначенияПоказателей);
		
		РасчетЗарплаты.СформироватьДвиженияПлановыхНачислений(ЭтотОбъект, Движения, СтруктураПлановыхНачислений); 
	КонецЕсли;
	
	Если ДанныеДляПроведения.Свойство("ЕжегодныеОтпуска") И ДанныеДляПроведения.ЕжегодныеОтпуска.Количество() > 0 Тогда
		ОстаткиОтпусков.СформироватьДвиженияПоложенныхЕжегодныхОтпусков(Ссылка, Движения, ДанныеДляПроведения.ЕжегодныеОтпуска);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, НачалоПериода, "Объект.НачалоПериода", Отказ, НСтр("ru = 'Начало периода';
																										|en = 'Period start'"), , , Ложь);
	
	Если ЗначениеЗаполнено(ОкончаниеПериода) И ОкончаниеПериода < НачалоПериода Тогда
		
		ТекстСообщения = НСтр("ru = 'Неверно задано окончание периода';
								|en = 'Period end is specified incorrectly'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "Объект.ОкончаниеПериода", , Отказ);
		
	КонецЕсли;
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияРабочихМестВОрганизацийПоВременнойТаблице();
	ПараметрыПолученияСотрудниковОрганизаций.Организация 				= Организация;
	ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода				= НачалоПериода;
	ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода			= НачалоПериода;
	ПараметрыПолученияСотрудниковОрганизаций.РаботникиПоДоговорамГПХ 	= Неопределено;
	
	КадровыйУчет.ПроверитьРаботающихСотрудников(
		Сотрудники.ВыгрузитьКолонку("Сотрудник"),
		ПараметрыПолученияСотрудниковОрганизаций,
		Отказ,
		Новый Структура("ИмяПоляСотрудник, ИмяОбъекта", "Сотрудник", "Объект.Сотрудники"));
	
	ИсправлениеДокументовЗарплатаКадры.ПроверитьЗаполнение(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, "ПериодическиеСведения", "НачалоПериода");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НазначениеРабочихМест.НачалоПериода КАК Период,
		|	НазначениеРабочихМестСотрудники.Сотрудник КАК Сотрудник,
		|	НазначениеРабочихМест.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	НазначениеРабочихМестСотрудники.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НазначениеРабочихМестРабочиеМеста.ВыплачиваетсяНадбавкаЗаВредность КАК Используется,
		|	ВЫБОР
		|		КОГДА НазначениеРабочихМестРабочиеМеста.ВыплачиваетсяНадбавкаЗаВредность
		|			ТОГДА НазначениеРабочихМестРабочиеМеста.ПроцентНадбавкиЗаВредность
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Значение,
		|	ВЫБОР
		|		КОГДА НазначениеРабочихМестРабочиеМеста.ВыплачиваетсяНадбавкаЗаВредность
		|			ТОГДА НазначениеРабочихМестРабочиеМеста.КоличествоДнейДополнительногоОтпускаВГод
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоДнейВГод,
		|	0 КАК Размер,
		|	ЛОЖЬ КАК ПересчетНеТребуется,
		|	НазначениеРабочихМестРабочиеМеста.РабочееМесто КАК РабочееМесто
		|ИЗ
		|	Документ.НазначениеРабочихМестСотрудникам КАК НазначениеРабочихМест
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеРабочихМестСотрудникам.Сотрудники КАК НазначениеРабочихМестСотрудники
		|		ПО НазначениеРабочихМест.Ссылка = &Ссылка
		|		И НазначениеРабочихМест.Ссылка = НазначениеРабочихМестСотрудники.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НазначениеРабочихМестСотрудникам.РабочиеМеста КАК НазначениеРабочихМестРабочиеМеста
		|		ПО НазначениеРабочихМестРабочиеМеста.Ссылка = НазначениеРабочихМестСотрудники.Ссылка
		|		И НазначениеРабочихМестРабочиеМеста.ИдентификаторСтрокиРабочегоМеста = НазначениеРабочихМестСотрудники.ИдентификаторСтрокиРабочегоМеста";
	
	РабочиеМестаСотрудников = Запрос.Выполнить().Выгрузить();
	Если ЗначениеЗаполнено(ОкончаниеПериода) Тогда
		РабочиеМестаСотрудников.Колонки.Добавить("ДействуетДо", Новый ОписаниеТипов("Дата"));
		РабочиеМестаСотрудников.ЗаполнитьЗначения(КонецДня(ОкончаниеПериода) + 1, "ДействуетДо");
	КонецЕсли;
	
	Данные = Новый Структура("РабочиеМестаСотрудников", РабочиеМестаСотрудников);
		
	Если Не (РабочиеМестаОхраныТруда.УсловияТрудаРабочихМестОхраныТрудаПрименяются(Организация)
		И ПолучитьФункциональнуюОпцию("ИспользоватьНадбавкуЗаВредность")) Тогда
		
		Возврат Данные;
	КонецЕсли;
	
	ОтпускЗаВредность = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.ОтпускЗаВредность");
	Если ЗначениеЗаполнено(ОтпускЗаВредность) Тогда
		ПоложенныеЕжегодныеОтпуска = РабочиеМестаСотрудников.Скопировать();
		ПоложенныеЕжегодныеОтпуска.Колонки.Период.Имя = "ДатаСобытия";
		ПоложенныеЕжегодныеОтпуска.Колонки.Добавить("ВидЕжегодногоОтпуска", Новый ОписаниеТипов("СправочникСсылка.ВидыОтпусков"));
		ПоложенныеЕжегодныеОтпуска.ЗаполнитьЗначения(ОтпускЗаВредность, "ВидЕжегодногоОтпуска");
		Данные.Вставить("ЕжегодныеОтпуска", ПоложенныеЕжегодныеОтпуска);
	КонецЕсли;
	
	НачислениеНадбавкаЗаВредность = ПланыВидовРасчета.Начисления.НачислениеНадбавкаЗаВредность();
	Если ЗначениеЗаполнено(НачислениеНадбавкаЗаВредность) Тогда
		ПлановыеНачисления = РабочиеМестаСотрудников.Скопировать();
		ПлановыеНачисления.Колонки.Период.Имя = "ДатаСобытия";
		ПлановыеНачисления.Колонки.Добавить("ДокументОснование", Новый ОписаниеТипов());
		ПлановыеНачисления.Колонки.Добавить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
		ПлановыеНачисления.ЗаполнитьЗначения(НачислениеНадбавкаЗаВредность, "Начисление");
		Данные.Вставить("ПлановыеНачисления", ПлановыеНачисления);
	КонецЕсли;
		
	ПоказательПроцентНадбавкиЗаВредность = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.ПроцентНадбавкиЗаВредность");
	Если ЗначениеЗаполнено(ПоказательПроцентНадбавкиЗаВредность) Тогда
		ЗначенияПоказателей = РабочиеМестаСотрудников.Скопировать();
		ЗначенияПоказателей.Колонки.Период.Имя = "ДатаСобытия";
		ЗначенияПоказателей.Колонки.ГоловнаяОрганизация.Имя = "Организация";
		ЗначенияПоказателей.Колонки.Добавить("Показатель", Новый ОписаниеТипов("СправочникСсылка.ПоказателиРасчетаЗарплаты"));
		ЗначенияПоказателей.ЗаполнитьЗначения(ПоказательПроцентНадбавкиЗаВредность, "Показатель");
		Данные.Вставить("ЗначенияПоказателей", ЗначенияПоказателей);
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

Функция ДанныеДляРегистрацииВУчетаСтажаПФР()
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(Ссылка);
	
	ДанныеДляРегистрации = Документы.НазначениеРабочихМестСотрудникам.ДанныеДляРегистрацииВУчетаСтажаПФР(МассивСсылок);
	
	Возврат ДанныеДляРегистрации[Ссылка];	
КонецФункции	

Процедура ЗаполнитьСотрудников(ИдентификаторыРабочихМест) Экспорт
	
	Сотрудники.Очистить();
	
	Для Каждого СтрокаРабочегоМеста Из РабочиеМеста Цикл
		МассивСотрудников = ИдентификаторыРабочихМест.Получить(СтрокаРабочегоМеста.ИдентификаторСтрокиРабочегоМеста);
		Если МассивСотрудников = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого ДанныеСотрудника Из МассивСотрудников Цикл
			НоваяСтрока = Сотрудники.Добавить();
			НоваяСтрока.ИдентификаторСтрокиРабочегоМеста = СтрокаРабочегоМеста.ИдентификаторСтрокиРабочегоМеста;
			НоваяСтрока.Сотрудник = ДанныеСотрудника.Сотрудник;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли