#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Номер = Данные.Номер;
	Направление = "";
	Если ЗначениеЗаполнено(Данные.Ссылка) Тогда
		РеквизитыСсылки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Данные.Ссылка, "ТитулГрузоотправителяТранспортнаяНакладнаяНомер,ЭтоВходящий", Истина);
		Если ЗначениеЗаполнено(РеквизитыСсылки.ТитулГрузоотправителяТранспортнаяНакладнаяНомер) Тогда
			Номер = РеквизитыСсылки.ТитулГрузоотправителяТранспортнаяНакладнаяНомер;
		КонецЕсли;
		Если ЗначениеЗаполнено(РеквизитыСсылки.ЭтоВходящий) Тогда
			Направление = ?(РеквизитыСсылки.ЭтоВходящий, " (входящая)", "");
		КонецЕсли;
	КонецЕсли;
	Дата = Формат(Данные.Дата, "ДФ=dd.MM.yyyy");
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Электронная транспортная накладная%1 № %2 от %3';
							|en = 'Электронная транспортная накладная%1 № %2 от %3'"), Направление, Номер, Дата);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ЭТрН";
	КомандаПечати.Представление = НСтр("ru = 'Транспортная накладная';
										|en = 'Транспортная накладная'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.Картинка = БиблиотекаКартинок.Печать;
	
КонецПроцедуры

// Формирует печатные формы
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати         - СписокЗначений:
//  * Значение - ДокументСсылка.РеализацияТоваровУслуг - ссылка на объект;
//  * Представление - Строка - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной
//                                            параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЭТрН") Тогда
		
		ИмяПечатнойФормы = Неопределено;
		Если МассивОбъектов.Количество() = 1 Тогда
			ДатаДокумента = МассивОбъектов[0].Дата;
			ИмяПечатнойФормы = ОбменСГИСЭПДПереопределяемый.ИмяПечатнойФормыЭТрН(ДатаДокумента);
		КонецЕсли;
	
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ЭТрН",
			НСтр("ru = 'Транспортная накладная';
				|en = 'Транспортная накладная'"),
			СформироватьТранспортнуюНакладную(МассивОбъектов, ОбъектыПечати, ИмяПечатнойФормы),
			,
			ИмяПечатнойФормы);
			
	КонецЕсли;
	
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции
	
// Формирует печатную форму транспортной накладной.
//
// Параметры:
//  МассивОбъектов - Массив из ДокументСсылка.РеализацияТоваровУслуг - документы РТУ на печать.
//  ОбъектыПечати - СписокЗначений из ДокументСсылка - объекты печати (значение - ссылка на объект, 
// 		представление - имя области, в которой был выведен объект).
//  ИмяПечатнойФормы - Строка, Неопределено - Имя печатной формы.
//
// Возвращаемое значение:
// 	ТабличныйДокумент
//
Функция СформироватьТранспортнуюНакладную(МассивОбъектов, ОбъектыПечати, Знач ИмяПечатнойФормы) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб         = Истина;
	ТабличныйДокумент.ОриентацияСтраницы  = ОриентацияСтраницы.Портрет;
	
	ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЭлектроннаяТранспортнаяНакладная_ТранспортнаяНакладная";
	ТабличныйДокумент.ИмяПараметровПечати = ИмяПараметровПечати;
	
	ОбменСГИСЭПД.УстановитьМинимальныеПоляПечати(ТабличныйДокумент);
	
	МакетПереданный = Неопределено;
	Если ИмяПечатнойФормы <> Неопределено Тогда
		МакетПереданный = УправлениеПечатью.МакетПечатнойФормы(ИмяПечатнойФормы);
	КонецЕсли;
	
	Для Каждого СсылкаНаОбъект Из МассивОбъектов Цикл
		
		ДанныеФормирования = ОбменСГИСЭПД.ДанныеРеквизитовЭПД(СсылкаНаОбъект);
		
		Если МакетПереданный = Неопределено Тогда
			ИмяПечатнойФормы = ОбменСГИСЭПДПереопределяемый.ИмяПечатнойФормыЭТрН(СсылкаНаОбъект.Дата);
			Макет = УправлениеПечатью.МакетПечатнойФормы(ИмяПечатнойФормы);
		Иначе
			Макет = МакетПереданный;
		КонецЕсли;
		
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ПараметрыМакета = ПараметрыМакетаЭТрН(СсылкаНаОбъект, ДанныеФормирования);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ГоризонтальнаяЛицеваяСторона");
		ОбластьМакета.Параметры.Заполнить(ПараметрыМакета);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		ОбластьМакетаОборотная = Макет.ПолучитьОбласть("ГоризонтальнаяОборотнаяСторона");
		ОбластьМакетаОборотная.Параметры.Заполнить(ПараметрыМакета);
		
		ЗаполнитьОтметкиУчастников("ТитулГрузоотправителяОтметкиГрузоотправителя", 
									"ТитулГрузоотправителяРасчетыШтрафов", 
									"ТитулГрузоотправителяПодписант",
									ОбластьМакетаОборотная,
									ДанныеФормирования);
									
		ЗаполнитьОтметкиУчастников("ТитулПеревозчикаПриемкаОтметкиПеревозчикаПриПриемеГруза", 
									"ТитулПеревозчикаПриемкаРасчетыШтрафов", 
									"ТитулПеревозчикаПриемкаПодписант",
									ОбластьМакетаОборотная,
									ДанныеФормирования);
									
		ЗаполнитьОтметкиУчастников("ТитулГрузополучателяОтметкиГрузополучателя", 
									"ТитулГрузополучателяРасчетыШтрафов", 
									"ТитулГрузополучателяПодписант",
									ОбластьМакетаОборотная,
									ДанныеФормирования);
									
		ЗаполнитьОтметкиУчастников("ТитулПеревозчикаВыдачаОтметкиПриВыдачеГруза", 
									"ТитулПеревозчикаВыдачаРасчетыШтрафов", 
									"ТитулПеревозчикаВыдачаПодписант",
									ОбластьМакетаОборотная,
									ДанныеФормирования);
					
		ТабличныйДокумент.Вывести(ОбластьМакетаОборотная);
		
		// Добавить штампы
		Если ЗначениеЗаполнено(СсылкаНаОбъект.УИДМинтранс) Тогда
			ИдентификаторДокумента = СсылкаНаОбъект.УИДМинтранс;
		Иначе
			ИдентификаторДокумента = СсылкаНаОбъект.ТитулГрузоотправителяИдентификаторФайла;
		КонецЕсли;     
		
		ОбменСГИСЭПД.УстановитьШтампыПодписейПоДокументуЭПД(СсылкаНаОбъект, ТабличныйДокумент, ИдентификаторДокумента);
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, СсылкаНаОбъект);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
 
КонецФункции

Процедура ЗаполнитьОтметкиУчастников(ИмяТаблицыОтметок, ИмяТаблицыШтрафов, ПрефиксРеквизитовПодписанта,
		ОбластьЗаполнения, ДанныеЗаполнения)
	
	ОтметкиУчастника = Неопределено;
	Если ДанныеЗаполнения.Свойство(ИмяТаблицыОтметок, ОтметкиУчастника) Тогда
		Для Каждого СтрОтметка Из ОтметкиУчастника Цикл
			ОбластьЗамечания = ОбластьЗаполнения.Область("R37:R38");
			ОбластьЗаполнения.ВставитьОбласть(ОбластьЗамечания, ОбластьЗамечания, ТипСмещенияТабличногоДокумента.ПоВертикали, );
			
			ОбластьЗаполнения.Область(37, 2).Текст = СтрОтметка.ОписаниеОбстоятельств;
			
			МассивШтрафов = Новый Массив;	
			СтрокиШтрафов = ОбменСГИСЭПД.НайтиСтрокиВМассивеСтруктур(
									ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ИмяТаблицыШтрафов), 
									"ИдентификаторСтрокиРодителя", СтрОтметка.ИдентификаторСтроки);
			Для Каждого СтрШтраф Из СтрокиШтрафов Цикл
				Если ЗначениеЗаполнено(СтрШтраф.РазмерШтрафаВВалюте) Тогда
					ПредставлениеШтрафа = Строка(СтрШтраф.РазмерШтрафаВВалюте) + " " + СтрШтраф.НаименованиеВалютыШтрафа
						+ ?(ЗначениеЗаполнено(СтрШтраф.Курс), " " + НСтр("ru = 'по курсу';
																		|en = 'по курсу'", "ru") + " " + СтрШтраф.Курс, "");	
				Иначе
					ПредставлениеШтрафа = Строка(СтрШтраф.РазмерШтрафа) + " руб.";
				КонецЕсли;
				ПредставлениеШтрафа = ПредставлениеШтрафа + " (" + СтрШтраф.РасчетШтрафа + ")";
				МассивШтрафов.Добавить(ПредставлениеШтрафа);	
			КонецЦикла;
			ОбластьЗаполнения.Область(37, 29).Текст = СтрСоединить(МассивШтрафов, Символы.ПС);
			ОбластьЗаполнения.Область(37, 44).Текст = Строка(СтрОтметка.ДатаВремя) + ", " 
																	+ ОбменСГИСЭПД.ПредставлениеПодписи(ДанныеЗаполнения, 
																							ПрефиксРеквизитовПодписанта);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыМакетаЭТрН(СсылкаНаОбъект, ДанныеЗаполнения)
	
	Результат = Новый Структура;
	
	// Шапка
    ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт0_0", "ТитулГрузоотправителяТранспортнаяНакладнаяДата",,"ДЛФ=D");
    ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт0_5", "ТитулГрузоотправителяТранспортнаяНакладнаяНомер");
    ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт0_2", "ТитулГрузоотправителяЗаказДата",,"ДЛФ=D");
    ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт0_3", "ТитулГрузоотправителяЗаказНомер");
	
	Результат.Вставить("Пункт0_1", СсылкаНаОбъект.РольУчастника);
	
	// Грузоотправитель 
	Пункт_Э = "";
	ЭкспедиторГрузоотправителя = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяЭкспедитор");
	Если ЭкспедиторГрузоотправителя = Истина Тогда
		Пункт_Э = "V";	
	КонецЕсли;
	
	Результат.Вставить("Пункт_Э", Пункт_Э);
	
	ОснованиеРасчетовНомер = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяОснованиеРасчетовИнымЛицомНомер");
	Если ЗначениеЗаполнено(ОснованиеРасчетовНомер) Тогда
		ОснованиеРасчетовНаименование = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
			"ТитулГрузоотправителяОснованиеРасчетовИнымЛицомНаименование", "");
		ОснованиеРасчетовДата = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
			"ТитулГрузоотправителяОснованиеРасчетовИнымЛицомДата", "");
		ОснованиеРасчетовСтрокой = ПредставлениеНаименования(ОснованиеРасчетовНаименование,
			ОснованиеРасчетовНомер, ОснованиеРасчетовДата);  
		Результат.Вставить("Пункт1_2", ОснованиеРасчетовСтрокой);
	КонецЕсли;

	Результат.Вставить("Пункт1_1", ОбменСГИСЭПД.ПредставлениеУчастника(ДанныеЗаполнения, "ТитулГрузоотправителяГрузоотправитель"));
	
	// Заказчик
	Результат.Вставить("Пункт1а_1", ОбменСГИСЭПД.ПредставлениеУчастника(ДанныеЗаполнения, "ТитулГрузоотправителяЗаказчик"));

	УсловияПеревозкиНомер = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяДоговорНаУсловияПеревозкиНомер");
	Если ЗначениеЗаполнено(УсловияПеревозкиНомер) Тогда
		УсловияПеревозкиНаименование = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
			"ТитулГрузоотправителяДоговорНаУсловияПеревозкиНаименование", "");
		УсловияПеревозкиДата = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
			"ТитулГрузоотправителяДоговорНаУсловияПеревозкиДата", "");
		УсловияПеревозкиСтрокой = ПредставлениеНаименования(УсловияПеревозкиНаименование,
			УсловияПеревозкиНомер, УсловияПеревозкиДата);  
		Результат.Вставить("Пункт1а_2", УсловияПеревозкиСтрокой);
	КонецЕсли;
	
	// Грузополучатель
	Результат.Вставить("Пункт2_1", ОбменСГИСЭПД.ПредставлениеУчастника(ДанныеЗаполнения, "ТитулГрузоотправителяГрузополучатель"));
	Результат.Вставить("Пункт2_2", ОбменСГИСЭПД.ПредставлениеАдреса(ДанныеЗаполнения, "ТитулГрузоотправителяГрузополучательАдресДоставки"));
	
	// Груз
	КодМетодаОпределенияМассыГруза = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяКодМетодаОпределенияМассыГруза");
	
	Если КодМетодаОпределенияМассыГруза = "01" Тогда
		МетодОпределенияМассы = НСтр("ru = 'определено взвешиванием по общей массе';
									|en = 'определено взвешиванием по общей массе'", "ru");
	ИначеЕсли КодМетодаОпределенияМассыГруза = "02" Тогда
		МетодОпределенияМассы = НСтр("ru = 'определено взвешиванием поосно';
									|en = 'определено взвешиванием поосно'", "ru");
	Иначе
		МетодОпределенияМассы = НСтр("ru = 'определено расчетным способом';
									|en = 'определено расчетным способом'", "ru");
	КонецЕсли;
	
	МассивПункт3_1 = Новый Массив;
	МассивПункт3_2 = Новый Массив;
	МассивПункт3_3 = Новый Массив;
	МассивПункт3_4 = Новый Массив;
	МассаБрутто = 0;
	МассаНетто = 0;
	ОбъемГруза = 0;
	ВсегоМест = 0;
	СчетчикМест = 0;
	НомераМест = "";
	ФорматДаты = "ДФ='dd.MM.yyyy ЧЧ:мм';"; 
	
	ГрузыГрузоотправителя = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяГрузы", Новый Массив);
	ПереченьМаркировокГруза = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяПереченьМаркировокГруза");
	СведенияОбОпасныхГрузах = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяСведенияОбОпасныхГрузах");
	
	НужныНомераМест = ГрузыГрузоотправителя.Количество() > 1;	
	КлючиПроверкиГрузов = НеобязательныеСвойстваТаблицы("ТитулГрузоотправителяГрузы");
	КлючиПроверкиДОПОГ = НеобязательныеСвойстваТаблицы("ДОПОГ");
	КлючиПроверкиДокументов = НеобязательныеСвойстваТаблицы("Документы"); 
	КлючиПроверкиВодителей = НеобязательныеСвойстваТаблицы("Водители"); 
	КлючиПроверкиУсловийДвижения = НеобязательныеСвойстваТаблицы("СпециальныеУсловияДвижения"); 

	Для Каждого СтрГруз Из ГрузыГрузоотправителя Цикл
		
		ПроверитьКлючиСтруктуры(СтрГруз, КлючиПроверкиГрузов);
		
		МассивПункт3_1.Добавить(СтрГруз.ОтгрузочноеНаименованиеГруза);
		
		// Маркировка
		СтрокиМаркировки = ОбменСГИСЭПД.НайтиСтрокиВМассивеСтруктур(ПереченьМаркировокГруза, "ИдентификаторСтрокиРодителя",
			СтрГруз.ИдентификаторСтроки);
		
		МассивМаркировок = Новый Массив;
		
		Для Каждого СтрМарк Из СтрокиМаркировки Цикл
			МассивМаркировок.Добавить(СтрМарк.МаркировкаГруза);	
		КонецЦикла;
		
		Если НужныНомераМест = Истина Тогда
			ШаблонНомераМест = НСтр("ru = 'Места N N %1-%2:';
									|en = 'Места N N %1-%2:'", "ru") + " ";
			НомераМест = СтрШаблон(ШаблонНомераМест, Строка(СчетчикМест + 1), Строка(СчетчикМест + СтрГруз.КоличествоГрузовыхМест));	
		КонецЕсли;
		
		// Опасный груз
		МассивДОПОГ = Новый Массив;
		СтрокиДОПОГ = ОбменСГИСЭПД.НайтиСтрокиВМассивеСтруктур(СведенияОбОпасныхГрузах,	"ИдентификаторСтрокиРодителя",
			СтрГруз.ИдентификаторСтроки);
		
		ШаблонДОПОГ = НСтр("ru = 'номер ООН:%1; класс: %2; описание: %3';
							|en = 'номер ООН:%1; класс: %2; описание: %3'", "ru");
		Для Каждого СтрДОПОГ Из СтрокиДОПОГ Цикл
			
			ПроверитьКлючиСтруктуры(СтрДОПОГ, КлючиПроверкиДОПОГ);			
			МассивДОПОГ.Добавить(СтрШаблон(ШаблонДОПОГ, СтрДОПОГ.НомерООН, СтрДОПОГ.Класс, СтрДОПОГ.Описание));
			
		КонецЦикла;
		
		Если МассивДОПОГ.Количество() > 0 Тогда
			Пункт3_4 = НомераМест + СтрСоединить(МассивДОПОГ);
			МассивПункт3_4.Добавить(Пункт3_4);
		КонецЕсли;
		
		ВсегоМест = ВсегоМест + СтрГруз.КоличествоГрузовыхМест;
		
		ШаблонПункт3_2 = НСтр("ru = 'Мест: %1, маркировка: %2, вид тары: %3, способ упаковки: %4';
								|en = 'Мест: %1, маркировка: %2, вид тары: %3, способ упаковки: %4'", "ru");
		Маркировки = СтрСоединить(МассивМаркировок, ", "); 
		ТекстДляПункт3_2 = СтрШаблон(ШаблонПункт3_2, СтрГруз.КоличествоГрузовыхМест, Маркировки, СтрГруз.ВидТары,
			СтрГруз.СпособУпаковки);
		
		МассивПункт3_2.Добавить(ТекстДляПункт3_2); 
		
		МассаНеттоСтрокой = "";
		ВысотаШиринаДлинаСтрокой = ""; 
		ОбъемГрузаСтрокой = "";
		ПлотностьСтрокой = "";
		
		Если СтрГруз.МассаНетто > 0 Тогда
			ШаблонМассаНетто = " " + НСтр("ru = '/ %1кг (нетто)';
											|en = '/ %1кг (нетто)'", "ru");
			МассаНеттоСтрокой = СтрШаблон(ШаблонМассаНетто, СтрГруз.МассаНетто);	
		КонецЕсли;
		
		Если СтрГруз.Высота + СтрГруз.Ширина + СтрГруз.Длина > 0 Тогда
			ШаблонВШД = НСтр("ru = '; В x Ш x Д: %1м x %2м x %3м';
							|en = '; В x Ш x Д: %1м x %2м x %3м'", "ru");
			ВысотаШиринаДлинаСтрокой = СтрШаблон(ШаблонВШД, СтрГруз.Высота, СтрГруз.Ширина, СтрГруз.Длина);		
		КонецЕсли;
		
		Если СтрГруз.ОбъемГруза > 0 Тогда
			ШаблонОбъемГруза = НСтр("ru = '; объем: %1м3';
									|en = '; объем: %1м3'", "ru");
			ОбъемГрузаСтрокой = СтрШаблон(ШаблонОбъемГруза, СтрГруз.ОбъемГруза);		
		КонецЕсли;
		
		Если СтрГруз.Плотность > 0 Тогда
			ШаблонПлотность = НСтр("ru = '; плотность: %1кг/м3';
									|en = '; плотность: %1кг/м3'", "ru");
			ОбъемГрузаСтрокой = СтрШаблон(ШаблонПлотность, СтрГруз.Плотность);
		КонецЕсли;
		
		ШаблонПункт3_3 = НСтр("ru = '%1%2кг (брутто)%3 (%4)%5%6%7';
								|en = '%1%2кг (брутто)%3 (%4)%5%6%7'", "ru");
		Пункт3_3 = СтрШаблон(ШаблонПункт3_3,
			НомераМест, СтрГруз.МассаБрутто, МассаНеттоСтрокой, МетодОпределенияМассы, 
			ВысотаШиринаДлинаСтрокой, ОбъемГрузаСтрокой, ПлотностьСтрокой);   
		
		МассивПункт3_3.Добавить(Пункт3_3);
		
		МассаБрутто = МассаБрутто + СтрГруз.МассаБрутто;
		МассаНетто = МассаНетто + СтрГруз.МассаНетто;	
		ОбъемГруза = ОбъемГруза + СтрГруз.ОбъемГруза;
		СчетчикМест = СчетчикМест + СтрГруз.КоличествоГрузовыхМест;
	КонецЦикла;
	
	Если НужныНомераМест = Истина Тогда

		ВсегоМестСтрокой = НСтр("ru = 'Всего мест:';
								|en = 'Всего мест:'", "ru") + " " + ВсегоМест;	
		МассивПункт3_2.Добавить(ВсегоМестСтрокой);
		
		МассаНеттоИтогоСтрокой = "";
		ОбъемГрузаИтогоСтрокой = "";
		
		Если МассаНетто > 0 Тогда
			МассаНеттоИтогоСтрокой = " " + СтрШаблон(НСтр("ru = '/ %1кг (нетто)';
															|en = '/ %1кг (нетто)'", "ru"), МассаНетто); 
		КонецЕсли;
		
		Если ОбъемГруза > 0 Тогда
			ОбъемГрузаИтогоСтрокой = СтрШаблон(НСтр("ru = '; объем: %1м3';
													|en = '; объем: %1м3'", "ru"), ОбъемГруза); 
		КонецЕсли; 
		
		МассаОбъемИтогоСтрокой = СтрШаблон(НСтр("ru = 'Всего: %1кг (брутто)%2%3';
												|en = 'Всего: %1кг (брутто)%2%3'", "ru"),
			МассаБрутто, МассаНеттоИтогоСтрокой, ОбъемГрузаИтогоСтрокой);
		МассивПункт3_3.Добавить(МассаОбъемИтогоСтрокой);
		
	КонецЕсли; 	
	
	Результат.Вставить("Пункт3_1", СтрСоединить(МассивПункт3_1, Символы.ПС));
	Результат.Вставить("Пункт3_2", СтрСоединить(МассивПункт3_2, Символы.ПС));
	Результат.Вставить("Пункт3_3", СтрСоединить(МассивПункт3_3, Символы.ПС));
	Результат.Вставить("Пункт3_4", СтрСоединить(МассивПункт3_4, Символы.ПС));
	
	СтоимостьГрузаГрузоотправителя = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяСтоимостьГруза");
	КодВалютыГрузоотправителя = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяКодВалюты"); 
	НаименованиеВалюты = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяНаименованиеВалюты");
	
	КодВалютыСтрокой = " " + НСтр("ru = 'RUB';
									|en = 'RUB'", "ru");
	
	Если КодВалютыГрузоотправителя <> "643" Тогда
		КодВалютыСтрокой = " " + СокрЛП(НаименованиеВалюты); 
	КонецЕсли;                    
	
	Результат.Вставить("Пункт3_5", Новый ФорматированнаяСтрока(Строка(СтоимостьГрузаГрузоотправителя), КодВалютыСтрокой)); 
	
	// Документы на груз
	МассивПункт4_1 = Новый Массив; 
	РеквизитыСторонСопроводительныхДокументов = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяРеквизитыСторонСопроводительныхДокументов");
	СопроводительныеДокументыГрузоотправителя = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяСопроводительныеДокументы", Новый Массив);
	
	Для Каждого СтрСопрДок Из СопроводительныеДокументыГрузоотправителя Цикл
		
		ПроверитьКлючиСтруктуры(СтрСопрДок, КлючиПроверкиДокументов);

		МассивСостСопрДок = Новый Массив;
		
		СтрокиСостСопрДок = ОбменСГИСЭПД.НайтиСтрокиВМассивеСтруктур(РеквизитыСторонСопроводительныхДокументов, 
		"ИдентификаторСтрокиРодителя", СтрСопрДок.ИдентификаторСтроки);
		
		Для Каждого СтрСостСопрДок Из СтрокиСостСопрДок Цикл
			
			Составитель = ОбменСГИСЭПД.ПредставлениеСтороныПодписавшейДокумент(СтрСостСопрДок);
			
			Если ЗначениеЗаполнено(Составитель) Тогда
				МассивСостСопрДок.Добавить(Составитель);			
			КонецЕсли;
			
		КонецЦикла;  
		
		Составители = "";
		Если МассивСостСопрДок.Количество() > 0 Тогда
			Составители = НСтр("ru = '; Составители:';
								|en = '; Составители:'", "ru") + " " + СтрСоединить(МассивСостСопрДок, ", ")	
		КонецЕсли;  
		
		НаименованиеИСоставители = СтрШаблон(НСтр("ru = '%1 №%2 от %3%4';
													|en = '%1 №%2 от %3%4'", "ru"), СтрСопрДок.НаименованиеДокумента, 
		СтрСопрДок.НомерДокумента, СтрСопрДок.ДатаДокумента, Составители);   
		
		МассивПункт4_1.Добавить(НаименованиеИСоставители); 
		
	КонецЦикла; 
	
	Результат.Вставить("Пункт4_1", СтрСоединить(МассивПункт4_1, Символы.ПС));
	
	МассивПункт4_2 = Новый Массив;
	
	РеквизитыСторонСертификатовУдостоверений = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяРеквизитыСторонСертификатовУдостоверений");
	СертификатыУдостоверенияГрузоотправителя = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяСертификатыУдостоверения", Новый Массив);
	
	Для Каждого СтрСерт Из СертификатыУдостоверенияГрузоотправителя Цикл
		
		ПроверитьКлючиСтруктуры(СтрСерт, КлючиПроверкиДокументов);
	
		МассивСостСерт = Новый Массив;
		СтрокиСостСерт = ОбменСГИСЭПД.НайтиСтрокиВМассивеСтруктур(РеквизитыСторонСертификатовУдостоверений,
			"ИдентификаторСтрокиРодителя", СтрСерт.ИдентификаторСтроки);
		
		Для Каждого СтрСостСерт Из СтрокиСостСерт Цикл
						
			Составитель = ОбменСГИСЭПД.ПредставлениеСтороныПодписавшейДокумент(СтрСостСерт);
			
			Если ЗначениеЗаполнено(Составитель) Тогда
				МассивСостСерт.Добавить(Составитель);			
			КонецЕсли;

		КонецЦикла;
		
		Составители = "";
		Если МассивСостСерт.Количество() > 0 Тогда
			Составители = НСтр("ru = '; Составители:';
								|en = '; Составители:'", "ru") + " " + СтрСоединить(МассивСостСерт, ", ")	
		КонецЕсли;
		
		НаименованиеИСоставители = СтрШаблон(НСтр("ru = '%1 №%2 от %3%4';
													|en = '%1 №%2 от %3%4'", "ru"), СтрСерт.НаименованиеДокумента, 
		СтрСерт.НомерДокумента, СтрСерт.ДатаДокумента, Составители);   
		
		МассивПункт4_2.Добавить(НаименованиеИСоставители); 
		
	КонецЦикла;
	
	Результат.Вставить("Пункт4_2", СтрСоединить(МассивПункт4_2, Символы.ПС)); 
	
	РеквизитыСторонДокументаОтгрузки = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяРеквизитыСторонДокументаПодтверждающегоОтгрузку"); 
	ДокументыПодтверждающиеОтгрузку = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяДокументыПодтверждающиеОтгрузку", Новый Массив);
	
	МассивПункт4_3 = Новый Массив;

	Для Каждого СтрПодтв Из ДокументыПодтверждающиеОтгрузку Цикл
			
		ПроверитьКлючиСтруктуры(СтрПодтв, КлючиПроверкиДокументов);
	
		МассивСостПодтв = Новый Массив;

		СтрокиСостПодтв = ОбменСГИСЭПД.НайтиСтрокиВМассивеСтруктур(РеквизитыСторонДокументаОтгрузки,
			"ИдентификаторСтрокиРодителя", СтрПодтв.ИдентификаторСтроки);  
		
		Для Каждого СтрСостПодтв Из СтрокиСостПодтв Цикл
			МассивСостПодтв.Добавить(ОбменСГИСЭПД.ПредставлениеСтороныПодписавшейДокумент(СтрСостПодтв));	
		КонецЦикла;
		
		Составители = "";
		
		Если МассивСостПодтв.Количество() > 0 Тогда
			Составители = НСтр("ru = '; Составители:';
								|en = '; Составители:'", "ru") + " " + СтрСоединить(МассивСостПодтв, ", ")	
		КонецЕсли;
		
		НаименованиеИСоставители = СтрШаблон(НСтр("ru = '%1 №%2 от %3%4';
													|en = '%1 №%2 от %3%4'", "ru"), СтрПодтв.НаименованиеДокумента, 
		СтрПодтв.НомерДокумента, СтрПодтв.ДатаДокумента, Составители);   
		
		МассивПункт4_3.Добавить(НаименованиеИСоставители); 
		
	КонецЦикла;
	
	Результат.Вставить("Пункт4_3", СтрСоединить(МассивПункт4_3, Символы.ПС));
	
	// Условия перевозки
	НаименованиеМаршрута = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяНаименованиеМаршрута", "");
	ДатаИВремяДоставкиСтрокой = "";
	ДатаИВремяДоставки = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяДатаИВремяДоставки");
	
	Если ЗначениеЗаполнено(ДатаИВремяДоставки) Тогда
		ДатаИВремяДоставкиСтрокой = " " + СтрШаблон(НСтр("ru = 'дата доставки: %1';
														|en = 'дата доставки: %1'", "ru"),
			Формат(ДатаИВремяДоставки, "ДФ='dd.MM.yyyy ЧЧ:мм';")); 
	КонецЕсли;  
	
	Пункт5_1 = НаименованиеМаршрута + ДатаИВремяДоставкиСтрокой;
	Результат.Вставить("Пункт5_1", Пункт5_1);
	
	ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт5_2", "ТитулГрузоотправителяУказанияВОтношенииВыполненияНормПеревозки");
	
	МассивТелефоныПереадресовщика = Новый Массив;
	НомераТелефоновЗаПереадресовку = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяОтветственныйЗаПереадресовкуНомераТелефонов", Новый Массив);	

	Для Каждого СтрТелефоны Из НомераТелефоновЗаПереадресовку Цикл
		МассивТелефоныПереадресовщика.Добавить(СтрТелефоны.Телефон);
	КонецЦикла;  
	
	КонтактыПереадресовщика = "";
	Если МассивТелефоныПереадресовщика.Количество() > 0 Тогда
		КонтактыПереадресовщика = " " + НСтр("ru = 'тел.:';
											|en = 'тел.:'", "ru") + " " + СтрСоединить(МассивТелефоныПереадресовщика, ", ");
	КонецЕсли;
	
	МассивЭлПочтыПереадресовщика = Новый Массив;
	АдресЭлектроннойПочты = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяОтветственныйЗаПереадресовкуАдресаЭлектроннойПочты", Новый Массив);
	Для Каждого СтрЭлПочта Из АдресЭлектроннойПочты Цикл
		МассивЭлПочтыПереадресовщика.Добавить(СтрЭлПочта.АдресЭлектроннойПочты);
	КонецЦикла; 
	Если МассивЭлПочтыПереадресовщика.Количество() > 0 Тогда
		Если Не ПустаяСтрока(КонтактыПереадресовщика) Тогда
			КонтактыПереадресовщика = КонтактыПереадресовщика + Символы.ПС;
		КонецЕсли;
		КонтактыПереадресовщика = КонтактыПереадресовщика + " " + НСтр("ru = 'эл. почта:';
																		|en = 'эл. почта:'", "ru") + " " 
			+ СтрСоединить(МассивЭлПочтыПереадресовщика, ", ");
	КонецЕсли;
	
	Лицо = НРег(ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяЛицоИмеющееПолномочияПереадресовки", ""));
	
	Результат.Вставить("Пункт5_3", КонтактыПереадресовщика + " (" + Лицо + ")");
	
	НижнийПределТемпературы = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяНижнийПределТемпературыПриПеревозке"); 
	ВерхнийПределТемпературы = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяВерхнийПределТемпературыПриПеревозке");
	
	ЗначенияПределов = Новый Массив;

	Если ЗначениеЗаполнено(НижнийПределТемпературы) Тогда
		ЗначенияПределов.Добавить(НСтр("ru = 'от';
										|en = 'от'", "ru") + " " + НижнийПределТемпературы);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВерхнийПределТемпературы) Тогда
		ЗначенияПределов.Добавить(НСтр("ru = 'до';
										|en = 'до'", "ru") + " " + ВерхнийПределТемпературы);
	КонецЕсли;  
	
	Пункт5_4 = СтрСоединить(ЗначенияПределов, " ");
	
	Результат.Вставить("Пункт5_4", Пункт5_4);
		
	// Перевозчик
	Результат.Вставить("Пункт6_1", ОбменСГИСЭПД.ПредставлениеУчастника(ДанныеЗаполнения, "ТитулГрузоотправителяПеревозчик"));
	
	// Водители
	МассивПункт6_2 = Новый Массив;  
	НомераТелефоновВодителей = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяВодительНомераТелефонов");
	ВодителиГрузоотправителя = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяВодители", Новый Массив);
	
	Для Каждого СтрВодитель Из ВодителиГрузоотправителя Цикл
		
		ПроверитьКлючиСтруктуры(СтрВодитель, КлючиПроверкиВодителей);

		МассивТелефоны = Новый Массив;
		СтрокиТелефоны = ОбменСГИСЭПД.НайтиСтрокиВМассивеСтруктур(НомераТелефоновВодителей, "ИдентификаторСтрокиРодителя",
			СтрВодитель.ИдентификаторСтроки);
		
		Для Каждого СтрТелефон Из СтрокиТелефоны Цикл
			МассивТелефоны.Добавить(СтрТелефон.Телефон);	
		КонецЦикла; 
		
		Телефоны = "";
		Если МассивТелефоны.Количество() > 0 Тогда
			Телефоны = НСтр("ru = ', тел.:';
							|en = ', тел.:'", "ru") + " " + СтрСоединить(МассивТелефоны, ", ")	
		КонецЕсли;  
		
		СерияНомерВУ = "";
		Если ЗначениеЗаполнено(СтрВодитель.НомерВУ) Тогда
			СерияНомерВУ = СтрШаблон(НСтр("ru = ', ВУ %1 %2  выдано %3';
											|en = ', ВУ %1 %2  выдано %3'", "ru"), СтрВодитель.СерияВУ, СтрВодитель.НомерВУ,
				Формат(СтрВодитель.ДатаВыдачиВУ, "ДЛФ=D")); 
		КонецЕсли; 
		
		ИННВодителя = "";
		Если ЗначениеЗаполнено(СтрВодитель.ФЛ_ИНН) Тогда
			ИННВодителя = НСтр("ru = ', ИНН:';
								|en = ', ИНН:'", "ru") + " " + СтрВодитель.ФЛ_ИНН;
		КонецЕсли; 
	 
	    ФИОВодителя = Новый Массив;
		ФИОВодителя.Добавить(СтрВодитель.Фамилия);
		ФИОВодителя.Добавить(СтрВодитель.Имя);
		ФИОВодителя.Добавить(СтрВодитель.Отчество); 
		
		ФИОВодителяСтрокой = СтрСоединить(ФИОВодителя, " ");
		
		ДанныеВодителя = Новый ФорматированнаяСтрока(ФИОВодителяСтрокой, ИННВодителя, СерияНомерВУ,	Телефоны); 
		
		МассивПункт6_2.Добавить(ДанныеВодителя); 
		
	КонецЦикла;   
	
	Результат.Вставить("Пункт6_2", СтрСоединить(МассивПункт6_2, Символы.ПС));
	
	// Транспортное средство
	МассивТС = Новый Массив;
	МассивРегНомеров = Новый Массив;  
	МассивТиповВладений = Новый Массив;
	
	ТипТС = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяТранспортноеСредствоТип", "");
	МаркаТС = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяТранспортноеСредствоМарка");
	ГрузоподъемностьТС = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяТранспортноеСредствоГрузоподъемность", "");
	ВместимостьТС = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяТранспортноеСредствоВместимость", "");
	
	ТипВладенияТС = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяТранспортноеСредствоТипВладения");
	МассивТиповВладений.Добавить(ТипВладенияТС);
	
	ШаблонТС = НСтр("ru = '%1 %2, грузоподъемность %3 тонн, объем кузова %4 м3';
					|en = '%1 %2, грузоподъемность %3 тонн, объем кузова %4 м3'", "ru");
	СтрокаТС = СтрШаблон(ШаблонТС, ТипТС, МаркаТС, ГрузоподъемностьТС, ВместимостьТС); 
	
	МассивТС.Добавить(СтрокаТС);
	
	МассивРегНомеров.Добавить(ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяТранспортноеСредствоРегистрационныйНомер"));
	
	ПрицепыГрузоотправителя = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяПрицепы", Новый Массив); 
	
	ШаблонПрицепа = НСтр("ru = '%1 %2, грузоподъемность %3 тонн, вместимость %4 м3';
						|en = '%1 %2, грузоподъемность %3 тонн, вместимость %4 м3'", "ru");
	
	Для Каждого СтрПрицеп Из ПрицепыГрузоотправителя Цикл
		
		СвойстваПрицепа = СтрШаблон(ШаблонПрицепа,СтрПрицеп.Тип, СтрПрицеп.Марка, СтрПрицеп.Грузоподъемность, СтрПрицеп.Вместимость); 
		
		МассивТС.Добавить(СвойстваПрицепа);
		МассивРегНомеров.Добавить(СтрПрицеп.РегистрационныйНомер);
		Если ЗначениеЗаполнено(СтрПрицеп.ТипВладения) 
			И МассивТиповВладений.Найти(СтрПрицеп.ТипВладения) = Неопределено Тогда
			МассивТиповВладений.Добавить(СтрПрицеп.ТипВладения);
		КонецЕсли;
		
	КонецЦикла;				
	
	Результат.Вставить("Пункт7_1", СтрСоединить(МассивТС, Символы.ПС));			
	Результат.Вставить("Пункт7_2", СтрСоединить(МассивРегНомеров, Символы.ПС));
	Результат.Вставить("Пункт7_3", СтрСоединить(МассивТиповВладений, ", "));
	
	МассивОснВладТС = Новый Массив;
	ОснованияВладенияТС = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяОснованияВладенияТС",
		Новый Массив);
	
	Для Каждого СтрОснВладТС Из ОснованияВладенияТС Цикл
		
		ПроверитьКлючиСтруктуры(СтрОснВладТС, КлючиПроверкиДокументов);
		
		ТСДатаДокумента = Формат(СтрОснВладТС.ДатаДокумента, "ДЛФ=D");
		ОснованиеВладения = ПредставлениеНаименования(СтрОснВладТС.НаименованиеДокумента, СтрОснВладТС.НомерДокумента, ТСДатаДокумента);

		МассивОснВладТС.Добавить(ОснованиеВладения);							  
		
	КонецЦикла;
	
	ОснованияВладенияПрицепом = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяОснованияВладенияПрицепом", Новый Массив); 
	
	Для Каждого СтрОснВладПрицеп Из ОснованияВладенияПрицепом Цикл
		
		ПроверитьКлючиСтруктуры(СтрОснВладПрицеп, КлючиПроверкиДокументов);
		
		ПрицепДатаДокумента = Формат(СтрОснВладПрицеп.ДатаДокумента, "ДЛФ=D");
		
		ОснованиеВладенияПрицепом = ПредставлениеНаименования(СтрОснВладПрицеп.НаименованиеДокумента,
			СтрОснВладПрицеп.НомерДокумента, ПрицепДатаДокумента);

		МассивОснВладТС.Добавить(ОснованиеВладенияПрицепом);							

	КонецЦикла;
	
	Результат.Вставить("Пункт7_4", СтрСоединить(МассивОснВладТС, Символы.ПС));
	
	МассивСпецУсл = Новый Массив;
	
	СпециальныеУсловияДвижения = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяСпециальныеУсловияДвижения", Новый Массив); 
	
	ШаблонСпецУсловий = НСтр("ru = 'Специальное разрешение №%1 от %2, срок действия %3, маршрут: %4';
							|en = 'Специальное разрешение №%1 от %2, срок действия %3, маршрут: %4'", "ru");
	
	Для Каждого СтрСпецУсл Из СпециальныеУсловияДвижения Цикл
		
		ПроверитьКлючиСтруктуры(СтрСпецУсл, КлючиПроверкиУсловийДвижения);
		ДатаРазрешения = Формат(СтрСпецУсл.СпециальноеРазрешенияДата, "ДЛФ=D");
		СрокДействияРазрешения = Формат(СтрСпецУсл.СпециальноеРазрешенияСрокДействия, "ДЛФ=D");
		
		УсловиеДвижения = СтрШаблон(ШаблонСпецУсловий,СтрСпецУсл.СпециальноеРазрешенияНомер, ДатаРазрешения,
			СрокДействияРазрешения, СтрСпецУсл.МаршрутДвижения); 

		МассивСпецУсл.Добавить(УсловиеДвижения);  
		
	КонецЦикла;
	
	Результат.Вставить("Пункт7_5", СтрСоединить(МассивСпецУсл, Символы.ПС));
	
	// Прием груза  
	ПогрузкаОтправителем = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяЛицоОсуществляющееПогрузкуЯвляетсяГрузоотправителем", "1"); 
	ТитулПогрузчика = ?(ПогрузкаОтправителем = "1", "ТитулГрузоотправителяГрузоотправитель",
		"ТитулГрузоотправителяЛицоОсуществляющееПогрузку"); 
	Погрузчик = ОбменСГИСЭПД.ПредставлениеУчастника(ДанныеЗаполнения, ТитулПогрузчика);
	
	Результат.Вставить("Пункт8_0", Погрузчик);

	ВладелецПунктаОтправитель = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
		"ТитулГрузоотправителяВладелецПунктаПогрузкиЯвляетсяГрузоотправителем", "1");
	ТитулВладельцаПункта = ?(ВладелецПунктаОтправитель = "1", "ТитулГрузоотправителяГрузоотправитель",
		"ТитулГрузоотправителяВладелецПунктаПогрузки"); 
	ВладельцаПункта = ОбменСГИСЭПД.ПредставлениеУчастника(ДанныеЗаполнения, ТитулВладельцаПункта);
	
	Результат.Вставить("Пункт8_1", ВладельцаПункта);
	
	Пункт8_2 = ОбменСГИСЭПД.ПредставлениеАдреса(ДанныеЗаполнения, "ТитулГрузоотправителяАдресПогрузки");
	Результат.Вставить("Пункт8_2", Пункт8_2);
	
	ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт8_3", "ТитулГрузоотправителяЗаявленныеДатаВремяПодачи", , ФорматДаты);
	ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт8_4", "ТитулГрузоотправителяФактическиеДатаВремяПодачи", , ФорматДаты);
	ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт8_41", "ТитулГрузоотправителяФактическиеДатаВремяУбытия", , ФорматДаты);
	
	МассаГрузаБрутто = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяМассаБруттоГруза", "");
	
	Пункт8_5 = МассаГрузаБрутто	+ " " + СтрШаблон(НСтр("ru = 'кг (%1)';
															|en = 'кг (%1)'", "ru"), МетодОпределенияМассы);
	Результат.Вставить("Пункт8_5", Пункт8_5); 
	
	ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт8_51", "ТитулГрузоотправителяКоличествоГрузовыхМест");
	
	МассивПункт8_6 = Новый Массив;  
	
	ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт8_6, "ТитулПеревозчикаПриемкаЗамечанияДатаВремяПрибытия",
		НСтр("ru = 'дата/время подачи:';
			|en = 'дата/время подачи:'", "ru") + " ", ФорматДаты);
	ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт8_6, "ТитулПеревозчикаПриемкаЗамечанияДатаВремяУбытия",
		НСтр("ru = 'дата/время убытия:';
			|en = 'дата/время убытия:'", "ru") + " ", ФорматДаты);
	ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт8_6, "ТитулПеревозчикаПриемкаЗамечанияДатаВремяПостановкиНаПогрузку",
		НСтр("ru = 'дата/время постановки на погрузку';
			|en = 'дата/время постановки на погрузку'", "ru"), ФорматДаты);
	ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт8_6, "ТитулПеревозчикаПриемкаЗамечанияВОтношенииФактическогоСостояниеГруза");
	ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт8_6, "ТитулПеревозчикаПриемкаЗамечанияВОтношенииМассыГруза");
	ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт8_6, "ТитулПеревозчикаПриемкаЗамечанияВОтношенииКоличестваГрузовыхМест");
	ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт8_6, "ТитулПеревозчикаПриемкаЗамечанияВОтношенииПроведенияПогрузочныхРабот");
	
	Результат.Вставить("Пункт8_6", СтрСоединить(МассивПункт8_6, Символы.ПС)); 
	
	// Переадресовка  
	ИДФайлПереадресовки = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПереадресовкаИдентификаторФайла");
	
	Если ЗначениеЗаполнено(ИДФайлПереадресовки) Тогда
		
		ШаблонПереадресовки = НСтр("ru = '%1 (%2 переадресовка, %3)';
									|en = '%1 (%2 переадресовка, %3)'", "ru");
		ДатаПереадресовки = Формат(ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПереадресовкаДатаВремя"),
			ФорматДаты);
		ФормаПереадресовки = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПереадресовкаФормаПереадресовки");
		ВидДоставкиДокумента = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
			"ТитулПереадресовкаВидДоставкиДокументаОПереадресовке");
		
		Пункт9_1 = СтрШаблон(ШаблонПереадресовки, ДатаПереадресовки, ФормаПереадресовки, ВидДоставкиДокумента); 
		
		Результат.Вставить("Пункт9_1", Пункт9_1);
		
		ВремяПодачиПодВыгрузку = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
			"ТитулПереадресовкаНовыеДатаВремяПодачиПодВыгрузку");
		ВремяВыгрузкиСтрокой = "";
		
		Если ЗначениеЗаполнено(ВремяПодачиПодВыгрузку) Тогда
			ВремяВыгрузкиСтрокой = НСтр("ru = ', дата/время подачи под выгрузку:';
										|en = ', дата/время подачи под выгрузку:'", "ru") + " "
				 + Формат(ВремяПодачиПодВыгрузку, ФорматДаты); 
		КонецЕсли; 
		
		ПереадресовкаАдресВыгрузки = ОбменСГИСЭПД.ПредставлениеАдреса(ДанныеЗаполнения, "ТитулПереадресовкаАдресВыгрузки");
		
		Пункт9_2 = Новый ФорматированнаяСтрока(ПереадресовкаАдресВыгрузки, ВремяВыгрузкиСтрокой);
		Результат.Вставить("Пункт9_2", Пункт9_2);
		
		Пункт9_4 = ОбменСГИСЭПД.ПредставлениеУчастника(ДанныеЗаполнения, "ТитулПереадресовкаНовыйГрузополучатель");
		Результат.Вставить("Пункт9_4", Пункт9_4);
		
	КонецЕсли;
	
	// Выдача груза
	ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт10_1", "ТитулГрузополучателяАдресВыгрузки");
	
	ИДФайлГрузополучателя = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузополучателяИдентификаторФайла");
	Если ЗначениеЗаполнено(ИДФайлГрузополучателя) Тогда
		
		ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт10_2", "ТитулГрузополучателяЗаявленныеДатаВремяПрибытия", , ФорматДаты);
		ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт10_3", "ТитулГрузополучателяФактическиеДатаВремяПрибытия", , ФорматДаты);
		ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт10_31", "ТитулГрузополучателяФактическиеДатаВремяУбытия", , ФорматДаты);
		ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт10_4", "ТитулГрузополучателяФактическоеСостояниеГруза", , ФорматДаты);
		ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт10_5", "ТитулГрузополучателяОбщееКоличествоПринятыхГрузовыхМест", , ФорматДаты);
		
		МассаНеттоГрузаСтрокой = "";
		МассаНеттоПринятогоГруза = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
			"ТитулГрузополучателяМассаНеттоПринятогоГруза");
		Если ЗначениеЗаполнено(МассаНеттоПринятогоГруза) Тогда
			МассаНеттоГрузаСтрокой = " " + СтрШаблон(НСтр("ru = '/ %1  кг (нетто)';
															|en = '/ %1  кг (нетто)'", "ru"), Строка(МассаНеттоПринятогоГруза)); 
		КонецЕсли;
		
		МассаБруттоПринятогоГруза = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
			"ТитулГрузополучателяМассаБруттоПринятогоГруза", "");
		МассаБруттоПринятогоГрузаСтрокой = СтрШаблон(НСтр("ru = '%1 кг (брутто)';
															|en = '%1 кг (брутто)'", "ru"), МассаБруттоПринятогоГруза);
		
		Пункт10_6 = МассаБруттоПринятогоГрузаСтрокой + МассаНеттоГрузаСтрокой; 
		
		Результат.Вставить("Пункт10_6", Пункт10_6); 
		
	КонецЕсли;
	
	ИДФайлаВыдачи = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаВыдачаИдентификаторФайла");
	
	Если ЗначениеЗаполнено(ИДФайлаВыдачи) Тогда
		
		МассивПункт10_7 = Новый Массив;
		
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт10_7, "ТитулПеревозчикаВыдачаДатаВремяПрибытия",
			НСтр("ru = 'дата/время прибытия:';
				|en = 'дата/время прибытия:'", "ru") + " ");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт10_7, "ТитулПеревозчикаВыдачаДатаИВремяПостановкиНаВыгрузку",
			НСтр("ru = 'дата/время постановки на выгрузку:';
				|en = 'дата/время постановки на выгрузку:'", "ru") + " ");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт10_7, "ТитулПеревозчикаВыдачаДатаВремяУбытия",
			НСтр("ru = 'дата/время убытия:';
				|en = 'дата/время убытия:'", "ru") + " ");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт10_7, "ТитулПеревозчикаВыдачаЗамечанияВОтношенииФактическогоСостоянияГруза");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт10_7, "ТитулПеревозчикаВыдачаЗамечанияВОтношенииПроведенияРаботПоВыгрузке");
		
		Результат.Вставить("Пункт10_7", СтрСоединить(МассивПункт10_7, Символы.ПС));
		
	КонецЕсли;
	
	// Стоимость 
	ИДФайлаФХЖПеревозчика = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулПеревозчикаФХЖИдентификаторФайла");
	
	Если ЗначениеЗаполнено(ИДФайлаФХЖПеревозчика) Тогда
		
		ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт12_1", "ТитулПеревозчикаФХЖСтоимостьПеревозкиБезНалога");
		ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт12_2", "ТитулПеревозчикаФХЖНалоговаяСтавка");
		ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт12_3", "ТитулПеревозчикаФХЖСуммаНДС");
		ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт12_4", "ТитулПеревозчикаФХЖСтоимостьПеревозкиСНалогом");
		ЗначениеВРезультат(ДанныеЗаполнения, Результат, "Пункт12_5", "ТитулПеревозчикаФХЖПорядокМеханизмРасчетаПлаты");
		
		МассивПункт12_6 = Новый Массив;
		
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт12_6, "ТитулПеревозчикаФХЖПеревозчикОКПО",
			НСтр("ru = 'ОКПО:';
				|en = 'ОКПО:'", "ru") + " ");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт12_6, "ТитулПеревозчикаФХЖПеревозчикСтруктурноеПодразделение",
			НСтр("ru = 'структурное подразделение:';
				|en = 'структурное подразделение:'", "ru") + " ");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт12_6, "ТитулПеревозчикаФХЖПеревозчикКраткоеНаименование",
			НСтр("ru = 'краткое наименование:';
				|en = 'краткое наименование:'", "ru") + " ");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт12_6, "ТитулПеревозчикаФХЖБанковскиеРеквизитыНомерБанковскогоСчета",
			НСтр("ru = 'р/с:';
				|en = 'р/с:'", "ru") + " ");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт12_6, "ТитулПеревозчикаФХЖБанковскиеРеквизитыНаименованиеБанка",
			НСтр("ru = 'банк:';
				|en = 'банк:'", "ru") + " ");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт12_6, "ТитулПеревозчикаФХЖБанковскиеРеквизитыБИК",
			НСтр("ru = 'БИК:';
				|en = 'БИК:'", "ru") + " ");
		
		Результат.Вставить("Пункт12_6", СтрСоединить(МассивПункт12_6, ", ")); 
		
	КонецЕсли;
	
	ИДФайлаФХЖГрузоотправителя = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, "ТитулГрузоотправителяФХЖИдентификаторФайла");
	
	Если ЗначениеЗаполнено(ИДФайлаФХЖГрузоотправителя) Тогда
		
		МассивПункт12_7 = Новый Массив;
		
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт12_7, "ТитулГрузоотправителяФХЖГрузоотправительОКПО",
			НСтр("ru = 'ОКПО:';
				|en = 'ОКПО:'", "ru") + " ");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт12_7, "ТитулГрузоотправителяФХЖГрузоотправительСтруктурноеПодразделение",
			НСтр("ru = 'структурное подразделение:';
				|en = 'структурное подразделение:'", "ru") + " ");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт12_7, "ТитулГрузоотправителяФХЖГрузоотправительКраткоеНаименование",
			НСтр("ru = 'краткое наименование:';
				|en = 'краткое наименование:'", "ru") + " ");
		
		Результат.Вставить("Пункт12_7", СтрСоединить(МассивПункт12_7, ", "));
		
		ОснованиеПлатежаНаименование = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
			"ТитулГрузоотправителяФХЖОснованиеПлатежаНаименование");
		
		Если ЗначениеЗаполнено(ОснованиеПлатежаНаименование) Тогда
			
			ОснованиеПлатежаНомер = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
				"ТитулГрузоотправителяФХЖОснованиеПлатежаНомер");
			ОснованиеПлатежаДата = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения,
				"ТитулГрузоотправителяФХЖОснованиеПлатежаДата");
			ОснованиеПлатежаСтрокой = ПредставлениеНаименования(ОснованиеПлатежаНаименование,
				ОснованиеПлатежаНомер, ОснованиеПлатежаДата); 
			
			Результат.Вставить("Пункт12_9", ОснованиеПлатежаСтрокой);
			
		КонецЕсли;  
		
		МассивПункт12_10 = Новый Массив;
		
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт12_10, "ТитулГрузоотправителяФХЖНаименованиеПлательщика",
			НСтр("ru = 'плательщик:';
				|en = 'плательщик:'", "ru") + " ");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт12_10, "ТитулГрузоотправителяФХЖБанковскиеРеквизитыНомерБанковскогоСчета",
			НСтр("ru = 'р/с:';
				|en = 'р/с:'", "ru") + " ");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт12_10, "ТитулГрузоотправителяФХЖБанковскиеРеквизитыНаименованиеБанка",
			НСтр("ru = 'банк:';
				|en = 'банк:'", "ru") + " ");
		ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивПункт12_10, "ТитулГрузоотправителяФХЖБанковскиеРеквизитыБИК",
			НСтр("ru = 'БИК:';
				|en = 'БИК:'", "ru") + " ");
		
		Результат.Вставить("Пункт12_10", СтрСоединить(МассивПункт12_10, ", "));
		
	КонецЕсли;  

	// Подписи 
	ПодписьВРезультат(ДанныеЗаполнения, Результат, "Пункт8_7", "ТитулГрузоотправителяИдентификаторФайла",
		"ТитулГрузоотправителяПодписант");
	ПодписьВРезультат(ДанныеЗаполнения, Результат, "Пункт8_8", "ТитулПеревозчикаПриемкаИдентификаторФайла",
		"ТитулПеревозчикаПриемкаПодписант");
	ПодписьВРезультат(ДанныеЗаполнения, Результат, "Пункт10_8", "ТитулГрузополучателяИдентификаторФайла",
		"ТитулГрузополучателяПодписант");
	ПодписьВРезультат(ДанныеЗаполнения, Результат, "Пункт10_9", "ТитулПеревозчикаВыдачаИдентификаторФайла",
		"ТитулПеревозчикаВыдачаПодписант");
	
	Если ЗначениеЗаполнено(ИДФайлаФХЖПеревозчика) Тогда
		
		МассивПункт12_11 = Новый Массив;
		ПодписиВМассив(ДанныеЗаполнения, МассивПункт12_11, "ТитулПеревозчикаФХЖПодписанты");
		Результат.Вставить("Пункт12_11", СтрСоединить(МассивПункт12_11, Символы.ПС));
		
	КонецЕсли;  
	
	Если ЗначениеЗаполнено(ИДФайлаФХЖГрузоотправителя) Тогда
		
		МассивПункт12_12 = Новый Массив;
		ПодписиВМассив(ДанныеЗаполнения, МассивПункт12_12, "ТитулГрузоотправителяФХЖПодписанты");
		Результат.Вставить("Пункт12_12", СтрСоединить(МассивПункт12_12, Символы.ПС));
		
	КонецЕсли; 
	
	Возврат Результат;
	
КонецФункции

Процедура ЗначениеДобавитьВМассив(ДанныеЗаполнения, МассивДляЗаполнения, ИмяКлюча, ТекстЗначения = "", ФорматЗначения = Неопределено)
	
	ЗначениеПоиска = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ИмяКлюча);
	
	Если Не ЗначениеЗаполнено(ЗначениеПоиска) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФорматЗначения) Тогда
		ЗначениеПоиска = Формат(ЗначениеПоиска, ФорматЗначения);	
	КонецЕсли;
	
	ЗначениеПоискаСтрокой = ТекстЗначения + ЗначениеПоиска;
	МассивДляЗаполнения.Добавить(ЗначениеПоискаСтрокой);	
	
КонецПроцедуры 

Процедура ЗначениеВРезультат(ДанныеЗаполнения, Результат, ИмяКлючаРезультата, ИмяКлюча,
		ЗначениеПоУмолчанию = Неопределено, ФорматЗначения = Неопределено)
	
	ЗначениеПоиска = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, ИмяКлюча, ЗначениеПоУмолчанию);
	Если ЗначениеЗаполнено(ФорматЗначения) Тогда
		ЗначениеПоиска = Формат(ЗначениеПоиска, ФорматЗначения);	
	КонецЕсли;
	Результат.Вставить(ИмяКлючаРезультата, ЗначениеПоиска); 
	
КонецПроцедуры

Процедура ПодписьВРезультат(ДанныеЗаполнения, Результат, ИмяКлючаРезультата, КлючИДФайла, КлючПодписи)
	
	ИДФайл = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, КлючИДФайла);
	Если Не ЗначениеЗаполнено(ИДФайл) Тогда
		Возврат;
	КонецЕсли;	
	
	Подпись = ОбменСГИСЭПД.ПредставлениеПодписи(ДанныеЗаполнения, КлючПодписи);	
	Если ЗначениеЗаполнено(Подпись) Тогда
		Результат.Вставить(ИмяКлючаРезультата, Подпись);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодписиВМассив(ДанныеЗаполнения, МассивДляЗаполнения, КлючПодписей)
	
	Подписанты = ОбменСГИСЭПД.ПолучитьЗначениеСтруктуры(ДанныеЗаполнения, КлючПодписей, Новый Массив);
	
	Для каждого СтрокаМассива Из Подписанты Цикл
		Подпись = ОбменСГИСЭПД.ПредставлениеПодписи(СтрокаМассива, "");	   	
		Если ЗначениеЗаполнено(Подпись) Тогда
			МассивДляЗаполнения.Добавить(Подпись);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьКлючиСтруктуры(СтруктураПроверки, КлючиПроверки)

	Для каждого КлючПроверки Из КлючиПроверки Цикл
	   Если СтруктураПроверки.Свойство(КлючПроверки.Ключ) Тогда
	      Продолжить;	   
	   КонецЕсли;
	СтруктураПроверки.Вставить(КлючПроверки.Ключ, КлючПроверки.Значение)	
	
	КонецЦикла;

КонецПроцедуры 

Функция НеобязательныеСвойстваТаблицы(ИмяТаблицы)
	
	Результат = Новый Соответствие;
	
	Если ИмяТаблицы = "ТитулГрузоотправителяГрузы" Тогда
		Результат.Вставить("Высота", 0);
		Результат.Вставить("Ширина", 0);
		Результат.Вставить("Длина", 0);
		Результат.Вставить("МассаНетто", 0);
		Результат.Вставить("ОбъемГруза", 0);
		Результат.Вставить("Плотность", 0);
		
	ИначеЕсли ИмяТаблицы = "ДОПОГ" Тогда
		Результат.Вставить("НомерООН", "-");   
		Результат.Вставить("Класс", "-"); 	
		Результат.Вставить("Описание", "-"); 

	ИначеЕсли ИмяТаблицы = "Документы" Тогда
		Результат.Вставить("НаименованиеДокумента", "-"); 
		Результат.Вставить("ДатаДокумента", "-"); 
		Результат.Вставить("НомерДокумента", "-"); 

	ИначеЕсли ИмяТаблицы = "Водители" Тогда
		Результат.Вставить("СерияВУ", "-"); 
		Результат.Вставить("НомерВУ", "-"); 
		Результат.Вставить("ДатаВыдачиВУ", "-");
		Результат.Вставить("Фамилия", "");
		Результат.Вставить("Имя", "");
		Результат.Вставить("Отчество", "");
		Результат.Вставить("ФЛ_ИНН", "-");

	ИначеЕсли ИмяТаблицы = "СпециальныеУсловияДвижения" Тогда
		Результат.Вставить("СпециальноеРазрешенияНомер", "-"); 
		Результат.Вставить("СпециальноеРазрешенияДата", "-"); 
		Результат.Вставить("СпециальноеРазрешенияСрокДействия", "-"); 
		Результат.Вставить("МаршрутДвижения", "-"); 

	КонецЕсли;                                 
	
	Возврат Результат;
	
КонецФункции

Функция ПредставлениеНаименования(НаименованиеДокумента, ДатаДокумента, НомерДокумента)
	
	Результат = СтрШаблон(НСтр("ru = '%1 №%2 от %3';
								|en = '%1 №%2 от %3'", "ru"), НаименованиеДокумента, ДатаДокумента, НомерДокумента);
	Возврат Результат;

КонецФункции

#КонецОбласти

#КонецЕсли