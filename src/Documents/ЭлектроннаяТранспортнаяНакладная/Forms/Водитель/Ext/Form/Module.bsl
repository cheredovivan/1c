
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	                                                           
	ОбменСГИСЭПД.ПриСозданииНаСервереПодчиненнойФормы(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	ОбменСГИСЭПД.СоздатьЭлементыВводаЗначенийСписка(ЭтотОбъект, "ВодительНомераТелефонов", "Телефон");
	
	ОформитьПоляПриСозданииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбменСГИСЭПДКлиент.ПриОткрытииПодчиненнойФормы(ЭтотОбъект);
																		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВодительНомераТелефонов

&НаКлиенте
Процедура ТитулГрузоотправителяВодительНомераТелефоновПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОбменСГИСЭПДКлиент.ОчиститьПодчиненныеТаблицы(ЭтотОбъект, Элемент.Имя, ТекущиеДанные.ИдентификаторСтроки, Отказ);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТитулГрузоотправителяВодительНомераТелефоновПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	ОбменСГИСЭПДКлиент.ТаблицаПриНачалеРедактирования(Элемент, ЭтотОбъект, НоваяСтрока, Копирование);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Сохранить(Команда)
		
	ОбменСГИСЭПДКлиент.СохранитьПараметрыПодчиненнойФормы(ЭтотОбъект);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПодключаемыеМетоды

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ДобавлениеПоляВвода(Команда)
	
	ИмяТаблицыИПоля = СтрЗаменить(Команда.Имя, "ДобавлениеПоляВвода", "");
	МассивЧастей = ОбменСГИСЭПДКлиентСервер.РазделитьСтрокуСоСложнымРазделителем(ИмяТаблицыИПоля, "__");
	
	ДобавлениеПоляВводаСервер(МассивЧастей[0], МассивЧастей[1]);
	
КонецПроцедуры

&НаСервере
Процедура ДобавлениеПоляВводаСервер(ИмяТаблицы, ИмяПоля)
	
	ОбменСГИСЭПД.СоздатьЭлементыВводаЗначенийСписка(ЭтотОбъект, ИмяТаблицы, ИмяПоля, Истина);
	
КонецПроцедуры

//@skip-check module-unused-method
&НаКлиенте
Процедура Подключаемый_ПолеЗначенияСпискаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	МассивЧастей = ОбменСГИСЭПДКлиентСервер.РазделитьСтрокуСоСложнымРазделителем(Элемент.Имя, "__");	
	УстановитьФорматированноеЗначениеПоляВвода(МассивЧастей[0], МассивЧастей[1], Число(МассивЧастей[2]), Текст);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФорматированноеЗначениеПоляВвода(ИмяТаблицы, ИмяПоля, НомерСтроки, Текст)
	
	ТекущиеДанные = ЭтотОбъект[ИмяТаблицы][НомерСтроки];	 
	ОбменСГИСЭПД.УстановитьФорматированноеЗначениеПоляВвода(ЭтотОбъект, ТекущиеДанные, ИмяПоля, Текст);
	
КонецПроцедуры

#КонецОбласти

#Область ОформлениеОбязательныхРеквизитов

&НаСервере
Процедура ОформитьПоляПриСозданииНаСервере()
	
	ДобавитьГруппуИ = Истина;
	ДобавитьГруппуИли = Ложь;
	// Фиксированная длина у ИНН
	ПустойИНН = "            ";
	
	// ( НомерВУ Не заполнено ИЛИ СерияВУ Не заполнено ИЛИ ДатаВыдачиВУ Не заполнено ) И ФЛ_ИНН Равно ПустойИНН
	ПутьКЭлементуФормы = "ФЛ_ИНН";
	ОтметкаНезаполненного = Истина;
	ЭлементОформления = ДобавитьЭлементОформления(ОтметкаНезаполненного, ПутьКЭлементуФормы);
	ГруппаОтбораОбщая = НоваяГруппаОтбораУсловногоОформления(ЭлементОформления.Отбор, ДобавитьГруппуИ);
	ГруппаОтбораПоВУ = НоваяГруппаОтбораУсловногоОформления(ГруппаОтбораОбщая, ДобавитьГруппуИли);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораПоВУ, "НомерВУ", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораПоВУ, "СерияВУ", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораПоВУ, "ДатаВыдачиВУ",ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "ФЛ_ИНН", ВидСравненияКомпоновкиДанных.Равно, ПустойИНН);
	
	// ( НомерВУ Заполнено И СерияВУ Заполнено И ДатаВыдачиВУ Заполнено ) ИЛИ ФЛ_ИНН Не равно ПустойИНН
	ПутьКЭлементуФормы = "ФЛ_ИНН";
	ОтметкаНезаполненного = Ложь;
	ЭлементОформления = ДобавитьЭлементОформления(ОтметкаНезаполненного, ПутьКЭлементуФормы);
	ГруппаОтбораОбщая = НоваяГруппаОтбораУсловногоОформления(ЭлементОформления.Отбор, ДобавитьГруппуИли);
	ГруппаОтбораПоВУ = НоваяГруппаОтбораУсловногоОформления(ГруппаОтбораОбщая, ДобавитьГруппуИ);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораПоВУ, "НомерВУ", ВидСравненияКомпоновкиДанных.Заполнено);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораПоВУ, "СерияВУ", ВидСравненияКомпоновкиДанных.Заполнено);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораПоВУ, "ДатаВыдачиВУ", ВидСравненияКомпоновкиДанных.Заполнено);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "ФЛ_ИНН", ВидСравненияКомпоновкиДанных.НеРавно, ПустойИНН);
	
	// И ФЛ_ИНН Равно ПустойИНН И СерияВУ Не заполнено
	ПутьКЭлементуФормы = "СерияВУ";
	ОтметкаНезаполненного = Истина;
	ЭлементОформления = ДобавитьЭлементОформления(ОтметкаНезаполненного, ПутьКЭлементуФормы);
	ГруппаОтбораОбщая = НоваяГруппаОтбораУсловногоОформления(ЭлементОформления.Отбор, ДобавитьГруппуИ);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "ФЛ_ИНН", ВидСравненияКомпоновкиДанных.Равно, ПустойИНН);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "СерияВУ", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	// ФЛ_ИНН Не равно ПустойИНН ИЛИ СерияВУ Заполнено
	ПутьКЭлементуФормы = "СерияВУ";
	ОтметкаНезаполненного = Ложь;
	ЭлементОформления = ДобавитьЭлементОформления(ОтметкаНезаполненного, ПутьКЭлементуФормы);
	ГруппаОтбораОбщая = НоваяГруппаОтбораУсловногоОформления(ЭлементОформления.Отбор, ДобавитьГруппуИли);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "ФЛ_ИНН", ВидСравненияКомпоновкиДанных.НеРавно, ПустойИНН);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "СерияВУ", ВидСравненияКомпоновкиДанных.Заполнено);

	// И ФЛ_ИНН Равно ПустойИНН И НомерВУ Не заполнено
	ПутьКЭлементуФормы = "НомерВУ";
	ОтметкаНезаполненного = Истина;
	ЭлементОформления = ДобавитьЭлементОформления(ОтметкаНезаполненного, ПутьКЭлементуФормы);
	ГруппаОтбораОбщая = НоваяГруппаОтбораУсловногоОформления(ЭлементОформления.Отбор, ДобавитьГруппуИ);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "ФЛ_ИНН", ВидСравненияКомпоновкиДанных.Равно, ПустойИНН);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "НомерВУ", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	// ( ФЛ_ИНН Не равно ПустойИНН ИЛИ НомерВУ Заполнено )
	ПутьКЭлементуФормы = "НомерВУ";
	ОтметкаНезаполненного = Ложь;
	ЭлементОформления = ДобавитьЭлементОформления(ОтметкаНезаполненного, ПутьКЭлементуФормы);
	ГруппаОтбораОбщая = НоваяГруппаОтбораУсловногоОформления(ЭлементОформления.Отбор, ДобавитьГруппуИли);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "ФЛ_ИНН", ВидСравненияКомпоновкиДанных.НеРавно, ПустойИНН);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "НомерВУ", ВидСравненияКомпоновкиДанных.Заполнено);
	
	// И ФЛ_ИНН Равно ПустойИНН И ДатаВыдачиВУ Не заполнено
	ПутьКЭлементуФормы = "ДатаВыдачиВУ";
	ОтметкаНезаполненного = Истина;
	ЭлементОформления = ДобавитьЭлементОформления(ОтметкаНезаполненного, ПутьКЭлементуФормы);
	ГруппаОтбораОбщая = НоваяГруппаОтбораУсловногоОформления(ЭлементОформления.Отбор, ДобавитьГруппуИ);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "ФЛ_ИНН", ВидСравненияКомпоновкиДанных.Равно, ПустойИНН);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "ДатаВыдачиВУ", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	// ( ФЛ_ИНН Не равно ПустойИНН ИЛИ ДатаВыдачиВУ Заполнено )
	ПутьКЭлементуФормы = "ДатаВыдачиВУ";
	ОтметкаНезаполненного = Ложь;
	ЭлементОформления = ДобавитьЭлементОформления(ОтметкаНезаполненного, ПутьКЭлементуФормы);
	ГруппаОтбораОбщая = НоваяГруппаОтбораУсловногоОформления(ЭлементОформления.Отбор, ДобавитьГруппуИли);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "ФЛ_ИНН", ВидСравненияКомпоновкиДанных.НеРавно, ПустойИНН);
	ДобавитьЭлементОтбораУсловногоОформления(ГруппаОтбораОбщая, "ДатаВыдачиВУ", ВидСравненияКомпоновкиДанных.Заполнено);
	
КонецПроцедуры

&НаСервере
Функция ДобавитьЭлементОформления(ОтметкаНезаполненного, ПутьКЭлементуФормы)
	
	НовыйЭлементОформления = УсловноеОформление.Элементы.Добавить();	
	НовыйЭлементОформления.Оформление.УстановитьЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ОтметкаНезаполненного"),
		ОтметкаНезаполненного);
	
	ОформляемоеПолеБезОтметки = НовыйЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПолеБезОтметки.Поле = Новый ПолеКомпоновкиДанных(ПутьКЭлементуФормы);
	ОформляемоеПолеБезОтметки.Использование = Истина;
	
	Возврат НовыйЭлементОформления;
	
КонецФункции

&НаСервере
Функция НоваяГруппаОтбораУсловногоОформления(Отбор, ДобавитьГруппуИ)
	
	ГруппаОтбора = Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	Если ДобавитьГруппуИ Тогда 
		ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	Иначе
		ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;	
	КонецЕсли;
	
	Возврат ГруппаОтбора;
	
КонецФункции

&НаСервере
Процедура ДобавитьЭлементОтбораУсловногоОформления(Отбор, ПутьКДанным, ВидСравнения, ПравоеЗначение = Неопределено)
	
	ЭлементОтбора = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным); 
	ЭлементОтбора.ВидСравнения = ВидСравнения;
	Если ПравоеЗначение <> Неопределено Тогда
		ЭлементОтбора.ПравоеЗначение = ПравоеЗначение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Описание реквизитов формы для работы с механизмом подчиненных форм ввода.
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Описание реквизитов формы:
// * ПараметрыФормы - Строка - имена реквизитов формы
// * ИменаТаблиц - Строка - имена таблиц формы
// * Представления - Структура - имена реквизитов и заголовки
// * ЕстьОбъект - Булево - имеется реквизит формы Объект
// * ПрефиксТитула - Строка - Префикс титула
// * ОписаниеТаблицФормы - Структура - имена таблиц и массив колонок
// * ХранимыеДанныеФормы - Массив из Строка - имена реквизитов хранимых данных
// * ПодчиненныеТаблицы - Структура - имя родительской таблицы и массив подчиненных
// 
//@skip-check constructor-function-return-section
//@skip-check doc-comment-description-ends-on-dot
//@skip-check function-return-types
&НаКлиенте
Функция ОписаниеРеквизитовФормы() Экспорт
	
	Возврат ОписаниеРеквизитовФормыСервер();
	
КонецФункции

&НаСервере
Функция ОписаниеРеквизитовФормыСервер()
	
	Возврат ОбменСГИСЭПД.ОписаниеРеквизитовФормы(ЭтотОбъект);
		
КонецФункции

#КонецОбласти