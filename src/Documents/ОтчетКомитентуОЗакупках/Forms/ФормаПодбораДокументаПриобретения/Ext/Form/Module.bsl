
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Договор          = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	Партнер          = Справочники.Партнеры.ПустаяСсылка();
	Заказы           = Новый Массив;
	ОтбиратьПоЗаказу = Ложь;
	
	Если ЗначениеЗаполнено(Параметры.Договор) Тогда
		Договор = Параметры.Договор;
		Элементы.ЗаголовокДоговор.Видимость = Истина;
		Элементы.ОтборДоговор.Заголовок = Строка(Договор);
		Элементы.ОтборДоговор.Видимость = Истина;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.Партнер) Тогда 
		Партнер = Параметры.Партнер;
		Элементы.ЗаголовокКомитент.Видимость = Истина;
		Элементы.ОтборКомитент.Заголовок = Строка(Партнер);
		Элементы.ОтборКомитент.Видимость = Истина;
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.Заказы) Тогда 
		Заказы = Параметры.Заказы.ВыгрузитьЗначения();
		ОтбиратьПоЗаказу = Истина;
		Элементы.ЗаголовокЗаказ.Видимость = Истина;
		Элементы.ОтборЗаказ.Заголовок = СтрСоединить(Заказы, ", ");
		Элементы.ОтборЗаказ.Видимость = Истина;
	КонецЕсли;
	
	СписокДокументов.Параметры.УстановитьЗначениеПараметра("Договор",          Договор);
	СписокДокументов.Параметры.УстановитьЗначениеПараметра("Партнер",          Партнер);
	СписокДокументов.Параметры.УстановитьЗначениеПараметра("Заказы",           Заказы);
	СписокДокументов.Параметры.УстановитьЗначениеПараметра("ОтбиратьПоЗаказу", ОтбиратьПоЗаказу);
	СписокДокументов.Параметры.УстановитьЗначениеПараметра("СостояниеНеНачет", НСтр("ru = 'Не начат';
																					|en = 'Not started'"));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбранныйДокумент = Неопределено;
	
	ТекущиеДанные = Элементы.СписокДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ВыбранныйДокумент = ТекущиеДанные.Ссылка;
	КонецЕсли;
	Оповестить("ВыборДокументовПриобретенияДляОтчетаКомитенту", ВыбранныйДокумент, ЭтотОбъект);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьДокумент(Команда)
	
	ВыбранныйДокумент = Неопределено;
	
	ТекущиеДанные = Элементы.СписокДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ВыбранныйДокумент = ТекущиеДанные.Ссылка;
	КонецЕсли;

	Оповестить("ВыборДокументовПриобретенияДляОтчетаКомитенту", ВыбранныйДокумент, ЭтотОбъект);
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
