///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
// 
// Параметры:
//  КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной
//                                                            параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной
//                                            параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.ПодключаемыеКоманды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании
//  Параметры                  - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Команда = СозданиеНаОсновании.ДобавитьКомандуСозданияНаОсновании(
		КомандыСозданияНаОсновании, Метаданные.Документы.ДополнительныеСведенияДляОплатыОтпускаСФР);
	Если Команда <> Неопределено Тогда
		Команда.РежимЗаписи = "Проводить";
		Команда.МножественныйВыбор = Ложь;
		Команда.ИмяФормы = "Документ.ДополнительныеСведенияДляОплатыОтпускаСФР.ФормаОбъекта";
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(Команда, "ИдентификаторСообщения", , ВидСравненияКомпоновкиДанных.Заполнено);
	КонецЕсли;

	Команда = СозданиеНаОсновании.ДобавитьКомандуСозданияНаОсновании(
		КомандыСозданияНаОсновании, Метаданные.Документы.СведенияДляОплатыОтпускаСФР);
	Если Команда <> Неопределено Тогда
		Команда.Представление = НСтр("ru = 'Сведения для оплаты отпуска СФР (Перерасчет)';
									|en = 'Information for leave pay by Social Insurance Fund (Recalculation)'");
		Команда.РежимЗаписи = "Проводить";
		Команда.МножественныйВыбор = Ложь;
		Команда.ИмяФормы = "Документ.СведенияДляОплатыОтпускаСФР.ФормаОбъекта";
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(Команда, "ИдентификаторСообщения", , ВидСравненияКомпоновкиДанных.Заполнено);
	КонецЕсли;
КонецПроцедуры

// Для использования в процедуре ДобавитьКомандыСозданияНаОсновании других модулей менеджеров объектов.
//  Добавляет в список команд создания на основании этот объект.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании
// 
// Возвращаемое значение:
//  СтрокаТаблицыЗначений, Неопределено - описание добавленной команды.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Возврат СозданиеНаОсновании.ДобавитьКомандуСозданияНаОсновании(КомандыСозданияНаОсновании, Метаданные());
	
КонецФункции

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ФизическоеЛицо)
	|	И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область СЭДО

Функция ТекстXML(Документ) Экспорт

#Область ПримерТекстаXML
	//<tns:additionalVacationStatement xmlns:tns="http://www.fss.ru/additionalVacationStatement"
	//		xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
	//		xsi:schemaLocation="http://www.fss.ru/additionalVacationStatement additionalVacationStatement.xsd">
	//	<tns:vacationStatementCalc>PRIMARYCALC</tns:vacationStatementCalc>
	//	<tns:insurerInfo>
	//		<tns:insurerName>ООО "СТРАХОВАТЕЛЬ"</tns:insurerName>
	//		<tns:regNum>2222222222</tns:regNum>
	//		<tns:inn>1111111111</tns:inn>
	//		<tns:documentsSendPerson>
	//			<tns:rank>Заместитель генерального директора</tns:rank>
	//			<tns:fullName>
	//				<tns:lastName>Иванов</tns:lastName>
	//				<tns:firstName>Иван</tns:firstName>
	//				<tns:middleName>Иванович</tns:middleName>
	//			</tns:fullName>
	//		</tns:documentsSendPerson>
	//	</tns:insurerInfo>
	//	<tns:insuredInfo>
	//		<tns:fullNamePU>
	//			<tns:lastName>Петров</tns:lastName>
	//			<tns:firstName>Петр</tns:firstName>
	//			<tns:middleName>Петрович</tns:middleName>
	//		</tns:fullNamePU>
	//		<tns:snils>99999999999</tns:snils>
	//	</tns:insuredInfo>
	//	<tns:orderInfo>
	//		<tns:orderNum>И-3345</tns:orderNum>
	//		<tns:orderDate>2001-08-01</tns:orderDate>
	//		<tns:orderBeginDate>2001-09-01</tns:orderBeginDate>
	//		<tns:orderEndDate>2001-09-21</tns:orderEndDate>
	//		<tns:daysAmount>21</tns:daysAmount>
	//	</tns:orderInfo>
	//	<tns:calculationData>
	//		<tns:periodsData>
	//			<tns:period>2022-01-01</tns:period>
	//			<tns:periodSumm>20200.2</tns:periodSumm>
	//			<tns:countDays>29.3</tns:countDays>
	//		</tns:periodsData>
	//		<tns:periodsData>
	//			<tns:period>2022-02-01</tns:period>
	//			<tns:periodSumm>20200.2</tns:periodSumm>
	//			<tns:countDays>29.3</tns:countDays>
	//		</tns:periodsData>
	//		<tns:periodsData>
	//			<tns:period>2022-03-01</tns:period>
	//			<tns:periodSumm>20200.2</tns:periodSumm>
	//			<tns:countDays>29.3</tns:countDays>
	//		</tns:periodsData>
	//		<tns:periodsData>
	//			<tns:period>2022-04-01</tns:period>
	//			<tns:periodSumm>20200.2</tns:periodSumm>
	//			<tns:countDays>29.3</tns:countDays>
	//		</tns:periodsData>
	//			<tns:periodsData>
	//			<tns:period>2022-05-01</tns:period>
	//			<tns:periodSumm>20200.2</tns:periodSumm>
	//			<tns:countDays>29.3</tns:countDays>
	//		</tns:periodsData>
	//		<tns:periodsData>
	//			<tns:period>2022-06-01</tns:period>
	//			<tns:periodSumm>20200.2</tns:periodSumm>
	//			<tns:countDays>29.3</tns:countDays>
	//		</tns:periodsData>
	//		<tns:periodsData>
	//			<tns:period>2022-07-01</tns:period>
	//			<tns:periodSumm>20200.2</tns:periodSumm>
	//			<tns:countDays>29.3</tns:countDays>
	//		</tns:periodsData>
	//		<tns:totalSum>141401.4</tns:totalSum>
	//		<tns:totalDays>205.1</tns:totalDays>
	//		<tns:premiumSum>15000.0</tns:premiumSum>
	//		<tns:totalIncomeSum>141401.4</tns:totalIncomeSum>
	//		<tns:dailyAverageSum>689.43</tns:dailyAverageSum>
	//		<tns:totalDaysAmount>21</tns:totalDaysAmount>
	//		<tns:vacationSum>14478.23</tns:vacationSum>
	//	</tns:calculationData>
	//	<tns:receivePayment>
	//		<tns:bankInfo>
	//			<tns:accountNum>12345678901234567890</tns:accountNum>
	//			<tns:bik>123456789</tns:bik>
	//			<tns:bankName>БАНК</tns:bankName>
	//		</tns:bankInfo>
	//	</tns:receivePayment>
	//	<tns:documents>
	//		<tns:docNmber>И-3345</tns:docNmber>
	//		<tns:docDate>2022-08-01</tns:docDate>
	//		<tns:docComment>Сведения</tns:docComment>
	//		<tns:attachment>
	//			<tns:content>base64Binary</tns:content>
	//			<tns:ext>PNG</tns:ext>
	//			<tns:mimeType>PNG</tns:mimeType>
	//			<tns:size>10504</tns:size>
	//			<tns:name>приказ.png</tns:name>
	//		</tns:attachment>
	//	</tns:documents>
	//</tns:additionalVacationStatement>
#КонецОбласти
	
	МенеджерXML = Обработки.ПостроительXML.Создать();
	МенеджерXML.ФорматДат = "ДФ=yyyy-MM-dd";
	
	КореньXML = МенеджерXML.ДобавитьУзел(МенеджерXML.ДеревоXML, "tns:additionalVacationStatement");
	
	// Пространства имен.
	МенеджерXML.ДобавитьПространствоИмен(КореньXML, "tns", "http://www.fss.ru/additionalVacationStatement");
	МенеджерXML.ДобавитьПространствоИмен(КореньXML, "xsi", "http://www.w3.org/2001/XMLSchema-instance");
	МенеджерXML.ДобавитьАтрибут(КореньXML, "xsi:schemaLocation", "http://www.fss.ru/additionalVacationStatement additionalVacationStatement.xsd");

	
	// Тип расчета.
	Если Документ.ВидДокумента = Перечисления.ВидыСведенийДляОплатыОтпускаСФР.Перерасчет Тогда
		МенеджерXML.ДобавитьУзел(КореньXML, "tns:vacationStatementCalc", "RECALC");
		МенеджерXML.ДобавитьУзел(КореньXML, "tns:primaryStatementUuid", Документ.ИдентификаторПервичногоДокумента);
	Иначе
		МенеджерXML.ДобавитьУзел(КореньXML, "tns:vacationStatementCalc", "PRIMARYCALC");
	КонецЕсли;
	
	ВыгрузитьСведенияОСтрахователе(Документ, МенеджерXML, КореньXML);
	ВыгрузитьСведенияОЗастрахованномЛице(Документ, МенеджерXML, КореньXML);
	ВыгрузитьСведенияОбОтпуске(Документ, МенеджерXML, КореньXML);
	ВыгрузитьСведенияДляРасчетаОплаты(Документ, МенеджерXML, КореньXML);
	ВыгрузитьСпособВыплаты(Документ, МенеджерXML, КореньXML);
	ВыгрузитьВложения(Документ, МенеджерXML, КореньXML);
	
	МенеджерXML.НачатьЗаписьВСтрокуXML(Истина);
	СтрокаXML = МенеджерXML.СтрокаXML();
	Возврат СтрокаXML;
КонецФункции

Процедура ВыгрузитьСведенияОСтрахователе(Документ, МенеджерXML, УзелДанных)
	УзелСтрахователь = МенеджерXML.ДобавитьУзел(УзелДанных, "tns:insurerInfo");
	
	МенеджерXML.ДобавитьУзел(УзелСтрахователь, "tns:insurerName",
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ.Страхователь, "НаименованиеПолное"));
	МенеджерXML.ДобавитьУзел(УзелСтрахователь, "tns:regNum", Документ.РегистрационныйНомерСФР);
	МенеджерXML.ДобавитьУзел(УзелСтрахователь, "tns:inn", Документ.ИНН);
	
	УзелПредставитель = МенеджерXML.ДобавитьУзел(УзелСтрахователь, "tns:documentsSendPerson");
	МенеджерXML.ДобавитьУзел(УзелПредставитель, "tns:rank", Строка(Документ.ДолжностьПредставителяСФР));
	
	УзелПредставительФИО = МенеджерXML.ДобавитьУзел(УзелПредставитель, "tns:fullName");
	МенеджерXML.ДобавитьУзел(УзелПредставительФИО, "tns:lastName", Документ.ПредставительФамилия);
	МенеджерXML.ДобавитьУзел(УзелПредставительФИО, "tns:firstName", Документ.ПредставительИмя);
	МенеджерXML.ДобавитьУзел(УзелПредставительФИО, "tns:middleName", Документ.ПредставительОтчество);
КонецПроцедуры

Процедура ВыгрузитьСведенияОЗастрахованномЛице(Документ, МенеджерXML, УзелДанных)
	УзелЗастрахованноеЛицо = МенеджерXML.ДобавитьУзел(УзелДанных, "tns:insuredInfo");
	
	УзелЗастрахованноеЛицоФИО = МенеджерXML.ДобавитьУзел(УзелЗастрахованноеЛицо, "tns:fullNamePU");
	МенеджерXML.ДобавитьУзел(УзелЗастрахованноеЛицоФИО, "tns:lastName", Документ.СотрудникФамилия);
	МенеджерXML.ДобавитьУзел(УзелЗастрахованноеЛицоФИО, "tns:firstName", Документ.СотрудникИмя);
	МенеджерXML.ДобавитьУзел(УзелЗастрахованноеЛицоФИО, "tns:middleName", Документ.СотрудникОтчество);
	
	МенеджерXML.ДобавитьУзел(УзелЗастрахованноеЛицо, "tns:snils", СтроковыеФункцииБЗККлиентСервер.УбратьПробелы(Документ.СотрудникСНИЛС, Истина));
КонецПроцедуры

Процедура ВыгрузитьСведенияОбОтпуске(Документ, МенеджерXML, УзелДанных)
	УзелОтпуск = МенеджерXML.ДобавитьУзел(УзелДанных, "tns:orderInfo");
	МенеджерXML.ДобавитьУзел(УзелОтпуск, "tns:orderNum", Документ.НомерПриказа);
	МенеджерXML.ДобавитьУзел(УзелОтпуск, "tns:orderDate", Документ.ДатаПриказа);
	МенеджерXML.ДобавитьУзел(УзелОтпуск, "tns:orderBeginDate", Документ.ДатаНачалаОтпуска);
	МенеджерXML.ДобавитьУзел(УзелОтпуск, "tns:orderEndDate", Документ.ДатаОкончанияОтпуска);
	МенеджерXML.ДобавитьУзел(УзелОтпуск, "tns:daysAmount", Документ.КоличествоДнейОтпуска);
КонецПроцедуры

Процедура ВыгрузитьСведенияДляРасчетаОплаты(Документ, МенеджерXML, УзелДанных)
	ДнейИтого = 0;
	СуммаИтого = 0;
	
	УзелСведенияДляРасчетаОплаты = МенеджерXML.ДобавитьУзел(УзелДанных, "tns:calculationData");
	
	Для Каждого ПериодЗаработка Из Документ.СреднийЗаработок Цикл
		УзелЗаработокПоПериодам = МенеджерXML.ДобавитьУзел(УзелСведенияДляРасчетаОплаты, "tns:periodsData");
		МенеджерXML.ДобавитьУзел(УзелЗаработокПоПериодам, "tns:period", ПериодЗаработка.Период);
		МенеджерXML.ДобавитьУзел(УзелЗаработокПоПериодам, "tns:periodSumm", ПериодЗаработка.СуммаВыплат);
		МенеджерXML.ДобавитьУзел(УзелЗаработокПоПериодам, "tns:countDays", ПериодЗаработка.КалендарныхДней);
		СуммаИтого = СуммаИтого + ПериодЗаработка.СуммаВыплат;
		ДнейИтого = ДнейИтого + ПериодЗаработка.КалендарныхДней;
	КонецЦикла;
	
	МенеджерXML.ДобавитьУзел(УзелСведенияДляРасчетаОплаты, "tns:totalSum", СуммаИтого);
	МенеджерXML.ДобавитьУзел(УзелСведенияДляРасчетаОплаты, "tns:totalDays", ДнейИтого);
	МенеджерXML.ДобавитьУзел(УзелСведенияДляРасчетаОплаты, "tns:premiumSum", Документ.СуммаПремии);
	МенеджерXML.ДобавитьУзел(УзелСведенияДляРасчетаОплаты, "tns:totalIncomeSum", Документ.ЗаработокВключаяПремию);
	МенеджерXML.ДобавитьУзел(УзелСведенияДляРасчетаОплаты, "tns:dailyAverageSum", Документ.СреднийДневнойЗаработок);
	МенеджерXML.ДобавитьУзел(УзелСведенияДляРасчетаОплаты, "tns:totalDaysAmount", Документ.КоличествоДнейОтпуска);
	МенеджерXML.ДобавитьУзел(УзелСведенияДляРасчетаОплаты, "tns:vacationSum", Документ.СуммаКВыплате);
КонецПроцедуры

Процедура ВыгрузитьСпособВыплаты(Документ, МенеджерXML, УзелДанных)
	// Пример: МенеджерXML.ДобавитьУзел(Родитель, Имя, Значение = Неопределено, МожетОтсутствовать = Ложь, МожетБытьНеопределено = Ложь).
	УзелСпособ = МенеджерXML.ДобавитьУзел(УзелДанных, "tns:receivePayment");
	
	// Способ выплаты пособий.
	Если Документ.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ЧерезБанк Тогда
		Узел = МенеджерXML.ДобавитьУзел(УзелСпособ, "tns:bankInfo");
		МенеджерXML.ДобавитьУзел(Узел, "tns:accountNum", Документ.НомерСчета);
		МенеджерXML.ДобавитьУзел(Узел, "tns:bik",		 Документ.БанкБИК);
		МенеджерXML.ДобавитьУзел(Узел, "tns:bankName",	 Документ.БанкНаименование);
	ИначеЕсли Документ.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.НаКартуМИР Тогда
		НомерКарты = БанковскиеКарты.ПолныйНомерКарты(Документ.КартаМИР);
		МенеджерXML.ДобавитьУзел(УзелСпособ, "tns:cardMir", УбратьПробелы(НомерКарты));
	ИначеЕсли Документ.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ПочтовымПереводом Тогда
		Узел = МенеджерXML.ДобавитьУзел(УзелСпособ, "tns:postalTransfer");
		МенеджерXML.ДобавитьУзел(Узел, "tns:postIndex",	Документ.АдресПроживанияИндекс);
		МенеджерXML.ДобавитьУзел(Узел, "tns:region",	Документ.АдресПроживанияРегион);
		МенеджерXML.ДобавитьУзел(Узел, "tns:district",	Документ.АдресПроживанияРайон);
		МенеджерXML.ДобавитьУзел(Узел, "tns:city",		Документ.АдресПроживанияНаселенныйПункт);
		МенеджерXML.ДобавитьУзел(Узел, "tns:street",	Документ.АдресПроживанияУлица);
		МенеджерXML.ДобавитьУзел(Узел, "tns:house",		Документ.АдресПроживанияДом);
		МенеджерXML.ДобавитьУзел(Узел, "tns:block",		Документ.АдресПроживанияКорпус, Истина);
		МенеджерXML.ДобавитьУзел(Узел, "tns:building",	Документ.АдресПроживанияСтроение, Истина);
		МенеджерXML.ДобавитьУзел(Узел, "tns:flat",		Документ.АдресПроживанияКвартира, Истина);
	ИначеЕсли Документ.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ИнаяОрганизация Тогда
		// Сведения об иной организации. Нет в спецификации для 94 сообщения, но поля присутствую в утвержденной форме печати.
		Узел = МенеджерXML.ДобавитьУзел(УзелСпособ, "tns:otherOrg");
		МенеджерXML.ДобавитьУзел(Узел, "tns:orgName",                Документ.ИнаяОрганизацияНаименование);
		МенеджерXML.ДобавитьУзел(Узел, "tns:bik",                    Документ.ИнаяОрганизацияБИК);
		МенеджерXML.ДобавитьУзел(Узел, "tns:accountNum",             Документ.ИнаяОрганизацияНомерСчетаСтрахователя);
		МенеджерXML.ДобавитьУзел(Узел, "tns:personalOrgAccount",     Документ.ИнаяОрганизацияЛицевойСчетОрганизации);
		МенеджерXML.ДобавитьУзел(Узел, "tns:personalInsuredAccount", Документ.ИнаяОрганизацияЛицевойСчетСотрудника, Истина);
		МенеджерXML.ДобавитьУзел(Узел, "tns:kbk",                    Документ.ИнаяОрганизацияКБК, Истина);
		МенеджерXML.ДобавитьУзел(Узел, "tns:regNumSFRCorrectionalFacility",
			Документ.ИнаяОрганизацияРегистрационныйНомерСФР);
	КонецЕсли;
КонецПроцедуры

Процедура ВыгрузитьВложения(Документ, МенеджерXML, УзелДанных)
	Если Документ.Вложения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ДанныеФайлов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		Документ.Вложения.Выгрузить().ВыгрузитьКолонку("Файл"), "Расширение,Размер,Наименование");
	Для Каждого Вложение Из Документ.Вложения Цикл
		УзелВложение = МенеджерXML.ДобавитьУзел(УзелДанных, "tns:documents");
		МенеджерXML.ДобавитьУзел(УзелВложение, "tns:docNmber",		Вложение.Номер, Истина);
		МенеджерXML.ДобавитьУзел(УзелВложение, "tns:docDate",		Вложение.Дата);
		МенеджерXML.ДобавитьУзел(УзелВложение, "tns:docComment",	Вложение.Комментарий, Истина);
		
		ДанныеФайла = ДанныеФайлов.Получить(Вложение.Файл);
		УзелФайл = МенеджерXML.ДобавитьУзел(УзелВложение, "tns:attachment");
		МенеджерXML.ДобавитьУзел(УзелФайл, "tns:content",	Строка(Вложение.Файл.УникальныйИдентификатор()));
		МенеджерXML.ДобавитьУзел(УзелФайл, "tns:ext",		ДанныеФайла.Расширение);
		МенеджерXML.ДобавитьУзел(УзелФайл, "tns:mimeType",	ДанныеФайла.Расширение); // MIMEТипПоСтроке(ДанныеФайла.Расширение)
		МенеджерXML.ДобавитьУзел(УзелФайл, "tns:size",		ДанныеФайла.Размер);
		МенеджерXML.ДобавитьУзел(УзелФайл, "tns:name",		ДанныеФайла.Наименование);
	КонецЦикла;
КонецПроцедуры

Функция Метаданные() Экспорт
	Возврат Метаданные.Документы.СведенияДляОплатыОтпускаСФР;
КонецФункции

Функция ТипСообщения(Документ = Неопределено) Экспорт
	Возврат 94;
КонецФункции

Функция ЗаголовокФормыОтправкиСведенийВФСС() Экспорт
	Возврат СтрШаблон(НСтр("ru = 'Отправка %1';
							|en = 'Send %1'"), Метаданные().ПредставлениеСписка);
КонецФункции

Процедура ПриЗаполненииПараметровОтправки(МассивСсылок, ТаблицаОтправки) Экспорт
	Синоним = Метаданные().Синоним;
	
	ИменаРеквизитовСтроки = "Ссылка, Номер, Дата, ФизическоеЛицо, Организация";
	ИменаРеквизитов = "Ссылка, Номер, Дата, ДатаОтправки, ФизическоеЛицо, Организация, ХранилищеXML, ПометкаУдаления, Проведен";
	Соответствие = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСсылок, ИменаРеквизитов);
	Для Каждого КлючИЗначение Из Соответствие Цикл
		Документ = КлючИЗначение.Значение;
		// Добавление строки отправки и заполнение представления.
		СтрокаОтправки = ТаблицаОтправки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаОтправки, Документ, ИменаРеквизитовСтроки);
		СтрокаОтправки.ТипСообщения = ТипСообщения(Документ);
		СтрокаОтправки.ТекстXML     = Документ.ХранилищеXML.Получить();
		СтрокаОтправки.Отправить    = Ложь;
		СтрокаОтправки.ШаблонОшибки = СтрШаблон(
			НСтр("ru = 'Не удалось отправить %1 сотрудника %2:%3';
				|en = 'Cannot send %1 of the %2 employee:%3'"),
			Синоним,
			СтрокаОтправки.ФизическоеЛицо,
			"%1");
		// Проверки и обновление вторичных данных документа перед отправкой.
		Если Документ.ПометкаУдаления Тогда
			СтрокаОтправки.ИндексКартинки = СЭДОФССКлиентСервер.ИндексКартинкиУдален();
			СтрокаОтправки.Результат = НСтр("ru = 'Документ помечен на удаление';
											|en = 'The document is marked for deletion'");
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(Документ.ДатаОтправки) Тогда
			СтрокаОтправки.ИндексКартинки = СЭДОФССКлиентСервер.ИндексКартинкиОтправлен();
			Если НачалоДня(Документ.ДатаОтправки) <= ТекущаяДатаСеанса() Тогда
				Текст = НСтр("ru = 'Документ уже отправлен %1';
							|en = 'The document is already sent %1'");
				СтрокаОтправки.Результат = СтрШаблон(Текст, Формат(Документ.ДатаОтправки, "ДЛФ=D"));
			Иначе
				Текст = НСтр("ru = 'Документ уже отправлен в %1';
							|en = 'The document is already sent to %1'");
				СтрокаОтправки.Результат = СтрШаблон(Текст, Формат(Документ.ДатаОтправки, "ДФ=HH:mm"));
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		ДокументОбъект = Документ.Ссылка.ПолучитьОбъект();
		Если Не ДокументОбъект.ПроверитьЗаполнение() Тогда
			СтрокаОтправки.ИндексКартинки = СЭДОФССКлиентСервер.ИндексКартинкиПредупреждение();
			ЗаполнитьТекстОшибкиПроверкиЗаполнения(СтрокаОтправки, "");
			Если Не ДокументОбъект.ОтключитьПроверкиПроведения Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		// Проведение документа если требуется.
		ЕстьИзменения = ДокументОбъект.ОбновитьВторичныеДанные();
		СтрокаОтправки.Отправить = Истина;
		Если ЕстьИзменения Или Не ДокументОбъект.Проведен Тогда
			Попытка
				СтрокаОтправки.ИндексКартинки = СЭДОФССКлиентСервер.ИндексКартинкиПроведен();
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
				ЗаполнитьЗначенияСвойств(СтрокаОтправки, ДокументОбъект, ИменаРеквизитовСтроки);
				СтрокаОтправки.ТекстXML = ДокументОбъект.ХранилищеXML.Получить();
			Исключение
				СтрокаОтправки.Отправить = Ложь;
				СтрокаОтправки.ИндексКартинки = СЭДОФССКлиентСервер.ИндексКартинкиОстановлен();
				СтрокаОтправки.ЗначениеРасшифровки = Документ.Ссылка;
				ТекстИсключения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаполнитьТекстОшибкиПроверкиЗаполнения(СтрокаОтправки, ТекстИсключения);
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриПодготовкеВыбранныхДокументовКОтправке(МассивСсылок, ТаблицаОтправки) Экспорт
	// Заменим ГУИДы вложений на двоичные данные в тексте сообщения
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияДляОплатыОтпускаСФРВложения.Файл КАК Файл,
		|	СведенияДляОплатыОтпускаСФРВложения.Ссылка КАК Документ
		|ИЗ
		|	Документ.СведенияДляОплатыОтпускаСФР.Вложения КАК СведенияДляОплатыОтпускаСФРВложения
		|ГДЕ
		|	СведенияДляОплатыОтпускаСФРВложения.Ссылка В(&Документы)
		|ИТОГИ ПО
		|	Документ";
	Запрос.УстановитьПараметр("Документы", ТаблицаОтправки.ВыгрузитьКолонку("Ссылка"));
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	СоответствиеДокументов = Новый Соответствие;
	МассивВложений = Новый Массив;
	ВыборкаДокумент = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаДокумент.Следующий() Цикл
		МассивФайловДокумента = Новый Массив;
		Выборка = ВыборкаДокумент.Выбрать();
		Пока Выборка.Следующий() Цикл
			МассивФайловДокумента.Добавить(Выборка.Файл);
			МассивВложений.Добавить(Выборка.Файл);
		КонецЦикла;
		СоответствиеДокументов.Вставить(ВыборкаДокумент.Документ, МассивФайловДокумента);
	КонецЦикла;
	
	ДвоичныеДанныеФайлов = РаботаСФайлами.ДвоичныеДанныеФайлов(МассивВложений);
	Для Каждого СообщениеКОтправке Из ТаблицаОтправки Цикл
		ВложенияКОтправке = СоответствиеДокументов.Получить(СообщениеКОтправке.Ссылка);
		Для Каждого Вложение Из ВложенияКОтправке Цикл
			ГУИД = Строка(Вложение.УникальныйИдентификатор());
			ДвоичныеДанные = ДвоичныеДанныеФайлов.Получить(Вложение);
			Если ДвоичныеДанные <> Неопределено Тогда
				СообщениеКОтправке.ТекстXML = СтрЗаменить(СообщениеКОтправке.ТекстXML, ГУИД, Base64Строка(ДвоичныеДанные));
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

// См. СЭДОФСС.ЗарегистрироватьНепосредственныйРезультатОтправкиДокумента.
Процедура ЗарегистрироватьРезультатОтправки(РезультатОтправки) Экспорт
	// Сначала записывается отправленный документ, т.к. его данные используются при обновлении регистров (кэшей).
	ДокументОбъект = РезультатОтправки.Ссылка.ПолучитьОбъект();
	ДокументОбъект.ДатаОтправки           = РезультатОтправки.ДатаОтправки;
	ДокументОбъект.ИдентификаторСообщения = РезультатОтправки.ИдентификаторСообщения;
	ДокументОбъект.Страхователь           = РезультатОтправки.Страхователь;
	ДокументОбъект.ГоловнаяОрганизация    = РезультатОтправки.ГоловнаяОрганизация;
	СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, Ложь, РежимЗаписиДокумента.Запись);
	
	// Затем записывается транспортный регистр,
	// который при записи запускает обновление остальных регистров, зависящих от состояния отправки.
	РегистрыСведений.РегистрацииСведенийДляОплатыОтпускаСФР.ЗаполнитьПоДокументу(ДокументОбъект, РезультатОтправки);
	РезультатОтправки.Измененные.Добавить(ДокументОбъект.Ссылка);
КонецПроцедуры

// См. СЭДОФСС.ЗагрузитьРезультатДоставкиСообщенияСЭДО.
Процедура ЗагрузитьРезультатДоставкиСообщения94(Страхователь, Сообщение, Результат) Экспорт
	Таблица = РегистрыСведений.РегистрацииСведенийДляОплатыОтпускаСФР.НайтиПоИдентификаторуСообщения(
		Страхователь,
		Сообщение.Идентификатор,
		Результат);
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		// Сначала записывается отправленный документ, т.к. его данные используются при обновлении регистров (кэшей).
		Если Не Сообщение.ДоставленоФонду Тогда
			ДокументОбъект = СтрокаТаблицы.ИсходящийДокумент.ПолучитьОбъект();
			ДокументОбъект.ДатаОтправки           = '00010101';
			ДокументОбъект.ИдентификаторСообщения = "";
			СЭДОФСС.ЗаписатьДокумент(ДокументОбъект, Истина, Ложь, РежимЗаписиДокумента.Запись);
		КонецЕсли;
		
		// Затем записывается транспортный регистр,
		// который при записи запускает обновление остальных регистров, зависящих от состояния отправки.
		РегистрыСведений.РегистрацииСведенийДляОплатыОтпускаСФР.ЗаполнитьПоРезультатуДоставки(СтрокаТаблицы.ИсходящийДокумент, Сообщение);
		
		// Отметка для БРО и других потребителей что сообщение обработано.
		Результат.Обработано = Истина;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СоставДокументов

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
//
Функция ОписаниеСоставаОбъекта() Экспорт
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаФизическоеЛицоВШапке("ФизическоеЛицо", "Сотрудник");
КонецФункции

#КонецОбласти

#Область ФиксацияВторичныхДанныхВДокументах

Функция ПараметрыФиксацииВторичныхДанных(Документ = Неопределено) Экспорт
	Возврат ФиксацияВторичныхДанныхВДокументах.ПараметрыФиксации(ФиксируемыеРеквизиты(Документ), ФиксируемыеТаблицы());
КонецФункции

// Определяет по данным документа признак фиксации.
//
// Параметры:
//   Документ - ДокументОбъект.ОтветНаЗапросРеквизитовДляВыплатыПособия, ДанныеФормыСтруктура
//
// Возвращаемое значение:
//   Булево
//
Функция ОбъектЗафиксирован(Документ) Экспорт
	Возврат Не Документ.ПометкаУдаления И ЗначениеЗаполнено(Документ.ДатаОтправки);
КонецФункции

#КонецОбласти

#Область МногофункциональныеДокументы

// Возвращает метаданные разделов документа.
//
// Возвращаемое значение:
//   Соответствие, Неопределено - Описание разделов документа.
//
Функция ОписаниеРазделовДанных() Экспорт
	Если РасширеннаяРеализация() Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("МногофункциональныеДокументыБЗККлиентСервер");
		ВсеРазделы = Модуль.РазделыДанных();
		
		ОписаниеРазделовДанных = Новый Соответствие();
		
		ОписаниеРаздела = Модуль.НовыйОписаниеРазделаДанных();
		ОписаниеРазделовДанных.Вставить(ВсеРазделы.КадровыеДанные, ОписаниеРаздела);
		ОписаниеРаздела.РеквизитСостояние    = "Проведен";
		ОписаниеРаздела.РеквизитОтветсвенный = "Ответственный";
		
		ОписаниеРаздела = Модуль.НовыйОписаниеРазделаДанных();
		ОписаниеРазделовДанных.Вставить(ВсеРазделы.НачисленнаяЗарплата, ОписаниеРаздела);
		Возврат ОписаниеРазделовДанных;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

// Возвращает значения по которым будут проверяться права на документ.
//
// Параметры:
//   ДокументОбъект - ДокументОбъект, ДанныеФормыСтруктура
//
// Возвращаемое значение:
//   Структура - Значения доступа по которым будут проверяться права на документ
//
Функция ЗначенияДоступа(ДокументОбъект) Экспорт
	Если РасширеннаяРеализация() Тогда
		МодульМногофункциональныеДокументыБЗК = ОбщегоНазначения.ОбщийМодуль("МногофункциональныеДокументыБЗК");
		Возврат МодульМногофункциональныеДокументыБЗК.ЗначенияДоступаПоСоставуДокумента(
			ДокументОбъект,
			ДокументОбъект.Организация);
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ФиксацияВторичныхДанныхВДокументах

Функция ФиксируемыеРеквизиты(Документ = Неопределено)
	ФиксируемыеРеквизиты = Новый Соответствие;
	
	РеквизитыРучногоЗаполнения = "Ответственный, Комментарий, ФизическоеЛицо, ГоловнаяОрганизация";
	
	Если Документ = Неопределено Тогда
		ЗаполненОтпуск		= Ложь;
		ЗаполненСотрудник	= Ложь;
		ЗаполненоФизлицо	= Ложь;
	Иначе
		ЗаполненОтпуск		= ЗначениеЗаполнено(Документ.Отпуск);
		ЗаполненСотрудник	= ЗначениеЗаполнено(Документ.Сотрудник);
		ЗаполненоФизлицо	= ЗначениеЗаполнено(Документ.ФизическоеЛицо);
	КонецЕсли;
	
	// Реквизиты входящего запроса.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения      = "ЭтотОбъект";
	Группа.ИмяГруппы                = "ЭтотОбъект";
	Группа.ОтображатьПредупреждение = ЗаполненОтпуск;
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Отпуск", ЗаполненОтпуск);
	
	// Реквизиты входящего запроса.
	Если РасширеннаяРеализация() Тогда
		Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
		Группа.ОснованиеЗаполнения = "Отпуск";
		Группа.ИмяГруппы           = "ОтпускРеквизиты";
		Группа.ОтображатьПредупреждение = ЗаполненОтпуск;
		
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Сотрудник", ЗаполненОтпуск);
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Организация", ЗаполненОтпуск);
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ДатаНачалаОтпуска", ЗаполненОтпуск);
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ДатаОкончанияОтпуска", ЗаполненОтпуск);
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "КоличествоДнейОтпуска", ЗаполненОтпуск);
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ДатаПриказа", ЗаполненОтпуск);
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "НомерПриказа", ЗаполненОтпуск);
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ПервичныйДокумент", ЗаполненОтпуск);
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "СреднийЗаработок", ЗаполненОтпуск);
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "СуммаПремии", ЗаполненОтпуск);
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ВидДокумента", ЗаполненОтпуск);
	Иначе
		РеквизитыРучногоЗаполнения = РеквизитыРучногоЗаполнения + ", Сотрудник, Организация, ДатаНачалаОтпуска, ДатаОкончанияОтпуска,
			|КоличествоДнейОтпуска, ДатаПриказа, НомерПриказа, СреднийЗаработок, СуммаПремии";
	КонецЕсли;
	РеквизитыРучногоЗаполнения = Новый Структура(РеквизитыРучногоЗаполнения);
	
	// Реквизиты организации.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения = "Организация";
	Группа.ИмяГруппы           = "Организация";
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Страхователь");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "РегистрационныйНомерСФР");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ПредставительСФР", Ложь);
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ДолжностьПредставителяСФР", Ложь);
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ОснованиеПодписиПредставителяСФР", Ложь);
	
	// Уполномоченный представитель.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения = "ПредставительСФР";
	Группа.ИмяГруппы           = "ПредставительСФР";
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ПредставительФамилия");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ПредставительИмя");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ПредставительОтчество");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ПредставительТелефон");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ПредставительЭлектроннаяПочта");
	
	// Физлицо.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения      = "Сотрудник";
	Группа.ИмяГруппы                = "СотрудникКадровыеДанные";
	Группа.ОтображатьПредупреждение = ЗаполненСотрудник;
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "СотрудникФамилия");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "СотрудникИмя");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "СотрудникОтчество");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "СотрудникСНИЛС");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживания");
	
	// Способ выплаты пособия.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения = "Сотрудник";
	Группа.ИмяГруппы           = "СпособВыплатыПособия";
	Группа.ФиксацияГруппы      = Истина;
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "СпособВыплатыПособия");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "КартаМИР");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "Банк");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "НомерСчета");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияВид");
	
	// Способ выплаты пособия: Сведения о банке.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения = "Банк";
	Группа.ИмяГруппы           = "РеквизитыБанка";
	Группа.ФиксацияГруппы      = Истина;
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "БанкБИК");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "БанкКоррСчет");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "БанкНаименование");
	
	// Способ выплаты пособия: реквизиты адреса проживания.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения = "АдресПроживания";
	Группа.ИмяГруппы           = "АдресПроживания";
	Группа.ФиксацияГруппы      = Истина;
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияЗаПределамиРФ");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияДом");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияИндекс");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияКвартира");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияСтроение");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияКорпус");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияФИАСДом");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "АдресПроживанияФИАСРО");
	
	// Способ выплаты пособия: Сведения о банке.
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ОснованиеЗаполнения      = "ДокументОснование";
	Группа.ИмяГруппы                = "ИнаяОрганизация";
	Группа.ФиксацияГруппы           = Истина;
	Группа.ОтображатьПредупреждение = Ложь;
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияБИК");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияКБК");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияЛицевойСчетОрганизации");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияЛицевойСчетСотрудника");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияНаименование");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияНомерСчетаСтрахователя");
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, "ИнаяОрганизацияРегистрационныйНомерСФР");
	
	// Реквизиты среднего заработка.
	Если РасширеннаяРеализация() Тогда
		Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
		Шаблон.ОснованиеЗаполнения = "СреднийЗаработок";
		Шаблон.ИмяГруппы           = "ПериодыСреднегоЗаработока";
		Шаблон.РеквизитСтроки      = Истина;
		Шаблон.Путь                = "СреднийЗаработок";
		Шаблон.ОтображатьПредупреждение = Истина;
		
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "Период", Истина);
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "СуммаВыплат", Истина);
		ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "КалендарныхДней", Истина);
	КонецЕсли;
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.ОснованиеЗаполнения = "ПериодыСреднегоЗаработока";
	Шаблон.ИмяГруппы           = "ИтогСреднийЗаработок";
	
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "СуммаКВыплате", Истина);
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "СреднийДневнойЗаработок", Истина);
	ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ЗаработокВключаяПремию", Истина);
	
	// Добавление всех оставшихся реквизитов одной группой "по умолчанию".
	Группа = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Группа.ИмяГруппы                = "СлужебныеРеквизиты";
	Группа.ОтображатьПредупреждение = Ложь;
	Для Каждого Реквизит Из Метаданные().Реквизиты Цикл
		Если Не РеквизитыРучногоЗаполнения.Свойство(Реквизит.Имя)
			И ФиксируемыеРеквизиты[Реквизит.Имя] = Неопределено Тогда
			ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, Реквизит.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(ФиксируемыеРеквизиты);
КонецФункции

Функция ФиксируемыеТаблицы()
	Возврат Новый Структура("СреднийЗаработок", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Период"));
КонецФункции

Процедура ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Группа, ИмяРеквизита, ОтображатьПредупреждение = Неопределено)
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(
		ФиксируемыеРеквизиты,
		Группа,
		ИмяРеквизита,
		ОтображатьПредупреждение);
КонецПроцедуры

#КонецОбласти

#Область ПриЗаполненииПараметровОтправки

Функция ЗаполнитьТекстОшибкиПроверкиЗаполнения(СтрокаОтправки, ТекстИсключенияПриПроведении)
	СтрокаОтправки.Ошибки = ПолучитьСообщенияПользователю(Истина);
	
	Количество = СтрокаОтправки.Ошибки.Количество();
	Если Количество = 1 Тогда
		СтрокаОтправки.Результат = СтрокаОтправки.Ошибки[0].Текст;
	ИначеЕсли Количество > 1 Тогда
		СтрокаОтправки.Результат = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			НСтр("ru = ';%1 ошибка заполнения;;%1 ошибки заполнения;%1 ошибок заполнения;';
				|en = ';%1 filling error;;%1 filling errors;%1 filling errors;'"),
			Количество);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстИсключенияПриПроведении) Тогда
		Если СтрокаОтправки.Результат = "" Тогда
			СтрокаОтправки.Результат = СтрШаблон(НСтр("ru = 'Ошибка проведения: %1';
														|en = 'Posting error: %1'"), ТекстИсключенияПриПроведении);
		Иначе
			СтрокаОтправки.Результат = СтрокаОтправки.Результат + ";" + Символы.ПС + СтрШаблон(НСтр("ru = 'И ошибка проведения: %1';
																									|en = 'And posting error: %1'"), ТекстИсключенияПриПроведении);
		КонецЕсли;
	КонецЕсли;
КонецФункции

#КонецОбласти

#Область Вложения

Функция MIMEТипПоСтроке(СтрТипСодержимогоИлиРасширение) Экспорт
	
	СтрокаТипаВНижнемРегистре = НРег(СтрТипСодержимогоИлиРасширение);
	Если СтрокаТипаВНижнемРегистре = "txt" ИЛИ СтрокаТипаВНижнемРегистре = "plain866"
		ИЛИ СтрокаТипаВНижнемРегистре = "текст1251" ИЛИ СтрокаТипаВНижнемРегистре = "plain1251" Тогда
		Возврат "text/plain";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "xml" Тогда
		Возврат "application/xml";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "htm" ИЛИ СтрокаТипаВНижнемРегистре = "html" Тогда
		Возврат "text/html";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "pdf" Тогда
		Возврат "application/pdf";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "rtf" Тогда
		Возврат "application/rtf";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "tif" ИЛИ СтрокаТипаВНижнемРегистре = "tiff" Тогда
		Возврат "image/tiff";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "jpg" ИЛИ СтрокаТипаВНижнемРегистре = "jpeg" Тогда
		Возврат "image/jpeg";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "png" Тогда
		Возврат "image/png";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "gif" Тогда
		Возврат "image/gif";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "bmp" Тогда
		Возврат "image/bmp";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "doc" Тогда
		Возврат "application/msword";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "xls" Тогда
		Возврат "application/vnd.ms-excel";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "odt" Тогда
		Возврат "application/vnd.oasis.opendocument.text";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "ods" Тогда
		Возврат "application/vnd.oasis.opendocument.spreadsheet";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "docx" Тогда
		Возврат "application/vnd.openxmlformats-officedocument.wordprocessingml.document";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "xlsx" Тогда
		Возврат "application/vnd.openxmlformats-officedocument.spreadsheetml.sheet";
	ИначеЕсли СтрокаТипаВНижнемРегистре = "sgn" Тогда
		Возврат "application/x-pkcs7-signature";
	Иначе // "unknown"
		Возврат "application/octet-stream";
	КонецЕсли;
	
КонецФункции

#КонецОбласти

Функция РасширеннаяРеализация() Экспорт
	Возврат ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы");
КонецФункции

Функция ОписаниеПодписейДокумента() Экспорт 
	ОписаниеПодписей = ПодписиДокументов.ОписаниеТаблицыПодписей();
	ПодписиДокументов.ДобавитьОписаниеПодписейОрганизации(ОписаниеПодписей, "ПредставительСФР");
	Возврат ОписаниеПодписей;
КонецФункции

Функция УбратьПробелы(Строка, УбратьТире = Истина)
	Возврат СтроковыеФункцииБЗККлиентСервер.УбратьПробелы(Строка, УбратьТире);
КонецФункции

Функция КоличествоОжидаемыхСообщений(ГоловнаяОрганизация, Организации) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК Поле1
	|ИЗ
	|	РегистрСведений.РегистрацииСведенийДляОплатыОтпускаСФР КАК Регистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО Регистрации.ГоловнаяОрганизация = Организации.Ссылка
	|ГДЕ
	|	Регистрации.Состояние = &Отправлен
	|	И Регистрации.ДатаОтправки > &ДатаНачалаАктуальности
	|	И Регистрации.ГоловнаяОрганизация = &ГоловнаяОрганизация
	|	И Организации.Ссылка В(&Организации)
	|	И Регистрации.ИсходящийДокумент ССЫЛКА Документ.СведенияДляОплатыОтпускаСФР
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1
	|ИЗ
	|	РегистрСведений.РегистрацииСведенийДляОплатыОтпускаСФР КАК Регистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО Регистрации.ГоловнаяОрганизация = Организации.Ссылка
	|ГДЕ
	|	Регистрации.Состояние = &ОтправленОператору
	|	И Регистрации.ДатаОтправкиОператору > &ДатаНачалаАктуальности
	|	И Регистрации.ГоловнаяОрганизация = &ГоловнаяОрганизация
	|	И Организации.Ссылка В(&Организации)
	|	И Регистрации.ИсходящийДокумент ССЫЛКА Документ.СведенияДляОплатыОтпускаСФР";
	
	Запрос.УстановитьПараметр("Отправлен", Перечисления.СостоянияДокументаСЭДОФСС.Отправлен);
	Запрос.УстановитьПараметр("ОтправленОператору", Перечисления.СостоянияДокументаСЭДОФСС.ОтправленОператору);
	Запрос.УстановитьПараметр("ДатаНачалаАктуальности", НачалоДня(ДобавитьМесяц(ТекущаяДатаСеанса(), -1)));
	
	Если ЗначениеЗаполнено(ГоловнаяОрганизация) Тогда
		Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Регистрации.ГоловнаяОрганизация = &ГоловнаяОрганизация", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организации) Тогда
		Запрос.УстановитьПараметр("Организации", Организации);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПО Регистрации.ГоловнаяОрганизация = Организации.Ссылка", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Организации.Ссылка В(&Организации)", "");
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выбрать().Количество();
КонецФункции

#КонецОбласти

#КонецЕсли