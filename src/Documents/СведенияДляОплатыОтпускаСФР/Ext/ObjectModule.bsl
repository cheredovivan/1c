#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	ХранилищеXML				= Неопределено;
	ДатаОтправки				= '00010101';
	ИдентификаторСообщения		= "";
	ОтключитьПроверкиПроведения = Ложь;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	СсылкаОснования = ДанныеЗаполнения;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		СсылкаОснования = ОбщегоНазначенияБЗК.ЗначениеСвойства(ДанныеЗаполнения, "Ссылка");
	КонецЕсли;
	
	Если ТипЗнч(СсылкаОснования) = Тип("ДокументСсылка.СведенияДляОплатыОтпускаСФР") Тогда
		ДокументОснование = СсылкаОснования;
		ВидДокумента = Перечисления.ВидыСведенийДляОплатыОтпускаСФР.Перерасчет;
		Если РасширеннаяРеализация() Тогда
			Отпуск = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование, "Отпуск");
		Иначе
			Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование,
				"Организация, Сотрудник, ДатаНачалаОтпуска, ДатаОкончанияОтпуска, КоличествоДнейОтпуска,
				|ДатаПриказа, НомерПриказа, СреднийЗаработок, СуммаПремии");
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты, , "СреднийЗаработок");
			СреднийЗаработок.Загрузить(Реквизиты.СреднийЗаработок.Выгрузить());
		КонецЕсли;
	ИначеЕсли ТипЗнч(СсылкаОснования) = Тип("ДокументСсылка.Отпуск") Тогда
		ДокументОснование = СсылкаОснования;
		Отпуск = СсылкаОснования;
	КонецЕсли;
	
	ЗначенияПоУмолчанию = Новый Структура;
	Если ДанныеЗаполнения = Неопределено Или Не ЗначениеЗаполнено(Ответственный) Тогда
		ЗначенияПоУмолчанию.Вставить("Ответственный");
	КонецЕсли;
	Если ДанныеЗаполнения = Неопределено Или Не ЗначениеЗаполнено(Организация) Тогда
		ЗначенияПоУмолчанию.Вставить("Организация");
	КонецЕсли;
	Если ЗначенияПоУмолчанию.Количество() > 0 Тогда
		ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗначенияПоУмолчанию);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначенияПоУмолчанию);
	КонецЕсли;
	ДополнительныеСвойства.Вставить("Заполнение", Истина);
	
	ОбновитьВторичныеДанные();
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	ПроверяемыеРеквизиты.Очистить();
	ПроверяемыеРеквизиты.Добавить("Дата");
	ПроверяемыеРеквизиты.Добавить("ДатаНачалаОтпуска");
	ПроверяемыеРеквизиты.Добавить("ДатаОкончанияОтпуска");
	ПроверяемыеРеквизиты.Добавить("ДатаПриказа");
	ПроверяемыеРеквизиты.Добавить("ИНН");
	ПроверяемыеРеквизиты.Добавить("КоличествоДнейОтпуска");
	ПроверяемыеРеквизиты.Добавить("НомерПриказа");
	ПроверяемыеРеквизиты.Добавить("Организация");
	Если РасширеннаяРеализация() Тогда
		ПроверяемыеРеквизиты.Добавить("Отпуск");
	КонецЕсли;
	ПроверяемыеРеквизиты.Добавить("Сотрудник");
	ПроверяемыеРеквизиты.Добавить("СотрудникСНИЛС");
	ПроверяемыеРеквизиты.Добавить("СреднийДневнойЗаработок");
	ПроверяемыеРеквизиты.Добавить("СуммаКВыплате");
	ПроверяемыеРеквизиты.Добавить("РегистрационныйНомерСФР");
	Если ВидДокумента = Перечисления.ВидыСведенийДляОплатыОтпускаСФР.Перерасчет Тогда
		ПроверяемыеРеквизиты.Добавить("ИдентификаторПервичногоДокумента");
	КонецЕсли;

	ПроверитьСведенияОЗастрахованномЛице(Отказ, ПроверяемыеРеквизиты);
	ПроверитьСпособВыплаты(Отказ, ПроверяемыеРеквизиты);
	ПроверитьПредставителяСФР(Отказ, ПроверяемыеРеквизиты);
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение недостающих полей.
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ДатаСоздания) Тогда
		ДатаСоздания = ТекущаяДата(); // АПК:143 Для фильтрации событий в журнале регистрации требуется дата сервера.
	КонецЕсли;
	
	// Заполнение текста XML.
	ЭтоПроведение = (РежимЗаписи = РежимЗаписиДокумента.Проведение
		Или (Проведен И РежимЗаписи = РежимЗаписиДокумента.Запись));
	Если ЭтоПроведение Тогда
		ТекстXML = Документы.СведенияДляОплатыОтпускаСФР.ТекстXML(ЭтотОбъект);
	Иначе
		ТекстXML = "";
	КонецЕсли;
	ХранилищеXML = Новый ХранилищеЗначения(ТекстXML, Новый СжатиеДанных(9));
	
	// Чтение реквизитов "До записи" для проверки и подготовки сведений к процедуре ПриЗаписи.
	ЗначенияРеквизитовДоЗаписи = ЗначенияРеквизитовДоЗаписи();
	ДополнительныеСвойства.Вставить("ЗначенияРеквизитовДоЗаписи", ЗначенияРеквизитовДоЗаписи);
	
	// Проверка отсутствия критичных изменений в отправленном документе.
	Если ОбъектЗафиксирован() Тогда
		Если ЗначениеЗаполнено(ЗначенияРеквизитовДоЗаписи.ФизическоеЛицо)
			И ЗначенияРеквизитовДоЗаписи.ФизическоеЛицо <> ФизическоеЛицо Тогда
			ВызватьИсключение НСтр("ru = 'Недопустимо изменять физическое лицо в отправленном документе.';
									|en = 'You cannot change an individual in the sent document.'");
		КонецЕсли;
		Если ЗначениеЗаполнено(ЗначенияРеквизитовДоЗаписи.Страхователь)
			И ЗначенияРеквизитовДоЗаписи.Страхователь <> Страхователь Тогда
			ВызватьИсключение НСтр("ru = 'Недопустимо изменять страхователя в отправленном документе.';
									|en = 'Cannot change an insurant in the sent document.'");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	РегистрыСведений.РегистрацииСведенийДляОплатыОтпускаСФР.ЗаполнитьПоДокументу(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПередЗаписью

Функция ЗначенияРеквизитовДоЗаписи()
	ИменаРеквизитов = "Дата, ГоловнаяОрганизация, Страхователь, ФизическоеЛицо, ПометкаУдаления, Проведен";
	ЭтоНовый = ЭтоНовый();
	Если ЭтоНовый Тогда
		Значения = ОбщегоНазначенияБЗК.ЗначенияСвойств(ЭтотОбъект, ИменаРеквизитов);
	Иначе
		Значения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов);
	КонецЕсли;
	Значения.Вставить("ЭтоНовый", ЭтоНовый);
	Возврат Значения;
КонецФункции

#КонецОбласти

#Область ФиксацияВторичныхДанныхВДокументах

Функция Менеджер()
	Возврат Документы.СведенияДляОплатыОтпускаСФР;
КонецФункции

Функция ОбъектЗафиксирован() Экспорт
	Возврат Менеджер().ОбъектЗафиксирован(ЭтотОбъект);
КонецФункции

Функция ОбновитьВторичныеДанные(ПараметрыФиксации = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Модифицирован = Ложь;
	
	Если ОбъектЗафиксирован() Тогда
		Возврат Модифицирован;
	КонецЕсли;
	
	Если ПараметрыФиксации = Неопределено Тогда
		ПараметрыФиксации = Менеджер().ПараметрыФиксацииВторичныхДанных(ЭтотОбъект);
	КонецЕсли;
	
	ОрганизацияДо = Организация;
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ЗаполнитьДанныеОтпуска(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьДанныеОрганизации(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьВидДокумента(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если (Не ЗначениеЗаполнено(ПредставительСФР) Или ОрганизацияДо <> Организация)
		И СЭДОФСС.ЗаполнитьПодписантов(ЭтотОбъект, ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьКадровыеДанныеПредставителя(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьКадровыеДанныеСотрудника(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьСпособВыплатыПособия(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьКонтактыФизическогоЛица(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьКодыАдреса(ПараметрыФиксации, "АдресПроживания", Истина, Истина) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьДанныеСреднегоЗаработка(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если РассчитатьОплатуОтпуска(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Возврат Модифицирован;
КонецФункции

Функция ЗаполнитьДанныеОтпуска(ПараметрыФиксации) Экспорт
	Если Не РасширеннаяРеализация() Или Не ЗначениеЗаполнено(Отпуск) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	РеквизитыОтпуска = ДанныеОтпуска();
	ПриказДата = ЗарплатаКадрыОтчеты.ДатаНаПечать(РеквизитыОтпуска.Дата, РеквизитыОтпуска.ДатаПервичногоДокумента);
	ПриказНомер = ЗарплатаКадрыОтчеты.НомерНаПечать(РеквизитыОтпуска.Номер, РеквизитыОтпуска.НомерПервичногоДокумента);
	ВидОтпуска = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.ОтпускНаСанаторноКурортноеЛечение");
	Отбор = Новый Структура("ВидОтпуска", ВидОтпуска);
	СтрокиОтпуска = РеквизитыОтпуска.ДополнительныеОтпуска.НайтиСтроки(Отбор);

	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ДатаПриказа",	ПриказДата);
	Реквизиты.Вставить("НомерПриказа",	ПриказНомер);
	Реквизиты.Вставить("Организация",	РеквизитыОтпуска.Организация);
	Реквизиты.Вставить("Сотрудник",		РеквизитыОтпуска.Сотрудник);
	Если СтрокиОтпуска.Количество() = 0 Тогда
		Реквизиты.Вставить("ДатаНачалаОтпуска", '00010101');
		Реквизиты.Вставить("ДатаОкончанияОтпуска", '00010101');
		Реквизиты.Вставить("КоличествоДнейОтпуска", 0);
	Иначе
		Реквизиты.Вставить("ДатаНачалаОтпуска", СтрокиОтпуска[0].ДатаНачала);
		Реквизиты.Вставить("ДатаОкончанияОтпуска", СтрокиОтпуска[0].ДатаОкончания);
		Реквизиты.Вставить("КоличествоДнейОтпуска", СтрокиОтпуска[0].КоличествоДней);
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ОбновитьДанныеСреднегоЗаработка(ПараметрыФиксации, ИмяСобытия) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Модифицирован = Ложь;
	
	Если ИмяСобытия = "Заполнение" Тогда
		ДополнительныеСвойства.Вставить("Заполнение", Истина);
	КонецЕсли;
	
	Если ИмяСобытия <> "Расчет" И ЗаполнитьДанныеСреднегоЗаработка(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если РассчитатьОплатуОтпуска(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Возврат Модифицирован;
КонецФункции

Функция ЗаполнитьДанныеОрганизации(ПараметрыФиксации)
	// Головная организация заполняется безусловно, т.к. определяет права.
	ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Организация);
	
	Реквизиты = Новый Структура("Страхователь, РегистрационныйНомерСФР, ИНН");
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитыШапкиЗафиксированы(ЭтотОбъект, Реквизиты) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Сведения = СЭДОФСС.СведенияОСтрахователе(Организация, "РегНомерСФР,ИННЮЛ", Дата);
		Реквизиты.Страхователь            = Сведения.Страхователь;
		Реквизиты.РегистрационныйНомерСФР = Сведения.РегНомерСФР;
		Реквизиты.ИНН                     = Сведения.ИННЮЛ;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьВидДокумента(ПараметрыФиксации)
	Реквизиты = Новый Структура("ВидДокумента,ПервичныйДокумент",
		Перечисления.ВидыСведенийДляОплатыОтпускаСФР.Первичный, Неопределено);
	ИмяРеквизита = "ИдентификаторПервичногоДокумента";
	
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитыШапкиЗафиксированы(ЭтотОбъект, Реквизиты) Тогда
		Если ЗначениеЗаполнено(ПервичныйДокумент) Тогда
			Реквизиты = Новый Структура(ИмяРеквизита, ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПервичныйДокумент, ИмяРеквизита));
			Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	Реквизиты.Вставить(ИмяРеквизита, "");
	ЗаполнитьПервичныйДокумент(Реквизиты);
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьКадровыеДанныеПредставителя(ПараметрыФиксации)
	Реквизиты = Новый Структура("ПредставительФамилия, ПредставительИмя, ПредставительОтчество,
		|ПредставительТелефон, ПредставительЭлектроннаяПочта");
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитыШапкиЗафиксированы(ЭтотОбъект, Реквизиты) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПредставительСФР) Тогда
		ИменаПолей = "Фамилия, Имя, Отчество, ТелефонРабочийПредставление, EMailПредставление";
		КадровыеДанные = КадровыйУчет.КадровыеДанныеФизическогоЛица(Ложь, ПредставительСФР, ИменаПолей, ТекущаяДатаСеанса());
		Если КадровыеДанные <> Неопределено Тогда
			Реквизиты.ПредставительФамилия          = КадровыеДанные.Фамилия;
			Реквизиты.ПредставительИмя              = КадровыеДанные.Имя;
			Реквизиты.ПредставительОтчество         = КадровыеДанные.Отчество;
			Реквизиты.ПредставительТелефон          = КадровыеДанные.ТелефонРабочийПредставление;
			Реквизиты.ПредставительЭлектроннаяПочта = КадровыеДанные.EMailПредставление;
		КонецЕсли;
	КонецЕсли;
	
	Если (Не ЗначениеЗаполнено(Реквизиты.ПредставительТелефон)
			Или Не ЗначениеЗаполнено(Реквизиты.ПредставительЭлектроннаяПочта))
		И ЗначениеЗаполнено(Организация) Тогда
		ИменаПолей = "ТелОрганизации, АдресЭлектроннойПочтыОрганизации";
		СведенияОбОрганизации = СЭДОФСС.СведенияОСтрахователе(Организация, ИменаПолей, ТекущаяДатаСеанса());
		Если Не ЗначениеЗаполнено(Реквизиты.ПредставительТелефон) Тогда
			Реквизиты.ПредставительТелефон = СведенияОбОрганизации.ТелОрганизации;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Реквизиты.ПредставительЭлектроннаяПочта) Тогда
			Реквизиты.ПредставительЭлектроннаяПочта = СведенияОбОрганизации.АдресЭлектроннойПочтыОрганизации;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьКадровыеДанныеСотрудника(ПараметрыФиксации)
	ЕстьИзменения = Ложь;
	КадровыеДанные = Неопределено;
	ИменаПолей = "СотрудникФамилия,СотрудникИмя,СотрудникОтчество,СотрудникСНИЛС,КодСтраныГражданства";
	ИменаРеквизитов = "Фамилия,Имя,Отчество,СтраховойНомерПФР,Страна";
	
	Если ЗначениеЗаполнено(Сотрудник) Тогда
		КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудника(Истина, Сотрудник, ИменаРеквизитов, ТекущаяДатаСеанса());
	КонецЕсли;
	
	// Физлицо заполняется безусловно, т.к. определяет права.
	НовоеФизлицо = ?(КадровыеДанные = Неопределено, Неопределено, КадровыеДанные.ФизическоеЛицо);
	Если ФиксацияВторичныхДанныхВДокументах.ИзменитьЗначениеРеквизита(ФизическоеЛицо, НовоеФизлицо) Тогда
		ЕстьИзменения = Истина;
	КонецЕсли;
	
	Реквизиты = Новый Структура(ИменаПолей);
	Если КадровыеДанные <> Неопределено Тогда
		Реквизиты.СотрудникФамилия	= КадровыеДанные.Фамилия;
		Реквизиты.СотрудникИмя		= КадровыеДанные.Имя;
		Реквизиты.СотрудникОтчество	= КадровыеДанные.Отчество;
		Реквизиты.СотрудникСНИЛС	= КадровыеДанные.СтраховойНомерПФР;
		Если ЗначениеЗаполнено(КадровыеДанные.Страна) Тогда
			Реквизиты.КодСтраныГражданства = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КадровыеДанные.Страна, "Код");
		КонецЕсли;
	КонецЕсли;
	Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации) Тогда
		ЕстьИзменения = Истина;
	КонецЕсли;
	
	Возврат ЕстьИзменения;
КонецФункции

Функция ЗаполнитьСпособВыплатыПособия(ПараметрыФиксации)
	// Получение сведений о способах выплаты.
	Если ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		НастройкиПрямыхВыплат = СпособыПрямыхВыплатФСС.НастройкиСотрудников(Организация, ФизическоеЛицо, Неопределено);
		СпособПрямыхвыплат    = НастройкиПрямыхВыплат.ТаблицаРезультат[0];
		СпособЗаполнен        = Не (СпособПрямыхвыплат.ОпределеноАвтоматически
			И СпособПрямыхвыплат.Значение = КадровыйУчет.ВидКонтактнойИнформацииАдресМестаПроживанияФизическогоЛица());
	Иначе
		НастройкиПрямыхВыплат = Неопределено;
		СпособПрямыхвыплат    = Неопределено;
		СпособЗаполнен        = Ложь;
	КонецЕсли;
	// Заполнение первичных сведений о способе выплаты.
	ЕстьИзменения = Ложь;
	РеквизитыСпособа = РеквизитыСпособаПрямыхвыплат(СпособПрямыхвыплат);
	Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(РеквизитыСпособа, ЭтотОбъект, ПараметрыФиксации) Тогда
		ЕстьИзменения = Истина;
	КонецЕсли;
	// Заполнение банковских реквизитов.
	РеквизитыБанка = РеквизитыБанка(НастройкиПрямыхВыплат, СпособПрямыхвыплат);
	Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(РеквизитыБанка, ЭтотОбъект, ПараметрыФиксации) Тогда
		ЕстьИзменения = Истина;
	КонецЕсли;
	Возврат ЕстьИзменения;
КонецФункции

Функция РеквизитыСпособаПрямыхвыплат(СпособПрямыхвыплат)
	РеквизитыСпособа = Новый Структура("СпособВыплатыПособия, КартаМИР, Банк, НомерСчета, АдресПроживанияВид");
	Если СпособПрямыхвыплат = Неопределено Тогда
		Возврат РеквизитыСпособа;
	КонецЕсли;
	
	ТипыСпособовПрямыхВыплат = СпособыПрямыхВыплатФСС.ТипыСпособовПрямыхВыплат();
	ТипСпособа = ТипыСпособовПрямыхВыплат.Найти(ТипЗнч(СпособПрямыхвыплат.Значение), "Тип");
	Если ТипСпособа = Неопределено Тогда
		Возврат РеквизитыСпособа;
	КонецЕсли;
	
	РеквизитыСпособа.СпособВыплатыПособия = ТипСпособа.СпособВыплатыПособия;
	Если РеквизитыСпособа.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.НаКартуМИР Тогда
		РеквизитыСпособа.КартаМИР           = СпособПрямыхвыплат.Значение;
	ИначеЕсли РеквизитыСпособа.СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ЧерезБанк Тогда
		РеквизитыСпособа.Банк               = СпособПрямыхвыплат.Банк;
		РеквизитыСпособа.НомерСчета         = СпособПрямыхвыплат.НомерСчета;
	Иначе
		РеквизитыСпособа.АдресПроживанияВид = СпособПрямыхвыплат.Значение;
	КонецЕсли;
	
	Возврат РеквизитыСпособа;
КонецФункции

Функция РеквизитыБанка(НастройкиПрямыхВыплат, СпособПрямыхвыплат)
	РеквизитыБанка = Новый Структура("БанкНаименование, БанкБИК, БанкКоррСчет");
	Если Не ЗначениеЗаполнено(Банк) Тогда
		Возврат РеквизитыБанка;
	КонецЕсли;
	
	// Определение способа прямых выплат по выбранному банку.
	Если НастройкиПрямыхВыплат <> Неопределено
		И (СпособПрямыхвыплат = Неопределено Или Банк <> СпособПрямыхвыплат.Банк) Тогда
		Найденные = НастройкиПрямыхВыплат.БанковскиеРеквизиты.НайтиСтроки(Новый Структура("Банк, ПометкаУдаления", Банк, Ложь));
		Если Найденные.Количество() = 0 Тогда
			Найденные = НастройкиПрямыхВыплат.БанковскиеРеквизиты.НайтиСтроки(Новый Структура("Банк", Банк));
		КонецЕсли;
		Если Найденные.Количество() = 0 Тогда
			СпособПрямыхвыплат = Неопределено;
		Иначе
			СпособПрямыхвыплат = Найденные[0];
		КонецЕсли;
	КонецЕсли;
	
	// Определение реквизитов банка.
	Если СпособПрямыхвыплат = Неопределено Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Банк, "Наименование, Код, КоррСчет");
		РеквизитыБанка.БанкНаименование = Реквизиты.Наименование;
		РеквизитыБанка.БанкБИК          = Реквизиты.Код;
		РеквизитыБанка.БанкКоррСчет     = Реквизиты.КоррСчет;
	Иначе
		РеквизитыБанка.БанкНаименование = СпособПрямыхвыплат.НаименованиеБанка;
		РеквизитыБанка.БанкБИК          = СпособПрямыхвыплат.БИК;
		РеквизитыБанка.БанкКоррСчет     = СпособПрямыхвыплат.КоррСчет;
	КонецЕсли;
	
	Возврат РеквизитыБанка;
КонецФункции

Функция ЗаполнитьКонтактыФизическогоЛица(ПараметрыФиксации)
	Реквизиты = Новый Структура("АдресПроживания");
	
	Если ЗначениеЗаполнено(ФизическоеЛицо) И ЗначениеЗаполнено(АдресПроживанияВид) Тогда
		ВидыКИ = Новый Массив;
		ВидыКИ.Добавить(АдресПроживанияВид);
		
		ТаблицаКИ = КонтактнаяИнформацияБЗК.КонтактнаяИнформацияОбъектов(ФизическоеЛицо, , ВидыКИ);
		
		СтрокаКИ = ТаблицаКИ.Найти(АдресПроживанияВид, "Вид");
		Если СтрокаКИ <> Неопределено Тогда
			Реквизиты.АдресПроживания = СтрокаКИ.Значение;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации, Истина);
КонецФункции

Функция ЗаполнитьКодыАдреса(ПараметрыФиксации, ИмяПоляАдрес, ЕстьПолеЗаПределамиРФ, ЕстьСтроение)
	Суффиксы = "Индекс, Регион, Район, НаселенныйПункт, Улица, Дом, Корпус, Квартира, ФИАСДом, ФИАСРО";
	Если ЕстьПолеЗаПределамиРФ Тогда
		Суффиксы = Суффиксы + ", ЗаПределамиРФ";
	КонецЕсли;
	Если ЕстьСтроение Тогда
		Суффиксы = Суффиксы + ", Строение";
	КонецЕсли;
	Массив = СтрРазделить(Суффиксы, ", ", Ложь);
	ИменаРеквизитов = ИмяПоляАдрес + СтрСоединить(Массив, ", " + ИмяПоляАдрес);
	Реквизиты = Новый Структура(ИменаРеквизитов);
	
	Адрес = ЭтотОбъект[ИмяПоляАдрес];
	Если Не ЗначениеЗаполнено(Адрес) Тогда
		Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("БезПредставлений",               Не ЕстьПолеЗаПределамиРФ);
	ДополнительныеПараметры.Вставить("КодыАдреса",                     Истина);
	ДополнительныеПараметры.Вставить("КодыКЛАДР",                      Ложь);
	ДополнительныеПараметры.Вставить("НаименованиеВключаетСокращение", Истина);
	ДополнительныеПараметры.Вставить("ПроверитьАдрес",                 Ложь);
	СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(Адрес, ДополнительныеПараметры);
	
	ИменаПолей = "Индекс, Регион, Район, НаселенныйПункт, Улица, Дом, Корпус, Строение, Сооружение, Участок, Квартира, МуниципальныйРайон";
	СтруктураАдреса = КонтактнаяИнформацияБЗК.СвернутьСведенияОбАдресе(СведенияОбАдресе, ИменаПолей);
	Если ЕстьСтроение Тогда
		СтроениеДо = СтруктураАдреса.Строение;
		СтруктураАдреса.Строение = "";
		СтруктураАдреса.Корпус = КонтактнаяИнформацияБЗК.ПредставлениеКорпусов(СтруктураАдреса, 30);
		СтруктураАдреса.Строение = СтроениеДо;
	Иначе
		СтруктураАдреса.Корпус = КонтактнаяИнформацияБЗК.ПредставлениеКорпусов(СтруктураАдреса, 30);
		СтруктураАдреса.Удалить("Строение");
	КонецЕсли;
	СтруктураАдреса.Удалить("Сооружение");
	СтруктураАдреса.Удалить("Участок");
	Если Не ЗначениеЗаполнено(СтруктураАдреса.Район) И ЗначениеЗаполнено(СтруктураАдреса.МуниципальныйРайон) Тогда
		СтруктураАдреса.Район = СтруктураАдреса.МуниципальныйРайон
	КонецЕсли;
	СтруктураАдреса.Удалить("МуниципальныйРайон");
	Если Не ЗначениеЗаполнено(СтруктураАдреса.НаселенныйПункт) Тогда
		СтруктураАдреса.НаселенныйПункт = СтруктураАдреса.Регион;
	КонецЕсли;
	
	СтруктураАдреса.Вставить("ФИАСДом", Строка(СведенияОбАдресе.ИдентификаторДома));
	СтруктураАдреса.Вставить("ФИАСРО",  Строка(СведенияОбАдресе.ИдентификаторАдресногоОбъекта));
	Если Не ЗначениеЗаполнено(СтруктураАдреса.ФИАСДом)
		И СтруктураАдреса.ФИАСРО <> СведенияОбАдресе.ИдентификаторЗемельногоУчастка Тогда
		СтруктураАдреса.ФИАСДом = Строка(СведенияОбАдресе.ИдентификаторЗемельногоУчастка);
	КонецЕсли;
	
	Если ЕстьПолеЗаПределамиРФ Тогда
		Если СведенияОбАдресе.КодСтраны = "643" Тогда
			ЗаПределамиРФ = "";
		Иначе
			ЗаПределамиРФ = СведенияОбАдресе.Представление;
		КонецЕсли;
		СтруктураАдреса.Вставить("ЗаПределамиРФ", ЗаПределамиРФ);
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из СтруктураАдреса Цикл
		Реквизиты[ИмяПоляАдрес + КлючИЗначение.Ключ] = КлючИЗначение.Значение;
	КонецЦикла;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьДанныеСреднегоЗаработка(ПараметрыФиксации)
	Возврат ПрямыеВыплатыПособийСоциальногоСтрахования.ОбновитьДанныеСреднегоЗаработкаДляОплатыОтпускаСФР(ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция РассчитатьОплатуОтпуска(ПараметрыФиксации)
	Модифицирован = Ложь;
	Реквизиты = Новый Структура();
	ИтогоСуммаВыплат = СреднийЗаработок.Итог("СуммаВыплат");
	ИтогоКалендарныхДней = СреднийЗаработок.Итог("КалендарныхДней");
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "ЗаработокВключаяПремию") Тогда
		ЗаработокВключаяПремиюДляРасчета = ЗаработокВключаяПремию;
	Иначе
		ЗаработокВключаяПремиюДляРасчета= ИтогоСуммаВыплат + СуммаПремии;
		Реквизиты.Вставить("ЗаработокВключаяПремию", ЗаработокВключаяПремиюДляРасчета);
	КонецЕсли;
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "СреднийДневнойЗаработок") Тогда
		СреднийДневнойЗаработокДляРасчета = СреднийДневнойЗаработок;
	Иначе
		Если РасширеннаяРеализация() Тогда
			МодульУчетСреднегоЗаработка = ОбщегоНазначения.ОбщийМодуль("УчетСреднегоЗаработкаКлиентСервер");
			СреднийДневнойЗаработокДляРасчета = МодульУчетСреднегоЗаработка.СреднийЗаработок(
				ЗаработокВключаяПремиюДляРасчета, ИтогоКалендарныхДней);
		Иначе
			СреднийДневнойЗаработокДляРасчета = ?(ИтогоКалендарныхДней = 0, 0, Окр(ЗаработокВключаяПремиюДляРасчета / ИтогоКалендарныхДней, 2));
		КонецЕсли;
		Реквизиты.Вставить("СреднийДневнойЗаработок", СреднийДневнойЗаработокДляРасчета);
	КонецЕсли;
	Если Не ФиксацияВторичныхДанныхВДокументах.РеквизитШапкиЗафиксирован(ЭтотОбъект, "СуммаКВыплате") Тогда
		Реквизиты.Вставить("СуммаКВыплате", СреднийДневнойЗаработокДляРасчета * КоличествоДнейОтпуска);
	КонецЕсли;
	Если Реквизиты.Количество() > 0 Тогда
		Модифицирован = ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
	КонецЕсли;
	
	Возврат Модифицирован;
КонецФункции

Функция ДанныеОтпуска() Экспорт
	РеквизитыОтпуска = ОбщегоНазначенияБЗК.ЗначениеСвойства(ДополнительныеСвойства, "РеквизитыОтпуска");
	Если РеквизитыОтпуска <> Неопределено Тогда
		Возврат РеквизитыОтпуска;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Отпуск) Тогда
		Возврат Неопределено;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ * ИЗ Документ.Отпуск ГДЕ Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Отпуск);
	РеквизитыОтпуска = Запрос.Выполнить().Выгрузить()[0];
	ДополнительныеСвойства.Вставить("РеквизитыОтпуска", РеквизитыОтпуска);
	Возврат РеквизитыОтпуска;
КонецФункции

Процедура ЗаполнитьПервичныйДокумент(Реквизиты)
	СтатусыСообщения = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("VACATIONRETURN,VACATIONNEEDDOC,VACATIONPAY");
	
	Запрос = Новый Запрос;
	
	Если РасширеннаяРеализация() Тогда
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА СведенияДляОплатыОтпускаСФР.ПервичныйДокумент = ЗНАЧЕНИЕ(Документ.СведенияДляОплатыОтпускаСФР.ПустаяСсылка)
			|			ТОГДА СведенияДляОплатыОтпускаСФР.Ссылка
			|		ИНАЧЕ СведенияДляОплатыОтпускаСФР.ПервичныйДокумент
			|	КОНЕЦ КАК ПервичныйДокумент,
			|	СведенияДляОплатыОтпускаСФР.ДатаОтправки КАК ДатаОтправки,
			|	ВЫБОР
			|		КОГДА СведенияДляОплатыОтпускаСФР.ИдентификаторПервичногоДокумента = """"
			|			ТОГДА СведенияДляОплатыОтпускаСФР.ИдентификаторСообщения
			|		ИНАЧЕ СведенияДляОплатыОтпускаСФР.ИдентификаторПервичногоДокумента
			|	КОНЕЦ КАК ИдентификаторПервичногоДокумента
			|ПОМЕСТИТЬ ДокументыСведений
			|ИЗ
			|	РегистрСведений.РегистрацииСведенийДляОплатыОтпускаСФР КАК РегистрацииСведенийДляОплатыОтпускаСФР
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СведенияДляОплатыОтпускаСФР КАК СведенияДляОплатыОтпускаСФР
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Отпуск КАК ОтпускСФР
			|			ПО СведенияДляОплатыОтпускаСФР.Отпуск = ОтпускСФР.Ссылка
			|		ПО РегистрацииСведенийДляОплатыОтпускаСФР.ИсходящийДокумент = СведенияДляОплатыОтпускаСФР.Ссылка
			|ГДЕ
			|	ОтпускСФР.Ссылка = &Отпуск
			|	И ОтпускСФР.Проведен
			|	И СведенияДляОплатыОтпускаСФР.Проведен
			|	И СведенияДляОплатыОтпускаСФР.ИдентификаторСообщения <> """"
			|	И ЕСТЬNULL(РегистрацииСведенийДляОплатыОтпускаСФР.РегистрацияСтатус, """") В (&СтатусыСообщения)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА СведенияДляОплатыОтпускаСФР.ПервичныйДокумент = ЗНАЧЕНИЕ(Документ.СведенияДляОплатыОтпускаСФР.ПустаяСсылка)
			|			ТОГДА СведенияДляОплатыОтпускаСФР.Ссылка
			|		ИНАЧЕ СведенияДляОплатыОтпускаСФР.ПервичныйДокумент
			|	КОНЕЦ,
			|	СведенияДляОплатыОтпускаСФР.ДатаОтправки,
			|	ВЫБОР
			|		КОГДА СведенияДляОплатыОтпускаСФР.ИдентификаторПервичногоДокумента = """"
			|			ТОГДА СведенияДляОплатыОтпускаСФР.ИдентификаторСообщения
			|		ИНАЧЕ СведенияДляОплатыОтпускаСФР.ИдентификаторПервичногоДокумента
			|	КОНЕЦ
			|ИЗ
			|	РегистрСведений.РегистрацииСведенийДляОплатыОтпускаСФР КАК РегистрацииСведенийДляОплатыОтпускаСФР
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СведенияДляОплатыОтпускаСФР КАК СведенияДляОплатыОтпускаСФР
			|		ПО РегистрацииСведенийДляОплатыОтпускаСФР.ИсходящийДокумент = СведенияДляОплатыОтпускаСФР.Ссылка
			|ГДЕ
			|	СведенияДляОплатыОтпускаСФР.Ссылка = &ДокументОснование
			|	И СведенияДляОплатыОтпускаСФР.Проведен
			|	И СведенияДляОплатыОтпускаСФР.ИдентификаторСообщения <> """"
			|	И ЕСТЬNULL(РегистрацииСведенийДляОплатыОтпускаСФР.РегистрацияСтатус, """") В (&СтатусыСообщения)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА СведенияДляОплатыОтпускаСФР.ПервичныйДокумент = ЗНАЧЕНИЕ(Документ.СведенияДляОплатыОтпускаСФР.ПустаяСсылка)
			|			ТОГДА СведенияДляОплатыОтпускаСФР.Ссылка
			|		ИНАЧЕ СведенияДляОплатыОтпускаСФР.ПервичныйДокумент
			|	КОНЕЦ,
			|	СведенияДляОплатыОтпускаСФР.ДатаОтправки,
			|	ВЫБОР
			|		КОГДА СведенияДляОплатыОтпускаСФР.ИдентификаторПервичногоДокумента = """"
			|			ТОГДА СведенияДляОплатыОтпускаСФР.ИдентификаторСообщения
			|		ИНАЧЕ СведенияДляОплатыОтпускаСФР.ИдентификаторПервичногоДокумента
			|	КОНЕЦ
			|ИЗ
			|	РегистрСведений.РегистрацииСведенийДляОплатыОтпускаСФР КАК РегистрацииСведенийДляОплатыОтпускаСФР
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СведенияДляОплатыОтпускаСФР КАК СведенияДляОплатыОтпускаСФР
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Отпуск КАК ИсправленныйОтпускСФР
			|			ПО СведенияДляОплатыОтпускаСФР.Отпуск = &РасширеннаяРеализация
			|		ПО РегистрацииСведенийДляОплатыОтпускаСФР.ИсходящийДокумент = СведенияДляОплатыОтпускаСФР.Ссылка
			|ГДЕ
			|	ИсправленныйОтпускСФР.Ссылка = &Отпуск
			|	И ИсправленныйОтпускСФР.Проведен
			|	И СведенияДляОплатыОтпускаСФР.Проведен
			|	И СведенияДляОплатыОтпускаСФР.ИдентификаторСообщения <> """"
			|	И ЕСТЬNULL(РегистрацииСведенийДляОплатыОтпускаСФР.РегистрацияСтатус, """") В (&СтатусыСообщения)";
		// Для возможности открыть конструктором, заменим условие
		Если ЗначениеЗаполнено(ДокументОснование) Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РасширеннаяРеализация", "ИсправленныйОтпускСФР.ИсправленныйДокумент");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&РасширеннаяРеализация", "Ложь");
		КонецЕсли;
		Запрос.УстановитьПараметр("Отпуск", Отпуск);
	Иначе
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА СведенияДляОплатыОтпускаСФР.ПервичныйДокумент = ЗНАЧЕНИЕ(Документ.СведенияДляОплатыОтпускаСФР.ПустаяСсылка)
			|			ТОГДА СведенияДляОплатыОтпускаСФР.Ссылка
			|		ИНАЧЕ СведенияДляОплатыОтпускаСФР.ПервичныйДокумент
			|	КОНЕЦ КАК ПервичныйДокумент,
			|	СведенияДляОплатыОтпускаСФР.ДатаОтправки КАК ДатаОтправки,
			|	ВЫБОР
			|		КОГДА СведенияДляОплатыОтпускаСФР.ИдентификаторПервичногоДокумента = """"
			|			ТОГДА СведенияДляОплатыОтпускаСФР.ИдентификаторСообщения
			|		ИНАЧЕ СведенияДляОплатыОтпускаСФР.ИдентификаторПервичногоДокумента
			|	КОНЕЦ КАК ИдентификаторПервичногоДокумента
			|ПОМЕСТИТЬ ДокументыСведений
			|ИЗ
			|	РегистрСведений.РегистрацииСведенийДляОплатыОтпускаСФР КАК РегистрацииСведенийДляОплатыОтпускаСФР
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СведенияДляОплатыОтпускаСФР КАК СведенияДляОплатыОтпускаСФР
			|		ПО РегистрацииСведенийДляОплатыОтпускаСФР.ИсходящийДокумент = СведенияДляОплатыОтпускаСФР.Ссылка
			|ГДЕ
			|	СведенияДляОплатыОтпускаСФР.Ссылка = &ДокументОснование
			|	И СведенияДляОплатыОтпускаСФР.Проведен
			|	И СведенияДляОплатыОтпускаСФР.ИдентификаторСообщения <> """"
			|	И ЕСТЬNULL(РегистрацииСведенийДляОплатыОтпускаСФР.РегистрацияСтатус, """") В (&СтатусыСообщения)";
	КонецЕсли;
		
	Запрос.Текст = Запрос.Текст + ";" + Символы.ПС + 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ДокументыСведений.ПервичныйДокумент КАК ПервичныйДокумент,
		|	ДокументыСведений.ИдентификаторПервичногоДокумента КАК ИдентификаторПервичногоДокумента
		|ИЗ
		|	ДокументыСведений КАК ДокументыСведений
		|ГДЕ
		|	ДокументыСведений.ПервичныйДокумент <> &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДокументыСведений.ДатаОтправки";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("СтатусыСообщения", СтатусыСообщения);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(Реквизиты, Выборка);
	Реквизиты.ВидДокумента = Перечисления.ВидыСведенийДляОплатыОтпускаСФР.Перерасчет;
КонецПроцедуры

Функция РасширеннаяРеализация()
	Возврат Документы.СведенияДляОплатыОтпускаСФР.РасширеннаяРеализация();
КонецФункции

#КонецОбласти

#Область ОбработкаПроверкиЗаполнения

Процедура ПроверитьСведенияОЗастрахованномЛице(Отказ, ПроверяемыеРеквизиты)
	// СНИЛС.
	Если Не ЗначениеЗаполнено(СотрудникСНИЛС) Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "СотрудникСНИЛС");
	Иначе
		ТекстОшибки = "";
		Если Не РегламентированныеДанныеКлиентСервер.СтраховойНомерПФРСоответствуетТребованиям(СотрудникСНИЛС, ТекстОшибки) Тогда
			Текст = НСтр("ru = 'Ошибка в СНИЛС %1: %2';
						|en = 'Error in SNILS %1: %2'");
			Текст = СтрШаблон(Текст, СотрудникСНИЛС, ТекстОшибки);
			СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, "СотрудникСНИЛС");
		КонецЕсли;
	КонецЕсли;
	
	// ФИО.
	ПроверяемыеРеквизиты.Добавить("СотрудникФамилия");
	ПроверяемыеРеквизиты.Добавить("СотрудникИмя");
КонецПроцедуры

Процедура ПроверитьСпособВыплаты(Отказ, ПроверяемыеРеквизиты)
	Если СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ПочтовымПереводом Тогда
		ЭтоРезидент = (КодСтраныГражданства = "643" Или КодСтраныГражданства = "");
		Если Не ЭтоРезидент Тогда
			ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "АдресПроживанияЗаПределамиРФ");
		Иначе
			ПроверитьАдрес(Отказ, "АдресПроживания");
		КонецЕсли;
	ИначеЕсли СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.НаКартуМИР Тогда
		ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "КартаМИР");
	ИначеЕсли СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ЧерезБанк Тогда
		Если ЗначениеЗаполнено(БанкБИК) И ЗначениеЗаполнено(НомерСчета) Тогда
			РезультатПроверки = ПроверкиБЗК.РезультатПроверкиНомераСчета(НомерСчета, БанкБИК, БанкКоррСчет);
			Если Не РезультатПроверки.Успех Тогда
				СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, РезультатПроверки.Пояснение, "НомерСчета");
			КонецЕсли;
		Иначе
			ПроверяемыеРеквизиты.Добавить("Банк");
			ПроверяемыеРеквизиты.Добавить("БанкБИК");
			ПроверяемыеРеквизиты.Добавить("НомерСчета");
		КонецЕсли;
	ИначеЕсли СпособВыплатыПособия = Перечисления.СпособыВыплатыПособия.ИнаяОрганизация Тогда
		ПроверяемыеРеквизиты.Добавить("ИнаяОрганизацияНаименование");
		ПроверяемыеРеквизиты.Добавить("ИнаяОрганизацияНомерСчетаСтрахователя");
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьАдрес(Отказ, ИмяПоляАдрес)
	ТекстОшибки = ТекстОшибкиЗаполненияАдреса(ИмяПоляАдрес);
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		Текст = Метаданные().Реквизиты[ИмяПоляАдрес].Представление() + ": " + ТекстОшибки;
		СообщенияБЗК.СообщитьОбОшибкеВОбъекте(Отказ, ЭтотОбъект, Текст, ИмяПоляАдрес);
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьПредставителяСФР(Отказ, ПроверяемыеРеквизиты)
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ПредставительФамилия");
	ПроверкиБЗК.ПроверитьЗаполнениеРеквизитаОбъекта(Отказ, ЭтотОбъект, "ПредставительИмя");
КонецПроцедуры

Функция ТекстОшибкиЗаполненияАдреса(ИмяПоляАдрес)
	ЗначениеАдреса = ЭтотОбъект[ИмяПоляАдрес];
	Если Не ЗначениеЗаполнено(ЗначениеАдреса) Тогда
		Возврат НСтр("ru = 'Адрес не заполнен';
					|en = 'Address is required.'");
	КонецЕсли;
	
	ТекстыОшибок = Новый Массив;
	
	Индекс  = ЭтотОбъект[ИмяПоляАдрес + "Индекс"];
	ФИАСДом = ЭтотОбъект[ИмяПоляАдрес + "ФИАСДом"];
	ФИАСРО  = ЭтотОбъект[ИмяПоляАдрес + "ФИАСРО"];
	
	Если Не ЗначениеЗаполнено(Индекс) Тогда
		ТекстыОшибок.Добавить(НСтр("ru = 'Не заполнен индекс.';
									|en = 'Postal code is not filled in.'"));
	ИначеЕсли СтрДлина(Индекс) <> 6 Тогда
		ТекстыОшибок.Добавить(СтрШаблон(НСтр("ru = 'Индекс занимает %1 символов, ожидалось 6.';
											|en = 'Zip code is %1 characters long, 6 was expected.'"), СтрДлина(Индекс)));
	Иначе
		ЛишниеСимволы = СтроковыеФункцииБЗККлиентСервер.УдалитьЦифрыИзСтроки(Индекс);
		Если Не ПустаяСтрока(ЛишниеСимволы) Тогда
			Если СтрДлина(ЛишниеСимволы) = 1 Тогда
				Текст = НСтр("ru = 'В индексе должны быть только цифры (%1 - не цифра).';
							|en = 'Zip code should contain numbers only (%1 is not a number).'");
			Иначе
				Текст = НСтр("ru = 'В индексе должны быть только цифры (%1 - не цифры).';
							|en = 'Postal code must contain digits only (%1 are not digits).'");
			КонецЕсли;
			ТекстыОшибок.Добавить(СтрШаблон(Текст, ЛишниеСимволы));
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ФИАСДом) И Не ЗначениеЗаполнено(ФИАСРО) Тогда
		ТекстыОшибок.Добавить(НСтр("ru = 'Не заполнен идентификатор ФИАС.';
									|en = 'FIAS ID is not filled in.'"));
	КонецЕсли;
	
	Возврат СтрСоединить(ТекстыОшибок, Символы.ПС);
КонецФункции

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли