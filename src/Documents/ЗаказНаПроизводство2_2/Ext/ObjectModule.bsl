
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Устанавливает статус для объекта документа
//
// Параметры:
//	НовыйСтатус - Строка - Имя статуса, который будет установлен у заказов
//	ДополнительныеПараметры - Структура - Структура дополнительных параметров установки статуса
//								Конструктор структуры: ЗаказыСервер.СтруктураКорректировкиСтрокЗаказа().
//
// Возвращаемое значение:
//	Булево - Истина, в случае успешной установки нового статуса.
//
Функция УстановитьСтатус(НовыйСтатус, ДополнительныеПараметры) Экспорт
	
	ЗначениеНовогоСтатуса = Перечисления.СтатусыЗаказовНаПроизводство2_2[НовыйСтатус];
	
	Статус = ЗначениеНовогоСтатуса;
	
	Возврат ПроверитьЗаполнение();
	
КонецФункции

// Отменить продукцию заказа.
// 
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура
//  Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//  Спецификация - СправочникСсылка.РесурсныеСпецификации
//  Количество - Число
//  ПричинаОтмены - СправочникСсылка.ПричиныОтменыПроизводства
//
Процедура ОтменитьПродукцию(Номенклатура, Характеристика, Спецификация, Знач Количество, ПричинаОтмены) Экспорт
	
	СтрокиКОбработке = Новый Массив;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Номенклатура", Номенклатура);
	СтруктураПоиска.Вставить("Характеристика", Характеристика);
	СтруктураПоиска.Вставить("Спецификация", Спецификация);
	СтруктураПоиска.Вставить("Отменено", Ложь);
	
	НайденныеСтроки = Продукция.НайтиСтроки(СтруктураПоиска);
	Для Индекс = -НайденныеСтроки.ВГраница() По 0 Цикл
		Строка = НайденныеСтроки[-Индекс];
		
		Если Строка.Количество > Количество Тогда
			НоваяСтрока = Продукция.Вставить(Продукция.Индекс(Строка)+1);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка,, "Количество, КоличествоУпаковок, КлючНоменклатура");
			НоваяСтрока.Количество = Количество;
			НоваяСтрока.Отменено = Истина;
			НоваяСтрока.ПричинаОтмены = ПричинаОтмены;
			СтрокиКОбработке.Добавить(НоваяСтрока);
			
			Строка.Количество = Строка.Количество - Количество;
			СтрокиКОбработке.Добавить(Строка);
			
			Количество = 0;
		Иначе
			Строка.Отменено = Истина;
			Строка.ПричинаОтмены = ПричинаОтмены;
			Количество = Количество - Строка.Количество;
		КонецЕсли;
		
		Если Количество = 0 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если СтрокиКОбработке.Количество() <> 0 Тогда
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
		
		КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
		
		ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
			СтрокиКОбработке,
			СтруктураДействий,
			Продукция,
			КэшированныеЗначения);
	КонецЕсли;
	
КонецПроцедуры

// Заменить продукцию заказа.
// 
// Параметры:
//  КлючиПродукции - Структура - содержит:
//    * Номенклатура - СправочникСсылка.Номенклатура
//    * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//    * Спецификация - СправочникСсылка.РесурсныеСпецификации
//  Количество - Число
//  ПричинаОтмены - СправочникСсылка.ПричиныОтменыПроизводства
//  КлючиНовойПродукции - Структура - содержит:
//    * Номенклатура - СправочникСсылка.Номенклатура
//    * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
//    * Спецификация - СправочникСсылка.РесурсныеСпецификации
//
Процедура ЗаменитьПродукцию(КлючиПродукции, Знач Количество, ПричинаОтмены, КлючиНовойПродукции) Экспорт
	
	СтрокиКОбработке = Новый Массив;
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Номенклатура", КлючиПродукции.Номенклатура);
	СтруктураПоиска.Вставить("Характеристика", КлючиПродукции.Характеристика);
	СтруктураПоиска.Вставить("Спецификация", КлючиПродукции.Спецификация);
	СтруктураПоиска.Вставить("Отменено", Ложь);
	
	НайденныеСтроки = Продукция.НайтиСтроки(СтруктураПоиска);
	Для Индекс = -НайденныеСтроки.ВГраница() По 0 Цикл
		Строка = НайденныеСтроки[-Индекс];
		СтрокаОтмена = Неопределено;
		
		Если Строка.Количество > Количество Тогда
			НоваяСтрока = Продукция.Вставить(Продукция.Индекс(Строка)+1);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка,, "Количество, КоличествоУпаковок, КлючНоменклатура");
			НоваяСтрока.Количество = Количество;
			НоваяСтрока.Отменено = Истина;
			НоваяСтрока.ПричинаОтмены = ПричинаОтмены;
			СтрокиКОбработке.Добавить(НоваяСтрока);
			СтрокаОтмена = НоваяСтрока;
			
			Строка.Количество = Строка.Количество - Количество;
			СтрокиКОбработке.Добавить(Строка);
			
			Количество = 0;
		Иначе
			Строка.Отменено = Истина;
			Строка.ПричинаОтмены = ПричинаОтмены;
			СтрокаОтмена = Строка;
			Количество = Количество - Строка.Количество;
		КонецЕсли;
		
		НоваяСтрока = Продукция.Вставить(Продукция.Индекс(СтрокаОтмена)+1);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОтмена,, "Номенклатура, Характеристика, Спецификация, Отменено, ПричинаОтмены, КлючНоменклатура");
		НоваяСтрока.Номенклатура = КлючиНовойПродукции.Номенклатура;
		НоваяСтрока.Характеристика = КлючиНовойПродукции.Характеристика;
		НоваяСтрока.Спецификация = КлючиНовойПродукции.Спецификация;
		Если Не НоваяСтрока.Упаковка.Пустая() Тогда
			НоваяСтрока.Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ИдентичнаяУпаковка(
				СтрокаОтмена.Номенклатура, НоваяСтрока.Номенклатура, НоваяСтрока.Упаковка);
		КонецЕсли;
		Если СтрокиКОбработке.Количество() <> 0 Тогда
			СтрокиКОбработке.Добавить(НоваяСтрока);
		КонецЕсли;
		
		Если Количество = 0 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если СтрокиКОбработке.Количество() <> 0 Тогда
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
		
		КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
		
		ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
			СтрокиКОбработке,
			СтруктураДействий,
			Продукция,
			КэшированныеЗначения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Автор = Пользователи.АвторизованныйПользователь();
	
	ТипПроизводственногоПроцесса = Метаданные.Документы.ЗаказНаПроизводство2_2.Реквизиты.ТипПроизводственногоПроцесса.ЗначениеЗаполнения;
	ДинамическаяСтруктура = Документы.ЗаказНаПроизводство2_2.ДоступенРасчетСтруктурыЗаказа(ЭтотОбъект);
	НачатьНеРанее = НачалоДня(ТекущаяДатаСеанса());
	РазмещениеВыпуска = ?(ДинамическаяСтруктура, 
		Перечисления.СпособыПривязкиОперацийПроизводства.КОкончанию, // не используется в динамической структуре
		Метаданные.Документы.ЗаказНаПроизводство2_2.Реквизиты.РазмещениеВыпуска.ЗначениеЗаполнения);

	УстановитьПриоритетИНомерОчередиПоПотребности = Ложь;
	ОчиститьХозяйственнуюОперацию = Ложь;

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("РеквизитыШапки") Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения.РеквизитыШапки);
		Иначе
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		КонецЕсли;
		
		Если ДанныеЗаполнения.Свойство("Основание") Тогда // создание на основании заказа
			
			ТипОснования = ТипЗнч(ДанныеЗаполнения.Основание);
			Если ТипОснования = Тип("ДокументСсылка.ЗаказКлиента") Тогда
				ЗаполнитьПоЗаказуКлиента(ДанныеЗаполнения);
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЗаказНаПеремещение") Тогда
				ЗаполнитьПоЗаказуНаПеремещение(ДанныеЗаполнения);
				Назначения = Продукция.Выгрузить(, "Назначение").ВыгрузитьКолонку("Назначение");
				ХозяйственнаяОперация = ДавальческаяСхема.ХозяйственнаяОперацияПроизводства(Назначения);
				ОчиститьХозяйственнуюОперацию = Не ЗначениеЗаполнено(ХозяйственнаяОперация);
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента") Тогда
				ЗаполнитьПоЗаявкаНаВозврат(ДанныеЗаполнения);
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЗаказНаВнутреннееПотребление") Тогда
				ЗаполнитьПоЗаказуНаВнутреннееПотребление(ДанныеЗаполнения);
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЗаказНаРемонт") Тогда
				ЗаполнитьПоЗаказуНаРемонт(ДанныеЗаполнения);
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЗаказНаСборку") Тогда
				ЗаполнитьПоЗаказуНаСборку(ДанныеЗаполнения);
			ИначеЕсли ТипОснования = Тип("ДокументСсылка.ЗаказМатериаловВПроизводство") Тогда
				ЗаполнитьПоЗаказуМатериаловВПроизводство(ДанныеЗаполнения);
				Назначения = Продукция.Выгрузить(, "Назначение").ВыгрузитьКолонку("Назначение");
				ХозяйственнаяОперация = ДавальческаяСхема.ХозяйственнаяОперацияПроизводства(Назначения);
				ОчиститьХозяйственнуюОперацию = Не ЗначениеЗаполнено(ХозяйственнаяОперация);
			КонецЕсли;
			
		ИначеЕсли ДанныеЗаполнения.Свойство("Товары") Тогда // заполнение из обработки ОбеспечениеПотребностей, ФормированиеЗаказовНаПроизводствоПоПлану
			
			ЗаполнитьПоТаблицеТовары(ДанныеЗаполнения.Товары);
			
			УстановитьПриоритетИНомерОчередиПоПотребности = Истина;
			
		КонецЕсли;
		
	//++ Устарело_Переработка24
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказДавальца") Тогда
		ЗаполнитьПоЗаказуДавальца(ДанныеЗаполнения);
	//-- Устарело_Переработка24
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказДавальца2_5") Тогда
		ЗаполнитьПоЗаказуДавальца2_5(ДанныеЗаполнения);
	КонецЕсли;
	
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	Подразделение = ЗначениеНастроекПовтИсп.ПолучитьПодразделениеДиспетчерПоУмолчанию(Подразделение, Ответственный);
	
	Если УстановитьПриоритетИНомерОчередиПоПотребности Тогда
		Если ДанныеЗаполнения.Свойство("Источник") И ДанныеЗаполнения.Источник = "ФормированиеЗаказовНаПроизводствоПоПлану" Тогда
				УстановитьНовыйНомерОчередиПоДатеПотребности();
			Иначе
				УстановитьПриоритетИНомерОчередиПоНазначениюПотребности();
		КонецЕсли;
	КонецЕсли;

	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	Приоритет = ЗначениеНастроекПовтИсп.ПолучитьПриоритетПоУмолчанию(Приоритет);
	Ответственный = Пользователи.ТекущийПользователь();

	ПараметрыЗаполнения = Документы.ЗаказНаПроизводство2_2.ПараметрыЗаполненияВидаДеятельностиНДС(ЭтотОбъект);
	УчетНДСУП.ЗаполнитьВидДеятельностиНДС(ЗаказПодДеятельность, ПараметрыЗаполнения);
	
	ПараметрыВыбораСтатейИАналитик = Документы.ЗаказНаПроизводство2_2.ПараметрыВыбораСтатейИАналитик(ЭтотОбъект);
	ДоходыИРасходыСервер.ОбработкаЗаполнения(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	Если Не ЗначениеЗаполнено(ХозяйственнаяОперация) Тогда
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СобственноеПроизводство;
	КонецЕсли;
	Если ОчиститьХозяйственнуюОперацию Тогда
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПустаяСсылка();
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ПланируетсяГрафикПроизводства = УправлениеПроизводствомПовтИсп.ИспользуетсяГрафикПроизводства();
	
	ДатаПотребностиОбязательна = ДинамическаяСтруктура И Не ПланируетсяГрафикПроизводства
		ИЛИ Не ДинамическаяСтруктура И РазмещениеВыпуска = Перечисления.СпособыПривязкиОперацийПроизводства.КОкончанию;
		
#Область Шапка	
	
	// Подразделение-диспетчер
	Если ЗначениеЗаполнено(Подразделение) 
		И ПроизводствоСерверПовтИсп.ПараметрыПроизводственногоПодразделения(Подразделение).ПодразделениеДиспетчер <> Истина Тогда
		ТекстОшибки = НСтр("ru = 'Подразделением, ответственным за выполнение заказа, должно быть подразделение-диспетчер';
							|en = 'Dispatching unit should be a business unit responsible for the order fulfillment'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,"Подразделение", "Объект", Отказ);
	КонецЕсли;
	
	// Желаемая дата, начать не ранее, размешение выпуска
	МассивНепроверяемыхРеквизитов.Добавить("ДатаПотребности");
	
	Если Не ДатаПотребностиУказываетсяВСтроках
		И (ДатаПотребностиОбязательна И Не ЗначениеЗаполнено(ДатаПотребности)) Тогда
		ТекстОшибки = НСтр("ru = 'Желаемая дата выпуска не заполнена';
							|en = 'Requested release date is unfilled'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,"ДатаПотребностиНаФорме",, Отказ);
	КонецЕсли;
	
	Если НЕ ДатаПотребностиУказываетсяВСтроках
		И (НачатьНеРанее > ДатаПотребности И ЗначениеЗаполнено(НачатьНеРанее) И ЗначениеЗаполнено(ДатаПотребности)) Тогда
		ТекстОшибки = НСтр("ru = 'Желаемая дата выпуска меньше даты начала производства';
							|en = 'Required release date is earlier than the production start date'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,"ДатаПотребностиНаФорме",, Отказ);
	КонецЕсли;
	
	Если Статус = Перечисления.СтатусыЗаказовНаПроизводство2_2.Формируется Или ДинамическаяСтруктура Тогда
		МассивНепроверяемыхРеквизитов.Добавить("РазмещениеВыпуска");
	КонецЕсли;
	
	// Давальческое производство
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СобственноеПроизводство Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Партнер");
		МассивНепроверяемыхРеквизитов.Добавить("Договор");
	Иначе
		Если ДоговорНеОбязателен Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Договор");
		КонецЕсли;
	КонецЕсли;
	
	// Назначение выходных изделий получаемых в результате разборки должно быть заполнено в давальческой схеме.
	Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПроизводствоИзДавальческогоСырья2_5
			//++ Устарело_Переработка24
			И ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПроизводствоИзДавальческогоСырья
			//-- Устарело_Переработка24
			И Истина
		ИЛИ НЕ НазначениеУказываетсяВШапкеДокумента() Тогда
		МассивНепроверяемыхРеквизитов.Добавить("НазначениеПродукция");
	КонецЕсли;
	
	// Способ распределения затрат на выходные изделия должен быть заполнен только для заказов
	// с типом процесса "Без спецификаций"
	Если ТипПроизводственногоПроцесса <> Перечисления.ТипыПроизводственныхПроцессов.БезСпецификаций Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СпособРаспределенияЗатратНаВыходныеИзделия");
	КонецЕсли;

#КонецОбласти	
	
#Область Продукция
	
	// Проверка табличной части "Продукция"

	ЗаголовокТЧ = ПроизводствоКлиентСервер.ЗаголовокТабличнойЧастиПоТипуПроцесса(ТипПроизводственногоПроцесса);
	
	Если Не ЗначениеЗаполнено(Продукция) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не заполнена табличная часть ""%1""';
										|en = 'Table ""%1"" is not filled in'"), ЗаголовокТЧ);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Продукция",, Отказ);
		Возврат;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Продукция");
	МассивНепроверяемыхРеквизитов.Добавить("Продукция.Номенклатура");
	МассивНепроверяемыхРеквизитов.Добавить("Продукция.Характеристика");
	МассивНепроверяемыхРеквизитов.Добавить("Продукция.Склад");
	МассивНепроверяемыхРеквизитов.Добавить("Продукция.Подразделение");
	МассивНепроверяемыхРеквизитов.Добавить("Продукция.Назначение");
	МассивНепроверяемыхРеквизитов.Добавить("Продукция.Спецификация");
	МассивНепроверяемыхРеквизитов.Добавить("Продукция.ДатаПотребности");
	МассивНепроверяемыхРеквизитов.Добавить("Продукция.ПричинаОтмены");
	МассивНепроверяемыхРеквизитов.Добавить("Продукция.КоличествоУпаковок");
	МассивНепроверяемыхРеквизитов.Добавить("Продукция.Количество");
	МассивНепроверяемыхРеквизитов.Добавить("Продукция.ДоляСтоимости");
	
	// Детальная проверка строк табличной части "Продукция"
	
	РеквизитыНоменклатуры = Новый Соответствие;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СправочникНоменклатура.Ссылка КАК Номенклатура,
		|	СправочникНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры,
		|	СправочникНоменклатура.ВестиУчетПоГТД КАК ВестиУчетПоГТД,
		|	СправочникНоменклатура.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
		|	ВЫБОР
		|		КОГДА СправочникНоменклатура.ИспользованиеХарактеристик В (
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры),
		|				ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры))
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ХарактеристикиИспользуются
		|
		|ИЗ
		|	Справочник.Номенклатура КАК СправочникНоменклатура
		|ГДЕ
		|	СправочникНоменклатура.Ссылка В (&МассивСсылок)");
	Запрос.УстановитьПараметр("МассивСсылок", Продукция.ВыгрузитьКолонку("Номенклатура"));

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ИменаРеквизитов		= "ТипНоменклатуры, ХарактеристикиИспользуются, ВестиУчетПоГТД, ПрослеживаемыйТовар";
		ЗначенияРеквизитов	= Новый Структура(ИменаРеквизитов);
		
		ЗаполнитьЗначенияСвойств(ЗначенияРеквизитов, Выборка);
		РеквизитыНоменклатуры.Вставить(Выборка.Номенклатура, ЗначенияРеквизитов);
	КонецЦикла;
	
	ХарактеристикиИспользуются = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	
	ЗапретитьОформлениеОперацийСИмпортнымиТоварамиБезНомеровГТД =
		ПолучитьФункциональнуюОпцию("ЗапретитьОформлениеОперацийСИмпортнымиТоварамиБезНомеровГТД");
	
	ШаблонАдреса = НСтр("ru = 'в строке %1 списка ""%2""';
						|en = 'in line %1 of the ""%2"" list'");
	ШаблонГТД = НСтр("ru = 'В строке %1 списка ""%2"" выбрана номенклатура, для которой ведется учет по ГТД.
		|При включенной опции ""Запретить операции с товарами без номеров ГТД"" выбор такой номенклатуры запрещен.';
		|en = 'Items with CCD records are selected in line %1 of the %2 list. 
		|You cannot select such products while the ""Prohibit operations with goods without CCD numbers"" option is enabled.'");
	
	Для Каждого Строка Из Продукция Цикл
		
		АдресОшибки = СтрШаблон(ШаблонАдреса, Строка.НомерСтроки, ЗаголовокТЧ);
		
		// Проверка заполнения колонки "Номенклатура"
		Если Строка.Номенклатура.Пустая() Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Номенклатура""';
									|en = 'Column ""Items"" is not filled in'");
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "Номенклатура");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения + " " + АдресОшибки,ЭтотОбъект,Поле,,Отказ);
			Продолжить;
		КонецЕсли;
		
		// Проверка правильности выбора изделий при учете по ГТД
		Если Не Строка.Номенклатура.Пустая()
			И ЗапретитьОформлениеОперацийСИмпортнымиТоварамиБезНомеровГТД
			И РеквизитыНоменклатуры[Строка.Номенклатура].ВестиУчетПоГТД = Истина
			И Не РеквизитыНоменклатуры[Строка.Номенклатура].ПрослеживаемыйТовар = Истина
			И (РеквизитыНоменклатуры[Строка.Номенклатура].ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар
				ИЛИ РеквизитыНоменклатуры[Строка.Номенклатура].ТипНоменклатуры = Перечисления.ТипыНоменклатуры.МногооборотнаяТара) Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонГТД,
				Строка.НомерСтроки,
				ЗаголовокТЧ);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "Номенклатура");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,ЭтотОбъект, Поле,,Отказ);
			
		КонецЕсли;
		
		// Проверка заполнения колонки "Характеристика"
		Если ХарактеристикиИспользуются И РеквизитыНоменклатуры[Строка.Номенклатура].ХарактеристикиИспользуются
			И Строка.Характеристика.Пустая() Тогда
			
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Характеристика""';
									|en = 'Column ""Variant"" is not filled in'");
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "Характеристика");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения + " " + АдресОшибки,ЭтотОбъект,Поле,, Отказ);
			
		КонецЕсли;
		
		// Проверка заполнения колонки "Назначение"
		Если (ХозяйственнаяОперация = ДавальческаяСхемаКлиентСервер.ХозяйственнаяОперацияДоговора()
				//++ Устарело_Переработка24
				Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПроизводствоИзДавальческогоСырья
				//-- Устарело_Переработка24
				Или Ложь)
			И Не НазначениеУказываетсяВШапкеДокумента()
			И Не Строка.Отменено
			И Не РеквизитыНоменклатуры[Строка.Номенклатура].ТипНоменклатуры = Перечисления.ТипыНоменклатуры.МногооборотнаяТара
			И Строка.Назначение.Пустая() Тогда
			
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Назначение""';
									|en = 'Column ""Assignment"" is not filled in'");
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "Назначение");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения + " " + АдресОшибки,ЭтотОбъект,Поле,, Отказ);
			
		КонецЕсли;
		
		// Проверка заполнения колонок "Склад" и "Подразделение"
		Если РеквизитыНоменклатуры[Строка.Номенклатура].ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Работа Тогда
			
			Если Строка.СписатьНаРасходы Тогда
				
				Если Строка.Подразделение.Пустая() Тогда
					
					ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Подразделение""';
											|en = 'Column ""Business unit"" is not filled in'");
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "Подразделение");
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения + " " + АдресОшибки,ЭтотОбъект,Поле,, Отказ);
					
				КонецЕсли;
			Иначе
				
				Если Строка.Склад.Пустая() Тогда
					
					ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Склад""';
											|en = 'Column ""Warehouse"" is not filled in'");
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "Склад");
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения + " " + АдресОшибки,ЭтотОбъект,Поле,, Отказ);
					
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			Если ТипПроизводственногоПроцесса <> Перечисления.ТипыПроизводственныхПроцессов.Сборка
				И ТипПроизводственногоПроцесса <> Перечисления.ТипыПроизводственныхПроцессов.БезСпецификаций Тогда
				
				ТекстСообщения = НСтр("ru = 'Не допускается указание работы';
										|en = 'It is not allowed to specify work'");
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "Номенклатура");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения + " " + АдресОшибки,ЭтотОбъект,Поле,,Отказ);
				
			ИначеЕсли Строка.Подразделение.Пустая() Тогда
				
				ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Подразделение""';
										|en = 'Column ""Business unit"" is not filled in'");
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "Подразделение");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения + " " + АдресОшибки, ЭтотОбъект, Поле,, Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Проверка заполнения колонки "Количество"
		Если Строка.Количество = 0 И Не Строка.Отменено Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Количество""';
									|en = 'Column ""Quantity"" is not filled in'");
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "Количество");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения + " " + АдресОшибки, ЭтотОбъект, Поле,, Отказ);
		КонецЕсли;
			
		// Причина отмены обязательна для заполнения в строках без признака Отменено
		Если Строка.Отменено И Не ЗначениеЗаполнено(Строка.ПричинаОтмены) Тогда
			ТекстСообщения = НСтр("ru = 'Необходимо указать причину отмены';
									|en = 'Cancellation reason is required'");
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "ПричинаОтмены");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения + " " + АдресОшибки, ЭтотОбъект, Поле,, Отказ);
		КонецЕсли;
		
		// Проверка заполнения колонки "Спецификация"
		Если ДинамическаяСтруктура И Строка.Спецификация.Пустая() И Не Статус = Перечисления.СтатусыЗаказовНаПроизводство2_2.Формируется Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Спецификация""';
									|en = 'Column ""BOM"" is not filled in'");
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "Спецификация");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения + " " + АдресОшибки, ЭтотОбъект, Поле,, Отказ);
		КонецЕсли;

		// Проверка заполнения колонки "Дата потребности"
		Если ДатаПотребностиУказываетсяВСтроках И ДатаПотребностиОбязательна И Не ЗначениеЗаполнено(Строка.ДатаПотребности) Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Желаемая дата""';
									|en = 'The ""Requested date"" column is not filled'");
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "ДатаПотребности");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения + " " + АдресОшибки, ЭтотОбъект, Поле,, Отказ);
		КонецЕсли;
		
		// Проверка соответствия дат "Начать не ранее" и "Дата потребности"
		Если ДатаПотребностиУказываетсяВСтроках И НачатьНеРанее > Строка.ДатаПотребности И ЗначениеЗаполнено(НачатьНеРанее) И ЗначениеЗаполнено(Строка.ДатаПотребности) Тогда
			ТекстСообщения = НСтр("ru = 'Желаемая дата выпуска меньше даты начала производства';
									|en = 'The required release date is earlier than the production start date'");
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "ДатаПотребности");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения + " " + АдресОшибки, ЭтотОбъект, Поле,, Отказ);
		КонецЕсли;
		
		// Проверка заполнения колонки "Доля стоимости"
		Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.БезСпецификаций
			И ПроизводствоСервер.ТребуетсяЗаполнитьДолюСтоимости(ЭтотОбъект, Строка) Тогда
			
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Доля стоимости""';
									|en = 'Column ""Cost share"" is not filled in'");
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "ДоляСтоимости");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения + " " + АдресОшибки, ЭтотОбъект, Поле,, Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СобственноеПроизводство
			И (ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоИзДавальческогоСырья2_5")
				//++ Устарело_Переработка24
				Или ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоИзДавальческогоСырья")
				//-- Устарело_Переработка24
				Или Ложь) Тогда
		ПроверитьДавальческиеНазначенияВТЧПродукция(Отказ); // нужна проверка указания давальческих назначений.
	КонецЕсли;
	
	ПараметрыВыбораСтатейИАналитик = Документы.ЗаказНаПроизводство2_2.ПараметрыВыбораСтатейИАналитик(ЭтотОбъект);
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораСтатейИАналитик);
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ИмяТЧ = "Продукция";
	НоменклатураСервер.ПроверитьОкруглениеКоличества(ЭтотОбъект, Отказ, ПараметрыПроверки);
	
	ПроверитьКорректностьУказанияПодакцизныхТоваров(Отказ);
#КонецОбласти
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ЗаказНаПроизводство2_2Локализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если ЭтоНовый() Тогда
		СсылкаНового = ПолучитьСсылкуНового();
		Если СсылкаНового.Пустая() Тогда
			СсылкаНового = Документы.ЗаказНаПроизводство2_2.ПолучитьСсылку();
			УстановитьСсылкуНового(СсылкаНового);
		КонецЕсли;
		УникальныйИдентификатор = СсылкаНового.УникальныйИдентификатор();
	ИначеЕсли Не ЗначениеЗаполнено(УникальныйИдентификатор) Тогда
		УникальныйИдентификатор = Ссылка.УникальныйИдентификатор();
	КонецЕсли;
	Если ПустаяСтрока(УникальныйИдентификаторСтрока) Тогда
		УникальныйИдентификаторСтрока = Строка(УникальныйИдентификатор);
	КонецЕсли;
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения); 
	
	ДополнительныеСвойства.Вставить("ДанныеДокументаПередЗаписью",
		ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Приоритет,Подразделение,Очередь" + ",НачатьНеРанее,ДатаПотребности,РазмещениеВыпуска"));
	
	СтруктураЗаказаПроведениеДокументов.ЗаказНаПроизводствоПередЗаписью(ЭтотОбъект, ДополнительныеСвойства, Отказ);
	
	УстановитьНовыйНомерОчереди();
	
	Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.БезСпецификаций И Не ЗначениеЗаполнено(ПартияПроизводства) Тогда
		ПартияПроизводства = Справочники.ПартииПроизводства.ПолучитьСсылку();
	КонецЕсли;
	
	#Область ЗаполнениеНазначений
	
	ТекущийВариантОбособления = ВариантОбособления;
	
	Если ВариантОбособления.Пустая() Тогда
		ВариантОбособления = УправлениеПроизводством.ВариантОбособленияМатериаловВПроизводстве(
								ХозяйственнаяОперация,
								ДинамическаяСтруктура);
	КонецЕсли;
	
	Если ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.БезСпецификаций
		И ВариантОбособления = Перечисления.ВариантыОбособленияМатериаловВПроизводстве.ЭтапПроизводства Тогда
		ВариантОбособления = Перечисления.ВариантыОбособленияМатериаловВПроизводстве.ЗаказНаПроизводство;
	КонецЕсли;
	
	Если ДинамическаяСтруктура
		И ВариантОбособления = Перечисления.ВариантыОбособленияМатериаловВПроизводстве.НазначениеПродукции Тогда
		Если Не УкрупненныйЗаказПоНазначениямПродукции
			И ОбщегоНазначенияКлиентСервер.СвернутьМассив(Продукция.ВыгрузитьКолонку("Назначение")).Количество() > 1 Тогда
			УкрупненныйЗаказПоНазначениямПродукции = Истина;
		КонецЕсли;
	Иначе
		УкрупненныйЗаказПоНазначениямПродукции = Ложь;
	КонецЕсли;
	
	Если ТекущийВариантОбособления <> ВариантОбособления Тогда
		Назначение = Неопределено;
	КонецЕсли;
	
	ШаблонНазначения = Документы.ЗаказНаПроизводство2_2.ШаблонНазначения(ЭтотОбъект);
	ПустойШаблон = ВариантОбособления = Перечисления.ВариантыОбособленияМатериаловВПроизводстве.ЭтапПроизводства;
	ПерегенерацияНазначения = Справочники.Назначения.ПроверитьЗаполнитьПередЗаписью(Назначение, ШаблонНазначения,
		ЭтотОбъект, "НаправлениеДеятельности,Партнер,Договор,ХозяйственнаяОперация", Отказ, Ложь, ПустойШаблон);
	
	Если ПерегенерацияНазначения Тогда
		ОбособлениеПолуфабрикатовПоНазначениюЭтаповНеИспользуется = Ложь;
	КонецЕсли;
	
	Если НазначениеУказываетсяВШапкеДокумента() И (ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Сборка
		ИЛИ ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Ремонт) Тогда
		ОбщегоНазначенияУТКлиентСервер.ЗаполнитьЗначенияСвойствКоллекции(Продукция, НазначениеПродукция, "Назначение");
	КонецЕсли;
	
	#КонецОбласти
	
	ПараметрыОкругления = НоменклатураСервер.ПараметрыОкругленияКоличестваШтучныхТоваров();
	ПараметрыОкругления.ИмяТЧ = "Продукция";
	НоменклатураСервер.ОкруглитьКоличествоШтучныхТоваров(ЭтотОбъект, РежимЗаписи, ПараметрыОкругления);
	
	ПараметрыВыбораСтатейИАналитик = Документы.ЗаказНаПроизводство2_2.ПараметрыВыбораСтатейИАналитик(ЭтотОбъект);
	ДоходыИРасходыСервер.ПередЗаписью(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	Если ДатаПотребностиУказываетсяВСтроках Тогда
		НоваяДатаПотребности = Дата(1,1,1);
		
		Если Продукция.Количество() > 0 Тогда
			
			НайденныеСтроки = Продукция.НайтиСтроки(Новый Структура("Отменено", Ложь));
			Если НайденныеСтроки.Количество() > 0 Тогда
				НаборСтрок = Продукция.Выгрузить(НайденныеСтроки, "ДатаПотребности");
				НаборСтрок.Сортировать("ДатаПотребности Возр");
				НоваяДатаПотребности = НаборСтрок[0].ДатаПотребности;
			КонецЕсли;
			
		КонецЕсли;
		
		ДатаПотребности = НоваяДатаПотребности;
	Иначе
		ПроизводствоСервер.ЗаполнитьРеквизитВКоллекции(Продукция, "ДатаПотребности", ДатаПотребности);
	КонецЕсли;	
	
	// Очистка неправильных данных
	
	Для каждого СтрокаТЧ Из Продукция Цикл
		
		Если СтрокаТЧ.СписатьНаРасходы Тогда
			СтрокаТЧ.Назначение = Неопределено;
		КонецЕсли;
		
	КонецЦикла;
	
	ДополнительныеСвойства.Вставить("ПометкаУдаленияДоЗаписи",
		?(ЭтоНовый(), Ложь,
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления")));
	
	Если ЭтоНовый() Тогда
		Если Не ЗначениеЗаполнено(Автор) Тогда
			Автор = Пользователи.АвторизованныйПользователь();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
	#Область ЗаполнениеНазначений
	
	ШаблонНазначения = Документы.ЗаказНаПроизводство2_2.ШаблонНазначения(ЭтотОбъект);
	Справочники.Назначения.ПриЗаписиДокумента(Назначение, ШаблонНазначения, ЭтотОбъект, Подразделение, ЗаказПодДеятельность);
	
	#КонецОбласти
	
	#Область ЗаполнениеПартииПроизводства
	
	Если ЗначениеЗаполнено(ПартияПроизводства) Тогда
		
		ПоляПартии = Документы.ЗаказНаПроизводство2_2.ПоляПартии(ЭтотОбъект);
		ПоляПартии.ПометкаУдаления = ПометкаУдаления;
		Если Не ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.БезСпецификаций Тогда
			ПоляПартии.ПометкаУдаления = Истина;
		КонецЕсли;
		
		Справочники.ПартииПроизводства.СоздатьОбновитьПартиюПроизводства(
			ПартияПроизводства,
			ПоляПартии,
			ДоступноОбновлениеПартииПроизводства());
		
		Если Не ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.БезСпецификаций Тогда
			ПартияПроизводства = Справочники.ПартииПроизводства.ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
	#КонецОбласти
	
	СтруктураЗаказаПроведениеДокументов.ЗаказНаПроизводствоПриЗаписи(ЭтотОбъект, ДополнительныеСвойства, Отказ);
	
	ПриИзмененииПриоритетаЗаказа();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	МетаданныеРеквизиты = Метаданные.Документы.ЗаказНаПроизводство2_2.Реквизиты;
	
	УникальныйИдентификаторСтрока = "";
	УникальныйИдентификатор = Неопределено;
	ДокументОснование = Неопределено;
	Назначение = Неопределено;
	ДоговорНеОбязателен = Ложь;
	ПартияПроизводства = Неопределено;
	ВариантОбособления = Неопределено;
	ОбособлениеПолуфабрикатовПоНазначениюЭтаповНеИспользуется = Ложь;
	ОбособленноеОбеспечениеМатериаловИРабот = Ложь;
	
	Статус = МетаданныеРеквизиты.Статус.ЗначениеЗаполнения;
	Очередь = МетаданныеРеквизиты.Очередь.ЗначениеЗаполнения;
	
	ДинамическаяСтруктура =
		Документы.ЗаказНаПроизводство2_2.ДоступенРасчетСтруктурыЗаказа(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(НачатьНеРанее) Тогда
		ИспользоватьИнтервалПланированияЧас = Константы.ИспользоватьИнтервалПланированияЧас.Получить();
		НачатьНеРанее = Макс(
			НачатьНеРанее,
			?(ИспользоватьИнтервалПланированияЧас, НачалоЧаса(ТекущаяДатаСеанса()), НачалоДня(ТекущаяДатаСеанса())));
	КонецЕсли;
	
	РазмещениеВыпуска = ?(ДинамическаяСтруктура,
							Перечисления.СпособыПривязкиОперацийПроизводства.КОкончанию,
							Метаданные.Документы.ЗаказНаПроизводство2_2.Реквизиты.РазмещениеВыпуска.ЗначениеЗаполнения);
	
	СтруктураЗаказаПроведениеДокументов.ОчиститьКлючиСтрокВТабличнойЧасти(ЭтотОбъект, "Продукция", "КлючНоменклатура");
	
	ПараметрыЗаполнения = Документы.ЗаказНаПроизводство2_2.ПараметрыЗаполненияВидаДеятельностиНДС(ЭтотОбъект);
	УчетНДСУП.ЗаполнитьВидДеятельностиНДС(ЗаказПодДеятельность, ПараметрыЗаполнения);
	
	Автор = Пользователи.АвторизованныйПользователь();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ВыполнитьКонтрольЗаказаПослеПроведения(Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ВыполнитьКонтрольЗапланированныхЭтапов(Отказ, Истина);
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Заполнение

//++ Устарело_Переработка24

Процедура ЗаполнитьПоЗаказуДавальца(ЗаказДавальца)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗаказДавальца.Ссылка КАК Заказ,
	|	ЗаказДавальца.Статус КАК СтатусДокумента,
	|
	|	ЗаказДавальца.Приоритет                    КАК Приоритет,
	|	ЗаказДавальца.ТипПроизводственногоПроцесса КАК ТипПроизводственногоПроцесса,
	|
	|	ЗаказДавальца.Организация        КАК Организация,
	|	ЗаказДавальца.НалогообложениеНДС КАК ЗаказПодДеятельность,
	|	ЗаказДавальца.Партнер            КАК Партнер,
	|	ЗаказДавальца.Договор            КАК Договор,
	|	ВЫБОР 
	|		КОГДА ЗаказДавальца.НаправлениеДеятельности.УчетЗатрат
	|			ТОГДА ЗаказДавальца.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ                            КАК НаправлениеДеятельности,
	|
	// Ошибки заполнения
	|	ВЫБОР
	|		КОГДА ЗаказДавальца.Статус НЕ В
	|					(ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовДавальцев.КПроизводству),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовДавальцев.КОтгрузке),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовДавальцев.Закрыт))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                     КАК ЕстьОшибкиСтатус,
	|	НЕ ЗаказДавальца.Проведен КАК ЕстьОшибкиПроведен
	|ИЗ
	|	Документ.ЗаказДавальца КАК ЗаказДавальца
	|ГДЕ
	|	ЗаказДавальца.Ссылка = &ЗаказДавальца
	|");
	Запрос.УстановитьПараметр("ЗаказДавальца", ЗаказДавальца);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	МассивДопустимыхСтатусов = Новый Массив;
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.КПроизводству);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.КОтгрузке);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовДавальцев.Закрыт);
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
		ЗаказДавальца,
		Реквизиты.СтатусДокумента,
		Реквизиты.ЕстьОшибкиПроведен,
		Реквизиты.ЕстьОшибкиСтатус,
		МассивДопустимыхСтатусов);
	
	// Заполнение шапки
	РеквизитыШапки = "Приоритет, ТипПроизводственногоПроцесса, Организация, ЗаказПодДеятельность, НаправлениеДеятельности, Партнер, Договор";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты, РеквизитыШапки);
	ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПроизводствоИзДавальческогоСырья;
	ДокументОснование = ЗаказДавальца;
	
	МассивЗаказов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЗаказДавальца);
	ТаблицаТоваров = Документы.ЗаказНаПроизводство2_2.ОстаткиПродукцииКОбеспечениюДавальцу(МассивЗаказов, ТипПроизводственногоПроцесса);
	ЗаполнитьПоТаблицеТовары(ТаблицаТоваров);    
	
КонецПроцедуры
//-- Устарело_Переработка24

Процедура ЗаполнитьПоЗаказуДавальца2_5(ЗаказДавальца)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказДавальца2_5.Ссылка                       КАК Заказ,
	|	ЗаказДавальца2_5.Статус                       КАК СтатусДокумента,
	|	ЗаказДавальца2_5.Приоритет                    КАК Приоритет,
	|	ЗаказДавальца2_5.ТипПроизводственногоПроцесса КАК ТипПроизводственногоПроцесса,
	|	ЗаказДавальца2_5.Организация                  КАК Организация,
	|	ЗаказДавальца2_5.НалогообложениеНДС           КАК ЗаказПодДеятельность,
	|	ЗаказДавальца2_5.Партнер                      КАК Партнер,
	|	ЗаказДавальца2_5.Договор                      КАК Договор,
	|	ВЫБОР 
	|		КОГДА ЗаказДавальца2_5.НаправлениеДеятельности.УчетЗатрат
	|			ТОГДА ЗаказДавальца2_5.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ                                         КАК НаправлениеДеятельности,
	|	ЗаказДавальца2_5.Статус НЕ В (
	|						ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовДавальцев.КПроизводству),
	|						ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовДавальцев.КОтгрузке),
	|						ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовДавальцев.Закрыт)) КАК ЕстьОшибкиСтатус,
	|	НЕ ЗаказДавальца2_5.Проведен                                               КАК ЕстьОшибкиПроведен
	|ИЗ
	|	Документ.ЗаказДавальца2_5 КАК ЗаказДавальца2_5
	|ГДЕ
	|	ЗаказДавальца2_5.Ссылка = &ЗаказДавальца
	|;
	|";
	
	Запрос.УстановитьПараметр("ЗаказДавальца", ЗаказДавальца);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	ДопустимыеСтатусы = Новый Массив;
	ДопустимыеСтатусы.Добавить(Перечисления.СтатусыЗаказовДавальцев.КПроизводству);
	ДопустимыеСтатусы.Добавить(Перечисления.СтатусыЗаказовДавальцев.КОтгрузке);
	ДопустимыеСтатусы.Добавить(Перечисления.СтатусыЗаказовДавальцев.Закрыт);
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
		ЗаказДавальца,
		Реквизиты.СтатусДокумента,
		Реквизиты.ЕстьОшибкиПроведен,
		Реквизиты.ЕстьОшибкиСтатус,
		ДопустимыеСтатусы);
	
	// Заполнение шапки
	РеквизитыШапки = "Приоритет,
					 |ТипПроизводственногоПроцесса,
					 |Организация,
					 |ЗаказПодДеятельность,
					 |НаправлениеДеятельности,
					 |Партнер,
					 |Договор";
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Реквизиты, РеквизитыШапки);
	
	ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПроизводствоИзДавальческогоСырья2_5;
	ДокументОснование     = ЗаказДавальца;
	
	МассивЗаказов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЗаказДавальца);
	ТаблицаТоваров = Документы.ЗаказНаПроизводство2_2.ОстаткиПродукцииКОбеспечениюДавальцу(МассивЗаказов, ТипПроизводственногоПроцесса);
	ЗаполнитьПоТаблицеТовары(ТаблицаТоваров);
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуКлиента(ДанныеЗаполнения)
	
	ЗаказКлиента = ДанныеЗаполнения.Основание;
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗаказКлиента.Приоритет          КАК Приоритет,
		|	ЗаказКлиента.Организация        КАК Организация,
		|	ЗаказКлиента.НалогообложениеНДС КАК НалогообложениеНДС,
		|	ВЫБОР 
		|		КОГДА ЗаказКлиента.НаправлениеДеятельности.УчетЗатрат
		|			ТОГДА ЗаказКлиента.НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельности
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка = &ЗаказКлиента");
	
	Запрос.УстановитьПараметр("ЗаказКлиента", ЗаказКлиента);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	// Заполнение шапки
	Организация             = Реквизиты.Организация;
	ДокументОснование       = ЗаказКлиента;
	ЗаказПодДеятельность    = Реквизиты.НалогообложениеНДС;
	Приоритет               = Реквизиты.Приоритет;
	НаправлениеДеятельности = Реквизиты.НаправлениеДеятельности;
	
	// Заполнение табличной части
	ЗаполнитьПоТаблицеТоварыИзХранилища(ДанныеЗаполнения.АдресТовары, "ДатаОтгрузки");
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуНаПеремещение(ДанныеЗаполнения)
	
	ЗаказНаПеремещение = ДанныеЗаполнения.Основание;
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗаказНаПеремещение.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА ЗаказНаПеремещение.НаправлениеДеятельности.УчетЗатрат
		|			ТОГДА ЗаказНаПеремещение.НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельности
		|ИЗ
		|	Документ.ЗаказНаПеремещение КАК ЗаказНаПеремещение
		|ГДЕ
		|	ЗаказНаПеремещение.Ссылка = &ЗаказНаПеремещение");
	
	Запрос.УстановитьПараметр("ЗаказНаПеремещение", ЗаказНаПеремещение);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	// Заполнение шапки
	Организация             = Реквизиты.Организация;
	ДокументОснование       = ЗаказНаПеремещение;
	НаправлениеДеятельности = Реквизиты.НаправлениеДеятельности;
	
	// Заполнение табличной части
	ЗаполнитьПоТаблицеТоварыИзХранилища(ДанныеЗаполнения.АдресТовары, "ДатаОтгрузки");

КонецПроцедуры

Процедура ЗаполнитьПоЗаявкаНаВозврат(ДанныеЗаполнения)
	
	ЗаявкаНаВозврат = ДанныеЗаполнения.Основание;
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗаказКлиента.Организация КАК Организация,
		|	ЗаказКлиента.НалогообложениеНДС КАК НалогообложениеНДС,
		|	ВЫБОР
		|		КОГДА ЗаказКлиента.НаправлениеДеятельности.УчетЗатрат
		|			ТОГДА ЗаказКлиента.НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельности
		|ИЗ
		|	Документ.ЗаявкаНаВозвратТоваровОтКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка = &ЗаявкаНаВозврат");
	
	Запрос.УстановитьПараметр("ЗаявкаНаВозврат", ЗаявкаНаВозврат);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	// Заполнение шапки
	Организация				= Реквизиты.Организация;
	ДокументОснование		= ЗаявкаНаВозврат;
	ЗаказПодДеятельность	= Реквизиты.НалогообложениеНДС;
	НаправлениеДеятельности = Реквизиты.НаправлениеДеятельности;
	
	// Заполнение табличной части
	ЗаполнитьПоТаблицеТоварыИзХранилища(ДанныеЗаполнения.АдресТовары, "ДатаОтгрузки");
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуНаВнутреннееПотребление(ДанныеЗаполнения)
	
	ЗаказНаВнутреннееПотребление = ДанныеЗаполнения.Основание;
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Заказ.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА Заказ.НаправлениеДеятельности.УчетЗатрат
		|			ТОГДА Заказ.НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельности
		|ИЗ
		|	Документ.ЗаказНаВнутреннееПотребление КАК Заказ
		|ГДЕ
		|	Заказ.Ссылка = &Заказ");
	
	Запрос.УстановитьПараметр("Заказ", ЗаказНаВнутреннееПотребление);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	// Заполнение шапки
	Организация				= Реквизиты.Организация;
	ДокументОснование		= ЗаказНаВнутреннееПотребление;
	НаправлениеДеятельности = Реквизиты.НаправлениеДеятельности;
	
	// Заполнение табличной части
	ЗаполнитьПоТаблицеТоварыИзХранилища(ДанныеЗаполнения.АдресТовары, "ДатаОтгрузки");
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуМатериаловВПроизводство(ДанныеЗаполнения)
	
	ЗаказМатериаловВПроизводство = ДанныеЗаполнения.Основание;
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Заказ.Организация КАК Организация,
		|	ВЫБОР
		|		КОГДА Заказ.НаправлениеДеятельности.УчетЗатрат
		|			ТОГДА Заказ.НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельности
		|ИЗ
		|	Документ.ЗаказМатериаловВПроизводство КАК Заказ
		|ГДЕ
		|	Заказ.Ссылка = &Заказ");
	Запрос.УстановитьПараметр("Заказ", ЗаказМатериаловВПроизводство);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	// Заполнение шапки
	Организация	      = Реквизиты.Организация;
	ДокументОснование = ЗаказМатериаловВПроизводство;
	
	// Заполнение табличной части
	ЗаполнитьПоТаблицеТоварыИзХранилища(ДанныеЗаполнения.АдресТовары, "ДатаОтгрузки");
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуНаРемонт(ДанныеЗаполнения)
	
	ЗаказНаРемонт = ДанныеЗаполнения.Основание;
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Заказ.Организация КАК Организация,
		|	Заказ.ДатаНачала КАК ДатаПотребности,
		|	ВЫБОР
		|		КОГДА Заказ.НаправлениеДеятельности.УчетЗатрат
		|			ТОГДА Заказ.НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельности
		|ИЗ
		|	Документ.ЗаказНаРемонт КАК Заказ
		|ГДЕ
		|	Заказ.Ссылка = &ЗаказНаРемонт");
	
	Запрос.УстановитьПараметр("ЗаказНаРемонт", ЗаказНаРемонт);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	// Заполнение шапки
	Организация				= Реквизиты.Организация;
	ДокументОснование		= ЗаказНаРемонт;
	НаправлениеДеятельности = Реквизиты.НаправлениеДеятельности;
	ДатаПотребности         = Реквизиты.ДатаПотребности;
	
	// Заполнение табличной части
	ЗаполнитьПоТаблицеТоварыИзХранилища(ДанныеЗаполнения.АдресТовары);
	ПроизводствоСервер.ЗаполнитьРеквизитВКоллекции(Продукция, "ДатаПотребности", ДатаПотребности);
	
КонецПроцедуры

Процедура ЗаполнитьПоЗаказуНаСборку(ДанныеЗаполнения)
	
	ЗаказНаСборку = ДанныеЗаполнения.Основание;
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Заказ.Организация             КАК Организация,
		|	Заказ.НачалоСборкиРазборки    КАК ДатаПотребности,
		|	ВЫБОР
		|		КОГДА Заказ.НаправлениеДеятельности.УчетЗатрат
		|			ТОГДА Заказ.НаправлениеДеятельности
		|	КОНЕЦ КАК НаправлениеДеятельности
		|ИЗ
		|	Документ.ЗаказНаСборку КАК Заказ
		|ГДЕ
		|	Заказ.Ссылка = &ЗаказНаСборку");
		
	Запрос.УстановитьПараметр("ЗаказНаСборку", ЗаказНаСборку);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	// Заполнение шапки
	Организация				= Реквизиты.Организация;
	ДокументОснование		= ЗаказНаСборку;
	НаправлениеДеятельности = Реквизиты.НаправлениеДеятельности;
	ДатаПотребности         = Реквизиты.ДатаПотребности;
	
	// Заполнение табличной части
	ЗаполнитьПоТаблицеТоварыИзХранилища(ДанныеЗаполнения.АдресТовары);
	ПроизводствоСервер.ЗаполнитьРеквизитВКоллекции(Продукция, "ДатаПотребности", ДатаПотребности);
	
КонецПроцедуры

Процедура ЗаполнитьПоТаблицеТоварыИзХранилища(АдресТовары, ИмяКолонкиДатаПотребности = "")
	
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(АдресТовары); // ТаблицаЗначений
	УдалитьИзВременногоХранилища(АдресТовары);
	
	Если ИмяКолонкиДатаПотребности <> "" Тогда
		ТаблицаТоваров.Колонки[ИмяКолонкиДатаПотребности].Имя = "ДатаПотребности";
	КонецЕсли;
	
	ЗаполнитьПоТаблицеТовары(ТаблицаТоваров);
	
КонецПроцедуры

Процедура ЗаполнитьПоТаблицеТовары(ТаблицаТоваров)
	
	Если ТаблицаТоваров.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьСпецификация = ТаблицаТоваров.Колонки.Найти("Спецификация") <> Неопределено;
	
	ЕстьНачатьНеРанее = ТаблицаТоваров.Колонки.Найти("НачатьНеРанее") <> Неопределено;
	Если ЕстьНачатьНеРанее Тогда
		НачатьНеРанее = ТаблицаТоваров[0].НачатьНеРанее;
	КонецЕсли;

	ДоступноНесколькоДатПотребности = ДинамическаяСтруктура;
	НесколькоДатПотребности = Ложь;

	ЕстьДатаПотребности = ТаблицаТоваров.Колонки.Найти("ДатаПотребности") <> Неопределено;	
	Если ЕстьДатаПотребности Тогда
		ДатаПотребности = ТаблицаТоваров[0].ДатаПотребности;
	КонецЕсли;
	
	КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	СтруктураДействий = Новый Структура;      
	
	Если ТаблицаТоваров.Колонки.Найти("КоличествоУпаковок") = Неопределено Тогда
		СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
	конецЕсли;
	
	ОбрабатываемыеСтроки = Новый Массив;
	Для каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		СтрокаПродукция = Продукция.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПродукция, СтрокаТовара);

		Если ЕстьНачатьНеРанее Тогда
			НачатьНеРанее = Мин(НачатьНеРанее, СтрокаТовара.НачатьНеРанее);
		КонецЕсли;
		
		Если ЕстьДатаПотребности Тогда

			Если ДатаПотребности <> СтрокаТовара.ДатаПотребности И Не НесколькоДатПотребности Тогда
				НесколькоДатПотребности = Истина;
			КонецЕсли;
			
			ДатаПотребности = Мин(ДатаПотребности, СтрокаТовара.ДатаПотребности);
			
		КонецЕсли;
		
		ОбрабатываемыеСтроки.Добавить(СтрокаПродукция);
		
	КонецЦикла;
	
	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
		ОбрабатываемыеСтроки,
		СтруктураДействий,
		Продукция,
		КэшированныеЗначения);
	
	Если ЗначениеЗаполнено(ДатаПотребности) Тогда
		РазмещениеВыпуска = Перечисления.СпособыПривязкиОперацийПроизводства.КОкончанию;
	КонецЕсли;
	Если НесколькоДатПотребности Тогда
		Если Не ДоступноНесколькоДатПотребности Тогда 
			ПроизводствоСервер.ЗаполнитьРеквизитВКоллекции(Продукция, "ДатаПотребности", ДатаПотребности);
		Иначе
			ДатаПотребностиУказываетсяВСтроках = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьДанныеСпецификацииВСтроках();
	
	// если вместе с товарами передается спецификация, нужно исключить возможные конфликты 
	Если ЕстьСпецификация И ДинамическаяСтруктура Тогда 
		ИсключитьДублированиеСпецификацийВТабличнойЧасти(Продукция);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДанныеСпецификацииВСтроках(ТолькоНезаполненные = Истина)
	
	ПараметрыВыбораСпецификаций = УправлениеДаннымиОбИзделиях.ПараметрыВыбораСпецификаций(ЭтотОбъект, Документы.ЗаказНаПроизводство2_2);
	
	// Заполнение спецификаций
	МассивДанных = Новый Массив;
	МассивСтрок = Новый Массив;
	
	Для каждого Строка Из Продукция Цикл
		
		Если ТолькоНезаполненные И Не Строка.Спецификация.Пустая() Тогда
			Продолжить;
		КонецЕсли;

		ДанныеОбИзделии = УправлениеДаннымиОбИзделияхКлиентСервер.СобратьДанныеОбИзделииДляВыбораСпецификации(
			ЭтотОбъект, Строка, ПараметрыВыбораСпецификаций);
		
		МассивДанных.Добавить(ДанныеОбИзделии);
		МассивСтрок.Добавить(Строка);
		
	КонецЦикла;
	
	// Заполним спецификации
	УправлениеДаннымиОбИзделиях.ЗаполнитьСпецификациюВСтроках(МассивСтрок, МассивДанных, ПараметрыВыбораСпецификаций);
	
КонецПроцедуры

Процедура ИсключитьДублированиеСпецификацийВТабличнойЧасти(ТабличнаяЧасть, ДобавленныеСтроки = Неопределено) 
		
	Запрос = Новый Запрос();    
	Запрос.УстановитьПараметр("ТЧ", ОбщегоНазначенияУТ.ВыгрузитьТаблицуЗначений(ТабличнаяЧасть,, "Номенклатура,Характеристика,Склад,Подразделение,Назначение,Спецификация"));
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЧ.Номенклатура КАК Номенклатура,
		|	ТЧ.Характеристика КАК Характеристика,
		|	ТЧ.Склад КАК Склад,
		|	ТЧ.Подразделение КАК Подразделение,
		|	ТЧ.Назначение КАК Назначение,
		|	ТЧ.Спецификация КАК Спецификация
		|ПОМЕСТИТЬ ТЧ
		|ИЗ
		|	&ТЧ КАК ТЧ
		|;
		|
		|ВЫБРАТЬ
		|	ТЧ.Номенклатура КАК Номенклатура,
		|	ТЧ.Характеристика КАК Характеристика,
		|	ТЧ.Склад КАК Склад,
		|	ТЧ.Подразделение КАК Подразделение,
		|	ТЧ.Назначение КАК Назначение
		|ИЗ
		|	ТЧ КАК ТЧ
		|
		|СГРУППИРОВАТЬ ПО
		|	ТЧ.Номенклатура,
		|	ТЧ.Характеристика,
		|	ТЧ.Склад,
		|	ТЧ.Подразделение,
		|	ТЧ.Назначение
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТЧ.Спецификация) > 1";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОтбора = Новый Структура("Номенклатура,Характеристика,Склад,Подразделение,Назначение");
	ПроблемныеСтроки = Запрос.Выполнить().Выгрузить();
	ПроблемныеСтроки.Индексы.Добавить("Номенклатура,Характеристика,Склад,Подразделение,Назначение");
	
	ОбрабатываемыеСтроки = ?(ДобавленныеСтроки = Неопределено, 
		ТабличнаяЧасть, ДобавленныеСтроки);
			
	Для Каждого Строка Из ОбрабатываемыеСтроки Цикл
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, Строка);
		Если ПроблемныеСтроки.НайтиСтроки(СтруктураОтбора).Количество() > 0 Тогда 
			Строка.Спецификация = Неопределено;
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область Очередь

Процедура УстановитьНовыйНомерОчереди()
	
	Если Статус <> Перечисления.СтатусыЗаказовНаПроизводство2_2.КПроизводству
		ИЛИ ЗначениеЗаполнено(Очередь) Тогда
		
		Если Статус = Перечисления.СтатусыЗаказовНаПроизводство2_2.Формируется Тогда
			Очередь = 0;
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Очередь) Тогда
		Очередь = Документы.ЗаказНаПроизводство2_2.НовыйНомерОчереди();
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПриоритетИНомерОчередиПоНазначениюПотребности()
	
	Если Продукция.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НазначенияПродукции = ОбщегоНазначенияУТ.УникальныеЗначенияИзКолонкиТаблицы(Продукция, "Назначение");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НазначенияПродукции", НазначенияПродукции);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Назначения.Заказ КАК Заказ
		|ПОМЕСТИТЬ ЗаказыКОбеспечению
		|ИЗ
		|	Справочник.Назначения КАК Назначения
		|ГДЕ
		|	Назначения.Ссылка В(&НазначенияПродукции)
		|	И Назначения.Заказ <> НЕОПРЕДЕЛЕНО
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Заказ
		|;
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Т.Приоритет КАК Приоритет
		|ИЗ
		|	РегистрСведений.РеестрДокументов КАК Т
		|ГДЕ
		|	Т.Ссылка В
		|			(ВЫБРАТЬ
		|				Т.Заказ
		|			ИЗ
		|				ЗаказыКОбеспечению КАК Т)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Т.Приоритет.РеквизитДопУпорядочивания
		|;
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказыКОбеспечению.Заказ) = ТИП(Документ.ЭтапПроизводства2_2)
		|			ТОГДА ВЫРАЗИТЬ(ЗаказыКОбеспечению.Заказ КАК Документ.ЭтапПроизводства2_2).Распоряжение
		|		ИНАЧЕ ЗаказыКОбеспечению.Заказ
		|	КОНЕЦ КАК Заказ
		|ИЗ
		|	ЗаказыКОбеспечению КАК ЗаказыКОбеспечению
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ЗаказыКОбеспечению.Заказ) В (ТИП(Документ.ЗаказНаПроизводство2_2), ТИП(Документ.ЭтапПроизводства2_2))";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);

	Если Не РезультатЗапроса[1].Пустой() Тогда
		
		Выборка = РезультатЗапроса[1].Выбрать();
		Выборка.Следующий();
		
		Приоритет = Выборка.Приоритет;
		
	КонецЕсли;
	
	Если Не РезультатЗапроса[2].Пустой() Тогда
		
		Распоряжения = РезультатЗапроса[2].Выгрузить().ВыгрузитьКолонку(0);
		Очередь = Документы.ЗаказНаПроизводство2_2.НомерОчередиДляВставкиПередДокументами(Распоряжения);
		
	КонецЕсли
	
КонецПроцедуры

Процедура УстановитьНовыйНомерОчередиПоДатеПотребности()
	
	Если Не ЗначениеЗаполнено(ДатаПотребности)
		ИЛИ Приоритет.Пустая()
		ИЛИ Подразделение.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Заказы.Ссылка
		|ИЗ
		|	Документ.ЗаказНаПроизводство2_2 КАК Заказы
		|ГДЕ
		|	Заказы.Приоритет = &Приоритет
		|	И Заказы.Подразделение = &Подразделение
		|	И Заказы.ДатаПотребности > &ДатаПотребности
		|	И Заказы.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	Заказы.Приоритет.РеквизитДопУпорядочивания,
		|	Заказы.Подразделение.РеквизитДопУпорядочивания,
		|	Заказы.Очередь");
	
	Запрос.УстановитьПараметр("Приоритет", Приоритет);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ДатаПотребности", ДатаПотребности);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Распоряжения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Выборка.Ссылка);
		Очередь = Документы.ЗаказНаПроизводство2_2.НомерОчередиДляВставкиПередДокументами(Распоряжения);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КонтрольРезультатовПроведения

Процедура ВыполнитьКонтрольЗаказаПослеПроведения(Отказ)
	
	ВыполнитьКонтрольЗакрытияЗаказа(Отказ);
	ВыполнитьКонтрольЗапланированныхЭтапов(Отказ);
	
КонецПроцедуры

Процедура ВыполнитьКонтрольЗакрытияЗаказа(Отказ)
	
	Если Статус <> Перечисления.СтатусыЗаказовНаПроизводство2_2.Закрыт Тогда
		Возврат;
	КонецЕсли;
	
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(Ссылка);
	
	ПараметрыПроверки = Новый Структура("КонтрольВыполненияЗаказа");
	Запрос =
		Документы.ЗаказНаПроизводство2_2.СформироватьЗапросПроверкиПриСменеСтатуса(
			МассивСсылок,
			Статус,
			ПараметрыПроверки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Отказ = Отказ Или Не Документы.ЗаказНаПроизводство2_2.ПроверкаПередСменойСтатуса(Выборка, Статус, ПараметрыПроверки);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыполнитьКонтрольЗапланированныхЭтапов(Отказ, УдалениеПроведения = Ложь)
	
	Если Статус <> Перечисления.СтатусыЗаказовНаПроизводство2_2.Формируется
		И Не УдалениеПроведения Тогда
		Возврат;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("Документ.ЭтапПроизводства2_2");
	ЭлементБлокировки.УстановитьЗначение("Распоряжение", ЭтотОбъект.Ссылка);
	
	Блокировка.Заблокировать();

	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК Таблица
	|ГДЕ
	|	Таблица.Распоряжение = &Ссылка
	|	И Таблица.Проведен");
	
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Если УдалениеПроведения Тогда
			ТекстШаблона = НСтр("ru = 'Не удалось сделать непроведенным заказ на производство № %1 от %2';
								|en = 'Cannot make production order No.%1, %2 unposted'");
		Иначе
			ТекстШаблона = НСтр("ru = 'Не удалось провести заказ на производство № %1 от %2';
								|en = 'Cannot post production order No.%1, %2'");
		КонецЕсли;
		
		ТекстШаблона = ТекстШаблона + НСтр("ru = ', т.к. по заказу запланированы этапы.';
											|en = ', as stages are planned for the order.'");
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстШаблона,
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Номер, Ложь, Истина),
			Формат(Дата, "ДЛФ=D"));
		
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, ,, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ПроверитьДавальческиеНазначенияВТЧПродукция(Отказ) 
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Продукция", Продукция.Выгрузить(Новый Структура("Отменено", Ложь), "НомерСтроки,Назначение"));
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", ХозяйственнаяОперация);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Продукция.НомерСтроки КАК НомерСтроки,
		|	ВЫРАЗИТЬ(Продукция.Назначение КАК Справочник.Назначения) КАК Назначение
		|ПОМЕСТИТЬ ПроверяемаяТаблица
		|ИЗ
		|	&Продукция КАК Продукция
		|ИНДЕКСИРОВАТЬ ПО
		|	Назначение
		|;
		|
		|/////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.НомерСтроки КАК НомерСтроки,
		|	Таблица.ОшибкаУказанияДавальческогоНазначения КАК ОшибкаУказанияДавальческогоНазначения
		|ИЗ(
		|	ВЫБРАТЬ
		|		Продукция.НомерСтроки КАК НомерСтроки,
		|		Продукция.Назначение КАК Назначение,
		|		ВЫБОР
		|			КОГДА &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СобственноеПроизводство)
		|					И Продукция.Назначение.ТипНазначения В(
		|						ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Давальческое2_5),
		//++ Устарело_Переработка24
		|						ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеПродукция22),
		|						ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалы22),
		//-- Устарело_Переработка24
		|						ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалыПодЭтап22))
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК ОшибкаУказанияДавальческогоНазначения
		|	ИЗ
		|		ПроверяемаяТаблица КАК Продукция) КАК Таблица
		|ГДЕ
		|	Таблица.ОшибкаУказанияДавальческогоНазначения
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		
	ТаблицаОшибок = Запрос.Выполнить().Выгрузить();
	
	Для Индекс = 0 По ТаблицаОшибок.Количество() - 1 Цикл
		
		ТекущаяОшибка = ТаблицаОшибок[Индекс];
		Строка = Продукция[ТекущаяОшибка.НомерСтроки - 1];
		
		Если ТекущаяОшибка.ОшибкаУказанияДавальческогоНазначения Тогда
		
			ШаблонСообщения = НСтр("ru = 'Запрещено выбирать назначение давальца при собственном производстве (строка %1 списка ""%2"").';
									|en = 'Cannot select the provider assignment for in-house production (line %1 of the ""%2"" list).'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, Строка.НомерСтроки, НСтр("ru = 'Продукция';
																				|en = 'Manufactured product'"));
			ПолеНазначение = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Строка.НомерСтроки, "Назначение");
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				ЭтотОбъект,
				ПолеНазначение,
				Неопределено,
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДоступноОбновлениеПартииПроизводства()
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Документ.ЭтапПроизводства2_2");
	ЭлементБлокировки.УстановитьЗначение("Распоряжение", Ссылка);
	ЭлементБлокировки.УстановитьЗначение("ПартияПроизводства", ПартияПроизводства);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЭтапПроизводства22.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства22
	|ГДЕ
	|	ЭтапПроизводства22.ПартияПроизводства = &ПартияПроизводства
	|	И ЭтапПроизводства22.Проведен");
	Запрос.УстановитьПараметр("ПартияПроизводства", ПартияПроизводства);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат.Пустой();
	
КонецФункции

Функция НазначениеУказываетсяВШапкеДокумента()
	
	Если ЭтотОбъект.ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ЭтотОбъект.ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.БезСпецификаций Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура ПриИзмененииПриоритетаЗаказа()
	
	Если ДополнительныеСвойства.ПроведениеДокументов.СвойстваДокумента.ЭтоНовый Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.ПроведениеДокументов.СвойстваДокумента.Проведен
		ИЛИ ДополнительныеСвойства.ПроведениеДокументов.СвойстваДокумента.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Статус = Перечисления.СтатусыЗаказовНаПроизводство2_2.КПроизводству Тогда
		Возврат;
	КонецЕсли;

	ПриоритетИзменен = Приоритет <> ДополнительныеСвойства.ДанныеДокументаПередЗаписью.Приоритет;
	
	Если ПриоритетИзменен Тогда
		УниверсальнаяОчередьОперативныхЗаданий.ОбработатьЗадание(Ссылка, Перечисления.ЗаданияУниверсальнойОчереди.ОбновлениеПриоритетаВЭтапахПроизводства);
	КонецЕсли;
	
	ИзмененоМестоВОчереди =
		Приоритет         <> ДополнительныеСвойства.ДанныеДокументаПередЗаписью.Приоритет
		ИЛИ Подразделение <> ДополнительныеСвойства.ДанныеДокументаПередЗаписью.Подразделение
		ИЛИ Очередь       <> ДополнительныеСвойства.ДанныеДокументаПередЗаписью.Очередь;
	
	Если ДинамическаяСтруктура И ИзмененоМестоВОчереди Тогда
		УниверсальнаяОчередьОперативныхЗаданий.ОбработатьЗадание(Ссылка, Перечисления.ЗаданияУниверсальнойОчереди.ОбновлениеСтруктурыЗаказаПриИзмененииМестаВОчереди);
	КонецЕсли;
	
КонецПроцедуры
Процедура ПроверитьКорректностьУказанияПодакцизныхТоваров(Отказ)
	
	ИспользоватьПодакцизныеТовары = ПолучитьФункциональнуюОпцию("ИспользоватьПодакцизныеТовары");
	
	Если Не ИспользоватьПодакцизныеТовары Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапроса = Новый Массив;
	
	ЧастьТекстаЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки	КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура	КАК Номенклатура,
	//++ НЕ УТКА
	|	ТаблицаТоваров.Назначение	КАК Назначение,
	//-- НЕ УТКА
	|	ТаблицаТоваров.Склад		КАК Склад
	|ПОМЕСТИТЬ ВтПродукция
	|ИЗ
	|	&Продукция КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки	КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура	КАК Номенклатура,
	|	ТаблицаТоваров.Склад		КАК Склад
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	ВтПродукция КАК ТаблицаТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаТоваров.Номенклатура = СправочникНоменклатура.Ссылка
	//++ НЕ УТКА
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СправочникНазначения
	|		ПО ТаблицаТоваров.Назначение = СправочникНазначения.Ссылка
	//-- НЕ УТКА
	|
	|ГДЕ
	|	СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	//++ НЕ УТКА
	|	И НЕ (ЕСТЬNULL(СправочникНазначения.ТипНазначения, ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПустаяСсылка)) В (
	//++ Устарело_Переработка24
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеПродукция22),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалы22),
	//-- Устарело_Переработка24
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалыПодЭтап22),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Давальческое2_5))
	|	)
	//-- НЕ УТКА
	|";
	ТекстыЗапроса.Добавить(ЧастьТекстаЗапроса);
	ТекстыЗапроса.Добавить(УчетПодакцизныхТоваров.ТекстЗапросаВТПодакцизныеКатегорииТоваров());
	
	ЧастьТекстаЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаОшибок.НомерСтроки				КАК НомерСтроки,
	|	ТаблицаОшибок.УказанПодакцизныйТовар	КАК УказанПодакцизныйТовар,
	|	ТаблицаОшибок.УказанРозничныйСклад		КАК УказанРозничныйСклад
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТоваров.НомерСтроки	КАК НомерСтроки,
	|		ВЫБОР
	|			КОГДА НЕ &ИспользуетсяПроизводствоПодакцизныхТоваров
	|					И ЕСТЬNULL(ДанныеКатегорий.ПодакцизныйТовар, ЛОЖЬ)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ						КАК УказанПодакцизныйТовар,
	|		ВЫБОР
	|			КОГДА &ИспользуетсяПроизводствоПодакцизныхТоваров
	|					И ЕСТЬNULL(ДанныеКатегорий.ПодакцизныйТовар, ЛОЖЬ)
	|					И ЕСТЬNULL(СправочникСклады.ТипСклада, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.РозничныйМагазин)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ						КАК УказанРозничныйСклад
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТПодакцизныеКатегорииТоваров КАК ДанныеКатегорий
	|			ПО ТаблицаТоваров.Номенклатура = ДанныеКатегорий.Номенклатура
	|			
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СправочникСклады
	|			ПО ТаблицаТоваров.Склад = СправочникСклады.Ссылка
	|	
	|	) КАК ТаблицаОшибок
	|
	|ГДЕ
	|	(ТаблицаОшибок.УказанПодакцизныйТовар
	|		ИЛИ ТаблицаОшибок.УказанРозничныйСклад
	|	)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	ТекстыЗапроса.Добавить(ЧастьТекстаЗапроса);
	
	ВыгружаемыеКолонки = "НомерСтроки, Номенклатура, Назначение, Склад";
	
	СтранаРегистрации = ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(Организация);
	ИспользуетсяПроизводствоПодакцизныхТоваров =
		УчетПодакцизныхТоваров.ИспользуетсяПроизводствоПодакцизныхТоваров(Организация, Дата);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос.УстановитьПараметр("Дата",			Дата);
	Запрос.УстановитьПараметр("Организация",	Организация);
	Запрос.УстановитьПараметр("Страна",			СтранаРегистрации);
	Запрос.УстановитьПараметр("Продукция",		Продукция.Выгрузить(, ВыгружаемыеКолонки));
	Запрос.УстановитьПараметр("ИспользоватьПодакцизныеТовары", ИспользоватьПодакцизныеТовары);
	Запрос.УстановитьПараметр("ИспользуетсяПроизводствоПодакцизныхТоваров", ИспользуетсяПроизводствоПодакцизныхТоваров);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаголовокТЧ = ПроизводствоКлиентСервер.ЗаголовокТабличнойЧастиПоТипуПроцесса(ТипПроизводственногоПроцесса);
	
	ШаблонПодакцизныйТовар = НСтр("ru = 'В строке %1 списка ""%2"" указана подакцизная продукция.
		|Выбор такой номенклатуры запрещен, так как учетная политика финансового учета организации ""%Организация%"" не предусматривает производство подакцизной продукции.';
		|en = 'Excisable products are specified in line %1 of the ""%2"" list.
		|Cannot select this item, as the financial accounting policy of the ""%Организация%"" company does not allow producing excisable products.'");
	ШаблонПодакцизныйТовар = СтрЗаменить(ШаблонПодакцизныйТовар, "%Организация%", Строка(Организация));
	ШаблонРозничныйСклад = НСтр("ru = 'В строке %1 списка ""%2"" в качестве получателя указан розничный склад.
		|Выпуск подакцизной продукции на розничный склад запрещен.';
		|en = 'In line %1 of the ""%2"" list, the recipient is a retail warehouse.
		|Cannot release excisable goods to a retail warehouse.'");
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ИмяПоля			= "";
		ТекстСообщения	= "";
		
		Если Выборка.УказанПодакцизныйТовар Тогда
			ИмяПоля			= "Номенклатура";
			ТекстСообщения	= СтрШаблон(ШаблонПодакцизныйТовар, Выборка.НомерСтроки, ЗаголовокТЧ);
		ИначеЕсли Выборка.УказанРозничныйСклад Тогда
			ИмяПоля			= "Склад";
			ТекстСообщения	= СтрШаблон(ШаблонРозничныйСклад, Выборка.НомерСтроки, ЗаголовокТЧ);
		КонецЕсли;
		
		Если Не ПустаяСтрока(ТекстСообщения) Тогда
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Выборка.НомерСтроки, ИмяПоля);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект , Поле, "", Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
