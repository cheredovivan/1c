#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭДОБЗК.ДокументЭДОБЗКФормаСпискаПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.КомандыФормыГруппа;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПриСозданииНаСервереФормыСписка(ЭтотОбъект, "Список");
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
	// ПроцессыОбработкиДокументов
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ПроцессыОбработкиДокументовЗарплата") Тогда
		МодульПроцессыОбработкиДокументовЗарплата = ОбщегоНазначения.ОбщийМодуль("ПроцессыОбработкиДокументовЗарплата");
		МодульПроцессыОбработкиДокументовЗарплата.ПриСозданииНаСервереФормыСписка(ЭтотОбъект, "Список");
	КонецЕсли;	
	// Конец ПроцессыОбработкиДокументов
	
	СтруктураПараметровОтбора = Новый Структура();
	ИмяФОСтруктураПредприятияНеСоответствуетСтруктуреЮридическихЛиц = "СтруктураПредприятияНеСоответствуетСтруктуреЮридическихЛиц";
	ИмяТипаСтруктураПредприятия = "СправочникСсылка.СтруктураПредприятия";
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы")
		И ПолучитьФункциональнуюОпцию(ИмяФОСтруктураПредприятияНеСоответствуетСтруктуреЮридическихЛиц) Тогда
		
		ЗарплатаКадры.ДобавитьПараметрОтбора(СтруктураПараметровОтбора, "МестоВСтруктуреПредприятия",
			Новый ОписаниеТипов(ИмяТипаСтруктураПредприятия), НСтр("ru = 'Подразделение';
																	|en = 'Business unit'"), "");
	Иначе
		ЗарплатаКадры.ДобавитьПараметрОтбора(СтруктураПараметровОтбора, "Подразделение",
			Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"), НСтр("ru = 'Подразделение';
																					|en = 'Business unit'"), "");
	КонецЕсли;
	
	ЗарплатаКадры.ДобавитьПараметрОтбора(СтруктураПараметровОтбора, "ФизическоеЛицо",
		Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"), НСтр("ru = 'Сотрудник';
																	|en = 'Employee'"));
	
	ЗарплатаКадры.ДобавитьПараметрОтбора(СтруктураПараметровОтбора, "ВидОснования",
		Новый ОписаниеТипов(), НСтр("ru = 'Вид основания';
									|en = 'Basis kind'"));
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список",,
		СтруктураПараметровОтбора, "СписокКритерииОтбора");
	
	ЗапрещенныеРасширения = РаботаСФайламиСлужебный.СписокЗапрещенныхРасширений();
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора = Истина;
	Иначе
		Элементы.Список.РежимВыбора = Ложь;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВидСпискаПодменю",
		"Видимость",
		ПолучитьФункциональнуюОпцию("ИспользоватьЭДОБЗК"));
	
	УстановитьУсловноеОформление();
	ПоказыватьРеквизитыПериодаИПубликации = Ложь;
	Если Параметры.РасчетныеЛисты Тогда
		
		СкрытьПодменюВыбораВидаСписка();
		УстановитьВидСписка(ЭтотОбъект, Команды.ВсеДокументы);
		ПоказыватьРеквизитыПериодаИПубликации = Истина;
		Заголовок = НСтр("ru = 'Документы ЭДО (расчетные листки)';
						|en = 'EDI documents (payslips)'");
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "ВидДокумента", ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыДокументовЭДОБЗК.РасчетныйЛисток"),
			ВидСравненияКомпоновкиДанных.Равно, , Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	ИначеЕсли Параметры.ДокументыНаПодпись Тогда
		СкрытьПодменюВыбораВидаСписка();
		УстановитьВидСписка(ЭтотОбъект, Команды.НаПодпись);
	Иначе
		ВидСпискаПоУмолчанию = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ЭлектронныйКадровыйДокументооборот", "ВидСписка");
		Если Не ЗначениеЗаполнено(ВидСпискаПоУмолчанию)
			Или ВидСпискаПоУмолчанию = Команды.ВсеДокументы.Имя Тогда
			
			УстановитьВидСписка(ЭтотОбъект, Команды.ВсеДокументы);
		Иначе
			УстановитьВидСписка(ЭтотОбъект, Команды.ПоВидамДокументов);
		КонецЕсли;
	КонецЕсли;
	
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			Список, "СодержимоеДокументов", ЭДОБЗК.ДоступныеСодержанияДокументовПользователя(Пользователи.ТекущийПользователь()), Истина);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Период",
		"Видимость",
		ПоказыватьРеквизитыПериодаИПубликации);
	
	НайденныеПараметры = ЭтотОбъект.ПараметрыКритерияОтбора.НайтиСтроки(Новый Структура("ИмяПараметра", "ВидОснования"));
	Если НайденныеПараметры.Количество() > 0 Тогда
		Элемент = Элементы.Найти(НайденныеПараметры[0].ИмяЭлементаФормыПараметра);
		Если Элемент <> Неопределено Тогда
			Элемент.РежимВыбораИзСписка = Истина;
			Элемент.ВыбиратьТип = Ложь;
			Элемент.КнопкаОчистки = Истина;
			Элемент.СписокВыбора.Очистить();
			Элемент.СписокВыбора.Добавить(Перечисления.КатегорииДокументовЭДОБЗК.РасчетныйЛисток,
				Строка(Перечисления.КатегорииДокументовЭДОБЗК.РасчетныйЛисток));
			Элемент.СписокВыбора.Добавить(Перечисления.КатегорииДокументовЭДОБЗК.ЗаявлениеСотрудника,
				Строка(Перечисления.КатегорииДокументовЭДОБЗК.ЗаявлениеСотрудника));
			Для Каждого ТипСПечатнымиФормами Из Метаданные.ОпределяемыеТипы.ОбъектСПечатнымиФормами.Тип.Типы() Цикл
				Если ТипСПечатнымиФормами = Тип("ДокументСсылка.ДокументЭДОБЗК") Тогда
					Элемент.СписокВыбора.Добавить(ТипСПечатнымиФормами, НСтр("ru = 'Внешний документ';
																			|en = 'External document'"));
				Иначе
					Элемент.СписокВыбора.Добавить(ТипСПечатнымиФормами, Строка(ТипСПечатнымиФормами));
				КонецЕсли;
			КонецЦикла;
			Элемент.СписокВыбора.СортироватьПоПредставлению();
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТекущийИсполнитель", Пользователи.ТекущийПользователь());
	УстановитьОтборПоказыватьДокументыВыбывшихСотрудников();
	
	ЗадаватьВопросПриИзмененииВидаДокумента = ЗначениеЗадаватьВопросПриИзмененииВидаДокумента();
	ОтветНаВопросПриИзмененииВидаДокумента = ЗначениеОтветаНаВопросПриИзмененииВидаДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы Тогда
		ПередЗакрытиемНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновленыДанныеДокументовЭДОБЗК" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
		Если Поле.Имя = "ОснованиеДокумента" Тогда
			СтандартнаяОбработка = Ложь;
			ПоказатьЗначение(, Элементы.Список.ТекущиеДанные.ОснованиеДокумента);
		ИначеЕсли Поле.Имя = "СостояниеПодписей" Тогда
			СтандартнаяОбработка = Ложь;
			ВозможноМенятьСостояниеПолученияОригиналов = Истина;
			ПодписанныеОригиналыПолучены = Неопределено;
			Для Каждого ДокументЭДОБЗК Из Элементы.Список.ВыделенныеСтроки Цикл
				ТекущиеДанные = Элементы.Список.ДанныеСтроки(ДокументЭДОБЗК);
				Если ТипЗнч(ТекущиеДанные.ОснованиеДокумента) = Тип("ДокументСсылка.СогласиеНаПрисоединениеКЭДОБЗК") Тогда
					ВозможноМенятьСостояниеПолученияОригиналов = Ложь;
					ТекстПредупреждения = Новый ФорматированнаяСтрока(
						НСтр("ru = 'Для ""Согласие на присоединение к ЭДО"" получения оригиналов регистрируется в собственном ';
							|en = 'For ""Consent to join EDI"", receipt of originals is registered in its own'"),
						Новый ФорматированнаяСтрока(НСтр("ru = 'списке документов';
														|en = 'list of documents'"), , , , "e1cib/data/Документ.СогласиеНаПрисоединениеКЭДОБЗК.Форма.ФормаСписка"));
					ПоказатьПредупреждение(, ТекстПредупреждения);
					Прервать;
				КонецЕсли;
				Если ТекущиеДанные.ВариантПодписания <> ПредопределенноеЗначение("Перечисление.ВариантыПодписанияДокументовЭДОБЗК.НеобходимаСобственноручнаяПодпись")
					Или ТекущиеДанные.КоличествоПЭП = 0 Тогда
					
					ВозможноМенятьСостояниеПолученияОригиналов = Ложь;
					ПоказатьПредупреждение(, НСтр("ru = 'Выбраны строки не поддерживающие регистрацию получения оригиналов ';
													|en = 'The selected lines do not support registering original receipt'"));
					Прервать;
				КонецЕсли;
				Если ПодписанныеОригиналыПолучены = Неопределено Тогда
					ПодписанныеОригиналыПолучены = ТекущиеДанные.ПодписанныеОригиналыПолучены;
				Иначе
					Если ПодписанныеОригиналыПолучены <> ТекущиеДанные.ПодписанныеОригиналыПолучены Тогда
						ВозможноМенятьСостояниеПолученияОригиналов = Ложь;
						ПоказатьПредупреждение(, НСтр("ru = 'Выбраны строки с разным состоянием регистрации получения оригиналов ';
														|en = 'Lines with different states of original receipt registration are selected'"));
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если ВозможноМенятьСостояниеПолученияОригиналов Тогда
				ДополнительныеПараметры = Новый Структура;
				ДополнительныеПараметры.Вставить("ДокументыЭДОБЗК", Элементы.Список.ВыделенныеСтроки);
				Оповещение = Новый ОписаниеОповещения("ПриИзмененииСостоянияПодписей", ЭтотОбъект, ДополнительныеПараметры);
				СписокПунктов = Новый СписокЗначений;
				Если ПодписанныеОригиналыПолучены Тогда
					СписокПунктов.Добавить("Обновить", НСтр("ru = 'Обновить сведения о получении';
															|en = 'Update receipt information'"));
					СписокПунктов.Добавить("Очистить", НСтр("ru = 'Очистить сведения о получении';
															|en = 'Clear receipt information'"));
				Иначе
					СписокПунктов.Добавить("Зарегистрировать", НСтр("ru = 'Подписанные оригиналы получены';
																	|en = 'Signed originals are received'"));
				КонецЕсли;
				ПоказатьВыборИзСписка(Оповещение, СписокПунктов, Поле);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСостоянияПодписей(ВыбранноеЗначение, ДополнительныеПараметры) Экспорт
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		ЗарегистрироватьПолучениеПодписанныхОригиналов(ДополнительныеПараметры.ДокументыЭДОБЗК, ВыбранноеЗначение.Значение <> "Очистить");
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПараметрОтбораПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ПараметрОтбораНаФормеСДинамическимСпискомПриИзменении(ЭтотОбъект, Элемент.Имя);
	Если СтрНачинаетсяС(Элемент.Имя, "ВидОснования_") Тогда
		
		НайденныеПараметры = ЭтотОбъект.ПараметрыКритерияОтбора.НайтиСтроки(Новый Структура("ИмяПараметра", "ВидОснования"));
		Если НайденныеПараметры.Количество() > 0 Тогда
			
			Использование = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
				ЭтотОбъект, НайденныеПараметры[0].ИмяРеквизитаФормыПараметраИспользование);
			
			ВидОснования = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(
				ЭтотОбъект, НайденныеПараметры[0].ИмяРеквизитаФормыПараметра);
			
			Если ВидОснования = Неопределено Тогда
				ВидОснования = Тип("Строка");
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				Список, "Внешний", Истина,
				Использование И ВидОснования = Тип("ДокументСсылка.ДокументЭДОБЗК"));
			
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				Список, "ВидОснованияКатегория", ВидОснования,
				Использование И ТипЗнч(ВидОснования) = Тип("ПеречислениеСсылка.КатегорииДокументовЭДОБЗК"));
			
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				Список, "ВидОснованияДокумент", ВидОснования,
				Использование И ТипЗнч(ВидОснования) <> Тип("ПеречислениеСсылка.КатегорииДокументовЭДОБЗК"));
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Элемент.ТекущиеДанные <> Неопределено
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элемент.ТекущиеДанные, "Внешний") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ФормаСкопировать",
			"Доступность",
			Элемент.ТекущиеДанные.Внешний);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не Копирование Тогда
		
		Отказ = Истина;
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения());
		
		ОткрытьФорму("Документ.ДокументЭДОБЗК.ФормаОбъекта", ПараметрыОткрытия, ЭтотОбъект, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	// СтандартныеПодсистемы.КонтрольВеденияУчета
	КонтрольВеденияУчетаБЗК.ПриПолученииДанныхНаСервере(Настройки, Строки);
	// Конец СтандартныеПодсистемы.КонтрольВеденияУчета
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	Если Элементы.Список.ВыделенныеСтроки.Количество() > 0 Тогда
		ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ВсеДокументы(Команда)
	
	УстановитьВидСписка(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура НаПодпись(Команда)
	
	УстановитьВидСписка(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Подписанные(Команда)
	
	УстановитьВидСписка(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьДокументыВыбывшихСотрудников(Команда)
	
	ПоказыватьДокументыВыбывшихСотрудников = Не ПоказыватьДокументыВыбывшихСотрудников;
	УстановитьОтборПоказыватьДокументыВыбывшихСотрудников();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоВидамДокументов(Команда)
	
	УстановитьВидСписка(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.КонтрольВеденияУчета

&НаКлиенте
Процедура Подключаемый_Выбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) Экспорт
	
	КонтрольВеденияУчетаКлиентБЗК.ОткрытьОтчетПоПроблемамИзСписка(ЭтотОбъект, "Список", Поле, СтандартнаяОбработка);
	СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.КонтрольВеденияУчета

&НаСервере
Процедура НастроитьДинамическийСписокНаСервере(ОписаниеМодификации) Экспорт
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		МодульЗарплатаКадрыРасширенный = ОбщегоНазначения.ОбщийМодуль("ЗарплатаКадрыРасширенный");
		МодульЗарплатаКадрыРасширенный.НастроитьДинамическийСписокПоОписаниюМодификации(ЭтаФорма, ОписаниеМодификации);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СкрытьПодменюВыбораВидаСписка()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВидСпискаПодменю",
		"Видимость",
		Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидСписка(УправляемаяФорма, Команда)
	
	Элементы = УправляемаяФорма.Элементы;
	Команды = УправляемаяФорма.Команды;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		УправляемаяФорма.Список, "Исполнители", УправляемаяФорма.ТекущийПользователь,
		Команда = Команды.НаПодпись);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		УправляемаяФорма.Список, "Подписанты", УправляемаяФорма.ТекущийПользователь,
		Команда = Команды.Подписанные);
		
	Если Команда <> Команды.ВсеДокументы Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			УправляемаяФорма.Список, "ПометкаУдаления", Ложь, ВидСравненияКомпоновкиДанных.Равно,
			НСтр("ru = 'Не помеченные на удаление';
				|en = 'Not marked for deletion'"), Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный,
			"640dbb80-5a11-4a41-8427-547f2666567d");
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
			УправляемаяФорма.Список, "ПометкаУдаления", НСтр("ru = 'Не помеченные на удаление';
															|en = 'Not marked for deletion'"));
	КонецЕсли;
	
	УстановитьОтображениеСпискаПоВидамДокументов(УправляемаяФорма, Команда = Команды.ПоВидамДокументов);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ВидыДокументов",
		"Видимость",
		Команда = Команды.ПоВидамДокументов);
	
	Если Команда = Команды.ВсеДокументы Тогда
		УправляемаяФорма.Заголовок = НСтр("ru = 'Документы ЭДО';
											|en = 'EDI documents'");
		УстановитьПометкуВидаСпискаСписка(УправляемаяФорма, Элементы.ВсеДокументы);
	ИначеЕсли Команда = Команды.НаПодпись Тогда
		УправляемаяФорма.Заголовок = НСтр("ru = 'Документы ЭДО (на подпись мне)';
											|en = 'EDI documents (for me to sign)'");
		УстановитьПометкуВидаСпискаСписка(УправляемаяФорма, Элементы.НаПодпись);
	ИначеЕсли Команда = Команды.Подписанные Тогда
		УправляемаяФорма.Заголовок = НСтр("ru = 'Документы ЭДО (подписанные мною)';
											|en = 'EDI documents (signed by me)'");
		УстановитьПометкуВидаСпискаСписка(УправляемаяФорма, Элементы.Подписанные);
	ИначеЕсли Команда = Команды.ПоВидамДокументов Тогда
		УправляемаяФорма.Заголовок = НСтр("ru = 'Документы ЭДО (по видам)';
											|en = 'EDI documents (by kinds)'");
		УстановитьПометкуВидаСпискаСписка(УправляемаяФорма, Элементы.ПоВидамДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Использование = Истина;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование		= Истина;
	ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("Список.ЭлектронныйДокумент");
	ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.НеЗаполнено;
	ЭлементОтбора.ПравоеЗначение	= Истина;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("Список.ОснованиеДокумента"));
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ЭлектронныйДокумент");
	ОформляемоеПоле.Использование = Истина;
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Использование = Истина;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование		= Истина;
	ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("Список.ТипОснованияДокумента");
	ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение	= Тип("ДокументСсылка.ДокументЭДОБЗК");
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных(""));
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ОснованиеДокумента");
	ОформляемоеПоле.Использование = Истина;
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Использование = Истина;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование		= Истина;
	ЭлементОтбора.ЛевоеЗначение		= Новый ПолеКомпоновкиДанных("Список.ВидДокументаЭДО");
	ЭлементОтбора.ВидСравнения		= ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Список");
	ОформляемоеПоле.Использование = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПометкуВидаСпискаСписка(УправляемаяФорма, ВыбранныйВидСписка)
	
	Элементы = УправляемаяФорма.Элементы;
	Для Каждого ПодчиненныйЭлемент Из Элементы.ВидСпискаПодменю.ПодчиненныеЭлементы Цикл
		ПодчиненныйЭлемент.Пометка = ПодчиненныйЭлемент = ВыбранныйВидСписка;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗарегистрироватьПолучениеПодписанныхОригиналов(ДокументыЭДОБЗК, ОригиналыПолучены)
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСведений.СостоянияДокументовЭДОБЗК.ЗарегистрироватьПолучениеПодписанныхОригиналов(
		ДокументыЭДОБЗК, ОригиналыПолучены);
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоказыватьДокументыВыбывшихСотрудников()
	
	Элементы.ПоказыватьДокументыВыбывшихСотрудников.Пометка = ПоказыватьДокументыВыбывшихСотрудников;
	
	УстановитьПривилегированныйРежим(Истина);
	Настройки = РегистрыСведений.НастройкиИнтеграцииКабинетСотрудника.НастройкиИнтеграции();
	УстановитьПривилегированныйРежим(Ложь);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, "ДатаДляПроверкиВыбывших", ТекущаяДатаСеанса(), Не ПоказыватьДокументыВыбывшихСотрудников);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, "ДнейСохраненияПубликации", Настройки.ДнейСохраненияПубликации, Не ПоказыватьДокументыВыбывшихСотрудников);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыДокументовПриАктивизацииСтроки(Элемент)
	
	УстановитьОтображениеСпискаПоВидамДокументов(ЭтотОбъект, Истина);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтображениеСпискаПоВидамДокументов(УправляемаяФорма, Значение)
	
	Если Не Значение
		Или Не ЗначениеЗаполнено(УправляемаяФорма.Элементы.ВидыДокументов.ТекущаяСтрока) Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			УправляемаяФорма.Список, "ВидДокумента", УправляемаяФорма.Элементы.ВидыДокументов.ТекущаяСтрока, Ложь);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			УправляемаяФорма.Список, "ВидДокумента", УправляемаяФорма.Элементы.ВидыДокументов.ТекущаяСтрока, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗакрытиемНаСервере()
	ВидСпискаПоУмолчанию = Команды.ВсеДокументы.Имя;
	Если Элементы.ПоВидамДокументов.Пометка Тогда
		ВидСпискаПоУмолчанию = Команды.ПоВидамДокументов.Имя;
	КонецЕсли;
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ЭлектронныйКадровыйДокументооборот",
		"ВидСписка", ВидСпискаПоУмолчанию);
КонецПроцедуры

&НаКлиенте
Процедура ВидыДокументовПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Строка = ПредопределенноеЗначение("Справочник.ВидыДокументовЭДОБЗК.ПустаяСсылка")
		Или Элементы.ВидыДокументов.ДанныеСтроки(Строка).ЭтоГруппа Тогда
		
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВидыДокументовПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Перемещение
		И ЗначениеЗаполнено(ПараметрыПеретаскивания.Значение) Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ВидДокумента", Строка);
		ДополнительныеПараметры.Вставить("Документы", ПараметрыПеретаскивания.Значение);
		
		Если Не ЗадаватьВопросПриИзмененииВидаДокумента
			И ОтветНаВопросПриИзмененииВидаДокумента <> Неопределено Тогда
			
			Если ОтветНаВопросПриИзмененииВидаДокумента = Истина Тогда
				ЗначениеОтвета = КодВозвратаДиалога.Да;
			Иначе
				ЗначениеОтвета = КодВозвратаДиалога.Нет;
			КонецЕсли;
			
			Ответ = Новый Структура;
			Ответ.Вставить("БольшеНеЗадаватьЭтотВопрос", Истина);
			Ответ.Вставить("Значение", ЗначениеОтвета);
			
			ОбработатьОтветНаВопросОЗаменеВидаДокумента(Ответ, ДополнительныеПараметры);
		Иначе
			ТекстВопроса = НСтр("ru = 'При смене вида документа, также обновляется
				|код кадрового документа (для видов документов КЭДО).
				|
				|Продолжить?';
				|en = 'When changing the document kind, the
				|HR document code is also updated (for HR EDI document kinds).
				|
				|Continue?'");
			Оповещение = Новый ОписаниеОповещения("ОбработатьОтветНаВопросОЗаменеВидаДокумента",
				ЭтотОбъект, ДополнительныеПараметры);
			ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
			ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Истина;
			ПараметрыВопроса.БольшеНеЗадаватьЭтотВопрос = Не ЗадаватьВопросПриИзмененииВидаДокумента;
			СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(
				Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, ПараметрыВопроса);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаВопросОЗаменеВидаДокумента(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ.Значение = КодВозвратаДиалога.Да Тогда
		Результат = ЗаменитьВидДокумента(ДополнительныеПараметры.Документы, ДополнительныеПараметры.ВидДокумента);
		Если Результат > 0 Тогда
			Пояснение = СтрокаСЧислом(
				НСтр("ru = ';Обновлен %1 документ; ;Обновлено %1 документа;Обновлено %1 документов;Обновлено %1 документов';
					|en = ';%1 document is updated;;;;%1 documents are updated'"),
				Результат,
				ВидЧисловогоЗначения.Количественное);
			Состояние(Пояснение);
			Элементы.Список.Обновить();
		КонецЕсли;
	КонецЕсли;
	Если Ответ.Значение <> КодВозвратаДиалога.Отмена Тогда
		Если Ответ.БольшеНеЗадаватьЭтотВопрос И ЗадаватьВопросПриИзмененииВидаДокумента Тогда
			ЗадаватьВопросПриИзмененииВидаДокумента = Ложь;
			СохранитьЗначениеЗадаватьВопросПриИзмененииВидаДокумента(ЗадаватьВопросПриИзмененииВидаДокумента);
			Если Ответ.Значение = КодВозвратаДиалога.Да Тогда
				ОтветНаВопросПриИзмененииВидаДокумента = Истина;
			Иначе
				ОтветНаВопросПриИзмененииВидаДокумента = Ложь;
			КонецЕсли;
			СохранитьЗначениеОтветаНаВопросПриИзмененииВидаДокумента(ОтветНаВопросПриИзмененииВидаДокумента);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ЗаменитьВидДокумента(ДокументыЭДО, ВидДокумента)
	Результат = 0;
	ВидыДокументовЭДО = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ДокументыЭДО, "ВидДокумента");
	Для Каждого СсылкаНаДокумент Из ДокументыЭДО Цикл
		ТекущийВидДокумента = ВидыДокументовЭДО.Получить(СсылкаНаДокумент);
		Если ТекущийВидДокумента = ВидДокумента Тогда
			Продолжить;
		КонецЕсли;
		ОбъектДокумента = СсылкаНаДокумент.ПолучитьОбъект();
		ОбъектДокумента.ВидДокумента = ВидДокумента;
		ОбъектДокумента.Записать();
		Результат = Результат + 1;
	КонецЦикла;
	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Процедура СохранитьЗначениеЗадаватьВопросПриИзмененииВидаДокумента(ЗадаватьВопросПриИзмененииВидаДокумента)
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ЭлектронныйКадровыйДокументооборот",
		"ЗадаватьВопросПриИзмененииВидаДокумента", ЗадаватьВопросПриИзмененииВидаДокумента);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьЗначениеОтветаНаВопросПриИзмененииВидаДокумента(ОтветНаВопросПриИзмененииВидаДокумента)
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ЭлектронныйКадровыйДокументооборот",
		"ОтветНаВопросПриИзмененииВидаДокумента", ОтветНаВопросПриИзмененииВидаДокумента);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначениеЗадаватьВопросПриИзмененииВидаДокумента()
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ЭлектронныйКадровыйДокументооборот",
		"ЗадаватьВопросПриИзмененииВидаДокумента", Истина);
КонецФункции

&НаСервереБезКонтекста
Функция ЗначениеОтветаНаВопросПриИзмененииВидаДокумента()
	Возврат ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ЭлектронныйКадровыйДокументооборот",
		"ОтветНаВопросПриИзмененииВидаДокумента");
КонецФункции

&НаКлиенте
Функция ЗначенияЗаполнения()
	
	ЗначенияЗаполнения = Новый Структура;
	
	ЗначениеПараметраКомпоновкиДанных = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВидДокумента"));
	Если ЗначениеПараметраКомпоновкиДанных.Использование
		И ЗначениеЗаполнено(ЗначениеПараметраКомпоновкиДанных.Значение) Тогда
		
		ЗначенияЗаполнения.Вставить("ВидДокумента", ЗначениеПараметраКомпоновкиДанных.Значение);
	КонецЕсли;
	
	ЗначениеПараметраКомпоновкиДанных = Список.Параметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ФизическоеЛицо"));
	Если ЗначениеПараметраКомпоновкиДанных.Использование
		И ЗначениеЗаполнено(ЗначениеПараметраКомпоновкиДанных.Значение) Тогда
		
		ЗначенияЗаполнения.Вставить("ФизическоеЛицо", ЗначениеПараметраКомпоновкиДанных.Значение);
	КонецЕсли;
	
	КоллекцииОтборов = Новый Массив;
	КоллекцииОтборов.Добавить(Список.КомпоновщикНастроек.Настройки.Отбор.Элементы);
	Для Каждого ЭлементНастроек Из Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(ЭлементНастроек) = Тип("ОтборКомпоновкиДанных") Тогда
			КоллекцииОтборов.Добавить(ЭлементНастроек.Элементы);
		ИначеЕсли ТипЗнч(ЭлементНастроек) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			КоллекцииОтборов.Добавить(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭлементНастроек));
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого КоллекцияОтборов Из КоллекцииОтборов Цикл
		Для Каждого ЭлементОтбора Из КоллекцияОтборов Цикл
			Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
				Если ЭлементОтбора.Использование Тогда
					
					Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно
						И ЗначениеЗаполнено(ЭлементОтбора.ПравоеЗначение) Тогда
					
						ЗначенияЗаполнения.Вставить(ЭлементОтбора.ЛевоеЗначение ,ЭлементОтбора.ПравоеЗначение);
					ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке
						И ЭлементОтбора.ПравоеЗначение.Количество() = 1 Тогда
						
						ЗначенияЗаполнения.Вставить(ЭлементОтбора.ЛевоеЗначение ,ЭлементОтбора.ПравоеЗначение[0]);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ЗначенияЗаполнения;
	
КонецФункции

#КонецОбласти
