#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ДляВсехСтрок( ЗначениеРазрешено(ВнешниеПодписанты.ФизическоеЛицо, NULL КАК ИСТИНА))
	|	И ЗначениеРазрешено(Организация)";
КонецПроцедуры

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.ОпределитьОбъектыСКомандамиОтчетов
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Отчеты.ДокументыЭДОБЗКСОтказами.ДобавитьКомандыФормированияОтчетов(КомандыОтчетов, Параметры);
	Отчеты.ДокументыЭДОБЗКСотрудников.ДобавитьКомандыФормированияОтчетов(КомандыОтчетов, Параметры);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

// Конец СтандартныеПодсистемы.УправлениеДоступом

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	
	ОписаниеСостава = ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта();
	ОписаниеСостава.ЗаполнятьФизическиеЛицаПоСотрудникам = Ложь;
	ОписаниеСостава.ИспользоватьКраткийСостав = Истина;
	ОписаниеСостава.ИспользоватьОграничениеДоступа = Ложь;
	ОписаниеСостава.ЗаполнятьТабличнуюЧастьФизическиеЛицаДокумента = Ложь;
	
	ЗарплатаКадрыСоставДокументов.ДобавитьОписаниеХраненияСотрудниковФизическихЛиц(
		ОписаниеСостава.ОписаниеХраненияСотрудниковФизическихЛиц,
		"ВнешниеПодписанты",
		"ФизическоеЛицо");
	
	Возврат ОписаниеСостава;
	
КонецФункции

Функция СвойстваДокумента(ЭлектронныйДокумент, Организация, ФизическоеЛицо,
	ОснованиеДокумента = Неопределено, КатегорияДокумента = Неопределено, СодержимоеДокумента = Неопределено,
	ВидДокумента = Неопределено, ФорматДокументаСоответствуетТребованиям = Истина,
	ВариантПодписания = Неопределено) Экспорт
	
	Свойства = Новый Структура(
		"ЭлектронныйДокумент,
		|Организация,
		|ФизическоеЛицо,
		|ОснованиеДокумента,
		|КатегорияДокумента,
		|СодержимоеДокумента,
		|Дата,
		|ИдентификаторДокумента,
		|ИдентификаторЗаявкиКабинетСотрудника,
		|ДополнительныеСвойства,
		|НаСписокСотрудников,
		|Внешний,
		|ВидДокумента,
		|ФорматДокументаСоответствуетТребованиям,
		|ВариантПодписания");
	
	Свойства.ЭлектронныйДокумент				= ЭлектронныйДокумент;
	Свойства.Организация						= Организация;
	Свойства.ФизическоеЛицо						= ФизическоеЛицо;
	Свойства.ОснованиеДокумента					= ОснованиеДокумента;
	Свойства.КатегорияДокумента					= КатегорияДокумента;
	Свойства.СодержимоеДокумента				= СодержимоеДокумента;
	Свойства.НаСписокСотрудников 				= Ложь;
	Свойства.Внешний 							= Ложь;
	Свойства.ВидДокумента 						= ВидДокумента;
	Свойства.ФорматДокументаСоответствуетТребованиям 	= ФорматДокументаСоответствуетТребованиям;
	Если ВариантПодписания = Неопределено Тогда
		Свойства.ВариантПодписания = Перечисления.ВариантыПодписанияДокументовЭДОБЗК.Требуется;
	Иначе
		Свойства.ВариантПодписания = ВариантПодписания;
	КонецЕсли;
	
	Возврат Свойства;
	
КонецФункции

// Добавляет новый документ и возвращает на него ссылку
//
// Параметры:
//	Свойства - Структура, см.ОписаниеСоставаОбъекта
//
// Возвращаемое значение:
//	ДокументСсылка.ДокументЭДОБЗК
//
Функция Добавить(Свойства) Экспорт
	
	ДокументЭДО = НовыйДокумент(Свойства);
	ДокументЭДО.Записать();
	ЭДОБЗК.ЗарегистрироватьДокументЭДОБЗККПересчетуСостояний(ДокументЭДО.Ссылка);
	Возврат ДокументЭДО.Ссылка;
	
КонецФункции

// Возвращает вновь созданный, еще не сохраненный объект документа
//
// Параметры:
//	Свойства - Структура, см.ОписаниеСоставаОбъекта
//
// Возвращаемое значение:
//	ДокументОбъект.ДокументЭДОБЗК
//
Функция НовыйДокумент(Свойства) Экспорт
	
	ДокументЭДО = Документы.ДокументЭДОБЗК.СоздатьДокумент();
	
	Если ЗначениеЗаполнено(Свойства.Дата) Тогда
		ДокументЭДО.Дата = Свойства.Дата;
	Иначе
		ДокументЭДО.Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ДокументЭДО.ЭлектронныйДокумент =	Свойства.ЭлектронныйДокумент;
	ДокументЭДО.Организация =			Свойства.Организация;
	
	Если ТипЗнч(Свойства.ФизическоеЛицо) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		ДокументЭДО.ВнешниеПодписанты.Добавить().ФизическоеЛицо = Свойства.ФизическоеЛицо;
	Иначе
		Для Каждого ФизическоеЛицо Из Свойства.ФизическоеЛицо Цикл
			ДокументЭДО.ВнешниеПодписанты.Добавить().ФизическоеЛицо = ФизическоеЛицо;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Свойства.ОснованиеДокумента) Тогда
		ДокументЭДО.ОснованиеДокумента = Свойства.ОснованиеДокумента;
	Иначе
		СсылкаНового = Документы.ДокументЭДОБЗК.ПолучитьСсылку();
		ДокументЭДО.ОснованиеДокумента = СсылкаНового;
		ДокументЭДО.ДополнительныеСвойства.Вставить("СсылкаНового", ДокументЭДО.ОснованиеДокумента)
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Свойства.ИдентификаторДокумента) Тогда
		ДокументЭДО.ИдентификаторДокумента = Свойства.ИдентификаторДокумента;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Свойства.ИдентификаторЗаявкиКабинетСотрудника) Тогда
		ДокументЭДО.ИдентификаторЗаявкиКабинетСотрудника = Свойства.ИдентификаторЗаявкиКабинетСотрудника;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Свойства.КатегорияДокумента) Тогда
		КатегорияДокумента = Свойства.КатегорияДокумента;
	Иначе
		Если ЭДОБЗК.ЭтоОбъектСПечатнымиФормами(Свойства.ОснованиеДокумента)
			И ТипЗнч(Свойства.ОснованиеДокумента) <> Тип("ДокументСсылка.СправкаНДФЛ")
			И ТипЗнч(Свойства.ОснованиеДокумента) <> Тип("ДокументСсылка.СведенияОДоходахСотрудникаДляСоцВыплат")
			И Не ЗаявкиСотрудников.ВладелецЗаявкаСотрудника(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Свойства.ОснованиеДокумента)) Тогда
			
			КатегорияДокумента = Перечисления.КатегорииДокументовЭДОБЗК.КадровыйДокумент;
		Иначе
			КатегорияДокумента = Перечисления.КатегорииДокументовЭДОБЗК.СправкаСотруднику;
		КонецЕсли;
	КонецЕсли;
	ДокументЭДО.КатегорияДокумента = КатегорияДокумента;
	
	Если ЗначениеЗаполнено(Свойства.СодержимоеДокумента) Тогда
		ДокументЭДО.СодержимоеДокумента = Свойства.СодержимоеДокумента;
	Иначе
		ДокументЭДО.СодержимоеДокумента = ЭДОБЗК.МаксимальноДоступноеСодержимоеДокументовПользователя();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Свойства.ДополнительныеСвойства) Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(
			ДокументЭДО.ДополнительныеСвойства, Свойства.ДополнительныеСвойства, Истина);
	КонецЕсли;
	
	ДокументЭДО.НаСписокСотрудников = Свойства.НаСписокСотрудников;
	ДокументЭДО.Внешний				= Свойства.Внешний;
	
	Если ЗначениеЗаполнено(Свойства.ВидДокумента) Тогда
		ДокументЭДО.ВидДокумента = Свойства.ВидДокумента;
	Иначе
		ДокументЭДО.ВидДокумента = ЭДОБЗК.ВидДокументаПоОснованию(ДокументЭДО.ОснованиеДокумента);
	КонецЕсли;
	
	ДокументЭДО.ФорматДокументаСоответствуетТребованиям = Свойства.ФорматДокументаСоответствуетТребованиям;
	ДокументЭДО.ВариантПодписания = Свойства.ВариантПодписания;
	
	
	Возврат ДокументЭДО;
	
КонецФункции

Процедура УдалитьДокументыПечатныхФормОбъекта(СсылкаНаОбъект) Экспорт
	
	ФайлыПоОбъектам = ЭДОБЗК.ФайлыПечатныхФормПоОбъектам(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СсылкаНаОбъект), , Истина, Истина);
	
	ФайлыПечатныхФорм = Новый Массив;
	Для Каждого ОписаниеФайловОбъекта Из ФайлыПоОбъектам Цикл
		Для Каждого ОписаниеФайловИдентификатора Из ОписаниеФайловОбъекта.Значение Цикл
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ФайлыПечатныхФорм, ОписаниеФайловИдентификатора.Значение);
		КонецЦикла;
	КонецЦикла;
	
	Если ФайлыПечатныхФорм.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ЭлектронныеДокументы", ФайлыПечатныхФорм);
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ДокументЭДОБЗК.Ссылка КАК Ссылка
			|ИЗ
			|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
			|ГДЕ
			|	ДокументЭДОБЗК.ЭлектронныйДокумент В(&ЭлектронныеДокументы)
			|	И НЕ ДокументЭДОБЗК.ПометкаУдаления";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбъектДокумента = Выборка.Ссылка.ПолучитьОбъект();
			ОбъектДокумента.ДополнительныеСвойства.Вставить("РазрешенаПометкаУдаления", Истина);
			ОбъектДокумента.УстановитьПометкуУдаления(Истина);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Обновляет ответственного находя связанные ДокументыЭДОБЗК по основанию документ.
//	Параметры:
//		ОснованиеДокумента - ОпределяемыйТип.ВладелецПрисоединенныхФайлов - ссылка на документ основание;
//		Ответственный - СправочникСсылка.Пользователи - новый ответственный по документу.
Процедура ОбновитьОтветственногоПоОснованиюДокумента(ОснованиеДокумента, Ответственный) Экспорт
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДокументЭДОБЗК.Ссылка КАК ДокументЭДОБЗК
	               |ИЗ
	               |	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
	               |ГДЕ
	               |	ДокументЭДОБЗК.ОснованиеДокумента = &ОснованиеДокумента";
	
	Запрос.УстановитьПараметр("ОснованиеДокумента", ОснованиеДокумента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДокументЭДОБЗК = Выборка.ДокументЭДОБЗК.ПолучитьОбъект();
		ДокументЭДОБЗК.Ответственный = Ответственный;
		ДокументЭДОБЗК.Записать();
	КонецЦикла;
	
КонецПроцедуры

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Отчеты.ДокументыЭДОБЗКСотрудников.ДобавитьКомандуПечати(КомандыПечати);
	
КонецПроцедуры

#КонецОбласти

Процедура ПометитьНаУдалениеДокументыУдаленныхОснований(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|ГДЕ
		|	НЕ ДокументЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И НЕ ДокументЭДОБЗК.ПометкаУдаления
		|	И ДокументЭДОБЗК.ЭлектронныйДокумент.ПометкаУдаления";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Документ.ДокументЭДОБЗК", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ПометкаУдаления = Истина;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьНаличияСведенийОЗарплате(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка,
		|	ПодписанныеПечатныеФормы.ИдентификаторПечатнойФормы КАК ИдентификаторПечатнойФормы
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПодписанныеПечатныеФормы КАК ПодписанныеПечатныеФормы
		|		ПО ДокументЭДОБЗК.ЭлектронныйДокумент = ПодписанныеПечатныеФормы.ПрисоединенныйФайл
		|ГДЕ
		|	НЕ ДокументЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И ДокументЭДОБЗК.СодержимоеДокумента = ЗНАЧЕНИЕ(Перечисление.СодержимоеДокументов.ПустаяСсылка)";
	
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Документ.ДокументЭДОБЗК", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.СодержимоеДокумента = ЭДОБЗКВызовСервера.СодержимоеДокументаПечатнойФормы(Выборка.ИдентификаторПечатнойФормы);
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПодписантов(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка,
		|	ДокументЭДОБЗК.Дата КАК Дата,
		|	ДокументЭДОБЗК.УдалитьФизическоеЛицо КАК УдалитьФизическоеЛицо,
		|	ДокументЭДОБЗК.УдалитьПодписанСотрудником КАК УдалитьПодписанСотрудником,
		|	МАКСИМУМ(ЭлектронныеПодписи.Отпечаток) КАК Отпечаток,
		|	МАКСИМУМ(ЭлектронныеПодписи.ДатаПодписи) КАК ДатаПодписи
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДокументЭДОБЗК.ВнешниеПодписанты КАК ДокументЭДОБЗКВнешниеПодписанты
		|		ПО ДокументЭДОБЗК.Ссылка = ДокументЭДОБЗКВнешниеПодписанты.Ссылка
		|			И ДокументЭДОБЗК.УдалитьФизическоеЛицо = ДокументЭДОБЗКВнешниеПодписанты.ФизическоеЛицо
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|		ПО ДокументЭДОБЗК.ЭлектронныйДокумент = ЭлектронныеПодписи.ПодписанныйОбъект
		|			И ((ВЫРАЗИТЬ(ЭлектронныеПодписи.Комментарий КАК СТРОКА(1024))) <> """")
		|ГДЕ
		|	НЕ ДокументЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И ДокументЭДОБЗК.УдалитьФизическоеЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
		|	И ДокументЭДОБЗКВнешниеПодписанты.ФизическоеЛицо ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументЭДОБЗК.Ссылка,
		|	ДокументЭДОБЗК.УдалитьФизическоеЛицо,
		|	ДокументЭДОБЗК.УдалитьПодписанСотрудником,
		|	ДокументЭДОБЗК.Дата";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("Документ.ДокументЭДОБЗК");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПодписиДокументовЭДОБЗК");
			ЭлементБлокировки.УстановитьЗначение("Объект", Выборка.Ссылка);
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			НоваяСтрока = ДокументОбъект.ВнешниеПодписанты.Добавить();
			НоваяСтрока.ФизическоеЛицо = Выборка.УдалитьФизическоеЛицо;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			
			Если Выборка.УдалитьПодписанСотрудником Тогда
				
				НаборЗаписей = РегистрыСведений.ПодписиДокументовЭДОБЗК.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Объект.Установить(Выборка.Ссылка);
				НаборЗаписей.Отбор.ФизическоеЛицо.Установить(Выборка.УдалитьФизическоеЛицо);
				
				Запись = НаборЗаписей.Добавить();
				Запись.Объект = Выборка.Ссылка;
				Запись.ФизическоеЛицо = Выборка.УдалитьФизическоеЛицо;
				Запись.Отпечаток = Выборка.Отпечаток;
				Если ЗначениеЗаполнено(Выборка.ДатаПодписи) Тогда
					Запись.ДатаПодписи = Выборка.ДатаПодписи;
				Иначе
					Запись.ДатаПодписи = Выборка.Дата;
				КонецЕсли;
				
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
				
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКодыДокументовКадровыхМероприятий(ПараметрыОбновления = Неопределено) Экспорт
	
	ИмяРеквизита = "КодДокументаКадровогоМероприятия_7696640660b0414980c152bb6380949b";
	РеквизитКодДокументаКадровогоМероприятия =
		ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", ИмяРеквизита);
	
	Если Не ЗначениеЗаполнено(РеквизитКодДокументаКадровогоМероприятия) Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	
	Запрос.УстановитьПараметр("Свойство", РеквизитКодДокументаКадровогоМероприятия);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗКДополнительныеРеквизиты.Ссылка КАК Ссылка,
		|	ДокументЭДОБЗКДополнительныеРеквизиты.Значение КАК Значение
		|ИЗ
		|	Документ.ДокументЭДОБЗК.ДополнительныеРеквизиты КАК ДокументЭДОБЗКДополнительныеРеквизиты
		|ГДЕ
		|	НЕ ДокументЭДОБЗКДополнительныеРеквизиты.Ссылка В (&МассивОбновленных)
		|	И ДокументЭДОБЗКДополнительныеРеквизиты.Свойство = &Свойство
		|	И ДокументЭДОБЗКДополнительныеРеквизиты.Значение <> """"
		|	И ДокументЭДОБЗКДополнительныеРеквизиты.Ссылка.УдалитьКодДокументаКадровогоМероприятия = ЗНАЧЕНИЕ(Справочник.КодыДокументовКадровыхМероприятий.ПустаяСсылка)";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("Документ.ДокументЭДОБЗК");
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.УдалитьКодДокументаКадровогоМероприятия = ЭДОБЗКПовтИсп.СсылкаНаКодДокументаКадровогоМероприятия(Выборка.Значение);
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПеренестиСведенияЭлектронногоДокумента(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|ГДЕ
		|	НЕ ДокументЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И (ДокументЭДОБЗК.ЭлектронныйДокумент = НЕОПРЕДЕЛЕНО
		|				И ДокументЭДОБЗК.УдалитьЭлектронныйДокумент <> НЕОПРЕДЕЛЕНО
		|			ИЛИ ДокументЭДОБЗК.ОснованиеДокумента = НЕОПРЕДЕЛЕНО
		|				И ДокументЭДОБЗК.УдалитьОснованиеДокумента <> НЕОПРЕДЕЛЕНО)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДокументЭДОБЗК.Дата УБЫВ";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Документ.ДокументЭДОБЗК", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ЭлектронныйДокумент = ДокументОбъект.УдалитьЭлектронныйДокумент;
		ДокументОбъект.ОснованиеДокумента = ДокументОбъект.УдалитьОснованиеДокумента;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьРасширенияЭлектронныхДокументов(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка,
		|	ДокументЭДОБЗК.ЭлектронныйДокумент КАК ЭлектронныйДокумент
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|ГДЕ
		|	НЕ ДокументЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И ДокументЭДОБЗК.Расширение = """"
		|	И ДокументЭДОБЗК.ЭлектронныйДокумент <> НЕОПРЕДЕЛЕНО";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	ТаблицаОбновляемых = Запрос.Выполнить().Выгрузить();
	Если ТаблицаОбновляемых.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	РасширенияДокументов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
		ТаблицаОбновляемых.ВыгрузитьКолонку("ЭлектронныйДокумент"), "Расширение");
	Для Каждого ДанныеДокумента Из ТаблицаОбновляемых Цикл
		
		МассивОбновленных.Добавить(ДанныеДокумента.Ссылка);
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Документ.ДокументЭДОБЗК", "Ссылка", ДанныеДокумента.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = ДанныеДокумента.Ссылка.ПолучитьОбъект();
		ДокументОбъект.Расширение = РасширенияДокументов.Получить(ДанныеДокумента.ЭлектронныйДокумент);
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПризнакСоответствияФорматаДокументаТребованиям(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка,
		|	ДокументЭДОБЗК.ЭлектронныйДокумент КАК ЭлектронныйДокумент
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПодписанныеПечатныеФормы КАК ПодписанныеПечатныеФормы
		|		ПО ДокументЭДОБЗК.ЭлектронныйДокумент = ПодписанныеПечатныеФормы.ПрисоединенныйФайл
		|ГДЕ
		|	НЕ ДокументЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И НЕ ДокументЭДОБЗК.ФорматДокументаСоответствуетТребованиям";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	ТаблицаОбновляемых = Запрос.Выполнить().Выгрузить();
	Если ТаблицаОбновляемых.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Для Каждого ДанныеДокумента Из ТаблицаОбновляемых Цикл
		
		МассивОбновленных.Добавить(ДанныеДокумента.Ссылка);
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Документ.ДокументЭДОБЗК", "Ссылка", ДанныеДокумента.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = ДанныеДокумента.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ФорматДокументаСоответствуетТребованиям = Истина;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьВариантПодписанияДокументов(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(НастройкиПечатныхФормБЗК.ВариантПодписания, ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.ПустаяСсылка)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.Требуется)
		|		ИНАЧЕ НастройкиПечатныхФормБЗК.ВариантПодписания
		|	КОНЕЦ КАК ВариантПодписания
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодписанныеПечатныеФормы КАК ПодписанныеПечатныеФормы
		|		ПО ДокументЭДОБЗК.ЭлектронныйДокумент = ПодписанныеПечатныеФормы.ПрисоединенныйФайл
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПечатныхФормБЗК КАК НастройкиПечатныхФормБЗК
		|		ПО (ПодписанныеПечатныеФормы.ИдентификаторПечатнойФормы = НастройкиПечатныхФормБЗК.ИдентификаторПечатнойФормы)
		|ГДЕ
		|	НЕ ДокументЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И ДокументЭДОБЗК.ВариантПодписания = ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.ПустаяСсылка)";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	ТаблицаОбновляемых = Запрос.Выполнить().Выгрузить();
	Если ТаблицаОбновляемых.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	ДокументыЭДОБЗККПересчетуСостояний = Новый Массив;
	Для Каждого ДанныеДокумента Из ТаблицаОбновляемых Цикл
		
		МассивОбновленных.Добавить(ДанныеДокумента.Ссылка);
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Документ.ДокументЭДОБЗК", "Ссылка", ДанныеДокумента.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = ДанныеДокумента.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ВариантПодписания = ДанныеДокумента.ВариантПодписания;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
		ДокументыЭДОБЗККПересчетуСостояний.Добавить(ДанныеДокумента.Ссылка);
		
	КонецЦикла;
	
	ЭДОБЗК.ЗарегистрироватьДокументыЭДОБЗККПересчетуСостояний(ДокументыЭДОБЗККПересчетуСостояний);
	
КонецПроцедуры

Процедура ЗаполнитьВидыДокументовРасчетныхЛистков(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленныхРасчетныхЛистков") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленныхРасчетныхЛистков;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленныхРасчетныхЛистков", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивИсключений = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивИсключений") Тогда
			МассивИсключений = ПараметрыОбновления.МассивИсключений;
		Иначе
			МассивИсключений = Новый Массив;
			ПараметрыОбновления.Вставить("МассивИсключений", МассивИсключений);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.УстановитьПараметр("МассивИсключений", МассивИсключений);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка,
		|	ДокументЭДОБЗК.УдалитьКодДокументаКадровогоМероприятия КАК КодКадровогоДокумента
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|ГДЕ
		|	ДокументЭДОБЗК.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовЭДОБЗК.ПустаяСсылка)
		|	И ДокументЭДОБЗК.КатегорияДокумента = ЗНАЧЕНИЕ(Перечисление.КатегорииДокументовЭДОБЗК.РасчетныйЛисток)
		|	И НЕ ДокументЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И НЕ ДокументЭДОБЗК.Ссылка В (&МассивИсключений)";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ЗаполнитьВидыДокументовПечатныхФорм(ПараметрыОбновления);
		Возврат;
	КонецЕсли;
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	ВидДокументаРасчетныйЛисток = Справочники.ВидыДокументовЭДОБЗК.ПредопределенныйВидДокумента("РасчетныйЛисток");
	Пока Выборка.Следующий() Цикл
		МассивОбновленных.Добавить(Выборка.Ссылка);
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Документ.ДокументЭДОБЗК", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ДокументОбъект.ВидДокумента = ВидДокументаРасчетныйЛисток;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьВидыДокументовПечатныхФорм(ПараметрыОбновления)
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленныхДокументовПечатныхФорм") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленныхДокументовПечатныхФорм;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленныхДокументовПечатныхФорм", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивИсключений = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивИсключений") Тогда
			МассивИсключений = ПараметрыОбновления.МассивИсключений;
		Иначе
			МассивИсключений = Новый Массив;
			ПараметрыОбновления.Вставить("МассивИсключений", МассивИсключений);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.УстановитьПараметр("МассивИсключений", МассивИсключений);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка,
		|	НастройкиПечатныхФормБЗК.ВидДокумента КАК ВидДокумента,
		|	ДокументЭДОБЗК.УдалитьКодДокументаКадровогоМероприятия КАК КодКадровогоДокумента,
		|	ПодписанныеПечатныеФормы.ИдентификаторПечатнойФормы КАК ИдентификаторПечатнойФормы,
		|	ЕСТЬNULL(НастройкиПечатныхФормБЗК.ВидДокумента.КодКадровогоДокумента, ЗНАЧЕНИЕ(Справочник.КодыДокументовКадровыхМероприятий.ПустаяСсылка)) КАК ВидДокументаКодКадровогоДокумента
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПодписанныеПечатныеФормы КАК ПодписанныеПечатныеФормы
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиПечатныхФормБЗК КАК НастройкиПечатныхФормБЗК
		|			ПО ПодписанныеПечатныеФормы.ИдентификаторПечатнойФормы = НастройкиПечатныхФормБЗК.ИдентификаторПечатнойФормы
		|		ПО ДокументЭДОБЗК.ЭлектронныйДокумент = ПодписанныеПечатныеФормы.ПрисоединенныйФайл
		|ГДЕ
		|	ДокументЭДОБЗК.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовЭДОБЗК.ПустаяСсылка)
		|	И НЕ ДокументЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И НЕ ДокументЭДОБЗК.Ссылка В (&МассивИсключений)";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ЗаполнитьНедостающиеВидыДокументовСозданныхСотрудниками(ПараметрыОбновления);
		Возврат;
	КонецЕсли;
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Пока Выборка.Следующий() Цикл
		ВидДокумента = Выборка.ВидДокумента;
		ВидДокументаКодКадровогоДокумента = Выборка.ВидДокументаКодКадровогоДокумента;
		Если Не ЗначениеЗаполнено(ВидДокумента) И ЗначениеЗаполнено(Выборка.ИдентификаторПечатнойФормы) Тогда
			ВидДокумента = Справочники.ВидыДокументовЭДОБЗК.ВидПоИдентификаторуПечатнойФормы(Выборка.ИдентификаторПечатнойФормы);
			Если ЗначениеЗаполнено(ВидДокумента) Тогда
				ВидДокументаКодКадровогоДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидДокумента, "КодКадровогоДокумента");
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(ВидДокумента) И ВидДокумента <> Справочники.ВидыДокументовЭДОБЗК.ПредопределенныйВидДокумента("Прочие")
			И (ВидДокументаКодКадровогоДокумента = Выборка.КодКадровогоДокумента
				Или (Не ЗначениеЗаполнено(ВидДокументаКодКадровогоДокумента)
					И Выборка.КодКадровогоДокумента = ЭДОБЗКПовтИсп.КодКадровогоДокументаПоУмолчанию())) Тогда
			
			МассивОбновленных.Добавить(Выборка.Ссылка);
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Документ.ДокументЭДОБЗК", "Ссылка", Выборка.Ссылка) Тогда
				Продолжить;
			КонецЕсли;
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.ВидДокумента = ВидДокумента;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		Иначе
			Если СтрНачинаетсяС(Выборка.ИдентификаторПечатнойФормы, "ПФ_MXL_") Тогда
				МассивИсключений.Добавить(Выборка.Ссылка);
			Иначе
				МассивОбновленных.Добавить(Выборка.Ссылка);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьНедостающиеВидыДокументовСозданныхСотрудниками(ПараметрыОбновления)
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленныхСозданныхСотрудниками") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленныхСозданныхСотрудниками;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленныхСозданныхСотрудниками", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивИсключений = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивИсключений") Тогда
			МассивИсключений = ПараметрыОбновления.МассивИсключений;
		Иначе
			МассивИсключений = Новый Массив;
			ПараметрыОбновления.Вставить("МассивИсключений", МассивИсключений);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.УстановитьПараметр("МассивИсключений", МассивИсключений);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка,
		|	ДокументЭДОБЗК.ОснованиеДокумента КАК ОснованиеДокумента,
		|	ДокументЭДОБЗК.УдалитьКодДокументаКадровогоМероприятия КАК КодКадровогоДокумента
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|ГДЕ
		|	НЕ ДокументЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И НЕ ДокументЭДОБЗК.Ссылка В (&МассивИсключений)
		|	И ДокументЭДОБЗК.ОснованиеДокумента <> ДокументЭДОБЗК.Ссылка
		|	И ДокументЭДОБЗК.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовЭДОБЗК.ПустаяСсылка)";
	
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ЗаполнитьНедостающиеВидыДокументов(ПараметрыОбновления);
		Возврат;
	КонецЕсли;
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	КодыДокументовКлассификатора = Новый Соответствие;
	ЗапросККлассификатору = Новый Запрос;
	ЗапросККлассификатору.Текст =
		"ВЫБРАТЬ
		|	ВидыДокументовЭДОБЗК.Ссылка КАК Ссылка,
		|	ВидыДокументовЭДОБЗК.КодКадровогоДокумента КАК КодКадровогоДокумента
		|ИЗ
		|	Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗК";
	ВыборкаКлассификатора = ЗапросККлассификатору.Выполнить().Выбрать();
	Пока ВыборкаКлассификатора.Следующий() Цикл
		КодыДокументовКлассификатора.Вставить(ВыборкаКлассификатора.Ссылка, ВыборкаКлассификатора.КодКадровогоДокумента);
	КонецЦикла;
	
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Документ.ДокументЭДОБЗК", "Ссылка", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		КлассификаторСсылка = ЭДОБЗК.ВидДокументаПоОснованию(Выборка.ОснованиеДокумента);
		Если КодыДокументовКлассификатора.Получить(КлассификаторСсылка) <> Выборка.КодКадровогоДокумента Тогда
			МассивИсключений.Добавить(Выборка.Ссылка);
		Иначе
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.ВидДокумента = КлассификаторСсылка;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
		КонецЕсли;
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьНедостающиеВидыДокументов(ПараметрыОбновления)
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленныхБезВидаДокументов") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленныхБезВидаДокументов;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленныхБезВидаДокументов", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивИсключений = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивИсключений") Тогда
			МассивИсключений = ПараметрыОбновления.МассивИсключений;
		Иначе
			МассивИсключений = Новый Массив;
			ПараметрыОбновления.Вставить("МассивИсключений", МассивИсключений);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.УстановитьПараметр("МассивИсключений", МассивИсключений);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(ДокументЭДОБЗК.ЭлектронныйДокумент) КАК ЭлектронныйДокумент,
		|	ДокументЭДОБЗК.УдалитьКодДокументаКадровогоМероприятия КАК КодКадровогоДокумента
		|ПОМЕСТИТЬ ВТКОбновлению
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|ГДЕ
		|	НЕ ДокументЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И НЕ ДокументЭДОБЗК.Ссылка В (&МассивИсключений)
		|	И ДокументЭДОБЗК.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовЭДОБЗК.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КОбновлению.Ссылка КАК Ссылка,
		|	КОбновлению.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
		|	КОбновлению.КодКадровогоДокумента КАК КодКадровогоДокумента
		|ИЗ
		|	ВТКОбновлению КАК КОбновлению
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЭлектронныйДокумент,
		|	КодКадровогоДокумента";
	
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Пока Выборка.СледующийПоЗначениюПоля("ЭлектронныйДокумент") Цикл
		Пока Выборка.СледующийПоЗначениюПоля("КодКадровогоДокумента") Цикл
			Если Выборка.КодКадровогоДокумента = ЭДОБЗКПовтИсп.КодКадровогоДокументаПоУмолчанию() Тогда
				КлассификаторСсылка = Справочники.ВидыДокументовЭДОБЗК.ПредопределенныйВидДокумента("ПрочиеКЭДО");
			ИначеЕсли Не ЗначениеЗаполнено(Выборка.КодКадровогоДокумента) Тогда
				КлассификаторСсылка = Справочники.ВидыДокументовЭДОБЗК.ПредопределенныйВидДокумента("Прочие");
			Иначе
				КлассификаторСсылка = Справочники.ВидыДокументовЭДОБЗК.ВидПоКодуКадровогоДокумент(Выборка.КодКадровогоДокумента);
			КонецЕсли;
			Пока Выборка.Следующий() Цикл
				МассивОбновленных.Добавить(Выборка.Ссылка);
				Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "Документ.ДокументЭДОБЗК", "Ссылка", Выборка.Ссылка) Тогда
					Продолжить;
				КонецЕсли;
				ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
				ДокументОбъект.ВидДокумента = КлассификаторСсылка;
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
				ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
