#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Позволяет указать объекты метаданных, для которых задана логика ограничения доступа к данным.
// Параметры:
//  Список - ОбъектМетаданных - список, для которого нужно вернуть текст ограничения.
//                              В процедуре ПриЗаполненииСписковСОграничениемДоступа нужно
//                              указать для списка значение Ложь, иначе вызова не будет.
//
//  Ограничение - Структура:
//    * Текст                             - Строка - ограничение доступа для пользователей.
//                                          Если пустая строка, значит, доступ разрешен.
//    * ТекстДляВнешнихПользователей      - Строка - ограничение доступа для внешних пользователей.
//                                          Если пустая строка, значит, доступ запрещен.
//    * ПоВладельцуБезЗаписиКлючейДоступа - Неопределено - определить автоматически.
//                                        - Булево - если Ложь, то всегда записывать ключи доступа,
//                                          если Истина, тогда не записывать ключи доступа,
//                                          а использовать ключи доступа владельца (требуется,
//                                          чтобы ограничение было строго по объекту-владельцу).
//   * ПоВладельцуБезЗаписиКлючейДоступаДляВнешнихПользователей - Неопределено, Булево - также
//                                          как у параметра ПоВладельцуБезЗаписиКлючейДоступа.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

// Выгружает документ и возвращает свойства файла выгрузки.
// 
// Параметры:
//  ДокументацияУведомления - ДокументСсылка.ДокументацияПоКонтролируемымСделкам - ссылка на документацию
//                                                                               по контролируемым сделкам.
// 
// Возвращаемое значение:
//  Булево - Истина, если у документации табличная часть "МетодыЦенообразования" содержит ссылку на документацию.
Функция ДокументацияЗаполнена(ДокументацияУведомления) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументацияУведомления", ДокументацияУведомления);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ДокументацияПоКонтролируемымСделкам.МетодыЦенообразования КАК ДокументацияПоКонтролируемымСделкамМетодыЦенообразования
	|ГДЕ
	|	ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.Ссылка = &ДокументацияУведомления";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

// Выгружает документ и возвращает свойства файла выгрузки.
// 
// Параметры:
//  ДокументацияУведомления - ДокументСсылка.ДокументацияПоКонтролируемымСделкам - ссылка на документацию
//                                                                               по контролируемым сделкам.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - свойства методов ценообразования:
//   * ИдентификаторСтроки      - УникальныйИдентификатор - идентификатор строки.
//   * КодМетодаЦенообразования - Строка - код метода ценообразования.
//   * КодСтороныСделки         - Строка - код стороны сделки.
//   * Контрагент               - СправочникСсылка.Организации,СправочникСсылка.Контрагенты - контрагент.
//   * НомераСделок             - Массив Из Число - номера сделок.
//   * ПорядковыеНомераСделок   - Строка - порядковые номера сделок.
//   * СвойстваЦенообразования  - ТаблицаЗначений - свойства ценообразования:
//     ** ИдентификаторСтрокиМетода               - УникальныйИдентификатор - идентификатор строки метода.
//     ** ИсточникИнформацииСопоставимыхЦен       -  Строка - идентификатор строки.
//     ** КодМетодаЦенообразования                  - Строка - код метода ценообразования.
//     ** НаименованиеДокументаЦеныСопоставимыхЦен  - Строка - наименование документа цены сопоставимых цен.
//     ** НаименованиеКорректировкиДляСопоставимостиСопоставимыхЦен  - Строка - наименование корректировки для
//                                                                             сопоставимости сопоставимых цен.
//     ** НаименованиеКотировкиСопоставимыхЦен      - Строка - наименование корректировки сопоставимых цен.
//     ** ОписаниеМетодаРаспределенияПрибыли        - Строка - описание метода распределения прибыли.
//     ** ПоказательРентабельности                  - Строка - показатель рентабельности.
//     ** ПорядокПримененияКотировкиСопоставимыхЦен - Строка - порядок применения котировки сопоставимых цен.
//     ** Рентабельность                            - Число - значение рентабельности.
//     ** РыночнаяРентабельность                    - Число - значение рыночной рентабельности.
//     ** ТестируемаяСторона                        - Строка - код тестируемой стороны.
//   * СуммаДоходов                                 - Число - сумма доходов.
//   * СуммаРасходов                                - Число - сумма расходов.
//   * ТипВзаимозависимости - ПеречислениеСсылка.ТипыВзаимозависимости - тип взаимозависимости организации и контрагента.
//
//@skip-check doc-comment-field-name
Функция ДанныеЗаполненияМетодовЦенообразования(ДокументацияУведомления) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДокументацияУведомления", ДокументацияУведомления);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументацияПоКонтролируемымСделкам.Ссылка КАК ДокументацияУведомления,
	|	ДокументацияПоКонтролируемымСделкам.УведомлениеОКонтролируемойСделке КАК Уведомление
	|ПОМЕСТИТЬ ДанныеУведомления
	|ИЗ
	|	Документ.ДокументацияПоКонтролируемымСделкам КАК ДокументацияПоКонтролируемымСделкам
	|ГДЕ
	|	ДокументацияПоКонтролируемымСделкам.Ссылка = &ДокументацияУведомления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КонтролируемаяСделка.Номер КАК НомерЛиста1А,
	|	КонтролируемаяСделка.Контрагент КАК Контрагент,
	|	КонтролируемаяСделка.СпособОпределенияЦеныСделки КАК СпособОпределенияЦеныСделки,
	|	КонтролируемаяСделка.СуммаДоходов КАК СуммаДоходов,
	|	КонтролируемаяСделка.СуммаРасходов КАК СуммаРасходов,
	|	КонтролируемаяСделка.КодСтороныСделки КАК КодСтороныСделки,
	|	КонтролируемаяСделка.ТипВзаимозависимости КАК ТипВзаимозависимости,
	|	ЕСТЬNULL(КонтролируемаяСделка.СпособОпределенияЦеныСделки.КодМетодаЦенообразования, """") КАК КодМетодаЦенообразования
	|ИЗ
	|	ДанныеУведомления КАК ДанныеУведомления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КонтролируемаяСделка КАК КонтролируемаяСделка
	|		ПО ДанныеУведомления.Уведомление = КонтролируемаяСделка.УведомлениеОКонтролируемойСделке
	|			И (НЕ КонтролируемаяСделка.ПометкаУдаления)
	|			И (КонтролируемаяСделка.Строка122СделкаВОбластиВнешнейТорговли)
	|
	|УПОРЯДОЧИТЬ ПО
	|	КонтролируемаяСделка.УведомлениеОКонтролируемойСделке,
	|	Контрагент,
	|	КонтролируемаяСделка.Договор,
	|	ТипВзаимозависимости,
	|	КодСтороныСделки,
	|	СпособОпределенияЦеныСделки,
	|	КодМетодаЦенообразования,
	|	НомерЛиста1А";
	
	РеквизитыМетодовЦенообразования = Метаданные.Документы.ДокументацияПоКонтролируемымСделкам.ТабличныеЧасти.МетодыЦенообразования.Реквизиты;
	
	МетодыЦенообразования = Новый ТаблицаЗначений;
	МетодыЦенообразования.Колонки.Добавить("ПорядковыеНомераСделок", РеквизитыМетодовЦенообразования.ПорядковыеНомераСделок.Тип);
	МетодыЦенообразования.Колонки.Добавить("НомераСделок", Новый ОписаниеТипов("Массив"));
	МетодыЦенообразования.Колонки.Добавить("СуммаДоходов", РеквизитыМетодовЦенообразования.СуммаДоходов.Тип);
	МетодыЦенообразования.Колонки.Добавить("СуммаРасходов", РеквизитыМетодовЦенообразования.СуммаРасходов.Тип);
	МетодыЦенообразования.Колонки.Добавить("Контрагент", РеквизитыМетодовЦенообразования.Контрагент.Тип);
	МетодыЦенообразования.Колонки.Добавить("КодСтороныСделки", РеквизитыМетодовЦенообразования.КодСтороныСделки.Тип);
	МетодыЦенообразования.Колонки.Добавить("ТипВзаимозависимости", РеквизитыМетодовЦенообразования.ТипВзаимозависимости.Тип);
	МетодыЦенообразования.Колонки.Добавить("КодМетодаЦенообразования", РеквизитыМетодовЦенообразования.КодМетодаЦенообразования.Тип);
	МетодыЦенообразования.Колонки.Добавить("ИдентификаторСтроки", РеквизитыМетодовЦенообразования.ИдентификаторСтроки.Тип);
	МетодыЦенообразования.Колонки.Добавить("СвойстваЦенообразования", Новый ОписаниеТипов("ТаблицаЗначений"));
	
	КлючСтроки = Неопределено;
	ТекущаяСтрокаЦенообразования = Неопределено;
	ПоляКлюча = "Контрагент,КодСтороныСделки,ТипВзаимозависимости,КодМетодаЦенообразования";
	
	Методы = Запрос.Выполнить().Выгрузить();
	
	СпособыЦенообразования = Методы.ВыгрузитьКолонку("СпособОпределенияЦеныСделки");
	СвойстваСпособовЦенообразования = Справочники.СпособыОпределенияЦенКонтролируемыхСделок.СвойстваСпособовЦенообразования(СпособыЦенообразования);
	
	Для Каждого СтрокаМетода Из Методы Цикл
		
		Если Не КлючСовпадает(КлючСтроки, СтрокаМетода) Тогда
			КлючСтроки = Новый Структура(ПоляКлюча);
			ЗаполнитьЗначенияСвойств(КлючСтроки, СтрокаМетода);
			
			ТекущаяСтрокаЦенообразования = МетодыЦенообразования.Добавить();
			ЗаполнитьЗначенияСвойств(ТекущаяСтрокаЦенообразования, СтрокаМетода, ПоляКлюча);
			
			ТекущаяСтрокаЦенообразования.ИдентификаторСтроки = Новый УникальныйИдентификатор();
			ТекущаяСтрокаЦенообразования.СвойстваЦенообразования = НовыйТаблицаСвойстваЦенообразования();
			
			СвойстваСпособа = СвойстваСпособовЦенообразования.Получить(СтрокаМетода.СпособОпределенияЦеныСделки);
			Если СвойстваСпособа <> Неопределено Тогда
				Для Каждого СвойствоСпособа Из СвойстваСпособа Цикл
					НовоеСвойствоСпособа = ТекущаяСтрокаЦенообразования.СвойстваЦенообразования.Добавить();
					ЗаполнитьЗначенияСвойств(НовоеСвойствоСпособа, СвойствоСпособа);
					НовоеСвойствоСпособа.ИдентификаторСтрокиМетода = ТекущаяСтрокаЦенообразования.ИдентификаторСтроки;
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		ТекущаяСтрокаЦенообразования.СуммаДоходов = 
			ТекущаяСтрокаЦенообразования.СуммаДоходов + СтрокаМетода.СуммаДоходов;
		ТекущаяСтрокаЦенообразования.СуммаРасходов = 
			ТекущаяСтрокаЦенообразования.СуммаРасходов + СтрокаМетода.СуммаРасходов;
		ТекущаяСтрокаЦенообразования.НомераСделок.Добавить(СтрокаМетода.НомерЛиста1А);
		
	КонецЦикла;
	
	Для Каждого СтрокаМетода Из МетодыЦенообразования Цикл
		СтрокаМетода.ПорядковыеНомераСделок = ОписаниеПорядковыхНомеровСделок(СтрокаМетода.НомераСделок);
	КонецЦикла;
	
	Возврат МетодыЦенообразования;
	
КонецФункции

Процедура ПерезаполнитьМетодыЦенообразования(ДокументацияУведомленияОбъект, ДанныеЗаполненияЦенообразования) Экспорт
	
	ДокументацияУведомленияОбъект.МетодыЦенообразования.Очистить();
	ДокументацияУведомленияОбъект.СвойстваЦенообразования.Очистить();
	
	ДокументацияУведомленияОбъект.МетодыЦенообразования.Загрузить(ДанныеЗаполненияЦенообразования);
	
	Для Каждого СтрокаЦенообразования Из ДанныеЗаполненияЦенообразования Цикл
		
		Для Каждого СтрокаСвойств Из СтрокаЦенообразования.СвойстваЦенообразования Цикл
			ЗаполнитьЗначенияСвойств(ДокументацияУведомленияОбъект.СвойстваЦенообразования.Добавить(), СтрокаСвойств);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает структуру для выгрузки основных сведений документации по контролируемым сделках.
//
// Параметры:
//  ДокументацияУведомления - ДокументСсылка.УведомлениеОКонтролируемыхСделках - документация уведомления
//                                                                               о контролируемых сделках.
// 
// Возвращаемое значение:
//  Структура - структура параметров печати уведомления о контролируемых сделках:
//  * ДокументацияУведомления - ДокументСсылка.УведомлениеОКонтролируемыхСделках - документация уведомления 
//                                                                                 о контролируемых сделках.
//  * Организация - СправочникСсылка.Организации - организация, подающая уведомление.
//  * ЮридическоеФизическоеЛицо - ПеречислениеСсылка.ЮридическоеФизическоеЛицо - каким лицом является организация.
//  * ДатаВыгрузки - Дата - дата выгрузки документации уведомления.
//  * ОтчетныйГод - Число - отчетный год документации уведомления.
//  * НомерКорректировки - Число - номер корректировки документации уведомления.
//  * КодНалоговогоОргана - Строка - код налогового органа, в который подается документация уведомления.
//  * РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане - регистрация в налоговом органе.
//  * ИдентификаторФайлаОснования - Строка - идентификатор файла уведомления о контролируемых сделках.
//  * СведенияОНалогоплательщике - Структура - сведения организации для налоговой:
//    ** Наименование - Строка - полное наименование организации.
//    ** ИНН - ОпределяемыйТип.ИНН - ИНН организации.
//    ** КПП - Строка - КПП организации.
//  * Подписант - Структура - свойства подписанта:
//    ** ПризнакПодписанта - Строка - тип подписанта.
//    ** Фамилия - Строка - фамилия подписанта.
//    ** Имя - Строка - имя подписанта.
//    ** Отчество - Строка - отчетство подписанта.
//    ** НаименованиеДокумента - Строка - документ представителя.
//
Функция ОсновныеСведенияДокументацииДляВыгрузки(ДокументацияУведомления) Экспорт
	
	ОсновныеСведения = Новый Структура;
	
	РеквизитыДокументации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументацияУведомления,
		"Организация, ОтчетныйГод, НомерКорректировки, УведомлениеОКонтролируемойСделке, ИдентификаторФайлаУведомления");
	
	Организация  = РеквизитыДокументации.Организация;
	ДатаСведений = ТекущаяДатаСеанса();
	
	// Т.к. часть реквизитов отправки зависит от уведомления о контролируемых сделках,
	// то сведения об организации получаем из этого документа.
	СведенияОбОрганизацииДляОтправки = Документы.УведомлениеОКонтролируемыхСделках.СведенияОбОрганизацииДляОтправки(
		РеквизитыДокументации.УведомлениеОКонтролируемойСделке, ДатаСведений);
	
	ОсновныеСведения.Вставить("ДокументацияУведомления", ДокументацияУведомления);
	ОсновныеСведения.Вставить("Организация", Организация);
	ОсновныеСведения.Вставить("ЮридическоеФизическоеЛицо", СведенияОбОрганизацииДляОтправки.ЮридическоеФизическоеЛицо);
	ОсновныеСведения.Вставить("ДатаВыгрузки", ДатаСведений);
	ОсновныеСведения.Вставить("ОтчетныйГод", Год(РеквизитыДокументации.ОтчетныйГод));
	ОсновныеСведения.Вставить("НомерКорректировки", РеквизитыДокументации.НомерКорректировки);
	ОсновныеСведения.Вставить("КодНалоговогоОргана", СведенияОбОрганизацииДляОтправки.КодНалоговогоОргана);
	ОсновныеСведения.Вставить("РегистрацияВНалоговомОргане", СведенияОбОрганизацииДляОтправки.РегистрацияВНалоговомОргане);
	
	ОсновныеСведения.Вставить("ИдентификаторФайлаОснования", РеквизитыДокументации.ИдентификаторФайлаУведомления);
	
	СведенияНП = Новый Структура;
	СведенияНП.Вставить("Наименование", СведенияОбОрганизацииДляОтправки.НаименованиеПолное);
	СведенияНП.Вставить("ИНН", СведенияОбОрганизацииДляОтправки.ИНН);
	СведенияНП.Вставить("КПП", СведенияОбОрганизацииДляОтправки.КПП);
	
	ОсновныеСведения.Вставить("СведенияОНалогоплательщике", СведенияНП);
	
	Подписант = Новый Структура;
	Подписант.Вставить("ПризнакПодписанта", "");
	Подписант.Вставить("Фамилия", "");
	Подписант.Вставить("Имя", "");
	Подписант.Вставить("Отчество", "");
	Подписант.Вставить("НаименованиеДокумента", "");
	ЗаполнитьСведенияПодписанта(Подписант, СведенияОбОрганизацииДляОтправки);
	
	ОсновныеСведения.Вставить("Подписант", Подписант);
	
	Возврат ОсновныеСведения;
	
КонецФункции

// Выгружает документ и возвращает свойства файла выгрузки.
// 
// Параметры:
//  ДокументацияУведомления - ДокументСсылка.ДокументацияПоКонтролируемымСделкам - ссылка на документацию
//                                                                               по контролируемым сделкам.
//  ДатаАктуальностиСведений - Дата - дата, на которую выгружаются сведения о контрагентах в документации.
// 
// Возвращаемое значение:
//  Структура - структура выгрузки данных документации:
//   * МетодыЦенообразования - ТаблицаЗначений - свойства методов ценообрабозания:
//     ** ИНН - Строка - ИНН контрагента.
//     ** ИдентификаторСтроки - УникальныйИдентификатор - идентификатор строки.
//     ** КодВзаимозависимости - Строка - код взаимозависимости контрагента и организации.
//     ** КодМетодаЦенообразования - Строка - код метода ценообразования.
//     ** КодСтороныСделки - Строка - код стороны сделки.
//     ** КодСтраныРегистрации - Строка - код страны регистрации контрагента.
//     ** Контрагент - СправочникСсылка.Организации,СправочникСсылка.Контрагенты - контрагент.
//     ** НаименованиеЛат - Строка - наименование контрагента в латинской транслитерации.
//     ** НаименованиеРус - Строка - наименование контрагента в русской транслитерации.
//     ** НомерСтроки - Число - номер строки.
//     ** ПорядковыеНомераСделок - Строка - порядковые номера сделок.
//     ** РегистрационныйНомер - Строка -  регистрационный номер контрагента.
//     ** СвойстваМетода - ТаблицаЗначений - свойства метода ценообразования:
//        *** ИдентификаторСтрокиМетода               - УникальныйИдентификатор - идентификатор строки метода.
//        *** ИсточникИнформацииСопоставимыхЦен       -  Строка - идентификатор строки.
//        *** КодМетодаЦенообразования                  - Строка - код метода ценообразования.
//        *** НаименованиеДокументаЦеныСопоставимыхЦен  - Строка - наименование документа цены сопоставимых цен.
//        *** НаименованиеКорректировкиДляСопоставимостиСопоставимыхЦен  - Строка - наименование корректировки для
//                                                                                  сопоставимости сопоставимых цен.
//        *** НаименованиеКотировкиСопоставимыхЦен      - Строка - ниаменование корректировки сопоставимых цен.
//        *** ОписаниеМетодаРаспределенияПрибыли        - Строка - описание метода распределения прибыли.
//        *** ПоказательРентабельности                  - Строка - показатель рентабельности.
//        *** ПорядокПримененияКотировкиСопоставимыхЦен - Строка - порядок применения котировки сопоставимых цен.
//        *** Рентабельность                            - Число - значение рентабельности.
//        *** РыночнаяРентабельность                    - Число - значение рыночной рентабельности.
//        *** ТестируемаяСторона                        - Строка - код тестируемой стороны.
//     ** СуммаДоходов                                  - Число - сумма доходов.
//     ** СуммаРасходов                                 - Число - сумма расходов.
//     ** ТипВзаимозависимости - ПеречислениеСсылка.ТипыВзаимозависимости - тип взаимозависимости организации и контрагента.
//   * Приложения - ТаблицаЗначений - свойства приложений документации:
//     ** ДатаСоздания           - Дата - дата загрузки присоединенного файла.
//     ** ИдентификаторДокумента - Строка - идентификатор документа.
//     ** ИдентификаторФайла     - Строка - идентификатор файла.
//     ** ИмяФайла               - Строка - имя файлв.
//     ** НаименованиеДокумента  - Строка - наименование документа приложения.
//     ** НомерСтроки            - Число - номер строки.
//     ** НомерСтрокиФайла       - Число - номер строки присоедененного файла.
//     ** Файл - СправочникСсылка.ДокументацияПоКонтролируемымСделкамПрисоединенныеФайлы - ссылка на файл.
//     ** ФайлНаименование       - Строка - наименование присоедиенного файла.
//     ** ФайлРасширение         - Строка - расширение файла.
//
Функция ДанныеДокументацииДляВыгрузки(ДокументацияУведомления, ДатаАктуальностиСведений) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ДокументацияУведомления", ДокументацияУведомления);
		Запрос.УстановитьПараметр("ДатаАктуальностиСведений", ДатаАктуальностиСведений);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.Контрагент КАК Контрагент,
	|	ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.НомерСтроки КАК НомерСтроки,
	|	ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.ПорядковыеНомераСделок КАК ПорядковыеНомераСделок,
	|	ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.СуммаДоходов КАК СуммаДоходов,
	|	ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.СуммаРасходов КАК СуммаРасходов,
	|	ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.КодСтороныСделки КАК КодСтороныСделки,
	|	ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.ТипВзаимозависимости КАК ТипВзаимозависимости,
	|	ВЫБОР
	|		КОГДА ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.ТипВзаимозависимости = ЗНАЧЕНИЕ(Перечисление.ТипыВзаимозависимости.ВзаимозависимыПоКодексу)
	|			ТОГДА ""1""
	|		КОГДА ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.ТипВзаимозависимости = ЗНАЧЕНИЕ(Перечисление.ТипыВзаимозависимости.СамостоятельноеПризнаниеВзаимозависимости)
	|			ТОГДА ""2""
	|		КОГДА ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.ТипВзаимозависимости = ЗНАЧЕНИЕ(Перечисление.ТипыВзаимозависимости.ВзаимозависимостьПоРешениюСуда)
	|			ТОГДА ""3""
	|		ИНАЧЕ ""0""
	|	КОНЕЦ КАК КодВзаимозависимости,
	|	ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.КодМетодаЦенообразования КАК КодМетодаЦенообразования,
	|	ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ДокументацияМетодыЦенообразования
	|ИЗ
	|	Документ.ДокументацияПоКонтролируемымСделкам.МетодыЦенообразования КАК ДокументацияПоКонтролируемымСделкамМетодыЦенообразования
	|ГДЕ
	|	ДокументацияПоКонтролируемымСделкамМетодыЦенообразования.Ссылка = &ДокументацияУведомления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Контрагент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОрганизацииИсторияНаименований.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ОрганизацииИсторияНаименований.Период) КАК Период,
	|	ЕСТЬNULL(СтраныМира.Код, """") КАК КодСтраныРегистрации,
	|	ВЫБОР
	|		КОГДА Организации.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА Организации.ИНН
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ИНН,
	|	ВЫБОР
	|		КОГДА Организации.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА """"
	|		ИНАЧЕ Организации.РегистрацияВНалоговомОргане
	|	КОНЕЦ КАК РегистрационныйНомер
	|ПОМЕСТИТЬ ДатыАктуальностиНаименованийОрганизаций
	|ИЗ
	|	Справочник.Организации.ИсторияНаименований КАК ОрганизацииИсторияНаименований
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументацияМетодыЦенообразования КАК ДокументацияМетодыЦенообразования
	|		ПО ОрганизацииИсторияНаименований.Ссылка = ДокументацияМетодыЦенообразования.Контрагент
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ОрганизацииИсторияНаименований.Ссылка = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
	|		ПО Организации.СтранаРегистрации = СтраныМира.Ссылка
	|ГДЕ
	|	ОрганизацииИсторияНаименований.Период <= &ДатаАктуальностиСведений
	|
	|СГРУППИРОВАТЬ ПО
	|	ОрганизацииИсторияНаименований.Ссылка,
	|	СтраныМира.Код,
	|	Организации.ИНН,
	|	Организации.СтранаРегистрации,
	|	Организации.РегистрацияВНалоговомОргане
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументацияМетодыЦенообразования.НомерСтроки КАК НомерСтроки,
	|	ДокументацияМетодыЦенообразования.ПорядковыеНомераСделок КАК ПорядковыеНомераСделок,
	|	ДокументацияМетодыЦенообразования.СуммаДоходов КАК СуммаДоходов,
	|	ДокументацияМетодыЦенообразования.СуммаРасходов КАК СуммаРасходов,
	|	ДокументацияМетодыЦенообразования.Контрагент КАК Контрагент,
	|	ДокументацияМетодыЦенообразования.КодСтороныСделки КАК КодСтороныСделки,
	|	ДокументацияМетодыЦенообразования.ТипВзаимозависимости КАК ТипВзаимозависимости,
	|	ДокументацияМетодыЦенообразования.КодВзаимозависимости КАК КодВзаимозависимости,
	|	ДокументацияМетодыЦенообразования.КодМетодаЦенообразования КАК КодМетодаЦенообразования,
	|	ДокументацияМетодыЦенообразования.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ЕСТЬNULL(СтраныМира.Код, """") КАК КодСтраныРегистрации,
	|	Контрагенты.НаименованиеВТранскрипции КАК НаименованиеРус,
	|	Контрагенты.НаименованиеМеждународное КАК НаименованиеЛат,
	|	ВЫБОР
	|		КОГДА Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА Контрагенты.ИНН
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ИНН,
	|	ВЫБОР
	|		КОГДА Контрагенты.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.Россия)
	|			ТОГДА """"
	|		ИНАЧЕ Контрагенты.РегистрационныйНомер
	|	КОНЕЦ КАК РегистрационныйНомер
	|ИЗ
	|	ДокументацияМетодыЦенообразования КАК ДокументацияМетодыЦенообразования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ДокументацияМетодыЦенообразования.Контрагент = Контрагенты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтраныМира КАК СтраныМира
	|		ПО (Контрагенты.СтранаРегистрации = СтраныМира.Ссылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументацияМетодыЦенообразования.НомерСтроки,
	|	ДокументацияМетодыЦенообразования.ПорядковыеНомераСделок,
	|	ДокументацияМетодыЦенообразования.СуммаДоходов,
	|	ДокументацияМетодыЦенообразования.СуммаРасходов,
	|	ДокументацияМетодыЦенообразования.Контрагент,
	|	ДокументацияМетодыЦенообразования.КодСтороныСделки,
	|	ДокументацияМетодыЦенообразования.ТипВзаимозависимости,
	|	ДокументацияМетодыЦенообразования.КодВзаимозависимости,
	|	ДокументацияМетодыЦенообразования.КодМетодаЦенообразования,
	|	ДокументацияМетодыЦенообразования.ИдентификаторСтроки,
	|	ДатыАктуальностиНаименованийОрганизаций.КодСтраныРегистрации,
	|	ОрганизацииИсторияНаименований.НаименованиеПолное,
	|	ОрганизацииИсторияНаименований.НаименованиеМеждународное,
	|	ДатыАктуальностиНаименованийОрганизаций.ИНН,
	|	ДатыАктуальностиНаименованийОрганизаций.РегистрационныйНомер
	|ИЗ
	|	ДокументацияМетодыЦенообразования КАК ДокументацияМетодыЦенообразования
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыАктуальностиНаименованийОрганизаций КАК ДатыАктуальностиНаименованийОрганизаций
	|		ПО ДокументацияМетодыЦенообразования.Контрагент = ДатыАктуальностиНаименованийОрганизаций.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации.ИсторияНаименований КАК ОрганизацииИсторияНаименований
	|		ПО (ДатыАктуальностиНаименованийОрганизаций.Ссылка = ОрганизацииИсторияНаименований.Ссылка)
	|			И (ДатыАктуальностиНаименованийОрганизаций.Период = ОрганизацииИсторияНаименований.Период)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДокументацияМетодыЦенообразования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДатыАктуальностиНаименованийОрганизаций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.НомерСтроки КАК НомерСтроки,
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.ИдентификаторСтрокиМетода КАК ИдентификаторСтрокиМетода,
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.КодМетодаЦенообразования КАК КодМетодаЦенообразования,
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.ИсточникИнформацииСопоставимыхЦен КАК ИсточникИнформацииСопоставимыхЦен,
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.НаименованиеКотировкиСопоставимыхЦен КАК НаименованиеКотировкиСопоставимыхЦен,
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.ПорядокПримененияКотировкиСопоставимыхЦен КАК ПорядокПримененияКотировкиСопоставимыхЦен,
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.НаименованиеДокументаЦеныСопоставимыхЦен КАК НаименованиеДокументаЦеныСопоставимыхЦен,
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.НаименованиеКорректировкиДляСопоставимостиСопоставимыхЦен КАК НаименованиеКорректировкиДляСопоставимостиСопоставимыхЦен,
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.ПоказательРентабельности КАК ПоказательРентабельности,
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.ТестируемаяСторона КАК ТестируемаяСторона,
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.Рентабельность КАК Рентабельность,
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.РыночнаяРентабельность КАК РыночнаяРентабельность,
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.ОписаниеМетодаРаспределенияПрибыли КАК ОписаниеМетодаРаспределенияПрибыли
	|ИЗ
	|	Документ.ДокументацияПоКонтролируемымСделкам.СвойстваЦенообразования КАК ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования
	|ГДЕ
	|	ДокументацияПоКонтролируемымСделкамСвойстваЦенообразования.Ссылка = &ДокументацияУведомления
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументацияПоКонтролируемымСделкамПриложения.НомерСтроки КАК НомерСтроки,
	|	ДокументацияПоКонтролируемымСделкамПриложения.НаименованиеДокумента КАК НаименованиеДокумента,
	|	ДокументацияПоКонтролируемымСделкамПриложения.ИдентификаторДокумента КАК ИдентификаторДокумента,
	|	ДокументацияПоКонтролируемымСделкамФайлыПриложения.НомерСтроки КАК НомерСтрокиФайла,
	|	ДокументацияПоКонтролируемымСделкамФайлыПриложения.ИдентификаторФайла КАК ИдентификаторФайла,
	|	ДокументацияПоКонтролируемымСделкамФайлыПриложения.Файл КАК Файл,
	|	ЕСТЬNULL(ДокументацияПоКонтролируемымСделкамФайлыПриложения.Файл.ДатаСоздания, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаСоздания,
	|	ЕСТЬNULL(ДокументацияПоКонтролируемымСделкамФайлыПриложения.Файл.Расширение, """") КАК ФайлРасширение,
	|	ЕСТЬNULL(ДокументацияПоКонтролируемымСделкамФайлыПриложения.Файл.Наименование, """") КАК ФайлНаименование
	|ИЗ
	|	Документ.ДокументацияПоКонтролируемымСделкам.Приложения КАК ДокументацияПоКонтролируемымСделкамПриложения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДокументацияПоКонтролируемымСделкам.ФайлыПриложения КАК ДокументацияПоКонтролируемымСделкамФайлыПриложения
	|		ПО ДокументацияПоКонтролируемымСделкамПриложения.Ссылка = ДокументацияПоКонтролируемымСделкамФайлыПриложения.Ссылка
	|			И ДокументацияПоКонтролируемымСделкамПриложения.ИдентификаторДокумента = ДокументацияПоКонтролируемымСделкамФайлыПриложения.ИдентификаторДокумента
	|ГДЕ
	|	ДокументацияПоКонтролируемымСделкамПриложения.Ссылка = &ДокументацияУведомления
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	МетодыЦенообразования = РезультатЗапроса[2].Выгрузить(); // ТаблицаЗначений
	МетодыЦенообразования.Колонки.Добавить("СвойстваМетода", Новый ОписаниеТипов("ТаблицаЗначений"));
	
	СвойстваМетодов = РезультатЗапроса[5].Выгрузить();
	СвойстваМетодов.Индексы.Добавить("ИдентификаторСтрокиМетода");
	
	Приложения = РезультатЗапроса[6].Выгрузить(); // ТаблицаЗначений
	Приложения.Колонки.Добавить("ИмяФайла", ОбщегоНазначения.ОписаниеТипаСтрока(255));
	
	Для Каждого СтрокаМетода Из МетодыЦенообразования Цикл
		
		ОтборСтрок = Новый Структура("ИдентификаторСтрокиМетода", СтрокаМетода.ИдентификаторСтроки);
		СтрокиМетода = СвойстваМетодов.НайтиСтроки(ОтборСтрок);
		
		СтрокаМетода.СвойстваМетода = СвойстваМетодов.Скопировать(СтрокиМетода);
		СтрокаМетода.СвойстваМетода.Сортировать("НомерСтроки");
		
	КонецЦикла;
	
	//@skip-check bsl-legacy-check-string-literal
	Уведомление = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументацияУведомления, "УведомлениеОКонтролируемойСделке");
	ПрефиксИмениФайла = Документы.ДокументацияПоКонтролируемымСделкам.ПрефиксИмениФайлаПриложенияДляОтправки(Уведомление);
	
	Для Каждого СтрокаПриложения Из Приложения Цикл
		РеквизитыФайла = Новый Структура("Наименование,Расширение,ДатаСоздания");
		РеквизитыФайла.Наименование = СтрокаПриложения.ФайлНаименование;
		РеквизитыФайла.Расширение = СтрокаПриложения.ФайлРасширение;
		РеквизитыФайла.ДатаСоздания = СтрокаПриложения.ДатаСоздания;
		СтрокаПриложения.ИмяФайла = Документы.ДокументацияПоКонтролируемымСделкам.ИмяФайлаПриложенияДляОтправки(
			РеквизитыФайла, ПрефиксИмениФайла, СтрокаПриложения.ИдентификаторДокумента, СтрокаПриложения.ИдентификаторФайла);
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("МетодыЦенообразования", МетодыЦенообразования); // ТаблицаЗначений
	Результат.Вставить("Приложения", Приложения); // ТаблицаЗначений
	
	Возврат Результат;
	
КонецФункции

// Выгружает документ и возвращает свойства файла выгрузки.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации - организация, подающая документацию.
//  Уведомление - ДокументСсылка.УведомлениеОКонтролируемыхСделках - уведомление о контролируемых сделках.
//  ТипДокументации - Число - тип документации уведомления о контролируемых сделках.
//  НомерКорректировкиДокументации - Число - номер корректировки документации уведомления о контролируемых сделках.
//  ТипПоиска - Строка - тип поиска.
// 
// Возвращаемое значение:
//  ДокументСсылка.ДокументацияПоКонтролируемымСделкам - если поиск успешен.
//  Неопределено - в случае неуспешного поиска.
Функция НайтиДокументациюУведомленияВОтчетномГоду(Организация, Уведомление, ТипДокументации = Неопределено, 
	НомерКорректировкиДокументации = Неопределено, ТипПоиска = "Последний") Экспорт
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Организация", Организация);
	Запрос.Параметры.Вставить("Уведомление", Уведомление);
	Запрос.Параметры.Вставить("НомерКорректировки",
		?(ТипДокументации = 0, 0, ?(НомерКорректировкиДокументации = Неопределено, 0, НомерКорректировкиДокументации)));
		
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДокументацияПоКонтролируемымСделкам.Ссылка КАК Документация,
	|	ДокументацияПоКонтролируемымСделкам.НомерКорректировки КАК НомерКорректировки,
	|	ДокументацияПоКонтролируемымСделкам.Организация
	|ИЗ
	|	Документ.ДокументацияПоКонтролируемымСделкам КАК ДокументацияПоКонтролируемымСделкам
	|ГДЕ
	|	ДокументацияПоКонтролируемымСделкам.Организация = &Организация
	|	И ДокументацияПоКонтролируемымСделкам.УведомлениеОКонтролируемойСделке = &Уведомление
	|	И ДокументацияПоКонтролируемымСделкам.НомерКорректировки >= &НомерКорректировки
	|	И НЕ ДокументацияПоКонтролируемымСделкам.ПометкаУдаления";
	
	Если ТипДокументации = Неопределено
		И ТипПоиска = "Последний" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И ДокументацияПоКонтролируемымСделкам.НомерКорректировкиДокументации >= &НомерКорректировки","");	
	КонецЕсли;
	
	Если ТипПоиска = "Последний" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерКорректировки УБЫВ";
			
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"И ДокументацияПоКонтролируемымСделкам.НомерКорректировки >= &НомерКорректировки", "");
	ИначеЕсли ТипПоиска = "Следующий" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерКорректировки ВОЗР";
	ИначеЕсли ТипПоиска = "Предыдущий" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерКорректировки УБЫВ";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И ДокументацияПоКонтролируемымСделкам.НомерКорректировки >= &НомерКорректировки",
			"И ДокументацияПоКонтролируемымСделкам.НомерКорректировки <= &НомерКорректировки");
	ИначеЕсли ТипПоиска = "Указанный" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"И ДокументацияПоКонтролируемымСделкам.НомерКорректировки >= &НомерКорректировки",
			"И ДокументацияПоКонтролируемымСделкам.НомерКорректировки = &НомерКорректировки");	
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		Возврат Результат.Документация;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возвращает номер последней корректировки документации уведомления о контролируемых сделках.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации - организация, подающая документацию по контролируемым сделкам.
//  ОтчетныйГод - Дата - отчетный год документации уведомления о контролируемых сделках.
// 
// Возвращаемое значение:
//  Число - номер последней корректировки документации уведомления о контролируемых сделках.
Функция ПоследняяКорректировкаДокументации(Организация, ОтчетныйГод) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ОтчетныйГод", Дата(ОтчетныйГод, 1, 1));
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДокументацияПоКонтролируемымСделкам.НомерКорректировки КАК НомерКорректировки
	|ИЗ
	|	Документ.ДокументацияПоКонтролируемымСделкам КАК ДокументацияПоКонтролируемымСделкам
	|ГДЕ
	|	ДокументацияПоКонтролируемымСделкам.Организация = &Организация
	|	И ДокументацияПоКонтролируемымСделкам.ОтчетныйГод = &ОтчетныйГод
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерКорректировки УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.НомерКорректировки + 1;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

// Возвращает коды методов ценообразования для документации уведомления о контролируемых сделках.
// 
// Возвращаемое значение:
//  СписокЗначений Из Строка - коды методов ценообразования для документации уведомления о контролируемых сделках.
Функция КодыМетодовЦенообразования() Экспорт
	
	КодыМетодов = Новый СписокЗначений;
	КодыМетодов.Добавить(КонтролируемыеСделкиЦенообразование.КодМетодаСопоставимыхРыночныхЦен(), НСтр("ru = '01 - метод сопоставимых рыночных цен';
																										|en = '01: Comparable market price method'"));
	КодыМетодов.Добавить(КонтролируемыеСделкиЦенообразование.КодМетодаЦеныПоследующейРеализации(), НСтр("ru = '02 - метод цены последующей реализации';
																										|en = '02: Following selling price method'"));
	КодыМетодов.Добавить(КонтролируемыеСделкиЦенообразование.КодМетодаЗатратного(), НСтр("ru = '03 - затратный метод';
																						|en = '03: Cost method'"));
	КодыМетодов.Добавить(КонтролируемыеСделкиЦенообразование.КодМетодаСопоставимойРентабельности(), НСтр("ru = '04 - метод сопоставимой рентабельности';
																										|en = '04: Comparable profitability method'"));
	КодыМетодов.Добавить(КонтролируемыеСделкиЦенообразование.КодМетодаРаспределенияПрибыли(), НСтр("ru = '05 - метод распределения прибыли';
																									|en = '05: Profit allocation method'"));
	КодыМетодов.Добавить(КонтролируемыеСделкиЦенообразование.КодМетодаКомбинацияМетодов(), НСтр("ru = '06 - комбинация методов';
																								|en = '06: Method combination'"));
	
	Возврат КодыМетодов;
	
КонецФункции

// Возвращает список выбора значений тестируемой стороны.
// 
// 
// Возвращаемое значение:
//	 СписокЗначений Из Строка - список значений для выбора значений тестируемой стороны.
//	                            Значение "1" - Налогоплательщик.
//	                            Значение "2" - Контрагент.
//
Функция СписокВыбораТестируемойСтороны() Экспорт
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить("1", НСтр("ru = 'Налогоплательщик';
									|en = 'Taxpayer'"));
	СписокВыбора.Добавить("2", НСтр("ru = 'Контрагент';
									|en = 'Counterparty'"));
	
	Возврат СписокВыбора;
	
КонецФункции

// Возвращает префикс имени файла приложения для отправки документации по контролируемым сделкам.
// 
// Параметры:
//  Уведомление - ДокументСсылка.УведомлениеОКонтролируемыхСделках - уведомление о контролируемых сделках.
// 
// Возвращаемое значение:
//  Строка - префикс имени файла приложения для отправки документации по контролируемым сделкам.
Функция ПрефиксИмениФайлаПриложенияДляОтправки(Уведомление) Экспорт
	
	Если Не ЗначениеЗаполнено(Уведомление) Тогда
		Возврат "";
	КонецЕсли;
	
	// Префикс имени документа имеет вид:
	// KD_O_P, где:
	// KD - префикс, принимающий значение равное 0300;
	// O - идентификатор отправителя имеет вид:
	//     для организаций - девятнадцатиразрядный код (ИНН и КПП организации);
	//     для физических лиц - 12-разрядный код (ИНН физического лица,
	//                          при отсутствии ИНН - последовательность из 12 нулей);
	// P - идентификатор конечного получателя - налогового органа, - 4-разрядный код налогового органа;
	
	ОсновныеСведения = Документы.УведомлениеОКонтролируемыхСделках.СведенияОбОрганизацииДляОтправки(
		Уведомление, ТекущаяДатаСеанса());
	
	Если ОсновныеСведения.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		ИНН_КПП = СокрЛП(ОсновныеСведения.ИНН) + СокрЛП(ОсновныеСведения.КПП);
	Иначе
		ИНН_КПП = СокрЛП(ОсновныеСведения.ИНН)
	КонецЕсли;
	
	Префикс = СтрШаблон("%1_%2_%3", "0300", ИНН_КПП, ОсновныеСведения.КодНалоговогоОргана);
	
	Возврат Префикс;
	
КонецФункции

// Возвращает имя файла приложения для отправки документации по контролируемым сделкам.
// 
// Параметры:
//  РеквизитыФайла         - Структура - реквизиты файла приложения к документации по контролируемым сделкам:
//  * ДатаСоздания        - Дата - дата загрузки файла в приложение к документации.
//  * Наименование        - Строка - наименование файла.
//  * Расширение          - Строка - расширение файла.
//  ПрефиксИмениФайла      - Строка - префикс имени файла.
//  ИдентификаторДокумента - Строка - идентификатор документа.
//  ИдентификаторФайла     - Строка - идентификатор файла.
// 
// Возвращаемое значение:
//  Строка - имя файла приложения для отправки документации по контролируемым сделкам.
Функция ИмяФайлаПриложенияДляОтправки(РеквизитыФайла, ПрефиксИмениФайла,
	ИдентификаторДокумента, ИдентификаторФайла) Экспорт
	
	Если Не ЗначениеЗаполнено(РеквизитыФайла) Тогда
		Возврат "";
	КонецЕсли;
	
	// Имя файла имеет формат: Префикс + _N1_GGGGMMDD_N2
	// GGGGMMDD - дата формирования файла;
	// N1, N2 - идентификационные номера файла (GUID).
	// Если документ состоит из нескольких файлов, проставляется:
	// N1 одинаковый для всех файлов одного документа,
	// N2 уникален для каждого файла независимо от принадлежности к документу.
	
	Возврат СтрШаблон("%1_%2_%3_%4", ПрефиксИмениФайла, ИдентификаторДокумента,
		Формат(РеквизитыФайла.ДатаСоздания, "ДФ=yyyyMMdd"), ИдентификаторФайла);
	
КонецФункции

// Проверяет заполнение документации по контролируемым сделкам.
// 
// Параметры:
//  ДокументацияУведомления - ДокументСсылка.ДокументацияПоКонтролируемымСделкам - ссылка на документацию
//                                                                               по контролируемым сделкам.
//  ДатаАктуальностиСведений - Дата - дата, на которую проверяются сведения по уведомлению.
//  УникальныйИдентификатор - УникальныйИдентификатор - идентификатор формы клиентского приложения документации.
// 
// Возвращаемое значение:
//  Структура - свойства адреса хранилища:
//  * АдресХранилища - Строка - адрес хранилища значений формы клиентного приложения документации.
Функция ПроверитьЗаполнениеДокументации(ДокументацияУведомления, ДатаАктуальностиСведений, УникальныйИдентификатор = Неопределено) Экспорт
	
	ОсновныеСведенияВыгрузки = Документы.ДокументацияПоКонтролируемымСделкам.ОсновныеСведенияДокументацииДляВыгрузки(ДокументацияУведомления);
	ДанныеДокументации = Документы.ДокументацияПоКонтролируемымСделкам.ДанныеДокументацииДляВыгрузки(
		ДокументацияУведомления, ДатаАктуальностиСведений);
	
	ИнформацияОбОшибках = ВыводСообщенийОбОшибках.НовыйДетальнаяИнформацияОбОшибках();
	
	Ошибки = Новый СписокЗначений;
	
	ПроверитьТитульныйЛист(ОсновныеСведенияВыгрузки, Ошибки);
	ПроверитьМетодыЦенообразования(ОсновныеСведенияВыгрузки, ДанныеДокументации, Ошибки);
	ПроверитьПриложения(ОсновныеСведенияВыгрузки, ДанныеДокументации, Ошибки);
	
	СформироватьИнформациюОбОшибках(ИнформацияОбОшибках, Ошибки);
	
	ДанныеДляВывода = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументацияУведомления, "Организация");
	ХранилищеЗначения = ВыводСообщенийОбОшибках.ОписаниеОшибок(ИнформацияОбОшибках, ПолучитьМакет("МакетВыводаОшибок"),
		ДанныеДляВывода);
	
	Если УникальныйИдентификатор = Неопределено Тогда
		АдресХранилища = ПоместитьВоВременноеХранилище(ХранилищеЗначения, Новый УникальныйИдентификатор);
	Иначе
		АдресХранилища = ПоместитьВоВременноеХранилище(ХранилищеЗначения, УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат Новый Структура("АдресХранилища", АдресХранилища);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьСведенияПодписанта(Подписант, СведенияОбОрганизацииДляОтправки)
	
	Подписант.ПризнакПодписанта = СведенияОбОрганизацииДляОтправки.Подписант.ПризнакПодписанта;
	Подписант.Фамилия = СведенияОбОрганизацииДляОтправки.Подписант.Фамилия;
	Подписант.Имя = СведенияОбОрганизацииДляОтправки.Подписант.Имя;
	Подписант.Отчество = СведенияОбОрганизацииДляОтправки.Подписант.Отчество;
	
	Если Подписант.ПризнакПодписанта <> "1" Тогда
		Подписант.НаименованиеДокумента =
			СведенияОбОрганизацииДляОтправки.Подписант.НаименованиеДоверенностиПредставителя;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьТитульныйЛист(ОсновныеСведения, Ошибки)
	
	ТекущиеОшибки = ОписаниеТаблицыОшибок();
	
	// Код налогового органа
	Если НЕ ЗначениеЗаполнено(ОсновныеСведения.КодНалоговогоОргана) 
		ИЛИ СтрДлина(ОсновныеСведения.КодНалоговогоОргана) <> 4 Тогда
		ДобавитьОшибку("1010", ОсновныеСведения.Организация, ТекущиеОшибки);
	КонецЕсли;
	
	// ИНН, КПП юр. лица
	Если НЕ ЗначениеЗаполнено(ОсновныеСведения.СведенияОНалогоплательщике.ИНН)
		ИЛИ СтрДлина(ОсновныеСведения.СведенияОНалогоплательщике.ИНН) <> 10
		ИЛИ НЕ ЗначениеЗаполнено(ОсновныеСведения.СведенияОНалогоплательщике.КПП)
		ИЛИ СтрДлина(ОсновныеСведения.СведенияОНалогоплательщике.КПП) <> 9 Тогда
		
		ДобавитьОшибку("1020", ОсновныеСведения.Организация, ТекущиеОшибки);
		
	КонецЕсли;
	
	// Наименование юр.лица
	Если НЕ ЗначениеЗаполнено(ОсновныеСведения.СведенияОНалогоплательщике.Наименование) Тогда
		ДобавитьОшибку("1030", ОсновныеСведения.Организация, ТекущиеОшибки);
	КонецЕсли;
	
	// Титульный лист ФИО подписанта
	ФамилияПодписанта = Неопределено; ИмяПодписанта = Неопределено;
	
	ОсновныеСведения.Подписант.Свойство("Фамилия", ФамилияПодписанта);
	ОсновныеСведения.Подписант.Свойство("Имя", ИмяПодписанта);
	
	ФИОПодписантаЗаполнено = ЗначениеЗаполнено(ФамилияПодписанта) И ЗначениеЗаполнено(ИмяПодписанта);
	
	Если Не ФИОПодписантаЗаполнено Тогда
		Если ОсновныеСведения.Подписант.ПризнакПодписанта = "1" Тогда // Если представителя нет
			ДобавитьОшибку("1040", ОсновныеСведения.Организация, ТекущиеОшибки);
		Иначе // есть представитель
			ДобавитьОшибку("1050", ОсновныеСведения.РегистрацияВНалоговомОргане, ТекущиеОшибки);
		КонецЕсли;
	КонецЕсли;
	
	// Наименование документа представителя
	Если ОсновныеСведения.Подписант.ПризнакПодписанта <> "1"
		И НЕ ЗначениеЗаполнено(ОсновныеСведения.Подписант.НаименованиеДокумента) Тогда
		ДобавитьОшибку("1060", ОсновныеСведения.РегистрацияВНалоговомОргане, ТекущиеОшибки);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОсновныеСведения.ИдентификаторФайлаОснования) Тогда
		ДобавитьОшибку("1070", ОсновныеСведения.ДокументацияУведомления, ТекущиеОшибки);
	КонецЕсли;
	
	Ошибки.Добавить(ТекущиеОшибки, НСтр("ru = 'Титульный лист';
										|en = 'Cover page'"));
	
КонецПроцедуры

Процедура ПроверитьМетодыЦенообразования(ОсновныеСведения, ДанныеДокументации, Ошибки)
	
	ТекущиеОшибки = ОписаниеТаблицыОшибок();
	
	КодыМетодовЦенообразования = Документы.ДокументацияПоКонтролируемымСделкам.КодыМетодовЦенообразования();
	
	Для Каждого СтрокаМетода Из ДанныеДокументации.МетодыЦенообразования Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаМетода.ПорядковыеНомераСделок)
			ИЛИ НЕ СтруктураПорядковыхНомеровСделокКорректна(СтрокаМетода.ПорядковыеНомераСделок) Тогда
			ДобавитьОшибку("2010", ДанныеВыводаОшибкиМетодаЦенообразования(ОсновныеСведения, СтрокаМетода), 
				ТекущиеОшибки);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаМетода.КодМетодаЦенообразования)
			Или КодыМетодовЦенообразования.НайтиПоЗначению(СтрокаМетода.КодМетодаЦенообразования) = Неопределено Тогда
			ДобавитьОшибку("2020", ДанныеВыводаОшибкиМетодаЦенообразования(ОсновныеСведения, СтрокаМетода),
				ТекущиеОшибки);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаМетода.СвойстваМетода) Тогда
			
			ДобавитьОшибку("2110", ДанныеВыводаОшибкиМетодаЦенообразования(ОсновныеСведения, СтрокаМетода),
				ТекущиеОшибки);
			
		Иначе
			
			Если СтрокаМетода.КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаКомбинацияМетодов() Тогда
				
				Для Каждого СтрокаСвойствМетода Из СтрокаМетода.СвойстваМетода Цикл
					ПроверитьСвойстваМетодаЦенообразования(ОсновныеСведения, СтрокаМетода, СтрокаСвойствМетода,
						ТекущиеОшибки);
				КонецЦикла;
				
			Иначе
				
				ПроверитьСвойстваМетодаЦенообразования(ОсновныеСведения, СтрокаМетода, СтрокаМетода.СвойстваМетода[0],
					ТекущиеОшибки);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаМетода.НаименованиеРус) Тогда
			ДобавитьОшибку("2070", СтрокаМетода.Контрагент, ТекущиеОшибки);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаМетода.КодСтраныРегистрации) Тогда
			ДобавитьОшибку("2080", СтрокаМетода.Контрагент, ТекущиеОшибки);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаМетода.КодСтороныСделки) Тогда
			ДобавитьОшибку("2090", ДанныеВыводаОшибкиМетодаЦенообразования(ОсновныеСведения, СтрокаМетода),
				ТекущиеОшибки);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаМетода.ИНН)
			И Не ЗначениеЗаполнено(СтрокаМетода.РегистрационныйНомер) Тогда
			ДобавитьОшибку("2100", СтрокаМетода.Контрагент, ТекущиеОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
	Ошибки.Добавить(ТекущиеОшибки, НСтр("ru = 'Методы ценообразования';
										|en = 'Pricing methods'"));
	
КонецПроцедуры

Процедура ПроверитьПриложения(ОсновныеСведения, ДанныеДокументации, Ошибки)
	
	ТекущиеОшибки = ОписаниеТаблицыОшибок();
	
	Если ОсновныеСведения.НомерКорректировки = 0
		И Не ЗначениеЗаполнено(ДанныеДокументации.Приложения) Тогда
		ДобавитьОшибку("3010", ОсновныеСведения.ДокументацияУведомления, ТекущиеОшибки);
	КонецЕсли;
	
	ИдентификаторыНезаполненныхДокументов = Новый Соответствие;
	
	Для Каждого СтрокаПриложения Из ДанныеДокументации.Приложения Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаПриложения.НаименованиеДокумента) Тогда
			ИдентификаторыНезаполненныхДокументов.Вставить(СтрокаПриложения.ИдентификаторДокумента, СтрокаПриложения);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого КлючЗначение Из ИдентификаторыНезаполненныхДокументов Цикл
		ДобавитьОшибку("3020", ДанныеВыводаОшибкиПриложенияДокумента(ОсновныеСведения, КлючЗначение.Значение),
			ТекущиеОшибки);
	КонецЦикла;
	
	Для Каждого СтрокаПриложения Из ДанныеДокументации.Приложения Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаПриложения.Файл) Тогда
			ДобавитьОшибку("3030", ДанныеВыводаОшибкиПриложенияФайлаДокумента(ОсновныеСведения, СтрокаПриложения),
				ТекущиеОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
	Ошибки.Добавить(ТекущиеОшибки, "Приложения");
	
КонецПроцедуры

Процедура СформироватьИнформациюОбОшибках(ИнформацияОбОшибках, Ошибки)
	
	ПредставлениеОшибок = ПолучитьОписаниеОшибок();
	
	Для Каждого Ошибка Из Ошибки Цикл
		
		РазделОтчета  = Ошибка.Представление;
		ТаблицаОшибок = Ошибка.Значение;
		
		ОписаниеОшибки = ВыводСообщенийОбОшибках.ДобавитьОписаниеОшибки(ИнформацияОбОшибках);
		
		СекцияРазделОтчета = ВыводСообщенийОбОшибках.ДобавитьСекцию(ОписаниеОшибки, "Раздел", РазделОтчета);
		
		Если ТаблицаОшибок.Количество() > 0 Тогда
			
			ТаблицаОшибок.Сортировать("Код");
			
			Для Каждого ДанныеОшибки Из ТаблицаОшибок Цикл
				
				ОписаниеОшибки = ПредставлениеОшибок.Найти(ДанныеОшибки.Код, "Код");
				
				Если ОписаниеОшибки = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				СекцияОшибка = ВыводСообщенийОбОшибках.ДобавитьСекцию(СекцияРазделОтчета, "Раздел",
					ОписаниеОшибки.Текст);
				
				Если ЗначениеЗаполнено(ОписаниеОшибки.Описание) Тогда
					ВыводСообщенийОбОшибках.ДобавитьСекцию(СекцияОшибка, "Текст", ОписаниеОшибки.Описание);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ОписаниеОшибки.Рекомендации) Тогда
					ВыводСообщенийОбОшибках.ДобавитьСекцию(СекцияОшибка, "Текст", ОписаниеОшибки.Рекомендации);
				КонецЕсли;
				
				Для Каждого ЭлементКоллекции Из ДанныеОшибки.Объекты Цикл
					Если ТипЗнч(ЭлементКоллекции.Значение) = Тип("Структура") Тогда
						ВыводСообщенийОбОшибках.ДобавитьСекцию(СекцияОшибка, "СсылкаПроизвольная",
							ЭлементКоллекции.Значение);
					Иначе
						ВыводСообщенийОбОшибках.ДобавитьСекцию(СекцияОшибка, "Ссылка", ЭлементКоллекции.Значение);
					КонецЕсли;
				КонецЦикла;
				
			КонецЦикла;
			
		Иначе
			
			ВыводСообщенийОбОшибках.ДобавитьСекцию(СекцияРазделОтчета, "Текст", НСтр("ru = 'Ошибок не обнаружено.';
																					|en = 'No errors detected.'"));
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьОшибку(КодОшибки, Объект, ТаблицаОшибок)
	
	ДанныеОшибки = ТаблицаОшибок.Найти(КодОшибки, "Код");
	
	Если ДанныеОшибки = Неопределено Тогда
		ДанныеОшибки = ТаблицаОшибок.Добавить();
		ДанныеОшибки.Код 	 = КодОшибки;
		ДанныеОшибки.Объекты = Новый Соответствие;
	КонецЕсли;
	
	Если ТипЗнч(Объект) = Тип("Структура") Тогда
		ДанныеОшибки.Объекты.Вставить(Объект.Ключ, Объект);
	Иначе
		ДанныеОшибки.Объекты.Вставить(Объект, Объект);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьОписаниеОшибок()
	
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка");
	
	Таблица = Новый ТаблицаЗначений;
	
	Таблица.Колонки.Добавить("Код", ОписаниеТиповСтрока);
	Таблица.Колонки.Добавить("Текст", ОписаниеТиповСтрока);
	Таблица.Колонки.Добавить("Описание", ОписаниеТиповСтрока);
	Таблица.Колонки.Добавить("Рекомендации", ОписаниеТиповСтрока);
	
	Макет = ПолучитьМакет("ОписаниеОшибок");
	ОписаниеОшибок = Макет.ПолучитьОбласть("ОписаниеОшибок");
	
	Для НомерСтроки = 1 По Макет.ВысотаТаблицы Цикл
		
		Код = ОписаниеОшибок.Область(НомерСтроки, 1).Текст;
		
		Если Не ЗначениеЗаполнено(Код) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Таблица.Добавить();
		НоваяСтрока.Код = Код;
		НоваяСтрока.Текст = ОписаниеОшибок.Область(НомерСтроки, 2).Текст;
		НоваяСтрока.Описание = ОписаниеОшибок.Область(НомерСтроки, 3).Текст;
		НоваяСтрока.Рекомендации = ОписаниеОшибок.Область(НомерСтроки, 4).Текст;
		
	КонецЦикла;
	
	Таблица.Индексы.Добавить("Код");
	
	Возврат Таблица;
	
КонецФункции

Функция ОписаниеТаблицыОшибок()
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Код");
	Таблица.Колонки.Добавить("Объекты");
	
	Возврат Таблица;
	
КонецФункции

Функция СтруктураПорядковыхНомеровСделокКорректна(ПорядковыеНомераСделок)
	
	ЧастиПорядковыхНомеров = СтрРазделить(ПорядковыеНомераСделок, "#");
	Для Каждого ЧастьПорядковыхНомеров Из ЧастиПорядковыхНомеров Цикл
		// Каждая часть может содержать либо диапазон номеров через "-", либо просто номер.
		ЧастиДиапазона = СтрРазделить(ЧастьПорядковыхНомеров, "-");
		Если ЧастиДиапазона.Количество() = 2 Тогда
			
			// Первая и вторая часть диапазона должна быть строками
			Если Не ЗначениеЗаполнено(ЧастиДиапазона[0])
				Или Не ЗначениеЗаполнено(ЧастиДиапазона[1])
				Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЧастиДиапазона[0])
				Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЧастиДиапазона[1]) Тогда
				Возврат Ложь;
			КонецЕсли;
			
			// Первая часть строки диапазона должна быть меньше, чем вторая
			Если Число(ЧастиДиапазона[0]) >= Число(ЧастиДиапазона[1]) Тогда
				Возврат Ложь;
			КонецЕсли;
			
		Иначе
			// Это не диапазон, проверим наличие символов, не равных числам.
			Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЧастьПорядковыхНомеров) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ДанныеВыводаОшибкиМетодаЦенообразования(ОсновныеСведения, СтрокаМетода)
	
	Расшифровка = ВыводСообщенийОбОшибках.НовыйОписаниеРасшифровки("ФормаДокументаСтрокаТабличнойЧасти");
	Расшифровка.Вставить("Объект", "Документ.ДокументацияПоКонтролируемымСделкам");
	Расшифровка.Вставить("Документ", ОсновныеСведения.ДокументацияУведомления);
	Расшифровка.Вставить("ИмяТабличнойЧасти", "МетодыЦенообразования");
	Расшифровка.Вставить("НомерСтроки", СтрокаМетода.НомерСтроки);
	
	ДанныеВывода = Новый Структура;
	ДанныеВывода.Вставить("Ключ",   ОсновныеСведения.ДокументацияУведомления);
	ДанныеВывода.Вставить("Ссылка", Расшифровка);
	ДанныеВывода.Вставить("Представление", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '""Методы ценообразования"", строка № %1';
			|en = '""Pricing methods"", line No. %1'"),
		СтрокаМетода.НомерСтроки));
	
	Возврат ДанныеВывода;
	
КонецФункции

Функция ДанныеВыводаОшибкиПриложенияДокумента(ОсновныеСведения, СтрокаПриложения)
	
	Расшифровка = ВыводСообщенийОбОшибках.НовыйОписаниеРасшифровки("ФормаДокументаПриложения");
	Расшифровка.Вставить("Объект", "Документ.ДокументацияПоКонтролируемымСделкам");
	Расшифровка.Вставить("Документ", ОсновныеСведения.ДокументацияУведомления);
	Расшифровка.Вставить("ИмяДерева", "ДеревоПриложений");
	Расшифровка.Вставить("ИдентификаторДокумента", СтрокаПриложения.ИдентификаторДокумента);
	
	ДанныеВывода = Новый Структура;
	ДанныеВывода.Вставить("Ключ",   ОсновныеСведения.ДокументацияУведомления);
	ДанныеВывода.Вставить("Ссылка", Расшифровка);
	ДанныеВывода.Вставить("Представление", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '""Приложения"", строка № %1';
			|en = '""Applications"", line No. %1'"),
		СтрокаПриложения.НомерСтроки));
	
	Возврат ДанныеВывода;
	
КонецФункции

Функция ДанныеВыводаОшибкиПриложенияФайлаДокумента(ОсновныеСведения, СтрокаПриложения)
	
	Расшифровка = ВыводСообщенийОбОшибках.НовыйОписаниеРасшифровки("ФормаДокументаПриложения");
	Расшифровка.Вставить("Объект", "Документ.ДокументацияПоКонтролируемымСделкам");
	Расшифровка.Вставить("Документ", ОсновныеСведения.ДокументацияУведомления);
	Расшифровка.Вставить("ИмяДерева", "ДеревоПриложений");
	Расшифровка.Вставить("ИдентификаторДокумента", СтрокаПриложения.ИдентификаторДокумента);
	Расшифровка.Вставить("ИдентификаторФайла", СтрокаПриложения.ИдентификаторФайла);
	
	ДанныеВывода = Новый Структура;
	ДанныеВывода.Вставить("Ключ",   ОсновныеСведения.ДокументацияУведомления);
	ДанныеВывода.Вставить("Ссылка", Расшифровка);
	ДанныеВывода.Вставить("Представление", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '""Приложения"", строка № %1';
			|en = '""Applications"", line No. %1'"),
		СтрокаПриложения.НомерСтроки));
	
	Возврат ДанныеВывода;
	
КонецФункции

Процедура ПроверитьСвойстваМетодаЦенообразования(ОсновныеСведения, СтрокаМетода, СтрокаСвойствМетода, ТекущиеОшибки)
	
	Если СтрокаСвойствМетода.КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаСопоставимыхРыночныхЦен() Тогда
		
		Если Не ЗначениеЗаполнено(СтрокаСвойствМетода.ИсточникИнформацииСопоставимыхЦен)
			Или Не ЗначениеЗаполнено(СтрокаСвойствМетода.НаименованиеКотировкиСопоставимыхЦен)
			Или Не ЗначениеЗаполнено(СтрокаСвойствМетода.ПорядокПримененияКотировкиСопоставимыхЦен)
			Или Не ЗначениеЗаполнено(СтрокаСвойствМетода.НаименованиеДокументаЦеныСопоставимыхЦен)
			Или Не ЗначениеЗаполнено(СтрокаСвойствМетода.НаименованиеКорректировкиДляСопоставимостиСопоставимыхЦен) Тогда
			ДобавитьОшибку("2030", ДанныеВыводаОшибкиМетодаЦенообразования(ОсновныеСведения, СтрокаМетода), ТекущиеОшибки);
		КонецЕсли;
		
	ИначеЕсли СтрокаМетода.КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаЦеныПоследующейРеализации()
		Или СтрокаМетода.КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаЗатратного()
		Или СтрокаМетода.КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаСопоставимойРентабельности() Тогда
		
		Если Не ЗначениеЗаполнено(СтрокаСвойствМетода.ПоказательРентабельности)
			Или Не ЗначениеЗаполнено(СтрокаСвойствМетода.ТестируемаяСторона) Тогда
			ДобавитьОшибку("2040", ДанныеВыводаОшибкиМетодаЦенообразования(ОсновныеСведения, СтрокаМетода), ТекущиеОшибки);
		КонецЕсли;
		
		Если СтрокаСвойствМетода.ТестируемаяСторона <> "1"
			И СтрокаСвойствМетода.ТестируемаяСторона <> "2" Тогда
			ДобавитьОшибку("2050", ДанныеВыводаОшибкиМетодаЦенообразования(ОсновныеСведения, СтрокаМетода), ТекущиеОшибки);
		КонецЕсли;
		
	ИначеЕсли СтрокаМетода.КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаРаспределенияПрибыли() Тогда
		
		Если Не ЗначениеЗаполнено(СтрокаСвойствМетода.ОписаниеМетодаРаспределенияПрибыли) Тогда
			ДобавитьОшибку("2060", ДанныеВыводаОшибкиМетодаЦенообразования(ОсновныеСведения, СтрокаМетода), ТекущиеОшибки);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция КлючСовпадает(КлючСтроки, Выборка)
	
	Если Не ЗначениеЗаполнено(КлючСтроки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из КлючСтроки Цикл
		Если КлючЗначение.Значение <> Выборка[КлючЗначение.Ключ] Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция НовыйТаблицаСвойстваЦенообразования()
	
	РеквизитыСвойстваЦенообразования = Метаданные.Документы.ДокументацияПоКонтролируемымСделкам.ТабличныеЧасти.СвойстваЦенообразования.Реквизиты;
	
	СвойстваЦенообразования = Новый ТаблицаЗначений;
	СвойстваЦенообразования.Колонки.Добавить("ИдентификаторСтрокиМетода",
		РеквизитыСвойстваЦенообразования.ИдентификаторСтрокиМетода.Тип);
	СвойстваЦенообразования.Колонки.Добавить("КодМетодаЦенообразования",
		РеквизитыСвойстваЦенообразования.КодМетодаЦенообразования.Тип);
	СвойстваЦенообразования.Колонки.Добавить("ИсточникИнформацииСопоставимыхЦен",
		РеквизитыСвойстваЦенообразования.ИсточникИнформацииСопоставимыхЦен.Тип);
	СвойстваЦенообразования.Колонки.Добавить("НаименованиеКотировкиСопоставимыхЦен",
		РеквизитыСвойстваЦенообразования.НаименованиеКотировкиСопоставимыхЦен.Тип);
	СвойстваЦенообразования.Колонки.Добавить("ПорядокПримененияКотировкиСопоставимыхЦен",
		РеквизитыСвойстваЦенообразования.ПорядокПримененияКотировкиСопоставимыхЦен.Тип);
	СвойстваЦенообразования.Колонки.Добавить("НаименованиеДокументаЦеныСопоставимыхЦен",
		РеквизитыСвойстваЦенообразования.НаименованиеДокументаЦеныСопоставимыхЦен.Тип);
	СвойстваЦенообразования.Колонки.Добавить("НаименованиеКорректировкиДляСопоставимостиСопоставимыхЦен",
		РеквизитыСвойстваЦенообразования.НаименованиеКорректировкиДляСопоставимостиСопоставимыхЦен.Тип);
	СвойстваЦенообразования.Колонки.Добавить("ПоказательРентабельности",
		РеквизитыСвойстваЦенообразования.ПоказательРентабельности.Тип);
	СвойстваЦенообразования.Колонки.Добавить("ТестируемаяСторона",
		РеквизитыСвойстваЦенообразования.ТестируемаяСторона.Тип);
	СвойстваЦенообразования.Колонки.Добавить("Рентабельность",
		РеквизитыСвойстваЦенообразования.Рентабельность.Тип);
	СвойстваЦенообразования.Колонки.Добавить("РыночнаяРентабельность",
		РеквизитыСвойстваЦенообразования.РыночнаяРентабельность.Тип);
	СвойстваЦенообразования.Колонки.Добавить("ОписаниеМетодаРаспределенияПрибыли",
		РеквизитыСвойстваЦенообразования.ОписаниеМетодаРаспределенияПрибыли.Тип);
	
	Возврат СвойстваЦенообразования;
	
КонецФункции

Функция ОписаниеПорядковыхНомеровСделок(МассивНомеров) 
	
	ЧастиОписанияПорядковыхНомеров = Новый Массив;
	
	ФорматнаяСтрока = "ЧЦ=10; ЧДЦ=0; ЧГ=0";
	
	НачалоДиапазона = МассивНомеров[0];
	ОкончаниеДиапазона = МассивНомеров[0];
	
	Для ИндексСтроки = 1 По МассивНомеров.Количество() - 1 Цикл
		
		ЗначениеНомера = МассивНомеров[ИндексСтроки];
		Если ОкончаниеДиапазона + 1 = ЗначениеНомера Тогда
			ОкончаниеДиапазона = ЗначениеНомера;
			Продолжить;
		КонецЕсли;
		
		Если НачалоДиапазона = ОкончаниеДиапазона Тогда
			// Диапазон состоит из одного значения, его и запишем
			ЧастиОписанияПорядковыхНомеров.Добавить(Формат(ОкончаниеДиапазона, ФорматнаяСтрока));
		Иначе
			// Диапазон состоит из нескольких значений. Запишем его через "-"
			ЧастиОписанияПорядковыхНомеров.Добавить(СтрШаблон("%1-%2", Формат(НачалоДиапазона, ФорматнаяСтрока),
				Формат(ОкончаниеДиапазона, ФорматнаяСтрока)));
		КонецЕсли;
		
		НачалоДиапазона = ЗначениеНомера;
		ОкончаниеДиапазона = ЗначениеНомера;
		
	КонецЦикла;
	
	Если НачалоДиапазона = ОкончаниеДиапазона Тогда
		// Диапазон состоит из одного значения, его и запишем
		ЧастиОписанияПорядковыхНомеров.Добавить(Формат(ОкончаниеДиапазона, ФорматнаяСтрока));
	Иначе
		// Диапазон состоит из нескольких значений. Запишем его через "-"
		ЧастиОписанияПорядковыхНомеров.Добавить(СтрШаблон("%1-%2", Формат(НачалоДиапазона, ФорматнаяСтрока) ,
			Формат(ОкончаниеДиапазона, ФорматнаяСтрока)));
	КонецЕсли;
	
	Возврат СтрСоединить(ЧастиОписанияПорядковыхНомеров, "#");
	
КонецФункции

#КонецОбласти

#КонецЕсли