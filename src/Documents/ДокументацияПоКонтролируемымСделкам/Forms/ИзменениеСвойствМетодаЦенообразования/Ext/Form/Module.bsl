#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	//@skip-check unknown-form-parameter-access
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ДанныеЗаполнения);
	
	ЗаполнитьСписокВыбораМетодыЦенообразования();
	
	ЗаполнитьСписокВыбораТестируемойСтороны();
	
	НастроитьФормуПоМетодуЦенообразования();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодМетодаЦенообразованияПриИзменении(Элемент)
	
	НастроитьФормуПоМетодуЦенообразования();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		Модифицированность = Ложь;
		СтруктураЗакрытия = СтруктураЗакрытия();
		Закрыть(СтруктураЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораМетодыЦенообразования()
	
	СписокМетодовЦенообразования = Документы.ДокументацияПоКонтролируемымСделкам.КодыМетодовЦенообразования();
	
	Элементы.КодМетодаЦенообразования.СписокВыбора.Очистить();
	Для Каждого ЭлементСписка Из СписокМетодовЦенообразования Цикл
		Если ЭлементСписка.Значение = КонтролируемыеСделкиЦенообразование.КодМетодаКомбинацияМетодов() Тогда
			Продолжить;
		КонецЕсли;
		Элементы.КодМетодаЦенообразования.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораТестируемойСтороны()
	
	СписокВыбораТестируемойСтороны = Документы.ДокументацияПоКонтролируемымСделкам.СписокВыбораТестируемойСтороны();
	Элементы.ТестируемаяСторона.СписокВыбора.Очистить();
	Для Каждого ЭлементСписка Из СписокВыбораТестируемойСтороны Цикл
		Элементы.ТестируемаяСторона.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоМетодуЦенообразования()
	
	КлючСохраненияПоложенияОкна = "КодМетодаЦенообразования:" + КодМетодаЦенообразования;
	
	Элементы.ГруппаМетодСопоставимыхЦен.Видимость = КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаСопоставимыхРыночныхЦен();
	Элементы.ГруппаИнформацияПоМетодам02_04.Видимость = КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаЦеныПоследующейРеализации()
		Или КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаЗатратного()
		Или КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаСопоставимойРентабельности();
	Элементы.ГруппаМетодРаспределенияПрибыли.Видимость = КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаРаспределенияПрибыли();
	
КонецПроцедуры

&НаСервере
Функция СтруктураЗакрытия()
	
	Структура = Новый Структура();
	
	Для Каждого Реквизит Из ПолучитьРеквизиты() Цикл
		Структура.Вставить(Реквизит.Имя, ЭтотОбъект[Реквизит.Имя]);
	КонецЦикла;
	
	Возврат Структура;
	
КонецФункции

#КонецОбласти