#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	КонтролируемыеСделки.ЗаполнитьСписокГоловныхОрганизаций(Элементы.Организация.СписокВыбора);
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) 
		И НЕ ЗначениеЗаполнено(Объект.ОтчетныйГод) Тогда
		Объект.ОтчетныйГод = НачалоГода(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	РегламентированнаяОтчетностьКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтотОбъект);
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганами.ОтметитьКакПрочтенное(Объект.Ссылка);
	
	НастроитьБаннерНекорректнойОрганизации();
	
	Если Параметры.Свойство("АктивироватьСтрокуТабличнойЧасти") Тогда
		//@skip-check unknown-form-parameter-access
		АктивироватьСтрокуТабличнойЧасти = Параметры.АктивироватьСтрокуТабличнойЧасти;
		ТекущийЭлемент = Элементы[АктивироватьСтрокуТабличнойЧасти.ТабличнаяЧасть];
		ТекущийЭлемент.ТекущаяСтрока = АктивироватьСтрокуТабличнойЧасти.НомерСтроки - 1;
	КонецЕсли;
	
	Если Параметры.Свойство("АктивироватьСтрокуПриложения") Тогда
		//@skip-check unknown-form-parameter-access
		АктивироватьСтрокуПриложения = Параметры.АктивироватьСтрокуПриложения;
		ТекущийЭлемент = Элементы[АктивироватьСтрокуПриложения.Дерево];
		Если АктивироватьСтрокуПриложения.Свойство("ИдентификаторДокумента") Тогда
			СтрокаНужногоДокумента = Неопределено;
			СтрокаНужногоФайла = Неопределено;
			Для Каждого СтрокаДокумента Из ДеревоПриложений.ПолучитьЭлементы() Цикл
				Если СтрокаДокумента.ИдентификаторДокумента = АктивироватьСтрокуПриложения.ИдентификаторДокумента Тогда
					СтрокаНужногоДокумента = СтрокаДокумента;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если СтрокаНужногоДокумента <> Неопределено
				И АктивироватьСтрокуПриложения.Свойство("ИдентификаторФайла") Тогда
				Для Каждого СтрокаФайлаДокумента Из СтрокаНужногоДокумента.ПолучитьЭлементы() Цикл
					Если СтрокаФайлаДокумента.ИдентификаторФайла = АктивироватьСтрокуПриложения.ИдентификаторФайла Тогда
						СтрокаНужногоФайла = СтрокаФайлаДокумента;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если СтрокаНужногоДокумента <> Неопределено Тогда
				Если СтрокаНужногоФайла = Неопределено Тогда
					Элементы[АктивироватьСтрокуПриложения.Дерево].ТекущаяСтрока = СтрокаНужногоДокумента.ПолучитьИдентификатор();
				Иначе
					Элементы[АктивироватьСтрокуПриложения.Дерево].ТекущийРодитель = СтрокаНужногоДокумента.ПолучитьИдентификатор();
					Элементы[АктивироватьСтрокуПриложения.Дерево].ТекущаяСтрока = СтрокаНужногоФайла.ПолучитьИдентификатор();
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.РаботаСФайлами
	ПараметрыГиперссылки = РаботаСФайлами.ГиперссылкаФайлов();
	ПараметрыГиперссылки.Размещение = "КоманднаяПанель";
	РаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыГиперссылки);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПодготовитьФормуНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	СохранитьДеревоПриложений(ТекущийОбъект);
	ПометитьНаУдалениеЛишниеПрисоединенныеФайлы(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	СохранитьСтатусОтправки();
	
	УправлениеФормой(ЭтотОбъект);
	Оповестить ("Запись_ДокументацияПоКонтролируемымСделкам", ПараметрыЗаписи, Объект.Ссылка);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область ПанельОтправкиВКонтролирующиеОрганы

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтотОбъект);
	ПараметрыИзменения.Вставить("Организация", Объект.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС"));
	ПараметрыИзменения.Вставить("ТекстВопроса", НСтр("ru = 'Уверены, что уведомление уже сдано?';
													|en = 'Are you sure the notification is already submitted?'"));
	
	ИнтерфейсыВзаимодействияБРОКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ПриИзмененииОрганизацииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтчетныйГодПриИзменении(Элемент)
	
	Объект.ОтчетныйГод = Дата(ОтчетныйГод, 1, 1);
	ПриИзмененииОтчетногоГодаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УведомлениеОКонтролируемойСделкеПриИзменении(Элемент)
	ПриИзмененииУведомленияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТипУведомленияПриИзменении(Элемент)
	
	Если ТипДокументации = 0 Тогда
		Объект.НомерКорректировки = 0;
	Иначе
		Объект.НомерКорректировки = ?(Объект.НомерКорректировки = 0, 1, Объект.НомерКорректировки);
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМетодыЦенообразования

&НаКлиенте
Процедура МетодыЦенообразованияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИзменениеСтрокиМетодаЦенообразования();
	
КонецПроцедуры

&НаКлиенте
Процедура МетодыЦенообразованияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ИзменениеСтрокиМетодаЦенообразования(Истина, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура МетодыЦенообразованияПередНачаломИзменения(Элемент, Отказ)
	
	ИзменениеСтрокиМетодаЦенообразования();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоПриложений

&НаКлиенте
Процедура ДеревоПриложенийПослеУдаления(Элемент)
	ЗаполнитьКоличествоДокументовВДереве(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.УровеньДерева = УровеньДереваФайл() Тогда
		ДанныеФайла = РаботаСФайламиКлиент.ДанныеФайла(ТекущиеДанные.Файл,
			УникальныйИдентификатор, Истина, Ложь);
		РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПриложенийПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ДеревоПриложений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.УровеньДерева = УровеньДереваФайл() Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область ОтправкаВФНС

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьУОператораЭДО(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(
		ЭтотОбъект, "ФНС");
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ДобавитьДокумент(Команда)
	
	ДобавитьДокументНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайлДокумента(Команда)
	
	ВыбратьИЗагрузитьФайлы();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыгрузитьУведомлениеВXML(Команда)
	
	Если Объект.Ссылка = ПредопределенноеЗначение("Документ.ДокументацияПоКонтролируемымСделкам.ПустаяСсылка") Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьДокументацию();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПроверитьЗаполнениеДокументации(Команда)
	
	Если Объект.Ссылка = ПредопределенноеЗначение("Документ.ДокументацияПоКонтролируемымСделкам.ПустаяСсылка") Тогда
		Возврат;
	КонецЕсли;
	
	ДатаАктуальностиСведений = КонецГода(Объект.ОтчетныйГод);
	РезультатВыполнения = ПроверитьЗаполнениеДокументацииНаСервере(Объект.Ссылка, ДатаАктуальностиСведений, УникальныйИдентификатор);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Объект.Ссылка);
	ПараметрыФормы.Вставить("АдресХранилища", РезультатВыполнения.АдресХранилища);
	
	ОткрытьФорму("Документ.ДокументацияПоКонтролируемымСделкам.Форма.ФормаОшибок",
		ПараметрыФормы, ЭтотОбъект, Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьДокументацию(Команда)
	
	ЗадатьВопросИОбновитьДокументациюАсинх();
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтотОбъект);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды


&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)
	 РаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	 РаботаСФайламиКлиент.ПолеПредпросмотраПроверкаПеретаскивания(ЭтотОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	 РаботаСФайламиКлиент.ПолеПредпросмотраПеретаскивание(ЭтотОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраНажатие(Элемент, СтандартнаяОбработка)
	 РаботаСФайламиКлиент.ПолеПредпросмотраНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ОтчетныйГод = Год(Объект.ОтчетныйГод);
	ТипДокументации = ?(Объект.НомерКорректировки = 0, 0, 1);
	
	ПрефиксИмениФайлаПриложения = Документы.ДокументацияПоКонтролируемымСделкам.ПрефиксИмениФайлаПриложенияДляОтправки(
		Объект.УведомлениеОКонтролируемойСделке);
	
	ЗаполнитьДеревоПриложений();
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ДеревоПриложенийНаименованиеДокумента");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ДеревоПриложений.УровеньДерева", ВидСравненияКомпоновкиДанных.Равно, УровеньДереваФайл());
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ГиперссылкаЦвет);
	
	ШрифтГиперссылка = Метаданные.ЭлементыСтиля.ШрифтГиперссылка;
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтГиперссылка.Значение);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Форма.Элементы.НомерКорректировки.Доступность = Форма.ТипДокументации <> 0;
	
	ДокументЗаписан = Форма.Объект.Ссылка <> ПредопределенноеЗначение("Документ.ДокументацияПоКонтролируемымСделкам.ПустаяСсылка");
	Форма.Элементы.МетодыЦенообразованияКомандаЗаполнитьДокументацию.Доступность = ДокументЗаписан;
	Форма.Элементы.ФормаКомандаПроверитьЗаполнениеДокументации.Доступность = ДокументЗаписан;
	Форма.Элементы.ФормаКомандаВыгрузитьУведомлениеВXML.Доступность = ДокументЗаписан;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьСтатусОтправки()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("СсылкаНаОбъект", Объект.Ссылка);
	СтруктураПараметров.Вставить("Форма", ЭтотОбъект);
	
	ИнтерфейсыВзаимодействияБРО.СохранитьСтатусОтправки(СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДокументацию()
	
	ВыгружаемыеДанные= ВыгрузитьДокументациюНаСервере(Объект.Ссылка);
	Если ВыгружаемыеДанные <> Неопределено Тогда
		ВыгрузитьДокументациюВФайлы(ВыгружаемыеДанные, "");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДокументациюВФайлы(ВыгружаемыеДанные, ПутьВыгрузки="")
	
	ПолучаемыеФайлы = Новый Массив;
	Для Каждого ФайлВыгрузки Из ВыгружаемыеДанные Цикл
		ПолучаемыеФайлы.Добавить(
			Новый ОписаниеПередаваемогоФайла(ФайлВыгрузки.ИмяФайлаВыгрузки, ФайлВыгрузки.АдресФайлаВыгрузки));
	КонецЦикла;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ВыгружаемыеДанные", ВыгружаемыеДанные);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьДокументациюВФайлыЗавершение",
		ЭтотОбъект, ДополнительныеПараметры);
	
	ФайловаяСистемаКлиент.СохранитьФайлы(ОписаниеОповещения, ПолучаемыеФайлы);
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьДвоичныеДанныеВыгрузки(ВыгружаемыеДанные)
	
	Для Каждого ФайлВыгрузки Из ВыгружаемыеДанные Цикл
		УдалитьИзВременногоХранилища(ФайлВыгрузки.АдресФайлаВыгрузки);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьДокументациюВФайлыЗавершение(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ПолученныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ВыгружаемыеДанные")
		И ЗначениеЗаполнено(ДополнительныеПараметры.ВыгружаемыеДанные) Тогда
		ОчиститьДвоичныеДанныеВыгрузки(ДополнительныеПараметры.ВыгружаемыеДанные);
	КонецЕсли;
	
	Для Каждого ПолученныйФайл Из ПолученныеФайлы Цикл
		Если ПолученныйФайл <> Неопределено Тогда
			ТекстОповещения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Нстр("ru = 'Выгружен файл %1';
					|en = 'File exported %1'"), ПолученныйФайл.Имя);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОповещения);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьДокументациюНаСервере(ДокументацияУведомления)
	
	Если ЗначениеЗаполнено(ДокументацияУведомления) Тогда
		ОбъектДокументации = ДокументацияУведомления.ПолучитьОбъект();
		Возврат ОбъектДокументации.ВыгрузитьДокумент(УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДеревоПриложений()
	
	ДеревоПриложений.ПолучитьЭлементы().Очистить();
	
	ФайлыДокументов = Новый Соответствие;
	Для Каждого СтрокаФайла Из Объект.ФайлыПриложения Цикл
		ФайлыДокумента = ФайлыДокументов.Получить(СтрокаФайла.ИдентификаторДокумента);
		Если ФайлыДокумента = Неопределено Тогда
			ФайлыДокумента = Новый Массив;
			ФайлыДокументов.Вставить(СтрокаФайла.ИдентификаторДокумента, ФайлыДокумента);
		КонецЕсли;
		
		ФайлыДокумента.Добавить(СтрокаФайла);
		
	КонецЦикла;
	
	СвойстваПрисоединенныхФайлов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		Объект.ФайлыПриложения.Выгрузить().ВыгрузитьКолонку("Файл"), "Наименование,Расширение,ДатаСоздания");
	
	Для Каждого СтрокаДокумента Из Объект.Приложения Цикл
		
		СтрокаДокументаВДереве = ДеревоПриложений.ПолучитьЭлементы().Добавить();
		СтрокаДокументаВДереве.НаименованиеДокумента = СтрокаДокумента.НаименованиеДокумента;
		СтрокаДокументаВДереве.ИдентификаторДокумента = СтрокаДокумента.ИдентификаторДокумента;
		СтрокаДокументаВДереве.УровеньДерева = УровеньДереваДокумент();
		
		ФайлыДокумента = ФайлыДокументов.Получить(СтрокаДокумента.ИдентификаторДокумента);
		Если Не ЗначениеЗаполнено(ФайлыДокумента) Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаФайлаДокумента Из ФайлыДокумента Цикл
			
			СтрокаФайлаДокументаВДереве = СтрокаДокументаВДереве.ПолучитьЭлементы().Добавить();
			СтрокаФайлаДокументаВДереве.УровеньДерева = УровеньДереваФайл();
			СтрокаФайлаДокументаВДереве.Файл = СтрокаФайлаДокумента.Файл;
			СтрокаФайлаДокументаВДереве.ИдентификаторФайла = СтрокаФайлаДокумента.ИдентификаторФайла;
			
			ЗаполнитьОписаниеФайлаСтроки(СтрокаФайлаДокументаВДереве, СтрокаДокумента, СвойстваПрисоединенныхФайлов);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ЗаполнитьКоличествоДокументовВДереве(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеФайлаСтроки(СтрокаФайлаДокументаВДереве, СтрокаДокумента, СвойстваПрисоединенныхФайлов)
	
	ПриложенныйФайл = СвойстваПрисоединенныхФайлов.Получить(СтрокаФайлаДокументаВДереве.Файл);
	
	Если Не ЗначениеЗаполнено(ПриложенныйФайл) Тогда
		СтрокаФайлаДокументаВДереве.НаименованиеДокумента = НСтр("ru = '<Не выбрано>';
																|en = '<Not selected>'");
	Иначе
		СтрокаФайлаДокументаВДереве.НаименованиеДокумента = ПриложенныйФайл.Наименование
			+ "." + ПриложенныйФайл.Расширение;
		СтрокаФайлаДокументаВДереве.ИмяФайлаДляОтправки = Документы.ДокументацияПоКонтролируемымСделкам.ИмяФайлаПриложенияДляОтправки(
			ПриложенныйФайл, ПрефиксИмениФайлаПриложения,
			СтрокаДокумента.ИдентификаторДокумента,
			СтрокаФайлаДокументаВДереве.ИдентификаторФайла);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьКоличествоДокументовВДереве(Форма)
	
	Форма.КоличествоДокументовВПриложении = Форма.ДеревоПриложений.ПолучитьЭлементы().Количество();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОрганизацииНаСервере()
	
	ПриИзмененииУведомленияНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОтчетногоГодаНаСервере()
	
	Если ЗначениеЗаполнено(Объект.УведомлениеОКонтролируемойСделке)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.УведомлениеОКонтролируемойСделке,
			"ОтчетныйГод") <> Объект.ОтчетныйГод Тогда
		Объект.УведомлениеОКонтролируемойСделке = Неопределено;
		ПриИзмененииУведомленияНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииУведомленияНаСервере()
	
	НастроитьБаннерНекорректнойОрганизации();
	
	НовыйПрефиксИмениФайлаПриложения = Документы.ДокументацияПоКонтролируемымСделкам.ПрефиксИмениФайлаПриложенияДляОтправки(
		Объект.УведомлениеОКонтролируемойСделке);
	
	Если НовыйПрефиксИмениФайлаПриложения <> ПрефиксИмениФайлаПриложения Тогда
		ПрефиксИмениФайлаПриложения = НовыйПрефиксИмениФайлаПриложения;
		
		ОбновитьИменаФайловВДереве();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеСтрокиМетодаЦенообразования(ЭтоНовый = Ложь, Копирование = Ложь)
	
	СтрокаТаблицы = Элементы.МетодыЦенообразования.ТекущиеДанные;
	Если Копирование
		И СтрокаТаблицы <> Неопределено Тогда
		ИдентификаторСкопированнойСтроки = СтрокаТаблицы.ИдентификаторСтроки;
		СтрокаТаблицы.ИдентификаторСтроки = Новый УникальныйИдентификатор();
		
		СтрокиСвойствСкопированнойСтроки = Объект.СвойстваЦенообразования.НайтиСтроки(
			Новый Структура("ИдентификаторСтрокиМетода", ИдентификаторСкопированнойСтроки));
		Для Каждого СтрокаСвойствСкопированнойСтроки Из СтрокиСвойствСкопированнойСтроки Цикл
			НоваяСтрокаСвойств = Объект.СвойстваЦенообразования.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСвойств,
				СтрокаСвойствСкопированнойСтроки, , "ИдентификаторСтрокиМетода");
			НоваяСтрокаСвойств.ИдентификаторСтрокиМетода = СтрокаТаблицы.ИдентификаторСтроки;
		КонецЦикла;
		
	КонецЕсли;
	
	ДанныеЗаполнения = ?(Не ЭтоНовый ИЛИ Копирование, СтруктураТабличнойЧастиМетодыЦенообразования(), Новый Структура);
	СтруктураПараметров	= Новый Структура;
	СтруктураПараметров.Вставить("ЭтоНовый", ЭтоНовый);
	СтруктураПараметров.Вставить("Копирование", Копирование);
	СтруктураПараметров.Вставить("ДанныеЗаполнения", ДанныеЗаполнения);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтруктураПараметров", СтруктураПараметров);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ИзменениеСтрокиМетодаЦенообразованияЗавершение",
		ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("Документ.ДокументацияПоКонтролируемымСделкам.Форма.ИзменениеМетодаЦенообразования",
		СтруктураПараметров, ЭтотОбъект,,,, ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураТабличнойЧастиМетодыЦенообразования()
	
	СтрокаТаблицы = Элементы.МетодыЦенообразования.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	СтруктураТабличнойЧастиМетодыЦенообразования = Новый Структура;
	СтруктураТабличнойЧастиМетодыЦенообразования.Вставить("ПорядковыеНомераСделок");
	СтруктураТабличнойЧастиМетодыЦенообразования.Вставить("СуммаДоходов");
	СтруктураТабличнойЧастиМетодыЦенообразования.Вставить("СуммаРасходов");
	СтруктураТабличнойЧастиМетодыЦенообразования.Вставить("Контрагент");
	СтруктураТабличнойЧастиМетодыЦенообразования.Вставить("КодСтороныСделки");
	СтруктураТабличнойЧастиМетодыЦенообразования.Вставить("ТипВзаимозависимости");
	СтруктураТабличнойЧастиМетодыЦенообразования.Вставить("КодМетодаЦенообразования");
	СтруктураТабличнойЧастиМетодыЦенообразования.Вставить("ИдентификаторСтроки");
	
	ЗаполнитьЗначенияСвойств(СтруктураТабличнойЧастиМетодыЦенообразования, СтрокаТаблицы);
	
	СвойстваЦенообразованияМетода = Новый Массив;
	Если ЗначениеЗаполнено(СтрокаТаблицы.ИдентификаторСтроки) Тогда
		
		ПоляСвойств = "КодМетодаЦенообразования,ИсточникИнформацииСопоставимыхЦен,НаименованиеКотировкиСопоставимыхЦен,
			|ПорядокПримененияКотировкиСопоставимыхЦен,НаименованиеДокументаЦеныСопоставимыхЦен,
			|НаименованиеКорректировкиДляСопоставимостиСопоставимыхЦен,ПоказательРентабельности,ТестируемаяСторона,
			|Рентабельность,РыночнаяРентабельность,ОписаниеМетодаРаспределенияПрибыли";
		СтрокиСвойств = СвойстваЦенообразованияДляМетода(СтрокаТаблицы.ИдентификаторСтроки);
		Для Каждого Строка Из СтрокиСвойств Цикл
			СвойстваЦенообразования = Новый Структура(ПоляСвойств);
			ЗаполнитьЗначенияСвойств(СвойстваЦенообразования, Строка);
			СвойстваЦенообразованияМетода.Добавить(СвойстваЦенообразования);
		КонецЦикла;
		
	КонецЕсли;
	
	СтруктураТабличнойЧастиМетодыЦенообразования.Вставить("СвойстваЦенообразованияМетода",
		СвойстваЦенообразованияМетода);
	
	Возврат СтруктураТабличнойЧастиМетодыЦенообразования;
	
КонецФункции

&НаКлиенте
Процедура ИзменениеСтрокиМетодаЦенообразованияЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	СтруктураДанныхСтроки = РезультатЗакрытия;
	
	Если СтруктураДанныхСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоНовый = ДополнительныеПараметры.СтруктураПараметров.ЭтоНовый;
	
	Если ЭтоНовый Тогда
		СтрокаТаблицы = Объект.МетодыЦенообразования.Добавить();
	Иначе
		СтрокаТаблицы = Объект.МетодыЦенообразования.НайтиПоИдентификатору(
			Элементы.МетодыЦенообразования.ТекущаяСтрока);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтруктураДанныхСтроки);
	ЗаполнитьИдентификаторСтрокиМетодаЦенообразования(СтрокаТаблицы);
	
	СтрокиСвойствЦенообразования = СвойстваЦенообразованияДляМетода(СтрокаТаблицы.ИдентификаторСтроки);
	Для Каждого СтрокаСвойствЦенообразования Из СтрокиСвойствЦенообразования Цикл
		Объект.СвойстваЦенообразования.Удалить(СтрокаСвойствЦенообразования);
	КонецЦикла;
	
	Для Каждого СтрокаСвойствЦенообразования Из СтруктураДанныхСтроки.СвойстваЦенообразования Цикл
		НоваяСтрокаСвойстваЦенообразования = Объект.СвойстваЦенообразования.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСвойстваЦенообразования, СтрокаСвойствЦенообразования);
		НоваяСтрокаСвойстваЦенообразования.ИдентификаторСтрокиМетода = СтрокаТаблицы.ИдентификаторСтроки;
	КонецЦикла;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьИдентификаторСтрокиМетодаЦенообразования(СтрокаМетодаЦенообразования)
	Если Не ЗначениеЗаполнено(СтрокаМетодаЦенообразования.ИдентификаторСтроки) Тогда
		СтрокаМетодаЦенообразования.ИдентификаторСтроки = Новый УникальныйИдентификатор();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция СвойстваЦенообразованияДляМетода(ИдентификаторСтрокиМетода)
	Возврат Объект.СвойстваЦенообразования.НайтиСтроки(
		Новый Структура("ИдентификаторСтрокиМетода", ИдентификаторСтрокиМетода));
КонецФункции

&НаКлиенте
Процедура ДобавитьДокументНаКлиенте()
	
	СтрокаДереваДокумент = ДобавитьДокументВДерево(ДеревоПриложений);
	
	Элементы.ДеревоПриложений.ТекущаяСтрока = СтрокаДереваДокумент.ПолучитьИдентификатор();
	Элементы.ДеревоПриложений.ТекущийЭлемент = Элементы.ДеревоПриложенийНаименованиеДокумента;
	Элементы.ДеревоПриложений.ИзменитьСтроку();
	
	ЗаполнитьКоличествоДокументовВДереве(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДобавитьДокументВДерево(ДеревоПриложений)
	
	СтрокаДереваДокумент = ДеревоПриложений.ПолучитьЭлементы().Добавить();
	СтрокаДереваДокумент.ИдентификаторДокумента = Новый УникальныйИдентификатор();
	СтрокаДереваДокумент.УровеньДерева = УровеньДереваДокумент();
	Возврат СтрокаДереваДокумент;
	
КонецФункции

&НаКлиенте
Процедура ВыбратьИЗагрузитьФайлы()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Записать();
	КонецЕсли;
	
	ОписаниеОповещенияЗагрузкиФайлов = Новый ОписаниеОповещения("ВыбратьИЗагрузитьФайлыЗавершение", ЭтотОбъект);
	
	Фильтр = " (*.pdf)|*.pdf|(*.ods)|*.ods";
	РаботаСФайламиКлиент.ДобавитьФайлы(Объект.Ссылка, УникальныйИдентификатор,
		Фильтр, , ОписаниеОповещенияЗагрузкиФайлов);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИЗагрузитьФайлыЗавершение(ДобавленныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ДобавленныеФайлы) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторТекущейСтроки = Элементы.ДеревоПриложений.ТекущаяСтрока;
	ДобавитьФайлыКДокументу(ДобавленныеФайлы, ИдентификаторТекущейСтроки);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьФайлыКДокументу(ДобавленныеФайлы, ИдентификаторТекущейСтроки)
	
	Если ИдентификаторТекущейСтроки = Неопределено Тогда
		ДокументДляВставки = ДобавитьДокументВДерево(ДеревоПриложений);
	Иначе
		ТекущиеДанные = ДеревоПриложений.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
		Родитель = ТекущиеДанные.ПолучитьРодителя();
		Если Родитель = Неопределено Тогда
			ДокументДляВставки = ТекущиеДанные;
		Иначе
			ДокументДляВставки = Родитель;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ДобавленныйФайл Из ДобавленныеФайлы Цикл
		СтрокаДереваФайл = ДобавитьФайлКСтрокеДокумента(ДокументДляВставки, ДобавленныйФайл);
	КонецЦикла;
	
	Элементы.ДеревоПриложений.ТекущаяСтрока = СтрокаДереваФайл.ПолучитьИдентификатор();
	Элементы.ДеревоПриложений.ТекущийЭлемент = Элементы.ДеревоПриложенийНаименованиеДокумента;
	Записать();
КонецПроцедуры

&НаСервере
Функция ДобавитьФайлКСтрокеДокумента(СтрокаФайлаДокумента, Файл)
	
	СтрокаДереваФайл = СтрокаФайлаДокумента.ПолучитьЭлементы().Добавить();
	СтрокаДереваФайл.ИдентификаторФайла = Новый УникальныйИдентификатор();
	СтрокаДереваФайл.УровеньДерева = УровеньДереваФайл();
	СтрокаДереваФайл.Файл = Файл;
	
	СвойстваПрисоединенныхФайлов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Файл), "Наименование,Расширение,ДатаСоздания");
	
	ЗаполнитьОписаниеФайлаСтроки(СтрокаДереваФайл, СтрокаФайлаДокумента, СвойстваПрисоединенныхФайлов);
	
	Возврат СтрокаДереваФайл;
	
КонецФункции

&НаСервере
Процедура ОбновитьИменаФайловВДереве()
	
	СтрокиФайлов = Новый Массив;
	ФайлыДокументов = Новый Массив;
	
	Для Каждого СтрокаДокумента Из ДеревоПриложений.ПолучитьЭлементы() Цикл
		Для Каждого СтрокаФайла Из СтрокаДокумента.ПолучитьЭлементы() Цикл
			СтрокиФайлов.Добавить(СтрокаФайла);
			Если ЗначениеЗаполнено(СтрокаФайла.Файл) Тогда
				ФайлыДокументов.Добавить(СтрокаФайла.Файл);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	СвойстваПрисоединенныхФайлов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		ФайлыДокументов, "Наименование,Расширение,ДатаСоздания");
	
	Для Каждого СтрокаДокумента Из ДеревоПриложений.ПолучитьЭлементы() Цикл
		Для Каждого СтрокаФайла Из СтрокаДокумента.ПолучитьЭлементы() Цикл
			ЗаполнитьОписаниеФайлаСтроки(СтрокаФайла, СтрокаДокумента, СвойстваПрисоединенныхФайлов);
		КонецЦикла;
	КонецЦикла
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДеревоПриложений(ТекущийОбъект)
	
	ТекущийОбъект.Приложения.Очистить();
	ТекущийОбъект.ФайлыПриложения.Очистить();
	
	Для Каждого СтрокаДокументаДерева Из ДеревоПриложений.ПолучитьЭлементы() Цикл
		СтрокаДокумента = ТекущийОбъект.Приложения.Добавить();
		СтрокаДокумента.НаименованиеДокумента = СтрокаДокументаДерева.НаименованиеДокумента;
		СтрокаДокумента.ИдентификаторДокумента = СтрокаДокументаДерева.ИдентификаторДокумента;
		
		Для Каждого СтрокаФайлаДерева Из СтрокаДокументаДерева.ПолучитьЭлементы() Цикл
			СтрокаФайла = ТекущийОбъект.ФайлыПриложения.Добавить();
			СтрокаФайла.ИдентификаторДокумента = СтрокаДокументаДерева.ИдентификаторДокумента;
			СтрокаФайла.ИдентификаторФайла = СтрокаФайлаДерева.ИдентификаторФайла;
			СтрокаФайла.Файл = СтрокаФайлаДерева.Файл;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция УровеньДереваДокумент()
	Возврат 1;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция УровеньДереваФайл()
	Возврат 2;
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьЗаполнениеДокументацииНаСервере(ДокументацияУведомления, ДатаАктуальностиСведений, УникальныйИдентификатор)
	
	РезультатВыполнения = Документы.ДокументацияПоКонтролируемымСделкам.ПроверитьЗаполнениеДокументации(
		ДокументацияУведомления, ДатаАктуальностиСведений, УникальныйИдентификатор);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервере
Процедура НастроитьБаннерНекорректнойОрганизации()
	
	ЭтоФизЛицо = ОпределитьПризнакФизическогоЛица(Объект.Организация);
	Элементы.ГруппаБаннерНекорректнойОрганизации.Видимость = ЭтоФизЛицо;
	Если ЭтоФизЛицо Тогда
		Элементы.ГруппаОрганизацияГод.ЦветФона = ЦветаСтиля.ЦветФонаПредупреждения;
	Иначе
		Элементы.ГруппаОрганизацияГод.ЦветФона = Новый Цвет();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОпределитьПризнакФизическогоЛица(Организация)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЮридическоеФизическоеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ЮридическоеФизическоеЛицо");
	Возврат ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	
КонецФункции

&НаКлиенте
Асинх Процедура ЗадатьВопросИОбновитьДокументациюАсинх()
	
	Если ЗначениеЗаполнено(Объект.МетодыЦенообразования) Тогда
		
		ТекстВопроса = НСтр("ru = 'Методы ценообразования уже заполнены.
			|Перезаполнить?';
			|en = 'Pricing methods are already filled in.
			|Refill?'");
		Ответ = Ждать ВопросАсинх(ТекстВопроса,РежимДиалогаВопрос.ДаНет, , ,
			НСтр("ru = 'Заполнение методов ценообразования';
				|en = 'Fill in pricing methods'"));
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьДокументациюНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументациюНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЗаполнения = Документы.ДокументацияПоКонтролируемымСделкам.ДанныеЗаполненияМетодовЦенообразования(
		Объект.Ссылка);
	
	Документы.ДокументацияПоКонтролируемымСделкам.ПерезаполнитьМетодыЦенообразования(
		Объект, ДанныеЗаполнения);
	
КонецПроцедуры

&НаСервере
Процедура ПометитьНаУдалениеЛишниеПрисоединенныеФайлы(ТекущийОбъект)
	
	Если Не ЗначениеЗаполнено(ТекущийОбъект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	// Пользователь может удалить строку с документом. Соответственно, нужно пометить на удаление
	// и этот самый присоединенный файл. Это нужно делать только в том случае, если документ уже
	// существовал.
	// Для этого вытащим все присоединенные файлы, которые указаны в документации до записи. И удалим
	// те, которые есть в сохраненных данных, но нет в текущих.
	
	ПрисоединенныеФайлыПриложений = Новый Соответствие;
	Для Каждого СтрокаФайла Из ТекущийОбъект.ФайлыПриложения Цикл
		Если ЗначениеЗаполнено(СтрокаФайла.Файл) Тогда
			ПрисоединенныеФайлыПриложений.Вставить(СтрокаФайла.Файл, Истина);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документация", ТекущийОбъект.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|       ДокументацияПоКонтролируемымСделкамФайлыПриложения.Файл КАК Файл
	|ИЗ
	|       Документ.ДокументацияПоКонтролируемымСделкам.ФайлыПриложения КАК ДокументацияПоКонтролируемымСделкамФайлыПриложения
	|ГДЕ
	|       ДокументацияПоКонтролируемымСделкамФайлыПриложения.Ссылка = &Документация";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

	Если Не ЗначениеЗаполнено(Выборка.Файл) Тогда
	Продолжить;
	КонецЕсли;
	
	Если ПрисоединенныеФайлыПриложений.Получить(Выборка.Файл) <> Неопределено Тогда
		Продолжить;
	КонецЕсли;
	
	ФайлОбъект = Выборка.Файл.ПолучитьОбъект();
	Если ФайлОбъект = Неопределено Тогда
		Продолжить;
	КонецЕсли;

	ФайлОбъект.УстановитьПометкуУдаления(Истина, Истина);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

