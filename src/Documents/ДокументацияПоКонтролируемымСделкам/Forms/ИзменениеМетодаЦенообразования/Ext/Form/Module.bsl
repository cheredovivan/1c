#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	//@skip-check unknown-form-parameter-access
	ДанныеЗаполнения = Параметры.ДанныеЗаполнения;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ДанныеЗаполнения.Свойство("СвойстваЦенообразованияМетода") Тогда
		Для Каждого СтрокаСвойствЦенообразования Из ДанныеЗаполнения.СвойстваЦенообразованияМетода Цикл
			ЗаполнитьЗначенияСвойств(СвойстваЦенообразования.Добавить(), СтрокаСвойствЦенообразования);
		КонецЦикла;
		ЗаполнитьОписанияСтрокСвойствЦенообразованияМетода();
	КонецЕсли;
	
	ЗаполнитьСписокВыбораМетодыЦенообразования();
	
	ЗаполнитьСписокКодовСтороныСделок();
	
	ЗаполнитьСписокВыбораТестируемойСтороны();
	
	НастроитьФормуПоМетодуЦенообразования();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодМетодаЦенообразованияПриИзменении(Элемент)
	
	НастроитьФормуПоМетодуЦенообразования();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСвойстваЦенообразования

&НаКлиенте
Процедура СвойстваЦенообразованияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьСвойстваМетодаЦенообразования();
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваЦенообразованияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ОткрытьСвойстваМетодаЦенообразования(Истина, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваЦенообразованияПередНачаломИзменения(Элемент, Отказ)
	
	ОткрытьСвойстваМетодаЦенообразования();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		Модифицированность = Ложь;
		СтруктураЗакрытия = СтруктураЗакрытия();
		Закрыть(СтруктураЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СтруктураЗакрытия()
	
	Структура = Новый Структура();
	
	ОчиститьЛишниеПоляСвойстваЦенообразования();
	
	Для Каждого Реквизит Из ПолучитьРеквизиты() Цикл
		Структура.Вставить(Реквизит.Имя, ЭтотОбъект[Реквизит.Имя]);
	КонецЦикла;
	
	Возврат Структура;
	
КонецФункции

&НаСервере
Процедура ОчиститьЛишниеПоляСвойстваЦенообразования()
	
	ИменаКолонок = Новый Массив;
	
	ТаблицаСвойств = СвойстваЦенообразования.Выгрузить();
	Для Каждого Колонка Из ТаблицаСвойств.Колонки Цикл
		Если Колонка.Имя = "ИдентификаторСтрокиМетода"
			Или Колонка.Имя = "КодМетодаЦенообразования" Тогда
			Продолжить;
		КонецЕсли;
		ИменаКолонок.Добавить(Колонка.Имя);
	КонецЦикла;
	
	Для Каждого СтрокаСвойства Из СвойстваЦенообразования Цикл
		ПоляСвойствМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.ПоляСвойствМетодаЦенообразования(
			СтрокаСвойства.КодМетодаЦенообразования);
		Для Каждого ИмяКолонки Из ИменаКолонок Цикл
			Если ПоляСвойствМетодаЦенообразования.Найти(ИмяКолонки) = Неопределено Тогда
				СтрокаСвойства[ИмяКолонки] = Неопределено;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораМетодыЦенообразования()
	
	СписокМетодовЦенообразования = Документы.ДокументацияПоКонтролируемымСделкам.КодыМетодовЦенообразования();
	
	Элементы.КодМетодаЦенообразования.СписокВыбора.Очистить();
	Для Каждого ЭлементСписка Из СписокМетодовЦенообразования Цикл
		Элементы.КодМетодаЦенообразования.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокКодовСтороныСделок()
	
	СоответствиеКодов = КонтролируемыеСделки.ПолучитьСоответствиеКодовСтороныСделки();
	Элементы.КодСтороныСделки.СписокВыбора.Очистить();
	
	Для Каждого КлючЗначение Из СоответствиеКодов Цикл
		Для Каждого ЭлементСписка Из КлючЗначение.Значение Цикл
			Если Элементы.КодСтороныСделки.СписокВыбора.НайтиПоЗначению(ЭлементСписка.Значение) = Неопределено Тогда
				Элементы.КодСтороныСделки.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Элементы.КодСтороныСделки.СписокВыбора.СортироватьПоЗначению();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораТестируемойСтороны()
	
	СписокВыбораТестируемойСтороны = Документы.ДокументацияПоКонтролируемымСделкам.СписокВыбораТестируемойСтороны();
	Элементы.ТестируемаяСторона.СписокВыбора.Очистить();
	Для Каждого ЭлементСписка Из СписокВыбораТестируемойСтороны Цикл
		Элементы.ТестируемаяСторона.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоМетодуЦенообразования()
	
	УстановитьСтрокиСвойстваЦенообразования();
	
	КлючСохраненияПоложенияОкна = "КодМетодаЦенообразования:" + КодМетодаЦенообразования;
	
	Элементы.ГруппаМетодСопоставимыхЦен.Видимость = КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаСопоставимыхРыночныхЦен();
	Элементы.ГруппаИнформацияПоМетодам02_04.Видимость = КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаЦеныПоследующейРеализации()
		Или КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаЗатратного()
		Или КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаСопоставимойРентабельности();
	Элементы.ГруппаМетодРаспределенияПрибыли.Видимость = КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаРаспределенияПрибыли();
	Элементы.ГруппаКомбинацияМетодов.Видимость = КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаКомбинацияМетодов();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтрокиСвойстваЦенообразования()
	
	Если КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаКомбинацияМетодов() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КодМетодаЦенообразования) Тогда
		Возврат;
	КонецЕсли;
	
	Если СвойстваЦенообразования.Количество() = 0 Тогда
		ПерваяСтрока = СвойстваЦенообразования.Добавить();
		ПерваяСтрока.КодМетодаЦенообразования = КодМетодаЦенообразования;
	Иначе
		СтрокиМетодаЦенообразования = СвойстваЦенообразования.НайтиСтроки(
			Новый Структура("КодМетодаЦенообразования", КодМетодаЦенообразования));
		Если СтрокиМетодаЦенообразования.Количество() = 0 Тогда
			// С таким методом ценообразования строки нет.
			// Поэтому вставим на 1 место строку с нужным методом.
			ПерваяСтрока = СвойстваЦенообразования.Вставить(0);
			ПерваяСтрока.КодМетодаЦенообразования = КодМетодаЦенообразования;
		Иначе
			// Передвинем эту строку на первое место.
			ПерваяСтрока = СтрокиМетодаЦенообразования[0];
			ИндексСтроки = СвойстваЦенообразования.Индекс(ПерваяСтрока);
			Если ИндексСтроки <> 0 Тогда
				СвойстваЦенообразования.Сдвинуть(ИндексСтроки, -ИндексСтроки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписанияСтрокСвойствЦенообразованияМетода()
	
	Для Каждого Строка Из СвойстваЦенообразования Цикл
		Строка.ОписаниеМетода = ОписаниеМетодаСтрокиСвойствЦенообразования(Строка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеМетодаСтроки(ИдентификаторТекущейСтроки)
	
	ТекущаяСтрока = СвойстваЦенообразования.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
	ТекущаяСтрока.ОписаниеМетода = ОписаниеМетодаСтрокиСвойствЦенообразования(ТекущаяСтрока);
	
КонецПроцедуры

&НаСервере
Функция ОписаниеМетодаСтрокиСвойствЦенообразования(Строка)
	
	ПоляМетода = КонтролируемыеСделкиЦенообразование.ПоляСвойствМетодаЦенообразования(
		Строка.КодМетодаЦенообразования);
	
	ОписаниеСвойствПоМетоду = КонтролируемыеСделкиЦенообразование.ОписаниеМетодаСтрокиСвойствЦенообразования(Строка, ПоляМетода);
	
	Если ЗначениеЗаполнено(ОписаниеСвойствПоМетоду) Тогда
		Возврат ОписаниеСвойствПоМетоду;
	Иначе
		Возврат ТекстЗаполнить();
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ТекстЗаполнить()
	Возврат НСтр("ru = 'Заполнить';
				|en = 'Fill'");
КонецФункции

&НаКлиенте
Процедура ОткрытьСвойстваМетодаЦенообразования(ЭтоНовый = Ложь, Копирование = Ложь)
	
	ДанныеЗаполнения = ?(Не ЭтоНовый ИЛИ Копирование, СтруктураСтрокиСвойстваМетода(), Новый Структура);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ЭтоНовый", ЭтоНовый);
	СтруктураПараметров.Вставить("Копирование", Копирование);
	СтруктураПараметров.Вставить("ДанныеЗаполнения", ДанныеЗаполнения);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтруктураПараметров", СтруктураПараметров);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОткрытьСвойстваМетодаЦенообразованияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("Документ.ДокументацияПоКонтролируемымСделкам.Форма.ИзменениеСвойствМетодаЦенообразования",
		СтруктураПараметров, ЭтотОбъект,,,, ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураСтрокиСвойстваМетода()
	
	СтрокаТаблицы = Элементы.СвойстваЦенообразования.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	СтруктураТабличнойЧастиСвойстваЦенообразования = Новый Структура;
	СтруктураТабличнойЧастиСвойстваЦенообразования.Вставить("КодМетодаЦенообразования");
	СтруктураТабличнойЧастиСвойстваЦенообразования.Вставить("ИсточникИнформацииСопоставимыхЦен");
	СтруктураТабличнойЧастиСвойстваЦенообразования.Вставить("НаименованиеКотировкиСопоставимыхЦен");
	СтруктураТабличнойЧастиСвойстваЦенообразования.Вставить("ПорядокПримененияКотировкиСопоставимыхЦен");
	СтруктураТабличнойЧастиСвойстваЦенообразования.Вставить("НаименованиеДокументаЦеныСопоставимыхЦен");
	СтруктураТабличнойЧастиСвойстваЦенообразования.Вставить("НаименованиеКорректировкиДляСопоставимостиСопоставимыхЦен");
	СтруктураТабличнойЧастиСвойстваЦенообразования.Вставить("ПоказательРентабельности");
	СтруктураТабличнойЧастиСвойстваЦенообразования.Вставить("ТестируемаяСторона");
	СтруктураТабличнойЧастиСвойстваЦенообразования.Вставить("Рентабельность");
	СтруктураТабличнойЧастиСвойстваЦенообразования.Вставить("РыночнаяРентабельность");
	СтруктураТабличнойЧастиСвойстваЦенообразования.Вставить("ОписаниеМетодаРаспределенияПрибыли");
	
	ЗаполнитьЗначенияСвойств(СтруктураТабличнойЧастиСвойстваЦенообразования, СтрокаТаблицы);
	
	Возврат СтруктураТабличнойЧастиСвойстваЦенообразования;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьСвойстваМетодаЦенообразованияЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	СтруктураДанныхСтроки = РезультатЗакрытия;
	
	Если СтруктураДанныхСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоНовый = ДополнительныеПараметры.СтруктураПараметров.ЭтоНовый;
	Если ЭтоНовый Тогда
		СтрокаТаблицы = СвойстваЦенообразования.Добавить();
		ИдентификаторТекущейСтроки = СтрокаТаблицы.ПолучитьИдентификатор();
	Иначе
		ИдентификаторТекущейСтроки = Элементы.СвойстваЦенообразования.ТекущаяСтрока;
		СтрокаТаблицы = СвойстваЦенообразования.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтруктураДанныхСтроки);
	
	ЗаполнитьОписаниеМетодаСтроки(ИдентификаторТекущейСтроки);
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

