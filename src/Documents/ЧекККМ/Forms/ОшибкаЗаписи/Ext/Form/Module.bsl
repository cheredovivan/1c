
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = Параметры.ТекстСообщения;
	
	МассивСтрокТекстаСообщения = Новый Массив;
	МассивСтрокТекстаСообщения.Добавить(ТекстСообщения);
	
	ДанныеДляЖурналаРегистрации = Параметры.ДанныеДляЖурналаРегистрации;
	Если ТипЗнч(ДанныеДляЖурналаРегистрации) = Тип("Структура") Тогда
		МассивДанных = Новый Массив;
		Для Каждого КлючИЗначение Из ДанныеДляЖурналаРегистрации Цикл
			МассивДанных.Добавить(Строка(КлючИЗначение.Ключ) + "=" + Строка(КлючИЗначение.Значение));
		КонецЦикла;
		Если МассивДанных.Количество() Тогда
			МассивСтрокТекстаСообщения.Добавить(НСтр("ru = 'Данные';
													|en = 'Data'")+ ":" + СтрСоединить(МассивДанных, ","));
		КонецЕсли;
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Розничные продажи';
			|en = 'Retail sales'", ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Ошибка,,
		Параметры.Данные,
		СтрСоединить(МассивСтрокТекстаСообщения, " "));
	
КонецПроцедуры
