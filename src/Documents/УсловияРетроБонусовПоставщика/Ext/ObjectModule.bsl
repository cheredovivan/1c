//@strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("ДокументСсылка.УсловияРетроБонусовПоставщика") Тогда
		
		СсылкаНаДокумент = ДанныеЗаполнения; // ДокументСсылка.УсловияРетроБонусовПоставщика
		ЗаполнитьПоУсловиюРетроБонусовПоставщиков(СсылкаНаДокумент);
		
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(НачалоДействия)
	   И ЗначениеЗаполнено(ОкончаниеДействия)
	   И ОкончаниеДействия < НачалоДействия Тогда
		
		ТекстСообщения = НСтр("ru = 'Дата окончания действия должна быть не меньше даты начала действия';
								|en = 'End date must be later than the start date'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОкончаниеДействия",, Отказ);
		
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив; // Массив Из Строка
	
	Если ВидРетроБонуса.Пустая() Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ОтборУчастников");
		МассивНепроверяемыхРеквизитов.Добавить("ОтборТоваров");
		МассивНепроверяемыхРеквизитов.Добавить("ПериодичностьУсловий");
		МассивНепроверяемыхРеквизитов.Добавить("ПериодичностьНачислений");
		МассивНепроверяемыхРеквизитов.Добавить("СуммаПлан");
		МассивНепроверяемыхРеквизитов.Добавить("БонусПроцент");
		МассивНепроверяемыхРеквизитов.Добавить("ШкалыРасчета");
		МассивНепроверяемыхРеквизитов.Добавить("Валюта");
		
		МассивНепроверяемыхРеквизитов.Добавить("КонтрагентыКлиентов");
		МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентовКлиентов");
		МассивНепроверяемыхРеквизитов.Добавить("Товары");
		МассивНепроверяемыхРеквизитов.Добавить("СвойстваНоменклатуры");
		МассивНепроверяемыхРеквизитов.Добавить("СегментыТоваров");
		МассивНепроверяемыхРеквизитов.Добавить("Склады");
		МассивНепроверяемыхРеквизитов.Добавить("Поставщики");
		
	Иначе
		
		Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Все
		 ИЛИ ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка() Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("КонтрагентыКлиентов");
			МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентовКлиентов");
			
		КонецЕсли;
		
		Если ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.Все Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("Товары");
			МассивНепроверяемыхРеквизитов.Добавить("СвойстваНоменклатуры");
			МассивНепроверяемыхРеквизитов.Добавить("СегментыТоваров");
			
		КонецЕсли;
		
		РеквизитыВида = ДанныеВидаДокумента();
		
		РасчетПоШкале =
			(РеквизитыВида.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
		     И ВидШкалыРасчета <> 0); // Не единственное значение
		
		Если НЕ РасчетПоШкале Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ШкалыРасчета");
		КонецЕсли;
		
		Если ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Остатки Тогда
			
			#Область ЗаполнитьМассивНепроверяемыхРеквизитовОстатки
			МассивНепроверяемыхРеквизитов.Добавить("ОтборУчастников");
			МассивНепроверяемыхРеквизитов.Добавить("БонусПроцент");
			МассивНепроверяемыхРеквизитов.Добавить("ШкалыРасчета");
			МассивНепроверяемыхРеквизитов.Добавить("СуммаПлан");
			МассивНепроверяемыхРеквизитов.Добавить("СуммаБонус");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Процент");
			МассивНепроверяемыхРеквизитов.Добавить("НачалоДействия");
			МассивНепроверяемыхРеквизитов.Добавить("ОкончаниеДействия");
			МассивНепроверяемыхРеквизитов.Добавить("ПериодичностьНачислений");
			
			МассивНепроверяемыхРеквизитов.Добавить("Поставщики");
			МассивНепроверяемыхРеквизитов.Добавить("КонтрагентыКлиентов");
			МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентовКлиентов");
			
			Если ОтборСкладов = Перечисления.СоставыСписковРетроБонусов.Все Тогда
				МассивНепроверяемыхРеквизитов.Добавить("Склады");
			КонецЕсли;
			#КонецОбласти
			
		ИначеЕсли ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Закупки Тогда
			
			#Область ЗаполнитьМассивНепроверяемыхРеквизитовЗакупки
			МассивНепроверяемыхРеквизитов.Добавить("ДатаОстатков");
			МассивНепроверяемыхРеквизитов.Добавить("ОтборСкладов");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Характеристика");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.СуммаБонус");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.БонусЗаЕдиницу");
			МассивНепроверяемыхРеквизитов.Добавить("ОтборУчастников");
			МассивНепроверяемыхРеквизитов.Добавить("Склады");
			МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
			МассивНепроверяемыхРеквизитов.Добавить("Партнер");
			
			Если НЕ РежимНесколькоПоставщиков Тогда
				
				МассивНепроверяемыхРеквизитов.Добавить("Поставщики");
				
			КонецЕсли;
			
			Если РеквизитыВида.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.Номенклатура Тогда
				
				МассивНепроверяемыхРеквизитов.Добавить("СегментыТоваров");
				
			ИначеЕсли РеквизитыВида.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры Тогда	
				
				МассивНепроверяемыхРеквизитов.Добавить("Товары");
				
			КонецЕсли;
			
			Если РеквизитыВида.СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.ИНН Тогда
				
				МассивНепроверяемыхРеквизитов.Добавить("КонтрагентыКлиентов");
				
			ИначеЕсли РеквизитыВида.СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Контрагенты Тогда
				
				МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентовКлиентов");
				
			КонецЕсли;
			#КонецОбласти
			
		ИначеЕсли ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Продажи Тогда
			
			#Область ЗаполнитьМассивНепроверяемыхРеквизитовПродажи
			МассивНепроверяемыхРеквизитов.Добавить("ДатаОстатков");
			МассивНепроверяемыхРеквизитов.Добавить("ОтборСкладов");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Характеристика");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.СуммаБонус");
			МассивНепроверяемыхРеквизитов.Добавить("Товары.БонусЗаЕдиницу");
			
			МассивНепроверяемыхРеквизитов.Добавить("Склады");
			МассивНепроверяемыхРеквизитов.Добавить("Поставщики");
			
			Если РеквизитыВида.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.Номенклатура Тогда
				
				МассивНепроверяемыхРеквизитов.Добавить("СегментыТоваров");
				
			ИначеЕсли РеквизитыВида.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры Тогда	
				
				МассивНепроверяемыхРеквизитов.Добавить("Товары");
				
			КонецЕсли;
			
			Если РеквизитыВида.СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.ИНН Тогда
				
				МассивНепроверяемыхРеквизитов.Добавить("КонтрагентыКлиентов");
				
			ИначеЕсли РеквизитыВида.СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Контрагенты Тогда
				
				МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентовКлиентов");
				
			КонецЕсли;
			#КонецОбласти
			
		КонецЕсли;
		
		#Область БонусПроцент
		ЗапретПревышенияПоСумме =
			(РеквизитыВида.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
			 И ЗапретНачисленияСверхПлана);
		Если (РеквизитыВида.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.Номенклатура
		      ИЛИ РеквизитыВида.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры)
		   И ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.Выбранные
		   И НЕ ЗапретПревышенияПоСумме Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("БонусПроцент");
			
		КонецЕсли;
		
		Если БезРасчета
		 ИЛИ РасчетПоШкале
		 ИЛИ (РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.НеИспользуется
			  И РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма) Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("БонусПроцент");
			
		КонецЕсли;
		#КонецОбласти
		
		ПроверитьЗаполнениеПериодовШапки(РеквизитыВида, Отказ);
		
		НужнаПроверкаПериодаУсловий =
			(ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Закупки
			ИЛИ ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Продажи)
				 И РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.НеИспользуется;
		Если БезРасчета
		 ИЛИ НЕ НужнаПроверкаПериодаУсловий Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("ПериодичностьУсловий");
			
		КонецЕсли;
		
		Если БезРасчета Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("ДатаОстатков");
			МассивНепроверяемыхРеквизитов.Добавить("ОтборСкладов");
			
			МассивНепроверяемыхРеквизитов.Добавить("Товары");
			МассивНепроверяемыхРеквизитов.Добавить("СвойстваНоменклатуры");
			МассивНепроверяемыхРеквизитов.Добавить("СегментыТоваров");
			МассивНепроверяемыхРеквизитов.Добавить("КонтрагентыКлиентов");
			МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентовКлиентов");
			МассивНепроверяемыхРеквизитов.Добавить("ОтборУчастников");
			МассивНепроверяемыхРеквизитов.Добавить("ОтборТоваров");
			МассивНепроверяемыхРеквизитов.Добавить("Склады");
			МассивНепроверяемыхРеквизитов.Добавить("Поставщики");
			
		Иначе
			
			МассивНепроверяемыхРеквизитов.Добавить("СуммаБонус");
			
		КонецЕсли;
		
		// Если показатель не сумма или сумма, но не по единственному значению
		Если РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
		 ИЛИ (РеквизитыВида.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
		      И ВидРетроБонуса <> 0) Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("СуммаПлан");
			
		КонецЕсли;
		
		НужнаПроверкаВалюты =
			(БезРасчета
			 ИЛИ РеквизитыВида.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.СуммаЗакупкиБазовыеЦены
			 ИЛИ РеквизитыВида.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены)
			 ИЛИ ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Остатки
			 ИЛИ РеквизитыВида.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма;
		
		Если НЕ НужнаПроверкаВалюты Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("Валюта");
			
		КонецЕсли;
		
		ПроверитьПоСоставуПоставщики(Отказ);
		
		ПроверитьПоСоставуУчастниковКонтрагенты(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ);
		ПроверитьПоСоставуУчастниковИНН(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ);
		
		ПроверитьПоСоставуТоваровНоменклатура(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ);
		ПроверитьПоСоставуТоваровСвойстваНоменклатуры(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ);
		ПроверитьПоСоставуТоваровСегментыНоменклатуры(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ);
		ПроверитьПоСоставуСкладов(РеквизитыВида, Отказ);
		ПроверитьДанныеПоставщиков(РеквизитыВида, Отказ);
		
		РетроБонусыСервер.ПроверитьШкалыРасчета(ШкалыРасчета, ВидШкалыРасчета, РеквизитыВида, Отказ);
		ПроверитьОграниченияПоНачислениям(РеквизитыВида, Отказ);
		
	КонецЕсли;
	
	ИсправлениеДокументов.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	Если ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Остатки Тогда
		НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		
		ТекстСообщения = НСтр("ru = 'Запрещено изменение документа в подчиненном узле обмена';
								|en = 'Cannot change the document in a subordinate exchange node'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,, Отказ);
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	РетроБонусыСервер.ПроверитьЦепочкуДокументов(
		ИсправляемыйДокумент, СторнируемыйДокумент, ИдентификаторЦепочки, РежимЗаписи);
	
	ПропуститьПроверкуИзмененияПоСтатусу = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеСвойства,
		"ПропуститьПроверкуИзмененияПоСтатусу",
		Ложь);
	
	РазрешеноИзменение = Истина;
	
	Если НЕ Статус.Пустая()
	   И НЕ ПропуститьПроверкуИзмененияПоСтатусу Тогда
		
		РазрешеноИзменение = Документы.УсловияРетроБонусовПоставщика.РазрешеноИзменениеПоСтатусу(Статус);
		Если НЕ РазрешеноИзменение Тогда
			
			ШаблонОшибки = НСтр("ru = 'Изменение документа в статусе ""%1"" запрещено';
								|en = 'Cannot change the document in the ""%1"" status'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, Строка(Статус));
			
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "Статус",, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ВидРетроБонуса.Пустая() Тогда
		
		РеквизитыВида = ДанныеВидаДокумента();
		
		Если РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
		   И РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
			
			ВидШкалыРасчета = 0; // Значение по умолчанию
			
		КонецЕсли;
		
		РетроБонусыСервер.ОчиститьШкалыРасчета(ШкалыРасчета, ВидШкалыРасчета, РеквизитыВида);
		
		Если ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Остатки Тогда
			
			ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка();
			ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.ПустаяСсылка();
			СуммаПлан = 0;
			БонусПроцент = 0;
			НачалоДействия = Дата(1, 1, 1);
			ОкончаниеДействия = Дата(1, 1, 1);
			
			Если НачислитьСразу Тогда
				
				СуммаБонус = Товары.Итог("СуммаБонус");
				Склады.Очистить();
				ОтборСкладов = Перечисления.СоставыСписковРетроБонусов.Все;
				
			Иначе
				
				СуммаБонус = 0;
				
			КонецЕсли;
			
		Иначе
			
			ОтборСкладов = Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка();
			ДатаОстатков = Дата(1, 1, 1);
			Если НЕ БезРасчета Тогда
				НачислитьСразу = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Остатки Тогда
			
			ПериодичностьНачислений = Перечисления.ПериодичностиРетроБонусов.ПустаяСсылка();
			
		КонецЕсли;
		
		Если ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Закупки Тогда
			
			ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка();
			
		КонецЕсли;
		
		Если БезРасчета Тогда
			
			ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.ПустаяСсылка();
			ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка();
			
		КонецЕсли;
		
		КорректировкаДанныхПоставщикаИРежимаВводаПоСоставуДанных();
		
		Если БезРасчета
		 ИЛИ (РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество
			  И РеквизитыВида.ПоказательТоваров <> Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма) Тогда
			
			ЗапретНачисленияСверхПлана = Ложь;
			
		КонецЕсли;
		
		ЗапретПревышенияПоСумме =
			(РеквизитыВида.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
			 И ЗапретНачисленияСверхПлана);
		Если БезРасчета
		 ИЛИ НЕ (ОтборТоваров <> Перечисления.СоставыСписковРетроБонусов.Выбранные
				 ИЛИ РеквизитыВида.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры
				 ИЛИ ЗапретПревышенияПоСумме) Тогда
			
			БонусПроцент = 0;
			
		КонецЕсли;
		
		ПараметрыУчастников = Документы.УсловияРетроБонусовПоставщика.ПараметрыОчисткиРеквизитовУчастников();
		ПараметрыУчастников.СоставУчастников = РеквизитыВида.СоставУчастников;
		ПараметрыУчастников.ОтборУчастников = ОтборУчастников;
		Документы.УсловияРетроБонусовПоставщика.ОчиститьНеиспользуемыеРеквизитыУчастников(
			ЭтотОбъект, ПараметрыУчастников);
		
		ПараметрыТоваров = Документы.УсловияРетроБонусовПоставщика.ПараметрыОчисткиРеквизитовТоваров();
		ПараметрыТоваров.СоставТоваров = РеквизитыВида.СоставТоваров;
		ПараметрыТоваров.ОтборТоваров = ОтборТоваров;
		ПараметрыТоваров.ПоказательТоваров = РеквизитыВида.ПоказательТоваров;
		ПараметрыТоваров.БазаРасчета = РеквизитыВида.БазаРасчета;
		ПараметрыТоваров.ТипБонуса = ТипБонуса;
		ПараметрыТоваров.НачислитьСразу = НачислитьСразу;
		Документы.УсловияРетроБонусовПоставщика.ОчиститьНеиспользуемыеРеквизитыТоваров(ЭтотОбъект, ПараметрыТоваров);
		
		Если НЕ РежимНесколькоПоставщиков
		 ИЛИ НЕ Исправление Тогда
			
			ПустаяДата = Дата(1, 1, 1);
			
			Для Каждого СтрокаТЧ Из Поставщики Цикл
				
				СтрокаТЧ.Отменено = Ложь;
				СтрокаТЧ.НачалоДействияДоИзменений = ПустаяДата;
				СтрокаТЧ.ОкончаниеДействияДоИзменений = ПустаяДата;
				
			КонецЦикла
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Исправление Тогда
		
		РеквизитыУсловий = Документы.УсловияРетроБонусовПоставщика.АктуальныеДанные(ИсправляемыйДокумент);
		Если РеквизитыУсловий.ФиксацияВыполнена
		   И РазрешеноИзменение Тогда
			
			ТекстУведомления = НСтр("ru = 'Состав документа зафиксирован, ввод исправлений невозможен.';
									|en = 'The document content is recorded. Cannot register the corrections.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстУведомления, ЭтотОбъект,,, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если РазрешеноИзменение Тогда
		
		ПроверитьОграниченияПоНачислениям(РеквизитыВида, Отказ, Истина);
		
	КонецЕсли;
	
	ИсправлениеДокументов.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИсправлениеДокументов.ПриКопировании(ЭтотОбъект, ОбъектКопирования);
	
	Статус = Перечисления.СтатусыДокументовРетроБонусов.Черновик;
	
	ИменаОчищаемыхПолей = Новый Массив; // Массив из Строка
	ИменаОчищаемыхПолей.Добавить("НачалоДействияДоИзменений");
	ИменаОчищаемыхПолей.Добавить("ОкончаниеДействияДоИзменений");
	
	РетроБонусыСервер.ИнициализироватьКопируемыйСписок(ЭтотОбъект, "КонтрагентыКлиентов");
	РетроБонусыСервер.ИнициализироватьКопируемыйСписок(ЭтотОбъект, "ИННКонтрагентовКлиентов", ИменаОчищаемыхПолей);
	
	РетроБонусыСервер.ИнициализироватьКопируемыйСписок(ЭтотОбъект, "Товары", ИменаОчищаемыхПолей);
	РетроБонусыСервер.ИнициализироватьКопируемыйСписок(ЭтотОбъект, "СвойстваНоменклатуры", ИменаОчищаемыхПолей);
	РетроБонусыСервер.ИнициализироватьКопируемыйСписок(ЭтотОбъект, "СегментыТоваров");
	
	РетроБонусыСервер.ИнициализироватьКопируемыйСписок(ЭтотОбъект, "Поставщики", ИменаОчищаемыхПолей);
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	Префикс = ?(Исправление, "К", "0");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Данные вида документа.
// 
// Возвращаемое значение:
//  Структура:
//	* БезРасчета - Булево
//	* НачалоДействия - Дата
//	* ОкончаниеДействия - Дата 
//	* ПериодичностьУсловий - ПеречислениеСсылка.ПериодичностиРетроБонусов
//	* ПериодичностьНачислений - ПеречислениеСсылка.ПериодичностиРетроБонусов
//	* ОтборТоваров - ПеречислениеСсылка.СоставыСписковРетроБонусов
//	* ОтборУчастников - ПеречислениеСсылка.СоставыСписковРетроБонусов
//	* ПоказательТоваров - ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам
//	* БазаРасчета - ПеречислениеСсылка.БазыРасчетаРетроБонусов
//	* СоставТоваров - ПеречислениеСсылка.СоставыТоваровРетроБонусов
//	* СоставУчастников - ПеречислениеСсылка.СоставыУчастниковРетроБонусов
//	* СвойствоНоменклатуры - Строка
//
Функция ДанныеВидаДокумента()
	
	МассивРеквизитов = Новый Массив; // Массив Из Строка
	МассивРеквизитов.Добавить("НачалоДействия");
	МассивРеквизитов.Добавить("ОкончаниеДействия");
	МассивРеквизитов.Добавить("ПериодичностьУсловий");
	МассивРеквизитов.Добавить("ПериодичностьНачислений");
	МассивРеквизитов.Добавить("СоставТоваров");
	МассивРеквизитов.Добавить("ОтборТоваров");
	МассивРеквизитов.Добавить("СоставУчастников");
	МассивРеквизитов.Добавить("ОтборУчастников");
	МассивРеквизитов.Добавить("БезРасчета");
	МассивРеквизитов.Добавить("ПоказательТоваров");
	МассивРеквизитов.Добавить("БазаРасчета");
	МассивРеквизитов.Добавить("СвойствоНоменклатуры");
	
	РеквизитыВида = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидРетроБонуса, МассивРеквизитов);
	
	Возврат РеквизитыВида;
	
КонецФункции

// Заполнить по условию ретро бонусов клиентов.
// 
// Параметры:
//  ДанныеЗаполнения - ДокументСсылка.УсловияРетроБонусовПоставщика - Данные заполнения
//
Процедура ЗаполнитьПоУсловиюРетроБонусовПоставщиков(ДанныеЗаполнения)
	
	ДокументЗаполнения = ДанныеЗаполнения; // ДокументСсылка.УсловияРетроБонусовПоставщика
	ПоследнееИсправление = ИсправлениеДокументов.ПоследнийИсправительныйДокумент(ДокументЗаполнения);
	
	ИменаРеквизитов = Новый Структура;
	ИменаРеквизитов.Вставить("НачалоДействия");
	ИменаРеквизитов.Вставить("ОкончаниеДействия");
	ИменаРеквизитов.Вставить("СуммаПлан");
	ИменаРеквизитов.Вставить("ПоказательТоваров", "ВидРетроБонуса.ПоказательТоваров");
	ИменаРеквизитов.Вставить("СоставУчастников", "ВидРетроБонуса.СоставУчастников");
	
	Если ПоследнееИсправление = Неопределено Тогда
		
		ИсправлениеДокументов.ЗаполнитьИсправление(ЭтотОбъект, ДокументЗаполнения);
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументЗаполнения, ИменаРеквизитов);
		
	Иначе
		
		ПоследнееИсправлениеСсылка = ПоследнееИсправление.Ссылка; // ДокументСсылка.УсловияРетроБонусовПоставщика
		
		ПоследнееИсправлениеСтатус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ПоследнееИсправлениеСсылка, "Статус");
		Если ПоследнееИсправлениеСтатус <> Перечисления.СтатусыДокументовРетроБонусов.Согласован Тогда
			
			ТекстУведомления = НСтр("ru = 'Последний в цепочке исправлений документ не согласован, ввод исправлений невозможен';
									|en = 'The last document in the correction chain is not approved. Cannot register the corrections'");
			ВызватьИсключение ТекстУведомления;
			
		КонецЕсли;
		
		ИсправлениеДокументов.ЗаполнитьИсправление(ЭтотОбъект, ПоследнееИсправление.Ссылка);
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПоследнееИсправление.Ссылка, ИменаРеквизитов);
		
	КонецЕсли;
	
	Если ПериодичностьНачислений = Перечисления.ПериодичностиРетроБонусов.Однократно
	 ИЛИ ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Остатки Тогда
		
		МаксимальнаяДата = РетроБонусыСервер.ГраницаНачисленийПоставщиков(ИсправляемыйДокумент);
		Если МаксимальнаяДата <> Дата(1, 1, 1) Тогда
			
			ТекстУведомления = НСтр("ru = 'Зарегистрировано начисление ретро-бонусов, ввод исправлений невозможен';
									|en = 'Rebate accrual is registered. Cannot register the corrections'");
			ВызватьИсключение ТекстУведомления;
			
		КонецЕсли;
		
	КонецЕсли;
	
	РеквизитыУсловий = Документы.УсловияРетроБонусовПоставщика.АктуальныеДанные(ИсправляемыйДокумент);
	Если РеквизитыУсловий.ФиксацияВыполнена Тогда
		
		ТекстУведомления = НСтр("ru = 'Состав документа зафиксирован, ввод исправлений невозможен.';
								|en = 'The document content is recorded. Cannot register the corrections.'");
		ВызватьИсключение ТекстУведомления;
		
	КонецЕсли;
	
	Статус = Перечисления.СтатусыДокументовРетроБонусов.Черновик;
	НомерВходящегоДокумента = "";
	ДатаВходящегоДокумента = Дата(1, 1, 1);
	
	#Область ТЧ_КонтрагентыКлиентов
	СтрокиКУдалению = Новый Массив; // Массив из ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.КонтрагентыКлиентов
	
	Для Каждого СтрокаТЧ Из КонтрагентыКлиентов Цикл
		
		Если СтрокаТЧ.Отменено Тогда
			
			СтрокиКУдалению.Добавить(СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТЧ.ИсходнаяСтрока = Истина;
		
		Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
			
			ЗаполнитьДанныеПериодовПоОснованию(СтрокаТЧ, РеквизитыОснования);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		
		КонтрагентыКлиентов.Удалить(СтрокаКУдалению);
		
	КонецЦикла;
	#КонецОбласти
	
	#Область ТЧ_ИННКонтрагентовКлиентов
	СтрокиКУдалению = Новый Массив; // Массив из ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.ИННКонтрагентовКлиентов
	
	Для Каждого СтрокаТЧ Из ИННКонтрагентовКлиентов Цикл
		
		Если СтрокаТЧ.Отменено Тогда
			
			СтрокиКУдалению.Добавить(СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТЧ.ИсходнаяСтрока = Истина;
		
		Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
			
			ЗаполнитьДанныеПериодовПоОснованию(СтрокаТЧ, РеквизитыОснования);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		
		ИННКонтрагентовКлиентов.Удалить(СтрокаКУдалению);
		
	КонецЦикла;
	#КонецОбласти
	
	#Область ТЧ_Товары
	СтрокиКУдалению = Новый Массив; // Массив из ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Товары
	
	Для Каждого СтрокаТЧ Из Товары Цикл
		
		Если СтрокаТЧ.Отменено Тогда
			
			СтрокиКУдалению.Добавить(СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТЧ.ИсходнаяСтрока = Истина;
		
		Если РетроБонусыКлиентСервер.ИспользуетсяПериодУсловийТоваров(РеквизитыОснования.ПоказательТоваров) Тогда
			
			ЗаполнитьДанныеПериодовПоОснованию(СтрокаТЧ, РеквизитыОснования);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		
		Товары.Удалить(СтрокаКУдалению);
		
	КонецЦикла;
	#КонецОбласти
	
	#Область ТЧ_СвойстваНоменклатуры
	СтрокиКУдалению = Новый Массив; // Массив из ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.СвойстваНоменклатуры
	
	Для Каждого СтрокаТЧ Из СвойстваНоменклатуры Цикл
		
		Если СтрокаТЧ.Отменено Тогда
			
			СтрокиКУдалению.Добавить(СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТЧ.ИсходнаяСтрока = Истина;
		
		Если РетроБонусыКлиентСервер.ИспользуетсяПериодУсловийТоваров(РеквизитыОснования.ПоказательТоваров) Тогда
			
			ЗаполнитьДанныеПериодовПоОснованию(СтрокаТЧ, РеквизитыОснования);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		
		СвойстваНоменклатуры.Удалить(СтрокаКУдалению);
		
	КонецЦикла;
	#КонецОбласти
	
	#Область ТЧ_СегментыТоваров
	СтрокиКУдалению = Новый Массив; // Массив из ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.СегментыТоваров
	
	Для Каждого СтрокаТЧ Из СегментыТоваров Цикл
		
		Если СтрокаТЧ.Отменено Тогда
			
			СтрокиКУдалению.Добавить(СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТЧ.ИсходнаяСтрока = Истина;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		
		СегментыТоваров.Удалить(СтрокаКУдалению);
		
	КонецЦикла;
	#КонецОбласти
	
	#Область ТЧ_Поставщики
	СтрокиКУдалению = Новый Массив; // Массив из ДокументТабличнаяЧастьСтрока.УсловияРетроБонусовПоставщика.Поставщики
	
	Для Каждого СтрокаТЧ Из Поставщики Цикл
		
		Если СтрокаТЧ.Отменено Тогда
			
			СтрокиКУдалению.Добавить(СтрокаТЧ);
			Продолжить;
			
		КонецЕсли;
		
		СтрокаТЧ.ИсходнаяСтрока = Истина;
		
		ЗаполнитьДанныеПериодовПоОснованию(СтрокаТЧ, РеквизитыОснования);
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		
		Поставщики.Удалить(СтрокаКУдалению);
		
	КонецЦикла;
	#КонецОбласти
	
КонецПроцедуры

// Параметры:
//  СтрокаДанных - СтрокаТабличнойЧасти:
//  * НачалоДействия - Дата
//  * НачалоДействияДоИзменений - Дата
//  * ОкончаниеДействия - Дата
//  * ОкончаниеДействияДоИзменений - Дата
//	ДанныеОснования - Структура:
//  * НачалоДействия - Дата
//  * ОкончаниеДействия - Дата
//
Процедура ЗаполнитьДанныеПериодовПоОснованию(СтрокаДанных, Знач ДанныеОснования)
	
	ПустаяДата = Дата(1, 1, 1);
	
	Если СтрокаДанных.НачалоДействия = ПустаяДата Тогда
				
		СтрокаДанных.НачалоДействияДоИзменений = ДанныеОснования.НачалоДействия;
		
	Иначе
		
		СтрокаДанных.НачалоДействияДоИзменений = СтрокаДанных.НачалоДействия;
		
	КонецЕсли;
	
	Если СтрокаДанных.ОкончаниеДействия = ПустаяДата Тогда
		
		СтрокаДанных.ОкончаниеДействияДоИзменений = ДанныеОснования.ОкончаниеДействия;
		
	Иначе
		
		СтрокаДанных.ОкончаниеДействияДоИзменений = СтрокаДанных.ОкончаниеДействия;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  Отказ - Булево - Отказ
//
Процедура ПроверитьЗаполнениеПериодовШапки(РеквизитыВида, Отказ)
	
	Если ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Остатки Тогда
		Возврат;
	КонецЕсли;
	
	ПустаяДата = Дата(1, 1, 1);
	МаксимальнаяДата = Дата(2999, 12, 31);
	
	НачалоДействияВида = РеквизитыВида.НачалоДействия;
	ОкончаниеДействияВида = РеквизитыВида.ОкончаниеДействия;
	Если ОкончаниеДействияВида = ПустаяДата Тогда
		ОкончаниеДействияВида = МаксимальнаяДата;
	КонецЕсли;
	
	Если НачалоДействия <> ПустаяДата
	   И НачалоДействия < НачалоДействияВида Тогда
		
		ТекстСообщения = НСтр("ru = 'Начало периода действия документа раньше, чем начало действия вида ретро-бонуса';
								|en = 'The document validity period start is earlier than the rebate type start'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "НачалоДействия",, Отказ);
		
	КонецЕсли;
	
	Если ОкончаниеДействия <> ПустаяДата
	   И ОкончаниеДействия > ОкончаниеДействияВида Тогда
		
		ТекстСообщения = НСтр("ru = 'Окончание периода действия документа больше, чем окончание действия вида ретро-бонуса';
								|en = 'The document validity period end is later than the rebate type end'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ОкончаниеДействия",, Отказ);
		
	КонецЕсли;
	
	Если НачалоДействия <> ПустаяДата
	   И ОкончаниеДействия <> ПустаяДата Тогда
		
		РезультатПроверки = РетроБонусыСервер.ПроверитьПериодыПоПериодичности(
			ПериодичностьУсловий, 
			НачалоДействия,
			ОкончаниеДействия);
		Если РезультатПроверки.ЕстьНарушениеНачала Тогда
			
			ОбщегоНазначения.СообщитьПользователю(
				РезультатПроверки.ТекстОшибкиНачала, ЭтотОбъект, "НачалоДействия",, Отказ);
			
		КонецЕсли;
		
		Если РезультатПроверки.ЕстьНарушениеОкончания Тогда
			
			ОбщегоНазначения.СообщитьПользователю(
				РезультатПроверки.ТекстОшибкиОкончания, ЭтотОбъект, "ОкончаниеДействия",, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  МассивНепроверяемыхРеквизитов - Массив из Строка- Массив непроверяемых реквизитов
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуУчастниковКонтрагенты(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ)
	
	Если РеквизитыВида.СоставУчастников <> Перечисления.СоставыУчастниковРетроБонусов.Контрагенты
	 ИЛИ ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Все
	 ИЛИ ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка() Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТаблицы = "КонтрагентыКлиентов";
	ПредставлениеТаблицы = РетроБонусыКлиентСервер.ЗаголовокСтраницыПоНастройкам(
		РеквизитыВида.СоставУчастников, ОтборУчастников, Истина);
	Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
		
		РетроБонусыСервер.ПроверитьПериодыТаблицы(
			ЭтотОбъект, ИмяТаблицы, ПредставлениеТаблицы, Отказ);
		
	КонецЕсли;
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Отменено", Ложь);
	РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
		ЭтотОбъект, ИмяТаблицы, "Контрагент", Отказ, ПредставлениеТаблицы,, ОтборСтрок);
	
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(
		ЭтотОбъект, ИмяТаблицы, ПредставлениеТаблицы, Отказ);
	
	МассивНепроверяемыхРеквизитов.Добавить("ИННКонтрагентовКлиентов");
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  МассивНепроверяемыхРеквизитов - Массив из Строка- Массив непроверяемых реквизитов
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуУчастниковИНН(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ)
	
	Если РеквизитыВида.СоставУчастников <> Перечисления.СоставыУчастниковРетроБонусов.ИНН
	 ИЛИ ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Все
	 ИЛИ ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка() Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТаблицы = "ИННКонтрагентовКлиентов";
	ПредставлениеТаблицы = РетроБонусыКлиентСервер.ЗаголовокСтраницыПоНастройкам(
		РеквизитыВида.СоставУчастников, ОтборУчастников, Истина);
	Если ОтборУчастников = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
		
		РетроБонусыСервер.ПроверитьПериодыТаблицы(
			ЭтотОбъект, ИмяТаблицы, ПредставлениеТаблицы, Отказ);
		
	КонецЕсли;
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Отменено", Ложь);
	РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
		ЭтотОбъект, ИмяТаблицы, "ИНН", Отказ, ПредставлениеТаблицы,, ОтборСтрок);
	
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(
		ЭтотОбъект, ИмяТаблицы, ПредставлениеТаблицы, Отказ);
	
	РетроБонусыСервер.ПроверитьЗаполнениеИНН(ЭтотОбъект, ИмяТаблицы, ПредставлениеТаблицы, Отказ);
	МассивНепроверяемыхРеквизитов.Добавить("КонтрагентыКлиентов");
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуСкладов(РеквизитыВида, Отказ)
	
	Если ТипБонуса <> Перечисления.ТипыРетроБонусовПоставщиков.Остатки
	 ИЛИ ОтборСкладов = Перечисления.СоставыСписковРетроБонусов.Все Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТаблицы = "Склады";
	ПредставлениеТаблицы = РетроБонусыКлиентСервер.ЗаголовокСтраницыСклады(ОтборСкладов);
	
	РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
		ЭтотОбъект, ИмяТаблицы, "Склад", Отказ, ПредставлениеТаблицы,);
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  Отказ - Булево - Отказ
//
Процедура ПроверитьДанныеПоставщиков(РеквизитыВида, Отказ)
	
	Если ТипБонуса <> Перечисления.ТипыРетроБонусовПоставщиков.Закупки Тогда
		Возврат;
	КонецЕсли;
		
	ШаблонСообщения = НСтр("ru = 'В строке %1 списка Поставщики необходимо заполнить одно из полей ""Контрагент"", ""Поставщик""';
							|en = 'In line %1 of the ""Vendors"" list, fill in one of the fields: ""Counterparty"" or ""Vendor""'");
	ТекстОшибки = НСтр("ru = 'Необходимо заполнить одно из полей ""Контрагент"", ""Поставщик""';
						|en = 'Fill in one of the fields: ""Counterparty"" or ""Vendor""'");
	
	Если РежимНесколькоПоставщиков Тогда
		
		ИмяТаблицы = "Поставщики";
		ПредставлениеТаблицы = НСтр("ru = 'Поставщики';
									|en = 'Vendors'");
		РетроБонусыСервер.ПроверитьПериодыТаблицы(ЭтотОбъект, ИмяТаблицы, ПредставлениеТаблицы, Отказ);
		
		ОтборСтрок = Новый Структура;
		ОтборСтрок.Вставить("Отменено", Ложь);
		
		КлючевыеПоля = "Контрагент,Партнер,Договор,Соглашение";
		
		РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
			ЭтотОбъект, ИмяТаблицы, КлючевыеПоля, Отказ, ПредставлениеТаблицы,, ОтборСтрок);
		
		Для Каждого ТекущаяСтрока Из Поставщики Цикл
			
			НомерСтроки = ТекущаяСтрока.НомерСтроки;
			
			КонтрагентНеЗаполнен = НЕ ЗначениеЗаполнено(ТекущаяСтрока.Контрагент);
			ПартнерНеЗаполнен = НЕ ЗначениеЗаполнено(ТекущаяСтрока.Партнер);
			ПоляНеЗаполнены = (КонтрагентНеЗаполнен И ПартнерНеЗаполнен);
			
			Если ПоляНеЗаполнены Тогда
				
				Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					ИмяТаблицы, НомерСтроки, "НомерСтроки");
					
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонСообщения, Строка(НомерСтроки));
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		КонтрагентНеЗаполнен = НЕ ЗначениеЗаполнено(Контрагент);
		ПартнерНеЗаполнен = НЕ ЗначениеЗаполнено(Партнер);
		
		ПоляНеЗаполнены = (КонтрагентНеЗаполнен И ПартнерНеЗаполнен);
		
		Если ПоляНеЗаполнены Тогда
			
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "Контрагент", "Объект", Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  МассивНепроверяемыхРеквизитов - Массив из Строка- Массив непроверяемых реквизитов
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуТоваровНоменклатура(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ)
	
	Если РеквизитыВида.СоставТоваров <> Перечисления.СоставыТоваровРетроБонусов.Номенклатура
	 ИЛИ ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.Все Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоКоличествоПоДиапазонам = РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(
		ВидШкалыРасчета, РеквизитыВида.ПоказательТоваров);
	ПоказателиТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	МассивНепроверяемыхРеквизитов.Добавить("СвойстваНоменклатуры");
	МассивНепроверяемыхРеквизитов.Добавить("СегментыТоваров");
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры") Тогда
		
		КлючевыеПоля = "Номенклатура,Характеристика";
		
	Иначе
		
		КлючевыеПоля = "Номенклатура";
		
	КонецЕсли;
	
	ПредставлениеТаблицы = РетроБонусыКлиентСервер.ЗаголовокСтраницыПоНастройкам(
		РеквизитыВида.СоставТоваров, ОтборТоваров);
	
	Если ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.Выбранные
	   И ТипБонуса <> Перечисления.ТипыРетроБонусовПоставщиков.Остатки Тогда
		
		ПериодыУсловияЗаполнены = (ЗначениеЗаполнено(НачалоДействия)
								   И ЗначениеЗаполнено(ОкончаниеДействия));
		
		РетроБонусыСервер.ПроверитьПериодыТаблицы(ЭтотОбъект, "Товары", ПредставлениеТаблицы, Отказ);
		
		Если РеквизитыВида.ПоказательТоваров = ПоказателиТоваров.КоличествоСовокупно
		 ИЛИ РеквизитыВида.ПоказательТоваров = ПоказателиТоваров.ПакетноеПредложение Тогда
			
			ОтборСтрок = Новый Структура;
			ОтборСтрок.Вставить("Отменено", Ложь);
			РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
				ЭтотОбъект, "Товары", КлючевыеПоля, Отказ, ПредставлениеТаблицы,, ОтборСтрок);
			
		КонецЕсли;
		
		Если ПериодыУсловияЗаполнены Тогда
			
			Если РеквизитыВида.ПоказательТоваров = ПоказателиТоваров.Сумма
			 ИЛИ РеквизитыВида.ПоказательТоваров = ПоказателиТоваров.НеИспользуется
			 ИЛИ РеквизитыВида.ПоказательТоваров = ПоказателиТоваров.Количество Тогда
				
				ПараметрыПроверки = РетроБонусыСервер.ПараметрыПроверкиПериодовПоКлючам();
				ПараметрыПроверки.НачалоДействия = НачалоДействия;
				ПараметрыПроверки.ОкончаниеДействия = ОкончаниеДействия;
				ПараметрыПроверки.ПериодичностьУсловий = ПериодичностьУсловий;
				ПараметрыПроверки.ПоказательТоваров = РеквизитыВида.ПоказательТоваров;
				ПараметрыПроверки.ПредставлениеТаблицы = ПредставлениеТаблицы;
				ПараметрыПроверки.ВидШкалыРасчета = ВидШкалыРасчета;
				
				РетроБонусыСервер.ПроверитьПериодыТоваровПоКлючам(Товары, ПараметрыПроверки, Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если РеквизитыВида.ПоказательТоваров = ПоказателиТоваров.Сумма
		   И (ВидШкалыРасчета <> 0 // Процент шкалой
		      ИЛИ ЗапретНачисленияСверхПлана) Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("Товары.Процент");
			
		КонецЕсли;
		
	Иначе
		
		ОтборСтрок = Новый Структура;
		ОтборСтрок.Вставить("Отменено", Ложь);
		РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
			ЭтотОбъект, "Товары", КлючевыеПоля, Отказ, ПредставлениеТаблицы,, ОтборСтрок);
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.НачалоДействия");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ОкончаниеДействия");
		
	КонецЕсли;
	
	Если РеквизитыВида.БазаРасчета <> Перечисления.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены
	   И РеквизитыВида.БазаРасчета <> Перечисления.БазыРасчетаРетроБонусов.СуммаЗакупкиБазовыеЦены Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.БазоваяЦена");
		
	КонецЕсли;
	
	Если ОтборТоваров <> Перечисления.СоставыСписковРетроБонусов.Выбранные
	 ИЛИ ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Остатки
	 ИЛИ ЭтоКоличествоПоДиапазонам Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Процент");
		
	КонецЕсли;
	
	МассивПоказатели = Новый Массив; // Массив Из ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам
	МассивПоказатели.Добавить(ПоказателиТоваров.Количество);
	МассивПоказатели.Добавить(ПоказателиТоваров.КоличествоСовокупно);
	МассивПоказатели.Добавить(ПоказателиТоваров.ПакетноеПредложение);
	
	ПроверитьКоличествоПлан = (ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Остатки
							   ИЛИ МассивПоказатели.Найти(РеквизитыВида.ПоказательТоваров) <> Неопределено)
							   И ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.Выбранные;
	
	Если НЕ ПроверитьКоличествоПлан
	 ИЛИ ЭтоКоличествоПоДиапазонам Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.КоличествоПлан");
		
	КонецЕсли;
	
	Если ЭтоКоличествоПоДиапазонам Тогда
		
		МенеджерВТ = Новый МенеджерВременныхТаблиц();
		
		РетроБонусыСервер.ПроверитьТоварыНаличиеНачальногоДиапазона(
			ЭтотОбъект, ПредставлениеТаблицы, МенеджерВТ, Отказ);
		
		РетроБонусыСервер.ПроверитьТоварыИдентичностьДанныхДиапазонов(
			ЭтотОбъект, ПредставлениеТаблицы, МенеджерВТ, Отказ);
		
		РетроБонусыСервер.ПроверитьТоварыНаличиеПроцентовБонуса(
			ЭтотОбъект, ПредставлениеТаблицы, МенеджерВТ, Отказ);
		
	КонецЕсли;
	
	Если ТипБонуса = ПредопределенноеЗначение("Перечисление.ТипыРетроБонусовПоставщиков.Остатки")
	   И НЕ НачислитьСразу Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СуммаБонус");
		
	КонецЕсли;
	
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(ЭтотОбъект, "Товары", ПредставлениеТаблицы, Отказ);
	Если ТипБонуса <> Перечисления.ТипыРетроБонусовПоставщиков.Остатки Тогда
		РетроБонусыСервер.ПроверитьУказаниеПустыхЗначенийХарактеристик(ЭтотОбъект, ПредставлениеТаблицы, РеквизитыВида, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  МассивНепроверяемыхРеквизитов - Массив из Строка- Массив непроверяемых реквизитов
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуТоваровСвойстваНоменклатуры(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ)
	
	Если РеквизитыВида.СоставТоваров <> Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоКоличествоПоДиапазонам = РетроБонусыКлиентСервер.ЭтоКоличествоПоДиапазонам(
		ВидШкалыРасчета, РеквизитыВида.ПоказательТоваров);
	ПоказателиТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	МассивНепроверяемыхРеквизитов.Добавить("Товары");
	МассивНепроверяемыхРеквизитов.Добавить("СегментыТоваров");
	
	КлючевыеПоля = "ЗначениеСвойства";
	
	РезультатПроверкиСвойства  = РетроБонусыСервер.ДанныеПроверкиСвойстваНоменклатуры(РеквизитыВида.СвойствоНоменклатуры);
	Если НЕ РезультатПроверкиСвойства.РазрешеноИспользовать Тогда
		
		РетроБонусыСервер.СообщитьОбОшибкеСвойстваНоменклатуры(РезультатПроверкиСвойства, "ВидРетроБонуса", Отказ);
		
	КонецЕсли;
	
	Если ОтборТоваров = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
		
		ПериодыУсловияЗаполнены = (ЗначениеЗаполнено(НачалоДействия)
								   И ЗначениеЗаполнено(ОкончаниеДействия));
		
		ПредставлениеТаблицы = НСтр("ru = 'Выбранные значения свойства';
									|en = 'Selected property values'");
		РетроБонусыСервер.ПроверитьПериодыТаблицы(ЭтотОбъект, "СвойстваНоменклатуры", ПредставлениеТаблицы, Отказ);
		
		Если РеквизитыВида.ПоказательТоваров = ПоказателиТоваров.КоличествоСовокупно
		 ИЛИ РеквизитыВида.ПоказательТоваров = ПоказателиТоваров.ПакетноеПредложение Тогда
			
			ОтборСтрок = Новый Структура;
			ОтборСтрок.Вставить("Отменено", Ложь);
			РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
				ЭтотОбъект, "СвойстваНоменклатуры", КлючевыеПоля, Отказ, ПредставлениеТаблицы,, ОтборСтрок);
			
		КонецЕсли;
		
		Если ПериодыУсловияЗаполнены Тогда
			
			Если РеквизитыВида.ПоказательТоваров = ПоказателиТоваров.Сумма
			 ИЛИ РеквизитыВида.ПоказательТоваров = ПоказателиТоваров.НеИспользуется
			 ИЛИ РеквизитыВида.ПоказательТоваров = ПоказателиТоваров.Количество Тогда
				
				ПараметрыПроверки = РетроБонусыСервер.ПараметрыПроверкиПериодовПоКлючам();
				ПараметрыПроверки.НачалоДействия = НачалоДействия;
				ПараметрыПроверки.ОкончаниеДействия = ОкончаниеДействия;
				ПараметрыПроверки.ПериодичностьУсловий = ПериодичностьУсловий;
				ПараметрыПроверки.ПоказательТоваров = РеквизитыВида.ПоказательТоваров;
				ПараметрыПроверки.ПредставлениеТаблицы = ПредставлениеТаблицы;
				ПараметрыПроверки.ВидШкалыРасчета = ВидШкалыРасчета;
				
				РетроБонусыСервер.ПроверитьПериодыСвойствНоменклатурыПоКлючам(СвойстваНоменклатуры, ПараметрыПроверки, Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если РеквизитыВида.ПоказательТоваров = ПоказателиТоваров.Сумма
		   И (ВидШкалыРасчета <> 0 // Процент шкалой
		      ИЛИ ЗапретНачисленияСверхПлана) Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("СвойстваНоменклатуры.Процент");
			
		КонецЕсли;
		
	Иначе
		
		ПредставлениеТаблицы = НСтр("ru = 'Кроме значений свойств';
									|en = 'Except for property values'");
		
		ОтборСтрок = Новый Структура;
		ОтборСтрок.Вставить("Отменено", Ложь);
		РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
			ЭтотОбъект, "СвойстваНоменклатуры", КлючевыеПоля, Отказ, ПредставлениеТаблицы,, ОтборСтрок);
		
		МассивНепроверяемыхРеквизитов.Добавить("СвойстваНоменклатуры.НачалоДействия");
		МассивНепроверяемыхРеквизитов.Добавить("СвойстваНоменклатуры.ОкончаниеДействия");
		
	КонецЕсли;
	
	Если РеквизитыВида.БазаРасчета <> Перечисления.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены
	   И РеквизитыВида.БазаРасчета <> Перечисления.БазыРасчетаРетроБонусов.СуммаЗакупкиБазовыеЦены Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("СвойстваНоменклатуры.БазоваяЦена");
		
	КонецЕсли;
	
	Если ОтборТоваров <> Перечисления.СоставыСписковРетроБонусов.Выбранные
	 ИЛИ ЭтоКоличествоПоДиапазонам Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("СвойстваНоменклатуры.Процент");
		
	КонецЕсли;
	
	Если (РеквизитыВида.ПоказательТоваров <> ПоказателиТоваров.Количество
		  И РеквизитыВида.ПоказательТоваров <> ПоказателиТоваров.КоличествоСовокупно
		  И РеквизитыВида.ПоказательТоваров <> ПоказателиТоваров.Количество)
	 ИЛИ ЭтоКоличествоПоДиапазонам Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("СвойстваНоменклатуры.КоличествоПлан");
		
	КонецЕсли;
	
	Если ЭтоКоличествоПоДиапазонам Тогда
		
		МенеджерВТ = Новый МенеджерВременныхТаблиц();
		
		РетроБонусыСервер.ПроверитьСвойстваНоменклатурыНаличиеНачальногоДиапазона(
			ЭтотОбъект, ПредставлениеТаблицы, МенеджерВТ, Отказ);
		
		РетроБонусыСервер.ПроверитьСвойстваНоменклатурыИдентичностьДанныхДиапазонов(
			ЭтотОбъект, ПредставлениеТаблицы, МенеджерВТ, Отказ);
		
		РетроБонусыСервер.ПроверитьСвойстваНоменклатурыНаличиеПроцентовБонуса(
			ЭтотОбъект, ПредставлениеТаблицы, МенеджерВТ, Отказ);
		
	КонецЕсли;
	
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(ЭтотОбъект, "СвойстваНоменклатуры", ПредставлениеТаблицы, Отказ);
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  МассивНепроверяемыхРеквизитов - Массив из Строка- Массив непроверяемых реквизитов
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуТоваровСегментыНоменклатуры(РеквизитыВида, МассивНепроверяемыхРеквизитов, Отказ)
	
	Если РеквизитыВида.СоставТоваров <> Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары");
	МассивНепроверяемыхРеквизитов.Добавить("СвойстваНоменклатуры");
	
	ИмяТаблицы = "СегментыТоваров";
	ПредставлениеТаблицы = РетроБонусыКлиентСервер.ЗаголовокСтраницыПоНастройкам(
		РеквизитыВида.СоставТоваров, ОтборТоваров);
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Отменено", Ложь);
	РетроБонусыСервер.ПроверитьНаличиеДублейСтрокТЧ(
		ЭтотОбъект, ИмяТаблицы, "Сегмент", Отказ, ПредставлениеТаблицы,, ОтборСтрок);
	
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(ЭтотОбъект, ИмяТаблицы, ПредставлениеТаблицы, Отказ);
	
КонецПроцедуры

// Параметры:
//  Отказ - Булево - Отказ
//
Процедура ПроверитьПоСоставуПоставщики(Отказ)

	Если НЕ РежимНесколькоПоставщиков Тогда
		Возврат;
	КонецЕсли;
	
	ПредставлениеТаблицы = НСтр("ru = 'Поставщики';
								|en = 'Vendors'");
	
	РетроБонусыСервер.ПроверитьНаличиеДействующихСтрок(ЭтотОбъект, "Поставщики", ПредставлениеТаблицы, Отказ);

КонецПроцедуры

Процедура ИнициализироватьДокумент()
	
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
КонецПроцедуры

// Параметры:
//  РеквизитыВида - см. ДанныеВидаДокумента
//  Отказ - Булево -
//  ВыполнятьСБлокировкой - Булево -
//
Процедура ПроверитьОграниченияПоНачислениям(РеквизитыВида, Отказ, ВыполнятьСБлокировкой = Ложь)
	
	ПропуститьПроверкуНаОграничениеПоНачислениямПриОтсутствииИзменений = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеСвойства,
		"ПропуститьПроверкуНаОграничениеПоНачислениямПриОтсутствииИзменений",
		Истина);
	
	Если ПропуститьПроверкуНаОграничениеПоНачислениямПриОтсутствииИзменений
	   И Проведен Тогда
		
		ИзмененияДокумента = ОбщегоНазначенияУТ.ИзмененияДокумента(ЭтотОбъект);
		Если ИзмененияДокумента.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Результат = ДанныеДляПроверки();
	
	Если НЕ ЗначениеЗаполнено(Результат.ДокументПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	ДокументПроверки = Результат.ДокументПроверки;
	ИзменениеСогласованногоДокумента = Результат.ИзменениеСогласованногоДокумента;
	СтароеЗначениеСуммаПлан = Результат.СтароеЗначениеСуммаПлан;
	
	Если ВыполнятьСБлокировкой Тогда
		
		Попытка
		
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РетроБонусыПоставщиков");
			ЭлементБлокировки.УстановитьЗначение("ДокументУсловий", ДокументПроверки);
			
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			Блокировка.Заблокировать();
			
		Исключение
			
			ШаблонОшибки = НСтр("ru = 'Не удалось выполнить проверку на наличие уже введенных начислений по документу условий по причине:
								|%1';
								|en = 'Cannot check whether there are already entered accruals for the condition document due to:
								|%1'");
			ОписаниеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибки, 
				ОписаниеОшибки);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецПопытки;
	
	КонецЕсли;
	
	МаксимальнаяДата = РетроБонусыСервер.ГраницаНачисленийПоставщиков(ДокументПроверки);
	
	Если МаксимальнаяДата <> Дата(1, 1, 1) Тогда
		
		Если ИзменениеСогласованногоДокумента Тогда
			
			ТекстСообщения = НСтр("ru = 'Зарегистрировано начисление ретро-бонусов, ввод исправлений невозможен.';
									|en = 'Rebate accrual is registered. Cannot register the corrections.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,, "Объект", Отказ);
			
		ИначеЕсли ПериодичностьНачислений = Перечисления.ПериодичностиРетроБонусов.Однократно
		  ИЛИ ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Остатки Тогда
			
			ТекстСообщения = НСтр("ru = 'Зарегистрировано начисление ретро-бонусов, ввод исправлений невозможен';
									|en = 'Rebate accrual is registered. Cannot register the corrections'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			
		Иначе
			
			Если СтароеЗначениеСуммаПлан <> СуммаПлан Тогда
				
				ТекстСообщения = НСтр("ru = 'Зарегистрировано начисление ретро-бонусов, изменение суммы плана запрещено';
										|en = 'Rebate accrual is registered. Changing the plan amount is prohibited'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "СуммаПлан", "Объект", Отказ);
				
			КонецЕсли;
			
			ЦелевыеПоказателиБезПериода = Новый Массив; // Массив из ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам
			ЦелевыеПоказателиБезПериода.Добавить(Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.ПакетноеПредложение);
			ЦелевыеПоказателиБезПериода.Добавить(Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.КоличествоСовокупно);
			
			МаксимальнаяДатаСтрокой = Формат(МаксимальнаяДата, "ДЛФ=D;");
			ВыполнятьПроверкуПериодовВТаблицах = Истина;
			
			Если ОкончаниеДействия < МаксимальнаяДата Тогда
				
				Шаблон = НСтр("ru = 'Дата окончания действия не может быть меньше %1 - даты окончания периода зарегистрированного начисления';
								|en = 'The end date must be later than %1, the registered accrual period end date'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Шаблон, МаксимальнаяДатаСтрокой);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ОкончаниеДействия", "Объект", Отказ);
				ВыполнятьПроверкуПериодовВТаблицах = Ложь;
				
			КонецЕсли;
			
			#Область ПроверкаУчастников
			ИмяПредставлениеТаблицы = РетроБонусыСервер.ИмяПредставлениеТаблицыПоРеквизитамУсловийПоставщиков(
				РеквизитыВида.СоставУчастников, ОтборУчастников);
			
			Если ИмяПредставлениеТаблицы.ИмяТаблицы <> "" Тогда
				
				ПараметрыПроверки = РетроБонусыСервер.НовыеПараметрыПроверкиУсловийВТаблице();
				ПараметрыПроверки.Объект = ЭтотОбъект;
				ПараметрыПроверки.ИмяТаблицы = ИмяПредставлениеТаблицы.ИмяТаблицы;
				ПараметрыПроверки.ПредставлениеТаблицы = ИмяПредставлениеТаблицы.ПредставлениеТаблицы;
				ПараметрыПроверки.НачалоДействия = НачалоДействия;
				ПараметрыПроверки.ОкончаниеДействия = ОкончаниеДействия;
				ПараметрыПроверки.МаксимальнаяДата = МаксимальнаяДата;
				ПараметрыПроверки.СоставСписка = ОтборУчастников;
				ПараметрыПроверки.ВыполнятьПроверкуПериодовВТаблицах = ВыполнятьПроверкуПериодовВТаблицах;
				
				РетроБонусыСервер.ПроверитьИзмененияУсловийВТаблице(ПараметрыПроверки, Отказ);
				
			КонецЕсли;
			#КонецОбласти
			
			#Область ПроверкаТоваров
			ИмяПредставлениеТаблицы = РетроБонусыСервер.ИмяПредставлениеТаблицыПоРеквизитамУсловийПоставщиков(
				РеквизитыВида.СоставТоваров, ОтборТоваров);
			
			Если ИмяПредставлениеТаблицы.ИмяТаблицы <> "" Тогда
				
				Если ЦелевыеПоказателиБезПериода.Найти(РеквизитыВида.ПоказательТоваров) = Неопределено Тогда
					
					ПараметрыПроверки = РетроБонусыСервер.НовыеПараметрыПроверкиУсловийВТаблице();
					ПараметрыПроверки.Объект = ЭтотОбъект;
					ПараметрыПроверки.ИмяТаблицы = ИмяПредставлениеТаблицы.ИмяТаблицы;
					ПараметрыПроверки.ПредставлениеТаблицы = ИмяПредставлениеТаблицы.ПредставлениеТаблицы;
					ПараметрыПроверки.НачалоДействия = НачалоДействия;
					ПараметрыПроверки.ОкончаниеДействия = ОкончаниеДействия;
					ПараметрыПроверки.МаксимальнаяДата = МаксимальнаяДата;
					ПараметрыПроверки.СоставСписка = ОтборТоваров;
					ПараметрыПроверки.ВыполнятьПроверкуПериодовВТаблицах = ВыполнятьПроверкуПериодовВТаблицах;
					
					РетроБонусыСервер.ПроверитьИзмененияУсловийВТаблице(ПараметрыПроверки, Отказ);
					
				Иначе
					
					РетроБонусыСервер.ПроверитьИзмененияУсловийВТаблицеБезПериодовДействия(
						ЭтотОбъект,
						ОтборТоваров,
						ИмяПредставлениеТаблицы.ИмяТаблицы,
						ИмяПредставлениеТаблицы.ПредставлениеТаблицы,
						Отказ);
					
				КонецЕсли;
				
			КонецЕсли;
			#КонецОбласти
			
			Если ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Закупки
			   И Поставщики.Количество() > 0 Тогда
				
				ПараметрыПроверки = РетроБонусыСервер.НовыеПараметрыПроверкиУсловийВТаблице();
				ПараметрыПроверки.Объект = ЭтотОбъект;
				ПараметрыПроверки.ИмяТаблицы = "Поставщики";
				ПараметрыПроверки.ПредставлениеТаблицы = НСтр("ru = 'Поставщики';
																|en = 'Vendors'");
				ПараметрыПроверки.НачалоДействия = НачалоДействия;
				ПараметрыПроверки.ОкончаниеДействия = ОкончаниеДействия;
				ПараметрыПроверки.МаксимальнаяДата = МаксимальнаяДата;
				ПараметрыПроверки.СоставСписка = Перечисления.СоставыСписковРетроБонусов.Выбранные;
				ПараметрыПроверки.ВыполнятьПроверкуПериодовВТаблицах = ВыполнятьПроверкуПериодовВТаблицах;
				
				РетроБонусыСервер.ПроверитьИзмененияУсловийВТаблице(ПараметрыПроверки, Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает документ требующий проверки и признак изменения согласованного документа.
// 
// Возвращаемое значение:
//  Структура - Данные для проверки:
// * ДокументПроверки - ДокументСсылка.УсловияРетроБонусовПоставщика - 
// * ИзменениеСогласованногоДокумента - Булево
// * СтароеЗначениеСуммаПлан- Число
// 
Функция ДанныеДляПроверки()
	
	Результат = Новый Структура;
	Результат.Вставить("ДокументПроверки", Документы.УсловияРетроБонусовПоставщика.ПустаяСсылка());
	Результат.Вставить("ИзменениеСогласованногоДокумента", Ложь);
	Результат.Вставить("СтароеЗначениеСуммаПлан", 0);
	
	Если НЕ ИсправляемыйДокумент.Пустая() Тогда
		
		Результат.ДокументПроверки = ИсправляемыйДокумент;
		Результат.СтароеЗначениеСуммаПлан = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СторнируемыйДокумент, "СуммаПлан");;
		
	Иначе
		
		Если НЕ Ссылка.Пустая() Тогда
			
			СтатусСсылки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Статус");
			
			Если СтатусСсылки = Перечисления.СтатусыДокументовРетроБонусов.Согласован Тогда
				
				Результат.ДокументПроверки = Ссылка;
				Результат.ИзменениеСогласованногоДокумента = Истина;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура КорректировкаДанныхПоставщикаИРежимаВводаПоСоставуДанных()
	
	Если РежимНесколькоПоставщиков Тогда
		
		ПереключениеРазрешено = РазрешеноПереключениеРежима();
		
		Если ПереключениеРазрешено Тогда
			
			РежимНесколькоПоставщиков = Ложь;
			
			Если Поставщики.Количество() = 1 Тогда
				
				СтрокаПоставщика = Поставщики[0];
				Контрагент = СтрокаПоставщика.Контрагент;
				Партнер = СтрокаПоставщика.Партнер;
				Договор = СтрокаПоставщика.Договор;
				Соглашение = СтрокаПоставщика.Соглашение;
				
				Поставщики.Очистить();
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если РежимНесколькоПоставщиков Тогда
		
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		Партнер = Справочники.Партнеры.ПустаяСсылка();
		Договор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		Соглашение = Справочники.СоглашенияСПоставщиками.ПустаяСсылка();
		
	КонецЕсли;
	
КонецПроцедуры

Функция РазрешеноПереключениеРежима()
	
	Если Поставщики.Количество() = 0 Тогда
		
		ПереключениеРазрешено = Истина;
		
	ИначеЕсли Поставщики.Количество() > 1 Тогда
		
		ПереключениеРазрешено = Ложь;
		
	Иначе
		
		ПустаяДата = Дата(1, 1, 1);
		СтрокаПоставщика = Поставщики[0];
		
		Если СтрокаПоставщика.НачалоДействия = ПустаяДата Тогда
			ТекущаяДатаНачала = НачалоДействия
		Иначе
			ТекущаяДатаНачала = СтрокаПоставщика.НачалоДействия;
		КонецЕсли;
			
		Если СтрокаПоставщика.ОкончаниеДействия = ПустаяДата Тогда
			ТекущаяДатаОкончания = ОкончаниеДействия;
		Иначе
			ТекущаяДатаОкончания = СтрокаПоставщика.ОкончаниеДействия;
		КонецЕсли;
		
		ПереключениеРазрешено = (ТекущаяДатаНачала = НачалоДействия
			И ТекущаяДатаОкончания = ОкончаниеДействия);
		
	КонецЕсли;
	
	Возврат ПереключениеРазрешено;
	
КонецФункции

#КонецОбласти

#КонецЕсли