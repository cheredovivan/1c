#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Проводит документ по учетам. Если в параметре ВидыУчетов передано Неопределено, то документ проводится по всем учетам.
// Процедура вызывается из обработки проведения и может вызываться из вне.
// 
// Параметры:
//  ДокументСсылка	- ДокументСсылка.ОперацияУчетаПоСтраховымВзносам
//  РежимПроведения - РежимПроведенияДокумента - Режим проведения документа (оперативный, неоперативный).
//  Отказ 			- Булево - Признак отказа от выполнения проведения.
//  ВидыУчетов 		- Строка - Список видов учета, по которым необходимо провести документ. Если параметр пустой или Неопределено, то документ проведется по всем учетам.
//  Движения 		- КоллекцияДвижений - Передается только при вызове из обработки проведения документа.
//  Объект			- ДокументОбъект.ОперацияУчетаПоСтраховымВзносам - Передается только при вызове из обработки проведения документа.
//  ДополнительныеПараметры - Структура - Дополнительные параметры, необходимые для проведения документа.
//
Процедура ПровестиПоУчетам(ДокументСсылка, РежимПроведения, Отказ, ВидыУчетов = Неопределено, Движения = Неопределено, Объект = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	СтруктураВидовУчета = ПроведениеСервер.СтруктураВидовУчета();	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвиженийПоВидамУчета(РежимПроведения, ДокументСсылка, СтруктураВидовУчета, ВидыУчетов, Движения, Объект, Отказ);
	
	ДанныеДляПроведения = ДанныеДляПроведения(ДокументСсылка, СтруктураВидовУчета);
	
	Если СтруктураВидовУчета.СтраховыеВзносы Тогда
		УчетСтраховыхВзносов.ДополнитьДоходыДополнительнойОблагаемойБазойРуководителя(Движения, Отказ, ДанныеДляПроведения.Организация, ДанныеДляПроведения.Сотрудник, ДанныеДляПроведения.ПериодРегистрации, ДанныеДляПроведения.СуммаОблагаемойБазы, ,Истина)
	КонецЕсли;
	
	ПроведениеСервер.ЗаписьДвиженийПоУчетам(Движения, СтруктураВидовУчета);
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает описание состава документа
//
// Возвращаемое значение:
//  Структура - см. ЗарплатаКадрыСоставДокументов.НовоеОписаниеСоставаОбъекта.
Функция ОписаниеСоставаОбъекта() Экспорт
	Возврат ЗарплатаКадрыСоставДокументов.ОписаниеСоставаОбъектаФизическоеЛицоВШапке();
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеДляПроведения(Ссылка, СтруктураВидовУчета)
	
	ДанныеДляПроведения = Новый Структура("Организация, Сотрудник, СуммаОблагаемойБазы, ПериодРегистрации");
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрацияОблагаемойБазыРуководителя.Организация КАК Организация,
	|	РегистрацияОблагаемойБазыРуководителя.Сотрудник КАК Сотрудник,
	|	РегистрацияОблагаемойБазыРуководителя.ПериодРегистрации КАК ПериодРегистрации,
	|	РегистрацияОблагаемойБазыРуководителя.СуммаОблагаемойБазы КАК СуммаОблагаемойБазы
	|ИЗ
	|	Документ.РегистрацияОблагаемойБазыРуководителя КАК РегистрацияОблагаемойБазыРуководителя
	|ГДЕ
	|	РегистрацияОблагаемойБазыРуководителя.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	ВыборкаРеквизиты = Запрос.Выполнить().Выбрать();
	Если ВыборкаРеквизиты.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ДанныеДляПроведения, ВыборкаРеквизиты);
	КонецЕсли;
	
	Возврат ДанныеДляПроведения;

КонецФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли