#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Действие") И ДанныеЗаполнения.Действие = "Заполнить" Тогда 
			ЗаполнитьПоДаннымЗаполнения(ДанныеЗаполнения);	
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли; 
			
	Документы.НачислениеПоДоговорам.ЗаполнитьПредставлениеРаспределенияРезультатовРасчета(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для каждого СтрокаНачислений Из НачисленияПоДоговорам Цикл
		
		ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, СтрокаНачислений.ПланируемаяДатаВыплаты, "Объект.НачисленияПоДоговорам[" + Формат(СтрокаНачислений.НомерСтроки - 1, "ЧГ=") + "].ПланируемаяДатаВыплаты",
			Отказ, НСтр("ru = 'Дата выплаты';
						|en = 'Payment date'"), Дата, НСтр("ru = 'даты документа';
														|en = 'document dates'"));
		
	КонецЦикла;
	
	ПроверитьПериодДействияУдержаний(Отказ);
	
	ПроверитьНачисленияПоДоговорам(Отказ);
	
	ПроверитьРаспределениеПоИсточникамФинансирования(Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИсправлениеДокументовЗарплатаКадры.ПроверитьСторнированиеОснований(Отказ, ЭтотОбъект.Ссылка);
	
	Документы.НачислениеПоДоговорам.ПровестиПоУчетам(Ссылка, РежимПроведения, Отказ, Неопределено, Движения, ЭтотОбъект, ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ИсправлениеДокументовЗарплатаКадры.ПроверитьСторнированиеОснований(Отказ, ЭтотОбъект.Ссылка, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции.

Процедура ПроверитьПериодДействияУдержаний(Отказ)
	
	ПараметрыПроверки = РасчетЗарплатыРасширенный.ПараметрыПроверкиПериодаДействия();
	ПараметрыПроверки.Ссылка = Ссылка;
	
	ПроверяемыеКоллекции = Новый Массив;
	ПроверяемыеКоллекции.Добавить(РасчетЗарплатыРасширенный.ОписаниеКоллекцииДляПроверкиПериодаДействия("Удержания", НСтр("ru = 'Удержания';
																															|en = 'Deductions'"), "Удержание"));
	
	РасчетЗарплатыРасширенный.ПроверитьПериодДействияВКоллекцияхНачислений(ЭтотОбъект, ПараметрыПроверки, ПроверяемыеКоллекции, Отказ);
	
КонецПроцедуры

Процедура ПроверитьНачисленияПоДоговорам(Отказ)
			
	Для Каждого СтрокаПоДоговору Из НачисленияПоДоговорам Цикл
		Если Не ЗначениеЗаполнено(СтрокаПоДоговору.ДокументОснование) Тогда
			Продолжить;
		КонецЕсли;
		СотрудникДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаПоДоговору.ДокументОснование, "Сотрудник");
		Если СтрокаПоДоговору.Сотрудник <> СотрудникДоговора Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В строке %1, табличной части ""Начисления по договорам"" для сотрудника %2 указан договор другого сотрудника (%3)';
					|en = 'In row %1 of the ""Accruals under contracts"" tabular section, a contract of another employee (%3) was specified for employee %2'"),
				СтрокаПоДоговору.НомерСтроки,
				СтрокаПоДоговору.Сотрудник,
				СотрудникДоговора);
			ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения, , "Объект.НачисленияПоДоговорам[" + Формат(СтрокаПоДоговору.НомерСтроки - 1, "ЧН=; ЧГ=") + "].ДокументОснование" , , Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьРаспределениеПоИсточникамФинансирования(Отказ)
	
	ИменаТаблицРаспределяемыхПоСтатьямФинансирования =
		"НачисленияПоДоговорам,Удержания,НДФЛ,КорректировкиВыплаты";
	
	ОтражениеЗарплатыВБухучетеРасширенный.ПроверитьРезультатыРаспределенияНачисленийУдержанийОбъекта(
		ЭтотОбъект, ИменаТаблицРаспределяемыхПоСтатьямФинансирования, Отказ);
			
КонецПроцедуры

Процедура ЗаполнитьПоДаннымЗаполнения(ДанныеЗаполнения) Экспорт
	
	ЗаполнитьИсходныеДанныеДокумента(ДанныеЗаполнения);
	
	ЗапрашиваемыеЗначения = Новый Структура;
	ЗапрашиваемыеЗначения.Вставить("Ответственный", "Ответственный");
	ФиксированныеЗначения = Новый Массив;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ЗапрашиваемыеЗначения.Вставить("Организация", "Организация");
		ФиксированныеЗначения.Добавить("Организация");
	КонецЕсли;
	
	ЗарплатаКадры.ЗаполнитьЗначенияВФорме(ЭтотОбъект, ЗапрашиваемыеЗначения, ФиксированныеЗначения);
	
КонецПроцедуры  

Функция КадровыеДанныеСотрудника(КадровыеДанные, Сотрудник)
	Возврат КадровыеДанные.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник));
КонецФункции	

Функция СотрудникиДляРасчета(Сотрудники) Экспорт
	
	Если ТипЗнч(Сотрудники) = Тип("СправочникСсылка.ФизическиеЛица")
		Или ТипЗнч(Сотрудники) = Тип("Массив") И Сотрудники.Количество() > 0 И ТипЗнч(Сотрудники[0]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда 
		Возврат Сотрудники;
	КонецЕсли;
	
	СотрудникиДокумента = Новый Массив;
	
	ОписанияТаблиц = Документы.НачислениеПоДоговорам.ОписанияТаблицНачисленийДляРасчетногоЛистка();
	Для Каждого ОписаниеТаблицы Из ОписанияТаблиц Цикл 
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СотрудникиДокумента, ОбщегоНазначения.ВыгрузитьКолонку(ЭтотОбъект[ОписаниеТаблицы.ИмяТаблицы], ОписаниеТаблицы.ИмяРеквизитаСотрудник, Истина));
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.УстановитьПараметр("СотрудникиДокумента", СотрудникиДокумента);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Сотрудники.ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&Сотрудники)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сотрудники.Ссылка
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизическиеЛица КАК ФизическиеЛица
	|		ПО Сотрудники.ФизическоеЛицо = ФизическиеЛица.ФизическоеЛицо
	|			И (Сотрудники.Ссылка В (&СотрудникиДокумента))
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Сотрудники.Ссылка
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&Сотрудники)";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеИРасчет	

Функция ПараметрыРасчета() Экспорт
	
	ПараметрыРасчета = Новый Структура();
	ПараметрыРасчета.Вставить("Сотрудники", Неопределено);
	ПараметрыРасчета.Вставить("Отбор", Неопределено);
	ПараметрыРасчета.Вставить("ИзмененныеДанные", Неопределено);
	ПараметрыРасчета.Вставить("ПозицииВставки", Новый Структура);
	ПараметрыРасчета.Вставить("ОкончательныйРасчетНДФЛ", Ложь);
	ПараметрыРасчета.Вставить("АдресТаблицыРаспределенияНачислений", Неопределено);
	ПараметрыРасчета.Вставить("АдресТаблицыРаспределенияУдержаний", Неопределено);
	ПараметрыРасчета.Вставить("АдресТаблицыРаспределенияПоТерриториямУсловиямТруда", Неопределено);
	ПараметрыРасчета.Вставить("РежимПересчетаНДФЛ", Ложь);
	ПараметрыРасчета.Вставить("РежимПересчетаВзносов", Ложь);
	ПараметрыРасчета.Вставить("ЭтоРасчетСУстановленнойСуммой", Ложь);
	
	Возврат ПараметрыРасчета;
	
КонецФункции

Процедура Рассчитать(ПараметрыРасчета = Неопределено) Экспорт
	
	Если ПараметрыРасчета = Неопределено Тогда
		ПараметрыРасчета = ПараметрыРасчета();
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ПараметрыРасчета.Сотрудники) Тогда
		ПараметрыРасчета.Сотрудники = ЭтотОбъект.НачисленияПоДоговорам.ВыгрузитьКолонку("Сотрудник");	
	КонецЕсли;

	Если ПараметрыРасчета.Сотрудники <> Неопределено И ТипЗнч(ПараметрыРасчета.Сотрудники) <> Тип("Массив") Тогда
		ПараметрыРасчета.Сотрудники = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыРасчета.Сотрудники);
	КонецЕсли;
	
	Если ПараметрыРасчета.ЭтоРасчетСУстановленнойСуммой Тогда
		ЗавершитьРасчетСУстановленнойСуммой(ПараметрыРасчета);							
	Иначе	
		ЗавершитьРасчетПоУмолчанию(ПараметрыРасчета);	
	КонецЕсли;								

КонецПроцедуры   

Процедура ЗавершитьРасчетСУстановленнойСуммой(ПараметрыРасчета)
	
	Если ПараметрыРасчета.Отбор = Неопределено Тогда
		Отбор = СотрудникиФизическиеЛицаОтбор(ПараметрыРасчета.Сотрудник);
		ПараметрыРасчета.Отбор = Отбор;	
	КонецЕсли;
	
	Если ПараметрыРасчета.Отбор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерРасчета = РасчетЗарплатыРасширенный.СоздатьМенеджерРасчета(ЭтотОбъект.МесяцНачисления, ЭтотОбъект.Организация);
	ЗаполнитьНастройкиМенеджераРасчета(ПараметрыРасчета.Отбор.ФизическиеЛица, МенеджерРасчета, ПараметрыРасчета.ОкончательныйРасчетНДФЛ);
	
	Если ЗначениеЗаполнено(ПараметрыРасчета.ИзмененныеДанные) Тогда
		РасчетЗарплатыРасширенныйФормы.ЗаполнитьИсточникиИзмененийМенеджераРасчета(МенеджерРасчета, ПараметрыРасчета.ИзмененныеДанные);
	КонецЕсли;
	
	ДанныеОбъектаВДанныеМенеджераРасчета(МенеджерРасчета, ПараметрыРасчета.Отбор, ПараметрыРасчета);
	МенеджерРасчета.РассчитатьЗарплату();
	РасчетЗарплатыВДанныeОбъекта(МенеджерРасчета.Зарплата, ПараметрыРасчета.ПозицииВставки);
	
КонецПроцедуры

Процедура ЗавершитьРасчетПоУмолчанию(ПараметрыРасчета)
	
	СписокСотрудников = Неопределено;
	Если ПараметрыРасчета.Сотрудники <> Неопределено Тогда
		СписокСотрудников = СотрудникиДляРасчета(ПараметрыРасчета.Сотрудники);
	КонецЕсли;
	
	ОчиститьТаблицыДокумента(Документы.НачислениеПоДоговорам.ОписаниеДокумента());
	ПерезаполнитьДанныеОбъекта(Документы.НачислениеПоДоговорам.ОписаниеДокумента(), 
									СписокСотрудников, 
									Истина, 
									Ложь, 
									Истина, 
									Неопределено,
									ПараметрыРасчета);
	
КонецПроцедуры

Процедура ЗаполнитьНастройкиМенеджераРасчета(СотрудникиФизическиеЛица, МенеджерРасчета, ОкончательныйРасчетНДФЛ) Экспорт
	
	МенеджерРасчета.ИсключаемыйРегистратор = ЭтотОбъект.Ссылка;
	МенеджерРасчета.НастройкиРасчета.РассчитыватьДоговорыПодряда = Истина;
	МенеджерРасчета.НастройкиРасчета.РассчитыватьУдержания = Истина;
	МенеджерРасчета.НастройкиРасчета.РассчитыватьНДФЛ = Истина;
	МенеджерРасчета.НастройкиРасчета.РассчитыватьКорректировкиВыплаты = Истина;
	МенеджерРасчета.НастройкиРасчета.ОкончательныйРасчет = Истина;
	МенеджерРасчета.НастройкиРасчета.Сотрудники = СотрудникиФизическиеЛица;
	
	МенеджерРасчета.НастройкиНДФЛ.ДатаВыплаты = ЭтотОбъект.Дата;
	МенеджерРасчета.НастройкиНДФЛ.Сотрудники = СотрудникиФизическиеЛица;
	МенеджерРасчета.НастройкиНДФЛ.ОкончательныйРасчет = ОкончательныйРасчетНДФЛ;
	
КонецПроцедуры

Процедура ДанныеОбъектаВДанныеМенеджераРасчета(МенеджерРасчета, Отбор, ПараметрыРасчета) Экспорт
	
	ДанныеРезультатовРаспределенияНачислений = Неопределено;
	Если ЗначениеЗаполнено(ПараметрыРасчета.АдресТаблицыРаспределенияНачислений) Тогда 
		ДанныеРезультатовРаспределенияНачислений = ПолучитьИзВременногоХранилища(ПараметрыРасчета.АдресТаблицыРаспределенияНачислений);
	КонецЕсли;
	
	ДанныеРезультатовРаспределенияУдержаний = Неопределено;
	Если ЗначениеЗаполнено(ПараметрыРасчета.АдресТаблицыРаспределенияУдержаний) Тогда 
		ДанныеРезультатовРаспределенияУдержаний = ПолучитьИзВременногоХранилища(ПараметрыРасчета.АдресТаблицыРаспределенияУдержаний);
	КонецЕсли;
	
	ДанныеРаспределенияПоТерриториямУсловиямТруда = Неопределено;
	Если ЗначениеЗаполнено(ПараметрыРасчета.АдресТаблицыРаспределенияПоТерриториямУсловиямТруда) Тогда 
		ДанныеРаспределенияПоТерриториямУсловиямТруда = ПолучитьИзВременногоХранилища(ПараметрыРасчета.АдресТаблицыРаспределенияПоТерриториямУсловиямТруда);
	КонецЕсли;
	
	ТаблицыНачисленийДокумента = РасчетДокументовБЗК.ТаблицыНачисленийДокумента();
	ТаблицыНачисленийДокумента.Показатели = ЭтотОбъект.Показатели;
	ТаблицыНачисленийДокумента.РаспределениеПоСтатьям = ЭтотОбъект.РаспределениеРезультатовНачислений;
	ТаблицыНачисленийДокумента.РаспределениеПоТерриториямУсловиямТруда = ЭтотОбъект.РаспределениеПоТерриториямУсловиямТруда;
	
	РасчетДокументовБЗК.НачисленияПоДоговорамВДанныеМенеджераРасчета
		(ЭтотОбъект.НачисленияПоДоговорам, 
		МенеджерРасчета, 
		ТаблицыНачисленийДокумента, 
		Отбор, 
		ПараметрыРасчета.ПозицииВставки,
		ДанныеРезультатовРаспределенияНачислений,
		ДанныеРаспределенияПоТерриториямУсловиямТруда);
	
	ТаблицыУдержанийДокумента = РасчетДокументовБЗК.ТаблицыУдержанийДокумента();
	ТаблицыУдержанийДокумента.Показатели = ЭтотОбъект.Показатели;
	ТаблицыУдержанийДокумента.РаспределениеПоСтатьям = ЭтотОбъект.РаспределениеРезультатовНачислений;
	
	РасчетДокументовБЗК.УдержанияВМенеджерРасчета(ЭтотОбъект.Удержания, 
													МенеджерРасчета, 
													ТаблицыУдержанийДокумента,
													, 
													Отбор, 
													ПараметрыРасчета.ПозицииВставки);
	
	ТаблицыНДФЛ = РасчетДокументовБЗК.ТаблицыНДФЛДокумента();
	ТаблицыНДФЛ.НДФЛ = ЭтотОбъект.НДФЛ;
	ТаблицыНДФЛ.Вычеты = ЭтотОбъект.ПримененныеВычетыНаДетейИИмущественные;
	ТаблицыНДФЛ.РаспределениеПоСтатьям = ЭтотОбъект.РаспределениеРезультатовУдержаний; 
	
	РасчетДокументовБЗК.НДФЛВДанныеМенеджераРасчета(ТаблицыНДФЛ, МенеджерРасчета, Отбор, ПараметрыРасчета.ПозицииВставки);

	РасчетДокументовБЗК.КорректировкиВыплатыВМенеджерРасчета(ЭтотОбъект.КорректировкиВыплаты, 
																МенеджерРасчета, 
																ТаблицыНачисленийДокумента.РаспределениеПоСтатьям, 
																Отбор, 
																ПараметрыРасчета.ПозицииВставки);
	
КонецПроцедуры

Процедура РасчетЗарплатыВДанныeОбъекта(ДанныеМенеджераРасчета, ПозицииВставки = Неопределено) Экспорт
	
	СортироватьДанныеДляЗаполнения(ДанныеМенеджераРасчета);
	
	РезультатРасчетаУдержанийВДанныеОбъекта(ДанныеМенеджераРасчета, ПозицииВставки);
	РезультатРасчетаНачисленийВДанныеОбъекта(ДанныеМенеджераРасчета, ПозицииВставки);
	РезультатРасчетаНДФЛВДанныеОбъекта(ДанныеМенеджераРасчета, ПозицииВставки);
    РезультатРасчетаКорректировкиВыплатыВДанныеОбъекта(ДанныеМенеджераРасчета, ПозицииВставки);
	
КонецПроцедуры

Процедура РезультатРасчетаНачисленийВДанныеОбъекта(ДанныеМенеджераРасчета, ПозицииВставки)
	
	ТаблицыНачислений = РасчетДокументовБЗК.ТаблицыНачисленийДокумента();
	
	ТаблицыНачислений.Вставить("НачисленияПоДоговорам", ЭтотОбъект.НачисленияПоДоговорам);
	ТаблицыНачислений.Показатели = ЭтотОбъект.Показатели;
	ТаблицыНачислений.РаспределениеПоСтатьям = ЭтотОбъект.РаспределениеРезультатовНачислений;
	ТаблицыНачислений.РаспределениеПоТерриториямУсловиямТруда = ЭтотОбъект.РаспределениеПоТерриториямУсловиямТруда;
	
	ОписаниеТаблиц = РасчетДокументовБЗК.ОписаниеТаблицНачисленийДокумента();
	ОписаниеТаблиц.Начисления = Документы.НачислениеПоДоговорам.ОписаниеТаблицыНачисленийПоДоговорам();
	
	Псевдоним = Документы.НачислениеПоДоговорам.ИмяТаблицыНачисленияПоДоговорам(ТипЗнч(ДанныеМенеджераРасчета), ДанныеМенеджераРасчета.Свойство("НачисленияПоДоговорам"));
	
	РасчетДокументовБЗК.РасчетЗарплатыНачисленияПоДоговорамВДанныеОбъекта(
		ТаблицыНачислений, 
		ДанныеМенеджераРасчета[Псевдоним], 
		ЭтотОбъект.Организация, 
		ОписаниеТаблиц, 
		Псевдоним,
		ПозицииВставки)   
	
КонецПроцедуры       

Процедура РезультатРасчетаУдержанийВДанныеОбъекта(ДанныеМенеджераРасчета, ПозицииВставки)

	ТаблицыУдержаний = РасчетДокументовБЗК.ТаблицыУдержанийДокумента();
	ТаблицыУдержаний.Удержания = ЭтотОбъект.Удержания;
	ТаблицыУдержаний.РаспределениеПоСтатьям = ЭтотОбъект.РаспределениеРезультатовУдержаний;
	ТаблицыУдержаний.Показатели = ЭтотОбъект.Показатели;
	
	ОписаниеТаблицУдержаний = РасчетДокументовБЗК.ОписаниеТаблицУдержанийДокумента();
	ОписаниеТаблицУдержаний.Удержания = Документы.НачислениеПоДоговорам.ОписаниеТаблицыУдержаний();
	
	РасчетДокументовБЗК.РасчетЗарплатыУдержанияВДанныеОбъекта(
		ТаблицыУдержаний, 
		ДанныеМенеджераРасчета.Удержания, 
		ОписаниеТаблицУдержаний,
		,
		,
		ПозицииВставки);
	
КонецПроцедуры

Процедура РезультатРасчетаНДФЛВДанныеОбъекта(ДанныеМенеджераРасчета, ПозицииВставки)
	
	ТаблицыНДФЛ = РасчетДокументовБЗК.ТаблицыНДФЛДокумента();
	ТаблицыНДФЛ.НДФЛ = ЭтотОбъект.НДФЛ;
	ТаблицыНДФЛ.РаспределениеПоСтатьям = ЭтотОбъект.РаспределениеРезультатовУдержаний;	
	ТаблицыНДФЛ.Вычеты = ЭтотОбъект.ПримененныеВычетыНаДетейИИмущественные;	
	
	ОписаниеТаблицыНДФЛ = Документы.НачислениеПоДоговорам.ОписаниеТаблицыНДФЛ();
	
	РасчетДокументовБЗК.РасчетЗарплатыНДФЛВДанныеОбъекта(
		ТаблицыНДФЛ, 
		ДанныеМенеджераРасчета.НДФЛ, 
		ОписаниеТаблицыНДФЛ,
		,
		ПозицииВставки);
	
КонецПроцедуры

Процедура РезультатРасчетаКорректировкиВыплатыВДанныеОбъекта(ДанныеМенеджераРасчета, ПозицииВставки)
	
	РасчетДокументовБЗК.РасчетЗарплатыКорректировкиВыплатыВДанныеОбъекта(
		ЭтотОбъект.КорректировкиВыплаты, 
		ДанныеМенеджераРасчета.КорректировкиВыплаты, 
		ЭтотОбъект.РаспределениеРезультатовУдержаний, 
		Документы.НачислениеПоДоговорам.ОписаниеТаблицыКорректировкиВыплаты(), 
		ПозицииВставки);
		
КонецПроцедуры

Процедура СортироватьДанныеДляЗаполнения(ДанныеЗаполнения)
	
	СортироватьРезультатРасчетаНачислений(ДанныеЗаполнения);
	СортироватьРезультатыРасчетаПоФизическимЛицам(ДанныеЗаполнения);
	
КонецПроцедуры

Процедура СортироватьРезультатРасчетаНачислений(ДанныеЗаполнения)
	
	СортируемыеКоллекции = Новый Массив;
	
	Псевдоним = ИмяТаблицыНачисленияПоДоговорам(ДанныеЗаполнения);
	МетаданныеРеквизитов = Метаданные.Документы.НачислениеПоДоговорам.ТабличныеЧасти.НачисленияПоДоговорам.Реквизиты;
	
	Коллекция = СотрудникиФормыРасширенный.СортируемаяКоллекцияСотрудников(ДанныеЗаполнения[Псевдоним]);
	Коллекция.ОписаниеКоллекции.Псевдоним = Псевдоним;
	Коллекция.ОписаниеКоллекции.ИмяПоляСотрудник = "Сотрудник";
	Коллекция.ОписаниеКоллекции.ИмяПоляПодразделение = "Подразделение";
	Коллекция.ОписаниеКоллекции.ИмяПоляПериод = "ДатаНачала";
	СортируемыеКоллекции.Добавить(Коллекция);
	
	НастройкиСортировки = СотрудникиФормыРасширенный.НастройкиСортировкиПоУмолчанию();
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "ДокументОснование", МетаданныеРеквизитов.ДокументОснование.Тип);
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "ДатаНачала", МетаданныеРеквизитов.ДатаНачала.Тип);
	
	УстановитьПривилегированныйРежим(Истина);
	ПорядокСортировки = СотрудникиФормыРасширенный.ПорядокСтрокКоллекцийСотрудников(СортируемыеКоллекции, НастройкиСортировки);
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого КлючЗначение Из ПорядокСортировки Цикл
		
		ТаблицаИсходныхДанных = ДанныеЗаполнения[КлючЗначение.Ключ];
		ТаблицаИсходныхДанных.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Число"));
			
		Для Каждого СтрокаПорядка Из КлючЗначение.Значение Цикл
			СтрокаТаблицы = ТаблицаИсходныхДанных[СтрокаПорядка.ИндексСтрокиИсходнойТаблицы];
			СтрокаТаблицы.Порядок = СтрокаПорядка.Порядок;
		КонецЦикла;
		
		ТаблицаИсходныхДанных.Сортировать("Порядок");
		ТаблицаИсходныхДанных.Колонки.Удалить("Порядок");
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СортироватьРезультатыРасчетаПоФизическимЛицам(ДанныеЗаполнения)
	
	СортируемыеКоллекции = Новый Массив;
	
	Коллекция = СотрудникиФормыРасширенный.СортируемаяКоллекцияФизическихЛиц(ДанныеЗаполнения.Удержания);
	Коллекция.ОписаниеКоллекции.Псевдоним = "Удержания";
	Коллекция.ОписаниеКоллекции.ИмяПоляФизическоеЛицо = "ФизическоеЛицо";
	
	МетаданныеРеквизитов = Метаданные.Документы.НачислениеПоДоговорам.ТабличныеЧасти.Удержания.Реквизиты;
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "Удержание.ОчередностьРасчета", МетаданныеРеквизитов.Удержание.Тип);
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "Удержание", МетаданныеРеквизитов.Удержание.Тип);
	
	СортируемыеКоллекции.Добавить(Коллекция);
	
	Коллекция = СотрудникиФормыРасширенный.СортируемаяКоллекцияФизическихЛиц(ДанныеЗаполнения.НДФЛ);
	Коллекция.ОписаниеКоллекции.Псевдоним = "НДФЛ";
	Коллекция.ОписаниеКоллекции.ИмяПоляФизическоеЛицо = "ФизическоеЛицо";
	
	МетаданныеРеквизитов = Метаданные.Документы.НачислениеПоДоговорам.ТабличныеЧасти.НДФЛ.Реквизиты;
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "МесяцНалоговогоПериода", МетаданныеРеквизитов.МесяцНалоговогоПериода.Тип);
	СотрудникиФормыРасширенный.ДобавитьДополнительноеПолеСортировки(Коллекция, "Подразделение", МетаданныеРеквизитов.Подразделение.Тип);
	
	СортируемыеКоллекции.Добавить(Коллекция);
	
	Коллекция = СотрудникиФормыРасширенный.СортируемаяКоллекцияФизическихЛиц(ДанныеЗаполнения.КорректировкиВыплаты);
	Коллекция.ОписаниеКоллекции.Псевдоним = "КорректировкиВыплаты";
	Коллекция.ОписаниеКоллекции.ИмяПоляФизическоеЛицо = "ФизическоеЛицо";
	СортируемыеКоллекции.Добавить(Коллекция);
	
	УстановитьПривилегированныйРежим(Истина);
	ПорядокСортировки = СотрудникиФормыРасширенный.ПорядокСортировкиКоллекцияФизическихЛиц(СортируемыеКоллекции, ЭтотОбъект.Организация, КонецМесяца(ЭтотОбъект.МесяцНачисления));
	УстановитьПривилегированныйРежим(Ложь);
	
	Для Каждого КлючЗначение Из ПорядокСортировки Цикл
		
		ТаблицаИсходныхДанных = ДанныеЗаполнения[КлючЗначение.Ключ];
		ТаблицаИсходныхДанных.Колонки.Добавить("Порядок", Новый ОписаниеТипов("Число"));
			
		Для Каждого СтрокаПорядка Из КлючЗначение.Значение Цикл
			СтрокаТаблицы = ТаблицаИсходныхДанных[СтрокаПорядка.ИндексСтрокиИсходнойТаблицы];
			СтрокаТаблицы.Порядок = СтрокаПорядка.Порядок;
		КонецЦикла;
		
		ТаблицаИсходныхДанных.Сортировать("Порядок");
		ТаблицаИсходныхДанных.Колонки.Удалить("Порядок");
	КонецЦикла;
	
КонецПроцедуры

Функция ИмяТаблицыНачисленияПоДоговорам(ДанныеЗаполнения)
	Возврат Документы.НачислениеПоДоговорам.ИмяТаблицыНачисленияПоДоговорам(ТипЗнч(ДанныеЗаполнения), 
																			ДанныеЗаполнения.Свойство("НачисленияПоДоговорам"));	
КонецФункции

Функция СотрудникиФизическиеЛицаОтбор(Сотрудники) Экспорт
	
	СотрудникиДокумента = Новый Массив;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СотрудникиДокумента, ОбщегоНазначения.ВыгрузитьКолонку(ЭтотОбъект.НачисленияПоДоговорам, "Сотрудник"), Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.УстановитьПараметр("СотрудникиДокумента", СотрудникиДокумента);
	
	Если ТипЗнч(Сотрудники) = Тип("СправочникСсылка.ФизическиеЛица")
		Или ТипЗнч(Сотрудники) = Тип("Массив") И Сотрудники.Количество() > 0 И ТипЗнч(Сотрудники[0]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда 
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Сотрудники.Ссылка КАК Сотрудник,
			|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|ГДЕ
			|	Сотрудники.Ссылка В(&СотрудникиДокумента)
			|	И Сотрудники.ФизическоеЛицо В(&Сотрудники)";
		
		Если ТипЗнч(Сотрудники) <> Тип("Массив") Тогда
			МассивФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники);
		Иначе
			МассивФизическиеЛица = Сотрудники;
		КонецЕсли;
		СотрудникиФизическиеЛица = Запрос.Выполнить().Выбрать();
		
	Иначе
		
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Сотрудники.ФизическоеЛицо
			|ПОМЕСТИТЬ ВТФизическиеЛица
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|ГДЕ
			|	Сотрудники.Ссылка В(&Сотрудники)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Сотрудники.Ссылка КАК Сотрудник,
			|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТФизическиеЛица КАК ФизическиеЛица
			|		ПО Сотрудники.ФизическоеЛицо = ФизическиеЛица.ФизическоеЛицо
			|			И (Сотрудники.Ссылка В (&СотрудникиДокумента))
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТФизическиеЛица.ФизическоеЛицо
			|ИЗ
			|	ВТФизическиеЛица КАК ВТФизическиеЛица";
			
		Результат = Запрос.ВыполнитьПакет();
		МассивФизическиеЛица = Результат[2].Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо");
		СотрудникиФизическиеЛица = Результат[1].Выбрать();
		
	КонецЕсли;
	
	Отбор = Новый Структура("СотрудникиКРасчету, ФизическиеЛицаСотрудников, ФизическиеЛицаОтбор, ФизическиеЛица");
	Отбор.ФизическиеЛица = МассивФизическиеЛица;
	Отбор.ФизическиеЛицаСотрудников = Новый Соответствие;
	Отбор.ФизическиеЛицаОтбор = Новый Соответствие;
	Отбор.СотрудникиКРасчету = Новый Массив;
	
	Пока СотрудникиФизическиеЛица.Следующий() Цикл
		Отбор.ФизическиеЛицаСотрудников.Вставить(СотрудникиФизическиеЛица.Сотрудник, СотрудникиФизическиеЛица.ФизическоеЛицо);
		Отбор.СотрудникиКРасчету.Добавить(СотрудникиФизическиеЛица.Сотрудник);
	КонецЦикла;
	
	Для каждого ФизическоеЛицо Из МассивФизическиеЛица Цикл
		Отбор.ФизическиеЛицаОтбор.Вставить(ФизическоеЛицо, Истина)
	КонецЦикла;
	
	Возврат Отбор;
	
КонецФункции

Процедура ЗаполнитьИсходныеДанныеДокумента(ДанныеЗаполнения)

	Если Не ЗначениеЗаполнено(МесяцНачисления) Тогда
		ДанныеЗаполнения.Свойство("МесяцНачисления", МесяцНачисления);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		ДанныеЗаполнения.Свойство("Дата", Дата);
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ДанныеЗаполнения.Свойство("Организация", Организация);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Подразделение) Тогда
		ДанныеЗаполнения.Свойство("Подразделение", Подразделение);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПорядокВыплаты) Тогда
		ДанныеЗаполнения.Свойство("ПорядокВыплаты", ПорядокВыплаты);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаПервичногоДокумента) Тогда
		ДанныеЗаполнения.Свойство("ДатаПервичногоДокумента", ДатаПервичногоДокумента);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(НомерПервичногоДокумента) Тогда
		ДанныеЗаполнения.Свойство("НомерПервичногоДокумента", НомерПервичногоДокумента);
	КонецЕсли;
	
	ЗарплатаКадрыРасширенный.УстановитьНаименованиеПервичногоДокумента(ЭтотОбъект);

	Если НЕ ДанныеЗаполнения.Свойство("НачисленияПоДоговорам") Тогда 
		Возврат;
	КонецЕсли;
	
	ОписаниеТаблицы = Документы.НачислениеПоДоговорам.ОписаниеТаблицыНачисленийПоДоговорам();
	ИдентификаторСтроки = Документы.НачислениеПоДоговорам.НовыйИдентификаторСтроки(ОписаниеТаблицы.НомерТаблицы);
	
	СписокСотрудников = ДанныеЗаполнения.НачисленияПоДоговорам.ВыгрузитьКолонку("Сотрудник");
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, СписокСотрудников, "Подразделение", ТекущаяДатаСеанса());
	
	Если Не НачисленияПоДоговорам.Количество() Тогда
		
		ТаблицаНачисленияПоДоговорам = Новый ТаблицаЗначений;
		ДанныеЗаполнения.Свойство("НачисленияПоДоговорам", ТаблицаНачисленияПоДоговорам);
		
		Для Каждого ДанныеНачислений Из ТаблицаНачисленияПоДоговорам Цикл
			СтрокаДокумента = ЭтотОбъект.НачисленияПоДоговорам.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДокумента, ДанныеНачислений);
			
			КадровыеДанныеСотрудника = КадровыеДанныеСотрудника(КадровыеДанные, ДанныеНачислений.Сотрудник);
			
			Если КадровыеДанныеСотрудника.Количество() Тогда
				СтрокаДокумента.Подразделение = КадровыеДанныеСотрудника[0].Подразделение;
			КонецЕсли;
			
			СтрокаДокумента[ОписаниеТаблицы.ИмяРеквизитаИдентификаторСтроки] = ИдентификаторСтроки;
			ИдентификаторСтроки = ИдентификаторСтроки + 1; 
		КонецЦикла;    
	КонецЕсли; 
	
КонецПроцедуры 

Функция ПерезаполнитьДанныеОбъекта(ОписаниеДокумента, 
									Знач СотрудникиФизическиеЛица = Неопределено, 
									СохранятьИсправления = Истина, 
									ИспользоватьДлительнуюОперацию = Ложь, 
									ПерезаполнятьВедущиеТаблицы = Истина, 
									СотрудникиПериодДействияПерерасчет = Неопределено,
									ПараметрыРасчета) Экспорт
	
	ПараметрМесяцНачисления = ЭтотОбъект[ОписаниеДокумента.МесяцНачисленияИмя];
	
	ПараметрОрганизация = ЭтотОбъект.Организация;
	ПараметрДокументСсылка = ЭтотОбъект.Ссылка;
	ПараметрПодразделение = ЭтотОбъект.Подразделение;
	
	ПараметрФизическиеЛица = Неопределено;
	ПозицииВставки = ПараметрыРасчета.ПозицииВставки;
	
	// В качестве параметров могут быть получены физические лица, а не сотрудники.
	Если СотрудникиФизическиеЛица <> Неопределено Тогда
		ВедущиеОбъекты = РасчетЗарплатыРасширенный.СотрудникиФизическиеЛица(СотрудникиФизическиеЛица, Организация);
		Если ВедущиеОбъекты = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		ПараметрСотрудники = ВедущиеОбъекты.Сотрудники;
		ПараметрФизическиеЛица = ВедущиеОбъекты.ФизическиеЛица;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПодработки") И ПараметрСотрудники <> Неопределено Тогда 
		ПараметрыПолученияПодработок = 
			КадровыйУчет.ПараметрыДляЗапросВТРабочиеМестаСотрудниковПоСпискуСотрудников(ПараметрСотрудники, ПараметрМесяцНачисления, КонецМесяца(ПараметрМесяцНачисления));
	    МассивПодработок = КадровыйУчетРасширенный.ПодработкиСотрудников(Истина, ПараметрыПолученияПодработок);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрСотрудники, МассивПодработок, Истина);
	КонецЕсли;
	
	// Период заполнения данных определяем по месяцу начисления, 
	// если он не определяется отдельными полями.
	ПараметрОкончаниеПериода = КонецМесяца(ПараметрМесяцНачисления);
	Если ЗначениеЗаполнено(ОписаниеДокумента.Окончание) Тогда
		ПараметрОкончаниеПериода = КонецДня(ЭтотОбъект[ОписаниеДокумента.Окончание]);
	КонецЕсли;
	
	ПараметрРежимНачисления = РежимНачисления(ОписаниеДокумента);
	ПараметрПорядокВыплаты = ПорядокВыплаты(ОписаниеДокумента);
	ПараметрДатаВыплаты = ДатаВыплаты(ОписаниеДокумента);
	
	СтруктураПараметров = РасчетЗарплатыРасширенныйКлиентСервер.ПараметрыПолученияДанныхЗаполненияДокумента();
	СтруктураПараметров.Организация = ПараметрОрганизация;
	СтруктураПараметров.ДокументСсылка = ПараметрДокументСсылка;
	СтруктураПараметров.Подразделение = ПараметрПодразделение;
	СтруктураПараметров.МесяцНачисления = ПараметрМесяцНачисления;
	СтруктураПараметров.ДатаВыплаты = ПараметрДатаВыплаты;
	СтруктураПараметров.Сотрудники = ПараметрСотрудники;
	СтруктураПараметров.ОкончаниеПериода = ПараметрОкончаниеПериода;
	СтруктураПараметров.РежимНачисления = ПараметрРежимНачисления;
	СтруктураПараметров.ПорядокВыплаты = ПараметрПорядокВыплаты;
	СтруктураПараметров.ОписаниеДокумента = ОписаниеДокумента;
	СтруктураПараметров.СохранятьИсправления = СохранятьИсправления;
	СтруктураПараметров.СодержимоеДокумента = РасчетЗарплатыРасширенный.СодержимоеДокументаРасчетаЗарплаты(ЭтотОбъект, ОписаниеДокумента, ПараметрыРасчета.ПозицииВставки);
	СтруктураПараметров.КонтролируемыеПоля = Документы.НачислениеПоДоговорам.ПолучитьКонтролируемыеПоля();
	СтруктураПараметров.СотрудникиПериодДействияПерерасчет = СотрудникиПериодДействияПерерасчет;
	СтруктураПараметров.РежимПересчетаНДФЛ = ОписаниеДокумента.ЭтоНачислениеЗарплаты И ПараметрыРасчета.РежимПересчетаНДФЛ;
	СтруктураПараметров.РежимПересчетаВзносов = ОписаниеДокумента.ЭтоНачислениеЗарплаты И ПараметрыРасчета.РежимПересчетаВзносов;
	СтруктураПараметров.ПозицииВставки = ПараметрыРасчета.ПозицииВставки;
	
	Если ОписаниеДокумента.ЭтоНачислениеЗарплаты
		И ПараметрРежимНачисления = Перечисления.РежимНачисленияЗарплаты.ОкончательныйРасчет Тогда
		
		ПараметрыВС = Новый Структура("ИспользоватьВоеннуюСлужбу,НачислениеЗарплатыВоеннослужащим",Ложь,Ложь);
		ЗаполнитьЗначенияСвойств(ПараметрыВС, ЭтотОбъект);
		СтруктураПараметров.ИспользоватьВоеннуюСлужбу = ПараметрыВС.ИспользоватьВоеннуюСлужбу;
		СтруктураПараметров.НачислениеЗарплатыВоеннослужащим = ПараметрыВС.НачислениеЗарплатыВоеннослужащим;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ОписаниеДокумента.НДФЛИмя) Тогда
		СтруктураПараметров.ОкончательныйРасчетНДФЛ = ПараметрыРасчета.ОкончательныйРасчетНДФЛ;
	КонецЕсли;
	
	ПараметрДанныеРаспределения = Новый Структура;
	ПараметрДанныеРаспределенияПоТерриториямУсловиямТруда = Неопределено;
	
	Если ОписаниеДокумента.ОписанияТаблицДляРаспределенияРезультата <> Неопределено Тогда 
		Для Каждого КлючИЗначение Из ОписаниеДокумента.ОписанияТаблицДляРаспределенияРезультата Цикл
			ПутьКДанным = КлючИЗначение.Значение.ПутьКДаннымАдресРаспределенияРезультатовВХранилище;
			Если ЗначениеЗаполнено(ПутьКДанным) И ЗначениеЗаполнено(ПараметрыРасчета[ПутьКДанным]) И Не ПараметрДанныеРаспределения.Свойство(ПутьКДанным) Тогда
				ПараметрДанныеРаспределения.Вставить(ПутьКДанным, ПолучитьИзВременногоХранилища(ПараметрыРасчета[ПутьКДанным]));
			КонецЕсли;
			ПутьКДанным = КлючИЗначение.Значение.ПутьКДаннымАдресРаспределенияПоТерриториямУсловиямТруда;
			Если ЗначениеЗаполнено(ПутьКДанным) И ЗначениеЗаполнено(ПараметрыРасчета[ПутьКДанным]) И ПараметрДанныеРаспределенияПоТерриториямУсловиямТруда = Неопределено Тогда
				ПараметрДанныеРаспределенияПоТерриториямУсловиямТруда = ПолучитьИзВременногоХранилища(ПараметрыРасчета[ПутьКДанным]);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СтруктураПараметров.РаспределениеРезультатов = ПараметрДанныеРаспределения;
	СтруктураПараметров.ДанныеРаспределенияПоТерриториямУсловиямТруда = ПараметрДанныеРаспределенияПоТерриториямУсловиямТруда;
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
	РасчетЗарплатыРасширенный.ПодготовитьДанныеДляПерезаполнения(СтруктураПараметров, АдресХранилища);
	ЗаполнениеПослеВыполненияДлительнойОперации(АдресХранилища); 
	
КонецФункции

Функция ПорядокВыплаты(ОписаниеДокумента)
	
	Если ЗначениеЗаполнено(ОписаниеДокумента.ПорядокВыплатыИмя) Тогда
		Возврат ЭтотОбъект[ОписаниеДокумента.ПорядокВыплатыИмя];
	КонецЕсли;
	
	Возврат ОписаниеДокумента.ПорядокВыплаты;
	
КонецФункции

Функция ДатаВыплаты(ОписаниеДокумента)
	
	Если ЗначениеЗаполнено(ОписаниеДокумента.ПланируемаяДатаВыплатыИмя) Тогда
		Возврат ЭтотОбъект[ОписаниеДокумента.ПланируемаяДатаВыплатыИмя];
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеДокумента.МесяцНачисленияИмя) Тогда
		
		МесяцНачисления = ЭтотОбъект[ОписаниеДокумента.МесяцНачисленияИмя];
		
		Если ОписаниеДокумента.РежимНачисления = Перечисления.РежимНачисленияЗарплаты.РасчетПервойПоловиныМесяца Тогда
			Возврат РасчетЗарплатыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыАванса(ЭтотОбъект.Организация, МесяцНачисления);
		Иначе
			Возврат РасчетЗарплатыРасширенныйКлиентСервер.ПланируемаяДатаВыплатыЗарплаты(ЭтотОбъект.Организация, МесяцНачисления);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция РежимНачисления(ОписаниеДокумента)
	
	Если ЗначениеЗаполнено(ОписаниеДокумента.РежимНачисления) Тогда
		Возврат ОписаниеДокумента.РежимНачисления;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ЗаполнениеПослеВыполненияДлительнойОперации(АдресХранилища)
	
	СтруктураДанных = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если ТипЗнч(СтруктураДанных) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляЗаполнения = Неопределено;
	Если СтруктураДанных.Свойство("ДанныеДляЗаполненияТаблицДокумента", ДанныеДляЗаполнения) Тогда
		
		ЭтотОбъект.НачисленияПоДоговорам.Очистить();
		ЭтотОбъект.Удержания.Очистить();
		ЭтотОбъект.НДФЛ.Очистить();
		ЭтотОбъект.ПримененныеВычетыНаДетейИИмущественные.Очистить();
		ЭтотОбъект.КорректировкиВыплаты.Очистить();
		
		РасчетЗарплатыВДанныeОбъекта(ДанныеДляЗаполнения);
		
	КонецЕсли;
	
	Если СтруктураДанных.Свойство("ДанныеДляПерезаполненияТаблицДокумента", ДанныеДляЗаполнения) Тогда
		РасчетЗарплатыВДанныeОбъекта(ДанныеДляЗаполнения, СтруктураДанных.СтруктураПараметров.ПозицииВставки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОчиститьТаблицыДокумента(ОписаниеДокумента) Экспорт
	
	ИменаПолейИменТаблиц = Новый Массив;
	ИменаПолейИменТаблиц.Добавить("НачисленияИмя");
	ИменаПолейИменТаблиц.Добавить("НачисленияПерерасчетИмя");
	ИменаПолейИменТаблиц.Добавить("НачисленияПоДоговорамИмя");
	ИменаПолейИменТаблиц.Добавить("ПособияИмя");
	ИменаПолейИменТаблиц.Добавить("УдержанияИмя");
	ИменаПолейИменТаблиц.Добавить("НДФЛИмя");
	ИменаПолейИменТаблиц.Добавить("ВзносыИмя");
	ИменаПолейИменТаблиц.Добавить("СторноИмя");
	ИменаПолейИменТаблиц.Добавить("ПогашениеЗаймовИмя");
	ИменаПолейИменТаблиц.Добавить("ПримененныеВычетыИмя");
	ИменаПолейИменТаблиц.Добавить("ЛьготыИмя");
	ИменаПолейИменТаблиц.Добавить("ЛьготыПерерасчетИмя");
	ИменаПолейИменТаблиц.Добавить("УдержанияПерерасчетИмя");
	
	Для Каждого ИмяПоляИмениТаблицы Из ИменаПолейИменТаблиц Цикл
		Если ОписаниеДокумента[ИмяПоляИмениТаблицы] <> Неопределено Тогда
			ЭтотОбъект[ОписаниеДокумента[ИмяПоляИмениТаблицы]].Очистить();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти    

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли