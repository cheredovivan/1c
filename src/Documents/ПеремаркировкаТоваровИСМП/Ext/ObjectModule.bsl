#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ДополнительныеСвойства.Свойство("НеЗаполнятьТабличнуюЧасть") Тогда
		Товары.Очистить();
	КонецЕсли;
	
	ИнтеграцияИСМППереопределяемый.ОбработкаЗаполненияДокумента(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
	ЗаполнитьОбъектПоСтатистике();
	
	ПроверитьДоступныеПричиныПеремаркировки();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если ТребуетсяИндивидуализацияКИЗ
		И ВидПродукции <> Перечисления.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха Тогда
		ТребуетсяИндивидуализацияКИЗ = Ложь;
	КонецЕсли;
	
	ИнтеграцияИСПереопределяемый.ПередЗаписьюОбъекта(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("ЭтоНовый") Или Не ДополнительныеСвойства.ЭтоНовый Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Документ", Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Статусы.Статус КАК Статус
		|ИЗ
		|	РегистрСведений.СтатусыДокументовИСМП КАК Статусы
		|ГДЕ
		|	Статусы.Документ = &Документ";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если Выборка.Статус = Перечисления.СтатусыОбработкиПеремаркировкиТоваровИСМП.Черновик Тогда
				ДополнительныеСвойства.Вставить("ТребуетсяПерезаписьСтатусаПоУмолчанию", Истина);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбменДаннымиИСМП.ЗаписатьСтатусДокументаПоУмолчанию(ЭтотОбъект);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	НепроверяемыеРеквизиты.Добавить("Товары.Номенклатура");
	НепроверяемыеРеквизиты.Добавить("Товары.Характеристика");
	НепроверяемыеРеквизиты.Добавить("Товары.КодМаркировки");
	
	НепроверяемыеРеквизиты.Добавить("Товары.НоваяХарактеристика");
	НепроверяемыеРеквизиты.Добавить("Товары.НовыйКодМаркировки");
	НепроверяемыеРеквизиты.Добавить("Товары.КодТНВЭД");
	
	ШаблонСообщения = НСтр("ru = 'Не заполнена колонка ""%1"" в строке %2 списка ""Товары""';
							|en = 'Не заполнена колонка ""%1"" в строке %2 списка ""Товары""'");
	
	ТаблицаТовары = Товары.Выгрузить();
	ТаблицаТовары.Колонки.Добавить("ХарактеристикиИспользуются",      Новый ОписаниеТипов("Булево"));
	ТаблицаТовары.Колонки.Добавить("НоваяХарактеристикаИспользуется", Новый ОписаниеТипов("Булево"));

	ИнтеграцияИСПереопределяемый.ЗаполнитьСлужебныеРеквизитыВКоллекции(Неопределено, ТаблицаТовары);
	
	ДанныеКэша = Новый Структура;
	ДанныеКэша.Вставить("ДанныеКодовМаркировки", Новый Соответствие);
	Документы.ПеремаркировкаТоваровИСМП.ОбновитьДанныеКэшаКодовМаркировки(ЭтотОбъект, ДанныеКэша);
	
	Для Каждого СтрокаТовары Из ТаблицаТовары Цикл
		
		НомерСтроки = СтрокаТовары.НомерСтроки;
		
		ОшибкаЗаполненияСтарогоКода = Ложь;
		
		Если Не ЗначениеЗаполнено(СтрокаТовары.КодМаркировки) Тогда
			Если СтрокаТовары.ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировкиТоваровИСМП.ОшибкиОписанияТовара Тогда
				ОшибкаЗаполненияСтарогоКода = Истина;
			ИначеЕсли ЗначениеЗаполнено(СтрокаТовары.НовыйКодМаркировки) Тогда
				ДанныеКодаМаркировки = ДанныеКэша.ДанныеКодовМаркировки[СтрокаТовары.НовыйКодМаркировки];
				Если ДанныеКодаМаркировки <> Неопределено
					И (ДанныеКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Набор
						Или ДанныеКодаМаркировки.ВидУпаковки = Перечисления.ВидыУпаковокИС.Групповая) Тогда
					ОшибкаЗаполненияСтарогоКода = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
			
		Если ОшибкаЗаполненияСтарогоКода Тогда
			
			ТекстОшибки = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Код маркировки (старый)';
															|en = 'Код маркировки (старый)'"), НомерСтроки);
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					"Товары", НомерСтроки, "КодМаркировки"),,
				Отказ);
			
			Если Не ЗначениеЗаполнено(СтрокаТовары.Номенклатура) Тогда
				ТекстОшибки = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Номенклатура (старая)';
																|en = 'Номенклатура (старая)'"), НомерСтроки);
				
				ОбщегоНазначения.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
						"Товары", НомерСтроки, "Номенклатура"),,
					Отказ);
			КонецЕсли;
			
			Если СтрокаТовары.ХарактеристикиИспользуются
				И Не ЗначениеЗаполнено(СтрокаТовары.Характеристика) Тогда
				ТекстОшибки = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Характеристика (старая)';
																|en = 'Характеристика (старая)'"), НомерСтроки);
				
				ОбщегоНазначения.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
						"Товары", НомерСтроки, "Характеристика"),,
					Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
		Если СтрокаТовары.НоваяХарактеристикаИспользуется
			И Не ЗначениеЗаполнено(СтрокаТовары.НоваяХарактеристика) Тогда
			ТекстОшибки = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Характеристика (новая)';
															|en = 'Характеристика (новая)'"), НомерСтроки);
			
			ОбщегоНазначения.СообщитьПользователю(
				ТекстОшибки,
				ЭтотОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
					"Товары", НомерСтроки, "НоваяХарактеристика"),,
				Отказ);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТовары.КодТНВЭД)
			И Не ЗначениеЗаполнено(СтрокаТовары.КодМаркировки)
			И ЗначениеЗаполнено(СтрокаТовары.НовыйКодМаркировки) Тогда
			ДанныеКодаМаркировки = ДанныеКэша.ДанныеКодовМаркировки[СтрокаТовары.НовыйКодМаркировки];
			Если ДанныеКодаМаркировки = Неопределено
				Или ДанныеКодаМаркировки.ВидУпаковки <> Перечисления.ВидыУпаковокИС.Набор
					И ДанныеКодаМаркировки.ВидУпаковки <> Перечисления.ВидыУпаковокИС.Групповая Тогда
				ТекстОшибки = СтрШаблон(ШаблонСообщения, НСтр("ru = 'Код ТН ВЭД';
																|en = 'Код ТН ВЭД'"), НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
						"Товары", НомерСтроки, "КодТНВЭД"),,
					Отказ);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ИнтеграцияИСМППереопределяемый.ПриОпределенииОбработкиПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДокументОснование   = Неопределено;
	ИдентификаторЗаявки = Неопределено;
	Товары.Очистить();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаЗаполнения

Процедура ЗаполнитьОбъектПоСтатистике()
	
	ДанныеСтатистики = ЗаполнениеОбъектовПоСтатистикеИСМП.ДанныеЗаполненияПеремаркировкиТоваровИСМП(Организация);
	
	Для Каждого КлючИЗначение Из ДанныеСтатистики Цикл
		ЗаполнениеОбъектовПоСтатистикеИСМП.ЗаполнитьПустойРеквизит(ЭтотОбъект, ДанныеСтатистики, КлючИЗначение.Ключ);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьДоступныеПричиныПеремаркировки()
	
	Если ИнтеграцияИСМПКлиентСервер.ИспользуетсяОграниченныйСписокПричинПеремаркировки(ВидПродукции) Тогда
		Для Каждого Строка Из Товары Цикл
			Если Строка.ПричинаПеремаркировки <> Перечисления.ПричиныПеремаркировкиТоваровИСМП.ОшибкиОписанияТовара
				И Строка.ПричинаПеремаркировки <> Перечисления.ПричиныПеремаркировкиТоваровИСМП.ИспорченоУтрачено Тогда
				Строка.ПричинаПеремаркировки = Перечисления.ПричиныПеремаркировкиТоваровИСМП.ИспорченоУтрачено;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли