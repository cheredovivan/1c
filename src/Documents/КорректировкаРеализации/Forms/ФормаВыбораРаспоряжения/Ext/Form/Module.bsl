
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем ЗначениеОтбора;
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	ЗаполнитьЗначенияСвойств(СвойстваСписка, Список);
	СвойстваСписка.ТекстЗапроса = ТекстЗапросаСписок();
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	ЭтоПереходПраваСобственности2_5 = Ложь;
	
	Если Параметры.Отбор.Свойство("ХозяйственнаяОперация") Тогда
		
		Если ЗначениеЗаполнено(Параметры.Отбор.ХозяйственнаяОперация) Тогда
			
			ХозяйственнаяОперацияПараметр = Параметры.Отбор.ХозяйственнаяОперация;
			
			Если Параметры.Отбор.ХозяйственнаяОперация =
				Перечисления.ХозяйственныеОперации.ПереходПраваСобственности Тогда
				
				ХозяйственнаяОперацияПараметр = Перечисления.ХозяйственныеОперации.РеализацияКлиенту;
				ЭтоПереходПраваСобственности2_5 = Истина;
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"ХозяйственнаяОперация",
				ХозяйственнаяОперацияПараметр,
				,,Истина);
			
		Иначе
			
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Список,
				"ХозяйственнаяОперация",
				Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию,
				ВидСравненияКомпоновкиДанных.НеРавно,, Истина);
			
		КонецЕсли;
		
		Параметры.Отбор.Удалить("ХозяйственнаяОперация");
		
	КонецЕсли;
	
	МассивОтборов = Новый Массив;
	МассивОтборов.Добавить("Организация");
	МассивОтборов.Добавить("Валюта");
	МассивОтборов.Добавить("Контрагент");
	МассивОтборов.Добавить("Договор");
	МассивОтборов.Добавить("НалогообложениеНДС");
	МассивОтборов.Добавить("Партнер");
	МассивОтборов.Добавить("Соглашение");
	МассивОтборов.Добавить("ЦенаВключаетНДС");
	
	Для каждого ИмяОтбора Из МассивОтборов Цикл
		Если Параметры.Отбор.Свойство(ИмяОтбора, ЗначениеОтбора) Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
				Список,
				ИмяОтбора,
				ЗначениеОтбора);
			
			Параметры.Отбор.Удалить(ИмяОтбора);
			
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"Склад",
		Параметры.Склад);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"ДокументОснование",
		Параметры.ДокументОснование);
		
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"ИспользоватьСоглашенияСКлиентами",
		ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами"));
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список,
		"ЭтоПереходПраваСобственности2_5",
		ЭтоПереходПраваСобственности2_5);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	ОповеститьОВыборе(ТекущаяСтрока.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьРаспоряжение(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		ОповеститьОВыборе(ТекущаяСтрока.Ссылка);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокумент(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		ПоказатьЗначение(Неопределено, ТекущаяСтрока.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ТекстЗапросаСписок()
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаЗаказы.ЗаказКлиента КАК Ссылка,
	|	ТИПЗНАЧЕНИЯ(ТаблицаЗаказы.ЗаказКлиента) КАК ТипРаспоряжения,
	|	ТаблицаЗаказы.ЗаказКлиента.Дата КАК Дата,
	|	ТаблицаЗаказы.ЗаказКлиента.Номер КАК Номер,
	|	ТаблицаЗаказы.ЗаказКлиента.Партнер КАК Партнер,
	|	ТаблицаЗаказы.ЗаказКлиента.Контрагент КАК Контрагент,
	|	ТаблицаЗаказы.ЗаказКлиента.Договор КАК Договор,
	|	ТаблицаЗаказы.ЗаказКлиента.Соглашение КАК Соглашение,
	|	ТаблицаЗаказы.ЗаказКлиента.Организация КАК Организация,
	|	ТаблицаЗаказы.ЗаказКлиента.Склад КАК Склад,
	|	ТаблицаЗаказы.ЗаказКлиента.Валюта КАК Валюта,
	|	ТаблицаЗаказы.ЗаказКлиента.Менеджер КАК Менеджер,
	|	ТаблицаЗаказы.ЗаказКлиента.Статус КАК Статус,
	|	ТаблицаЗаказы.ЗаказКлиента.СуммаДокумента КАК СуммаДокумента,
	|	ТаблицаЗаказы.ЗаказКлиента.Приоритет КАК Приоритет,
	|	ТаблицаЗаказы.ЗаказКлиента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаЗаказы.ЗаказКлиента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаЗаказы.ЗаказКлиента ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ИСТИНА
	|		КОГДА ТаблицаЗаказы.ЗаказКлиента ССЫЛКА Документ.ПервичныйДокумент
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ
	|			ТаблицаЗаказы.ЗаказКлиента.ЦенаВключаетНДС
	|	КОНЕЦ КАК ЦенаВключаетНДС,
	|	ТаблицаЗаказы.ЗаказКлиента.Комментарий КАК Комментарий,
	|	ВЫБОР
	|		КОГДА ТаблицаЗаказы.ЗаказКлиента.Приоритет В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания)
	|			ТОГДА 0
	|		КОГДА ТаблицаЗаказы.ЗаказКлиента.Приоритет В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания УБЫВ)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КартинкаПриоритета
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаЗаказы.ЗаказКлиента КАК ЗаказКлиента,
	|		СУММА(ТаблицаЗаказы.КОформлению) КАК КОформлению
	|	ИЗ
	|		(ВЫБРАТЬ
	|			РаспоряженияОбороты.Распоряжение КАК ЗаказКлиента,
	|			ВЫБОР
	|				КОГДА &ЭтоПереходПраваСобственности2_5
	|					ТОГДА РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|							+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|							- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|							- РаспоряженияОбороты.ПринятоКоличествоОборот
	|							- РаспоряженияОбороты.ОформленоКоличествоОборот
	|				ИНАЧЕ
	|					РаспоряженияОбороты.КОтгрузкеКоличествоОборот
	|						- РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|			КОНЕЦ КАК КОформлению
	|		ИЗ
	|			#РаспоряженияНаОтгрузкуОборотыРазвернутая1 КАК РаспоряженияОбороты
	|		ГДЕ
	|			ВЫБОР
	|				КОГДА &ЭтоПереходПраваСобственности2_5
	|					ТОГДА РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|							+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|							- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|							- РаспоряженияОбороты.ПринятоКоличествоОборот
	|							- РаспоряженияОбороты.ОформленоКоличествоОборот
	|				ИНАЧЕ
	|					РаспоряженияОбороты.КОтгрузкеКоличествоОборот
	|						- РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|			КОНЕЦ <> 0
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			РаспоряженияОбороты.Распоряжение КАК ЗаказКлиента,
	|			ВЫБОР
	|				КОГДА &ЭтоПереходПраваСобственности2_5
	|					ТОГДА РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|							+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|							- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|							- РаспоряженияОбороты.ПринятоКоличествоОборот
	|							- РаспоряженияОбороты.ОформленоКоличествоОборот
	|				ИНАЧЕ
	|					РаспоряженияОбороты.КОтгрузкеКоличествоОборот
	|						- РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|			КОНЕЦ КАК КОформлению
	|		ИЗ
	|			#РаспоряженияНаОтгрузкуОборотыРазвернутая2 КАК РаспоряженияОбороты
	|		ГДЕ
	|			ВЫБОР
	|				КОГДА &ЭтоПереходПраваСобственности2_5
	|					ТОГДА РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|							+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|							- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|							- РаспоряженияОбороты.ПринятоКоличествоОборот
	|							- РаспоряженияОбороты.ОформленоКоличествоОборот
	|				ИНАЧЕ
	|					РаспоряженияОбороты.КОтгрузкеКоличествоОборот
	|						- РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|			КОНЕЦ <> 0
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	//++ НЕ УТКА
	|		ВЫБРАТЬ
	|			РаспоряженияОбороты.Распоряжение КАК ЗаказКлиента,
	|			(РаспоряженияОбороты.КОформлениюКоличествоОборот -
	|				РаспоряженияОбороты.ОформленоКоличествоОборот) КАК КОформлению
	|		ИЗ
	|			#РаспоряженияНаОтгрузкуОборотыРазвернутая3 КАК РаспоряженияОбороты
	|		ГДЕ
	|			(РаспоряженияОбороты.КОформлениюКоличествоОборот -
	|				РаспоряженияОбороты.ОформленоКоличествоОборот) <> 0
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	//-- НЕ УТКА
	|
	|		ВЫБРАТЬ
	|			РаспоряженияОбороты.Распоряжение КАК ЗаказКлиента,
	|			РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|				+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|				- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|				- РаспоряженияОбороты.ПринятоКоличествоОборот
	|				- РаспоряженияОбороты.ОформленоКоличествоОборот КАК КОформлению
	|		ИЗ
	|			#РаспоряженияНаОтгрузкуОборотыРазвернутая4 КАК РаспоряженияОбороты
	|		ГДЕ
	|			РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|				+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|				- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|				- РаспоряженияОбороты.ПринятоКоличествоОборот
	|				- РаспоряженияОбороты.ОформленоКоличествоОборот <> 0
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			РаспоряженияОбороты.Распоряжение КАК ЗаказКлиента,
	|			РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|				+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|				- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|				- РаспоряженияОбороты.ПринятоКоличествоОборот
	|				- РаспоряженияОбороты.ОформленоКоличествоОборот КАК КОформлению
	|		ИЗ
	|			#РаспоряженияНаОтгрузкуОборотыРазвернутая5 КАК РаспоряженияОбороты
	|		ГДЕ
	|			РаспоряженияОбороты.ОтгруженоКоличествоОборот
	|				+ РаспоряженияОбороты.ОприходованоДоППСКоличествоОборот
	|				- РаспоряженияОбороты.СписаноДоППСКоличествоОборот
	|				- РаспоряженияОбороты.ПринятоКоличествоОборот
	|				- РаспоряженияОбороты.ОформленоКоличествоОборот <> 0
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			РаспоряженияДвижения.Распоряжение,
	|			РаспоряженияДвижения.КОформлениюКоличество + РаспоряженияДвижения.ОформленоКоличество
	|		ИЗ
	|			#РаспоряженияНаОтгрузкуДвиженияРазвернутая КАК РаспоряженияДвижения) КАК ТаблицаЗаказы
	|
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаЗаказы.ЗаказКлиента) КАК ТаблицаЗаказы
	|ГДЕ
	|	ТаблицаЗаказы.КОформлению > 0";
	
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ЗаказКлиента)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Организация = &Организация
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Валюта = &Валюта
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Контрагент = &Контрагент
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Договор = &Договор
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).НалогообложениеНДС В (&НалогообложениеНДС)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Партнер = &Партнер
		|	И ВЫБОР
		|		КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ
		|			ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).Соглашение = &Соглашение
		|	КОНЕЦ
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказКлиента).ЦенаВключаетНДС = &ЦенаВключаетНДС
		|	И (Склад = &Склад
		|		ИЛИ Склад В ИЕРАРХИИ (&Склад)
		|		ИЛИ Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|		ИЛИ &ЭтоПереходПраваСобственности2_5)";
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение";
	ПараметрыФормированияТаблицы.Показатели =
		"КОтгрузке, Отгружено, ОприходованоДоППС, СписаноДоППС, Принято, Оформлено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая1 =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая1",
		РаспоряженияНаОтгрузкуОборотыРазвернутая1);
	
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Организация = &Организация
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Валюта = &Валюта
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Контрагент = &Контрагент
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Договор = &Договор
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).НалогообложениеНДС В (&НалогообложениеНДС)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Партнер = &Партнер
		|	И ВЫБОР
		|		КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Соглашение = &Соглашение
		|	КОНЕЦ
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ЦенаВключаетНДС = &ЦенаВключаетНДС
		|	И (Склад = &Склад
		|		ИЛИ Склад В ИЕРАРХИИ (&Склад)
		|		ИЛИ Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|		ИЛИ &ЭтоПереходПраваСобственности2_5)";
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение";
	ПараметрыФормированияТаблицы.Показатели =
		"КОтгрузке, Отгружено, ОприходованоДоППС, СписаноДоППС, Принято, Оформлено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая2 =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая2",
		РаспоряженияНаОтгрузкуОборотыРазвернутая2);
	
	//++ НЕ УТКА
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ЗаказДавальца2_5)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказДавальца2_5).Организация = &Организация
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказДавальца2_5).Валюта = &Валюта
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказДавальца2_5).Контрагент = &Контрагент
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказДавальца2_5).Договор = &Договор
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказДавальца2_5).НалогообложениеНДС В (&НалогообложениеНДС)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказДавальца2_5).Партнер = &Партнер
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ЗаказДавальца2_5).ЦенаВключаетНДС = &ЦенаВключаетНДС
		|	И (Склад = &Склад
		|		ИЛИ Склад В ИЕРАРХИИ (&Склад)
		|		ИЛИ Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))";
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение";
	ПараметрыФормированияТаблицы.Показатели = "КОформлению, Оформлено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая3 =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая3",
		РаспоряженияНаОтгрузкуОборотыРазвернутая3);
	//-- НЕ УТКА
	
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ОтгрузкаТоваровКлиенту)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Организация = &Организация
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Валюта = &Валюта
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Контрагент = &Контрагент
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Договор = &Договор
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).НалогообложениеНДС В (&НалогообложениеНДС)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Партнер = &Партнер
		|	И ВЫБОР
		|		КОГДА НЕ &ИспользоватьСоглашенияСКлиентами
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ВЫРАЗИТЬ(Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Соглашение = &Соглашение
		|	КОНЕЦ
		|	И &ЦенаВключаетНДС = ИСТИНА";
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение";
	ПараметрыФормированияТаблицы.Показатели =
		"КОтгрузке, Отгружено, ОприходованоДоППС, СписаноДоППС, Принято, Оформлено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая4 =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая4",
		РаспоряженияНаОтгрузкуОборотыРазвернутая4);

	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ПервичныйДокумент)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ПервичныйДокумент).Организация = &Организация
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ПервичныйДокумент).Валюта = &Валюта
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ПервичныйДокумент).Контрагент = &Контрагент
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ПервичныйДокумент).Договор = &Договор
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ПервичныйДокумент).НалогообложениеНДС В (&НалогообложениеНДС)
		|	И ВЫРАЗИТЬ(Распоряжение КАК Документ.ПервичныйДокумент).Партнер = &Партнер";
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение";
	ПараметрыФормированияТаблицы.Показатели =
		"КОтгрузке, Отгружено, ОприходованоДоППС, СписаноДоППС, Принято, Оформлено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая5 =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая5",
		РаспоряженияНаОтгрузкуОборотыРазвернутая5);

	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"(Регистратор = &ДокументОснование
		|		ИЛИ Регистратор В
		|			(ВЫБРАТЬ
		|					Корректировки.Ссылка
		|				ИЗ
		|					Документ.КорректировкаРеализации КАК Корректировки
		|				ГДЕ
		|					Корректировки.ДокументОснование = &ДокументОснование
		|					И Корректировки.Проведен))
		|	И Активность
		|	И (ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ЗаказКлиента)
		//++ НЕ УТКА
		|		ИЛИ ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ЗаказДавальца2_5)
		//-- НЕ УТКА
		|		ИЛИ ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента))";
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение";
	ПараметрыФормированияТаблицы.Показатели = "КОформлению, Оформлено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	ПараметрыФормированияТаблицы.ФизическаяТаблица = Истина;
	
	РаспоряженияНаОтгрузкуДвиженияРазвернутая =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуДвиженияРазвернутая",
		РаспоряженияНаОтгрузкуДвиженияРазвернутая);

	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти