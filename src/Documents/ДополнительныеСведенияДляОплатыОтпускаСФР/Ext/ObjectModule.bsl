#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	ХранилищеXML				= Неопределено;
	ДатаОтправки				= '00010101';
	ИдентификаторСообщения		= "";
	ОтключитьПроверкиПроведения = Ложь;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	СсылкаОснования = ДанныеЗаполнения;
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		СсылкаОснования = ОбщегоНазначенияБЗК.ЗначениеСвойства(ДанныеЗаполнения, "Ссылка");
	КонецЕсли;
	
	Если ТипЗнч(СсылкаОснования) = Тип("ДокументСсылка.СведенияДляОплатыОтпускаСФР") Тогда
		ДокументОснование = СсылкаОснования;
	КонецЕсли;
	
	ЗначенияПоУмолчанию = Новый Структура;
	Если ДанныеЗаполнения = Неопределено Или Не ЗначениеЗаполнено(Ответственный) Тогда
		ЗначенияПоУмолчанию.Вставить("Ответственный");
	КонецЕсли;
	Если ДанныеЗаполнения = Неопределено Или Не ЗначениеЗаполнено(Организация) Тогда
		ЗначенияПоУмолчанию.Вставить("Организация");
	КонецЕсли;
	Если ЗначенияПоУмолчанию.Количество() > 0 Тогда
		ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗначенияПоУмолчанию);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначенияПоУмолчанию);
	КонецЕсли;
	
	ОбновитьВторичныеДанные();
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	ПроверяемыеРеквизиты.Очистить();
	ПроверяемыеРеквизиты.Добавить("Дата");
	ПроверяемыеРеквизиты.Добавить("Организация");
	ПроверяемыеРеквизиты.Добавить("ДокументОснование");
	ПроверяемыеРеквизиты.Добавить("ИдентификаторОснования");
	
	Если Вложения.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не добавлены файлы к отправке в СФР';
													|en = 'Files to send to the Social Insurance Fund are not added'"),,,, Отказ);
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение недостающих полей.
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ДатаСоздания) Тогда
		ДатаСоздания = ТекущаяДата(); // АПК:143 Для фильтрации событий в журнале регистрации требуется дата сервера.
	КонецЕсли;
	
	// Заполнение текста XML.
	ЭтоПроведение = (РежимЗаписи = РежимЗаписиДокумента.Проведение
		Или (Проведен И РежимЗаписи = РежимЗаписиДокумента.Запись));
	Если ЭтоПроведение Тогда
		ТекстXML = Документы.ДополнительныеСведенияДляОплатыОтпускаСФР.ТекстXML(ЭтотОбъект);
	Иначе
		ТекстXML = "";
	КонецЕсли;
	ХранилищеXML = Новый ХранилищеЗначения(ТекстXML, Новый СжатиеДанных(9));
	
	// Чтение реквизитов "До записи" для проверки и подготовки сведений к процедуре ПриЗаписи.
	ЗначенияРеквизитовДоЗаписи = ЗначенияРеквизитовДоЗаписи();
	ДополнительныеСвойства.Вставить("ЗначенияРеквизитовДоЗаписи", ЗначенияРеквизитовДоЗаписи);
	
	// Проверка отсутствия критичных изменений в отправленном документе.
	Если ОбъектЗафиксирован() Тогда
		Если ЗначениеЗаполнено(ЗначенияРеквизитовДоЗаписи.ФизическоеЛицо)
			И ЗначенияРеквизитовДоЗаписи.ФизическоеЛицо <> ФизическоеЛицо Тогда
			ВызватьИсключение НСтр("ru = 'Недопустимо изменять физическое лицо в отправленном документе.';
									|en = 'You cannot change an individual in the sent document.'");
		КонецЕсли;
		Если ЗначениеЗаполнено(ЗначенияРеквизитовДоЗаписи.Страхователь)
			И ЗначенияРеквизитовДоЗаписи.Страхователь <> Страхователь Тогда
			ВызватьИсключение НСтр("ru = 'Недопустимо изменять страхователя в отправленном документе.';
									|en = 'Cannot change an insurant in the sent document.'");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	РегистрыСведений.РегистрацииСведенийДляОплатыОтпускаСФР.ЗаполнитьПоДокументу(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПередЗаписью

Функция ЗначенияРеквизитовДоЗаписи()
	ИменаРеквизитов = "Дата, ГоловнаяОрганизация, Страхователь, ФизическоеЛицо, ПометкаУдаления, Проведен";
	ЭтоНовый = ЭтоНовый();
	Если ЭтоНовый Тогда
		Значения = ОбщегоНазначенияБЗК.ЗначенияСвойств(ЭтотОбъект, ИменаРеквизитов);
	Иначе
		Значения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов);
	КонецЕсли;
	Значения.Вставить("ЭтоНовый", ЭтоНовый);
	Возврат Значения;
КонецФункции

#КонецОбласти

#Область ФиксацияВторичныхДанныхВДокументах

Функция Менеджер()
	Возврат Документы.ДополнительныеСведенияДляОплатыОтпускаСФР;
КонецФункции

Функция ОбъектЗафиксирован() Экспорт
	Возврат Менеджер().ОбъектЗафиксирован(ЭтотОбъект);
КонецФункции

Функция ОбновитьВторичныеДанные(ПараметрыФиксации = Неопределено) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Модифицирован = Ложь;
	
	Если ОбъектЗафиксирован() Тогда
		Возврат Модифицирован;
	КонецЕсли;
	
	Если ПараметрыФиксации = Неопределено Тогда
		ПараметрыФиксации = Менеджер().ПараметрыФиксацииВторичныхДанных(ЭтотОбъект);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ЗаполнитьПоДокументуОснованию(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ЗаполнитьДанныеОрганизации(ПараметрыФиксации) Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Возврат Модифицирован;
КонецФункции

Функция ЗаполнитьПоДокументуОснованию(ПараметрыФиксации)
	Если Не ЗначениеЗаполнено(ДокументОснование) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Реквизиты = Новый Структура("Организация,Сотрудник,ФизическоеЛицо,ИдентификаторОснования");
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияДляОплатыОтпускаСФР.Организация КАК Организация,
		|	СведенияДляОплатыОтпускаСФР.Сотрудник КАК Сотрудник,
		|	СведенияДляОплатыОтпускаСФР.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияДляОплатыОтпускаСФР.ИдентификаторСообщения КАК ИдентификаторОснования
		|ИЗ
		|	Документ.СведенияДляОплатыОтпускаСФР КАК СведенияДляОплатыОтпускаСФР
		|ГДЕ
		|	СведенияДляОплатыОтпускаСФР.Ссылка = &ДокументОснование";
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Реквизиты, Выборка);
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

Функция ЗаполнитьДанныеОрганизации(ПараметрыФиксации)
	// Головная организация заполняется безусловно, т.к. определяет права.
	ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Организация);
	
	Реквизиты = Новый Структура("Страхователь");
	Если ФиксацияВторичныхДанныхВДокументах.РеквизитыШапкиЗафиксированы(ЭтотОбъект, Реквизиты) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Сведения = СЭДОФСС.СведенияОСтрахователе(Организация, "", Дата);
		Реквизиты.Страхователь = Сведения.Страхователь;
	КонецЕсли;
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(Реквизиты, ЭтотОбъект, ПараметрыФиксации);
КонецФункции

#КонецОбласти

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли