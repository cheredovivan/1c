
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

 Процедура ПриКопировании(ОбъектКопирования)
	
	Автор = Пользователи.ТекущийПользователь();
	Комментарий = "";
	ДокументОснование = Неопределено;
	РасчетныйДокумент = Неопределено;
	ИдентификаторФинЗаписи = "";
	НовыйГрафик.Очистить();
	Валюта = Справочники.Валюты.ПустаяСсылка();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ОбщегоНазначения.ЗначениеСсылочногоТипа(ДанныеЗаполнения) Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.КорректировкаГрафикаВзаиморасчетов") Тогда
			ИменаРеквизитов = "Организация,Контрагент,РасчетныйДокумент, ОрганизацияПолучатель, ХозяйственнаяОперация";
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения,ИменаРеквизитов);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыОснования);
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			ИменаРеквизитов = "Организация,Контрагент,ОрганизацияПолучатель";
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения,ИменаРеквизитов);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыОснования);
			РасчетныйДокумент = ДанныеЗаполнения;
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациямиОСписании") Тогда
			ИменаРеквизитов = "Организация,Контрагент,Комиссионер";
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения,ИменаРеквизитов);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыОснования);
			ОрганизацияПолучатель = РеквизитыОснования.Комиссионер;
			РасчетныйДокумент = ДанныеЗаполнения;
		
		//++ НЕ УТКА
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетДавальцуМеждуОрганизациями") Тогда
			ИменаРеквизитов = "Организация,Давалец";
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения,ИменаРеквизитов);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыОснования);
			ОрганизацияПолучатель = РеквизитыОснования.Давалец;
			РасчетныйДокумент = ДанныеЗаполнения;
		//-- НЕ УТКА
		
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Бронирование") Тогда
			ИменаРеквизитов = "Организация,КонтрагентПеревозчик";
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения,ИменаРеквизитов);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыОснования);
			Контрагент = РеквизитыОснования.КонтрагентПеревозчик;
			РасчетныйДокумент = ДанныеЗаполнения;
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РеализацияТоваровУслуг")
			Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
			ИменаРеквизитов = "Организация,Контрагент,Договор,КлиентКонтрагент";
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения,ИменаРеквизитов);
			ВестиРасчетыЧерезКонечныхПокупателей = Ложь;
			Если ЗначениеЗаполнено(РеквизитыОснования.Договор) Тогда
				ВестиРасчетыЧерезКонечныхПокупателей = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыОснования.Договор, "ВестиРасчетыЧерезКонечныхПокупателей");
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыОснования);
			Если ВестиРасчетыЧерезКонечныхПокупателей Тогда
				Контрагент = РеквизитыОснования.КлиентКонтрагент;
			КонецЕсли;
			РасчетныйДокумент = ДанныеЗаполнения;
		Иначе
			ИменаРеквизитов = "Организация,Контрагент";
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения,ИменаРеквизитов);
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыОснования);
			РасчетныйДокумент = ДанныеЗаполнения;
		КонецЕсли;
		
		Если Не (ДополнительныеСвойства.Свойство("ВыборРасчетногоДокумента")
			И ДополнительныеСвойства.ВыборРасчетногоДокумента) Тогда
			ДокументОснование = ДанныеЗаполнения;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("РасчетныйДокумент", ДокументОснование);
		Запрос.УстановитьПараметр("РасчетныйДокументКорректировки", РасчетныйДокумент);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		ОтборПоПервичномуДокументу = ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПервичныйДокумент");
		ТекстыЗапроса = Новый Массив;
		ТекстыЗапроса.Добавить(Документы.КорректировкаГрафикаВзаиморасчетов.ТекстЗапросаДанныхЗаполнения(ОтборПоПервичномуДокументу));
		ТекстыЗапроса.Добавить(Документы.КорректировкаГрафикаВзаиморасчетов.ТекстЗапросаДокументовНаОсновании());
		Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());;
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		ВыборкаПоХозОперации = РезультатЗапроса[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ВведенныеДокументы = РезультатЗапроса[3].Выгрузить();
		СообщенияОбОшибках = Новый Массив;
		ВалютыВзаиморасчетов = Новый Массив;
		ШаблонСообщения = НСтр("ru = 'На основании текущего документа уже введена %1.';
								|en = '%1 is already entered based on the current document.'");
		Пока ВыборкаПоХозОперации.Следующий() Цикл
			
			// При вводе на основании предыдущей корректировки необходимо заполнять такую-же операцию.
			Если ЗначениеЗаполнено(ХозяйственнаяОперация) 
				И ХозяйственнаяОперация <> ВыборкаПоХозОперации.ХозяйственнаяОперация Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокиВведенныхКорректировок = ВведенныеДокументы.НайтиСтроки(Новый Структура("ХозяйственнаяОперация", ВыборкаПоХозОперации.ХозяйственнаяОперация));
			СтрокаДокумента = Документы.КорректировкаГрафикаВзаиморасчетов.ПоследняяКорректировка(СтрокиВведенныхКорректировок);
			Если СтрокаДокумента <> Неопределено
				И СтрокаДокумента.Ссылка <> ДокументОснование Тогда
				СообщенияОбОшибках.Добавить(
					Новый Структура("ТекстСообщения, Ссылка",
						СтрШаблон(ШаблонСообщения, СтрокаДокумента.Ссылка),
						СтрокаДокумента.Ссылка));
				Продолжить;
			КонецЕсли;
			
			ХозяйственнаяОперация = ВыборкаПоХозОперации.ХозяйственнаяОперация;
			
			Выборка = ВыборкаПоХозОперации.Выбрать();
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = НовыйГрафик.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				Если ЗначениеЗаполнено(Выборка.Валюта) Тогда
					ВалютыВзаиморасчетов.Добавить(Выборка.Валюта);
				КонецЕсли;
				Если Не ЗначениеЗаполнено(НоваяСтрока.Дата) Тогда
					НоваяСтрока.Дата = ТекущаяДатаСеанса();
				КонецЕсли;
			КонецЦикла;
			
			Прервать;
			
		КонецЦикла;
		
		СуммаДокумента = НовыйГрафик.Итог("Сумма");
		ВалютыВзаиморасчетов = ОбщегоНазначенияКлиентсервер.СвернутьМассив(ВалютыВзаиморасчетов);
		Если ВалютыВзаиморасчетов.Количество() = 1 Тогда
			Валюта = ВалютыВзаиморасчетов[0];
		ИначеЕсли ВалютыВзаиморасчетов.Количество() > 1 Тогда 
			СуммаДокумента = 0;
		КонецЕсли;
		
		Если НовыйГрафик.Количество() = 0 Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не требуется вводить Корректировку графика взаиморасчетов на основании документа %1.';
					|en = 'Generating AR/AP accounting schedule adjustment from the %1 base document is not required.'"),
				ДокументОснование);
			Для Каждого СообщениеОбОшибке Из СообщенияОбОшибках Цикл
				ОбщегоНазначения.СообщитьПользователю(СообщениеОбОшибке.ТекстСообщения, СообщениеОбОшибке.Ссылка);
			КонецЦикла;
			ВызватьИсключение Текст;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(ОрганизацияПолучатель) Тогда
		ПроверяемыеРеквизиты.Удалить(
			ПроверяемыеРеквизиты.Найти(
				Метаданные.Документы.КорректировкаГрафикаВзаиморасчетов.Реквизиты.Контрагент.Имя));
	Иначе
		ПроверяемыеРеквизиты.Удалить(
			ПроверяемыеРеквизиты.Найти(
				Метаданные.Документы.КорректировкаГрафикаВзаиморасчетов.Реквизиты.ОрганизацияПолучатель.Имя));
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СсылкаДляПроверкиДат", РасчетныйДокумент);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("РасчетныйДокумент", ДокументОснование);
	Запрос.УстановитьПараметр("РасчетныйДокументКорректировки", РасчетныйДокумент);
	
	// Проверка других корректировок
	Запрос.Текст = Документы.КорректировкаГрафикаВзаиморасчетов.ТекстЗапросаДокументовНаОсновании();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ХозяйственнаяОперация = ХозяйственнаяОперация 
			И Выборка.Ссылка <> Ссылка
			И Выборка.ДокументОснование = ДокументОснование Тогда
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'На основании %1 уже введена %2 %3.';
					|en = '%2 %3 is already generated based on %1.'"),
				ДокументОснование,
				ХозяйственнаяОперация,
				Выборка.Ссылка);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Выборка.Ссылка,, "Объект.ХозяйственнаяОперация", Отказ);
		КонецЕсли;
	КонецЦикла;
	
	// Проверка неизменности суммы документа по ключевым полям
	КлючевыеПоля = "Партнер, ОбъектРасчетов, Валюта, Договор, НаправлениеДеятельности";
	ТаблицаРасхождений = НовыйГрафик.ВыгрузитьКолонки(КлючевыеПоля);
	ТаблицаРасхождений.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	ОтборПоПервичномуДокументу = ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПервичныйДокумент");
	Запрос.Текст = Документы.КорректировкаГрафикаВзаиморасчетов.ТекстЗапросаДанныхЗаполнения(ОтборПоПервичномуДокументу);
	
	ВыборкаПоХозОперации = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоХозОперации.Следующий() Цикл
		Если ВыборкаПоХозОперации.ХозяйственнаяОперация <> ХозяйственнаяОперация Тогда
			Продолжить;
		КонецЕсли;
		Выборка = ВыборкаПоХозОперации.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаРасхождений.Добавить(), Выборка);
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого СтрокаИсходнойТаблицы Из ТаблицаРасхождений Цикл
		СтрокаИсходнойТаблицы.Сумма = -СтрокаИсходнойТаблицы.Сумма;
	КонецЦикла;
	Для Каждого СтрокаНовогоГрафика Из НовыйГрафик Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаРасхождений.Добавить(), СтрокаНовогоГрафика);
	КонецЦикла;
	
	ТаблицаРасхождений.Свернуть(КлючевыеПоля, "Сумма");
	
	Для Каждого СтрокаРасхождений Из ТаблицаРасхождений Цикл
		Если СтрокаРасхождений.Сумма = 0 Тогда
			Продолжить;
		КонецЕсли;
		Сумма = ?(СтрокаРасхождений.Сумма > 0, СтрокаРасхождений.Сумма, -СтрокаРасхождений.Сумма);
		ШаблонПоля = "%1: %2;";
		МассивПолей = Новый Массив;
		РеквизитыТЧ = Метаданные.Документы.КорректировкаГрафикаВзаиморасчетов.ТабличныеЧасти.НовыйГрафик.Реквизиты;
		МассивПолей.Добавить(СтрШаблон(ШаблонПоля, РеквизитыТЧ.Партнер.Представление(), СтрокаРасхождений.Партнер));
		МассивПолей.Добавить(СтрШаблон(ШаблонПоля, РеквизитыТЧ.ОбъектРасчетов.Представление(), СтрокаРасхождений.ОбъектРасчетов));
		Если ЗначениеЗаполнено(СтрокаРасхождений.Договор) Тогда
			МассивПолей.Добавить(СтрШаблон(ШаблонПоля, РеквизитыТЧ.Договор.Представление(), СтрокаРасхождений.Договор));
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаРасхождений.НаправлениеДеятельности) Тогда
			МассивПолей.Добавить(СтрШаблон(ШаблонПоля, РеквизитыТЧ.НаправлениеДеятельности.Представление(), СтрокаРасхождений.НаправлениеДеятельности));
		КонецЕсли;
		
		ШаблонСообщения = НСтр("ru = 'Сумма по ключевым полям:
		                             |%1
		                             |не соответствует исходному графику на %2 %3';
		                             |en = 'Amount by key fields:
		                             |%1
		                             |does not match the original schedule by %2 %3'");
		ТекстСообщения = СтрШаблон(
			ШаблонСообщения,
			СтрСоединить(МассивПолей),
			Формат(Сумма, "ЧДЦ=2; ЧН="),
			СтрокаРасхождений.Валюта);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "",, Отказ);
	КонецЦикла;
	
	// Проверка даты исходного документа по измененным датам графика
	КлючевыеПоля = "Партнер, ОбъектРасчетов, Валюта, Договор, НаправлениеДеятельности, Дата";
	ТаблицаИзменений = НовыйГрафик.ВыгрузитьКолонки(КлючевыеПоля);
	ТаблицаИзменений.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	ВыборкаПоХозОперации.Сбросить();
	Пока ВыборкаПоХозОперации.Следующий() Цикл
		Если ВыборкаПоХозОперации.ХозяйственнаяОперация <> ХозяйственнаяОперация Тогда
			Продолжить;
		КонецЕсли;
		Выборка = ВыборкаПоХозОперации.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаИзменений.Добавить(), Выборка);
		КонецЦикла;
	КонецЦикла;
	Для Каждого СтрокаИсходнойТаблицы Из ТаблицаИзменений Цикл
		СтрокаИсходнойТаблицы.Сумма = -СтрокаИсходнойТаблицы.Сумма;
	КонецЦикла;
	Для Каждого СтрокаНовогоГрафика Из НовыйГрафик Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаИзменений.Добавить(), СтрокаНовогоГрафика);
	КонецЦикла;
	
	ТаблицаИзменений.Свернуть(КлючевыеПоля, "Сумма");
	
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(РеестрДокументов.ДатаДокументаИБ, ДЕНЬ) КАК Дата
		|ИЗ
		|	РегистрСведений.РеестрДокументов КАК РеестрДокументов
		|ГДЕ
		|	РеестрДокументов.Ссылка = &СсылкаДляПроверкиДат
		|	И НЕ РеестрДокументов.ДополнительнаяЗапись";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.Дата) Тогда
		Для Каждого СтрокаИзменений Из ТаблицаИзменений Цикл
			Если СтрокаИзменений.Сумма > 0 Тогда
				ОтборСтрок = Новый Структура(КлючевыеПоля);
				ЗаполнитьЗначенияСвойств(ОтборСтрок, СтрокаИзменений);
				СтрокиНовогоГрафика = НовыйГрафик.НайтиСтроки(ОтборСтрок);
				Для Каждого СтрокаНовогоГрафика Из СтрокиНовогоГрафика Цикл
					Если ЗначениеЗаполнено(СтрокаНовогоГрафика.Дата)
						И НачалоДня(СтрокаНовогоГрафика.Дата) < Выборка.Дата Тогда
						ОбщегоНазначения.СообщитьПользователю(
							НСтр("ru = 'Новая дата не может быть меньше даты исходного документа.';
								|en = 'New date cannot be earlier than the source document date.'"),
							ЭтотОбъект,
							ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("НовыйГрафик", СтрокаНовогоГрафика.НомерСтроки, "Дата"),,
							Отказ);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение
		И Не ЗначениеЗаполнено(ИдентификаторФинЗаписи) Тогда
		ИдентификаторФинЗаписи = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	НовыйГрафик.Сортировать("Дата");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

#КонецОбласти

#КонецОбласти

#КонецЕсли