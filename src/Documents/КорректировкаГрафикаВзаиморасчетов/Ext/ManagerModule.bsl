#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив из Строка - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("Взаиморасчеты");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	
КонецПроцедуры

// Формирует таблицы движений
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура из КлючИЗначение - список имен регистров, для которых необходимо получить таблицы:
//  * Ключ - Строка - Имя таблицы регистра.
//  * Значение - Неопределено
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения:
//  * Ключ - Строка - Имя параметра.
//  * Значение - Произвольный - Значение параметра.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт

	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;

	Если ТипЗнч(Документ) = Тип("ДокументОбъект.КорректировкаГрафикаВзаиморасчетов") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;

	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРасчетыСКлиентами(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции
	
#КонецОбласти

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Добавляет команду создания документа "Приобретение товаров и услуг".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  
// Возвращаемое значение:
//	КомандаФормы - добавляемая команда.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.КорректировкаГрафикаВзаиморасчетов) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.КорректировкаГрафикаВзаиморасчетов.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.КорректировкаГрафикаВзаиморасчетов);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ЗадолженностьКлиентовПоСрокам) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.ЗадолженностьКлиентовПоСрокам.ПолноеИмя();
		КомандаОтчет.Представление = Метаданные.Отчеты.ЗадолженностьКлиентовПоСрокам.Синоним;
		
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Важное";
		КомандаОтчет.КлючВарианта = "ЗадолженностьКлиентовПоСрокамКонтекст";
		
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			КомандаОтчет,
			"ХозяйственнаяОперация",
			Перечисления.ХозяйственныеОперации.КорректировкаГрафикаОплатыКлиентом);
			
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			КомандаОтчет,
			"Контрагент",
			,
			ВидСравненияКомпоновкиДанных.Заполнено);
		
	КонецЕсли;
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ЗадолженностьПоставщикамПоСрокам) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.ЗадолженностьПоставщикамПоСрокам.ПолноеИмя();
		КомандаОтчет.Представление = Метаданные.Отчеты.ЗадолженностьПоставщикамПоСрокам.Синоним;
		
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Важное";
		КомандаОтчет.КлючВарианта = "ЗадолженностьПоставщикамПоСрокамКонтекст";
		
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			КомандаОтчет,
			"ХозяйственнаяОперация",
			Перечисления.ХозяйственныеОперации.КорректировкаГрафикаОплатыПоставщику);
		
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			КомандаОтчет,
			"Контрагент",
			,
			ВидСравненияКомпоновкиДанных.Заполнено);
		
	КонецЕсли;
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ОплатыКлиентовПоСрокам) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.ОплатыКлиентовПоСрокам.ПолноеИмя();
		КомандаОтчет.Представление = Метаданные.Отчеты.ОплатыКлиентовПоСрокам.Синоним;
		
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Важное";
		КомандаОтчет.КлючВарианта = "ОплатыКлиентовПоСрокамКонтекст";
		
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			КомандаОтчет,
			"ХозяйственнаяОперация",
			Перечисления.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаКлиента);
		
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			КомандаОтчет,
			"Контрагент",
			,
			ВидСравненияКомпоновкиДанных.Заполнено);
		
	КонецЕсли;
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ОплатыПоставщикамПоСрокам) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.ОплатыПоставщикамПоСрокам.ПолноеИмя();
		КомандаОтчет.Представление = Метаданные.Отчеты.ОплатыПоставщикамПоСрокам.Синоним;
		
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Важное";
		КомандаОтчет.КлючВарианта = "ОплатыПоставщикамПоСрокамКонтекст";
		
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			КомандаОтчет,
			"ХозяйственнаяОперация",
			Перечисления.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаПоставщику);
		
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			КомандаОтчет,
			"Контрагент",
			,
			ВидСравненияКомпоновкиДанных.Заполнено);
		
	КонецЕсли;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(НовыйГрафик.Партнер)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Получает объекты расчетов из документов корректировки графика взаиморасчетов.
// 
// Параметры:
//  МассивДокументов - Массив из ДокументСсылка.КорректировкаГрафикаВзаиморасчетов
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.ОбъектыРасчетов
Функция ОбъектыРасчетовКорректировок(МассивДокументов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КорректировкаГрафикаВзаиморасчетовНовыйГрафик.ОбъектРасчетов КАК ОбъектРасчетов
		|ИЗ
		|	Документ.КорректировкаГрафикаВзаиморасчетов.НовыйГрафик КАК КорректировкаГрафикаВзаиморасчетовНовыйГрафик
		|ГДЕ
		|	КорректировкаГрафикаВзаиморасчетовНовыйГрафик.Ссылка В(&МассивДокументов)";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ОбъектРасчетов");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)

	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                           КАК Ссылка,
	|	ЕСТЬNULL(РеестрДокументов.ДатаДокументаИБ, ДанныеДокумента.Дата) КАК Период,
	|	ДанныеДокумента.ХозяйственнаяОперация                            КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.ИдентификаторФинЗаписи                           КАК ИдентификаторФинЗаписи
	|ИЗ
	|	Документ.КорректировкаГрафикаВзаиморасчетов КАК ДанныеДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|		ПО РеестрДокументов.Ссылка = ДанныеДокумента.РасчетныйДокумент
	|			И НЕ РеестрДокументов.ДополнительнаяЗапись
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеГрафика.Валюта КАК Валюта
	|ИЗ
	|	Документ.КорректировкаГрафикаВзаиморасчетов.НовыйГрафик КАК ДанныеГрафика
	|ГДЕ
	|	ДанныеГрафика.Ссылка = &Ссылка
	|";

	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	ТаблицаРеквизитов = РезультатыЗапроса[0].Выгрузить();
	Реквизиты = ТаблицаРеквизитов[0];
	Для Каждого Колонка Из ТаблицаРеквизитов.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	Запрос.УстановитьПараметр("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.КорректировкаГрафикаВзаиморасчетов"));
	НастройкаХО = Справочники.НастройкиХозяйственныхОпераций.НайтиПоРеквизиту("ХозяйственнаяОперация", Реквизиты.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("НастройкаХозяйственнойОперации", НастройкаХО);
	Запрос.УстановитьПараметр("ОперацияВзаиморасчетов", Перечисления.ОперацииВзаиморасчетов.КорректировкаЗадолженности);
	Выборка = РезультатыЗапроса[1].Выбрать();
	Валюты = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Валюты.Добавить(Выборка.Валюта);
	КонецЦикла;
	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
	Валюты.Добавить(ВалютаУпр);
	Запрос.УстановитьПараметр("МассивВалют", Валюты);
	Запрос.УстановитьПараметр("ВалютаУпр", ВалютаУпр);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "РеестрДокументов";

	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	ДанныеДокумента.ХозяйственнаяОперация   КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация             КАК Организация,
	|	ДанныеОснования.Партнер                 КАК Партнер,
	|	НЕОПРЕДЕЛЕНО                            КАК МестоХранения,
	|	ДанныеДокумента.Контрагент              КАК Контрагент,
	|	ДанныеОснования.Подразделение           КАК Подразделение,
	|	ДанныеДокумента.Дата                    КАК ДатаДокументаИБ,
	|	ДанныеДокумента.Ссылка                  КАК Ссылка,
	|	ДанныеДокумента.Номер                   КАК НомерДокументаИБ,
	|	НЕОПРЕДЕЛЕНО                            КАК Статус,
	|	ДанныеДокумента.Автор                   КАК Ответственный,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	""""                                    КАК Дополнительно,
	|	ДанныеДокумента.Комментарий             КАК Комментарий,
	|	ДанныеДокумента.Проведен                КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления         КАК ПометкаУдаления,
	|	ДанныеДокумента.Дата                    КАК ДатаПервичногоДокумента,
	|	ДанныеДокумента.Номер                   КАК НомерПервичногоДокумента,
	|	ДанныеДокумента.СуммаДокумента          КАК Сумма,
	|	ДанныеДокумента.Валюта                  КАК Валюта,
	|	ДанныеОснования.Договор                 КАК Договор,
	|	ДанныеОснования.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Дата                    КАК ДатаОтраженияВУчете
	|ИЗ Документ.КорректировкаГрафикаВзаиморасчетов КАК ДанныеДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК ДанныеОснования
	|		ПО ДанныеОснования.Ссылка = ДанныеДокумента.РасчетныйДокумент
	|			И НЕ ДанныеОснования.ДополнительнаяЗапись
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСКлиентами(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСКлиентами";

	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтКурсыВалют", ТекстыЗапроса) Тогда
		ТекстЗапросаВтКурсыВалют(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.Дата                      КАК Период,
	|	Расчеты.Дата                      КАК ДатаРегистратора,
	|	Расчеты.НомерРегистратора         КАК НомерРегистратора,
	|	Расчеты.ДатаПлатежа               КАК ДатаПлатежа,
	|	Расчеты.ВидДвижения               КАК ВидДвижения,
	|	
	|	// Измерения
	|	РегистрАналитики.КлючАналитики    КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Организация               КАК Организация,
	|	Расчеты.Контрагент                КАК Контрагент,
	|	Расчеты.Партнер                   КАК Партнер,
	|	Расчеты.Договор                   КАК Договор,
	|	Расчеты.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	
	|	Расчеты.ОбъектРасчетов            КАК ОбъектРасчетов,
	|	Расчеты.Валюта                    КАК Валюта,
	|	
	|	// Ресурсы
	|	// Сумма в валюте взаиморасчетов.
	|	СУММА(Расчеты.Сумма)              КАК Сумма,
	|	СУММА(0)                          КАК КОплате,
	|	
	|	СУММА(
	|		ВЫБОР 
	|			КОГДА Расчеты.Валюта = Расчеты.ОбъектРасчетов.Организация.ВалютаРегламентированногоУчета 
	|				ТОГДА Расчеты.Сумма
	|			КОГДА ЕСТЬNULL(КурсВалютыВзаиморасчетов.КурсЗнаменатель, 0) = 0 
	|				ТОГДА Расчеты.Сумма
	|			ИНАЧЕ ВЫРАЗИТЬ(Расчеты.Сумма * ЕСТЬNULL(КурсВалютыВзаиморасчетов.КурсЧислитель, 1) / ЕСТЬNULL(КурсВалютыВзаиморасчетов.КурсЗнаменатель, 1) КАК ЧИСЛО(31,2))
	|		КОНЕЦ)                        КАК СуммаРегл,
	|	СУММА(
	|		ВЫБОР 
	|			КОГДА Расчеты.Валюта = &ВалютаУпр 
	|				ТОГДА Расчеты.Сумма
	|			КОГДА ЕСТЬNULL(КурсВалютыУпр.КурсЗнаменатель, 0) = 0 
	|				ТОГДА Расчеты.Сумма
	|			ИНАЧЕ ВЫРАЗИТЬ(Расчеты.Сумма * ЕСТЬNULL(КурсВалютыВзаиморасчетов.КурсЧислитель, 1) * ЕСТЬNULL(КурсВалютыУпр.КурсЗнаменатель, 1)  
	|			                                / (ЕСТЬNULL(КурсВалютыВзаиморасчетов.КурсЗнаменатель, 1) * ЕСТЬNULL(КурсВалютыУпр.КурсЧислитель, 1)) КАК ЧИСЛО(31,2))
	|		КОНЕЦ)                        КАК СуммаУпр,
	|	
	|	// Реквизиты
	|	Расчеты.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|	Расчеты.РасчетныйДокумент         КАК РасчетныйДокумент,
	|	Расчеты.Валюта                    КАК ВалютаДокумента,
	|	&ИдентификаторФинЗаписи           КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации   КАК НастройкаХозяйственнойОперации,
	|	5                                 КАК ВидОперацииДляСортировки,
	|	Расчеты.Порядок                   КАК ПорядокОперацииВПределахДокумента,
	|	&ОперацияВзаиморасчетов           КАК ОперацияВзаиморасчетов
	|
	|ИЗ (ВЫБРАТЬ
	|		ДанныеДокумента.Дата                      КАК Дата,
	|		ДанныеДокумента.Номер                     КАК НомерРегистратора,
	|		ДАТАВРЕМЯ(1,1,1)                          КАК ДатаПлатежа,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА ДанныеДокумента.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ТабличнаяЧасть.ОбъектРасчетов.Контрагент
	|			ИНАЧЕ ДанныеДокумента.ОрганизацияПолучатель
	|		КОНЕЦ                                     КАК Контрагент,
	|		ТабличнаяЧасть.Партнер                    КАК Партнер,
	|		ТабличнаяЧасть.Договор                    КАК Договор,
	|		ТабличнаяЧасть.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов             КАК ОбъектРасчетов,
	|		ТабличнаяЧасть.Валюта                     КАК Валюта,
	|		
	|		ТабличнаяЧасть.Сумма                      КАК Сумма,
	|		
	|		ДанныеДокумента.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|		ДанныеДокумента.РасчетныйДокумент         КАК РасчетныйДокумент,
	|		1                                         КАК Порядок
	|	
	|	ИЗ
	|		Документ.КорректировкаГрафикаВзаиморасчетов.НовыйГрафик КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.КорректировкаГрафикаВзаиморасчетов КАК ДанныеДокумента
	|		ПО
	|			ДанныеДокумента.Ссылка = ТабличнаяЧасть.Ссылка
	|	ГДЕ
	|		ТабличнаяЧасть.Ссылка = &Ссылка
	|		И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаГрафикаОплатыКлиентом)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеДокумента.Дата                      КАК Дата,
	|		ДанныеДокумента.Номер                     КАК НомерРегистратора,
	|		ТабличнаяЧасть.Дата                       КАК ДатаПлатежа,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА ДанныеДокумента.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ТабличнаяЧасть.ОбъектРасчетов.Контрагент
	|			ИНАЧЕ ДанныеДокумента.ОрганизацияПолучатель
	|		КОНЕЦ                                     КАК Контрагент,
	|		ТабличнаяЧасть.Партнер                    КАК Партнер,
	|		ТабличнаяЧасть.Договор                    КАК Договор,
	|		ТабличнаяЧасть.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов             КАК ОбъектРасчетов,
	|		ТабличнаяЧасть.Валюта                     КАК Валюта,
	|		
	|		ТабличнаяЧасть.Сумма                      КАК Сумма,
	|		
	|		ДанныеДокумента.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|		ДанныеДокумента.РасчетныйДокумент         КАК РасчетныйДокумент,
	|		2                                         КАК Порядок
	|	
	|	ИЗ
	|		Документ.КорректировкаГрафикаВзаиморасчетов.НовыйГрафик КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.КорректировкаГрафикаВзаиморасчетов КАК ДанныеДокумента
	|		ПО
	|			ДанныеДокумента.Ссылка = ТабличнаяЧасть.Ссылка
	|	ГДЕ
	|		ТабличнаяЧасть.Ссылка = &Ссылка
	|		И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаГрафикаОплатыКлиентом)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеДокумента.Дата                      КАК Дата,
	|		ДанныеДокумента.Номер                     КАК НомерРегистратора,
	|		ДАТАВРЕМЯ(1,1,1)                          КАК ДатаПлатежа,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА ДанныеДокумента.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ТабличнаяЧасть.ОбъектРасчетов.Контрагент
	|			ИНАЧЕ ДанныеДокумента.ОрганизацияПолучатель
	|		КОНЕЦ                                     КАК Контрагент,
	|		ТабличнаяЧасть.Партнер                    КАК Партнер,
	|		ТабличнаяЧасть.Договор                    КАК Договор,
	|		ТабличнаяЧасть.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов             КАК ОбъектРасчетов,
	|		ТабличнаяЧасть.Валюта                     КАК Валюта,
	|		
	|		-ТабличнаяЧасть.Сумма                     КАК Сумма,
	|		
	|		ДанныеДокумента.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|		ДанныеДокумента.РасчетныйДокумент         КАК РасчетныйДокумент,
	|		1                                         КАК Порядок
	|	
	|	ИЗ
	|		Документ.КорректировкаГрафикаВзаиморасчетов.НовыйГрафик КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.КорректировкаГрафикаВзаиморасчетов КАК ДанныеДокумента
	|		ПО
	|			ДанныеДокумента.Ссылка = ТабличнаяЧасть.Ссылка
	|	ГДЕ
	|		ТабличнаяЧасть.Ссылка = &Ссылка
	|		И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаКлиента)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеДокумента.Дата                      КАК Дата,
	|		ДанныеДокумента.Номер                     КАК НомерРегистратора,
	|		ТабличнаяЧасть.Дата                       КАК ДатаПлатежа,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА ДанныеДокумента.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ТабличнаяЧасть.ОбъектРасчетов.Контрагент
	|			ИНАЧЕ ДанныеДокумента.ОрганизацияПолучатель
	|		КОНЕЦ                                     КАК Контрагент,
	|		ТабличнаяЧасть.Партнер                    КАК Партнер,
	|		ТабличнаяЧасть.Договор                    КАК Договор,
	|		ТабличнаяЧасть.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов             КАК ОбъектРасчетов,
	|		ТабличнаяЧасть.Валюта                     КАК Валюта,
	|		
	|		ТабличнаяЧасть.Сумма                      КАК Сумма,
	|		
	|		ДанныеДокумента.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|		ДанныеДокумента.РасчетныйДокумент         КАК РасчетныйДокумент,
	|		2                                         КАК Порядок
	|	
	|	ИЗ
	|		Документ.КорректировкаГрафикаВзаиморасчетов.НовыйГрафик КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.КорректировкаГрафикаВзаиморасчетов КАК ДанныеДокумента
	|		ПО
	|			ДанныеДокумента.Ссылка = ТабличнаяЧасть.Ссылка
	|	ГДЕ
	|		ТабличнаяЧасть.Ссылка = &Ссылка
	|		И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаКлиента)
	|	) КАК Расчеты
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитики
	|	ПО
	|		Расчеты.Организация = РегистрАналитики.Организация
	|		И Расчеты.Партнер = РегистрАналитики.Партнер
	|		И Расчеты.Контрагент = РегистрАналитики.Контрагент
	|		И Расчеты.Договор = РегистрАналитики.Договор
	|		И Расчеты.НаправлениеДеятельности = РегистрАналитики.НаправлениеДеятельности
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтКурсыВалют КАК КурсВалютыВзаиморасчетов
	|	ПО
	|		Расчеты.ОбъектРасчетов = КурсВалютыВзаиморасчетов.ОбъектРасчетов
	|		И Расчеты.Валюта = КурсВалютыВзаиморасчетов.Валюта
	|		И Расчеты.ОбъектРасчетов.Организация.ВалютаРегламентированногоУчета = КурсВалютыВзаиморасчетов.БазоваяВалюта
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтКурсыВалют КАК КурсВалютыУпр
	|	ПО
	|		Расчеты.ОбъектРасчетов = КурсВалютыУпр.ОбъектРасчетов
	|		И &ВалютаУпр = КурсВалютыУпр.Валюта
	|		И Расчеты.ОбъектРасчетов.Организация.ВалютаРегламентированногоУчета = КурсВалютыУпр.БазоваяВалюта
	|
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.Дата,
	|	Расчеты.НомерРегистратора,
	|	Расчеты.ДатаПлатежа,
	|	Расчеты.ВидДвижения,
	|
	|	// Измерения
	|	РегистрАналитики.КлючАналитики,
	|	Расчеты.Организация,
	|	Расчеты.Контрагент,
	|	Расчеты.Партнер,
	|	Расчеты.Договор,
	|	Расчеты.НаправлениеДеятельности,
	|	
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.Валюта,
	|	
	|	// Реквизиты
	|	Расчеты.ХозяйственнаяОперация,
	|	Расчеты.РасчетныйДокумент,
	|	Расчеты.Порядок
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСПоставщиками";

	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтКурсыВалют", ТекстыЗапроса) Тогда
		ТекстЗапросаВтКурсыВалют(ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Расчеты.Дата                      КАК Период,
	|	Расчеты.Дата                      КАК ДатаРегистратора,
	|	Расчеты.НомерРегистратора         КАК НомерРегистратора,
	|	Расчеты.ДатаПлатежа               КАК ДатаПлатежа,
	|	Расчеты.ВидДвижения               КАК ВидДвижения,
	|	
	|	// Измерения
	|	РегистрАналитики.КлючАналитики    КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.Организация               КАК Организация,
	|	Расчеты.Контрагент                КАК Контрагент,
	|	Расчеты.Партнер                   КАК Партнер,
	|	Расчеты.Договор                   КАК Договор,
	|	Расчеты.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|	
	|	Расчеты.ОбъектРасчетов            КАК ОбъектРасчетов,
	|	Расчеты.Валюта                    КАК Валюта,
	|	
	|	// Ресурсы
	|	// Сумма в валюте взаиморасчетов.
	|	СУММА(Расчеты.Сумма)              КАК Сумма,
	|	СУММА(0)                          КАК КОплате,
	|	
	|	СУММА(
	|		ВЫБОР 
	|			КОГДА Расчеты.Валюта = Расчеты.ОбъектРасчетов.Организация.ВалютаРегламентированногоУчета 
	|				ТОГДА Расчеты.Сумма
	|			КОГДА ЕСТЬNULL(КурсВалютыВзаиморасчетов.КурсЗнаменатель, 0) = 0 
	|				ТОГДА Расчеты.Сумма
	|			ИНАЧЕ ВЫРАЗИТЬ(Расчеты.Сумма * ЕСТЬNULL(КурсВалютыВзаиморасчетов.КурсЧислитель, 1) / ЕСТЬNULL(КурсВалютыВзаиморасчетов.КурсЗнаменатель, 1) КАК ЧИСЛО(31,2))
	|		КОНЕЦ)                        КАК СуммаРегл,
	|	СУММА(
	|		ВЫБОР 
	|			КОГДА Расчеты.Валюта = &ВалютаУпр 
	|				ТОГДА Расчеты.Сумма
	|			КОГДА ЕСТЬNULL(КурсВалютыУпр.КурсЗнаменатель, 0) = 0 
	|				ТОГДА Расчеты.Сумма
	|			ИНАЧЕ ВЫРАЗИТЬ(Расчеты.Сумма * ЕСТЬNULL(КурсВалютыВзаиморасчетов.КурсЧислитель, 1) * ЕСТЬNULL(КурсВалютыУпр.КурсЗнаменатель, 1)  
	|			                                / (ЕСТЬNULL(КурсВалютыВзаиморасчетов.КурсЗнаменатель, 1) * ЕСТЬNULL(КурсВалютыУпр.КурсЧислитель, 1)) КАК ЧИСЛО(31,2))
	|		КОНЕЦ)                        КАК СуммаУпр,
	|	
	|	// Реквизиты
	|	Расчеты.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|	Расчеты.РасчетныйДокумент         КАК РасчетныйДокумент,
	|	Расчеты.Валюта                    КАК ВалютаДокумента,
	|	&ИдентификаторФинЗаписи           КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации   КАК НастройкаХозяйственнойОперации,
	|	5                                 КАК ВидОперацииДляСортировки,
	|	Расчеты.Порядок                   КАК ПорядокОперацииВПределахДокумента,
	|	&ОперацияВзаиморасчетов           КАК ОперацияВзаиморасчетов
	|
	|ИЗ (ВЫБРАТЬ
	|		ДанныеДокумента.Дата                      КАК Дата,
	|		ДанныеДокумента.Номер                     КАК НомерРегистратора,
	|		ДАТАВРЕМЯ(1,1,1)                          КАК ДатаПлатежа,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА ДанныеДокумента.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ТабличнаяЧасть.ОбъектРасчетов.Контрагент
	|			ИНАЧЕ ДанныеДокумента.Организация
	|		КОНЕЦ                                     КАК Контрагент,
	|		ТабличнаяЧасть.Партнер                    КАК Партнер,
	|		ТабличнаяЧасть.Договор                    КАК Договор,
	|		ТабличнаяЧасть.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов             КАК ОбъектРасчетов,
	|		ТабличнаяЧасть.Валюта                     КАК Валюта,
	|		
	|		ТабличнаяЧасть.Сумма                      КАК Сумма,
	|		
	|		ДанныеДокумента.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|		ДанныеДокумента.РасчетныйДокумент         КАК РасчетныйДокумент,
	|		1                                         КАК Порядок
	|	
	|	ИЗ
	|		Документ.КорректировкаГрафикаВзаиморасчетов.НовыйГрафик КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.КорректировкаГрафикаВзаиморасчетов КАК ДанныеДокумента
	|		ПО
	|			ДанныеДокумента.Ссылка = ТабличнаяЧасть.Ссылка
	|	ГДЕ
	|		ТабличнаяЧасть.Ссылка = &Ссылка
	|		И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаГрафикаОплатыПоставщику)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеДокумента.Дата                      КАК Дата,
	|		ДанныеДокумента.Номер                     КАК НомерРегистратора,
	|		ТабличнаяЧасть.Дата                       КАК ДатаПлатежа,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА ДанныеДокумента.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ТабличнаяЧасть.ОбъектРасчетов.Контрагент
	|			ИНАЧЕ ДанныеДокумента.Организация
	|		КОНЕЦ                                     КАК Контрагент,
	|		ТабличнаяЧасть.Партнер                    КАК Партнер,
	|		ТабличнаяЧасть.Договор                    КАК Договор,
	|		ТабличнаяЧасть.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов             КАК ОбъектРасчетов,
	|		ТабличнаяЧасть.Валюта                     КАК Валюта,
	|		
	|		ТабличнаяЧасть.Сумма                      КАК Сумма,
	|		
	|		ДанныеДокумента.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|		ДанныеДокумента.РасчетныйДокумент         КАК РасчетныйДокумент,
	|		2                                         КАК Порядок
	|	
	|	ИЗ
	|		Документ.КорректировкаГрафикаВзаиморасчетов.НовыйГрафик КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.КорректировкаГрафикаВзаиморасчетов КАК ДанныеДокумента
	|		ПО
	|			ДанныеДокумента.Ссылка = ТабличнаяЧасть.Ссылка
	|	ГДЕ
	|		ТабличнаяЧасть.Ссылка = &Ссылка
	|		И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаГрафикаОплатыПоставщику)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеДокумента.Дата                      КАК Дата,
	|		ДанныеДокумента.Номер                     КАК НомерРегистратора,
	|		ДАТАВРЕМЯ(1,1,1)                          КАК ДатаПлатежа,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА ДанныеДокумента.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ТабличнаяЧасть.ОбъектРасчетов.Контрагент
	|			ИНАЧЕ ДанныеДокумента.Организация
	|		КОНЕЦ                                     КАК Контрагент,
	|		ТабличнаяЧасть.Партнер                    КАК Партнер,
	|		ТабличнаяЧасть.Договор                    КАК Договор,
	|		ТабличнаяЧасть.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов             КАК ОбъектРасчетов,
	|		ТабличнаяЧасть.Валюта                     КАК Валюта,
	|		
	|		-ТабличнаяЧасть.Сумма                     КАК Сумма,
	|		
	|		ДанныеДокумента.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|		ДанныеДокумента.РасчетныйДокумент         КАК РасчетныйДокумент,
	|		1                                         КАК Порядок
	|	
	|	ИЗ
	|		Документ.КорректировкаГрафикаВзаиморасчетов.НовыйГрафик КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.КорректировкаГрафикаВзаиморасчетов КАК ДанныеДокумента
	|		ПО
	|			ДанныеДокумента.Ссылка = ТабличнаяЧасть.Ссылка
	|	ГДЕ
	|		ТабличнаяЧасть.Ссылка = &Ссылка
	|		И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаПоставщику)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДанныеДокумента.Дата                      КАК Дата,
	|		ДанныеДокумента.Номер                     КАК НомерРегистратора,
	|		ТабличнаяЧасть.Дата                       КАК ДатаПлатежа,
	|		ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА ДанныеДокумента.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				ТОГДА ТабличнаяЧасть.ОбъектРасчетов.Контрагент
	|			ИНАЧЕ ДанныеДокумента.Организация
	|		КОНЕЦ                                     КАК Контрагент,
	|		ТабличнаяЧасть.Партнер                    КАК Партнер,
	|		ТабличнаяЧасть.Договор                    КАК Договор,
	|		ТабличнаяЧасть.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|		
	|		ТабличнаяЧасть.ОбъектРасчетов             КАК ОбъектРасчетов,
	|		ТабличнаяЧасть.Валюта                     КАК Валюта,
	|		
	|		ТабличнаяЧасть.Сумма                      КАК Сумма,
	|		
	|		ДанныеДокумента.ХозяйственнаяОперация     КАК ХозяйственнаяОперация,
	|		ДанныеДокумента.РасчетныйДокумент         КАК РасчетныйДокумент,
	|		2                                         КАК Порядок
	|	
	|	ИЗ
	|		Документ.КорректировкаГрафикаВзаиморасчетов.НовыйГрафик КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.КорректировкаГрафикаВзаиморасчетов КАК ДанныеДокумента
	|		ПО
	|			ДанныеДокумента.Ссылка = ТабличнаяЧасть.Ссылка
	|	ГДЕ
	|		ТабличнаяЧасть.Ссылка = &Ссылка
	|		И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаПоставщику)
	|	) КАК Расчеты
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитики
	|	ПО
	|		Расчеты.Организация = РегистрАналитики.Организация
	|		И Расчеты.Партнер = РегистрАналитики.Партнер
	|		И Расчеты.Контрагент = РегистрАналитики.Контрагент
	|		И Расчеты.Договор = РегистрАналитики.Договор
	|		И Расчеты.НаправлениеДеятельности = РегистрАналитики.НаправлениеДеятельности
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтКурсыВалют КАК КурсВалютыВзаиморасчетов
	|	ПО
	|		Расчеты.ОбъектРасчетов = КурсВалютыВзаиморасчетов.ОбъектРасчетов
	|		И Расчеты.Валюта = КурсВалютыВзаиморасчетов.Валюта
	|		И Расчеты.ОбъектРасчетов.Организация.ВалютаРегламентированногоУчета = КурсВалютыВзаиморасчетов.БазоваяВалюта
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтКурсыВалют КАК КурсВалютыУпр
	|	ПО
	|		Расчеты.ОбъектРасчетов = КурсВалютыУпр.ОбъектРасчетов
	|		И &ВалютаУпр = КурсВалютыУпр.Валюта
	|		И Расчеты.ОбъектРасчетов.Организация.ВалютаРегламентированногоУчета = КурсВалютыУпр.БазоваяВалюта
	|
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.Дата,
	|	Расчеты.НомерРегистратора,
	|	Расчеты.ДатаПлатежа,
	|	Расчеты.ВидДвижения,
	|
	|	// Измерения
	|	РегистрАналитики.КлючАналитики,
	|	Расчеты.Организация,
	|	Расчеты.Контрагент,
	|	Расчеты.Партнер,
	|	Расчеты.Договор,
	|	Расчеты.НаправлениеДеятельности,
	|	
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.Валюта,
	|	
	|	// Реквизиты
	|	Расчеты.ХозяйственнаяОперация,
	|	Расчеты.РасчетныйДокумент,
	|	Расчеты.Порядок
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтКурсыВалют(ТекстыЗапроса)
	
	ИмяВременнойТаблицы = "ВтКурсыВалют";
	
	ТекстЗапроса = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЕСТЬNULL(ЕСТЬNULL(ДоговорыМеждуОрганизациями.Ссылка, ДоговорыКонтрагентов.Ссылка), НЕОПРЕДЕЛЕНО) КАК Договор,
		|	ЕСТЬNULL(ЕСТЬNULL(ДоговорыМеждуОрганизациями.Дата, ДоговорыКонтрагентов.Дата), &Период) КАК Дата,
		|	ЕСТЬNULL(ЕСТЬNULL(ДоговорыМеждуОрганизациями.ВариантКурсаДоговора, ДоговорыКонтрагентов.ВариантКурсаДоговора), ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.Переменный)) КАК ВариантКурсаДоговора,
		|	ЕСТЬNULL(ЕСТЬNULL(ДоговорыМеждуОрганизациями.ВалютаВзаиморасчетов, ДоговорыКонтрагентов.ВалютаВзаиморасчетов), ОбъектыРасчетов.Валюта) КАК ВалютаВзаиморасчетов,
		|	ОбъектыРасчетов.Валюта КАК ВалютаДокумента,
		|	ОбъектыРасчетов.Организация.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
		|	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов,
		|	ОбъектыРасчетов.Объект КАК ОбъектРасчетовОбъект,
		|	ОбъектыРасчетов.ТипОбъектаРасчетов КАК ТипОбъектаРасчетов
		|ПОМЕСТИТЬ ВтОбъектыРасчетов
		|ИЗ
		|	Документ.КорректировкаГрафикаВзаиморасчетов.НовыйГрафик КАК ДанныеДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|		ПО (ДанныеДокумента.ОбъектРасчетов = ОбъектыРасчетов.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыМеждуОрганизациями КАК ДоговорыМеждуОрганизациями
		|		ПО (ОбъектыРасчетов.Договор = ДоговорыМеждуОрганизациями.Ссылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ПО (ОбъектыРасчетов.Договор = ДоговорыКонтрагентов.Ссылка)
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбъектыРасчетов.Объект КАК ОбъектРасчетовОбъект,
		|	ОбъектыРасчетов.Ссылка КАК ОбъектРасчетов,
		|	ОбъектыРасчетов.Дата КАК ДатаКурса,
		|	ВалютыИКурсыДокументов.ВалютаДокумента КАК ВалютаДокумента,
		|	ВалютыИКурсыДокументов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	ВалютыИКурсыДокументов.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
		|	ВалютыИКурсыДокументов.КурсЧислительВалютыВзаиморасчетов КАК КурсЧислительВалютыВзаиморасчетов,
		|	ВалютыИКурсыДокументов.КурсЗнаменательВалютыВзаиморасчетов КАК КурсЗнаменательВалютыВзаиморасчетов,
		|	ВалютыИКурсыДокументов.КурсЧислительВалютыУправленческогоУчета КАК КурсЧислительВалютыУправленческогоУчета,
		|	ВалютыИКурсыДокументов.КурсЗнаменательВалютыУправленческогоУчета КАК КурсЗнаменательВалютыУправленческогоУчета,
		|	ВалютыИКурсыДокументов.КурсЧислительВалютыДокумента КАК КурсЧислительВалютыДокумента,
		|	ВалютыИКурсыДокументов.КурсЗнаменательВалютыДокумента КАК КурсЗнаменательВалютыДокумента
		|ПОМЕСТИТЬ ВтФиксированныйКурс
		|ИЗ
		|	Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОбъектыРасчетов КАК ВтОбъектыРасчетов
		|		ПО ОбъектыРасчетов.Договор = ВтОбъектыРасчетов.Договор
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВалютыИКурсыДокументов КАК ВалютыИКурсыДокументов
		|		ПО ОбъектыРасчетов.Объект = ВалютыИКурсыДокументов.Документ
		|ГДЕ
		|	ОбъектыРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
		|	И ВтОбъектыРасчетов.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.ФиксированныйНаДатуОтгрузки)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Курсы.ОбъектРасчетов  КАК ОбъектРасчетов,
		|	Курсы.Валюта          КАК Валюта,
		|	Курсы.БазоваяВалюта   КАК БазоваяВалюта,
		|	Курсы.КурсЧислитель   КАК КурсЧислитель,
		|	Курсы.КурсЗнаменатель КАК КурсЗнаменатель
		|ПОМЕСТИТЬ ВтКурсыВалют
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВтОбъектыРасчетов.ОбъектРасчетов КАК ОбъектРасчетов,
		|		ВтОбъектыРасчетов.ВалютаВзаиморасчетов КАК Валюта,
		|		ВтОбъектыРасчетов.ВалютаРегламентированногоУчета КАК БазоваяВалюта,
		|		КурсыПоДоговорам.Период КАК ДатаКурса,
		|		КурсыПоДоговорам.КурсЧислитель КАК КурсЧислитель,
		|		КурсыПоДоговорам.КурсЗнаменатель КАК КурсЗнаменатель
		|	ИЗ
		|		ВтОбъектыРасчетов КАК ВтОбъектыРасчетов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютРасчетовПоДоговорам.СрезПоследних(
		|					&Период,
		|					Договор В
		|						(ВЫБРАТЬ
		|							ВтОбъектыРасчетов.Договор
		|						ИЗ
		|							ВтОбъектыРасчетов)) КАК КурсыПоДоговорам
		|			ПО ВтОбъектыРасчетов.Договор = КурсыПоДоговорам.Договор
		|	ГДЕ
		|		ВтОбъектыРасчетов.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
		|		И ВтОбъектыРасчетов.ВалютаДокумента = ВтОбъектыРасчетов.ВалютаРегламентированногоУчета
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВтОбъектыРасчетов.ОбъектРасчетов,
		|		ОтносительныеКурсы.Валюта,
		|		ОтносительныеКурсы.БазоваяВалюта,
		|		ОтносительныеКурсы.Период,
		|		ОтносительныеКурсы.КурсЧислитель,
		|		ОтносительныеКурсы.КурсЗнаменатель
		|	ИЗ
		|		ВтОбъектыРасчетов КАК ВтОбъектыРасчетов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(
		|					&Период,
		|					Валюта В
		|							(&МассивВалют)
		|						И БазоваяВалюта В
		|							(ВЫБРАТЬ
		|								ВтОбъектыРасчетов.ВалютаРегламентированногоУчета
		|							ИЗ
		|								ВтОбъектыРасчетов)) КАК ОтносительныеКурсы
		|			ПО ВтОбъектыРасчетов.ВалютаРегламентированногоУчета = ОтносительныеКурсы.БазоваяВалюта
		|	ГДЕ
		|		НЕ(ВтОбъектыРасчетов.ВалютаВзаиморасчетов = ОтносительныеКурсы.Валюта
		|					И ВтОбъектыРасчетов.ВариантКурсаДоговора = ЗНАЧЕНИЕ(Перечисление.ВариантыКурсаДоговора.УстановленныйВДоговоре)
		|					И ВтОбъектыРасчетов.ВалютаДокумента = ВтОбъектыРасчетов.ВалютаРегламентированногоУчета)
		|		И НЕ ВтОбъектыРасчетов.ОбъектРасчетовОбъект В
		|					(ВЫБРАТЬ
		|						Т.ОбъектРасчетовОбъект
		|					ИЗ
		|						ВтФиксированныйКурс КАК Т)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВтФиксированныйКурс.ОбъектРасчетов,
		|		ВтФиксированныйКурс.ВалютаДокумента,
		|		ВтФиксированныйКурс.ВалютаРегламентированногоУчета,
		|		ВтФиксированныйКурс.ДатаКурса,
		|		ВтФиксированныйКурс.КурсЧислительВалютыДокумента,
		|		ВтФиксированныйКурс.КурсЗнаменательВалютыДокумента
		|	ИЗ
		|		ВтФиксированныйКурс КАК ВтФиксированныйКурс
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВтФиксированныйКурс.ОбъектРасчетов,
		|		ВтФиксированныйКурс.ВалютаВзаиморасчетов,
		|		ВтФиксированныйКурс.ВалютаРегламентированногоУчета,
		|		ВтФиксированныйКурс.ДатаКурса,
		|		ВтФиксированныйКурс.КурсЧислительВалютыВзаиморасчетов,
		|		ВтФиксированныйКурс.КурсЗнаменательВалютыВзаиморасчетов
		|	ИЗ
		|		ВтФиксированныйКурс КАК ВтФиксированныйКурс
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВтФиксированныйКурс.ОбъектРасчетов,
		|		&ВалютаУпр,
		|		ВтФиксированныйКурс.ВалютаРегламентированногоУчета,
		|		ВтФиксированныйКурс.ДатаКурса,
		|		ВтФиксированныйКурс.КурсЧислительВалютыУправленческогоУчета,
		|		ВтФиксированныйКурс.КурсЗнаменательВалютыУправленческогоУчета
		|	ИЗ
		|		ВтФиксированныйКурс КАК ВтФиксированныйКурс
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВтФиксированныйКурс.ОбъектРасчетов,
		|		ВтФиксированныйКурс.ВалютаРегламентированногоУчета,
		|		ВтФиксированныйКурс.ВалютаРегламентированногоУчета,
		|		ВтФиксированныйКурс.ДатаКурса,
		|		1,
		|		1
		|	ИЗ
		|		ВтФиксированныйКурс КАК ВтФиксированныйКурс) КАК Курсы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтОбъектыРасчетов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВтФиксированныйКурс
		|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяВременнойТаблицы);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

Функция ТекстЗапросаДанныхЗаполнения(ОтборПоПервичномуДокументу = Ложь) Экспорт 
	
	ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ИСТИНА                                                 КАК РасчетыСКлиентами,
		|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам            КАК АналитикаУчетаПоПартнерам, 
		|	КлючиАналитикиУчетаПоПартнерам.Партнер                 КАК Партнер,
		|	РасчетыСКлиентами.ОбъектРасчетов                       КАК ОбъектРасчетов,
		|	КлючиАналитикиУчетаПоПартнерам.Договор                 КАК Договор,
		|	КлючиАналитикиУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	РасчетыСКлиентами.Валюта                               КАК Валюта,
		|	ВЫБОР
		|		КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыСКлиентами.Сумма
		|		ИНАЧЕ -РасчетыСКлиентами.Сумма
		|	КОНЕЦ                                                  КАК Сумма,
		|	РасчетыСКлиентами.ДатаПлатежа                          КАК ДатаПлатежа,
		|	РасчетыСКлиентами.ДатаРегистратора                     КАК ДатаРегистратора,
		|	РасчетыСКлиентами.Регистратор                          КАК Регистратор
		|ПОМЕСТИТЬ ВтДанныеРасчетов
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиАналитикиУчетаПоПартнерам
		|		ПО РасчетыСКлиентами.АналитикаУчетаПоПартнерам = КлючиАналитикиУчетаПоПартнерам.Ссылка
		|ГДЕ
		|	&УсловиеОтбора
		|	И Активность
		|	И Сумма <> 0
		|	И ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносПлатежаМеждуФилиалами)
		|	И ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносЗадолженностиМеждуФилиалами)
		|	
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ                                                   КАК РасчетыСКлиентами,
		|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам, 
		|	КлючиАналитикиУчетаПоПартнерам.Партнер                 КАК Партнер,
		|	РасчетыСПоставщиками.ОбъектРасчетов                    КАК ОбъектРасчетов,
		|	КлючиАналитикиУчетаПоПартнерам.Договор                 КАК Договор,
		|	КлючиАналитикиУчетаПоПартнерам.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	РасчетыСПоставщиками.Валюта                            КАК Валюта,
		|	ВЫБОР
		|		КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА РасчетыСПоставщиками.Сумма
		|		ИНАЧЕ -РасчетыСПоставщиками.Сумма
		|	КОНЕЦ                                                  КАК Сумма,
		|	РасчетыСПоставщиками.ДатаПлатежа                       КАК ДатаПлатежа,
		|	РасчетыСПоставщиками.ДатаРегистратора                  КАК ДатаРегистратора,
		|	РасчетыСПоставщиками.Регистратор                       КАК Регистратор
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиАналитикиУчетаПоПартнерам
		|		ПО РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = КлючиАналитикиУчетаПоПартнерам.Ссылка
		|ГДЕ
		|	&УсловиеОтбора
		|	И Активность
		|	И Сумма <> 0
		|	И ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносПлатежаМеждуФилиалами)
		|	И ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносЗадолженностиМеждуФилиалами)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	РасчетыСКлиентами,
		|	АналитикаУчетаПоПартнерам,
		|	ОбъектРасчетов,
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Расчеты.РасчетыСКлиентами         КАК РасчетыСКлиентами,
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов            КАК ОбъектРасчетов,
		|	Расчеты.Валюта                    КАК Валюта,
		|	СУММА(Расчеты.Сумма)              КАК Сумма
		|ПОМЕСТИТЬ ВтОтборРасчетов
		|ИЗ
		|	ВтДанныеРасчетов КАК Расчеты
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыСКлиентами,
		|	АналитикаУчетаПоПартнерам,
		|	ОбъектРасчетов,
		|	Валюта
		|
		|ИМЕЮЩИЕ
		|	СУММА(Расчеты.Сумма) <> 0
		|
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Расчеты.РасчетыСКлиентами         КАК РасчетыСКлиентами,
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов            КАК ОбъектРасчетов,
		|	Расчеты.Валюта                    КАК Валюта,
		|	1                                 КАК Сумма
		|ИЗ
		|	ВтДанныеРасчетов КАК Расчеты
		|ГДЕ
		|	Расчеты.Регистратор ССЫЛКА Документ.КорректировкаГрафикаВзаиморасчетов
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыСКлиентами,
		|	АналитикаУчетаПоПартнерам,
		|	ОбъектРасчетов,
		|	Валюта
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Расчеты.РасчетыСКлиентами         КАК РасчетыСКлиентами,
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов            КАК ОбъектРасчетов,
		|	Расчеты.Валюта                    КАК Валюта,
		|	-1                                КАК Сумма
		|ИЗ
		|	ВтДанныеРасчетов КАК Расчеты
		|ГДЕ
		|	Расчеты.Регистратор ССЫЛКА Документ.КорректировкаГрафикаВзаиморасчетов
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыСКлиентами,
		|	АналитикаУчетаПоПартнерам,
		|	ОбъектРасчетов,
		|	Валюта
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	РасчетыСКлиентами,
		|	АналитикаУчетаПоПартнерам,
		|	ОбъектРасчетов,
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Расчеты.ХозяйственнаяОперация   КАК ХозяйственнаяОперация,
		|	Расчеты.Партнер                 КАК Партнер,
		|	Расчеты.ОбъектРасчетов          КАК ОбъектРасчетов,
		|	Расчеты.Договор                 КАК Договор,
		|	Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	Расчеты.Валюта                  КАК Валюта,
		|	СУММА(Расчеты.Сумма)            КАК Сумма,
		|	Расчеты.Дата                    КАК Дата
		|ИЗ (ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаГрафикаОплатыКлиентом) КАК ХозяйственнаяОперация,
		|		Расчеты.Партнер                 КАК Партнер,
		|		Расчеты.ОбъектРасчетов          КАК ОбъектРасчетов,
		|		Расчеты.Договор                 КАК Договор,
		|		Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Расчеты.Валюта                  КАК Валюта,
		|		Расчеты.Сумма                   КАК Сумма,
		|		ВЫБОР
		|			КОГДА Расчеты.ДатаПлатежа = ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА НАЧАЛОПЕРИОДА(Расчеты.ДатаРегистратора, ДЕНЬ)
		|			ИНАЧЕ Расчеты.ДатаПлатежа
		|		КОНЕЦ                           КАК Дата
		|	ИЗ
		|		ВтДанныеРасчетов КАК Расчеты
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОтборРасчетов КАК ОтборРасчетов
		|			ПО Расчеты.РасчетыСКлиентами = ОтборРасчетов.РасчетыСКлиентами 
		|				И Расчеты.АналитикаУчетаПоПартнерам = ОтборРасчетов.АналитикаУчетаПоПартнерам
		|				И Расчеты.ОбъектРасчетов = ОтборРасчетов.ОбъектРасчетов
		|				И Расчеты.Валюта = ОтборРасчетов.Валюта
		|	ГДЕ
		|		Расчеты.Сумма > 0
		|		И ОтборРасчетов.Сумма > 0
		|		И Расчеты.РасчетыСКлиентами
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаГрафикаОплатыПоставщику) КАК ХозяйственнаяОперация,
		|		Расчеты.Партнер                 КАК Партнер,
		|		Расчеты.ОбъектРасчетов          КАК ОбъектРасчетов,
		|		Расчеты.Договор                 КАК Договор,
		|		Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Расчеты.Валюта                  КАК Валюта,
		|		Расчеты.Сумма                   КАК Сумма,
		|		ВЫБОР
		|			КОГДА Расчеты.ДатаПлатежа = ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА НАЧАЛОПЕРИОДА(Расчеты.ДатаРегистратора, ДЕНЬ)
		|			ИНАЧЕ Расчеты.ДатаПлатежа
		|		КОНЕЦ                           КАК Дата
		|	ИЗ
		|		ВтДанныеРасчетов КАК Расчеты
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОтборРасчетов КАК ОтборРасчетов
		|			ПО Расчеты.РасчетыСКлиентами = ОтборРасчетов.РасчетыСКлиентами 
		|				И Расчеты.АналитикаУчетаПоПартнерам = ОтборРасчетов.АналитикаУчетаПоПартнерам
		|				И Расчеты.ОбъектРасчетов = ОтборРасчетов.ОбъектРасчетов
		|				И Расчеты.Валюта = ОтборРасчетов.Валюта
		|	ГДЕ
		|		Расчеты.Сумма > 0
		|		И ОтборРасчетов.Сумма > 0
		|		И НЕ Расчеты.РасчетыСКлиентами
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаКлиента) КАК ХозяйственнаяОперация,
		|		Расчеты.Партнер                 КАК Партнер,
		|		Расчеты.ОбъектРасчетов          КАК ОбъектРасчетов,
		|		Расчеты.Договор                 КАК Договор,
		|		Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Расчеты.Валюта                  КАК Валюта,
		|		-Расчеты.Сумма                  КАК Сумма,
		|		Расчеты.ДатаПлатежа             КАК Дата
		|	ИЗ
		|		ВтДанныеРасчетов КАК Расчеты
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОтборРасчетов КАК ОтборРасчетов
		|			ПО Расчеты.РасчетыСКлиентами = ОтборРасчетов.РасчетыСКлиентами 
		|				И Расчеты.АналитикаУчетаПоПартнерам = ОтборРасчетов.АналитикаУчетаПоПартнерам
		|				И Расчеты.ОбъектРасчетов = ОтборРасчетов.ОбъектРасчетов
		|				И Расчеты.Валюта = ОтборРасчетов.Валюта
		|	ГДЕ
		|		Расчеты.Сумма < 0
		|		И ОтборРасчетов.Сумма < 0
		|		И Расчеты.РасчетыСКлиентами
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаПоставщику) КАК ХозяйственнаяОперация,
		|		Расчеты.Партнер                 КАК Партнер,
		|		Расчеты.ОбъектРасчетов          КАК ОбъектРасчетов,
		|		Расчеты.Договор                 КАК Договор,
		|		Расчеты.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Расчеты.Валюта                  КАК Валюта,
		|		-Расчеты.Сумма                  КАК Сумма,
		|		Расчеты.ДатаПлатежа             КАК Дата
		|	ИЗ
		|		ВтДанныеРасчетов КАК Расчеты
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОтборРасчетов КАК ОтборРасчетов
		|			ПО Расчеты.РасчетыСКлиентами = ОтборРасчетов.РасчетыСКлиентами 
		|				И Расчеты.АналитикаУчетаПоПартнерам = ОтборРасчетов.АналитикаУчетаПоПартнерам
		|				И Расчеты.ОбъектРасчетов = ОтборРасчетов.ОбъектРасчетов
		|				И Расчеты.Валюта = ОтборРасчетов.Валюта
		|	ГДЕ
		|		Расчеты.Сумма < 0
		|		И ОтборРасчетов.Сумма < 0
		|		И НЕ Расчеты.РасчетыСКлиентами
		|	) КАК Расчеты
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозяйственнаяОперация,
		|	Партнер,
		|	ОбъектРасчетов,
		|	Договор,
		|	НаправлениеДеятельности,
		|	Валюта,
		|	Дата
		|
		|УПОРЯДОЧИТЬ ПО
		|	Дата,
		|	ОбъектРасчетов
		|
		|ИТОГИ ПО
		|	ХозяйственнаяОперация
		|";
	
	Если ОтборПоПервичномуДокументу Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбора", "РасчетныйДокумент = &РасчетныйДокумент И Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбора", "Регистратор = &РасчетныйДокумент");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДокументовНаОсновании() Экспорт
	
	Возврат "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КорректировкаГрафикаВзаиморасчетов.Ссылка КАК Ссылка,
		|	КорректировкаГрафикаВзаиморасчетов.ДокументОснование КАК ДокументОснование,
		|	КорректировкаГрафикаВзаиморасчетов.ХозяйственнаяОперация КАК ХозяйственнаяОперация
		|ИЗ
		|	Документ.КорректировкаГрафикаВзаиморасчетов КАК КорректировкаГрафикаВзаиморасчетов
		|ГДЕ
		|	КорректировкаГрафикаВзаиморасчетов.РасчетныйДокумент = &РасчетныйДокументКорректировки
		|	И КорректировкаГрафикаВзаиморасчетов.Ссылка <> &Ссылка
		|	И КорректировкаГрафикаВзаиморасчетов.Проведен";
	
КонецФункции

Функция ПоследняяКорректировка(СтрокиТаблицы, ТекущаяСтрока = Неопределено) Экспорт
	Результат = ТекущаяСтрока;
	Для Каждого СтрокаКорректировки Из СтрокиТаблицы Цикл
		Если ТекущаяСтрока = Неопределено Тогда
			ТекущаяСтрока = СтрокаКорректировки;
			Если ТипЗнч(СтрокаКорректировки.ДокументОснование) <> Тип("ДокументСсылка.КорректировкаГрафикаВзаиморасчетов") Тогда
				Результат = СтрокаКорректировки;
			КонецЕсли;
		КонецЕсли;
		Если СтрокаКорректировки.ДокументОснование = ТекущаяСтрока.Ссылка Тогда
			Результат = ПоследняяКорректировка(СтрокиТаблицы, СтрокаКорректировки.Ссылка);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента           = ПустаяСсылка().Метаданные().ПолноеИмя();
	СинонимТаблицыДокумента      = "";
	ВЗапросеЕстьИсточник         = Истина;

	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ВЗапросеЕстьИсточник,,);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
