
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// СтандартныеПодсистемы.РаботаСФайлами
	ПараметрыГиперссылки = РаботаСФайлами.ГиперссылкаФайлов();
	ПараметрыГиперссылки.Размещение = "КоманднаяПанель";
	РаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыГиперссылки);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	УстановитьУсловноеОформление();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование)
		И Объект.Проведен Тогда
		Элементы.Организация.ТолькоПросмотр           = Истина;
		Элементы.Контрагент.ТолькоПросмотр            = Истина;
		Элементы.ОрганизацияПолучатель.ТолькоПросмотр = Истина;
		Элементы.РасчетныйДокумент.ТолькоПросмотр     = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Элементы.Организация.ТолькоПросмотр           = Объект.Проведен;
		Элементы.Контрагент.ТолькоПросмотр            = Объект.Проведен;
		Элементы.ОрганизацияПолучатель.ТолькоПросмотр = Объект.Проведен;
		Элементы.РасчетныйДокумент.ТолькоПросмотр     = Объект.Проведен;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
	РаспределитьОстатки(Объект.НовыйГрафик, ТаблицаОстатков);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ПриЧтенииСозданииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РасчетныйДокументПриИзменении(Элемент)
	
	РасчетныйДокументПриИзмененииНаСервере();;
	
КонецПроцедуры

&НаКлиенте
Процедура ХозяйственнаяОперацияПриИзменении(Элемент)
	
	ЗаполнитьПоОстаткамНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	Объект.РасчетныйДокумент = Неопределено;
	Объект.ДокументОснование = Неопределено;
	ИсходныйГрафик.Очистить();
	Объект.НовыйГрафик.Очистить();
	Объект.Валюта = ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПолучательПриИзменении(Элемент)
	
	Объект.РасчетныйДокумент = Неопределено;
	Объект.ДокументОснование = Неопределено;
	ИсходныйГрафик.Очистить();
	Объект.НовыйГрафик.Очистить();
	Объект.Валюта = ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Объект.РасчетныйДокумент = Неопределено;
	Объект.ДокументОснование = Неопределено;
	ИсходныйГрафик.Очистить();
	Объект.НовыйГрафик.Очистить();
	Объект.Валюта = ПредопределенноеЗначение("Справочник.Валюты.ПустаяСсылка");
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетныйДокументНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	РасчетныйДокументНачалоВыбораНаСервере();
	
КонецПроцедуры

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраНажатие(Элемент, СтандартнаяОбработка)
	РаботаСФайламиКлиент.ПолеПредпросмотраНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	РаботаСФайламиКлиент.ПолеПредпросмотраПеретаскивание(ЭтотОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	РаботаСФайламиКлиент.ПолеПредпросмотраПроверкаПеретаскивания(ЭтотОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНовыйГрафик

&НаКлиенте
Процедура НовыйГрафикПриИзменении(Элемент)
	
	СуммаНовогоГрафика = ?(МультивалютнаяТаблица(Объект.НовыйГрафик), 0, Объект.НовыйГрафик.Итог("Сумма"));
	РаспределитьОстатки(Объект.НовыйГрафик, ТаблицаОстатков);
	ОстатокНовогоГрафика = ?(МультивалютнаяТаблица(Объект.НовыйГрафик), 0, Объект.НовыйГрафик.Итог("Остаток"));
	
КонецПроцедуры

&НаКлиенте
Процедура НовыйГрафикОбъектРасчетовПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НовыйГрафик.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиИсходногоГрафика = ИсходныйГрафик.НайтиСтроки(Новый Структура("ОбъектРасчетов", ТекущиеДанные.ОбъектРасчетов));
	Если СтрокиИсходногоГрафика.Количество() > 0 Тогда
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, СтрокиИсходногоГрафика[0], , "Дата, Сумма");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)
	РаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ПоказатьПодтверждениеЗакрытияФормыСФайлами(ЭтотОбъект, Отказ, ЗавершениеРаботы, Объект.Ссылка);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ЭтоДокументМеждуОрганизациями = ЗначениеЗаполнено(Объект.ОрганизацияПолучатель) И Не ЗначениеЗаполнено(Объект.Контрагент);
	
	СуммаНовогоГрафика = ?(МультивалютнаяТаблица(Объект.НовыйГрафик), 0, Объект.НовыйГрафик.Итог("Сумма"));
	ЗаполнитьГрафикИсходнымиДанными(ИсходныйГрафик);
	ЗаполнитьОстатки();
	РаспределитьОстатки(ИсходныйГрафик, ТаблицаОстатков);
	РаспределитьОстатки(Объект.НовыйГрафик, ТаблицаОстатков);
	ОстатокНовогоГрафика = ?(МультивалютнаяТаблица(Объект.НовыйГрафик), 0, Объект.НовыйГрафик.Итог("Остаток"));
	ОстатокИсходногоГрафика = ?(МультивалютнаяТаблица(ИсходныйГрафик), 0, ИсходныйГрафик.Итог("Остаток"));
	СуммаИсходногоГрафика = ?(МультивалютнаяТаблица(ИсходныйГрафик), 0, ИсходныйГрафик.Итог("Сумма"));
	Объект.СуммаДокумента = ?(МультивалютнаяТаблица(Объект.НовыйГрафик), 0, Объект.НовыйГрафик.Итог("Сумма"));
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаСервере
Процедура РасчетныйДокументПриИзмененииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.РасчетныйДокумент) Тогда
		Возврат;
	КонецЕсли;
	Объект.НовыйГрафик.Очистить();
	Объект.Валюта = Справочники.Валюты.ПустаяСсылка();
	Запрос = Новый Запрос;
	Запрос.Текст = Документы.КорректировкаГрафикаВзаиморасчетов.ТекстЗапросаДокументовНаОсновании();
	Запрос.УстановитьПараметр("РасчетныйДокументКорректировки", Объект.РасчетныйДокумент);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	ВведенныеДокументы = Запрос.Выполнить().Выгрузить();
	СтрокиВведенныхКорректировок = ВведенныеДокументы.НайтиСтроки(Новый Структура("ХозяйственнаяОперация", Объект.ХозяйственнаяОперация));
	СтрокаДокумента = Документы.КорректировкаГрафикаВзаиморасчетов.ПоследняяКорректировка(СтрокиВведенныхКорректировок);
	Объект.ДокументОснование = Объект.РасчетныйДокумент;
	Если СтрокаДокумента <> Неопределено Тогда
		Объект.ДокументОснование = СтрокаДокумента.Ссылка;
	КонецЕсли;
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ДополнительныеСвойства.Вставить("ВыборРасчетногоДокумента", Истина);
	ДокументОбъект.Заполнить(Объект.РасчетныйДокумент);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	ПриЧтенииСозданииНаСервере();
	НастроитьЭлементыФормы();
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства 
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура ЗаполнитьГрафикИсходнымиДанными(Таблица)
	
	Таблица.Очистить();
	ДоступныеОбъектыРасчетов.Очистить();
	Элементы.ХозяйственнаяОперация.СписокВыбора.Очистить();
	Если Не ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Элементы.ХозяйственнаяОперация.СписокВыбора.Добавить(Перечисления.ХозяйственныеОперации.КорректировкаГрафикаОплатыКлиентом);
		Элементы.ХозяйственнаяОперация.СписокВыбора.Добавить(Перечисления.ХозяйственныеОперации.КорректировкаГрафикаОплатыПоставщику);
		Элементы.ХозяйственнаяОперация.СписокВыбора.Добавить(Перечисления.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаКлиента);
		Элементы.ХозяйственнаяОперация.СписокВыбора.Добавить(Перечисления.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаПоставщику);
	ИначеЕсли ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.КорректировкаГрафикаВзаиморасчетов") Тогда
		Объект.ХозяйственнаяОперация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "ХозяйственнаяОперация");
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РасчетныйДокумент", Объект.ДокументОснование);
	ОтборПоПервичномуДокументу = ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ПервичныйДокумент");
	Запрос.Текст = Документы.КорректировкаГрафикаВзаиморасчетов.ТекстЗапросаДанныхЗаполнения(ОтборПоПервичномуДокументу);
	ВалютыВзаиморасчетов = Новый Массив;
	
	ВыборкаПоХозОперации = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоХозОперации.Следующий() Цикл
		Элементы.ХозяйственнаяОперация.СписокВыбора.Добавить(ВыборкаПоХозОперации.ХозяйственнаяОперация);
		Если ВыборкаПоХозОперации.ХозяйственнаяОперация <> Объект.ХозяйственнаяОперация Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка = ВыборкаПоХозОперации.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Таблица.Добавить(), Выборка);
			ДоступныеОбъектыРасчетов.Добавить(Выборка.ОбъектРасчетов);
			Если ЗначениеЗаполнено(Выборка.Валюта) Тогда
				ВалютыВзаиморасчетов.Добавить(Выборка.Валюта);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Если ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.КорректировкаГрафикаВзаиморасчетов") Тогда
		Элементы.ХозяйственнаяОперация.СписокВыбора.Очистить();
		Элементы.ХозяйственнаяОперация.СписокВыбора.Добавить(Объект.ХозяйственнаяОперация);
	КонецЕсли;
	
	ВалютыВзаиморасчетов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ВалютыВзаиморасчетов);
	Объект.СуммаДокумента = Таблица.Итог("Сумма");
	Если ВалютыВзаиморасчетов.Количество() = 1 Тогда
		Объект.Валюта = ВалютыВзаиморасчетов[0];
	ИначеЕсли ВалютыВзаиморасчетов.Количество() > 1 Тогда 
		Объект.СуммаДокумента = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОстатки()
	
	ТаблицаОстатков.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РасчетныйДокумент", Объект.РасчетныйДокумент);
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	КлючиАналитикиУчетаПоПартнерам.Партнер                  КАК Партнер,
		|	КлючиАналитикиУчетаПоПартнерам.Договор                  КАК Договор,
		|	КлючиАналитикиУчетаПоПартнерам.НаправлениеДеятельности  КАК НаправлениеДеятельности,
		|	РасчетыПоСрокамОстатки.ОбъектРасчетов                   КАК ОбъектРасчетов,
		|	РасчетыПоСрокамОстатки.Валюта                           КАК Валюта,
		|	РасчетыПоСрокамОстатки.ДолгОстаток                      КАК Остаток
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки КАК РасчетыПоСрокамОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиАналитикиУчетаПоПартнерам
		|		ПО РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам = КлючиАналитикиУчетаПоПартнерам.Ссылка
		|ГДЕ
		|	РасчетыПоСрокамОстатки.РасчетныйДокумент = &РасчетныйДокумент
		|";
	
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаКлиента Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.Долг.Имя,
			Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.Предоплата.Имя);
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КорректировкаГрафикаПогашенияАвансаПоставщику Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Ресурсы.Долг.Имя,
			Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.Ресурсы.Предоплата.Имя);
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Имя,
			Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.Имя);
	ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КорректировкаГрафикаОплатыПоставщику Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Имя,
			Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.Имя);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаОстатков.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РаспределитьОстатки(Таблица, ТаблицаОстатков)
	
	КлючевыеПоля = "Партнер, Договор, НаправлениеДеятельности, ОбъектРасчетов, Валюта";
	МассивСвойств = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КлючевыеПоля,,Истина,Истина);
	
	Для Каждого СтрокаГрафика Из Таблица Цикл
		СтрокаГрафика.Остаток = 0;
	КонецЦикла;
	
	Для Каждого СтрокаОстатка Из ТаблицаОстатков Цикл
		
		СтрокиГрафикаВПорядкеУбыванияДат = Новый Массив;
		
		Для Каждого СтрокаГрафика Из Таблица Цикл
			Если ОбщегоНазначенияУТКлиентСервер.СтруктурыРавны(СтрокаГрафика, СтрокаОстатка, МассивСвойств) Тогда
				Индекс = 0;
				Пока Индекс < СтрокиГрафикаВПорядкеУбыванияДат.Количество()
					И СтрокиГрафикаВПорядкеУбыванияДат[Индекс].Дата > СтрокаГрафика.Дата Цикл
					Индекс = Индекс + 1;
				КонецЦикла;
				СтрокиГрафикаВПорядкеУбыванияДат.Вставить(Индекс, СтрокаГрафика)
			КонецЕсли;
		КонецЦикла;
		
		СуммаОстатка = СтрокаОстатка.Остаток;
		Индекс = 0;
		Пока Индекс < СтрокиГрафикаВПорядкеУбыванияДат.Количество() Цикл
			СтрокиГрафикаВПорядкеУбыванияДат[Индекс].Остаток = Мин(СтрокиГрафикаВПорядкеУбыванияДат[Индекс].Сумма, СуммаОстатка);
			СуммаОстатка = СуммаОстатка - СтрокиГрафикаВПорядкеУбыванияДат[Индекс].Остаток;
			СуммаОстатка = Макс(СуммаОстатка, 0);
			Индекс = Индекс + 1;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Цвет текста ИсходныйГрафик
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходныйГрафик.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИсходныйГрафик.Остаток");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.СерыйЦветТекста1);
	
	// Цвет текста НовыйГрафик
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НовыйГрафик.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НовыйГрафик.Остаток");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.СерыйЦветТекста1);
	
	// Только просмотр ИсходныйГрафик
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Для Каждого КолонкаТаблицы Из Элементы.ИсходныйГрафик.ПодчиненныеЭлементы Цикл
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(КолонкаТаблицы.Имя);
	КонецЦикла;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// Только просмотр НовыйГрафик.Остаток
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НовыйГрафикОстаток.Имя);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткам(Команда)
	ЗаполнитьПоОстаткамНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОстаткамНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.РасчетныйДокумент) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьГрафикИсходнымиДанными(ИсходныйГрафик);
	ЗаполнитьГрафикИсходнымиДанными(Объект.НовыйГрафик);
	Для Каждого СтрокаТаблицы Из Объект.НовыйГрафик Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Дата) Тогда
			СтрокаТаблицы.Дата = ТекущаяДатаСеанса();
		КонецЕсли;
	КонецЦикла;
	СуммаНовогоГрафика = ?(МультивалютнаяТаблица(Объект.НовыйГрафик), 0, Объект.НовыйГрафик.Итог("Сумма"));
	ЗаполнитьОстатки();
	РаспределитьОстатки(ИсходныйГрафик, ТаблицаОстатков);
	РаспределитьОстатки(Объект.НовыйГрафик, ТаблицаОстатков);
	ОстатокНовогоГрафика = ?(МультивалютнаяТаблица(Объект.НовыйГрафик), 0,  Объект.НовыйГрафик.Итог("Остаток"));
	ОстатокИсходногоГрафика = ?(МультивалютнаяТаблица(ИсходныйГрафик), 0, ИсходныйГрафик.Итог("Остаток"));
	СуммаИсходногоГрафика = ?(МультивалютнаяТаблица(ИсходныйГрафик), 0, ИсходныйГрафик.Итог("Сумма"));
	Объект.СуммаДокумента = ?(МультивалютнаяТаблица(Объект.НовыйГрафик), 0,  Объект.НовыйГрафик.Итог("Сумма"));
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	ОрганизацияПолучательВидимость = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций")
		И (ЭтоДокументМеждуОрганизациями Или ЗначениеЗаполнено(Объект.ОрганизацияПолучатель));
	
	Элементы.Организация.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	Элементы.ОрганизацияПолучатель.Видимость = ОрганизацияПолучательВидимость;
	Элементы.Организация.Заголовок = ?(
		ОрганизацияПолучательВидимость,
		НСтр("ru = 'Организация-отправитель';
			|en = 'Shipping company'"),
		НСтр("ru = 'Организация';
			|en = 'Company'"));
	Элементы.Контрагент.Видимость = Не ЭтоДокументМеждуОрганизациями;
	
	Элементы.ХозяйственнаяОперация.ТолькоПросмотр = Элементы.ХозяйственнаяОперация.СписокВыбора.Количество() = 1;
	
	Если ТолькоПросмотр Тогда
		Элементы.НовыйГрафикЗаполнитьПоОстаткам.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РасчетныйДокументНачалоВыбораНаСервере()
	
	МассивОтборов = Новый Массив;
	
	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Если ТипЗнч(Объект.РасчетныйДокумент) = Тип("ДокументСсылка.Бронирование") Тогда
			МассивОтборов.Добавить(Новый СвязьПараметраВыбора("Отбор.КонтрагентПеревозчик", "Объект.Контрагент"));
		Иначе
			МассивОтборов.Добавить(Новый СвязьПараметраВыбора("Отбор.Контрагент", "Объект.Контрагент"));
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		МассивОтборов.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ОрганизацияПолучатель) Тогда
		Если ТипЗнч(Объект.РасчетныйДокумент) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями")
			Или ТипЗнч(Объект.РасчетныйДокумент) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациямиОСписании") Тогда
			МассивОтборов.Добавить(Новый СвязьПараметраВыбора("Отбор.Комиссионер", "Объект.ОрганизацияПолучатель"));
		Иначе
			МассивОтборов.Добавить(Новый СвязьПараметраВыбора("Отбор.ОрганизацияПолучатель", "Объект.ОрганизацияПолучатель"));
		КонецЕсли;
	КонецЕсли;
	
	Элементы.РасчетныйДокумент.СвязиПараметровВыбора = Новый ФиксированныйМассив(МассивОтборов);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция МультивалютнаяТаблица(Таблица)
	
	Валюта = Неопределено;
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Валюта) Тогда
			Продолжить;
		КонецЕсли;
		Если Валюта = Неопределено Тогда 
			Валюта = СтрокаТаблицы.Валюта
		ИначеЕсли Валюта <> СтрокаТаблицы.Валюта Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

