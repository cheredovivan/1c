#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	//++ НЕ УТКА
	МеханизмыДокумента.Добавить("МеждународныйУчет");
	//-- НЕ УТКА
	МеханизмыДокумента.Добавить("НематериальныеАктивы");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("ОбесценениеВНА");
	
	СписаниеНМАЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка, ДокументОбъект - ссылка на документ или объект, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура:
//     * Ключ - Строка - Имя таблицы.
//     * Значение - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.СписаниеНМА2_4") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда

		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
		
		ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииНМАБУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПараметрыАмортизацииНМАУУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаНМАБУ(ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПорядокУчетаНМАУУ(ТекстыЗапроса, Регистры);
		
		//++ НЕ УТКА
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Регистры);
		//-- НЕ УТКА
		
		СписаниеНМАЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	КонецЕсли;
	
	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
	Возврат ТаблицыДвижений;
	
КонецФункции

// Формирует таблицы движений при отложенном проведении.
//
// Параметры:
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Содержит временные таблицы, которые используются при формировании движений.
//
// Возвращаемое значение:
//  см. ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения
//
Функция ТаблицыОтложенногоФормированияДвижений(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ПолучитьДанныеДокумента(Запрос);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализацииПоПериодам(Запрос, Запрос.Параметры,, "ВтСписокДокументов");

	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения();
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииНМАБУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПараметрыАмортизацииНМАУУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаНМАБУ(ТекстыЗапроса, Неопределено);
	ТекстЗапросаТаблицаПорядокУчетаНМАУУ(ТекстыЗапроса, Неопределено);
	
	ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаАмортизацияНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаОбесценениеВНА(ТекстыЗапроса);
	ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса);
	ТекстЗапросаТаблицаПартииПрочихРасходов(ТекстыЗапроса);
	ТекстЗапросаТаблицаПрочиеАктивыПассивы(ТекстыЗапроса);
	ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(ТекстыЗапроса);
	
	//++ НЕ УТКА
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаОтражениеДокументовВМеждународномУчете(ТекстыЗапроса, Неопределено);
	//-- НЕ УТКА

	СписаниеНМАЛокализация.ДополнитьТекстыЗапросаОтложенногоФормированияДвижений(ТекстыЗапроса);
	
	ТаблицыДвижений = ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса);
	
	Возврат ТаблицыДвижений
	
КонецФункции

#КонецОбласти

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Команда = ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.ВидимостьВФормах = "ФормаСписка";
	КонецЕсли;
	
	СписаниеНМАЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);

КонецПроцедуры

// Добавляет команду создания документа "Списание НМА".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - добавленная команда
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.СписаниеНМА2_4) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.СписаниеНМА2_4.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.СписаниеНМА2_4);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьВнеоборотныеАктивы2_4";
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	СписаниеНМАЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	СписаниеНМАЛокализация.ДобавитьКомандыПечати(КомандыПечати);

КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.СписаниеНМА2_4";
	
	ЗначенияПараметров = ЗначенияПараметровПроведения();
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("НомерНаПечать", """""");
	
	ВЗапросеЕстьИсточник = Истина;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	ИначеЕсли ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';
								|en = 'In document %ПолноеИмяДокумента%, adaptation of request for generating records of register %ИмяРегистра% is not implemented.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов"
		ИЛИ ИмяРегистра = "ДокументыПоНМА" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ВЗапросеЕстьИсточник,
										ПереопределениеРасчетаПараметров);
	Иначе	
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
										ТекстЗапроса,
										ПолноеИмяДокумента,
										СинонимТаблицыДокумента,
										ПереопределениеРасчетаПараметров);
	КонецЕсли; 

	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры)
	
	ПроведениеДокументов.ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДопПараметры);
	
	СформироватьТаблицуВтСписокДокументов(Запрос);
	ПолучитьДанныеДокумента(Запрос);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Период КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	ЗначенияПараметровПроведения = ЗначенияПараметровПроведения(Реквизиты);
	Для каждого КлючИЗначение Из ЗначенияПараметровПроведения Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла; 
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
КонецПроцедуры

Процедура СформироватьТаблицуВтСписокДокументов(Запрос)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СписокДокументов.Ссылка КАК Ссылка,
	|	СписокДокументов.Организация КАК Организация,
	|	СписокДокументов.Дата КАК Период
	|
	|ПОМЕСТИТЬ ВтСписокДокументов
	|
	|ИЗ
	|	Документ.СписаниеНМА2_4 КАК СписокДокументов
	|
	|ГДЕ
	|	СписокДокументов.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПолучитьДанныеДокумента(Запрос)
	
	СписокЗапросов = Новый Массив;
	
	#Область ДанныеДокументаРеквизиты
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                     КАК Ссылка,
	|	ДанныеДокумента.Дата                       КАК Период,
	|	ДанныеДокумента.Номер                      КАК Номер,
	|	ДанныеДокумента.Проведен                   КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления            КАК ПометкаУдаления,
	|	ДанныеДокумента.ОтражатьВРеглУчете         КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете          КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ХозяйственнаяОперация      КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                КАК Организация,
	|	ДанныеДокумента.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеДокумента.Подразделение              КАК Подразделение,
	|	ДанныеДокумента.СтатьяРасходов             КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов          КАК АналитикаРасходов,
	|	ДанныеДокумента.АналитикаАктивовПассивов   КАК АналитикаАктивовПассивов,
	|	ДанныеДокумента.НастройкаСчетовУчета       КАК НастройкаСчетовУчета,
	|	ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА КАК АналитикаСоответствуетСписываемомуНМА,
	|	ДанныеДокумента.ПодразделениеРасходов      КАК ПодразделениеРасходов,
	|	ДанныеДокумента.НаправлениеДеятельностиРасходов КАК НаправлениеДеятельностиРасходов,
	|	ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета КАК ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета,
	|	ДанныеДокумента.Ответственный              КАК Ответственный,
	|	ДанныеДокумента.Комментарий                КАК Комментарий,
	|	НастройкиХозяйственныхОпераций.Ссылка      КАК НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ ДанныеДокументаРеквизиты
	|
	|ИЗ
	|	Документ.СписаниеНМА2_4 КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|		ПО ДанныеДокумента.ХозяйственнаяОперация = НастройкиХозяйственныхОпераций.ХозяйственнаяОперация
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	#Область ДанныеДокументаТаблицаНМА
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаДокумента.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	ТаблицаДокумента.ЛиквидационнаяСтоимостьРегл КАК ЛиквидационнаяСтоимостьРегл,
	|	ТаблицаДокумента.СуммаСписанияУУ КАК СуммаСписанияУУ,
	|	ТаблицаДокумента.СуммаСписанияБУ  КАК СуммаСписанияБУ,
	|	ТаблицаДокумента.СуммаСписанияНУ КАК СуммаСписанияНУ,
	|	ТаблицаДокумента.СуммаСписанияПР КАК СуммаСписанияПР,
	|	ТаблицаДокумента.СуммаСписанияВР КАК СуммаСписанияВР
	|
	|ПОМЕСТИТЬ ДанныеДокументаТаблицаНМА
	|
	|ИЗ
	|	Документ.СписаниеНМА2_4.НМА КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (
	|		ВЫБРАТЬ
	|			ВтСписокДокументов.Ссылка
	|		ИЗ
	|			ВтСписокДокументов КАК ВтСписокДокументов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НематериальныйАктив
	|";
	СписокЗапросов.Добавить(ТекстЗапроса);
	#КонецОбласти
	
	Запрос.Текст = СтрСоединить(СписокЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ЗначенияПараметровПроведения(Реквизиты = Неопределено)
	
	ЗначенияПараметровПроведения = Новый Структура;
	ЗначенияПараметровПроведения.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.СписаниеНМА2_4"));
	ЗначенияПараметровПроведения.Вставить("НазваниеДокумента", НСтр("ru = 'Списание НМА';
																	|en = 'Dispose intangible assets'"));
	ЗначенияПараметровПроведения.Вставить("ИспользоватьУчетПрочихДоходовРасходов", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов"));
	ЗначенияПараметровПроведения.Вставить("ВедетсяРегламентированныйУчетВНА", ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА());
	ЗначенияПараметровПроведения.Вставить("ИдентификаторНеиспользуемойФинЗаписи", ПроведениеДокументов.ИдентификаторНеиспользуемойФинЗаписи());
	
	ЗначенияПараметровПроведения.Вставить("ХО_Стоимость", Перечисления.ХозяйственныеОперации.СписаниеСтоимостиНМА);
	ЗначенияПараметровПроведения.Вставить("ХО_СтоимостьЦФ", Перечисления.ХозяйственныеОперации.СписаниеСтоимостиЦФНМА);
	ЗначенияПараметровПроведения.Вставить("ХО_Амортизация", Перечисления.ХозяйственныеОперации.СписаниеАмортизацииНМА);
	ЗначенияПараметровПроведения.Вставить("ХО_АмортизацияЦФ", Перечисления.ХозяйственныеОперации.СписаниеАмортизацииЦФНМА);
	ЗначенияПараметровПроведения.Вставить("ХО_РезервСтоимость", Перечисления.ХозяйственныеОперации.СписаниеРезерваПереоценкиСтоимостиНМА);
	ЗначенияПараметровПроведения.Вставить("ХО_РезервАмортизация", Перечисления.ХозяйственныеОперации.СписаниеРезерваПереоценкиАмортизацииНМА);
	ЗначенияПараметровПроведения.Вставить("ХО_Обесценение", Перечисления.ХозяйственныеОперации.СписаниеОбесцененияНМА);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_Стоимость", Справочники.НастройкиХозяйственныхОпераций.СписаниеСтоимостиНМА);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_СтоимостьЦФ", Справочники.НастройкиХозяйственныхОпераций.СписаниеСтоимостиЦФНМА);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_Амортизация", Справочники.НастройкиХозяйственныхОпераций.СписаниеАмортизацииНМА);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_АмортизацияЦФ", Справочники.НастройкиХозяйственныхОпераций.СписаниеАмортизацииЦФНМА);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_РезервСтоимость", Справочники.НастройкиХозяйственныхОпераций.СписаниеРезерваПереоценкиСтоимостиНМА);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_РезервАмортизация", Справочники.НастройкиХозяйственныхОпераций.СписаниеРезерваПереоценкиАмортизацииНМА);
	ЗначенияПараметровПроведения.Вставить("НастройкаХО_Обесценение", Справочники.НастройкиХозяйственныхОпераций.СписаниеОбесцененияНМА);
	
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_НМА", ПланыВидовХарактеристик.СтатьиАктивовПассивов.УпрБалансНематериальныеАктивы);
	ЗначенияПараметровПроведения.Вставить("СтатьяАП_ПрибыльУбытки", ПланыВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки);

	Если Реквизиты <> Неопределено Тогда
		ЗначенияПараметровПроведения.Вставить("НомерНаПечать", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.Номер));
	КонецЕсли;
	
	Возврат ЗначенияПараметровПроведения;
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументыПоНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДокументыПоНМА";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ТаблицаНМА.НомерСтроки-1, 0)           КАК НомерЗаписи,
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив                  КАК НематериальныйАктив,
	|	ДанныеДокумента.ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                     КАК Организация,
	|	ДанныеДокумента.Подразделение                   КАК Подразделение,
	|	ДанныеДокумента.Дата                            КАК Дата,
	|	&ИдентификаторМетаданных                        КАК ТипСсылки,
	|	ДанныеДокумента.Проведен                        КАК Проведен,
	|	ДанныеДокумента.ОтражатьВУпрУчете               КАК ОтражатьВУпрУчете,
	|	ДанныеДокумента.ОтражатьВРеглУчете              КАК ОтражатьВРеглУчете
	|
	|ИЗ
	|	Документ.СписаниеНМА2_4 КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеНМА2_4.НМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Ссылка,
	|	ДанныеДокумента.Дата           КАК ДатаДокументаИБ,
	|	ДанныеДокумента.Номер          КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных       КАК ТипСсылки,
	|	ДанныеДокумента.Организация    КАК Организация,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                   КАК НаправлениеДеятельности,
	|	ДанныеДокумента.Подразделение  КАК Подразделение,
	|	ДанныеДокумента.Ответственный  КАК Ответственный,
	|	ДанныеДокумента.Комментарий    КАК Комментарий,
	|	ДанныеДокумента.Проведен       КАК Проведен,
	|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
	|	ЛОЖЬ                           КАК ДополнительнаяЗапись,
	|	ДанныеДокумента.Дата           КАК ДатаПервичногоДокумента,
	|	&НомерНаПечать                 КАК НомерПервичногоДокумента,
	|	ЛОЖЬ                           КАК СторноИсправление,
	|	НЕОПРЕДЕЛЕНО                   КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО                   КАК ИсправляемыйДокумент,
	|	ДанныеДокумента.Дата           КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                   КАК Приоритет
	|
	|ИЗ
	|	Документ.СписаниеНМА2_4 КАК ДанныеДокумента
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПервоначальныеСведенияНМА(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПервоначальныеСведенияНМА";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПервоначальныеСведенияНМА(ТекстыЗапроса);
	
	ТекстЗапроса = СписаниеНМАЛокализация.ТекстЗапросаТаблицаПервоначальныеСведенияНМА();
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка                                             КАК Регистратор,
		|	ДанныеДокумента.Период                                             КАК Период,
		|	ДанныеДокумента.Организация                                        КАК Организация,
		|	ТаблицаНМА.НематериальныйАктив                                     КАК НематериальныйАктив,
		|
		|	ПервоначальныеСведенияНМА.МетодНачисленияАмортизацииБУ             КАК МетодНачисленияАмортизацииБУ,
		|	ПервоначальныеСведенияНМА.ДатаПриобретения                         КАК ДатаПриобретения,
		|	ПервоначальныеСведенияНМА.ДатаПринятияКУчетуУУ                     КАК ДатаПринятияКУчетуУУ,
		|	ПервоначальныеСведенияНМА.ДатаПринятияКУчетуБУ                     КАК ДатаПринятияКУчетуБУ,
		|	ПервоначальныеСведенияНМА.ДокументПринятияКУчетуУУ                 КАК ДокументПринятияКУчетуУУ,
		|	ПервоначальныеСведенияНМА.ДокументПринятияКУчетуБУ                 КАК ДокументПринятияКУчетуБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
		|			ТОГДА ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ - ТаблицаНМА.СуммаСписанияУУ                
		|		ИНАЧЕ ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьУУ
		|	КОНЕЦ КАК ПервоначальнаяСтоимостьУУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
		|			ТОГДА ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ - ТаблицаНМА.СуммаСписанияБУ                
		|		ИНАЧЕ ПервоначальныеСведенияНМА.ПервоначальнаяСтоимостьБУ
		|	КОНЕЦ КАК ПервоначальнаяСтоимостьБУ,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
		|			ТОГДА ДанныеДокумента.Ссылка
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДокументСписания
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
		|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
		|	
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПервоначальныеСведенияНМА КАК ПервоначальныеСведенияНМА
		|		ПО ПервоначальныеСведенияНМА.Ссылка = ТаблицаНМА.Ссылка
		|			И ПервоначальныеСведенияНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|";
		
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииНМАБУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииНМАБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииНМАБУ(ТекстыЗапроса);

	ТекстЗапроса = СписаниеНМАЛокализация.ТекстЗапросаТаблицаПараметрыАмортизацииНМАБУ();
	
	Если ТекстЗапроса = Неопределено Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка                        КАК Регистратор,
		|	ДанныеДокумента.Период                        КАК Период,
		|	ДанныеДокумента.Организация                   КАК Организация,
		|	ДанныеРегистра.НематериальныйАктив            КАК НематериальныйАктив,
		|	ДанныеРегистра.СрокПолезногоИспользованияБУ   КАК СрокПолезногоИспользованияБУ,
		|	ДанныеРегистра.КоэффициентБУ                  КАК КоэффициентБУ,
		|	ТаблицаНМА.ЛиквидационнаяСтоимостьРегл        КАК ЛиквидационнаяСтоимость,
		|	ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
		|	ДанныеРегистра.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
		|	ДанныеРегистра.ДатаПоследнегоИзменения        КАК ДатаПоследнегоИзменения
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
		|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
		|	
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПараметрыАмортизацииНМАБУ КАК ДанныеРегистра
		|		ПО ДанныеРегистра.Ссылка = ТаблицаНМА.Ссылка
		|			И ДанныеРегистра.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|ГДЕ
		|	ДанныеДокумента.ОтражатьВРеглУчете
		|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
		|	И ТаблицаНМА.ЛиквидационнаяСтоимостьРегл <> ДанныеРегистра.ЛиквидационнаяСтоимость
		|";
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПараметрыАмортизацииНМАУУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииНМАУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииНМАУУ(ТекстыЗапроса);

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                         КАК Регистратор,
	|	ДанныеДокумента.Период                         КАК Период,
	|	ДанныеДокумента.Организация                    КАК Организация,
	|	ДанныеРегистра.НематериальныйАктив             КАК НематериальныйАктив,
	|	ДанныеРегистра.СрокИспользования               КАК СрокИспользования,
	|	ДанныеРегистра.КоэффициентУскорения            КАК КоэффициентУскорения,
	|	ДанныеРегистра.МетодНачисленияАмортизации      КАК МетодНачисленияАмортизации,
	|	ТаблицаНМА.ЛиквидационнаяСтоимость             КАК ЛиквидационнаяСтоимость,
	|	ТаблицаНМА.ЛиквидационнаяСтоимостьРегл         КАК ЛиквидационнаяСтоимостьРегл,
	|	ДанныеРегистра.СрокИспользованияДляВычисленияАмортизации   КАК СрокИспользованияДляВычисленияАмортизации,
	|	ДанныеРегистра.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ДанныеРегистра.ДатаПоследнегоИзменения         КАК ДатаПоследнегоИзменения
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втПараметрыАмортизацииНМАУУ КАК ДанныеРегистра
	|		ПО ДанныеРегистра.Ссылка = ТаблицаНМА.Ссылка
	|			И ДанныеРегистра.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВУпрУчете
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|	И (ТаблицаНМА.ЛиквидационнаяСтоимость <> ДанныеРегистра.ЛиквидационнаяСтоимость
	|		ИЛИ ТаблицаНМА.ЛиквидационнаяСтоимостьРегл <> ДанныеРегистра.ЛиквидационнаяСтоимостьРегл)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаНМАБУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаНМАБУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = СписаниеНМАЛокализация.ТекстЗапросаТаблицаПорядокУчетаНМАБУ(ТекстыЗапроса);

	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка           КАК Регистратор,
		|	ДанныеДокумента.Период           КАК Период,
		|	ДанныеДокумента.Организация      КАК Организация,
		|	ТаблицаНМА.НематериальныйАктив   КАК НематериальныйАктив,
		|	ЛОЖЬ                             КАК НачислятьАмортизациюБУ,
		|	ЗНАЧЕНИЕ(Перечисление.СостоянияНМА.Списан) КАК СостояниеБУ
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
		|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
		|
		|ГДЕ
		|	ДанныеДокумента.ОтражатьВРеглУчете
		|	И ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
		|";
		
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаПорядокУчетаНМАУУ(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокУчетаНМАУУ";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Регистратор,
	|	ДанныеДокумента.Период         КАК Период,
	|	ДанныеДокумента.Организация    КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияНМА.Списан) КАК Состояние
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВУпрУчете
	|	И ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаАмортизацияНМА(ТекстыЗапроса)

	ИмяРегистра = "АмортизацияНМА";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтАмортизацияСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"// Списание амортизации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)     КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                     КАК Регистратор,
	|	ДанныеДокумента.Период                     КАК Период,
	|	ДанныеДокумента.Организация                КАК Организация,
	|	ДанныеДокумента.Подразделение              КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив             КАК НематериальныйАктив,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Амортизация 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК Амортизация,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК АмортизацияРегл,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ТаблицаНМА.АмортизацияНУ
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК АмортизацияНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ТаблицаНМА.АмортизацияПР
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК АмортизацияПР,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ТаблицаНМА.АмортизацияВР
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК АмортизацияВР,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ТаблицаНМА.АмортизацияЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК АмортизацияЦФ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ТаблицаНМА.АмортизацияНУЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК АмортизацияНУЦФ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ТаблицаНМА.АмортизацияПРЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК АмортизацияПРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ТаблицаНМА.АмортизацияВРЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК АмортизацияВРЦФ,
	|
	|	0                                          КАК РезервПереоценкиАмортизации,
	|	0                                          КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ТаблицаНМА.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов             КАК КорСтатьяРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
	|			ТОГДА ТаблицаНМА.НематериальныйАктив
	|		ИНАЧЕ ДанныеДокумента.АналитикаРасходов
	|	КОНЕЦ КАК КорАналитикаРасходов,
	|
	|	&ХО_Амортизация                            КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки             КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Амортизация                   КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАмортизацияСписанияНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Списание резерва переоценки
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)     КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                     КАК Регистратор,
	|	ДанныеДокумента.Период                     КАК Период,
	|	ДанныеДокумента.Организация                КАК Организация,
	|	ДанныеДокумента.Подразделение              КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив             КАК НематериальныйАктив,
	|
	|	0                                          КАК Амортизация,
	|	0                                          КАК АмортизацияРегл,
	|	0                                          КАК АмортизацияНУ,
	|	0                                          КАК АмортизацияПР,
	|	0                                          КАК АмортизацияВР,
	|	0                                          КАК АмортизацияЦФ,
	|	0                                          КАК АмортизацияНУЦФ,
	|	0                                          КАК АмортизацияПРЦФ,
	|	0                                          КАК АмортизацияВРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете
	|			ТОГДА ТаблицаНМА.РезервПереоценкиАмортизации
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК РезервПереоценкиАмортизации,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ТаблицаНМА.РезервПереоценкиАмортизацииРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ТаблицаНМА.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов             КАК КорСтатьяРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
	|			ТОГДА ТаблицаНМА.НематериальныйАктив
	|		ИНАЧЕ ДанныеДокумента.АналитикаРасходов
	|	КОНЕЦ КАК КорАналитикаРасходов,
	|
	|	&ХО_РезервАмортизация                      КАК ХозяйственнаяОперация,
	|	
	|	&ИдентификаторНеиспользуемойФинЗаписи      КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО                               КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАмортизацияСписанияНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка 
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВУпрУчете И ТаблицаНМА.РезервПереоценкиАмортизации <> 0
	|		ИЛИ ДанныеДокумента.ОтражатьВРеглУчете И ТаблицаНМА.РезервПереоценкиАмортизацииРегл <> 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаСтоимостьНМА(ТекстыЗапроса)

	ИмяРегистра = "СтоимостьНМА";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтАмортизацияСписанияНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтОбесценениеСписанияНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСтоимостьСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"
	// Списание амортизации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                 КАК Регистратор,
	|	ДанныеДокумента.Период                 КАК Период,
	|	ДанныеДокумента.Организация            КАК Организация,
	|	ДанныеДокумента.Подразделение          КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив         КАК НематериальныйАктив,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Амортизация 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК Стоимость,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияНУ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияПР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьПР,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияВР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьВР,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьЦФ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияНУЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьНУЦФ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияПРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьПРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.АмортизацияВРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьВРЦФ,
	|
	//++ Локализация
	|	0                                      КАК СуммаРасходовНУПриИНВ,
	//-- Локализация
	|	0                                      КАК ПредварительнаяСтоимость,
	|	0                                      КАК ПредварительнаяСтоимостьРегл,
	|	0                                      КАК РезервПереоценкиСтоимости,
	|	0                                      КАК РезервПереоценкиСтоимостиРегл,
	|	0                                      КАК ПриростСтоимости,
	|	0                                      КАК ПриростСтоимостиРегл,
	|	
	|	ТаблицаНМА.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	&ХО_Амортизация                        КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СтатьяРасходов         КАК КорСтатьяРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
	|			ТОГДА ТаблицаНМА.НематериальныйАктив
	|		ИНАЧЕ ДанныеДокумента.АналитикаРасходов
	|	КОНЕЦ КАК КорАналитикаРасходов,
	|
	|	ДанныеДокумента.ОтражатьВРеглУчете     КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете      КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки         КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Амортизация               КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАмортизацияСписанияНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Списание остаточной стоимости
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                 КАК Регистратор,
	|	ДанныеДокумента.Период                 КАК Период,
	|	ДанныеДокумента.Организация            КАК Организация,
	|	ДанныеДокумента.Подразделение          КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив         КАК НематериальныйАктив,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Стоимость 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК Стоимость,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьНУ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьНУ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьПР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьПР,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьВР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьВР,
	|
	|	0                                      КАК СтоимостьЦФ,
	|	0                                      КАК СтоимостьНУЦФ,
	|	0                                      КАК СтоимостьПРЦФ,
	|	0                                      КАК СтоимостьВРЦФ,
	//++ Локализация
	|	0                                      КАК СуммаРасходовНУПриИНВ,
	//-- Локализация
	|	0                                      КАК ПредварительнаяСтоимость,
	|	0                                      КАК ПредварительнаяСтоимостьРегл,
	|
	|	0                                      КАК РезервПереоценкиСтоимости,
	|	0                                      КАК РезервПереоценкиСтоимостиРегл,
	|
	|	0                                      КАК ПриростСтоимости,
	|	0                                      КАК ПриростСтоимостиРегл,
	|	
	|	ТаблицаНМА.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	&ХО_Стоимость                          КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СтатьяРасходов         КАК КорСтатьяРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
	|			ТОГДА ТаблицаНМА.НематериальныйАктив
	|		ИНАЧЕ ДанныеДокумента.АналитикаРасходов
	|	КОНЕЦ КАК КорАналитикаРасходов,
	|
	|	ДанныеДокумента.ОтражатьВРеглУчете     КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете      КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки         КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Стоимость                 КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстаточнаяСтоимостьНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	(ДанныеДокумента.ОтражатьВУпрУчете 
	|			И ТаблицаНМА.Стоимость <> 0
	|		ИЛИ ДанныеДокумента.ОтражатьВРеглУчете
	|			И (ТаблицаНМА.СтоимостьРегл <> 0
	|				ИЛИ ТаблицаНМА.СтоимостьНУ <> 0
	|				ИЛИ ТаблицаНМА.СтоимостьПР <> 0
	|				ИЛИ ТаблицаНМА.СтоимостьВР <> 0))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                 КАК Регистратор,
	|	ДанныеДокумента.Период                 КАК Период,
	|	ДанныеДокумента.Организация            КАК Организация,
	|	ДанныеДокумента.Подразделение          КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив         КАК НематериальныйАктив,
	|
	|	0                                      КАК Стоимость,
	|	0                                      КАК СтоимостьРегл,
	|	0                                      КАК СтоимостьНУ,
	|	0                                      КАК СтоимостьПР,
	|	0                                      КАК СтоимостьВР,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьЦФ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьНУЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьНУЦФ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьПРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьПРЦФ,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьВРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СтоимостьВРЦФ,
	|
	//++ Локализация
	|	0                                      КАК СуммаРасходовНУПриИНВ,
	//-- Локализация
	|	0                                      КАК ПредварительнаяСтоимость,
	|	0                                      КАК ПредварительнаяСтоимостьРегл,
	|
	|	0                                      КАК РезервПереоценкиСтоимости,
	|	0                                      КАК РезервПереоценкиСтоимостиРегл,
	|
	|	0                                      КАК ПриростСтоимости,
	|	0                                      КАК ПриростСтоимостиРегл,
	|	
	|	ТаблицаНМА.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	&ХО_СтоимостьЦФ                        КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СтатьяРасходов         КАК КорСтатьяРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
	|			ТОГДА ТаблицаНМА.НематериальныйАктив
	|		ИНАЧЕ ДанныеДокумента.АналитикаРасходов
	|	КОНЕЦ КАК КорАналитикаРасходов,
	|
	|	ДанныеДокумента.ОтражатьВРеглУчете     КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете      КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки         КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_СтоимостьЦФ               КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстаточнаяСтоимостьНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВРеглУчете
	|	И (ТаблицаНМА.СтоимостьЦФ <> 0
	|		ИЛИ ТаблицаНМА.СтоимостьНУЦФ <> 0
	|		ИЛИ ТаблицаНМА.СтоимостьПРЦФ <> 0
	|		ИЛИ ТаблицаНМА.СтоимостьВРЦФ <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Списание резерва переоценки
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                   КАК Регистратор,
	|	ДанныеДокумента.Период                   КАК Период,
	|	ДанныеДокумента.Организация              КАК Организация,
	|	ДанныеДокумента.Подразделение            КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив           КАК НематериальныйАктив,
	|
	|	0                                        КАК Стоимость,
	|	0                                        КАК СтоимостьРегл,
	|	0                                        КАК СтоимостьНУ,
	|	0                                        КАК СтоимостьПР,
	|	0                                        КАК СтоимостьВР,
	|	0                                        КАК СтоимостьЦФ,
	|	0                                        КАК СтоимостьНУЦФ,
	|	0                                        КАК СтоимостьПРЦФ,
	|	0                                        КАК СтоимостьВРЦФ,
	//++ Локализация
	|	0                                        КАК СуммаРасходовНУПриИНВ,
	//-- Локализация
	|	0                                        КАК ПредварительнаяСтоимость,
	|	0                                        КАК ПредварительнаяСтоимостьРегл,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиСтоимости 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК РезервПереоценкиСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиСтоимостиРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	0                                        КАК ПриростСтоимости,
	|	0                                        КАК ПриростСтоимостиРегл,
	|
	|	ТаблицаНМА.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	&ХО_РезервСтоимость                      КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СтатьяРасходов           КАК КорСтатьяРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
	|			ТОГДА ТаблицаНМА.НематериальныйАктив
	|		ИНАЧЕ ДанныеДокумента.АналитикаРасходов
	|	КОНЕЦ КАК КорАналитикаРасходов,
	|
	|	ДанныеДокумента.ОтражатьВРеглУчете       КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете        КАК ОтражатьВУпрУчете,
	|	
	|	&ИдентификаторНеиспользуемойФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезерваПереоценкиСтоимостиНМА) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтоимостьСписанияНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ТаблицаНМА.РезервПереоценкиСтоимости <> 0
	|		ИЛИ ТаблицаНМА.РезервПереоценкиСтоимостиРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)   КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                   КАК Регистратор,
	|	ДанныеДокумента.Период                   КАК Период,
	|	ДанныеДокумента.Организация              КАК Организация,
	|	ДанныеДокумента.Подразделение            КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив           КАК НематериальныйАктив,
	|
	|	0                                        КАК Стоимость,
	|	0                                        КАК СтоимостьРегл,
	|	0                                        КАК СтоимостьНУ,
	|	0                                        КАК СтоимостьПР,
	|	0                                        КАК СтоимостьВР,
	|	0                                        КАК СтоимостьЦФ,
	|	0                                        КАК СтоимостьНУЦФ,
	|	0                                        КАК СтоимостьПРЦФ,
	|	0                                        КАК СтоимостьВРЦФ,
	//++ Локализация
	|	0                                        КАК СуммаРасходовНУПриИНВ,
	//-- Локализация
	|	0                                        КАК ПредварительнаяСтоимость,
	|	0                                        КАК ПредварительнаяСтоимостьРегл,
	|	0                                        КАК РезервПереоценкиСтоимости,
	|	0                                        КАК РезервПереоценкиСтоимостиРегл,
	|	
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.ПриростСтоимости 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ПриростСтоимости,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.ПриростСтоимостиРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ПриростСтоимостиРегл,
	|
	|	ТаблицаНМА.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	&ХО_РезервСтоимость                      КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СтатьяРасходов           КАК КорСтатьяРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
	|			ТОГДА ТаблицаНМА.НематериальныйАктив
	|		ИНАЧЕ ДанныеДокумента.АналитикаРасходов
	|	КОНЕЦ КАК КорАналитикаРасходов,
	|
	|	ДанныеДокумента.ОтражатьВРеглУчете       КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете        КАК ОтражатьВУпрУчете,
	|	
	|	&ИдентификаторНеиспользуемойФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СписаниеРезерваПереоценкиСтоимостиНМА) КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтоимостьСписанияНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|ГДЕ
	|	ТаблицаНМА.РезервПереоценкиСтоимости <> 0
	|		ИЛИ ТаблицаНМА.РезервПереоценкиСтоимостиРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Списание прочих сумм
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                   КАК Регистратор,
	|	ДанныеДокумента.Период                   КАК Период,
	|	ДанныеДокумента.Организация              КАК Организация,
	|	ДанныеДокумента.Подразделение            КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив           КАК НематериальныйАктив,
	|
	|	0                                        КАК Стоимость,
	|	0                                        КАК СтоимостьРегл,
	|	0                                        КАК СтоимостьНУ,
	|	0                                        КАК СтоимостьПР,
	|	0                                        КАК СтоимостьВР,
	|	0                                        КАК СтоимостьЦФ,
	|	0                                        КАК СтоимостьНУЦФ,
	|	0                                        КАК СтоимостьПРЦФ,
	|	0                                        КАК СтоимостьВРЦФ,
	|
	//++ Локализация
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА втСтоимостьСписанияНМА.СуммаРасходовНУПриИНВ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаРасходовНУПриИНВ,
	//-- Локализация
	|	
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете
	|				И ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|			ТОГДА СтоимостьНМА.ПредварительнаяСтоимость 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ПредварительнаяСтоимость,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|				И ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|			ТОГДА СтоимостьНМА.ПредварительнаяСтоимостьРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ПредварительнаяСтоимостьРегл,
	|
	|	0                                        КАК РезервПереоценкиСтоимости,
	|	0                                        КАК РезервПереоценкиСтоимостиРегл,
	|	0                                        КАК ПриростСтоимости,
	|	0                                        КАК ПриростСтоимостиРегл,
	|	
	|	ТаблицаНМА.ГруппаФинансовогоУчета        КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	ДанныеДокумента.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                             КАК КорСтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                             КАК КорАналитикаРасходов,
	|	ДанныеДокумента.ОтражатьВРеглУчете       КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете        КАК ОтражатьВУпрУчете,
	|	
	|	&ИдентификаторНеиспользуемойФинЗаписи   КАК ИдентификаторФинЗаписи,
	|	НЕОПРЕДЕЛЕНО                            КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка 
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	//++ Локализация
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтоимостьСписанияНМА КАК втСтоимостьСписанияНМА
	|		ПО втСтоимостьСписанияНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И втСтоимостьСписанияНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	//-- Локализация
	|
	|ГДЕ
	|	ДанныеДокумента.ОтражатьВУпрУчете
	|			И ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|			И СтоимостьНМА.ПредварительнаяСтоимость <> 0
	|		ИЛИ ДанныеДокумента.ОтражатьВРеглУчете
	|			И ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|			И СтоимостьНМА.ПредварительнаяСтоимостьРегл <> 0
	//++ Локализация
	|		ИЛИ ДанныеДокумента.ОтражатьВРеглУчете
	|			И втСтоимостьСписанияНМА.СуммаРасходовНУПриИНВ <> 0
	//-- Локализация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Списание обесценения
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)     КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                     КАК Регистратор,
	|	ДанныеДокумента.Период                     КАК Период,
	|	ДанныеДокумента.Организация                КАК Организация,
	|	ДанныеДокумента.Подразделение              КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив             КАК НематериальныйАктив,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.ОбесценениеУпр 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                      КАК Стоимость,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.ОбесценениеРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ                                      КАК СтоимостьРегл,
	|
	|	0                                          КАК СтоимостьНУ,
	|	0                                          КАК СтоимостьПР,
	|	0                                          КАК СтоимостьВР,
	|	0                                          КАК СтоимостьЦФ,
	|	0                                          КАК СтоимостьНУЦФ,
	|	0                                          КАК СтоимостьПРЦФ,
	|	0                                          КАК СтоимостьВРЦФ,
	|
	//++ Локализация
	|	0                                          КАК СуммаРасходовНУПриИНВ,
	//-- Локализация
	|	0                                          КАК ПредварительнаяСтоимость,
	|	0                                          КАК ПредварительнаяСтоимостьРегл,
	|
	|	0                                          КАК РезервПереоценкиСтоимости,
	|	0                                          КАК РезервПереоценкиСтоимостиРегл,
	|	0                                          КАК ПриростСтоимости,
	|	0                                          КАК ПриростСтоимостиРегл,
	|	
	|	ТаблицаНМА.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	&ХО_Обесценение                            КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.СтатьяРасходов             КАК КорСтатьяРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
	|			ТОГДА ТаблицаНМА.НематериальныйАктив
	|		ИНАЧЕ ДанныеДокумента.АналитикаРасходов
	|	КОНЕЦ КАК КорАналитикаРасходов,
	|
	|	ДанныеДокумента.ОтражатьВРеглУчете         КАК ОтражатьВРеглУчете,
	|	ДанныеДокумента.ОтражатьВУпрУчете          КАК ОтражатьВУпрУчете,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки             КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Обесценение                   КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОбесценениеСписанияНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаПрочиеРасходы(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДопПоля = "Регистратор";

	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса, ДопПоля);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы(ДопПоля, Истина);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(ТекстыЗапроса, Регистры = Неопределено)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ДопПоля = "Регистратор";

	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(ТекстыЗапроса, ДопПоля);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов(ДопПоля);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтАмортизацияСписанияНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСтоимостьСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)         КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                         КАК Регистратор,
	|	ДанныеДокумента.Период                         КАК Период,
	|	&ХО_Стоимость                                  КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация                    КАК Организация,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаНМА.Подразделение
	|		ИНАЧЕ ДанныеДокумента.ПодразделениеРасходов
	|	КОНЕЦ КАК Подразделение,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаНМА.НаправлениеДеятельности
	|		ИНАЧЕ ДанныеДокумента.НаправлениеДеятельностиРасходов
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	ДанныеДокумента.СтатьяРасходов                 КАК Статья,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
	|			ТОГДА ТаблицаНМА.НематериальныйАктив
	|		ИНАЧЕ ДанныеДокумента.АналитикаАктивовПассивов
	|	КОНЕЦ КАК Аналитика,
	|
	|	ТаблицаНМА.Стоимость                           КАК Сумма
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаточнаяСтоимостьНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|
	|ГДЕ
	|	ТаблицаНМА.Стоимость <> 0
	|	И ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Резерв переоценки амортизации
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                 КАК Регистратор,
	|	ДанныеДокумента.Период                 КАК Период,
	|	&ХО_РезервАмортизация                  КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация            КАК Организация,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаНМА.Подразделение
	|		ИНАЧЕ ДанныеДокумента.ПодразделениеРасходов
	|	КОНЕЦ КАК Подразделение,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаНМА.НаправлениеДеятельности
	|		ИНАЧЕ ДанныеДокумента.НаправлениеДеятельностиРасходов
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	&СтатьяАП_НМА                          КАК Статья,
	|	ТаблицаНМА.НематериальныйАктив         КАК Аналитика,
	|
	|	ТаблицаНМА.РезервПереоценкиАмортизации КАК Сумма
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАмортизацияСписанияНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|ГДЕ
	|	ТаблицаНМА.РезервПереоценкиАмортизации <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Резерв переоценки стоимости
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                 КАК Регистратор,
	|	ДанныеДокумента.Период                 КАК Период,
	|	&ХО_РезервСтоимость                    КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация            КАК Организация,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаНМА.Подразделение
	|		ИНАЧЕ ДанныеДокумента.ПодразделениеРасходов
	|	КОНЕЦ КАК Подразделение,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаНМА.НаправлениеДеятельности
	|		ИНАЧЕ ДанныеДокумента.НаправлениеДеятельностиРасходов
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	&СтатьяАП_НМА                          КАК Статья,
	|	ТаблицаНМА.НематериальныйАктив         КАК Аналитика,
	|
	|	ТаблицаНМА.РезервПереоценкиСтоимости   КАК Сумма
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСтоимостьСписанияНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|
	|ГДЕ
	|	ТаблицаНМА.РезервПереоценкиСтоимости <> 0
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтАмортизацияСписанияНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСтоимостьСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                   КАК Регистратор,
	|	ДанныеДокумента.Период                   КАК Период,
	|	ДанныеДокумента.Организация              КАК Организация,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаНМА.Подразделение
	|		ИНАЧЕ ДанныеДокумента.ПодразделениеРасходов
	|	КОНЕЦ КАК Подразделение,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаНМА.НаправлениеДеятельности
	|		ИНАЧЕ ДанныеДокумента.НаправлениеДеятельностиРасходов
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	ДанныеДокумента.СтатьяРасходов           КАК Статья,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
	|			ТОГДА ТаблицаНМА.НематериальныйАктив
	|		ИНАЧЕ ДанныеДокумента.АналитикаАктивовПассивов
	|	КОНЕЦ КАК Аналитика,
	|
	|	ДанныеДокумента.НастройкаСчетовУчета     КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьРегл + ТаблицаНМА.СтоимостьЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаРегл,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьПР + ТаблицаНМА.СтоимостьПРЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ПостояннаяРазница,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьВР + ТаблицаНМА.СтоимостьВРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ВременнаяРазница,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Стоимость
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаУпр,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Стоимость
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаСНДС,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Стоимость
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	ТаблицаНМА.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Стоимость КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстаточнаяСтоимостьНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|
	|ГДЕ
	|	(ТаблицаНМА.Стоимость <> 0 ИЛИ ТаблицаНМА.СтоимостьРегл <> 0 ИЛИ ТаблицаНМА.СтоимостьЦФ <> 0)
	|	И ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Резерв переоценки амортизации
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка             КАК Регистратор,
	|	ДанныеДокумента.Период             КАК Период,
	|	ДанныеДокумента.Организация        КАК Организация,
	|	ДанныеДокумента.Подразделение      КАК Подразделение,
	|	ТаблицаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА                      КАК Статья,
	|	ТаблицаНМА.НематериальныйАктив     КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка) КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиАмортизацииРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиАмортизации
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиАмортизации
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаСНДС,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиАмортизации
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	&ИдентификаторНеиспользуемойФинЗаписи КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_РезервАмортизация КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАмортизацияСписанияНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|
	|ГДЕ
	|	ТаблицаНМА.РезервПереоценкиАмортизации <> 0 ИЛИ ТаблицаНМА.РезервПереоценкиАмортизацииРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Резерв переоценки стоимости
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка             КАК Регистратор,
	|	ДанныеДокумента.Период             КАК Период,
	|	ДанныеДокумента.Организация        КАК Организация,
	|	ДанныеДокумента.Подразделение      КАК Подразделение,
	|	ТаблицаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяАП_НМА                      КАК Статья,
	|	ТаблицаНМА.НематериальныйАктив     КАК Аналитика,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиСчетовУчетаПрочихОпераций.ПустаяСсылка) КАК НастройкаСчетовУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит) КАК ДебетКредит,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиСтоимостиРегл
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиСтоимости
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаУпр,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиСтоимости
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаСНДС,
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.РезервПереоценкиСтоимости
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	&ИдентификаторНеиспользуемойФинЗаписи КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_РезервСтоимость КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтоимостьСписанияНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|
	|ГДЕ
	|	ТаблицаНМА.РезервПереоценкиСтоимости <> 0 ИЛИ ТаблицаНМА.РезервПереоценкиСтоимостиРегл <> 0
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = СписаниеНМАЛокализация.ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ТекстЗапросаТаблицаВтОстаточнаяСтоимостьНМА(ТекстыЗапроса);
		
		ТекстЗапроса =
		"// Списание амортизации
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка             КАК Регистратор,
		|	ДанныеДокумента.Период             КАК Период,
		|	&ХО_Амортизация                    КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.Организация        КАК Организация,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
		|			ТОГДА ТаблицаНМА.Подразделение
		|		ИНАЧЕ ДанныеДокумента.ПодразделениеРасходов
		|	КОНЕЦ КАК Подразделение,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
		|			ТОГДА ТаблицаНМА.НаправлениеДеятельности
		|		ИНАЧЕ ДанныеДокумента.НаправлениеДеятельностиРасходов
		|	КОНЕЦ КАК НаправлениеДеятельности,
		|
		|	ДанныеДокумента.СтатьяРасходов     КАК Статья,
		|	НЕОПРЕДЕЛЕНО                       КАК АналитикаДоходов,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
		|			ТОГДА ТаблицаНМА.НематериальныйАктив
		|		ИНАЧЕ ДанныеДокумента.АналитикаРасходов
		|	КОНЕЦ КАК АналитикаРасходов,
		|
		|	ТаблицаНМА.НематериальныйАктив     КАК АналитикаАктивовПассивов,
		|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
		|
		|	ДанныеДокумента.Подразделение      КАК КорПодразделение,
		|	ТаблицаНМА.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|	&СтатьяАП_НМА                      КАК КорСтатья,
		|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО                       КАК КорАналитикаРасходов,
		|	ТаблицаНМА.НематериальныйАктив     КАК КорАналитикаАктивовПассивов,
		|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
		|
		|	ВЫБОР 
		|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
		|			ТОГДА ТаблицаНМА.Амортизация 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК Сумма,
		|
		|	ВЫБОР 
		|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
		|			ТОГДА ТаблицаНМА.Амортизация 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК СуммаУпр,
		|
		|	ВЫБОР 
		|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
		|			ТОГДА ТаблицаНМА.АмортизацияРегл + ТаблицаНМА.АмортизацияЦФ 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК СуммаРегл
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втАмортизацияСписанияНМА КАК ТаблицаНМА
		|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|// Списание остаточной стоимости
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Ссылка КАК Регистратор,
		|	ВложенныйЗапрос.Период КАК Период,
		|	ВложенныйЗапрос.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|
		|	ВложенныйЗапрос.Подразделение КАК Подразделение,
		|	ВложенныйЗапрос.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ВложенныйЗапрос.Статья КАК Статья,
		|	ВложенныйЗапрос.АналитикаДоходов КАК АналитикаДоходов,
		|	ВложенныйЗапрос.АналитикаРасходов КАК АналитикаРасходов,
		|	ВложенныйЗапрос.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
		|	ВложенныйЗапрос.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
		|
		|	ВложенныйЗапрос.КорПодразделение КАК КорПодразделение,
		|	ВложенныйЗапрос.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|	ВложенныйЗапрос.КорСтатья КАК КорСтатья,
		|	ВложенныйЗапрос.КорАналитикаДоходов КАК КорАналитикаДоходов,
		|	ВложенныйЗапрос.КорАналитикаРасходов КАК КорАналитикаРасходов,
		|	ВложенныйЗапрос.КорАналитикаАктивовПассивов КАК КорАналитикаАктивовПассивов,
		|	ВложенныйЗапрос.КорГруппаФинансовогоУчета КАК КорГруппаФинансовогоУчета,
		|
		|	ВЫБОР 
		|		КОГДА ВложенныйЗапрос.ОтражатьВУпрУчете 
		|			ТОГДА СУММА(ВложенныйЗапрос.Сумма) 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК Сумма,
		|
		|	ВЫБОР 
		|		КОГДА ВложенныйЗапрос.ОтражатьВУпрУчете 
		|			ТОГДА СУММА(ВложенныйЗапрос.СуммаУпр) 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК СуммаУпр,
		|
		|	ВЫБОР 
		|		КОГДА ВложенныйЗапрос.ОтражатьВРеглУчете 
		|			ТОГДА СУММА(ВложенныйЗапрос.СуммаРегл) 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК СуммаРегл
		|
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеДокумента.Ссылка             КАК Ссылка,
		|		ДанныеДокумента.Период             КАК Период,
		|		&ХО_Стоимость                      КАК ХозяйственнаяОперация,
		|		ДанныеДокумента.Организация        КАК Организация,
		|		ДанныеДокумента.ОтражатьВУпрУчете  КАК ОтражатьВУпрУчете,
		|		ДанныеДокумента.ОтражатьВРеглУчете КАК ОтражатьВРеглУчете,
		|
		|		ДанныеДокумента.Подразделение      КАК Подразделение,
		|		ТаблицаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|
		|		&СтатьяАП_НМА                      КАК Статья,
		|		НЕОПРЕДЕЛЕНО                       КАК АналитикаДоходов,
		|		НЕОПРЕДЕЛЕНО                       КАК АналитикаРасходов,
		|		ТаблицаНМА.НематериальныйАктив     КАК АналитикаАктивовПассивов,
		|		ТаблицаНМА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
		|
		|		ВЫБОР
		|			КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
		|				ТОГДА ТаблицаНМА.Подразделение
		|			ИНАЧЕ ДанныеДокумента.ПодразделениеРасходов
		|		КОНЕЦ КАК КорПодразделение,
		|
		|		ВЫБОР
		|			КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
		|				ТОГДА ТаблицаНМА.НаправлениеДеятельности
		|			ИНАЧЕ ДанныеДокумента.НаправлениеДеятельностиРасходов
		|		КОНЕЦ КАК КорНаправлениеДеятельности,
		|
		|		ДанныеДокумента.СтатьяРасходов     КАК КорСтатья,
		|		НЕОПРЕДЕЛЕНО                       КАК КорАналитикаДоходов,
		|
		|		ВЫБОР
		|			КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
		|				ТОГДА ТаблицаНМА.НематериальныйАктив
		|			ИНАЧЕ ДанныеДокумента.АналитикаРасходов
		|		КОНЕЦ КАК КорАналитикаРасходов,
		|
		|		НЕОПРЕДЕЛЕНО                       КАК КорАналитикаАктивовПассивов,
		|		ТаблицаНМА.ГруппаФинансовогоУчета  КАК КорГруппаФинансовогоУчета,
		|
		|		ТаблицаНМА.Стоимость               КАК Сумма,
		|		ТаблицаНМА.Стоимость               КАК СуммаУпр,
		|		ТаблицаНМА.СтоимостьРегл + ТаблицаНМА.СтоимостьЦФ КАК СуммаРегл
		|	ИЗ
		|		ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ втОстаточнаяСтоимостьНМА КАК ТаблицаНМА
		|			ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
		|
		|	) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Ссылка,
		|	ВложенныйЗапрос.Период,
		|	ВложенныйЗапрос.ХозяйственнаяОперация,
		|	ВложенныйЗапрос.Организация,
		|	ВложенныйЗапрос.ОтражатьВУпрУчете,
		|	ВложенныйЗапрос.ОтражатьВРеглУчете,
		|
		|	ВложенныйЗапрос.Подразделение,
		|	ВложенныйЗапрос.НаправлениеДеятельности,
		|	ВложенныйЗапрос.Статья,
		|	ВложенныйЗапрос.АналитикаДоходов,
		|	ВложенныйЗапрос.АналитикаРасходов,
		|	ВложенныйЗапрос.АналитикаАктивовПассивов,
		|	ВложенныйЗапрос.ГруппаФинансовогоУчета,
		|
		|	ВложенныйЗапрос.КорПодразделение,
		|	ВложенныйЗапрос.КорНаправлениеДеятельности,
		|	ВложенныйЗапрос.КорСтатья,
		|	ВложенныйЗапрос.КорАналитикаДоходов,
		|	ВложенныйЗапрос.КорАналитикаРасходов,
		|	ВложенныйЗапрос.КорАналитикаАктивовПассивов,
		|	ВложенныйЗапрос.КорГруппаФинансовогоУчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|// Списание резерва переоценки амортизации НМА
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка                  КАК Регистратор,
		|	ДанныеДокумента.Период                  КАК Период,
		|	&ХО_РезервАмортизация                   КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.Организация             КАК Организация,
		|
		|	ДанныеДокумента.Подразделение           КАК Подразделение,
		|	ТаблицаНМА.НаправлениеДеятельности      КАК НаправлениеДеятельности,
		|
		|	&СтатьяАП_НМА                           КАК Статья,
		|	НЕОПРЕДЕЛЕНО                            КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
		|	ТаблицаНМА.НематериальныйАктив          КАК АналитикаАктивовПассивов,
		|	ТаблицаНМА.ГруппаФинансовогоУчета       КАК ГруппаФинансовогоУчета,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
		|			ТОГДА ТаблицаНМА.Подразделение
		|		ИНАЧЕ ДанныеДокумента.ПодразделениеРасходов
		|	КОНЕЦ КАК КорПодразделение,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
		|			ТОГДА ТаблицаНМА.НаправлениеДеятельности
		|		ИНАЧЕ ДанныеДокумента.НаправлениеДеятельностиРасходов
		|	КОНЕЦ КАК КорНаправлениеДеятельности,
		|
		|	&СтатьяАП_ПрибыльУбытки                 КАК КорСтатья,
		|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО                            КАК КорАналитикаАктивовПассивов,
		|	ТаблицаНМА.ГруппаФинансовогоУчета       КАК КорГруппаФинансовогоУчета,
		|
		|	ВЫБОР 
		|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
		|			ТОГДА ТаблицаНМА.РезервПереоценкиАмортизации 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК Сумма,
		|
		|	ВЫБОР 
		|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
		|			ТОГДА ТаблицаНМА.РезервПереоценкиАмортизации 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК СуммаУпр,
		|
		|	ВЫБОР 
		|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
		|			ТОГДА ТаблицаНМА.РезервПереоценкиАмортизацииРегл 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК СуммаРегл
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втАмортизацияСписанияНМА КАК ТаблицаНМА
		|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
		|
		|ГДЕ
		|	ДанныеДокумента.ОтражатьВУпрУчете И ТаблицаНМА.РезервПереоценкиАмортизации <> 0
		|		ИЛИ ДанныеДокумента.ОтражатьВРеглУчете И ТаблицаНМА.РезервПереоценкиАмортизацииРегл <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|// Списание резерва переоценки стоимости НМА
		|ВЫБРАТЬ
		|	ДанныеДокумента.Ссылка                 КАК Регистратор,
		|	ДанныеДокумента.Период                 КАК Период,
		|	&ХО_РезервСтоимость                    КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.Организация            КАК Организация,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
		|			ТОГДА ТаблицаНМА.Подразделение
		|		ИНАЧЕ ДанныеДокумента.ПодразделениеРасходов
		|	КОНЕЦ КАК Подразделение,
		|
		|	ВЫБОР
		|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
		|			ТОГДА ТаблицаНМА.НаправлениеДеятельности
		|		ИНАЧЕ ДанныеДокумента.НаправлениеДеятельностиРасходов
		|	КОНЕЦ КАК НаправлениеДеятельности,
		|
		|	&СтатьяАП_ПрибыльУбытки                КАК Статья,
		|	НЕОПРЕДЕЛЕНО                           КАК АналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО                           КАК АналитикаРасходов,
		|	НЕОПРЕДЕЛЕНО                           КАК АналитикаАктивовПассивов,
		|	ТаблицаНМА.ГруппаФинансовогоУчета      КАК ГруппаФинансовогоУчета,
		|
		|	ДанныеДокумента.Подразделение          КАК КорПодразделение,
		|	ТаблицаНМА.НаправлениеДеятельности     КАК КорНаправлениеДеятельности,
		|	&СтатьяАП_НМА                          КАК КорСтатья,
		|	НЕОПРЕДЕЛЕНО                           КАК КорАналитикаДоходов,
		|	НЕОПРЕДЕЛЕНО                           КАК КорАналитикаРасходов,
		|	ТаблицаНМА.НематериальныйАктив         КАК КорАналитикаАктивовПассивов,
		|	ТаблицаНМА.ГруппаФинансовогоУчета      КАК КорГруппаФинансовогоУчета,
		|
		|	ВЫБОР 
		|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
		|			ТОГДА ТаблицаНМА.РезервПереоценкиСтоимости 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК Сумма,
		|
		|	ВЫБОР 
		|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
		|			ТОГДА ТаблицаНМА.РезервПереоценкиСтоимости 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК СуммаУпр,
		|
		|	ВЫБОР 
		|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
		|			ТОГДА ТаблицаНМА.РезервПереоценкиСтоимостиРегл 
		|		ИНАЧЕ 0 
		|	КОНЕЦ КАК СуммаРегл
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втСтоимостьСписанияНМА КАК ТаблицаНМА
		|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
		|
		|ГДЕ
		|	ДанныеДокумента.ОтражатьВУпрУчете И ТаблицаНМА.РезервПереоценкиСтоимости <> 0
		|		ИЛИ ДанныеДокумента.ОтражатьВРеглУчете И ТаблицаНМА.РезервПереоценкиСтоимостиРегл <> 0
		|";

	КонецЕсли; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаОбесценениеВНА(ТекстыЗапроса)

	ИмяРегистра = "ОбесценениеВНА";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтОбесценениеСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"// Списание обесценения
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)     КАК ВидДвижения,
	|	ДанныеДокумента.Ссылка                     КАК Регистратор,
	|	ДанныеДокумента.Период                     КАК Период,
	|	ДанныеДокумента.Организация                КАК Организация,
	|	ДанныеДокумента.Подразделение              КАК Подразделение,
	|	ТаблицаНМА.НематериальныйАктив             КАК ВнеоборотныйАктив,
	|	ТаблицаНМА.ЕГДС                            КАК ЕГДС,
	|
	|	ТаблицаНМА.ОбесценениеУпр                  КАК ОбесценениеУпр,
	|	ТаблицаНМА.ОбесценениеРегл                 КАК ОбесценениеРегл,
	|
	|	ТаблицаНМА.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ДанныеДокумента.СтатьяРасходов             КАК КорСтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов          КАК КорАналитикаРасходов,
	|	&ХО_Обесценение                            КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки             КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Обесценение                   КАК НастройкаХозяйственнойОперации
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОбесценениеСписанияНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса, ДопПоля)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьНМА(ТекстыЗапроса);
	
	СписокЗапросовОбъединение = Новый Массив;
	СписокЗапросовОбъединение.Добавить(РегистрыНакопления.ПрочиеРасходы.ТекстОписаниеВтИсходныеПрочиеРасходы(ДопПоля));

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период                            КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)            КАК ВидДвижения,
	|	ДанныеДокумента.Организация                       КАК Организация,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаНМА.Подразделение
	|		ИНАЧЕ ДанныеДокумента.ПодразделениеРасходов
	|	КОНЕЦ КАК Подразделение,
	|
	|	ДанныеДокумента.СтатьяРасходов                    КАК СтатьяРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
	|			ТОГДА ТаблицаНМА.НематериальныйАктив
	|		ИНАЧЕ ДанныеДокумента.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаНМА.НаправлениеДеятельности
	|		ИНАЧЕ ДанныеДокумента.НаправлениеДеятельностиРасходов
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	НЕОПРЕДЕЛЕНО                                      КАК ВидДеятельностиНДС,
	|	
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Стоимость 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаСНДС,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Стоимость 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаБезНДС,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете 
	|			ТОГДА ТаблицаНМА.Стоимость  
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаСНДСРегл,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьРегл 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьПР
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ПостояннаяРазница,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьВР 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ВременнаяРазница,
	|	
	|	&ХО_Стоимость КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_Стоимость КАК НастройкаХозяйственнойОперации
	|
	|	,ДанныеДокумента.Ссылка КАК Регистратор
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстаточнаяСтоимостьНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|
	|	И (ДанныеДокумента.ОтражатьВУпрУчете 
	|			И ТаблицаНМА.Стоимость <> 0
	|		ИЛИ ДанныеДокумента.ОтражатьВРеглУчете
	|			И (ТаблицаНМА.СтоимостьРегл <> 0
	|				ИЛИ ТаблицаНМА.СтоимостьПР <> 0
	|				ИЛИ ТаблицаНМА.СтоимостьВР <> 0))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДокумента.Период                            КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)            КАК ВидДвижения,
	|	ДанныеДокумента.Организация                       КАК Организация,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаНМА.Подразделение
	|		ИНАЧЕ ДанныеДокумента.ПодразделениеРасходов
	|	КОНЕЦ КАК Подразделение,
	|
	|	ДанныеДокумента.СтатьяРасходов                    КАК СтатьяРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
	|			ТОГДА ТаблицаНМА.НематериальныйАктив
	|		ИНАЧЕ ДанныеДокумента.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаНМА.НаправлениеДеятельности
	|		ИНАЧЕ ДанныеДокумента.НаправлениеДеятельностиРасходов
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	НЕОПРЕДЕЛЕНО                                      КАК ВидДеятельностиНДС,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	0 КАК СуммаБезНДСУпр,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаСНДСРегл,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьПРЦФ
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ПостояннаяРазница,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете 
	|			ТОГДА ТаблицаНМА.СтоимостьВРЦФ 
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК ВременнаяРазница,
	|	
	|	&ХО_Стоимость КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	&НастройкаХО_СтоимостьЦФ КАК НастройкаХозяйственнойОперации
	|
	|	,ДанныеДокумента.Ссылка КАК Регистратор
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстаточнаяСтоимостьНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеДокумента.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиРасходов)
	|
	|	И ДанныеДокумента.ОтражатьВРеглУчете
	|		И (ТаблицаНМА.СтоимостьЦФ <> 0
	|			ИЛИ ТаблицаНМА.СтоимостьПРЦФ <> 0
	|			ИЛИ ТаблицаНМА.СтоимостьВРЦФ <> 0)
	|";
	СписокЗапросовОбъединение.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(СписокЗапросовОбъединение, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(ТекстыЗапроса, ДопПоля)
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(ТекстыЗапроса, ДопПоля);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы(ДопПоля, Истина);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(ТекстыЗапроса, ДопПоля)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяРегистра, ТекстыЗапроса) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтОстаточнаяСтоимостьНМА(ТекстыЗапроса);
	
	МассивЗапросов = Новый Массив;
	МассивЗапросов.Добавить(РегистрыНакопления.ПартииПрочихРасходов.ТекстОписаниеВтИсходныеПартииПрочихРасходов(ДопПоля));
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ДанныеДокумента.Организация КАК Организация,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаРасходы.Подразделение
	|		ИНАЧЕ ДанныеДокумента.ПодразделениеРасходов
	|	КОНЕЦ КАК Подразделение,
	|
	|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.АналитикаСоответствуетСписываемомуНМА
	|			ТОГДА ТаблицаРасходы.НематериальныйАктив
	|		ИНАЧЕ ДанныеДокумента.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаРасходов,
	|
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	ДанныеДокумента.Ссылка КАК ДокументПоступленияРасходов,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ПодразделениеИНаправлениеДеятельностиСовпадаютСДаннымиУчета
	|			ТОГДА ТаблицаРасходы.НаправлениеДеятельности
	|		ИНАЧЕ ДанныеДокумента.НаправлениеДеятельностиРасходов
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ВидДеятельностиНДС,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете
	|			ТОГДА ТаблицаРасходы.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Стоимость,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВУпрУчете
	|			ТОГДА ТаблицаРасходы.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьБезНДС,
	|
	|	0 КАК НДСУпр,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ТаблицаРасходы.СтоимостьРегл + ТаблицаРасходы.СтоимостьЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	0 КАК НДСРегл,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ТаблицаРасходы.СтоимостьПР + ТаблицаРасходы.СтоимостьПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПостояннаяРазница,
	|
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ОтражатьВРеглУчете
	|			ТОГДА ТаблицаРасходы.СтоимостьВР + ТаблицаРасходы.СтоимостьВРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВременнаяРазница,
	|
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаРасходы.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ДанныеДокумента.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|
	|	,ДанныеДокумента.Ссылка КАК Регистратор
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОстаточнаяСтоимостьНМА КАК ТаблицаРасходы
	|		ПО ДанныеДокумента.Ссылка = ТаблицаРасходы.Ссылка
	|";
	МассивЗапросов.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(МассивЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(ТекстыЗапроса, ДопПоля)
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(ТекстыЗапроса, ДопПоля);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов(ДопПоля, Истина);
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаВтСтоимостьСписанияНМА(ТекстыЗапроса)

	ИмяТаблицы = "втСтоимостьСписанияНМА";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаНМА(ТекстыЗапроса);
	
	//++ Локализация
	ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаНМАБУ(ТекстыЗапроса);
	//-- Локализация
	
	ТекстЗапроса =
	"// Списание амортизации
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                     КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив             КАК НематериальныйАктив,
	|
	|	АмортизацияНМА.Амортизация                 КАК Стоимость,
	|	АмортизацияНМА.АмортизацияРегл             КАК СтоимостьРегл,
	|	АмортизацияНМА.АмортизацияНУ               КАК СтоимостьНУ,
	|	АмортизацияНМА.АмортизацияПР               КАК СтоимостьПР,
	|	АмортизацияНМА.АмортизацияВР               КАК СтоимостьВР,
	|	АмортизацияНМА.АмортизацияЦФ               КАК СтоимостьЦФ,
	|	АмортизацияНМА.АмортизацияНУЦФ             КАК СтоимостьНУЦФ,
	|	АмортизацияНМА.АмортизацияПРЦФ             КАК СтоимостьПРЦФ,
	|	АмортизацияНМА.АмортизацияВРЦФ             КАК СтоимостьВРЦФ,
	//++ Локализация
	|	СтоимостьНМА.СуммаРасходовНУПриИНВ         КАК СуммаРасходовНУПриИНВ,
	//-- Локализация
	|	СтоимостьНМА.РезервПереоценкиСтоимости     КАК РезервПереоценкиСтоимости,
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл КАК РезервПереоценкиСтоимостиРегл,
	|	СтоимостьНМА.ПриростСтоимости              КАК ПриростСтоимости,
	|	СтоимостьНМА.ПриростСтоимостиРегл          КАК ПриростСтоимостиРегл,
	|
	|	1                                          КАК ДоляСписания,
	|	1                                          КАК ДоляСписанияБУ,
	|	1                                          КАК ДоляСписанияЦФ,
	|	1                                          КАК ДоляСписанияНУ,
	|	1                                          КАК ДоляСписанияНУЦФ,
	|	1                                          КАК ДоляСписанияПР,
	|	1                                          КАК ДоляСписанияПРЦФ,
	|	
	|	ТаблицаНМА.Подразделение                   КАК Подразделение,
	|	ТаблицаНМА.ГруппаФинансовогоУчета          КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	ТаблицаНМА.СтатьяДоходов                   КАК СтатьяДоходов,
	|	ТаблицаНМА.АналитикаДоходов                КАК АналитикаДоходов,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки             КАК ИдентификаторСтроки
	|
	|ПОМЕСТИТЬ втСтоимостьСписанияНМА
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Списание частичной стоимости
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка         КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|
	|	ТаблицаНМА.СуммаСписанияУУ  КАК Стоимость,
	|	ТаблицаНМА.СуммаСписанияБУ - ТаблицаНМА.СуммаСписанияБУ * СтоимостьНМА.ДоляЦФБУ КАК СтоимостьРегл,
	|	ТаблицаНМА.СуммаСписанияНУ - ТаблицаНМА.СуммаСписанияНУ * СтоимостьНМА.ДоляЦФНУ КАК СтоимостьНУ,
	|	ТаблицаНМА.СуммаСписанияПР - ТаблицаНМА.СуммаСписанияПР * СтоимостьНМА.ДоляЦФПР КАК СтоимостьПР,
	|
	|	ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияБУ - ТаблицаНМА.СуммаСписанияБУ * СтоимостьНМА.ДоляЦФБУ) КАК ЧИСЛО(31,2))
	|	- ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияНУ - ТаблицаНМА.СуммаСписанияНУ * СтоимостьНМА.ДоляЦФНУ) КАК ЧИСЛО(31,2))
	|	- ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияПР - ТаблицаНМА.СуммаСписанияПР * СтоимостьНМА.ДоляЦФПР) КАК ЧИСЛО(31,2)) КАК СтоимостьВР,
	|
	|	ТаблицаНМА.СуммаСписанияБУ * СтоимостьНМА.ДоляЦФБУ КАК СтоимостьЦФ,
	|	ТаблицаНМА.СуммаСписанияНУ * СтоимостьНМА.ДоляЦФНУ КАК СтоимостьНУЦФ,
	|	ТаблицаНМА.СуммаСписанияПР * СтоимостьНМА.ДоляЦФПР КАК СтоимостьПРЦФ,
	|
	|	ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияБУ * СтоимостьНМА.ДоляЦФБУ) КАК ЧИСЛО(31,2))
	|	- ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияНУ * СтоимостьНМА.ДоляЦФНУ) КАК ЧИСЛО(31,2))
	|	- ВЫРАЗИТЬ((ТаблицаНМА.СуммаСписанияПР * СтоимостьНМА.ДоляЦФПР) КАК ЧИСЛО(31,2)) КАК СтоимостьВРЦФ,
	|	
	//++ Локализация
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВтПорядокУчетаНМАБУ.ИнвестиционныйНалоговыйВычет, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияИНВ.Региональный)
	|				И (ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) 
	|							+ ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0) 
	|							- ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0)
	|							- ЕСТЬNULL(АмортизацияНМА.АмортизацияЦФ, 0)
	|							- ЕСТЬNULL(ОбесценениеОС.ОбесценениеРегл, 0)) <> 0
	|
	|			ТОГДА ВЫРАЗИТЬ(ВЫРАЗИТЬ(СтоимостьНМА.СуммаРасходовНУПриИНВ КАК ЧИСЛО (31, 10))
	|					* ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияБУ КАК ЧИСЛО (31, 10)) 
	|						/ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) 
	|									+ ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0) 
	|									- ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0)
	|									- ЕСТЬNULL(АмортизацияНМА.АмортизацияЦФ, 0)
	|									- ЕСТЬNULL(ОбесценениеОС.ОбесценениеРегл, 0) КАК ЧИСЛО (31, 10)) КАК ЧИСЛО (31,2))
	|
	|		КОГДА ЕСТЬNULL(ВтПорядокУчетаНМАБУ.ИнвестиционныйНалоговыйВычет, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыПримененияИНВ.Федеральный)
	|				И (ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0) 
	|							- ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0)) <> 0
	|
	|			ТОГДА ВЫРАЗИТЬ(ВЫРАЗИТЬ(СтоимостьНМА.СуммаРасходовНУПриИНВ КАК ЧИСЛО (31, 10))
	|					* ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияНУ КАК ЧИСЛО (31, 10)) 
	|						/ ВЫРАЗИТЬ(ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0) 
	|									- ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0) КАК ЧИСЛО (31, 10)) КАК ЧИСЛО (31,2))
	|
	|		ИНАЧЕ 0
	|
	|	КОНЕЦ КАК СуммаРасходовНУПриИНВ,
	//-- Локализация
	|
	|	СтоимостьНМА.РезервПереоценкиСтоимости
	|	* ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ(ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияУУ КАК ЧИСЛО(31, 10)) 
	|				/ СтоимостьНМА.Стоимость КАК ЧИСЛО(31, 10))
	|	КОНЕЦ КАК РезервПереоценкиСтоимости,
	|
	|	СтоимостьНМА.РезервПереоценкиСтоимостиРегл
	|	* ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ(ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияБУ КАК ЧИСЛО(31, 10)) 
	|				/ СтоимостьНМА.Стоимость КАК ЧИСЛО(31, 10))
	|	КОНЕЦ КАК РезервПереоценкиСтоимостиРегл,
	|
	|	СтоимостьНМА.ПриростСтоимости
	|	* ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ(ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияУУ КАК ЧИСЛО(31, 10))
	|				/ СтоимостьНМА.Стоимость КАК ЧИСЛО(31, 10))
	|	КОНЕЦ КАК ПриростСтоимости,
	|
	|	СтоимостьНМА.ПриростСтоимостиРегл
	|	* ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ(ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияБУ КАК ЧИСЛО(31, 10)) 
	|				/ СтоимостьНМА.Стоимость КАК ЧИСЛО(31, 10))
	|	КОНЕЦ КАК ПриростСтоимостиРегл,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ(ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияУУ КАК ЧИСЛО(31, 10)) 
	|						/ СтоимостьНМА.Стоимость КАК ЧИСЛО(31, 10))
	|	КОНЕЦ КАК ДоляСписания,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ(ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияБУ - ТаблицаНМА.СуммаСписанияБУ * СтоимостьНМА.ДоляЦФБУ КАК ЧИСЛО(31, 10)) 
	|						/ СтоимостьНМА.СтоимостьРегл КАК ЧИСЛО(31, 10))
	|	КОНЕЦ КАК ДоляСписанияБУ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ(ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияБУ * СтоимостьНМА.ДоляЦФБУ КАК ЧИСЛО(31, 10)) 
	|						/ СтоимостьНМА.СтоимостьЦФ КАК ЧИСЛО(31, 10))
	|	КОНЕЦ КАК ДоляСписанияЦФ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ(ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияНУ - ТаблицаНМА.СуммаСписанияНУ * СтоимостьНМА.ДоляЦФНУ КАК ЧИСЛО(31, 10)) 
	|						/ СтоимостьНМА.СтоимостьНУ КАК ЧИСЛО(31, 10))
	|	КОНЕЦ КАК ДоляСписанияНУ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьНУЦФ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ(ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияНУ * СтоимостьНМА.ДоляЦФНУ КАК ЧИСЛО(31, 10)) 
	|						/ СтоимостьНМА.СтоимостьНУЦФ КАК ЧИСЛО(31, 10))
	|	КОНЕЦ КАК ДоляСписанияНУЦФ,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ(ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияПР - ТаблицаНМА.СуммаСписанияПР * СтоимостьНМА.ДоляЦФПР КАК ЧИСЛО(31, 10)) 
	|						/ СтоимостьНМА.СтоимостьПР КАК ЧИСЛО(31, 10))
	|	КОНЕЦ КАК ДоляСписанияПР,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(СтоимостьНМА.СтоимостьПРЦФ, 0) = 0
	|		ТОГДА 1
	|		ИНАЧЕ ВЫРАЗИТЬ(ВЫРАЗИТЬ(ТаблицаНМА.СуммаСписанияПР * СтоимостьНМА.ДоляЦФПР КАК ЧИСЛО(31, 10)) 
	|						/ СтоимостьНМА.СтоимостьПРЦФ КАК ЧИСЛО(31, 10))
	|	КОНЕЦ КАК ДоляСписанияПРЦФ,
	|
	|	ТаблицаНМА.Подразделение           КАК Подразделение,
	|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаНМА.СтатьяДоходов           КАК СтатьяДоходов,
	|	ТаблицаНМА.АналитикаДоходов        КАК АналитикаДоходов,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки     КАК ИдентификаторСтроки
	|	
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	//++ Локализация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив 
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК ОбесценениеОС
	|		ПО ОбесценениеОС.Ссылка = ТаблицаНМА.Ссылка
	|			И ОбесценениеОС.ОбъектУчета = ТаблицаНМА.НематериальныйАктив 
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПорядокУчетаНМАБУ КАК ВтПорядокУчетаНМАБУ
	|		ПО ВтПорядокУчетаНМАБУ.Ссылка = ТаблицаНМА.Ссылка
	|			И ВтПорядокУчетаНМАБУ.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	//-- Локализация
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НематериальныйАктив
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтАмортизацияСписанияНМА(ТекстыЗапроса)

	ИмяТаблицы = "втАмортизацияСписанияНМА";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтСтоимостьСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"// Списание амортизации
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                         КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив                 КАК НематериальныйАктив,
	|
	|	АмортизацияНМА.Амортизация                     КАК Амортизация,
	|	АмортизацияНМА.АмортизацияРегл                 КАК АмортизацияРегл,
	|	АмортизацияНМА.АмортизацияНУ                   КАК АмортизацияНУ,
	|	АмортизацияНМА.АмортизацияПР                   КАК АмортизацияПР,
	|	АмортизацияНМА.АмортизацияВР                   КАК АмортизацияВР,
	|	АмортизацияНМА.АмортизацияЦФ                   КАК АмортизацияЦФ,
	|	АмортизацияНМА.АмортизацияНУЦФ                 КАК АмортизацияНУЦФ,
	|	АмортизацияНМА.АмортизацияПРЦФ                 КАК АмортизацияПРЦФ,
	|	АмортизацияНМА.АмортизацияВРЦФ                 КАК АмортизацияВРЦФ,
	|	АмортизацияНМА.РезервПереоценкиАмортизации     КАК РезервПереоценкиАмортизации,
	|	АмортизацияНМА.РезервПереоценкиАмортизацииРегл КАК РезервПереоценкиАмортизацииРегл,
	|
	|	ТаблицаНМА.Подразделение                       КАК Подразделение,
	|	ТаблицаНМА.ГруппаФинансовогоУчета              КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности             КАК НаправлениеДеятельности,
	|	&ХО_Амортизация                                КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки                 КАК ИдентификаторСтроки
	|
	|ПОМЕСТИТЬ втАмортизацияСписанияНМА
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Списание частичной амортизации
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                    КАК Ссылка,
	|	СтоимостьСписанияНМА.НематериальныйАктив  КАК НематериальныйАктив,
	|
	// Амортизация
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.Амортизация = ЕСТЬNULL(СтоимостьНМА.Стоимость, 0)
	|			ТОГДА СтоимостьСписанияНМА.Стоимость
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.Амортизация * СтоимостьСписанияНМА.ДоляСписания КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК Амортизация,
	|
	// АмортизацияРегл
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияРегл = ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьРегл
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияРегл * СтоимостьСписанияНМА.ДоляСписанияБУ КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияРегл,
	|
	// АмортизацияНУ
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияНУ = ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьНУ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияНУ * СтоимостьСписанияНМА.ДоляСписанияНУ КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияНУ,
	|
	// АмортизацияПР
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияПР = ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьПР
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияПР * СтоимостьСписанияНМА.ДоляСписанияПР КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияПР,
	|
	// АмортизацияВР
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияРегл = ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьРегл
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияРегл * СтоимостьСписанияНМА.ДоляСписанияБУ КАК ЧИСЛО(31,2))
	|	КОНЕЦ
	|		- ВЫБОР
	|				КОГДА АмортизацияНМА.АмортизацияНУ = ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0)
	|					ТОГДА СтоимостьСписанияНМА.СтоимостьНУ
	|				ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияНУ * СтоимостьСписанияНМА.ДоляСписанияНУ КАК ЧИСЛО(31,2))
	|			КОНЕЦ
	|		- ВЫБОР
	|				КОГДА АмортизацияНМА.АмортизацияПР = ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0)
	|					ТОГДА СтоимостьСписанияНМА.СтоимостьПР
	|				ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияПР * СтоимостьСписанияНМА.ДоляСписанияПР КАК ЧИСЛО(31,2))
	|			КОНЕЦ КАК АмортизацияВР,
	|
	// АмортизацияЦФ
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияЦФ = ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьЦФ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияЦФ * СтоимостьСписанияНМА.ДоляСписанияЦФ КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияЦФ,
	|
	// АмортизацияНУЦФ
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияНУЦФ = ЕСТЬNULL(СтоимостьНМА.СтоимостьНУЦФ, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьНУЦФ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияНУЦФ * СтоимостьСписанияНМА.ДоляСписанияНУЦФ КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияНУЦФ,
	|
	// АмортизацияПРЦФ
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияПРЦФ = ЕСТЬNULL(СтоимостьНМА.СтоимостьПРЦФ, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьПРЦФ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияПРЦФ * СтоимостьСписанияНМА.ДоляСписанияПРЦФ КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК АмортизацияПРЦФ,
	|
	// АмортизацияВРЦФ
	|	ВЫБОР
	|		КОГДА АмортизацияНМА.АмортизацияЦФ = ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0)
	|			ТОГДА СтоимостьСписанияНМА.СтоимостьЦФ
	|		ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияЦФ * СтоимостьСписанияНМА.ДоляСписанияЦФ КАК ЧИСЛО(31,2))
	|	КОНЕЦ
	|		- ВЫБОР
	|				КОГДА АмортизацияНМА.АмортизацияНУЦФ = ЕСТЬNULL(СтоимостьНМА.СтоимостьНУЦФ, 0)
	|					ТОГДА СтоимостьСписанияНМА.СтоимостьНУЦФ
	|				ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияНУЦФ * СтоимостьСписанияНМА.ДоляСписанияНУЦФ КАК ЧИСЛО(31,2))
	|			КОНЕЦ
	|		- ВЫБОР
	|				КОГДА АмортизацияНМА.АмортизацияПРЦФ = ЕСТЬNULL(СтоимостьНМА.СтоимостьПРЦФ, 0)
	|					ТОГДА СтоимостьСписанияНМА.СтоимостьПРЦФ
	|				ИНАЧЕ ВЫРАЗИТЬ(АмортизацияНМА.АмортизацияПРЦФ * СтоимостьСписанияНМА.ДоляСписанияПРЦФ КАК ЧИСЛО(31,2))
	|			КОНЕЦ КАК АмортизацияВРЦФ,
	|
	|	АмортизацияНМА.РезервПереоценкиАмортизации                   КАК РезервПереоценкиАмортизации,
	|	АмортизацияНМА.РезервПереоценкиАмортизацииРегл               КАК РезервПереоценкиАмортизацииРегл,
	|
	|	СтоимостьСписанияНМА.Подразделение                           КАК Подразделение,
	|	СтоимостьСписанияНМА.ГруппаФинансовогоУчета                  КАК ГруппаФинансовогоУчета,
	|	СтоимостьСписанияНМА.НаправлениеДеятельности                 КАК НаправлениеДеятельности,
	|	&ХО_Амортизация                                              КАК ХозяйственнаяОперация,
	|	СтоимостьСписанияНМА.ИдентификаторСтроки                     КАК ИдентификаторСтроки
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО ДанныеДокумента.Ссылка = АмортизацияНМА.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтоимостьСписанияНМА КАК СтоимостьСписанияНМА
	|		ПО АмортизацияНМА.ОбъектУчета = СтоимостьСписанияНМА.НематериальныйАктив
	|			И АмортизацияНМА.Ссылка = СтоимостьСписанияНМА.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО АмортизацияНМА.ОбъектУчета = СтоимостьНМА.ОбъектУчета
	|			И АмортизацияНМА.Ссылка = СтоимостьНМА.Ссылка
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтОбесценениеСписанияНМА(ТекстыЗапроса)

	ИмяТаблицы = "втОбесценениеСписанияНМА";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтСтоимостьСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"// Списание обесценения НМА полное.
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                         КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив                 КАК НематериальныйАктив,
	|
	|	ОбесценениеНМА.ЕГДС                            КАК ЕГДС,
	|
	|	ОбесценениеНМА.ОбесценениеУпр                  КАК ОбесценениеУпр,
	|	ОбесценениеНМА.ОбесценениеРегл                 КАК ОбесценениеРегл,
	|
	|	ТаблицаНМА.ГруппаФинансовогоУчета              КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности             КАК НаправлениеДеятельности,
	|	&ХО_Обесценение                                КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки                 КАК ИдентификаторСтроки
	|
	|ПОМЕСТИТЬ втОбесценениеСписанияНМА
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК ОбесценениеНМА
	|		ПО ОбесценениеНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И ОбесценениеНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Списание обесценения НМА частичное.
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                         КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив                 КАК НематериальныйАктив,
	|
	|	ОбесценениеНМА.ЕГДС                            КАК ЕГДС,
	|
	|	ОбесценениеНМА.ОбесценениеУпр 
	|		* втСтоимостьСписанияНМА.ДоляСписания      КАК ОбесценениеУпр,
	|	
	|	ОбесценениеНМА.ОбесценениеРегл 
	|		* втСтоимостьСписанияНМА.ДоляСписанияБУ    КАК ОбесценениеРегл,
	|
	|	ТаблицаНМА.ГруппаФинансовогоУчета              КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности             КАК НаправлениеДеятельности,
	|	&ХО_Обесценение                                КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки                 КАК ИдентификаторСтроки
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТаблицаНМА КАК ТаблицаНМА
	|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК ОбесценениеНМА
	|		ПО ОбесценениеНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И ОбесценениеНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСтоимостьСписанияНМА КАК втСтоимостьСписанияНМА
	|		ПО втСтоимостьСписанияНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И втСтоимостьСписанияНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НематериальныйАктив
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтОстаточнаяСтоимостьНМА(ТекстыЗапроса) Экспорт

	ИмяТаблицы = "втОстаточнаяСтоимостьНМА";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаТаблицаВтТаблицаНМА(ТекстыЗапроса);
	ТекстЗапросаТаблицаВтАмортизацияСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапросаТаблицаВтОбесценениеСписанияНМА(ТекстыЗапроса);
	
	ТекстЗапроса =
	"// Списание остаточной стоимости
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка          КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив  КАК НематериальныйАктив,
	|
	|	ЕСТЬNULL(СтоимостьНМА.Стоимость, 0) 
	|		- ЕСТЬNULL(АмортизацияНМА.Амортизация, 0)
	|		- ЕСТЬNULL(ОбесценениеВНА.ОбесценениеУпр, 0) КАК Стоимость,
	|
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0)
	|		- ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0) 
	|		- ЕСТЬNULL(ОбесценениеВНА.ОбесценениеРегл, 0) КАК СтоимостьРегл,
	|
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0)     КАК СтоимостьНУ,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияПР, 0)     КАК СтоимостьПР,
	|
	|	// ВР как БУ-НУ-ПР
	|	(ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0)
	|		- ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0)
	|		- ЕСТЬNULL(ОбесценениеВНА.ОбесценениеРегл, 0))
	|		- (ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0)
	|			- ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0))
	|		- (ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0)
	|			- ЕСТЬNULL(АмортизацияНМА.АмортизацияПР, 0)) КАК СтоимостьВР,
	|
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияЦФ, 0)     КАК СтоимостьЦФ,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьНУЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУЦФ, 0) КАК СтоимостьНУЦФ,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьПРЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияПРЦФ, 0) КАК СтоимостьПРЦФ,
	|	ЕСТЬNULL(СтоимостьНМА.СтоимостьВРЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияВРЦФ, 0) КАК СтоимостьВРЦФ,
	|
	|	ТаблицаНМА.Подразделение            КАК Подразделение,
	|	ТаблицаНМА.ГруппаФинансовогоУчета   КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ТаблицаНМА.СтатьяДоходов            КАК СтатьяДоходов,
	|	ТаблицаНМА.АналитикаДоходов         КАК АналитикаДоходов,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки      КАК ИдентификаторСтроки,
	|	1 КАК ИДПодзапроса
	|
	|ПОМЕСТИТЬ втОстаточнаяСтоимостьНМА
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втТаблицаНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтоимостьВНА КАК СтоимостьНМА
	|		ПО СтоимостьНМА.Ссылка = ТаблицаНМА.Ссылка 
	|			И СтоимостьНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияВНА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.Ссылка = ТаблицаНМА.Ссылка
	|			И АмортизацияНМА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбесценениеВНА КАК ОбесценениеВНА
	|		ПО ОбесценениеВНА.Ссылка = ТаблицаНМА.Ссылка
	|			И ОбесценениеВНА.ОбъектУчета = ТаблицаНМА.НематериальныйАктив
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|	И (ЕСТЬNULL(СтоимостьНМА.Стоимость, 0)
	|		- ЕСТЬNULL(АмортизацияНМА.Амортизация, 0)
	|		- ЕСТЬNULL(ОбесценениеВНА.ОбесценениеУпр, 0)<> 0
	|		ИЛИ
	|			ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0)
	|				- ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0)
	|				- ЕСТЬNULL(ОбесценениеВНА.ОбесценениеРегл, 0) <> 0
	|		ИЛИ
	|			ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0) <> 0
	|		ИЛИ
	|			ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияПР, 0) <> 0
	|	
	|		ИЛИ
	|			(ЕСТЬNULL(СтоимостьНМА.СтоимостьРегл, 0)
	|				- ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0)
	|				- ЕСТЬNULL(ОбесценениеВНА.ОбесценениеРегл, 0))
	|			- (ЕСТЬNULL(СтоимостьНМА.СтоимостьНУ, 0)
	|				- ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0))
	|			- (ЕСТЬNULL(СтоимостьНМА.СтоимостьПР, 0)
	|				- ЕСТЬNULL(АмортизацияНМА.АмортизацияПР, 0)) <> 0
	|	
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияЦФ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьНУЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУЦФ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьПРЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияПРЦФ, 0) <> 0
	|		ИЛИ ЕСТЬNULL(СтоимостьНМА.СтоимостьВРЦФ, 0) - ЕСТЬNULL(АмортизацияНМА.АмортизацияВРЦФ, 0) <> 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//Частичное списание остаточной стоимости
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка         КАК Ссылка,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|
	|	ТаблицаНМА.Стоимость 
	|		- ЕСТЬNULL(АмортизацияНМА.Амортизация, 0)
	|		- ЕСТЬNULL(ОбесценениеВНА.ОбесценениеУпр, 0)         КАК Стоимость,
	|
	|	ТаблицаНМА.СтоимостьРегл 
	|		- ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0)
	|		- ЕСТЬNULL(ОбесценениеВНА.ОбесценениеРегл, 0) КАК СтоимостьРегл,
	|
	|	ТаблицаНМА.СтоимостьНУ - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0)     КАК СтоимостьНУ,
	|
	|	ТаблицаНМА.СтоимостьПР - ЕСТЬNULL(АмортизацияНМА.АмортизацияПР, 0)     КАК СтоимостьПР,
	|
	|	ТаблицаНМА.СтоимостьВР 
	|		- ЕСТЬNULL(АмортизацияНМА.АмортизацияВР, 0)
	|		- ЕСТЬNULL(ОбесценениеВНА.ОбесценениеРегл, 0)     КАК СтоимостьВР,
	|
	|	ТаблицаНМА.СтоимостьЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияЦФ, 0)     КАК СтоимостьЦФ,
	|	ТаблицаНМА.СтоимостьНУЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУЦФ, 0) КАК СтоимостьНУЦФ,
	|	ТаблицаНМА.СтоимостьПРЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияПРЦФ, 0) КАК СтоимостьПРЦФ,
	|	ТаблицаНМА.СтоимостьВРЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияВРЦФ, 0) КАК СтоимостьВРЦФ,
	|
	|	ТаблицаНМА.Подразделение           КАК Подразделение,
	|	ТаблицаНМА.ГруппаФинансовогоУчета  КАК ГруппаФинансовогоУчета,
	|	ТаблицаНМА.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаНМА.СтатьяДоходов           КАК СтатьяДоходов,
	|	ТаблицаНМА.АналитикаДоходов        КАК АналитикаДоходов,
	|	
	|	ТаблицаНМА.ИдентификаторСтроки     КАК ИдентификаторСтроки,
	|	2 КАК ИДПодзапроса
	|
	|ИЗ
	|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСтоимостьСписанияНМА КАК ТаблицаНМА
	|		ПО ТаблицаНМА.Ссылка = ДанныеДокумента.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАмортизацияСписанияНМА КАК АмортизацияНМА
	|		ПО АмортизацияНМА.Ссылка = ТаблицаНМА.Ссылка 
	|			И АмортизацияНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОбесценениеСписанияНМА КАК ОбесценениеВНА
	|		ПО ОбесценениеВНА.Ссылка = ТаблицаНМА.Ссылка
	|			И ОбесценениеВНА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
	|
	|ГДЕ
	|	ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеНМАЧастичное)
	|	И (
	|			ТаблицаНМА.Стоимость 
	|				- ЕСТЬNULL(АмортизацияНМА.Амортизация, 0)
	|				- ЕСТЬNULL(ОбесценениеВНА.ОбесценениеУпр, 0) <> 0
	|		ИЛИ 
	|			ТаблицаНМА.СтоимостьРегл 
	|				- ЕСТЬNULL(АмортизацияНМА.АмортизацияРегл, 0)
	|				- ЕСТЬNULL(ОбесценениеВНА.ОбесценениеРегл, 0) <> 0
	|		ИЛИ 
	|			ТаблицаНМА.СтоимостьНУ - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУ, 0) <> 0
	|		ИЛИ 
	|			ТаблицаНМА.СтоимостьПР - ЕСТЬNULL(АмортизацияНМА.АмортизацияПР, 0) <> 0
	|		ИЛИ 
	|			ТаблицаНМА.СтоимостьВР 
	|				- ЕСТЬNULL(АмортизацияНМА.АмортизацияВР, 0)
	|				- ЕСТЬNULL(ОбесценениеВНА.ОбесценениеРегл, 0) <> 0
	|		ИЛИ 
	|			ТаблицаНМА.СтоимостьЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияЦФ, 0) <> 0
	|		ИЛИ 
	|			ТаблицаНМА.СтоимостьНУЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияНУЦФ, 0) <> 0
	|		ИЛИ 
	|			ТаблицаНМА.СтоимостьПРЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияПРЦФ, 0) <> 0
	|		ИЛИ 
	|			ТаблицаНМА.СтоимостьВРЦФ - ЕСТЬNULL(АмортизацияНМА.АмортизацияВРЦФ, 0) <> 0
	|		)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	НематериальныйАктив
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ТекстЗапросаТаблицаВтТаблицаНМА(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаНМА";
	
	Если ПроведениеДокументов.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = СписаниеНМАЛокализация.ТекстЗапросаТаблицаВтТаблицаНМА(ТекстыЗапроса);
	
	Если ТекстЗапроса = Неопределено Тогда
		
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПорядокУчетаНМА(ТекстыЗапроса);
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтМестоУчетаНМА(ТекстыЗапроса);
		ВнеоборотныеАктивыСлужебный.ТекстЗапросаТаблицаВтПараметрыАмортизацииНМАУУ(ТекстыЗапроса);
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаНМА.Ссылка                      КАК Ссылка,
		|	ТаблицаНМА.НомерСтроки                 КАК НомерСтроки,
		|	ТаблицаНМА.НематериальныйАктив         КАК НематериальныйАктив,
		|
		|	ПорядокУчета.ГруппаФинансовогоУчета    КАК ГруппаФинансовогоУчета,
		|	ПорядокУчета.НаправлениеДеятельности   КАК НаправлениеДеятельности,
		|	ПорядокУчета.ОбъемНаработки            КАК ОбъемНаработки,
		|
		|	МестоУчетаНМА.Организация              КАК Организация,
		|	МестоУчетаНМА.Подразделение            КАК Подразделение,
		|
		|	НЕОПРЕДЕЛЕНО                           КАК СтатьяДоходов,
		|	НЕОПРЕДЕЛЕНО                           КАК АналитикаДоходов,
		|
		|	ТаблицаНМА.СуммаСписанияУУ             КАК СуммаСписанияУУ,
		|	ТаблицаНМА.СуммаСписанияБУ             КАК СуммаСписанияБУ,
		|	ТаблицаНМА.СуммаСписанияНУ             КАК СуммаСписанияНУ,
		|	ТаблицаНМА.СуммаСписанияПР             КАК СуммаСписанияПР,
		|	ТаблицаНМА.СуммаСписанияВР             КАК СуммаСписанияВР,
		|	
		|	ТаблицаНМА.ИдентификаторСтроки 		   КАК ИдентификаторСтроки
		|	
		|ПОМЕСТИТЬ втТаблицаНМА
		|
		|ИЗ
		|	ДанныеДокументаРеквизиты КАК ДанныеДокумента
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокументаТаблицаНМА КАК ТаблицаНМА
		|		ПО ДанныеДокумента.Ссылка = ТаблицаНМА.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокУчетаНМА КАК ПорядокУчета
		|		ПО ПорядокУчета.Ссылка = ТаблицаНМА.Ссылка
		|			И ПорядокУчета.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втМестоУчетаНМА КАК МестоУчетаНМА
		|		ПО МестоУчетаНМА.Ссылка = ТаблицаНМА.Ссылка
		|			И МестоУчетаНМА.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПараметрыАмортизацииНМАУУ КАК ПараметрыАмортизацииУУ
		|		ПО ПараметрыАмортизацииУУ.Ссылка = ТаблицаНМА.Ссылка
		|			И ПараметрыАмортизацииУУ.НематериальныйАктив = ТаблицаНМА.НематериальныйАктив
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка,
		|	НематериальныйАктив
		|";
	
	КонецЕсли; 
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяТаблицы, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт

	Возврат СписаниеНМАЛокализация.ТекстЗапросаВТОтраженияВРеглУчете();

КонецФункции

Функция ТекстОтраженияВРеглУчете() Экспорт

	Возврат СписаниеНМАЛокализация.ТекстОтраженияВРеглУчете();

КонецФункции

#КонецОбласти

#Область Прочее

Функция ПредставлениеРеквизитов(ВалютаРегл) Экспорт

	ПредставлениеРеквизитов = Новый Соответствие;

	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
	
	ВалютаРеглПредставление = "";
	Если ЗначениеЗаполнено(ВалютаРегл) Тогда
		ВалютаРеглПредставление = "(" + Строка(ВалютаРегл) + ")";
	КонецЕсли;
	
	ВедетсяРегламентированныйУчетВНА = ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА();
	
	Если ВедетсяРегламентированныйУчетВНА Тогда
		
		ПредставлениеРеквизитов.Вставить("НМА.СуммаСписанияБУ", СокрЛП(СтрШаблон(НСтр("ru = 'Сумма регл. %1';
																						|en = 'Local amount %1'"), ВалютаРеглПредставление)));
		ПредставлениеРеквизитов.Вставить("НМА.СуммаСписанияУУ", СтрШаблон(НСтр("ru = 'Сумма упр. (%1)';
																				|en = 'Amount, manag. (%1)'"), Строка(ВалютаУпр)));
		
	Иначе
		
		Если ВалютаРегл = ВалютаУпр Тогда
			ПредставлениеРеквизитов.Вставить("НМА.СуммаСписанияБУ", НСтр("ru = 'Сумма';
																		|en = 'Amount'"));
			ПредставлениеРеквизитов.Вставить("НМА.СуммаСписанияУУ", НСтр("ru = 'Сумма';
																		|en = 'Amount'"));
		Иначе
			ПредставлениеРеквизитов.Вставить("НМА.СуммаСписанияБУ", СокрЛП(СтрШаблон(НСтр("ru = 'Сумма %1';
																							|en = 'Amount %1'"), ВалютаРеглПредставление)));
			ПредставлениеРеквизитов.Вставить("НМА.СуммаСписанияУУ", СтрШаблон(НСтр("ru = 'Сумма (%1)';
																					|en = 'Amount (%1)'"), Строка(ВалютаУпр)));
		КонецЕсли;
		
	КонецЕсли; 
	
	Возврат ПредставлениеРеквизитов;
	
КонецФункции

Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьи";
	ПараметрыВыбора.ДоступностьПоОперации = Неопределено;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивов");
	
	СписокУсловий = Новый Структура;
	СписокУсловий.Вставить("АналитикаРасходовОбязательна", Истина);
	СписокУсловий.Вставить("АналитикаСоответствуетСписываемомуНМА", Ложь);
	ПараметрыВыбора.УсловияОбязательногоЗаполненияАналитикиВСтроках = СписокУсловий;

	Возврат ПараметрыВыбора;
	
КонецФункции

Функция ПараметрыНастройкиСчетовУчета() Экспорт
	
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.Представление = "ПредставлениеОтраженияВРеглУчете";
	ПараметрыНастройки.ТипСтатьи     = "ТипСтатьи";
	
	ПараметрыНастройки.Организация = "Объект.Организация";

	ПараметрыНастройки.ЭлементыФормы.Добавить("ПредставлениеОтраженияВРеглУчете");
	
	Возврат ПараметрыНастройки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
