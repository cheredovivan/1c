
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.ВыбытиеДенежныхДокументов) Тогда
		
		ПолноеИмяДокумента		= "Документ.ВыбытиеДенежныхДокументов";
		ТипДокумента			= Тип("ДокументСсылка.ВыбытиеДенежныхДокументов");
		ИдентификаторДокумента= ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента);
		
		МассивХозяйственныхОпераций = Новый Массив;
		МассивХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ВыдачаДенежныхДокументовПодотчетнику);
		МассивХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхДокументовПоставщику);
		МассивХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.СписаниеДенежныхДокументов);
		МассивХозяйственныхОпераций.Добавить(Перечисления.ХозяйственныеОперации.ПеремещениеДенежныхДокументов);
				
		Для Каждого ДоступнаяХозяйственнаяОперация Из МассивХозяйственныхОпераций Цикл
		
			Строка = ХозяйственныеОперацииИДокументы.Добавить();
			Строка.ДобавитьКнопкуСоздать	= Истина;
			Строка.ПравоДоступаДобавление	= Истина;
			Строка.ПравоДоступаИзменение	= Истина;
			Строка.Отбор					= Истина;
			Строка.ТипДокумента				= ТипДокумента;
			Строка.ПолноеИмяДокумента		= ПолноеИмяДокумента;
			Строка.ХозяйственнаяОперация 	= ДоступнаяХозяйственнаяОперация;
			Строка.ИдентификаторОбъектаМетаданных = ИдентификаторДокумента;
		
		КонецЦикла;
		
		СтруктураПараметров = ОбщегоНазначенияУТ.СтруктураПараметровНастройкиКнопокУправленияДокументами();
		СтруктураПараметров.Форма = ЭтотОбъект;
		ОбщегоНазначенияУТ.НастроитьКнопкиУправленияДокументами(СтруктураПараметров);
		
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыПриСозданииНаСервере = ОбменСКонтрагентами.ПараметрыПриСозданииНаСервере_ФормаСписка();
	ПараметрыПриСозданииНаСервере.Форма = ЭтотОбъект;
	ПараметрыПриСозданииНаСервере.МестоРазмещенияКоманд = Элементы.ПодменюЭДО;
	ПараметрыПриСозданииНаСервере.КолонкаСостоянияЭДО = Элементы.ПредставлениеСостояния;
	ОбменСКонтрагентами.ПриСозданииНаСервере_ФормаСписка(ПараметрыПриСозданииНаСервере);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыОповещенияЭДО = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаСписка();
	ПараметрыОповещенияЭДО.Форма = ЭтотОбъект;
	ПараметрыОповещенияЭДО.ИмяДинамическогоСписка = "Список";
	ПараметрыОповещенияЭДО.ЕстьОбработчикОбновленияВидимостиСостоянияЭДО = Истина;
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаСписка(ИмяСобытия, Параметр, Источник, ПараметрыОповещенияЭДО);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле = Элементы.ПредставлениеСостояния Тогда
		// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
		ОбменСКонтрагентамиКлиент.СостояниеЭДОНажатие_ФормаСписка(ВыбраннаяСтрока, СтандартнаяОбработка);
		// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_СоздатьДокумент(Команда)
	ОбщегоНазначенияУТКлиент.СоздатьДокументЧерезКоманду(Команда.Имя, Новый Структура);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// ЭлектронноеВзаимодействие.ОбменСКонтрагентами

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьВидимостьСостоянияЭДО()
	
	ОбменСКонтрагентамиКлиент.ОбработчикОбновленияВидимостьСостоянияЭДО(ЭтотОбъект, Элементы.ПредставлениеСостояния);
	
КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Настройка = УсловноеОформление.Элементы;
	Настройка.Очистить();
	
	ОперацииДенежныхСредств = Новый СписокЗначений;
	ОперацииДенежныхСредств.Добавить(Перечисления.ХозяйственныеОперации.ВыдачаДенежныхДокументовПодотчетнику);
	ОперацииДенежныхСредств.Добавить(Перечисления.ХозяйственныеОперации.ПеремещениеДенежныхДокументов);
	ОперацииДенежныхСредств.Добавить(Перечисления.ХозяйственныеОперации.СписаниеДенежныхДокументов);
	
	// Валюта и Сумма
	ЭлементНастройки = Настройка.Добавить();
	ПолеВалюта = ЭлементНастройки.Поля.Элементы.Добавить();
	ПолеВалюта.Поле = Новый ПолеКомпоновкиДанных("Валюта");
	ПолеСумма = ЭлементНастройки.Поля.Элементы.Добавить();
	ПолеСумма.Поле = Новый ПолеКомпоновкиДанных("СуммаДокумента");
	
	ФинансоваяОтчетностьСервер.НовыйОтбор(
		ЭлементНастройки.Отбор,
		"Список.ХозяйственнаяОперация",
		ОперацииДенежныхСредств, ,
		ВидСравненияКомпоновкиДанных.ВСписке);
		
	ФинансоваяОтчетностьСервер.НовыйОтбор(
		ЭлементНастройки.Отбор,
		"Список.Валюта", , ,
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ФинансоваяОтчетностьСервер.НовыйОтбор(
		ЭлементНастройки.Отбор,
		"Список.СуммаДокумента", , ,
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементНастройки.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<несколько валют>';
																			|en = '<multiple currencies>'"));
	ЭлементНастройки.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстИнформационнойНадписи);	
	
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, "Список.Дата", Элементы.Дата.Имя);
	
КонецПроцедуры

#КонецОбласти
